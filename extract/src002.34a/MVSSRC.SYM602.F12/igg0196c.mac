         TITLE 'IGG0196C - UPDATE DCB FROM FORMAT 2 DSCB'
IGG0196C CSECT
*
***********************************************************************
*                                                                     *
* MODULE-NAME = IGG0196C                                              *
*                                                                     *
* DESCRIPTIVE-NAME = ISAM OPEN, RESUME LOAD MODE                      *
*                                                                     *
* COPYRIGHT = NONE                                                    *
*                                                                     *
* STATUS = RELEASE OS/VS2-02, LEVEL 0                                 *
*                                                                     *
* FUNCTION = MOVE APPLICABLE F2 DSCB FIELDS INTO DCB.  ALSO MOVE F2   *
*            DSCB DATA INTO ISLNIRT AND ISLHIRT IN ISAM WORK AREA.    *
*                                                                     *
* NOTES = SEE BELOW                                                   *
*                                                                     *
*    DEPENDENCIES = NONE                                              *
*                                                                     *
*    RESTRICTIONS = NONE                                              *
*                                                                     *
*    REGISTER-CONVENTIONS = SEE REGISTER EQUATES.                     *
*                                                                     *
*    PATCH-LABEL = PATCH, A DC STATEMENT.                             *
*                                                                     *
* MODULE-TYPE = OPEN EXECUTOR                                         *
*                                                                     *
*    PROCESSOR = ASSEMXF-370R                                         *
*                                                                     *
*    MODULE-SIZE = 392 DECIMAL BYTES                                  *
*                                                                     *
*    ATTRIBUTES = PRIVILEGED, REENTRANT                               *
*                                                                     *
* ENTRY-POINT = IGG0196C                                              *
*                                                                     *
*    PURPOSE = SEE FUNCTION                                           *
*                                                                     *
*    LINKAGE = RECEIVES CONTROL FROM ISAM OPEN EXECUTOR IGG01922 FOR  *
*              RESUME LOAD WITH FLR, OR IGG01950 FOR RESUME LOAD WITH *
*              VLR.  RECEIVES CONTROL IN STORAGE PROTECT KEY 5 AND    *
*              PRIVILEGED STATE.                                      *
*                                                                     *
* INPUT = REGISTERS ESTABLISHED UPON ENTRY ARE                        *
*         REG 5 (RPAR) POINTS TO THE DCB COPY PARAMETER LIST          *
*         REG 6 (RWTG) POINTS TO THE O/C/EOV WHERE-TO-GO TABLE        *
*         REG 7 (RPARC) POINTS TO THE CURRENT ENTRY IN THE DCB COPY   *
*               PARAMETER LIST                                        *
*         REG 8 (RWTGC) POINTS TO THE CURRENT ENTRY IN THE WTG TABLE  *
*                                                                     *
* OUTPUT = REGISTERS 5 THROUGH 8 WILL BE IN THE SAME CONDITION AS     *
*          UPON ENTRY TO THIS MODULE                                  *
*                                                                     *
* EXIT-NORMAL = XCTL TO ISAM OPEN EXECUTOR IGG0196D IN STORAGE        *
*               PROTECT KEY 5.                                        *
*                                                                     *
* EXIT-ERROR = NONE                                                   *
*                                                                     *
* EXTERNAL-REFERENCES = SEE BELOW                                     *
*                                                                     *
*    ROUTINES = NONE                                                  *
*                                                                     *
*    DATA-AREAS = AREAS REFERENCED ARE:                               *
*                 FORCORE - OPEN WORK AREA                            *
*                 ISLCOMON - ISAM LOAD MODE WORK AREA                 *
*                                                                     *
*    CONTROL-BLOCKS = DCB COPY AND DSCB                               *
*                                                                     *
* TABLES = NONE                                                       *
*                                                                     *
* MACROS = MODESET AND XCTL                                           *
*                                                                     *
* CHANGE-ACTIVITY = AS FOLLOWS:                                       *
*                                                                     *
*                                                                     *
***********************************************************************
         EJECT
* SYMBOLIC REGISTER EQUATES
*
R0       EQU   0                   USED ONLY FOR SAVING AND RESTORING
*                                  REGISTERS
R1       EQU   1                   UNUSED
RFMT2    EQU   2                   BASE FOR FORMAT 2 DSCB
RBASE    EQU   3                   BASE REGISTER FOR THIS MODULE
RCORE    EQU   4                   BASE FOR OPEN WORK AREA
RPAR     EQU   5                   POINTER TO PARAMETER LIST
RWTG     EQU   6                   POINTER TO WHERE-TO-GO TABLE
RPARC    EQU   7                   CURRENT ENTRY IN PARAMETER LIST
RWTGC    EQU   8                   CURRENT ENTRY IN WHERE-TO-GO TABLE
RISLMWA  EQU   9                   POINTER TO LOAD MODE WORK AREA
RDCB     EQU   10                  CURRENT DCB BEING PROCESSED
RWORK    EQU   11                  WORK REGISTER
RC       EQU   12                  UNUSED
RD       EQU   13                  UNUSED
RE       EQU   14                  UNUSED
RF       EQU   15                  USED ONLY FOR SAVING AND RESTORING
*                                  REGISTERS AND FOR XCTL
NULL     EQU   0                   ZERO FOR DUMMY INDEXING USE
WAOFF    EQU   4                   OFFSET IN WTG TABLE OF OPEN WORK
*                                  AREA ADDRESS
WTGOFF   EQU   8                   VALUE FOR INCREMENTING WTG ENTRY
DCBOFF   EQU   4                   VALUE FOR INCREMENTING TO NEXT DCB
IDLEN    EQU   2                   LENGTH OF ID
TTRLEN   EQU   3                   LENGTH OF TTR
IDOFF    EQU   6                   OFFSET FOR XCTL ID
TTROFF   EQU   14                  OFFSET FOR XCTL TTR
RESETOFF EQU   32                  OFFSET TO REPOSITION TO START OF
*                                  LIST
*
*
         BALR  RBASE,NULL          ESTABLISH
         USING *,RBASE             *         ADDRESSABILITY
         USING FORCORE,RCORE       ADDRESSABILITY FOR OPEN WORK AREA
         USING DSCBF2,RFMT2        ADDRESSABILITY FOR FORMAT 2 DSCB
         USING IHADCB,RDCB         ADDRESSABILITY FOR DCB
         USING ISLCOMON,RISLMWA    ADDRESSABILITY FOR ISAM WORK AREA
*
UPDTLOOP EQU   *
         L     RCORE,WAOFF(RWTGC)  ADDRESS OF OPEN WORK AREA
         L     RFMT2,DSCCORE       ADDRESS OF FORMAT 2 DSCB
         L     RDCB,NULL(RPARC)    GET DCB TO PROCESS
         L     RISLMWA,DCBWKPT1    ADDRESS OF LOAD MODE WORK AREA
         STM   R0,RF,DXCCW1        SAVE REGISTERS                Y02072
         SR    RWORK,RWORK         CLEAR REGISTER
         STC   RWORK,DCBEXCD1      SET DCBEXCD1 TO ZERO
         STC   RWORK,DCBEXCD2      SET DCBEXCD2 TO ZERO
*
***********************************************************************
* THE FOLLOWING INSTRUCTIONS UPDATE DCB AND WORK AREA FIELDS FROM THE *
* FORMAT 2 DSCB.                                                      *
***********************************************************************
*
         MVC   DCBNLEV,DS2NOLEV    NUMBER OF INDEX LEVELS
         MVC   DCBNTM,DS2DVIND     NUMBER OF TRACKS THAT DETERMINE THE
*                                  DEVELOPMENT OF A MASTER INDEX
         MVC   DCBFIRSH,DS21RCYL   HHR OF THE FIRST DATA RECORD ON
*                                  EACH CYLINDER
         MVC   DCBLDT,DS2LTCYL     HH OF THE LAST PRIME DATA TRACK ON
*                                  EACH CYLINDER
         MVC   DCBCYLOF,DS2CYLOV   NUMBER OF TRACKS ON EACH PRIME
*                                  CYLINDER TO BE RESERVED FOR
*                                  RECORDS THAT OVERFLOW FROM OTHER
*                                  TRACKS ON THAT CYLINDER
         MVC   DCBHIRCM,DS2HIRIN   HIGHEST POSSIBLE R ON A TRACK
*                                  CONTAINING HIGH LEVEL INDEX ENTRIES
         MVC   DCBHIRPD,DS2HIRPR   HIGHEST R ON ANY PRIME TRACK
*                                  IN THE DATA SET
         MVC   DCBHIROV,DS2HIROV   HIGHEST POSSIBLE R FOR OVERFLOW
*                                  DATA TRACKS
         MVC   DCBHIRSH,DS2RSHTR   R OF LAST DATA RECORD ON A
*                                  SHARED TRACK
         MVC   DCBTDC,DS2TAGDT     USER SUPPLIED NUMBER OF RECORDS
*                                  TAGGED FOR DELETION
         MVC   DCBRORG3+L'DCBRORG3-L'DS2RORG3(L'DS2RORG3),DS2RORG3
*                                  FOR EACH USE OF A DATA SET, THE
*                                  NUMBER OF ACCESSES TO AN OVERFLOW
*                                  RECORD WHICH IS NOT THE FIRST IN
*                                  A CHAIN OF SUCH RECORDS
         MVC   DCBNCRHI,DS2NOBYT   NUMBER OF BYTES OF MAIN STORAGE
*                                  NEEDED TO HOLD THE HIGHEST LEVEL
*                                  INDEX
         MVC   DCBNTHI,DS2NOTRK    NUMBER OF TRACKS OCCUPIED BY THE
*                                  HIGHEST LEVEL INDEX
*
***********************************************************************
* THE FOLLOWING THREE STATEMENTS MOVE FIELDS WHICH ARE CONTIGUOUS IN  *
* THE FORMAT 2 AND IN THE DCB.  THEY MAY BE COMBINED INTO ONE         *
* INSTRUCTION TO SAVE SPACE.                                          *
***********************************************************************
*
         MVC   DCBNREC,DS2PRCTR    NUMBER OF LOGICAL RECORDS IN THE
*                                  PRIME DATA AREA
         MVC   DCBST,DS2STIND      STATUS INDICATORS
         MVC   DCBFTCI,DS2CYLAD    MBBCCHH OF THE FIRST TRACK OF THE
*                                  CYLINDER INDEX
***********************************************************************
*
*
         MVC   DCBFTMI1,DS2ADLIN   MBBCCHH OF THE LOWEST LEVEL MASTER
*                                  INDEX
*
*
***********************************************************************
* THE FOLLOWING THREE STATEMENTS MOVE FIELDS WHICH ARE CONTIGUOUS IN  *
* THE FORMAT 2 AND IN THE DCB.  THEY MAY BE COMBINED INTO ONE         *
* INSTRUCTION TO SAVE SPACE.                                          *
***********************************************************************
*
         MVC   DCBFTHI,DS2ADHIN    MBBCCHH OF THE FIRST TRACK OF THE
*                                  HIGHEST LEVEL INDEX
         MVC   DCBLPDA,DS2LPRAD    MBBCCHHR OF THE LAST PRIME DATA
*                                  RECORD IN THE PRIME DATA AREA
         MVC   DCBLETI,DS2LTRAD    CCHHR OF THE LAST ACTIVE NORMAL
*                                  ENTRY OF THE TRACK INDEX ON THE
*                                  LAST ACTIVE CYLINDER
***********************************************************************
         MVC   DCBLECI,DS2LCYAD    CCHHR OF THE LAST ACTIVE ENTRY IN
*                                  THE CYLINDER INDEX
         MVC   DCBLEMI1,DS2LMSAD   CCHHR OF THE LAST INDEX ENTRY IN
*                                  THE FIRST LEVEL INDEX
         MVC   DCBLIOV,DS2LOVAD    MBBCCHHR OF THE LAST RECORD WRITTEN
*                                  IN THE INDEPENDENT OVERFLOW AREA
         MVC   DCBNBOV,DS2BYOVL    NUMBER OF BYTES REMAINING ON THE
*                                  CURRENT OVERFLOW TRACK
         MVC   DCBRORG2,DS2RORG2   NUMBER OF TRACKS REMAINING IN THE
*                                  INDEPENDENT OVERFLOW AREA
         MVC   DCBNOREC,DS2OVRCT   NUMBER OF LOGICAL RECORDS IN THE
*                                  OVERFLOW AREA
         MVC   DCBRORG1,DS2RORG1   NUMBER OF CYLINDER OVERFLOW  SA59407
*                                  AREAS THAT ARE FULL
*
***********************************************************************
* THE FOLLOWING FOUR STATEMENTS MOVE FIELDS WHICH ARE CONTIGUOUS IN   *
* THE FORMAT 2 AND IN THE DCB.  THEY MAY BE COMBINED INTO ONE         *
* INSTRUCTION TO SAVE SPACE.                                          *
***********************************************************************
*
         MVC   DCBFTMI2,DS22MIND   MBBCCHH OF THE FIRST TRACK OF THE
*                                  SECOND LEVEL MASTER INDEX
         MVC   DCBLEMI2,DS2L2MEN   CCHHR OF THE LAST ACTIVE ENTRY IN
*                                  THE SECOND LEVEL MASTER INDEX
         MVC   DCBLEMI3,DS2L3MIN   CCHHR OF THE LAST ACTIVE ENTRY IN
*                                  THE THIRD LEVEL MASTER INDEX
         MVC   DCBLPDT,DS2LPDT     MBBCCHHR OF THE LAST PRIME TRACK
*                                  ON THE LAST PRIME CYLINDER
***********************************************************************
         MVC   DCBHIRTI,DS2HIRTI   HIGHEST NUMBER OF INDEX ENTRIES THAT
*                                  FIT ON A PRIME DATA TRACK
*
* ISAM WORK AREA FIELDS
*
         MODESET  KEYADDR=DXUKEY,WORKREG=11  SET USERS KEY       Y02072
*
         MVC   ISLNIRT,DS2NIRT     HHR OF THE DUMMY TRACK INDEX ENTRY
         MVC   ISLHIRT,DS2HIRTI    HIGHEST NUMBER OF INDEX ENTRIES
*                                  THAT FIT ON A PRIME DATA TRACK
*
*
EXITRTN  EQU   *
         LM    R0,RF,DXCCW1        RESTORE REGISTERS             Y02072
*
         MODESET  EXTKEY=DATAMGT        SET DATA MGMT KEY        Y02072
*
         MVC   NULL(L'LOAD6D,RWTGC),LOAD6D ID OF IGG0196D        Y02072
*
LOOP     EQU   *
         LA    RWTGC,WTGOFF(RWTGC) POINT TO NEXT WTG ENTRY
         LA    RPARC,DCBOFF(RPARC) POINT TO NEXT ENTRY IN PARM LIST
         CLC   NULL(L'THISLOAD,RWTGC),THISLOAD
*                                  SHOULD WE PROCESS IN THIS MODULE
         BE    UPDTLOOP            YES, GO DO NEXT DCB
         CLC   NULL(L'OPNLOD7,RWTGC),OPNLOD7
*                                  IS IT THE END OF THE WTG TABLE
         BNE   LOOP                NO, CHECK NEXT DCB
END      EQU   *
         LR    RPARC,RPAR          RESET PARM LIST POINTER
         LA    RWTGC,RESETOFF(RWTG)
*                                  RESET WTG TABLE POINTER
ZEROCHK  EQU   *
         CLI   NULL(RWTGC),NULL    IS ENTRY ALREADY PROCESSED
         BNE   XCTLRTN             NO, GIVE CONTROL TO PROPER MODULE
         LA    RWTGC,WTGOFF(RWTGC) POINT TO NEXT WTG ENTRY
         LA    RPARC,DCBOFF(RPARC) POINT TO NEXT ENTRY IN PARM LIST
         B     ZEROCHK             CHECK NEXT ENTRY
*
*
*
XCTLRTN  EQU   *
         MVC   IDOFF(IDLEN,RWTG),NULL(RWTGC)
*                                  MOVE ID
         LA    RF,DXCCW12
         XCTL  EPLOC=(RWTG),DCB=0,SF=(E,(15)) XCTL TO NEXT LOAD  Y02072
*
*
* CONSTANTS
THISLOAD DC    C'6C'               CURRENT MODULE ID
OPNLOD7  DC    C'0S'               LAST OPEN MODULE ID
*
LOAD6D   DC    C'6D'                    ID OF MODULE IGG0196D    Y02072
*
PATCH    DC    XL50'00'                 ZEROED PATCH AREA        Y02072
         EJECT
FORCORE  DSECT
         IECDSECT
DSCBF2   DSECT
         IECSDSL1 (2)
         EJECT
         DCBD  DSORG=IS,DEVD=DA
ISLCOMON IGGLOAD
         END
