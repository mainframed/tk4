         TITLE 'IGG01922-VALIDATE FORMAT2 DSCB FOR SCAN AND BISAM'
IGG01922 CSECT
*
***********************************************************************
*                                                                     *
* MODULE-NAME = IGG01922                                              *
*                                                                     *
* DESCRIPTIVE-NAME = ISAM OPEN, FIXED LENGTH VALIDATION ROUTINE       *
*                                                                     *
* COPYRIGHT = NONE                                                    *
*                                                                     *
* STATUS = RELEASE OS/VS2-02, LEVEL 01                                *
*                                                                     *
* FUNCTION = INITIALIZE DCB HIIOV.  IF DCB OPENED FOR SHR, AND M OF   *
*            DFALIOV IS NOT 0, OR INDEPENDENT OVERFLOW NOT SPECIFIED, *
*            RETURN TO RESTART, OR XCTL TO NEXT OPEN EXECUTOR.        *
*            OTHERWISE INITIALIZE CP VXCCW1B TO READ EOF OR END OF    *
*            LOVAD TRACK FOR INDEPENDENT OVERFLOW RECORDS, AND UPDATE *
*            DS2RORG2, DS2OVRCT, AND DS2LOVAD.  IF DCB OPENED FOR SHR *
*            AND M OF DFALIOV IS 0, MERGE F2 DSCB FIELDS INTO FIELD   *
*            AREA.                                                    *
*                                                                     *
* NOTES = SEE BELOW                                                   *
*                                                                     *
*    DEPENDENCIES = NONE                                              *
*                                                                     *
*    RESTRICTIONS = NONE                                              *
*                                                                     *
*    REGISTER-CONVENTIONS = SEE REGISTER EQUATES.                     *
*                                                                     *
*    PATCH-LABEL = PATCH, A DC STATEMENT.                             *
*                                                                     *
* MODULE-TYPE = OPEN EXECUTOR                                         *
*                                                                     *
*    PROCESSOR = ASSEMXF-370R                                         *
*                                                                     *
*    MODULE-SIZE = 930 DECIMAL BYTES                                  *
*                                                                     *
*    ATTRIBUTES = PRIVILEGED, REENTRANT                               *
*                                                                     *
* ENTRY-POINT = IGG01922                                              *
*                                                                     *
*    PURPOSE = SEE FUNCTION                                           *
*                                                                     *
*    LINKAGE = RECEIVES CONTROL FROM ISAM OPEN EXECUTOR IGG01920 FOR  *
*              FIXED LENGTH RECORDS (FLR) ONLY.  RECEIVES CONTROL IN  *
*              STORAGE PROTECT KEY 5 AND PRIVILEGED STATE.            *
*                                                                     *
* INPUT = REGISTERS ESTABLISHED UPON ENTRY ARE                        *
*         REG 5 (RPAR) POINTS TO THE DCB COPY PARAMETER LIST          *
*         REG 6 (RWTG) POINTS TO THE O/C/EOV WHERE-TO-GO TABLE        *
*         REG 7 (RPARC) POINTS TO THE CURRENT ENTRY IN THE DCB COPY   *
*               PARAMETER LIST                                        *
*         REG 8 (RWTGC) POINTS TO THE CURRENT ENTRY IN THE WTG TABLE  *
*                                                                     *
*         HOWEVER, IF IGG01920 WAS ENTERED FROM RESTART, REGISTERS    *
*         ARE ESTABLISHED AS FOLLOWS:                                 *
*         REG 4 (RCORE) POINTS TO THE OPEN WORK AREA                  *
*         REG 7 (RPARC) IS ZERO                                       *
*         REG 8 (RWTGC) POINTS TO THE CURRENT WHERE-TO-GO ENTRY       *
*         REG 9 (RDEB) POINTS TO THE DEB                              *
*                                                                     *
* OUTPUT = REGISTERS 5 THROUGH 8 WILL BE IN THE SAME CONDITION AS     *
*          UPON ENTRY TO THIS MODULE                                  *
*                                                                     *
* EXIT-NORMAL = XCTL TO IGC0W05B IF RESTART ENTERED IGG01920;         *
*               OTHERWISE TO ISAM OPEN EXECUTOR IGG0196C FOR LOAD     *
*               MODE, IGG01928 FOR SCAN MODE, OR IGG0192H FOR BISAM.  *
*                                                                     *
* EXIT-ERROR = ABEND CODES:                                           *
*              033 - I/O ERROR TRYING TO READ EOF                     *
*                                                                     *
*              IF RESTART ENTERED IGG01920, DO NOT ISSUE ABEND, BUT   *
*              XCTL TO IGC0W05B WITH ABEND CODE IN REGISTER 1         *
*                                                                     *
* EXTERNAL-REFERENCES = SEE BELOW                                     *
*                                                                     *
*    ROUTINES = RESIDENT CONVERT ROUTINE                              *
*                                                                     *
*    DATA-AREAS = AREAS REFERENCED ARE:                               *
*                 FORCORE - OPEN WORK AREA                            *
*                                                                     *
*    CONTROL-BLOCKS = DCB, DCB COPY, DCBFA, DEB, DSCB, IOB, AND CVT.  *
*                                                                     *
* TABLES = NONE                                                       *
*                                                                     *
* MACROS = ABEND, EXCP, WAIT, AND XCTL                                *
*                                                                     *
* CHANGE-ACTIVITY = AS FOLLOWS:                                       *
*                                                                     *
*          RELEASE OS/VS2-02 DELETIONS                                *
*                                                               YM03922
*         CHANGES TO VS2-030
*A624160,624730,624760                                         @ZA06668
*C624240                                                       @ZA06668
*         CHANGES SINCE VS2-037
*A506700                                                       @ZA30069
*
*                                                                     *
***********************************************************************
         EJECT
DS2DSECT DSECT           FORMAT 2 DSCB
         DS    0D
DS2KEY   DS    CL44
DS2FMTID DS    CL1
DS2NOLEV DS    CL1
DS2DVIND DS    CL1
DS21RCYL DS    CL3
DS2LTCYL DS    CL2
DS2CYLOV DS    CL1
DS2HIRIN DS    CL1
DS2HIRPR DS    CL1
DS2HIROV DS    CL1
DS2RSHTR DS    CL1
DS2HIRTI DS    CL1                                               O19113
DS2HIIOV DS    CL1                                               O19113
DS2TAGDT DS    CL2
DS2RORG3 DS    CL3
DS2NOBYT DS    CL2
DS2NOTRK DS    CL1
DS2PRCTR DS    CL4
DS2STIND DS    CL1
DS2CYLAD DS    CL7
DS2ADLIN DS    CL7
DS2ADHIN DS    CL7
DS2LPRAD DS    CL8
DS2LTRAD DS    CL5
DS2LCYAD DS    CL5
DS2LMSAD DS    CL5
DS2LOVAD DS    CL8
DS2BYOVL DS    CL2
DS2RORG2 DS    CL2
DS2OVRCT DS    CL2
DS2RORG1 DS    CL2
FORCORE  DSECT
         IECDSECT
*                       CP TO READ TO EOF                        A25984
         ORG   DXCCW                                             A25984
VXCCW1   CCW   X'31',000,X'40',5        SRCH ID EQ   LPDA+3,5,CC
VXCCW2   CCW   X'08',000,X'00',0        TIC          VALCCW1
VXCCW3   CCW   X'9E',040,X'60',8       RD CKD(MT) VALCCW6,8,CC,SK
VXCCW4   CCW   X'9E',048,X'60',8       RD CKD(MT) VALCCW7,8,CC,SK
VXCCW5   CCW   X'08',016,X'00',0        TIC          VALCCW3
VXCCW6   DC    2F'0000'                 COUNT FLD    CCHHRKDD
VXCCW7   DC    2F'0000'                 COUNT FLD    CCHHRKDD
VXCCW8   DC    2F'0000'                NEW MBBCCHHR
SAVEREG  DS    CL8                     REG. SAVE AREA
         DS    CL4                     OLD TTR
SAVER2   DS    CL2                     NEW R VALUE
SAVER1   DS    CL2                     OLD R VALUE
         DCBD  DSORG=IS                                          O19113
SIDEQ    EQU   X'31'
TIC      EQU   X'08'
RDCNT    EQU   X'12'
RCKD     EQU   X'1E'
MT       EQU   X'80'
RDATA    EQU   X'06'
DC       EQU   X'80'
CC       EQU   X'40'
SILI     EQU   X'20'
SKIP     EQU   X'10'
EQ       EQU   8
LO       EQU   4
         EJECT
IHADEB   IGGDEBD                                                 Y02072
DCBFA     IGGDCBFA
R0       EQU   0                        WORK REG
RVAL     EQU   1                        BASE REG FOR VALIDATE CP
RDSCB    EQU   2                        BASE REG FOR DSCB FORMAT 2
RBASE    EQU   3                        BASE REG FOR THIS MODULE
RCORE    EQU   4                        BASE REG FOR OPEN WORK AREA
RPAR     EQU   5         *              ADDR OF PARAMETER TABLE
RWTG     EQU   6         *              ADDR OF WHERE-TO-GO TABLE
RPARC    EQU   7         *              CURRENT PARAMETER ENTRY
RWTGC    EQU   8         *              CURRENT WHERE-TO-GO ENTRY
RRSTC    EQU   9                        CURRENT RESTART ENTRY    A44475
RDEB     EQU   9                        DEB PTR
RA       EQU   10                       WORK REG
RB       EQU   11                       WORK REG
RC       EQU   12                       WORK REG
RD       EQU   13                       WORK REG
RE       EQU   14                       WORK REG
RF       EQU   15                       WORK REG
RJ       EQU   15
R1       EQU   1
*
*
CVTPTR   EQU   16                       LOCATION OF THE CVT PTR
SHR      EQU   X'80'                    TEST FOR DISP EQ SHR     Y02072
L3BYTE   EQU   7                        STORE 3 LOW ORDER BYTES  Y02072
         EJECT                                                   A25984
IGG01922 CSECT                                                   A25984
         BALR  RBASE,0                  ESTABLISH ADDRESSABILITY
         USING *,RBASE
         USING FORCORE,RCORE
         USING DS2DSECT,RDSCB
         USING IHADCB,RB                                         A25984
         LTR   RPARC,RPARC              CHECK FOR RESTART ENTRY  A25984
         BNZ   OPEN                     BRANCH IF NOT            A25984
         L     RRSTC,DXDSCB+28          GET CURRENT TBL ENTRY PT A44475
         L     RB,0(RRSTC)              GET CURRENT DCB ADDR     A44475
         LA    RDSCB,DXJBF              POINT TO FORMAT 2 DSCB   A25984
         B     COMMON                                            A25984
OPEN     EQU   *                                                 A25984
         USING IHADCB,RB                                         O19113
         L     RB,0(RPARC)              SET DCB ADDR
         L     RCORE,4(RWTGC)           SET OPEN WK AREA ADDR
         L     RDSCB,DSCCORE            SET PTR TO F2 DSCB       A25984
COMMON   EQU   *                                                 A25984
*
*
         L     RDEB,DCBDEBAD            SET DEB BASE             A25984
         USING IHADEB,RDEB              DEB ADDRESSABILITY       Y02072
         SR    RVAL,RVAL                CLEAR REGISTER           O19113
         IC    RVAL,DS2HIIOV            RVAL=HI R FOR IND OVFLOW O19113
         LTR   RVAL,RVAL                TEST IF ZERO             O19113
         BNZ   OVMERGE                  BRANCH IF NOT            O19113
         IC    RVAL,DS2HIROV            RVAL=HI R FOR OVERFLOW   O19113
OVMERGE  STC   RVAL,DCBHIIOV            HIIOV=HI R IND OVFLOW    O19113
*
         ST    RB,IOBWGHT               SET DCB ADDR IN PHONY IOB
*
*    IF DATA SET HAS BEEN OPENED FOR  SHR, DEBDCBFA WILL CONTAIN
*         A POINTER TO THE DCBFA.  IF THE M BYTE IN DFALIOV IS
*         nonzERO, WILL NOT VALIDATE DCBFIELDS AS IT HAS BEEN
*         done.
*
         L     RB,DEBEXPTR              ADDRESS OF DEB EXTNSION  Y02072
         USING DEBEXT,RB                USING ON DCB EXTENSION   Y02072
         TM    DEBDCBFA,SHR             TEST FOR DISP EQ SHR     Y02072
         BZ    CNTINU                   BR IF NOT TO CONTINUE    A37175
         L     RB,DEBDCBFA              FIELD AREA ADDRESS       Y02072
         USING DCBFA,RB                                          A43189
         CLI   DFALIOV,X'00'            TEST IF ALREZDY VALID    A43189
         BNZ   NOOVRFL                  B IF SO TO END           A43189
         DROP  RB                                                A43189
CNTINU   EQU   *                                                 A37175
         CLI   DS2LOVAD,X'00'          IS THERE INDEPENDENT OVFL
         BE    NOOVRFL                 NO
* VALIDATE PTR TO LAST REC IN INDEPENDENT O VRFLO
         LA    RVAL,DXCCW1              POINT TO WORKAREA CCWS   A25984
*INITIALIZE CHANNEL PROG TO READ EOF
*
         LM    RA,RF,VALCCW1
         LA    R0,DXDAADDR+3            SET ADDR IN CCW1 TO CCHHR 21684
         ALR   RA,R0                       IN DS2LPRAD
         ALR   RC,RVAL                  RELOCATE ADDR FLDS IN
         ALR   RE,RVAL                     CCW2 AND CCW3
         STM   RA,RF,VXCCW1             INIT.VXCCW1,2,3
         LM    RA,RD,VALCCW4
         ALR   RA,RVAL                  RELOCATE ADDR FLDS IN
         ALR   RC,RVAL                     CCW4 AND CCW5
         STM   RA,RD,VXCCW4             INIT.VXCCW4,5
*
         XC    VXCCW6(VXCCW7-VXCCW6+8),VXCCW6 CLEAR VXCCW6&7     A41406
*
*
         MVC   DXDAADDR,DS2LOVAD        SET UP IOB SEEK ADDR
*
         BAL   RC,EOFREAD               GO READ EOF FOR          A35340
*                                       INDEPENDENT              A35340
*                                         OVERFLOW
TESTIO   EQU   *                                                  21684
         TM    IOBSTAT0,X'01'           END OF FILE
         BO    SETLOVAD                 YES
         CLI   IOBECBPT,X'42'           TEST FOR END OF EXTENT    21684
         BNE   GOABEND                  NO END OF FILE AND NO END 21684
*                                       OF EXTENT IMPLIES AN I/O  21684
*                                       ERROR.                    21684
*                                                                 21684
*  GO TO NEXT INDEPENDENT OVERFLOW EXTENT.                        21684
*                                                                 21684
         SR    RA,RA                                              21684
         IC    RA,DXDAADDR              GET M FROM IOB            21684
         LA    RA,1(R0,RA)              ADD ONE FOR NEXT EXTENT   21684
         STC   RA,DXDAADDR              PUT M+1 IN IOBSEEK        21684
         CLC   DXDAADDR(1),16(RDEB)     DEBNMEXT VS NEXT M        21684
         BE    NOOVRFL                  EXIT--EXTENTS EXHAUSTED   21684
         SLL   RA,4                     M TIMES 16                21684
         LA    RA,32(RA,RDEB)           POINT TO NEW EXTENT       21684
         MVC   DXDAADDR+3(4),6(RA)      MOVE CCHH INTO IOB        21684
         MVI   DXDAADDR+7,X'00'         START AT FIRST RECORD     21684
         BAL   RC,EOTREAD               CHECK NEXT EXTENT        A35340
*                                        2168                    A35340
         B     TESTIO                   CHECK RESULT OF CP        21684
*
SETLOVAD LA    RB,SETLIOV2
         LA    RE,NOOVRFL
         BAL   RF,STOPCHK
*
* SET UP NEW MBBCCHHR
SETLIOV1 MVC   VXCCW8+3(5),VXCCW6       CCHHR FROM 1ST CT FLD
         B     SETLIOV3
SETLIOV2 MVC   VXCCW8+3(5),VXCCW7       CCHHR FROM 2ND CT FLD
SETLIOV3 MVC   VXCCW8(3),DXDAADDR       MBB SAME AS IN IOBSEEK    21684
*
NEWADDR  L     RC,CVTPTR               LOAD CVT POINTER
         L     RF,32(RC)               LOAD ADDR OF CONVERT RT
         STM   RDSCB,RBASE,SAVEREG     SAVE REG.2,3
         LR    RBASE,RVAL              SAVE REG.1
         LR    R1,RDEB                  SET PTR TO DEB
         LA    RDSCB,DS2LOVAD           SET PTR TO MBBCCHHR INPUT
         BALR  RE,RF                    CONVERT MBBCCHHR TO TTR
         LR    RDEB,R1                 RESTORE DEB PTR.
         LR    RVAL,RBASE              RESTORE REG.1
         ST    R0,SAVEREG+8            SAVE OLD TTR
         LA    RDSCB,VXCCW8             SET PTR TO MBBCCHHR INPUT
         LR    R1,RDEB                 SET DEB PTR FOR CONVERT RT
         L     RC,CVTPTR               LOAD CVT PTR
         L     RF,32(RC)               LOAD ADDR OF CONVERT RT
         BALR  RE,RF                    CON VERT MBBCCHHR TO TTR
         LR    RVAL,RBASE              RESTORE REG.1
         LM    RDSCB,RBASE,SAVEREG     RESTORE REG.2,3
         L     RA,SAVEREG+8             RA=OLD TTR. R0=NEW TTR
* UPDATE # OF UNUSED TRKS IN INDEPENDENT OVRFLO
*
         STH   R0,SAVER2                SAVE NEW R VALUE
         STH   RA,SAVER1                SAVE OLD R VALUE
         SRL   R0,16                    SHIFT TO LOW ORDER TWO    MC0P
*                                       BYTES                     MC0P
         SRL   RA,16                    SHIFT TO LOW ORDER TWO    MC0P
*                                       BYTES                     MC0P
         SLR   R0,RA                    R0= NEW TT - OLD TT       MC0P
         LR    RC,R0                   LOAD RC WITH DIFF.OF TTS
         LH    RB,DS2RORG2             OLD NO.UNUSED TRKS
         CLC   SAVER2(1),SAVER1        NEW R EQUAL OLD R
         BE    UPDATE1                 YES
         CLC   SAVER2(1),DS2HIIOV       NEW R = HI R FOR OVFLOW  O19113
         BNE   UPDATE1                 NO
         LA    RC,1(RC)                ADD1 TO DIFFERENCE
UPDATE1  SR    RB,RC                   SUBTRACT DIFF, FROM NO. OF
*                                         UNUSED TRKS
         STH   RB,DS2RORG2              UPDATE DS2RDRG2
*
* UPDATE # OF RECORDS IN INDEPENDENT OVRFLO
*
         LR    RD,R0                    RD=# TRKS DIFF.
         SR    RC,RC                    CLEAR REG
         IC    RC,DS2HIIOV              RC = NO. RECS/TRACK      O19113
         MR    RC,RC                    RD=NO.REC.DIFFERENCE
         SR    RA,RA                    CLEAR REG
         SR    RB,RB
         IC    RA,SAVER2                PICK UP R2
         IC    RB,SAVER1                PICK UP R1
         SR    RA,RB                    RA=NO.REC.DIFFERENCE
         AR    RD,RA                    RD=TOTAL NO.REC.DIFF.
         LH    RC,DS2OVRCT
         AR    RC,RD                   ADD TO NO OF REC IN OVRFLO
         STH   RC,DS2OVRCT              UPDATE DS2OVRCT
*
         XC    VXCCW8+1(2),VXCCW8+1    ZERO BB
         MVC   DS2LOVAD,VXCCW8          UPDATE DS2LOVAD
         B     NOOVRFL
*
* DETERMINE WHICH CCW THE CHANNEL PROG STOPPED ON;THE ONLY VALID STOPS
* ARE ON CCW4 OR CCW5
*
*
STOPCHK  L     RC,IOBCOMAD              ADDR OF NXT CCW TO BE EXEC.
         LA    RC,0(RC)                 CLEAR HI ORDER BYTE
         LA    RD,VXCCW4                ADDR OF CCW4
         CR    RC,RD                    NXT CCW IS VXCCW4
         BNE   SNDSTOP                  NO
         NC    VXCCW7(8),VXCCW7         2ND COUNT FLD EMPTY
         BCR   8,RE                     YES,DSCB PTR CORRECT     A35340
FRSTOUT  BR    RB                       NO,UPDATE DSCB PTR TO 2ND CNT
SNDSTOP  LA    RD,VXCCW5                ADDR OF CCW5
         CR    RC,RD                    NXT CCW IS VXCCW5
         BCR   8,RF                     YES,UPDATE DSCB PTR TO   A35340
*                                       1ST CNT
GOABEND  EQU   *                        COME HERE IF ERROR       A25984
         L     R1,ABENDCD               PERMANENT ERROR          A25984
         LTR   RPARC,RPARC              IS RESTART IN CONTROL    A25984
         BZ    RESTARTX                 DON'T ABEND IF SO        A25984
         L     RCORE,DXUDCBAD           USERS DCB ADDRESS      @ZA30069
         ABEND X'033',DUMP,,SYSTEM      SYSTEM 033 ABEND         Y02072
EOFREAD  SR    RA,RA                    CLEAR WORK REG.           16068
         IC    RA,DXDAADDR              GET M FROM IDB
         SLL   RA,4                     M X 16
         LA    RA,32(RA,RDEB)           POINT TO EXTENT OF M
EOTREAD  MVC   DXDAADDR+1(2),4(RA)      MOVE BB INTO IOB          16068
         XC    VXCCW6(16),VXCCW6        INIT.VXCCW6,7 TO ZERO    A35340
*                                        1606                    A35340
         LR    RB,RVAL                  SAVE REG.1
         MVC   SAVEREG(4),28(RDEB)      SAVE APP VECT TBL PTR    A25984
         MVC   29(3,RDEB),DXDEBAPP+1    POINT TO DUMMY VECT TBL  A25984
         L     RVAL,IOBWGHT             ADDR OF COPY DCB         Y02072
         STCM  RVAL,L3BYTE,DEBDCBAD+1   DEB PT TO COPY DCB       Y02072
*
         EXCP  DXIOB                    READ TO EOF
         WAIT  1,ECB=DXECB
         L     RVAL,DXUDCBAD            USERS DCB ADDRESS        Y02072
         STCM  RVAL,L3BYTE,DEBDCBAD+1   DEB PT TO USER DCB       Y02072
         MVC   28(4,RDEB),SAVEREG       RESTORE VECTOR TABLE PTR A25984
         LR    RVAL,RB                  RESTORE REG.1
         BR    RC                                                A35340
* RESTORE IOB TO ORIGINAL FORM
NOOVRFL  EQU   *                                                 A25984
         L     RB,IOBWGHT               RESTORE DCB ADDRESS      A35340
         USING IHADCB,RB                                         A43189
         L     RA,DEBEXPTR              ADDR OF DEB EXTENSION    Y02072
         USING DEBEXT,RA                USING ON DEB EXTENSION   Y02072
         TM    DEBDCBFA,SHR             TEST FOR DISP EQ SHR     Y02072
         BZ AROUND                      BR IF NOT TO CONTINUE    A37175
         L     RA,DEBDCBFA              ADDRESS OF FIELD AREA    Y02072
         USING DCBFA,RA                                          A43189
         CLI   DFALIOV,X'00'            VALIDATED?               A43189
         BNZ   AROUND                   B IF SO TO END           A43189
         MVC   DFANBOV,DS2BYOVL         NO. BYTES REMAINING ON  YM03922
*                                       CURRENT OVERFLOW TRACK  YM03922
         MVC   DFARORG2,DS2RORG2        FREE TRKS IN IND OVRFLW YM03922
         MVC   DFANOREC,DS2OVRCT        NO. RCDS IN OVERFLOW    YM03922
         MVC   DFALIOV,DS2LOVAD         IF NOT, SET UP LIOV      A43189
AROUND   EQU   *                                                 A37175
         LA    RA,DXDCB                 ADDR OF PHONY DCB
         ST    RA,IOBWGHT
NOEXEC1  EQU   *                                                 A35340
         LTR   RPARC,RPARC              IF REGISTER IS ZERO,     A35340
*                                       A2598                    A35340
         BZ    RESTART                  ENTRY WAS FROM RESTART   A35340
*                                       A2598                    A35340
         TM    DCBMACRF+1,X'18'         LOAD MODE                A25984
         BZ    TESTSCAN                                           P4701
         MVC   0(L'LOAD6C,RWTGC),LOAD6C RESUME LOAD - IGG0196C   Y02072
         B     RELOOP                                             P4701
TESTSCAN TM    DCBMACRF,X'40'           TEST IF GET              A25984
         BO    SCANNEXT                 YES, SCAN MODE
         MVC   0(L'LOAD2H,RWTGC),LOAD2H INITIALIZE WHERE-TO-GO   Y02072
         B     RELOOP
SCANNEXT MVC   0(L'LOAD28,RWTGC),LOAD28 INITIALIZE WHERE-TO-GO   Y02072
***********************************************************************
RELOOP   LA    RWTGC,8(RWTGC)           INCREMENT WTG CURNT ENTRY
         LA    RPARC,4(RPARC)           INCREMENT DCB CURNT ENTRY
         CLC   0(2,RWTGC),THISLOAD
         BCR   8,RBASE                  BRANCH=BEGINNING OF THIS MODULE
         CLC   0(2,RWTGC),OPNLOD7       TEST FOR END OF WTG TABLE
         BC    7,RELOOP                 BRANCH=NOT AT END
ATEND    LR    RPARC,RPAR               INITIALIZE REPARC
         LA    RWTGC,32(RWTG)           INITIALIZE RWTGC
ZCHECK   CLI   0(RWTGC),X'00'           TEST FOR ENTRY = 0
         BC    7,TCTLRTN                BRANCH = NOT ZERO
ITSZERO  LA    RWTGC,8(RWTGC)
         LA    RPARC,4(RPARC)
         B     ZCHECK
TCTLRTN  EQU   *
         MVC   6(2,RWTG),0(RWTGC)       MOVE ID
*
         LA    RJ,DXCCW12
         XCTL  EPLOC=(RWTG),DCB=0,SF=(E,(15)) XCTL TO NEXT LOAD  Y02072
RESTART  SR    RVAL,RVAL                ZERO RETURN CODE         A25984
         MODESET  KEYADDR=DXUKEY,WORKREG=1  CHANGE TO USER KEY @ZA06668
         L     RDEB,DXDSCB+28           GET CURRENT TBL ENTRY PT M3874
         L     RB,DXUDCBAD              GET USER DCB ADDR      @ZA06668
         MVC   DCBLPDA(8),DS2LPRAD      MERGE FORMAT 2 FIELDS    A25984
         MVC   DCBST(1),DS2STIND        INTO DCB.                A25984
         MVC   DCBLIOV(8),DS2LOVAD                               A25984
         MVC   DCBNOREC(2),DS2OVRCT                              A25984
         MVC   DCBRORG2(2),DS2RORG2                              A25984
         MODESET  EXTKEY=DATAMGT        CHANGE TO DATA MGT KEY @ZA06668
         SR    RVAL,RVAL                RESTORE REG 1          @ZA06668
RESTARTX EQU   *                                                 A42870
         LA    RWTG,DXDSCB+464          SET UP WORKAREA FOR XCTL A25984
         MVC   0(8,RWTG),IGC0W05B       RESTART MODULE NAME      A25984
         ST    RWTG,DXDSCB+16           SAVE RWTG FOR RESTORE
         LM    RDSCB,RA,DXDSCB                                   A42870
         LA    RJ,8(RWTG)               SET UP REG FOR XCTL      A25984
         XCTL  EPLOC=(RWTG),DCB=0,SF=(E,(15)) RETURN TO RESTART  Y02072
* CONSTANTS
VALCCW1  CCW   X'31',000,X'40',5        SRCH ID EQ   LPDA+3,5,CC
VALCCW2  CCW   X'08',000,X'00',0        TIC          VALCCW1
VALCCW3  CCW   X'9E',040,X'60',8       RD CKD(MT) VALCCW6,8,CC,SK
VALCCW4  CCW   X'9E',048,X'60',8       RD CKD(MT) VALCCW7,8,CC,SK
VALCCW5  CCW   X'08',016,X'00',0        TIC          VALCCW3
THISLOAD DC    C'22'
OPNLOD7  DC    C'0S'
LOAD2H   DC    C'2H'                    BISAM MODULE IGG0192H    Y02072
LOAD28   DC    C'28'                    SCAN MODULE IGG01928     Y02072
LOAD6C   DC    C'6C'                    RESUME LOAD - IGG0196C   Y02072
IGC0W05B DC    C'IGC0W05B'              ISAM RESTART MODULE      A25984
         DS    0F                                                A25984
ABENDCD  DC    X'80033000'             ABEND 33, DUMP OPTION ON
*
*
MODID    DC    C'IGG01922'             MODULE NAME             @ZA30069
DATE     DC    CL8'&SYSDATE'           COMPILATION DATE        @ZA30069
FIX      DC    C'OZ30069'              LATEST FIX              @ZA30069
PATCH    DC    XL50'00'                ZEROED PATCH AREA         Y02072
         END
