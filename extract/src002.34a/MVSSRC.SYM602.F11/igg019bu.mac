         TITLE 'IGG019BU--OFFSET READ OF A KEYED, VS, BDAM DATA SET'
IGG019BU CSECT
*
*        RELEASE 20 DELETIONS
*        RELEASE 21 DELETIONS
*        RELEASE 22 DELETIONS
*        RELEASE 23 DELETIONS
*        RELEASE 24 DELETIONS
*        RELEASE 25 DELETIONS
*        RELEASE 26 DELETIONS
*        RELEASE 27 DELETIONS
*        RELEASE 28 DELETIONS
*
*STATUS CHANGE LEVEL 000
*
*FUNCTION-OPERATION--OFFSET READ ROUTINE FOR BDAM DATA SETS
*     1. THE ROUTINE RECEIVES CONTROL WHEN A READ MACRO-INSTRUCTION
*        IS ENCOUNTERED IN THE PROCESSING PROGRAM.
*     2. IT ENTERS THE ADDRESS OF THE IOB INTO THE DECB TO PER-
*        MIT THE CHECK ROUTINE TO LATER TEST EXECUTION OF THE
*        CHANNEL PROGRAM.
*     3. IT COMPLETES THE CHANNEL PROGRAM BY INSERTING THE BUFFER
*        ADDRESS AND THE RECORD LENGTH.  THE BUFFER ADDRESS IS OBTAINED
*        FROM THE DECB AND OFFSET BY KEY LENGTH IF THERE IS NO KEY
*        WITH THIS SEGMENT (I.E., IF THIS IS A SEGMENT SUBSEQUENT TO
*        THE FIRST SEGMENT OF A RECORD).  THIS DETERMINATION IS MADE
*        BY CHECKING TO SEE IF THE CCW HAS BEEN CHANGED BY MODULE
*        IGG019BV TO READ JUST DATA.  THE RECORD LENGTH IS OBTAINED
*        FROM THE DCB AND MODIFIED BY KEY LENGTH IF APPROPRIATE.
*     4. IT THEN ISSUES EXCP
*
*ENTRY POINT--IGG019BU IS ENTERED FROM A READ MACRO EXPANSION
*              LA    1,DECB
*              LA    14,DCB
*              L     15,48(14)
*              BALR  14,15
*
*INPUT--DECB
*
*OUTPUT--DECB
*         DECSDECB--ECB
*         DECIOBPT--ADDRESS OF IOS IOB (SAM IOB + 8)
*        DCB
*         DCBIOBA--ADDRESS OF CURRENT IOB
*        IOB
*         IOBFLAG0--READ INDICATED--X'20'
*         IOBECBPT--ADDRESS OF ECB
*         IOBSTART--STARTING ADDRESS OF CHANNEL PROGRAM
*        CHANNEL PROGRAM
*         ADDRESS OF I/O AREA (OFFSET BY KEY LENGTH IF APPROPRIATE)
*         LENGTH OF RECORD (INCLUDING KEY LENGTH IF APPROPRIATE)
*
*EXTERNAL ROUTINES--NONE
*
*EXITS NORMAL--RETURN TO CALLER ON REG 14 AFTER SCHEDULING I/0
*
*EXITS ERROR--NONE
*                        REGISTER ASSIGNMENTS
*
WORKR    EQU   0                        WORK REG
DECBR    EQU   1                        DECB ADDR
PARMR    EQU   DECBR                    REG FOR PASSING PARAMETERS
DCBR     EQU   2                        DCB ADDR
IOBR     EQU   3                        ADDR OF IOB (SAM, PAM, PREFIX)
KEYLEN   EQU   4                        KEY LENGTH
WORKR2   EQU   5                        WORK REG
BLKSIZE  EQU   6                        BLOCK SIZE
CPR      EQU   7                        CHANNEL PROG ADDR
BASER    EQU   8                        BASE REG
SAVER    EQU   13                       SAVE AREA ADDR
RETRNR   EQU   14                       RETURN ADDR
ENTRYR   EQU   15                       ENTRY POINT ADDR
*
         USING IGG019BU,BASER           BASE FOR MODULE
         USING IHADCB,DCBR              BASE FOR DCB
         USING DEFDECB,DECBR            BASE FOR DECB
         USING DEFIOB,IOBR              BASE FOR IOB
         USING DEFCP,CPR                BASE FOR CHANNEL PROG
*
*                          CONSTANT EQUATES
*
IOBSIZE  EQU   48                       IOB SIZE
ADDRLEN  EQU   3                        LENGTH OF ADDRESS
CCHHRLEN EQU   5                        LENGTH OF BLOCK ID (CCHHR)
PREFXLEN EQU   8                        LENGTH OF IOB PREFIX
ADOFFSET EQU   1                        OFFSET TO ADDR FROM FULL WORD
SAVOFFST EQU   20                       OFFSET IN SAVE AREA
RDKYDATA EQU   X'08'                    BIT=1 MEANS READ KEY & DATA
BITSOFF  EQU   X'7F'                    TURN OFF BITS IN DCBOFLGS
BITOFF   EQU   X'01'                    TURN OFF BIT IN IOBFLAG0
READBIT  EQU   X'20'                    READ BIT IN IOBFLAG1
INVLDNPT EQU   X'41'                    CODE FOR INVALID NOTE-POINT
ERRORMSK EQU   X'FF'                    TEST FOR ERROR FROM POINT RTN
RDBKBIT  EQU   X'BF'                    READ BACK BIT IN DCBOFLGS
*
*
ENTRY    EQU   *
         STM   RETRNR,BASER,SAVOFFST(SAVER)  SAVE USER REGS
         LR    BASER,ENTRYR             ESTABLISH ADDRESSABILITY
         L     DCBR,DECDCBAD            DCB ADDR
         L     IOBR,DCBIOBA             IOB ADDR (MINUS 8)
         L     IOBR,IOBCHAIN            NEXT IOB ON CHAIN
         LA    CPR,IOBSIZE(IOBR)        CHANNEL PROG ADDR
         NI    DCBOFLGS,RDBKBIT         TURN OFF READ BACK BIT IN OFLGS
         LA    WORKR,IOBFLAG1           IOS IOB POINTER
         ST    WORKR,DECIOBPT           PLACE IN DECB
         ST    DECBR,IOBECBPT           PUT ECB PTR IN IOB
         CLI   IOBECB,ERRORMSK          ERROR IN A POINT RTN
         BNE   PROCESS                  NO--BRANCH TO PROCESS
         MVI   DECSDECB,INVLDNPT        YES--INDICATE ERROR IN ECB
         LM    RETRNR,BASER,SAVOFFST(SAVER)  RESTORE USER REGS
         BR    RETRNR                   RETURN TO USER
*
*
PROCESS  EQU   *
         NI    IOBFLAG0,BITOFF          TURN OFF BITS IN IOB
         OI    IOBFLAG0,READBIT         TURN ON READ BIT
         ST    CPR,IOBSTART             PUT ADDR OF CHANNEL PROG IN IOB
         NI    DCBOFLGS,BITSOFF         TURN OFF OFLAGS
         SR    KEYLEN,KEYLEN
         IC    KEYLEN,DCBKEYLE          GET KEY LENGTH
         LH    BLKSIZE,DCBBLKSI         BLOCK SIZE FROM DCB
         L     WORKR2,DECAREA           BUFFER ADDR
         LA    WORKR2,0(WORKR2)         CLEAR HIGH ORDER BYTE
*
*  THIS SECTION OF CODE DETERMINES IF THE RECORD ABOUT TO BE READ IS A
*  SEGMENT OF A SEGMENTED RECORD.  IF IT IS AND IT IS NOT THE FIRST
*  SEGMENT, THEN THE READ WILL BE OFFSET IN THE BUFFER BY KEY LENGTH.
*  THE DETERMINATION IS MADE BY CHECKING TO SEE IF THE CCW HAS BEEN
*  CHANGED BY THE CHANNEL END APPENDAGE TO READ JUST DATA RATHER THAN
*  KEY AND DATA
*
         TM    RKDCC,RDKYDATA           IS CCW FOR READ JUST DATA
         BO    NOOFFSET                 NO--OFFSET NOT NEEDED
         AR    WORKR2,KEYLEN            ADD KEY LENGTH
FILLCCW  EQU   *
         IC    WORKR,RKDCC              SAVE CCW OP CODE
         ST    WORKR2,RKDCC             PUT BUFFER ADDR IN CCW
         STC   WORKR,RKDCC              PUT OP CODE BACK
         STH   BLKSIZE,RKDBYTCT         PUT LENGTH IN CCW
         B     UPDATDCB                 GO TO UPDATE DCB
*
NOOFFSET EQU   *
         AR    BLKSIZE,KEYLEN           ADJUST LENGTH FOR KEY
         B     FILLCCW                  GO TO COMPLETE CCW
UPDATDCB EQU   *
         ST    IOBR,DCBIOBA             UPDATE DCB IOB POINTER
         MVC   IOBSKCC(CCHHRLEN),CPCOUNT   MOVE CCHHR INTO IOBSEEK
         LA    PARMR,PREFXLEN(IOBR)     ADDR OF IOS IOB
         EXCP  (1)
*
         LM    RETRNR,BASER,SAVOFFST(SAVER) RESTORE USER REGS
         BR    RETRNR                   RETURN TO USER
*
DEFDECB  DSECT                          DECB STRUCTURE
DECSDECB DS    CL4                      ECB
DECTYPE  DS    CL2                      TYPE OF I/O MACRO & OPTIONS
DECLNGTH DS    CL2                      LENGTH OF DATA OR KEY & DATA
DECDCBAD DS    A                        ADDR OF DCB
DECAREA  DS    A                        ADDR OF READ/WRITE AREA
DECIOBPT DS    A                        ADDR OF IOB
*
DEFIOB   DSECT                          IOB STRUCTURE
IOBFLAG0 DS    0BL1                     I/O FLAGS
IOBCHAIN DS    A                        ADDR OF NEXT IOB
IOBECB   DS    F                        ECB
IOBFLAG1 DS    BL1                      I/O FLAGS
IOBFLAG2 DS    BL1                      I/O FLAGS
IOBSENS0 DS    BL1                      FIRST SENSE BYTE
IOBSENS1 DS    BL1                      SECOND SENSE BYTE
IOBECBCC DS    0X                       COMPLETION CODE
IOBECBPT DS    A                        ADDR OF ECB
IOBFLAG3 DS    BL1                      I/O FLAGS
IOBCSW   DS    CL7                      LOW ORDER BYTES OF LAST CSW
IOBSIOCC DS    0X                       SIO CONDITION CODE
IOBSTART DS    A                        ADDR OF CHANNEL PROG
         DS    0C                       RESERVED
IOBDCBPT DS    A                        ADDR OF DCB
IOBRESTR DS    CL4                      PURGE CHAIN/CCHH/COMMAND,CP
IOBINCAM DS    2C                       USE VARIES
IOBERRCT DS    2X                       NO OF ERROR RETRIES
IOBSEEK  DS    0D                       SEEK FIELD FOR DA
IOBSKM   DS    1X                       EXTENT NUMBER
IOBSKBB  DS    2X                       BIN
IOBSKCC  DS    2X                       CYLINDER
IOBSKHH  DS    2X                       HEAD
IOBSKR   DS    1X                       RECORD
*
DEFCP    DSECT                          OFFSET READ CHANNEL PROG
CPNOP1   DS    0D                       NOP
NOP1CC   DS    0X                       COMMAND CODE
NOP1DATA DS    A                        DATA ADDR
NOP1FLAG DS    BL1                      FLAGS
         DS    1X                       NOT USED
NOP1BYTC DS    2X                       BYTE COUNT
CPSCH    DS    D                        SEARCH ID EQUAL
CPTIC1   DS    D                        TIC TO SEARCH
CPTIC2   DS    D                        TIC TO READ COUNT
CPRDCNT  DS    0D                       READ COUNT
RCTCC    DS    0X                       COMMAND CODE
RCTDATAD DS    A                        DATA ADDR
RCTFLAGS DS    BL1                      FLAGS
         DS    1X                       NOT USED
RCTBYTCT DS    2X                       BYTE COUNT
CPRDKD   DS    0D                       READ (KEY) DATA
RKDCC    DS    0X                       COMMAND CODE
RKDDATAD DS    A                        DATA ADDR
RKDFLAGS DS    BL1                      FLAGS
         DS    1X                       NOT USED
RKDBYTCT DS    2X                       BYTE COUNT
CPNOP2   DS    0D                       NOP
NOP2CC   DS    0X                       COMMAND CODE
NOP2DATA DS    A                        DATA ADDR
NOP2FLAG DS    BL1                      FLAGS
         DS    1X                       NOT USED
NOP2BYTC DS    2X                       BYTE COUNT
CPCOUNT  DS    D                        FIELD TO READ COUNT INTO
         ORG   *-3                      BACK TO KEY LENGTH FIELD
KEYLNGTH DS    1X                       KEY LENGTH IN COUNT
*
         DCBD  DSORG=BS,DEVD=DA         DCB DSECT
         END
