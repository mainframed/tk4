IGG019KI CSECT
*
*
*        RELEASE 21 DELETIONS/CHANGES
*        RELEASE 20 DELETIONS/CHANGES
*3000000400,013200,015200,019200                                 S20201
 TITLE   'IGG019KI - KEY MODULE'
*STATUS CHANGE LEVEL 003
*FUNCTION/OPERATION- THIS ROUTINE IS ENTERED FROM IGG019KA TO         *
*   CONSTRUCT A CHANNEL PROGRAM TO PERFORM A READ OR WRITE BY KEY,    *
*   (SEARCH ARGUMENT IS BLOCK KEY).                                   *
*   THE CCWS ARE CONSTRUCTED OF CONSTANTS DEFINED IN THIS MODULE AND  *
*   ADDRESSES RESOLVED AT THE TIME THIS ROUTINE IS EXECUTED.          *
*                                                                     *
*ENTRY POINT- 'IGG019KI' (BEGIN) IS ENTERED BY THE CALLING SEQUENCE,  *
*   L    15,DCBDKEY                                                   *
*   BALR 14,15                                                        *
*                                                                     *
*INPUT- THE REGISTERS ARE INPUT AS FOLLOWS,                           *
*   4 - DCB ADDRESS                                                   *
*   5 - DECB ADDRESS                                                  *
*   6 - ADDRESS OF CCW 1 (CHANNEL PROGRAM START)                      *
*   7 - IOB ADDRESS                                                   *
*   11- BASE ADDRESS OF FOUNDATION MODULE (IGG019KA)                  *
*                                                                     *
*OUTPUT- THE REQUIRED CCWS WILL BE CONSTRUCTED FOLLOWING THE IOB.     *
*   REGISTER 6 WILL CONTAIN THE ADDRESS OF THE LAST CCW.              *
*                                                                     *
*EXTERNAL ROUTINES- 'IGG019KQ' WILL BE ENTERED TO CONSTRUCT THE VERIFY*
*   CCWS IF THE REQUEST IS A READ AND THE WRITE VALIDITY CHECK OPTION *
*   HAS BEEN SPECIFIED. 'IGG019KW' WILL BE ENTERED TO CONSTRUCT THE   *
*   EXTENDED SEARCH PORTION OF THE CHANNEL PROGRAM IF SPECIFIED.      *
*                                                                     *
*EXITS-NORMAL- 'D4TOVER' IS THE EXIT TO IGG019KQ IF REQUIRED.         *
*   'NONCARN' IS THE EXIT TO IGG019KW IF REQUIRED.                    *
*   'D4EXIT' IS THE NORMAL RETURN TO THE MODULE IGG019KA.             *
*                                                                     *
*TABLES/WORK AREAS- N/A                                               *
*                                                                     *
*ATTRIBUTES- THIS ROUTINE IS REENTRANT AND EXECUTED ENABLED IN USER   *
*   KEY, PROBLEM PROGRAM STATE.                                       *
*                                                                     *
*NOTE- REGISTER USAGE IN THIS MODULE MUST REMAIN CONSISTENT WITH THE  *
*   MODULES, IGG019KA, IGG019KQ, AND IGG019KW.                        *
         EJECT
         USING IGG019KI,D4EPREG
         USING IHADCB,DCBREG
         USING DECBDEF,DECBREG
         USING IOBDEF,IOBREG
*
*
*  REGISTER USAGE
*
D4WKREG  EQU   0         WORK REGISTER
D4VERR   EQU   1         POINTER TO A SEARCH CCW FOR VERIFY
DEBREG   EQU   3         DEB ADDRESS
DCBREG   EQU   4         DCB ADDRESS
DECBREG  EQU   5         DECB ADDRESS
D4CCWIND EQU   6         CHANNEL COMMAND INDEX
IOBREG   EQU   7         IOB ADDRESS
SECTOR   EQU   8         POINTER TO SECTOR1                      S20201
VERBASE  EQU   10        BASE FOR THE VERIFY MODULE
FDNBASE  EQU   11        BASE FOR THE FOUNDATION MODULE
D4       EQU   12        RETURN FOR INTERNAL BAL
RETREG   EQU   14        RETURN TO FOUNDATION MODULE
D4EPREG  EQU   15        BASE FOR THIS MODULE
*
*
*
BEGIN    B     D4ENTRY            BRANCH TO THE BEGINNING OF THE CODE
D4EKAD   DC    V(IGG019KW)        ADDR OF KEY-EXTENDED SRCH MOD  Y02072
*
*
D4ENTRY  EQU   *                                                 S20201
         LA    D4WKREG,IOBDNRCF+D2      GET AND STORE ADDR OF    S20201
         ST    D4WKREG,0(D4CCWIND)       NEW RECORD COUNT FIELD IN CCW1
         MVI   0(D4CCWIND),D4RDCNT      MOVE READ COUNT OP CODE TO CCW1
         MVC   4(5,D4CCWIND),D4CON1     MOVE FLAGS,COUNT AND SEARCH OP.
         MVC   9(3,D4CCWIND),DECKYADR+1 MOVE KEY ADDRESS FROM DECB
         MVI   12(D4CCWIND),D4CCHN+SILI SET COMMAND CHAIN+SILI FLAGS
         MVC   15(1,D4CCWIND),DCBKEYLE MOVE KEY LENGTH AS COUNT
         LR    D4WKREG,D4CCWIND        MOVE TO A COMMON LOCATION
         TM    IOBDTYPE,EXSCH           AND USE AS TIC ADDRESS
         BC    8,*+8                     UNLESS THIS IS EXTENDED
         LA    D4WKREG,IOBCP+D96                   SEARCH        S20201
         ST    D4WKREG,16(D4CCWIND)    STORE TIC ADDRESS
         MVI   16(D4CCWIND),D4TIC       MOVE TIC OP CODE
         MVI   24(D4CCWIND),RDDOP      MOVE READ DATA OP-CODE
         MVC   25(3,D4CCWIND),DECAREA+1 MOVE AREA ADDRESS FROM DECB
         MVC   30(2,D4CCWIND),DECLNGTH  MOVE DECB LENGTH AS COUNT
         TM    IOBDTYPE,EXSCH          IF THIS IS EXTENDED SEARCH
         BC    1,D4EXSCH                GO TO THAT MODULE
         LA    D4CCWIND,24(D4CCWIND)   INCREMENT TO LAST CCW
         TM    IOBDTYPE+1,READ         TEST FOR READ OR WRITE
         BCR   1,RETREG                EXIT IF THIS WAS A READ REQUEST
         MVI   0(D4CCWIND),D4WRITE     CHANGE READ OP  TO WRITE DATA OP
         TM    IOBDTYPE,VERIFY          TEST FOR VERIFY OPTION
         BCR   8,RETREG                  IF NOT GO EXIT
         LA    D4VERR,IOBCP+8           SET A POINTER TO THE SEARCH CCW
         TM    IOBDCPND,YESCARN         IS RPS FEATURED          S20201
         BNO   NOTCARN                  NO, THEN TEST FOR READ   S20201
*                                       OR WRIT                  S20201
         L     SECTOR,IOBDCPND          GET SECTOR ADDR IF RPS   S20201
         MVI   HALFCCW(D4CCWIND),CCHN   COMMAND CHAIN THE READ   S20201
*                                       SECTOR                   S20201
         LA    SECTOR,ONEBYTE(0,SECTOR) POINT TO SECTOR2         S20201
         ST    SECTOR,FULLCCW(0,D4CCWIND)  PUT PT IN RD SECT     S20201
*                                       ADDR AREA                S20201
         MVI   FULLCCW(D4CCWIND),RDSECT PUT IN RD SECT COMMAND   S20201
*                                       CODE                     S20201
         MVC   CARN12(HALFCCW,D4CCWIND),FLGCNT  PUT IN           S20201
*                                       FLAG/COUNT FIEL          S20201
         LA    D4CCWIND,FULLCCW(0,D4CCWIND) UPDATE PT TO NEXT    S20201
*                                       CCW                      S20201
NOTCARN  EQU   *                                                 S20201
         L     VERBASE,12(0,FDNBASE)   LOAD VERIFY'S ENTRY POINT/BASE
         BR    VERBASE                 GO CONSTRUCT VERIFY CCWS
*                                      VERIFY MODULE WILL RETURN TO
*                                      FOUNDATION ON REGISTER 14
*
D4EXSCH  EQU   *                                                 S20201
         TM    IOBDCPND,YESCARN         IS RPS AROUND            S20201
         BNO   NONCARN                  IF NOT, BRANCH           S20201
         MVI   IOBDCPND,SETSECT         PUT IN HEX 23 FOR KW     S20201
NONCARN  EQU   *                                                 S20201
         L     D4EPREG,D4EKAD           LOAD ADDR OF EXTENDED    S20201
*                                       SEARCH                   S20201
         BCR   15,D4EPREG            GO TO KEY-EXTENDED SEARCH MODULE
*
*
D4CON1   DC    X'6000000529'  FLAGS, COUNT, SEARCH-KEY-EQUAL OP-CODE
D4RDCNT  EQU   X'12'   OP CODE FOR READ COUNT
D4CCHN   EQU   X'40'   COMMAND CHAIN FLAG
SILI     EQU   X'20'                   SUPPRESS INCORRECT LENGTH
D4TIC    EQU   X'08'   TIC OP CODE
D4WRITE  EQU   X'05'   WRITE DATA OP CODE
RDDOP    EQU   X'06'    READ DATA OP-CODE
*
*
*      RPS CONSTANTS AND EQUATES
*
FLGCNT   DC    X'00000001'              FLAG/COUNT FIELD FOR RD  S20201
*                                       SECT                     S20201
*
ONEBYTE  EQU   1                        ONE BYTE  DISPLACEMENT   S20201
D2       EQU   2                        2 BYTE DISPLACEMENT      S20201
HALFCCW  EQU   4                        HALF CCW  DISPLACEMENT   S20201
FULLCCW  EQU   8                        FULL CCW DISPLACEMENT    S20201
CARN12   EQU   12                       1-1/2 CCW DISPLACEMENT   S20201
D96      EQU   96                       12 CCW DISPLACEMENT      S20201
*
RDSECT   EQU   X'22'                    READ SECTOR COMMAND CODE S20201
CCHN     EQU   X'40'                    COMMAND CHAINING FLAG    S20201
SETSECT  EQU   X'23'                    CODE FOR KW MODULE       S20201
YESCARN  EQU   X'FF'                   BYTE TO TEST FOR RPS      S20201
         DC    X'00'   FILLER - FILLER - FILLER
HYGDN    DC    C'IGGR'                  THIS CONSTANT IS TO      S20201
*                                         POSITION SENSING
*                                          MODULE
*              THE FOLLOWING CNOP MUST REMAIN ON A HALF-WORD BOUNDARY
         CNOP  0,8                 PAD MODULE TO DOUBLE WORD MULTIPLE
         EJECT
*              EXCEPTION CODES
NOREC    EQU   X'80'
LGNCK    EQU   X'40'
NOSPA    EQU   X'20'
INVAL    EQU   X'10'
UNCOR    EQU   X'08'
ENDOD    EQU   X'04'
UNREL    EQU   X'02'
*              OPTIONS AND TYPE
VERIFY   EQU   X'80'
OFLOW    EQU   X'40'
EXSCH    EQU   X'20'
FEEDB    EQU   X'10'
ACTAD    EQU   X'08'
DYNBF    EQU   X'04'
READX    EQU   X'02'
RELRD    EQU   X'01'
KEYOP    EQU   X'80'
LGNOP    EQU   X'40'
READ     EQU   X'08'
KEY      EQU   X'04'
ADD      EQU   X'02'
*      DATA CONTROL BLOCK (DCB) DEFINITION
*
         DCBD  DSORG=DA
*
*
*
*      DATA EVENT CONTROL BLOCK (DECB) DEFINITION
         DS    0F
DECBDEF  DSECT
DECSDECB DS    CL4            STANDARD EVENT CONTROL BLOCK (ECB)
DECTYPE  DS    CL2            TYPE FIELD
DECLNGTH DS    CL2            DATA LENGTH
DECDCBAD DS    CL4            ADDRESS OF DCB
DECAREA  DS    CL4            ADDRESS OF DATA AREA
DECIOBPT DS    CL4            ADDRESS OF THE IOB
DECKYADR DS    CL4            ADDRESS OF KEY
DECRECPT DS    CL4            ADDRESS OF RECORD REFERENCE
         EJECT
*
*      I/O BLOCK (IOB) DEFINITION
*
*              STANDARD IOB
         DS    0F
IOBDEF   DSECT
IOBFLAG1 DS    CL1            STATUS FLAG
IOBFLAG2 DS    CL1            ERROR FLAG
IOBSENSE DS    CL2            SENSE BYTES 0 AND 1
IOBECBPT DS    CL4            ECB POINTER
IOBCSW   DS    0CL8           CHANNEL STATUS WORD
IOBCSWCA DS    0CL4           COMMAND ADDRESS
         DS    CL1
         DS    CL3
IOBCSWST DS    CL2            STATUS BYTES
IOBCSWCT DS    CL2            REMAINING COUNT
IOBSTART DS    0CL4           CHANNEL PROGRAM POINTER
IOBSIOCC DS    CL1            SIO CONDITION CODE
         DS    CL3
IOBDCBPT DS    0CL4           DCB POINTER
IOBWGHT  DS    CL1            CHANNEL WEIGHT
         DS    CL3
IOBRESTR DS    0CL4           RESTART POINTER
         DS    CL1
         DS    CL3
IOBINCAM DS    CL2            BLOCK COUNT INCREMENT AMOUNT
IOBERRCT DS    CL2            ERROR COUNTER
IOBSEEK  DS    CL8            SEEK ADDRESS
*        BDAM EXTENDED IOB
*
IOBDBYTR DS    CL2            BYTES REMAINING ON TRACK
IOBDIOBS DS    CL2            SIZE OF THIS IOB
IOBDPLAD DS    0CL4           IOB POOL LINK ADDRESS
IOBDAVLI DS    CL1            IOB AVAILABILITY INDICATOR
         DS    CL3
IOBDTYPE DS    CL2            TYPE FIELD
IOBDSTAT DS    CL2            IOB STATUS
IOBDCPND DS    CL4            CHANNEL PROGRAM NORMAL END ADDRESS
IOBDBYTN DS    CL2            BYTES NEEDED BY NEW RECORD
         DS    CL2
IOBDQPTR DS    CL4            LINK FIELD FOR QUEUED IOB'S
IOBUPLIM DS    CL8            LIMIT MBBCCHHR FOR EXTENDED SEARCH
IOBDNRCF DS    CL8            COUNT FIELD FOR NEW RECORD
IOBCP    DS    CL200                   CHANNEL PROGRAM AREA
         END
