      TITLE 'IGG019LI -  BDAM CHECK MODULE '
IGG019LI CSECT                                                        *
*                                                                     *
*MODULE NAME - IGG019LI                                               *
*                                                                     *
*DESCRIPTIVE NAME - BDAM CHECK MODULE                                 *
*                                                                     *
*COPYRIGHT = NONE                                                     *
*                                                                     *
*CHANGE ACTIVITY                                                      *
*
*        RELEASE 21 CHANGES/DELETIONS
*3043014050-014150                                               21905
*3043014200                                                      M0055
*        RELEASE VS2-2 CHANGES/DELETIONS
*014050-014150                                                   Y02072
*                                                                YM3029
*        RELEASE VS2-3 CHANGES/UPDATES
*C016400,C017200                                                ZA02222
*                                                                     *
* STATUS CHANGE LEVEL 003
*                                                                     *
*FUNCTIONS/OPERATION - THIS MODULE IS ENTERED BY THE CHECK MACRO      *
*    ISSUED BY THE PROBLEM PROGRAM.THIS ROUTINE WAITS(IF NECESSARY)   *
*    FOR THE COMPLETION OF THE INPUT/OUTPUT OPERATION ASSOCIATED WITH *
*    THE DATA EVENT CONTROL BLOCK(DECB) PASSED TO THIS ROUTINE BY     *
*    THE PROBLEM PROGRAM. IF THE I/O OPERATION HAS COMPLETED WITHOUT  *
*    ANY OF THE EXCEPTION BITS IN THE DECB BEING SET, THIS ROUTINE    *
*    THEN FREES THE IOB ASSOCIATED WITH THE I/O EVENT TO THE POOL OF  *
*    IOBS. IF ONE OF THE EXCEPTION BITS HAS BEEN SET THIS ROUTINE WILL*
*    (1) ABEND IF NO SYNAD ROUTINE HAS BEEN SUPPLIED BY THE USER      *
*    (2) GO TO THE USER'S SYNAD ROUTINE IF ONE IS SUPPLIED AND WHEN   *
*    CONTROL IS RETURNED TO THIS ROUTINE BY THE USER THE PROCESSING   *
*    CONTINUES AS IF NONE OF THE EXCEPTION BITS HAD BEEN SET          *
*                                                                     *
*ENTRY POINT -'IGG019LI'IS THE ENTRY POINT FROM THE CHECK MACRO       *
*    EXPANSION VIA THE CALLING SEQUENCE-          L    15,DCBCHECK    *
*                                                 BALR 14,15          *
*                                                                     *
*INPUT- INPUT TO THE CHECK ROUTINE IS THE ADDRESS OF THE DECB ASSIGNED*
*    TO THE READ OR WRITE BEING CHECKED. THIS ADDRESS WILL BE LOADED  *
*    INTO REGISTER 1 PRIOR TO ENTERING THIS ROUTINE.                  *
*                                                                     *
*OUTPUT- THE IOB(IF ONE HAS BEEN ASSIGNED) FOR THIS REQUEST IS FREED  *
*    TO THE POOL OF IOBS.                                             *
*    IF THE USER'S SYNAD EXIT IS TAKEN,REGISTER 0 WILL CONTAIN THE    *
*    DECB ADDRESS AND REGISTER 1 WILL CONTAIN THE DCB ADDRESS PLUS    *
*    SOME INFORMATIONAL BITS.                                         *
*                                                                     *
*EXTERNAL ROUTINES- NONE                                              *
*                                                                     *
*EXITS - 'RETURN' IS THE EXIT TO RETURN TO THE PROBLEM PROGRAM ON     *
*    NORMAL COMPLETION.                                               *
*    'SETSYNAD' IS THE EXIT TO THE USER'S SYNCHRONOUS ERROR ROUTINE.  *
*                                                                     *
*TABLES/WORK AREAS- NONE                                              *
*                                                                     *
*ATTRIBUTES - THIS ROUTINE IS REENTRANT AND EXECUTED ENABLED.         *
*                                                                     *
         EJECT
*
*        REGISTER USAGE
*
*
PARAM1   EQU   0                       PARAMETER REGISTER FOR SYNAD
DCBR     EQU   1                       ADDRESS OF DCB FOR SYNAD+CODES
DECBR    EQU   2                       DECB ADDRESS
CODER    EQU   3                       CODE REGISTER
CODER1   EQU   4                       CODE REGISTER
IOBR     EQU   4                       IOB ADDRESS
BASE     EQU   5                       BASE OF THIS MODULE
RETREG   EQU   14                      RETURN REGISTER
EPREG    EQU   15                      ENTRY POINT REGISTER
*
*
*
         USING IOBDEF,IOBR
         USING IGG019LI,BASE
         USING DECB,DECBR                                        Y02072
         USING IHADCB,DCBR
*
*
         SAVE  (14,5)                  SAVE REGISTERS 14-5
         LR    BASE,EPREG
         LR    DECBR,DCBR
         TM    DECSDECB,COMPLETE       HAS THIS ECB BEEN POSTED COMPLET
         BZ    WAIT                    NO WAIT ON THIS ECB
CHECKERR TM    DECSDECB+1,ALLONES      ANY ERRORS IN ECB         M0055
         BZ    MKIOBAVL                NO.MAKE IOB AVAILABLE
         LA    CODER,WRITE             PUT WRITE BIT ON
         TM    DECTYPE+1,READ          IS THIS A READ
         BZ    WRITEADD                NO GO ON
         LA    CODER,READ1             YES CHANGE WRITE TO READ
         L     IOBR,DECIOBPT           GET IOB ADDRESS
         LTR   IOBR,IOBR               WAS AN IOB ASSIGNRD
         BZ    INVREQ                  NO, SKIP TESTING IF BLOCK IS IN
         CLC   IOBDCPND+1(3),IOBCSW+1  IS CHANNEL PROGRAM COMPLETE
         BNE   INVREQ                  NO GO ON
         LA    CODER1,BIT3             YES INDICATE BLOCK IS    ZA02222
         OR    CODER,CODER1            IN CORE.
WRITEADD TM    DECTYPE+1,ADD           IS THIS A WRITE ADD
         BO    INVREQ                  YES GO ON
         LA    CODER1,BIT3             NO INDICATE THIS IS AN   ZA02222
         OR    CODER,CODER1            UPDATE RATHER THAN A NEW RECORD
INVREQ   TM    DECSDECB+1,INVALID      IS THIS AN INVALID REQUEST
         BZ    UNRELATD                NO GO ON
         LA    CODER1,BADREQ           YES INDICATE THAT THIS IS AN
         OR    CODER,CODER1            INVALID REQUEST
UNRELATD TM    DECSDECB+1,NOTRELTD     IS THIS AN UNREACHABLE BLOCK
         BZ    NOSPACE                 NO GO ON
         LA    CODER1,UNREACH          YES INDICATE THIS BLOCK COULD
         OR    CODER,CODER1            NOT BE REACHED
NOSPACE  TM    DECSDECB+1,NOSPACEB     WAS NO SPACE AVAILABLE
         BZ    SYNAD                   NO GO ON
         LA    CODER1,NOSPACEF         YES INDICATE NO SPACE FOUND
         OR    CODER,CODER1
SYNAD    EQU   *
         L     DCBR,DECDCBAD           PUT DCB ADDR INTO REG 1
         TM    DCBSYNAD+3,X'01'        IS THERE A SYNAD ROUTINE
         BZ    SETSYNAD                YES GO SET UP TO LINK TO IT
         LH    PARAM1,ABNOSYND                                   I19116
         SVC   55                                                I19116
SETSYNAD LR    PARAM1,DECBR            PUT DECB ADDR INTO REG 0
         SLL   CODER,24                PUT ERROR SUMMARY BITS INTO
         OR    DCBR,CODER              HI-ORDER BYTE OF REG 1
         L     EPREG,DCBSYNAD          GET SYNAD ADDRESS
         STM   2,5,44(13)              SAVE OUR REGISTERS
         LM    2,5,28(13)              RESTORE USER'S REGISTERS
         BALR  RETREG,EPREG            LINK TO IT
         STM   2,5,28(13)              SAVE USER'S REGISTERS
         LM    2,5,44(13)              RESTORE OUR REGISTERS
MKIOBAVL L     IOBR,DECIOBPT           GET  THE IOB ADDR.
         LTR   IOBR,IOBR               WAS AN IOB ASSIGNED
         BZ    RETURN                  NO, RETURN TO USER
FROMPOOL MVI   IOBDAVLI,0              ZERO AVAILABILITY BYTE
RETURN   EQU   *
         RETURN (14,5),T
WAIT     WAIT  ECB=(1)                 WAIT FOR EVENT TO COMPLETE
         B     CHECKERR                GO BACK TO CHECK ERRORS
*
AVLMSK   EQU   X'80'                   IOB AVAILABILITY BYTE
ALLONES  EQU   X'FF'                   MASK TO TEST FOR ANY ERRORS
WRITE    EQU   X'40'                   WRITE BIT IN CODE
READ     EQU   X'08'                   READ BIT IN DECTYPE FIELD
ADD      EQU   X'02'                   WRITE ADD BIT IN DECTYPE FIELD
INVALID  EQU   X'10'                   INVALID REQUEST IN DECB
BADREQ   EQU   X'08'                   INVALID REQUEST IN CODE
NOTRELTD EQU   X'02'                   UNRELATED ERROR IN DECB
UNREACH  EQU   X'02'                   UNRELATED ERROR IN CODE
NOSPACEB EQU   X'20'                   NO SPACE FOUND BIT IN DECB
NOSPACEF EQU   X'01'                   NO SPACE FOUND BIT IN CODE
ABNOSYND DC    XL2'1000'               ABEND CODE IF NO SYNAD IS GIVEN
COMPLETE EQU   X'40'                   COMPLETE BIT IN ECB
READ1    EQU   X'80'                   READ BIT IN CODE
MODID    DC    C'IGG019LI'              MODULE ID                Y02072
PATCH    DC    XL50'0'                  PATCH AREA OF 50 BYTES   Y02072
         EJECT
***********************************************************************
*              DSECTS                                                 *
***********************************************************************
         DCBD  DSORG=DA
         EJECT
         IHADECB                                                 Y02072
         EJECT
*
*      I/O BLOCK (IOB) DEFINITION
*
*              STANDARD IOB
         DS    0F
IOBDEF   DSECT
IOBFLAG1 DS    CL1            STATUS FLAG
IOBFLAG2 DS    CL1            ERROR FLAG
IOBSENSE DS    CL2            SENSE BYTES 0 AND 1
IOBECBPT DS    CL4            ECB POINTER
IOBCSW   DS    0CL8           CHANNEL STATUS WORD
IOBCSWCA DS    0CL4           COMMAND ADDRESS
         DS    CL1
         DS    CL3
IOBCSWST DS    CL2            STATUS BYTES
IOBCSWCT DS    CL2            REMAINING COUNT
IOBSTART DS    0CL4           CHANNEL PROGRAM POINTER
IOBSIOCC DS    CL1            SIO CONDITION CODE
         DS    CL3
IOBDCBPT DS    0CL4           DCB POINTER
IOBWGHT  DS    CL1            CHANNEL WEIGHT
         DS    CL3
IOBRESTR DS    0CL4           RESTART POINTER
         DS    CL1
         DS    CL3
IOBINCAM DS    CL2            BLOCK COUNT INCREMENT AMOUNT
IOBERRCT DS    CL2            ERROR COUNTER
IOBSEEK  DS    CL8            SEEK ADDRESS
*        BDAM EXTENDED IOB
*
IOBDBYTR DS    CL2            BYTES REMAINING ON TRACK
IOBDIOBS DS    CL2            SIZE OF THIS IOB
IOBDPLAD DS    0CL4           IOB POOL LINK ADDRESS
IOBDAVLI DS    CL1            IOB AVAILABILITY INDICATOR
         DS    CL3
IOBDTYPE DS    CL2            TYPE FIELD
IOBDSTAT DS    CL2            IOB STATUS
IOBDCPND DS    CL4            CHANNEL PROGRAM NORMAL END ADDRESS
IOBDBYTN DS    CL2            BYTES NEEDED BY NEW RECORD
         DS    CL2
IOBDQPTR DS    CL4            LINK FIELD FOR QUEUED IOB'S
IOBUPLIM DS    CL8            LIMIT MBBCCHHR FOR EXTENDED SEARCH
IOBDNRCF DS    CL8            COUNT FIELD FOR NEW RECORD
IOBCP    DS    CL8            FIRST CCW IN CHANNEL PROGRAM
IOBDIOBX EQU   42                      OFFSET FOR IOBDIOS
IOBDPLAX EQU   44                      OFFSET FOR IOBDPLAD
         END
