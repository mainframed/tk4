         TITLE 'IGG019KW - KEY - EXTENDED SEARCH MODULE'
IGG019KW CSECT
*MODULE NAME - IGG019KW                                               *
*                                                                     *
*DESCRIPTIVE NAME - BUILDS EXTENDED SEARCH CHANNEL PROGRAM FOR        *
*                   BDAM SEARCH ON KEYS                               *
*                                                                     *
*COPYRIGHT - NONE                                                     *
*                                                                     *
*CHANGE ACTIVITY                                                      *
*
*          RELEASE 19 CHANGES/DELETIONS
*2762001000-001200,002200,002800,003200,003600,005600-006200,    S19015
*2762013200-013600,015400-017600,018200-018800,019400-021000,    S19015
*2762025600                                                      S19015
*          RELEASE 20 CHANGES/DELETIONS
*2971000250-000400,000600-000700,015970,016530                   20201
*          RELEASE 21 CHANGES/DELETIONS
*                                                               SA53177
*          VS1-1 CHANGES/DELETIONS
*          VS2-1 CHANGES/DELETIONS
*          VS1-2 CHANGES/DELETIONS
*          VS2-2 CHANGES/DELETIONS
*
*STATUS CHANGE LEVEL 006
*                                                                     *
*FUNCTION/OPERATION- THIS MODULE IS ENTERED FROM IGG019KI OR IGG019KR *
*   TO COMPLETE THE CONSTRUCTION OF A CHANNEL PROGRAM WHICH SEARCHES  *
*   MULTIPLE TRACKS FOR AN EQUAL TO THE KEY FIELD SPECIFIED IN A      *
*   READ OR WRITE TYPE 'DK'.                                          *
*                                                                     *
*ENTRY POINT- 'IGG019KW' (BEGIN) IS ENTERED BY THE CALLING SEQUENCE,  *
*   L     15,4(IGG019KI BASE) OR IGG019KR
*   BALR  14,15                                                       *
*                                                                     *
*INPUT- THE FOLLOWING REGISTERS ARE PASSED,                           *
*   5 - DECB ADDRESS                                                  *
*   6 - ADDRESS OF CCW1                                               *
*   7 - IOB ADDRESS                                                   *
*   11- BASE ADDRESS OF IGG019KA OR IGG019KJ (BASE ROUTINE)           *
*                                                                     *
*OUTPUT- THE CCWS WILL BE CONSTRUCTED AND APPENDED TO THE IOB, AND    *
*   REGISTER 6 WILL CONTAIN THE ADDRESS OF THE LAST CCW.              *
*                                                                     *
*EXTERNAL ROUTINE- 'IGG019KQ' (WRITE-VERIFY) WILL BE ENTERED TO       *
*   CONSTRUCT THE CCWS REQUIRED TO VERIFY THE NEW DATA IF THIS IS A   *
*   WRITE REQUEST AND THE WRITE VALIDITY CHECK OPTION IS SPECIFIED IN *
*   THE DCB.                                                          *
*                                                                     *
*EXIT-NORMAL- 'EXIT1' IS A RETURN TO IGG019KA OR IGG019KJ WHEN THE    *
*   CCWS FOR A READ TYPE DK ARE COMPLETE.                             *
*   'EXIT2' IS A RETURN TO IGG019KA OR IGG019KJ WHEN THE CCWS FOR A   *
*   WRITE TYPE DK, NO VERIFY, ARE COMPLETE.                           *
*   'EXIT3' IS THE EXIT TO IGG019KQ TO CONSTRUCT THE VERIFY CCWS.     *
*                                                                     *
*TABLES/WORK AREAS- N/A                                               *
*                                                                     *
*ATTRIBUTES- THIS ROUTINE IS REENTRANT AND EXECUTED ENABLED.          *
*                                                                     *
*                                                                     *
*
*   REGISTER USAGE
*
         USING IGG019KW,EPREG
         USING IHADCB,DCBREG
         USING IOBDEF,IOBREG
         USING DECBDEF,DECBREG
         USING EXTSRCH,CCWIND                                    S19015
*
WKREG    EQU   0         WORK REGISTER/VERIFY PARAMETER
PARAM    EQU   1         VERIFY PARAMETER
WKREG1   EQU   2         WORK REGISTER
DDEBREG  EQU   3         DEB ADDRESS
DCBREG   EQU   4         DCB ADDRESS
DECBREG  EQU   5         DECB ADDRESS
CCWIND   EQU   6         CCW INDEX
IOBREG   EQU   7         IOB ADDRESS
VERBASE  EQU   10        VERIFY BASE/ENTRY POINT
FDNBASE  EQU   11        FOUNDATION MODULE BASE
INTRET   EQU   12        RETURN FOR LINKAGE TO VERIFY
SAVE     EQU   13        USER'S SAVE AREA ADDRESS
RETREG   EQU   14        RETURN TO FOUNDATION MODULE
EPREG    EQU   15        ENTRY POINT/BASE OF THIS MODULE
*
*
*
*
BEGIN    EQU   *
         LA    PARAM,ESSKE1            ADDR OF SEARCH KEY EQUAL  S19015
         MVC   ESNOP3(CCWLNG),ESMTSIE  MOVE READ DATA CCW TO     S19015
*                                       CCW9                     S19015
         LA    WKREG1,ESNOP3           POINT TO CCW9-NOP OR READ S19015
*                                       SE                       S19015
         TM    IOBDTYPE,READX+FEEDB    ARE FEEDBACK CCWS NEEDED
         BC    7,FDBK                  YES, GO MOVE THEM
         TM    DCBBFTEK,RBFTKMSK       DOES BUFFER TECHNIQUE     S19015
*                                       EQUAL                    S19015
         BO    FDBK                    YES,GO SET UP FEEDBACK    S19015
*                                       CCWS                     S19015
*
         TM    IOBDTYPE,OFLOW+VERIFY   IS A SEEK ADDRESS NEEDED
         BC    14,NOFDBK                NO, OMIT FEEDBACK CCWS
         TM    IOBDTYPE+1,READ         SEEK ADDR IS NOT NEEDED FOR A
         BC    1,NOFDBK                 READ REQUEST.
*
FDBK     EQU   *                                                 S19015
         MVC   ESRC(FOURCCWS),ESNOP1   MOVE ORIG 4 CCWS TO       S19015
*                                       CCW13-16                 S19015
         LA    PARAM,ESRC              ADDR OF READ COUNT CCW    S19015
*                                       FOR                      S19015
         ST    PARAM,ESTIC4            TIC CCW15                 S19015
         MVI   ESTIC4,TICOP            INSERT TIC OP CODE IN     S19015
*                                       CCW15                    S19015
         LA    PARAM,ESSKE2            ADDR OF CCW14-SRCH KEY    S19015
*                                       EQUAL                    S19015
         MVI   ESRHA,RDHMADDR          INSERT READ HA INTO CCW11 S19015
         MVI   ESRHA+BYTE4,CCHN+SILI+SKIP  TURN OFF INDEX POINT  S19015
         MVI   ESRHA+BYTE7,ONE             LATCH                 S19015
         MVC   ESNOP3(CCWLNG),NOP      NOP OR READ SECTOR-CCW9   S19015
         MVC   ESNOP3A(CCWLNG),NOP     NOP OR SET SECTOR         S19015
*                                       (0)-CCW10                S19015
         MVC   ESNOP3B(CCWLNG),NOP     NOP OR SET SECTOR-CCW12   S19015
         CLI   IOBDCPND,SETSC           RCD READY USED           20201
         BNE   NOTRRA                   BRANCH IF NO             20201
         L     WKREG1,IOBDCPND          GET SECTOR ADDR          20201
         ST    WKREG1,ESNOP3A           PUT IN CCW WITH SET      20201
*                                       SECTOR                   20201
*                                       CMND
         LA    WKREG1,ONE(WKREG1)       INCR ADDR BY ONE         20201
         ST    WKREG1,ESNOP3B           PUT IN CCW WITH CMND     20201
         ST    WKREG1,ESNOP3            PUT IN CCW               20201
         MVI   ESNOP3,RDSC              PUT IN RD SECTOR CMND    20201
*
         MVI   ESNOP3B,SETSC            RESTORE COMMAND          20201
         MVI   ESNOP3+BYTE4,CCHN        COMMAND CHAIN RD SECTOR  20201
         MVI   ESNOP3A+BYTE4,CCHN       COMMAND CHAIN SET        20201
*                                       SECTOR=0                 20201
         MVI   ESNOP3B+BYTE4,CCHN       COMMAND CHAIN SET SECTOR 20201
NOTRRA   EQU   *                                                 20201
         LA    WKREG1,ESRD1             PT TO CCW 16             20201
*
NOFDBK   EQU   *                                                 S19015
         MVC   ESNOP1(CCWLNG),NOP      INSERT NOP IN CCW1        S19015
         CLI   IOBDCPND,SETSC           RCD READY USED           20201
         BNE   NOTRRD                   BRANCH NO                20201
         MVC   ESNOP1(BYTE4),IOBDCPND   MOVE ADDR AND CMND TO    20201
*                                       CCW                      20201
         MVI   ESNOP1+BYTE4,CCHN        COMMAND CHAIN CCW        20201
NOTRRD   EQU   *                                                 20201
         LA    WKREG,ESSIE1            ADDR OF CCW2-SRCH ID      S19015
*                                       EQUAL                    S19015
         ST    WKREG,ESTIC1            TIC ADDR FOR CCW3         S19015
         MVI   ESTIC1,TICOP            INSERT TIC OP CODE IN     S19015
*                                       CCW3                     S19015
         MVC   ESSKE1(CCWLNG),ESSIE1   MOVE ORIG SEARCH KEY = TO S19015
*                                       CC                       S19015
         OI    ESSKE1,SILI             TURN ON SILI BIT          S19015
         LA    WKREG,ESMTSIE           ADDR OF CCW4-MULTI-TRK    S19015
*                                       SRCH                     S19015
         ST    WKREG,ESTIC3            EQUAL FOR TIC CCW8        S19015
         MVI   ESTIC3,TICOP            INSERT TIC OP CODE IN     S19015
*                                       CCW8                     S19015
*
         LA    WKREG,IOBSEEK+3         CONSTRUCT SEARCH-ID-EQUAL (R0)
         ST    WKREG,ESSIE1            AND INSERT INTO CCW2      S19015
         MVI   ESSIE1,SCHIDEQ          MOVE IN SEARCH ID EQUAL   S19015
*                                       OP                       S19015
         MVC   ESSIE1+BYTE4(BYTE4),FLGCTTIC  INSERT FLAG         S19015
*                                       BYTES-CCW2               S19015
*
         LA    WKREG,IOBUPLIM+3         CONSTRUCT SEARCH-ID-EQUAL CCW
         ST    WKREG,ESMTSIE            INSERT SEARCH LIMIT INTO S19015
*                                       CCW                      S19015
         MVI   ESMTSIE,MSCHIDEQ         MULTI-TRK SRCH ID EQUAL  S19015
         TM    DCBOPTCD,DCBOPTA         ACTUAL ADDR SPECIFIED?  SA53177
         BNO   NOTACT                   NO, USE EXTENDED SRCH   SA53177
         MVI   ESMTSIE,SCHIDEQ          IF ACTUAL DONT DO MULTI SA53177
*                                       TRK SRCH SINCE EXTENDED SA53177
*                                       SRCH NOT VALID WITH ACT SA53177
NOTACT   EQU   *                                                SA53177
         LA    WKREG,ESSKE1            ADDR OF CCW7-SEARCH KEY   S19015
*                                       EQUA                     S19015
         ST    WKREG,ESTIC2            USED FOR CCW5-TIC AADRESS S19015
         MVC   ESMTSIE+BYTE4(BYTE5),FLGCTTIC INSERT FLAG BYTES   S19015
*                                       IN C                     S19015
*                                      AND TIC OP CODE FOR CCW5
         MVC   ESNOP2(CCWLNG),NOP      NOP FOR CCW6              S19015
         MVI   ESNOP2+BYTE4,SILI       TURN OFF COMMAND CHAINING S19015
*
*
         LR    CCWIND,WKREG1           SET CCWIND TO LAST CCW
         NI    BYTE4(CCWIND),NOCCHN    TURN OFF CMD CHAIN IN     S19015
*                                       LAST C                   S19015
         TM    IOBDTYPE+1,READ         IF THIS IS A READ REQUEST
EXIT1    BCR   1,RETREG                 RETURN TO THE FOUNDATION
*
         TM    DCBBFTEK,RBFTKMSK       DOES BUFFER TECHNIQUE     S19015
*                                       EQUAL                    S19015
         BO    IOBTEST                 YES, GO CHECK IOB TYPE    S19015
         MVI   0(CCWIND),WRTDATA
IOBTEST  EQU   *                                                 S19015
         TM    IOBDTYPE,VERIFY         IF NO VERIFY OPTION
EXIT2    BCR   8,RETREG                 RETURN TO THE FOUNDATION MODULE
*
         TM    DCBBFTEK,RBFTKMSK       DOES BUFFER TECHNIQUE     S19015
*                                       EQUAL                    S19015
         BZ    NOVRE                   NO,LEAVE PARAM POINT TO   S19015
*                                       SK=                      S19015
         LA    PARAM,IOBCP+CCWLNG      POINT TO SRCH ID EQUAL    S19015
*                                       CCW                      S19015
         CLI   IOBDCPND,SETSC           RCD READY USED           20201
         BNE   NOTRRJ                   BRANCH IF NO             20201
         MVI   IOBDCPND,MASK            RESET RCD READY          20201
*                                       INDICATOR                20201
*                                       TO NON VRE
NOTRRJ   EQU   *                                                 20201
NOVRE    EQU   *                                                 S19015
         L     VERBASE,12(0,FDNBASE)   LOAD ENTRY POINT/BASE OF VERIFY
EXIT3    BR    VERBASE
*
*
*
FLGCTTIC DC    X'4000000508'  COMMAND CHAIN,COUNT FIVE,TIC OP-CODE
         DC    X'00'                   PAD TO HALF WORD BOUNDARY
*              THE FOLLOWING CNOP MUST REMAIN ON A HALF-WORD BOUNDARY
         CNOP  0,8                     PAD TO DOUBLE WORD SIZE FOR TTR
SCHIDEQ  EQU   X'31'          SEARCH-ID-EQUAL OP-CODE
TICOP    EQU   X'08'          TIC OP-CODE
MSCHIDEQ EQU   X'B1'          MULTIPLE TRACK,SEARCH-ID-EQUAL
NOP      DC    X'0300000060000001'     NOP CCW                   S19015
RBFTKMSK EQU   X'20'                   DCBBFTEK=R--VS            S19015
CCWLNG   EQU   8                       LENGTH OF A CCW-8 BYTES   S19015
FOURCCWS EQU   32                      LENGTH OF 4 CCWS-32 BYTES S19015
BYTE4    EQU   4                       LENGTH FOR MOVE           S19015
*                                       INSTRUCTION              S19015
BYTE5    EQU   5                       LENGTH FOR MOVE           S19015
*                                       INSTRUCTION              S19015
BYTE7    EQU   7                       USED FOR DISPLACEMENT     S19015
ONE      EQU   1                       LATCH BYTE                S19015
RDDATA   EQU   X'06'          READ DATA OP-CODE
CHNSILI  EQU   X'60'          COMMAND CHAIN,SUPPRESS INCORRECT LENGTH
FIVE     EQU   X'05'          COUNT OF FIVE
WRTDATA  EQU   X'05'          WRITE DATA OP-CODE
CCHN     EQU   X'40'          COMMAND CHAIN FLAG
NOCCHN   EQU   X'BF'                   MASK TO TURN OFF CMD      S19015
*                                       CHAININ                  S19015
SILI     EQU   X'20'          SUPPRESS INCORRECT LENGTH INDICATION
SKIP     EQU   X'10'          SKIP FLAG
RDHMADDR EQU   X'1A'          READ HOME ADDRESS OP CODE
         EJECT
*              OPTIONS AND TYPE
VERIFY   EQU   X'80'
OFLOW    EQU   X'40'
EXSCH    EQU   X'20'
FEEDB    EQU   X'10'
ACTAD    EQU   X'08'
DYNBF    EQU   X'04'
READX    EQU   X'02'
RELRD    EQU   X'01'
KEYOP    EQU   X'80'
LGNOP    EQU   X'40'
READ     EQU   X'08'
KEY      EQU   X'04'
ADD      EQU   X'02'
*
RDSC     EQU   X'22'                    RD SECTOR CCW CMND       20201
SETSC    EQU   X'23'                    SET SECTOR CCW CMND      20201
MASK     EQU   X'FF'                    NON VRE RECORD READY     20201
*                                       INDIC                    20201
PATCH    DC    XL50'0'                  PATCH AREA               Y02072
         EJECT
*
*
*
         DCBD  DSORG=DA
         EJECT
*
*
*      DATA EVENT CONTROL BLOCK %DECB< DEFINITION
         DS    0F
DECBDEF  DSECT
DECSDECB DS    CL4            STANDARD EVENT CONTROL BLOCK %ECB<
DECTYPE  DS    CL2            TYPE FIELD
DECLNGTH DS    CL2            DATA LENGTH
DECDCBAD DS    CL4            ADDRESS OF DCB
DECAREA  DS    CL4            ADDRESS OF DATA AREA
DECIOBPT DS    CL4            ADDRESS OF THE IOB
DECKYADR DS    CL4            ADDRESS OF KEY
DECRECPT DS    CL4            ADDRESS OF RECORD REFERENCE
*
*
*
*
*      I/O BLOCK (IOB) DEFINITION
*
*              STANDARD IOB
         DS    0D
IOBDEF   DSECT
IOBFLAG1 DS    CL1            STATUS FLAG
IOBFLAG2 DS    CL1            ERROR FLAG
IOBSENSE DS    CL2            SENSE BYTES 0 AND 1
IOBECBPT DS    CL4            ECB POINTER
IOBCSW   DS    0CL8           CHANNEL STATUS WORD
IOBCSWCA DS    0CL4           COMMAND ADDRESS
         DS    CL1
         DS    CL3
IOBCSWST DS    CL2            STATUS BYTES
IOBCSWCT DS    CL2            REMAINING COUNT
IOBSTART DS    0CL4           CHANNEL PROGRAM POINTER
IOBSIOCC DS    CL1            SIO CONDITION CODE
         DS    CL3
IOBDCBPT DS    0CL4           DCB POINTER
IOBWGHT  DS    CL1            CHANNEL WEIGHT
         DS    CL3
IOBRESTR DS    0CL4           RESTART POINTER
         DS    CL1
         DS    CL3
IOBINCAM DS    CL2            BLOCK COUNT INCREMENT AMOUNT
IOBERRCT DS    CL2            ERROR COUNTER
IOBSEEK  DS    CL8            SEEK ADDRESS
*        BDAM EXTENDED IOB
*
IOBDBYTR DS    CL2            BYTES REMAINING ON TRACK
IOBDIOBS DS    CL2            SIZE OF THIS IOB
IOBDPLAD DS    0CL4           IOB POOL LINK ADDRESS
IOBDAVLI DS    CL1            IOB AVAILABILITY INDICATOR
         DS    CL3
IOBDTYPE DS    CL2            TYPE FIELD
IOBDSTAT DS    CL2            IOB STATUS
IOBDCPND DS    CL4            CHANNEL PROGRAM NORMAL END ADDRESS
IOBDBYTN DS    CL2            BYTES NEEDED BY NEW RECORD
         DS    CL2
IOBDQPTR DS    CL4            LINK FIELD FOR QUEUED IOB'S
IOBUPLIM DS    CL8            LIMIT MBBCCHHR FOR EXTENDED SEARCH
IOBDNRCF DS    CL8            COUNT FIELD FOR NEW RECORD
IOBCP    DS    CL256                   FIRST CCW IN CHANNEL PROGRAM
EXTSRCH  DSECT                                                   S19015
*                                       ,                        S19015
ESNOP1   DS    D                  CCW1  NOP OR SET SECTOR (0)    S19015
ESSIE1   DS    D                  CCW2  SEARCH ID EQUAL (R0)     S19015
ESTIC1   DS    D                  CCW3  TIC TO CCW2              S19015
ESMTSIE  DS    D                  CCW4  MULTI-TRACK SEARCH ID    S19015
*                                       EQUAL                    S19015
ESTIC2   DS    D                  CCW5  TIC TO CCW7              S19015
ESNOP2   DS    D                  CCW6  NOP                      S19015
ESSKE1   DS    D                  CCW7  SEARCH KEY EQUAL         S19015
ESTIC3   DS    D                  CCW8  TIC TO CCW4              S19015
ESNOP3   DS    D                  CCW9  NOP OR READ SECTOR       S19015
ESNOP3A  DS    D                  CCW10 NOP OR SET SECTOR (0)    S19015
ESRHA    DS    D                  CCW11 READ HOME ADDRESS        S19015
ESNOP3B  DS    D                  CCW12 NOP OR SET SECTOR        S19015
ESRC     DS    D                  CCW13 READ COUNT               S19015
ESSKE2   DS    D                  CCW14 SEARCH KEY EQUAL         S19015
ESTIC4   DS    D                  CCW15 TIC TO CCW13             S19015
ESRD1    DS    D                  CCW16 READ DATA TO GET SEGMENT S19015
*                                       LE                       S19015
ESNOP4   DS    D               ** CCW17 NOP OR SET SECTOR        S19015
ESSIE2   DS    D               ** CCW18 SEARCH ID EQUAL          S19015
ESTIC5   DS    D               ** CCW19 TIC TO CCW18             S19015
ESRD2    DS    D               ** CCW20 READ DATA TO DO WRITE    S19015
*                                       CHECK                    S19015
*
*                              ** INDICATES THAT THE CCW'S ARE PRES
*                                 ONLY IF WRITE CHECK IS REQUESTED.
*
         SPACE 3                                                 S19015
         END
