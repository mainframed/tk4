 TITLE   'IGG019LA - BUILDS CHAN PGM FOR EXTENDED SEARCH, PRE-FORMAT'
IGG019LA CSECT                                                        *
*                                                                     *
*MODULE NAME - IGG019LA                                               *
*                                                                     *
*DESCRIPTIVE NAME - BDAM CHAN PGM MOD FOR EXTENDED SEARCH, WRITE ADD  *
*                                                                     *
*COPYRIGHT - NONE                                                     *
*                                                                     *
*CHANGE ACTIVITY                                                      *
*
*        RELEASE 20 DELETIONS/CHANGES
*000400                                                          S20201
*        RELEASE 21 DELETIONS/CHANGES
*                                                                YM3694
*        VS2 RELEASE 2
*                                                                YM3029
*                                                                Y02072
*
*STATUS CHANGE LEVEL 002     13SEP72
*FUNCTION/OPERATION- THIS MODULE IS ENTERED TO CONSTRUCT THE CCWS     *
*   REQUIRED FOR A WRITE, TYPE DA, IF THE DCB INDICATES THE           *
*   EXTENDED SEARCH OPTION. THE CCWS UNIQUE TO EXTENDED SEARCH ARE    *
*   CONSTRUCTED BY THIS ROUTINE THEN A RETURN TO IGG019KO IS EXECUTED *
*   TO CONSTRUCT THE CCWS COMMON TO ANY WRITE TYPE DA (FORMAT F).     *
*                                                                     *
*ENTRY POINT- 'IGG019LA' IS ENTERED FROM MODULE IGG019KO BY THE       *
*   CALLING SEQUENCE,    L    15,4(IGG019KO BASE)                     *
*                        BALR 12,15                                   *
*                                                                     *
*INPUT- THE REGISTERS PASSED AS INPUT BY IGG019KO ARE,                *
*   4 - DCB ADDRESS                                                   *
*   5 - DECB ADDRESS                                                  *
*   6 - ADDRESS OF CCW1                                               *
*   7 - IOB ADDRESS                                                   *
*                                                                     *
*OUTPUT- THE CCWS 1 THRU 11 AND THE ADDRESS PORTION OF CCW 13 ARE     *
*   CONSTRUCTED FOLLOWING THE IOB. REGISTER 6 WILL BE POSITIONED TO   *
*   THE LOCATION OF CCW 12.                                           *
*                                                                     *
*EXTERNAL ROUTINES- N/A                                               *
*                                                                     *
*EXIT-NORMAL- 'EXIT' IS THE RETURN TO IGG019KO BY EXECUTING A BRANCH  *
*   ON REGISTER 12.                                                   *
*                                                                     *
*TABLES/WORK AREAS- N/A                                               *
*                                                                     *
*ATTRIBUTES- THIS ROUTINE IS REENTRANT AND EXECUTED ENABLED.          *
*                                                                     *
*                                                                     *
         EJECT
*
*
*  REGISTER USAGE
*
*
         USING IGG019LA,EPREG
         USING IOBDEF,IOBREG
         USING IHADCB,DCBREG
*
*
WKREG    EQU   0         WORK REGISTER
PARAM    EQU   1         PARAMETER REGISTER
WKREG2   EQU   2         WORK REGISTER
DEBREG   EQU   3         DEB ADDRESS
DCBREG   EQU   4         DCB ADDRESS
DECBREG  EQU   5         DECB ADDRESS
CCWIND   EQU   6         CCW INDEX
IOBREG   EQU   7         IOB ADDRESS
SECTOR   EQU   8         POINTER TO SECTOR STORE AREAS           S20201
INTRET   EQU   12        INTERNAL LINKAGE RETURN
SAVE     EQU   13        USER'S SAVE AREA ADDRESS
RETREG   EQU   14        RETURN TO FOUNDATION MODULE
EPREG    EQU   15        ENTRY POINT/BASE
*
NCCWC    EQU   X'36'                    COUNT FIELD OF TIC       YM3694
*
*
BEGIN    EQU   *
         TM    IOBDCPND,YESCARN         IS RPS FEATURED          S20201
         BNO   NOTCARN                  NO, BUILD SCH-ID-EQ CCW  S20201
         L     SECTOR,IOBDCPND          GET SECTOR ADDR IF RPS   S20201
         LA    SECTOR,TWOBYTE(0,SECTOR) POINT TO SECTOR=0 BYTE   S20201
         ST    SECTOR,D0(CCWIND)        PUT PTR IN SET SECT CCW  S20201
         MVI   D0(CCWIND),SETSECT       PUT IN SET SECT COMMAND  S20201
         MVC   HALFCCW(HALFCCW,CCWIND),FLGCNT  FILL IN           S20201
*                                       FLAG/COUNT               S20201
         LA    CCWIND,FULLCCW(0,CCWIND) PT TO NEXT CCW           S20201
         BCTR  SECTOR,WKREG             RESTORE PTR TO SECTOR    S20201
         BCTR  SECTOR,WKREG              ADDR STORAGE AREAS      S20201
NOTCARN  EQU   *                                                 S20201
*
         LA    WKREG,IOBSEEK+3         CONSTRUCT SEARCH-ID-EQUAL CCW 1
         ST    WKREG,0(0,CCWIND)        (POSITION TO START OF TRACK)
         MVI   0(CCWIND),SCHIDOP
         ST    CCWIND,8(0,CCWIND)      INSERT TIC ADDRESS
         MVC   4(5,CCWIND),FLGCTTIC      (TIC TO CCW 1)
*
         LA    WKREG,IOBDNRCF+2        CONSTRUCT READ-DATA CCW 3
         ST    WKREG,16(0,CCWIND)       (READ HIGHEST CCHHR ON TRACK
         MVI   16(CCWIND),RDDATAOP       FROM CAPACITY RECORD INTO IOB)
         MVI   20(CCWIND),CSFLG
         MVI   23(CCWIND),FIVE
*
         ST    WKREG,24(0,CCWIND)      CONSTRUCT SEARCH-ID-EQUAL CCW 4
         MVI   24(CCWIND),SCHIDOP       (TEST-CURRENT CCHHR LAST ON
         LA    WKREG,88(0,CCWIND)        THE TRACK)
         ST    WKREG,32(0,CCWIND)      CONSTRUCT TIC CCW 5
         MVC   28(5,CCWIND),FLGCTTIC     (TIC TO CCW 12)
*
         LA    WKREG2,NCCWC(CCWIND)     CONSTRUCT SEARCH-KEY-    YM3694
*                                         EQUAL CCW 6. TIC HAS   YM3694
         MVI   0(WKREG2),X'FF'            KEY OF DUMMY RECORD    YM3694
         ST    WKREG2,40(0,CCWIND)      (TEST FOR DUMMY RECORD)  YM3694
         MVI   40(CCWIND),SCHKEYOP
         MVI   44(CCWIND),CSFLG
         MVI   47(CCWIND),1
*
         LA    WKREG,64(0,CCWIND)      CONSTRUCT TIC CCW 7
         ST    WKREG,48(0,CCWIND)       (TIC TO CCW 9)
         MVI   48(CCWIND),TICOP
*
         LA    WKREG,104(0,CCWIND)     CONSTRUCT TIC CCW 8
         ST    WKREG,56(0,CCWIND)       (TIC TO CCW 14)
         MVI   56(CCWIND),TICOP
*
         LA    WKREG,IOBUPLIM+3        CONSTRUCT SEARCH-ID-EQUAL (MT)
         ST    WKREG,64(0,CCWIND)       AS CCW9
         MVI   64(CCWIND),MSCHIDOP      (CHECK END OF SEARCH LIMIT)
         LA    WKREG,16(0,CCWIND)      CONSTRUCT TIC CCW 10
         ST    WKREG,72(0,CCWIND)       (TIC TO CCW3)
         MVC   68(5,CCWIND),FLGCTTIC
*
         MVI   80(CCWIND),NOP          CONSTRUCT NOP CCW 11
         MVI   84(CCWIND),SILI          (SEARCH LIMIT REACHED)
         MVI   87(CCWIND),1
*
         LA    WKREG,24(0,CCWIND)      STORE TIC ADDRESS IN CCW 13
         ST    WKREG,96(0,CCWIND)       (TIC TO CCW4)
*
*
         LA    CCWIND,88(0,CCWIND)     INCREMENT TO THE LAST CCW
         L     EPREG,DCBDFOR           RELOAD PRE-FORMAT MODULE BASE
EXIT     BR    INTRET                  RETURN TO CONSTRUCT COMMON CCWS
*
*
DUMKEY   DC    X'FF'          DUMMY KEY (UNUSED RECORD) SEARCH ARGUMENT
FLGCTTIC DC    X'4000000508'  COMMAND CHAIN, COUNT OF FIVE, TIC OP-CODE
*
* CARNIVAL OPTION CONSTANTS
*
FLGCNT   DC    X'40000001'              FLAG/COUNT FIELD FOR RPS S20201
*
D0       EQU   0                        ZERO DISPLACEMENT        S20201
TWOBYTE  EQU   2                        TWO BYTE DISPLACEMENT    S20201
HALFCCW  EQU   4                        FOUR BYTE DISPLACEMENT   S20201
FULLCCW  EQU   8                        EIGHT BYTE DISPLACEMENT  S20201
*
SETSECT  EQU   X'23'                    SET SECTOR COMMAND CODE  S20201
YESCARN  EQU   X'FF'                   BYTE TO TEST FOR          S20201
*                                       ROTATIONAL               S20201
*                                      POSITION SENSING OTHER-
*                                      -WISE KNOWN AS RPS OR
*                                      RECORD READY
*
*
TICOP    EQU   X'08'          TIC  OP-CODE
SILI     EQU   X'20'          SUPPRESS INCORRECT LENGTH INDICATION
SCHIDOP  EQU   X'31'          SEARCH-ID-EQUAL OP-CODE
MSCHIDOP EQU   X'B1'          MULTIPLE TRACK,SEARCH-ID-EQUAL OP-CODE
NOP      EQU   X'03'
RDDATAOP EQU   X'06'          READ DATA OP-CODE
CSFLG    EQU   X'60'          COMMAND CHAIN, SUPPRESS INCORRECT LENGTH
FIVE     EQU   X'05'          COUNT OF FIVE
SCHKEYOP EQU   X'29'          SEARCH-KEY-EQUAL OP-CODE
MODID    DC    C'IGG019LA'              MODULE ID                Y02072
PATCH    DC    XL50'0'                  PATCH AREA               Y02072
         EJECT
         DCBD  DSORG=DA
*      I/O BLOCK (IOB) DEFINITION
*
*              STANDARD IOB
         DS    0F
IOBDEF   DSECT
IOBFLAG1 DS    CL1            STATUS FLAG
IOBFLAG2 DS    CL1            ERROR FLAG
IOBSENSE DS    CL2            SENSE BYTES 0 AND 1
IOBECBPT DS    CL4            ECB POINTER
IOBCSW   DS    0CL8           CHANNEL STATUS WORD
IOBCSWCA DS    0CL4           COMMAND ADDRESS
         DS    CL1
         DS    CL3
IOBCSWST DS    CL2            STATUS BYTES
IOBCSWCT DS    CL2            REMAINING COUNT
IOBSTART DS    0CL4           CHANNEL PROGRAM POINTER
IOBSIOCC DS    CL1            SIO CONDITION CODE
         DS    CL3
IOBDCBPT DS    0CL4           DCB POINTER
IOBWGHT  DS    CL1            CHANNEL WEIGHT
         DS    CL3
IOBRESTR DS    0CL4           RESTART POINTER
         DS    CL1
         DS    CL3
IOBINCAM DS    CL2            BLOCK COUNT INCREMENT AMOUNT
IOBERRCT DS    CL2            ERROR COUNTER
IOBSEEK  DS    CL8            SEEK ADDRESS
*        BDAM EXTENDED IOB
*
IOBDBYTR DS    CL2            BYTES REMAINING ON TRACK
IOBDIOBS DS    CL2            SIZE OF THIS IOB
IOBDPLAD DS    0CL4           IOB POOL LINK ADDRESS
IOBDAVLI DS    CL1            IOB AVAILABILITY INDICATOR
         DS    CL3
IOBDTYPE DS    CL2            TYPE FIELD
IOBDSTAT DS    CL2            IOB STATUS
IOBDCPND DS    CL4            CHANNEL PROGRAM NORMAL END ADDRESS
IOBDBYTN DS    CL2            BYTES NEEDED BY NEW RECORD
         DS    CL2
IOBDQPTR DS    CL4            LINK FIELD FOR QUEUED IOB'S
IOBUPLIM DS    CL8            LIMIT MBBCCHHR FOR EXTENDED SEARCH
IOBDNRCF DS    CL8            COUNT FIELD FOR NEW RECORD
IOBCP    DS    CL8            FIRST CCW IN CHANNEL PROGRAM
         END
