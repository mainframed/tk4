         TITLE 'IGG019KH - FEEDBACK RELATIVE BLOCK WITH TRACK OVERFLOW'
IGG019KH CSECT
*MODULE NAME - IGG019KH                                               *
*                                                                     *
*DESCRIPTIVE NAME - BDAM FEEDBACK, RELATIVE BLOCK, TRK OVERFLOW       *
*                                                                     *
*COPYRIGHT - NONE                                                     *
*                                                                     *
*CHANGE ACTIVITY                                                      *
*                                                                     *
*        OS REL 21 CHANGES/DELETIONS
*        VS 1-1 CHANGES/DELETIONS
*        VS 2-1 CHANGES/DELETIONS
*        VS 1-2 CHANGES/DELETIONS
*        VS 2-2 CHANGES/DELETIONS
*                                                                Y02072
*031300                                                          YM3918
*                                                                YM4617
*                                                                     *
*STATUS CHANGE LEVEL 002                                              *
*                                                                     *
*FUNCTION/OPERATION- THIS MODULE IS ENTERED FROM THE ASYNCHRONOUS     *
*   INTERRUPT ROUTINE TO CONVERT THE ACTUAL ADDRESS OF THE BLOCK      *
*   JUST PROCESSED TO A RELATIVE BLOCK NUMBER. THIS IS DONE IF THE    *
*   USER HAS SPECIFIED THE FEEDBACK OPTION IN THE READ OR WRITE MACRO *
*   AND THE RELATIVE BLOCK AND FEEDBACK OPTIONS IN THE OPTCD PARAMETER*
*   OF THE DCB MACRO.                                                 *
*   THIS ROUTINE FIRST USES THE RESIDENT ROUTINE WHICH CONVERTS ACTUAL*
*   ADDRESSES TO RELATIVE TRACK ADDRESSES(TTR). THIS VALUE IS THEN    *
*   CONVERTED TO A RELATIVE BLOCK NUMBER BY BRANCHING TO IGG019KF,    *
*   THE REL BLK CONVERSION MOD, WHICH CALCULATES THE REL BLK ID FROM  *
*   DEB EXTENTS (THE TRACKS/PERIOD AND RECORDS/PERIOD VALUES ADDED    *
*   TO THE DEB BY THE BDAM OPEN EXECUTOR 'IGG0193A'.)                 *
*                                                                     *
*ENTRY POINTS- 'IGG019KH' IS THE ENTRY POINT FROM THE ASI AND SVC 53  *
*   ROUTINES BY THE CALLING SEQUENCE,                                 *
*            L    15,96(DCB)                                          *
*            BALR 12,15                                               *
*         OR SYNCH (15) - IF SVC 53                                   *
*   'NOMORE' IS THE RETURN POINT FROM THE RELATIVE BLOCK CONVERT      *
*   ROUTINE BY THE CALLING SEQUENCE,                                  *
*            L    15,96(DCB)                                          *
*            B    4(0,15)                                             *
*                                                                     *
*INPUT- THE FOLLOWING REGISTERS ARE LOADED BY CALLING ROUTINE,        *
*   1 - IOB ADDRESS                                                   *
*   13- DCB ADDRESS                                                   *
*   THE ACTUAL ADDRESS TO BE CONVERTED WILL BE FOUND IN THE IOB AT    *
*   IOBSEEK.                                                          *
*                                                                     *
*OUTPUT- THE CALCULATED RELATIVE BLOCK NUMBER WILL BE MOVED TO THE    *
*   USER'S BLKREF FIELD. BLKREF WILL BE LOCATED BY USING THE ECB      *
*   ADDRESS IN THE IOB TO LOCATE THE DECB AND THE ADDRESS OF BLKREF   *
*   WILL BE LOADED FROM THE DECB FIELD 'DECRECPT'.                    *
*                                                                     *
*EXTERNAL ROUTINES- 'IECPRLTV' IS USED TO CONVERT MBBCCHHR TO A TTR   *
*   VALUE.                                                            *
*   'IGG019KF' IS USED TO DETERMINE THE NUMBER OF BLOCKS THE GIVEN    *
*   TTR IS WITHIN A PERIOD.                                           *
*                                                                     *
*EXIT- NORMAL- 'EXTASI' IS THE EXIT TO RETURN TO THE ASI ROUTINE      *
*                 VIA,    B   8(R11)                                  *
*              RETURN TO THE READ EXCLUSIVE MODULE(IGC0005C) IS       *
*                 VIA,     EXIT                                       *
*                                                                     *
*TABLES/WORK AREAS- THE COMMUNICATIONS VECTOR TABLE IS REFERENCED TO  *
*   OBTAIN THE ADDRESS OF THE RESIDENT CONVERT ROUTINE (IECPRLTV)     *
*                                                                     *
*ATTRIBUTES- THIS ROUTINE IS REENTRANT AND EXECUTED ENABLED.          *
*   IT EXECUTES IN PROBLEM PROGRAM STATE, USER KEY.                   *
*                                                                     *
*NOTES- REGISTER USAGE IN THIS MODULE MUST REMAIN CONSISTENT WITH THE *
*   MODULES IGG019KA,IGG019KF, AND IECPRLTV.                          *
*                                                                     *
         EJECT
*                                                                     *
         USING IGG019KH,BASE
         USING IOBSTDRD,IOBREG
         USING IHADCB,DCBREG
         USING DECBDEF,DECBREG
*
*              REGISTER USAGE
*
WKREG0   EQU   0                       WORKING REGISTER
SREG1    EQU   1                        PARAMETER REGISTER FOR BPAM
IOBREG   EQU   1                        IOB ADDRESS
SREG2    EQU   2                        PARAMETER REGISTER FOR BPAM
WKREG2   EQU   3                       WORKING REGISTER
DEBREG   EQU   3                       BASE REGISTER FOR DEB
DCB2REG   EQU  4                       BASE REGISTER FOR DCB FOR RR
IOB2REG  EQU   7                       IOB REG. FOR REL. BL. ROUTINE
SREG9    EQU   9                        START OF SAVED REGISTERS
WKREG3   EQU   10                      WORKING REGISTER
DECBREG  EQU   11                      DECB ADDRESS
WKREG1   EQU   11                      WORKING REGISTER
FDNBASE  EQU   11                       BASE FOR FOUNDATION MODULE
SREG12   EQU   12                      ERROR CONDITION REGISTER
INTREG   EQU   12                      INTERNAL RETURN FOR THIS MODULE
DCBREG   EQU   13                      DCB ADDRESS
RETREG   EQU   14                      RETURN TO FOUNDATION MODULE
SREG14   EQU   14                       PARAMETER REGISTER FOR BPAM
WKREG4   EQU   14                      WORKING REGISTER
SREG15   EQU   15                       PARAMETER REGISTER FOR BPAM
BASE     EQU   15                      BASE REGISTER FOR THIS MODULE
*
         EJECT
         B     FDBSTAR                 BRANCH TO FIRST INSTRUCTION
         B     NOMORE                  BRANCH TO RETURN POINT FROM RR
FDBSTAR  STM   SREG9,SREG1,IOBDNCRF     SAVE REGISTERS 9-1       YM3918
         LA    SREG2,IOBSEEK            SET PTR TO ACTUAL ADDR   Y02072
         USING IOBSEEK,SREG2                                     Y02072
         L     SREG1,DCBDEBAD           SET DEB POINTER FOR CONVERT
         DROP  IOBREG                                            Y02072
         L     SREG15,CVTPTR(0,0)      LOAD ADDR.OF COMM. VECTOR TABLE
         L     SREG15,CVTPRLTV-CVTDEF(0,SREG15) LOAD ADDRESS OF
*                                                 CONVERSION ROUTINE
         BALR  SREG14,SREG15            GO TO ROUTINE
*
*
*                   RETURN FROM CONVERT ROUTINE
RETURN   EQU   *
         LM    SREG9,SREG15,IOBDNCRF    RESTORE REGISTERS  9-15  YM3918
         L     IOBREG,IOBDNCRF+DSAVER1  RESTORE IOB POINTER      YM3918
         USING IOBSTDRD,IOBREG                                   Y02072
         DROP  SREG2                                             Y02072
RETPT    SR    WKREG1,WKREG1            CLEAR REGISTER
         SRL   WKREG0,8                 SET REGISTER TO 0TTR
         BCTR  WKREG0,0                 SET R VALUE RELATIVE TO ZERO
         ST    WKREG1,IOBSTART          CLEAR IOB START POINTER
         STC   WKREG0,IOBSTART+3        PLACE R VALUE RETURNED BY
         IC    WKREG1,IOBSTART+3        CONVERT ROUTINE IN RESULT REG.
         L     SREG2,DCBDEBAD           LOAD DEB POINTER
         USING DEBDEF,SREG2
         SRL   WKREG0,8                 SET REGISTER TO 00TT
         LA    WKREG2,EXT1(0,SREG2)     SET POINTER TO FIRST EXTENT
         SR    WKREG4,WKREG4            CLEAR REGISTER
         IC    WKREG4,DEBNMEXT          LOAD NUMBER OF EXTENTS
         LR    WKREG3,WKREG2            SET POINTER TO FIRST EXTENT
INCPT    LA    WKREG3,EXTSZ(0,WKREG3)   INCREMENT POINTER BY EXT. SIZE
         BCT   WKREG4,INCPT             IS THIS THE LAST EXTENT
*                                       NO-GO TO INCPT
*                                       YES WKREG3 NOW POINTS TO FIRST
*                                        RELATIVE RECORD EXTENT
         LR    WKREG4,WKREG3           YES SAVE POINTER TO PERIOD FIELD
         LA    WKREG3,8(0,WKREG3)      SET POINTER TO FIRST REL EXTENT
SUBTRK   SH    WKREG0,NMTRK(0,WKREG2)   SUBTRACT NO.OF TRACKS IN THIS
*                                        EXTENT FROM CONVERTED VALUE
         BC    4,ADDBACK                BRANCH IF RESULT IS MINUS
         MVC   IOBSTART+1(3),1(WKREG3)  ALIGN RECORDS PER EXTENT VALUE
         A     WKREG1,IOBSTART          ADD VALUE TO RESULT
         LA    WKREG2,EXTSZ(0,WKREG2)   INCREMENT NORMAL EXTENT POINTER
         LA    WKREG3,REXTSZ(0,WKREG3)  INCREMENT RELATIVE EXTENT PTR
         BC    15,SUBTRK                REPEAT CALCULATION
*
ADDBACK  AH    WKREG0,NMTRK(0,WKREG2)   ADD NO. OF TRACK IN THIS EXTENT
*                                        BACK TO TT REG.
         BC    8,NOMORE                 IF RESULT IS ZERO GO TO NOMORE
SUBPRD   S     WKREG0,0(0,WKREG4)      YES SUBTRACT  TRACKS/PERIOD
         BM    OVER                    MINUS RESULT?GO ADD BACK TRACKS
         A     WKREG1,4(0,WKREG4)      NO ADD ON RECORDS/PERIOD
         B     SUBPRD                  LOOP BACK
OVER     A     WKREG0,0(0,WKREG4)      ADD BACK TRACKS SUBTRACTED
         BZ    NOMORE                  IF NOT GO AROUND OVERFLOW
         OI    IOBDTYP2,IOBRBFDB       INDICATE ENTRY FROM FEEDBACK
         L     DEBREG,DCBDEBAD         GET DEB ADDR INTO CORRECT REG
         LR    DCB2REG,DCBREG          GET DCB ADDR INTO CORRECT REG
         LR    IOB2REG,IOBREG          LOAD IOB ADDR FOR REL. BL MODULE
         L     BASE,DCBREAD            GET ADDRESS OF THE FOUNDATION
         L     BASE,32(0,BASE)          MODULE.  GET ADDR OF      13883
         B     4(0,BASE)               OF THE RELATIVE RECORD MOD. BR
NOMORE   ST    WKREG1,IOBSTART          STORE RESULT
         L     DECBREG,IOBECBPT         SET DECB POINTER
         L     WKREG2,DECRECPT          LOAD ADDRESS OF USER'S BLKREF
*                                        FIELD
         MVC   0(3,WKREG2),IOBSTART+1   MOVE RELATIVE RECORD VALUE TO
*                                        USER'S AREA
         TM    IOBSTAT1,IOBSYNCH        TEST FOR SYNCH ENTRY     Y02072
*                                       (ENTRY FROM EXCL CNTROL) Y02072
         BNO   EXITASI                  IF NOT, BR TO ASI EXIT   YM3918
         SVC   EXIT                     ELSE,EXIT BACK TO SVC 53 YM3918
EXITASI  EQU   *                        RETURN TO ASI            YM3918
         L     FDNBASE,DCBREAD          GET ADDRESS OF 19KA      YM3918
         L     FDNBASE,ASIOFF(FDNBASE)  GET ADDRESS OF ASI       YM3918
         LR    BASE,FDNBASE             SET UP R15 WITH ASI BASE YM4617
         B     DASIC(FDNBASE)           RETURN TO ASI+8 ENTRY    YM3918
***********************************************************************
*            CONSTANTS AND EQUATES
***********************************************************************
EXT1     EQU   32                       FIRST EXTENT OFFSET FROM START
*                                        OF DEB
EXTSZ    EQU   16                       DEB EXTENT SIZE
REXTSZ   EQU   4                        RELATIVE RECORD EXTENT SIZE
NMTRK    EQU   14                       NUMBER OF TRACKS OFFSET IN EXT.
DSAVER1  EQU   (16+1-9)*4               DISP OF R1 SAVEAREA FROM Y02072
*                                       R9 SAVED IN IOBUPLIM     Y02072
ASIOFF   EQU   16                       OFFSET IN 19KA TO ASI    YM3918
EXIT     EQU   3                        EXIT SVC 3               YM3918
DASIC    EQU   8                        OFFSET IN ASI TO BRANCH  YM3918
*                                       TO CLEAN UP AND POST RTN YM3918
*
*              EXCEPTION CODES
NOREC    EQU   X'80'
LGNCK    EQU   X'40'
NOSPA    EQU   X'20'
INVAL    EQU   X'10'
UNCOR    EQU   X'08'
ENDOD    EQU   X'04'
UNREL    EQU   X'02'
*              OPTIONS AND TYPE
VERIFY   EQU   X'80'
OFLOW    EQU   X'40'
EXSCH    EQU   X'20'
FEEDB    EQU   X'10'
ACTAD    EQU   X'08'
DYNBF    EQU   X'04'
READX    EQU   X'02'
RELRD    EQU   X'01'
KEYOP    EQU   X'80'
LGNOP    EQU   X'40'
READ     EQU   X'08'
KEY      EQU   X'04'
ADD      EQU   X'02'
OVERFL   EQU   2
MODID    DC    C'IGG019KH'              MODULE ID                Y02072
PATCH    DC    XL50'0'                  PATCH AREA               Y02072
         EJECT
***********************************************************************
*               DSECTS
***********************************************************************
CVTDEF   DSECT
         CVT
         EJECT
*      DATA CONTROL BLOCK (DCB) DEFINITION
*
         DCBD  DSORG=DA
         EJECT
*      DATA EVENT CONTROL BLOCK (DECB) DEFINITION
         DS    0F
DECBDEF  DS    0CL25
DECSDECB DS    CL4            STANDARD EVENT CONTROL BLOCK (ECB)
DECTYPE  DS    CL2            TYPE FIELD
DECLNGTH DS    CL2            DATA LENGTH
DECDCBAD DS    CL4            ADDRESS OF DCB
DECAREA  DS    CL4            ADDRESS OF DATA AREA
DECIOBPT DS    CL4            ADDRESS OF THE IOB
DECKYADR DS    CL4            ADDRESS OF KEY
DECRECPT DS    CL4            ADDRESS OF RECORD REFERENCE
         EJECT
*
*      I/O BLOCK (IOB) DEFINITION
*
         IEZIOB
         ORG   IOBDTYP2                                          Y02072
IOBRBFDB EQU   X'20'          INDICATES IGG019KF SHOULD RETURN   Y02072
*                             TO FEEDBACK MODULE                 Y02072
         ORG   IOBRSV35
IOBUPLIM DS    D
         EJECT
*
*      DATA EXTENT BLOCK (DEB) DEFINITION
*
         DS    0F
DEBDEF   DSECT
*
         DS    0F
DEBNMSUB DS    0CL1                    COUNT OF ROUTINES LOADED
DEBTCBAD DS    CL4                     TASK CONTROL BLOCK ADDRESS
DEBAMLNG DS    0CL1                    WORD CNT OF BDAM ADDED CORE
DEBDEBAD DS    CL4                     DEB CHAIN ADDRESS
DEBOFLGS DS    0CL1                    OPEN FLAGS
DEBIRBAD DS    CL4                     IRB ADDRESS
DEBOPATB DS    0CL1                    DATA SET ATTRIBUTES
DEBSYSPG DS    CL4
DEBNMEXT DS    0CL1                    COUNT OF EXTENTS
DEBUSRPG DS    CL4
DEBPRIOR DS    0CL1
DEBECBAD DS    CL4
DEBPROTG DS    0CL1
DEBDEBID DS    0CL1
DEBDCBAD DS    CL4                     DCB ADDRESS
DEBEXSCL DS    0CL1                    EXTENT SIZE SCALE
DEBAPPAD DS    CL4                     IOS APPENDAGE LIST ADDRESS
DEBDVMOD DS    0CL1                    FILE MASK CODE
DEBUCBAD DS    CL4                     UCB ADDRESS
DEBBINUM DS    CL2                     BIN NUMBER (BB)
DEBSTRCC DS    CL2                     STARTING CYLINDER NUMBER
DEBSTRHH DS    CL2                     STARTING TRACK NUMBER
DEBENDCC DS    CL2                     ENDING CYLINDER NUMBER
DEBENDHH DS    CL2                     ENDING TRACK NUMBER
DEBNMTRK DS    CL2                     NO. OF TRACKS IN THIS EXTENT
DEBSUBID EQU   0         SUBROUTINE ID'S
*
*                IO DEVICE TABLE
*
         DS    0F
DTAB     DC    X'000814202C380000'
DEVSZ    DS    CL4                     DEVICE SIZE   CCHH
TRKLNGTH DS    CL2                     TRACK LENGTH
OVERI    DS    CL1                     OVERHEAD  NOT LAST
OVERL    DS    CL1                     OVERHEAD  NLAST
OVERK    DS    CL1                     OVERHEAD  NNON KEYED
FLAG     DS    CL1                     FLAG
TOLER    DS    CL2                     TOLERANCE
         END
