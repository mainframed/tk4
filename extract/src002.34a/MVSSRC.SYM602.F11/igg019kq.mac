         TITLE 'IGG019KQ - WRITE VERIFY MODULE'
IGG019KQ CSECT
*
*
*        RELEASE 23 DELETIONS/CHANGES
*        RELEASE 22 DELETIONS/CHANGES
*        RELEASE 21 DELETIONS/CHANGES
*          RELEASE 18 DELETIONS
*          RELEASE 19 DELETIONS
*3000011800-012000,014200-014400                                 S19015
*          RELEASE 20 DELETIONS
*2971000240-000360,000520-000560,011870-012080,014000,018400     20201
*
*STATUS CHANGE LEVEL 004
*
*FUNCTION/OPERATION- THIS MODULE IS LOADED IF THE DCB SPECIFIED THE   *
*   WRITE VALIDITY CHECK OPTION, AND WILL CONSTRUCT THE ADDITIONAL    *
*   CCW'S NEEDED TO VERIFY THE DATA WRITTEN BY THE PREVIOUS CCW'S.    *
*   THE CCWS ARE CONSTRUCTED OF CONSTANTS DEFINED IN THIS MODULE AND  *
*   A SEARCH CCW AND TIC CODE MOVED FROM THE WRITE SEQUENCE OF CCWS.  *
*                                                                     *
*ENTRY POINT- 'IGG019KQ' (BEGIN) IS ENTERED FROM THE MODULE WHICH     *
*   CONSTRUCTED THE WRITE CHANNEL PROGRAM. CALLING SEQUENCE IS,       *
*   L    10,12(FOUNDATION MODULE BASE)                                *
*   BALR 12,10                                                        *
*                                                                     *
*INPUT- THE FOLLOWING REGISTERS ARE USED AS INPUT,                    *
*   1 - ADDRESS OF A SEARCH CCW TO BE MOVED TO THIS READ SEQUENCE     *
*   4 - DCB ADDRESS                                                   *
*   5 - DECB ADDRESS                                                  *
*   6 - ADDRESS OF THE LAST WRITE CCW                                 *
*   7 - IOB ADDRESS                                                   *
*                                                                     *
*OUTPUT- THE REQUIRED CCWS ARE ATTACHED TO THE WRITE CCWS IN THE      *
*   IOB. REGISTER 6 WILL BE POSITIONED AT THE LAST VERIFY CCW.        *
*                                                                     *
*EXTERNAL ROUTINES- N/A                                               *
*                                                                     *
*EXITS-NORMAL- 'EXIT1', 'EXIT2', 'EXIT3' ARE ALL NORMAL RETURNS TO THE*
*   CALLING ROUTINE.                                                  *
*                                                                     *
*TABLES/WORK AREAS- N/A                                               *
*                                                                     *
*ATTRIBUTES- THIS ROUTINE IS REENTRANT AND EXECUTED ENABLED.          *
*                                                                     *
*NOTES- THIS MODULE IS USED BY THE BDAM MODUES, IGG019KK, IGG019KI,   *
*   IGG019KM, IGG019KO AND THE REGISTER USAGE MUST REMAIN CONSISTENT  *
*   WITH THOSE MODULES.                                               *
         EJECT
*
         USING IGG019KQ,VERBASE
         USING IOBDEF,IOBREG
         USING IHADCB,DCBREG
         USING DECBDEF,DECBREG
*
*  REGISTER USAGE
*
SCHREG   EQU   1                       SEARCH CCW PARAMETER
VWKREG   EQU   2                       WORK REGISTER
DEBREG   EQU   3                       DEB ADDRESS
DCBREG   EQU   4                       DCB ADDRESS
DECBREG  EQU   5                       DECB ADDRESS
CCWIND   EQU   6                       CHANNEL PROGRAM INDEX
IOBREG   EQU   7                       IOB ADDRESS
VERBASE  EQU   10                      ENTRY POINT-BASE, THIS MODULE
VRETREG  EQU   14                      RETURN TO FOUNDATION MODULE
VEPREG   EQU   15                      BASE OF CALLING MODULE
*
*
BEGIN    EQU   *                                                 S19015
         TM    DCBBFTEK,RBFTKMSK        IS THIS VRE              S19015
         BZ    NOTVS                    NO,GO TURN ON  CMD       S19015
*                                       CHAINING                 S19015
         TM    DECTYPE+ONE,ADD          IS THIS TYPE DI-NOT ADD  S19015
         BZ    ESTST                    YES,GO TEST FOR EXTENDED S19015
*                                       SE                       S19015
         OI    FOUR(CCWIND),CCHN        TURN ON CMD CHAIN BIT    S19015
NOPS     EQU   *                                                 20201
         CLI   IOBDCPND,SETSC           RCD READY USED           20201
         BNE   NOTRRA                   BRANCH IF NO             20201
         L     VWKREG,IOBDCPND          GET SECTOR ADDR          20201
         LA    VWKREG,B1(VWKREG)        INCR BY ONE              20201
         ST    VWKREG,D8(CCWIND)        PUT IN CCW               20201
         MVI   D8(CCWIND),RDSC          PUT IN RD SECTOR CMND    20201
         MVC   D12(B4,CCWIND),SFLAG     CMND CHAIN AND BYTE      20201
*                                       CNT=1                    20201
         B     FEATA                    GO AROUND NOP            20201
NOTRRA   EQU   *                                                 20201
         MVC   D8(D8,CCWIND),NOP        INSERT NOP CMND          20201
FEATA    EQU   *                                                 20201
         LA    CCWIND,D8(CCWIND)        INCR CCW PTR             20201
NOP2     EQU   *                                                 20201
         CLI   IOBDCPND,SETSC           RCD READY USED           20201
         BNE   NOTRRD                   BRANCH IF NO             20201
         L     VWKREG,IOBDCPND          GET SECTOR ADDR          20201
         LA    VWKREG,B1(VWKREG)        INCR BY ONE              20201
         ST    VWKREG,D8(CCWIND)        PUT IN CHP               20201
         MVI   D8(CCWIND),SETSC         PUT IN CHP CMND          20201
         MVC   D12(B4,CCWIND),SFLAG     CMND CHAIN AND BYTE      20201
*                                       CNT=1                    20201
         B     FEATD                    BRANCH AROUND NOP        20201
NOTRRD   EQU   *                                                 20201
         MVC   D8(D8,CCWIND),NOP        INSERT NOP CMND          20201
FEATD    EQU   *                                                 20201
         LA    CCWIND,D8(CCWIND)        INCR CCW PTR             20201
NOTVS    EQU   *                                                 S19015
         OI    FOUR(CCWIND),CCHN        TURN ON COMMAND CHAINING S19015
DKNOES   EQU   *                                                 S19015
         LA    CCWIND,8(CCWIND)        INCREMENT TO NEXT CCW
         TM    IOBDTYPE,OFLOW          TEST FOR TRACK OVERFLOW OPTION
         BC    8,OMITSEEK               IF NO, OMIT SEEK HH CCW
         LA    VWKREG,IOBDNRCF          DEVELOP SEEK ADDRESS
         ST    VWKREG,0(CCWIND)         AND STORE IN CCW
         MVI   0(CCWIND),SEEKOP        MOVE SEEK HH OP-CODE
         MVI   4(CCWIND),CCHN          SET COMMAND CHAIN FLAG
         MVI   7(CCWIND),SIX           SET COUNT TO SIX
         LA    CCWIND,8(CCWIND)        INCREMENT INDEX BY ONE CCW
OMITSEEK EQU   *                                                 20201
         TM    IOBDCPND,YESCARN         IS RPS FEATURED WITHOUT  20201
*                                            VRE
         BNO   SCHKEYEQ                 NO, THEN GO SEARCH FOR   20201
*                                       KEY EQ.                  20201
         L     SECTOR,IOBDCPND          GET SECTOR ADDR IF RPS   20201
         LA    SECTOR,ONEBYTE(0,SECTOR) POINT TO SECTOR1         20201
         ST    SECTOR,ZERO(0,CCWIND)    FILL IN ADDR FIELD       20201
         MVI   ZERO(CCWIND),SETSECT      FILL IN SET SECTOR      20201
*                                          COMMAND CODE
         MVI   HALFCCW(CCWIND),CARNCHAN COMMAND CHAIN TO NEXT    20201
*                                       CCW                      20201
         MVI   CARN7(CCWIND),CARNCNT    PUT IN RPS COUNT OF ONE  20201
         LA    CCWIND,FULLCCW(0,CCWIND) UPDAT PT TO NEXT CCW     20201
SCHKEYEQ EQU   *                                                 20201
         ST    CCWIND,FULLCCW(0,CCWIND)  STORE AS TIC ADDRESS    20201
         MVC   ZERO(K8,CCWIND),ZERO(SCHREG)  MOVE SEARCH CCW IN  S19015
         MVI   K8(CCWIND),TICOP         INSERT TIC OP CODE       S19015
         MVI   16(CCWIND),RDKDOP       SET OP-CODE TO READ KEY-DATA
         TM    DCBBFTEK,RBFTKMSK        BUFFER TECHNIQUE EQUAL R S19015
         BO    SETCNT                   YES,GO LEAVE READ KEY    S19015
*                                       DATA                     S19015
         TM    IOBDTYPE+1,KEY          TEST FOR WRITE BY KEY
         BC    8,SETCNT                 BRANCH ON NO
         MVI   16(CCWIND),RDDOP        CHANGE OP-CODE TO READ DATA
*
SETCNT   MVI   22(CCWIND),1            SET LENGTH TO 256 BYTES
         MVI   20(CCWIND),SKIPSILI     SET FLAGS IN THE READ CCW
         LA    CCWIND,16(CCWIND)       INCREMENT TO LAST CCW
         TM    IOBDTYPE+1,ADD          TEST FOR WRITE ADD
EXIT1    BCR   8,VRETREG                IF NO, EXIT
         TM    DCBRECFM,FIXED          IS RECORD FORMAT 'F'
EXIT2    BCR   8,VRETREG                YES, CCWS ARE COMPLETE
         OI    4(CCWIND),CCHN          SET COMMAND CHAIN FLAG
*
         TM    IOBDCPND,YESCARN         RECORD READY I PRESUME   20201
         BNO   NOTCARN                  NO, THEN SEE ABOUT READ  20201
*                                       R0                       20201
         L     SECTOR,IOBDCPND          GET POINTER TO SECTORS   20201
         LA    SECTOR,TWOBYTE(0,SECTOR)  POINT TO SECTOR=0 AND   20201
*                                       POINT                    20201
         LA    CCWIND,FULLCCW(0,CCWIND)    TO NEXT CCW AND       20201
*                                       CREATE                   20201
         ST    SECTOR,ZERO(0,CCWIND)         THE SET SECT CCW    20201
         MVI   ZERO(CCWIND),SETSECT          WITH COMMAND        20201
*                                            CHAINING AND A
         MVI   HALFCCW(CCWIND),CARNCHAN    COUNT OF ONE.         20201
         MVI   CARN7(CCWIND),CARNCNT                             20201
NOTCARN  EQU   *                                                 20201
         MVC   8(8,CCWIND),RDR0CCW     MOVE CONSTANT CCW TO VERIFY
*                                       NEW CAPACITY RECORD
         LA    CCWIND,8(CCWIND)        INCREMENT TO LAST CCW
EXIT3    BR    VRETREG                 RETURN TO FOUNDATION MODULE
*
ESTST    EQU   *                                                 S19015
         TM    DECTYPE+ONE,KEY          IS THIS TYPE DK          S19015
         BZ    NOPS                     NO,GO INSERT TWO NOPS    S19015
         TM    DECTYPE,EXTSCH           IS THIS EXTENDED SEARCH  S19015
         BO    NOP2                     YES,GO INSERT ONE NOP    S19015
         NI    FOUR(CCWIND),NOCCHN      TURN OFF CMD CHAINING    S19015
         B     DKNOES                   GO CONTINUE CP BUILD     S19015
*
B1       EQU   1                        BYTE CNT OF ONE          20201
B4       EQU   4                        BYTE CNT OF FOUR         20201
D8       EQU   8                        DISPLACEMENT OF EIGHT    20201
D12      EQU   12                       DISPLACEMENT OF TWELVE   20201
*
SECTOR   EQU   2                        ALSO PTR TO SECTOR AREA  20201
SETSC    EQU   X'23'                    SET SECTOR CCW CMND      20201
RDSC     EQU   X'22'                    READ SECTOR  CW CMND     20201
*
SFLAG    DC    X'40000001'              SECTOR CMND FLAGS AND    20201
*                                       BYTE                     20201
*                                       COUNTS
RDR0CCW  DC    X'16'                    READ R0 CCW TO VERIFY THE
         DC    X'000001'                 CAPACITY RECORD
         DC    X'10'                     JUST
         DC    X'0000'
         DC    X'10'                     WRITTEN
CCHN     EQU   X'40'                   COMMAND CHAIN FLAG
SEEKOP   EQU   X'0B'                   SEEK CCHH OP-CODE
SIX      EQU   X'06'                   COUNT OF SIX
RDKDOP   EQU   X'0E'                   READ KEY-DATA OP-CODE
SKIPSILI EQU   X'30'                   SKIP, SUPPRESS INCORRECT LENGTH
FIXED    EQU   X'40'                   RECORD FORMAT FIXED
RDDOP    EQU   X'06'                   READ DATA OP-CODE
*              OPTIONS AND TYPE
OFLOW    EQU   X'40'
KEY      EQU   X'04'
ADD      EQU   X'02'
RBFTKMSK EQU   X'20'                    DCBBFTEK=R TEST BYTE     S19015
TICOP    EQU   X'08'                    TIC OP CODE              S19015
EXTSCH   EQU   X'20'                    EXTERNAL SEARCH CODE     S19015
NOCCHN   EQU   X'BF'                    MASK TO TURN OFF CMD     S19015
*                                       CHAIN                    S19015
ZERO     EQU   0                        DISPLACEMENT VALUE       S19015
ONE      EQU   1                        DISPLACEMENT VALUE       S19015
FOUR     EQU   4                        DISPLACEMENT VALUE       S19015
K8       EQU   8                        DISPLACEMENT VALUE       S19015
NOP      DC    X'0300000060000001'      NOP FOR CARNIVAL SUPPORT S19015
*
*        RPS VALUES AND MASKS
*
ONEBYTE  EQU   1                        ONE BYTE DISPLACEMENT    20201
TWOBYTE  EQU   2                        TWO BYTE DISPLACEMENT    20201
HALFCCW  EQU   4                        INCREMENT FOR 4 BYTES    20201
CARN7    EQU   7                        SEVEN BYTE DISPLACEMENT  20201
FULLCCW  EQU   8                        INCREMENT FOR 8 BYTES    20201
*
CARNCNT  EQU   X'01'                    RPS INSTR - COUNT        20201
SETSECT  EQU   X'23'                    SET SECTOR COMMAND CODE  20201
CARNCHAN EQU   X'40'                    COMMAND CHAINING BYTE    20201
YESCARN  EQU   X'FF'                   BYTE TO TEST FOR REC      20201
*                                       READY                    20201
         DC    C'IGGR'                  ROTATIONAL POSITION      20201
*                                       SENS-                    20201
*                                         ING MODULE
*              THE FOLLOWING CNOP MUST REMAIN ON A HALF-WORD BOUNDARY
         CNOP  0,8                 PAD MODULE TO DOUBLE WORD MULTIPLE
         EJECT
*      DATA CONTROL BLOCK (DCB) DEFINITION
*
         DCBD  DSORG=DA
*
*
*      DATA EVENT CONTROL BLOCK (DECB) DEFINITION
         DS    0F
DECBDEF  DSECT
DECSDECB DS    CL4            STANDARD EVENT CONTROL BLOCK (ECB)
DECTYPE  DS    CL2            TYPE FIELD
DECLNGTH DS    CL2            DATA LENGTH
DECDCBAD DS    CL4            ADDRESS OF DCB
DECAREA  DS    CL4            ADDRESS OF DATA AREA
DECIOBPT DS    CL4            ADDRESS OF THE IOB
DECKYADR DS    CL4            ADDRESS OF KEY
DECRECPT DS    CL4            ADDRESS OF RECORD REFERENCE
         EJECT
*
*      I/O BLOCK (IOB) DEFINITION
*
*              STANDARD IOB
         DS    0F
IOBDEF   DSECT
IOBFLAG1 DS    CL1            STATUS FLAG
IOBFLAG2 DS    CL1            ERROR FLAG
IOBSENSE DS    CL2            SENSE BYTES 0 AND 1
IOBECBPT DS    CL4            ECB POINTER
IOBCSW   DS    0CL8           CHANNEL STATUS WORD
IOBCSWCA DS    0CL4           COMMAND ADDRESS
         DS    CL1
         DS    CL3
IOBCSWST DS    CL2            STATUS BYTES
IOBCSWCT DS    CL2            REMAINING COUNT
IOBSTART DS    0CL4           CHANNEL PROGRAM POINTER
IOBSIOCC DS    CL1            SIO CONDITION CODE
         DS    CL3
IOBDCBPT DS    0CL4           DCB POINTER
IOBWGHT  DS    CL1            CHANNEL WEIGHT
         DS    CL3
IOBRESTR DS    0CL4           RESTART POINTER
         DS    CL1
         DS    CL3
IOBINCAM DS    CL2            BLOCK COUNT INCREMENT AMOUNT
IOBERRCT DS    CL2            ERROR COUNTER
IOBSEEK  DS    CL8            SEEK ADDRESS
*        BDAM EXTENDED IOB
*
IOBDBYTR DS    CL2            BYTES REMAINING ON TRACK
IOBDIOBS DS    CL2            SIZE OF THIS IOB
IOBDPLAD DS    0CL4           IOB POOL LINK ADDRESS
IOBDAVLI DS    CL1            IOB AVAILABILITY INDICATOR
         DS    CL3
IOBDTYPE DS    CL2            TYPE FIELD
IOBDSTAT DS    CL2            IOB STATUS
IOBDCPND DS    CL4            CHANNEL PROGRAM NORMAL END ADDRESS
IOBDBYTN DS    CL2            BYTES NEEDED BY NEW RECORD
         DS    CL2
IOBDQPTR DS    CL4            LINK FIELD FOR QUEUED IOB'S
IOBUPLIM DS    CL8            LIMIT MBBCCHHR FOR EXTENDED SEARCH
IOBDNRCF DS    CL8            COUNT FIELD FOR NEW RECORD
IOBCP    DS    CL8            FIRST CCW IN CHANNEL PROGRAM
         END
