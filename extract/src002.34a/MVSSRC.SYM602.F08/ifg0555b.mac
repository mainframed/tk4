         TITLE 'IFG0555B / DA OUTPUT USER TRAILER LABEL PROCESSING'
IFG0555B CSECT
***********************************************************************
*                                                                     *
*                                                                     *
*          VS2 ADDITIONS/CHANGES/DELETIONS                            *
*                                                                     *
* C49800021                                                  @ZA28829 *
***********************************************************************
*                                                                     *
* MODULE NAME = IFG0555B (OS/VS2)                                     *
*                                                                     *
* DESCRIPTIVE NAME =                                                  *
*        DA OUTPUT USER TRAILER LABEL PROCESSING                      *
*                                                                     *
* COPYRIGHT = NONE                                                    *
*                                                                     *
* STATUS = RELEASE 2, LEVEL 0                                         *
*                                                                     *
* FUNCTION =                                                          *
*        THIS MODULE IS ENTERED FROM IFG0554Z (EOV) TO WRITE UP TO    *
*        EIGHT USER TRAILER LABELS AS REQUIRED FOR DATA SETS ON       *
*        DIRECT ACCESS DEVICES. IT IS ENTERED ONCE FOR EACH DCB TO BE *
*        PROCESSED.                                                   *
*                                                                     *
*        A 152 BYTE USER LABEL WORK AREA IS OBTAINED FOR EOV FROM     *
*        FETCH PROTECTED SUBPOOL 229 IN DATA MANAGEMENT KEY. A 96     *
*        BYTE USER LABEL WORK AREA IS OBTAINED FOR THE USER FROM      *
*        FETCH PROTECTED SUBPOOL 229 IN THE KEY OF THE CALLER OF EOV. *
*                                                                     *
*        IF A TRACK WAS ALLOCATED FOR USER LABELS, CONTROL IS GIVEN   *
*        TO THE USER'S LABEL CONSTRUCTION ROUTINE WHOSE ADDRESS IS    *
*        PASSED TO THIS MODULE IN REGISTER 12, VIA THE IECRES-EXIT    *
*        MACRO WHICH GIVES CONTROL TO IFG0194A WHO TAKES THE EXIT     *
*        AND RETURNS TO THIS CALLING MODULE.                          *
*                                                                     *
*        ON RETURN FROM THE USER'S ROUTINE, IF A LABEL WAS            *
*        CONSTRUCTED, IT IS WRITTEN FOLLOWED BY ONE FILE MARK FOR     *
*        UTL(N+1). DEPENDING ON THE RETURN CODE PASSED BY THE USRE'S  *
*        ROUTINE, ANOTHER LABEL IS PROCESSED OR CONTROL IS GIVEN BACK *
*        TO IFG0554Z, THE MODULE WHICH CALLED THIS ONE.               *
         EJECT
* NOTES =                                                             *
*        NONE.                                                        *
*                                                                     *
*    DEPENDENCIES =                                                   *
*            CLASS ONE CHARACTER CODE.  THE EBCDIC CHARACTER CODE     *
*            WAS USED FOR ASSEMBLY.  THE MODULE MUST BE REASSEMBLED   *
*            IF A DIFFERENT CHARACTER SET IS USED FOR EXECUTION.      *
*                                                                     *
*    RESTRICTIONS = NONE                                              *
*                                                                     *
*    REGISTER CONVENTIONS =                                           *
*            R2 POINTS TO DCB                                         *
*            R4 POINTS TO OPEN WORK AREA                              *
*            R5 POINTS TO THE RESIDENT ROUTINE                        *
*            R6 POINTS TO THE WTG TABLE                               *
*            R7 POINTS TO THE CURRENT PARAMETER LIST ENTRY            *
*            R8 POINTS TO THE DATA MANAGEMENT KEY USER LABEL WORKAREA *
*            R10 POINTS TO THE USER KEY USER LABEL WORKAREA           *
*                                                                     *
*    PATCH LABEL = SEE THIRD FROM LAST LABEL BEFORE ORG STATEMENT AT  *
*                  END OF LISTING.                                    *
*                                                                     *
* MODULE TYPE = CONTROL (OPEN, CLOSE, EOV DATA MANAGEMENT)            *
*                                                                     *
*    PROCESSOR = ASSEMBLER XF                                         *
*                                                                     *
*    MODULE SIZE = SEE EXTERNAL SYMBOL DICTIONARY OR LOC FIELD ON     *
*                  ORG STATEMENT AT END OF LISTING                    *
*                                                                     *
*    ATTRIBUTES = REENTRANT, REFRESHABLE,READ-ONLY, ENABLED,          *
*                 PRIVILEGED, SUPERVISOR STATE, DATA MANAGEMENT KEY,  *
*                 LINK PACK AREA RESIDENT/PAGEABLE                    *
*                                                                     *
* ENTRY POINT =                                                       *
*        IFG0555B                                                     *
*                                                                     *
*    PURPOSE = SEE FUNCTION                                           *
*                                                                     *
*    LINKAGE =                                                        *
*        THIS MODULE IS TRANSFERRED CONTROL THROUGH THE IECRES-LOAD   *
*        MACRO INSTRUCTION.                                           *
         EJECT
* INPUT =                                                             *
*        GIVEN CONTROL IN PROTECT KEY 5.                              *
*        REGISTER 2 POINTS TO THE COPIED DCB.                         *
*        DEBDCBAD POINTS TO THE COPIED DCB.                           *
*        REGISTER 4 POINTS TO THE EOV WORKAREA                        *
*        REGISTER 12 CONTAINS THE ADDRESS OF THE USER LABEL EXIT.     *
*        BITS 5 AND 6 IN JFCBMASK+5 ARE SET TO ONE.                   *
*                                                                     *
* OUTPUT =                                                            *
*        THE NEXT MODULE IS GIVEN CONTROL IN PROTECT KEY 5 WITH       *
*        REGISTER 2 POINTING TO THE COPIED DCB,                       *
*        THE DEBDCBAD POINTING TO THE COPIED DCB,                     *
*        AND REGISTER 4 POINTING TO THE EOV WORKAREA.                 *
*        USER LABELS ARE WRITTEN AS REQUESTED.                        *
*        REGISTERS 2-11 ARE RETURNED AS ON ENTRY.                     *
*                                                                     *
* EXIT-NORMAL =                                                       *
*        IFG0554Z                                                     *
*                                                                     *
* EXIT-ERROR =                                                        *
*        IF NO USER LABEL EXTENT EXISTS, THE USER'S EXIT ROUTINE IS   *
*        ENTERED WITH THE BUFFER POINTER SET TO ZERO.                 *
*                                                                     *
* EXTERNAL REFERENCES = SEE BELOW                                     *
*                                                                     *
*    ROUTINES =                                                       *
*        IFG019RA THROUGH THE IECRES MACRO.                           *
*                                                                     *
*        USER'S OUTPUT LABEL EXIT ROUTINE ENTERED BY MEANS OF         *
*        IECRES-UEXIT MACRO WHICH GIVES CONTROL TO IFG019RA WHICH     *
*        TAKES THE USER EXIT WITH THE FOLLOWING INPUT -               *
*              REG 0  CONTAINS NOTHING OF INTEREST.                   *
*              REG 1  POINTER TO 4 WORD PARAMETER LIST -              *
*                   OFFSET (HEX)                                      *
*                   0  POINTER TO LABEL BUFFER                        *
*                   4  POINTER TO DCB                                 *
*                   8  FLAG BYTE                                      *
*                   9  POINTER TO STATUS INFORMATION (IF              *
*                   PERMANENT I/O ERROR) OR ZERO                      *
*                   C  POINTER TO IMAGE AREA IF USER TOTALING WAS     *
*                   REQUESTED OR ZERO.                                *
*              REG 2-13 CONTENTS OF USER'S REGS BEFORE PUT/CHECK      *
*              MACRO INSTRUCTION CAUSING EOV WAS ISSUED, OR BEFORE    *
*              EOV OR FEOV MACRO INSTRUCTION WAS ISSUED.              *
*              REG 14 RETURN ADDRESS.                                 *
*              REG 15 USER'S EXIT ADDRESS.                            *
         EJECT
*    DATA AREAS =                                                     *
*        EOV WORKAREA.                                                *
*        DATA MANAGEMENT KEY USER LABEL WORKAREA                      *
*        USER KEY USER LABEL WORKAREA                                 *
*                                                                     *
*    CONTROL BLOCK =                                                  *
*        DCB                                                          *
*        TCB                                                          *
*        CVT                                                          *
*        DEB                                                          *
*                                                                     *
* TABLES =                                                            *
*        NONE.                                                        *
*                                                                     *
* MACROS =                                                            *
*        MODESET                                                      *
*        SETLOCK                                                      *
*        IECRES-LOAD                                                  *
*        IECRES-GET                                                   *
*        IECRES-UEXIT                                                 *
*        IECRES-FREE                                                  *
*        EXCP                                                         *
*        WAIT                                                         *
*        XCTLTABL                                                     *
*        IECDSECS                                                     *
*        IECEQU                                                       *
*                                                                     *
* CHANGE ACTIVITY =                                                   *
*        SEE CHANGES/DELETIONS SECTION JUST AFTER CSECT CARD.         *
*                                                                     *
***********************************************************************
         EJECT
         USING ULDMWK,R8                                         Y02082
         USING IHADCB,RDCB
         USING FORCORE,RCORE
         USING WTG,RWTG                                          Y02080
         USING TCB,RET
         BALR  RBASE,0                  ESTABLISH BASE REGISTER
         USING *,RBASE
*
*    GET CORE FOR WORK AREA, LABEL BUFFER AND PARAMTER LIST      Y02080
*                                                                Y02082
*    OBTAIN CORE FOR DATA MANAGEMENT KEY USER LABEL WORK AREAS   Y02082
*                                                                Y02082
         IECRES GET,LV=USERLDM,PREFIX=YES,SP=K229,ID=ULWA,       Y02144*
               STM=(2,14,WTGPREFX)                               Y02082
*
         STM   RPAR,RC,K0(R1)           SAVE REGS 5-12 FOR CALLER
         LR    R8,R1                    SET BASE FOR ULWK DSECT
*                                                                Y02082
*    OBTAIN CORE FOR USER KEY USER LABEL WORK AREAS              Y02082
*                                                                Y02082
         IECRES GET,LV=USERLU,PREFIX=NO,SP=K229,KEY=USER,        Y02082*
               STM=(2,14,WTGPREFX)                               Y02082
         EJECT
         MODESET EXTKEY=DATAMGT         EOV KEY                  Y02082
         USING ULUWK,RA                 USER'S WORK AREA         Y02082
         LR    RA,R1                    ADDR OF GOTTEN CORE      Y02082
         L     R7,DXCCW5                PICK UP INFO AND SAVE IN DXCCW9
         NI    K28(R8),X'FF'-LASTNTRY   TURN OFF LAST ENTRY INDIC IF ON
         XC    DXCCW1(K80),DXCCW1       CLEAR CHAN PROG AREA
         ST    R7,DXCCW9                SAVE INFO FOR 554Z
*
*        DETERMINE IF A USER LABEL EXTENT HAS BEEN ALLOCATED FOR THIS
*        DATA SET.
*
         CLI   DSCEXTYP,ULEXTYP         IS 1ST EXTENT FOR USER LABELS
         BNE   EDO18050                 NO EXTENT, GO TO USER
*
*        EXTENT EXISTS - PROCESS USER LABELS
*
         XC    DXDAADDR(K8),DXDAADDR    ZERO IOBOADAD IN IOS' IOB
         MVC   DXDAADDR+K3(K4),DSCLOWLM  CCHH FROM START OF 1ST EXTENT
*
*        PROCESS USER TRAILER LABELS.
*
*        SET UP CHANNEL PROGRAM TO FIND UTL0
*
*        STRUCTURE COUNT AND KEY FIELDS IN WORK AREA.  SET IOBDADAD
*        IN I/O SUPPORT'S IOB.
*
         LA    R1,ULWK8
         ST    R1,DXCCW1                READ COUNT INTO WK1
         LA    R1,ULWK10
         ST    R1,DXCCW2                SEARCH ON KEY IN WK3
         LA    R1,DXCCW1
         ST    R1,DXCCW3                TIC TO CCW1
         OC    DXCCW1(K32),ULCP13       OR CP SKELETON OVER ADDRESSES
         MVC   ULWK10(K4),UTLCONS3      KEY=UTLO FOR SEARCH KEY EQUAL
*
         BAL   RC,EDO18100              EXECUTE CP TO FIND UTL0
         TM    DXECB,ECBNOERR           CHECK FOR I/O ERROR
         BZ    EDO17200                 ERROR, BR TO SET UP EXIT TO USR
         EJECT
*
*                        SET UP  CP TO WRITE USER TRAILE
*                        AS EACH LABEL (UTLN) IS WRITTEN, A FILE
*                           MARK (UTL(N+1)) WILL BE WRITTEN FOLLOWING
*
         XC    DXCCW1(K32),DXCCW1       CLEAR CHNL PGM 1 OUT
         BAL   RPARC,EDO18000           GO SET UP CP
         OC    DXCCW1(K32),ULCP23       OR CP SKELETON OVER ADDRESSES
         MVC   DXCCW5(K24),DXCCW1       APPEND CP FOR WRITE CHECKING
         LA    R1,DXCCW5
         ST    R1,DXCCW6                TIC BACK TO CCW5
         MVI   DXCCW6,CCWTIC            RESTORE TIC OP
         MVI   DXCCW7,CCWRDCKD          MAKE OP READ
         OI    DXCCW7+K4,CCWSKIP        SET SKIP BIT ON
         NI    DXCCW7+K4,X'FF'-CCWCMDCH  TURN OFF CC FLAG IN LAST CCW
         MVC   ULWK5(K12),ULWK8         SET UP COUNT, KEY FOR FILE MARK
         SR    R1,R1                    CLEAR RF
         STC   R1,ULWK6+K3              DATA LENGTH=0 FOR FILE MRK
*                                       IF DISP=MOD,UTL0 MAY HAVE
*                                       LENGTH OF 80 BYTES
*
EDO17100 EQU   *                        GET COUNT
         IC    R1,ULWK9                 R OF COUNT FOR UTL TO BE WRITTN
         BCTR  R1,0                     R-1
         STC   R1,DXDAADDR+K7           R IN IOBDADAD
         LA    R1,K2(,R1)               R-1+2=R+1
         STC   R1,ULWK6                 R IN COUNT FIELD OF UTL(N+1)
*
         IC    R1,ULWK10+K3             N FROM LABEL TO BE WRITTEN
         LA    R1,K1(,R1)               N+1
         STC   R1,ULWK7+K3              KEY FOR UTL(N+1)
*
         MVI   ULWK9+K3,K80             DATA LENGTH = 80 FOR UTLN
*
         CLI   ULWK10+K3,X'F8'          CHECK IF 8 TRAILERS WERE WRITTN
         BNL   EDO17300                 YES, BRANCH TO SET UP RETURN
*
         L     RPARC,ULWK7              GET UTLN                 Y02082
         MODESET KEYADDR=DXUKEY,WORKREG=1  ASSUME USER KEY       Y02082
         ST    RPARC,ULBUFR             PRIME BUFFER WITH UTLN   Y02082
         EJECT
*
*        GO TO USER'S ROUTINE TO CONSTRUCT LABEL.
*
         BAL   RPARC,EDO18200           GO EXIT TO USER'S ROUTINE
*        RETURN IN DATAMGT KEY                                   Y02082
         L     RPARC,ULWK7              UTL NUMBER               YM2891
         MODESET KEYADDR=DXUKEY,WORKREG=12 USER KEY              YM2891
         ST    RPARC,ULBUFR             UPDATE USER UTL NUMBER   YM2891
         MODESET EXTKEY=DATAMGT         RETURN TO DATAMGT KEY    YM2891
         LTR   RF,RF                    CHECK FOR RETURN CODE=0
         BZ    EDO17300                 CODE IS ZERO, BRANCH TO RETURN
         ST    RF,ULWK1                 SAVE RETURN CODE
*                                                                Y02082
*    COPY LABEL FROM USER'S USER LABEL WORK AREA                 Y02082
*                                                                Y02082
         L     RF,WTGPREFX              POINT TO PREFIX          Y02082
         STM   R0,RET,IECREGSV-IECPREFX(RF) SAVE REGS THRU LOCK  Y02082
         MODESET EXTKEY=SUPR            ASSUME KEY ZERO          Y02082
*    OBTAIN LOCAL LOCK                                           Y02082
         SETLOCK OBTAIN,TYPE=LOCAL,MODE=UNCOND,                  Y02082*
               RELATED=('PREVENT FREE USER CORE-RELEASED BELOW') Y02082
         LM    R0,RET,IECREGSV-IECPREFX(RF) RESTORE REGS         Y02082
         MODESET KEYADDR=DXUKEY,WORKREG=15 ASSUME USER KEY       Y02082
*    VERIFY THAT USER STILL HOLDS USER LABEL WORK AREA           Y02082
         OC    ULUWK(USERLU),ULUWK      PGM CHECK IF NOT         Y02082
         MODESET EXTKEY=SUPR            ASSUME KEY ZERO          Y02082
         MVC   ULDMBUFR,ULBUFR          COPY USER LABEL          Y02082
         L     RF,WTGPREFX              POINT TO PREFIX          Y02082
         STM   R0,RET,IECREGSV-IECPREFX(RF) SAVE REGS THRU LOCK  Y02082
*    RELEASE LOCAL LOCK                                          Y02082
         SETLOCK RELEASE,TYPE=LOCAL,RELATED=('SEE ABOVE')        Y02082
         LM    R0,RET,IECREGSV-IECPREFX(RF) RESTORE REGS         Y02082
         MODESET EXTKEY=DATAMGT         DATA MANAGEMENT KEY      Y02082
         EJECT
         BAL   RC,EDO18100              GO WRITE LABEL
*
         TM    DXECB,ECBNOERR           CHECK FOR I/O ERROR
         BZ    EDO17200                 YES, BR TO TAKE SPECIAL EXIT
         CLI   ULWK1+K3,XLOUTL          CHECK RET CODE FOR MORE LABELS
         BE    EDO17300                 NO, PREVIOUS LABEL WAS LAST
         CLI   ULWK1+K3,X08             CHECK,BR IF RETURN CODE
         BNE   EDO17300                 WAS NOT VALID
*
         MVC   ULWK8(K12),ULWK5         UTL(N+1) BECOMES C,K OF NXT LBL
         B     EDO17100                 BR TO PROCESS NEXT LABEL
         SPACE 2
EDO17200 EQU   *                        I/O ERROR ROUTINE
*
*        IF AN I/O ERROR OCCURS WRITING USER TRAILER LABELS, A SPECIAL
*        EXIT IS TAKEN TO THE USER'S ROUTINE.  INPUT TO THE USER'S
*        ROUTINE IS THE SAME AS FOR OTHER EXITS WITH ONE EXCEPTION-
*        IN THE THIRD WORD OF THE PARAMETER LIST, BIT 0 IS SET TO ONE
*        AND THE THREE LOW ORDER BYTES CONTAIN A POINTER TO STATUS
*        INFO (PTR TO IOB).
*
         LA    R1,DXIOB                 INSERT POINTER TO STATUS INFO
         MODESET KEYADDR=DXUKEY,WORKREG=15 ASSUME USER KEY   @ZA28829
         ST    R1,ULPARM+K8             IN PARAMETER LIST
         OI    ULPARM+K8,ERROR          FLAG ERROR
         BAL   RPARC,EDO18300           GO TO SYNCH TO USER
* RETURN IN DATAMGT KEY                                          Y02082
         EJECT
*
*        FREE WORKAREAS AND RETURN TO IFG0554Z
*
EDO17300 EQU   *                        SET RETURN FOR TRLR PROCESSING
*
*        FREEMAIN USER'S USER LABEL WORK AREA                    Y02082
*
         IECRES FREE,A=(RA),PREFIX=NO,LV=USERLU,SP=K229,         Y02082*
               KEY=USER,STM=(2,14,WTGPREFX)                      Y02082
         MODESET EXTKEY=DATAMGT         DATA MANAGEMENT KEY      Y02082
         MVC   DXCCW5(K8),DXCCW9        RESTORE DXCCW5 FOR 554Z
*
         LR    R1,R8                    START OF AREA TO BE FREED
         LM    RPAR,RDEB,K0(R1)         RESTORE CALLING MODULE'S REGS
         EJECT
*
*        FREE DATA MANAGEMENT WORKAREA AND LABEL BUFFER AND XCTL
*
         IECRES FREE,A=(R1),PREFIX=YES,STM=(0,14,WTGPREFX)       Y02082
         LA    RET,K4
         LA    R7,EOVT                  SET XCTL TO 554Z
         LA    RWTGC,DXXENTRY           SET R8 FOR RES RTN
         LA    RWTG,DXXAREA             POINT TO EOV WTG
         IECRES LOAD,MODID=(R7),BRCODE=(RET),BRANCH=QUEUED       Y02080
         EJECT
EDO18000 EQU   *                        BUILD CHANNEL PROGRAM
         LA    R1,DXDAADDR+K3
         ST    R1,DXCCW1                SEARCH ID FROM IOBDADAD+3
         LA    R1,DXCCW1
         ST    R1,DXCCW2                TIC TO CCW1
         LA    R1,ULWK8
         ST    R1,DXCCW3                WRITE C,K FROM ULWK1
         LA    R1,ULWK5
         ST    R1,DXCCW4                WRITE LABEL FROM BUFFER
         BR    RPARC                    UNCONDITIONAL BRANCH
         SPACE 2
*
*        NO USER LABEL TRACK WAS ALLOCATED.  EXIT TO USER'S ROUTINE
*        WITH POINTER TO LABEL BUFFER ZEROED.
*
EDO18050 EQU   *                        ZERO BUFFER POINTER
         MODESET KEYADDR=DXUKEY,WORKREG=1 USER WORK AREA         Y02082
         XC    ULPARM+K8(K4),ULPARM+K8  CLEAR ANY ERROR INDICTNS
         SR    R1,R1                    BUFFER POINTER TO ZERO
         LA    RPARC,EDO17300           POINT TO RETURN AFTER SYNCH
         B     EDO18400                 GO SYNCH TO USER
         SPACE 2
*
*        CLOSED SUBROUTINE TO ISSUE I/O OPERATIONS
*        IT IS ASSUMED THAT ALL CONTROL BLOCKS HAVE ALREADY BEEN SET UP
*
EDO18100 EQU   *                        EXCP, WAIT, RETURN
         EXCP  DXIOB
*
         WAIT  ,ECB=DXECB
*
         BR    RC                       UNCONDITIONAL BRANCH
         EJECT
*
*        CLOSED SUBROUTINE TO TAKE USER EXIT. REGISTER           Y02082
*        CONTENTS UPON ENTRY TO THE USER EXIT ARE AS FOLLOWS     Y02082
*                        R0 - CONTAINS NO MEANINGFUL INFO
*                        R1 - PTR TO PARAMETER LIST
*                    R2-R13 - USER'S CONTENTS BEFORE I/O SUPPORT
*                       R14 - RETURN ADDRESS (SET BY SYNCH)
*                       R15 - EXIT ADDRESS
*
EDO18200 EQU   *                        ZERO PARMLIST
         XC    ULPARM+K8(K4),ULPARM+K8  ZERO ERROR INDICATIONS
EDO18300 EQU   *                        GET LABEL BUFFER
         LA    R1,ULBUFR                PTR TO LABEL BUFFER
EDO18400 EQU   *                        SETUP PARM LIST
         ST    R1,ULPARM                PTR TO LABEL BUFFER      Y02082
         L     R1,DXUDCBAD              GET USER'S DCB ADDR      Y02082
         ST    R1,ULPARM+K4             PUT IN PARM LIST         Y02082
         NI    ULDCBPTR,X'FF'-EOFFLG    INSURE EOF FLG IS OFF
         XC    ULPARM+K12(K4),ULPARM+K12  ZERO TOTALING AREA
         TM    DCBDSORG,DCBORGDA        CK FOR DIRECT ORGANIZATION
         BO    EDO18500                 BR IF DIRECT
         TM    DCBOFLGS,DCBOPEN         CK IF DCB ALREADY OPEN
         BZ    EDO18500                 IF YES, CTL FROM OPEN, BYPASS
*                                       TOTALING
         TM    DCBMACRF,DCBMEXCP        CK IF EXCP
         BO    EDO18500                 BR IF EXCP,SKIP TOTALING
         TM    DCBOPTCD,DCBOPTT         TEST,BR IF USER TOTALING
         BZ    EDO18500                 HAS NOT BEEN REQUESTED
*
         L     RDEB,DCBDEBAD            GET ADDRESS OF USER'S DEB
         SR    R1,R1                    CLEAR WORK REG
         IC    R1,DEBNMEXT-DEB(RDEB)    GET NUMBER OF EXTENTS
         SLL   R1,K4                    MULTIPLY BY 16
         L     RDEB,K36(R1,RDEB)        GET ADDR TOTALING WORKAREA-
*                                       DEB ADDR+32+4+LENGTH EXTENTS
         USING TOTSAVWA,RDEB            SET BASE - TOTALING AREA
         MVC   ULTOTPTR,TOTEOVPT        CURRENT IMAGE AREA ADDR TO
*                                       USR LBL PARM LIST
         DROP  RDEB                     DROP TOTALING AREA BASE
         EJECT
EDO18500 EQU   *                        UPDATE USER DCB, TAKE EXIT
         MODESET EXTKEY=DATAMGT         EOV KEY                  Y02082
         NI    DCBOFLGS,X'FF'-DCBOLOCK  IND USER EXIT TAKEN      Y02082
*                                                                Y02082
* COPY THE DCB FROM THE WORK AREA TO USER'S STORAGE              Y02082
*                                                                Y02082
         IECRES INIT,DCBCOPY=FRWKAR,STM=(3,14,WTGPREFX)          Y02082
*                                                                Y02082
         LA    R1,ULPARM                POINT TO PARAMETER LIST
         IECRES UEXIT,EXITAD=K28(,R8),STM=(2,13,WTGPREFX)        Y02082
         EJECT
         L     RDCB,DXUDCBAD            GET PTR TO USER'S DCB    Y02082
         MODESET KEYADDR=DXUKEY,WORKREG=1 ASSUME USER'S KEY      Y02082
         OI    DCBOFLGS,DCBOLOCK        INDICATE RETURN FR USER  Y02082
         MODESET EXTKEY=DATAMGT         RESUME DATA MGT KEY      Y02082
         L     RDCB,DXPDCBAD            POINT TO COPIED DCB      Y02082
         OI    DCBOFLGS,DCBOLOCK        SET LOCK BIT = 1
         L     R1,ULWK10                GET UTLN                 Y02082
         MODESET KEYADDR=DXUKEY,WORKREG=12 ASSUME USER KEY       Y02082
         ST    R1,ULBUFR                RESET BUFFER WITH UTLN   Y02082
         MODESET EXTKEY=DATAMGT         DATA MANAGEMENT KEY      Y02082
         BR    RPARC                    UNCONDITIONAL BRANCH
         EJECT
*
*        CONSTANTS
*
         DS    0F
UTLCONS3 DC    C'UTL0'                  KEY USED IN SEARCH KEY EQ
ULCP13   CCW   X'12',0,X'40',8          READ COUNT
         CCW   X'29',0,X'40',4          SEARCH KEY EQUAL
         CCW   X'08',0,0,0              TIC
         CCW   X'03',0,0,1              NOP
*
ULCP23   CCW   X'31',0,X'40',5          SEARCH ID EQUAL
         CCW   X'08',0,0,0              TIC TO CCW1
         CCW   X'1D',0,X'40',92         WRITE CKD    UHLN     LABEL
         CCW   X'1D',0,X'40',12         WRITE CKD    UHL(N+1) FILE MARK
         CCW   X'1D',0,X'40',12         WRITE CKD    UTL0     FILE MARK
CCWSKIP  EQU   X'10'                    SKIP BIT IN CCW          Y02082
CCWRDCKD EQU   X'1E'                    READ COUNT,KEY,DATA      Y02082
         EJECT
*
*        XCTL  TABLE FOR IFG0555B
*
         XCTLTABL SVC=055,ID=(EOVT,4Z),BRT=YES,LENGTH=           Y02080
         IECDSECS CVT,TCB,TIOT,SMF,DCB,DEB,UCB,MAIN,UNITTAB,           X
               USERLAB,USERTOT,WTG,PREFX,PSA,RRPL,EXPAND=YES     Y02144
         IECEQU
         END
