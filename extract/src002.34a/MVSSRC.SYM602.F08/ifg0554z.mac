         TITLE 'IFG0554Z                      DSCB UPDATE CURRENT VOLUME
               E/USER LABEL DETERMINATION'                       Y02134
IFG0554Z CSECT
***********************************************************************
*                                                                     *
*                                                                     *
*          VS2 RELEASE 02 DELETIONS                                   *
*                                                                     *
*                                                                     *
***********************************************************************
*                                                                     *
* MODULE NAME = IFG0554Z (OS/VS2)                                     *
*                                                                     *
* DESCRIPTIVE NAME =                                                  *
*        UPDATE DSCB CURRENT VOLUME/USER LABEL DETERMINATION          *
*                                                                     *
* COPYRIGHT = NONE                                                    *
*                                                                     *
* STATUS = RELEASE 2, LEVEL 0                                         *
*                                                                     *
* FUNCTION =                                                          *
*        THIS MODULE IS ENTERED FROM IFG0554N DURING FEOV PROCESSING  *
*        IF USER LABEL PROCESSING IS REQUIRED. IF SMF PROCESSING HAS  *
*        BEEN REQUESTED, IFG0554N HAS GIVEN IFG0553B (ALIAS FOR       *
*        IFG0202H) CONTROL AND IFG0202I GIVES THIS MODULE CONTROL.    *
*                                                                     *
*        THIS MODULE IS ALSO ENTERED FROM IFG0554P DURING EOV         *
*        DA OUTPUT PROCESSING WHEN NO ADDITIONAL SPACE WILL BE        *
*        ACQUIRED ON THE CURRENT VOLUME AND MORE VOLUMES REMAIN       *
*        TO BE PROCESSED. IF SMF PROCESSING HAS BEEN REQUESTED,       *
*        IFG0554P HAS GIVEN IFG0553B (ALIAS FOR                       *
*        IFG0202H) CONTROL AND IFG0202I GIVES THIS MODULE CONTROL.    *
*                                                                     *
*        THE FORMAT 1 DSCB IS READ IF USER LABELS ARE SPECIFIED IN    *
*        THE JFCB, OR EXCP IS BEING USED, OR IF FEOV IS NOT BEING     *
*        PROCESSED. IF A VIO DATA SET IS BEING PROCESSED, THE VDSDSCB *
*        IS MOVED TO THE WORKAREA FROM SWA.                           *
         EJECT
*        AFTER THE DSCB IS READ IT IS UPDATED (THE LAST BLOCK TTR     *
*        AND THE TRACK BALANCE ARE ZEROED, AND THE LAST VOLUME BIT    *
*        IS TURNED OFF) IF EXCP IS BEING USED OR IF FEOV IS NOT       *
*        BEING PROCESSED. IF IT IS A PARTITIONED DATA SET, THE LAST   *
*        BLOCK POINTER AND TRACK BALANCE ARE NOT ZEROED OUT, AS       *
*        A PDS WILL NOT BE EXTENDED TO A NEW VOLUME. THE              *
*        DIRECTORY WILL BE OVERWRITTEN AT THE NEXT OPEN FOR           *
*        OUTPUT IF THE TTRLL IS ZEROED. INSTEAD, THE LL IS            *
*        ZEROED AND THE TTR IS SET FROM DCBRELAD, THE START OF        *
*        THE MEMBER CURRENTLY BEING WRITTEN. IF AN EXCP DCB IS        *
*        PROCESSING THE PDS AND DCBRELAD IS NOT DEFINED (THE DCB      *
*        DOES NOT HAVE A 5 WORD DEVICE INTERFACE) THE TTRLL IS NOT    *
*        CHANGED.                                                     *
*                                                                     *
*        THE USER TRAILER LABEL MODULE IFG0555B IS GIVEN CONTROL IF   *
*        USER LABELS ARE SPECIFIED IN THE JFCB AND THE DSORG IS PS    *
*        AND THERE IS AN 04 EXIT IN THE DCB EXIT LIST. BITS 5 AND 6   *
*        IN JFCBMASK+5 ARE SET TO ONE TO INDICATE A RETURN FROM       *
*        IFG0555H.                                                    *
*                                                                     *
*        AFTER RETURNING FROM IFG0555H OR IF USER LABEL PROCESSING    *
*        HAS NOT BEEN REQUIRED, MODULE IFG0554P IS GIVEN CONTROL      *
*        WITH REGISTER 14 SET TO 4.                                   *
         EJECT
* NOTES =                                                             *
*        NONE.                                                        *
*                                                                     *
*    DEPENDENCIES =                                                   *
*            CLASS ONE CHARACTER CODE.  THE EBCDIC CHARACTER CODE     *
*            WAS USED FOR ASSEMBLY.  THE MODULE MUST BE REASSEMBLED   *
*            IF A DIFFERENT CHARACTER SET IS USED FOR EXECUTION.      *
*                                                                     *
*    RESTRICTIONS = NONE                                              *
*                                                                     *
*    REGISTER CONVENTIONS =                                           *
*            R2 POINTS TO DCB                                         *
*            R4 POINTS TO OPEN WORK AREA                              *
*            R5 POINTS TO THE RESIDENT ROUTINE                        *
*            R6 POINTS TO THE WTG TABLE                               *
*            R7 POINTS TO THE CURRENT PARAMETER LIST ENTRY            *
*            R8 POINTS TO THE CURRENT WTG TABLE ENTRY                 *
*            R9 POINTS TO THE DD ENTRY IN THE TIOT                    *
*            R10 POINTS TO THE UCB                                    *
*                                                                     *
*    PATCH LABEL = SEE THIRD FROM LAST LABEL BEFORE ORG STATEMENT AT  *
*                  END OF LISTING.                                    *
*                                                                     *
* MODULE TYPE = CONTROL (OPEN, CLOSE, EOV DATA MANAGEMENT)            *
*                                                                     *
*    PROCESSOR = ASSEMBLER XF                                         *
*                                                                     *
*    MODULE SIZE = SEE EXTERNAL SYMBOL DICTIONARY OR LOC FIELD ON     *
*                  ORG STATEMENT AT END OF LISTING                    *
*                                                                     *
*    ATTRIBUTES = REENTRANT, REFRESHABLE,READ-ONLY, ENABLED,          *
*                 PRIVILEGED, SUPERVISOR STATE, DATA MANAGEMENT KEY,  *
*                 LINK PACK AREA RESIDENT/PAGEABLE                    *
*                                                                     *
* ENTRY POINT =                                                       *
*        IFG0554Z                                                     *
*                                                                     *
*    PURPOSE = SEE FUNCTION                                           *
*                                                                     *
*    LINKAGE =                                                        *
*        THIS MODULE IS TRANSFERRED CONTROL THROUGH THE IECRES-LOAD   *
*        MACRO INSTRUCTION.                                           *
         EJECT
* INPUT =                                                             *
*        GIVEN CONTROL IN PROTECT KEY 5.                              *
*        REGISTER 2 POINTS TO THE COPIED DCB.                         *
*        DEBDCBAD POINTS TO THE COPIED DCB.                           *
*        REGISTER 4 POINTS TO THE EOV WORKAREA                        *
*                                                                     *
*        IF BITS 5 AND 6 OF JFCBMASK+5 ARE SET TO ONE, A RETURN       *
*        FROM USER LABEL MODULE IFG0555H IS BEING MADE.               *
*                                                                     *
* OUTPUT =                                                            *
*        THE NEXT MODULE IS GIVEN CONTROL IN PROTECT KEY 5 WITH       *
*        REGISTER 2 POINTING TO THE COPIED DCB,                       *
*        THE DEBDCBAD POINTING TO THE COPIED DCB,                     *
*        AND REGISTER 4 POINTING TO THE EOV WORKAREA,                 *
*                                                                     *
*        IFG0554P -                                                   *
*        REGISTER 14 SET TO 4.                                        *
*                                                                     *
*        IFG0555B -                                                   *
*        BITS 5 AND 6 OF JFCBMASK+5 SET TO ONE.                       *
*                                                                     *
*        IFG0550P GIVEN CONTROL THROUGH DMABCOND MACRO -              *
*        REGISTER 0 CONTAINS INTERNAL ABEND CODE                      *
*                                                                     *
* EXIT-NORMAL =                                                       *
*        IFG0555B TO PROCESS USER LABELS.                             *
*        IFG0554P TO OBTAIN A NEW VOLUME.                             *
*                                                                     *
* EXIT-ERROR =                                                        *
*        IFG0550P GIVEN CONTROL THROUGH DMABCOND MACRO WITH AN        *
*        INTERNAL ABEND CODE IN REGISTER 0 -                          *
*        190 - 737-4 - I/O ERROR READING OR WRITING DSCB.             *
*                                                                     *
* EXTERNAL REFERENCES = SEE BELOW                                     *
*                                                                     *
*    ROUTINES =                                                       *
*        IFG019RA THROUGH THE IECRES MACRO.                           *
*                                                                     *
*    DATA AREAS =                                                     *
*        EOV WORKAREA.                                                *
         EJECT
*    CONTROL BLOCK =                                                  *
*        DCB                                                          *
*        DEB                                                          *
*        VDSCB                                                        *
*                                                                     *
* TABLES =                                                            *
*        DCB EXIT LIST.                                               *
*                                                                     *
* MACROS =                                                            *
*        MODESET                                                      *
*        IECRES-LOAD                                                  *
*        EXCP                                                         *
*        WAIT                                                         *
*        DMABCOND                                                     *
*        XCTLTABL                                                     *
*        IECDSECS                                                     *
*        IECEQU                                                       *
*                                                                     *
* CHANGE ACTIVITY =                                                   *
*        SEE CHANGES/DELETIONS SECTION JUST AFTER CSECT CARD.         *
*                                                                     *
***********************************************************************
         EJECT
EABD190  EQU   190                      737-4 INTERNAL ABEND CODE
*                                                                     *
         USING FORCORE,RCORE
         USING WTG,RWTG                 BASE FOR WTG TABLE       Y02080
         USING IHADCB,RDCB
         USING TIOENTRY,RTIOT
         USING UCB,RUCB
         USING DEB,RDEB
         BALR  RBASE,0                  ESTABLISH ADDRESSABILITY
         USING *,RBASE
*
*
         TM    JFCBMASK+K5,K6           IS THIS A RETURN FROM 555B
         BO    EDO15100                 YES, BRANCH
*                                                                Y02134
EDO14500 EQU   *                        NOT USER LABEL RETURN
*                                                                Y02134
         ST    RC,DXCCW5                SAVE FOR 554P            Y02134
         TM    JFCBLTYP,JFCSL+JFCBUL    TEST IF SUL SPECIFIED
         BO    EDO14550                 YES, BRANCH              A37870
         TM    DCBMACRF,DCBMEXCP        TEST FOR EXCP           SA59750
         BO    EDO14550                 BRANCH IF EXCP          SA59750
         TM    DCBCIND2,DCBC2FEO        FEOV                     A37870
         BO    EDO15300                 YES, BRANCH              A37870
         EJECT
EDO14550 EQU   *                        SUL, EXCP, NOT FEOV      A37870
*
*        READ FORMAT 1 DSCB
*
         MVC   DXDEBUCB+K1(K3),DXUCBADR+K1 UCB ADDRESS TO WA DEB Y02134
         L     RUCB,DXUCBADR            GET UCB ADDRESS          YM3862
         TM    UCBJBNR,UCBVRDEV         IS IT A VIO UNIT         YM3862
         BNO   EDO14560                 BRANCH IF NOT VIO        YM3862
*
*        MOVE DSCB FROM SWA TO EOV WORKAREA                      YM3862
*
         SPACE 1
*                                       MOVE VDSDSCB TO WORKAREA YM5385
         MVC   DXDSCB(DSCEXT2-DSCFMTID),VDSDSCB-VDSUCB+L'JFCBDSNM(RUCB)
         OC    DXDSCB(DSCEXT2-DSCFMTID),DXDSCB IS DSCB SCRATCHED YM3862
         BZ    EDO16310                 BR IF SCRATCHED          YM3862
         XC    DSCEXT2(DSCBEND-DSCEXT2),DSCEXT2 ZERO 3RD EXT AND YM3862
*                                       CHAIN POINTER            YM3862
         B     EDO14570                 BRANCH TO UPDATE DSCB    YM3862
*
*        READ DSCB WITH EXCP                                     YM3862
*
EDO14560 EQU   *                        BUILD CHANNEL PROGRAM    YM3862
         MVC   DXDEBSCC(K10),WFMDUMXL   MOVE DUMMY EXTENT TO DEB
         LR    R7,RDEB                  PICK UP DEB ADDRESS
         SH    R7,WFM0162L              POSITION AT (-16)
         MVC   DXDAADDR+K1(K7),DEB+K1-DEB(R7)  BBCCHHR OF
*                                           DSCB TO SEEK WRD
         MVI   DXDAADDR,K0              SET -M- TO ZERO
*
*        GENERATE CHANNEL PROGRAM TO READ DSCB
*
         XC    DXCCW1(K24),DXCCW1       ZERO CCWS 1-3
         LA    R1,DXDAADDR+K3           ADDR OF CCHHR
         ST    R1,DXCCW1                SEARCH ID TO CCW1
         LA    R1,DXCCW1                ADDR OF SRCH CMD TO TIC
         ST    R1,DXCCW2                STORE IT
         LA    R1,DXDSCB                READ AREA ADDRESS
         ST    R1,DXCCW3                TO READ M/T CMD.
         OC    DXCCW1(K24),WFMCCWSL     OR IN SKELETON CCWS
         BAL   R7,EDO16300              GO READ DSCB             A37870
*                                                                Y02134
EDO14570 EQU   *                        SEE IF DSCB UPDATED      YM3862
         TM    DCBMACRF,DCBMEXCP        TEST FOR EXCP           SA59750
         BO    EDO14575                 BRANCH IF EXCP           YM4634
         TM    DCBCIND2,DCBC2FEO        FEOV                     A37870
         BO    EDO14600                 YES, GO PROCESS USER LBL A37870
         EJECT
EDO14575 EQU   *                        UPDATE DSCB              YM4634
*
*        UPDATE FORMAT 1 DSCB LAST BLOCK POINTER AND TRACK BALANCE
*
         TM    DSCFILTY,DCBORGPO        IS THIS PDS            @ZA28976
         BO    EDO14582                 BRANCH YES             @ZA28976
         TM    DCBMACRF,DCBMRECP        CHECK FOR EXCP         @ZA28976
         BZ    EDO14580                 BRANCH IF NO           @ZA28976
         TM    DCBMACF2,DCBMR5WD+DCBMR4WD DCBFDAD EXIST        @ZA28976
         BZ    EDO14585                 DONT UPDATE IF NO      @ZA28976
EDO14580 EQU   *
         MVC   DXDAADDR,DCBFDAD         GET MBBCCHHR           @ZA28976
         L     RF,WTGPREFX              PTR TO PREFIX          @ZA28976
         STM   R0,RF,IECREGSV-IECPREFX(RF)  SAVE REGS          @ZA28976
         LR    R1,RDEB                  PTR TO  DEB            @ZA28976
         LA    R2,DXDAADDR              PTR TO MBBCCHHR        @ZA28976
         L     RF,CVTPTR                PTR TO CVT             @ZA28976
         L     RF,CVTPRLTV-CVT(,RF)     ADDR OF CONVERT RTN    @ZA28976
         BALR  RET,RF                   CONVERT TO TTR         @ZA28976
         L     RET,WTGPREFX             POINTER TO PREFIX      @ZA28976
         LM    R1,RET,IECREGSV-IECPREFX+K4(RET) RESTORE REGS   @ZA28976
         STCM  R0,RF,DSCLSTAR           SAVE TTR IN F1         @ZA28976
         LR    R7,RDEB                  GET DEB ADDR           @ZA28976
         SH    R7,WFM0162L              POSITION AT (-16)      @ZA28976
         MVC   DXDAADDR+K1(K7),DEB+K1-DEB(R7)  RESTABLISH      @ZA28976
*                                       BBCCHHRR FOR WRITE     @ZA28976
         MVI   DXDAADDR,K0              SET -M- TO ZERO        @ZA28976
         B     EDO14584
EDO14582 EQU   *
         TM    DCBMACRF,DCBMRECP        IS PDS PROCESSED         YM4634
*                                       WITH EXCP                YM4634
         BZ    EDO14583                 BRANCH IF NO             YM4634
         TM    DCBMACF2,DCBMR5WD        DOES DCBRELAD EXIST      YM4634
         BZ    EDO14585                 DON'T UPDATE TTRLL IF NO YM4634
EDO14583 EQU   *                        UPDATE PDS DSCB          YM4634
         MVC   DSCLSTAR,DCBRELAD        TTR OF MEM TO F1       @ZA28976
EDO14584 EQU   *                                               @ZA28976
         XC    DSCTRBAL,DSCTRBAL        ZERO TRACK BALANCE     @ZA28976
EDO14585 EQU   *                        REWRITE DSCB             YM4634
         NI    DSCDSIND,ALLBITS-LASTNTRY RESET LAST VOL IND      A37870
         OI    DXATDACC,DXATTRAK        LAST TRK USED AND TRK    Y02144
*                                       BALANCE FIELDS UPDATED   Y02144
         TM    UCBJBNR,UCBVRDEV         IS IT VIO UNIT           YM3862
         BNO   EDO14586                 BRANCH IF NOT            YM3862
*
*        MOVE DSCB FROM EOV WORKAREA TO SWA                      YM3862
*
         MODESET EXTKEY=SCHED           GET IN SWA KEY           YM3862
         MVC   VDSDSCB-VDSCB+L'JFCBDSNM(DSCEXT2-DSCFMTID,RUCB),DXDSCB
         MODESET EXTKEY=DATAMGT         GET IN KEY 5             YM3862
         B     EDO14588                 GO TO USER LBL PROCESS   YM3862
*
*        WRITE FORMAT 1 DSCB
*
EDO14586 EQU   *                        WRITE DSCB WITH EXCP     YM3862
         MVC   DXCCW4(K24),DXCCW1       CCW'S FOR RDBACK CHECK   A37870
         MVI   DXCCW3,CCWWRTDA          WRITE COMMAND TO CCW     A37870
         MVI   DXCCW3+K4,CCWCMDCH       CHAIN TO RDBACK CHECK    A37870
         LA    R1,DXCCW4                ADDR OF SEARCH           A37870
         ST    R1,DXCCW5                PUT IN TIC CCW           A37870
         MVI   DXCCW5,CCWTIC            RESTORE TIC COMMAND      A37870
         MVI   DXCCW6+K4,X10            NO DATA XFER FOR RDBACK  A37870
         BAL   R7,EDO16300              GO WRITE DSCB            A37870
EDO14588 EQU   *                        SET AUDIT TRAIL          YM3862
         OI    DXATDACC,DXATUPDB        UPDATED DSCB WRITTEN     Y02144
         EJECT
*
*        USER TRAILER LABEL PROCESSING
*
         ST    RC,DXCCW5                SAVE ADDRESS OF DEB      A37870
*                                       VOLSEQ REFERENCES NEEDED
*                                       IN IFG0554P              Y02134
         TM    DCBMACRF,DCBMEXCP        CHECK DSORG IF NOT EXCP YA02186
         BZ    EDO14590                 BRANCH IF NOT           YA02186
         TM    DCBMACR1,DCBMRFE         DOES EXITLIST EXIST      Y02082
         BZ    EDO15300                 BRANCH IF NO             Y02082
         TM    DCBMACR1,DCBMRCI         DOES DSORG EXIST         Y02082
         BZ    EDO14595                 BR IF NO, TREAT AS PS    Y02082
EDO14590 EQU   *                        IS DSORG PS              Y02082
         TM    DCBDSORG,DCBORGPS        IS DSORG PS              A37870
         BZ    EDO15300                 NO,  BRANCH              A37870
         TM    DCBMACRF,DCBMEXCP        SKIP JFCB TEST IF EXCP  YA02186
         BO    EDO14595                 BRANCH IF YES           YA02186
         TM    JFCDSORG,JFCORGDA        BDAM DATA SET           YA02186
         BO    EDO15300                 BRANCH IF YES-NO EXIT   YA02186
EDO14595 EQU   *                        IS SUL SPECIFIED        YA02186
         TM    JFCBLTYP,JFCSL+JFCBUL    USER LABELS SPECIFIED    A37870
         BNO   EDO15300                 NO, BRANCH               A37870
EDO14600 EQU   *                        IS EXIT LIST ZERO        A37870
         L     RC,DCBEXLST              GET EXIT LIST POINTER    A37870
         LA    RC,K0(,RC)               ZERO HIGH ORDER BYTE     A37870
         LTR   RC,RC                    IS EXIT LIST ZERO        A37870
         BZ    EDO15300                 YES, BRANCH              A37870
EDO14700 EQU   *                        IS THERE AN 04 EXIT      A37870
         MODESET KEYADDR=DXUKEY,WORKREG=7 USER EXIT LIST KEY     Y02082
         CLI   K0(RC),XLOUTL            IS THIS ACTIVE EXIT 4    A37870
         BE    EDO14800                 YES, GO PROCESS USER LBL A37870
         CLI   K0(RC),XLOUTL+LASTNTRY   IS IT EXIT 4 AND LAST    A37870
         BE    EDO14800                 YES, GO RPOCESS USER LBL A37870
         TM    K0(RC),LASTNTRY          IS THIS LAST ENTRY       A37870
         LA    RC,K4(,RC)               POINT TO NEXT ENTRY      A37870
         BZ    EDO14700                 BRANCH IF NOT LAST ENTRY A37870
         B     EDO15300                 BYPASS USER LBL PROCESS  A37870
         EJECT
EDO14800 EQU   *                        IS EXIT ADDRESS ZERO     A37870
         L     RC,K0(,RC)               PICK UP USER EXIT ADDR   A37870
         MODESET EXTKEY=DATAMGT         EOV KEY                  Y02082
         LR    R7,RC                    SAVE EXIT ADDRESS        A37870
         LA    R7,K0(,R7)               ZERO HIGH ORDER BYTE     A37870
         LTR   R7,R7                    IS EXIT ADDRESS ZERO     A37870
         BZ    EDO15300                 YES, BRANCH              A37870
*                                                                Y02134
         OI    JFCBMASK+K5,K6           SET SW TO IND EOV ENTRY
*                                                                Y02134
         LA    RWTGC,DXXENTRY           SET R8 FOR RES RTN
         LA    RWTG,DXXAREA             POINT TO EOV WTG
         USING WTG,RWTG                 WHERE-TO-GO TABLE        Y02080
         IECRES LOAD,MODID=ID4Z5B,BRANCH=QUEUED                  Y02134
         DROP  RWTG                                              Y02080
*
EDO15100 EQU   *                        ENTRY FROM IFG0555B
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
*        RETURN HERE FROM USER LABEL ROUTINE                     Y02134
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
         NI    JFCBMASK+K5,ALLBITS-K6   TURN OFF EOV-UL SWTCH
         EJECT
EDO15300 EQU   *                        AFTER BYPASSING USER LABELS
         MODESET EXTKEY=DATAMGT         EOV KEY                  Y02082
*                                                                Y02134
         L     RC,DXCCW5                RESTORE RC FOR 554P      Y02134
*                                                                Y02134
         LA    RWTGC,DXXENTRY           SET R8 FOR RES RTN
         LA    RWTG,DXXAREA             POINT TO EOV WTG
         USING WTG,RWTG                 WHERE-TO-GO TABLE        Y02080
         IECRES LOAD,MODID=ID4Z4P,BRCODE=K4,BRANCH=QUEUED        Y02134
         DROP  RWTG                                              Y02080
         SPACE 2
EDO16300 EQU   *                        READ/WRITE DSCB
         EXCP  DXIOB                                             A37870
         WAIT  ,ECB=DXECB                                        A37870
         TM    DXECB,ECBCOD7F           TEST COMPLETION CODE     A37870
         BOR   R7                       RETURN NO ERROR          A37870
*                                                                Y02134
EDO16310 EQU   *                        VIO 'I/O ERROR'          YM3862
         NI    DCBOFLGS,X'FF'-DCBOBUSY
         LA    RWTGC,DXXENTRY           SET R8 FOR RES RTN
         LA    RWTG,DXXAREA             POINT TO EOV WTG
         DMABCOND EABD190,ID4Z0P                                 Y02134
         EJECT
*
*        CONSTANTS
*
WFMDUMXL DC    X'000000007FFF7FFF7FFF'  MAX TO COVER ALL DEVICES
WFM0162L DC    H'16'                    POSITIONING FOR DEB
WFMCCWSL DC    X'3100000060000005'      SEARCH EQUAL ID
         DC    X'0800000000000000'      TIC
         DC    X'8600000000000060'      READ DATA M/T
         EJECT
*
*        XCTL  TABLE FOR IFG0554Z
*
         XCTLTABL ID=(ID4Z4P,4P,ID4Z5B,5B,ID4Z0P,0P),            Y02134X
               SVC=055,BRT=YES,LENGTH=                           Y02134
         IECDSECS CVT,TCB,TIOT,SMF,DCB,DEB,UCB,MAIN,USERLAB,     A37870X
               WTG,PREFX,EXPAND=YES                              Y02080
         IDDVDSCB                                                YM3862
         IECEQU
         END
