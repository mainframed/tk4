         TITLE 'IFG0555H                      END OF VOLUME DA OUTPUT -*
                BUILD DEB FUNCTION'
IFG0555H CSECT
***********************************************************************
*                                                                     *
*                                                                     *
*          VS2 RELEASE 02 DELETIONS                                   *
*                                                                     *
*          VS2 RELEASE 3 DELETIONS                                    *
*0000007475,019419,196918-196951,197328                        @Y30LSBS
*
*          VS2 RELEASE SU24                                    @G24LSFS
*DELETE FIX FOR APAR OZ16031                                   @G24LSFS
*197148-197188,668054-668159                                   @G24LSFS
*D388020-456000,463000-467600                                  @G24LSFS
*                                                              @ZM44640
*
*          VS2 RELEASE 3.7 CHANGES/DELETIONS                          *
*0000196885,666852-667452,668054                               @ZA13555
*0000                                                          @ZA16031
*0000193700,193800,A905980                                     @ZA13538
*0000                                                          @ZA17972
***********************************************************************
*                                                                     *
* MODULE NAME = IFG0555H (OS/VS2)                                     *
*                                                                     *
* DESCRIPTIVE NAME = END OF VOLUME DA OUTPUT - BUILD DEB FUNCTION     *
*                                                                     *
* COPYRIGHT = NONE                                                    *
*                                                                     *
* STATUS = RELEASE 2, LEVEL 0                                         *
*                                                                     *
* FUNCTION =                                                          *
*        THIS MODULE IS ENTERED FROM -                                *
*        IFG0554C (ALIAS FOR IFG0194C) IF A NEW VOLUME WERE MOUNTED   *
*        IFG0554P AFTER EXTENDING ON THE SAME VOLUME                  *
*        IFG0555J AFTER RETURNING FROM PROCESSING USER HEADER LABELS  *
*                                                                     *
*        1) THE DEB IS PAGE FREED IF IT HAD BEEN PAGE FIXED.          *
*        THIS MODULE SAVES THE DEB IN THE EOV WORKAREA AND            *
*        UNCHAINS IT FROM THE TCB IT IS CHAINED TO (NOT NECESSARILY   *
*        THE CURRENT TCB.                                             *
*                                                                     *
*        2) IF THE DEB IS CHAINED TO THE CURRENT TCB, THE DEB IS      *
*        DELETED FROM THE DEB TABLE WITH THE DEBCHK-DELETE MACRO.     *
*        OTHERWISE, THE DEB ADDRESS IS LOCATED IN THE DEB TABLE AND   *
*        INVALIDATED (UNTIL THE NEW DEB IS BUILT) BY FLIPPING THE LOW *
*        ORDER BIT IN THE DEB ADDRESS.                                *
*                                                                     *
*        3) THE DEB IS FREED USING THE BRANCH ENTRY TO FREEMAIN,      *
*        DIRECTED TO THE TCB THE DEB WAS CHAINED TO.                  *
*                                                                     *
*        4) STORAGE FOR A NEW DEB IS OBTAINED USING THE BRANCH ENTRY  *
*        TO GETMAIN DIRECTED TO THE TCB THE DEB WAS CHAINED TO. THE   *
*        NEW DEB IS CONSTRUCTED FROM THE CONTENTS OF THE OLD DEB      *
*        SAVED IN THE EOV WORKAREA AND THE DSCB(S) READ BY THE        *
*        PREVIOUS MODULES.                                            *
*                                                                     *
*        5) THE DEB IS CHAINED TO THE TCB WHICH THE OLD DEB WAS       *
*        CHAINED TO. IF THAT IS THE CURRENT TCB, THE NEW DEB ADDRESS  *
*        IS ADDED TO THE DEB TABLE USING THE DEBCHK-ADD MACRO.        *
*        OTHERWISE THE ENTRY IN THE DEB TABLE POINTING TO THE OLD DEB *
*        IS  REPLACED WITH THE ADDRESS OF THE NEW DEB.                *
         EJECT
*        6) AFTER BUILDING THE DEB, IF A NEW VOLUME IS NOW BEING      *
*        PROCESSED THE USER HEADER LABEL MODULE, IFG0555J, IS GIVEN   *
*        CONTROL IF USER LABELS PROCESSING IS REQUIRED.               *
*                                                                     *
*        7) UPON RETURN FROM USER LABEL PROCESSING, OR IF IT IS NOT   *
*        REQUIRED, THE DEBDCBAD IN THE NEW DEB IS POINTED TO THE USER *
*        DCB, AND THE EOV ACCESS METHOD EXECUTOR, IFG0551L, IS GIVEN  *
*        CONTROL UNLESS THE DCB IS USING THE EXCP ACCESS METHOD. IN   *
*        THIS CASE THE USER DCB IS REFRESHED FROM THE COPY DCB, THE   *
*        EOV WORKAREA IS FREED AND THE CALLER OF EOV IS RETURNED TO.  *
*                                                                     *
* NOTES                                                               *
*        1) IF THE DEB IS NOT CHAINED OFF THE CURRENT TCB'S DEB       *
*        CHAIN, THE DEBCHK-DELETE, DEBCHK-ADD LOGIC IS AVOIDED        *
*        BECAUSE DEBCHK-ADD REQUIRES THE DEB TO BE CHAINED OFF THE    *
*        CURRENT TCB'S DEB CHAIN.                                     *
*                                                                     *
*        2) A DIRECTED FREEMAIN/GETMAIN IS USED TO FREE THE OLD DEB   *
*        AND OBTAIN CORE FOR THE NEW DEB SO THE CORE IS ATTACHED TO   *
*        THE TCB UNDER WHICH THE DEB WAS OPENED AND WILL BE FREED     *
*        DURING CLOSE. THIS TCB'S ADDRESS IS IN DEBTCBAD.             *
*                                                                     *
*        3) IF THE EXTENT IS ON ONE OF THE  MSS SUBSYSTEM'S           *
*        VIRTUAL DASD PACKS, AN ICBACREL MACRO (SVC 126) IS           *
*        ISSUED TO GET STAGING SPACE FOR THE EXTENT.  STAGING         *
*        WILL BE INHIBITED IF THE EXTENT IS ALLOCATED IN CYL-         *
*        INDERS.  THE PARAMETER LIST AREA FOR THIS MACRO IS           *
*        GOTTEN AND FREED VIA THE IECRES MACRO.  DXCCW2+4 WILL        *
*        SAVE THE POINTER TO THIS LIST.                               *
*                                                                     *
*    DEPENDENCIES =                                                   *
*            CLASS ONE CHARACTER CODE.  THE EBCDIC CHARACTER CODE     *
*            WAS USED FOR ASSEMBLY.  THE MODULE MUST BE REASSEMBLED   *
*            IF A DIFFERENT CHARACTER SET IS USED FOR EXECUTION.      *
*                                                                     *
*    RESTRICTIONS = NONE                                              *
*                                                                     *
*    REGISTER CONVENTIONS =                                           *
*            R2 POINTS TO DCB                                         *
*            R4 POINTS TO OPEN WORK AREA                              *
*            R5 POINTS TO THE RESIDENT ROUTINE                        *
*            R6 POINTS TO THE WTG TABLE                               *
*            R8 POINTS TO THE CURRENT WTG TABLE ENTRY                 *
*            R9 POINTS TO THE DD ENTRY IN THE TIOT                    *
*            R10 POINTS TO THE UCB                                    *
*            R11 POINTS TO THE DEB.                                   *
*                                                                     *
*    PATCH LABEL = SEE NEXT TO LAST LABEL BEFORE ORG STATEMENT AT END *
*                  OF LISTING.                                        *
         EJECT
* MODULE TYPE = CONTROL (OPEN, CLOSE, EOV DATA MANAGEMENT)            *
*                                                                     *
*    PROCESSOR = ASSEMBLER XF                                         *
*                                                                     *
*    MODULE SIZE = SEE EXTERNAL SYMBOL DICTIONARY OR LOC FIELD ON     *
*                  ORG STATEMENT AT END OF LISTING                    *
*                                                                     *
*    ATTRIBUTES = REENTRANT, REFRESHABLE,READ-ONLY, ENABLED,          *
*                  PRIVILEGED, SUPERVISOR STATE, DATA MANAGEMENT KEY, *
*                  LINK PACK AREA RESIDENT/PAGEABLE                   *
*                                                                     *
* ENTRY POINT = IFG0555H FROM:                                        *
*                 IFG0554C MOUNT VERIFY                               *
*                 IFG0554P DA OUTPUT                                  *
*                 IFG0555J DA OUTPUT USER HEADER LABELS               *
*                                                                     *
*    PURPOSE = SEE FUNCTION                                           *
*                                                                     *
*    LINKAGE =                                                        *
*        THIS MODULE IS GIVEN CONTROL THROUGH THE IECRES-LOAD MACRO   *
         EJECT
* INPUT =                                                             *
*        ENTERED IN PROTECT KEY 5.                                    *
*        THE DEB'S DEBDCBAD POINTS TO THE COPIED DCB.                 *
*        REGISTER 2 POINTS TO THE COPIED DCB                          *
*        REGISTER 4 POINTS TO THE EOV WORKAREA                        *
*        REGISTER 10 CONTAINS THE CURRENT UCB ADDRESS                 *
*        REGISTER 14 CONTAINS A BRANCH TABLE OFFSET -                 *
*              0 - RETURN FROM USER LABEL MODULE IFG0555J             *
*              4 - ENTRY FROM IFG0554C AFTER MOUNTING A NEW VOLUME    *
*              16 - ENTRY FROM IFG0554P AFTER EXTENDING ON THE        *
*              CURRENT VOLUME.                                        *
*                                                                     *
* OUTPUT =                                                            *
*        A NEW DEB IS BUILT, CHAINED TO THE TCB, AND IN THE DEB TABLE *
*                                                                     *
*        IFG0555J                                                     *
*              CONTROL IS PASSED IN PROTECT KEY 5.                    *
*              THE DEBDCBAD POINTS TO THE COPIED DCB.                 *
*              REGISTER 2 POINTS TO THE COPIED DCB.                   *
*              REGISTER 4 POINTS TO THE EOV WORKAREA                  *
*              DXRETMOD INDICATES IFG0555H IS TO BE RETURNED CONTROL  *
*              REGISTER 12 CONTAINS THE USER EXIT ENTRY FROM THE DCB  *
*              EXIT LIST, OR ZEROS IF USER LABELS ARE NOT SPECIFIED   *
*              IN THE JFCB BUT A USER LABEL TRACK IS ALLOCATED.       *
*        IFG0551L                                                     *
*              CONTROL IS PASSED IN PROTECT KEY 5.                    *
*              REGISTER 2 POINTS TO THE COPIED DCB.                   *
*              REGISTER 4 POINTS TO THE EOV WORKAREA                  *
*        EXIT TO CALLER                                               *
*              USER DCB REFRESHED FROM COPY                           *
*              DEB POINTS TO USER DCB                                 *
*              REGISTER 15 = 0                                        *
         EJECT
* EXIT-NORMAL =                                                       *
*        IFG0555J DA OUTPUT USER HEADER LABELS                        *
*        IFG0551L BSAM/QSAM END OF VOLUME EXECUTOR                    *
*        CALLER OF EOV                                                *
*                                                                     *
* EXIT-ERROR =                                                        *
*        IFG0550P PROBLEM DETERMINATION WITH INTERNAL ABEND CODE      *
*        IN REGISTER 0 -                                              *
*              199 - CAUSING 437-0C ABEND, DEB NOT IN DEB TABLE       *
*              111 - CAUSING 737-34 ABEND, ACQUIRE FAILED             *
*                                                                     *
*        DMABCOND PCK IF PAGE FREE RETURN CODE                   YM5929
*                                                                     *
* EXTERNAL REFERENCES = SEE BELOW                                     *
*                                                                     *
*    ROUTINES =                                                       *
*        IFG019RA TO TRANSFER CONTROL THROUGH IECRES MACRO            *
*                                                                     *
*        IEAVPSIB VIA CVTVPSIB TO PAGE FREE DEB                  YM5929
*        R0 = 0 INDICATING NO ECB                                YM5929
*        R1 = X'20', ADDR OF DEB PREFIX: START OF PAGE FREE      YM5929
*        R2 = ADDR OF LAST BYTE OF DEB + 1: END OF PAGE FREE     YM5929
*        R4 = ADDRESS OF TCB FROM DEBTCBAD                       YM5929
*                                                                     *
*    DATA AREAS =                                                     *
*        NONE                                                         *
*                                                                     *
*    CONTROL BLOCK =                                                  *
*        DEB                                                          *
*        DCB                                                          *
*        TCB                                                          *
*        CVT                                                          *
*                                                                     *
* TABLES = EOV WORK AREA                                              *
*                                                                     *
* MACROS = DEBCHK, MODESET, SETLOCK, IECRES, IECDSECS, ICBACREL@Y30LSBS
*                                                                     *
* CHANGE ACTIVITY =                                                   *
*        SEE CHANGES/DELETIONS SECTION JUST AFTER CSECT CARD          *
*                                                                     *
***********************************************************************
         EJECT
*                                                                     *
ERR199   EQU   199                      INTERNAL ABEND CODE      Y02082
         BALR  RBASE,0                  ESTABLISH ADDRESSABILITY
         USING FORCORE,RCORE
         USING WTG,RWTG                 BASE FOR WTG TABLE       Y02080
         USING UCB,RUCB
         USING DEBBASIC,RDEB                                     YM1272
         USING DCBRELAD,RDCB
         USING TIOENTRY,RTIOT           TIOT ENTRY USING         YM3116
         USING *,RBASE
*
*        DETERMINE ENTRY CONDITION
*
         B     EDO22700(RET)            BRANCH TABLE
*
EDO22700 EQU   *                        BRANCH TABLE
         B     EDO24800                 RETURNING FROM UL PROCESSING
         NOP   EDO22700                 NO ENTRY OFFSET 4        YM1193
         B     EDO22710                 ENTRY FROM NEW VOL MNT   Y02134
         B     EDO22720                 ENTRY FROM IFG0554P      Y02134
*                                                                Y02134
EDO22710 EQU   *                        ENTRY FROM NEW VOL MNT   Y02134
*                                                                Y02134
         L     RDEB,DCBDEBAD            LOAD DEB ADDRESS         Y02134
         LA    R8,K0(,RDEB)             DEB ADDRESS TO R8      @ZA13538
         SH    R8,MLCN10                POINT TO DEB PREFIX    @ZA13538
         MVC   DEB+K3-DEB(K5,R8),DXCCW7 CCHHR OF DSCB TO DEB   @ZA13538
         LA    RD,K4                    SET NEW VOL INDICATOR    Y02134
         B     EDO22730                 BRANCH TO PROCESS        Y02134
*                                                                Y02134
EDO22720 EQU   *                        ENTRY FROM IFG0554P      Y02134
*                                                                Y02134
         LA    RD,K1                    SET OLD VOL INDICATOR    Y02134
         EJECT
EDO22730 EQU   *                        PROCESS DEB              Y02134
         OI    DXATALL,DXATFC           FORCE CLOSE ON ERROR     YM7099
*                                                                Y02134
         MVC   DXCCW6(K1),JFCBLTYP      SAVE LBL TYPE FOR UL     Y02134
         LA    RDEB,DEBBASIC            CLEAR HIGH BYTE          YM5929
         L     RF,DEBAPPAD              DEB AVT ADDR             YM5929
         SH    RDEB,MLCN36              POSITION AT APPENDAGES   Y02134
         TM    DEBSIOAB-DEBAVT(RF),X10  IS DEB FIXED             YM5929
         BZ    EDO22735                 BRANCH IF NOT            YM5929
         XR    RF,RF                    ZERO FOR IC              YM5929
         IC    RF,DEBLNGTH-DEBAVT(RDEB) LENGTH IN DWORDS OF DEB  YM5929
         SLL   RF,K3                    LENGTH IN BYTES OF DEB   YM5929
         AR    RF,RDEB                  END OF DEB + 1           YM5929
         LR    R1,RDEB                  START OF DEB             YM5929
*        PGFREE R,A=(1),EA=(15)         PAGE FREE DEB            YM5929
         LA    R2,K0(,RF)               END ADDRESS OF PGFREE    YM5929
         L     RF,DXXPREFX              PREFIX ADDRESS           YM5929
         STM   RDEB,RET,IECREGSV-IECPREFX(RF) SAVE SETLOCK REGS  YM5929
         MODESET EXTKEY=SUPR            SETLOCK, PGFREE KEY      YM5929
EDO22731 SETLOCK OBTAIN,TYPE=LOCAL,MODE=UNCOND,                  YM5929*
               RELATED=(LOCAL,IFG0555H(EDO22732)) GET LOCAL LOCK YM5929
         LM    RDEB,RET,IECREGSV-IECPREFX(RF) RESTORE REGS       YM5929
         EJECT
         XR    R0,R0                    NO ECB                   YM5929
         ICM   R1,B'1000',=AL1(X20)     PGFREE BIT               YM5929
         LR    RDEB,RCORE               SAVE RCORE               YM5929
         L     RCORE,DEBTCBAD-DEB(,R1)  PGFREE BY TCB            YM5929
         L     RF,CVTPTR                CVT                      YM5929
         L     RF,CVTVPSIB-CVT(,RF)     IEAVPSIB ENTRY           YM5929
         BALR  RET,RF                   REGS 0-14 UNCHANGED      YM5929
         LTR   RF,RF                    ERROR IN PGFREE          YM5929
         BNZ   EDO25300                 BRANCH IF YES            YM5929
         LR    RCORE,RDEB               RESTORE RCORE            YM5929
         L     RDCB,DXPDCBAD            RESTORE DCB ADDRESS      YM5929
         L     RDEB,DCBDEBAD            RESTORE DEB ADDRESS      YM5929
         L     RF,DEBAPPAD              DEB AVT ADDR             YM5929
         NI    DEBSIOAB-DEBAVT(RF),K255-X10 ZERO DEB FIXED BIT   YM5929
         LA    RDEB,DEBBASIC            CLEAR HIGH BYTE          YM5929
         SH    RDEB,MLCN36              POSITION AT APPENDAGES   YM5929
         L     RF,DXXPREFX              PREFIX ADDRESS           YM5929
         STM   RDEB,RET,IECREGSV-IECPREFX(RF) SAVE SETLOCK REGS  YM5929
EDO22732 SETLOCK RELEASE,TYPE=LOCAL,                             YM5929*
               RELATED=(LOCAL,IFG0555H(EDO22731)) RELEASE LOCK   YM5929
         LM    RDEB,RET,IECREGSV-IECPREFX(RF) RESTORE REGS       YM5929
         MODESET EXTKEY=DATAMGT         RETURN TO D M KEY        YM5929
EDO22735 EQU   *                        MOVE DEB TO WORKAREA     YM5929
         MVC   JFCBDSNM(K74),DEBBASIC   MOVE DEB-APPENDAGE,OPEN  YM1272
*                                       PARAM,AND BASIC SECTS TO Y02134
*                                       WKAREA-ON TOP OF JFCB    Y02134
         LR    RC,RDEB                  DEB ADDR FOR FREEMAIN    Y02134
         AH    RDEB,MLCN36              RESET TO BASIC SECT      Y02134
         SR    R1,R1                    ZERO R1                  Y02134
         IC    R1,DEBNMEXT              NO. OF EXTENTS IN OLDDEB Y02134
         SLL   R1,K4                    MPY BY 16                Y02134
         SH    R1,MLCN6                 SUBTRACT 6 FOR MODIF,UCB Y02134
*                                           ADDR, AND BB         Y02134
         AR    R1,RC                    BUMP FWA PTR (APP.ADDR)  Y02134
         SR    R7,R7                    ZERO R7                  Y02134
         IC    R7,DEBNMSUB              NUMBER OF SUBROUT.       Y02134
         SLL   R7,K1                    MPY X 2                  Y02134
         SR    RF,RF                    CLEAR WORK REGISTER      Y02134
         IC    RF,DEBAMLNG              LENGTH FOR MEMBER NAME,  Y02134
         AR    R7,RF                    VOL SEQ AND NUM OF VOLS  Y02134
         ST    R7,DXCCW3                SAVE FOR LATER           Y02134
         LTR   R7,R7                    TEST IF NO BYTES TO MOVE A40437
         BZ    EDO22740                 BRANCH IF NO BYTES       A40437
         BCTR  R7,0                     SUB ONE FOR EXECUTE      Y02134
         EX    R7,EDO25650              MOVE SUBRTINES,VOLSQ,NO. Y02134
*                                           VOLS TO SAVE AREA    Y02134
         EJECT
*
*        PUT NEW EXTENT NUMBER AT DXCCW7+4 -                     Y02134
*        ZERO IF EXTENDED ON NEW VOLUME OR                       Y02134
*              FIRST EXTENT OF DATA SET JUST NOW OBTAINED        Y02134
*
EDO22740 EQU   *                        SET R7 TO SAVE OLD M     A40437
*                                                                Y02134
         XR    R7,R7                    CLEAR NO. OF EXTENTS     YM1252
*                                                                Y02134
*        UPDATE DEB VOL. SEQUENCE NUMBER                         Y02134
*                                                                Y02134
         TM    DCBMACRF,DCBMEXCP        IS ACCESS METHOD EXCP    YM1184
         BO    EDO22744                 BRANCH EXCP, ASSUME PS   YM1184
*                                                                YM1184
         NC    DCBDSORG,DCBDSORG        CHECK IF DSORG SPECIFIED YM1184
         BZ    EDO22744                 BRANCH 0, ASSUME PS      YM1184
*                                                                YM1184
         TM    DCBDSORG,DCBDSGPS        PHYSICAL SEQUENTIAL DCB  YM1184
         BNO   EDO22745                 BRANCH IF NOT PS         YM1184
*                                                                YM1184
EDO22744 EQU   *                        IF PS, SET VOLSEQ        YM1184
*                                                                YM1184
         MVC   JFCBDSNM+K75(1),DXVOLSEQ+1 MOVE IN SEQ NO.      @ZA13555
*                                                                YM1184
EDO22745 EQU   *                        CONTINUE                 YM1184
         CH    RD,MLCN1                 EXTEND ON OLD VOLUME     YM1252
         BNE   EDO22750                 BRANCH IF NEW VOLUME     Y02134
         IC    R7,DEBNMEXT              PICK UP NO. DEB EXTENTS  Y02134
         CLI   DEBNMEXT,K1              ONE EXTENT.Q             A40053
         BNE   EDO22750                 NO, CONTINUE             A40053
         LA    R1,DEBBASND              PT TO DA SECTION         YM1272
         USING DEBDASD,R1               BASE FOR DA SECTION      YM1272
         CLI   DEBSTRCC,ALLBITS         DUMMY EXTENT.Q           A40053
         BNE   EDO22750                 NO, CONTINUE             A40053
         SR    R7,R7                    YES, FIRST EXTENT DUMMY  A40053
EDO22750 EQU   *                        NEW VOLUME               Y02134
         DROP  R1                                                YM1272
         ST    R7,DXCCW7+K4             STORE NEW -M- FOR LATER  Y02134
         USING DEBDASD-(DEBBASND-DEBBASIC),RDEB                @G24LSFS
         MVC   OLDSTRCC(1),DEBSTRCC     OLD DEB CYL NUMBER     @G24LSFS
         DROP  RDEB                                            @G24LSFS
         USING DEBBASIC,RDEB                                   @G24LSFS
*                                                              @G24LSFS
*                                                              @Y30LSBS
*        ZERO DXCCW2+4 - ASSUME NO VIRTUAL  MSS DASD           @Y30LSBS
*        IF VIRTUAL DASD, GET 96 BYTES FOR PARAMETER LIST      @Y30LSBS
*        PARTIALLY FORMAT THE HEADER AND SAVE ADDR IN DXCCW2+4 @Y30LSBS
*                                                              @Y30LSBS
         XC    DXCCW2+K4(K4),DXCCW2+K4  ZERO POINTER TO LIST   @Y30LSBS
         TM    UCBTBYT2,UCBRVDEV        IS OLD VOL VIRT DASD   @Y30LSBS
         BNO   EDO22753                 NO, NOTHING TO DO THEN @Y30LSBS
         TM    DCBDSORG,DCBDSGIS        IS IT AN ISAM D.S.     @Y30LSBS
         BO    EDO22753                 ISAM CYLINDER FAULTS   @Y30LSBS
         IECRES GET,PREFIX=YES,ID=MSSH,LV=ACQPLN,              @Y30LSBS.
               STM=(2,14,WTGPREFX)                             @Y30LSBS
         USING ACQLST,R1                ACQUIRE ADDRESSABILITY @Y30LSBS
         ST    R1,DXCCW2+K4             SAVE POINTER TO CORE   @Y30LSBS
         LA    RF,AREXT                 POINT TO START OF ENTS @Y30LSBS
         ST    RF,ARECB                 SAVE IT IN ECB AREA    @Y30LSBS
         DROP  R1                                              @Y30LSBS
EDO22753 EQU   *                                               @Y30LSBS
*                                                              @Y30LSBS
         EJECT
*
*              DELETE OLD DEB FROM THE DEB TABLE
*
         L     R1,DEBTCBAD              TCB ADDRESS              YM2133
         LA    R1,K0(R1)                ZERO HIGH ORDER BYTE     YM2133
         L     RF,DXTCBADR              CURRENT TCB ADDR         Y02082
         LA    RF,K0(RF)                ZERO HIGH ORDER BYTE     YM2133
         CLR   R1,RF                    IS DEB ON CURRENT TCB    YM2133
         BNE   EDO22755                 NO - BYPASS DEBCHK DLTE  Y02134
         L     R1,DXPDCBAD              GET PTR TO COPIED DCB    Y02082
         DEBCHK (1),TYPE=DELETE         DELETE DEB               Y02082
         B     EDO22780                 BYPASS DEBTABLE SEARCH   YM2133
         EJECT
*                                                                YM2133
*        SEARCH DEBTABLE FOR OLD DEB ADDRESS                     YM2133
*                                                                YM2133
EDO22755 EQU   *                        GET TABLE OFFSET         Y02134
         USING TCB,R1                   R1 CONTAINS TCB ADDR     YM2133
         L     RC,TCBJSCB               ADDR OF JOB STEP JSCB    YM2133
         DROP  R1                                                YM2133
         L     R1,DCBDEBAD              GET ADDRESS OF DEB       Y02082
         LA    R1,K0(,R1)               CLEAR HIGH ORDER BYTE    Y02082
         BCTR  R1,R0                    POINT TO                 YM2133
         BCTR  R1,R0                    DEBTBLOF FLD IN DEB PFX  YM2133
         LH    R1,K0(R1)                GET DEB TABLE OFFSET     Y02080
         SLL   R1,K2                    MPY BY ENTRY LENGTH      YM2133
         ST    R1,DXCCW4+K4             SAVE DEB TABLE OFFSET    YM1387
         L     RF,DXXPREFX              GET PREFIX               Y02080
         STM   R0,RF,IECREGSV-IECPREFX(RF)  SAVE REGS            Y02080
         MODESET  EXTKEY=ZERO           KEY 0                    Y02080
EDO22758 SETLOCK OBTAIN,TYPE=LOCAL,MODE=UNCOND,                  Y02080X
               RELATED=(LOCAL,IFG055H(EDO22765,EDO23000))        Y02080
         MODESET  EXTKEY=DATAMGT        DATA MANAGEMENT KEY      Y02080
         LM    R0,RF,IECREGSV-IECPREFX(RF)   RESTORE REGS        Y02080
EDO22760 EQU   *                        DEBTABLE CHECK           YM2133
         USING IEZJSCB,RC               ADDRESSABILITY FOR JSCB  YM2133
         L     R7,JSCBDBTB              DEB TABLE ADDRESS        YM2133
         LA    RF,K0(R1,R7)             POINT TO ENTRY           YM2133
         CLM   RDEB,B'0111',K1(RF)      CORRECT ENTRY            Y02082
         BE    EDO22770                 YES - CONTINUE           YM2133
         EJECT
*        DEB NOT IN DEB TABLE - RELEASE LOCAL LOCK AND
*        CALL PROBLEM DETERMINATION
*
         L     RF,DXXPREFX              GET PREFIX               Y02080
         STM   R0,RF,IECREGSV-IECPREFX(RF)  SAVE REGS            Y02080
         MODESET  EXTKEY=ZERO           KEY 0                    Y02080
EDO22765 SETLOCK RELEASE,TYPE=LOCAL,                             Y02080X
               RELATED=(LOCAL,IFG0555H(EDO22758))                Y02080
         MODESET  EXTKEY=DATAMGT        DATA MANAGEMENT KEY      Y02080
         LM    R0,RF,IECREGSV-IECPREFX(RF)  RESTORE REGS         YM3116
*
*        REREAD JFCB TO REFRESH WORKAREA OVERLAID BY DEB         YM3116
*
         L     RTIOT,DXTIOTAD           GET TIOT ENTRY ADDRESS   YM3116
         LA    RF,TIOEJFCB              GET JFCB ADDRESS ADDRESS YM3116
         IECRES LOCJFCB,(RF)            LOCATE JFCB              YM3116
         MVC   DXJBF(JFCBLGTH),INFMJFCB-INFMJFCB(RF) MOVE JFCB   YM3116
*
         LA    RWTGC,DXXENTRY           SET RWTGC FOR RES RTN    YM2133
         LA    RWTG,DXXAREA             POINT TO EOV WTG         YM2133
         DMABCOND ERR199,MODPD5H        LOAD ERROR CODE          YM2133
         EJECT
*                                                                Y02080
*        SETUP TO UNCHAIN DEB FROM TCB AND FREE DEB STORAGE
*
EDO22770 EQU   *                        CONTINUE                 YM2133
         MODESET EXTKEY=ZERO            DEB TABLE                Y02082
         OI    K3(RF),K1                INVALIDATE OLD ENTRY     YM2133
         LA    RC,DEBBASIC              DEB ADDRESS              YM1272
         LA    RET,K36                  DEB PREFIX LENGTH        YM2133
         SLR   RC,RET                   POINT TO DEB PREFIX      YM2133
         B     EDO22790                 BRANCH IN KEY 0 TO       Y02082
*                                       UNCHAIN DEB - LOCK HELD  Y02082
         SPACE 2
EDO22780 EQU   *                        GET LOCK TO UNCHAIN DEB  YM2133
         L     RF,WTGPREFX              PT TO PREFIX             Y02082
         USING IECPREFX,RF                                       Y02082
         STM   R0,RET,IECREGSV          SAVE REGS THRU LOCK      Y02082
*        OBTAIN LOCAL LOCK                                       Y02082
         MODESET EXTKEY=ZERO            KEY FOR SETLOCK, DEB CHN Y02082
EDO22785 SETLOCK OBTAIN,TYPE=LOCAL,MODE=UNCOND,                  Y02082*
               RELATED=(LOCAL,IFG0555H(EDO23000))                Y02082
         LM    R0,RET,IECREGSV          RESTORE REGS             Y02082
         EJECT
EDO22790 EQU   *                        REMOVE DEB FROM CHAIN    Y02082
         MODESET EXTKEY=DATAMGT         RETURN TO DM KEY         Y02082
         LR    R7,RDEB                  PRIME REG 7              Y02082
         L     R1,DEBTCBAD              PICK UP TCB ADDR.
         LA    R8,K0(R1)                SAVE TCB ADDR& CLEAR HIBYTE
         LA    R1,K4(,R1)
EDO22800 EQU   *                        CHECK NEXT DEB
         CLC   DCBDEBAD+K1(K3),DEBDEBAD+K1-DEBBASIC(R1) R1=ADDR  YM1482
*                                       OF PREV DEB/TCB          YM1482
         BE    EDO22900                 CONTINUE
         L     R1,DEBDEBAD-DEBBASIC(,R1) R1=ADDR OF NEXT DEB     YM1482
         B     EDO22800                 GO CK AGAIN
EDO22900 EQU   *                        CONTINUE
         L     RET,DEBDEBAD             PT TO NEXT DEB           Y02082
         ICM   RET,K8,DEBDEBAD-DEBBASIC(R1) PRIME HI BYTE        YM1482
         ICM   R7,K8,DEBDEBAD-DEBBASIC(R1)                       YM1482
         MODESET EXTKEY=ZERO            KEY FOR SETLOCK, DEB CHN Y02082
         CS    R7,RET,DEBDEBAD-DEBBASIC(R1) DECHAIN DEB          YM1482
         MODESET EXTKEY=DATAMGT         RETURN TO DM KEY         Y02082
         BNZ   EDO22790                 BR IF NOT SUCCESSFUL     Y02082
         OI    DXATEOV,DXATDCHN         SHOW DEB DECHAINED       Y02144
         EJECT
*
*        BRANCH ENTER FREEMAIN TO FREE OLD DEB STORAGE
*
         SR    RET,RET                  CLEAR LENGTH             Y02080
         IC    RET,DEB+K32-DEB(,RC)     DEB LENGTH IN DUBL WDS.  Y02080
         SLL   RET,K3                   MPY X 8                  Y02080
         L     RF,DXXPREFX              EOV WORKAREA PREFIX      Y02082
         STM   R0,RET,IECREGSV          SAVE REGS FOR FREEMAIN   Y02082
         LR    RA,RF                    SAVE PREFIX ADDR         Y02082
         L     R7,DXASCBAD              ASCB ADDR FOR FREEMAIN   Y02082
         L     RCORE,DEBTCBAD           TCB ADDR FROM DEB        Y02082
         LA    RCORE,K0(,RCORE)         CLEAR HIGH BYTE          Y02082
         LA    R1,K0(,RC)               CLEAR HIHG BYTE OF DEBAD Y02082
         MODESET EXTKEY=SUPR            FREEMAIN KEY             Y02082
         FREEMAIN RU,LV=(RET),A=(1),SP=230,KEY=5,BRANCH=YES      Y02082*
                                        FREE DEB DIRECTED TO TCB Y02082
         MODESET EXTKEY=DATAMGT         RETURN TO D.M. KEY       Y02082
         LM    R0,RET,IECREGSV-IECPREFX(RA) RESTORE REGS         Y02082
         OI    DXATEOV,DXATDFRE         SHOW DEB FREED           Y02144
         EJECT
*
*        CALCULATE SIZE OF NEW DEB
*
         SR    RET,RET                  ZERO REG.14
         IC    RET,DSCNOEXT             NO.EXT IN NEW DSCB
         STC   RET,JFCBDSNM+K52         SAVE IN DEBNMEXT
         SLL   RET,K4                   MPY X 16
         LR    RC,RET                   SAVE IN RC
         L     R7,DXCCW3                PICK UP NO. SUBRT.BYTES
         AR    RET,R7                   ADD NO.SUBR BYTES(+4)
         LA    R0,K68(,RET)             ADD 68 FOR BASIC,APP,OPN Y02080
         ST    R0,DXCCW3+K4                                      Y02080
*
*****    GET CORE FOR NEW DEB  *****        PARAMETERS
*
         SPACE 1
         L     RF,DXXPREFX              PREFIX ADDR              Y02082
         STM   R0,RET,IECREGSV          SAVE REGS FOR GETMAIN,   Y02082
*                                       AND SETLOCK              Y02082
         LR    RA,RF                    SAVE PREFIX ADDR IN RA   Y02082
         DROP  RF                                                Y02082
         L     R7,DXASCBAD              ASCB ADDR FOR GETMAIN    Y02082
         L     RCORE,JFCBDSNM+K36+(DEBTCBAD-DEBBASIC) TCB ADDR   YM3873
*                                       IN OLD DEB SAVED IN W/A  YM3873
         LA    RCORE,K0(,RCORE)         CLEAR HIGH BYTE          Y02082
         MODESET EXTKEY=SUPR            GETMAIN KEY              Y02082
         GETMAIN RU,LV=(0),SP=230,KEY=5,BRANCH=YES GETMAIN DEB   Y02082
         EJECT
         MODESET EXTKEY=SUPR            SETLOCK KEY              Y02082
EDO23000 SETLOCK RELEASE,TYPE=LOCAL,    RELEASE LOCAL LOCK       Y02082*
               RELATED=(LOCAL,IFG0555H(EDO22785,EDO22758))       Y02082
         LM    R2,RET,IECREGSV-IECPREFX+R2*K4(RA) RESTORE REGS   Y02082
         OI    DXATEOV,DXATDNEW         SHOW NEW DEB CORE GOTTEN Y02144
         LR    RDEB,R1                  SET UP NEW DEB POINTER
*
*        MOVE DEB DATA FROM WKAREA TO NEW DEB CORE
*
         MVC   DEBBASIC(K74),JFCBDSNM   MOVE BASIC,APP,OPEN PARM YM1272
*        DEBMODIF, UCB ADDRESS, DEB EXTENSION PTR, AND BB        Y02134
         AR    RC,RDEB                  ADD NO. EXTENT BYTES TO DEB
*                                           APPENDAGE ADDRESS
         SH    RC,MLCN6                 SUBTRACT 6-MOD.,UCBAD,BB
*                                           ARE ALREADY MOVED
         LTR   R7,R7                    TEST IF NO BYTES TO MOVE A40437
         BZ    EDO23050                 BRANCH IF NO BYTES       A40437
         BCTR  R7,0                     SUBTRACT ONE FOR EXECUTE
         EX    R7,EDO25600              R7=NO.SUBR BYTES+4
*
***      MOVE SUBROUTINES TO NEW DEB  ***
*
EDO23050 EQU   *                        NO BYTES TO MOVE         A40437
         XC    JFCBDSNM+K72(K2),JFCBDSNM+K72  YES, ZERO BB
         ST    RUCB,DXCCW10             UCB ADDR TO HOLD
         MVC   JFCBDSNM+K69(K3),DXCCW10+K1  MOVE TO NEW DEB AREA
EDO23100 EQU   *                        APPENDAGE
         ST    RDEB,DXCCW10+K4          DEB-USE FOR APPENDAGE ADDR
         MVC   DEBAPPAD+K37(K3),DXCCW10+K5  TO APPENDADR-+36 BECAUSE
*                                       OF RDEB SETTING
*
*        COMPUTE DEB LENGTH IN DOUBLE WORDS AND INSERT
*        IN DEB LENGTH FIELD
*
         L     RF,DXCCW3+K4             NO. OF GOT CORE BYTES
         LA    RF,K7(,RF)               SET RF =DEB CORE LENGTH+7
         SRL   RF,K3                    DIVIDE BY EIGHT
         STC   RF,DEBBASIC+K32          STORE DEB LTH IN DW      YM1272
         EJECT
*
*        SET DEB REGISTER TO POINT TO BASIC SECTION OF DEB
*
         LA    RDEB,K36(,RDEB)          ESTABLISH DEB POINTER
         ST    RDEB,DXCCW11+K4          SAVE DEB ADDRESS
         L     R7,DXDEBXAD              GET DEB EXTENSION ADDR   Y02134
         USING DEBXTN,R7                                         Y02134
         ST    RDEB,DEBXDBPR            PUT DEB ADDR IN DEBX     Y02134
         DROP  R7                                                Y02134
         MVC   DCBDEBAD+K1(K3),DXCCW11+K5  STORE IN DCB
*
******   INSERT DSCB EXTENTS INTO NEW DEB  ******
*
         LR    R7,RCORE                 R7 SET AS CORE REGISTER
         ST    R7,DXWORK1               SAVE PTR TO F1           Y02134
         LA    R7,K50+K7(,R7)           ADD HEX 39 TO POS AT EXTNT
         LA    R8,DEBBASND              PT TO DA SECTION         YM1272
         USING DEBDASD,R8               BASE TO DA SECTION       YM1272
         SR    RWTG,RWTG
         STC   RWTG,DXCCW5              SET-M-TO ZERO
*
         MVC   DXCCW6+K1(K6),DSCEXTYP   SAVE ANY UL EXTENT INFO  M1784
         CLI   DSCEXTYP,ULEXTYP         IS FIRST EXTENT A UL EXT M1784
         BE    EDO23700                 BR IF YES TO SKIP UL EXT M1784
*
EDO23200 EQU   *                        BUILD AN EXTENT
         MVC   DEBDVMOD(K6),JFCBDSNM+K68 MOVE IN M,UCBAD AND BB  YM1272
         MVI   DEBDVMOD,X10             PERMIT SEEK HH COMMANDS  YM1272
         CLI   DSCBEXTY-DXDSCB(R7),X80+X01 CYLINDER ALLOCATION   Y02134
         BE    EDO23300                 YES,FILE MASK ALREADY SET UP
         OI    DEBDVMOD,DEBOFDCB        SET TO INHIBIT ALL       YM1272*
                                        SEEK COMMANDS
*
*                   FILE MASK SETTING NOW COMPLETE
*
         EJECT
EDO23300 EQU   *                        CHECK IF DCBFDAD UPDATE NOW
         MVC   DEBSTRCC(K8),DSCBLLMT-DXDSCB(R7) LOWER EXT        YM1272
*
*              MOVE -M- AND STARTING BBCCHH TO DEB     ******
*
         L     RC,DXCCW7+K4             PICK UP OLD DEB NOEXT OR 0
         CR    RC,RWTG                  IS THIS THE CCHH FOR DCB
         BNE   EDO23600                 NO,TRANSFER
         TM    DCBMACRF,DCBMEXCP        YES,CHECK FOR EXCP DCB
         BZ    EDO23500                 BR IF NO TO FULL DISK ADDR
         TM    DCBMACRF+K1,DEPEND       DOES DEV.DEP.SECT EXIST
         BZ    EDO23600                 NO,DONT UPDATE ADDR
EDO23500 EQU   *                        NO EXCP DCB
         STC   RC,DCBFDAD               STORE M- ZERO FOR NEW VOL.
*                                       NO.OLD DEB EXTNTS FOR SAME
         MVC   DCBFDAD+K1(K6),DEBBINUM  STARTING BBCCHH TO DCB   YM1272
         MVI   DCBFDAD+K7,K0            SET R = 0
*
*        COMPUTE NUMBER OF TRACKS
*
EDO23600 EQU   *                        DON'T UPDATE ADDR
         ST    RDCB,DXCCW7              SAVE DCB PIINTER
         STM   RTIOT,RD,DXCCW8          SAVE REGS.
         L     RF,CVTPTR                ADDR OF COMM VECTOR TABLE
         L     RF,CVTPRLTV-CVT(,RF)     CONVERT ROUTINE ADDR MBB-TTR
         LA    R1,DXDEB                 LOAD ADDR. OF DUMMY DEB
         LA    RDCB,DXCCW5              LOAD ADDR OF MBBCCHHR
         MVI   DXCCW5,K0                STORE-M- IN DUMMY AREA
         LA    RWTG,K1(,RWTG)
         MVC   DXCCW5+K1(K2),JFCBDSNM+K72  STORE BB IN INPUT PARAM.
         MVC   DXCCW5+K3(K4),DSCBLLMT-FORCORE(R7)  MOVE LOWER CCHH
         MVI   DXCCW5+K7,K0             ZERO R
*
****     CONVERT LOWER MBBCCHHR TO TTR  ***
*
         BALR  RET,RF                   GO TO CONVERT RTN
         ST    R0,DXCCW4                SAVE CONVERSION RESULT(TTR)
         EJECT
*
****     NOW CONVERT UPPER EXTENT BOUNDARY ***
*
         L     RF,CVTPTR                ADDR OF COMM VECTOR TABLE
         MVC   DXCCW5+K3(K4),DSCBULMT-DXDSCB(R7) UPPER EXTENT    Y02134
         L     RF,CVTPRLTV-CVT(,RF)     CONVERT RTN ADDR  MBB-TTR
         BALR  RET,RF                   BRANCH TO CONVERSION ROUT.
         L     RDCB,DXCCW7              RESTORE DCB PTR.
         LM    RTIOT,RD,DXCCW8
         S     R0,DXCCW4                SUBTRACT LO FROM HI TTR
         SRL   R0,K16                   RIGHT JUSTIFY
         LR    R1,R0
         LA    R1,K1(,R1)               ADD ONE
         STH   R1,DEBNMTRK              NO. TRKS TO DEB EXTENT   YM1272
         SR    R1,R1                    ZERO RF
         IC    R1,DEBNMEXT              NUMBER EXTENTS IN NEW DEB
         CR    R1,RWTG                  CHECK FOR LAST EXTENT
         BE    EDO24100                 YES,GO
         LA    R8,K16(,R8)              BUMP PTR TO DEB EXTENTS
         DROP  R8                                                YM1272
*
EDO23700 EQU   *                        SKIP USL EXT
         LA    R7,K10(,R7)              ADD 10 TO DSCB EXTNT REG
         LA    R1,K3                    SET REG =3,NO EXTNTS IN FM1
         CLI   DXCCW6+K1,ULEXTYP        IS THERE A UL EXTENT IN  M1784
         BNE   EDO23800                 FMT 1 DSCB, BR IF NO     M1784
         BCTR  R1,K0                    UL EXTNT, ONLY 2 EXTNTS IN F1
EDO23800 EQU   *                        NO FMT 1 DSCB
         CR    R1,RWTG                  CK IF LAST EXTNT FROM F1 IN DEB
         BNE   EDO23200                 MERGE NEXT EXTENT
         L     R1,DXWORK1               LOAD DSCB PTR            Y02134
         CLI   DSCBNEXT+K4-DXDSCB(R1),K0 CHECK CCHHR OF NXT DSCB Y02134
         BE    EDO24100                 TRANSFER IF ZEROS--FINSHED
*
         MVC   DXCCW11+K2(K4),DSCLOWLM-DXDSCB(R1) CCHH FOR 555J  Y02134
*
         L     R7,CORECH(,R1)           GET FORMAT 3 PTR         Y02134
         LA    R1,L'JFCBDSNM(,R7)       BUMP PASSED KEY          Y02134
         ST    R1,DXWORK1               SAVE PTR FOR NEXT EXTENT Y02134
*                                                                Y02134
         MVC   DSCBFMID+K44-DXDSCB(K91,R7),DSCBFMID+K45-DXDSCB(R7)
*                                           LAY FORMAT ID AND FOLLOW
*                                           KEY PORTION DIRECTLY
         B     EDO23200                 GO INSERT FORMAT 3 DSCB INTO
*                                            NEW DEB
         EJECT
*
*              INCLUDE NEW DEB IN THE CHAINING
*
EDO24100 EQU   *                        USE TCB IN DEB
         L     R1,DEBTCBAD              TCB ADDR
EDO24105 EQU   *                        CHAIN NEW DEB            Y02082
         L     R7,TCBDEB-TCB(,R1)       PT TO FIRST DEB          Y02080
         MVC   DEBDEBAD+K1(K3),K9(R1)   PT TO DEB
         LA    RDEB,K0(,RDEB)           CLEAR HI-ORDER BYTE      Y02082
         MODESET EXTKEY=SUPR            TCB KEY                  Y02082
         CS    R7,RDEB,K8(R1)           CHAIN NEW DEB            Y02082
         MODESET EXTKEY=DATAMGT         KEY OF EOV               Y02082
         BNZ   EDO24105                 BR IF NOT SUCCESS        Y02082
         NI    DXATEOV,ALLBITS-DXATDCHN SHOW DEB CHAINED         Y02144
         EJECT
*                                                                YM2133
*        CHECK FOR DEB CHAINED TO CURRENT TCB                    YM2133
*                                                                YM2133
         L     RF,DXTCBADR              CURRENT TCB ADDR         Y02082
         LA    RF,K0(RF)                ZERO HIGH ORDER BYTE     YM2133
         LA    R1,K0(R1)                ZERO HIGH ORDER BYTE     YM2133
*                                                                YM2133
*        THE EOV WAS ISSUED FROM A TASK DIFFERENT THAN THE ONE   YM2133
*        IN WHICH THE DEB WAS CREATED IF THE TCB ADDRESS IN THE  YM2133
*        DEB IS NOT THE SAME AS THE CURRENT TCB ADDRESS.         YM2133
*                                                                YM2133
         CLR   R1,RF                    IS DEB ON THIS TCB       YM2133
         BNE   EDO24110                 NO - DON'T ISSUE DEBCHK  YM2133
*                                                                YM2133
*        EOV IN SAME TASK AS OPEN   -   ISSUE DEBCHK ADD         YM2133
*                                                                YM2133
         LA    R7,K3                    DEBAMTYP NEGATIVE OFFSET Y01021
         SR    RDEB,R7                  ADDRESS DEBAMTYP         Y01021
         IC    R7,K0(RDEB)              GET ACCESS METHOD TYPE   Y01021
         XC    K0(K3,RDEB),K0(RDEB)     ZERO DEBAMTYP, DEBTBLOF  Y01021
         L     R1,DXPDCBAD              DEBCHK PROTECTED DCB     Y02082
         DEBCHK (R1),TYPE=ADD,AM=((R7)) ADD DEB                  Y02082
         LR    RDEB,R1                  RELOAD DEB ADDRESS       Y01021
         B     EDO24120                 GO TO UHL PROCESSING     YM2133
         EJECT                                                   YM2133
*                                                                YM2133
*        THE FOLLOWING CODE IS EXECUTED IN LIEU OF DEBCHK        YM2133
*        WHENEVER THE DEB IS CHAINED TO A TCB DIFFERENT THAN     YM2133
*        THE CURRENT ONE.  THIS IS ACCOMPLISHED BY REPLACING     YM2133
*        THE EXISTING DEBTABLE ENTRY WITH THE NEW DEB ADDRESS.   YM2133
*                                                                YM2133
EDO24110 EQU   *                        OBTAIN LOCK FOR DEBTABLE YM2133
         L     RF,DXXPREFX                                       Y02080
         STM   R0,RF,IECREGSV-IECPREFX(RF)  SAVE REGS            Y02080
         MODESET  EXTKEY=ZERO           KEY 0                    Y02080
EDO24112 SETLOCK OBTAIN,TYPE=LOCAL,MODE=UNCOND,                  Y02080X
               RELATED=(LOCAL,IFG0555H(EDO24114))                Y02080
         MODESET  EXTKEY=DATAMGT        DATA MANAGEMENT KEY      Y02080
         LM    R0,RF,IECREGSV-IECPREFX(RF)  RESTORE REGS         Y02080
         L     RF,DXTCBADR              GET CURRENT TCB          Y02080
         USING TCB,RF                   ESTABLISH TCB ADDR       Y02080
         L     RF,TCBJSCB               GET ADDR OF JSCB         Y02080
         DROP  RF                                                Y02080
         USING IEZJSCB,RF               ESTABLISH JSCB ADDR      Y02080
         L     R1,JSCBDBTB              GET ADDR OF DEB TABL     Y02080
         DROP  RF                                                Y02080
         L     RF,DXCCW4+K4             GET SAVED OFFSET         YM1387
         LA    RDEB,K0(RDEB)            ZERO HIGH ORDER BYTE     YM2133
         MODESET EXTKEY=ZERO            DEB TABLE                Y02082
         ST    RDEB,K0(RF,R1)           PLACE NEW ADDR IN TBL    Y02080
         EJECT
         L     RF,DXXPREFX              GET PREFIX               Y02080
         STM   R0,RF,IECREGSV-IECPREFX(RF)  SAVE REGS            Y02080
EDO24114 SETLOCK RELEASE,TYPE=LOCAL,                             Y02080X
               RELATED=(LOCAL,IFG0555H(EDO24112))                Y02080
         MODESET  EXTKEY=DATAMGT        EOV KEY                  Y02082
         LM    R0,RF,IECREGSV-IECPREFX(RF)  RESTORE REGS         Y02080
EDO24120 EQU   *                        UHL PROCESSING           YM2133
         STCM  RDEB,B'0111',DCBDEBAD+K1 REFRESH DCBCOPY'S DEBAD  Y02082
*
******** END OF NEW DEB CONSTRUCTION *********
*                                                              @Y30LSBS
*        IF DXCCW2+4 POINTS TO A PARAMETER LIST, THE VOLUME IS @Y30LSBS
*         MSS VIRTUAL DASD AND STAGING SPACE MUST BE ACQUIRED. @Y30LSBS
*        THIS LIST SPACE MUST ALSO BE FREED.                   @Y30LSBS
*                                                              @Y30LSBS
         USING ACQLST,R8                ACQUIRE ADDRESSABILITY @G24LSFS
         L     R8,DXCCW2+K4             SEE IF THERE'S A LIST  @G24LSFS
         LTR   R8,R8                    IF SO,  MSS            @G24LSFS
         BZ    EDO24140                 IF NOT, NOTHING TO DO  @Y30LSBS
         LR    RC,RDEB                  RC IS DEB POINTER      @G24LSFS
         USING DEBDASD-(DEBBASND-DEBBASIC),RC                  @G24LSFS
         CLI   DEBSTRCC,ALLBITS         IS THIS A DUMMY EXTENT @G24LSFS
         BE    EDO24130                 YES, NO NEW EXTENTS    @G24LSFS
         SR    R1,R1                    CLEAR REG              @G24LSFS
         IC    R1,DEBNMEXT              DEB EXTENT NUMBER      @G24LSFS
         CH    RD,NEWVOL                ON NEW VOL             @G24LSFS
         BE    EDO24125                 YES,GET NEW EXTENTS    @G24LSFS
**                                                             @G24LSFS
**   IF OLD EXTENT WAS DUMMY,ALL EXTENTS ARE NEW***************@G24LSFS
**                                                             @G24LSFS
         CLI   OLDSTRCC,ALLBITS         WAS OLD EXTENT DUMMY   @G24LSFS
         BE    EDO24125                 YES,ALL EXTENTS NEW    @G24LSFS
         L     RF,DXCCW7+4              OLD DEB EXTENT NUMBER  @G24LSFS
         SR    R1,RF                    NUMBER OF NEW EXTENTS  @G24LSFS
         SR    RC,RC                    CLEAR DEB REG          @G24LSFS
         IC    RC,DEBEXSCL              EXTENT SCALE           @G24LSFS
         SLL   RF,0(RC)                 DISPLACEMENT TO EXTS   @G24LSFS
         LA    RC,0(RF,RDEB)            PTR TO REF NEW EXTS    @G24LSFS
EDO24125 EQU   *                        CONTINUE               @G24LSFS
         STC   R1,ARNMEXTS              EXTENT NUMBER TO PARM  @G24LSFS
         LA    RF,AREXT                 EXTENTS AREA IN PARM   @G24LSFS
         OI    ARFLAG,INHIBST           SET TO INHIBIT STAGE   @G24LSFS
EDO24126 EQU  *                         LOOP THROUGH ALL EXTS  @G24LSFS
         MVC   K0(K2,RF),DEBSTRCC       START AND END CYLINDER-@G24LSFS
         MVC   K2(K2,RF),DEBENDCC       TO ACQUIRE PARM LIST   @G24LSFS
**                                                             @G24LSFS
** IF ANY EXTENT IS NOT ON A CYLINDER BOUNDARY THEN ALL THE    @G24LSFS
** EXTENTS WILL BE STAGED.                                     @G24LSFS
**                                                             @G24LSFS
         TM    DEBDVMOD,X'18'           IS EXT NOT CYL BNDRY.  @G24LSFS
         BO    EDO24127                 YES,STAGE ALL EXTENTS  @G24LSFS
         TM    DCBMACF1,DCBMRPT1        IS POINT BEING USED    @G24LSFS
         BZ    EDO24128                 NO,DON'T STAGE IF CYL  @G24LSFS
EDO24127 EQU   *                        HERE TO RESET INHIBIT  @G24LSFS
         NI    ARFLAG,X'FF'-INHIBST     STAGE THESE EXTENTS    @G24LSFS
EDO24128 EQU   *                        CONTINUE TO INHIBIT    @G24LSFS
         DROP  RC                                              @G24LSFS
         LA    RF,K4(RF)                INCR PARM LIST PTR     @G24LSFS
         LA    RC,K16(RC)               INCR DEB EXT PTR       @G24LSFS
         BCT   R1,EDO24126              BRANCH IF MORE EXTENTS @G24LSFS
         OI    ARFLAG,ARSEC             SET SECONDARY EXTEND   @G24LSFS
         XC    ARECB(K4),ARECB          ZERO ECB ADDRESS       @Y30LSBS
         MVC   ARVOL,UCBVOLI            MOVE VOLID TO HEADER   @Y30LSBS
         MVI   ACQLST+K3,ACQPLN         SET LIST LENGTH        @Y30LSFY
         MVI   AROP,ACQOP               ACQ. OPCODE IN LIST    @Y30LSFY
         ICBACREL TYPE=ACQ,MF=(E,ACQLST)  GET STAGING SPACE    @Y30LSBS
         LA    RWTGC,DXXENTRY           SET RWTGC FOR RES RTN  @Y30LSFY
         LA    RWTG,DXXAREA             POINT TO EOV WTG       @Y30LSFY
         LTR   RF,RF                    ANY ERRORS             @Y30LSBS
         BZ    EDO24130                 NO   FREE THE CORE     @Y30LSBS
*****    ACQUIRE FAILED - ABEND                                @Y30LSFY
* NOTE: IFG0199B MAY DESTROY THE 2ND WORD OF THE PARM LIST ON ABEND
*                                                              @Y30LSFY
ERR111   EQU   111                      IDMS FAILURE CODE      @Y30LSFY
         LR    RB,R1                    PASS PARM LIST PTR     @Y30LSFY
         DMABCOND ERR111,MODPD5H        XCTL TO PROB DET       @Y30LSFY
*                                                              @Y30LSFY
EDO24130 EQU   *                                               @Y30LSBS
         DROP  R8                                              @G24LSFS
         IECRES FREE,A=(1),PREFIX=YES,STM=(R2,RET,WTGPREFX)    @Y30LSBS
*                                                              @Y30LSBS
EDO24140 EQU   *                                               @Y30LSBS
         EJECT
*
***********    USER HEADER LABEL PROCESSING SECTION
*
*        REREAD JFCB TO REFRESH WORKAREA OVERLAID BY DEB         YM3116
*
         L     RTIOT,DXTIOTAD           GET TIOT ENTRY ADDRESS   YM3116
         LA    R7,TIOEJFCB              GET JFCB ADDRESS ADDRESS YM3116
         IECRES LOCJFCB,(R7)            LOCATE JFCB              YM3116
         MVC   DXJBF(JFCBLGTH),INFMJFCB-INFMJFCB(R7) MOVE JFCB   YM3116
*
         LA    R7,K1
         CR    RD,R7                    WAS EXTEND ON SAME VOLUME
         BE    EDO24800                 SKIP USER HEADER LABELS
         MVC   DSCEXTYP(K6),DXCCW6+K1   RESTORE UL EXTENT INFO   M1784
         TM    DXCCW6,JFCSL+JFCBUL      TEST IF LABEL TYPE IS SUL
         BNO   EDO24300                 BRANCH IF NOT SUL
         TM    DCBMACRF,DCBMEXCP        IS THIS EXCP DCB        YA02186
         BZ    EDO24150                 BRANCH IF NO            YA02186
         TM    DCBMACRF,DCBMRFE         IS EXIT LIST PRESENT     Y02082
         BZ    EDO24300                 BRANCH IF NO             Y02082
         TM    DCBMACRF,DCBMRCI         IS DSORG PRESENT         Y02082
         BZ    EDO24160                 BRANCH IF NO-ASSUME PS   Y02082
EDO24150 EQU   *                        TEST DSORG               Y02082
         TM    DCBDSORG,DCBORGPS        TEST IF DSORG IS PHYS SEQ
         BZ    EDO24300                 BRANCH IF NOT PHYS SEQ
         TM    DCBMACRF,DCBMEXCP        IS THIS EXCP DCB        YA02186
         BO    EDO24160                 BRANCH IF YES           YA02186
         TM    JFCDSORG,JFCORGDA        IS DATA SET BDAM        YA02186
         BO    EDO24300                 BR IF YES-SPECIAL EXIT  YA02186
EDO24160 EQU   *                        LOOK FOR UHL EXIT       YA02186
         L     RC,DCBEXLST              PICK UP EXIT LIST FROM DCB
         LA    RC,K0(,RC)               ZERO HI ORDER BYTE OF EXIT LST
         LTR   RC,RC                    IS EXIT LIST ZERO
         BZ    EDO24300                 GO TO CHECK PRE-ALLOC UL TRK
         MODESET KEYADDR=DXUKEY,WORKREG=7 USER'S EXIT LIST       Y02082
EDO24200 EQU   *                        PROCESS ENTRY
         CLI   K0(RC),K2                IS THIS ACTIVE EXIT 2
         BE    EDO24400                 GO TO PROCESS USER LABELS
         CLI   K0(RC),K2+LASTNTRY       SEE IF LAST IND ALSO ON
         BE    EDO24400                 GO TO PROCESS USER LABELS
         TM    K0(RC),LASTNTRY          IS THIS LAST ENTRY IN LIST
         LA    RC,K4(,RC)               POINT TO NEXT ENTRY
         BZ    EDO24200                 NO, BRANCH
         EJECT
         MODESET EXTKEY=DATAMGT         EOV KEY                  Y02082
EDO24300 EQU   *                        CHECK PRE-ALLOC UL TRK
         L     RC,MLZEROS               VALUE FOR IFG0555J       A40053
         CLI   DSCEXTYP,ULEXTYP         USER LABEL TRACK PRE-ALLOCATED
         BE    EDO24500                 YES, SPECIAL EXIT        A40053
         B     EDO24800                 BYPASS
*
EDO24400 EQU   *                        PROCESS USER LABELS
         L     RC,K0(,RC)               PICK UP USER'S EXIT ADDRESS
         MODESET EXTKEY=DATAMGT         EOV KEY                  Y02082
         LR    R7,RC
         LA    R7,K0(,R7)               ZERO HI ORDER BYTE
         LTR   R7,R7                    IS EXIT ADDRESS ZERO
         BZ    EDO24300                 YES,CK PRE-ALLOC UL TRK
EDO24500 EQU   *                        SPECIAL EXIT
         SR    RET,RET                  SET BRANCH TABLE RET TO  A40053
*                                       ZERO                     A40053
         MVC   DXRETMOD,LOAD0H0H        ID OF CURRENT MODULE
         LA    R7,LOAD2Q                LOAD 555J
         LA    RWTGC,DXXENTRY           SET RWTGC FOR RES RTN    M1773
         LA    RWTG,DXXAREA             POINT TO EOV WTG
         IECRES LOAD,MODID=(R7),BRCODE=(RET),BRANCH=QUEUED       Y02080
         EJECT
*
*        EXIT TO EXCP-DCB CALLER OR GO TO IFG0551L
*
EDO24800 EQU   *                        TURN OFF EOF BIT
         NI    DCBOFLGS,K255-DCBOEOF    TURN OFF FILE MARK FLAG
*                        CHECK IF EXCP SPECIFIED
EDO24900 EQU   *                        UPDATE DEBDCBAD
         L     RDEB,DCBDEBAD            GET ADDR OF DEB          Y02082
         MVC   DEBDCBAD+K1(L'DEBDCBAD-K1),DXUDCBAD+K1 POINT DEB  Y02082*
                                        TO USER DCB              Y02082
         L     RF,DXDEBXAD              GET PTR TO DEB EXTENSION YM1272
         NI    DEBXFLG1-DEBXTN(RF),ALLBITS-DEBXCDCB DEB PT USER  YM1272
         TM    DCBMACRF,DCBMEXCP        EXCP IND IN BIT 0
         BZ    EDO25100                 NO,BRANCH
*                                                                Y02080
* FREE THE END OF VOLUME WORK AREA                               Y02080
*                                                                Y02080
         NI    DCBOFLGS,ALLBITS-DCBOBUSY  RESET I/O SUPPORT BIT  Y02080
         IECRES INIT,DCBCOPY=FRWKAR,STM=(0,14,DXXPREFX)          Y02082
         LR    R1,RCORE                                          Y02080
         IECRES FREE,A=(1),PREFIX=EOV   RELEASE CORE             Y02080
         EJECT
*                                                                Y02080
*        RETURN TO CALLER                                        Y02080
*                                                                Y02080
         SR    RF,RF                    ZERO RETURN CODE         Y02082
         IECRES EXIT                    RETURN                   Y02080
         SPACE 2
EDO25100 EQU   *                        NO EXCP
         MVC   DXXMODID,VOL1A           ACCESS METHOD EXECUTOR
         MVC   DXXMODEP+K1(K3),VOL1A+K2 ADDR OF 551L             Y02080
         SR    RF,RF                    ZERO WORK REG            YM0992
EDO25200 EQU   *                        BRANCH DIRECT
         IECRES LOAD,PREFIX=DXXPREFX,BRANCH=DIRECT               Y02080
         SPACE 2
EDO25300 DMABCOND PCK,PGFREE            PAGE FREE ERROR          YM5929
         EJECT
*
*              EXECUTED INSTRUCTIONS
*
EDO25600 EQU   *                        USED IN EX INSTRUCTION
         MVC   DEB+K74-DEB(,RC),JFCBDSNM+K74  RESTORE SUBRTNS    Y02134
EDO25650 EQU   *                        USED IN EX INSTRUCTION
         MVC   JFCBDSNM+K74(K0),DEB+K74-DEB(R1)  SAVE SUBRTNS    Y02134
*
*                        CONSTANTS
*
         DS    0F
MLZEROS  DC    X'02000000'              VALUE PASSED TO 555J
MLCN36   DC    H'36'                    HALF WORD OF 36          Y02134
MLCN6    DC    H'06'                    HALF WORD OF 6           Y02134
MLCN1    DC    H'1'                     HALF WORD CONSTANT OF 1  YM1252
MLCN10   DC    H'16'                    HALF WORD CONST OF 16  @ZA13538
OLDVOL   EQU   MLCN1                    EXTEND ON CURRENT VOL  @G24LSFS
NEWVOL   DC    H'4'                     EXTEND ON NEW VOL      @G24LSFS
*                                                              @Y30LSBS
*                                                              @Y30LSBS
*        SOME  MSS EQUATES                                     @Y30LSBS
*                                                              @Y30LSBS
*                                                              @Y30LSBS
ARSEC    EQU   X'10'                    SECONDARY EXTEND       @G24LSFS
INHIBST  EQU   X'20'                    INHIBIT STAGING        @Y30LSBS
ACQPLN   EQU   96                       LENGTH OF ICBACREL LIST@Y30LSBS
ACQOP    EQU   X'02'                    OPCODE FOR ACQUIRE     @ZM44640
         EJECT
*
*        XCTL  TABLE FOR IFG0555H
*
         XCTLTABL SVC=055,ID=(MODPD5H,0P,VOL1A,1L,LOAD0H0H,5H,   YM1193X
               LOAD2Q,5J),BRT=YES,LENGTH=                        YM1193
         EJECT                                                   YM2133
         IECEQU IEZDEB=YES                                       YM1272
         IECDSECS CVT,TCB,DCB,IEZDEB,UCB,MAIN,WTG,PREFX,TIOT,    YM3116*
               JSCB,PSA,EXPAND=YES                               YM1272
ACQLST   DSECT                                                 @Y30LSFY
OLDSTRCC EQU   DXCCW+4                  OLD DEB START CYL NO.  @G24LSFS
         ICBACREL TYPE=ACQ,MF=L         LIST FORM OF ACQUIRE   @Y30LSBS
         END
