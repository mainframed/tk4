         TITLE 'IGC00020 - CLOSE INITIAL MODULE'
IGC00020 CSECT
         ENTRY IGC01020                 ENTRY FROM EOV DURING    Y02082
*                                       UNLIKE CONCAT PROCESSING Y02082
***********************************************************************
*
*               VS2-037 CHANGES
*A724800-725600,A761100                                        @ZA29752
*                                                                     *
*          VS2 RELEASE 03 DELETIONS                                   *
*0000343373,546337-546499                                       Y30ASJC
*0000414776                                                      ZM0798
*0000                                                           ZM30765
*          VS1 RELEASE 02 DELETIONS                                   *
*0000414520                                                      X02989
*0000                                                            XM0478
*0000762578                                                      XM1250
*0000                                                            XM1037
*          VS2 RELEASE 01 DELETIONS                                   *
*0000                                                            YM0903
*0000                                                            YM0850
*0000                                                            YM0954
*0000                                                            YM0992
*0000414280                                                      YM4395
*0000                                                           YA00180
*0000                                                           YA00312
*                                                                     *
*          RELEASE 21 DELETIONS/CHANGES                               *
*                                                                S21042
*                                                                M0001
*                                                                A37193
*                                                                M0039
*1141416000,682000                                               A38013
*0000442000,532000                                               M0115
* 528000-530000                                                  M1793
*0000568000                                                      M2275
*0000646000                                                      A49961
*0000478000,482000                                              SA53294
*                                                                     *
* MODULE NAME = IGC00020                                              *
*                                                                     *
* DESCRIPTIVE NAME = CLOSE INITIAL MODULE                             *
*                                                                     *
* COPYRIGHT = NONE                                                    *
*                                                                     *
* STATUS CHANGE LEVEL 000                                             *
*                                                                     *
* FUNCTION -                                                          *
*    THIS MODULE CONTAINS THE FUNCTION(S) OR PART(S) OF FUNCTION(S)-- *
*    CLOSE INITIALIZATION FUNCTION.                                   *
*    REFER TO THE FOLLOWING 'FUNCTION PROLOG(S)' FOR DETAILS.         *
*                                                                     *
* ENTRY POINTS -                                                      *
*         IGC00020 - ENTRY WHEN AN SVC 20 OR A CLOSE MACRO-           *
*                    INSTRUCTION IS ISSUED.                           *
*         IGC01020 - ENTRY VIA BRANCH FROM EOV FOR               Y02082
*                    CONCATENATION WITH UNLIKE ATTRIBUTES.       Y02082
*                                                                     *
* INPUT -                                                             *
*    ENTERED IN SUPERVISOR KEY                                   Y02082
*    REFER TO THE FOLLOWING 'FUNCTION PROLOG(S)'.                     *
*                                                                     *
* OUTPUT -                                                            *
*    EXIT IN DATA MANAGEMENT KEY                                 Y02082
*    REFER TO THE FOLLOWING 'FUNCTION PROLOG(S)'.                     *
*                                                                     *
* EXTERNAL REFERENCES -                                               *
*         IFG019RA - OPEN/CLOSE/EOV RESIDENT ROUTINE FOR XCTL AND     *
*                    WAIT.                                            *
*    REFER TO THE FOLLOWING 'FUNCTION PROLOG(S)'.                     *
*                                                                     *
* EXITS, ERROR-                                                  XM1037
*    XCTL TO IFG0200V IF CLOSE NOT ENTERED FOR A VSAM CATALOG.   XM1037
*    XCTL TO IFG0200N IF CLOSE ENTERED FOR A VSAM CATALOG.       XM1037
*    EXIT VIA AN SVC 3 TO CALLING PROGRAM IN NONE OF THE DCB'S   XM1037
*      IN THE PARAMETER LIST CAN BE CLOSED.                      XM1037
*      THE WTG TABLE IS FREED AND THE TIOT IS DEQED USING        Y02134
*      THE IECRES MACRO                                          Y02134
*    EXIT IS TO THE PROBLEM DETERMINATION MODULE IFG0200P, IF AN      *
*    ABEND SITUATION OCCURS IN THIS MODULE.  REFER TO THE FOLLOWING   *
*    'FUNCTION PROLOG(S)'.                                            *
*    DMABCOND PCK IF CALLER HOLDS TIOT RESOURCE EXCLUSIVELY      YM7883
*                                                                     *
* TABLES/WORK AREAS -                                                 *
*    REFER TO THE FOLLOWING 'FUNCTION PROLOG(S)'.                     *
*                                                                     *
* ATTRIBUTES -                                                        *
*    REENTRANT, REFRESHABLE, READ-ONLY, ENABLED, PRIVILEGED           *
*                                                                     *
* CHARACTER CODE DEPENDENCY -                                         *
*    CLASS ONE CHARACTER CODE DEPENDENCY - THE EBCDIC CHARACTER CODE  *
*    WAS USED FOR ASSEMBLY.  THE MODULE MUST BE REASSEMBLED IF A      *
*    DIFFERENT CHARACTER SET IS USED FOR EXECUTION.                   *
*                                                                     *
* NOTES -                                                             *
*    REFER TO THE FOLLOWING 'FUNCTION PROLOG(S)'.                     *
*                                                                     *
***********************************************************************
         EJECT
         USING TIOENTRY,RTIOT
         USING FORCORE,RCORE
         USING TCB,RET
         USING IHADCB,RDCB
         USING UCBOB,RUCB
         USING DEB,RDEB
         USING WTG,RWTG
         USING WTGENTRY,RWTGC
***********************************************************************
*                                                                     *
         BALR  RBASE,0                  ESTABLISH ADDRESSABILITY
         USING *,RBASE
CIN00000 EQU   *                        GET WTG TABLE            Y02082
*                                                                     *
***********************************************************************
         EJECT
*                                                                     *
*                          FUNCTION PROLOG                            *
*                                                                     *
***********************************************************************
*                                                                     *
* FUNCTION NAME -                                                     *
*    CLOSE INITIALIZATION FUNCTION.                                   *
*                                                                     *
* (STATUS) -                                                          *
*    NOT APPLICABLE                                                   *
*                                                                     *
* FUNCTION -                                                          *
*    DETERMINE IF THE DCB IS TO BE CLOSED.                            *
*    OBTAIN MAIN STORAGE FOR THE WHERE-TO-GO TABLE BY            Y02134
*     BRANCHING TO THE RESIDENT ROUTINE. THIS WILL ALSO RESULT   Y02134
*     IN THE TIOT BEING ENQED.                                   Y02134
*    OBTAIN MAIN STORAGE FOR THE WORKAREA AND CLEAR IT. ONE WORKAREA  *
*    PER DCB PROCESSED.                                               *
*    CONSTRUCT CONTROL BLOCKS NEEDED TO PERFORM I/O OPERATION.        *
*    PURGE QUEUED AND ACTIVE I/O REQUESTS ASSOCIATED WITH DATA SETS   *
*    THAT ARE BEING ACCESSED ON THE EXCP LEVEL.                       *
*    DETERMINE WHICH STRING IS TO CONTINUE PROCESSING.                *
*    READ JFCB INTO THE WORKAREA.                                     *
*    DETERMINE IF SMF DATA SET INFORMATION IS REQUESTED.              *
*    OBTAIN MAIN STORAGE FOR THE ECBLIST.                             *
*    INTERFACE WITH THE TRACE INITIALIZATION FUNCTION IF OPTIONAL     *
*    TRACE FACILITY IS REQUESTED.                                     *
*    DEB VALIDITY CHECKING IS ACCOMPLISHED VIA A DEB VALIDITY CHECK   *
*    ROUTINE WHICH MAINTAINS AND UPDATES A TABLE OF VALID DEB POINTERS*
*    IN PROTECTED CORE (LSQS).                                        *
*                                                                     *
*    IF A DCB IS ENCOUNTERED, THE FIRST FOUR BYTES OF WHOSE      YM6804
*    DDNAME IS 'IMGL', IT IS ASSUMED THAT THIS IS AN IMAGELIB    YM6804
*    DCB WHICH WILL BE CLOSED BY ISSUING SVC 105 WITH THE DCB    YM6804
*    ADDRESS IN REGISTER 1. UPON RETURNING FROM THE SVC 105,     YM6804
*    THE CALLER OF CLOSE WILL BE RETURNED CONTROL WITH A         YM6804
*    RETURN CODE OF ZERO IF THE IMAGELIB DCB IS THE ONLY         YM6804
*    DCB IN THE PARAMETER LIST. IF THERE ARE OTHER DCBS,         YM6804
*    PROCESSING ON THEM WILL CONTINUE, BUT SINCE NO CLOSE        YM6804
*    WORKAREA WAS OBTAINED FOR THE IMAGELIB DCB, AN EVENTUAL     YM6804
*    ERROR RETURN CODE WILL BE RETURNED TO THE CALLER.           YM6804
*    THIS METHOD OF CLOSING AN IMAGELIB DCB IS NOT INTENDED TO   YM6804
*    BE USED BY ANY OTHER ROUTINE THAN TASK CLOSE IN THE EVENT   YM6804
*    THAT IMAGELIB IS NOT CLOSED BEFORE TASK TERMINATION.        YM6804
*                                                                     *
* ENTRY POINTS -                                                      *
*    ENTERED FROM THE FOLLOWING--                                     *
*    WHEN AN SVC 20 OR A CLOSE MACRO-INSTRUCTION IS ISSUED.           *
*    WHEN A BRANCH ENTRY IS MADE FROM EOV FOR                    Y02082
*    CONCATENATION WITH UNLIKE ATTRIBUTES.                       Y02082
*                                                                     *
* INPUT -                                                             *
*    A POINTER IN REGISTER 1 TO THE PARAMETER LIST.                   *
*    FOR TSO TASKS WITH CONCATENATION OF UNLIKE ATTRIBUTES, REG 5     *
*    WILL CONTAIN THE VALUE 0 UPON ENTRY TO CLOSE IF EOV DID NOT      *
*    ISSUE A SUCCESSFUL ENQ ON THE TIOT.  REG 5 WILL CONTAIN THE      *
*    VALUE 1 IF EOV'S ENQ WAS SUCCESSFUL.                             *
*                                                                     *
* OUTPUT -                                                            *
*    A POINTER TO EACH OF THE FOLLOWING--                             *
*       CURRENT PARAMETER LIST ENTRY- RPARC                           *
*       DD ENTRY IN THE TIOT- RTIOT                                   *
*       WTG TABLE- RWTG                                               *
*       CURRENT WTG TABLE ENTRY- RWTGC                                *
*       DCB- RDCB                                                     *
*       WORK AREA- RCORE                                              *
*       RESIDENT ROUTINE- RES                                         *
*       UCB- RUCB                                                     *
*       DEB- RDEB                                                     *
*    THE JFCB IS IN THE WORKAREA.                                     *
*                                                                     *
* EXTERNAL REFERENCES -                                               *
*    REFER TO THE PRECEEDING MODULE PROLOG.                           *
*    CONVERT ROUTINE TTR TO MBBCCHHR.                                 *
*                                                                     *
* EXITS, NORMAL -                                                     *
*    REFER TO THE PRECEEDING MODULE PROLOG.                           *
*    CLOSE TAPE ACCESS METHOD EXECUTOR INTERFACE FUNCTION.            *
*    CLOSE UNIT RECORD/TELEPROCESSING FUNCTION.                       *
*    CLOSE DA READ DSCB FUNCTION.                                     *
*                                                                     *
* EXITS, ERROR -                                                      *
*    REFER TO THE PRECEEDING MODULE PROLOG.                           *
*    EXIT WITH ONE OF THE FOLLOWING INTERNAL CODES--                  *
*       111 - 514 ABEND - I/O ERROR READING JFCB.                     *
*       112 -           - INVALID CONTROL BLOCK(S) FOUND.             *
*                                                                     *
* TABLES/WORK AREAS -                                                 *
*    THE OPEN, CLOSE, OR EOV WORK AREA AND THE WHERE-TO-GO (WTG)      *
*    TABLE ARE DESCRIBED BY THE DSECTS AT THE END OF THE LISTING.     *
*                                                                     *
* ATTRIBUTES -                                                        *
*    REFER TO THE PRECEEDING MODULE PROLOG.                           *
*                                                                     *
* CHARACTER CODE DEPENDENCY -                                         *
*    REFER TO THE PRECEEDING MODULE PROLOG.                           *
*                                                                     *
* NOTES -                                                             *
*    NOT APPLICABLE                                                   *
*                                                                     *
***********************************************************************
         EJECT
*
*****          INITIALIZATION
*
***********************************************************************
*
*        GETMAIN THE WHERE-TO-GO TABLE
*
*                                                                Y02080
* SAVE REGISTERS 2-12 IN SVRB EXTENDED SAVE AREA                 Y02080
*                                                                Y02080
         L     RF,CVTPTR                GET POINTER TO CVT       Y02080
         L     RF,CVTTCBP-CVT(,RF)      GET TCB WORDS            Y02080
         L     RF,K4(,RF)               TCB ADDRESS              Y02080
         L     RF,K0(,RF)               POINTER TO SVRB          Y02080
         USING RBBASIC,RF                                        Y02080
*                                                                Y02080
         IECRES GET,PREFIX=WTG,            GETMAIN FOR WTG TABLE Y02080*
               STM=(2,12,RBEXSAVE),A=(1)                         Y02080
*
* IF REG 0 IS NONZERO, IT IS BECAUSE THE CALLER HAS ENQUEUED     YM7883
* EXCLUSIVELY ON THE TIOT. THIS CAN NOT BE TOLERATED.            YM7883
*
         LTR   R0,R0                    IS R0 ZERO               YM7883
         BNZ   CIN01705                 BRANCH IF NOT            YM7883
*
* RETURN FROM GET IN DATAMGT KEY                                 Y02082
*
         DROP  RF                                                Y02080
         LR    RWTG,R1                  WTG ADDR TO RWTG REG     Y02080
         IECRES LOAD,PREFIX=WTGPREFX,MODNM=CLOSE,BRANCH=NO       Y02080*
                                        GIVE OPT TRACE A SHOT    Y02080
         XC    WTGMODEP,WTGMODEP        CLEAR ADDR NEXT MOD      Y02080
CIN00075 EQU   *                        INITIALIZE POINTERS      Y01018
         L     RPAR,WTGPREFX            PT TO BASE               Y02134
         L     RC,IECRRPRM-IECPREFX(,RPAR) GET RRPLIST ADDRESS   Y02144
         MVI   RRFUNCTN-RRPLIST(RC),RRFCLOS  INDICATE CLOSE      Y02144
         L     RPAR,IECUPRML-IECPREFX(,RPAR) PT COPY USER PLIST  Y02134
         LR    RPARC,RPAR               SET CURRENT TO FIRST     Y02134
         SR    RC,RC                    SET GO SWITCH TO 0       YM0903
         LA    RWTGC,WTGENTRY-WTG(,RWTG) GET CURRENT WTG ENTRY   Y02134
CIN00100 EQU   *
*                                                                Y02134
* SEMI-FORCED CLOSE ON IMAGELIB                                  Y02134
*                                                                Y02134
         L     RDCB,PLISTDCB(,RPARC)    LD ADDR OF ACB/DCB       Y02134
         L     RET,CVTPTR               GET POINTER TO CVT       Y02080
         L     RET,CVTTCBP-CVT(,RET)    GET TCB WORDS            Y02080
         L     RET,K4(,RET)             TCB ADDRESS              Y02080
         MODESET EXTKEY=RBT234,WORKREG=1 USER'S KEY              Y02134
         CLC   DCBDDNAM(K4),IMGDDNAM    IS THIS IMGLIB DCB      YA00180
         BNE   CIN0010A                 NO  BRANCH              YA00180
         LR    K1,RDCB                  YES  DET DCB ADDRESS    YA00180
         SVC   105                      ISSUE IMGLIB CLOSE      YA00180
         TM    PLISTOPT(RPARC),LASTNTRY LAST ENTRY IN THE PLIST  YM6804
         BNO   CIN01500                 BRANCH IF NOT            YM6804
         CR    RPAR,RPARC               IS THIS THE ONLY ENTRY   YM6804
*                                       IN THE PARM LIST         YM6804
         BNE   CIN01500                 BRANCH IF NO             YM6804
         MODESET EXTKEY=DATAMGT         RETURN TO D.M. KEY       YM6804
         LA    RTIOT,K0                 ZERO RETURN CODE         YM6804
         B     CIN01700                 EXIT                     YM6804
*                                                                Y02134
CIN0010A EQU   *                        TEST DUPLICATE DCB ADDR  YM2869
*
*        DON'T PROCESS DCB IF IT IS PREVIOUSLY DUPLICATED IN     YM2869
*        THE PARAMETER LIST                                      YM2869
*
         DROP  RWTGC                                             YM2869
         LA    R1,WTGENTRY              FIRST DCB'S WTG ENTRY    YM2869
         USING WTGENTRY,R1                                       YM2869
CIN0010C EQU   *                        CHECK EACH DCB           YM2869
         CR    R1,RWTGC                 ARE WE AT CURRENT DCB    YM2869
         BE    CIN0010G                 BR IF YES-NO DUPLICATES  YM2869
         L     RCORE,WTGCORE-K1         GET WORKAREA IF ANY      YM2869
         LA    RCORE,K0(,RCORE)         CLEAR FOR LTR            YM2869
         LTR   RCORE,RCORE              IS WORKAREA PRESENT      YM2869
         BZ    CIN0010E                 BR IF NO TO CHECK NEXT   YM2869
         CLM   RDCB,B'0111',DXUDCBAD+K1 IS CURRENT DCB BEING     YM2869
*                                       PROCESSED ALREADY        YM2869
         BE    CIN01500                 BR IF YES TO AVOID IT    YM2869
CIN0010E EQU   *                        GET NEXT DCB'S WTG ENTRY YM2869
         LA    R1,WTGENTRY+L'WTGENTRY   INCREMENT WTG ENTRY PTR  YM2869
         B     CIN0010C                 CHECK NEXT DCB           YM2869
         DROP  R1                                                YM2869
         USING WTGENTRY,RWTGC                                    YM2869
CIN0010G EQU   *                        NO DUPLICATES            YM2869
         TM    DCBOFLGS,DCBOLOCK        IS LOCK BIT OFF (1)      X02989
         BNO   CIN01500                 BR IF NO                 Y02134
         TM    DCBOFLGS,DCBOBUSY        BUSY BIT ALREADY ON?     Y02134
         BO    CIN01500                 BR IF YES-NO WORKAREA    Y02134
         TM    DCBOFLGS,DCBOPEN         OPEN BIT ON?             Y02134
         BNO   CIN01460                 BR IF NO                 Y02134
         LA    R0,CLOSSIZE              SIZE OF CLOSE WORKAREA   Y02134
*****
*****          TEST FOR DEFERRED TRAILER LABEL PROCESSING ON NSL Y02134
*****          VOLUME AT EOF TIME                                Y02134
*****
         TM    DCBMACRF,DCBMEXCP        EXCP DCB                 Y02134
         BO    CIN00101                 BRANCH IF YES            Y02134
         TM    DCBDSRG2,ACBDORGA        ACB/AMB                  Y02134
         BNO   CIN00101                 BRANCH IF NO             YM4688
         CLI   AMBID-IDAAMB(RDCB),AMBIDENT  TEST FOR AMB         YM4688
         BE    CIN01500                 BRANCH IF AMB            YM4688
         B     CIN00105                 BRANCH IF ACB            YM4688
CIN00101 EQU   *                        TEST FOR EOF AND NSL     Y02134
         L     RDEB,DCBDEBAD            ADDR OF CURRENT DEB      Y02134
         TM    DEBOFLGS,DEBOFEOF+DEBOFNSL  EOF AND NSL ON THIS   Y02134
*                                       DCB                      Y02134
         BNO   CIN00105                 NO,CONTINUE NORMAL CLOSE Y02134
         L     R1,DCBEXLST              ADDR OF DCB EXIT LIST    Y02134
         LA    R1,0(,R1)                CLEAR HIGH ORDER BYTE    Y02134
         LTR   R1,R1                    IS THERE AN ADDR PRESENT Y02134
         BZ    CIN00105                 NO,CONTINUE NORMAL CLOSE Y02134
         LA    R0,DXEWAEND-FORCORE      SPECIFY EOV WKAR SIZE    YM3062
CIN00103 EQU   *                        CHECK EXIT LIST          Y02134
         CLI   0(R1),XLDEFNSL           EXIT LIST CODE BYTE      Y02134
*                                       MATCH?                   Y02134
         BE    CIN00105                 YES,GO GET MORE          Y02134
*                                       CORE FOR EOV             Y02134
         CLI   0(R1),XLDEFNSL+LASTNTRY  EXIT LIST LAST CODE BYTE Y02134
*                                       MATCH?                   Y02134
         BE    CIN00105                 YES,GO GET MORE          Y02134
*                                       CORE FOR EOV             Y02134
         TM    0(R1),LASTNTRY           LAST CODE BYTE IN LIST   Y02134
         LA    R1,K4(,R1)               INCR CODE BYTE POINTER   Y02134
         BNO   CIN00103                 NO,GO CHECK NEXT         Y02134
         LA    R0,CLOSSIZE              SIZE OF WORK AREA RQD    Y02134
         SPACE 1
*        GET CORE FOR CONTROL BLOCKS FOR THIS PARTICULAR DCB     Y02134
CIN00105 EQU   *                        GET DCB WORK AREA        Y02134
         MODESET EXTKEY=DATAMGT         KEY OF REG SAVE AREA     Y02134
*
         IECRES GET,LV=(0),PREFIX=YES,STM=(2,14,WTGPREFX),       Y02134*
               ID=CLWA                  GETMAIN FOR WORK AREA    Y02144
*
*        STORE THE ADDRESS OF THE GOTTEN CORE IN ITS PROPER      Y02134
*        PLACE IN THE WTG TABLE                                  Y02134
*
         LR    RCORE,R1                 SET UP BASE WORK AREA    Y02134
         LR    RC,RCORE                 SET GO SWITCH            Y02080
*
*        PLACE ADDRESS OF CONTROL BLOCK CORE IN WTG TABLE        Y02134
*        FOR THIS PARTICULAR DCB                                 Y02134
*
         ST    RCORE,WTGCORE-K1         STORE WORK AREA ADDRESS  Y02134
         MVC   WTGIDTTR,INITMOD         SET DEFAULT NEXT MODULE  Y02080
         L     RF,WTGPREFX              GET ADDR OF PREFIX       Y02144
         L     RF,IECRRPRM-IECPREFX(,RF) GET RRPLIST PTR         Y02144
         ST    RF,DXATCOM3              SAVE RRPLIST PTR IN W/A  Y02144
         IECRES INIT,DEB=YES,DCBCOPY=TOWKAR,STM=(3,14,WTGPREFX)  Y02134
         CLI   DXWCOPYE,K0              CHECK IF ERROR           Y02134
         BNE   CIN00808                 BR IF ERROR IN INIT      Y02134
*                                                                Y02083
*        IF A CHECKPOINT DATA SET SECURITY INTERFACE EXISTS;     Y02083
*        FREE THE COPY OF THE USER'S DCB AND COPY THE            Y02083
*        PROTECTED DCB.                                          Y02083
*                                                                Y02083
         L     RF,DXDSAB                FETCH DSAB ADDRESS       Y02083
         LTR   RF,RF                    IS THERE A DSAB PTR      YM1342
         BZ    CIN00102                 BRANCH IF NO             YM1342
         USING DSAB,RF                  ADDRESS DSAB             Y02083
         TM    DSABFLG4,DSABCKSI        Q - CHKPT SI PRESENT     Y02083
         BZ    CIN00102                 IF NOT, BRANCH           Y02083
         DROP  RF                       DSAB ADDRESSABILITY      Y02083
         L     RDEB,DCBDEBAD            GET DEB ADDRESS          Y02083
         L     RDCB,DEBDCBAD            GET PROTECTED DCB ADDRESSY02083
         ST    RDCB,DXUDCBAD            UPDATE WA USER DCB ADDR  Y02083
         MVI   DXUKEY,K0                SET USER KEY TO ZERO     Y02083
         L     R1,DXPDCBAD              GET ADDR OF COPIED DCB   Y02083
*                                       TO BE FREED              Y02083
         IECRES FREE,A=(R1),PREFIX=YES,STM=(2,14,WTGPREFX)       Y02083
         MVC   DXPDCBAD,DXUDCBAD        TELL INIT TO COPY        Y02083
         IECRES INIT,DCBCOPY=TOWKAR,STM=(3,14,WTGPREFX)          Y02083
         CLI   DXWCOPYE,K0              CHECK FOR ERROR          Y02083
         BNE   CIN00808                 BR IF ERROR IN INIT      Y02083
         OI    DCBOFLGS,DCBOBUSY        TURN ON BUSY BIT         YM5932
*                                       IN COPIED DCB            YM5932
         L     RF,DXUDCBAD              GET PTR TO USER DCB      YM5932
         MODESET  KEYADDR=DXUKEY,WORKREG=1  GET USER KEY         YM5932
         OI    DCBOFLGS-IHADCB(RF),DCBOBUSY  TURN ON BUSY BIT    YM5932
*                                       IN USER DCB              YM5932
         MODESET  EXTKEY=DATAMGT        RESUME DM KEY            YM5932
CIN00102 EQU   *                        TEST DCBOFLAGS           Y02083
         TM    DCBOFLGS,DCBOLOCK+DCBOPEN+DCBOBUSY                Y02134
         BNO   CIN00808                 BR IF NOT ALL ON         Y02134
CIN00106 EQU   *                        CHECK VSAM CATALOG       X02989
         L     RTIOT,DXTIOTAD           TIOT ADDRESS             Y02080
         L     RUCB,DXUCBADR            ESTABLISH UCB ADDR       Y02080
         L     RDEB,DCBDEBAD            GET DEB ADDR             Y02080
         NI    DCBOFLGS,X'FF'-DCBOCON   INSURE EOVC BIT = 0      Y02082
         TM    DCBMACRF,DCBMEXCP        IS ACCESS METHOD EXCP    Y02080
         BO    CIN00602                 YES - DO DEB CHK         Y02080
         TM    DCBDSORG+K1,ACBDORGA     IS CONTROL BLOCK AN ACB  Y02080
         BNO   CIN00601                 NO - DO DEBCHK           Y02080
         USING IFGACB,RDCB              DEFINE BASE TO ACB       Y02080
         XC    ACBERFLG,ACBERFLG        RESET ACB ERROR FLAGS    Y02080
         CLI   ACBAMETH,ACBSUBS         SUBSYSTEM ACB?           Y02120
         BE    CIN00602                 YES - DO DEBCHK          Y02120
         CLI   ACBAMETH,ACBVTAM         VTAM DCB?                YM3011
         BE    CIN00200                 YES - DO DEBCHK         Y30ASJC
         TM    ACBINFL,ACBCAT+ACBSCRA+ACBUCRA  TEST VSAM CATLG @Y30DSDT
         BZ    CIN00110                 SKIP IF NOT VSAM CATLOG@Y30DSDT
         XR    RET,RET                  SET RETURN CODE TO 200V  Y02080
         STC   RET,DXRETCOD             RET CODE FOR INIT ENTRY  Y02080
         MVC   DXRETMOD,INITMOD         RET FROM CATLG TO 200V   Y02080
         MVC   WTGIDTTR,VSCATMOD        SET NEXT MOD VSAM CTLG   Y02080
         B     CIN00650                 CONTIN PROC VSAM CATLG   Y02080
CIN00110 EQU   *                        CHECK VSAM               Y02080
         CLI   ACBAMETH,ACBVSAM         VSAM ACB?                Y02134
         BNE   CIN00808                 BRANCH IF NO             Y02134
         B     CIN00650                 CONTINUE                 Y02134
*
***********************************************************************
*
*        LINK TO VTAM ACDEB VALIDITY CHECK ROUTINE
*
*              EP=ISTOCMDC
*              R0=ACB COPY PTR
*              R1=USER ACB PTR
*              R13=18 FW KEY 5 SAVE AREA
*              R14=RETURN ADDRESS
*              R15=EP ADDRESS OF ISTOCMDC
*              R2-R12 RESTORED
*
***********************************************************************
*
CIN00200 EQU   *                        LINK TO VTAM ISTOCMDC   Y30ASJC
*
         L     R0,DXPDCBAD              ACB COPY PTR            Y30ASJC
         L     R1,DXUDCBAD              USER ACB PTR            Y30ASJC
         L     RD,WTGPREFX              18 FW SAVE AREA         Y30ASJC
         LA    RD,IECREGSV-IECPREFX(,RD) PTR TO 18 FW SAVE AREA ZM30765
*
         IECRES LOAD,MODNM=ISTOCMDC,BRCODE=CIN00250,            Y30ASJCX
               BRANCH=DIRECT,PREFIX=WTGPREFX                    Y30ASJC
*
CIN00250 EQU   *                        RETURN FROM ISTOCMDC    Y30ASJC
*
         MODESET EXTKEY=DATAMGT         DM KEY                  Y30ASJC
         LR    RET,RF                   SAVE RETURN CODE         ZM0798
*
         IECRES LOAD,PREFIX=WTGPREFX,MODNM=CLOSE,BRANCH=NO      Y30ASJC*
                                        GIVE OPT TRACE A SHOT   Y30ASJC
         XC    WTGMODEP,WTGMODEP        CLEAR ADDR NEXT MOD     Y30ASJC
*
         LTR   RET,RET                  INVALID VTAM ACB         ZM0798
         BZ    CIN01450                 BRANCH IF OK            Y30ASJC
         MVC   DXRETMOD,PDMOD           INDICATE ERROR          Y30ASJC
         B     CIN00830                 BRANCH IF NOT ACDEB     Y30ASJC
*
         USING IHADCB,RDCB              REDEFINE BASE TO DCB     Y02080
*
*    BYPASS DEBCHK IF BTAM OR SPOOLED DCB                        Y02120
*
CIN00601 EQU   *                        CHECK BTAM               Y02120
         TM    DCBDSRG1,DCBDSGCX        BTAM?                    Y02120
         BO    CIN00650                 BRANCH IF YES            Y02120
CIN00602 EQU   *                        INITIALIZE DEB REG       Y02120
         L     RET,DXTCBADR             RELOAD TCB ADDRESS       Y02134
         LA    RDEB,K4(,RET)            POINT RDEB TO TCBPIE SO  A38013
*                                       THAT NEXT INST. WILL GET
*                                       THE DEB ADDR
*    CHECK THE DEB CHAIN OFF OF THE TCB FOR VALID DCB'S          YM3017
*    FOR A SPOOLED DCB THE DEBECBAD POINTS TO THE DCB.           YM3017
*                                                                YM3017
CIN00610 EQU   *
         L     RDEB,DEBDEBAD            GET DEB ADDR
         LA    RDEB,0(,RDEB)            CLEAR REG HI-ORDER BYTE
         LTR   RDEB,RDEB                ANY DEB ADDR
         BZ    CIN00808                 NO, BR TO SET ERROR      Y02080
         TM    DCBDSRG2,DCBACBM         IS IT AN ACB             YM3062
         BO    CIN00620                 YES, GO CHECK DEBDCBAD   YM3062
         TM    TIOELINK,TIOTSPOL        IS IT A SPOOLED DCB      YM3017
         BZ    CIN00620                 NO, CHECK DEBDCBAD       YM3017
         CLC   DEBECBAD+K1(K3),DXUDCBAD+K1 CHECK DEBECBAD        YM3017
         B     CIN00630                 BRANCH TO CONTINUE LOOP  YM3017
CIN00620 EQU   *                        NOT A SPOOLED DCB        YM3017
         CLC   DEBDCBAD+K1(K3),DXUDCBAD+K1 CHK IF DEB PTS TO DCB Y02134
CIN00630 EQU   *                        DO DCB POINTERS MATCH    YM3017
         BNE   CIN00610                 NO, GO CHK NEXT DEB
         L     R1,DCBDEBAD              GET DEB ADDR FROM DCB
         LA    R1,0(,R1)                CLEAR HI-ORDER BYTE
         CR    RDEB,R1                  IS DEB ADDR IN DCB OK
         BNE   CIN00610                 CONTINUE DEB  SEARCH    SA51581
CIN00650 EQU   *
*
*        BYPASS DEBCHK FOR VSAM, BTAM.                           Y02120
*
*        FOR A SPOOLED DCB THE DEBDCBAD POINTS TO AN ACB - THE   Y02120
*        DEBECBAD POINTS TO THE DCB                              Y02120
*
         TM    DCBMACRF,DCBMEXCP        IS ACCESS METHOD EXCP
         BO    CIN00810                 YES-GO CHECK FOR SPOOLED DCB
         TM    DCBDSORG+K1,ACBDORGA     IS CONTROL BLOCK AN ACB
         BNO   CIN00807                 NO-TEST FOR BTAM, SPOOL  Y02120
         CLI   ACBAMETH-IFGACB(RDCB),ACBVSAM  IS ACB FOR VSAM    X02989
         BNE   CIN00810                 BR IF NO: DEBCHK         Y02120
         B     CIN01450                 PROC NEXT ENTRY          Y02080
CIN00807 EQU   *                        SPOOLED DCB OR BTAM      Y02120
*
*        BYPASS DEBCHK FOR BTAM.                                 Y02120
*        DO SPECIAL PROCESSING FOR SPOOLED DCB.                  Y02120
*
         TM    DCBDSRG1,DCBDSGCX        BTAM?                    Y02120
         BO    CIN00830                 BRANCH IF YES            Y02120
         TM    TIOELINK,TIOTSPOL        SPOOLED DCB?             Y02120
         BZ    CIN00810                 BRANCH IF NO             Y02120
         L     RDEB,DCBDEBAD            GET DEB ADDRESS          Y02120
         LA    RDEB,K0(RDEB)            CLEAR FOR COMPARE        Y02120
         CLC   DEBECBAD+K1(K3),DXUDCBAD+K1 DEB -> TO OUR DCB?    Y02120
         BNE   CIN00808                 BRANCH IF NO             Y02120
         L     R1,DEBDCBAD              ACB ADDRESS              Y02120
         DEBCHK (R1),AM=SUBSYS          IS DEB VALID?            Y02120
         CR    RDEB,R1                  IS IT OUR DEB?           Y02120
         BE    CIN00830                 BRANCH IF YES            Y02120
CIN00808 EQU   *                        ERROR                    Y02120
         MVC   DXRETMOD,PDMOD           MOVE IN P.D. MOD ID      Y02120
* TURN OFF THE BUSY BIT IN BOTH THE USER AND COPIED DCB'S        YM7304
         NI    DCBOFLGS,ALLBITS-DCBOBUSY                         YM7304
         L     RF,DXUDCBAD              POINT TO USER'S DCB      YM7304
         MODESET KEYADDR=DXUKEY,WORKREG=1                        YM7304
         NI    DCBOFLGS-IHADCB(RF),ALLBITS-DCBOBUSY              YM7304
         MODESET EXTKEY=DATAMGT         RETURN TO DM KEY         YM7304
         B     CIN00830                 GO GET ERROR CODE        Y02120
CIN00810 EQU   *                        VALIDATE DEB             Y02120
*        VALIDATE DEB                                            Y02120
*
         L     R1,DXUDCBAD              POINT TO USER'S DCB      Y02082
         DEBCHK (R1)                    VALIDATE DEB             Y02082
         LR    RDEB,R1                  LOAD VERIFIED DEB ADDR   Y01021
         STCM  RDEB,B'0111',DCBDEBAD+K1 REFRESH DCBCOPY'S DEBAD  Y02082
CIN00830 EQU   *                        TEST FOR ERROR           Y02120
         CLC   DXRETMOD,PDMOD           ERROR INDICATED          Y02080
         BNE   CIN00845                 NO,CONSTRUCT CONTROL BLOCKS
         LA    R0,CABD112               ERR- INVALID CNTRL BLOCK
*    THE CALLING MOD ID 'DXCALLID' WILL BE SET IN IFG0200V.      YM6507
         B     CIN01450                 BRANCH TO P. D. RTN
*                                                                Y02082
**************************************************************** Y02082
*                                                                Y02082
**  1. THE FOLLOWING LABEL (IGC01020) IS THE ENTRY POINT TO      Y02082
**     CLOSE FROM EOV FOR THE FOLLOWING PURPOSES:                Y02082
**        (A) UNLIKE CONCATENATION PROCESSING.                   Y02082
**        (B) CLOSING OF A DCB IF AN EOV ERROR IS TO BE IGNORED. Y02082
**  2. REGISTER R1 CONTAINS A POINTER TO A SINGLE-MEMBER         Y02082
**     CLOSE PARAMETER LIST.                                     Y02082
**  3. THE EOV WORK AREA (WHICH INCLUDES THE WTG TABLE) IS       Y02082
**     PASSED TO CLOSE, WITH REGISTER RCORE ALREADY              Y02082
**     INITIALIZED WITH THE WORK AREA ADDRESS. THE EOV WTG       Y02082
**     TABLE BEGINS AT 'DXXMODNM' IN THE EOV WORK AREA.          Y02082
*                                                                Y02082
IGC01020 EQU   *                        EOV ENTRY                Y02082
         BALR  RBASE,K0                 ESTABLISH ADDR           Y02082
CIN00840 EQU   *                        RESET BASE               Y02082
         LA    RBASE,0(RBASE)           INSURE HI-ORDER BYTE = 0 Y02082
         LA    RF,CIN00840-CIN00000     SET RF FOR BASE REG CALC Y02082
         SR    RBASE,RF                 SET RBASE AS BASE REG    Y02082
*                                                                Y02082
         LR    RPAR,R1                  LOAD PARM LIST ADDRESS   Y02082
         LA    RWTG,DXXMODNM            SET UP WTG TABLE ADDRESS Y02082
         LR    RPARC,RPAR               INIT CURR PARM LIST ADDR Y02082
         L     RDCB,PLISTDCB(,RPARC)    GET DCB ADDRESS          Y02082
         LA    RWTGC,WTGENTRY-WTG(,RWTG) INIT CURR WTG ADDRESS   Y02082
         XC    WTGMODEP,WTGMODEP        CLEAR ENTRY POINT        Y02080
         MVC   WTGIDTTR,INITMOD         SET NEXT MOD             Y02080
         L     RTIOT,DXTIOTAD           GET TIOT DD ENTRY ADDR   Y02082
         L     RUCB,DXUCBADR            SET UP UCB ADDRESS       Y02082
         LR    RC,RCORE                 SET GO SWITCH            Y02080
         L     RDEB,DCBDEBAD            SET UP DEB ADDRESS       Y02082
         LA    RDEB,0(RDEB)             CLEAR HI-ORDER BYTE      Y02082
         L     RF,DXATCOM3              GET ADDR OF RRPLIST      Y02144
         MVI   RRFUNCTN-RRPLIST(RF),RRFCLOS  SET CLOSE FUNCTION  Y02144
CIN00845 EQU   *                        CHECK FOR DUMMY         Y30ASJC
*
*        FOR DUMMY DATA SETS, NO JFCB IS READ. THEREFORE,
*        THERE IS NO NEED TO BUILD THE CONTROL BLOCKS.
*
         TM    TIOELINK,TIOTSPOL        SPOOLED DATA SET         Y02120
         BM    CIN00860                 BRANCH IF YES            Y02120
         LTR   RUCB,RUCB                TEST FOR NULL DATA SET   M1793
         BZ    CIN01450                 Y - PROC NEXT DCB ENTRY  Y02080
CIN00860 EQU   *                        RESET TCB PTR            Y02120
         L     RD,CVTPTR                PT CVT                   Y02080
         USING CVT,RD                   CVT ADDR                 Y02080
         L     RET,DXTCBADR             TCB ADDR                 Y02080
         USING TCB,RET                  TCB ADDR                 Y02080
*
*
*        CONSTRUCT CONTROL BLOCKS TO READ JFCB
*
*
*        DEB
         L     RF,CVTSVDCB              ADDRESS OF SVCLIB DCB    Y02080
         L     RF,K44(,RF)              ADDRESS OF TASK SCHEDULER DEB
         MVC   DXDEBDEB(K44),K4(RF)     USE TASK SCHEDULER DEB
         MVI   DXDEB+(DEBNMEXT-DEB),K1  SET NO. OF EXTENTS TO 1 YA03003
         MVC   DXDEBAPP,CVTXAPG         SET DUMMY APP VEC TBL    Y02080
         MVI   DXDEBMOD,K0              SET FILE MASK TO ZERO    YM6841
         XC    DXDEBECB+1(K3),DXDEBECB+1  ZERO DEBECBAD
         MVC   DXDEBSCC(K5),MYZERO      SET EXTENTS FOR DISK     Y02080
         MVC   DXDEBECC,ANTR            SET UP HIGHEST ENDING CC IN DEB
         MVC   DXDEBNTR,ANTR            SET UP HIGHEST NUM OF TRACKS
*
*        DCB 8-BYTE
*
         LA    RF,DXDEB                 DEB ADDRESS
         ST    RF,DXDCBDEB              STORE DEB ADDRESS IN DCB
         LA    RF,DXDCB                 DCB ADDRESS
         ST    RF,DXDCBAD               STORE DCB ADDRESS IN DEB
         MVI   DXDEBID,DEBIDENT         MOVE IN ID OF DEB        M2275
*
*        IOB 32-BYTE
*
         ST    RF,IOBWGHT               STORE DCB ADDRESS IN IOB
         MVI   IOBFLAG1,IOBUNREL        MAKE I/O REQUESTS UNRELATED
*
*        ECB 4-BYTE
*
         LA    RF,DXECB                 ECB ADDRESS
         ST    RF,IOBECBPT              STORE ECB ADDRESS IN IOB
         LA    RF,DXCCW                 START CCW ADDRESS
         ST    RF,IOBSIOCC              STORE IN IOB
*
         TM    TIOELINK,TIOTSPOL        SPOOLED DATA SET?        Y02120
         BM    CIN01450                 BRANCH IF YES            Y02120
         TM    DCBMACRF,DCBMEXCP        CK FOR EXCP
         BO    CIN00900                 BR IF YES TO PURGE I/O
*                                                                X02989
* IF VSAM TO ISAM COMPATIBILITY INTERFACE, BYPASS I/O PURGE      X02989
*                                                                X02989
         TM    DEBFLGS1,DEBCIFLG        IS CI INDICATED          X02989
         BO    CIN01450                 PROC NEXT DCB            Y02080
CIN00875 EQU   *                        TEST FOR DELAYED ABEND   X02989
         TM    TCBFLG,TCBFA             IS ABEND BIT ON
         BZ    CIN01450                 NO - PROC NEXT DCB ENTRY Y02080
*
*****   PURGE I/O TO HALT ANY OUTSTANDING I/O FOR THIS DCB
*              ***  PURGE WITH HALT I/O  ***
*
         TM    DCBDSORG,DCBORGIS        CHECK FOR ISAM
         BZ    CIN00900                 NO, PURGE THIS DEB       M0001
         MVC   DXCCW6+K4(K4),DEBUCBAD   SAVE FIRST 4 BYTES OF    A37193
*                                       ISAM SECTION OF DEB      A37193
         MVC   DEBUCBAD,K48(RDEB)       SHIFT FIRST UCB ADDRESS
CIN00900 EQU   *
         ST    RDEB,DXCCW5              SETTING UP PURGE PARAMETER
         MVI   DXCCW5,PURGPOST          PURGE PARAMETER          A49961
         LA    R0,DEBUSRPG              IOB CHAIN FIELD FOR PURGE
         ST    R0,DXCCW6                SAVE IN CCW
         LA    R1,DXCCW5                POINT TO PURGE PARAMETER
         PURGE (1)                      SVC PURGE
         WAIT  ,ECB=DXCCW5+K4           WAIT FOR PURGE COMPLETION
         TM    DCBMACRF,DCBMEXCP        CK FOR EXCP              A38013
         BO    CIN01450                 BR IF YES                A38013
         TM    DCBDSORG,DCBORGIS        CHECK FOR ISAM           A37193
         BZ    CIN01450                 NO, CONTINUE PROCESSING  A37193
         MVC   DEBUCBAD,DXCCW6+K4       MOVE IN FIRST 4 BYTES OF A37193
*                                       ISAM SECTION OF DEB      A37193
CIN01450 EQU   *
         XR    RET,RET                  SET INIT ENTRY           Y02080
         STM   RTIOT,RET,DXREGSAV       SAVE REG 9 THRU 14
         STM   R0,R1,DXREG0             SAVE REG 0 AND 1
         B     CIN01500                 PROC NEXT DCB ENTRY      Y02080
CIN01460 EQU   *                        ERROR DCB NOT OPEN       Y02080
         TM    DCBMACR,DCBMEXCP         CHK IF EXCP              Y02080
         BO    CIN01500                 Y - PROC NEXT DCB        Y02080
         TM    DCBDSORG+K1,ACBDORGA     CHK IF ACB               Y02080
         BNO   CIN01500                 NO - PROC NEXT DCB ENTRY Y02080
         MVI   ACBERFLG-IFGACB(RDCB),ACBCALR IND CLOSE ERROR   @ZA02186
CIN01500 EQU   *
         MODESET EXTKEY=DATAMGT         RET TO CLOSE KEY         Y02082
         TM    PLISTOPT(RPARC),LASTNTRY  CK LAST ENTRY ON LIST
         LA    RWTGC,K8(,RWTGC)         NEXT WTG ENTRY
         BO    CIN01600                 BRANCH IF LAST ENTRY
         LA    RPARC,K4(,RPARC)         NEXT DCB ENTRY
         B     CIN00100                 BRANCH TO NEXT ENTRY     Y02080
CIN01600 EQU   *
         LTR   RC,RC                    CHK IF ANYTHING TO CLOSE Y02080
         LA    RTIOT,K4                 ERROR RETURN IF NO       YM6804
         BZ    CIN01700                 BR - NOTHING TO CLOSE    Y02080
         L     RD,CVTPTR                GET CVT ADDRESS          Y02080
         L     RES,CVTDMSR              LOAD ADDR OF RES ROUT    Y02080
         ST    RES,K4(RWTGC)            SAVE RES RTN ADDR IN WTG Y02080
         LR    RCORE,RC                 SET LAST GOOD WORK AREA  Y02080
         L     RF,DXATCOM3              GET ADDR OF RRPLIST      YM7099
         OI    RRFLAGS2-RRPLIST(RF),RRFFIN1 1ST LOAD COMPLETED   YM7099
         MVC   WTGMODNM,LOWMOD          SET LOWEST MOD ID        YM7099
         LM    R0,R1,DXREG0             GET HIS REGS             Y02080
         LM    RTIOT,RET,DXREGSAV       GET REGS 9-14            Y02080
         IECRES XCTL                    EXIT NEXT MOD QUEUED     Y02080
CIN01700 EQU   *                        DCB UNCLOSEABLE
*
*        FREEMAIN THE WHERE-TO-GO TABLE
*
         IECRES FREE,PREFIX=WTG,A=(RWTG) FREEMAIN WTG TABLE CORE Y02080
         LR    RF,RTIOT                 SET RETURN CODE          YM6804
*                                                                Y02080
*        RETURN TO CALLER                                        Y02080
*                                                                Y02080
         IECRES EXIT                    RETURN                   Y02080
*
*        THE CALLER OF CLOSE HOLDS THE TIOT RESOURCE EXCLUSIVELY YM7883
*        THIS SHOULD NEVER VALIDLY HAPPEN AND CAN NOT BE ALLOWED YM7883
*
CIN01705 EQU *
         SR    RF,RF                   ZERO REG                @ZA29752
         SR    RC,RC                   ZERO REG                @ZA29752
         LA    RF,K8                   RETURN CODE             @ZA29752
         L     RC,ABND50D              ABEND CODE              @ZA29752
         ABEND (RC),DUMP                                       @ZA29752
         EJECT
         SPACE 1
***********************************************************************
*                                                                     *
*                       CONSTANTS                                     *
*                                                                     *
***********************************************************************
         SPACE 1
MYZERO   DC    XL8'00000000'            CONSTANT                 Y02080
ANTR     DC    X'7FFF'                  DEB END CC AND NUM TRACKS
LOADID   DC    X'F2C0'                  ID OF FIRST LOAD OF CLOSE
IMGDDNAM DC    CL8'IMGLIB  '            IMAGELIB DDNAME          XM1250
LOWMOD   DC    CL8'IFG0200 '            LOWEST CLOSE MOD NAME    Y02080
ABND50D  DC    XL4'0000050D'            ABEND 50D              @ZA29752
         XCTLTABL ID=(,ISTOCMDC,INITMOD,0V,PDMOD,0P,            Y30ASJCX
               VSCATMOD,0N,CLOSE,IGC00020),                     Y30ASJCX
               SVC=020,BRT=YES,LENGTH=                           Y02080
TIOTSPOL EQU   X'06'                    JES SPOOL D.S. INDICATORS     $
*
         IECDSECS CVT,TCB,TIOT,RB,                               Y02080X
               DSAB,                                             Y02083X
               DCB,ACB,DEB,UCB,MAIN,WTG,PREFX,RRPL,EXPAND=YES    Y02144
         IDAAMB                                                  YM4688
         EJECT
         IECEQU AOS=YES
         END
