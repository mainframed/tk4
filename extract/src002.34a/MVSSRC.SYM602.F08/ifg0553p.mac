         TITLE 'IFG0553P                      EOV INPUT DA INITIAL MODUL
               LE'                                               Y02134
IFG0553P CSECT
***********************************************************************
*                                                                     *
*                                                                     *
*          VS2 RELEASE 03 DELETIONS                                   *
*0000450420,451960,457800                                      @ZA01342
*0000                                                          @ZA08315
*0000                                                          @ZA13566
*A45190803,45190804                                            @ZA33029
*          VS2 RELEASE 02 DELETIONS                                   *
*                                                                     *
*                                                                     *
***********************************************************************
*                                                                     *
* MODULE NAME = IFG0553P (OS/VS2)                                     *
*                                                                     *
* DESCRIPTIVE NAME = EOV INPUT DA INITIAL MODULE                      *
*                                                                     *
* COPYRIGHT = NONE                                                    *
*                                                                     *
* STATUS = RELEASE 2, LEVEL 0                                         *
*                                                                     *
* FUNCTION =                                                          *
*        ENTERED FROM IFG0551H - EOV INITIALIZATION                   *
*        IFG0554L - RETURN FROM USER TRAILER LABEL PROCESSING         *
*        IFG0202I - RETURN FROM SMF PROCESSING                        *
*                                                                     *
*        1) SETUP WORKAREA DEB REFLECTING UNIT CURRENTLY              *
*        BEING PROCESSED.                                             *
*                                                                     *
*        2) EXIT TO IFG0554L TO PROCESS USER TRAILER LABELS,          *
*        SETTING BITS 5 AND 6 OF JFCBMASK+5 TO ONE. USER LABEL        *
*        PROCESSING IS BYPASSED IF FEOV, DCBDSORG IS NOT PS, OR       *
*        USER LABELS HAVE ALREADY BEEN PROCESSED IN A                 *
*        PREVIOUS PASS THROUGH EOV FOR VBS UPDATE.                    *
*                                                                     *
*        IFG0554L IS GIVEN CONTROL IF -                               *
*        AN 03 EXIT EXISTS IN THE EXIT LIST AND EITHER AN             *
*        EOF CONDITION IS NOT PRESENT, OR AN EOF CONDITION IS         *
*        PRESENT AND AN 0C EXIT DOES NOT EXIST.                       *
*        AN 0C EXIT EXISTS IN THE EXIT LIST AND AN 03 EXIT DOES       *
*        NOT EXIST.                                                   *
*                                                                     *
*        3) AFTER RETURNING FROM IFG0554L OR IF USER LABEL            *
*        PROCESSING IS NOT REQUIRED, AN EXIT IS MADE TO IFG0552X      *
*        IF NO MORE VOLUMES ARE PRESENT IN THE JFCB OR, AFTER         *
*        READING THE FORMAT 1 DSCB, IF THE LAST VOLUME BIT (BIT 0     *
*        IN DSCDSIND) IS ON IN THE DSCB.                              *
*                                                                     *
*        4) IF NOT EOF AND SMF PROCESSING IS REQUIRED, MODULE         *
*        IFG0553B (ALIAS NAME FOR IFG0202H) IS GIVEN CONTROL.         *
         EJECT
*        5) AFTER RETURNING FROM SMF PROCESSING (MODULE IFG0202I)     *
*        OR IF SMF PROCESSING IS NOT REQUIRED, THE DATA MANAGEMENT    *
*        COUNT IN THE CURRENT VOLUME'S UNIT IS DECREMENTED.           *
*        IF THE DATA MANAGEMENT COUNT DROPS TO ZERO FOR A VIO         *
*        UNIT, A WICLOSE IS ISSUED.                                   *
*                                                                     *
*        6) DXVOLSEQ IS INCREMENTED BY ONE AND MODULE IFG0554A (ALIAS *
*        NAME FOR IFG0194A) IS GIVEN CONTROL TO MOUNT AND VERIFY THE  *
*        NEXT VOLUME AND READ THE DSCB(S), AND PASS CONTROL TO        *
*        MODULE IFG0553X.                                             *
*                                                                     *
* NOTES = SEE BELOW                                                   *
*                                                                     *
*    DEPENDENCIES =                                                   *
*            CLASS ONE CHARACTER CODE.  THE EBCDIC CHARACTER CODE     *
*            WAS USED FOR ASSEMBLY.  THE MODULE MUST BE REASSEMBLED   *
*            IF A DIFFERENT CHARACTER SET IS USED FOR EXECUTION.      *
*                                                                     *
*    RESTRICTIONS = NONE                                              *
*                                                                     *
*    REGISTER CONVENTIONS =                                           *
*            R2 POINTS TO DCB                                         *
*            R4 POINTS TO OPEN WORK AREA                              *
*            R5 POINTS TO THE RESIDENT ROUTINE                        *
*            R6 POINTS TO THE WTG TABLE                               *
*            R7 POINTS TO THE CURRENT PARAMETER LIST ENTRY            *
*            R8 POINTS TO THE CURRENT WTG TABLE ENTRY                 *
*            R9 POINTS TO THE DD ENTRY IN THE TIOT                    *
*            R10 POINTS TO THE UCB                                    *
*                                                                     *
*    PATCH LABEL = SEE THIRD FROM LAST LABEL BEFORE ORG STATEMENT AT  *
*                  END OF LISTING.                                    *
*                                                                     *
* MODULE TYPE = CONTROL (OPEN, CLOSE, EOV DATA MANAGEMENT)            *
*                                                                     *
*    PROCESSOR = ASSEMBLER XF                                         *
*                                                                     *
*    MODULE SIZE = SEE EXTERNAL SYMBOL DICTIONARY OR LOC FIELD ON     *
*                  ORG STATEMENT AT END OF LISTING                    *
*                                                                     *
*    ATTRIBUTES = REENTRANT, REFRESHABLE,READ-ONLY, ENABLED,          *
*                 PRIVILEGED, SUPERVISOR STATE, DATA MANAGEMENT KEY,  *
*                 LINK PACK AREA RESIDENT/PAGEABLE                    *
*                                                                     *
* ENTRY POINT = IFG0553P                                              *
*                                                                     *
*    PURPOSE = SEE FUNCTION                                           *
*                                                                     *
*    LINKAGE =                                                        *
*        THIS MODULE IS PASSED CONTROL THROUGH THE IECRES-LOAD MACRO. *
         EJECT
* INPUT =                                                             *
*        ENTERED IN PROTECT KEY 5.                                    *
*        REGISTER 2 POINTS TO THE COPIED DCB.                         *
*        REGISTER 4 POINTS TO THE EOV WORKAREA.                       *
*        THE DEBDCBAD POINTS TO THE COPIED DCB.                       *
*        JFCBMASK+5 BITS 5 AND 6 SET TO ONE IF RETURN FROM IFG0554L.  *
*        REGISTER 14 CONTAINS ENTRY CODE -                            *
*              0 - ENTERED FROM IFG0551H.                             *
*              4 - RETURN FROM SMF PROCESSING MODULE IFG0202I.        *
*                                                                     *
* OUTPUT =                                                            *
*        CURRENT VOLUME'S DSCB READ (EXCEPT TO IFG0552X)              *
*        DXVOLSEQ UPDATED (IFG0554A)                                  *
*        DATA MANAGEMENT COUNT DECREMENTED ON CURRENT                 *
*              VOLUME AND WICLOSE IF VIO (IFG0554A)                   *
*        REGISTER 14 CONTAINING ENTRY CODE -                          *
*              IFG0552X - 0                                           *
*              IFG0553B - 4, ALSO USED AS RETURN CONTENTS OF REG 14   *
*        DXRETMOD INDICATING IFG0553P (IFG0554L, IFG0553B)            *
*        DXRETMOD INDICATING IFG0553X (IFG0554A)                      *
*                                                                     *
* EXIT-NORMAL =                                                       *
*        IFG0552X - END OF FILE CONDITION.                            *
*        IFG0554L - USER TRAILER LABEL PROCESSING.                    *
*        IFG0553B - SMF PROCESSING.                                   *
*        IFG0554A - MOUNT AND VERIFY NEXT VOLUME.                     *
*                                                                     *
* EXIT-ERROR =                                                        *
*        IFG0550P - PROBLEM DETERMINATION WITH INTERNAL ABEND CODE    *
*        IN REGISTER 0 -                                              *
*        175 - INDICATING 737-14 ABEND, I/O ERROR READING DSCB.       *
         EJECT
* EXTERNAL REFERENCES = SEE BELOW                                     *
*                                                                     *
*    ROUTINES = IFG019RA THROUGH IECRES MACRO.                        *
*                                                                     *
*    DATA AREAS =                                                     *
*        EOV WORKAREA.                                                *
*                                                                     *
*    CONTROL BLOCK =                                                  *
*        DEB                                                          *
*        UCB                                                          *
*        CVT                                                          *
*        DCB                                                          *
*        VDSCB                                                        *
*                                                                     *
* TABLES =                                                            *
*        DEVICE CHARACTERISTICS TABLE                                 *
*        DCB EXIT LIST                                                *
*                                                                     *
* MACROS =                                                            *
*        MODESET                                                      *
*        EXCP                                                         *
*        IECRES WAIT                                                  *
*        IECRES-LOAD                                                  *
*        DMABCOND                                                     *
*        XCTL                                                         *
*        IECDSECS                                                     *
*        IECEQU                                                       *
*        IDDVDSCB                                                     *
*                                                                     *
* CHANGE ACTIVITY =                                                   *
*        SEE CHANGES/DELETIONS SECTION JUST AFTER CSECT CARD          *
*                                                                     *
***********************************************************************
         EJECT
ERR175   EQU   175                      ABEND CODE - 737-04
         BALR  RBASE,0                  ESTABLISH ADDRESSABILITY
         USING FORCORE,RCORE
         USING WTG,RWTG                 BASE FOR WTG TABLE       Y02080
         USING TIOENTRY,RTIOT
         USING UCB,RUCB
         USING DEB,RDEB
         USING IHADCB,RDCB
         USING *,RBASE
         SPACE 2
         L     RDEB,DCBDEBAD            ADDRESS OF USER'S DEB
         TM    JFCBMASK+K5,K6           TEST,BR IF RETURNING FROM
         BO    EDI00600                 UL PROCESSING
         B     EDI00000(RET)            BR, ACCORDING TO RETURN CODE
EDI00000 B     EDI00050                 FROM EOV BASIC
         B     EDI01450                 FROM SMF
EDI00050 EQU   *                        INITIAL ENTRY
         L     RUCB,DXUCBADR            LOAD CURRENT UCB         Y02134
         SPACE 1
*
*                   SET UP THE WORK AREA DEB FOR OTHER DA DEVICES
*
         MVC   DXDEBUCB+K1(K3),DXUCBADR+K1                       Y02134
*                                       PLACE UCB ADDRESS IN WORK DEB
         MVC   DXDEBSCC(K10),COLCDU0I   CREATE DUMMY EXTENT IN WORK DEB
         L     R7,CVTPTR                GET CVT ADDRESS          M0983
         L     R7,CVTZDTAB-CVT(,R7)     GET ADDR OF I/O DEVICE   M0983
*                                       TABLE                    M0983
         XR    R1,R1                    CLEAR REG FOR OFFSET     M0983
         IC    R1,UCBTBYT4              GET UCB DEVICE TYPE      M0983
         IC    R1,0(R1,R7)              INDEX TO DEVICE OFFSET   M0983
*                                       BYTE                     M0983
         AR    R7,R1                    ADDR OF DEVICE ENTRY IN  M0983
*                                       TABLE                    M0983
         MVC   DXDEBECC(K4),0(R7)       SET MAX EXTENT FROM      M0983
*                                       DEVICE TABLE             M0983
         EJECT
*
*****    FORCE END VOLUME PROCESS - SKIP USER LABEL PROCESSING
*
         TM    DCBMACRF,DCBMEXCP        TEST FOR EXCP           SA59750
         BO    EDI00075                 BRANCH IF EXCP          SA59750
         TM    DCBCIND2,DCBC2FEO        IS THIS FEOV- FORCE END OF VOL
         BO    EDI00800                 YES,GO TO EOV
*
***********    USER TRAILER LABEL PROCESSING SECTION
*
*        DCBIOBAD CAN BE TESTED ONLY IF NOT CHAINED SCHEDULING   YM1194
*        AND NOT EXCP                                            YM1194
*
         TM    DCBCIND2,DCBC2CHN        CHAINED SCHEDULING TEST  YM1194
         BO    EDI00075                 BRANCH IF YES            YM1194
         CLC   DCBIOBAD,MASKFL0I        HAS UL BEEN PROCESSED
         BNE   EDI00800                 BRANCH IF YES
EDI00075 EQU   *                        TEST FOR USER LABELS     YM1194
         TM    JFCBLTYP,JFCSUL          TEST,BR IF USER HAS NOT
*                                       SPECIFIED
         BNO   EDI00800                 'SUL' LABEL TYPE
         TM    DCBMACRF,DCBMEXCP        IS IT EXCP               YM1194
         BNO   EDI00077                 BRANCH IF NO             YM1194
         TM    DCBMACRF,DCBMRFE         IS DCBEXLST PRESENT      Y02080
         BNO   EDI00800                 BRANCH IF NO             Y02080
         TM    DCBMACRF,DCBMRCI         IS DSORG PRESENT         YM1194
         BO    EDI00080                 BRANCH IF NO             YM1194
EDI00077 EQU   *                        SUL ONLY FOR PS          YM1194
         TM    DCBDSORG,DCBORGPS        TEST,BR IF DSORG IS NOT
         BZ    EDI00800                 PHYSICAL SEQUENTIAL
         EJECT
*
*        USER LABEL PROCESSING - CHECK FOR END OF FILE
*
EDI00080 EQU   *                        EOF TEST                 YM1194
*                                                                Y02134
         CLC   JFCBNVOL,DXVOLSEQ+K1     CHECK FOR EOF            Y02134
         BNH   EDI00235                 BRANCH IF EOF          @ZA01342
EDI00150 EQU   *                        NOT EOF                  Y01234
         TM    UCBJBNR,UCBVRDEV         TEST FOR VIO UCB         Y02132
         BNO   EDI00175                 BR IF NOT                Y02132
*        CALL SUBROUTINE TO READ VIO DSCB FROM SWA               Y02132
         BAL   RD,EDI01250              GO READ VIO DSCB         Y02132
*                                                                Y02132
         B     EDI00200                 CONTINUE PROCESSING      Y02132
EDI00175 EQU   *                        READ REAL DSCB           Y02132
*                                                                Y02134
         BAL   RD,EDI02200              BUILD CHANNEL PROGRAM   SA57255
*                                       TO READ F1 DSCB         SA57255
         BAL   RD,EDI01200              READ F1 DSCB            SA57255
         BNO   EDI01300                 BR IF I/O ERROR ON READ SA57255
EDI00200 EQU   *                        SEE IF LAST VOLUME       Y02132
         OI    DXATDACC,DXATRDDB        SHOW DSCB READ           Y02144
         CLI   DSCFMTID,CHAR4           CHECK IF OPEN TO VTOC  @ZA33029
         BE    EDI00240                 TREAT AS EOV IF YES    @ZA33029
         TM    DSCDSIND,LASTNTRY        TEST LAST VOLUME BIT    SA57255
*                                       TO DETERMINE EOF OR EOV SA57255
         BNO   EDI00240                 IF NOT LAST VOL-EOV    @ZA01342
EDI00235 EQU   *                                               @ZA01342
         L     R1,DXDSABAD              GET CURRENT DSAB       @ZA01342
         USING DSAB,R1                                         @ZA01342
         L     R1,DSABFCHN              GET NEXT DSAB          @ZA01342
         LTR   R1,R1                    IS THERE ANOTHER DSAB? @ZA01342
         BZ    EDI00250                 NO, BR (EOF)           @ZA01342
         L     R1,DSABTIOT              GET NEXT TIOT ENTRY    @ZA01342
         CLI   TIOEDDNM-TIOENTRY(R1),BLANK IS DDNAME BLANK?    @ZA01342
         BNE   EDI00250                 NO, BR (EOF)           @ZA01342
         TM    TIOELINK-TIOENTRY(R1),TIOESYIN+TIOESYOT IS IT   @ZA01342
*                                       A SPOOLED DATA SET?    @ZA01342
         BM    EDI00250                 YES, BR (EOF)          @ZA01342
         TM    DCBOFLGS,DCBOUNLK        UNLIKE CONCATENATION?  @ZA01342
         BO    EDI00250                 YES, BR (EOF)          @ZA01342
         L     RF,TIOESTTB-TIOENTRY(R1) GET CONC UCB ADDR      @ZA01342
         LA    RF,K0(RF)                CLEAR HIGH ORDER BYTE  @ZA01342
         LTR   RF,RF                    DUMMY DATA SET?        @ZA01342
         BZ    EDI00250                 YES, BR (EOF)          @ZA01342
EDI00240 EQU   *                                               @ZA01342
*
*        NOT EOF - SETUP TO SEARCH FOR 03 EXIT, THEN 0C
*
         LA    RF,EDI01000              RETURN ADDR IF NOT SUR EXIT 03
         LA    R7,ULEXIT                ADDR OF UL EXIT CODE BYTE 03
         LA    R1,EDI00500              EXIT ADDR IF CODE MATCH
         LA    RET,K2                   SET INCR VALUE TO 1     SA55619
         B     EDI00300                 BR TO TEST UL EXIT      SA55619
*
*        EOF - SETUP TO SEARCH FOR 0C EXIT, THEN 03
*
EDI00250 EQU   *                        SETUP IF EOF            SA55619
         LA    RF,EDI01000              RETURN ADDR IF          SA55619
*                                       NOT UL EXIT             SA55619
         LA    R7,SULDFR0I              ADDR OF UL DEFER CODE   SA55619
         LA    R1,EDI00900              EXIT ADDR IF CODE MATCH@ZA01342
         LA    RET,K2                   SET INCR VALUE TO -2    SA55619
         LNR   RET,RET                                          SA55619
         EJECT
EDI00300 EQU   *                        GET EXIT LIST ENTRIES FOR
*                                       COMPARE
         L     RC,DCBEXLST              PICK UP EXIT LIST FROM DCB
         LA    RC,0(,RC)                ZERO HI ORDER BYTE OF EXIT LIST
         LTR   RC,RC                    IS EXIT LIST ZERO
         BZ    EDI00800                 YES, BRANCH             SA57255
EDI00400 EQU   *                        TRY TO FIND EXIT LIST ENTRY
         MODESET KEYADDR=DXUKEY,WORKREG=13 GET INTO USER KEY     Y02082
         CLC   K0(K1,RC),K0(R7)         DO CODE BYTES MATCH
         BE    EDI00430                 BRANCH YES             @ZA13566
         CLC   K0(K1,RC),K1(R7)         DO CODE BYTES MATCH-LST
*                                       ON EXL
         BE    EDI00430                 BRANCH YES             @ZA13566
         B     EDI00450                 GO CHECK FOR LAST ENTRY@ZA13566
EDI00430 BR    R1                       GO TO EXIT ADDR        @ZA08315
EDI00450 TM    0(RC),LASTNTRY           IS THIS LAST ENTRY IN LIST
         LA    RC,K4(,RC)               POINT TO NEXT ENTRY
         BZ    EDI00400                 CHECK NEXT ENTRY
         BR    RF                       EXIT TO CALLER SPECIFIED
*                                       ADDR
*
EDI00500 EQU   *                        SEE IF EXIT ADDR SPECIFIED
         L     RC,K0(,RC)               PICK UP USER'S EXIT ADDR
         LR    R7,RC
         LA    R7,K0(,R7)               CLEAR HIGH ORDER BYTE
         MODESET EXTKEY=DATAMGT         RETURN TO DATA MGT KEY   Y02082
         LTR   R7,R7                    IS EXIT ADDRESS ZERO
         BZ    EDI00800                 YES, BYPASS USER LABEL PROC
         EJECT
*
*        USER LABELS TO BE PROCESSED - ENSURE DSCB READ
*
         CLI   DSCFMTID,CHAR1           HAS F1 DSCB BEEN READ   SA57255
         BE    EDI00550                 YES, DO NOT REREAD      SA57255
         TM    UCBJBNR,UCBVRDEV         TEST FOR VIO UCB         Y02132
         BNO   EDI00525                 BR IF NOT                Y02132
*        CALL SUBROUTINE TO READ VIO DSCB FROM SWA               Y02132
         BAL   RD,EDI01250              GO READ VIO DSCB         Y02132
*                                                                Y02132
         B     EDI00535                 CONTINUE PROCESSING      Y02132
EDI00525 EQU   *                        READ READ DSCB           Y02132
         BAL   RD,EDI02200              BRANCH TO BUILD CHANNEL SA57255
*                                       PROGRAM TO READ DSCB    SA57255
         BAL   RD,EDI01200              READ DSCB
         BNO   EDI01300                 BR IF I/O ERROR-ABEND 737
EDI00535 EQU   *                        DSCB IN WORKAREA         Y02132
         OI    DXATDACC,DXATRDDB        SHOW DSCB READ           Y02144
*
EDI00550 EQU   *                        SET INPUT TO SUL MOD    SA57255
         MVC   DXRETMOD(K5),LOAD553P    SET RETURN ID
         OI    JFCBMASK+K5,K6           SET FLAGS TO INDICATE EOV ENTRY
         LA    RF,LOAD554L              ID AND TTR
         B     EDI02000                 GO TO RESIDENT ROUTINE
EDI00600 EQU   *                        RETURN FROM USER LABEL MODULE
         NI    JFCBMASK+K5,K255-K6      TURN OFF EOV-UL ENTRY FLAGS
         EJECT
*
*        USER LABELS NOT PROCESSED OR RETURED FROM PROCESSING
*
EDI00800 EQU   *                        CONT. IF NO USER LABELS SA57255
         TM    DCBOFLGS,DCBOCON         CONCAT IN PROCESS       SM5271
         BO    EDI01400                 YES, PROCESS EOV        SM5271
*
*****    TEST EOV/EOF
*
         MODESET EXTKEY=DATAMGT         DATA MGT KEY FROM NOW ON Y02082
*                                                                Y02134
         CLC   JFCBNVOL,DXVOLSEQ+K1     CHECK FOR EOF            Y02134
         BNH   EDI00900                 GO TO EOF PROCESSING     Y02134
*                                                                Y02134
EDI00820 EQU   *                        NOT EOF - TEST DSCB TO VERIFY
         CLI   DSCFMTID,CHAR1           HAS F1 DSCB BEEN READ   SA57255
         BE    EDI00850                 YES, DO NOT REREAD      SA57255
         TM    UCBJBNR,UCBVRDEV         TEST FOR VIO UCB         Y02132
         BNO   EDI00835                 BR IF NOT                Y02132
*        CALL SUBROUTINE TO READ VIO DSCB FROM SWA               Y02132
         BAL   RD,EDI01250              GO READ VIO DSCB         Y02132
*                                                                Y02132
         B     EDI00845                 CONTINUE PROCESSING      Y02132
EDI00835 EQU   *                        READ READ DSCB           Y02132
         BAL   RD,EDI02200              BUILD CHANNEL PROGRAM   SA57255
*                                       TO READ DSCB            SA57255
         BAL   RD,EDI01200              READ F1 DSCB            SA57255
         BNO   EDI01300                 BRANCH TO ABEND IF I/O  SA57255
*                                       ERROR ON READ           SA57255
EDI00845 EQU   *                        DSCB IN WORKAREA         Y02132
         OI    DXATDACC,DXATRDDB        SHOW DSCB READ           Y02144
*                                                                Y02144
EDI00850 EQU   *                        SEE IF LAST VOLUME      SA57255
         TM    DSCDSIND,LASTNTRY        TEST LAST VOLUME BIT    SA57255
*                                       TO DETERMINE EOF OR EOV SA57255
*                                       IF MORE VOLS SPECIFIED  SA57255
*                                                               SA57255
         BNO   EDI01400                 IF BIT IS OFF, BRANCH   SA57255
*                                       TO INITIATE EOV PROC    SA57255
*****    END OF FILE PROCESS
*
EDI00900 EQU   *                        GO TO END OF FILE MODULE
         MODESET EXTKEY=DATAMGT         RETURN TO DM KEY       @ZA13566
         LA    RF,LOAD552X              LOAD ADDRESS OF 552X
         B     EDI02000                 BRANCH TO RES
         EJECT
EDI01000 EQU   *                        RESET REGS IF EXIT NOT FOUND
*
*        IF 03 OR 0C EXIT NOT FOUND, LOOK FOR THE OTHER
*
         LA    RF,EDI00800              RETURN ADDR IF NOT UL   SA57255
*                                       DEFER CD
         LA    R7,K0(RET,R7)            ADDR OF NEXT UL CODE    SA55619
         LA    R1,EDI00500              EXIT ADDR IF CODE MATCH SA55619
         B     EDI00300                 GO CHECK FOR UL EXIT
         SPACE 2
*
*   THIS SUBROUTINE ISSUES I/O OPERATIONS
*
EDI01200 EQU   *                        EXCP, RETURN IF NO I/O ERROR
         EXCP  DXIOB                    EXECUTE CHANNEL PROGRAM
         BAL   RET,EDI02050             GO SET UP REG RWTG AND RWTGC
         IECRES WAIT                    SEND TO RES FOR WAIT
*
         TM    DXECB,ALLBITS-DCBMEXCP   WAS THERE AN I/O ERROR
         BR    RD                       RETURN TO CALLING POINT
         EJECT
*                                                                Y02132
*        SUBROUTINE TO 'READ' VIO FORMAT ONE DSCB                Y02132
*                                                                Y02132
EDI01250 EQU   *                        MOVE VDSCB TO WORKAREA   Y02132
         MVC   DXDSCB(DSCEXT2-DSCFMTID),VDSDSCB-VDSUCB+L'JFCBDSNM(RUCB)
*                                                                Y02132
         OC    DXDSCB(DSCEXT2-DSCFMTID),DXDSCB IS DSCB = ZERO?   Y02132
         BZ    EDI01300                 BR IF YES, I/O ERROR     Y02132
*                                                                Y02132
         XC    DSCEXT2(DSCBEND-DSCEXT2),DSCEXT2 ZERO REMAIN CORE Y02132
*                                                                Y02132
         BR    RD                       RETURN                   Y02132
         SPACE 2
*
*                   ABNORMAL END OF TASK
*
EDI01300 EQU   *                        GO TO PROBLEM DETERMINATION
         BAL   RET,EDI02050             GO SET UP REG RWTG AND RWTGC
         DMABCOND ERR175,LOAD0P
         SPACE 2
*
*        COMMENCE EOV PROCESSING
*
*        THIS SECTION DETERMINES IF SMF RECORDS REQUIRED              *
*
EDI01400 EQU   *                        RETURN IF SMF NOT REQUIRED
         TM    DXXPATHS,WTGSMF          IS SMF REQUIRED FOR DATA A38013
*                                       SET                      A38013
         BNO   EDI01450                 NO, CONTINUE PROCESSING  A38013
         LA    RF,LOAD200I
         LA    RET,K4                   SET RETURN INDICATOR
         MVC   DXRETMOD,LOAD553P        XCTL TO SMF MODULE
         B     EDI02025                 GO TO SMF LOAD
         EJECT
***********************************************************************
*
*        DECREMENT DM COUNT - WICLOSE IF VIO AND COUNT GOES ZERO
*
*****************************************************************Y02134
*                                                                Y02134
EDI01450 EQU   *                        INCREMENT DXVOLSEQ       YM7099
         LH    R1,DXVOLSEQ              LOAD VOL SEQ             YM7099
         LA    R1,K1(R1)                INCREMENT TO NEXT VOL    YM7099
         STH   R1,DXVOLSEQ              SAVE SEQUENCE NUMBER     YM7099
*
*        GET LOCAL LOCK
*
         L     RF,DXXPREFX              GET PREFIX ADDR          YM7883
         USING IECPREFX,RF              USING FOR PREFIX         YM7883
         STM   RB,RET,IECREGSV          SAVE SETLOCK REGS        YM7883
         MODESET EXTKEY=SUPR            SETLOCK KEY              YM7883
EDI01455 SETLOCK OBTAIN,TYPE=LOCAL,MODE=UNCOND,                  YM7883*
               RELATED=(LOCAL,IFG0553P(EDI01466)) GET LOCAL LOCK YM7883
         MODESET EXTKEY=DATAMGT         RETURN TO D.M. KEY       YM7883
         LM    RB,RET,IECREGSV          RESTORE REGS             YM7883
*
         LA    RF,K1                    LOAD DECREMENT VALUE     Y02134
*
*        SHIFT DECREMENT TO USE WITH CS INSTRUCTION              Y02134
*
         SLL   RF,K24-(K8*((UCBDMCT-UCB)-(((UCBDMCT-UCB)/4)*4))) Y02134
         L     R0,UCB+(((UCBDMCT-UCB)/K4)*K4) ALIGN & LOAD WORD  Y02134
         EJECT
EDI01460 EQU   *                        DECREMENT IF NONZERO     Y02134
         LR    RET,R0                   SAVE CS OPERAND          Y02134
         SLL   RET,K25                  LEAVE ONLY UCBDMCT COUNT Y02134
         LTR   RET,RET                  TEST FOR COUNT OF 0      Y02134
         BZ    EDI01464                 BRANCH IF ALREADY 0      Y02134
         LR    R1,R0                    LOAD OLD VALUE TO MODIFY Y02134
         SR    R1,RF                    ADD DECREMENT TO COUNT   Y02134
         MODESET EXTKEY=SUPR            UCB KEY                  Y02082
*                                                                Y02134
         CS    R0,R1,UCB+(((UCBDMCT-UCB)/K4)*K4) STORE NEW COUNT Y02134
*                                                                Y02134
         MODESET EXTKEY=DATAMGT         RETURN TO DATA MGMNT KEY Y02082
         BNE   EDI01460                 BRANCH IF NOT THE SAME   Y02134
         TM    UCBJBNR,UCBVRDEV         IS IT VIO UNIT           YM7883
         BZ    EDI01464                 BRANCH IF NO             YM7883
*
*        VIO UNIT - WICLOSE IF DATA MANAGEMENT COUNT NOW ZERO    YM7883
*
         SLL   R1,K25                   GET COUNT IN REG         YM7883
         LTR   R1,R1                    IS COUNT ZERO            YM7883
         BNZ   EDI01464                 BRANCH IF NOT            YM7883
         LA    R1,VDSUCB-VDSCB(,RUCB)   GET VDSCB ADDR           YM7883
         L     RD,DXXPREFX              GET PREFIX ADDR          YM7883
         LA    RD,IECREGSV-IECPREFX(,RD) GET REG SAVE AREA       YM7883
         MODESET EXTKEY=SUPR            GET IN KEY ZERO          YM7883
         WICLOSE VDSCB=(1)              WICLOSE DATA SET         YM7883
         MODESET EXTKEY=DATAMGT         RETURN TO D.M. KEY       YM7883
         EJECT
EDI01464 EQU   *                        RELEASE LOCK             YM7883
         OI    DXATEOV,DXATNVOL         INDIC CURR UNIT INACTIVE YM6520
         L     RF,DXXPREFX              GET PREFIX ADDR          YM7883
         STM   RB,RET,IECREGSV          SAVE MODESET REGS        YM7883
         MODESET EXTKEY=SUPR            GET IN KEY ZERO          YM7883
EDI01466 SETLOCK RELEASE,TYPE=LOCAL,    RELEASE LOCAL LOCK       YM7883*
               RELATED=(LOCAL,IFG0553P(EDI01455))                YM7883
         MODESET EXTKEY=DATAMGT         RETURN TO D.M. KEY       YM7883
         LM    RB,RET,IECREGSV          RESTORE MODESET REGS     YM7883
         DROP  RF                                                YM7883
         SPACE 2
*
*        DATA MANAGEMENT COUNT DECREMENTATION COMPLETED
*
         LA    RF,ID3P4A                SET UP FOR MOUNT VERIFY  Y02134
         MVC   DXRETMOD,ID3P3X          SET UP IFG0553X RETURN   Y02134
*                                                                Y02134
EDI02000 EQU   *                        ENTRY CODE 0 FOR NEXT MODULE
         SR    RET,RET                  SET RETURN CODE TO ZERO
EDI02025 EQU   *                        GIVE NEXT MODULE CONTROL
         LA    RWTGC,DXXENTRY           NEXT MODULE ID/TTR
         LA    RWTG,DXXAREA             CURRENT MODULE IN WORKAREA
         IECRES LOAD,MODID=(RF),BRCODE=(RET),BRANCH=QUEUED       Y02080*
                                        ENTER RESIDENT ROUTINE   Y02080
         EJECT
EDI02050 EQU   *                        SUBROUTINE TO RELOAD REGS 6, 7
         LA    RWTGC,DXXENTRY           NEXT MODULE ID/TTR
         LA    RWTG,DXXAREA             CURRENT MODULE IN WORKAREA
         BR    RET                      RETURN TO CALLER
         SPACE 2
*
*        READ DSCB FROM VTOC USING BBCCHHR IN DEB
*
EDI02200 EQU   *                        SUBROUTINE ENTRY POINT  SA57255
         LA    R1,K15                   DECREMENT FROM DEB      SA57255
*                                       ADDR TO DSCB BBCCHHR    SA57255
         SR    RDEB,R1                  ADDR IN PREFIX PORTION  SA57255
         MVC   DXDAADDR+K1(K7),0(RDEB)  TRK ADDR OF DSCB IN IOB SA57255
         MVI   DXDAADDR,K0              SET M=0                 SA57255
*                                                               SA57255
*   THE FOLLOWING SUBROUTINE SETS UP A CHANNEL PROGRAM TO READ  SA57255
*   THE DSCB OR VOLUME LABEL INTO THE WORKAREA.                 SA57255
*   IN THIRD CCW.                                               SA57255
*                   SEARCH ID EQUAL     IOBDADAD+3   C,SILI   5 SA57255
*                   TIC                 *-8         -         - SA57255
*                   READ DATA           WORKAREA    -        96 SA57255
*                                                               SA57255
*                                                               SA57255
*                                                               SA57255
         XC    DXCCW1(K24),DXCCW1       CLEAR 3 CCW'S           SA57255
         LA    R0,DXDAADDR+K3           ADDRESS OF IOBDADAD+3   SA57255
         ST    R0,DXCCW1                SEARCH ADDR IN CCW1     SA57255
         LA    R0,DXCCW1                ADDR SEARCH CCW         SA57255
         ST    R0,DXCCW2                PUT IN CCW2             SA57255
         ST    RCORE,DXCCW3             WORKAREA ADDR IN CCW3   SA57255
         MVI   DXCCW1,CCWSCHID          SEARCH ID COMMAND CODE  SA57255
         MVC   DXCCW1+K4(K5),ACCW10I    REST OF CCW1 AND TIC    SA57255
*                                       COMMAND                 SA57255
         MVI   DXCCW3,CCWMTRD           READ DATA COMMAND       SA57255
         MVI   DXCCW3+K7,K96            LENGTH IS 96 BYTES      SA57255
         L     RDEB,DCBDEBAD            RESTORE USER'S DEB      SA57255
*                                       ADDRESS                 SA57255
*                                                               SA57255
         BR    RD                       RETURN TO CALLER        SA57255
         EJECT
*
*        CONSTANTS
*
         DS    0F
ACCW10I  DC    X'6000000508'            SILI,COMMAND CHAIN,5 CHAR,TIC O
COLCDU0I DC    X'000000007FFF7FFF7FFF'  EXTENT TO COVER ENTIRE DISK
MASKFL0I DC    F'1'                     MASK TO TEST DCBIOBAD
ULEXIT   DC    X'03'                    UTL EXIT LIST CODE BYTE
         DC    X'83'                    UTL EXIT LIST CODE BYTE
*                                       + LAST
SULDFR0I DC    X'0C'                    DEFER UTL PROC CODE BYTE
         DC    X'8C'                    DEFER UTL PROC CODE BYTE
*                                       +LAST
         EJECT
         XCTLTABL ID=(LOAD553P,3P,LOAD554L,4L,LOAD0P,0P,         Y02080X
               LOAD552X,2X,ID3P4A,4A,ID3P3X,3X,LOAD200I,3B),     Y02134X
               SVC=055,BRT=YES,LENGTH=                           Y02080
         IECDSECS TIOT,DCB,DEB,UCB,MAIN,WTG,PREFX,CVT,PSA,       YM7883*
               DSAB,EXPAND=YES                                 @ZA01342
         IDDVDSCB                                                Y02132
         IECEQU
         END
