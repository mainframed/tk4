   TITLE 'IGC0010C - SVC 103 - EBCDIC/ASCII TRANSLATE SVC ROUTINE'
IGC0010C CSECT
*
*        VS2 RELEASE 03 DELETIONS/CHANGES
*0000796000                                                     ZA00005
*        VS2 RELEASE 02 DELETIONS/CHANGES
*0000220000,292500,552000-560000                                 YM1440
*
*
*
*
***********************************************************************
*                                                                     *
* MODULE NAME = IGC0010C (OS/VS2)                                     *
*                                                                     *
* DESCRIPTIVE NAME = SVC 103 - EBCDIC/ASCII TRANSLATE SVC ROUTINE     *
*                                                                     *
* COPYRIGHT = NONE                                                    *
*                                                                     *
* STATUS = RELEASE 2, LEVEL 0                                         *
*                                                                     *
* FUNCTION =                                                          *
*        1. TRANSLATE FROM ASCII CODE TO EBCDIC CODE OR FROM          *
*        EBCDIC TO ASCII CODE DEPENDING UPON BIT 0 OF REGISTER 1:     *
*           BIT 0 = 0 ASCII TO EBCDIC,                                *
*           BIT 0 = 1 EBCDIC TO ASCII.                                *
*                                                                     *
*        2. REGISTER 1 ALSO CONTAINS THE ADDRESS OF THE CODE TO BE    *
*        TRANSLATED, AND REGISTER 0 CONTAINS THE NUMBER OF BYTES TO   *
*        BE TRANSLATED.                                               *
*                                                                     *
*        3. TRANSLATION IS BY TABLE LOOKUP WITH THE BYTE BEING        *
*        TRANSLATED USED AS AN INDEX VALUE TO OBTAIN THE              *
*        APPROPRIATE TRANSLATED CODE. THE TRANSLATED CODE             *
*        REPLACES THE ORIGINAL CODE IN CORE. SUBSTITUTION CODE        *
*        IS SUPPLIED FOR ALL BYTES THAT HAVE NO STANDARD TRANSLATION. *
*                                                                     *
* NOTES = SEE BELOW                                                   *
*                                                                     *
*    DEPENDENCIES =                                                   *
*            CLASS ONE CHARACTER CODE.  THE EBCDIC CHARACTER CODE     *
*            WAS USED FOR ASSEMBLY.  THE MODULE MUST BE REASSEMBLED   *
*            IF A DIFFERENT CHARACTER SET IS USED FOR EXECUTION.      *
*                                                                     *
*    RESTRICTIONS = NONE                                              *
*                                                                     *
*    REGISTER CONVENTIONS =                                           *
*            REGISTER 0 CONTAINS THE NUMBER OF BYTES REMAINING        *
*            TO BE TRANSLATED.                                        *
*            REGISTER 1 POINTS TO THE DATA TO BE TRANSLATED.          *
*            REGISTER 3 POINTS TO THE CVT.                            *
*            REGISTER 4 POINTS TO THE TCB.                            *
*            REGISTER 6 IS THE BASE REGISTER.                         *
*                                                                     *
*    PATCH LABEL = IHB0008D (THIRD LABEL BEFORE ORG STATEMENT AT END  *
*                  OF LISTING).                                       *
*                                                                     *
* MODULE TYPE = CONTROL (OPEN, CLOSE, EOV DATA MANAGEMENT)            *
*                                                                     *
*    PROCESSOR = ASSEMBLER XF                                         *
*                                                                     *
*    MODULE SIZE = SEE EXTERNAL SYMBOL DICTIONARY OR LOC FIELD ON     *
*                  ORG STATEMENT AT END OF LISTING                    *
*                                                                     *
*    ATTRIBUTES = REENTRANT, REFRESHABLE, READ-ONLY, ENABLED,         *
*                 PRIVILEGED, SUPERVISOR STATE, DATA MANAGEMENT KEY,  *
*                 LINK PACK AREA RESIDENT/PAGEABLE.                   *
*                                                                     *
* ENTRY POINT = IGC0010C FROM SVC 103 (XLATE MACRO)                   *
*                                                                     *
*    PURPOSE = SEE FUNCTION                                           *
*                                                                     *
*    LINKAGE = THIS MODULE IS ENTERED VIA AN SVC 103.                 *
*                                                                     *
* INPUT = REGISTER 0 CONTAINS THE NUMBER OF BYTES TO BE TRANSLATED.   *
*         REGISTER 1 INDICATES THE DIRECTION OF TRANSLATION AND       *
*         CONTAINS THE ADDRESS OF THE DATA.                           *
*         REGISTER 3 POINTS TO THE CVT (INITIALIZED BY THE SLIH).     *
*         REGISTER 4 POINTS TO THE TCB (INITIALIZED BY THE SLIH).     *
*                                                                     *
* OUTPUT = THE TRANSLATED DATA REPLACES THE ORIGINAL DATA.            *
*          REGISTER 15 CONTAINS A RETURN CODE.                        *
*                                                                     *
* EXIT-NORMAL = RETURN TO CALLER WITH A RETURN CODE OF 0 IN REG 15.   *
*                                                                     *
* EXIT-ERROR = RETURN TO CALLER WITH AN ERROR CODE IN REGISTER 15:    *
*              4 = ZERO OR NEGATIVE NUMBER OF BYTES TO TRANSLATE      *
*              8 = INVALID DATA ADDRESS.                              *
*                                                                     *
* EXTERNAL REFERENCES = SEE BELOW                                     *
*                                                                     *
*    ROUTINES = SETLOCK                                               *
*               TESTAUTH                                              *
*                                                                     *
*    DATA AREAS = DATA AREA POINTED TO BY REGISTER 1 AT ENTRY.        *
*                                                                     *
*    CONTROL BLOCKS = CVT                                             *
*                     PSA                                             *
*                     RB                                              *
*                     TCB                                             *
*                                                                     *
* TABLES = TWO 256 BYTE TABLES CONTAIN THE TRANSLATION CODES USED     *
*          BY THIS MODULE.                                            *
*                                                                     *
* MACROS = IECDSECS                                                   *
*          IECRES                                                     *
*          MODESET                                                    *
*          XCTLTABL                                                   *
*                                                                     *
* CHANGE ACTIVITY = SEE CHANGES/DELETIONS AFTER CSECT STATEMENT.      *
*                                                                     *
***********************************************************************
         EJECT
*
*                        REGISTERS
*
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
BASE     EQU   6                        BASE REGISTER            YM1440
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
*
*                   SYMBOLIC REGISTER USAGE
*
BYTES    EQU   0                        NUMBER OF BYTES TO TRANSLATE
DATAD    EQU   1                        DIRECTION BIT--ADDR OF DATA
TRANSTAB EQU   2                        ADDRESS OF TRANSLATION TABLE
CVTAD    EQU   3                        CVT ADDRESS
TCBAD    EQU   4                        TCB ADDRESS
RETURN2  EQU   R7                       RETURN REGISTER          Y02082
RETURN   EQU   14                       RETURN REGISTER
VCBASE   EQU   R15                      VALIDITY CHECK RTN BASE  Y02082
RTNCODE  EQU   R15                      RETURN CODE REGISTER     Y02082
*                                                                     *
*********************************************************************
*                             MASKS AND EQUATES
*********************************************************************
*                                                                     *
P0       EQU   0                        CONSTANT OF ZERO
P256     EQU   256                      CONSTANT OF 256
CODE4    EQU   4                        CONDITION CODE OF 4
CODE8    EQU   8                        CONDITION CODE OF 8
*                                                                     *
*********************************************************************
*********************************************************************
         BALR  BASE,P0                  ESTABLISH ADDRESSABILITY
         USING *,BASE
         USING CVT,CVTAD                USING ON CVT             Y02082
         USING TCB,TCBAD                USING ON TCB             Y02082
*
* CHECK FOR POSITIVE NUMBER OF BYTES TO TRANSLATE
*
         LTR   BYTES,BYTES              TEST BYTE COUNT NOT ZERO OR NEG
         BP    VALIDCHK                 BRANCH IF POSITIVE
         LA    RTNCODE,CODE4            SET CONDITION CODE TO 4  Y02082
EXIT     IECRES EXIT                    RETURN                   Y02082
*
*        THE RESIDENT VALIDITY CHECK ROUTINE IS USED TO VERIFY   Y02082
*        THAT THE CALLERS AREA IS IN THE TCB KEY.                Y02082
*              REGISTER 1 - FIRST ADDRESS TO BE CHECKED          Y02082
*              REGISTER 2 - LAST ADDRESS TO BE CHECKED           Y02082
*              REGISTER 4 - CURRENT TCB                          Y02082
*              REGISTER 14 - RETURN ADDRESS                      Y02082
*              REGISTER 15 - ENTRY POINT OF ROUTINE              Y02082
*              THE LOCAL LOCK MUST BE HELD                       Y02082
*                                                                Y02082
VALIDCHK LA    R2,P0(DATAD)             LOAD ADDRESS OF DATA    SA51421
         AR    R2,BYTES                 ADD DATA LENGTH         SA51421
         BCTR  R2,P0                    R2 POINTS TO LAST BYTE   Y02082
*                                       TO BE TRANSLATED         Y02082
         LR    RETURN2,DATAD            SAVE DATAD               Y02082
         LR    R8,BYTES                 SAVE BYTES               Y02082
         TESTAUTH KEY=YES               IS CALLER IN SYSTEM KEY  Y02082
*                                                                Y02082
         LR    BYTES,R8                 RESTORE BYTES            Y02082
         LR    DATAD,RETURN2            RESTORE DATAD            Y02082
         LTR   RTNCODE,RTNCODE          TEST CODE                Y02082
         BZ    TSTDIREC                 BRANCH IF CALLER IN      Y02082
*                                       SYSTEM KEY               Y02082
         L     VCBASE,CVT0VL00          ENTRY POINT ADDRESS      Y02082
SETLOCK  EQU   *                        OBTAIN LOCAL LOCK        Y02082
         SETLOCK OBTAIN,MODE=UNCOND,TYPE=LOCAL,                  Y02082*
               RELATED=(RESVALCK,IGC0010C(FREELOCK))             Y02082
         BALR  RETURN,VCBASE            GO TO VALIDITY CHK RTN   Y02082
         BZ    ADDROK                   BRANCH-ADDRESS OK        Y02082
*                                                                Y02082
VCERR    EQU   *                        VALIDITY CHECK ERROR     Y02082
         LA    RETURN2,EXIT             RETURN FROM FREELOCK     Y02082
         LA    RTNCODE,CODE8            SET CONDITION CODE TO 8  Y02082
FREELOCK EQU   *                        RELEASE LOCAL LOCK       Y02082
         SETLOCK RELEASE,TYPE=LOCAL,                             Y02082*
               RELATED=(RESVALCK,IGC0010C(GETLOCK))              Y02082
         BR    RETURN2                  RETURN                   Y02082
*
* DETERMINE THE DIRECTION OF TRANSLATION
*
ADDROK   EQU   *                        ADDRESS IS OK            Y02082
         BAL   RETURN2,FREELOCK         RELEASE LOCAL LOCK       Y02082
         MODESET EXTKEY=RBT234,WORKREG=14 ASSUME CALLER'S KEY    Y02082
TSTDIREC EQU   *                        DETERMINE DIRECTION      Y02082
         LTR   DATAD,DATAD              TEST DIRECTION OF XLATE  Y02082
         BM    TOASC                    BRANCH IF NEGATIVE--TO USASCII
         LA    TRANSTAB,TABE            LOAD ADDRESS OF EBCDIC TABLE
         B     TRANS                    BRANCH TO TRANSLATE  LOOP
TOASC    LA    TRANSTAB,TABA            LOAD ADDRESS OF USASCII TABLE
*
*  TRANSLATE 256 BYTES AT A TIME
*
TRANS    S     BYTES,K256               DECREMENT BYTES COUNT
         BM    REST                     BRANCH TO TRANSLATE <256 BYTES
         TR    P0(P256,DATAD),P0(TRANSTAB) TRANSLATE 256 BYTES
         LA    DATAD,P256(DATAD)        INCREMENT DATA POINTER BY 256
         B     TRANS                    GO TRANSLATE DATA
*
* TRANSLATE LESS THEN 256 BYTES
*
REST     EQU   *                        TRANSLATE REMAINDER      Y02082
         SR    RTNCODE,RTNCODE          ZERO CONDITION CODE      Y02082
         A     BYTES,K256               RESTORE REMAINING COUNT  Y02082
         BZ    EXIT                     RETURN TO CALLER         Y02082
         BCTR  BYTES,P0                 DECREMENT BYTE COUNT
         LR    R8,BYTES                 LOAD REG FOR TRANSLATE   YM1440
         EX    R8,TRANS1                TRANSLATE LAST BYTES     YM1440
         B     EXIT                     RETURN TO CALLER         Y02082
*********************************************************************
*
* CONSTANTS
*
K256     DC    F'256'                   DECREMENT VALUE FOR LENGTH
TRANS1   TR    P0(P0,DATAD),P0(TRANSTAB) TRANSLATE LAST BYTES
*
*        TABLE FOR TRANSLATING FROM EBCDIC TO ASCII
*
TABA     DC    X'000102031A091A7F'      ASCII TABLE
ENT080FA DC    X'1A1A1A0B0C0D0E0F'        *
ENT1017A DC    X'101112131A1A081A'        *
ENT181FA DC    X'18191A1A1C1D1E1F'        *
ENT2027A DC    X'1A1A1A1A1A0A171B'        *
ENT282FA DC    X'1A1A1A1A1A050607'        *
ENT3037A DC    X'1A1A161A1A1A1A04'        *
ENT383FA DC    X'1A1A1A1A14151A1A'        *
ENT4047A DC    X'201A1A1A1A1A1A1A'        *
ENT484FA DC    X'1A1A5B2E3C282B21'        *
ENT5057A DC    X'261A1A1A1A1A1A1A'        *
ENT585FA DC    X'1A1A5D242A293B5E'        *
ENT6067A DC    X'2D2F1A1A1A1A1A1A'        *
ENT686FA DC    X'1A1A7C2C255F3E3F'        *
ENT7077A DC    X'1A1A1A1A1A1A1A1A'        *
ENT787FA DC    X'1A603A2340273D22'        *
ENT8087A DC    X'1A61626364656667'        *
ENT888FA DC    X'68691A1A1A1A1A1A'        *
ENT9097A DC    X'1A6A6B6C6D6E6F70'        *
ENT989FA DC    X'71721A1A1A1A1A1A'        *
ENTA0A7A DC    X'1A7E737475767778'        *
ENTA8AFA DC    X'797A1A1A1A1A1A1A'        *
ENTB0B7A DC    X'1A1A1A1A1A1A1A1A'        *
ENTB8BFA DC    X'1A1A1A1A1A1A1A1A'        *
ENTC0C7A DC    X'7B41424344454647'        *
ENTC8CFA DC    X'48491A1A1A1A1A1A'        *
ENTD0D7A DC    X'7D4A4B4C4D4E4F50'        *
ENTD8DFA DC    X'51521A1A1A1A1A1A'        *
ENTE0E7A DC    X'5C1A535455565758'        *
ENTE8EFA DC    X'595A1A1A1A1A1A1A'        *
ENTF0F7A DC    X'3031323334353637'        *
ENTF8FFA DC    X'38391A1A1A1A1A1A'        *
*
*        TABLE FOR TRANSLATING FROM ASCII TO EBCDIC
*
TABE     DC    X'00010203372D2E2F'      EBCDIC TABLE
ENT080FE DC    X'1605250B0C0D0E0F'        *
ENT1017E DC    X'101112133C3D3226'        *
ENT181FE DC    X'18193F271C1D1E1F'        *
ENT2027E DC    X'404F7F7B5B6C507D'        *
ENT282FE DC    X'4D5D5C4E6B604B61'        *
ENT3037E DC    X'F0F1F2F3F4F5F6F7'        *
ENT383FE DC    X'F8F97A5E4C7E6E6F'        *
ENT4047E DC    X'7CC1C2C3C4C5C6C7'        *
ENT484FE DC    X'C8C9D1D2D3D4D5D6'        *
ENT5057E DC    X'D7D8D9E2E3E4E5E6'        *
ENT585FE DC    X'E7E8E9ADE0BD5F6D'        *                     ZA00005
ENT6067E DC    X'7981828384858687'        *
ENT686FE DC    X'8889919293949596'        *
ENT7077E DC    X'979899A2A3A4A5A6'        *
ENT787FE DC    X'A7A8A9C06AD0A107'        *
ENT8087E DC    X'3F3F3F3F3F3F3F3F'        *
ENT888FE DC    X'3F3F3F3F3F3F3F3F'        *
ENT9097E DC    X'3F3F3F3F3F3F3F3F'        *
ENT989FE DC    X'3F3F3F3F3F3F3F3F'        *
ENTA0A7E DC    X'3F3F3F3F3F3F3F3F'        *
ENTA8AFE DC    X'3F3F3F3F3F3F3F3F'        *
ENTB0B7E DC    X'3F3F3F3F3F3F3F3F'        *
ENTB8BFE DC    X'3F3F3F3F3F3F3F3F'        *
ENTC0C7E DC    X'3F3F3F3F3F3F3F3F'        *
ENTC8CFE DC    X'3F3F3F3F3F3F3F3F'        *
ENTD0D7E DC    X'3F3F3F3F3F3F3F3F'        *
ENTD8DFE DC    X'3F3F3F3F3F3F3F3F'        *
ENTE0E7E DC    X'3F3F3F3F3F3F3F3F'        *
ENTE8EFE DC    X'3F3F3F3F3F3F3F3F'        *
ENTF0F7E DC    X'3F3F3F3F3F3F3F3F'        *
ENTF8FFE DC    X'3F3F3F3F3F3F3F3F'        *
*********************************************************************
*
         XCTLTABL SVC=103,BRT=YES,LENGTH=                        Y02080
         IECDSECS CVT,PSA,RB,TCB,EXPAND=YES                      Y02082
         END
