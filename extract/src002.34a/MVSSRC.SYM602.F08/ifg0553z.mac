         TITLE ' EOV INPUT DA FINAL EXIT MODULE -  IFG0553Z '
***********************************************************************
*                                                                     *
*                                                                     *
*        VS 2 RELEASE 03 DELETIONS/CHANGES                            *
*0000671500,672000,684500                                       ZA01290
*          VS2 RELEASE 02 DELETIONS                                   *
*                                                                     *
*                                                                     *
***********************************************************************
*                                                                     *
* MODULE NAME = IFG0553Z (OS/VS2)                                     *
*                                                                     *
* DESCRIPTIVE NAME =                                                  *
*        EOV INPUT DA FINAL EXIT MODULE                               *
*                                                                     *
* COPYRIGHT = NONE                                                    *
*                                                                     *
* STATUS = RELEASE 2, LEVEL 0                                         *
*                                                                     *
* FUNCTION =                                                          *
*        THIS MODULE IS GIVEN CONTROL FROM IFG0553X AFTER BUILDING    *
*        A NEW DEB FOR A NEW VOLUME.                                  *
*                                                                     *
*        1) IF USER LABEL PROCESSING IS REQUIRED, MODULE IFG0554L     *
*        IS GIVEN CONTROL WITH BITS 5 AND 6 OF JFCBMASK+5 SET TO 1    *
*        AND DXRETMOD SET TO RETURN TO THIS MODULE.                   *
*                                                                     *
*        2) UPON RETURN FROM USER LABEL PROCESSING OR IF IT WAS NOT   *
*        REQUIRED, THE DCBFDAD IS INITIALIZED FROM THE FIRST EXTENT   *
*        IN THE DEB. IF CONCATENATION TO A MEMBER NAME A BLDL IS      *
*        PERFORMED TO OBTAIN THE TTR OF THE MEMBER. THIS IS PASSED    *
*        TO THE TTR-MBBCCHHR RESIDENT CONVERT ROUTINE TO OBTAIN AN    *
*        MBBCCHHR WHICH IS PLACED IN DCBFDAD.                         *
*                                                                     *
*        3) IF AN EXCP DCB, THE USER DCB IS REFRESHED, THE EOV        *
*        WORKAREA IS FREED AND THE CALLER IS EXITED TO.               *
*                                                                     *
*        4) IF NOT EXCP, THE BSAM/QSAM EXECUTOR IS TRANSFERRED        *
*        CONTROL.                                                     *
         EJECT
* NOTES =                                                             *
*        SEE BELOW.                                                   *
*                                                                     *
*    DEPENDENCIES =                                                   *
*            CLASS ONE CHARACTER CODE.  THE EBCDIC CHARACTER CODE     *
*            WAS USED FOR ASSEMBLY.  THE MODULE MUST BE REASSEMBLED   *
*            IF A DIFFERENT CHARACTER SET IS USED FOR EXECUTION.      *
*                                                                     *
*    RESTRICTIONS = NONE                                              *
*                                                                     *
*    REGISTER CONVENTIONS =                                           *
*            R2 POINTS TO DCB                                         *
*            R4 POINTS TO OPEN WORK AREA                              *
*            R5 POINTS TO THE RESIDENT ROUTINE                        *
*            R6 POINTS TO THE WTG TABLE                               *
*            R7 POINTS TO THE CURRENT PARAMETER LIST ENTRY            *
*            R8 POINTS TO THE CURRENT WTG TABLE ENTRY                 *
*            R9 POINTS TO THE DD ENTRY IN THE TIOT                    *
*            R10 POINTS TO THE UCB                                    *
*                                                                     *
*    PATCH LABEL = SEE THIRD FROM LAST LABEL BEFORE ORG STATEMENT AT  *
*                  END OF LISTING.                                    *
*                                                                     *
* MODULE TYPE = CONTROL (OPEN, CLOSE, EOV DATA MANAGEMENT)            *
*                                                                     *
*    PROCESSOR = ASSEMBLER XF                                         *
*                                                                     *
*    MODULE SIZE = SEE EXTERNAL SYMBOL DICTIONARY OR LOC FIELD ON     *
*                  ORG STATEMENT AT END OF LISTING                    *
*                                                                     *
*    ATTRIBUTES = REENTRANT, REFRESHABLE,READ-ONLY, ENABLED,          *
*                 PRIVILEGED, SUPERVISOR STATE, DATA MANAGEMENT KEY,  *
*                 LINK PACK AREA RESIDENT/PAGEABLE                    *
*                                                                     *
* ENTRY POINT =                                                       *
*        IFG0553Z                                                     *
*                                                                     *
*    PURPOSE = SEE FUNCTION                                           *
*                                                                     *
*    LINKAGE =                                                        *
*        THIS MODULE IS TRANSFERRED CONTROL THROUGH THE IECRES-LOAD   *
*        MACRO INSTRUCTION.                                           *
         EJECT
* INPUT =                                                             *
*        GIVEN CONTROL IN PROTECT KEY 5.                              *
*        REGISTER 2 POINTS TO THE COPIED DCB.                         *
*        DEBDCBAD POINTS TO THE COPIED DCB.                           *
*        REGISTER 4 POINTS TO THE EOV WORKAREA                        *
*                                                                     *
*        FROM IFG0553X - BITS 5&6 IN JFCBMASK+5 SET TO ZERO.          *
*                                                                     *
*        FROM IFG0554L - BITS 5&6 IN JFCBMASK+5 SET TO ONE.           *
*                                                                     *
* OUTPUT =                                                            *
*        THE NEXT MODULE IS GIVEN CONTROL IN PROTECT KEY 5 WITH       *
*        REGISTER 2 POINTING TO THE COPIED DCB,                       *
*        AND REGISTER 4 POINTING TO THE EOV WORKAREA,                 *
*                                                                     *
*        IFG0554L -                                                   *
*        DXRETMOD SET TO ID-VCON OF IFG0553Z.                         *
*        REGISTER 14 SET TO ZERO.                                     *
*                                                                     *
*        IFG0551L -                                                   *
*        DEBDCBAD POINTS TO USER'S DCB.                               *
*                                                                     *
*        IFG0550P -                                                   *
*        INTERNAL ABEND CODE IN REGISTER 0.                           *
*                                                                     *
*        EXIT TO CALLER OF EOV -                                      *
*        REGISTER 15 SET TO ZERO.                                     *
*        EOV WORKAREA(S) FREED.                                       *
*        DEB POINTS TO USER'S DCB.                                    *
*        USER DCB REFRESHED FROM COPIED DCB.                          *
*                                                                     *
* EXIT-NORMAL =                                                       *
*        IFG0554L TO PROCESS USER HEADER LABELS.                      *
*        IFG0551L TO COMPLETE EOV PROCESSING.                         *
*                                                                     *
* EXIT-ERROR =                                                        *
*        IFG0550P WITH AN INTERNAL ABEND CODE IN REGISTER 0 -         *
*        218 - CAUSING 737-24 ABEND - MEMBER NAME NOT FOUND.          *
*        171 - CAUSING 737-0C ABEND - I/O ERROR READING DIRECTORY.    *
         EJECT
* EXTERNAL REFERENCES = SEE BELOW                                     *
*                                                                     *
*    ROUTINES =                                                       *
*        IFG019RA THROUGH THE IECRES MACRO.                           *
*                                                                     *
*        IECPCNVT - TTR TO MBBCCHHR CONVERT ROUTINE                   *
*                                                                     *
*    DATA AREAS =                                                     *
*        EOV WORKAREA.                                                *
*                                                                     *
*    CONTROL BLOCK =                                                  *
*        DCB                                                          *
*        TCB                                                          *
*        CVT                                                          *
*        DEB                                                          *
*        DEB EXTENSION                                                *
*                                                                     *
* TABLES =                                                            *
*        NONE.                                                        *
*                                                                     *
* MACROS =                                                            *
*        MODESET                                                      *
*        BLDL                                                         *
*        IECRES-LOAD                                                  *
*        IECRES-INIT                                                  *
*        IECRES-FREE                                                  *
*        IECRES-EXIT                                                  *
*        DMABCOND                                                     *
*        XCTLTABL                                                     *
*        IECDSECS                                                     *
*        IECEQU                                                       *
*                                                                     *
* CHANGE ACTIVITY =                                                   *
*        SEE CHANGES/DELETIONS SECTION JUST AFTER CSECT CARD.         *
*                                                                     *
***********************************************************************
         EJECT
IFG0553Z CSECT
ERR171   EQU   171                      CODE FOR 737-0C ABEND
ERR218   EQU   218                      CODE FOR 737-24 ABEND   SA58102
*
         BALR  RBASE,0                  ESTABLISH ADDRESSABILITY
         USING FORCORE,RCORE
         USING WTG,RWTG                 BASE FOR WTG TABLE       Y02080
         USING DEBBASIC,RDEB                                     YM1272
         USING IHADCB,RDCB
         USING CVT,RF
         USING *,RBASE
         B     EDI10925(RET)            BR ACCORDING TO RETURN CODE
EDI10925 EQU   *                        DETERMINE ENTRY CONDITION
         B     EDI10950                 CONTINUE EOV PROCESSING  YM1193
         NOP   EDI10925                 NO ENTRY OFFSET 4        YM1193
*                                                                     *
***** USER HEADER LABEL PROCESSING SECTION                        *****
*                                                                     *
EDI10950 EQU   *                        TEST IF RETURN FROM SUL  Y02082
         TM    JFCBMASK+K5,K6
*                                       TEST IF RETURNING FROM UL
         BO    EDI11300                 PROCESSING, BRANCH
*        DCBIOBAD CAN BE TESTED ONLY IF NOT EXCP AND NOT CHAINED YM1194
*        SCHEDULING                                              YM1194
         TM    DCBMACRF,DCBMEXCP        IS THIS AN EXCP DCB      YM1194
         BO    EDI10960                 BR IF YES-NO DCBCIND2    YM1194
         TM    DCBCIND2,DCBC2CHN        IS CHAINED SCHEDULING    YM1194
         BO    EDI10960                 BR IF YES                YM1194
         TM    DCBIOBAD,MASKFL9J        HAVE UL BEEN PROCESSED
         BO    EDI11200                 YES,BRANCH
EDI10960 EQU   *                        SEE IF SUL               YM1194
         TM    JFCBLTYP,JFCSUL          CHECK IF 'SUL' SPECIFIED
         BNO   EDI11400                 BRANCH IF NOT SUL
         TM    DCBMACRF,DCBMEXCP        IS THIS AN EXCP DCB      YM1194
         BNO   EDI10975                 BR IF NO                 YM1194
         TM    DCBMACRF,DCBMRCI         IS DSORG PRESENT         YM1194
         BZ    EDI10980                 BRANCH IF NO             YM1194
EDI10975 EQU   *                        SUL ONLY FOR PS          YM1194
         TM    DCBDSORG,DCBORGPS        TEST,BRANCH IF DSORG IS NOT
         BZ    EDI11400                 PHYSICAL SEQUENTIAL
EDI10980 EQU   *                        EXCP MAY NOT HAVE EXLST  YM1194
         TM    DCBMACRF,DCBMEXCP        IS THIS AN EXCP DCB      YM1194
         BNO   EDI10990                 BR IF NO                 YM1194
         TM    DCBMACRF,DCBMFOUN        IS EXLST PRESENT         YM1194
         BZ    EDI11400                 BRANCH IF NO             YM1194
         EJECT
*
*        USER LABEL PROCESSING REQUESTED
*
EDI10990 EQU   *                        SEARCH EXIT LIST         YM1194
         L     RC,DCBEXLST              PICK UP EXIT LIST FROM DCB
         LA    RC,K0(K0,RC)             ZERO HI BYTE OF EXIT LIST
         LTR   RC,RC                    IS EXIT LIST ADDR ZERO     UL17
         BZ    EDI11400                 YES, SKIP USER LABELS
EDI11000 EQU   *                        LOOK FOR EXIT
         MODESET KEYADDR=DXUKEY,WORKREG=7 EXIT LIST IN USER      Y02082
         CLI   K0(RC),CSWUNITX          IS THIS ACTIVE EXIT ONE
         BE    EDI11100                 GO TO PROCESS USER LABELS
         CLI   K0(RC),CSWUNITX+LASTNTRY  SEE IF LAST INDICATOR  ON
         BE    EDI11100                 GO TO PROCESS USER LABELS
         TM    K0(RC),LASTNTRY          IS THIS LAST ENTRY IN LIST
         LA    RC,K4(K0,RC)             POINT TO NEXT ENTRY
         BZ    EDI11000                 NO,BRANCH
         MODESET EXTKEY=DATAMGT         RETURN TO O/C/E KEY      Y02082
         B     EDI11400                 NO EXIT FOR UL
EDI11100 EQU   *                        EXIT FOUND - SEE IF ZERO
         L     RC,K0(K0,RC)             PICK UP USER'S EXIT ADDRESS
         MODESET EXTKEY=DATAMGT         DATA MANAGEMENT KEY      YM1291
         LA    R7,K0(RC)                CLEAR HI ORDER BYTE
         LTR   R7,R7                    IS EXIT ADDRESS ZERO
         BZ    EDI11400                 YES, BYPASS UL PROCESSING
         OI    JFCBMASK+K5,K6           SET EOV BITS TO IFG0554L
         MVC   DXRETMOD(K5),LOAD3Z      RETURN ID/TTR
         LA    RF,LOAD4L                ID/TTR OF UTL ROUTINE
         SR    RET,RET                  SET RETURN CODE
         B     EDI11900                 TO NEXT MODULE VIA RES ROUTINE
EDI11200 EQU   *                        SET DCBIOBAD TO 1
         XC    DCBIOBAD,DCBIOBAD
         OI    DCBIOBAD+K3,K1
         B     EDI11400                 BRANCH
EDI11300 EQU   *                        RETURN FROM USER LABEL MODULE
         NI    JFCBMASK+K5,K255-K6      TURN OFF UL ENTRY FLG
         EJECT
EDI11400 EQU   *                        NO USER LABEL PROCESSING
         NI    DCBOFLGS,ALLBITS-DCBOFEOV-DCBOFTM RESET TPMRK AND CONCAT
*                                       SWITCHES
         TM    DCBMACRF,DCBMEXCP        IS THIS AN EXCP DCB
         BNO   EDI11500                 NO,GO TO SET UP FULL DISK ADDR
         TM    DCBMACR2,DCBMR5WD+DCBMR4WD DOES DA DEVICE DEPEND EXIST
         BZ    EDI11600                 NO, GO EXIT              M0064
*
*        SET DCBFDAD TO FIRST EXTENT IN NEW DEB
*
EDI11500 EQU   *                        SET DCBFDAD FROM DEB
         MVI   DCBFDAD,K0               SET M TO ZERO IN DCB FULL DISK
*                                       ADDRESS
         LA    R1,DEBBASND              PT TO DA SECT            YM1272
         USING DEBDASD,R1               BASE TO DA SECTION       YM1272
         MVC   DCBFDAD+K1(K6),DEBBINUM  SET BBCCHH IN DCB FULL DISK ADR
         DROP  R1                                                YM1272
         MVI   DCBFDAD+K7,K0            SET RECORD NUMBER TO ZERO
*                                                                     *
****    TEST FOR CONCATENATION OF MEMBERS WITH BSAM
*                                                                     *
         TM    JFCBIND1,JFCBPMEM        YES,JFCB SPECIFY PDS MEMBER
         BNO   EDI11600                 NO,CONTINUE NORMAL PROCESSING
         CLI   JFCBELNM,BLANK           WAS THE MEMBER SPECIFIED
         BC    12,EDI11600              NO,CONTINUE
         TM    DSCFILTY,DS1ORGPO        DSCB SPECIFY PO
         BC    12,EDI11600              NO,CONTINUE
         EJECT
*
*        BPAM MEMBER BEING CONCATENATED TO
*              ISSUE BLDL TO LOCATE (OBTAIN TTR) MEMBER
*              CONVERT TTR TO MBBCCHHR AND PLACE IN DCBFDAD
*
         XC    DXCCW10(K4),DXCCW10      CLEAR BLDL LIST
         MVC   DXCCW10+K4(K8),JFCBELNM  SET MEMBER NAME IN BLDL LIST
         MVI   DXCCW10+K1,K1            YES,SET BLDL LIST TO INE ENTRY
         MVI   DXCCW10+K3,K12           INDICATE THAT BLDL LIST 12 BYTE
         LA    R0,DXCCW10               SET REGISTER 0 TO BLDL LIST LOC
         SPACE 2
         BLDL  0(RDCB),(0)              FIND THE TTRN OF THE MEMBER
         SPACE 2
         LTR   RF,RF                    DID BLDL DETECT AN ERROR
         BNE   EDI12000                 YES, ISSUE ABEND
         L     R0,DXCCW10+K12           GET MEMBER TTR BLDL RETURNED
         SH    R0,ANUM1                 DECREMENT 1 FROM R
         LR    R1,RDEB                  DEB ADDRESS TO PARAMETER REG
         ST    RDCB,DXCCW7              SAVE THE DCB ADDRESS
         LA    RDCB,DCBFDAD             ADDRESS FOR RESULT OF CONVERT
         STM   RTIOT,RD,DXCCW8          SAVE REGISTERS
         L     RF,CVTPTR                ADDRESS OF CVT
         L     RF,CVTPCNVT              TTR TO MBBCCHHR CONVERT ROUTINE
         BALR  RET,RF                   GO TO THE CONVERT ROUTINE
         L     RDCB,DXCCW7              RESTORE THE DCB ADDRESS
         LM    RTIOT,RD,DXCCW8          RESTORE REGISTERS
         EJECT
EDI11600 EQU   *                        EXIT
*                                                                     *
*****  DETERMINE IF BSAM/QSAM EXECUTOR REQUIRED                   *****
*                                                                     *
EDI11725 EQU   *                        POINT DEB TO USER DCB    Y02080
         MVC   DEBDCBAD+K1(L'DEBDCBAD-K1),DXUDCBAD+K1 POINT DEB  Y02082*
                                        TO USER DCB              Y02082
         L     R1,DXDEBXAD              GET PTR TO DEB EXTENSION YM1272
         NI    DEBXFLG1-DEBXTN(R1),ALLBITS-DEBXCDCB DEB PT USER  YM1272
         TM    DCBMACRF,DCBMEXCP        IS THIS AN EXCP DCB
         BC    K12,EDI11800             NO,GO TO ACCESS METHOD EXECUTOR
*
*        EXCP - EXIT TO USER
*
         NI    DCBOFLGS,ALLBITS-DCBOBUSY  RESET I/O SUPPORT BIT
*                                                                Y02082
*        COPY DCB TO USER'S STORAGE                              Y02082
*                                                                Y02082
         IECRES INIT,DCBCOPY=FRWKAR,STM=(3,14,DXXPREFX)          Y02082
*                                                                Y02082
         IECRES FREE,A=(RCORE),PREFIX=EOV FREE EOV WORK AREA    ZA01290
         SR    RF,RF                    SET RETURN CODE TO 0     Y02080
         IECRES EXIT                    RETURN                   Y02080
         EJECT
*
*        GIVE EOV ACCESS METHOD EXECUTOR CONTROL
*
EDI11800 EQU   *                        SET UP EOV WTG TABEL
         MVC   DXXMODID(K2),LOAD1L                               Y02080
*                                       MOVE ID OF SAM EXEC TO WORKAREA
         MVC   DXXMODEP+K1(K3),LOAD1L+K2                         Y02080
*                                       MOVE TTR TO WORKAREA
         SR    RF,RF                    ZERO WORK REG           YM0992
         IC    RF,LOAD1L+K5             GET MODULE LENGTH       YM0992
         IECRES LOAD,PREFIX=DXXPREFX,BRANCH=DIRECT XCTL          Y02080
         EJECT
EDI11900 EQU   *                        GO TO USER LABEL MODULE
         BAL   RD,EDI12300              SET UP REGS RWTG AND RWTGC
         IECRES LOAD,MODID=(RF),BRCODE=(RET),BRANCH=QUEUED       Y02080
         SPACE 2
EDI12000 EQU   *                        ABEND ROUTINE
         BAL   RD,EDI12300              SET UP REGS RWTG AND RWTGC
         LA    RD,K4                    MISSING ENTRY CODE      SA58102
         CR    RF,RD                    DID BLDL DETECT MISSING SA58102
*                                       ENTRY                   SA58102
         BNE   EDI12100                 NO, I/O ERROR           SA58102
         DMABCOND ERR218,LOAD0P         GO TO PROBLEM DETERM.   SA58102
EDI12100 EQU   *                        I/O ERROR               SA58102
         DMABCOND ERR171,LOAD0P         GO TO PROBLEM DETERMINATION
         SPACE 2
EDI12300 EQU   *                        REESTABLISH REGISTERS 6, 8
         LA    RWTGC,DXXENTRY           CURRENT MODULE NAME
         LA    RWTG,DXXAREA             NEXT MODULE NAME
         BR    RD                       RETURN TO CALLER
         EJECT
*        CONSTANTS
*
ANUM1    DC    X'0100'                  DECREMENTS BLDL'S TTR BY 1
         SPACE 2
         XCTLTABL ID=(LOAD0P,0P,LOAD1L,1L,LOAD3Z,3Z,LOAD4L,4L),  YM1193X
               SVC=055,BRT=YES,LENGTH=                           YM1193
         IECDSECS CVT,TCB,DCB,IEZDEB,MAIN,WTG,PREFX,EXPAND=YES   YM1272
         IECEQU IEZDEB=YES                                       YM1272
         END
