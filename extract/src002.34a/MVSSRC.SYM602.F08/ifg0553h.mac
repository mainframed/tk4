         TITLE 'IFG0553H - EOV TAPE INPUT STD HEADER LABEL FUNCTION'
         COPY  LCGASMSW
IFG0553H CSECT
         ENTRY IGG0550D                                          Y02080
IGG0550D EQU   IFG0553H                 ALLIAS ENTRY POINT       Y02080
***********************************************************************
*                                                                     *
*                                                                     *
*          VS2 RELEASE 03 DELETIONS/CHANGES                           *
*                                                                     *
*101700,104980                                                 @ZA03195
*0000                                                          @ZA13589
*                                                                     *
*          VS2 RELEASE 02 DELETIONS                                   *
*                                                                     *
*                                                                     *
***********************************************************************
*                                                                     *
* MODULE NAME = IFG0553H (OS/VS2)                                     *
*                                                                     *
* DESCRIPTIVE NAME = INPUT TAPE - STANDARD HEADER LABEL FUNCTION      *
*                                                                     *
* COPYRIGHT = NONE                                                    *
*                                                                     *
* STATUS = RELEASE 2, LEVEL 0                                         *
*                                                                     *
* FUNCTION =                                                          *
*        1) ENTRY FROM NSLEHDRI- IF NSLEHDRI INDICATED THAT THE       *
*        WRONG NSL VOLUME HAD BEEN MOUNTED, CONTROL IS TRANSFERRED    *
*        TO IFG0194A TO PERFORM THE DISMOUNT/REMOUNT FUNCTION         *
*        AGAIN.                                                       *
*        2) FOR EXCP, THE DCB COPY IS PERFORMED, THE EOV WORK AREA    *
*        IS FREED, AND CONTROL IS RETURNED TO THE CALLER OF EOV.      *
*        3) IN ALL OTHER CASES, CONTROL IS PASSED TO IFG0551L,        *
*        THE ACCESS METHOD EXECUTOR.                                  *
*                                                                     *
* NOTES = SEE BELOW                                                   *
*                                                                     *
*    DEPENDENCIES =                                                   *
*            CLASS ONE CHARACTER CODE.  THE EBCDIC CHARACTER CODE     *
*            WAS USED FOR ASSEMBLY.  THE MODULE MUST BE REASSEMBLED   *
*            IF A DIFFERENT CHARACTER SET IS USED FOR EXECUTION.      *
*                                                                     *
*    RESTRICTIONS = NONE                                              *
*                                                                     *
*    REGISTER CONVENTIONS =                                           *
*            R2 POINTS TO DCB                                         *
*            R4 POINTS TO OPEN WORK AREA                              *
*            R5 POINTS TO THE RESIDENT ROUTINE                        *
*            R6 POINTS TO THE WTG TABLE                               *
*            R7 POINTS TO THE CURRENT PARAMETER LIST ENTRY            *
*            R8 POINTS TO THE CURRENT WTG TABLE ENTRY                 *
*            R9 POINTS TO THE DD ENTRY IN THE TIOT                    *
*            R10 POINTS TO THE UCB                                    *
*                                                                     *
*    PATCH LABEL = SEE THIRD FROM LAST LABEL BEFORE ORG STATEMENT AT  *
*                  END OF LISTING.                                    *
*                                                                     *
* MODULE TYPE = CONTROL (OPEN, CLOSE, EOV DATA MANAGEMENT)            *
*                                                                     *
*    PROCESSOR = ASSEMBLER XF                                         *
*                                                                     *
*    MODULE SIZE = SEE EXTERNAL SYMBOL DICTIONARY OR LOC FIELD ON     *
*                  ORG STATEMENT AT END OF LISTING                    *
*                                                                     *
*    ATTRIBUTES = REENTRANT, REFRESHABLE,READ-ONLY, ENABLED,          *
*                 PRIVILEGED, SUPERVISOR STATE, DATA MANAGEMENT KEY,  *
*                 LINK PACK AREA RESIDENT/PAGEABLE                    *
*                                                                     *
* ENTRY POINT =                                                       *
*        IFG0553H                                                     *
*        IGG0550D - ALIAS                                             *
*                                                                     *
*    PURPOSE = SEE FUNCTION                                           *
*                                                                     *
*    LINKAGE =                                                        *
*        THIS MODULE IS TRANSFERRED CONTROL THROUGH THE IECRES-LOAD   *
*        MACRO INSTRUCTION.                                           *
*                                                                     *
* INPUT =                                                             *
*        GIVEN CONTROL IN PROTECT KEY 5.                              *
*        REGISTER 2 POINTS TO THE COPIED DCB.                         *
*        DEBDCBAD POINTS TO THE COPIED DCB.                           *
*        REGISTER 4 POINTS TO THE EOV WORKAREA                        *
*                                                                     *
* OUTPUT =                                                            *
*        THE NEXT MODULE IS GIVEN CONTROL IN PROTECT KEY 5 WITH       *
*        REGISTER 2 POINTING TO THE COPIED DCB,                       *
*        DEBDCBAD POINTING TO THE COPIED DCB,                         *
*        AND REGISTER 4 POINTING TO THE EOV WORKAREA,                 *
*                                                                     *
* EXIT-NORMAL =                                                       *
*        IFG0551L - ACCESS METHOD EXECUTOR, BRANCH CODE = 0           *
*        IFG0554A - MOUNT/VERIFY FUNCTION, BRANCH CODE = 16           *
*           RETURN MODULE - IFG0553F                                  *
*                                                                     *
* EXIT-ERROR =                                                        *
*                                                                     *
* EXTERNAL REFERENCES = SEE BELOW                                     *
*                                                                     *
*    ROUTINES =                                                       *
*        IFG019RA THROUGH THE IECRES MACRO.                           *
*                                                                     *
*    DATA AREAS =                                                     *
*        EOV WORKAREA.                                                *
*                                                                     *
*    CONTROL BLOCK =                                                  *
*        CVT                                                          *
*        DEB                                                          *
*        JFCB                                                         *
*        TCB                                                          *
*        UCB                                                          *
*        TIOT                                                         *
*        RB                                                           *
*                                                                     *
* TABLES =                                                            *
*                                                                     *
* MACROS =                                                            *
*        IECRES LOAD                                                  *
*        IECRES INIT                                                  *
*        IECRES FREE                                                  *
*        IECRES EXIT                                                  *
*                                                                     *
* CHANGE ACTIVITY =                                                   *
*        SEE CHANGES/DELETIONS SECTION JUST AFTER CSECT CARD.         *
*                                                                     *
***********************************************************************
         EJECT
*
*****    INTERNAL ERROR CODES
*
EABD193  EQU   193                      ERROR UNLOADING MT AHEAD UNIT
*
         BALR  RBASE,0                  ESTABLISH ADDRESSABILITY FOR
         USING *,RBASE                  THIS SECTION OF CODE
         USING FORCORE,RCORE
         USING WTG,RWTG                 BASE FOR WTG TABLE       Y02080
         USING IHADCB,RDCB
         USING UCB,RUCB
         USING TIOENTRY,RTIOT
         USING DEBBASIC,RDEB                                     YM1272
*
*
         B     ETI28000(RET)            DETERMINE TYPE OF ENTRY TO MOD
ETI28000 EQU   *                        BRANCH TABLE
         B     ETI30200                 NORMAL ENTRY FOR 553F
         NOP   K0                       ENTRY NOT IN USE         Y02134
         NOP   K0                       ENTRY NOT IN USE         Y02134
         NOP   K0                       ENTRY NOT IN USE         YM1193
         NOP   K0                       ENTRY NOT IN USE         YM5950
         B     ETI28100                 BR IF RET FROM NSLEHDRI (20)
         B     ETI30200                 ENTRY FOR NL (24)        Y02134
*
ETI28100 EQU   *                        NSLEHDRI RETURN
         STM   R5,R6,DXREGSAV           SAVE MODESET REGISTERS   Y02082
         L     RDCB,DXUDCBAD            PT TO CALLER'S DCB       YM3005
         MODESET KEYADDR=DXUKEY,WORKREG=1 ASSUME CALLER'S KEY    YM3005
         OC    DCBOFLGS,DCBOFLGS        PGM CHK IF NOT CALLER'S  YM3005
         LA    R0,ALLBITS-DCBOBUSY      ISOLATE BUSY BIT MASK    YM3005
         IC    R1,DCBOFLGS              GET CALLER'S OFLGS       YM3005
         MODESET EXTKEY=DATAMGT         DATA MANAGEMENT KEY      YM3005
         MVI   DXECB,K32                RESTORE DXECB          @ZA13589
         L     RDCB,DXPDCBAD            PT TO COPIED DCB         YM3005
         OR    R0,R1                    ISOLATE CALLER BUSY BIT  YM3005
         IC    R1,DCBOFLGS              GET COPIED DCBOFLGS      YM3005
         NR    R1,R0                    UPDTE BUSY BIT TO CALLER YM3005
         STC   R1,DCBOFLGS              UPDATE OFLGS FIELD       YM3005
         MVC   DXXMODNM(L'IFG055NM),IFG055NM  MOVE IN MOD PREFIX YM3271
         MVC   DXXMODID,ID3H3H          CHANGE WTG MODULE NAME   YM5950
         MVC   DXXIDTTR,ID3H3H          ..AND ENTRY IDS TO '3H'  YM5950
         L     RTIOT,DXTIOTAD           RELOAD TIOT ADDRESS    @ZA03195
         TM    SRTEDMCT,UCBMOUNT        WAS NSL VOLUME THE CORRECT ONE-
         BZ    ETI30200                 ..BR IF YES TO EXIT      YM5950
*                                       ..NO- EXIT TO DISMT/MOUNT RTN
*                                                                Y02134
         MODESET EXTKEY=SUPR            UCB KEY                  Y02134
*                                                                Y02134
         NI    UCBDMCT,ALLBITS-UCBMOUNT SET OFF NSL REJECT       Y02134
         XC    UCBVOLI,UCBVOLI          CLEAR WRONG VOL SER      Y02134
*                                                                Y02134
         MODESET EXTKEY=DATAMGT         RESET TO DATA MNGMNT KEY Y02134
*                                                                Y02134
         SR    R0,R0                    VOL VERIFICATION ERROR @ZA03195
         MVC   DXRETMOD,ID3H3F          SET RETURN MODULE        Y02134
         MVI   DXRETCOD,K0              SET OFFSET TO ZERO       YM5950
*                                                                Y02134
         LA    RF,ID3H4A                POINT TO REJECT MOD ID   YM5950
         LA    RET,K16                  LOAD REJECT OFFSET       YM5950
*                                                                Y02134
***********************************************************************
*              EXITS                                                  *
***********************************************************************
*
*****          XCTL VIA THE RESIDENT ROUTINE
*
ETI30100 EQU   *                        TRANSFER CONTROL VIA RES RTN
*                                                                Y02134
         IECRES LOAD,MODID=(RF),BRCODE=(RET),BRANCH=QUEUED       Y02080
         EJECT
*                                                                Y02134
*****          EXIT TO USER OR ACCESS METHOD EXECUTOR
*
ETI30200 EQU   *                        NOT WRITE ONLY PASSWORD
         NI    DCBOFLGS,ALLBITS-DCBOCON TURN OFF CONCAT INDIC
*
*****          DETERMINE APPROPRIATE EXIT
*
ETI30300 EQU   *                        DETERMINE EXIT           Y02080
         MVC   DEBDCBAD+K1(L'DEBDCBAD-K1),DXUDCBAD+K1 POINT DEB  Y02082*
                                        TO USER DCB              Y02082
         L     RF,DXDEBXAD              GET PTR TO DEB EXTN      YM1272
         NI    DEBXFLG1-DEBXTN(RF),ALLBITS-DEBXCDCB INDIC PT USERYM1272
         TM    DCBMACRF,DCBMEXCP        IS THE ACCESS METHOD EXCP-
         BNZ   ETI30600                 ..BR IF YES TO EXIT
*
*****          EXIT TO THE ACCESS METHOD EXECUTOR
*
         LA    RF,MOD1L3H               PT TO EXECUTOR ID/TTR    M0110
         LA    RET,K0                   LOAD ENTRY OFFSET        Y02134
         B     ETI30100                 BRANCH TO EXECUTOR       Y02134
         SPACE 2
*
*****          RETURN TO USER
*
ETI30600 EQU   *                        RETURN TO CALLER
         LR    R1,RCORE                 PT TO BEGINNING OF EOV WK AREA
         NI    DCBOFLGS,ALLBITS-DCBOBUSY RESET I/O SUPPORT BIT
         IECRES INIT,DCBCOPY=FRWKAR,STM=(0,14,DXXPREFX)          Y02082
         IECRES FREE,A=(1),PREFIX=EOV   FREE EOV WORK AREA       Y02080
*                                                                Y02080
*        RETURN TO CALLER                                        Y02080
*                                                                Y02080
         IECRES EXIT                    RETURN                   Y02080
         EJECT
         SPACE 2
***********************************************************************
*              CONSTANTS                                              *
***********************************************************************
*
IFG055NM DC    C'IFG055'                PREFIX FOR XCTL  (EOV)   YM3271
*                                                                YM5950
***********************************************************************
*              XCTL TABLE                                             *
***********************************************************************
*
XCTLTB3H XCTLTABL ID=(MOD1L3H,1L,ID3H4A,4A,ID3H3F,3F,ID3H3H,3H), YM5950X
               SVC=055,BRT=YES,LENGTH=                           Y02080
         SPACE 2
         IECDSECS CVT,                                                 C
               TCB,                                                    C
               RB,                                                     C
               TIOT,                                                   C
               DCB,                                                    C
               IEZDEB,                                           YM1272C
               UCB,                                                    C
               MAIN,                                                   C
               WTG,                                                    C
               PREFX,                                            Y02080*
               EXPAND=YES
         IECEQU IEZDEB=YES              INVOKE OPEN/CLOSE/EOV EQUYM1272
         SPACE 2
         END
