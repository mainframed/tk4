         TITLE 'SECLOADA                      SCRATCH/RENAME/RESTART - P
               PASSWORD SECURITY INITIALIZATION'
SECLOADA CSECT
***********************************************************************
*                                                                     *
* MODULE NAME = SECLOADA (OS/VS2)                                     *
*                                                                     *
*              VS2 RELEASE 037 CHANGES                                *
*                                                                     *
*0000                                                          @ZA17953
*                                                                     *
***********************************************************************
* DESCRIPTIVE NAME = SCRATCH/RENAME/RESTART PASSWORD                  *
*                    SECURITY INITIALIZATION                          *
*                                                                     *
* COPYRIGHT = NONE                                                    *
*                                                                     *
* STATUS = RELEASE 2, LEVEL 0                                         *
*                                                                     *
* FUNCTION =                                                          *
*                                                                     *
*        1. CHECKS FOR AUTHORIZED PASSWORD BYPASS IN THE JSCB.        *
*                                                                     *
*        2. OBTAINS FETCH PROTECTED SECURITY WORK AREA IN DATA        *
*           MANAGEMENT KEY (KEY 5).                                   *
*                                                                     *
*        3. INITIALIZES WORK AREA TO APPEAR SIMILAR TO OPEN/CLOSE/EOV *
*           WORK AREA (THIS IS DONE TO USE THE SAME ROUTINES TO       *
*           PROCESS A OPEN/EOV REQUEST - SEE IFG0195V).               *
*                                                                     *
*        4. SET INDICATOR FOR SCRATCH, RENAME, OR RESTART.            *
*                                                                     *
*        5. SET UP RETURN TO THIS ROUTINE WHEN SECURITY PROCESSING    *
*           HAS COMPLETED.                                            *
*                                                                     *
*        6. RETURNS TO CALLING ROUTINE WITH RETURN CODE WHEN          *
*           SECURITY PROCESSING IS COMPLETE.                          *
*                                                                     *
* NOTES = THIS ROUTINE IS AN INTERFACE ROUTINE BETWEEN SCRATCH,       *
*         RENAME, AND RESTART ANDTHE PASSWORD PROCESSING ROUTINES.    *
*         THE FUNCTION IN THIS ROUTINE IS SIMILAR TO THE FUNCTION     *
*         IN THE OPEN/EOV INTERFACE ROUTINE (IFG0195T).               *
*                                                                     *
*    DEPENDENCIES =                                                   *
*                                                                     *
*            CLASS ONE CHARACTER CODE.  THE EBCDIC CHARACTER CODE     *
*            WAS USED FOR ASSEMBLY.  THE MODULE MUST BE REASSEMBLED   *
*            IF A DIFFERENT CHARACTER SET IS USED FOR EXECUTION.      *
*            THIS ROUTINE ADHERES TO OPEN/CLOSE/EOV PROGRAMMING       *
*            CONVENTIONS AND REGISTER USAGE.                          *
*                                                                     *
*    RESTRICTIONS = NONE                                              *
*                                                                     *
*    REGISTER CONVENTIONS = SEE IECEQU MACRO.                         *
*                                                                     *
*    PATCH LABEL = SEE XCTLTABL MACRO.                                *
*                                                                     *
* MODULE TYPE = CONTROL (OPEN, CLOSE, EOV DATA MANAGEMENT)            *
*                                                                     *
*    PROCESSOR = ASSEMBLER XF                                         *
*                                                                     *
*    MODULE SIZE = SEE EXTERNAL SYMBOL DICTIONARY OR LOC FIELD ON     *
*                  ORG STATEMENT AT END OF LISTING                    *
*                                                                     *
*    ATTRIBUTES = REENTRANT, REFRESHABLE,READ-ONLY, ENABLED,          *
*                 PRIVILEGED, SUPERVISOR STATE, DATA MANAGEMENT KEY,  *
*                 LINK PACK AREA RESIDENT/PAGEABLE                    *
*                                                                     *
* ENTRY POINT = SECLOADA                                              *
*                                                                     *
*    PURPOSE = SCRATCH/RENAME/RESTART INTERFACE ROUTINE TO            *
*              PASSWORD PROCESSING                                    *
*                                                                     *
*    LINKAGE = AS FOLLOWS -                                           *
*                                                                     *
*        IECRES LOAD,MODNM=SECLOADA,BRCODE=0,EXTPR=(RD),BRANCH=DIRECT *
*                                                                     *
* INPUT = AS FOLLOWS -                                                *
*                                                                     *
*        FOR RET (REG 14) = 0 (INITIAL ENTRY) -                       *
*        R1 = POINTER TO DSAB FOR DATA SET                            *
*        R4 = POINTER TO F1 DSCB IF DA                                *
*        R5 = POINTER TO DEB FOR RESTART                              *
*        RA = ID OF CALLER IN HI BYTE                                 *
*             2 = RENAME                                              *
*             3 = SCRATCH                                             *
*             4 = RESTART (DIRECT ACCESS)                             *
*             5 = RESTART (TAPE)                                      *
*        RB = POINTER TO DATA SET NAME                                *
*        RD = POINTER TO DADSM TYPE WORK AREA AND SAVE AREA           *
*                                                                     *
*        FOR RET (REG 14) = 4 (RETURNING FROM PASSWORD PROCESSING)    *
*        RD (REG 13) = POINTER TO SECURITY WORK AREA                  *
*                                                                     *
* OUTPUT = AS FOLLOWS -                                               *
*                                                                     *
*        WHEN RETURNING TO CALLER REGS 1 - 13 ARE RESTORED            *
*        HI ORDER BYTE OF REGISTER 10 CONTAINS A RETURN CODE          *
*                                                                     *
*        WHEN GOING TO PASSWORD PROCESSING -                          *
*        RCORE POINTS TO F1 DSCB FOR DA AND ZEROS FOR TAPE            *
*        RWTG POINTS TO SECURITY WTG TABLE                            *
*        RD POINTS TO SECURITY WORK AREA WITH FIELDS INITIALIZED      *
*        FOR RETURN SAVE AREA, DSNAME, DSCB, TCB, DSAB, AND JSCB.     *
*                                                                     *
* EXIT-NORMAL = AS FOLLOWS - TO CALLER                                *
*                                                                     *
*        RETURN TO RESTART -                                          *
*        IECRES LOAD,MODNM=IGC0N05B,BRCODE=0,EXTPR=(RD),BRANCH=DIRECT *
*                                                                     *
*        RETURN TO SCRATCH -                                          *
*        IECRES LOAD,MODNM=IGG0290A,BRCODE=0,EXTPR=(RD),BRANCH=DIRECT *
*                                                                     *
*        RETURN TO RENAME  -                                          *
*        IECRES LOAD,MODNM=IGG03001,BRCODE=0,EXTPR=(RD),BRANCH=DIRECT *
*                                                                     *
* EXIT-ERROR = AS ABOVE WITH CODE PASSED IN REG 10                    *
*                                                                     *
* EXTERNAL REFERENCES = SEE BELOW                                     *
*                                                                     *
*    ROUTINES = IFG0195V,IGG03001,IGG0290A,IGC0N05B.                  *
*                                                                     *
*    DATA AREAS = OPEN/EOV WORK AREA (SEE IECDSECS MAIN,WTG,PREFX.).  *
*                 SECURITY WORK AREA (SEE IECDSECS SECCORE).          *
*                                                                     *
*    CONTROL BLOCK = CVT,TCB,JSCB,DEB.                                *
*                                                                     *
* TABLES = SEE XCTLTABL.                                              *
*                                                                     *
* MACROS = IECRES,DMABCOND,XCTLTABL,IECDSECS,IECEQU.                  *
*                                                                     *
* CHANGE ACTIVITY = NEW RELEASE (LEVEL 0)                             *
*                                                                     *
***********************************************************************
         EJECT                                                   Y02134
*                                                                Y02134
         BALR  RF,R0                    ESTABLISH BASE REGISTER  Y02134
         USING *,RF                     DEFINE BASE REGISTER     Y02134
*                                                                Y02134
         B     SRP00000(RET)            BR TO INDICATED FUNCTION Y02134
*                                                                Y02134
SRP00000 B     SRP01000                 RET=0 INITIAL ENTRY      Y02134
         B     SRP09000                 RET=4 RETURN ENTRY       Y02134
         EJECT                                                   Y02134
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
*        INITIAL ENTRY FROM SCRATCH/RENAME                       Y02134
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
SRP01000 EQU   *                        INITIAL ENTRY            Y02134
*                                                                Y02134
         XR    R0,R0                    SET ERROR CODE TO ZERO   YM5101
         USING IECREGSV,RD              RD POINTS TO SAVE AREA   Y02134
         STM   R0,RD,IECREGSV           SAVE REGS                Y02134
         DROP  RD                                                Y02134
*                                                                Y02134
         LR    R8,RD                    LOAD POINTER TO DADSM WA Y02134
         LA    R0,IECSTART-IECPREFX     GET PREFIX OFFSET        Y02134
         SR    R8,R0                    POINT TO PREFIX          Y02134
         USING IECPREFX,R8              RF POINTS TO PREFIX      Y02134
*                                                                Y02134
         L     R9,CVTPTR                LOAD CVT ADDRESS         Y02134
         USING CVT,R9                   R9 POINTS TO CVT         Y02134
         L     R9,CVTTCBP               LOAD TCB POINTERS        Y02134
         DROP  R9                                                Y02134
         L     R9,K4(,R9)               LOAD CURRENT TCB         Y02134
         USING TCB,R9                   R9 POINTS TO TCB         Y02134
         L     RA,TCBJSCB               RA POINTS TO JSCB        Y02134
         USING IEZJSCB,RA               RA POINTS TO JSCB        Y02134
         L     RA,JSCBACT               LOAD ACTIVE JSCB POINTER Y02134
         TM    JSCBSWT1,JSCBPASS        CHECK FOR BYPASS         Y02134
         BO    SRP09100                 AUTHORIZATION, BR IF YES Y02134
         DROP  RA,RF                                             Y02134
         EJECT
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
*        GETMAIN SECURITY WORK AREA                              Y02134
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
         IECRES GET,LV=SECSIZE,SP=229,ID=SEWA,                   Y02134X
               A=IECSTART-IECPREFX(R8),PREFIX=YES                Y02134
*                                                                Y02134
         BALR  RBASE,R0                 ESTABLISH ADDRESSABILITY Y02134
         USING *,RBASE                  ESTABLISH BASE REG       Y02134
*                                                                Y02134
         LA    RD,0(R1)                 LOAD SECURITY WORK AREA  Y02134
         USING SECCORE,RD               RD POINTS TO SEC WA      Y02134
         LA    R0,IECSTART-IECPREFX     LOAD PREFIX OFFSET       Y02134
         SR    R1,R0                    POINT TO PREFIX          Y02134
         ST    R1,MYPREFX               SAVE PREFIX POINTER      Y02134
*                                                                Y02134
         ST    R8,MYRETURN              SAVE RETURN SAVE AREA    Y02134
         ST    R9,MYTCB                 SAVE TCB ADDRESS         Y02134
         ST    RA,MYJSCB                SAVE JSCB ADDRESS        Y02134
*                                                                Y02134
         L     R1,IECREGSV+K4*R1        LOAD DSAB ADDRESS (REG 1)Y02134
         ST    R1,MYDSAB                SAVE DSAB ADDRESS        Y02134
*                                                                Y02134
         MVC   MYWTGWTO,WTOLEN          SET UP MODULE WTO        Y02134
         MVC   MYWTGNM,NAME             MODNM TO WTG TABLE       Y02134
         MVC   MYRETMOD,NAME            SET UP RETURN LOAD       Y02134
         MVI   MYRETCOD,K4              SET UP RETURN OFFSET     Y02134
*                                                                Y02134
         L     R1,IECREGSV+K4*RB        DSN POINTER (REG 11)     Y02134
         MVC   MYDSN,0(R1)              MOVE IN DSNAME           Y02134
*                                                                Y02134
         L     R1,IECREGSV+K4*RA        CALLER ID (REG 10)       Y02134
         STCM  R1,B'1000',MYCODE1       CALLER INDICATOR         Y02134
         MVI   MYDEBSYS,PLISTOUT        INDICATE OUTPUT          Y02134
*                                                                Y02134
         LA    RWTG,MYWTG               LOAD POINTER TO WTG TABL Y02134
         USING WTG,RWTG                 RWTG POINTS TO WTG TABLE Y02134
         OI    WTGPATHS,X01             SCRATCH/RENAME ENQ BYPAS Y02134
*                                                                Y02134
         LA    RCORE,MYDSCB             LOAD POINTER TO DSCB     Y02134
         USING FORCORE,RCORE            POINTS TO OPEN-LIKE WA   Y02134
*                                                                Y02134
         LA    R1,DXATCOM1              LOAD PTR TO PSUEDO RRPL  Y02134
         ST    R1,IECRRPRM-IECSTART(,RD) SAVE RRPL POINTER       Y02134
*                                                                Y02134
         CLI   MYCODE1,K4               CHECK FOR RESTART        Y02134
         BL    SRP01010                 BRANCH NOT RESTART       Y02134
*                                                                Y02134
         NI    WTGPATHS,ALLBITS-X01     SET ENQ BYPASS OFF       Y02134
*                                                                Y02134
         L     R1,IECREGSV+K4*R5        GET DEB ADDR (REG 5)     Y02134
         USING DEB,R1                   R1 POINTS TO DEB         Y02134
         MVC   MYDEBSYS(L'DEBOPATB),DEBOPATB SET OPEN ATTRIBUTES Y02134
         DROP  R1                                                Y02134
*                                                                Y02134
         CLI   MYCODE1,K5               CHECK FOR RESTART TAPE   Y02134
         BE    SRP01020                 BRANCH IF TAPE           Y02134
*                                                                Y02134
SRP01010 EQU   *                        DIRECT ACCESS            Y02134
*                                                                Y02134
         L     R1,IECREGSV+K4*RCORE     F1 DSCB POINTER (REG 4)  Y02134
         MVC   MYDSCB,0(R1)             MOVE IN F1 DSCB          Y02134
*                                                                Y02134
SRP01020 EQU   *                        TAPE                     Y02134
*                                                                Y02134
         LA    RET,K0                   SET ENTRY OFFSET         Y02134
*                                                                Y02134
         IECRES LOAD,MODNM=IFG0195V,                             Y02134X
               PREFIX=WTGPREFX,BRANCH=DIRECT                     Y02134
         DROP  RWTG,RBASE,RCORE                                  Y02134
         EJECT                                                   Y02134
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
*        EXIT TO CALLER                                          Y02134
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
SRP09000 EQU   *                        ENTRY FOR RETURN         Y02134
*                                                                Y02134
         L     R8,MYRETURN              LOAD SAVE AREA           Y02134
         XR    R9,R9                    CLEAR REG FOR IC INST    Y02134
         IC    R9,MYERRCOD              GET ERROR CODE           Y02134
         ST    R9,IECREGSV+K4*R0        SAVE RETURN CODE (REG 0) Y02134
*                                                                Y02134
         IECRES FREE,PREFIX=YES,A=(RD)  FREE WORK AREA           Y02134
         DROP  RD                                                Y02134
*                                                                Y02134
SRP09100 EQU   *                        PASSWORD BYPASS ENTRY    Y02134
*                                                                Y02134
         BALR  RF,R0                    ESTABLISH ADDRESSABILITY Y02134
         USING *,RF                     SET BASE REG             Y02134
*                                                                Y02134
         MODESET EXTKEY=ZERO            RETURN IN KEY 0          YM1335
*                                                                YM1335
         LM    R0,RD,IECREGSV           RESTORE REGISTERS        Y02134
         DROP  R8                                                Y02134
         USING IECEXTPR,RD              RD POINTS TO PREFIX      Y02134
*                                                                Y02134
         OR    RA,R0                    SET ERROR RETURN REG     Y02134
         LTR   R0,R0                    TEST FOR ERROR           Y02134
         BNZ   SRP09200                 BRANCH IF ERROR          Y02134
         XR    RA,RA                    CLEAR RETURN CODE        Y02134
*                                                                Y02134
SRP09200 EQU   *                        RETURN TO CALLER         Y02134
*                                                                Y02134
         L     RET,IECWTGTB             LOAD WTG POINTER         Y02134
         USING WTG,RET                  RET POINTS TO DADSM WTG  Y02134
*                                                                Y02134
         CLI   IECREGSV+K4*RA,K2        TEST FOR RENAME          Y02134
         BE    SRP09400                 BRANCH IF RENAME         Y02134
*                                                                Y02134
         CLI   IECREGSV+K4*RA,K3        TEST FOR SCRATCH         Y02134
         BE    SRP09300                 BRANCH IF SCRATCH        Y02134
*                                                                Y02134
         MVC   WTGMODNM(K12),IGC0N05B   SET UP RESTART DA RET  @ZA17953
         CLI   IECREGSV+K4*RA,K4        CODE 4 = DA RESTART    @ZA17953
         BE    SRP09500                 BRANCH IF DA RESTART   @ZA17953
         MVC   WTGMODNM(K12),IGC0S05B   SET UP RESTART TAPE RETURN
*                                                              @ZA17953
         B     SRP09500                 CODE 5 = TAPE RESTART  @ZA17953
*                                                                Y02134
SRP09300 EQU   *                        SCRATCH RETURN           Y02134
*                                                                Y02134
         MVC   WTGMODNM(K12),IGG0290A   SET UP SCRATCH RETURN    Y02134
         B     SRP09500                 CODE 3 = SCRATCH         Y02134
*                                                                Y02134
SRP09400 EQU   *                        RENAME RETURN            Y02134
*                                                                Y02134
         MVC   WTGMODNM(K12),IGG03001   SET UP RENAME RETURN     Y02134
*                                                                Y02134
SRP09500 IECRES LOAD,BRCODE=K0,EXTPR=(RD),BRANCH=DIRECT          Y02134
         DROP  RET                                               Y02134
         EJECT
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
*        CONSTANTS                                               Y02134
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
WTOLEN   DC    0XL4'00',AL2(12),AL2(0)  MODULE TRACE WTO LENGTH  Y02134
*                                                                Y02134
         XCTLTABL ID=(NAME,SECLOADA,,IFG0195V,                   Y02134X
               ,IGG03001,,IGG0290A,,IGC0N05B,,IGC0S05B),       @ZA17953X
               BRT=YES,LENGTH=                                 @ZA17953
         IECDSECS CVT,TCB,JSCB,DEB,                              Y02134X
               SECCORE,MAIN,WTG,PREFX,EXPAND=YES                 Y02134
         IECEQU ,                       DEFINE EQUATES           Y02134
         END
