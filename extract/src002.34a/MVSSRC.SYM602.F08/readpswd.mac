         TITLE 'READPSWD                      READ PASSWORD ROUTINE - AS
               SK THE OPERATOR FOR PASSWORD'                     Y02134
READPSWD CSECT
***********************************************************************
*                                                                     *
*        VS2 RELEASE 03 DELETIONS/CHANGES                             *
*0000371000                                                     ZA01517
*                                                                     *
* MODULE NAME = READPSWD (OS/VS2)                                     *
*                                                                     *
* DESCRIPTIVE NAME = READ PASSWORD ROUTINE                            *
*                                                                     *
* COPYRIGHT = NONE                                                    *
*                                                                     *
* STATUS = RELEASE 2, LEVEL 0                                         *
*                                                                     *
* FUNCTION =                                                          *
*                                                                     *
*        1. INCREMENTS THE TRY COUNT IN THE SECURITY WORK AREA.       *
*           THE OPERATOR WILL GET TWO TRIES TO RESPOND CORRECTLY.     *
*                                                                     *
*        2. FOR A NON-TSO TERMINAL TASK, THIS ROUTINE ISSUES MESSAGE  *
*           IEC301A S TO THE OPERATOR AND WAITS FOR HIS REPLY         *
*           BEFORE PASSING CONTROL TO IFG0195U.                       *
*                                                                     *
*        3. FOR A TSO TERMINAL TASK, IF THE TRY COUNT IS 1,           *
*           MESSAGE IEC113A IS ISSUED. IF THE TRY COUNT IS 2,         *
*           MESSAGES IEC115I AND IEC116A PRECEDE IEC113A.             *
*                                                                     *
* NOTES = REQUESTS TO TSO TERMINALS ARE PRECEDED WITH A BYPASS        *
*         PRINT CHARACTER TO PREVENT THE PASSWORD FROM BEING          *
*         TYPED AT THE TERMINAL WHEN IT IS KEYED IN.                  *
*                                                                     *
*    DEPENDENCIES =                                                   *
*                                                                     *
*            CLASS ONE CHARACTER CODE.  THE EBCDIC CHARACTER CODE     *
*            WAS USED FOR ASSEMBLY.  THE MODULE MUST BE REASSEMBLED   *
*            IF A DIFFERENT CHARACTER SET IS USED FOR EXECUTION.      *
*                                                                     *
*    RESTRICTIONS = NONE                                              *
*                                                                     *
*    REGISTER CONVENTIONS = SEE IECEQU MACRO.                         *
*                                                                     *
*    PATCH LABEL = SEE XCTLTABL MACRO.                                *
*                                                                     *
* MODULE TYPE = CONTROL (OPEN, CLOSE, EOV DATA MANAGEMENT)            *
*                                                                     *
*    PROCESSOR = ASSEMBLER XF                                         *
*                                                                     *
*    MODULE SIZE = SEE EXTERNAL SYMBOL DICTIONARY OR LOC FIELD ON     *
*                  ORG STATEMENT AT END OF LISTING                    *
*                                                                     *
*    ATTRIBUTES = REENTRANT, REFRESHABLE,READ-ONLY, ENABLED,          *
*                 PRIVILEGED, SUPERVISOR STATE, DATA MANAGEMENT KEY,  *
*                 LINK PACK AREA RESIDENT/PAGEABLE                    *
*                                                                     *
* ENTRY POINT = READPSWD FROM IFG0195V OR IFG0195U.                   *
*                                                                     *
*    PURPOSE = TO REQUEST A PASSWORD FROM THE OPERATOR AND KEEP       *
*              A COUNT OF THE NUMBER OF TIMES IT IS REQUESTED.        *
*                                                                     *
*    LINKAGE =                                                        *
*        IECRES LOAD,MODNM=READPSWD,PREFIX=WTGPREFX,BRANCH=DIRECT     *
*                                                                     *
* INPUT = AS FOLLOWS -                                                *
*                                                                     *
*        R4 = ADDRESSABILITY TO FORCORE-LIKE WORK AREA.               *
*        R6 = ADDRESSABILITY TO WTG TABLE.                            *
*        RD = ADDRESSABILITY TO SECCORE WORK AREA                     *
*                                                                     *
* OUTPUT = PASSWORD AT MYREPLY IN SECCORE.                            *
*                                                                     *
* EXIT-NORMAL = IFG0195U - READ OR UPDATE PASSWORD RECORD.            *
*                                                                     *
* EXIT-ERROR =  IFG0195U TO ISSUE ERROR MESSAGE.                      *
*                                                                     *
* EXTERNAL REFERENCES = SEE BELOW                                     *
*                                                                     *
*    ROUTINES = IFG0195U.                                             *
*                                                                     *
*    DATA AREAS = IECDSECS SECCORE,MAIN,WTG,PREFX,RRPL                *
*                                                                     *
*    CONTROL BLOCK = CVT,TCB,JSCB,PSCB,UPT,DSAB,UCB,TIOT,ASCB.        *
*                                                                     *
* TABLES = SEE XCTLTABL MACRO.                                        *
*                                                                     *
* MACROS = WTOR,WAIT,IECRES,TCLEARQ,TGET,TPUT,                        *
*          XCTLTABL,IECDSECS,IECEQU.                                  *
*                                                                     *
* CHANGE ACTIVITY =                                                   *
*   VS2 RELEASE 3 DELETIONS                                    Z30TSRJ*
*                                                                     *
***********************************************************************
         EJECT
*
         USING FORCORE,RCORE            POINTS TO OPEN-LIKE WA   Y02134
         USING WTG,RWTG                 DEFINE BASE TO XCTL/WTG TABLE
         USING TIOENTRY,RTIOT           DEFINE BASE TO TIOT DD ENTRY
         USING SECCORE,RD               DEFINE BASE TO SECURITY WK AREA
         USING DSAB,RB                  RB WILL POINT TO DSAB    Y02134
*
         BALR  RBASE,R0                 ESTABLISH BASE REGISTER
         USING *,RBASE                  DEFINE BASE REGISTER
*                                                                Y02134
         EJECT
*                                                                Y02134
*****************************************************************Y02134
*
*        INCREMENT TRY COUNT                                     Y02134
*        THE TRY COUNT IS CHECKED IN IFG0195U                    Y02134
*        THE OPERATOR IS GIVEN TWO TRIES TO SUPPLY               Y02134
*        THE CORRECT PASSWORD.                                   Y02134
*                                                                Y02134
*****************************************************************Y02134
*
         XR    R1,R1                    CLEAR REG FOR IC INST    Y02134
         IC    R1,MYCODE2               GET TRY COUNT            Y02134
         LA    R1,K1(R1)                INCREMENT TRY COUNT BY 1 Y02134
         STC   R1,MYCODE2               STORE COUNT BACK         Y02134
*                                                                Y02134
         L     R1,MYASCB                LOAD ASCB ADDRESS        Y02134
         L     RF,ASCBTSB-ASCB(,R1)     LOAD TSB ADDRESS         Y02134
         LTR   RF,RF                    TEST FOR TERMINAL TASK   Y02134
         BNZ   OSQ44000                 BRANCH IF TERMINAL TASK  Y02134
*
***********************************************************************
*
*  CONSTRUCT 'IEC301A S JOB JOBNAMEX,STEP STEPNAME,DDNAME DDNAMEXX'
*  MESSAGE WITH OPTIONAL ',CONC000' IF CONCATENATED.
*                                                                Y02134
*****************************************************************Y02134
*
OSQ41000 EQU   *                        CONSTRUCT MESSAGE
*
         MVC   MYMCSFLG(OSQ4830L),OSQ4830K  TURN ON MCS FLAG IN MSG AND
*                                       MOVE 'IEC301A S JOB ' TO MSG
*
         CLI   MYCODE1,K2               CHECK IF RENAME          YM7895
         BE    OSQ41500                 BRANCH IF RENAME         YM7895
         CLI   MYCODE1,K3               CHECK IF SCRATCH         YM7895
         BE    OSQ41500                 BRANCH IF SCRATCH        YM7895
*                                                                YM7895
         L     R1,MYTCB                 GET CURRENT TCB ADDR
         L     R1,TCBTIO-TCB(,R1)       GET TIOT ADDRESS
         MVC   MYMJOBN,TIOCNJOB-TIOT(R1)  JOB NAME TO MESSAGE
         MVC   MYMSTEP,OSQ4835K         ',STEP ' TO MESSAGE
         MVC   MYMSTEPN,TIOCSTEP-TIOT(R1)  JOB STEP TO MESSAGE
         MVC   MYMDDNM,OSQ4840K         ',DDNAME ' TO MESSAGE
*
         L     RB,MYDSAB                LOAD CURRENT DSAB        Y02134
         XR    RC,RC                    CLEAR CONCATENATION CTR ZA01517
*                                                                Y02134
OSQ41200 EQU   *                        LOOP TO FIND DDNAME      Y02134
*                                                                Y02134
         L     RTIOT,DSABTIOT           LOAD TIOT ENTRY          Y02134
         CLI   TIOEDDNM,BLANK           IS DD NAME PRESENT
         BNE   OSQ41300                 BRANCH IF YES            Y02134
*                                                                Y02134
         LA    RC,K1(RC)                INCREMENT COUNTER        Y02134
         L     RB,DSABBCHN              LOAD PREVIOUS DSAB       Y02134
         LTR   RB,RB                    TEST IF PREVIOUS         Y02134
         BNZ   OSQ41200                 BRANCH IF PREVIOUS       Y02134
         XR    RC,RC                    CLEAR CONCATENATION CNTR Y02134
*
OSQ41300 EQU   *                        MOVE IN DDNAME           Y02134
*                                                                Y02134
         MVC   MYMDDNMN,TIOEDDNM        DD NAME TO MESSAGE
         LA    R1,MYMDDNMN+K7           POINT TO LAST CHAR OF DDNAME
*                                                                Y02134
         LTR   RC,RC                    CHECK FOR CONCATENATION  Y02134
         BZ    OSQ42200                 BRANCH IF NONE           Y02134
*                                                                Y02134
         LA    RC,K1(,RC)               INCREMENT FOR FIRST     ZA01517
*        CONCATENATION NUMBER WILL START AT 2
*        TO BE COMPATIBLE WITH PROBLEM DETERMINATION MESSAGE
         CVD   RC,DXWORK                CONVERT NO. TO DECIMAL   Y02134
         UNPK  MYMCONCN,DXWORK+K4       CONCATENATION NUMBER     YM7052
         OI    MYMCONCN+K2,ZONEOF       REMOVE SIGN BITS
         MVC   MYMCONC,OSQ4845K         ',CONC' TO MESSAGE
         LA    R1,MYMCONCN+K2           POINT TO LAST CHAR OF CONC000
         B     OSQ42200                 BRANCH TO ISSUE MSG      YM7895
*                                                                Y02134
OSQ41500 EQU   *                        SCRATCH OR RENAME MSG    YM7895
*                                                                YM7895
*****************************************************************YM7895
*                                                                YM7895
*  CONSTRUCT 'IEC301A S DSNAME DSNXX FOR SCRATCH AND RENAME      YM7895
*                                                                YM7895
*****************************************************************YM7895
*                                                                YM7895
         MVC   MYMDSN(MYMDSNN-MYMDSN),OSQ4855K SETUP DSNAME      YM7895
         MVC   MYMDSNN(L'MYDSN),MYDSN   DSNAME TO MESSAGE        YM7895
*                                                                YM7895
         LA    R1,MYMDSNN+L'MYDSN       POINT TO END OF MSG      YM7895
*                                                                YM7895
OSQ42000 EQU   *                        LOOP TO FIND END         YM7895
*                                                                YM7895
         CLI   MYDSN-MYDSN(R1),BLANK    CHECK IF AT END          YM7895
         BNE   OSQ42200                 BRANCH IF AT END         YM7895
         BCT   R1,OSQ42000              BACK UP/CHECK NEXT       YM7895
*                                                                YM7895
OSQ42200 EQU   *                        PREPARE TO ISSUE MESSAGE
*                                                                Y02134
         MVC   K1(K4,R1),OSQ4850K       MOVE IN DESC & ROUT CODES
         LA    RF,MYMSIZE-K1            ADDR START OF MSG-1
         SR    R1,RF                    CALCULATE MESSAGE LENGTH
         STH   R1,MYMSIZE               STORE MESSAGE LENGTH
*
***********************************************************************
***********************************************************************
***********************************************************************
***********************************************************************
***********************************************************************
***                                                                 ***
***THE SECTION OF CODE BELOW, ENCLOSED BY COMMENTS, IS THE ONLY     ***
***SECTION OF THIS MODULE (READPSWD) WHICH CAN BE REPLACED BY THE   ***
***USER IN ORDER TO ENTER A PASSWORD FOR A SECURITY-PROTECTED DATA  ***
***SET. THE REPLACED CODE BELOW ENTERS THE PASSWORD FROM THE CONSOLE***
***USING A WTOR. THE SECURITY WORK AREA DEFINED BY THE              ***
***DSECT SECCORE CONTAINS THE MESSAGE IEC301A S JOB..... FORMATTED  ***
***PROPERLY FOR OPEN,EOV,SCRATCH OR RENAME AND THE FIELDS WITHIN THIS**
***MESSAGE MAY BE USED AS REQUIRED. NOTE THAT WHEN THE FOLLOWING    ***
***CODE IS REPLACED, THE SYSTEM MESSAGE IEC301A..... NO LONGER      ***
***APPEARS ON THE CONSOLE. SAVE AND RESTORE REGISTERS, IF WORK      ***
***REGISTERS ARE NEEDED. GET/FREEMAIN NEEDED CORE, IF ADDITIONAL    ***
***WORK AREA IS REQUIRED. THE NET RESULT OF THIS REPLACED CODE IS   ***
***THAT THE 8-CHARACTER PASSWORD, LEFT-JUSTIFIED AND PADDED WITH    ***
***BLANKS, MUST BE PLACED IN LOCATION MYREPLY IN THE DSECT SECCORE. ***
***                                                                 ***
         LA    R1,MYREPLY               PUT ADDRESS OF REPLY
         ST    R1,MYRPLADR              INTO MYRPLADR
         MVI   MYRPLLTH,L'MYREPLY       SET REPLY LENGTH TO 8
         LA    R1,MYECB                 PUT ECB ADDRESS          Y02134
         ST    R1,MYRPLECB              INTO MYRPLECB
         MVI   MYECB,K0                 CLEAR ECB                Y02134
         MVI   MYREPLY,BLANK            SET REPLY
         MVC   MYREPLY+K1(K7),MYREPLY   AREA TO BLANKS
*
         WTOR  MF=(E,MYRPLLTH)          WRITE MESSAGE TO OPERATOR
*
         WAIT  ECB=MYECB,LONG=YES       WAIT LONG FOR REPLY     Z30TSRJ
***                                                                 ***
***THE ABOVE SECTION OF CODE IS THE ONLY SECTION IN THIS MODULE     ***
***WHICH CAN BE REPLACED.                                           ***
***                                                                 ***
***********************************************************************
***********************************************************************
***********************************************************************
***********************************************************************
***********************************************************************
*
OSQ43000 EQU   *                        GO GET PASSWORD RECORD
*                                                                Y02134
         IECRES LOAD,MODNM=IFG0195U,BRCODE=K0,                   Y02134X
               PREFIX=WTGPREFX,BRANCH=DIRECT                     Y02134
         EJECT
*
***********************************************************************
*
*        TPUT MESSAGE IEC113A TO TSO USER FOR PASSWORD
*
*****************************************************************Y02134
*                                                                Y02134
OSQ44000 EQU   *                        CHECK TRY COUNT
*
         CLI   MYCODE2,K1               CHECK FOR FIRST TRY      Y02134
         BNE   OSQ45000                 BRANCH IF NOT FIRST      Y02134
*                                                                Y02134
         MVC   MYMIEC(L'OSQ4860K),OSQ4860K  SET MESSAGE ID AND BODY
         MVC   MYMIEC+K36(K44),MYDSN    MOVE DSNAME TO MESSAGE   Y02134
*
*    SCAN FOR END OF DSNAME FIELD
*
         LA    RF,MYMIEC+K35+K44        POINT TO LAST CHAR OF DSNAME
*
OSQ44200 CLI   0(RF),BLANK              IS THIS NON-BLANK
         BNE   OSQ44400                 BR IF YES
         BCT   RF,OSQ44200              REPEAT IF NON-BLANK NOT FOUND
*
BYPASS   EQU   X'24'                    BYPASS PRINT CHAR
OSQ44400 MVI   K1(RF),BYPASS            INSERT PRINT BYPASS CHAR
         LA    R0,K2(RF)                ADDR END OF MESSAGE + 1
         LA    R1,MYMIEC                POINT MSG BUFR TO INCLUDE ID
         SR    R0,R1                    GET MSG LENGTH           YM6920
*
OSQ44600 EQU   *                        ISSUE TSO MESSAGE        Y02134
*                                                                Y02134
         BAL   R2,OSQ47000              ISSUE MESSAGE            Y02134
         BAL   R2,OSQ46000              GET REPLY                Y02134
*
         CLI   MYRPLLTH,X'0C'           WAS REPLY TOO LONG
         BNE   OSQ43000                 BR IF NO TO CHECK PASSWORD
*
         TCLEARQ INPUT                  CLEAR INPUT QUEUE
*
         BR    RBASE                    RETURN TO REISSUE MSG    Y02134
*                                                                Y02134
         EJECT
*                                                                Y02134
***********************************************************************
*
*        TPUT MSG 'IEC115I INVALID PASSWORD' TO TERMINAL USER
*        TPUT MSG 'IEC116A REENTER-' TO REPROMPT TERMINAL USER
*
*****************************************************************Y02134
*                                                                Y02134
OSQ45000 EQU   *                        CHECK TRY COUNT
*                                                                Y02134
         CLI   MYCODE2,K2               ALREADY TWO TRIES        Y02134
         BH    OSQ45500                 BRANCH IF ALREADY TWO    Y02134
*
*****************************************************************Y02134
*                                                                Y02134
*        TPUT MSG 'IEC115I INVALID PASSWORD' TO TERMINAL USER    Y02134
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
         MVC   MYMIEC(L'OSQ4870K),OSQ4870K  SET MESSAGE ID AND BODY
         LA    R0,L'OSQ4870K            SET LENGTH TO INCL ID
         LA    R1,MYMIEC                POINT MSG BUFR TO INCL ID
*
         BAL   R2,OSQ47000              ISSUE MESSAGE            Y02134
*
*****************************************************************Y02134
*                                                                Y02134
*        TPUT MSG 'IEC116A REENTER-' TO REPROMPT TERMINAL USER   Y02134
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
         MVC   MYMIEC(L'OSQ4880K),OSQ4880K  SET MESSAGE ID AND BODY
         LA    R1,MYMIEC                SET BUFR ADDR TO INCLUDE ID
         LA    R0,L'OSQ4880K            SET LENGTH TO INCLUDE ID
         B     OSQ44600                 PROMPT USER              Y02134
         EJECT
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
*        RETURN TO ISSUE ERROR MESSAGE                           Y02134
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
OSQ45500 EQU   *                        GO ISSUE ERROR MESSAGE   Y02134
*                                                                Y02134
         MVI   MYERRCOD,K22             SET ERROR CODE           Y02134
         IECRES LOAD,MODNM=IFG0195U,BRCODE=K8,                   Y02134X
               PREFIX=WTGPREFX,BRANCH=DIRECT                     Y02134
         EJECT
*                                                                Y02134
***********************************************************************
*
*        GET USER REPLY                                          Y02134
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
OSQ46000 EQU   *                        GET TERMINAL RESPONSE
*                                                                Y02134
         LA    R0,L'MYREPLY             SET MAX REPLY LENGTH TO 8
         LA    R1,MYREPLY               POINT TO REPLY ADDRESS
         O     R1,OSQ4894K              SET TGET WAIT OPTION
         TGET  (1),(0),R                ISSUE TGET
         STC   RF,MYRPLLTH              SAVE TGET RETURN CODE
         OC    MYREPLY,OSQ4890K         FOLD REPLY TO UPPER CASE
*
RESTORE  EQU   X'14'                    RESTORE CHARACTER RESETS PRINT
         MVI   MYMAREA,RESTORE          PUT RESTORE CHAR IN BUFR
         LA    R0,K1                    SET MESSAGE LENGTH
         LA    R1,MYMAREA               POINT TO MESSAGE ADDRESS
*
         O     R1,OSQ4892K              SET TPUT ASIS OPTION
         TPUT  (1),(0),R                ISSUE TPUT
         BR    RDCB                     RETURN TO MAINLINE
         EJECT
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
*        ISSUE TPUT MESSAGE                                      Y02134
*        R0=LENGTH R1=POINTER TO MESSAGE R2=RETURN ADDRESS       Y02134
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
OSQ47000 EQU   *                        ISSUE TERMINAL MESSAGE   Y02134
*                                                                Y02134
         STCM  R0,B'0011',MYMCSFLG      SAVE LENGTH              Y02134
*                                                                Y02134
         L     RF,MYJSCB                GET JSCB ADDRESS         Y02134
         MVC   MYMSIZE(L'JSCBTJID),JSCBTJID-IEZJSCB(RF) ID       Y02134
         L     RF,JSCBPSCB-IEZJSCB(,RF) GET THE PSCB ADDRESS     Y02134
         LTR   RF,RF                    PSCB INITIALIZED         Y02134
         BZ    OSQ47100                 DEFAULT TPUT W/MSG ID    Y02134
*                                                                Y02134
         MODESET EXTKEY=ZERO            SWITCH TO KEY ZERO       YM6712
*                                                                Y02134
         L     RF,PSCBUPT-PSCB(,RF)     GET UPT ADDRESS          Y02134
         TM    UPTSWS-UPT(RF),UPTMID    IS MESSAGE ID REQUIRED   Y02134
         BO    OSQ47100                 BRANCH IF REQUIRED       Y02134
*                                                                Y02134
         MODESET EXTKEY=DATAMGT         RETURN TO DATAMGT KEY    YM6712
*                                                                Y02134
         ICM   R0,B'0011',MYMCSFLG      LOAD MESSAGE LENGTH      Y02134
         LA    RF,L'MYMIEC              LOAD ID LENGTH           Y02134
         SR    R0,RF                    GET NEW MESSAGE LENGTH   Y02134
         STCM  R0,B'0011',MYMCSFLG      SAVE LENGTH              Y02134
         LA    R1,MYMNOID               POINT PASSED MSG ID      Y02134
*                                                                Y02134
OSQ47100 EQU   *                        ISSUE MESSAGE            Y02134
*                                                                Y02134
         MODESET EXTKEY=DATAMGT         RETURN TO DATAMGT KEY    YM6712
*                                                                Y02134
         ICM   R0,B'1111',MYMSIZE       LOAD ID AND LENGTH       Y02134
         O     R1,OSQ4892K              SET TPUT ASIS OPTION     Y02134
         TPUT  (1),(0),R                ISSUE TPUT               Y02134
         BR    R2                       RETURN TO MAINLINE       Y02134
         EJECT
*
***********************************************************************
*
*        CONSTANTS
*
*****************************************************************Y02134
*                                                                Y02134
OSQ4830K DC    X'8000'                  MCS MESSAGE FLAG
         DC    C'IEC301A S JOB '        FIXED PART OF MESSAGE
OSQ4830L EQU   *-OSQ4830K               LENGTH OF PART OF MSG
OSQ4835K DC    C',STEP '                FIXED PART OF MESSAGE
OSQ4840K DC    C',DDNAME '              FIXED PART OF MESSAGE
OSQ4845K DC    C',CONC'                 FIXED PART OF MESSAGE
OSQ4850K DC    X'40000080'              DESCRIPTOR, ROUTING CODES
OSQ4855K DC    C'DSNAME '               FIXED PART OF MESSAGE
*
OSQ4860K DC    C'IEC113A ENTER PASSWORD FOR DATA SET ' TSO MSG   Y02134
OSQ4870K DC    C'IEC115I INVALID PASSWORD ' TSO INVALID MESSAGE  Y02134
         ORG   *-1                      OVERLAY LAST MSG CHAR    Y02134
         DC    X'15'                    INSERT NEW LINE CHAR     Y02134
OSQ4880K DC    C'IEC116A REENTER- '     TSO REPROMT MESSAGE      Y02134
         ORG   *-1                      OVERLAY LAST MSG CHAR    Y02134
         DC    X'24'                    INSERT BYPASS PRINT CHAR Y02134
*
OSQ4890K DC    X'4040404040404040'      USED TO FOLD TSO REPLY TO
*                                       UPPER CASE
OSQ4892K DC    0F'0',X'01000000'        TPUT W/ASIS OPTION
OSQ4894K DC    0F'0',X'80000000'        SVC 93 TGET OPTION
*
         XCTLTABL ID=(,IFG0195U),BRT=YES,LENGTH=                 Y02134
         IECDSECS CVT,TCB,JSCB,PSCB,UPT,DSAB,TIOT,UCB,ASCB,      Y02134X
               SECCORE,MAIN,WTG,PREFX,EXPAND=YES                 Y02134
         IECEQU ,                       DEFINE EQUATES           Y02134
         END
