         TITLE ' IED1303D - TCAM COMMAND SCHEDULER '
IED1303D CSECT
IED1303D CSECT
*A375000,476200,479200,925000                                    A50213
*C415000                                                         A50213
*D300000,375000,540000                                           A50213
*A365000,475000,810000,865000                                     M2174
*C135000,155000-160000,360000-365000,415000,465000,755000-765000  M2174
*D355000                                                          M2174
*A665000,880000                                                  A42415
*C415000,550000-560000                                           A42415
*C465000,477400,760000-765000                                    X01004
*D811000-815000,855000                                           X01004
*A882000                                                         X02004
*C575000,730000-750000                                           X02004
*C744000                                                        OX02176
*THIS IS A DUMMY APAR TO CORRECT FICHE FLAGGING PROBLEM        @OZ19773
**********************************************************************
*                                                                     *
*TITLE: 'IGC1303D'-TCAM COMMAND SCHEDULER                             *
*                                                                     *
*FUNCTION:THIS MODULE BUILDS A COMMAND INPUT BLOCK (CIB) FOR THE TCAM *
*   OPERATOR CONTROL COMMANDS WHICH ARE ENTERED FROM THE SYSTEM       *
*   CONSOLE. THE COMMANDS ARE   VARY,HOLD,RELEASE,DISPLAY,AND HALT.   *
*   IT ISSUES A GETMAIN FOR THE REGION IN WHICH TO BUILD THE CIB.THEN *
*   IT ISSUES A QEDIT MACRO     (WHICH PUTS THE CIB INTO THE CIB      *
*   CHAIN)AND POSTS AN ECB FOR THE OPERATOR CONTROL SUBTASK. IF THE   *
*   RETURN CODE FROM QEDIT INDICATES THAT THE MAXIMUM CIB LIMIT HAS   *
*   BEEN REACHED,THIS MODULE ISSUES A FREEMAIN FOR THE CIB AND REJECTS*
*   THE COMMAND,ISSUING A MESSAGE TO THE CONSOLE.                     *
*ENTRY POINT:                                                         *
*        'IGC1303D'                                                   *
*                                                                     *
*INPUT:REGISTER 2 HAS THE ADDRESS OF THE EXTENDED SAVE AREA           *
*                                                                     *
*OUTPUT:A CIB IS BUILT AND PUT INTO THE CIB CHAIN.                    *
*                                                                     *
*EXTERNAL ROUTINES:NONE                                               *
*                                                                     *
*EXITS-NORMAL:THIS MODULE BRANCHES TO THE ADDRESS IN REGISTER 14.     *
*                                                                     *
*EXITS-ERROR:THIS MODULE XCTLS TO IGC0503D OR IGC2103D TO ISSUE   M2174
*   AN ERROR MESSAGE                                              M2174
*                                                                     *
*   THE ERRORS RECOGNIZED ARE:                                        *
*   1.TCAM IS NOT IN THE SYSTEM                                       *
*   2.TCAM NOT READY                                              M2174
*   3.REQUIRED PARAMETER MISSING                                  M2174
*   4.INSUFFICIENT CORE TO BUILD CIB                              M2174
*   5.MAXIMUM CIB LIMIT IS EXCEEDED                               M2174
*                                                                     *
*TABLES/WORK AREAS:                                                   *
*                                                                     *
*   EXTENDED SAVE AREA                                                *
*   CVT                                                               *
*   CIB                                                               *
*   AVTCAVTSPLPT                                                      *
*                                                                     *
*                                                                     *
*ATTRIBUTES:REENTERABLE                                               *
*                                                                     *
*NOTES:THE OPERATION OF THIS MODULE DEPENDS UPON AN INTERNAL REPRE-   *
*   SENTATION OF THE EXTERNAL CHARACTER SET WHICH IS EQUIVALENT TO    *
*   THE ONE USED AT ASSEMBLY TIME. THE CODING HAS BEEN ARRANGED SO    *
*   THAT REDEFINITION OF 'CHARACTER' CONSTANTS,BY REASSEMBLY, WILL    *
*   RESULT IN A CORRECT MODULE FOR THE NEW DEFINITIONS.               *
*                                                                     *
***********************************************************************
         SPACE 3
R0       EQU   0
R1       EQU   1
R2       EQU   2                        XSA-EXTENEED SAVE AREA ADDRESS
R3       EQU   3                        BASE REG
R4       EQU   4                        BASE FOR CVT
R5       EQU   5                        BASE FOR AVT
R6       EQU   6                        LENGTH OF CIB
R7       EQU   7                        WORK REG
R9       EQU   9                        BEGINNING OF OPERAND
R10      EQU   10
R11      EQU   11                       BASE FOR SPL
R12      EQU   12
R13      EQU   13
R14      EQU   14                       RETURN REG
R15      EQU   15                       XCTL REG
         SPACE 3
AQTCVTPT EQU   16                       CVT POINTER
AQTINVAL EQU   5                        OFFSET FOR MSG-INVALID COMMAND
AQTNOAQT EQU   36                       OFFSET FOR MSG-NOT ACTIVE M2174
AQTPMISS EQU   12                       OFFSET FOR MSG-PARM MISS  M2174
AQTNORDY EQU   37                       OFFSET FOR MSG-TASK BUSY  M2174
AQTTHREE EQU   3
TCBABEND EQU   X'80'                    ABNORM TERM IN PROGRESS  A50213
         SPACE 3
*INITIALIZATION
         SPACE
         BALR  R3,0                     SET UP BASE
         USING *,R3
IED1303D IEDHJN AQT010                  MODULE ID AND DATE       S22025
         USING AQTXSA,R2                BASE FOR XSA
         L     R4,AQTCVTPT              GET CVT POINTER
         USING CVT,R4                   CVT ADDRESSABILITY       Y02027
         L     R5,CVTAQAVT              GET TCX ADDRESS FROM CVT Y02027
         LTR   R5,R5                    CHECK FOR TCAM IN SYSTEM Y02027
         BNZ   AQT015                   BRANCH IF TCAM IN SYSTEM Y02027
         MVI   XAE,AQTNOAQT             SET UP FOR ERROR MSG
         MVC   XAV(8),AQTTCAM
         B     AQT060                   TAKE ERROR EXIT          X01004
AQT015   EQU   *
         USING IEDQTCX,R5               TCX ADDRESSABILITY       Y02027
         TM    TCXBIT1,AVTREADN         TCAM MARKED AS READY     Y02027
         BO    AQT017                   BRANCH IF READY           M2174
AQT016   EQU   *                                                 A50213
         MVI   XAE,AQTNORDY             SET FOR ERROR MESSAGE     M2174
         B     AQT060                   XCTL TO IGC0503D TO SEND X01004
*                                         IEE342I COMMAND REJECTEDM2174
*                                         TASK BUSY               M2174
AQT017   EQU   *                                                  M2174
         TM    TCXBIT1,AVTCLOSN+AVTQUCKN NORMAL TERMINATION      Y02027
         BNZ   AQT016                   REJECT COMMAND IF YES    A50213
         L     R6,XAL                   GET OPERAND FIELD POINTER
         LTR   R7,R6                    SAVE PTR AND CHECK FOR OPERAND
         BNZ   AQT020                   BRANCH IF OPERAND SPECIFIED
         MVI   XAE,AQTPMISS             SET MSG CODE
         B     AQT060                   SEND MSG
AQT020   EQU   *
         LA    R6,1(R6)                 INCREMENT POINTER
         CLI   0(R6),C' '               CHECK FOR END OF OPERAND
         BNE   AQT020                   BRANCH IF NOT THE END
         LR    R9,R7                    SAVE OPERAND POINTER
         SR    R6,R7                    GET LENGTH OF OPERANDS
         LA    R6,1(,R6)                ADD 1 FOR BLANK            1030
         LR    R10,R6
         LA    R6,CIBDATA-CIBNEXT(0,R6) GET LENGTH OF CIB
         LR    R7,R6                    SAVE LENGTH FOR TEST     A42415
         N     R7,AQTMASK               IS THIS MULTIPLE OF 8    A42415
         BZ    AQT027                   IF SO, BRANCH            A42415
         O     R6,AQTMASK               INSURE A SEVEN           A42415
         LA    R6,ONE(,R6)              GET NEXT MULTIPLE OF 8   A42415
AQT027   EQU   *                                                 A42415
         LM    R12,R13,XAV              SAVE VERB
         LA    R7,XAS+4
         STM   R6,R7,XAV                SET LENGTH AND ADDRESS   X02004
         MVI   XAV+NINE,SP245           SET SUBPOOL FOR VS/2     X02004
         GETMAIN EC,MF=(E,XAV)          GET CIB                  X02004
         STM   R12,R13,XAV              RESTORE VERB
         L     R1,XAS+4                 GET ADDRESS OF GETMAIN AREA
         LTR   R15,R15                  CHECK RETURN CODE
         BZ    AQT030                   BRANCH IF CORE WAS ALLOCATED
         SPACE 3
*SET UP PARAMETERS IN XSA FOR ERROR MESSAGE
         SPACE
         MVI   XAE,AQTINVAL             SET MESSAGE CODE
         MVC   XAV(8),AQTNOCOR          SET UP VARIABLE PART OF MSG
         B     AQT060                   EXIT TO WRITE MSG        Y02027
         SPACE 3
*BUILD CIB
         SPACE
AQT030   EQU   *
         USING CIBDSECT,R1              BASEE FOR CIB DSECT
         BCTR  R6,0                     DECREMENT FOR EX         Y02027
         EX    R6,AQTCLEAR              CLEAR CIB                Y02027
         LA    R6,ONE(R6)               RESTORE REGISTER 6       Y02027
         SRL   R6,AQTTHREE              CONVERT LENGTH TO DBL WORDS
         STC   R6,CIBLEN                SAVE LENGTH IN CIB
         MVC   CIBVERB(1),XAN           VERB CODE
         MVC   CIBCONID,XAU             MOVE CONSOLE ID          A42415
         STH   R10,CIBDATLN             LENGTH OF DATA
         BCTR  R10,0                    SUBTRACT 1 FOR EXECUTE
         EX    R10,AQTMOVE              MOVE OPERAND TO CIB
         L     R11,TCXCOMPT             ADDR OF TCAM CSCB        Y02027
         LA    R0,4(0,R11)              ADDR OF CIB CHAIN
         QEDIT BLOCK=(1),ORIGIN=(0)     QUEUE CIB OFF SPL
         LTR   R15,R15                  CHECK RETURN CODE
         BZ    AQT040                   BRANCH IF CIB WAS QUEUED
         SPACE 3
*MAXIMUM CIB LIMIT WAS REACHED-FREEMAIN AND SET UP PARM FOR ERROR MSG
         SPACE
         SLL   R6,AQTTHREE              GET CIB LENGTH IN BYTES
         LA    R0,SP245                 VS/2 SUBPOOL FOR FREE    X02004
         SLL   R0,TOHI                  SET SUBPOOL FOR FREEMAIN X02004
         OR    R0,R6                    SET SUBPOOL AND LENGTH   X02004
         L     R1,0(R7)                 SET ADDRESS TO FREE     OX02176
         FREEMAIN R,LV=(0),A=(1)        FREE CIB                 X02004
         MVI   XAE,AQTINVAL             ERROR INDICATOR           M2174
         MVC   XAV(8),AQTCSCB           FOR MAX CIB'S EXCEEDED    M2174
         B     AQT060                   TAKE ERROR EXIT           M2174
AQT040   EQU   *
         L     R1,0(0,R11)              ADDR OF ECB
         L     R11,TCXASCB              MCP-ASCB POINTER         Y02027
*              CROSS MEMORY POST ECB IN CSCB WAITED ON BY OPTCL  Y02027
         POST  (R1),ASCB=(R11),ERRET=CVTBRET,MF=(E,XASAVLOC)
         BR    R14                      RETURN
         SPACE 3
*SET UP XSA AND XCTL TO ERROR MSG ROUTINE
         SPACE
AQT060   EQU   *
         MVC   XAX(8),AQT0503D
         LA    R7,XAX                   POINTER TO XCTL NAME
         ST    R7,XAP
         XC    XAD(4),XAD               CLEAR DCB FIELD
         LA    R15,XAP                  ADDR OF PTR TO XCTL NAME
         XCTL  SF=(E,(15))
         SPACE 3
AQT0503D DC    CL8'IGC0503D'            MODULE NAME
AQTTCAM  DC    CL8'TCAM'                TCAM
AQTNOCOR DC    CL8'NO CORE-'            NO CORE
AQTCSCB  DC    CL8'CSCB USE'            MAX CIB'S EXCEEDED        M2174
         DS    0F                       ALIGNMENT
AQTMASK  DC    X'00000007'              MASK FRO TURNING OFF 1ST 29 BIT
AQTMOVE  MVC   CIBDATA(3),0(R9)         MOVE OPERAND TO CIB
AQTCLEAR XC    CIBNEXT(0),CIBNEXT       CLEAR CIB                Y02027
ONE      EQU   1                        DECIMAL VALUE 1          A42415
TOHI     EQU   24                       SHIFT MASK TO HIGH BYTE  X02004
NINE     EQU   9                        CONSTANT OF NINE         X02004
SP241    EQU   241                      SUBPOOL 241 FOR VS/1 CIB X02004
SP245    EQU   245                      SUBPOOL 245 FOR VS/2 CIB X02004
SYSTYPE  EQU   116                      CVT OFFSET TO SYSTEM TYPEX02004
SYSVS1   EQU   X'20'                    VS/1 SYSTEM SWITCH       X02004
         EJECT
AQTXSA   DSECT
         IEEXSA
         CVT   DSECT=YES
         TTCXD                                                   Y02027
         EJECT
         EJECT
CIBDSECT DSECT
         IEZCIB
         EJECT
         TAVTD
         TTCBD
         END
