         TITLE 'IGG020P3 - PARTIAL RELEASE PRECLOSE MODULE'
IGG020P3 CSECT
*
*          VS2 RELEASE 02 DELETIONS/CHANGES
*0000118000,158000,266000,296000-298000,405000,417800-417880,    Y02080
*0000424000,483400,492200-492600,492900-492950,493020,493055-    Y02080
*0000493062,493064-493065,493090-493100,493111,493154-493530,    Y02080
*0000497000-581000,598000,616000-644000,658000-665000,693000,    Y02080
*0000706040,706100,714000,721000-882000                          Y02080
*0000000600-003000,706200-714100                                 Y02078
*0000                                                            Y02144
*0000922600                                                      Y02082
*0000492950                                                      YM2880
*0000700400                                                      YM2887
*0000420000-421000,496600-496900,700100                          YM3048
*0000493300-493600,494100,495300                                 YM3912
*0000496300,497100-497120,499220-499240                          YM3997
*          RELEASE 21 DELETIONS/CHANGES
*0980105000-196000,298000,378000-385000,434000-462000,493000,    A37199
*0980493800-494000,504000-553000,595000-602000,742000,756000,    A37199
*0980840000,920500                                               A37199
*0980008000,315000,350000,494200,495200,917700-919800,921900     A39292
*          RELEASE 21.7 DELETIONS/CHANGES
*0000118000-138000,224000,241000,287000,493600-494000,693000,   SA54549
*0000706100-706600,770000,812000                                SA54549
*0000485600,487200-488200                                       SA56400
*0000230480-230624                                               SM5456
*          RELEASE 20 DELETIONS/CHANGES
*0960                                                            S20201
*          RELEASE 19 DELETIONS/CHANGES
*0950154000-168000,210000-224000,238000-245000,399000-420000,    O19117
*0950490000                                                      O19117
*0950014000-021000,028000-070000,658000                          M3660
*          RELEASE 18 DELETIONS/CHANGES
*                                                                MCS
*
* MODULE NAME - IGG020P3
*
* DESCRIPTIVE NAME - PARTIAL RELEASE PRECLOSE MODULE
*
* COPYRIGHT - NONE
*
* CHANGE ACTIVITY - SEE DELETIONS/CHANGES FOLLOWING CSECT CARD
*
*STATUS CHANGE LEVEL 005
*
***********************************************************************
*
*FUNCTION/OPERATION - THIS MODULE REWRITES THE MODIFIED FORMAT 4 DSCB,
*        IF NECESSARY. IT DEQUEUES THE VTOC AND FREES THE WORK AREA
*        BEFORE RETURNING TO EITHER CLOSE OR CHECKPOINT/RESTART. IF AN
*        RPS WORK AREA EXISTS, IT IS FREED BEFORE CONTROL IS RETURNED.
*        IF AN I/O ERROR HAS OCCURRED IN A PARTIAL RELEASE MODULE,
*        THIS MODULE ISSUES MESSAGE IEC603I WITH THE CODE=0.  IF
*        OVERLAPPING EXTENTS WERE FOUND IN THE FORMAT 5 DSCB AND
*        THE DIRF BIT WAS NOT PREVIOUSLY SET IN THE FORMAT 4 DSCB,
*        THIS MODULE ISSUES MESSAGE IEC603I WITH THE CODE=4 AND
*        REWRITES THE FORMAT 4 DSCB WITH THE DIRF BIT SET.
*
*ENTRIES - THE ONLY ENTRY POINT IS IGG020P3. ENTRY IS MADE VIA BRANCH
*        FROM IGG020D1 UNDER NORMAL CONDITIONS AND FROM ALL LOADS OF
*        PARTIAL RELEASE UNDER ERROR CONDITIONS.
*
*INPUT - AT ENTRY TO THIS MODULE REGISTER 11 CONTAINS A POINTER TO
*        RELEASE'S WORKAREA AND REGISTER 13 A POINTER TO THE CALLER'S
*        WORKAREA. REGISTER 2 IS USED TO PASS ERROR CODES FROM ALL
*        OTHER MODULES OF PARTIAL RELEASE TO IGG020P3.
*        THE RETURN CODES ARE:
*        X'02' - UNABLE TO FIND EXTENT IN FORMAT 1 DSCB.
*        X'04' - UNABLE TO FIND EXTENT IN FORMAT 3 DSCB.
*        X'08' - ANOTHER DCB IS OPEN TO THE DATA SET
*        X'10' - PERMANENT I/O ERROR.
*        X'20' - NO ROOM IN THE VTOC.
*        IF REGISTER 2 IS ZERO, NO ERROR OCCURRED.
*
*OUTPUT - AN UPDATED FORMAT 4 DSCB IF REQUIRED. THE VTOC IS DEQUEUED.
*
*EXTERNAL ROUTINES
*         EXCP(0) - EXECUTE A CHANNEL PROGRAM
*         WAIT(1) - WAIT ON EVENT CONTROL BLOCK
*         XCTL(7) - TRANSFER CONTROL TO CHECKPOINT/RESTART
*         WTO(35) - WRITE A MESSAGE TO THE OPERATOR
*         IECRES  - FREE WORK AREA(S), LOAD AND BRANCH TO CLOSE
*
*EXITS -  IGC0R05B (D.A. DISPOSITION MODULE OF REPI/O)
*         IFG0202E (D.A. MODULE OF CLOSE)
*
*STORAGE - PROGRAM CODE CSECT    - LESS THAN 1024 BYTES
*          RELEASE WORK AREA     - 620 BYTES
*          RPS WORK AREA         - 128 BYTES
*          I/O SUPPORT WORK AREA
*
*TABLES/WORKAREAS-
*         THE FORMAT AND CONTENTS OF THE WORKAREA USED ARE DESCRIBED
*         BY THE DSECTS AT THE END OF THE LISTING.
*
*ATTRIBUTES-
*    REENTRANT, ENABLED, PRIVILEGED
*
*
*REGISTER USAGE
*
R0       EQU   0                                                 O19117
R1       EQU   1                                                 O19117
R2       EQU   2                                                 O19117
ERRETREG EQU   2                        ERROR CODE REG           O19117
R3       EQU   3                                                 O19117
R4       EQU   4                                                 O19117
RX4      EQU   4                        CONTAINS WHERE FROM SW   O19117
R5       EQU   5                                                 O19117
R6       EQU   6                                                 O19117
R7       EQU   7                                                 O19117
R8       EQU   8                                                 O19117
R9       EQU   9                                                 O19117
R10      EQU   10                                                O19117
RXUCB    EQU   10                       PNTR TO UCB              O19117
R11      EQU   11                                                O19117
RXWKA    EQU   11                       BASE FOR WORKAREA        O19117
R12      EQU   12                                                O19117
RXBASE   EQU   12                       BASE FOR PROGRAM         O19117
R13      EQU   13                                                O19117
RXCLOWKA EQU   13                       BASE FOR CLOSE WORKAREA  O19117
R14      EQU   14                                                O19117
RLINK    EQU   14                       SUBROUTINE LINK REG      O19117
R15      EQU   15                                                O19117
*
*** OTHER EQUATES
*
K0       EQU   0                        CONSTANT OF 0            YM2880
K1       EQU   1                        CONSTANT OF 1            YM2880
K2       EQU   2                        CONSTANT OF 2            YM3912
DIRFBIT  EQU   X'04'                    VTOC INTERRUPT BIT       A37199
C4       EQU   4                        FOUR CHARACTERS          S20201
AVTDS    EQU   120                      DISP. TO AVT PTR IN W.A. S20201
UUU      EQU   37                       DISP. TO UUU IN MESSAGE  Y02078
VOLSER   EQU   41                       DISP. TO VOLSER IN MSG   Y02078
CODE     EQU   48                       DISP. TO CODE IN MESSAGE Y02078
CODE4    EQU   C'4'                     CODE 4 IN MESSAGE        Y02078
BLANK    EQU   C' '                     EBCDIC BLANK             YM2880
*
*
         BALR  RXBASE,0            LOAD BASE REGISTER
         USING CVT,R15
         USING *,RXBASE                 ESTABLISH PROGRAM BASE   O19117
         USING PRLSEWKA,RXWKA           WORK AREA ADDRESSABILITY Y02080
         USING CLOSEWKA,RXCLOWKA        ESTABLISH CLOSE WORKAREA O19117
*                                       BASE
*
* * DETERMINE IF THE FORMAT 4 DSCB MUST BE REWRITTEN
*
BEGIN    EQU   *                                                SA54549
         LTR   ERRETREG,ERRETREG        Q - ERROR ENTRY          O19117
         BZ    NOERROR                  IF NOT, BR               O19117
         CH    ERRETREG,IOCODE          TEST ERROR CODE          O19117
         BNL   NOWRT                    BR IF IT IS AN I/O ERROR O19117
*                                       OR THERE IS NO FORMAT 0
*                                       DSCB AVAILABLE IN VTOC
TESTDIRF EQU   *                                                SA56400
         XI    DS4VTOCI,DIRFBIT         RESET/SET DIRF BIT      SA56400
         TM    DS4VTOCI,DIRFBIT         TEST RESULTS            SA56400
         BO    NOWRT                    IF PAST INTERRUPTION,   SA56400
*                                       BRANCH                  SA56400
         TM    DADSMTBL,ERXTENT         TEST IF OVERLAPPING     SA56400
*                                       EXTENTS FOUND           SA56400
         BO    NOWRT                    BRANCH IF YES           SA56400
         B     WRTF4                    IF NO, WRITE BACK F4    SA56400
NOERROR  EQU   *                                                 O19117
         CLI   HOLENUM+1,X'00'          Q - HOLE COUNT CHANGE    O19117
         BE    TESTDIRF                 IF NOT, BR               O19117
         LH    R15,DS4DSREC             UPDATE HOLE              O19117
         AH    R15,HOLENUM                COUNT IN               O19117
         STH   R15,DS4DSREC                 FORMAT 4             O19117
*
*** THIS SECTION TESTS IF OVERLAPPING EXTENTS WERE FOUND BETWEEN
*   SPACE BEING RELEASED AND SPACE ALREADY EXISTING IN THE
*   FORMAT 5. IT ALSO RELOCATES THE CHANNEL PROGRAM AND WRITES
*   BACK THE FORMAT 4.
*
XTENTEST EQU   *                                                SA56400
         TM    DADSMTBL,ERXTENT         WERE OVERLAPPING        SA56400
*                                       EXTENTS FOUND           SA56400
         BNO   OFFDIRF                  IF NO BRANCH TO         SA56400
*                                       SET/RESET DIRF BIT      SA56400
         OI    DS4VTOCI,DIRFBIT         TURN ON DIRF BIT        SA56400
         B     WRTF4                    BRANCH TO WRITE BACK    SA56400
*                                       THE F4 WITH DIRF BIT ON SA56400
OFFDIRF  XI    DS4VTOCI,DIRFBIT         SET/RESET DIRF BIT      SA56400
WRTF4    EQU   *                                                 O19117
         LM    R3,R8,CHANPROG           PICK UP CCS'S            O19117
         ALR   R3,RXWKA                 RESOLVE                  O19117
         ALR   R5,RXCLOWKA                CCW                    O19117
         ALR   R7,RXWKA                     ADDRESSES            O19117
         STM   R3,R8,DXCCW1             STORE UPDATED CCW'S      O19117
         MVC   DXDAADDR+3(5),VTOCADR    INSERT VTOC ADDR INTO    O19117
*                                       DEB                      O19117
         LA    R15,DXCCW1               SET CCW POINTER          O19117
         ST    R15,IOBSIOCC               INTO IOB               O19117
         EXCP  DXIOB                                             O19117
         WAIT  ECB=DXECB                                         O19117
         TM    DXECB,X'20'              Q - I/O SUCCESSFUL       O19117
         BO    IOSUC                    IF YES, BR               O19117
         LA    ERRETREG,X'10'           SET PERM I/O ERROR CODE  O19117
IOSUC    EQU   *                                                 O19117
*
* * THIS SECTION DEQ'S FROM SYSDSN (FOR CHKPT/RESTART ONLY).
*
NOWRT    EQU   *                                                 O19117
         LA    R1,DXCCW1                RESTORE CCW PNTR IN IOB  O19117
         ST    R1,IOBSIOCC                FOR CALLER             O19117
         TM    DSMADTB2,DSMDSNE         TEST IF ENQ'ED ON DSN    Y02082
         BNO   DEQVTOC                  BRANCH IF NOT            Y02082
         L     R15,CVTPTR               LOAD CVT ADDRESS         Y02082
         L     R15,CVTTCBP              LOAD TCB DW ADDRESS      Y02082
         L     R3,C4(,R15)              LOAD CURRENT TCB ADDRESS Y02082
         USING TCB,R3                   TCB ADDRESSABILITY       Y02082
         L     R3,TCBJSCB               LOAD JSCB ADDRESS        Y02082
         USING IEZJSCB,R3               JSCB ADDRESSABILITY      Y02082
         L     R3,JSCBACT               GET ACTIVE JSCB ADDRESS  YM2887
         L     R3,JSCBTCBP              INITIATOR'S TCB ADDRESS  Y02082
         DROP  R3                                                Y02082
         MVC   MJNAME(L'SYSDSN),SYSDSN  MAJOR RESOURCE NAME      Y02082
         MVC   ENQAREA(DEQLTH),DEQLIST  MOVE IN DEQ PARM LIST    Y02082
         LA    R5,JFCBDSNM+L'JFCBDSNM-K1  LAST BYTE OF DSNAME    YM2880
TESTDSN  EQU   *                        SEARCH FOR NON-BLANK     YM2880
         CLI   K0(R5),BLANK             IS THIS ONE A BLANK      YM2880
         BNE   GETLNGTH                 BRANCH IF NO TO COMPUTE  YM2880X
                                        LENGTH OF NAME FOR DEQ   YM2880
         BCT   R5,TESTDSN               TRY NEXT BYTE            YM2880
GETLNGTH EQU   *                        CALCULATE LENGTH         YM2880
         LA    R6,JFCBDSNM-K1           BYTE PRECEDING DSNAME    YM2880
         SR    R5,R6                    R5 HAS LENGTH FOR DEQ    YM2880
         DEQ   (MJNAME,JFCBDSNM,(R5)),TCB=(R3),MF=(E,ENQAREA)    Y02082
*
         NI    DSMADTB2,X'FF'-DSMDSNE   RESET DSN ENQ'ED SWITCH  Y02082
*
* * THIS SECTION DEQ'S THE VTOC.
*
DEQVTOC  EQU   *                        DEQ VTOC                 Y02082
         USING UCB,RXUCB                UCB ADDRESSABILITY       YM3912
         L     RXUCB,DXDEBUCB           LOAD UCB ADDRESS         YM3912
         LA    R3,SRTEVOLI              POINT TO VOLSER          YM3912
         MVC   MJNAME(L'VTOCNAME),VTOCNAME  MAJOR RESOURCE NAME  YM3912
         MVI   ENQAREA,X'FF'            SET LAST ENTRY INDICATOR YM3912
         SR    R1,R1                    ZERO REGISTER            YM3912
         STH   R1,ENQAREA+K2            CLEAR FOR DEQ MACRO      YM3912
         DEQ   (MJNAME,(R3),6,SYSTEMS),RMC=STEP,MF=(E,ENQAREA)   YM3912
*
         NI    DSMADTB2,X'FF'-DSMVTOCR-DSMSMCE  RESET VTOC       Y02144
*                                       ENQ'ED SMC SWITCHES      Y02144
*
* * THIS SECTION ISSUES MESSAGE IEC603I WITH CODE=0 OR CODE=4.
*
MSGTEST  EQU   *                        ISSUE MESSAGE            Y02078
         CH    ERRETREG,IOCODE          TEST IF I/O ERROR ON VOL Y02078
         BNE   TSTOVLAP                 BRANCH IF NO I/O ERROR   Y02078
         LA    RLINK,ISSUEMSG           SET UP RETURN REGISTER   Y02078
         B     BUILDMSG                 GO BUILD MESSAGE IEC603I Y02078
TSTOVLAP EQU   *                        TEST DIRF                Y02078
         TM    DADSMTBL,ERXTENT         TEST IF DIRF BIT SET DUE Y02078
*                                       TO OVERLAPPING EXTENTS   Y02078
         BNO   RPSTEST                  BRANCH IF NOT            Y02078
         LA    RLINK,SETCODE            SET UP RETURN REGISTER   Y02078
BUILDMSG EQU   *                        BUILD MESSAGE            Y02078
         MVC   INDSCB(MSGEND-ERRMSG),ERRMSG  MOVE IN MESSAGE     Y02078
         MVC   INDSCB+UUU(L'UCBNAME),UCBNAME  MOVE IN EBCDIC UCB Y02078
*                                       CHANNEL/UNIT ADDRESS     Y02078
         MVC   INDSCB+VOLSER(L'SRTEVOLI),SRTEVOLI  MOVE IN VOLID Y02078
         BR    RLINK                    CONTINUE                 Y02078
SETCODE  EQU   *                        CODE=4                   Y02078
         MVI   INDSCB+CODE,CODE4        SET CODE=4               Y02078
ISSUEMSG EQU   *                        ISSUE MESSAGE            Y02078
         WTO   MF=(E,INDSCB)            ISSUE MESSAGE IEC603I    Y02078
*
* * THIS SECTION RESTORES THE DEB AVT PTR IF AN RPS WORK AREA EXISTS.
*
RPSTEST  EQU   *                        RPS TEST                 Y02078
         TM    DSMADTB1,DSMRPSAP        TEST IF RPS APPENDAGE    YM3048X
                                        WAS LOADED               YM3048
         BNO   CONTINUE                 BRANCH IF NOT            YM3048
         L     R1,DXDEBAPP              GET PTR TO RPS WORK AREA YM3048
         MVC   DXDEBAPP(C4),AVTDS(R1)   RESTORE DEB AVT POINTER  S20201
         NI    DSMADTB1,X'FF'-DSMRPSAP  RESET RPS APP LOADED BIT Y02144
*
* * THIS SECTION PREPARES TO RETURN TO CLOSE BY LOADING THE ENTRY
*   POINT ADDRESS.
*
CONTINUE EQU   *                                                 S20201
         TM    DADSMTBL,FROMCR          TEST IF ENTRY FROM C/R   Y02080
         BO    RELOADRG                 BRANCH IF YES            Y02080
         IECRES LOAD,EXTPR=(RXWKA),MODNM=CLOSE,BRANCH=NO         Y02080
         L     R14,WTGMODEP             LOAD ENTRY POINT ADDRESS Y02080
*
* * THIS SECTION FREES THE PARTIAL RELEASE WORK AREA (AND THE
*   RPS WORK AREA) AND RESTORES THE CALLER'S REGISTERS.  IF
*   REGISTER 4 CONTAINS A NEGATIVE VALUE, PARTIAL RELEASE WAS
*   ENTERED FROM THE 'REPI/O' FUNCTION OF CHECKPOINT/RESTART.
*
RELOADRG EQU   *                        LOAD REG                 Y02080
         L     R8,SVRBPTR               LOAD SVRB ADDRESS        Y02080
         LR    R9,RXCLOWKA              SAVE CLOSE W/A ADDRESS   Y02080
         STM   ERRETREG,R14,DXCCW1      SAVE REGISTERS 2-14      Y02080
         IECRES FREE,PREFIX=FIRST,A=(RXWKA)  FREE RELEASE WKAREA Y02080
         LM    ERRETREG,R14,DXCCW1-DXLBL(R9)  RESTORE REGISTERS  Y02080
         LR    R1,ERRETREG              CALLER EXPECTS ERROR    SA54549
*                                       CODE IN REGISTER 1
         LM    R2,R10,96(R8)            RESTORE REGS FROM CALLER A39292
         LTR   RX4,RX4             IS REGISTER 4 NEGATIVE
         BM    XCTL                BRANCH IF YES(ENTERED FROM REPDCB)
*
         IECRES BRANCH,EPA=(R14)        BRANCH TO CLOSE          Y02080
*
* * THIS SECTION XCTL'S TO CHECKPOINT/RESTART.
*
XCTL     EQU   *                                                 M3660
         XC    DXCCW1,DXCCW1            ZERO XCTL PARAMETER LIST Y02080
         LA    R15,DXCCW2               POINT TO MODULE NAME     Y02080
         ST    R15,DXCCW1               XCTL NAME ADDRESS        Y02080
         MVC   DXCCW2,REPDCB            MOVE IN C/R MODULE NAME  Y02080
*
         XCTL  MF=(E,(1)),SF=(E,DXCCW1)  XCTL TO CHKPT/RESTART   Y02080
*
*                             CONSTANTS
*
REPDCB   DC    C'IGC0R05B'         REPI/O NAME
SYSDSN   DC    CL8'SYSDSN'              MAJOR RESOURCE NAME      Y02082
VTOCNAME DC    CL8'SYSVTOC'             MAJOR QUEUE NAME         O19117
DEQLIST  DEQ   (,,,SYSTEM),TCB=0,MF=L   SYSDSN DEQ LIST          YM2887
DEQLTH   EQU   *-DEQLIST                LENGTH OF DEQ LIST       Y02082
IOCODE   DC    H'16'                    I/O ERROR CODE           O19117
ERRMSG   WTO   'IEC603I VTOC ERRORS MAY EXIST ON UUU,VOLSER,0',  Y02078X
               ROUTCDE=(4,10),DESC=4,MF=L                        Y02078
MSGEND   EQU   *                        END OF MSG               Y02078
*
* * CHANNEL PROGRAM TO WRITE BACK FORMAT 4 DSCB
*
CHANPROG DS    0F                                                O19117
*CCW1 - SEARCH ID EQ
         DC    X'31'                                             O19117
         DC    AL3(VTOCADR-FIRST)                                O19117
         DC    X'4000'                                           O19117
         DC    H'5'                                              O19117
*CCW2 - TIC
         DC    X'08'                                             O19117
         DC    AL3(DXCCW1-CLOSEWKA)                              O19117
         DC    F'0'                                              O19117
*CCW3 - WRITE DATA
         DC    X'05'                                             O19117
         DC    AL3(VTOCDSCB-FIRST)                               O19117
         DC    H'0'                                              O19117
         DC    H'96'                                             O19117
*
*** TABLE OF MODULE NAMES AND ENTRY POINT ADDRESSES
*
         XCTLTABL ID=(CLOSE,IFG0202E),SVC=020,LENGTH=,BRT=YES    Y02080
         SPACE 2                                                 Y02078
         IECDSECS CVT,JSCB,TCB,UCB,EXPAND=YES                    Y02082
*
*** RELEASE WORK AREA
*
WORKAREA IECPRLWA EP,F4,ADT=YES         RELEASE WORK AREA        Y02144
         EJECT                                                  SA54549
*
*** I/O SUPPORT WORK AREA
*
CLOSEWKA DSECT
         IECDSECT
ZERO     EQU   DXCCW4+4                                          O19117
MJNAME   EQU   DXCCW9                   MAJOR RESOURCE NAME      Y02082
ENQAREA  EQU   DXCCW11                                           O19117
         END
