         TITLE 'IGC00030 - DADSM RENAME - ENTRY POINT'           Y02082
IGC00030 CSECT
*          RELEASE 15 DELETIONS                                       *
*1652
*          RELEASE 16 DELETIONS                                       *
*          RELEASE 17 DELETIONS                                       *
*3242000430,000730,020400,020600                                   MC0M
*3242029720                                                       17762
*          RELEASE 18 DELETIONS                                       *
*3522029700-029720,030800-030900                                  25343
*3522030900                                                       22000
*          RELEASE 19 DELETIONS                                       *
*          RELEASE 20 DELETIONS                                       *
*2162                                                            M0013
*2162056800                                                      20201
*          RELEASE 21 DELETIONS                                       *
*1231000400,004200-004400,024400-025000,047800-049800,052800,    A38860
*1231056200-056400,057000,065000-065300,065600,079700            A38860
*0000023000,063800                                              SA49351
*          RELEASE 21.7 DELETIONS
*0000004200-004400,020002-020120,020200-020400,023400-023800,   SA53147
*0000025200,034800-053800,060600-063660,065200-065400           SA53147
*0000020161-020162,026200                                       SA57261
*          RELEASE 22 DELETIONS                                       *
*          VS2 RELEASE 02 DELETIONS/CHANGES                           *
*0000000030-000150,000700,001200,004800-005000,005600-007000,    Y02080
*0000007800-008000,012800-013600,015700-015800,021200,021700-    Y02080
*0000021920,021960-023000,025600-025800,029480-029720,030840-    Y02080
*0000030910,031900,032000-032700,033100-033200,034000,034300-    Y02080
*0000034400,035100,056600-056900,058830-059100,060600,063720-    Y02080
*0000064200,065000,066000-067200,071000-079800,080400,081200     Y02080
*0000081000-081200                                               Y02078
*0000000200,001600-003200,005200-006000,007300-007600,008400,    Y02082
*0000012400-014800,015700-015800,020020-020164,026200-029480,    Y02082
*0000030200-030400,031000-031100,033600,034200,035100-060200,    Y02082
*0000061100,065500-065600,071100-080800                          Y02082
*0000018440,027910,028060,028180                                 Y02132
*0000065400                                                      Y02144
*0000027010                                                      YM1312
*0000                                                            YM1241
*0000037000,038000,043240-043250,043280,043300,054000-054100     YM1337
*0000027610-027700,029350,047400,065250-065300,066650-066700     YM1063
*0000040200                                                      YM3960
*0000030100,030400,048400,051200                                 YM7036
*0000038200,040200,065050,065150                                 YM2880
*0000065450                                                      YM2887
*0000027760                                                      YM2895
*0000024090-024120                                               YM5748
*0000                                                            YM5399
*
*
* STATUS CHANGE LEVEL 003
*                                                              @G60ASBJ
*        SU60 CHANGES/DELETIONS                                @G60ASBJ
*A 020106,021650,022800,023000-023100,060800,060940            @G60ASBJ
*C 001060,001450,022900                                        @G60ASBJ
*D 060900-061000                                               @G60ASBJ
*                                                              @G60ASBJ
*   IGC00030 IS THE STANDARD MODULE NAME FOR IGC0003&              MC0M
*
*MODULE NAME - IGC00030
*
*DESCRIPTIVE NAME - DADSM RENAME                               @G60ASBJ
*
*COPYRIGHT - NONE
*
*CHANGE ACTIVITY - SEE DELETIONS/CHANGES FOLLOWING CSECT CARD
*
*ATTRIBUTES - REENTRANT
*
*FUNCTION - THIS MODULE IS THE  BEGINNING OF RENAME.   AFTER OBTAINING
*          THE RENAME WORK AREA, IT ISSUES AN ENQ ON THE TIOT.  IF
*          THIS RESOURCE IS UNAVAILABLE, THE DATA SET IS IN THE PROCESS
*          OF BEING OPENED OR CLOSED AND THEREFORE CANNOT BE RENAMED.
*          THIS MODULE THEN ATTEMPTS TO FIND A PRIMARY (DEMOUNTABLE)
*          UCB ADDRESS.  IF A UCB ADDRESS WAS SUPPLIED BY THE CALLER,
*          THE TIOT IS SEARCHED TO SEE IF THE UNIT IS ALLOCATED TO
*          THE JOB.  IF ALLOCATED, THE UNIT IS TESTED TO DETERMINE
*          IF ITS ASSOCIATED VOLUME CAN BE DEMOUNTED.  IF NO UCB
*          ADDRESS WAS SUPPLIED OR IF THE SUPPLIED ADDRESS IS EITHER
*          NOT ALLOCATED OR NOT DEMOUNTABLE, THE TIOT IS SEARCHED TO
*          SEE IF ANY VOLUME IN THE VOLUME LIST IS MOUNTED ON A UNIT
*          ALLOCATED TO THE JOB.  IF A VOLUME IS MOUNTED, ITS UCB IS
*          TESTED FOR DEMOUNTABILITY.  IF A UCB IS FOUND THAT IS BOTH
*          ALLOCATED AND DEMOUNTABLE, IT BECOMES THE PRIMARY UCB.
*          BEFORE BRANCHING TO THE NEXT LOAD OF RENAME, THIS MODULE
*          ISSUES AN ENQ ON THE DATA SET NAME.  IF THE RETURN CODE
*          INDICATES THAT THE TASK PREVIOUSLY HAD CONTROL OF THE
*          RESOURCE, THE ENQ IS REISSUED TO OBTAIN EXCLUSIVE CONTROL
*          AND A BIT IS SET TO INDICATE A DEB SEARCH MUST BE DONE
*          IN MODULE IGG03001 TO ENSURE NO ONE IS CURRENTLY OPEN TO
*          THE DATA SET.  IF THE RETURN CODE FROM EITHER ENQ INDICATES
*          THAT THE RESOURCE IS UNAVAILABLE, THE DATA SET IS OPEN AND
*          THERFORE CANNOT BE RENAMED.  IF THE DATA SET IS DETERMINED
*          TO BE A VIO DATA SET, IT IS NOT RENAMED.
*          FOR ANY ERROR CONDITIONS IN THIS MODULE, THE TIOT IS
*          DEQ'ED (IF IT WAS ENQ'ED), THE WORK AREA IS FREED, AND
*          CONTROL IS RETURNED TO THE CALLER WITH AN APPROPRIATE
*          ERROR CODE IN REGISTER 15.  FOR SOME ERROR CONDITIONS
*          (AS NOTED UNDER 'ERROR CONDITONS'), AN ERROR CODE IS SET
*          IN THE STATUS BYTE OF EACH VOLUME LIST ENTRY.
*
*ENTRIES - THE ONLY ENTRY POINT TO THIS MODULE IS IGC00030. ENTRY IS
*          MADE FROM IGG030DU.                                 @G60ASBJ
*
*SUPERVISOR CALLS AND EXTERNAL ROUTINES USED BY THIS MODULE -
*          DEQ(48)   - DEQ'S ON SYSZTIOT
*          ENQ(56)   - ENQ'S ON SYSZTIOT AND SYSDSN
*          TESTAUTH  - DETERMINES IF THE CALLER IS AUTHORIZED,
*                      IN SUPERVISOR STATE, OR IN KEY ZERO
*          MODESET   - SWITCHES TO THE CALLER'S KEY
*          SETLOCK   - OBTAINS/RELEASES THE LOCAL MEMORY LOCK
*          IEC0VL00  - CHECKS THE VALIDITY OF THE VOLUME LIST ADDRESS
*          IECRES    - OBTAINS AND FREES THE WORK AREA
*                    - BRANCHES TO THE NEXT LOAD OF RENAME
*
*OTHER MACROS USED:
*          IECDSECS - EXPANDS THE CVT, DSAB, JSCB, PSA, QDB, RB,
*                     RRPL, TIOT, TCB, AND UCB DSECTS
*          IDDVDSCB - EXPANDS THE VDSCB
*          IECRENWA - EXPANDS THE RENAME WORK AREA
*          XCTLTABL - BUILDS A LIST OF MODULE NAMES AND ADDRESSES
*
*INPUT -   AT ENTRY TO THIS MODULE, REGISTER 1 POINTS TO THE
*          PARAMETER LIST. REGISTER 0 MAY OR MAY NOT CONTAIN A UCB
*          ADDRESS. THE PARAMETER LIST CONSISTS OF FOUR WORDS:
*                    WORD 1 - RENAME = X'41002000'
*                    WORD 2 - ADDR OF OLD DATA SET NAME
*                    WORD 3 - ADDR OF NEW DATA SET NAME
*                    WORD 4 - ADDR OF VOLUME LIST
*          THE VOLUME LIST IS VARIABLE IN LENGTH. THE FIRST TWO BYTES
*          INDICATE THE NUMBER OF 12 BYTE VOLUME ID FIELDS WHICH
*          FOLLOW. A 12 BYTE VOLUME ID FIELD CONTAINS A FOUR BYTE
*          DEVICE CODE, SIX BYTE VOLUME SERIAL NUMBER, ONE BYTE SPARE,
*          AND ONE BYTE USED FOR RETURNING THE STATUS OF THE DATA SET
*          ON THAT VOLUME AS FOLLOWS:
*                    BINARY 0 - SUCCESSFUL RENAME
*                    BINARY 1 - DSCB NOT FOUND IN VTOC
*                    BINARY 2 - PASSWORD DIFFICULITY
*                    BINARY 3 - DUPLICATE NAME EXISTS
*                    BINARY 4 - PERMANENT I/O ERROR
*                    BINARY 5 - APPROPRIATE UCB UNAVAILABLE
*                    BINARY 6 - OPERATOR UNABLE TO MOUNT VOLUME
*                    BINARY 7 - DATA SET IS OPEN
*
*OUTPUT -  REGISTER 9 POINTS TO THE CVT, AND REGISTER 10 POINTS TO
*          THE PARAMETER LIST.  REGISTER 13 POINTS TO THE WORK AREA,
*          IN WHICH 'VOLNUM' CONTAINS THE NUMBER OF VOLUMES TO BE
*          RENAMED, AND 'VOLPTR' CONTAINS THE ADDRESS OF THE FIRST
*          VOLUME IN THE VOLUME LIST.  'PRUCBPTR' CONTAINS THE PRIMARY
*          (DEMOUNTABLE) UCB ADDRESS, IF ONE IS AVAILABLE.
*
*ERROR CONDITONS - UPON RETURN TO THE CALLER, REGISTER 15 CONTAINS
*          ONE OF THE FOLLOWING VALUES TO INDICATE THE COMPLETION
*          STATUS OF THE RENAME FUNCTION:
*              00 - SUCCESSFUL COMPLETION
*              04 - NO UNIT AVAILABLE FOR MOUNTING ANY OF THE VOLUMES
*                   IN THE VOLUME LIST.  THE STATUS BYTE OF EACH ENTRY
*                   IN THE VOLUME LIST IS SET TO X'05'.
*              08 - UNABLE TO RENAME ON ONE OR MORE VOLUMES. THIS CODE
*                   IS ACCOMPANIED BY A SETTING OF THE VOLUME STATUS
*                   BYTE AS DESCRIBED UNDER 'INPUT'.
*              12 - THE NUMBER OF VOLUMES IN THE VOLUME LIST WAS ZERO,
*                   OR THE VOLUME LIST WAS NOT IN THE USER'S REGION.
*
*STORAGE - PROGRAM CODE CSECT = LESS THAN 2048 BYTES
*          WORK AREA = 676 BYTES
*
*
*REGISTER USAGE:
R0       EQU   0
R1       EQU   1
RUCBSAVE EQU   1                        CALLER'S UCB ADDRESS     Y02082
R2       EQU   2
RDSAB    EQU   2                        POINTER TO THE DSAB      Y02082
R3       EQU   3
RTIOT    EQU   3                        POINTER TO THE TIOT      Y02082
P1       EQU   3
R4       EQU   4
REND     EQU   4                        POINTER TO END OF ENTRY  Y02082
P2       EQU   4
R5       EQU   5
VOLCTR   EQU   5                       NUMBER OF VOLUMES
P4       EQU   6
R7       EQU   7
VOLISTX  EQU   8                       VOLUME LIST POINTER
SVRBREG  EQU   8                       POINTER TO SVRB           Y02080
R9       EQU   9
RCVT     EQU   9                       CVT POINTER
PL       EQU   10                      PARAMETER LIST POINTER
RERRSAVE EQU   10                       ERROR CODE SAVE REGISTER Y02132
RUCB     EQU   11                      UCB POINTER
RBASE    EQU   12
RWKAREA  EQU   13                      WORKAREA POINTER
RETURN   EQU   14
R15      EQU   15                       EQUATE FOR REGISTER 15   Y02080
WORKREG  EQU   15
*
* OTHER EQUATES
*
K0       EQU   0                        CONSTANT OF 0            Y02082
K1       EQU   1                        CONSTANT OF 1            Y02082
K2       EQU   2                        CONSTANT OF 2            Y02080
K3       EQU   3                        CONSTANT OF 3            Y02082
NONZERO  EQU   4                        NON-ZERO VALUE           YM1312
K4       EQU   4                        CONSTANT OF 4            Y02080
K8       EQU   8                        CONSTANT OF 8            Y02132
K12      EQU   12                       CONSTANT OF 12           Y02080
RTNCODE8 EQU   8                        RETURN CODE OF 8         Y02082
K16      EQU   16                       CONSTANT OF 16           Y02132
K44      EQU   44                       CONSTANT OF 44         @G60ASBJ
NOLIST   EQU   12                       ERROR CODE FOR EMPTY    SA57261
*                                       VOL LIST                SA57261
VOLSER   EQU   4                        OFFSET TO THE VOLSER IN  Y02082
*                                       A VOLUME LIST ENTRY      Y02082
STATUS   EQU   11                       OFFSET TO STATUS BYTE    Y02082
ENTRYL   EQU   12                       VOLUME LIST ENTRY LENGTH Y02082
OPENDS   EQU   X'07'                    OPEN DATA SET ERROR      Y02082
RTNCODE4 EQU   4                        RETURN CODE OF 4         Y02132
NOF1     EQU   X'01'                    NO F1 DSCB ERROR CODE    Y02132
NODEVERR EQU   X'05'                    NO DEVICE ERROR CODE     Y02132
BLANK    EQU   C' '                     EBCDIC BLANK             YM2880
*
         BALR  RBASE,R0
         USING BEGIN,RBASE
         USING RENAMWKA,RWKAREA         WORK AREA ADDRESSABILITY Y02080
         USING UCB,RUCB
         USING CVT,RCVT
         USING RBBASIC,SVRBREG          SVRB ADDRESSABILITY    @G60ASBJ
*
* THIS SECTION OBTAINS THE RENAME WORK AREA.
*
BEGIN    EQU   *                                                 Y01077
         LPR   PL,R1                    LOAD PARAMETER LIST ADDR Y02080
         LR    RUCB,R0                  LOAD UCB ADDRESS         Y02080
         L     RCVT,CVTPTR              LOAD CVT ADDRESS         Y02080
         L     SVRBREG,0(R4)            LOAD SVRB ADDRESS        Y02080
         ST    RETURN,RBEXSAVE+44       SAVE REGISTER 14       @G60ASBJ
         IECRES GET,PREFIX=FIRST,LV=RWALNGTH,ID=REWA,            Y02080X
               STM=(R2,RBASE,RBEXSAVE)                         @G60ASBJ
         LR    RWKAREA,R1              LOAD BASE REG FOR WORK AREA
         L     RETURN,RBEXSAVE+44      RESTORE REGISTER 14     @G60ASBJ
         ST    RETURN,RENSAVE          SAVE R14 IN RENAME WORK @G60ASBJX
                                       AREA                    @G60ASBJ
         MODESET EXTKEY=ZERO           RETURN TO KEY ZERO AFTER  Y02082X
                                       ISSUING THE IECRES GET    Y02082
*
         IECRES LOAD,MODNM=LOAD1,EXTPR=(RWKAREA),BRANCH=NO       Y02080X
                                        LET OPTIONAL TRACE PRINT Y02080X
                                        THIS MODULE'S NAME       Y02080
*
         L     R1,IECRRPRM             RECOVERY RTN LIST ADDR    Y02144
         USING RRPLIST,R1              PARM LIST ADDRESSABILITY  Y02144
         OI    RRFUNCTN,RRFRENAM       SET RENAME FUNCTION BIT   Y02144
         DROP  R1                                                Y02144
*
         ST    R4,TCBADDR              SAVE INPUT TCB ADDRESS    Y02078
         ST    PL,OLDPLPTR             STORE PARAMETER LIST POINTER
         L     P1,K12(,PL)              LOAD ADDRESS OF VOL LIST YM5399
         LRA   P1,K0(,P1)               LOAD REAL ADDRESS OF     YM5399X
                                        VOLUME LIST              YM5399
         BC    5,EXIT                   BR IF INVALID (INVALID   YM5748
*                                       SEGMENT TABLE ENTRY OR   YM5748
*                                       INVALID SEGMENT OR PAGE  YM5399
*                                       TABLE LENGTH)            YM5399
         LM    P1,P4,0(PL)             LOAD PARAMETER LIST
*                                       P1 = RENAME OPTIONS
*                                       P2 = OLD DATA SET NAME ADDRESS
*                                       P3 = NEW DATA SET NAME ADDRESS
*                                       P4 = VOLUME LIST ADDRESS
         MVC   PDSNAME,K0(P2)          SAVE DSNAME IN PROTECTED  YM1337
*                                      WORK AREA                 YM1337
         LA    P2,PDSNAME              GET ADDR OF PROTECTED     YM1337
*                                      DSNAME                    YM1337
         ST    P2,DSMADTW3             SAVE ADDR OF DSNAME       Y02144
         OI    SISW1,NOSMFRCD          SET NO SMF RECORD SWITCH  Y02082
*                                      UNTIL AT LEAST ONE RENAME Y02082
*                                      IS SUCCESSFUL             Y02082
         MVC   VOLNUM,0(P4)            MOVE NUMBER OF VOLUMES    Y02080
         LH    VOLCTR,VOLNUM           LOAD NBR OF VOLS IN LIST  Y02080
         LTR   VOLCTR,VOLCTR           IS LIST EMPTY
         BNP   EXIT                    BRANCH IF YES             Y02082
*
* THIS SECTION CHECKS IF THE VOLUME LIST ADDRESS IS WITHIN THE USER'S
* REGION.  IF THE CALLER IS IN SUPERVISOR STATE OR IN KEY ZERO, THE
* VOLUME LIST ADDRESS DOES NOT HAVE TO BE CHECKED.
*
VALCHECK EQU   *                        VALIDITY CHECK           Y02078
         TESTAUTH FCTN=1,KEY=YES,STATE=YES,BRANCH=YES  TEST IF   Y02078X
                                        CALLER IS IN KEY 0 OR    Y02078X
                                        IN SUPERVISOR STATE      Y02078
         LTR   R15,R15                  TEST IF ANY TRUE         Y02078
         BZ    BYPASS                   BRANCH IF YES            Y02078
*
* THIS SECTION CALCULATES THE STARTING AND ENDING ADDRESSES OF
* THE AREA TO BE CHECKED.
*
         MH    VOLCTR,TWELVE            12 TIMES THE NBR OF VOLS Y02078
         LA    VOLCTR,K1(VOLCTR)        ADD ONE                  Y02078
         LA    R1,0(P4)                 STARTING ADDRESS         Y02078
         LR    R2,R1                                             Y02078
         AR    R2,VOLCTR                ADDRESS OF THE LAST BYTE Y02078
*                                       OF THE VOLUME LIST       Y02078
         STM   RUCB,P1,IECREGSV+K12     SAVE REGISTERS 11-3      Y02078
         L     R4,TCBADDR               TCB ADDRESS IN REG 4     Y02078
         LR    R7,RWKAREA               SAVE WORK AREA ADDRESS   Y02078
         XR    R5,R5                    INITIALIZE REGISTER      YM1241
OBTNLOCK SETLOCK OBTAIN,TYPE=LOCAL,MODE=UNCOND,                  Y02078X
               RELATED=(LOCAL,IGC00030(RLSELOCK))  OBTAIN LOCK   Y02078
         LM    RUCB,RETURN,IECREGSV+K12-RENAMWKA(R7) RSTORE REGS YM1241
         L     R15,CVT0VL00             VALIDITY CHECK RTN ADDR  Y02078
         BALR  RETURN,R15               LINK TO VALIDITY CHECK   Y02078
         BZ    RLSELOCK                 BRANCH IF VALID ADDRESS  YM1312
         LA    R5,NONZERO               INDICATE INVALID ADDRESS YM1312
*
RLSELOCK SETLOCK RELEASE,TYPE=LOCAL,                             Y02078X
               RELATED=(LOCAL,IGC00030(OBTNLOCK))  RELEASE LOCK  Y02078
         LM    RUCB,P1,IECREGSV+K12-RENAMWKA(R7)  RESTORE REGS   Y02078
         LTR   R5,R5                    TEST IF ADDRESS VALID    Y02078
         BNZ   EXIT                     BRANCH IF NOT VALID      Y02078
*
* THIS SECTION ATTEMPTS TO ENQ ON THE TIOT.  IF THE ENQ FAILS, THEN
* THE RENAME CANNOT PROCEED, SINCE THE CURRENT TASK ALREADY HAD
* CONTROL OF THE RESOURCE.
*
BYPASS   EQU   *                        ENQ ON TIOT             Y02082
         LH    VOLCTR,VOLNUM            RELOAD NUMBER OF VOLUMES Y02082
         L     RDSAB,TCBADDR            LOAD TCB ADDRESS         Y02082
         USING TCB,RDSAB                TCB ADDRESSABILITY       Y02082
         L     RDSAB,TCBJSCB            GET JSCB ADDRESS         Y02082
         USING IEZJSCB,RDSAB            JSCB ADDRESSABILITY      Y02082
         L     RDSAB,JSCBACT            GET ACTIVE JSCB ADDRESS  Y02082
         L     RDSAB,JSCDSABQ           GET DSAB QDB ADDRESS     Y02082
         ST    RDSAB,DSABQDB            SAVE FOR TIOT ENQ        Y02082
         L     R2,CVTTCBP               LOAD TCB/ASID POINTERS   YM1063
         L     R2,K12(,R2)              LOAD CURRENT ASID ADDR   YM1063
         USING ASCB,R2                  ASCB ADDRESSABILITY      YM1063
         MVC   TIOTMNR(K2),ASCBASID     MOVE ASID INTO ENQ MINOR YM1063
*                                       RESOURCE NAME            YM1063
         DROP  R2                                                YM1063
         MVC   MJNAME(L'TIOTMJR),TIOTMJR  MAJOR RESOURCE NAME    Y02082
         MVC   ENQAREA(ENQLTH1),ENQLIST1  MOVE IN PARAMETER LIST YM2895
         ENQ   (MJNAME,TIOTMNR),MF=(E,ENQAREA)  ENQ ON SYSZTIOT  Y02082
         LTR   R15,R15                  TEST IF SUCCESSFUL ENQ   Y02082
         BZ    SETENQSW                 BRANCH IF YES            Y02082
*
         LA    VOLISTX,K2(P4)           ADDRESS OF FIRST VOLUME  Y02082
OPENERR  EQU   *                        OPEN DATA SET ERROR      Y02082
         LA    R3,OPENDS                OPEN DATA SET ERROR CODE Y02132
         B     SETERRCD                 GO SET THE ERROR CODE    Y02132
NOF1DSCB EQU   *                        NO F1 DSCB ERROR         Y02132
         LA    R3,NOF1                  NO F1 DSCB ERROR CODE    Y02132
SETERRCD EQU   *                        RETURN CODE              Y02132
         LA    RERRSAVE,RTNCODE8        RETURN CODE OF 8         Y02132
SETSTATS EQU   *                        SET ERROR CODE IN VOLIST Y02132
         L     R1,TCBADDR               LOAD TCB ADDRESS         Y02078
         USING TCB,R1                   TCB ADDRESSABILITY       Y02078
         MODESET EXTKEY=RBT234,WORKREG=2  SWITCH TO CALLER'S KEY Y02078
ERRLOOP  EQU   *                        MORE VOLUMES             Y02082
         STC   R3,STATUS(VOLISTX)       SET ERROR CODE IN VOLIST Y02132
         LA    VOLISTX,ENTRYL(VOLISTX)  NEXT VOL LIST ENTRY ADDR Y02082
         BCT   VOLCTR,ERRLOOP           BRANCH IF MORE VOLUMES   Y02082
         MODESET EXTKEY=ZERO            RETURN TO KEY ZERO       Y02082
         B     DEQTEST                  GO PREPARE TO EXIT       Y02082
*
SETENQSW EQU   *                        SET TIOT ENQ'ED SWITCH   Y02082
         LA    R1,TIOTMNR               MINOR RESOURCE NAME ADDR Y02144
         ST    R1,DSMADTW2              SAVE MINOR NAME ADDR     Y02144
         OI    SISW1,TIOTENQ            SET TIOT ENQ'ED SWITCH   Y02082
         OI    DSMADTB2,DSMTIOTE        SET TIOT ENQ'ED SWITCH   Y02144
*
         LA    VOLISTX,K2(P4)           ADDRESS OF FIRST VOLUME  Y02082
         LTR   RUCBSAVE,RUCB            TEST FOR PASSED UCB ADDR Y02082
         BZ    FINDUNIT                 BRANCH IF ZERO           Y02082
         LA    RUCBSAVE,K0(RUCBSAVE)    CLEAR HIGH BYTE          Y02082
         TM    UCBJBNR,UCBVRDEV         TEST IF A VIRTUAL UCB    Y02132
         BZ    USERSUCB                 BRANCH IF NOT            Y02132
         ST    RUCB,PRUCBPTR            SAVE INPUT UCB ADDRESS   Y02132
         B     VIODSTST                 BRANCH TO VIO D/S TEST   Y02132
*
* IF THE USER SUPPLIED A UCB ADDRESS, THIS SECTION SEARCHES THE
* TIOT TO DETERMINE IF THE UNIT IS ALLOCATED TO THE JOB.  IF IT IS
* ALLOCATED, IT BECOMES THE PRIMARY UCB IF IT IS DEMOUNTABLE.
*
USERSUCB EQU   *                        USER SUPPLIED A UCB ADDR Y02082
         L     RDSAB,DSABQDB            LOAD DSAB QDB ADDRESS    Y02082
         USING QDB,RDSAB                QDB ADDRESSABILITY       Y02082
         L     RDSAB,QDBFELMP           GET FIRST DSAB ADDRESS   Y02082
         USING DSAB,RDSAB               DSAB ADDRESSABILITY      Y02082
UDSABTST EQU   *                        DSAB TEST                Y02082
         LTR   RDSAB,RDSAB              IS THERE ANOTHER DSAB    Y02082
         BZ    FINDUNIT                 BRANCH IF NOT            Y02082
         L     RTIOT,DSABTIOT           LOAD TIOT ADDRESS        Y02082
         USING TIOENTRY,RTIOT           TIOT ADDRESSABILITY      Y02082
         SR    REND,REND                ZERO REGISTER FOR IC     Y02082
         IC    REND,TIOELNGH            LENGTH OF TIOT ENTRY     Y02082
         AR    REND,RTIOT               ADDRESS OF FIRT BYTE     Y02082
*                                       PAST THE TIOT ENTRY      Y02082
         LA    RTIOT,TIOEFSRT-K1        ADDR OF FIRST UCB ADDR   Y02082
UNEXTUCB EQU   *                        END OF ENTRY             Y02082
         CR    RTIOT,REND               TEST IF AT END OF ENTRY  Y02082
         BNL   UNXTDSAB                 BRANCH IF YES            Y02082
         L     RUCB,K0(,RTIOT)          GET UCB ADDRESS          Y02082
         LA    RUCB,K0(RUCB)            CLEAR HIGH BYTE          Y02082
         LTR   RUCB,RUCB                IS THERE A UCB ADDRESS   Y02082
         BZ    UINCRPTR                 BRANCH IF NOT            YM1063
         CR    RUCB,RUCBSAVE            TEST IF EQUAL TO THE     Y02082
*                                       USER-SUPPLIED UCB ADDR   Y02082
         BE    LINKTEST                 BRANCH IF YES            Y02082
UINCRPTR EQU   *                                                 YM1063
         LA    RTIOT,L'TIOESTTB+L'TIOEFSRT(RTIOT)   INCREMENT TO Y02082
*                                       NEXT UCB ADDRESS IN TIOT Y02082
         B     UNEXTUCB                 GO TRY NEXT UCB ADDRESS  Y02082
*
UNXTDSAB EQU   *                        GET NEXT DSAB            Y02082
         L     RDSAB,DSABFCHN           GET NEXT DSAB ADDRESS    Y02082
         B     UDSABTST                 GO TEST IT               Y02082
*
* THIS SECTION LINKS TO A ROUTINE THAT DETERMINES IF THE UNIT IS
* ELIGIBLE FOR DEMOUNTING.
*
LINKTEST BAL   RETURN,TESTUCB           TEST FOR PRIMARY UCB
         LTR   RUCB,RUCB               TEST FOR PRIMARY UCB
         BZ    FINDUNIT                BR IF NONE
         ST    RDSAB,PRDSABAD           SAVE PRIMARY DSAB ADDR   Y02082
         ST    RUCB,PRUCBPTR            SAVE PRIMARY UCB ADDRESS Y02082
         L     RETURN,DSABTIOT          LOAD TIOT ENTRY ADDRESS  YM7036
         LA    RETURN,TIOEJFCB-TIOENTRY(,RETURN)  POINT TO JFCB  YM7036X
                                        PREFIX ADDRESS           YM7036
         IECRES LOCJFCB,(RETURN)        LOCATE THE JFCB          YM7036
         CLC   PDSNAME,JFCBDSNM-INFMJFCB(RETURN)  CHECK IF       YM7036X
                                        THIS IS THE RIGHT TIOT   YM7036X
                                        ENTRY FOR THIS DSNAME    YM7036
         BE    FOUNDUCB                 BRANCH IF CORRECT        YM7036
         OI    SISW2,UCBFNDSW           SET THE UCB FOUND SWITCH YM7036
         B     UINCRPTR                 GO TRY NEXT TIOT ENTRY   YM7036
*
* CHECK THE UCB FOR PRIMARY QUALITIES.
*
TESTUCB  EQU   *                                                  25343
         TM    UCBTBYT3,UCB3DACC        TEST IF DIRECT ACCESS    Y02082
         BZ    WRONGUCB                 BRANCH IF NOT            Y02082
         TM    UCBDMCT,UCBDMC           TEST IF DATAMGT COUNT=0  Y02082
         BNZ   WRONGUCB                 BRANCH IF NOT            Y02082
         TM    SRTESTAT,SRTEONLI        TEST STATUS A FOR ON LINE
         BZ    WRONGUCB                 BRANCH IF NOT
         TM    SRTESTAT,SRTERESV+SRTEPRES+SRTESYSR  TEST FORRESERVED,
*                                       PERMANENT OR SYSTEM RESIDENT
         BNE   WRONGUCB                 BRANCH IF YES
         TM    SRTESTAB,SRTEBSVL        TEST FOR SHARED VOLUME
         BZ    WRONGUCB                 BR IF SHARED
         BR    RETURN                   RETURN                   Y02080
WRONGUCB SR    RUCB,RUCB               DISCARD THIS UCB
         BR    RETURN                   RETURN                   Y02080
*
* THIS SECTION INITIALIZES REGISTERS BEFORE BRANCHING TO IGG03002.
*
NOPRIUCB EQU   *                        INITIALIZE REGS          Y02082
         OI    RTYPEFLG,NOMNTDEV        TURN ON NO DEVICE BIT    Y02082
FOUNDUCB LA    VOLISTX,2(P4)           LOAD ADDR OF FIRST VOLUME
         LH    VOLCTR,VOLNUM           LOAD NBR OF VOLS IN LIST  Y02080
         ST    VOLISTX,VOLPTR          STORE VOLUME LIST INDEX
         NI    SISW2,X'FF'-UCBFNDSW     TURN OFF UCB FOUND SWTCH YM7036
*
* THIS SECTION ISSUES AN EXCLUSIVE ENQ DIRECTED TO THE INITIATOR'S
* TCB WITH A MAJOR RESOURCE NAME OF SYSDSN AND THE DATA SET NAME AS
* THE MINOR RESOURCE NAME.  IF THE RETURN CODE INDICATES THE TASK
* PREVIOUSLY HAD CONTROL OF THE RESOURCE, THE ENQ IS REISSUED AND
* A BIT IS SET TO INDICATE A DEB SEARCH MUST BE DONE TO ENSURE NO
* ONE IS CURRENTLY OPEN TO THE DATA SET.
*
DSNAMENQ EQU   *                        ENQ DSNAME               Y02082
         MVC   ENQAREA(ENQLTH2),ENQLIST2  MOVE IN ENQ PARM LIST  Y02082
         MVC   MJNAME(L'DSNMJR),DSNMJR  MAJOR RESOURCE NAME      Y02082
         LA    P2,PDSNAME+L'PDSNAME-K1  LAST BYTE OF DSNAME      YM2880
TESTDSN  EQU   *                        SEARCH FOR NON-BLANK     YM2880
         CLI   K0(P2),BLANK             IS THIS ONE A BLANK      YM2880
         BNE   GETLNGTH                 BRANCH IF NO TO COMPUTE  YM2880X
                                        LENGTH OF NAME FOR ENQ   YM2880
         BCT   P2,TESTDSN               TRY NEXT BYTE            YM2880
GETLNGTH EQU   *                        CALCULATE LENGTH         YM2880
         LA    R2,PDSNAME-K1            BYTE PRECEDING DSNAME    YM2880
         SR    P2,R2                    P2 HAS LENGTH FOR ENQ    YM2880
         L     R2,TCBADDR               LOAD TCB ADDRESS         Y02082
         USING TCB,R2                   TCB ADDRESSABILITY       Y02082
         L     R2,TCBJSCB               GET JSCB ADDRESS         Y02082
         USING IEZJSCB,R2               JSCB ADDRESSABILITY      Y02082
         L     R2,JSCBACT               GET ACTIVE JSCB ADDRESS  YM2887
         L     R2,JSCBTCBP              INITIATOR TCB ADDRESS    Y02082
         ENQ   (MJNAME,PDSNAME,,(P2)),TCB=(R2),RET=USE,          YM2880X
               MF=(E,ENQAREA)           ENQ ON SYSDSN            YM2880
         LTR   R15,R15                  TEST RETURN CODE         Y02082
         BNZ   ERRTEST                  BRANCH IF ENQ FAILED     Y02082
*
         OI    DSMADTB2,DSMDSNE         SET DSN ENQ'ED SWITCH    Y02144
         OI    SISW1,DSNENQ             SET ENQ ON SYSDSN SWITCH Y02082
         B     LOADEXIT                 GO TO NEXT LOAD          Y02082
ERRTEST  EQU   *                        ERROR TEST               Y02082
         CLI   ENQAREA+K3,RTNCODE8      TEST IF TASK ALREADY     Y02082
*                                       ENQ'ED ON SYSDSN         Y02082
         BNE   OPENERR                  BRANCH IF NOT            Y02082
         MVC   ENQAREA(ENQLTH3),ENQLIST3  MOVE IN ENQ PARM LIST  Y02082
         ENQ   (MJNAME,PDSNAME,,(P2)),TCB=(R2),RET=CHNG,         YM2880X
               MF=(E,ENQAREA)           ENQ ON SYSDSN            YM2880
         LTR   R15,R15                  TEST RETURN CODE         Y02082
         BNZ   OPENERR                  BRANCH IF NOT SUCCESSFUL Y02082
         OI    SISW1,DEBSRCH            DEB SEARCH REQUIRED SW   Y02082
LOADEXIT EQU   *                        EXIT TO NEXT LOAD        Y02082
         IECRES LOAD,EXTPR=(RWKAREA),MODNM=LOAD2,BRANCH=DIRECT   Y02080
*
* THIS SECTION SEARCHES THE TIOT TO SEE IF ANY VOLUME IN THE VOLUME
* LIST IS MOUNTED ON A UNIT ALLOCATED TO THE JOB.  IF MOUNTED, IT
* LINKS TO A ROUTINE THAT DETERMINES IF THE UNIT IS DEMOUNTABLE.
*
FINDUNIT EQU   *                        FIND A UNIT              Y02082
         TM    SISW2,UCBFNDSW           TEST IF A UCB WAS FOUND  YM7036
         BO    FOUNDUCB                 BRANCH IF YES            YM7036
         OC    VOLSER(L'SRTEVOLI,VOLISTX),VOLSER(VOLISTX)  TEST  Y02082
*                                       FOR VALID VOLUME SERIAL  Y02082
         BZ    NOUNIT                   BRANCH IF ZERO           Y02082
         CLC   VOLSER(L'SRTEVOLI,VOLISTX),BLANKS  TEST FOR VALID Y02132
*                                       VIRTUAL UCB VOL SER      Y02132
         BNE   NOVIOVOL                 BRANCH IF NOT            Y02132
         CLC   CSYS,PDSNAME             TEST FOR CHARACTERS SYS  YM1337
*                                       IN BYTES 0-2 OF DSNAME   Y02132
         BNE   NOVIOVOL                 BRANCH IF NOT A VIO DSN  Y02132
         CLC   CDOTT,PDSNAME+K8         TEST FOR .T IN BYTES 8-9 YM1337
         BNE   NOVIOVOL                 BRANCH IF NOT            Y02132
         CLC   CDOTR,PDSNAME+K16        TEST FOR CHARACTERS .R   YM1337
*                                       IN BYTES 16-17           Y02132
         BE    VIODSTST                 BRANCH IF A POSSIBLE     Y02132
*                                       VIO DATA SET             Y02132
*
NOVIOVOL EQU   *                        NO VIO VOL               Y02132
         L     RDSAB,DSABQDB            LOAD DSAB QDB ADDRESS    Y02082
         USING QDB,RDSAB                QDB ADDRESSABILITY       Y02082
         L     RDSAB,QDBFELMP           GET FIRST DSAB ADDRESS   Y02082
         USING DSAB,RDSAB               DSAB ADDRESSABILITY      Y02082
DSABTEST EQU   *                        DSAB TEST                Y02082
         LTR   RDSAB,RDSAB              IS THERE ANOTHER DSAB    Y02082
         BZ    NOUNIT                   BRANCH IF NOT            Y02082
         L     RTIOT,DSABTIOT           GET TIOT POINTER         Y02082
         USING TIOENTRY,RTIOT           TIOT ADDRESSABILITY      Y02082
         SR    REND,REND                ZERO REGISTER FOR IC     Y02082
         IC    REND,TIOELNGH            LENGTH OF TIOT ENTRY     Y02082
         AR    REND,RTIOT               ADDRESS OF FIRST BYTE    Y02082
*                                       PAST THE TIOT ENTRY      Y02082
         LA    RTIOT,TIOEFSRT-K1        ADDR OF FIRST UCB ADDR   Y02082
NEXTUCB  EQU   *                        END OF ENTRY             Y02082
         CR    RTIOT,REND               TEST IF AT END OF ENTRY  Y02082
         BNL   NEXTDSAB                 BRANCH IF YES            Y02082
         L     RUCB,K0(,RTIOT)          GET UCB ADDRESS          Y02082
         LA    RUCB,K0(RUCB)            CLEAR HIGH BYTE          Y02082
         LTR   RUCB,RUCB                IS THERE A UCB ADDRESS   Y02082
         BZ    NXTENTRY                 BRANCH IF NOT            YM1063
         CLC   SRTEVOLI,VOLSER(VOLISTX)  COMPARE VOL SER NUMBERS Y02082
         BNE   NXTENTRY                 BRANCH IF NOT EQUAL      Y02082
*
         BAL   RETURN,TESTUCB           TEST IF UCB DEMOUNTABLE  Y02082
*
         LTR   RUCB,RUCB                TEST IF DEMOUNTABLE      Y02082
         BZ    NXTENTRY                 BRANCH IF NOT            YM7036
         ST    RDSAB,PRDSABAD           SAVE PRIMARY DSAB ADDR   YM7036
         ST    RUCB,PRUCBPTR            SAVE PRIMARY UCB ADDRESS YM7036
         L     RETURN,DSABTIOT          LOAD TIOT ENTRY ADDRESS  YM7036
         LA    RETURN,TIOEJFCB-TIOENTRY(,RETURN)  POINT TO JFCB  YM7036X
                                        PREFIX ADDRESS           YM7036
         IECRES LOCJFCB,(RETURN)        LOCATE THE JFCB          YM7036
         CLC   PDSNAME,JFCBDSNM-INFMJFCB(RETURN)  CHECK IF       YM7036X
                                        THIS IS THE RIGHT TIOT   YM7036X
                                        ENTRY FOR THIS DSNAME    YM7036
         BE    FOUNDUCB                 BRANCH IF CORRECT        YM7036
         OI    SISW2,UCBFNDSW           SET THE UCB FOUND SWITCH YM7036
NXTENTRY EQU   *                        NEXT ENTRY               Y02082
         LA    RTIOT,L'TIOESTTB+L'TIOEFSRT(,RTIOT)  INCREMENT TO Y02082
*                                       NEXT UCB ADDRESS IN TIOT Y02082
         B     NEXTUCB                  GO TEST NEXT UCB ADDRESS Y02082
*
NEXTDSAB EQU   *                        GET NEXT DSAB            Y02082
         L     RDSAB,DSABFCHN           GET NEXT DSAB ADDRESS    Y02082
         B     DSABTEST                 GO TEST IT               Y02082
NOUNIT   EQU   *                        MORE VOLUMES             Y02082
         BCT   VOLCTR,TRYNEXT           TEST IF MORE VOLUMES     Y02082
         TM    SISW2,UCBFNDSW           TEST IF A UCB WAS FOUND  YM7036
         BO    FOUNDUCB                 BRANCH IF YES            YM7036
         B     NOPRIUCB                 BRANCH IF NOT            Y02082
TRYNEXT  EQU   *                        NEXT ENTRY               Y02082
         LA    VOLISTX,ENTRYL(VOLISTX)  POINT TO NEXT ENTRY      Y02082
*                                       IN THE VOLUME LIST       Y02082
         B     NOVIOVOL                 GO TRY NEXT VOLUME       YM7036
*
* THIS SECTION SEARCHES THE DSAB CHAIN FOR A TIOT ENTRY CONTAINING
* A VIRTUAL UCB WHOSE VDSDSCB CONTAINS THE DATA SET NAME TO BE
* RENAMED AND WHOSE VOLUME SERIAL NUMBER IN THE VDSUCB MATCHES
* THE FIRST VOLUME SERIAL IN THE VOLUME LIST.  IF A DSNAME/VOL SER
* MATCH IS FOUND, NO RENAME FUNCTION IS PERFORMED.
*
VIODSTST EQU   *                        VIO DATA SET TEST        Y02132
         L     RDSAB,DSABQDB            GET DSAB QDB ADDRESS     Y02132
         USING QDB,RDSAB                QDB ADDRESSABILITY       Y02132
         L     RDSAB,QDBFELMP           GET FIRST DSAB ADDRESS   Y02132
         USING DSAB,RDSAB               DSAB ADDRESSABILITY      Y02132
VDSABTST EQU   *                        DSAB TEST                Y02132
         LTR   RDSAB,RDSAB              IS THERE ANOTHER DSAB    Y02132
         BZ    NOMATCH                  BRANCH IF NOT            Y02132
         L     RTIOT,DSABTIOT           GET TIOT ADDRESS         Y02132
         USING TIOENTRY,RTIOT           TIOT ADDRESSABILITY      Y02132
         L     RUCB,TIOEFSRT-K1         GET UCB ADDRESS          Y02132
         LA    RUCB,K0(RUCB)            CLEAR HIGH BYTE          Y02132
         LTR   RUCB,RUCB                IS THERE A UCB ADDRESS   Y02132
         BZ    VNXTDSAB                 IF NO, TRY NEXT DSAB     Y02132
         TM    UCBJBNR,UCBVRDEV         TEST IF A VIRTUAL UCB    Y02132
         BZ    VNXTDSAB                 BRANCH IF NOT            Y02132
         CLC   SRTEVOLI,VOLSER(VOLISTX)  COMPARE VOL SER'S       Y02132
         BNZ   VNXTDSAB                 BRANCH IF NOT EQUAL      Y02132
         LR    R7,RUCB                  VDSCB ADDRESS            Y02132
         USING VDSCB,R7                 VDSCB ADDRESSABILITY     Y02132
         CLC   DS1DSNAM,PDSNAME         COMPARE DSNAME'S         YM1337
         BNE   VNXTDSAB                 BRANCH IF NOT EQUAL      Y02132
VIORENAM EQU   *                        VIO DSNAME FOUND         Y02132
         LA    R3,NODEVERR              RENAME IS NOT SUPPORTED  Y02132
*                                       FOR A VIRTUAL DATA SET   Y02132
         LA    RERRSAVE,RTNCODE4        RETURN CODE OF 4         Y02132
         B     SETSTATS                 GO SET THE STATUS BYTE   Y02132
VNXTDSAB EQU   *                        GET NEXT DSAB            Y02132
         L     RDSAB,DSABFCHN           GET NEXT DSAB ADDRESS    Y02132
         B     VDSABTST                 GO TEST IT               Y02132
NOMATCH  EQU   *                        TEST FOR INPUT UCB       Y02132
         OC    PRUCBPTR,PRUCBPTR        TEST FOR INPUT UCB ADDR  Y02132
         BZ    NOF1DSCB                 BRANCH IF NONE           Y02132
         XC    PRUCBPTR,PRUCBPTR        CLEAR INVALID UCB ADDR   Y02132
         B     NOVIOVOL                 BRANCH TO NON-VIO LOGIC  Y02132
*
* IF RENAME ENQ'ED ON THE TIOT, THIS SECTION ISSUES A DEQ.
*
DEQTEST  EQU   *                        DEQ TIOT                 Y02082
         TM    SISW1,TIOTENQ            TEST IF ENQ'ED ON TIOT   Y02082
         BNO   FREEWKA                  BRANCH IF NOT            Y02082
         MVC   ENQAREA(DEQLTH1),DEQLIST1  MOVE IN DEQ PARM LIST  Y02082
         MVC   MJNAME(L'TIOTMJR),TIOTMJR  MAJOR RESOURCE NAME    Y02082
         DEQ   (MJNAME,TIOTMNR),MF=(E,ENQAREA)  DEQ FROM TIOT    Y02082
*
         NI    DSMADTB2,X'FF'-DSMTIOTE  RESET TIOT ENQ'ED SWITCH Y02144
         B     FREEWKA                  GO FREE THE WORK AREA    Y02082
*
* THIS SECTION FREES THE WORK AREA AND RETURNS TO THE CALLER.
*
EXIT     EQU   *                        RETURN TO THE CALLER     Y02080
         LA    RERRSAVE,NOLIST          LOAD ERROR CODE          Y02080
FREEWKA  EQU   *                        FREE THE WORK AREA       Y02082
         L     SVRBREG,RENSAVE          SAVE REG 14 IN REG 8   @G60ASBJ
         IECRES FREE,PREFIX=FIRST,A=(RWKAREA)  FREE WORK AREA    Y02080
         LR    RETURN,SVRBREG           RESTORE R14            @G60ASBJ
         LR    R15,RERRSAVE             RESTORE ERROR CODE REG   Y02080
         RETURN ,                       RETURN                   Y02080
*
*** PROGRAM CONSTANTS
*
CSYS     DC    CL3'SYS'                 CONSTANT OF 'SYS'        Y02132
CDOTT    DC    CL2'.T'                  CONSTANT OF '.T'         Y02132
CDOTR    DC    CL2'.R'                  CONSTANT OF '.R'         Y02132
BLANKS   DC    CL6' '                   SIX EBCDIC BLANKS        Y02132
TIOTMJR  DC    CL8'SYSZTIOT'            TIOT MAJOR RESOURCE NAME Y02082
DSNMJR   DC    CL8'SYSDSN'              DSN MAJOR RESOURCE NAME  Y02082
ENQLIST1 ENQ   (,,E,6,SYSTEM),RET=HAVE,MF=L   TIOT ENQ PARM LIST Y02082
ENQLTH1  EQU   *-ENQLIST1               LENGTH OF ENQ PARM LIST  Y02082
ENQLIST2 ENQ   (,,E,,SYSTEM),RET=USE,MF=L  1ST DSN ENQ LIST      YM2880
ENQLTH2  EQU   *-ENQLIST2               LENGTH OF ENQ PARM LIST  Y02082
ENQLIST3 ENQ   (,,E,,SYSTEM),RET=CHNG,MF=L  2ND DSN ENQ LIST     YM2880
ENQLTH3  EQU   *-ENQLIST3               LENGTH OF ENQ PARM LIST  Y02082
TWELVE   DC    H'12'                    VOL LIST ENTRY LENGTH    Y02078
DEQLIST1 DEQ   (,,6,SYSTEM),MF=L        SYSZTIOT DEQ LIST        YM2887
DEQLTH1  EQU   *-DEQLIST1               LENGTH OF DEQ LIST       Y02082
*
* TABLE OF MODULE NAMES AND ENTRY POINT ADDRESSES
*
         XCTLTABL ID=(LOAD1,IGC00030,LOAD2,IGG03002),            Y02080X
               SVC=030,LENGTH=,BRT=YES                           Y02080
         SPACE 2                                                 Y02078
         IECDSECS ASCB,CVT,DSAB,JSCB,PSA,QDB,RB,RRPL,TIOT,TCB,   YM1063X
               UCB,EXPAND=YES                                    YM1063
*
* *
* * * RENAME WORK AREA
* *
*
WORKAREA IECRENWA EP,ADT=YES            RENAME WORK AREA         Y02144
         EJECT                                                   Y02132
         IDDVDSCB                       VDSCB EXPANSION          Y02132
         EJECT                                                   Y02132
         ORG   VDSDSCB                                           Y02132
         IECSDSL1 (1)                   FORMAT 1 DSCB EXPANSION  Y02132
         EJECT                                                   YM7036
         IEFJFCBN                       JFCB DSECT               YM7036
         END
