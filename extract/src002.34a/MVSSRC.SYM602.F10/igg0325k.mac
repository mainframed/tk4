 TITLE 'IGG0325K - DADSM USER LABEL SPACE'
IGG0325K CSECT
*
*MODULE NAME = IGG0325K
*
*DESCRIPTIVE NAME = DADSM USER LABEL SPACE
*
*COPYRIGHT = NONE
*
*CHANGE ACTIVITY = SEE BELOW
*
*          RELEASE 18 DELETIONS
*          RELEASE 19 DELETIONS
*          RELEASE 20 DELETIONS
*          RELEASE 21 DELETIONS
*1196026000-044000,048000,056000,070000,102000,124000,200000-    A42477
*1196206000,212000-224000,234000,238000,266000-276000,298000-    A42477
*1196316000,342000,362000,386000,414000,428000,442000,482000,    A42477
*1196488000,496000,518000,522000-524000,562000-564000,570000,    A42477
*1196584000,592000,628000,808000                                 A42477
*0000060000,236160-236560,570000-592000,806000                   A46776
*          RELEASE 22 DELETIONS
*          VS2 RELEASE 02 DELETIONS/CHANGES
*0000000300-001500,016000-022000,027000-032000,037000,066000,    Y02080
*0000080000,086000-088200,092000,108000,122000-125000,203000,    Y02080
*0000230000,233000-233500,237040-237200,237360-238800,252000,    Y02080
*0000256000,262000,302000-305000,485000,501000-504000,508000-    Y02080
*0000510000,514000,518000,566000,568500-624000,626000-646000,    Y02080
*0000650000-660000,664000-790000                                 Y02080
*
* STATUS CHANGE LEVEL 003
*                                                                     *
*FUNCTION/OPERATION: THIS MODULE PROCESSES USER LABEL REQUESTS FOR    *
*   ALL ALLOCATION REQUESTS EXCEPT ABSOLUTE TRACK.  IT CONVERTS THE   *
*   FORMAT 5 DSCB IN CORE FROM XXYYZ TO RTA/NUMBER OF TRACKS FORMAT.  *
*   A ONE TRACK RTA/RTA+1 WILL BE TAKEN FROM THE SMALLEST EXTENT IN   *
*   THE FIRST FORMAT 5.                                               *
*                                                                     *
*ENTRY POINTS:                                                        *
*   IGG0325K - ENTRY IS MADE FROM IGG0325B VIA A BRANCH.              *
*                                                                     *
*INPUT: AT ENTRY TO THIS MODULE, THE WORK AREA CONTAINS THE DATA      *
*   PORTION OF THE FORMAT 4 DSCB AND ITS ADDRESS, THE FIRST DADSM     *
*   RECORD (F5) AND ITS ADDRESS, A DEB, AN IOB, AN ECB, AND A CHANNEL *
*   PROGRAM TO READ DADSM RECORDS.                                    *
*   THE 'FIRST FORMAT 5 DSCB IN CORE' SWITCH WILL BE ON.              *
*   REGISTER 10 WILL CONTAIN THE SPACE REQUEST IN TRACKS.             *
*   REGISTER 11 WILL POINT TO A JFCB OR PARTIAL DSCB.                 *
*   REGISTER 13 WILL POINT TO THE WORK AREA.                          *
*                                                                     *
*OUTPUT- UPON TRANSFER OF CONTROL, THE WORK AREA AND REGISTERS WILL   *
*   BE AS AT INPUT WITH THE FOLLOWING ADDITIONS.                      *
*   THE FIRST FORMAT 5 HAS BEEN UPDATED BY TAKING OUT THE TRACK(S)    *
*   NEEDED FOR THE USER LABEL(S).                                     *
*   A USER LABEL EXTENT IS SAVED IN THE WORK AREA.
*
*                                                                     *
*EXTERNAL ROUTINES-                                                   *
*        IECRES - BRANCH TO ANOTHER MODULE                            *
*                                                                     *
*
*EXITS- NORMAL- BRANCH TO IGG0325D TO OBTAIN PRIMARY QUANTITY         *
*               BRANCH TO IGG0325E IF A ZERO QUANTITY REQUEST         *
*       ERROR - BRANCH TO IGG0325H WITH A CODE IN THE ERROR CODE      *
*               PASS REGISTER                                         *
*                                                                     *
*       ERROR CODES THAT CAN BE ISSUED BY THE 'USER LABEL' ROUTINE:   *
*                                                                     *
*       14 - QUANTITY NOT AVAILABLE                                   *
*
*TABLES/WORK AREAS- DADSM WORK AREA DESCRIBED BY MACRO 'IECALLWA'     *
*                                                                     *
*              USER LABEL EXTENT AS FOLLOWS-
*              RTA = THE STARTING TRACK OF THE ALLOCATED EXTENT.      *
*                                                                     *
*              RTA+1 = THE STARTING TRACK OF THE NEXT AVAILABLE EXTENT*
*                                                                     *
*                                                                     *
*ATTRIBUTES- REENTRANT                                                *
*                                                                     *
*NOTES-                                                               *
*   OTHER MACROS USED                                                 *
*   IECALLWA - ALLOCATE WORK AREA EXPANSION                           *
*   IECSDSL1 - BUILD DSCB'S                                           *
*   IEFJFCBN - BUILD JFCB                                             *
*   CVT - BUILD CVT                                                   *
*                                                                     *
*STORAGE: PROGRAM CODE CSECT - LESS THAN 1024 BYTES                   *
*   WORK AREA - AS DEFINED IN THE IECALLWA MACRO                      *
*   RPS WORK AREA - AS DEFINED IN THE IECRPS MACRO                    *
*                                                                     *
*
*REGISTER USAGE-
RCNT     EQU   1                        LOOP COUNTER
ROUT     EQU   2                        OUTPUT POINTER
RIN      EQU   3                        DADSM POINTER
RTA1     EQU   4                        CONVERSION ROUTINE RTA+1 REG
R5       EQU   5
RTA      EQU   5                       CONVERSION ROUTINE RTA REG
R6       EQU   6
RHH      EQU   6                        CONVERSION ROUTINE TRK REG
R7       EQU   7
RCC      EQU   7                        CONVERSION ROUTINE CYL REG
RTTRPTR  EQU   7                        PTR TO XCTL BY TTR TABLE ENTRY
R8       EQU   8
RTRK     EQU   8                        CONTAINS NBR OF TRKS/CYL
RERRPASS EQU   8                        ERROR PASS REGISTER
R9       EQU   9
RJFCBA   EQU   9                        ALTERNATE JFCB POINTER   A42477
RA       EQU   10
REQ      EQU   10                 PRIMARY QUANTITY PASSED FROM CTR
RJFCB    EQU   11                       JFCB POINTER
RBASE    EQU   12                       BASE REGISTER
RWKA     EQU   13                       WORK AREA POINTER
RBAK     EQU   14                 RETURN REGISTER
RF       EQU   15
*
* OTHER EQUATES
*
ZERO     EQU   0
ONE      EQU   1
TWO      EQU   2
THREE    EQU   3
FOUR     EQU   4
FIVE     EQU   5
EGHT     EQU   8
TWEL     EQU   12
TWNTY    EQU   20
TWNTY6   EQU   26
NTY      EQU   90
ONE40    EQU   140
*
*
         BALR  RBASE,0                  ESTABLISH BASE REGISTERS
         USING START,RBASE
         USING ALLOCWKA,RWKA            WORK AREA ADDRESSABILITY Y02080
         USING JFCBFLD,RJFCB
*
* THIS SECTION TESTS FOR CYLINDER REQUESTS AND AVERAGE RECORD LENGTH
* REQUESTS SPECIFYING THE ROUND OPTION.
*
START    EQU   *                                                 A42477
         XC    DADSMTBL+2(14),DADSMTBL+2  CLEAR DADSM TABLE      A42477
         MVI   DADSMTBL+EGHT,X'7F'      INITIALIZE QUANTITY
         LTR   RJFCBA,RJFCB             INITIALIZE ALTERNATE PTR A42477
         BP    TESTYPE                  BRANCH IF NOT A PARTIAL  A46776
*                                       F1 DSCB                  A46776
         TM    PD1SCALO,JFCBCYL         TEST IF CYLINDER REQUEST A46776
         BO    ONETRACK                 BRANCH IF YES            A46776
         TM    PD1SCALO,JFCBAVR+JFCROUND  TEST IF RECORD         A46776
*                                       ROUNDED REQUEST          A46776
         BO    ONETRACK                 BRANCH IF YES            A46776
         B     CONVERT                  GO CONVERT THE F5 DSCB   A42477
TESTYPE  EQU   *                                                 A42477
         TM    JFCBCTRI,X'C0'           TEST IF CYLINDER REQUEST A42477
         BO    CYLREQ                   BRANCH IF YES            A42477
         TM    JFCBCTRI,X'41'           TEST IF RCD ROUNDED REQ  A42477
         BNO   CONVERT                  BRANCH IF NOT            Y02080
CYLREQ   EQU   *                                                 A42477
ONETRACK EQU   *                                                 A42477
         LA    RF,ONE                   ONE TRACK NECESSARY      A42477
         STH   RF,LABELREQ              NBR OF REQUESTED LABELS  A42477
*
* THIS SECTION CONVERTS THE FIRST FORMAT 5 DSCB FROM XXYYZ FORMAT
* TO RTA/NUMBER OF TRACKS.
*
CONVERT  EQU   *                                                 A42477
         MVC   DS5FMTID(NTY),DS5MAVET   REMOVE ID FROM FORMAT 5
         LA    RCNT,TWNTY6              SET TO MAX NBR OF EXTENTS
         LH    RTRK,DS4DEVSZ+2         LOAD NBR OF TRKS/CYL
         LA    RIN,DS5AVEXT        SET INPUT PTR TO 1ST EXTENT
         LA    ROUT,DS1DSNAM       SET UP OUTPUT PTR FOR CONVERTED EXTS
         XC    DS1DSNAM(ONE40),DS1DSNAM CLEAR OUTPUT AREA
TESTO    EQU   *                        BRANCH LABEL             Y02080
         MVC   FULLWORD(FOUR),ZERO(RIN) ALIGN RTA AND NBR CYLS   Y02080
         SR    RTA1,RTA1               CLEAR HIGH DIVIDEND REGISTER
         LH    RTA,FULLWORD             GET RTA                  Y02080
         LTR   RTA,RTA                 IS THIS EXTENT ZERO
         BZ    INCRIN                  BRANCH IF YES
         LH    RCC,FULLWORD+TWO         GET NUMBER OF FULL CYLS  Y02080
         MR    RHH,RTRK                CONVERT CYLS TO TRKS
         TM    LABELREQ+1,X'FF'         TEST IF CYLINDER REQUEST A42477
         BC    5,TESTTRK                BRANCH IF YES            A42477
ADD      IC    RHH,FOUR(RIN)            GET EXCESS TRKS
         AR    RCC,RHH                 CALCULATE TOTAL TRKS THIS EXT
*
* SAVE SMALL TRACK EXTENT
*
         CH    RCC,DADSMTBL+EGHT        LESS THAN TRACKS SAVED
         BH    ADDA                     BRANCH IF NOT
         STH   RCC,DADSMTBL+EGHT        SAVE NUMBER OF TRACKS
         ST    ROUT,DADSMTBL+4          STORE ADDRESS OF RTA
         B     ADDA                    GO STORE RTA AND NBR TRKS
*
* THIS SECTION ASSURES THAT CYLINDER REQUESTS
* AND AVERAGE RECORD LENGTH REQUESTS SPECIFYING THE
* ROUND OPTION ARE ALIGNED ON CYLINDER BOUNDARIES.
*
TESTTRK  LTR   RCC,RCC                  ARE TRACKS ZERO
         BZ    NOCYL                    BRANCH IF YES
         DR    RTA1,RTRK                CONVERT RTA TO CYL AND TRACKS
         LR    RHH,RTA1                 SAVE EXTRA TRACKS
         MR    RTA1,RTRK                REALIGN RTA ON CYL BOUNDARY
         LTR   RHH,RHH                  ARE TRACKS REMAINING
         BZ    SMALLEXT                 BRANCH IF NO
         AR    RTA,RTRK                 INCREMENT RTA
         LR    RBAK,RTRK                TRACK CONSTANT
         SR    RBAK,RHH                 SUBTRACT REMAINDER
         CLC   DADSMTBL+TWEL(TWO),ZEROS IS RTA SAVED
         BNE   SMALLEXT                 BRANCH IF YES
         CH    RBAK,LABELREQ            DO EXTRA TRACKS FILL     A42477
*                                       REQUEST?                 A42477
         BL    SMALLEXT                 BRANCH IF NO
         STH   RTA,DADSMTBL+TWEL        STORE RTA OF EXTENT - RTA+1
SMALLEXT CH    RCC,DADSMTBL+EGHT        TRACKS LESS THAN SAVED
         BH    ADDA                     BRANCH IF NO
         STH   RCC,DADSMTBL+EGHT        STORE NUMBER OF TRACKS
         ST    ROUT,DADSMTBL+4          STORE RTA ADDRESS
         B     ADDA                     UNCONDITIONAL BRANCH
NOCYL    SR    RHH,RHH
         IC    RHH,FOUR(RIN)            INSERT TRKS THIS EXTENT
         CH    RHH,LABELREQ             DOES IT FILL REQUEST?    A42477
         BL    ADDA                     BRANCH IF NO
         CH    RHH,DADSMTBL+EGHT        LESS THAN TRACKS SAVED
         BH    ADDA                     BR IF NOT
         STH   RHH,DADSMTBL+EGHT        SAVE NEW LOW
         ST    ROUT,DADSMTBL+4          RTA ADDRESS
ADDA     STH   RTA,ZERO(ROUT)           STORE RTA IN OUTPUT
         STH   RCC,TWO(ROUT)            STORE NUMBER OF TRKS AVAIL
CONT     LA    ROUT,FOUR(ROUT)          STEP OUTPUT POINTER
INCRIN   LA    RIN,FIVE(RIN)            STEP INPUT POINTER
         BCT   RCNT,TESTO              BRANCH IF MORE EXTENTS
         LA    ROUT,DS1DSNAM           RESET OUTPUT POINTER
         TM    LABELREQ+1,X'FF'         TEST IF CYLINDER REQUEST A42477
         BZ    TRKREQ                   BRANCH IF ZERO TO TRACK REQUEST
*
* CYLINDER USER LABEL REQUEST
* TAKE THE USER LABEL TRACK FROM A TRACK EXTENT IF ONE IS AVAILABLE.
* IF NOT USE LEFT OVER TRACKS IN FRONT OF A CYLINDER.
*
         L     ROUT,DADSMTBL+4          LOAD RTA ADDRESS
         LTR   ROUT,ROUT                RTA ZERO
         BZ    EXIT                     BRANCH IF YES
         CLC   TWO(ROUT,TWO),ZEROS      NUMBER OF TRACKS ZERO
         BNE   NONTRK                   BRANCH IF YES
         LH    RTA,ZERO(ROUT)           PICK UP RTA
         STH   RTA,USERLBL              RTA OF USER LABEL
         AH    RTA,LABELREQ             ADD NBR OF USER LABELS   A42477
         STH   RTA,USERLBL+2            STORE RTA+1
         B     EXIT                     UNCONDITIONAL BRANCH
NONTRK   CLC   DADSMTBL+TWEL(TWO),ZEROS EXTRA TRACKS AVAILABLE
         BE    CYLEXT                   BR IF NO
         LH    RTA,DADSMTBL+TWEL        RTA SAVED
         STH   RTA,USERLBL+2            RTA+1 OF USER LABEL
         SH    RTA,LABELREQ             SUBTRACT USER LABEL TRKS A42477
         STH   RTA,USERLBL              RTA OF USER LABEL
         B     EXIT                     UNCONDITIONAL BRANCH
CYLEXT   LH    RTA,ZERO(ROUT)           PICK UP RTA
         AR    RTA,RTRK                 ADD TRKS/CYL
         STH   RTA,ZERO(ROUT)           STORE NEW RTA
         STH   RTA,USERLBL+2            STORE USER LABEL RTA+1
         SH    RTA,LABELREQ             SUBTRACT USER LABEL TRKS A42477
         STH   RTA,USERLBL              STORE USER LABEL RTA
         LH    RCC,TWO(ROUT)            TRACKS AVAILABLE
         SR    RCC,RTRK                 SUBR. TRKS/CYL
         STH   RCC,TWO(ROUT)            STORE NEW TRACKS AVAILABLE
         B     EXIT                     UNCONDITIONAL BRANCH
*
* FILL USER LABEL EXTENT FOR A TRACK REQUEST FROM THE SMALL EXTENT
*
TRKREQ   L     ROUT,DADSMTBL+4          RTA ADDRESS
         LTR   ROUT,ROUT                HAS ADDRESS BEEN SAVED
         BZ    EXIT                     BRANCH IF NOT
         LH    RTA,ZERO(ROUT)           RTA
         STH   RTA,USERLBL              USER LABEL RTA
         LA    RTA,ONE(RTA)             ONE TRACK
         STH   RTA,ZERO(ROUT)           UPDATE RTA IN FORMAT 5
         STH   RTA,USERLBL+2            USER LABEL RTA+1
         LH    RCC,TWO(ROUT)            UPDATE NUMBER OF TRACKS AVAIL.
         BCTR  RCC,0                    COUNTING
         STH   RCC,TWO(ROUT)
*
* THIS SECTION DETERMINES THE 'BRANCH TO' MODULE.
*
EXIT     EQU   *                                                 A42477
         CLC   USERLBL(TWO),ZEROS       TEST FOR A USER LABEL    A42477
         BE    NOSPACE                  BRANCH IF NOT
         LA    ROUT,DS1DSNAM            RESTORE POINTER
         XC    DADSMTBL+4(TWEL),DADSMTBL+4   CLEAR DADSM TABLE
         LTR   REQ,REQ                  TEST IF ZERO QUANTITY    A42477
         BZ    SETTBL                   BRANCH IF YES
         LA    RTTRPTR,ALLOCA2          LOAD PTR TO CYL/TRK LOAD
*
XCTLHERE EQU   *                        BRANCH LABEL             Y02080
         XC    LABELREQ(2),LABELREQ     CLEAR UHOLECTR FIELD     A42477
*
* BRANCH TO A SUBSEQUENT DADSM ALLOCATE MODULE.
*
         IECRES LOAD,EXTPR=(RWKA),MODID=(RTTRPTR),BRANCH=DIRECT  Y02080
*
* IF ZERO QUANTITY, SET UP LABEL EXTENT IN DADSMTBL AND GO TO BUILD
* FORMAT 1 LOAD
*
SETTBL   MVC   DADSMTBL+4(FOUR),USERLBL SET UP USER LABEL EXTENT
         MVI   DADSMTBL+1,X'01'         ONE EXTENT IN TABLE
         LA    RTTRPTR,ALLOCA3          LOAD PTR TO F1/F3 ENTRY
         B     XCTLHERE                 UNCONDITIONAL BRANCH
*
* NO USER LABEL SPACE, EXIT TO LAST LOAD
*
NOSPACE  LA    RTTRPTR,ALLOCD2          LOAD PTR TO LAST ALLOCATE LOAD
         LA    RERRPASS,TWNTY           SET CODE 'QUANTITY NOT AVAIL'
         B     XCTLHERE                 UNCONDITIONAL BRANCH
* * * * * * * * * * * * *
*
***   CONSTANTS
*
* * * * * * * * * * * * *
*
* TABLE OF MODULE IDS AND ENTRY POINT ADDRESSES
*
         XCTLTABL ID=(ALLOCA2,5D,ALLOCA3,5E,ALLOCD2,5H),SVC=032, Y02080X
               LENGTH=,BRT=YES                                   Y02080
         EJECT                                                   Y02080
DSCBWKAR IECALLWA EP,F4,D1=(1),D2=(5)   ALLOCATE WORK AREA       Y02080
LABELREQ EQU   UHOLECTR                 NBR OF USER LABELS       A42477
ZEROS    EQU   CCW4+4
USERLBL  EQU   CCW10+4
         EJECT                                                   Y02080
CVT      DSECT                          CVT DSECT                Y02080
         CVT                                                     Y02080
         EJECT                                                   Y02080
*
* * * * * * * * * * * * *
*
* * * * * * * * * * * * *
*
JFCBFLD  DSECT
         IEFJFCBN
         EJECT                                                   Y02080
*
* * * * * * * * * * * * *
*
***   DSECT FOR THE PARTIAL DSCB PASSED TO ALLOCATE BY IEHMOVE   A46776
*
* * * * * * * * * * * * *
*
         ORG   INFMJFCB
PD1DSNAM DS    CL44                DATA SET NAME
PD1FMTID DS    CL1                 FORMAT IDENTIFIER
PD1DSSN  DS    CL6                 DATA SET SERIAL NUMBER
PD1VOLSQ DS    XL2                 VOLUME SEQUENCE NUMBER
PD1CREDT DS    XL3                 CREATION DATE
PD1EXPDT DS    XL3                 EXPIRATION DATE
PD1NOEPV DS    XL1                 NUMBER OF EXTENTS ON VOLUME
PD1NOBDB DS    XL1                 NUMBER OF BYTES USED IN LAST
*                                     DIRECTORY BLOCK
         DS    XL1                 RESERVED
PD1SYSCD DS    CL13                SYSTEM CODE
         DS    XL7                 RESERVED
PD1DSORG DS    XL2                 DATA SET ORGANIZATION
PD1RECFM DS    XL1                 RECORD FORMAT
PD1OPTCD DS    XL1                 OPTION CODE
PD1BLKL  DS    XL2                 BLOCK LENGTH
PD1LRECL DS    XL2                 RECORD LENGTH
PD1KEYL  DS    XL1                 KEY LENGTH
PD1RKP   DS    XL2                 RELATIVE KEY POSITION
PD1DSIND DS    XL1                 DATA SET INDICATORS
PD1SCALO DS    XL4                 SECONDARY ALLOCATION
PD1LSTAR DS    XL3                 LAST USED TRACK AND BLOCK ON TRACK
PD1TRBAL DS    XL2                 BYTES REMAINING ON LAST TRACK USED
         DS    XL2                 RESERVED
PD1EXT1  DS    XL1                 EXTENT TYPE INDICATOR
         DS    XL2                 UNUSED
PDPRIQTY DS    F                   PRIMARY SPACE REQUEST IN TRACKS
PDDIRQTY DS    F                   NUMBER OF DIRECTORY BLOCKS
         END   IGG0325K                                          A42477
