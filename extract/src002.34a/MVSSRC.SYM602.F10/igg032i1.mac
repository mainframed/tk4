 TITLE 'IGG032I1 - ISAM ALLOCATE - VALIDITY CHECK'
IGG032I1 CSECT
*
*MODULE NAME = IGG032I1
*
*DESCRIPTIVE NAME = ISAM ALLOCATE - VALIDITY CHECK
*
*COPYRIGHT = NONE
*
*CHANGE ACTIVITY = SEE BELOW
*
*          RELEASE 16 DELETIONS
*0000038600                                                       15708
*          RELEASE 17 DELETIONS
*1715001000,089680,089740                                         19342
*          RELEASE 18 DELETIONS
*3126038200,060600-060800,063400-063600,070200-070400,070800-     23312
*3126071000,077660,078400-078600,079200-079400,097600,103200,     23312
*3126104200-105200                                                23312
*3126                                                             23428
*3126093600                                                       23475
*3126033200                                                       22224
*          RELEASE 19 DELETIONS
*1776083800-090200                                               O19113
*1776069460                                                      A28380
*          RELEASE 20 DELETIONS
*          RELEASE 21 DELETIONS
*0000064400-064800                                               M1273
*1194012950,034730-034820                                        M0705
*1194003400-007000,007600-008800,011800-015000,024200,031800-    S21016
*1194032000,033400-035000,038000,038620,058600-058800,059200,    S21016
*1194064000-064200,066800-067000,068200,068800,071200-071400,    S21016
*1194072200-072400,073000-073400,080200-080400,081200-081400,    S21016
*1194083600,092200-092600                                        S21016
*1194003500,003800-004000,005300,005500-005700,009600,025200-    A38860
*1194025400,037400,038580,038680-039200,039800,041400,042800,    A38860
*1194043800-044200,050200-050400,051400,052000,052400-052600,    A38860
*1194053600-053800,054600-055400,056600,057000,059000,059800,    A38860
*1194061800,062800,066200,067600,068400,069200,069430,069600,    A38860
*1194070980,071040,071200,071320,071600,073800,074800,075600,    A38860
*1194079300,080600,081600,083400,090400,091000,093000,103300-    A38860
*1194104000,105000,115600,117800-118000                          A38860
*          RELEASE 22 DELETIONS
*          VS2 RELEASE 02 DELETIONS/CHANGES
*0000000030-000150,000880-000940,007600,007900-008000,010600,    Y02080
*0000011200,011600-011690,015600,024800,025100-025200,025400-    Y02080
*0000025800,032600,035200,035320,038200,074600-075000,078020,    Y02080
*0000078100,099200-103000,103200-103400,104200,105000-105600,    Y02080
*0000106000,106400-117720                                        Y02080
*0000009400-009600,011200,033002,032016,032020,035230-035400,    Y02134
*0000038580,039400-039600,041600-041800,042600-043000,044600-    Y02134
*0000044800,051200-052200,069000-070000,077640-077650,106600,    Y02134
*0000107000-118120                                               Y02134
*0000077630-077660                                               Y02130
*0000                                                           YA03038
*0000                                                            YM4707
*0000                                                            YM7213
*0000068000,068800                                               YM7212
*          VS1 RELEASE 2 DELETIONS/CHANGES
*0000065320-065340                                               XM1207
*          VS2 RELEASE 1 DELETIONS/CHANGES
*0000                                                           OY00240
*          VS2 RELEASE 3 DELETIONS/CHANGES
*0000                                                          @ZM30492
*
*STATUS CHANGE LEVEL 008
*
*FUNCTION - THIS MODULE CHECKS FOR A DUPLICATE NAME DSCB FORMAT 1
*           AND IF ONE EXISTS ITS LEGITIMACY IS EXAMINED. IF ILLEGITI-
*           MATE AN ERROR CODE IS SET AND A TRANSFER OF CONTROL IS
*           MADE TO MODULE IGG032I7 WHERE CODES ARE GOING TO BE
*           EXAMINED.   A CHECK IS MADE TO SEE IF THERE IS ENOUGH ROOM
*           IN THE VTOC.  ALSO THE ABSOLUTE TRACK AND QUANTITIES GIVEN
*           IN AN ABSOLUTE TRACK REQUEST ARE CHECKED FOR CYLINDER
*           BOUNDARIES.  CHECKS ARE MADE TO INSURE THAT THE TYPE OF
*           ALLOCATION IS THE SAME ACROSS DD CARDS AND THAT TWO
*           REQUESTS FOR THE SAME TYPE OF AREA ARE NOT MADE.
*
*           IF MORE THAN ONE DD STATEMENT IS USED IN REQUESTING
*           SPACE, THE SEVEN LOW-ORDER BYTES OF THE DDNAME FIELD
*           IN ALL BUT THE FIRST ISAM DD ENTRY ARE USED TO CREATE
*           THE FOLLOWING SPECIAL ISAM ALLOCATE INTERFACE:
*
*              BYTE 1
*                   BITS 0 AND 1   00 - ABSOLUTE TRACK REQUEST
*                                  01 - BLANK SPACE REQUEST
*                                  11 - CONTIGUOUS SPACE REQUEST
*
*                   BIT 2          THIS IS THE SECOND DD CARD.
*
*                   BIT 5          OVFLOW HAS BEEN ALLOCATED.
*
*                   BIT 6          PRIME HAS BEEN ALLOCATED.
*
*                   BIT 7          INDEX HAS BEEN ALLOCATED.
*
*              BYTES 2 - 3         CURRENT VOLUME SEQUENCE NUMBER
*                                  FOR SECOND OR THIRD DD ENTRY
*
*              BYTES 4 - 7         POINTER TO THE FIRST TIOT ENTRY
*                                  FOR THIS REQUEST
*
*ENTRIES - THE ONLY ENTRY POINT TO THIS MODULE IS IGG032I1.  ENTRY IS
*          MADE FROM THE SECOND LOAD OF ALLOCATE (MODULE IGG0325A)
*          OR FROM THE LAST LOAD OF ISAM ALLOCATE VIA A BRANCH.
*
*INPUT -  AT ENTRY TO THIS MODULE, REGISTER 11 POINTS TO A JFCB AND
*         REGISTER 13 POINTS TO THE ALLOCATE WORK AREA. THIS WORK AREA
*         CONTAINS A POINTER TO THE VARIABLE LENGTH DSAB LIST FOR THIS
*         REQUEST, THE ADDRESS OF THE FORMAT 4 DSCB, THE ADDRESS OF THE
*         FIRST FORMAT 5 DSCB, AND THE ADDRESS OF THE FORMAT 1 DSCB, IF
*         ONE EXISTS. THE FIRST FORMAT 5 DSCB IS ALSO IN THE WORK AREA.
*         THE VARIABLE LENGTH DSAB LIST HAS THE FOLLOWING FORMAT:
*         THE FIRST FULLWORD POINTS TO THE DSAB BEING ALLOCATED.
*         THE SECOND FULLWORD POINTS TO THE DSAB REPRESENTING THE
*         FIRST DD CARD.  THE THIRD IS ZERO IF THERE IS ONLY ONE
*         DD CARD, OR A POINTER TO THE DSAB FOR THE SECOND DD CARD.
*         THE FOURTH WORD IS ZERO IF THERE IS NO THIRD DD CARD, OR
*         A POINTER TO THE DSAB FOR THE THIRD DD CARD, OR ABSENT IF
*         THERE IS NO SECOND DD CARD.  THE FIFTH WORD IS ZERO IF
*         THERE ARE THREE CONCATENATED DD CARDS, OR ABSENT OTHERWISE.
*
*OUTPUT - UPON TRANSFERRING TO THE NEXT MODULE, IGG032I2, THE WORK AREA
*         CONTAINS THE PRIMARY AND DIRECTORY QUANTITIES, THE ABSOLUTE
*         TRACK ADDRESS, AND THE AMOUNT REQUESTED IN A MULTI-VOLUME
*         ABSOLUTE TRACK REQUEST.
*
*EXTERNAL ROUTINES -
*   SVCS ISSUED - NONE
*   MACROS USED - IEFJFCBN - BUILDS JFCB
*                 IECALLWA - ALLOCATE WORK AREA EXPANSION
*                 IECSDSL1 - BUILDS DSCBS
*                 IECDSECS - EXPANDS THE CVT, UCB, AND DSAB DSECTS
*
*EXITS - NORMAL - BRANCH TO MODULE IGG032I2
*        - ERROR - BRANCH TO MODULE IGG032I7 WITH A CODE IN REGISTER 5
*
*        THE FOLLOWING ERROR CODES CAN BE ISSUED BY IGG032I1:
*
*        04 - DUPLICATE NAME DSCB EXISTS
*
*        1C - WRONG DSORG OR DISP
*
*        20 - NO PRIME AREA REQUESTED
*
*        24 - PRIME AREA MUST BE REQUESTED BEFORE OVFLOW
*
*        2C - DSNAME ELEMENT DUPLICATION
*
*        4C - NO SPACE PARAMETER GIVEN FOR A NEW DATA SET OR
*             ZERO SPACE REQUESTED AT ABSOLUTE TRACK ZERO
*
*        50 - INVALID REQUEST FOR EMBEDDED INDEX
*
*        54 - MULTIVOLUME INDEX NOT ALLOWED
*
*        58 - DSNAME ELEMENT INVALID
*
*        5C - MULTIVOLUME OVFLOW NOT ALLOWED
*
*        60 - ABSTR AND CYL REQUESTS CONFLICT
*
*        64 - CYL AND CONTIG REQUESTS CONFLICT
*
*        68 - INVALID SPACE SUBPARAMETER
*
*        70 - INDEX REQUEST DUPLICATION
*
*        84 - SPACE REQUEST MUST BE ABSTR FOR A DOS VOLUME
*
*        8C - INDEX MUST BE REQUESTED BEFORE PRIME AREA
*
*        90 - INVALID THIRD DD CARD FOR ISAM ALLOCATION
*
*
*TABLES/WORK AREAS - DADSM WORK AREA DESCRIBED BY MACRO 'IECALLWA'
*
*              ***************************************
*              *             DADSM TABLE             *
*              ***************************************
*
*              ***************************************
*              *        *         *                  *
*              *  TYPE  *  NO OF  *     USED HOLE    *
*              *  FLAG  * ENTRIES *      COUNTER     *
*              *        *         *                  *
*              ***************************************
*              *                  *                  *
*              *       RTA        *      RTA+1       *
*              *                  *                  *
*              ***************************************
*              *                  *                  *
*              *       RTA        *      RTA+1       *
*              *                  *                  *
*              ***************************************
*              *                  *                  *
*              *       RTA        *      RTA+1       *
*              *                  *                  *
*              ***************************************
*              *                  *                  *
*              *       RTA        *      RTA+1       *
*              *                  *                  *
*              ***************************************
*              *                  *                  *
*              *       RTA        *      RTA+1       *
*              *                  *                  *
*              ***************************************
*
*
*              TYPE FLAG = 02 - BPAM DIRECTORIES REQUESTED.
*
*                        = 40 - USER LABELS REQUESTED
*
*                        = 80 - SET MUST COMPLETE IS ACTIVE
*
*
*              NO OF ENTRIES = NUMBER OF ENTRIES WITHIN DADSM TABLE.
*
*
*              RTA = THE STARTING TRACK OF THE ALLOCATED EXTENT.
*
*
*              RTA+1 = THE STARTING TRACK OF THE NEXT AVAILABLE EXTENT.
*
*
*ATTRIBUTES - REENTRANT
*
*STORAGE - PROGRAM CODE CSECT = LESS THAN 2048 BYTES
*          WORK AREA = AS DEFINED IN THE IECALLWA MACRO
*          RPS WORK AREA = AS DEFINED IN THE IECRPS MACRO
*
*
*REGISTER USAGE
*              REGISTERS 0-9 WORK REGISTERS
*              REGISTER 10 - TIOT SET UP TABLE POINTER
*              REGISTER 11 - JFCB POINTER
*              REGISTER 12 - BASE REGISTER
*              REGISTER 13 - WORK AREA POINTER
*              REGISTER 14 SUBROUTINE LINKAGE AND WORK REGISTER
*              REGISTER 15 WORK REGISTER
*
***** REGISTER NAMES *****
RZERO    EQU   0
RONE     EQU   1
RTWO     EQU   2
RTHREE   EQU   3
RFOUR    EQU   4
RFIVE    EQU   5
RSIX     EQU   6
RSEVEN   EQU   7
REIGHT   EQU   8
RNINE    EQU   9
RTEN     EQU   10
RJFCB    EQU   11
RBASE    EQU   12
RWORK    EQU   13
RETURN   EQU   14
RFTEN    EQU   15
*
*
* OTHER EQUATES
*
*
ZERO     EQU   0                   CONSTANT OF ZERO              Y02134
K1       EQU   1                   CONSTANT OF ONE               Y02080
K2       EQU   2                   CONSTANT OF TWO               Y02080
K3       EQU   3                   CONSTANT OF THREE             Y02080
FOUR     EQU   4                   CONSTANT OF FOUR              Y02134
DUPLCATE EQU   X'04'               DUPLICATE NAME ERROR CODE     S21016
BADDSORG EQU   X'1C'               WRONG DSORG OR DISP ERROR     S21016
NOPRIME  EQU   X'20'               NO PRIME AREA REQUESTED       S21016
PRIME1ST EQU   X'24'               PRIME BEFORE OVFLOW ERROR     S21016
DUPELMNT EQU   X'2C'               DSNAME ELEMENT DUPLICATION    S21016
NOSPACE  EQU   X'4C'               NO SPACE PARAMETER ERROR      S21016
INVALIND EQU   X'50'               INVALID EMBEDDED INDEX ERROR  S21016
MVINDEX  EQU   X'54'               MULTIVOLUME INDEX ERROR CODE  S21016
INVALELM EQU   X'58'               INVALID DSNAME ELEMENT ERROR  S21016
MVOVFLOW EQU   X'5C'               MULTIVOLUME OVFLOW ERROR      S21016
NOTCYL   EQU   X'60'               ABSTR AND CYL CONFLICT        S21016
NOCONTIG EQU   X'64'               CYL AND CONTIG CONFLICT       S21016
SPACERR  EQU   X'68'               INVALID SPACE SUBPARAMETER    S21016
DUPINDEX EQU   X'70'               INDEX REQUEST DUPLICATION     S21016
ABSTRREQ EQU   X'84'               SPACE REQUEST NOT ABSTR       S21016
INVINDEX EQU   X'8C'               PRIME BEFORE INDEX ERROR      S21016
INVDDCRD EQU   X'90'               INVALID THIRD DD CARD         S21016
TIOEVOLS EQU   2                   OFFSET TO NUMBER OF DEVICES   A38860
TIOEDDNM EQU   4                   OFFSET TO DDNAME              A38860
ISAMSTAT EQU   5                   OFFSET TO ALLOCATION STATUS   A38860
VOLSQCTR EQU   6                   OFFSET TO DS1VOLSQ COUNTER    A38860
TIOTPTR  EQU   8                   PTR TO 1ST ISAM TIOT ENTRY    A38860
UCBENTRY EQU   16                  OFFSET TO FIRST UCB ENTRY     A38860
BLANK    EQU   X'40'               EBCDIC BLANK                  A38860
CURRDSAB EQU   0                   CURRENT DSAB OFFSET           Y02134
FRSTDSAB EQU   4                   FIRST DSAB OFFSET             Y02134
SCNDDSAB EQU   8                   SECOND DSAB OFFSET            Y02134
THRDDSAB EQU   12                  THIRD DSAB OFFSET             Y02134
ONE16    EQU   116                 116 CYLINDERS                 Y02130
DA3340   EQU   X'0A'               3340 DEVICE TYPE              Y02130
K12      EQU   12                  CONSTANT OF TWLEVE            Y02130
*
         BALR  RBASE,0
         USING ALPHA,RBASE
         USING ALLOCWKA,RWORK           WORK AREA ADDRESSABILITY Y02080
         USING JFCBKEN,RJFCB
*
ALPHA    TM    DS4VTOCI,X'80'          CHECK FOR BOS PACK
         BC    8,NOTBOS                 IF NOT BOS, BRANCH        22224
         TM    JFCBCTRI,X'DF'      CHECK IF ABSTR WITH DOS PACK  S21016
         BZ    CHECK               BRANCH IF ABSOLUTE TRACK      S21016
         LA    RFIVE,ABSTRREQ      SPACE REQUEST MUST BE ABSTR   S21016
         B     ERRORXCT                                          S21016
NOTBOS   EQU   *                                                 S21016
         TM    JFCBCTRI,X'C0'      IS IT A CYL OR ABSTR REQUEST  S21016
         BC    9,CHECK             BRANCH IF CYL OR ABSTR        S21016
         LA    RFIVE,SPACERR       INVALID SPACE REQUEST ERROR   S21016
         B     ERRORXCT                                          S21016
CHECK    EQU   *                                                 S21016
         CLI   CTR,X'00'           CHECK IF ENTRY FROM SECOND    S21016
*                                  LOAD OF ALLOCATE OR LAST LOAD S21016
*                                  OF ISAM ALLOCATE              S21016
         BNE   REJOIN              BRANCH IF FROM LAST LOAD      S21016
         TM    JFCBCTRI,X'DF'      CHECK REQUEST TYPE FIELD      S21016
         BC    5,GETTIOT           BRANCH IF CYLINDERS           S21016
*
* THIS SECTION TESTS IF ANY SPACE WAS REQUESTED. IF NOT, THE ABSOLUTE
* TRACK ADDRESS IS TESTED FOR ZERO TO DETERMINE IF A SPACE PARAMETER
* WAS GIVEN FOR THIS DATA SET.
*
         OC    JFCBPQTY,JFCBPQTY        ANY SPACE REQUESTED      M0705
         BNZ   GETTIOT                  BRANCH IF YES            M0705
         OC    JFCBABST(2),JFCBABST     IS ABSOLUTE TRACK ZERO   M0705
         BNZ   GETTIOT                  BRANCH IF NOT ZERO       M0705
         LA    RFIVE,NOSPACE       NO SPACE PARAMETER ERROR      S21016
         B     ERRORXCT                                          S21016
GETTIOT  EQU   *                                                 S21016
         L     RTEN,ADSABLST            PICK UP PTR TO DSAB LIST Y02080
         L     RTEN,CURRDSAB(RTEN)      GET CURRENT DSAB ADDR    Y02134
         USING DSAB,RTEN                                         Y02134
         L     RTEN,DSABTIOT            GET CURRENT TIOT ADDR    Y02134
         DROP  RTEN                                              Y02134
* THIS SECTION CHECKS FOR LEGITIMACY OF DUPLICATE NAME F1 IF ONE EXISTS
         XC    SAVEADR(5),SAVEADR       CLEAR FORMAT 1 ADDRESS
         CLI   DS1FMTID,X'F1'           TEST FOR A DUPLICATE NAME DSCB
*                                          BY CHECKING THE FORMAT
*                                          IDENTIFIER TO SEE IF THE 1ST
*                                          LOAD OF ALLOCATE READ IN A
*                                          FORMAT 1 DSCB
         BNE   AROUND                   BRANCH IF NO DUPLICATE NAME
*                                          DSCB
         CLI   TIOEDDNM(RTEN),BLANK     CHECK FOR DDNAME IN TIOT A38860
         BE    MORETHAN                 BRANCH IF THERE IS NO DDNAME-
*                                          NOT FIRST TIOT ENTRY
DUPNAME  EQU   *                                                 S21016
         LA    RFIVE,DUPLCATE      DUPLICATE NAME ERROR CODE     S21016
ERRORXCT EQU   *                                                  23312
         LA    RTHREE,LASTLOAD          SET 7TH LOAD ID          Y02080
         B     XCT
*                                                                 15708
*              NO DDNAME IMPLIES CONCATENATION WITH A             15708
*              PREVIOUS DD CARD.  A BLANK TIOT POINTER ENTRY      15708
*              INDICATES THAT AN INVALID CONDITION EXISTS,        15708
*              DSORG=IS WAS NOT SPECIFIED FOR THE PRECEEDING      15708
*              DD CARD.                                           15708
*                                                                 15708
MORETHAN EQU   *                                                  15708
         CLC   TIOTPTR(FOUR,RTEN),BLANKS  CHECK TIOT POINTER     Y02134
         BC    7,CONTINUE         BRANCH IF NOT EQUAL TO BLANKS   15708
DSDISP   EQU   *                                                OY00240
         LA    RFIVE,BADDSORG      WRONG DSORG ERROR CODE        S21016
         B     ERRORXCT                                           15708
CONTINUE EQU   *                                                  15708
         L     RNINE,TIOTPTR(,RTEN)     PTR TO 1ST TIOT ENTRY    A38860
         L     RSIX,UCBENTRY(,RTEN)     SET PTR TO 1ST UCB ENTRY A38860
         LA    RSIX,ZERO(,RSIX)         TIOT ENTRY HAS UCB ADDR  Y02134
         TM    ISAMSTAT(RTEN),X'20'     CHECK STATUS BYTE IN THE A38860
*                                          NEXT TIOT ENTRY FOR THE FLAG
*                                          SET TO INDICATE THIS ENTRY
*                                          AS THE SECOND ENTRY
         BC    1,PICKUP                 BRANCH IF THIS ENTRY IS THE
*                                          SECOND ENTRY
* THIS SECTION CHECKS TO SEE IF THE UCB IN THIS ENTRY IS THE SAME AS
* THE UCB IN THE FIRST TIOT ENTRY
*
         L     RSEVEN,UCBENTRY(,RNINE)  POINT TO ONLY UCB ENTRY  A38860
         LA    RSEVEN,ZERO(,RSEVEN)     CLEAR HIGH BYTE          Y02134
         CR    RSIX,RSEVEN              CHECK THE 12 BIT UCB POINTERS
         BE    LEGITIMT                 BRANCH IF THE UCB POINTERS ARE
*                                          EQUAL
         L     RNINE,ADSABLST           GET ADDR OF DSAB LIST    Y02134
         L     RNINE,SCNDDSAB(,RNINE)   GET ADDR OF 2ND DSAB     Y02134
         USING DSAB,RNINE                                        Y02134
         L     RNINE,DSABTIOT           GET SECOND TIOT ENTRY    Y02134
         DROP  RNINE                                             Y02134
*
* THIS SECTION COMPARES THE FIRST UCB ENTRY IN THE SECOND OR THIRD
* TIOT ENTRY TO ALL OF THE UCB'S IN THE PRECEEDING TIOT ENTRY
*
PICKUP   SR    REIGHT,REIGHT
         IC    REIGHT,TIOEVOLS(,RNINE)  GET NUMBER OF UCB'S IN   A38860
*                                       THE PRECEDING TIOT ENTRY A38860
         LA    RNINE,UCBENTRY(,RNINE)   POINT TO 1ST UCB ENTRY   A38860
COMPARE  L     RSEVEN,0(RNINE)
         LA    RSEVEN,ZERO(,RSEVEN)     CLEAR HIGH BYTE          Y02134
         CR    RSIX,RSEVEN              CHECK UCB POINTERS
         BE    LEGITIMT                 BRANCH IF UCB POINTERS ARE
*                                          EQUAL
         BCTR  REIGHT,0                 REDUCE NUMBER OF UCB'S IN
*                                          PREVIOUS TIOT ENTRY
         LTR   REIGHT,REIGHT            CHECK FOR LAST UCB ENTRY
         BZ    DUPNAME                  BRANCH IF LAST UCB ENTRY - THE
*                                          DUPLICATE NAME DSCB FORMAT 1
*                                          IS ILLEGAL
         LA    RNINE,4(RNINE)           UPDATE POINTER
         B     COMPARE
* THIS SECTION TAKES THE CCHHR OF THE RECORD BEYOND THE DUPLICATE NAME
* DSCB FORMAT 1 AND CALCULATES THE ADDRESS OF THIS FORMAT 1.
LEGITIMT SR    RETURN,RETURN            REDUCE THE RECORD NUMBER BY ONE
         IC    RETURN,COUNT+4
         BCT   RETURN,ADJUST            CHECK FOR RECORD NUMBER ZERO -
*                                          BACKING OVER A TRACK BOUND-
*                                          ARY
*                                       BRANCH IF RECORD NUMBER NOT
*                                          ZERO
         IC    RETURN,DS4DEVDT          SET RECORD NUMBER TO MAXIMUM
*                                       NUMBER OF DCB'S PER TRACK
ADJUST   STC   RETURN,COUNT+4
         MVC   SAVEADR(5),COUNT         SAVE THE ADDRESS OF THE FORMAT1
* THIS SECTION CHECKS THE VALIDITY OF THE REQUEST AND SETS UP THE
* WORK AREA FOR THIS PASS THROUGH ISAM ALLOCATE
AROUND   EQU   *                                                 A38860
         CLI   TIOEDDNM(RTEN),BLANK     DDNAME FOR THIS ENTRY?   A38860
         BE    NONAME                   BRANCH IF NO DDNAME
         MVI   F2,X'80'                 HAVE A DDNAME-NEED A DSCB F2 -
*                                          SET F2 FLAG
*
* SINCE THE CURRENT TIOT ENTRY HAS A DDNAME, THIS SECTION OBTAINS
* THE SECOND DSAB POINTER TO GET THE SECOND TIOT ENTRY ADDRESS.
*
         L     REIGHT,ADSABLST          GET PTR TO DSAB LIST     Y02134
         L     REIGHT,SCNDDSAB(,REIGHT) GET SECOND DSAB PTR      Y02134
         LTR   REIGHT,REIGHT            TEST IF A SECOND EXISTS  Y02134
         BZ    THISIT                   BRANCH IF NOT            Y02134
         USING DSAB,REIGHT                                       Y02134
         L     REIGHT,DSABTIOT          GET 2ND TIOT ENTRY       Y02134
         DROP  REIGHT                                            Y02134
         CLI   TIOEDDNM(REIGHT),BLANK   CHECK FOR A DDNAME FOR   A38860
*                                       THE NEXT TIOT ENTRY      A38860
         BNE   THISIT                   BRANCH IF THERE IS NOT ANOTHER
*                                          TIOT ENTRY FOR THIS REQUEST
* THIS SECTION FILLS IN THE DDNAME FIELD OF THE NEXT TIOT ENTRY WITH
* INFORMATION ABOUT THIS TIOT ENTRY
         ST    RTEN,TIOTPTR(,REIGHT)    MOVE INTO NEXT ENTRY'S   A38860
*                                          DDNAME FIELD THE ADDRESS OF
*                                          THE FIRST TIOT ENTRY
         SR    RSIX,RSIX
         IC    RSIX,TIOEVOLS(,RTEN)     PICK UP NBR OF VOLUMES   A38860
         STC   RSIX,VOLSQCTR+1(,REIGHT) MOVE IN INITIAL VOL SEQ  A38860
         MVI   VOLSQCTR(REIGHT),X'00'                            A38860
         OI    ISAMSTAT(REIGHT),X'20'   TURN ON 2ND ENTRY BIT    A38860
         NI    ISAMSTAT(REIGHT),X'3F'   TURN OFF 2 BITS 0 AND 1  A38860
         TM    JFCBCTRI,X'DF'           CHECK REQUEST TYPE FIELD
         BC    8,AREADIAG               BRANCH IF AN ABSOLUTE TRACK
*                                          REQUEST
CYLREQ   TM    JFCBCTRI,X'08'           CHECK FOR CYLINDER TYPE
         BC    1,CONTIGS                BRANCH IF CONTIGUOUS REQUESTED
         OI    ISAMSTAT(REIGHT),X'40'   SET BIT 1 - BLANK OPTION A38860
         B     AREADIAG
CONTIGS  EQU   *                                                 A38860
         OI    ISAMSTAT(REIGHT),X'C0'   SET BITS 0 & 1 - CONTIG  A38860
*
AREADIAG CLC   JFCBELNM(8),INDEX        CHECK FOR THE ELEMENT NAME
*                                          ATTACHED TO THE DSNAME
INDEXTST EQU   AREADIAG                                          S21016
         BNE   NEXT1                    BRANCH IF NOT AN INDEX REQUEST
         OC    JFCBDQTY(3),JFCBDQTY     CHECK DIRECTORY QUANTITY FIELD
*                                          FOR EMBEDDED INDEX REQUEST
         BZ    CHEKMULT                 BRANCH IF NO EMBEDDING
ERROUT   EQU   *                                                 S21016
         LA    RFIVE,INVALIND      INVALID INDEX REQUEST ERROR   S21016
         B     ERRORXCT                                          S21016
CHEKMULT EQU   *                                                 A38860
         CLI   TIOEVOLS(RTEN),X'01'     TEST FOR MULTI-VOL INDEX A38860
         BE    SETTYPE             BRANCH IF NOT MULTI-VOLUME    S21016
MULTIVOL EQU   *                                                 S21016
         LA    RFIVE,MVINDEX       MULTI-VOLUME INDEX ERROR      S21016
         B     ERRORXCT                                          S21016
SETTYPE  EQU   *                                                 S21016
         MVI   TYPE,X'04'               SET TYPE FIELD TO INDICATE AN
*                                          INDEX REQUEST
         OI    ISAMSTAT(REIGHT),X'01'   SET BIT IN NEXT ENTRY    A38860
*                                          TO INDICATE AN  INDEX AREA
*                                          HAS BEEN REQUESTED
         B     MOVEFLDS
* THIS SECTION JUST SETS UP A DUMMY POINTER FOR THE ONE DD ENTRY CASE
THISIT   LA    REIGHT,DADSMTBL          SET UP DUMMY POINTER     M1273
NEXT1    EQU   *                                                  23312
         EX    0,PRIMETST               CHECK DSN ELEMENT         23312
         BNE   NEXT2                    BRANCH IF PRIME NOT ELEMENT
*                                          NAME
ALLGOOD  MVI   TYPE,X'01'               SET TYPE FIELD TO INDICATE A
*                                          PRIME REQUEST
         OI    ISAMSTAT(REIGHT),X'02'   SET BIT IN NEXT ENTRY    A38860
*                                          TO INDICATE A PRIME AREA
*                                          HAS BEEN REQUESTED
         OC    JFCBDQTY(3),JFCBDQTY     CHECK FOR EMBEDDED INDEX
         BZ    MOVEFLDS                 BRANCH IF NO EMBEDDED INDEX
         CLI   TIOEVOLS(RTEN),X'01'     TEST FOR MULTI-VOL PRIME A38860
         BE    MOVEFLDS                 BRANCH IF SINGLE VOLUME
         B     ERROUT
NEXT2    EQU   *                                                  23312
         EX    0,BLANKTST               CHECK DSN ELEMENT         23312
         BE    ALLGOOD                  BRANCH IF BLANK ELEMENT NAME
         EX    0,OVFLWTST               CHECK DSN ELEMENT        M1273
         BE    INVOVFL                  BRANCH IF OVFLOW         M1273
*                                       ELEMENT NAME             M1273
         EX    0,INDEXTST               TEST IF INDEX REQUESTED  YM7213
         BE    INVNAME                  BRANCH IF YES - NO PRIME YM7213X
                                        AREA WAS REQUESTED       YM7213
         CLI   TIOEDDNM(RTEN),BLANK     TEST IF 1ST DD CARD      YM4707
         BNE   INVELMNT                 IF YES, BR - INVALID     YM4707X
                                        DATA SET ELEMENT NAME    YM4707
INVNAME  EQU   *                                                 S21016
         LA    RFIVE,NOPRIME       NO PRIME AREA REQUESTED       S21016
         B     ERRORXCT                                          S21016
* THIS SECTION CHECKS THE VALIDITY OF THIS DD CARD AS IT IS NOT THE
* FIRST FOR AN ISAM ALLOCATE
NONAME   EQU   *                                                OY00240
         CLC   TIOTPTR(4,RTEN),BLANKS   CHECK TIOT POINTER       XM1207
         BE    DSDISP                   BRANCH IF NOT PRESENT    XM1207
*                                       TO ISSUE X'1C' RETURN    XM1207
*                                       CODE WHICH WILL CAUSE    XM1207
*                                       MESSAGE IEF260I TO BE    XM1207
*                                       ISSUED                   XM1207
         MVI   F2,X'00'                 SET F2 FIELD TO         OY00240
*                               INDICATE THAT NO FORMAT 2 DSCB BE BUILT
         TM    JFCBCTRI,X'DF'           CHECK REQUEST TYPE FIELD
         BC    5,CYLREQTD               BRANCH IF REQUEST FOR CYLINDERS
         TM    ISAMSTAT(RTEN),X'C0'     CHECK PREVIOUS ISAM      A38860
*                                          DD ENTRY'S REQUEST TYPE
         BC    8,CHEKLAST               BRANCH IF TYPES AGREE
INVSPACE EQU   *                                                 S21016
         LA    RFIVE,NOTCYL        ABSTR AND CYL CONFLICT ERROR  S21016
         B     ERRORXCT                                          S21016
CYLREQTD TM    JFCBCTRI,X'08'           CHECK CYLINDER REQUEST FIELD
         BC    1,CONTIGUO               BRANCH IF CONTIGUOUS REQUESTED
         TM    ISAMSTAT(RTEN),X'C0'     CHECK PREVIOUS ISAM      A38860
*                                          DD ENTRY'S REQUEST TYPE
         BM    CHEKLAST                 BRANCH IF BOTH SPECIFY   YM7212X
                                        CYL WITHOUT CONTIG       YM7212
         BZ    INVSPACE                 BRANCH IF PREVIOUS WAS   YM7212X
                                        ABSTR BUT CURRENT IS CYL YM7212
CONTGERR EQU   *                                                 S21016
         LA    RFIVE,NOCONTIG      CYL AND CONTIG CONFLICT       S21016
         B     ERRORXCT                                          S21016
CONTIGUO EQU   *                                                 A38860
         TM    ISAMSTAT(RTEN),X'C0'     CHECK PREVIOUS ISAM      A38860
*                                          DD ENTRY'S REQUEST TYPE
         BZ    INVSPACE                 BRANCH IF PREVIOUS WAS   YM7212X
                                        ABSTR BUT CURRENT IS     YM7212X
                                        BOTH CYL AND CONTIG      YM7212
         BM    CONTGERR                 BRANCH IF PREVIOUS WAS   YM7212X
                                        CYL BUT NOT CONTIG       YM7212
*
* THIS SECTION TESTS IF THERE ARE ANY MORE CONCATENATED DD CARDS.
* IF THE CURRENT DSAB IS THE THIRD DSAB ENTRY, THERE ARE NO MORE.
*
CHEKLAST EQU   *                        BRANCH LABEL             Y02134
         L     REIGHT,ADSABLST          GET DSAB LIST ADDR       Y02134
         CLC   THRDDSAB(FOUR,REIGHT),CURRDSAB(REIGHT) TEST IF    Y02134
*                                       CURRENT IS THE THIRD     Y02134
         BE    THISLAST                 BRANCH IF YES            Y02134
         L     REIGHT,THRDDSAB(,REIGHT) GET THIRD DSAB           Y02134
         LTR   REIGHT,REIGHT            TEST IF A THIRD EXISTS   Y02134
         BZ    THISLAST                 BRANCH IF NO             Y02134
         USING DSAB,REIGHT                                       Y02134
         L     REIGHT,DSABTIOT          GET TIOT ENTRY FOR 3RD   Y02134
         DROP  REIGHT                                            Y02134
         EX    0,PRIMETST               CHECK DSN ELEMENT         23312
         BE    CHEKSET                  BRANCH IF ELEMENT NAME IS PRIME
         EX    0,BLANKTST               CHECK DSN ELEMENT         23312
         BE    CHEKSET                  BRANCH IF BLANK          S21016
         EX    0,INDEXTST               CHECK DSN ELEMENT        S21016
         BNE   NOTINDEX                 BRANCH IF NOT INDEX      S21016
         TM    ISAMSTAT(RTEN),X'01'     TEST IF INDEX PREVIOUSLY A38860
*                                       REQUESTED BY 1ST DD CARD S21016
         BO    DUPLINDX                 BRANCH IF YES            S21016
         TM    ISAMSTAT(RTEN),X'02'     TEST IF PRIME PREVIOUSLY A38860
*                                       REQUESTED BY 1ST DD CARD S21016
         BNO   INVNAME                  NO PRIME REQUESTED ERROR S21016
INDEXPRE EQU   *                        INDEX BEFORE PRIME ERR  YA03038
         LA    RFIVE,INVINDEX           INDEX MUST PRECEDE PRIME S21016
         B     ERRORXCT                                          S21016
NOTINDEX EQU   *                                                 S21016
         EX    0,OVFLWTST               CHECK DSN ELEMENT        S21016
         BNE   INVELMNT                 BRANCH IF NOT OVFLOW     S21016
         TM    ISAMSTAT(RTEN),X'02'     TEST IF PRIME PREVIOUSLY A38860
*                                       REQUESTED BY 1ST DD CARD S21016
         BNO   LASTTEST                 BRANCH IF NOT            S21016
         LA    RFIVE,INVDDCRD           INVALID THIRD DD CARD    S21016
         B     ERRORXCT                                          S21016
LASTTEST EQU   *                                                 S21016
         TM    ISAMSTAT(RTEN),X'01'     TEST IF INDEX PREVIOUSLY A38860
*                                       REQUESTED BY 1ST DD CARD S21016
         BO    INVOVFL                  INVALID OVFLOW REQUEST   S21016
         B     INVNAME                  NO PRIME REQUESTED ERROR S21016
CHEKSET  EQU   *                                                 A38860
         TM    ISAMSTAT(RTEN),X'02'     CHECK TYPE PREVIOUSLY    A38860
*                                          REQUESTED IN THE DDNAME
         BC    8,CHEKAGN                BRANCH IF NOT REQUESTED TWICE
INVAREA  EQU   *                                                 S21016
         LA    RFIVE,DUPELMNT      DUPLICATE AREAS REQUESTED     S21016
         B     ERRORXCT                                          S21016
CHEKAGN OC     JFCBDQTY(3),JFCBDQTY     CHECK FOR EMBEDDED INDEX
*                                          REQUESTED
         BZ    SETPRIME            BRANCH IF NO ERROR            S21016
DUPLINDX EQU   *                                                 S21016
         LA    RFIVE,DUPINDEX      DUPLICATE INDEX REQUEST ERROR S21016
         B     ERRORXCT                                          S21016
SETPRIME EQU   *                                                 S21016
* THIS SECTION UPDATES THE INFORMATION IN THE DD ENTRY IN THE TIOT
         OI    ISAMSTAT(REIGHT),X'02'   TURN ON PRIME BIT        A38860
         MVI   TYPE,X'01'               SET TYPE TO PRIME
* THIS SECTION SETS UP THE WORK AREA FOR THIS PASS THROUGH ISAM
* ALLOCATE
MOVEFLDS EQU   *                        BRANCH LABEL             Y02080
         ST    RTEN,ATIOTPTR           SAVE THE DD ENTRY POINTER Y02080
         MVC   HOWMUCH+K1(K3),JFCBPQTY  SAVE PRIMARY QUANTITY    Y02080
         MVC   DIRQTY(2),JFCBDQTY+1    PICK UP EMBEDDED INDEX QUANTITY
         MVC   ABSTR(2),JFCBABST       PICK UP ABSOLUTE TRACK ADDRESS
         CLI   TIOEVOLS(RTEN),X'01'     MULTI-VOLUME REQUEST?    A38860
         BE    MOVEPRI                 BRANCH IF REQUEST FOR ONE
*                                         VOLUME
         TM    JFCBCTRI,X'DF'          CHECK FOR A CYLINDER REQUEST
         BC    5,MOVEPRI                BRANCH IF CYLINDERS REQUESTED
         LH    RSIX,DS4DEVSZ
         MH    RSIX,DS4DEVSZ+2         MULTIPLY TIMES THE NUMBER OF
*                                         TRACKS PER CYLINDER
         SR    RTWO,RTWO                CLEAR REG                 23312
         L     RTHREE,DEB+32            FETCH UCB PNTR            23312
         USING UCB,RTHREE               UCB ADDRESSABILITY       Y02130
         CLI   UCBTBYT4,DA3340          TEST IF 3340             Y02130
         BNE   NOT3340                  BRANCH IF NOT            Y02130
         SR    RTWO,RTWO                CLEAR FOR DIVIDE         Y02130
         LH    RTHREE,DS4DEVSZ          NBR OF CYLS ON VOLUME    Y02130
         LA    RFOUR,ONE16              4 ALTERNATE TRACKS       Y02130
         DR    RTWO,RFOUR               FOR EVERY 116 CYLINDERS  Y02130
*                                       SO DIVIDE CYL BY 116,    Y02130
         SLL   RTHREE,K2                MULTIPLY QUOTIENT BY 4   Y02130
*                                       EQUALS TOTAL ALTERNATES  Y02130
SUBTALT  EQU   *                        BRANCH LABEL             Y02130
         SR    RSIX,RTHREE              REDUCE TOTAL BY NUMBER   Y02130
*                                       OF ALTERNATE TRACKS      Y02130
         B     CONT                     CONTINUE CALCULATION     Y02130
NOT3340  EQU   *                        BRANCH LABEL             Y02130
         IC    RTWO,UCBTBYT4            FETCH UNIT TYPE          Y02130
         DROP  RTHREE                                            Y02130
         L     RTHREE,CVTPTR            LOAD CVT ADDRESS         Y02134
         L     RTHREE,CVTZDTAB-CVT(RTHREE)  DEVICE TABLE POINTER Y02134
         IC    RTWO,0(RTWO,RTHREE)      FETCH DEVICE ENTRY OFFSET 23312
         SH    RSIX,12(RTWO,RTHREE)     REDUCE TOTAL TRKS BY NR   23312
*                                       OF ALTERNATE TRACKS
CONT     EQU   *                        BRANCH LABEL             Y02130
         SH    RSIX,ABSTR              CALCULATE QUANTITY TO BE
         STH   RSIX,PRIQTY                OBTAINED ON THIS VOLUME
         CLC   HOWMUCH+K2(K2),PRIQTY    COMPARE AMOUNT NEEDED    Y02080
*                                       WITH SPACE AVAILABLE
         BH    REJOIN                   ALL OF AVAILABLE SPACE    23475
*                                       IS NEEDED
MOVEPRI  EQU   *                        BRANCH LABEL             Y02080
         MVC   PRIQTY,HOWMUCH+K2        AMOUNT WANTED IS LESS    Y02080
*                                       THEN WHATS AVAILABLE,
*                                       ONLY GET WHATS NEEDED
         B     REJOIN
THISLAST EQU   *                                                  23312
PRIMETST CLC   JFCBELNM(8),PRIME        CHECK DSN ELEMENT         23312
         BE    ANOTHER                  BRANCH IF A PRIME AREA IS
*                                          REQUESTED
BLANKTST EQU   *                                                 A38860
         CLC   JFCBELNM(8),BLANKS       CHECK DSN ELEMENT        A38860
         BE    ANOTHER                  BRANCH IF ELEMENT NAME IS BLANK
OVFLWTST EQU   *                                                 S21016
         CLC   JFCBELNM(8),OVFLOW       CHECK ELEMENT NAME ATTACHED TO
*                                          THE DS NAME
         BE    CHEKPREV            BRANCH IF ELEMENT IS OVFLOW   S21016
         CLC   JFCBELNM,INDEX           CHECK DSN ELEMENT       YA03038
         BE    INDEXPRE                 BR IF INDEX             YA03038
INVELMNT EQU   *                                                 S21016
         LA    RFIVE,INVALELM      ELEMENT NAME IS NOT OVFLOW    S21016
         B     ERRORXCT                                          S21016
CHEKPREV EQU   *                                                 S21016
         TM    ISAMSTAT(RTEN),X'02'     CHECK TYPES PREVIOUSLY   A38860
*                                          ALLOCATED (IN DDNAME FIELD)
         BC    1,TEST                   BRANCH IF PRIME PROCEEDS
         B     INVNAME                  NO PRIME REQUEST       @ZM30492
INVOVFL  EQU   *                                                 S21016
         LA    RFIVE,PRIME1ST      INVALID OVFLOW REQUEST ERROR  S21016
         B     ERRORXCT                                          S21016
TEST     EQU   *                                                 A38860
         TM    ISAMSTAT(RTEN),X'04'     CHECK TYPES PREVIOUSLY   A38860
*                                          ALLOCATED (IN DDNAME FIELD)
         BC    1,INVAREA                BRANCH IF OVERFLOW HAS ALREADY
*                                          BEEN REQUESTED
         OC    JFCBDQTY(3),JFCBDQTY     CHECK FOR EMBEDDED INDEX BEING
*                                          REQUESTED
         BC    7,ERROUT                 BRANCH IF EMBEDDED INDEX BEING
*                                          REQUESTED WITH REQUEST FOR
*                                          OVERFLOW
         CLI   TIOEVOLS(RTEN),X'01'     MULTI-VOLUME OVFLOW?     A38860
         BE    SETOVFLW            BRANCH IF NOT MULTI-VOLUME    S21016
         LA    RFIVE,MVOVFLOW      MULTI-VOLUME OVFLOW ERROR     S21016
         B     ERRORXCT                                          S21016
SETOVFLW EQU   *                                                 S21016
         OI    ISAMSTAT(RTEN),X'04'     TURN ON OVERFLOW BIT     A38860
         MVI   TYPE,X'02'               SET TYPE FOR OVFLOW
         B     MOVEFLDS
ANOTHER  EQU   *                                                 A38860
         TM    ISAMSTAT(RTEN),X'02'     CHECK AREA TYPES THAT    A38860
*                                          HAVE BEEN ALLOCATED
         BC    1,INVAREA                BRANCH IF PRIME AREA PREVIOUSLY
*                                          REQUESTED
         OC    JFCBDQTY(3),JFCBDQTY     CHECK FOR AN EMBEDDED INDEX
*                                          REQUEST
         BNZ   DUPLINDX            DUPLICATE INDEX REQUEST ERROR S21016
         MVI   TYPE,X'01'               TURN ON TYPE FOR PRIME
         OI    ISAMSTAT(RTEN),X'02'     TURN ON PRIME BIT        A38860
         B     MOVEFLDS
REJOIN   EQU   *                                                  23312
         LA    RTHREE,NEXTLOAD          SET 2ND LOAD ID          Y02080
*
* BRANCH TO ANOTHER LOAD OF ISAM ALLOCATE
*
XCT      EQU   *                                                  23312
         IECRES LOAD,EXTPR=(RWORK),MODID=(RTHREE),BRANCH=DIRECT  Y02080
*
* CONSTANTS
*
INDEX    DC    C'INDEX   '                                        23312
OVFLOW   DC    C'OVFLOW  '                                        23312
PRIME    DC    C'PRIME'                                           23312
BLANKS   DC    CL8' '                                            A38860
*
* TABLE OF MODULE IDS AND ENTRY POINT ADDRESSES
*
         XCTLTABL ID=(NEXTLOAD,I2,LASTLOAD,I7),SVC=032,BRT=YES,  Y02080X
               LENGTH=                                           Y02080
         EJECT                                                   Y02080
JFCBKEN  DSECT
         IEFJFCBN
         SPACE 3                                                 Y02080
         IECDSECS CVT,UCB,DSAB,EXPAND=YES                        Y02134
DSCBWKAR IECALLWA EP,F4,D1=(1)          ALLOCATE WORK AREA       Y02080
         END
