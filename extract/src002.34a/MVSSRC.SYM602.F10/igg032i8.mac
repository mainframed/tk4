 TITLE 'IGG032I8 - ISAM - NEW VOL DUPL NAME SEARCH'
IGG032I8 CSECT
*
*MODULE NAME = IGG032I8
*
*DESCRIPTIVE NAME = ISAM - NEW VOL DUPL NAME SEARCH
*
*COPYRIGHT = NONE
*
*CHANGE ACTIVITY = SEE BELOW
*
*          VS2 RELEASE 01 DELETIONS
*0000                                                            YM2930
*
*          RELEASE 16 DELETIONS
*1463                                                              AAAA
*          RELEASE 17 DELETIONS
*          RELEASE 18 DELETIONS
*          RELEASE 19 DELETIONS
*0793041000,045600                                               O19117
*          RELEASE 20 DELETIONS
*          RELEASE 21 DELETIONS
*1231003000-003400,006000-006200,015200,016000-016600,029600-    A38860
*1231029800,036000-036200,038800,040200-040400,041140,041400,    A38860
*1231047400,048000,048800,049600-049800,050000,069800-070000,    A38860
*1231081800                                                      A38860
*          RELEASE 21.7 DELETIONS/CHANGES
*0000040860-040880                                              SA53153
*0000064400-065000                                              SA48172
*          RELEASE 22 DELETIONS
*          VS2 RELEASE 02 DELETIONS/CHANGES
*0000000030-000150,000300,000348,000920-001000,002840,004400-    Y02080
*0000004600,005200,005800-005870,006600,016100-016400,022377-    Y02080
*0000022384,023200,025800,027400,028400,032100,033430-033820,    Y02080
*0000034200,034800,035800,036400,037080,037320,040400,040840,    Y02080
*0000040960-040980,041040,041080,041160-041200,046000-046800,    Y02080
*0000047200-047600,048000-048100,059000-059200,060800,062800,    Y02080
*0000065600,067400,069840-070080,070600-085320                   Y02080
*0000022307,022363,031000-032800,050200-050600,052000-052400     Y02134
*0000002000,004600,005200-005400,040940,041060,057200            Y02078
*0000000100,037100,037160-037308                                 Y02082
*0000                                                            Y02144
*
*STATUS CHANGE LEVEL 003
*FUNCTION - MULTI-VOLUME DATA SETS - THIS MODULE READS IN THE DSCB
*           FORMAT 4 DATA FIELD, THE FIRST DSCB FORMAT 5, AND PERFORMS
*           A DUPLICATE NAME SEARCH OF THE VTOC ON SUBSEQUENT VOLUMES.
*           IT ALSO READS IN THE DATA FIELD OF ANY DUPLICATE NAME
*           DSCB FOUND.  IF THE DIRF BIT IS SET IN THE FORMAT 4
*           DSCB, THIS MODULE ISSUES MESSAGE IEC602I WITH CODE=12
*           AND TERMINATES THE SPACE ALLOCATION.
*
*ENTRIES - THE ONLY ENTRY POINT TO THIS MODULE IS IGG032I8.  ENTRY IS
*          FROM MODULE IGG032I7 ONLY.
*
*INPUT - AT ENTRY TO THIS MODULE, REGISTER 10 POINTS TO THE TIOT
*        ENTRY AND REGISTER 13 POINTS TO THE ALLOCATE WORK AREA.
*
*        IF MORE THAN ONE DD STATEMENT IS USED IN REQUESTING SPACE,
*        THE SEVEN LOW-ORDER BYTES OF THE DDNAME FIELD IN ALL BUT THE
*        FIRST ISAM DD ENTRY ARE USED TO CREATE THE FOLLOWING SPECIAL
*        ISAM ALLOCATE INTERFACE:
*
*              BYTE 1
*                   BITS 0 AND 1   00 - ABSOLUTE TRACK REQUEST
*                                  01 - BLANK SPACE REQUEST
*                                  11 - CONTIGUOUS SPACE REQUEST
*
*                   BIT 2          THIS IS THE SECOND DD CARD.
*
*                   BIT 5          OVFLOW HAS BEEN ALLOCATED.
*
*                   BIT 6          PRIME HAS BEEN ALLOCATED.
*
*                   BIT 7          INDEX HAS BEEN ALLOCATED.
*
*              BYTES 2 - 3         CURRENT VOLUME SEQUENCE NUMBER
*                                  FOR SECOND OR THIRD DD CARD
*
*              BYTES 4 - 7         POINTER TO THE FIRST TIOT ENTRY
*                                  FOR THIS REQUEST
*
*OUTPUT - UPON TRANSFERRING TO THE FIRST LOAD, IGG032I1, THE WORK
*         AREA WILL CONTAIN THE FORMAT 4 AND FIRST FORMAT 5 DSCB'S,
*         THE VTOCADR AND DADSMADR, AND THE ADDRESS AND DATA
*         PORTION OF ANY DUPLICATE NAME FORMAT 1 DSCB.
*
*EXTERNAL ROUTINES - SVC'S ISSUED IN MACRO EXPANSIONS
*   EXCP(0) READ FROM OR WRITE TO DIRECT ACCESS DEVICE
*   WAIT(1) WAIT ON EVENT CONTROL BLOCK
*   OTHER SVCS ISSUED - RESERVE(56) ENQ THE VTOC
*                     - WTO(35) WRITE MESSAGE TO OPERATOR
*
*   OTHER MACROS ISSUED - IEFJFCBN - BUILD JFCB
*                       - IECALLWA - ALLOCATE WORK AREA
*                       - IECSDSL1 - BUILD DSCBS
*                       - IECDSECS - EXPAND CVT, RRPL, AND UCB DSECTS
*
*EXITS - NORMAL - BRANCH TO IGG032I1 IF MULTI-VOLUME
*      - ERROR  - BRANCH TO IGG032I7 WITH ERROR CODE IN REGISTER 5
*
*        ERROR CODES THAT CAN BE ISSUED BY IGG032I8:
*
*        04 - DUPLICATE NAME DSCB ON VOLUME
*
*        0C - PERMANENT I/O ERROR
*
*        A0 - ISAM ALLOCATION TERMINATED DUE TO POSSIBLE VTOC ERRORS
*
*TABLES/WORK AREA - DADSM WORK AREA DESCRIBED BY MACRO IECALLWA
*
*              ***************************************
*              *             DADSM TABLE             *
*              ***************************************
*
*              ***************************************
*              *        *         *                  *
*              *  TYPE  *  NO OF  *     USED HOLE    *
*              *  FLAG  * ENTRIES *      COUNTER     *
*              *        *         *                  *
*              ***************************************
*              *                  *                  *
*              *       RTA        *      RTA+1       *
*              *                  *                  *
*              ***************************************
*              *                  *                  *
*              *       RTA        *      RTA+1       *
*              *                  *                  *
*              ***************************************
*              *                  *                  *
*              *       RTA        *      RTA+1       *
*              *                  *                  *
*              ***************************************
*              *                  *                  *
*              *       RTA        *      RTA+1       *
*              *                  *                  *
*              ***************************************
*              *                  *                  *
*              *       RTA        *      RTA+1       *
*              *                  *                  *
*              ***************************************
*
*
*              TYPE FLAG = 02 - BPAM DIRECTORIES REQUESTED.
*
*                        = 40 - USER LABELS REQUESTED
*
*                        = 80 - SET MUST COMPLETE IS ACTIVE
*
*
*              NO OF ENTRIES = NUMBER OF ENTRIES WITHIN DADSM TABLE.
*
*
*              RTA = THE STARTING TRACK OF THE ALLOCATED EXTENT.
*
*
*              RTA+1 = THE STARTING TRACK OF THE NEXT AVAILABLE EXTENT.
*
*ATTRIBUTES - REENTRANT, TRANSIENT
*
*STORAGE - PROGRAM CODE CSECT = LESS THAN 1024 BYTES
*          WORK AREA = AS DEFINED IN THE IECALLWA MACRO
*          RPS WORK AREA = AS DEFINED IN THE IECRPS MACRO
*
*REGISTER USAGE
*              REGISTERS 0-9 WORK REGISTERS
*              REGISTER 10 - TIOT SET UP TABLE POINTER
*              REGISTER 11 - JFCB POINTER
*              REGISTER 12 - BASE REGISTER
*              REGISTER 13 - WORK AREA POINTER
*              REGISTER 14 - SUBROUTINE LINKAGE AND WORK REGISTER
*              REGISTER 15 - WORK REGISTER AND ERROR CODE REGISTER
*
***** REGISTER NAMES *****
RZERO    EQU   0
RONE     EQU   1
RTWO     EQU   2
RTHREE   EQU   3
RFOUR    EQU   4
RFIVE    EQU   5
RSIX     EQU   6
RSEVEN   EQU   7
REIGHT   EQU   8
RNINE    EQU   9
DRBAK    EQU   9
RTEN     EQU   10
RJFCB    EQU   11
RBASE    EQU   12
RWORK    EQU   13
RETURN   EQU   14
RFTEN    EQU   15
*
* OTHER EQUATES
*
TWO      EQU   2                        CONSTANT OF TWO          Y02080
K4       EQU   4                        CONSTANT OF FOUR        SA53153
EIGHT    EQU   8                        CONSTANT OF EIGHT        Y02080
K12      EQU   12                       CONSTANT OF TWELVE       Y02080
UNCHAIN  EQU   X'00'                    NO COMMAND CHAINING      Y02080
WRDATA   EQU   X'05'                    WRITE DATA COMMAND       Y02080
RDDATA   EQU   X'06'                    READ DATA COMMAND        Y02080
SKIP     EQU   X'10'                    SUPPRESS DATA TRANSFER   Y02080
CHAIN    EQU   X'40'                    COMMAND CHAINING BIT     Y02080
DIRFBIT  EQU   X'04'                    VTOC INTERRUPT BIT       O19117
TIOEVOLS EQU   2                        OFFSET TO NBR OF DEVICES A38860
TIOEDDNM EQU   4                        OFFSET TO DDNAME         A38860
ISAMSTAT EQU   5                        OFFSET TO ALLOCATION     A38860
*                                       STATUS                   A38860
VOLSQCTR EQU   6                        OFFSET TO DS1VOLSQ CTR   A38860
TIOTPTR  EQU   8                        OFFSET TO 1ST ENTRY PTR  A38860
UCBENTRY EQU   16                       OFFSET TO 1ST UCB ENTRY  A38860
BLANK    EQU   X'40'                    EBCDIC BLANK             A38860
CSWAD    EQU   8                        CSW OFFSET IN IOB       SA53153
UCBADDR  EQU   32                       DISP. TO UCB ADDR IN DEB Y02078
UUU      EQU   34                       DISP. TO UUU IN MESSAGE  Y02078
VOLSER   EQU   38                       DISP. TO VOLSER IN MSG   Y02078
DIRFERR  EQU   X'A0'                    DIRF BIT SET ERROR CODE  Y02078
*
         BALR  RBASE,0
         USING RELOCCP,RBASE
         USING ALLOCWKA,RWORK           WORK AREA ADDRESSABILITY Y02080
         USING JFCBKEN,RJFCB
*
* THIS SECTION RELOCATES THE CHANNEL PROGRAM FOR READING IN THE DSCB
* FORMAT 4, THE FIRST DSCB FORMAT 5, AND THE DUPLICATE NAME SEARCH OF
* THE VTOC ON THE NEXT VOLUME.
*
RELOCCP  LM    RZERO,RNINE,CHANPROG     LOAD THE FIRST FIVE CCWS
         ALR   RZERO,RWORK              ADD RELOCATION ADDRESS
         ALR   RTWO,RWORK
         ALR   RFOUR,RWORK
         ALR   RSIX,RWORK
         ALR   REIGHT,RWORK
         STM   RZERO,RNINE,CCW1         STORE THE 1ST FIVE CCWS  Y02080
         LM    RZERO,RNINE,CHANPROG+40  LOAD THE NEXT FIVE CCWS
         LA    RFTEN,0(RJFCB)
         ALR   RZERO,RWORK              ADD RELOCATION ADDRESS
         ALR   RTWO,RFTEN               ADD JFCB ADDRESS
         ALR   RFOUR,RWORK              ADD RELOCATION ADDRESS
         ALR   RSIX,RWORK
         ALR   REIGHT,RFTEN             ADD JFCB ADDRESS
         STM   RZERO,RNINE,CCW6         STORE SECOND FIVE CCWS   Y02080
         LM    RZERO,RFIVE,CHANPROG+80  LOAD LAST THREE CCWS
         ALR   RZERO,RWORK              ADD RELOCATION ADDRESS
         ALR   RTWO,RWORK
         ALR   RFOUR,RWORK
         STM   RZERO,RFIVE,CCW11        STORE LAST THREE CCWS    Y02080
*
* THIS SECTION UPDATES THE UCB POINTER IN THE DEB.
*
         L     RSIX,CVTPTR              PICK UP THE CVT POINTER
         USING CVT,RSIX
         LA    RTWO,UCBENTRY(,RTEN)     POINT TO 1ST UCB ENTRY   A38860
         SR    RFIVE,RFIVE
         IC    RFIVE,CTR                PICK UP UCB COUNTER AND
         SLL   RFIVE,2                     MULTIPLY TIMES FOUR TO
         AR    RTWO,RFIVE                  CALCULATE ADDRESS OF THIS
*                                          UCB ENTRY IN TIOT
         L     RTEN,0(,RTWO)            GET UCB ADDRESS          Y02134
         LA    RTEN,0(,RTEN)            CLEAR HIGH BYTE          Y02134
         USING UCB,RTEN
         L     RFTEN,CVTPCNVT           CONVERT ROUTINE ADDRESS
         LA    RONE,DEB                 R1 = DEB ADDRESS
MAINUCB  ST    RTEN,DEB+32              STORE MAIN UCB PTR IN DEB
         L     RZERO,SRTEFSCT           PICK UP THE VTOC TTR0
         MVC   MIELNAME(6),SRTEVOLI     SAVE VOLUME SERIAL NUMBER  AAAA
ZEROCLR  IC    RZERO,ZEROS              CLEAR LOW ORDER BYTE
         MVI   DEB+32,X'C0'             RESTORE DEVICE MODE
         LA    RTWO,SEEK                R2 = ADDRESS OF RESULTS
         STM   RJFCB,RFTEN,IECREGSV+K12  SAVE REGISTERS          Y02080
         LR    RTHREE,RWORK             SAVE WORK AREA POINTER
         BALR  RETURN,RFTEN             GO TO THE CONVERT ROUTINE
         LM    RJFCB,RFTEN,IECREGSV+K12-FIRST(RTHREE)            Y02080
*                                       REESTABLISH WORK AREA ADDRESS
         DROP  RSIX
         DROP  RTEN
         MVC   VTOCADR(5),SEEK+3        PICK UP VTOC AND FIRST DADSM
         LA    RTWO,CCW1                PICK UP 1ST CCW ADDRESS  Y02080
         ST    RTWO,IOB+16              SET UP ADDR OF FIRST CCW A38860
         SR    RONE,RONE                ZERO REGISTER            Y02080
         STH   RONE,ENQAREA+TWO         CLEAR FOR RESERVE MACRO  Y02080
*
* LINK TO THE ENQ ROUTINE TO ENQ THE VTOC ON THE NEXT VOLUME.
*
       RESERVE (MJELNAME,MIELNAME,E,6,SYSTEMS),MF=(E,ENQAREA),   YM3022X
               UCB=DEB+32
*
         OI    DSMADTB2,DSMVTOCR        SET VTOC ENQ'ED SWITCH   Y02144
         L     RONE,IECRRPRM            RECOVERY RTN LIST ADDR   Y02144
         USING RRPLIST,RONE             PARM LIST ADDRESSABILITY Y02144
         ST    RTEN,RRUCBPTR            SAVE UCB ADDRESS         Y02144
         DROP  RONE                                              Y02144
*
         XC    DSCBF1,DSCBF1            ZERO FORMAT 1 DSCB AREA  Y02080
         LA    RETURN,CHECK             SET UP RETURN ADDRESS
*
* EXECUTE THE CHANNEL PROGRAM TO READ IN THE DATA PORTION OF A FORMAT 4
* , THE FIRST FORMAT 5, TO PERFORM A DUPLICATE NAME SEARCH, AND TO
* READ IN THE DATA PORTION OF ANY DUPLICATE NAME DSCB FORMAT 1.
*
EXECUTET EQU   *                                                 A38860
         MVI   ECB,X'00'                CLEAR ECB                A38860
         EXCP  IOB
         WAIT  1,ECB=ECB
         TM    ECB,X'20'                CHECK FOR SUCCESSFUL COMPLETION
         BCR   7,RETURN                 BRANCH IF COMPLETION SUCCESSFUL
PERMERR  EQU   *                                                SA53153
         LA    RFIVE,12                 SET ERROR CODE FOR PERMANENT
*                                          I/O ERROR
ERRXCTL  EQU   *                                                 A38860
         LA    RTWO,ERRORLD             POINT TO ID OF IGG032I7  Y02080
         B     LEAVE
*
CHECK    EQU   *                                                 O19117
         TM    CCW3+K4,CHAIN+SKIP       LAST I/O WAS FOR         Y02080
         BZ    WRDONE                   REWRITE OF F4 DSCB      SA53153
         MVC   CCW9+K4(K4),IOB+CSWAD    SAVE CSW COMMAND ADDR   SA53153
         BO    CHK2                     BRANCH IF LAST I/O WAS  SA53153
*                                       FOR DUP DUP NAME SEARCH SA53153
         XI    DS4VTOCI,DIRFBIT         SET/TEST DIRF BIT        O19117
         TM    DS4VTOCI,DIRFBIT         TEST RESULTS             O19117
         BZ    ISSUEMSG                 BRANCH IF DIRF BIT       Y02078
*                                       WAS PREVIOUSLY SET       Y02078
         MVI   CCW3,WRDATA              SET WRITE DATA CMMD      Y02080
         MVI   CCW3+K4,UNCHAIN          RESET CMMD CHAINING      Y02080
         B     EXECUTET                 GO WRITE BACK F4         O19117
WRDONE   EQU   *                                                 O19117
         MVI   CCW3,RDDATA              RESTORE READ DATA CMMD   Y02080
         MVI   CCW3+K4,CHAIN+SKIP       SET SKIP FLAG TO PREVENT Y02080
*                                       REREAD OF F4
CHK2     EQU   *                                                 O19117
         L     RTEN,ATIOTPTR            LOAD PTR TO DD ENTRY     Y02080
         L     RTWO,CCW9+K4             GET CSW COMMAND ADDRESS SA53153
         LA    RTWO,0(RTWO)             CLEAR HIGH BYTE         SA53153
         LA    RTHREE,CCW9              GET CCW9 ADDRESS        SA53153
         XR    RONE,RONE                PREPARE TO RESTORE CCW9 SA53153
         ST    RONE,CCW9+K4             TO ITS ORIGINAL STATUS  SA53153
         CLR   RTWO,RTHREE              TEST IF DSNAME FOUND    SA53153
         BE    NODSNAME                 BRANCH IF NOT FOUND     SA53153
         CLI   F5OUT,X'F1'              CHECK FOR A FORMAT IDENTIFIER
*                                          FOR A FORMAT 1 DSCB
         BE    SAMENAME                 BRANCH IF A DUPLICATE NAME
*                                          DSCB FORMAT 1 HAS BEEN READ
         B     PERMERR                  BAD VTOC EXIT           SA53153
*
* THIS SECTION TESTS FOR BOS PACK AND SETS THE HIGH WATER MARK TO THE
* END OF THE CYLINDER FOR DUPLICATE NAME SEARCH.
*
NODSNAME EQU   *                                                SA53153
         TM    DS4VTOCI,X'80'           CHECK FOR A SPECIAL TYPE OF
*                                          VOLUME
         BC    8,NOTBOS                 BRANCH IF NOT A SPECIAL VOLUME
         CLC   DS4HPCHR(4),DS4VTOCE+6   CHECK HI WATER MARK BEING
*                                          UPDATED
         BH    UPDATE                   BRANCH IF NOT UPDATED
         CLC   DS4HPCHR+4(1),DS4DEVDT   CHECK FOR HI WATER MARK BEING
*                                          COMPLETELY UPDATED
         BNH   NOTBOS                   BRANCH IF UPDATED
UPDATE   MVC   DS4HPCHR(4),DS4VTOCE+6   UPDATE HI WATER MARK TO POINT
         MVC   DS4HPCHR+4(1),DS4DEVDT      TO THE LAST RECORD,TRACK
*                                          AND CYLINDER OF THE VTOC
         B     EXECUTET
NOTBOS   EQU   *                        BRANCH LABEL             Y02080
         MVC   DADSMADR,DSCBF5-EIGHT    SET UP F5 ADDRESS        Y02080
         MVC   CCW1(96),F5OUT           PICK UP F1 DATA IF SUCH AN F1
*                                       EXISTS AND REALIGN IT    Y02080
         MVC   DS1FMTID(DS1END-DS1FMTID),CCW1  REALIGN F1 DSCB   Y02080
         LA    RTWO,FIRSTLD             POINT TO ID OF IGG032I1  Y02080
*
LEAVE    EQU   *                                                 A38860
         IECRES LOAD,EXTPR=(RWORK),MODID=(RTWO),BRANCH=DIRECT    Y02080
*
* THIS SECTION EXAMINES THE LEGITMACY OF A DUPLICATE NAME FORMAT 1
*
SAMENAME EQU   *                                                 A38860
         CLI   TIOEDDNM(RTEN),BLANK     CHECK DDNAME IN TIOT     A38860
         BNE   DUPLNAME                 BRANCH IF THIS IS THE FIRST DD
*                                          ENTRY - ILLEGAL DUPLICATE
*                                          NAME DSCB FORMAT 1
         L     RNINE,TIOTPTR(,RTEN)     GET ADDR OF 1ST ENTRY    A38860
         L     RSIX,UCBENTRY(,RNINE)    PICK UP UCB ADDRESS      Y02134
         LA    RSIX,0(,RSIX)            CLEAR HIGH BYTE          Y02134
         SR    RFIVE,RFIVE
         IC    RFIVE,CTR                CALCULATE WHICH VOLUME DUPLI-
         SLL   RFIVE,2                     CATE NAME WAS FOUND ON
         AR    RFIVE,RTEN
         LA    RSEVEN,16                INCREMENT TO FIRST UCB
         AR    RFIVE,RSEVEN             PICK UP UCB ENTRY IN SECOND
         L     RSEVEN,0(,RFIVE)         GET UCB ADDRESS          Y02134
         LA    RSEVEN,0(,RSEVEN)        CLEAR HIGH BYTE          Y02134
         CR    RSIX,RSEVEN              COMPARE UCB POINTERS IN TIOT
         BNE   DUPLNAME                 BRANCH IF POINTERS AREN'T THE
*                                          SAME-ILLEGAL DUPLICATE NAME
*                                          FORMAT 1 DSCB
*
* THIS SECTION TAKES THE CCHHR OF THE RECORD BEYOND THE DUPLICATE NAME
* FORMAT 1 DSCB AND CALCULATES THE ADDRESS OF THIS FORMAT 1
*
         SR    RETURN,RETURN            REDUCE THE RECORD NUMBER BY ONE
         IC    RETURN,COUNT+4
         BCT   RETURN,ADJUST            CHECK FOR RECORD NUMBER ZERO -
*                                       BACKING OVER A TRACK BOUNDRY
*                                       BRANCH IF RECORD NUMBER NOT
*                                          ZERO
         IC    RETURN,DS4DEVDT          SET RECORD NUMBER TO MAXIMUM
*                                          NUMBER OF DSCB'S PER TRACK
ADJUST   STC   RETURN,COUNT+4
         MVC   SAVEADR(5),COUNT         SAVE THE ADDR OF THE FORMAT 1
         B     NOTBOS
DUPLNAME LA    RFIVE,4                  SET UP ERROR CODE FOR ILLEGAL
*                                          DUPLICATE NAME DSCB FORMAT 1
         B     ERRXCTL
*
* IF THE DIRF BIT WAS PREVIOUSLY SET IN THE FORMAT 4 DSCB, THIS
* SECTION ISSUES MESSAGE IEC602I WITH CODE=12.
*
ISSUEMSG EQU   *                        BRANCH LABEL             Y02078
         MVC   DSCBF1(MSGEND-ERRMSG),ERRMSG  MOVE IN MESSAGE     Y02078
         L     RTEN,DEB+UCBADDR         LOAD UCB ADDRESS         Y02078
         USING UCB,RTEN                 UCB ADDRESSABILITY       Y02078
         MVC   DSCBF1+UUU(L'UCBNAME),UCBNAME  MOVE IN EBCDIC UCB Y02078
*                                       CHANNEL/UNIT ADDRESS     Y02078
         MVC   DSCBF1+VOLSER(L'SRTEVOLI),SRTEVOLI  MOVE IN VOLID Y02078
         WTO   MF=(E,DSCBF1)            ISSUE ERROR MESSAGE      Y02078
         LA    RFIVE,DIRFERR            LOAD ERROR CODE          Y02078
         B     ERRXCTL                  GO BRANCH TO IGG032I7    Y02078
*
* CHANNEL PROGRAM
*
         DS    0F                       FULLWORD BOUNDARY        Y02078
CHANPROG EQU   *
*CCW1
         DC    X'31'                    SEARCH EQUAL ID FOR VTOC DSCB
         DC    AL3(0+VTOCADR-FIRST)
         DC    X'4000'
         DC    H'5'
*CCW2
         DC    X'08'                    TIC BACK TO SEARCH
         DC    AL3(0+CCW1-FIRST)        TIC TO CCW1              Y02080
ZEROS    DC    F'0'                     FULLWORD OF ZEROS        Y02080
*CCW3
         DC    X'06'                    READ DATA FOR VTOC DSCB
         DC    AL3(0+DS4IDFMT-FIRST)
         DC    X'4000'
         DC    H'96'
*CCW4
         DC    X'9E'                    READ KEY AND DATA FOR F5
         DC    AL3(DSCBF5-8-FIRST)      BUFFER ADDR              Y02080
         DC    X'4000'
         DC    H'148'
*CCW5
         DC    X'F1'                    SEARCH FOR LAST F1 DSCB
         DC    AL3(0+DS4HPCHR-FIRST)
         DC    X'4000'
         DC    H'5'
*CCW6
         DC    X'08'                    TIC TO SEARCH NAME EQUAL
         DC    AL3(0+CCW10-FIRST)       TIC TO CCW10             Y02080
         DC    F'0'
*CCW7
         DC    X'A9'                    SEARCH NAME EQUAL ON LAST F1
         DC    AL3(000000)                 THIS ADDRESS MUST BE ALTERED
         DC    X'6000'
         DC    H'44'
*CCW8
         DC    X'06'                    READ DATA OF THE LAST   SA48172
         DC    AL3(0+F5OUT-FIRST)       FORMAT 1 WITH THE SKIP  SA48172
         DC    X'3000'                  BIT SET IN ORDER TO END SA48172
         DC    H'96'                    THE CHANNEL PROGRAM     SA48172
*CCW9
         DC    X'08'                    TIC TO READ DATA OF DUP NAME
         DC    AL3(0+CCW12-FIRST)       TIC TO CCW12             Y02080
         DC    F'0'
*CCW10
         DC    X'A9'                    SEARCH NAME EQUAL NOT LAST F1
         DC    AL3(000000)                 THIS ADDRESS MUST BE ALTERED
         DC    X'6000'
         DC    H'44'
*CCW11
         DC    X'08'                    TIC BACK TO SEARCH LAST F1
         DC    AL3(0+CCW5-FIRST)        TIC TO CCW5              Y02080
         DC    F'0'
*CCW12
         DC    X'06'                    READ DATA OF DUP NAME DSCB
         DC    AL3(0+F5OUT-FIRST)
         DC    X'4000'
         DC    H'96'
*CCW13
         DC    X'12'                    READ COUNT OF NEXT DSCB FOR ID
         DC    AL3(0+COUNT-FIRST)
         DC    X'2000'
         DC    H'5'
*
* CONSTANTS
*
ERRMSG   WTO   'IEC602I VTOC NOT CONVERTED ON UUU,VOLSER,12',    Y02078X
               ROUTCDE=(4,10),DESC=4,MF=L                        Y02078
MSGEND   EQU   *                        MSG DELIMITER            Y02078
*
* TABLE OF MODULE IDS AND ENTRY POINT ADDRESSES
*
         XCTLTABL ID=(FIRSTLD,I1,ERRORLD,I7),SVC=032,LENGTH=,    Y02080X
               BRT=YES                                           Y02080
         SPACE 2                                                 Y02080
         IECDSECS CVT,RRPL,UCB,EXPAND=YES                        Y02144
JFCBKEN  DSECT
         IEFJFCBN
         EJECT                                                   Y02080
DSCBWKAR IECALLWA EP,F4,D1=(1),ADT=YES  ALLOCATE WORK AREA       Y02144
ENQAREA  EQU   ADSCBF4                  ENQ (RESERVE) PARM LIST  Y02080
MJELNAME EQU   ENQAREA+16               MAJOR ELEMENT NAME       Y02080
         END
