 TITLE 'IGC0003B - DADSM - SPACE ALLOCATION ENTRY POINT'
IGC0003B CSECT
*
*MODULE NAME = IGC0003B
*
*DESCRIPTIVE NAME = DADSM - SPACE ALLOCATION ENTRY POINT
*
*COPYRIGHT = NONE
*
*CHANGE ACTIVITY = SEE BELOW
*
*        VS2 RELEASE 02 DELETIONS/CHANGES
*0000000250-000260,000600,001800-003800,012800-014000,016800,    Y02080
*0000025000,028700-029400,030380-030390,031040-031070,031130-    Y02080
*0000031220,031340-031370,033000,033600,033800-034200,036200,    Y02080
*0000041920-041980,043500-043800,044000,044380-044400,044800-    Y02080
*0000045800,046400,048080-048520,061200-062200,090200-096200,    Y02080
*0000102500-103800,104100,111400-132400,132800-134000,134600     Y02080
*0000011000,039100,086200,088200,104400                          Y02132
*0000008600-008800,010880,046400                                 Y02134
*0000000030-000150,030410-030440                                 Y02078
*0000                                                            Y02082
*0000032600-032800,132400-132800,133600-134400                   Y02144
*0000                                                            Y02072
*0000001800-002800,061200-062200,104600                          YM3997
*0000                                                            YM5371
*0000063400-064000                                               YM6546
*0000034000-034200                                               XM2969
*        RELEASE 21.7 DELETIONS/CHANGES
*0000002800-003800,009400-010600,014000,035200-035600,041955-   SA53147
*0000041985,052200-053200,058200-059200,061200-081200,102500-   SA53147
*0000103000,134800                                              SA53147
*        RELEASE 21 DELETIONS/CHANGES
*1193                                                            S21042
*1193107900                                                      S21042
*1193024600,028800-030000,030400-031600,033800,104200-104400,    S21016
*1193104700-104800,105200                                        S21016
*1193010600-011400,033200-033400,034000,041900,044000,057200,    A42446
*1193068200,083200-086200,103500,113600                          A42446
*1193000441,033800,037800,038200,038600,039000,040200,104850-    A40967
*1193104950                                                      A40967
*1193030470,030950                                               M0705
*0000007200,008000-008200,011200,014000,030230,032400,036400,    A46776
*0000041950,046410-046510,090200,104300-109900                   A46776
*0000044400                                                     SA49351
*          RELEASE 19 DELETIONS
*1772043050-043350,045400-046000,056800-059000,059240-059520,    O19117
*1772062600-063000,065100,065520,065560,065620-065640,065720,    O19117
*1772066000-071800,073300,076660-076720,078800,086200,089400-    O19117
*1772089600,096200-097000,099600                                 O19117
*1772065100-065400                                               M4523
*          RELEASE 20 DELETIONS
*0613000260-000312,000410-000500,001000-005600,009200-011600,    20201
*0613012200-012600,013200-016400,043120-043240,049400-100400,    20201
*0613102600-103800,104500                                        20201
*
*STATUS CHANGE LEVEL 009
*
*FUNCTION/OPERATION: THIS MODULE OBTAINS THE CORE FOR THE WORK AREA   *
*   REQUIRED FOR THIS MODULE AND SUBSEQUENT DADSM AND ISAM ALLOCATE   *
*   MODULES.  IT THEN BUILDS THE DEB/DCB, THE IOB, AND THE ECB.       *
*   IF THE DEVICE HAS THE RPS FEATURE, THE ADDRESS OF THE RPS SIO     *
*   APPENDAGE IGG019EK IS LOCATED, AND A 128 BYTE RPS WORK AREA       *
*   IS OBTAINED.                                                      *
*                                                                     *
*ENTRY POINTS:                                                        *
*        IGC0003B - ENTRY IS MADE FROM TASK SCHEDULER OR UTILITY      *
*   PROGRAMS REQUESTING ALLOCATION OF SPACE ON A DIRECT ACCESS VOLUME.*
*   DADSM IS CALLED BY ISSUEING AN SVC 32.                            *
*                                                                     *
*INPUT: ON ENTRY TO THIS MODULE REGISTER ZERO WILL CONTAIN THE CORE   *
*   ADDRESS OF A JFCB OR A PARTIAL DSCB WHICH WILL CONTAIN THE        *
*   NECESSARY ALLOCATION DATA.  IF THE HIGH ORDER BIT OF REGISTER     *
*   ZERO IS A ZERO, ENTRY WAS MADE FROM THE TASK SCHEDULER AND        *
*   REGISTER ZERO WILL POINT TO A JFCB.  IF THE HIGH ORDER BIT OF     *
*   REGISTER ZERO IS A ONE, ENTRY WAS MADE FROM A UTILITY PROGRAM     *
*   AND REGISTER ZERO WILL POINT TO A PARTIAL FORMAT 1 DSCB.          *
*   REGISTER ONE WILL POINT TO A LIST OF UCB ADDRESSES THE FIRST OF   *
*   WHICH WILL BE USED IN THIS ALLOCATION.  REGISTER TWO WILL POINT   *
*   TO A VARIABLE LENGTH LIST OF DSAB POINTERS.  THE FIRST FULLWORD   *
*   POINTS TO THE DSAB BEING ALLOCATED.  THE SECOND FULLWORD POINTS   *
*   TO THE DSAB REPRESENTING THE FIRST DD CARD.  THE THIRD IS ZERO    *
*   IF THERE IS ONLY ONE DD CARD, OR A POINTER TO THE DSAB FOR THE    *
*   SECOND DD CARD.  THE FOURTH WORD IS ZERO IF THERE IS NO THIRD     *
*   DD CARD, OR A POINTER TO THE DSAB FOR THE THIRD DD CARD, OR       *
*   ABSENT IF THERE IS NO SECOND DD CARD.  THE FIFTH WORD IS ZERO     *
*   IF THERE ARE THREE CONCATENATED DD CARDS, OR ABSENT OTHERWISE.    *
*   IF BIT JFCVRDS IS SET IN THE JFCB, REGISTER 1 POINTS TO A         *
*   FULLWORD CONTAINING THE DEVICE CODE INSTEAD OF A UCB ADDRESS.     *
*                                                                     *
*OUTPUT: REGISTER 4 WILL BE NON-ZERO.                                 *
*   REGISTER 2 (A POINTER TO A DSAB LIST FOR ISAM ALLOCATE) WILL      *
*   BE LOADED FROM THE SVRB AND SAVED IN THE WORK AREA.               *
*   REGISTER 7 WILL POINT TO THE UCB OR TO THE DEVICE CODE.           *
*   REGISTER 11 WILL POINT TO THE JFCB OR PARTIAL F1 DSCB.            *
*   REGISTER 13 WILL POINT TO THE WORK AREA.                          *
*                                                                     *
*EXTERNAL ROUTINES:                                                   *
*        IECRES - GET CORE FOR WORK AREA AND BRANCH TO ANOTHER LOAD   *
*                                                                     *
*EXITS:  BRANCH TO IGG0325A FOR NON-VIO SPACE ALLOCATION.             *
*        BRANCH TO IGG0325I FOR VIO SPACE ALLOCATION.                 *
*                                                                     *
*TABLES/WORK AREAS: DADSM WORK AREA DESCRIBED BY MACRO 'IECALLWA'     *
*                                                                     *
*              ***************************************                *
*              *             DADSM TABLE             *                *
*              ***************************************                *
*                                                                     *
*              ***************************************                *
*              *        *         *                  *                *
*              *  TYPE  *  NO OF  *     USED HOLE    *                *
*              *  FLAG  * ENTRIES *      COUNTER     *                *
*              *        *         *                  *                *
*              ***************************************                *
*              *                  *                  *                *
*              *       RTA        *      RTA+1       *                *
*              *                  *                  *                *
*              ***************************************                *
*              *                  *                  *                *
*              *       RTA        *      RTA+1       *                *
*              *                  *                  *                *
*              ***************************************                *
*              *                  *                  *                *
*              *       RTA        *      RTA+1       *                *
*              *                  *                  *                *
*              ***************************************                *
*              *                  *                  *                *
*              *       RTA        *      RTA+1       *                *
*              *                  *                  *                *
*              ***************************************                *
*              *                  *                  *                *
*              *       RTA        *      RTA+1       *                *
*              *                  *                  *                *
*              ***************************************                *
*                                                                     *
*              TYPEFLG  =  02 - BPAM DIRECTORIES REQUESTED.           *
*                                                                     *
*                       =  40 - USER LABELS REQUESTED                 *
*                                                                     *
*                       =  80 - SET MUST COMPLETE IS ACTIVE           *
*                                                                     *
*              NO OF ENTRIES = NUMBER OF ENTRIES WITHIN DADSM TABLE.  *
*                                                                     *
*              RTA = THE STARTING TRACK OF THE ALLOCATED EXTENT.      *
*                                                                     *
*              RTA+1 = THE STARTING TRACK OF THE NEXT AVAILABLE EXTENT*
*                                                                     *
*ATTRIBUTES: REENTRANT                                                *
*                                                                     *
*NOTES:                                                               *
*   RETURN CODES THAT CAN BE ISSUED BY THE SPACE ALLOCATION FUNCTION. *
*                                                                     *
*   00 - SUCCESSFUL ALLOCATION                                        *
*                                                                     *
*   04 - DUPLICATE NAME DSCB EXISTS                                   *
*                                                                     *
*   08 - NO ROOM AVAILABLE IN THE VTOC                                *
*                                                                     *
*   0C - PERMANENT I/O ERROR                                          *
*                                                                     *
*   10 - REQUESTED ABSOLUTE TRACK NOT AVAILABLE                       *
*                                                                     *
*   14 - REQUESTED QUANTITY NOT AVAILABLE                             *
*                                                                     *
*   18 - AVERAGE RECORD LENGTH GREATER THAN 65,535 BYTES              *
*                                                                     *
*   1C - DSORG OR DISP WRONG IN ISAM INDEX REQUEST                    *
*                                                                     *
*   20 - NO PRIME AREA REQUESTED FOR ISAM                             *
*                                                                     *
*   24 - ISAM PRIME AREA MUST BE REQUESTED BEFORE OVFLOW              *
*                                                                     *
*   28 - SPACE PARAMETER WRONG - MUST BE ON CYLINDER BOUNDARY         *
*                                                                     *
*   2C - ISAM DSNAME ELEMENT DUPLICATION                              *
*                                                                     *
*   34 - INVALID JFCB OR PARTIAL DSCB POINTER                         *
*                                                                     *
*   38 - NOT ENOUGH SPACE ON VOLUME FOR DIRECTORY                     *
*                                                                     *
*   40 - INVALID USER LABEL REQUEST                                   *
*                                                                     *
*   4C - NO SPACE PARAMETER GIVEN FOR A NEW DATA SET OR               *
*        ZERO SPACE REQUESTED AT ABSOLUTE TRACK ZERO                  *
*                                                                     *
*   50 - INVALID REQUEST FOR ISAM INDEX                               *
*                                                                     *
*   54 - ISAM MULTIVOLUME INDEX NOT ALLOWED                           *
*                                                                     *
*   58 - ISAM DSNAME ELEMENT INVALID                                  *
*                                                                     *
*   5C - ISAM MULTIVOLUME OVFLOW REQUEST NOT ALLOWED                  *
*                                                                     *
*   60 - ABSTR AND CYL REQUESTS CONFLICT                              *
*                                                                     *
*   64 - CYL AND CONTIG REQUESTS CONFLICT                             *
*                                                                     *
*   68 - INVALID SPACE SUBPARAMETER                                   *
*                                                                     *
*   6C - PRIMARY SPACE REQUEST FOR AN ISAM DATA SET IS ZERO OR        *
*        PRIMARY SPACE IS ZERO FOR AN ABSTR REQUEST                   *
*                                                                     *
*   70 - ISAM INDEX REQUEST DUPLICATION                               *
*                                                                     *
*   80 - DIRECTORY SPACE REQUESTED IS LARGER THAN PRIMARY SPACE       *
*                                                                     *
*   84 - SPACE REQUEST MUST BE ABSTR FOR A DOS VOLUME                 *
*                                                                     *
*   8C - INDEX MUST BE REQUESTED BEFORE PRIME AREA                    *
*                                                                     *
*   90 - INVALID THIRD DD CARD FOR ISAM ALLOCATION                    *
*                                                                     *
*   94 - OVERLAPPING EXTENTS IN THE VTOC                              *
*                                                                     *
*   98 - OVERLAPPING DOS SPLIT CYLINDER EXTENTS IN THE VTOC           *
*                                                                     *
*   9C - DADSM ALLOCATION TERMINATED DUE TO POSSIBLE VTOC ERRORS      *
*                                                                     *
*   A0 - ISAM ALLOCATION TERMINATED DUE TO POSSIBLE VTOC ERRORS       *
*                                                                     *
*   A4 - ALLOCATION TERMINATED DUE TO DOS STACKED PACK FORMAT         *
*                                                                     *
*OTHER MACROS USED:                                                   *
*   IECSDSL1 - BUILD DSCB'S                                           *
*   IECDSECS - EXPAND THE CVT, RRPL, AND UCB DSECTS                   *
*   IECRPS - INITIALIZE FOR RPS PROCESSING                            *
*   IECALLWA - ALLOCATE WORK AREA EXPANSION                           *
*                                                                     *
*STORAGE: PROGRAM CODE - LESS THAN 1024 BYTES                         *
*   WORK AREA - AS DEFINED IN THE IECALLWA MACRO                 XM2969
*   RPS WORK AREA - AS DEFINED IN THE IECRPS MACRO               XM2969
*
*REGISTER USAGE-
*
         BALR  BASEREG,0
         USING BEGINA,BASEREG
         USING ALLOCWKA,RWKAREA         WORK AREA ADDRESSABILITY Y02080
         USING JFCB,RJFCBPTR            JFCB ADDRESSABILITY      Y02132
         USING UCB,RUCBPTRL
         USING CVT,RCVT
*
REGZERO  EQU   0
REGONE   EQU   1
REGTWO   EQU   2
REGTHREE EQU   3
REGFOUR  EQU   4
REGFIVE  EQU   5
REGSIX   EQU   6
REGSEVEN EQU   7
RUCBPTRL EQU   7                        UCB POINTER
REGEIGHT EQU   8                        WORK REGISTER
RERRPASS EQU   8                        ERROR PASS REGISTER
REGNINE  EQU   9                        WORK REGISTER
REGTEN   EQU   10                       WORK REGISTER
RJFCBPTR EQU   11                       JFCB POINTER
BASEREG  EQU   12                       BASE REGISTER
RWKAREA  EQU   13                       WORK AREA POINTER
RBAK     EQU   14
RCVT     EQU   15
REGFTEN  EQU   15                       WORK REGISTER
*
* OTHER EQUATES
*
SCHEDKEY EQU   1                        SCHEDULER KEY            Y02132
ECBADDR  EQU   4                        OFFSET TO ECB PTR IN IOB Y02132
K4       EQU   4                        CONSTANT OF 4            Y02080
DMKEY    EQU   5                        DATA MANAGEMENT KEY      Y02132
CHNPGADR EQU   16                       OFFSET TO CHAN PROG ADDR Y02132
UNRELATD EQU   X'02'                    UNRELATED FLAG IN IOB    Y02132
RPSMSK   EQU   X'10'                    TESTS RPS BIT IN UCB     20201
SVRBREG2 EQU   40                       OFFSET TO REGISTER 2     Y02134
*
* * * * * * * * * * * * *
*
*
* * * * * * * * * * * * *
*
BEGINA   EQU   *                                                 O19117
         LR    RJFCBPTR,REGZERO         POSITION JFCB POINTER    O19117
         LA    REGTEN,DMKEY             INITIALIZE FOR DM KEY    Y02132
         LTR   RJFCBPTR,RJFCBPTR        TEST FOR PARTIAL DSCB    Y02132
         BM    LOADUCB                  BRANCH IF YES            Y02132
         TM    JFCFLGS1,JFCVRDS         TEST FOR VIO DATA SET    Y02132
         BNO   LOADUCB                  BRANCH IF NOT VIO D/S    Y02132
         LR    REGSEVEN,REGONE          PTR TO DEVICE CODE       Y02132
         LA    REGTEN,SCHEDKEY          OBTAIN CORE IN SCHED KEY Y02132
         B     OBTNWKA                  GO GET THE WORK AREA     Y02132
LOADUCB  EQU   *                         BRANCH LABEL            Y02132
         L     REGSEVEN,0(REGONE)        GET UCB POINTER
OBTNWKA  EQU   *                         BRANCH LABEL            Y02132
         L     REGTWO,SVRBREG2(REGFIVE)  LOAD DSAB LIST ADDRESS  Y02134
*
* THIS SECTION OBTAINS THE ALLOCATE WORK AREA.
*
         IECRES GET,PREFIX=FIRST,LV=AWALNGTH,KEY=(REGTEN),       Y02132X
               ID=ALWA,STM=(REGTWO,BASEREG,96(REGFIVE))          Y02080
         LR    RWKAREA,REGONE           PICK UP WORKAREA POINTER
         MODESET EXTKEY=ZERO            RETURN TO KEY ZERO AFTER Y02082X
                                        ISSUING THE IECRES GET   Y02082
*
         IECRES LOAD,EXTPR=(RWKAREA),MODNM=LOAD1,BRANCH=NO       Y02080X
                                        LET OPTIONAL TRACE PRINT Y02080X
                                        THIS MODULE'S NAME       Y02080
*
         L     REGONE,IECRRPRM          RECOVERY RTN LIST ADDR   Y02144
         USING RRPLIST,REGONE           PARM LIST ADDRESSABILITY Y02144
         OI    RRFUNCTN,RRFALLOC        ALLOCATE FUNCTION BIT    Y02144
         DROP  REGONE                                            Y02144
*
         ST    REGTWO,ADSABLST          SAVE REGISTER 2 FOR ISAM Y02134
         ST    RJFCBPTR,AJFCBPTR        SAVE JFCB ADDRESS        Y02080
*
* THIS SECTION BUILDS A DEB
*
         MVI   DEB+16,X'01'             SET NUMBER OF EXTENTS = 1
         LA    REGONE,DCB               LOAD POINTER TO DCB
         L     RCVT,CVTPTR              LOAD COMM VECT TABLE BASE
         L     REGTWO,CVTXAPG           GET IOS APPENDAGE TABLE ADDR
         LR    REGTHREE,REGSEVEN        LOAD UCB POINTER
         SR    REGFOUR,REGFOUR          LOAD BB AND START CC
MAINUCB  LM    REGFIVE,REGSIX,EXTCON1   LOAD START HH,END CCHH AND TRKS
         STM   REGONE,REGSIX,DEB+24     STORE IN DEB
         MVI   DEB+24,X'0F'             INSERT DEB ID
         MVI   DEB+28,X'04'             INSERT EXTENT SCALE
         MVI   DEB+32,X'C0'             INSERT DEVICE MODE
*
* THIS SECTION BUILDS THE DCB, IOB, AND ECB.
*
         LA    REGONE,DEB               DEB ADDRESS              Y02132
         ST    REGONE,DEBPTR            PUT DEB ADDRESS IN DCB   Y02132
         LA    REGTWO,ECB               ECB ADDRESS              Y02132
         ST    REGTWO,IOB+ECBADDR       PUT ECB ADDRESS IN IOB   Y02132
         OI    IOB,UNRELATD             SET UNRELATED BIT IN IOB Y02132
         LA    REGTWO,CCW1              PUT IN CHAN PROG POINTER Y02132
         LA    REGTHREE,DCB             PUT DCB POINTER IN IOB   Y02132
         STM   REGTWO,REGTHREE,IOB+CHNPGADR  STORE IN IOB        Y02132
         LA    REGFOUR,VIOLOAD          INIT. BRANCH REGISTER    Y02132
         LTR   RJFCBPTR,RJFCBPTR        TEST FOR PARTIAL DSCB    Y02132
         BM    RPSTEST                  BRANCH IF YES            Y02132
         TM    JFCFLGS1,JFCVRDS         TEST FOR VIO DATA SET    Y02132
         BO    LOADRTN                  BRANCH IF VIO D/S        Y02132
*
***********************************************************************
*   TEST FOR THE RPS FEATURE AND ISSUE THE IECRPS MACRO               *
***********************************************************************
*
RPSTEST  EQU   *                        BRANCH LABEL             Y02132
         TM    UCBTBYT2,RPSMSK          DOES DEVICE HAVE RPS     20201
         BNO   NONRPS                   BRANCH IF NOT            A42446
         LA    REGFOUR,DEB              GET DEB'S ADDRESS        20201
         IECRPS RDEB=REGFOUR,WKREG1=REGNINE,EXTPR=RWKAREA        YM3997X
                                        INITIALIZE FOR RPS       YM3997X
                                        PROCESSING              SA53147
         MVC   WTGMODNM,LOAD1           RESTORE MODULE NAME      YM5371
*
NONRPS   EQU   *                                                 A42446
         LA    REGFOUR,LOAD2            NONZERO VALUE FOR REG 4  Y02132
LOADRTN  EQU   *                        BRANCH LABEL             Y02132
       IECRES LOAD,EXTPR=(RWKAREA),MODNM=(REGFOUR),BRANCH=DIRECT Y02132
*
* * * * * * * * * * * * *
*
***   CONSTANTS
*
* * * * * * * * * * * * *
*
         DS    0F
EXTCON1  DC    X'0000'                  BEGINNING CCHH FOR DEB   A40967
ENABLE   DC    X'FFFFFFFF7FFF'          ENDING CCHH FOR DEB      A40967
*
*** TABLE OF MODULE NAMES AND ENTRY POINT ADDRESSES
*
         XCTLTABL ID=(LOAD1,IGC0003B,LOAD2,IGG0325A,VIOLOAD,     Y02132X
               IGG0325I,RPS19EK,IGG019EK),SVC=032,LENGTH=,       YM3997X
               BRT=YES                                           YM3997
         SPACE 2                                                 Y02144
         IECDSECS CVT,RRPL,UCB,EXPAND=YES                        Y02144
*
* * * * * * * * * * * * *
*
***   WORKAREA
*
* * * * * * * * * * * * *
*
DSCBWKAR IECALLWA EP,ADT=YES            ALLOCATE WORK AREA       Y02144
         EJECT                                                   Y02080
JFCB     DSECT                          JFCB DSECT               Y02132
         IEFJFCBN                                                Y02132
         END   IGC0003B                                         SA53147
