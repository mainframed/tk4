 TITLE 'IGG032I2 - ISAM ALLOCATE - FORMAT 5 SEARCH'
IGG032I2 CSECT
*
*MODULE NAME = IGG032I2
*
*DESCRIPTIVE NAME = ISAM ALLOCATE - FORMAT 5 SEARCH
*
*COPYRIGHT = NONE
*
*CHANGE ACTIVITY = SEE BELOW
*
*          RELEASE 18 DELETIONS                                       *
*2272                                                             24988
*          RELEASE 19 DELETIONS                                       *
*1774089400                                                      A27089
*1774089400                                                      A27089
*          RELEASE 20 DELETIONS                                       *
*3537                                                            A36850
*          RELEASE 21 DELETIONS                                       *
*1203000400,017000,024500-024600,025456-025463,025477-025505,    S21016
*1203025528-025530,025624,040400,041600-041800,042800,048200     S21016
*1203003200,018000-018400,028800-029000,042000-042200,052800-    A38860
*1203053800,089800-090000,100800                                 A38860
*          RELEASE 22 DELETIONS                                       *
*          VS2 RELEASE 02 DELETIONS/CHANGES                           *
*0000000030-000150,000670-000710,002400,003200,005800,006600,    Y02080
*0000008600,017600,018000-018200,024060-024200,025200,025512,    Y02080
*0000027200,028400,040000,042200,052400,053000,053400-053600,    Y02080
*0000054400-054800,055400,060600,064800-065000,066400,069700,    Y02080
*0000070300,087400,089880-090400,090800,091200-103000            Y02080
*
*STATUS CHANGE LEVEL 006
*
*FUNCTION - THE REQUEST IS EXAMINED AND THE FORMAT 5 IS SEARCHED
*           ACCORDING TO ABSOLUTE TRACK, OR AVAILABLE CYLINDERS
*           DEPENDING UPON THIS REQUEST.  A DADSM TABLE IS BUILT
*           ACCORDING TO ASCENDING RTA'S.
*
*ENTRIES - THE ONLY ENTRY POINT TO THIS MODULE IS IGG032I2. ENTRY IS
*          MADE FROM THE FIRST LOAD OF ISAM ALLOCATE VIA A BRANCH.
*
*
*INPUT - AT ENTRY TO THIS MODULE REGISTER 11 POINTS TO A JFCB, REGISTER
*        13 POINTS TO THE ALLOCATE WORK AREA WHICH CONTAINS THE ADDRESS
*        OF A FORMAT 1 IF ONE EXISTS, VTOCADR, AND DADSMADR.
*        REGISTER 9 CONTAINS THE QUANTITY TO BE ALLOCATED,  THE FIRST
*        FORMAT 5 IS IN THE WORK AREA.
*
*OUTPUT - UPON TRANSFERRING TO THE NEXT MODULE, IGG032I3, THE WORK AREA
*         WILL CONTAIN A SORTED DADSM TABLE, THE VTOCADR, AND DADSMADR.
*
*
*EXTERNAL ROUTINES - THE FOLLOWING ARE SUPERVISOR CALLS ISSUED IN THE
*   MACRO EXPANSIONS
*   EXCP(0) READ FROM OR WRITE TO DIRECT ACCESS DEVICE
*   WAIT(1) WAIT ON EVENT CONTROL BLOCK
*   OTHER MACROS ISSUED - IEFJFCBN - BUILDS JFCB
*                         IECALLWA - ALLOCATE WORK AREA EXPANSION
*                         IECSDSL1 - BUILDS DSCBS
*                         CVT - BUILD CVT
*
*EXITS - NORMAL - BRANCH TO MODULE IGG032I3
*      - ERROR - ERROR CODES RETURNED TO MODULE IGG032I7 IN REG 5
*
*              0C   PERMANENT I/O ERROR
*
*              10   ABSOLUTE TRACK NOT AVAILABLE
*
*              14   SPACE REQUESTED NOT AVAILABLE
*
*              28   SPACE MUST BE ON CYLINDER BOUNDARY
*
*              6C   PRIMARY SPACE REQUEST IS ZERO
*
*TABLES/WORK AREAS - DADSM WORK AREA DESCRIBED BY MACRO IECALLWA
*              ***************************************
*              *             DADSM TABLE             *
*              ***************************************
*
*              ***************************************
*              *        *         *                  *
*              *  TYPE  *  NO OF  *     USED HOLE    *
*              *  FLAG  * ENTRIES *      COUNTER     *
*              *        *         *                  *
*              ***************************************
*              *                  *                  *
*              *       RTA        *      RTA+1       *
*              *                  *                  *
*              ***************************************
*              *                  *                  *
*              *       RTA        *      RTA+1       *
*              *                  *                  *
*              ***************************************
*              *                  *                  *
*              *       RTA        *      RTA+1       *
*              *                  *                  *
*              ***************************************
*              *                  *                  *
*              *       RTA        *      RTA+1       *
*              *                  *                  *
*              ***************************************
*              *                  *                  *
*              *       RTA        *      RTA+1       *
*              *                  *                  *
*              ***************************************
*
*
*              TYPE FLAG = 02 - BPAM DIRECTORIES REQUESTED.
*
*                        = 40 - USER LABELS REQUESTED
*
*                        = 80 - SET MUST COMPLETE IS ACTIVE
*
*
*              NO OF ENTRIES = NUMBER OF ENTRIES WITHIN DADSM TABLE.
*
*
*              RTA = THE STARTING TRACK OF THE ALLOCATED EXTENT.
*
*
*              RTA+1 = THE STARTING TRACK OF THE NEXT AVAILABLE EXTENT.
*
*
*ATTRIBUTES - REENTRANT
*
*STORAGE - PROGRAM CODE CSECT = LESS THAN 2048 BYTES
*          WORK AREA = AS DEFINED IN THE IECALLWA MACRO
*          RPS WORK AREA = AS DEFINED IN THE IECRPS MACRO
*
*
*REGISTER USAGE
*              REGISTERS 0-9 WORK REGISTERS
*              REGISTER 10 - TIOT SET UP TABLE POINTER
*              REGISTER 11 - JFCB POINTER
*              REGISTER 12 - BASE REGISTER
*              REGISTER 13 - WORK AREA POINTER
*              REGISTER 14 SUBROUTINE LINKAGE AND WORK REGISTER
*              REGISTER 15 WORK REGISTER
*
***** REGISTER NAMES *****
RZERO    EQU   0
RONE     EQU   1
RTWO     EQU   2
RTHREE   EQU   3
RFOUR    EQU   4
RFIVE    EQU   5
RSIX     EQU   6
RSEVEN   EQU   7
REIGHT   EQU   8
RNINE    EQU   9
RTEN     EQU   10
RJFCB    EQU   11
RBASE    EQU   12
RWORK    EQU   13
RETURN   EQU   14
RFTEN    EQU   15
*
* OTHER EQUATES
*
ZERO     EQU   0                   CONSTANT OF ZERO              Y02080
THREE    EQU   3                   CONSTANT OF THREE             Y02080
FIVE     EQU   5                   CONSTANT OF FIVE              Y02080
HELDQTY  EQU   X'FF'               HELD QUANTITY INDICATOR       Y02080
NODSCBS  EQU   X'08'               NO UNUSED DSCB'S AVAILABLE    S21016
IOERRCDE EQU   X'0C'               I/O ERROR CODE                S21016
NOTAVAIL EQU   X'10'               ABSOLUTE TRACK NOT AVAILABLE  S21016
NOTENUFF EQU   X'14'               SPACE REQUESTED NOT AVAILABLE S21016
BOUNDERR EQU   X'28'               BOUNDARY ERROR CODE           S21016
ZEROSPCE EQU   X'6C'               PRIMARY SPACE REQUEST IS ZERO S21016
*
         BALR  RBASE,0
         USING ALPA,RBASE
         USING ALLOCWKA,RWORK           WORK AREA ADDRESSABILITY Y02080
         USING JFCBKEN,RJFCB
*
* THIS SECTION CHECKS FOR ENOUGH ROOM IN THE VTOC
*
ALPA     LA    RTHREE,4                 COMPARE THE NUMBER OF HOLES
         CH    RTHREE,DS4DSREC             AVAILABLE WITH 4
         BNH   FINE                     BRANCH IF 4 OR MORE HOLES ARE
*                                       AVAILABLE
         LA    RFIVE,NODSCBS            LOAD ERROR CODE          S21016
         B     FREEUP
FINE     EQU   *                        BRANCH LABEL             Y02080
         XC    PDLIST,PDLIST            CLEAR PUSHDOWN LIST      Y02080
         XC    DADSMTBL,DADSMTBL        CLEAR DADSM EXTENT TABLE Y02080
         SR    REIGHT,REIGHT
         LH    RNINE,PRIQTY             PICK UP PRIMARY QUANTITY
         LTR   RNINE,RNINE              ZERO QUANTITY REQUEST?   S21016
         BP    GETINDEX                 BRANCH IF NOT            S21016
         LA    RFIVE,ZEROSPCE           LOAD ERROR CODE          S21016
         B     FREEUP                                            S21016
GETINDEX EQU   *                                                 S21016
         LR    RSIX,REIGHT
         LH    RSEVEN,DIRQTY            PICK UP EMBEDDED INDEX
         TM    JFCBCTRI,X'DF'           CHECK THE REQUEST TYPE FIELD
         BC    5,NOTHERE                BRANCH IF REQUEST FOR CYLINDERS
*
* THIS SECTION CHECKS FOR ABSOLUTE TRACK REQUEST BEING ONE OF AN
* INTEGRAL NUMBER OF CYLINDERS
*
         LH    RFIVE,DS4DEVSZ+2         DIVIDE THE PRIMARY QTY BY THE
         DR    REIGHT,RFIVE                NUMBER OF TRACKS/CYLINDERS
         LTR   REIGHT,REIGHT            TEST REMAINDER FOR ZERO
         BZ    NONEHERE                 BRANCH IF AN INTEGRAL NUMBER OF
*                                          CYLINDERS
INVSPACE EQU   *                                                 S21016
         LA    RFIVE,BOUNDERR           NOT ON CYLINDER          S21016
*                                       BOUNDARIES               S21016
         B     FREEUP                                            S21016
NONEHERE LR    RTWO,RNINE
         LH    RNINE,ABSTR              TEST FOR CYLINDER ALLIGNMENT
         DR    REIGHT,RFIVE             DIVIDE ABSOLUTE TRACK REQUESTED
*                                          BY TRACKS/CYLINDER
         LTR   REIGHT,REIGHT            TEST REMAINDER FOR ZERO
         BC    7,INVSPACE               BRANCH IF NOT AN INTEGRAL
*                                          NUMBER OF CYLINDERS
         LR    RNINE,RTWO
         LTR   RSEVEN,RSEVEN            CHECK FOR PRESENCE OF EMBEDDED
*                                          INDEX QUANTITY
         BZ    NOTHERE                  BRANCH IF THERE IS NO EMBEDDED
*                                          INDEX QUANTITY
         DR    RSIX,RFIVE               DIVIDE EMBEDDED INDEX QUANTITY
*                                          BY TRACKS/CYLINDER
         LTR   RSIX,RSIX                CHECK REMAINDER FOR ZERO
         BC    7,INVSPACE               BRANCH IF NOT AN INTEGRAL
*                                          NUMBER OF CYLINDERS
NOTHERE  AR    RNINE,RSEVEN             ADD TO PRIMARY QUANTITY THE
*                                          EMBEDDED INDEX QUANTITY
         TM    DS4VTOCI,X'80'           THE FORMAT 4 IS CHECKED FOR AN
*                                          INDICATION THAT THE VTOC IS
*                                          NOT IN THE EXPECTED FORM.
*                                          (SUCH AS A VOLUME INITIAL-
*                                           IZED BY BOS)
         BC    1,GOODQTY                BRANCH IF VTOC NOT IN THE
*                                          EXPECTED CONDITION
*
* THIS SECTION RELOCATES THE CHANNEL PROGRAM TO READ IN THE F5
*
GOREAD   EQU   *                        BRANCH LABEL             Y02080
         MVC   COUNT,DADSMADR           MOVE IN FIRST F5 CCHHR   Y02080
         LM    RZERO,RFIVE,CHANPROG    PICK UP FIRST 3 CCW'S
         ALR   RZERO,RWORK
         ALR   RTWO,RWORK
         ALR   RFOUR,RWORK
         STM   RZERO,RFIVE,CCW1        STORE FIRST 3 CCW'S
         MVC   SEEK+THREE(FIVE),COUNT   MOVE CCHHR TO IOB SEEK   Y02080
         LA    RFOUR,CCW1               UPDATE IOB TO POINT TO THE
         ST    RFOUR,IOB+16             FIRST CCW TO BE EXECUTED A38860
* THIS SECTION SETS UP FOR THE SEARCH FOR AVAILABLE EXTENTS
SUCCIO   MVC   DS5FMTID(90),DS5FMTID+1  TAKE OUT 1 BYTE FORMAT ID
         LA    RONE,26                  SET UP ENTRY COUNTER
         LA    RTWO,DS5AVEXT            SET UP ENTRY POINTER
         XC    PDLIST(4),PDLIST
         TM    JFCBCTRI,X'DF'           TEST FOR ABSOLUTE TRACK OR
*                                          CYLINDER REQUEST
         BC    4,CYLREQ                 BRANCH IF REQUEST FOR CYLINDERS
COMPRTA  CLC   ABSTR(2),0(RTWO)         CHECK FOR AN RTA HIGHER THAN
*                                          SEARCH RTA
         BNH   STOPHERE                 BRANCH IF THIS RTA IS HIGHER OR
*                                          EQUAL TO THE REQUEST
         OC    5(5,RTWO),5(RTWO)        CHECK FOR THE LAST ENTRY IN THE
*                                          FORMAT 5
         BE    CHECKOUT                 BRANCH IF THIS THE LAST F5
*                                          ENTRY
         BCTR  RONE,0                   DECREMENT ENTRY POINTER
         LTR   RONE,RONE                TEST THE ENTRY NUMBER FOR THE
*                                          LAST OF 26 F5 ENTRIES
         BZ    CHECKOUT                 BRANCH IF LAST FORMAT 5 ENTRY
         LA    RTWO,5(RTWO)             INCREMENT ENTRY POINTER
         B     COMPRTA
* THIS SECTION CHECKS FOR THE LAST EXTENT BEING THE DESIRED ONE
CHECKOUT MVC   PDLIST(5),0(RTWO)        CHECK FOR END RTA GREATER THAN
         LH    RFIVE,PDLIST+2              RTA LOOKING FOR
         MH    RFIVE,DS4DEVSZ+2         MULTIPLY THE NUMBER OF
*                                          CYLINDERS BY TRACKS PER
*                                          CYLINDER
         SR    RFOUR,RFOUR
         IC    RFOUR,PDLIST+4
         AR    RFOUR,RFIVE              ADD IN THE NUMBER OF EXCESS
*                                          TRACKS
         LH    RFIVE,PDLIST
         AR    RFOUR,RFIVE
         CH    RFOUR,ABSTR              CHECK ENDING RTA
         BNH   GOCHECK                  BRANCH IF THIS RTA IS NOT
*                                          HIGHER THAN THE REQUEST
         SH    RFOUR,ABSTR              CALCULATE DIFFERENCE BETWEEN
         LR    RFIVE,RFOUR                 RTA'S IN CYLINDERS
         SR    RFOUR,RFOUR
         LH    RSIX,DS4DEVSZ+2
         DR    RFOUR,RSIX               DIVIDE DIFFERENCE BY NUMBER OF
*                                          TRACKS PER CYLINDER
         CR    RFIVE,RNINE              COMPARE AVAILABLE QUANTITY TO
*                                          THAT REQUESTED
         BNL   GOODQTY                  BRANCH IF QUANTITY AVAILABLE IS
*                                          SUFFICIENT
         B     QTYNOTAV                 QUANTITY AVAILABLE IS NOT AS
*                                          NUCH AS IS NEEDED
GOCHECK  OC    DS5PTRDS(5),DS5PTRDS     CHECK THE CHAIN ADDRESS FIELD
*                                          OF THE FORMAT 5 DSCB
         BZ    RTANOT                   BRANCH IF THIS THE LAST F5
* THIS SECTION UPDATES THE SEARCH ADDRESS OF THE F5
UPDATESK MVC   SEEK+3(5),DS5PTRDS       UPDATE SEEK ADDRESS
         MVC   COUNT,DS5PTRDS           UPDATE F5 SEARCH ADDRESS Y02080
* EXECUTE THE CHANNEL PROGRAM TO READ IN A DSCB FORMAT 5
         MVI   ECB,X'00'                ZERO FIRST BYTE OF ECB   S21016
         EXCP  IOB
         WAIT  1,ECB=ECB
         TM    ECB,X'20'               CHECK THE ECB FOR SUCCESSFUL
*                                         COMPLETION
         BC    1,SUCCIO                 BRANCH IF SUCCESSFUL COMPLETION
IOERROR  EQU   *                                                 S21016
         LA    RFIVE,IOERRCDE           SET I/O ERROR CODE       S21016
FREEUP   EQU   *                                                 A38860
         LA    RTWO,ERRORLD             POINT TO ID OF IGG032I7  Y02080
         B     XCTLSKIP
* THIS SECTION SETS THE ERROR CODE FOR REQUESTED RTA NOT AVAILABLE
RTANOT   EQU   *                                                 S21016
         LA    RFIVE,NOTAVAIL           RTA NOT AVAILABLE ERROR  S21016
         B     FREEUP
* THIS SECTION CHECKS FOR THIS EXTENT HAVING QUANTITY AND RTA DESIRED
STOPHERE BE    CHECKQTY                 BRANCH IF RTA IS EQUAL TO THE
*                                          REQUESTED RTA
         LA    RZERO,DS5AVEXT           PICK UP ADDRESS OF 1ST ENTRY
*                                          IN F5
         CR    RZERO,RTWO               CHECK FOR FIRST F5 ENTRY
         BE    RTANOT                   BRANCH IF THIS THE FIRST F5
*                                          ENTRY - RTA NOT AVAILABLE
         LA    RTHREE,5                 BACK UP ONE ENTRY
         SR    RTWO,RTHREE
*    THIS IS THE FORMULA FOR THE FOLLOWING CALCULATIONS            1227
*                  YY - ( ABSTR - XX ) / DS4DEVSZ                  1227
         MVC   PDLIST(5),0(RTWO)   PICK UP NEXT ENTRY              1227
         LH    RFIVE,PDLIST        GET STARTING TT                 1227
         LH    RSEVEN,ABSTR        GET ABSTR REQUESTED             1227
         SR    RSEVEN,RFIVE        OBTAIN DIFFERENCE               1227
         SR    RSIX,RSIX           ZERO REGISTER                   1227
         LH    REIGHT,DS4DEVSZ+2   PICK UP = TRACKS/CYL            1227
         DR    RSIX,REIGHT                                         1227
         LH    RSIX,PDLIST+2
         SR    RSIX,RSEVEN
         LTR   RSIX,RSIX           TEST FOR RTA NOT AVAILABLE    A36850
         BNP   RTANOT              BRANCH IF RTA NOT AVAILABLE   A36850
         CR    RSIX,RNINE               CHECK FOR ENOUGH QUANTITY
         BNL   GOODQTY                  BRANCH IF QUANTITY AVAILABLE
*                                          IS SUFFICIENT
* THIS SECTION SETS THE ERROR CODE FOR QUANTITY REQUESTED NOT AVAILABLE
QTYNOTAV EQU   *                                                 S21016
         LA    RFIVE,NOTENUFF           QUANTITY NOT AVAILABLE   S21016
         B     FREEUP
CHECKQTY MVC   PDLIST(5),0(RTWO)       CHECK FOR ENOUGH CYLINDERS
         LH    RFIVE,PDLIST+2
         CR    RNINE,RFIVE             COMPARE THE NUMBER OF CYLINDERS
*                                         REQUESTED TO THE NUMBER
*                                         AVAILABLE
         BH    QTYNOTAV                BRANCH IF QUANTITY AVAILABLE IS
*                                         NOT SUFFICIENT
* THIS SECTION BUILDS A ONE ENTRY DADSM TABLE
GOODQTY  LH    RTWO,ABSTR               USE RTA REQUESTED AS THE
         STH   RTWO,ENTRIES                BEGINNING RTA
         MH    RNINE,DS4DEVSZ+2         MULTIPLY THE NUMBER OF
*                                          CYLINDERS REQUESTED BY THE
*                                          NUMBER OF TRACKS PER
*                                          CYLINDER - -
         AR    RTWO,RNINE               ADD TO BEGINNING RTA TO GET
         STH   RTWO,ENTRIES+2              ENDING RTA
MOVEONE  MVI   ENTRYNUM,X'01'           SET NUMBER OF ENTRIES IN DADSM
*                                          TABLE
         MVI   ENTRIES+4,X'00'
* THIS SECTION SETS UP THE WTG TABLE TO TRANSFER CONTROL TO ANOTHER
* LOAD MODULE
XCTLHERE EQU   *                                                 A38860
         LA    RTWO,NEXTLOAD            POINT TO ID OF IGG032I3  Y02080
XCTLSKIP EQU   *                                                 A38860
         MVI   ASWITCH,ZERO             ZERO THE ALLOCATE SWITCH Y02080
         IECRES LOAD,EXTPR=(RWORK),MODID=(RTWO),BRANCH=DIRECT    Y02080
*
* FOR A CONTIGUOUS REQUEST, THIS SECTION FINDS THE SMALLEST
* FORMAT 5 EXTENT ON THE VOLUME THAT FILLS THE REQUEST.
*
CHEKHELD EQU   *                        BRANCH LABEL             Y02080
         CLI   ASWITCH,HELDQTY          CHECK FOR HELD QUANTITY  Y02080
         BE    HAVEHELD                 BRANCH IF THERE IS A HELD
*                                          QUANTITY
         MVI   ASWITCH,HELDQTY          SET ON HELD FLAG         Y02080
THERE    MVC   PDLIST+20(5),0(RTWO)     MOVE IN F5 ENTRY
MORENTS  BCTR  RONE,0                  CHECK FOR MORE EXTENTS IN F5
         LTR   RONE,RONE               CHECK IF AT 26TH F5 ENTRY
         BZ    CHECKIT                 BRANCH IF AT THE 26TH ENTRY
         LA    RTWO,5(RTWO)            UPDATE F5 ENTRY POINT
CYLREQ   MVC   ENTRIES(5),0(RTWO)
         TM    JFCBCTRI,X'08'          CHECK REQUEST TYPE FIELD
         BC    8,BLANK                 BRANCH IF NO OPTION CHOSEN
         CH    RNINE,ENTRIES+2         COMPARE QUANTITY REQUESTED TO
*                                         THAT AVAILABLE
         BH    MORENTS                 BRANDH IF QUANTITY TOO SMALL
CONTIG   BL     CHEKHELD               BRANCH IF QUANTITY BIGGER THAN
*                                         THAT REQUESTED
         MVC   PDLIST+20(5),ENTRIES    PICK UP F5 ENTRY IF EQUAL TO
*                                         REQUEST IN QUANTITY
         B     BUILDDAD
* THIS SECTION CHECKS FOR NEW QUANTITY SMALLER THAN HELD QUANTITY
HAVEHELD CLC   PDLIST+22(2),ENTRIES+2   COMPARE NEW QUANTITY TO HELD
         BNH   MORENTS                  BRANCH IF NEW QUANTITY IS NOT
*                                          SMALLER THAN HELD QUANTITY
         B     THERE
*
CHECKIT  OC    DS5PTRDS(5),DS5PTRDS     CHECK CHAIN ADDRESS FIELD FOR
*                                          ANOTHER F5
         BC    7,UPDATESK               BRANCH IF ANOTHER F5 EXISTS
NONE     EQU   *                        BRANCH LABEL             Y02080
         CLI   ASWITCH,HELDQTY          CHECK FOR A HELD QTY- AN Y02080
*                                          ENTRY WITH QUANTITY GREATER
*                                          THAN REQUEST
         BNE   BLAKCONT                 BRANCH IF NO HELD QUANTITY
*                                          EXISTS
* THIS SECTION BUILDS A DADSM TABLE OF UP TO FIVE ENTRIES
BUILDDAD SR    RFOUR,RFOUR             CHECK TO SEE IF HELD QUANTITY
         LH    RFIVE,PDLIST+20             IS ON A CYLINDER BOUNDARY
         LH    RSIX,DS4DEVSZ+2             AND IF NOT ROUND UP TO THE
         DR    RFOUR,RSIX                  NEXT FULL CYLINDER
         LTR   RFOUR,RFOUR              CHECK REMAINDER- ON CYLINDER
*                                          BOUNDARY
         BZ    NOUPDTE                  BRANCH IF ON CYLINDER BOUNDARY
         LA    RFIVE,1(RFIVE)           INCREMENT NUMBER OF CYLINDERS
NOUPDTE  MH    RFIVE,DS4DEVSZ+2         MULTIPLY NUMBER OF CYLINDERS BY
*                                          TRACKS PER CYLINDER
         STH   RFIVE,ENTRIES
         MH    RNINE,DS4DEVSZ+2         CONVERT THE REQUEST INTO RTA
         AR    RFIVE,RNINE                 AND RTA+1 AND BUILD A
         STH   RFIVE,ENTRIES+2             DADSM TABLE WITH ONLY ONE
*                                          ENTRY
         MVI   ASWITCH,ZERO              INDICATE NO HELD QTY    Y02080
         B     MOVEONE
* THIS SECTION IS FOR BLANK REQUESTS
BLANK    CH    RNINE,ENTRIES+2          CHECK QUANTITY AVAILABLE TO SEE
*                                          IF ENOUGH
         BNH   CONTIG                   BRANCH IF QUANTITY AVAILABLE IS
*                                          SUFFICIENT
         CLI   ASWITCH,HELDQTY          TEST FOR A HELD QUANTITY Y02080
         BE    MORENTS                 BRANCH IF THERE IS A HELD
*                                         QUANTITY
* THIS SECTION BUILDS A SORTED PUSH DOWN LIST
BUILDPDL CLC   PDLIST+2,ENTRIES+2       COMPARE THE NEW QUANTITY TO THE
*                                          SMALLEST IN THE PUSH DOWN
*                                          LIST
         BNL   MORENTS                  BRANCH IF NEW QUANTITY IS NOT
*                                          LARGER
         MVC   PDLIST(5),ENTRIES        MOVE IN NEW QUANTITY AND DO A
         LA    REIGHT,PDLIST+5             SORT OF THE PUSH DOWN LIST
         LA    RSEVEN,4                 SET UP PD LIST PTR AND CTR
         LA    RSIX,PDLIST
LOOPFOR  CLC   2(2,RSIX),2(REIGHT)      COMPARE TWO ENTRIES IN PUSH
*                                          DOWN LIST
         BNH   ENDTEST                  BRANCH IF NO NEED TO
*                                         EXCHANGE THEM
EXCHANGE EQU   *                        BRANCH LABEL             Y02080
         MVC   SAVEREGB+3(5),0(REIGHT)  EXCHANGE TWO PDLIST ENTRIES
         MVC   0(5,REIGHT),0(RSIX)
         MVC   0(5,RSIX),SAVEREGB+3
* CHECK FOR LAST ENTRY
ENDTEST  BCTR  RSEVEN,0                 REDUCE ENTRY COUNTER
         LTR   RSEVEN,RSEVEN            TEST FOR THE LAST PUSH DOWN
*                                          LIST ENTRY
         BZ    MORENTS                  BRANCH IF THIS IS THE LAST OF
*                                          THE PUSH DOWN LIST ENTRIES
         LA    RSIX,5(RSIX)             UPDATE PTRS TO PD LIST ENTRIES
         LA    REIGHT,5(REIGHT)
         B     LOOPFOR
*
BLAKCONT TM    JFCBCTRI,X'08'          CHECK FOR TYPE OF CYLINDER
*                                          REQUEST
         BC    1,QTYNOTAV               BRANCH IF THE CONTIGUOUS OPTION
*                                          - NOT SUFFICIENT SPACE
* THIS SECTION PICKS FROM PUSH DOWN LIST DESIRED QUANTITY IF AVAILABLE
PICKLIST SR    RZERO,RZERO              SET TOTAL REGISTER TO ZERO
         LA    RONE,PDLIST+20           SET UP PDLIST POINTER
         LA    RSEVEN,ENTRIES           SET UP DADSM TABLE POINTER
         LA    RTWO,5                   SET UP ADDRESS DECREMENT
         LR    RTHREE,RTWO              SET UP ENTRY COUNTER
MOVELOOP MVC   TTRLL(5),0(RONE)         MOVE ENTRY TO FULL WORD  BOUND
         LH    RFIVE,TTRLL              DETERMINE WHETHER OR NOT THE
         SR    RFOUR,RFOUR                 RTA IS ON A CYLINDER BOUND
         LH    RSIX,DS4DEVSZ+2             AND IF NOT ROUND UP TO A
         DR    RFOUR,RSIX                  CYLINDER BOUNDARY
         LTR   RFOUR,RFOUR              CHECK FOR INTEGRAL NUMBER OF
*                                          CYLINDERS
         BZ    NOSTEPUP                 BRANCH IF ON A CYLINDER BOUND
         LA    RFIVE,1(RFIVE)           INCREMENT NUMBER OF CYLINDERS
NOSTEPUP MH    RFIVE,DS4DEVSZ+2
         STH   RFIVE,0(RSEVEN)          CALCULATE RTA AND RTA+1 FOR
         LH    RFOUR,TTRLL+2               DADSM TABLE ENTRY
         AR    RZERO,RFOUR
         MH    RFOUR,DS4DEVSZ+2
         CR    RZERO,RNINE              CHECK FOR QUANTITY AVAILABLE
*                                          BEING SUFFICIENT
         BNL   TAKEOUT                  BRANCH IF THE LAST PUSH DOWN
*                                          LIST ENTRY NEEDED
         AR    RFIVE,RFOUR              CALCULATE THE ENDIND RTA FOR
         STH   RFIVE,2(RSEVEN)             THIS ENTRY
         BCT   RTHREE,UPPTRS            BRANCH IF NOT AT THE END OF THE
*                                          PUSH DOWN LIST
         B     QTYNOTAV                 QUANTITY NOT AVAILABLE
UPPTRS   SR    RONE,RTWO                UPDATE POINTERS
         LA    RSEVEN,4(RSEVEN)
         B     MOVELOOP
TAKEOUT  BNH   TAKE                    BRANCH IS ALL OF THE LAST ENTRY
*                                         OF THE PUSHDOWN LIST IS
*                                         NEEDED
REDUCE   SR    RZERO,RNINE             REDUCE THE AMOUNT AVAILABLE TO
*                                         EQUAL THE AMOUNT REQUESTED
         MH    RZERO,DS4DEVSZ+2
         SR    RFOUR,RZERO             REDUCE AMOUNT AVAILABLE IN THE
*                                         ENTRY
TAKE     AR    RFIVE,RFOUR              ADD IN ALL OF THE LAST ENTRY IN
*                                          THE PUSH DOWN LIST
         STH   RFIVE,2(RSEVEN)
         BCTR  RTHREE,0
         SR    RTWO,RTHREE              SET UP NUMBER OF DADSM TABLE
         STC   RTWO,ENTRYNUM               ENTRIES
* THIS SECTION SORTS THE DADSM TABLE
SORTDAD  SR    RFOUR,RFOUR
         IC    RFOUR,ENTRYNUM           PICK UP NUMBER OF ENTRIES
         BCTR  RFOUR,0                  REDUCE BY ONE
SCANSTRT LR    RTHREE,RFOUR
         LA    RTWO,ENTRIES             SET UP POINTERS
         LA    RFIVE,ENTRIES+4
COMPARE  CLC   2(2,RTWO),2(RFIVE)       CHECK FOR ENTRIES IN THE DADSM
*                                          TABLE FOR RTA SEQUENCE
         BH    CHANGTHM                 BRANCH IF ENTRIES ARE OUT OF
*                                          SEQUENCE
REDUCT   BCT   RTHREE,UPTHEM            CHECK FOR END OF SCAN
         BCT   RFOUR,SCANSTRT           CHECK FOR END OF ENTIRE SORT
         B     XCTLHERE
UPTHEM   LA    RTWO,4(RTWO)             UPDATE POINTERS
         LA    RFIVE,4(RFIVE)
         B     COMPARE
CHANGTHM L     RSEVEN,0(RTWO)           EXCHANGE THE ENTRIES IN THE
         MVC   0(4,RTWO),0(RFIVE)          DADSM TABLE
         ST    RSEVEN,0(RFIVE)
         B     REDUCT
*
* CHANNEL PROGRAM
*
CHANPROG DS    0D
*CCW1
         DC    X'31'                   SEARCH EQUAL ID FOR DSCB F5
         DC    AL3(0+COUNT-FIRST)       SID ON COUNT             Y02080
         DC    X'4000'
         DC    H'5'
*CCW2
         DC    X'08'                   TIC BACK TO SEARCH
         DC    AL3(0+CCW1-FIRST)
ZEROFLD  DC    F'0'
*CCW3
         DC    X'0E'
         DC    AL3(0+F5IN-FIRST)
         DC    X'0000'                                           A27089
         DC    H'140'
*
* CONSTANTS
*
*
* TABLE OF MODULE IDS AND ENTRY POINT ADDRESSES
*
         XCTLTABL ID=(NEXTLOAD,I3,ERRORLD,I7),SVC=032,LENGTH=,   Y02080X
               BRT=YES                                           Y02080
         EJECT                                                   Y02080
JFCBKEN  DSECT
         IEFJFCBN
         EJECT                                                   Y02080
DSCBWKAR IECALLWA EP,F4,D2=(5)          ALLOCATE WORK AREA       Y02080
ENTRYNUM EQU   AENTRYNM                 EQUATE FOR AENTRYNM      Y02080
ENTRIES  EQU   AENTRIES                 EQUATE FOR AENTRIES      Y02080
SAVEREGB EQU   CCW12                    SAVE AREA                Y02080
         EJECT                                                   Y02080
CVT      DSECT                          CVT DSECT                Y02080
         CVT                                                     Y02080
         END
