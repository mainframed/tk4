         TITLE     'IGC0007H  -  DADSM  -  LSPACE SVC ROUTINE'
IGC0007H CSECT                                                   Y02080
*          VS2 RELEASE 03 DELETIONS/CHANGES
*0000265900                                                     Z30AAJC
*          VS2 RELEASE 01.6 DELETIONS                           YA00295
*0000
*          VS2 RELEASE 02 DELETIONS/CHANGES
*0000006000,018000-024000,044000,049000,059000-061000,118000-    Y02080
*0000122000,130000,152000,156000,170000,176000-180000,186000,    Y02080
*0000206000,233000,233420-233600,233720,233840-233900,234460-    Y02080
*0000234960,235982,238000-238040,238960-246800,246810-260000,    Y02080
*0000262300-262400,263110-263130,263300-264900,274810,278500-    Y02080
*0000280500,284000,287800-288000,292000-300000,316000-346000,    Y02080
*0000370300-370700,386000-390000,392000-402000,418900-419000,    Y02080
*0000419300,468000,488000-528000,687200-696000,722200-724000,    Y02080
*0000760010-761200,766000,780200-781000,782000-842000,852000-    Y02080
*0000876000                                                      Y02080
*0000053000-056000,090000-092000,114000-116000,124000-125200,    Y02078
*0000164000-168000                                               Y02078
*0000000300-001500,233780-233960,370100,418050-418100,461500,    Y02082
*0000463500-463900                                               Y02082
*0000                                                            Y02144
*0000                                                            Y02132
*0000247400                                                      YM1312
*0000                                                            YM1241
*0000240250-240350,247900                                        YM5076
*          VS2 RELEASE 01 DELETIONS
*0000                                                            YM2930
*
*          RELEASE 18 DELETIONS
*          RELEASE 19 DELETIONS
*2483034000-072000,084000-092000,098000-104000,144000-146000,    S19028
*2483244000-246000,272000-276000,294000,460000-678000,680000,    S19028
*2483694000,698000-700000,710000,714000-718000,722000,742000-    S19028
*2483752000,772000-776000,862000                                 S19028
*2483                                                            A27329
*2483238400-239200                                               M3941
*          RELEASE 20 DELETIONS
*1242300000                                                      M5414
*1242254000                                                      A35588
*1242                                                            S20201
*          RELEASE 21 DELETIONS
*1195087000                                                      A38860
*1195004000,233300                                               A37199
*1195033000,048000-058000,092000,098000-104000,132000,284000     A40793
*1195143000,720600-721200,793000,794000,826000-832000,844000-    A42182
*1195850000                                                      A42182
*1195370100                                                      A42199
*0000288000,688000                                              SA49351
*          RELEASE 21.7 DELETIONS
*0000188000,204000,206000,220000,234000,260000,264000,266000,   SA48490
*0000268000-270000,270300,276000,276600-278000,312000,316000,   SA48490
*0000318000-342000,344000,348000,564000-573000                  SA48490
*0000240000-242000,250000,252000-258000,274800,275700           SA57953
*          RELEASE 22 DELETIONS
*
*
* MODULE NAME - IGC0007H
*
* DESCRIPTIVE NAME -  DADSM  -  LSPACE SVC ROUTINE
*
* COPYRIGHT - NONE
*
* CHANGE ACTIVITY - SEE DELETIONS/CHANGES FOLLOWING CSECT CARD
*
*
* STATUS CHANGE LEVEL 003
*
* FUNCTION/OPERATION
*    THIS MODULE IS THE FIRST LOAD OF SVC 78. THE SVC IS ISSUED
*    WHEN A DISPLAY SPACE COMMAND IS IN EFFECT OR SMF VOLUME
*    INFORMATION IS REQUIRED AND A DIRECT ACCESS VOLUME IS TO
*    BE DISMOUNTED. ITS FUNCTION IS TO TOTAL UP THE SPACE STILL
*    AVAILABLE ON A VOLUME AT DISMOUNT TIME AND TO RETURN THIS
*    INFORMATION TO THE CALLER AND/OR RECORD IT ON THE SMF DATA
*    SET. THIS MODULE LINKS TO THE VALIDITY CHECK ROUTINE TO
*    CHECK THE CALLER'S MESSAGE AREA POINTER AND TO THE RESIDENT
*    DIRECT ACCESS ADDRESS CONVERSION ROUTINE TO CONVERT THE
*    'TTR0' OF THE VTOC TO AN ABSOLUTE ADDRESS. IT ALSO CHECKS
*    TO INSURE THAT THE CALLER IS IN PROTECT KEY ZERO IF SMF
*    FUNCTIONS HAVE BEEN SPECIFIED. IT OBTAINS THE CORE FOR THE
*    WORK AREA REQUIRED BY THE SVC 78. THE CHANNEL PROGRAMS TO
*    READ THE DATA PORTION OF THE FORMAT 4 DSCB AND THE FIRST
*    FORMAT 5 DSCB ARE RELOCATED IN THE WORK AREA AND
*    INITIALIZED. AFTER THIS MODULE ENQ'S ON THE VTOC, IT PASSES
*    CONTROL TO THE NEXT LOAD VIA IECRES (LOAD).  THE SECOND LOAD
*    (IGC0107H) READS IN THE FORMAT 4 DSCB AND THE FIRST FORMAT 5
*    DSCB. AS IT READS IN ANY CHAINED FORMAT 5 DSCB'S, IT TOTALS
*    THE AVAILABLE SPACE LISTED IN THEM AND SAVES THE LARGEST EXTENT.
*    IF SMF HAS BEEN SPECIFIED, THE MODULE IDENTIFICATION IS SENSED,
*    AND THE VOLUME LABEL IS READ.  IGC0107H BUILDS AN SMF RECORD
*    TYPE 19 AND RECORDS IT ON THE SMF DATA SET. IF 'DISPLAY SPACE'
*    INFORMATION IS REQUIRED, THE SPACE INFORMATION COLLECTED
*    FROM THE FORMAT 5 DSCB(S) IS CONVERTED TO EBCDIC AND PLACED IN
*    THE CALLER'S MESSAGE AREA. AFTER DEQ'ING THE VTOC AND FREEING
*    THE WORK AREA, IGC0107H RETURNS TO THE CALLER.
*
* ENTRY POINT
*    IGC0007H - ENTRY IS MADE TO THIS ROUTINE BY ISSUING AN SVC 78.
*
* INPUT ON ENTRY TO THIS MODULE
*    REGISTER ZERO WILL CONTAIN A POINTER TO THE UCB. BIT 0 OF
*    REGISTER ONE WILL BE ONE IF SMF VOLUME INFORMATION IS
*    DESIRED. BIT 1 OF REGISTER 1 WILL BE ONE IF THIS MODULE
*    SHOULD TEST TO DETERMINE IF SMF VOLUME INFORMATION IS
*    DESIRED. BITS 8 - 31 OF REGISTER ONE WILL CONTAIN THE
*    START ADDRESS OF A 30 BYTE AREA IN WHICH THE DISPLAY SPACE
*    DATA SHOULD BE RETURNED. A ZERO ADDRESS INDICATES THAT THE
*    DISPLAY SPACE INFORMATION IS NOT REQUIRED.  REGISTER FOUR
*    POINTS TO THE TCB.
*
* OUTPUT
*    WHEN CONTROL IS PASSED TO IGC0107H, THE VTOC IS ENQ'ED.
*    REGISTER 7 POINTS TO THE UCB.
*    REGISTER 9 POINTS TO THE 'DISPLAY SPACE' MESSAGE AREA.
*    REGISTER 13 POINTS TO THE WORKAREA.
*
* EXTERNAL ROUTINES
*    CVTPCNVT      - TTR CONVERSION ROUTINE
*    CVT0VL00      - VALIDITY CHECK ROUTINE
*    IECRES        - GET AND RELEASE CORE FOR WORK AREA
*    RESERVE (56)  - ENQUEUE THE VTOC
*
* EXITS
*    CONTROL IS RETURNED TO THE CALLER IN BOTH NORMAL AND
*    ERROR EXITS. REGISTER 15 WILL CONTAIN A RETURN CODE. IF AN
*    ERROR OCCURS AND THE CALLER IS 'DISPLAY SPACE', AN ERROR
*    MESSAGE IS PLACED IN THE CALLER'S MESSAGE AREA.
*
* RETURN CODES THAT CAN BE ISSUED BY LSPACE.
*    00 - SUCCESSFUL PROCESSING
*    04 - PERMANENT I/O ERROR
*    08 - NON-STANDARD OS VOLUME
*    0C - INVALID UCB POINTER OR NOT A DIRECT ACCESS VOLUME OR
*         UCB NOT READY
*    10 - UNSUCCESSFUL VALIDITY CHECK ON CALLER'S MESSAGE
*         POINTER OR INVALID SMF INDICATOR
*
* TABLES/WORK AREAS
*    LSPACE WORK AREA DESCRIBED BY MACRO IECLSPWA
*
* ATTRIBUTES  REENTRANT
*
* OTHER MACROS USED
*    IECLSPWA - LSPACE WORK AREA EXPANSION
*    IECSDSL1 - BUILD DSCB'S
*    IECDSECS - EXPANDS THE CVT, PSA, RB, RRPL, SMF, TCB, UCB DSECTS
*
*
         BALR  BASEREG,0
         USING BEGIN,BASEREG
         USING LSPACWKA,RWKAREA         WORK AREA ADDRESSABILITY Y02080
         USING CVT,RCVT
*
REGZERO  EQU   0
REGONE   EQU   1
RSMCA    EQU   1                                                 S19028
REGTWO   EQU   2
REGTHREE EQU   3
REGFOUR  EQU   4
REGFIVE  EQU   5                        MSG PTR TO USER'S AREA
REGSIX   EQU   6
REGSEVEN EQU   7
RUCBPTRL EQU   7                        UCB POINTER
REGEIGHT EQU   8
REGNINE  EQU   9
REGTEN   EQU   10
WORKR    EQU   11                       WORK REGISTER           SA48490
BASEREG  EQU   12                       BASE REGISTER
RWKAREA  EQU   13                       WORK AREA POINTER
REGSTORE EQU   13
RBAK     EQU   14
RCVT     EQU   15
RERRPASS EQU   15                       ERROR PASS REGISTER
R15      EQU   15                       REGISTER 15              Y02080
R0       EQU   0                        WORK REGISTER            S20201
R2       EQU   2                        WORK REGISTER            S20201
R3       EQU   3                        WORK REGISTER            S20201
R9       EQU   9                        WORK REGISTER            A37199
*
* OTHER EQUATES
*
FW       EQU   4                        BYTES IN A  FULL WORD    S20201
NONZERO  EQU   4                        NON-ZERO VALUE           YM1312
UNRELATD EQU   X'02'                    UNRELATED FLAG IN IOB    A37199
C7       EQU   7                        DISP OF SEVEN            S20201
C1       EQU   1                        ONE CHARACTER            S20201
EXTSA    EQU   96                       DISP. TO SVRB SAVE AREA  Y02080
DADEV    EQU   X'20'                    TESTS FOR DIRECT ACCESS SA48490
*                                       DEVICE                  SA48490
K2       EQU   2                        DISPLACEMENT OF TWO     SA48490
K29      EQU   29                       DISPLACEMENT OF 29      SA57953
*
* THIS SECTION SAVES REGISTERS BEFORE VALIDITY CHECKING THE
* MESSAGE AREA POINTER
*
BEGIN    LR    REGSTORE,REGZERO         SAVE UCB POINTER
         LR    REGFIVE,REGONE           SAVE MSG PTR             S19028
         XR    REGZERO,REGZERO                                   S19028
         SLDL  REGZERO,8                ISOLATE AND SAVE THE     S19028
         SRL   REGONE,8                 SMF INDICATOR            S19028
         LR    REGSIX,REGZERO                                    S19028
         LA    REGZERO,X'3F'            TEST FOR A VALID SMF     S19028
         NR    REGZERO,REGSIX           INDICATOR                S19028
         BNZ   ERRVAL                   BRANCH IF INVALID        S19028
*
*** IF THE CALLER IS IN PROTECT KEY ZERO OR IN SUPERVISOR
*   STATE, THE MESSAGE POINTER DOES NOT HAVE TO BE VALIDITY CHECKED
*   SINCE HE MAY USE ANY REGION IN MAIN STORAGE
*
         LR    REGNINE,REGONE           SAVE MESSAGE POINTER     Y02080
         L     REGEIGHT,0(,REGFOUR)     LOAD SVRB ADDRESS        Y02080
         STM   WORKR,REGFOUR,EXTSA(REGEIGHT)  SAVE REGS 11-4     Y02080
         XR    REGSEVEN,REGSEVEN        INITIALIZE REGISTER      Y02080
         TESTAUTH FCTN=1,KEY=YES,STATE=YES,BRANCH=YES            Y02080X
                                        TEST IF CALLER IN KEY 0  Y02080X
                                        OR SUPERVISOR STATE      Y02080
         LTR   R15,R15                  TEST IF ANY CASE IS TRUE Y02080
         BZ    RESTORE                  BRANCH IF YES            Y02080
         LTR   REGSIX,REGSIX            TEST IF SMF SPECIFIED    YM5076
         BZ    OBTNLOCK                 BRANCH IF NOT            YM5076
         LA    REGSEVEN,NONZERO         NO SMF FUNCTIONS CAN BE  YM5076X
                                        SPECIFIED IF NOT IN KEY  YM5076X
                                        ZERO OR SUPERVISOR STATE YM5076
         B     RESTORE                  GO RESTORE REGISTERS     YM5076
*                                                                YM5076
OBTNLOCK SETLOCK OBTAIN,TYPE=LOCAL,MODE=UNCOND,                  Y02080X
               RELATED=(LOCAL,IGC0007H(RLSELOCK))  OBTAIN LOCK   Y02080
         LM    WORKR,RBAK,EXTSA(REGEIGHT) RESTORE REGS 11-14     YM1241
*
*** THIS SECTION INITIALIZES FOR THE VALIDITY CHECK ROUTINE AND
*   CHECKS IF THE MESSAGE POINTER IS IN THE CALLER'S REGION
*
         LA    REGONE,0(REGFIVE)        BEGINNING ADDRESS        Y02080
         LA    REGTWO,K29(REGONE)       ENDING ADDRESS           Y02080
*                                       REG 4 POINTS TO TCB      Y02080
         L     RCVT,CVTPTR              LOAD CVT ADDRESS         Y02080
         L     RCVT,CVT0VL00            VALIDITY CHECK RTN ADDR  Y02080
         BALR  RBAK,RCVT                LINK TO VALIDITY CHECK   Y02080
         BZ    RLSELOCK                 BRANCH IF VALID ADDRESS  YM1312
         LA    REGSEVEN,NONZERO         INDICATE INVALID ADDRESS YM1312
*
RLSELOCK SETLOCK RELEASE,TYPE=LOCAL,                             Y02080X
               RELATED=(LOCAL,IGC0007H(OBTNLOCK))  RELEASE LOCK  Y02080
RESTORE  EQU   *                        RESTORE REGS             Y02080
         LM    WORKR,REGFOUR,EXTSA(REGEIGHT)  RESTORE REGS 11-4  Y02080
         LTR   REGSEVEN,REGSEVEN        TEST IF ADDRESS VALID    YM5076X
                                        OR IF THE SMF INDICATOR  YM5076X
                                        IS VALID                 YM5076
         BNZ   ERRVAL                   BRANCH IF NOT            Y02080
         L     RCVT,CVTPTR              RELOAD CVT ADDRESS       Y02080
         LR    REGZERO,REGSIX           SAVE SMF INDICATOR      SA48490
*
* THIS SECTION INSURES THE UCB ADDRESS IS WITHIN VALID RANGE.
*
         LR    REGTEN,REGSTORE          ADDR UCB                SA48490
         LA    REGTEN,0(REGTEN)         CLEAR HIGH-ORDER BYTE   SA48490
         SRDL  REGTEN,16                IS ADDR OF UCB          SA48490
         LTR   REGTEN,REGTEN            CONTAINED IN A HALFWORD SA48490
         BNZ   ERREXIT3                 IF NO,INVALID UCB       SA48490
         SLDL  REGTEN,16                RESTORE REGISTER        SA48490
         USING UCB,REGTEN                                       SA48490
*
         TM    UCBJBNR,UCBVRDEV         TEST IF A VIRTUAL UCB    Y02132
         BO    ERREXIT4                 ERROR IF A VDSUCB        Y02132
*
* THIS SECTION CHECKS FOR VALID UCB
*
UCBADDR  EQU   *                                                SA48490
         L     REGSIX,CVTILK2           ADDR PORTION UCB        SA48490
*                                       LOOKUP TABLE            SA48490
UCBLOOP  EQU   *                                                SA48490
         CLC   0(K2,REGSIX),ENDTAB      IS THIS END OF TABLE    SA48490
         BE    ERREXIT3                 IF YES, INVALID UCB     SA48490
         SLR   REGTWO,REGTWO            CLEAR REGISTER          Z30AAJC
         ICM   REGTWO,3,0(REGSIX)       LOAD UCB ADDRESS        Z30AAJC
         CR    REGTEN,REGTWO                                    SA48490
         BE    OKUCB                    VALID UCB FOUND         SA48490
         LA    REGSIX,K2(REGSIX)        POINT TO NEXT ADDR IN   SA48490
*                                       TABLE                   SA48490
         B     UCBLOOP                  PICK UP NEXT ADDR       SA48490
OKUCB    EQU   *                                                SA48490
         CLI   UCBTBYT3,DADEV           IS THIS D.A. UCB        SA48490
         BNE   ERREXIT4                 ERROR IF NOT            SA48490
         DROP  REGTEN                                           SA48490
         LR    RUCBPTRL,REGSTORE        RELOAD UCB POINTER      SA48490
         USING UCB,RUCBPTRL                                     SA48490
BEGINB   LTR   REGSIX,REGZERO           ANY SMF FUNCTIONS       SA48490
         BZ    BEGINC                   BRANCH IF NOT            S19028
         LA    REGZERO,X'40'                                     S19028
         CR    REGZERO,REGSIX           IS TEST FOR SMF FUNCTION S19028
*                                       REQUIRED
         BNE   BEGINC                   BRANCH IF NOT            S19028
         L     RSMCA,CVTSMCA                                     S19028
         LTR   RSMCA,RSMCA              TEST FOR SMF SYSTEM      S19028
         BZ    NOSMF                    BRANCH IF NOT            S19028
         USING SMCABASE,RSMCA                                    S19028
         TM    SMCAOPT,SMCAVOL          IS VOL INFO DESIRED      S19028
         BNO   NOSMF                    BRANCH IF NOT            S19028
         TM    SMCAMISC,SMCAUSER        ARE IBM RECORDS DESIRED  S19028
         BO    BEGINC                   BRANCH IF YES            S19028
NOSMF    XR    REGSIX,REGSIX            ZERO SMF INDICATOR       S19028
TESTEXIT EQU   *                                                SA57953
         LTR   REGNINE,REGNINE          IS DISPLAY SPACE         Y02080
*                                       DESIRED                 SA57953
         BNZ   BEGINC                   BRANCH IF YES            S19028
         XR    RERRPASS,RERRPASS        SET RETURN CODE          S19028
         B     RETURN                   RETURN TO CALLER        SA57953
BEGINC   EQU   *                                                SA48490
         LR    REGNINE,REGFIVE          SAVE MSG PTR             S19028
*
***  THIS SECTION OBTAINS THE LSPACE WORK AREA.
*
         L     REGEIGHT,0(,REGFOUR)     LOAD SVRB ADDRESS        Y02080
         IECRES GET,PREFIX=FIRST,ID=LSWA,LV=LWALNGTH,            Y02080X
               STM=(REGTWO,BASEREG,EXTSA(REGEIGHT))  OBTAIN W/A  Y02080
*                                       IGC0007H IS NOW RUNNING  Y02082
*                                       IN THE DATAMGT KEY AFTER Y02082
*                                       ISSUING THE IECRES GET   Y02082
*                                       FOR CORE IN KEY 5        Y02082
         LR    RWKAREA,REGONE           PICK UP WORK AREA POINTER
*
         IECRES LOAD,MODNM=LOAD1,EXTPR=(RWKAREA),BRANCH=NO       Y02080X
                                        LET OPTIONAL TRACE PRINT Y02080X
                                        THIS MODULE'S NAME       Y02080
*
         L     REGONE,IECRRPRM          RECOVERY RTN LIST ADDR   Y02144
         USING RRPLIST,REGONE           PARM LIST ADDRESSABILITY Y02144
         OI    RRFUNCTN,RRFLSPAC        SET LSPACE FUNCTION BIT  Y02144
         ST    RUCBPTRL,RRUCBPTR        SAVE UCB ADDRESS         Y02144
         DROP  REGONE                                            Y02144
*
         ST    REGFOUR,TCBADDR          SAVE INPUT TCB ADDRESS   Y02078
         STC   REGSIX,SMFFLAG           SAVE SMF INDICATOR       S19028
*
*** THIS SECTION BUILDS A DEB
*
         MVI   DEB+16,X'01'             SET NUMBER OF EXTENTS = 1
         LA    REGONE,DCB               LOAD DCB POINTER
         L     RCVT,CVTPTR              RELOAD CVT POINTER      SA48490
         USING CVT,RCVT                                         SA48490
         L     REGTWO,CVTXAPG           GET IOS APPENDAGE TABLE ADDRESS
         LR    REGTHREE,RUCBPTRL        LOAD MAIN UCB POINTER    Y02080
         XR    REGFOUR,REGFOUR          ZERO THE BIN NUMBER      Y02080
         LM    REGFIVE,REGSIX,EXTCON1   LOAD START HH,END CCHH AND TRKS
         STM   REGONE,REGSIX,DEB+24     STORE IN DEB
         MVI   DEB+24,X'0F'             INSERT DEB ID
         MVI   DEB+28,X'04'             INSERT EXTENT SCALE
         MVI   DEB+32,X'00'             INSERT DEVICE MODE
*
*** THIS SECTION BUILDS BLOCKS - DCB,IOB,ECB
*
BLDBLKS  LA    REGONE,DEB               PUT IN DEB POINTER
         ST    REGONE,DEBPTR
         OI    IOB,UNRELATD             SET UNRELATED BIT IN IOB A37199
         LA    REGTWO,ECB               PUT IN ECB POINTER
         ST    REGTWO,IOB+4
         TM    UCBTBYT2,UCB2OPT3        TEST FOR RPS DEVICE      Y02082
         BZ    NORPS1                   NO, CONTINUE AS NORMAL   S20201
         LM    REGTWO,REGTHREE,RPSCCW   LOAD SET SECTOR CCW      Y02080
         ALR   REGTWO,RWKAREA                                    Y02080
         STM   REGTWO,REGTHREE,CCW0     STORE SET SECTOR CCW     Y02080
         LA    R2,CCW0                  ADDR OF RPS CHANNEL PGM. S20201
         B     IOBCONT                  CONTINUE                 S20201
NORPS1   EQU   *                                                 S20201
         LA    REGTWO,CCW1              PUT IN CHAN PROG POINTER
IOBCONT  EQU   *                                                 S20201
         LA    REGTHREE,DCB             PUT IN DCB POINTER
         STM   REGTWO,REGTHREE,IOB+16
*
* THIS SECTION LINKS TO THE 'ADDRESS CONVERSION' ROUTINE TO GET THE
* VTOC ADDRESS
*
GOCVT    EQU   *                        GET THE VTOC ADDRESS     Y02080
         L     REGZERO,UCBVTOC          LOAD THE F4 DSCB TTR0    Y02080
         MVC   MIELNAME(6),SRTEVOLI     SAVE VOL SER FOR ENQ     A42182
ZEROCLR  IC    REGZERO,EXTCON1          CLEAR THE LOW ORDER BYTES
         STM   REGNINE,RWKAREA,REGSAVEA
         LA    REGTWO,SEEK              LOAD POINTER TO 8 BYTE AREA
         LR    REGTHREE,RWKAREA         SAVE WORK AREA POINTER
         L     RCVT,CVTPCNVT            LOAD CONVERSION ROUTINE BASE
         BALR  RBAK,RCVT                GO TO CONVERSION ROUTINE
         LM    REGNINE,RWKAREA,REGSAVEA-FIRST(REGTHREE)
         MVC   VTOCADR(5),SEEK+3        SAVE VTOC ADDRESS
         TM    UCBTBYT2,UCB2OPT3        DOES DEVICE HAVE RPS     Y02082
         BZ    NORPS2                   NO, CONTINUE AS NORMAL   S20201
         LA    R2,THETA                 GET THETA ADDRESS        S20201
         XC    CCW2+FW(FW),CCW2+FW      CLEAR WORK WORD          S20201
         MVC   CCW2+FW(C1),UCBTBYT4     GET DEVICE CODE          S20201
         O     R2,CCW2+FW               PUT IN HIGH BYTE         S20201
         L     R0,PARM0                 PREPARE PARAMETERS FOR   S20201
         IC    R0,SEEK+C7               GET RECORD NUMBER        S20201
         L     RCVT,CVTPTR              LOAD CVT ADDRESS         Y02080
         L     RCVT,CVT0SCR1            GET ADDR SECTOR CNVT RTN Y02080
         STM   R9,RWKAREA,REGSAVEA      SAVE REGISTERS           S20201
         LR    R3,RWKAREA               SAVE SAVE AREA PTR       S20201
         BALR  RBAK,RCVT                GET SECTOR VALUE         Y02080
         LM    R9,RWKAREA,REGSAVEA-FIRST(R3) RESTORE REGS        S20201
NORPS2   EQU   *                                                 S20201
*
*** THIS SECTION RELOCATES THE CHANNEL PROGRAM TO THE WORK AREA. IT
*** ALSO INITIALIZES IT TO READ IN THE FORMAT 4 DSCB AND THE FIRST
*** FORMAT 5 DSCB
*
         LM    REGZERO,REGSEVEN,CCWP    LOAD FIRST 4 CCW'S
         ALR   REGZERO,RWKAREA
         ALR   REGTWO,RWKAREA
         ALR   REGFOUR,RWKAREA
         ALR   REGSIX,RWKAREA
         STM   REGZERO,REGSEVEN,CCW1    STORE FIRST 4 CCW'S
*
         L     RUCBPTRL,DEB+32          RELOAD UCB POINTER
         LR    REGFIVE,REGNINE          RELOAD MESSAGE POINTER   A42182
         TM    UCBFL2,X'40'             IS UNIT NOT READY?       A42182
         BO    ERR5FREE                 BRANCH IF NOT READY      A42182
*
* LINK TO ENQ ROUTINE TO ENQ THE VTOC ON THIS VOLUME
* SET UP TO ENQUEUE ON THE VTOC OF THIS VOLUME
*
         MVC   MJELNAME(8),VTOCNAME     MOVE MAJOR QUE NAME TO WORKAREA
         MVI   ENQAREA,X'FF'            SET LAST ENTRY CODE
         SR    REGONE,REGONE            CLEAR CODE AND RETURN FIELDS
         STH   REGONE,ENQAREA+2
*
* LINK TO RESERVE ROUTINE TO ENQ THE VTOC ON THIS VOLUME.
*
         RESERVE (MJELNAME,MIELNAME,E,6,SYSTEMS),                YM3022X
               MF=(E,ENQAREA),UCB=UCBPTR                         A42182
*
         OI    DSMADTB2,DSMVTOCR        SET VTOC ENQ'ED BIT      Y02144
*
*** THIS SECTION BRANCHES TO THE NEXT LOAD OF LSPACE (IGC0107H).
*
         IECRES LOAD,EXTPR=(RWKAREA),MODNM=LOAD2,BRANCH=DIRECT   Y02080
*
****************************************************************
*
ERR5FREE EQU   *                                                 A42182
         LA    REGTWO,ERRMSG5           POINT TO MESSAGE         A42182
         LA    REGTHREE,12                                       S19028
EXIT     LA    REGNINE,0(REGFIVE)       SHOULD MSG BE RETURNED   S19028
         LTR   REGNINE,REGNINE                                   S19028
         BZ    FREE                     BRANCH IF NO             S19028
         L     REGONE,TCBADDR           LOAD TCB ADDRESS         Y02078
         USING TCB,REGONE               TCB ADDRESSABILITY       Y02078
         MODESET EXTKEY=RBT234,WORKREG=4  SWITCH TO CALLER'S KEY Y02078
         DROP  REGONE                                            Y02078
         MVC   0(30,REGFIVE),0(REGTWO)  MOVE MSG TO CALLER AREA  S19028
         MODESET EXTKEY=DATAMGT         RETURN TO DATAMGT KEY    Y02078
*
* FREE THE WORK AREA
*
FREE     EQU   *                                                SA49351
         LR    REGEIGHT,REGTHREE        SAVE RETURN CODE         Y02080
         LR    REGNINE,REGFIVE          SAVE MESSAGE POINTER     Y02080
         IECRES FREE,PREFIX=FIRST,A=(RWKAREA)  FREE WORK AREA    Y02080
RETURNPT EQU   *                        RETURN TO CALLER         Y02080
         MODESET EXTKEY=SUPR            RETURN TO SUPERVISOR KEY Y02082
         L     RCVT,CVTPTR              LOAD CVT ADDRESS         Y02080
         USING CVT,RCVT                 CVT ADDRESSABILITY       Y02080
         L     RBAK,CVTEXPRO            LOAD EXIT PROLOG ADDRESS Y02080
         LR    RERRPASS,REGEIGHT        RESTORE RETURN CODE      Y02080
         LR    REGONE,REGNINE           RESTORE MESSAGE POINTER  Y02080
         RETURN ,                       RETURN TO CALLER         Y02080
*
ERREXIT3 LA    REGTWO,ERRMSG3
         BC    15,EXIT4
ERREXIT4 LA    REGTWO,ERRMSG4
EXIT4    LA    RERRPASS,12
         LA    REGNINE,0(REGFIVE)       SHOULD MSG BE RETURNED   S19028
         LTR   REGNINE,REGNINE                                   S19028
         BZ    RETURN                   BRANCH IF NO             S19028
         USING TCB,REGFOUR              TCB ADDRESSABILITY       Y02078
         MODESET EXTKEY=RBT234,WORKREG=1  SWITCH TO CALLER'S KEY Y02078
         DROP  REGFOUR                                           Y02078
         MVC   0(30,REGFIVE),0(REGTWO)  MOVE MSG TO CALLER AREA  S19028
         MODESET EXTKEY=SUPR            RETURN TO SUPERVISOR KEY Y02082
         BC    15,RETURN
ERRVAL   LA    RERRPASS,16
RETURN   EQU   *                        SET FOR RETURN           Y02080
         LR    REGEIGHT,RERRPASS        SAVE RETURN CODE         Y02080
         LR    REGNINE,REGFIVE          SAVE MESSAGE POINTER     Y02080
         B     RETURNPT                 GO RETURN                Y02080
*
* CHANNEL PROGRAM FOR SEEK VTOC, READ IN FORMAT 4 AND FIRST FORMAT 5
* DSCB'S
*
CCWP     CCW   X'31',VTOCADR-FIRST,X'40',5   SEARCH EQUAL ID FOR VTOC
         CCW   X'08',CCW1-FIRST,X'00',0      TIC BACK TO SEARCH
         CCW   X'06',DS4IDFMT-FIRST,X'40',96 READ DATA FOR VTOC DSCB
         CCW   X'9E',DSCBF5-8-FIRST,X'00',148 READ COUNT/KEY/DATA F5
RPSCCW   CCW   X'23',THETA-FIRST,X'40',1  SET SECTOR COMMAND     Y02080
*
* CONSTANTS AND MESSAGES
*
         DS    0F
EXTCON1  DC    X'0000'                  CONSTANT FOR END CCHH OF DEB
         DC    X'FFFFFFFF7FFF'
ENDTAB   EQU   EXTCON1+K2               TEST FOR END OF UCB     SA48490
*                                       LOOKUP TABLE            SA48490
VTOCNAME DC    CL8'SYSVTOC'
*
ERRMSG3  DC    C'LSPACE-INVALID PARAMETER      '
ERRMSG4  DC    C'LSPACE-NOT A DIRECT ACCESS VOL'
ERRMSG5  DC    CL30'LSPACE-UCB NOT READY'                       SA42182
PARM0    DS    0F                       AREA TO BUILD PARAMETERS S20201
DD       DC    X'0060'                  DATA LENGTH              S20201
K        DC    X'2C'                    KEY  LENGTH              S20201
R        DC    X'0'                     RECORD NUMBER            S20201
*
* TABLE OF MODULE NAMES AND ENTRY POINT ADDRESSES
*
         XCTLTABL ID=(LOAD1,IGC0007H,LOAD2,IGC0107H),            Y02080X
               SVC=078,LENGTH=,BRT=YES                           Y02080
         SPACE 2                                                 Y02080
         IECDSECS CVT,PSA,RB,RRPL,SMF,TCB,UCB,EXPAND=YES         Y02144
WORKAREA IECLSPWA EP,F4,ADT=YES         LSPACE WORK AREA         Y02144
REGSAVEA EQU   IECREGSV+12              REGISTER SAVE AREA       Y02080
UCBPTR   EQU   DEB+32                                            A42182
         END   IGC0007H                                          Y02080
