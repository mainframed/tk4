         TITLE 'IGG0CLF2 - BPAM DIRECTORY FORMATTER'             Y02080
IGG0CLF2 CSECT                                                   Y02080
*
*01*  MODULE NAME = IGG0CLF2
*
*01*  DESCRIPTIVE NAME = BPAM DIRECTORY FORMATTER
*
*01*  COPYRIGHT = NONE
*
*01*  CHANGE ACTIVITY = SEE BELOW
*
*        VS2 RELEASE 02 DELETIONS/CHANGES
*0000002800                                                      Y02132
*0000000500,003500-008500,010500-011000,013000,014500-016000,    Y02080
*0000017000-018000,019500-024000,026500-027000,031000,033000-    Y02080
*0000035000,036000,037000-037500,038500-041000,049500,052000,    Y02080
*0000054000-054000,060000,062000,065000-102500,103500,105000-    Y02080
*0000106000,108000-109000,111000,112000-126361,128500-129500,    Y02080
*0000130500,133500,137000,138000,145500-146000,147500-149000,    Y02080
*0000150000-150500,151500,155500-156500,157500,160500,161500,    Y02080
*0000166000,167000,168000-171400,176500,177500,178500,180000-    Y02080
*0000181500,183000-183500,184500,185500-188500,206500,209500-    Y02080
*0000212500,214500-219000,237100-237500,245500-246500,248000-    Y02080
*0000248500,249500-429500                                        Y02080
*0000                                                            Y02144
*0000106500-107500,164000,182000                                 YM3048
*0000103500,109500-109600                                        YM3997
*              VS2 RELEASE 030 CHANGES
*0000                                                          @ZA09979
*0000                                                          @ZA12203
*01*  STATUS = 00
*
*01*  FUNCTION = THIS ROUTINE FORMATS A BPAM DIRECTORY.
*       256 BYTE RECORDS WITH 8 BYTE BINARY ZERO KEYS ARE WRITTEN.
*       THE FIRST BLOCK WILL BE INITIALIZED AS AN EMPTY DIRECTORY.
*       IF THE BPAM DATA SET WAS ALLOCATED BY VIO ALLOCATE, AFTER
*       FORMATTING THE DIRECTORIES, A WICLOSE IS ISSUED TO FREE
*       ITS ASSOCIATED CONTROL BLOCKS.
*01*  NOTES = EXCP IS THE ONLY ACCESS METHOD USED IN THIS MODULE.
*       ALL EXCP IS DONE AT THE LABEL IO.
*02*    DEPENDENCIES = CHARACTER CODE CHANGE REQUIRES REASSEMBLY.
*02*    RESTRICTIONS = NONE
*02*    REGISTER-CONVENTIONS = SEE REGISTER EQUATES.
*         REGISTER 12 IS USED AS THE BASE REGISTER FOR THE CSECT.
*         REGISTER 13 POINTS TO THE ALLOCATE WORK AREA.
*01*  MODULE-TYPE = MODULE
*02*    PROCESSOR = ASSEMBLER
*02*    MODULE-SIZE = LESS THAN 1024 BYTES
*02*    ATTRIBUTES = REENTERABLE, READ ONLY, ENABLED, SUPERVISORY MODE
*01*  ENTRY = IGG0CLF2
*02*    PURPOSE = SEE FUNCTION.
*02*    LINKAGE = BRANCH FROM ALLOCATE MODULES IGG0325H OR IGG0325I.
*02*    INPUT = THE CYLINDER AND TRACK START ADDRESS FIELDS IN THE DEB
*       IN THE ALLOCATE WORK AREA CONTAIN THE STARTING CCHH OF THE BPAM
*       DATA SET'S FIRST EXTENT.  THE FOLLOWING REGISTERS ARE PASSED:
*        REGISTER                      CONTENT
*        1     NUMBER OF BLOCKS TO BE WRITTEN IN BPAM DIRECTORY.
*        2     NUMBER OF BLOCKS PER TRACK FOR DEVICE WHICH
*              CONTAINS THE DATA SET.
*       13     ADDRESS OF THE ALLOCATE WORK AREA.
*02*    OUTPUT = THIS ROUTINE FORMATS A BPAM DIRECTORY.
*02*    EXIT-NORMAL = RETURN
*02*    EXIT-ERROR = RETURN.  REG 15 = 12 IF AN IO ERROR
*01*  EXTERNAL-REFERENCES = AS FOLLOWS
*02*    ROUTINES = IECPRLTV
*02*    DATA-SETS = NONE
*02*    DATA-AREA = DCB, DEB, IOB, AND ECB IN THE ALLOCATE WORK AREA
*01*  TABLES = CVT
*01*  MACROS = EXCP, WAIT, IECDSECS, IECALLWA, IECRES, WICLOSE
*
**** END OF SPECIFICATIONS ***/
         EJECT
***********************************************************************
*
         BALR  RGBASE,0                SET BASE REGISTER.
         USING *,RGBASE                SET CSECT ADDRESSABILITY.
         USING ALLOCWKA,RGAREA         WORK AREA ADDRESSABILITY  Y02080
*
* THIS SECTION SAVES THE INPUT PARAMETERS IN REGISTERS 1 AND 2.
*
IEINIT   EQU   *
         STM   R1,R2,SVBLKS            SAVE INPUT PARAMETERS     Y02080
         LR    RGDQTY,R1               SAVE NUMBER OF BLOCKS     Y02080
*
* THIS SECTION INITIALIZES THE IOB.
*
         LA    R5,CHPGA                CHAN PROG PTR
         ST    R5,IOB+CHNPGADR         POINT TO CHANNEL PROGRAM  Y02080
*
* THE FOLLOWING CODE FORMATS A BPAM DATA SET.
*
* FIRST FORMAT A BPAM DIRECTORY
*
         SR    RGTTR,RGTTR             INIT TTR0 START
         LA    RGDQTY,ONE(RGDQTY)      ACCOUNT FOR EOD MARK
         ST    RGDQTY,SVBLKS
BPLOOP1  BAL   RGRET,CNVT              CONVERT TTR
         LR    RGA,RGDQTY              IN CASE LAST TRACK
         C     RGDQTY,SVBLPTK
         BNH   BPLOOP2                 BRANCH IF LAST TRACK
         L     RGA,SVBLPTK             INIT FOR THIS TRACK
BPLOOP2  BAL   RGRET,RELOC             SET UP CHAN PROG
         C     RGDQTY,SVBLKS           SEE IF FIRST TRACK
         BNE   BPNFRST                 BRANCH IF NOT FIRST TRACK
         CLI   SEEK+RECNO,ZEROA        SEE IF 1ST RECORD ON TRK  Y02080
         BNE   BPNFRST                 BRANCH IF NOT FIRST RECORD
*
* MODIFY FIRST CCW TO WRITE HIGH ENTRY BLOCK
*
         MVC   BPDCNT,COUNTS           MOVE IN COUNT             Y02080
         MVC   BPDHIK,HIKEY            MOVE HI KEY.
         MVC   BPDBPB(L'BPDBPB+L'BPDHIE),EMPTYBLK  MOVE BYTES    Y02080
*                                      PER BLOCK AND HIGH ENTRY  Y02080
         LA    RGB,BPAMDIR-COUNTS      ADJUSTMENT VALUE          Y02080
         L     RGC,CHPGA3              FIRST WRITE CCW.
         AR    RGC,RGB                 ADJUST ADDR
         ST    RGC,CHPGA3              FIRST WRITE CCW.
         MVI   CHPGA3+7,B26            ADJUST COUNT.
BPNFRST  C     RGDQTY,SVBLPTK          SEE IF LAST TRACK
         BH    BPNLST                  BRANCH IF NOT LAST
         LTR   RGA,RGA                 SEE IF END OF LAST TRACK
         BP    BPNLST                  BR IF NOT YET AT END
*
* ZERO KL AND DL IN COUNT FIELD TO FORCE AN EOD TO BE WRITTEN
*
         XC    KEY(THREE,RGH),KEY(RGH)  ZERO KL AND DL           Y02080
         MVI   CHPGB-CHPGA(RGG),X12    CHANGE LAST CCW TO RD COUNT
         MVC   CHPGB-CHPGA+6(TWO,RGG),CCWWR+6 SET COUNT FLD TO 8
BPNLST   BAL   RGRET,IO                EXCP, WAIT, CHECK
         MVC   SEEK+RECNO(ONE),HICOUNT+FOUR  INCREMENT RECORD NO Y02080
         LTR   RGA,RGA                 SEE IF TRACK FINISHED
         BP    BPLOOP2                 NO, WRITE REST OF TRACK
         A     RGTTR,X10000            GO TO NEXT TRK
         S     RGDQTY,SVBLPTK          DECREASE DIR QTY
         BP    BPLOOP1                 BRANCH IF ANOTHER TRACK
***********************************************************************
*
* IF THE BPAM DATA SET WAS ALLOCATED BY VIO ALLOCATE, THEN THIS
* SECTION ISSUES A WICLOSE TO FREE ITS ASSOCIATED CONTROL BLOCKS.
*
***********************************************************************
         L     R8,DEB+K32               GET VDSUCB ADDRESS       Y02132
         TM    K0(R8),UCBVRDEV          TEST IF VIO BPAM DATASET Y02132
         BZ    NOVIOPDS                 BRANCH IF NOT            Y02132
*
         STM   R11,R14,IECREGSV+B12     SAVE SETLOCK REGISTERS   Y02132
         LR    R7,RGAREA                SAVE WORK AREA ADDRESS   Y02132
GETLOCK  SETLOCK OBTAIN,TYPE=LOCAL,MODE=UNCOND,                  Y02132X
               RELATED=(LOCAL,IGG0CLF2(RLSELOCK))  OBTAIN LOCK   Y02132
         LM    R11,R14,IECREGSV+B12-ALLOCWKA(R7)  RESTORE REGS   Y02132
*
         WICLOSE VDSCB=(R8)             ISSUE WICLOSE MACRO      Y02132x
                                        REGISTER 13 POINTS TO    Y02132X
                                        THE 18 WORD SAVE AREA    Y02132
*
         STM   R11,R14,IECREGSV+B12     SAVE SETLOCK REGISTERS   Y02132
         LR    R7,RGAREA                SAVE WORK AREA ADDRESS   Y02132
RLSELOCK SETLOCK RELEASE,TYPE=LOCAL,                             Y02132X
               RELATED=(LOCAL,IGG0CLF2(GETLOCK))  RELEASE LOCK   Y02132
         LM    R11,R14,IECREGSV+B12-ALLOCWKA(R7)  RESTORE REGS   Y02132
*
NOVIOPDS EQU   *                        BRANCH LABEL             Y02132
         SR    RERRPASS,RERRPASS        SET ZERO ERROR CODE      Y02080
         EJECT
*
* IF AN RPS WORK AREA EXISTS, THEN RESTORE THE DEB AVT POINTER.
*
RETURN   EQU   *
         TM    DSMADTB1,DSMRPSAP        TEST IF RPS APPENDAGE    YM3048X
                                        IGG019EK WAS LOADED      YM3048
         BNO   CONTINUE                 BRANCH IF NOT            YM3048
         L     R3,DEB+DEBAPDS           GET APPENDAGE TABLE ADDR Y02080
         MVC   DEB+DEBAPDS(FOUR),AVTSV(R3)  RESTORE DEB AVT PTR  Y02080
         NI    DSMADTB1,X'FF'-DSMRPSAP  RESET RPS APP LOADED SW  Y02144
*
CONTINUE EQU   *
*
* THIS SECTION FREES THE WORK AREA(S) AND RETURNS.
*
         IECRES FREE,PREFIX=FIRST,A=(RGAREA)  FREE WORK AREAS    Y02080
         USING CVT,R15                  CVT ADDRESSABILITY       Y02080
         L     R15,CVTPTR               LOAD POINTER TO CVT      Y02080
         L     R14,CVTEXPRO             LOAD EXIT PROLOG ADDRESS Y02080
         LR    R15,RERRPASS             SET ERROR CODE           Y02080
         RETURN ,                       RETURN                   Y02080
         EJECT
*
* CONVERT TTR IN RGTTR TO MBBCCHHR
*
CNVT     LR    R0,RGTTR
         LA    R1,DEB                  DEB ADDRESS               Y02080
         LA    R2,SEEK                 POINT TO RESULT AREA      Y02080
         LR    R3,RGAREA               SAVE AREA ADR
         STM   R9,R14,IECREGSV+B12     SAVE VOLATILE REGISTERS   Y02080
         USING CVT,R15
         L     R15,CVTPTR              GET ADR OF CVT
         L     R15,CVTPCNVT            GET ADR OF CONVERT ROUTINE
         DROP  R15
         BALR  R14,R15                 GO TO CONVERT ROUTINE.
         LM    R9,R14,IECREGSV+B12-ALLOCWKA(R3)  RESTORE REGS    Y02080
         BR    RGRET                   RETURN TO CALLING CODE.
         EJECT
*
* BUILD CHANNEL PROGRAM OF 1 THRU 15 WRITE CCW'S FOLLOWED BY AN EQUAL
*        NUMBER OF WRITE CHECK CCW'S.
* ENTER NUMBER OF CCW'S TO BE WRITTEN ON GIVEN TRACK IN RGA.
* 'SEEK' INDICATES PROPER TRACK AND ONE LESS THAN R OF FIRST RECORD.
* UPON RETURN, RGA CONTAINS EXCESS OF ORIGINAL VALUE OVER 15, AND
*        RGG POINTS TO LAST WRITE CCW.  RGH POINTS TO THE LAST COUNT.
*
RELOC    LR    RGB,RGA                 NUMBER OF CCW'S
         XC    ADSCBF4(96),ADSCBF4     CLEAR WKAREA            @ZA09979
         LA    RGC,B15                 MAXIMUM NO.  OF CCW'S
         SR    RGA,RGC
         BC    12,LTMAX                BRANCH IF LESS THAN MAX VALUE
         LA    RGB,B15                 SET TO MAX
LTMAX    LM    RGC,RGF,CCWSID          RELOCATE SID, TIC CCW'S
         AR    RGC,RGAREA
         LA    RGG,CHPGA               DEVELOP TIC ADDR
         AR    RGE,RGG
         STM   RGC,RGF,CHPGA           STORE SID, TIC
         LA    RGG,CHPGB-CHPGA         DEVELOP SECOND TIC ADDR
         AR    RGE,RGG
         STM   RGC,RGF,CHPGB           SECOND SID, TIC
         LM    RGE,RGF,SEEK            DEVELOP COUNT FIELD       Y02080
         SLDL  RGE,B24                 DROP MBB
         A     RGF,X080100             SET KL AND DL
         LA    RGG,CHPGA3-EIGHT        INIT. WRITE CCW POINTER   Y02080
         LA    RGH,COUNTS-EIGHT        INIT. COUNT FIELD PTR     Y02080
         LA    RGI,CHPGB3-EIGHT        INIT. WRITE CHK CCW PTR   Y02080
RELOOP1  EQU   *                       BRANCH LABEL              Y02080
         LA    RGG,EIGHT(RGG)          INCREMENT WRITE CCW PTR   Y02080
         LA    RGH,EIGHT(RGH)          INCREMENT COUNT POINTER   Y02080
         LA    RGI,EIGHT(RGI)          INCREMENT WRITE CHK PTR   Y02080
         LM    RGC,RGD,CCWWR           LOAD WRITE CKD CCW        Y02080
         AR    RGC,RGH                 POINT TO COUNT FIELD      Y02080
         STM   RGC,RGD,0(RGG)          MOVE IN WRITE CKD CCW     Y02080
         MVC   0(EIGHT,RGI),CCWRCH     MOVE IN WRITE CHECK CCW   Y02080
         A     RGF,X1000000            INCREMENT R
         STM   RGE,RGF,0(RGH)          STORE COUNT FIELD         Y02080
         BCT   RGB,RELOOP1             BRANCH IF ANOTHER CCW
         NI    FLAGS(RGI),X'FF'-CC     TURN OFF CMD CHAIN BIT    Y02080
         LTR   RGA,RGA                 SEE IF ALL CCW'S FILLED
         BCR   10,RGRET                BR IF ALL FILLED
         MVC   EIGHT(EIGHT,RGG),CHPGB2  MOVE TIC
         BR    RGRET                   RETURN TO CALLING CODE.
         EJECT
*
* EXCP, WAIT, AND I/O ERROR CHECK
*
IO       EQU   *                       BRANCH LABEL              Y02080
         EXCP  IOB                     EXECUTE CHANNEL PROGRAM   Y02080
         TM    ECB,CC                  TEST IMMED POSTING      @ZA12203
         BO    ERRTST                  GO TO ERROR CHECK       @ZA12203
         WAIT  1,ECB=ECB               WAIT FOR COMPLETION       Y02080
ERRTST   TM    ECB,X20                 CHECK FOR I/O ERROR     @ZA12203
         BCR   1,RGRET                 Q. NO ERROR
IOERRCD  EQU   *                       BRANCH LABEL              Y02132
         LA    RERRPASS,B12            SET I/O ERROR CODE        Y02080
         B     RETURN                  RETURN TO CALLER
 EJECT
*
* CONSTANTS AND CHANNEL COMMAND WORDS
*
EMPTYBLK DC    H'14'                   NUMBER OF BYTES PER BLOCK Y02080
HIKEY    DC    X'FFFFFFFFFFFFFFFF'     HIGHEST POSSIBLE KEY.
         DS    0F                      FILLER.
X10000   DC    X'00010000'             TTR STEP CONSTANT.
X080100  DC    X'00080100'             KEY AND DATA LENGTH CONSTANT.
X1000000 DC    X'01000000'             R INCREMENT VALUE.
*
* PATTERN CHANNEL COMMAND WORDS.
*
CCWSID   CCW   X'31',SEEK+3-ALLOCWKA,X'40',5  SEARCH ID          Y02080
         DC    X'0800000000000000'     TIC
CCWWR    CCW   X'1D',0,X'60',8         WRITE CKD, SLI            Y02080
CCWRCH   CCW   X'1E',0,X'50',272       READ CKD, SKIP
         EJECT
*
* MISCELLANEOUS EQUATES USED .
*
K0       EQU   0                       CONSTANT OF ZERO          Y02132
ZEROA    EQU   0                       A ZERO CONSTANT.
ONE      EQU   1                       A ONE CONSTANT.
TWO      EQU   2                       A TWO CONSTANT.
THREE    EQU   3                       A THREE CONSTANT.
FOUR     EQU   4                       A FOUR CONSTANT.
FLAGS    EQU   4                       OFFSET TO CCW FLAGS       Y02080
KEY      EQU   5                       OFFSET TO KEY IN COUNT    Y02080
RECNO    EQU   7                       OFFSET TO RECORD NUMBER   Y02080
EIGHT    EQU   8                       AN EIGHT CONSTANT.
B12      EQU   12                      BINARY 12
B15      EQU   15                      BINARY 15.
B24      EQU   24                      BINARY 24
B26      EQU   26                      BINARY 26
X00      EQU   X'00'                   HEX 00
X12      EQU   X'12'                   HEX 12
X20      EQU   X'20'                   HEX 20
CC       EQU   X'40'                   COMMAND CHAINING BIT      Y02080
AVTSV    EQU   120                     DISP TO AVT PTR IN RPSWA.
DEBAPDS  EQU   28                      APPNDG. PTR DISP. IN DEB.
CHNPGADR EQU   16                      DISP. TO CHANNEL PROGRAM  Y02080
*                                      START ADDRESS IN IOB      Y02080
K32      EQU   32                      CONSTANT OF 32            Y02132
         EJECT
*
* THE FOLLOWING ARE REGISTER EQUATES USED IN THIS PROGRAM.
*
R0       EQU   0                       REGISTER 0
R1       EQU   1                       REGISTER 1
R2       EQU   2                       REGISTER 2
R3       EQU   3                       REGISTER 3
R4       EQU   4                       REGISTER 4
R5       EQU   5                       REGISTER 5
R6       EQU   6                       REGISTER 6
R7       EQU   7                       REGISTER 7
R8       EQU   8                       REGISTER 8
R9       EQU   9                       REGISTER 9
R10      EQU   10                      REGISTER 10
R11      EQU   11                      REGISTER 11
R12      EQU   12                      REGISTER 12
R13      EQU   13                      REGISTER 13
R14      EQU   14                      REGISTER 14
R15      EQU   15                      REGISTER 15
RGI      EQU   1                       WORK REGISTER             Y02080
RGA      EQU   2                       REGISTER 2
RGB      EQU   3                       REGISTER 3
RGC      EQU   4                       REGISTER 4
RGD      EQU   5                       REGISTER 5
RGE      EQU   6                       REGISTER 6
RGF      EQU   7                       REGISTER 7
RGG      EQU   8                       REGISTER 8
RERRPASS EQU   8                       RETURN CODE REGISTER      Y02080
RGH      EQU   9                       REGISTER 9
RGDQTY   EQU   10                      DIRECTORY QUANTITY
RGTTR    EQU   11                      RELATIVE TRACK COUNTER
RGBASE   EQU   12                      BASE REGISTER
RGAREA   EQU   13                      WORK AREA ADDRESS
RGRET    EQU   14                      RETURN REGISTER
         EJECT                                                   Y02080
*
**********************************************************************
*
* END OF THE IGG0CLF2 CSECT.
*
**********************************************************************
         XCTLTABL SVC=000,BRT=YES,LENGTH=                        Y02080
         SPACE 2                                                 Y02080
         IECDSECS CVT,PSA,UCB,EXPAND=YES                         Y02080
*
* THIS DSECT DEFINES THE WORK AREA PASSED TO THE FORMAT ROUTINE.
*
DSCBWKAR IECALLWA EP,ADT=YES           ALLOCATE WORK AREA        Y02144
         EJECT                                                   Y02144
         ORG   ADSCBF4                 CCW'S BEGIN AT ADSCBF4    Y02080
CHPGA    DS    0D                      WRITE CHANNEL PROGRAM
CHPGA1   DS    D                       CCW 1.
CHPGA2   DS    D                       CCW 2.
CHPGA3   DS    D                       CCW 3.
CHPGA4   DS    D                       CCW 4.
CHPGA5   DS    D                       CCW 5.
CHPGA6   DS    D                       CCW 6.
CHPGA7   DS    D                       CCW 7.
CHPGA8   DS    D                       CCW 8.
CHPGA9   DS    D                       CCW 9.
CHPGA10  DS    D                       CCW 10.
CHPGA11  DS    D                       CCW 11.
CHPGA12  DS    D                       CCW 12.
CHPGA13  DS    D                       CCW 13.
CHPGA14  DS    D                       CCW 14.
CHPGA15  DS    D                       CCW 15.
CHPGA16  DS    D                       CCW 16.
CHPGA17  DS    D                       CCW 17.
CHPGB    DS    0D                      READ CHANNEL PROGRAM
CHPGB1   DS    D                       CCW 1.
CHPGB2   DS    D                       CCW 2.
CHPGB3   DS    D                       CCW 3.
CHPGB4   DS    D                       CCW 4.
CHPGB5   DS    D                       CCW 5.
CHPGB6   DS    D                       CCW 6.
CHPGB7   DS    D                       CCW 7.
CHPGB8   DS    D                       CCW 8.
CHPGB9   DS    D                       CCW 9.
CHPGB10  DS    D                       CCW 10.
CHPGB11  DS    D                       CCW 11.
CHPGB12  DS    D                       CCW 12.
CHPGB13  DS    D                       CCW 13.
CHPGB14  DS    D                       CCW 14.
CHPGB15  DS    D                       CCW 15.
CHPGB16  DS    D                       CCW 16.
CHPGB17  DS    D                       CCW 17.
         EJECT
         ORG   CCW1                    SAVE AREA & COUNT FIELDS  Y02080
SVBLKS   DS    F                       NBR OF DIRECTORY BLOCKS   Y02080
SVBLPTK  DS    F                       NUMBER OF BLOCKS/TRACK    Y02080
         SPACE 1                                                 Y02080
COUNTS   DS    0D                      15 COUNT FIELDS           Y02080
CTKEY    DS    CL8                     KEY OF DIR BLOCK.
CTCOUNT  DS    CL2                     COUNT OF DIRECTORY BLOCK.
CTDIR    DS    CL7                     FILLER.
CTLBK    DS    CL1                     LOW ORDER OF NAME.
         DS    CL2                     FILLER.
CTTTRC   DS    CL2                     TTRC
CTHITT   DS    CL2                     HI TT.
CTHIR    DS    CL1                     HI R.
         DS    CL3                     FILLER.
CTNABTR  DS    CL1                     NEXT AVAILABLE BLOCK TTR.
         DS    CL2                     FILLER.
CTNBLB   DS    CL1                     BYTES IN LAST BLOCK.
CTHIENT  DS    CL8                     HI ENTRY.
         DS    CL72                    FILLER.
HICOUNT  DS    D                       LAST RELATIVE TRACK
BPAMDIR  DS    0X                      BDAM DIRECTORY
BPDCNT   DS    CL8                     BPAM DIR COUNT.
BPDHIK   DS    XL8                     BPAM DIRECTORY HIGH KEY   Y02080
BPDBPB   DS    XL2                     BPAM DIR BYTES PER BLOCK  Y02080
BPDHIE   DS    CL8                     BPAM DIR HI ENTRY.
         END
