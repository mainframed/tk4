         TITLE 'IGG032I7 - F4 DSCB UPDATING AND ERROR HANDLING'  Y02080
IGG032I7 CSECT
*
*MODULE NAME = IGG032I7
*
*DESCRIPTIVE NAME = F4 DSCB UPDATING AND ERROR HANDLING
*
*COPYRIGHT = NONE
*
*CHANGE ACTIVITY = SEE BELOW
*
*          RELEASE 16 DELETIONS
*14630590000,060400,082600-082800                                  AAAA
*1463                                                             13145
*          RELEASE 17 DELETIONS
*          RELEASE 18 DELETIONS
*          RELEASE 19 DELETIONS
*0732027200-027400,028200,045800-046200                          O19117
*          RELEASE 20 DELETIONS
*3033039260-039320                                               A31595
*3033002200,061200                                               S20201
*          RELEASE 21 DELETIONS
*1231004800-005000,007800-008400,017600,018800-019400,031200-    A38860
*1231031400,032800,033200,035000-035200,043000-043200,044200,    A38860
*1231049200,049600,050000-050200,051200,051600,052000,052400-    A38860
*1231052600,053000,053600-054000,054600-054800,055000,055400,    A38860
*1231055800-056000,061820-061840,061880,061940,062020,062060,    A38860
*1231062120,062812-062816,062820,062824,062828,062832-062834,    A38860
*1231080400                                                      A38860
*1231025400                                                      A40449
*0000061200-061400                                              SA49351
*          RELEASE 22 DELETIONS
*0000000030-000200,001100-001200,001600-002200,002600-003200,    Y02080
*0000005600,006200-006600,007200-007400,007700,009000,018400,    Y02080
*0000018900-019200,025270,025600,026800,031800-032000,032600-    Y02080
*0000033000,037400-037600,038200-038400,040200,042200,042800-    Y02080
*0000043100,043600-044000,047000-047400,057600,058900,059100,    Y02080
*0000060300,060500,060800-060820,060844-060852,060854-060855,    Y02080
*0000060870-060900,061200-061800,061840-061860,069000,069320-    Y02080
*0000069380,069800-082200,082800-083400,083800                   Y02080
*0000007200,025204,025620,039280-039300,046600,048800-056800,    Y02134
*0000061804-062836,062883,069350,082700-083800                   Y02134
*0000039260                                                      Y02130
*0000063360                                                      Y02078
*0000060600                                                      Y02144
*0000025220-025230,025280,060840-060860,069300                   YM3048
*0000060630,060910-060920                                        YM3997
*0000019200-019300,069800                                        XM2969
*          VS2 RELEASE 03.0 DELETIONS/CHANGES
*0000                                                           ZA00009
*0000048000                                                    @ZA02216
*
*STATUS CHANGE LEVEL 007
*FUNCTION - THIS MODULE UPDATES THE HOLE COUNT FOR AVAILABLE DSCB
*           RECORDS AND WRITES OUT THE FORMAT 4 DSCB. IT THEN FREES
*           ANY WORK AREAS AND RETURNS TO THE SCHEDULER.  FOR ERROR
*           CONDITIONS, IT LOADS THE ERROR CODE IN REGISTER 15 BEFORE
*           RETURNING.  FOR MULTI-VOLUME PRIME REQUESTS, THIS MODULE
*           CALCULATES THE SPACE TO BE ALLOCATED ON THE NEXT VOLUME.
*           IF AN I/O ERROR HAS OCCURRED IN AN ISAM ALLOCATE MODULE,
*           THIS MODULE ISSUES MESSAGE IEC603I WITH CODE=0.
*
*ENTRIES - THE ONLY ENTRY TO THIS MODULE IS IGG032I7.  ENTRY IS
*          NORMALLY FROM IGG032I6.  ENTRY IS ALSO MADE FROM ANY
*          ISAM ALLOCATE MODULE WHEN AN ERROR IS ENCOUNTERED.
*
*
*INPUT - THE WORK AREA CONTAINS THE VTOCADR AND THE DATA PORTION OF THE
*        FORMAT 4 DSCB.  IN CASE OF AN ERROR ENTRY REGISTER FIVE WILL
*        BE SET TO THE PROPER CODE.
*
*        IF MORE THAN ONE DD STATEMENT IS USED IN REQUESTING SPACE,
*        THE SEVEN LOW-ORDER BYTES OF THE DDNAME FIELD IN ALL BUT THE
*        FIRST ISAM DD ENTRY ARE USED TO CREATE THE FOLLOWING SPECIAL
*        ISAM ALLOCATE INTERFACE:
*
*              BYTE 1
*                   BITS 0 AND 1   00 - ABSOLUTE TRACK REQUEST
*                                  01 - BLANK SPACE REQUEST
*                                  11 - CONTIGUOUS SPACE REQUEST
*
*                   BIT 2          THIS IS THE SECOND DD CARD.
*
*                   BIT 5          OVFLOW HAS BEEN ALLOCATED.
*
*                   BIT 6          PRIME HAS BEEN ALLOCATED.
*
*                   BIT 7          INDEX HAS BEEN ALLOCATED.
*
*              BYTES 2 - 3         CURRENT VOLUME SEQUENCE NUMBER
*                                  FOR SECOND OR THIRD DD CARD
*
*              BYTES 4 - 7         POINTER TO THE FIRST TIOT ENTRY
*                                  FOR THIS REQUEST
*
*
*OUTPUT - THE UPDATED FORMAT 4 DSCB IS WRITTEN OUT.
*
*
*EXTERNAL ROUTINES - SVC'S ISSUED IN MACRO EXPANSIONS
*   EXCP(0) READ FROM OR WRITE TO DIRECT ACCESS DEVICE
*   WAIT(1) WAIT ON EVENT CONTROL BLOCK
*   WTO(35) WRITE MESSAGE TO OPERATOR
*
*   OTHER MACROS ISSUED - IEFJFCBN - BUILDS JFCB
*   IECSDSL1 - BUILDS DSCBS
*   IECDSECS - EXPANDS THE CVT AND DSAB DSECTS
*   IECALLWA - ALLOCATE WORK AREA EXPANSION
*
*EXITS - NORMAL - BRANCH TO IGG032I8 IF MULTIVOLUME PRIME ALLOCATION
*               - RETURN TO SCHEDULER IF NOT MULTIVOLUME
*      - ERROR  - RETURN TO SCHEDULER WITH ERROR CODE IN REGISTER 15
*
*                 ERROR CODES THAT CAN BE ISSUED BY THIS MODULE:
*
*                 0C - PERMANENT I/O ERROR
*
*
*TABLES/WORK AREAS - DADSM WORK AREA DESCRIBED BY MACRO IECALLWA
*
*              ***************************************
*              *             DADSM TABLE             *
*              ***************************************
*
*              ***************************************
*              *        *         *                  *
*              *  TYPE  *  NO OF  *     USED HOLE    *
*              *  FLAG  * ENTRIES *      COUNTER     *
*              *        *         *                  *
*              ***************************************
*              *                  *                  *
*              *       RTA        *      RTA+1       *
*              *                  *                  *
*              ***************************************
*              *                  *                  *
*              *       RTA        *      RTA+1       *
*              *                  *                  *
*              ***************************************
*              *                  *                  *
*              *       RTA        *      RTA+1       *
*              *                  *                  *
*              ***************************************
*              *                  *                  *
*              *       RTA        *      RTA+1       *
*              *                  *                  *
*              ***************************************
*              *                  *                  *
*              *       RTA        *      RTA+1       *
*              *                  *                  *
*              ***************************************
*
*
*              TYPE FLAG = 02 - BPAM DIRECTORIES REQUESTED.
*
*                          40 - USER LABELS REQUESTED
*
*                          80 - ENQ SET-MUST-COMPLETE ISSUED
*
*
*              NO OF ENTRIES = NUMBER OF ENTRIES WITHIN DADSM TABLE.
*
*
*              RTA = THE STARTING TRACK OF THE ALLOCATED EXTENT.
*
*
*              RTA+1 = THE STARTING TRACK OF THE NEXT AVAILABLE EXTENT.
*
*
*
*ATTRIBUTES - REENTRANT
*
*STORAGE - PROGRAM CODE CSECT = LESS THAN 1024 BYTES
*          WORK AREA = AS DEFINED IN THE IECALLWA MACRO          XM2969
*          RPS WORK AREA = AS DEFINED IN THE IECRPS MACRO        XM2969
*
*
*REGISTER USAGE
*              REGISTERS 0-9 WORK REGISTERS
*              REGISTER 10 - TIOT SET UP TABLE POINTER
*              REGISTER 11 - JFCB POINTER
*              REGISTER 12 - BASE REGISTER
*              REGISTER 13 - WORK AREA POINTER
*              REGISTER 14 SUBROUTINE LINKAGE AND WORK REGISTER
*              REGISTER 15 WORK REGISTER AND ERROR CODE REGISTER
*
***** REGISTER NAMES *****
RZERO    EQU   0
RONE     EQU   1
RTWO     EQU   2
RTHREE   EQU   3
RFOUR    EQU   4
RFIVE    EQU   5
RSIX     EQU   6
RSEVEN   EQU   7
RUCB     EQU   7                        POINTER TO UCB           Y02078
REIGHT   EQU   8
RNINE    EQU   9
DRBAK    EQU   9
RTEN     EQU   10
RJFCB    EQU   11
RBASE    EQU   12
RWORK    EQU   13
RETURN   EQU   14
RFTEN    EQU   15
*
* OTHER EQUATES
*
BLANK    EQU   X'40'                    EBCDIC BLANK             A40449
TWO      EQU   2                        CONSTANT OF TWO          Y02080
FOURCH   EQU   4                        CONSTANT NUMBER 4        S20201
FOUR     EQU   4                        CONSTANT OF 4            Y02134
SEVEN    EQU   7                        CONSTANT OF 7            Y02134
APDIS    EQU   28                       DISP. TO AVT ADDR IN DEB S20201
AVTDS    EQU   120                      DISP. TO AVT PTR. IN     S20201
*                                       W.A.                     S20201
DIRFBIT  EQU   X'04'                    VTOC INTERRUPT BIT       O19117
TIOEVOLS EQU   2                        OFFSET TO NBR OF DEVICES A38860
TIOEDDNM EQU   4                        OFFSET TO DDNAME         A38860
ISAMSTAT EQU   5                        OFFSET TO ALLOCATION     A38860
*                                       STATUS                   A38860
VOLSQCTR EQU   6                        OFFSET TO DS1VOLSQ CTR   A38860
TIOTPTR  EQU   8                        OFFSET TO 1ST ENTRY PTR  A38860
UCBENTRY EQU   16                       OFFSET TO 1ST UCB ENTRY  A38860
CURRDSAB EQU   0                        OFFSET TO CURRENT DSAB   Y02134
*                                       POINTER IN DSAB LIST     Y02134
FRSTDSAB EQU   4                        OFFSET TO FIRST DSAB PTR Y02134
SCNDDSAB EQU   8                        OFFSET TO 2ND DSAB PTR   Y02134
THRDDSAB EQU   12                       OFFSET TO 3RD DSAB PTR   Y02134
ONE16    EQU   116                      116 CYLINDERS            Y02130
DA3340   EQU   X'0A'                    3340 DEVICE TYPE         Y02130
K1       EQU   1                        CONSTANT OF 1            XM2969
K3       EQU   3                        CONSTANT OF 3            XM2969
K4       EQU   4                        CONSTANT OF 4            XM2969
K5       EQU   5                        CONSTANT OF 5            XM2969
K12      EQU   12                       CONSTANT OF 12           Y02080
K16      EQU   16                       CONSTANT OF 16           XM2969
K140     EQU   140                      CONSTANT OF 140          XM2969
UCBADDR  EQU   32                       DISP. TO UCB ADDR IN DEB Y02078
UUU      EQU   37                       DISP. TO UUU IN MESSAGE  Y02078
VOLSER   EQU   41                       DISP. TO VOLSER IN MSG   Y02078
SECPASS  EQU   X'20'                    SECOND ENTRY SWITCH      Y02080
WRTCCW   EQU   X'0D'                    WRITE CCW CODE           XM2969
READCCW  EQU   X'0E'                    READ CCW CODE            XM2969
ZEROFLAG EQU   X'00'                    ZERO FLAG FOR READ       XM2969
SKIPFLAG EQU   X'10'                    SKIP FLAG                XM2969
*
         BALR  RBASE,0
         USING DELTA,RBASE
         USING ALLOCWKA,RWORK           WORK AREA ADDRESSABILITY Y02080
         USING JFCBKEN,RJFCB
*
DELTA    EQU   *                                                 O19117
         TM    ACNVSW,AI7CNTRL          TEST IF ENTRY FROM       XM2969
*                                       IGG032I6 TO WRITE A F0   XM2969
         BNO   DELTA1                   BRANCH IF ENTRY NOT      XM2969
*                                       FROM 32I6 TO WRITE A F0  XM2969
         NI    ACNVSW,X'FF'-AI7CNTRL    RESET PATH INDICATOR    ZA00009
*                                                                XM2969
* THIS SECTION WRITES A FORMAT 0 DSCB IF NECESSARY               XM2969
*                                                                XM2969
EXIT1    EQU   *                        TEST TO WRITE AN F0      XM2969
         SR    RFIVE,RFIVE              CLEAR ERROR REG          XM2969
         TM    ACNVSW,AF5ZERO           DOES A F5 WITH ALL ZERO  XM2969
*                                       EXTENTS EXIST            XM2969
         BNO   TESTF0                   ZERO EXTENTS DOES NOT    XM2969
*                                       EXIST                    XM2969
         NI    ACNVSW,X'FF'-AF5ZERO     RESET PATH INDICATOR    ZA00009
         TM    ASWITCH,FRSTF5           IS F5 IN CORE IND SET    XM2969
         BO    TESTF0                   BRANCH IF IND IS SET     XM2969
         MVC   TTRLL(K5),ALLCCHHR       CCHHR OF F5 TO BE READ   XM2969
         MVI   CCW11+K4,ZEROFLAG        ZERO FLAG BYTES          XM2969
         MVC   SEEK+K3(K5),TTRLL        INSERT SEEK ADDR         XM2969
         LA    RZERO,CCW9               START OF CHANNEL PGM     XM2969
         ST    RZERO,IOB+K16            STORE STARTING CCW       XM2969
         BAL   RETURN,EXECUTET          BAL TO READ F5 DSCB      XM2969
         MVC   SAVEADR+K5(K5),DS5PTRDS  SAVE CCHHR OF F5         XM2969
         XC    DS5PTRDS(K5),DS5PTRDS    ZERO CHAIN POINTER       XM2969
         MVI   CCW11,WRTCCW             CHANGE READ CCW TO WRITE XM2969
         MVC   SEEK+K3(K5),TTRLL        INSERT SEEK ADDRESS      XM2969
         LA    RZERO,CCW9               START OF CHANNEL PGM     XM2969
         ST    RZERO,IOB+K16            STORE STARTING CCW       XM2969
         BAL   RETURN,EXECUTET          BAL TO WRITE F5          XM2969
         OI    ASWITCH,WRZERO           SET WRITE F0 BIT         XM2969
         MVI   CCW11,READCCW            RESTORE READ CCW         XM2969
TESTF0   EQU   *                        TEST TO WRITE AN F0      XM2969
         TM    ASWITCH,WRZERO           MUST A F0 BE WRITTEN?    XM2969
         BZ    DELTA1                   BRANCH IF NOT            XM2969
         MVC   TTRLL(K5),SAVEADR+K5     ADDRESS OF DSCB TO BE    XM2969
*                                       ZEROED                   XM2969
         XC    DS1DSNAM(K140),DS1DSNAM  ZERO BUFFER              XM2969
         LH    RTWO,DS4DSREC            GET HOLE COUNT           XM2969
         LA    RTWO,K1(RTWO)            INCREMENT BY ONE         XM2969
         STH   RTWO,DS4DSREC            REPLACE IT               XM2969
         MVI   CCW11+K4,SKIPFLAG        SET SKIP FLAG            XM2969
         MVC   SEEK+K3(K5),TTRLL        INSERT SEEK ADDRESS      XM2969
         LA    RZERO,CCW6               START CHAN PROG AT CCW6  XM2969
         ST    RZERO,IOB+K16            STORE STARTING CCW ADDR  XM2969
         BAL   RETURN,EXECUTET          GO WRITE A FORMAT 0      XM2969
DELTA1   EQU   *                        BRANCH LABEL             XM2969
         LTR   DRBAK,RFIVE              TEST/SAVE ERROR CODE     O19117
         BZ    NOERROR                  BR IF NO ERROR           O19117
         XI    DS4VTOCI,DIRFBIT         RESET/SET DIRF BIT       O19117
         TM    DS4VTOCI,DIRFBIT         TEST RESULTS             O19117
         BO    MISTAKE                  BR IF PREVIOUS INTERRUPT O19117
         CH    RFIVE,IOCODE             TEST ERROR CODE          O19117
         BE    MISTAKE                  BR IF I/O ERROR          O19117
         B     FORCEF4                  GO FORCE WRITE F4        O19117
*                                          DETECTED
* THIS SECTION UPDATES THE DSCB FORMAT 4, RELOCATES A WRITE CHANNEL
* PROGRAM AND WRITES OUT THE UPDATED DSCB FORMAT 4.
NOERROR  EQU   *                                                 O19117
         XI    DS4VTOCI,DIRFBIT         RESET DIRF BIT           O19117
         LH    RTWO,DS4DSREC            UPDATE THE NUMBER OF AVAILABLE
         SH    RTWO,UHOLECTR               DSCB RECORDS
         STH   RTWO,DS4DSREC
FORCEF4  EQU   *                                                 O19117
         MVC   SEEK+3(5),DADSMADR       SETUP F4 ADDR            O19117
         LM    RTWO,RSEVEN,CHANNEL      RELOCATE CHANNEL PROGRAM TO
         ALR   RTWO,RWORK                 WRITE OUT FORMAT 4 DSCB
         ALR   RFOUR,RWORK
         ALR   RSIX,RWORK
         STM   RTWO,RSEVEN,CCW1
         LM    RTWO,RSEVEN,CHANNEL+24  RELOCATE LAST 3 CCW'S
         ALR   RTWO,RWORK
         ALR   RFOUR,RWORK
         ALR   RSIX,RWORK
         STM   RTWO,RSEVEN,CCW4
         LA    RTWO,CCW1                SET UP IOB TO POINT TO THE
         ST    RTWO,IOB+16              FIRST CCW                A38860
         BAL   RETURN,EXECUTET
         LTR   RFIVE,DRBAK              TEST/RESTORE ERROR CODE  O19117
         BNZ   MISTAKE                  BR IF THIS WAS A FORCE   O19117
*                                       WR                       O19117
         L     RJFCB,AJFCBPTR           PICK UP JFCB POINTER     Y02080
* A TEST IS MADE HERE TO DETERMINE WHETHER OR NOT THE REQUEST WAS MULTI
* VOLUME
         L     RTEN,ATIOTPTR            LOAD PTR TO DD ENTRY     Y02080
         CLI   TIOEVOLS(RTEN),X'01'     MULTI-VOLUME REQUEST?    A38860
         BE    ALLOUT                   BRANCH IF NOT A MULTI- VOLUME
*                                          REQUEST
         SR    RFIVE,RFIVE
         IC    RFIVE,CTR                PICK UP THE UCB COUNTER
         LA    RFIVE,1(RFIVE)           CHECK THE UCB COUNTER FOR AN
*                                          EQUAL COMPARE TO THE NUMBER
*                                          OF UCB'S TO BE ALLOCATED
         STC   RFIVE,CTR                UPDATE UCB POINTER
         CLC   CTR(1),TIOEVOLS(RTEN)    CHECK FOR COMPLETION OF  A38860
*                                       MULTI-VOLUME ALLOCATION  A38860
         BE    ALLOUT                   BRANCH IF MULTI-VOLUME ALLOCA-
*                                          TION IS COMPLETE
         MVI   F2,X'00'                 SET FOR NO FORMAT 2
         TM    JFCBCTRI,X'DF'           CHECK REQUEST TYPE FIELD FOR AN
*                                          ABSOLUTE TRACK REQUEST
         BC    4,MIXED                  BRANCH IF NOT AN ABSOLUTE TRACK
*                                          REQUEST
* THIS SECTION SUBTRACTS THE NUMBER OF TRACKS JUST ALLOCATED FROM THE
* NUMBER TO BE ALLOCATED IN A MULTI-VOLUME PRIME CASE.  THE AMOUNTS ARE
* ALSO UPDATED FOR THE NEXT ALLOCATION
         L     RTWO,HOWMUCH             SUBTRACT THE AMOUNT JUST Y02080
*                                       ALLOCATED FROM THE       Y02080
*                                       QUANTITY DESIRED         Y02080
         LH    RTHREE,PRIQTY
         SR    RTWO,RTHREE
         ST    RTWO,HOWMUCH             SET UP THE AMOUNT STILL  Y02080
*                                       TO BE ALLOCATED          Y02080
         LH    RSIX,DS4DEVSZ           TEST FOR REQUEST STILL LARGER
         BCTR  RSIX,0                   ACCOUNT FOR THE VTOC
         MH    RSIX,DS4DEVSZ+2
         SR    RFIVE,RFIVE              CLEAR REGISTER           A31595
         L     RTHREE,DEB+32            FETCH UCB PTR            A31595
         USING UCB,RTHREE               UCB ADDRESSABILITY       Y02130
         CLI   UCBTBYT4,DA3340          TEST IF 3340             Y02130
         BNE   NOT3340                  BRANCH IF NOT            Y02130
         SR    RTWO,RTWO                CLEAR FOR DIVIDE         Y02130
         LH    RTHREE,DS4DEVSZ          NBR OF CYLS ON VOLUME    Y02130
         LA    RFOUR,ONE16              4 ALTERNATE TRACKS       Y02130
         DR    RTWO,RFOUR               FOR EVERY 116 CYLINDERS  Y02130
*                                       SO DIVIDE CYL BY 116,    Y02130
         SLL   RTHREE,TWO               MULTIPLY QUOTIENT BY 4   Y02130
*                                       EQUALS TOTAL ALTERNATES  Y02130
SUBTALT  EQU   *                        BRANCH LABEL             Y02130
         SR    RSIX,RTHREE              REDUCE TOTAL BY NUMBER   Y02130
*                                       OF ALTERNATE TRACKS      Y02130
         L     RTWO,HOWMUCH             RESTORE REGISTER TWO     Y02130
         B     CONT                     CONTINUE CALCULATION     Y02130
NOT3340  EQU   *                        BRANCH LABEL             Y02130
         IC    RFIVE,UCBTBYT4           FETCH UNIT TYPE          Y02130
         DROP  RTHREE                                            Y02130
         L     RTHREE,CVTPTR            FETCH CVT POINTER        Y02134
         L     RTHREE,CVTZDTAB-CVT(RTHREE) DEVICE TABLE POINTER  Y02134
         IC    RFIVE,0(RFIVE,RTHREE)    FETCH DEVICE ENTRY       A31595
*                                       OFFSET                   A31595
         SH    RSIX,12(RFIVE,RTHREE)    REDUCE TOTAL TRACKS BY   A31595
*                                       NUMBER OF ALTERNATE TRKS
CONT     EQU   *                        BRANCH LABEL             Y02130
         CR    RTWO,RSIX                COMPARE AMOUNT TO BE ALLOCATED
*                                          TO DEVICE SIZE
         BH    MORE                     BRANCH IF AMOUNT NEEDED IS
*                                          LARGER THAN DEVICE SIZE
         MVC   PRIQTY,HOWMUCH+TWO       AMOUNT TO BE ALLOCATED   Y02080
*                                          ON NEXT VOLUME
         B     SETABSTR
MORE     STH   RSIX,PRIQTY              SET AMOUNT TO BE ALLOCATED ON
*                                          THE NEXT VOLUME TO DEVICE
*                                          SIZE
SETABSTR MVC   ABSTR(2),DS4DEVSZ+2      SET ABSOLUTE TRACK ADDRESS TO
*                                         CYLINDER ONE TRACK ZERO
MIXED    BAL   DRBAK,FINAL1             GO DEQ THIS VOLUME
*
* BRANCH TO LAST LOAD TO PERFORM A DUPLICATE NAME SEARCH ON THE NEXT
* VOLUME.
*
         IECRES LOAD,EXTPR=(RWORK),MODID=MVOLMOD,BRANCH=DIRECT   Y02080
*
* THIS SECTION EXECUTES A CHANNEL PROGRAM.
*
EXECUTET EQU   *                                                 A38860
         MVI   ECB,X'00'                CLEAR ECB                A38860
         EXCP  IOB
         WAIT  1,ECB=ECB
         TM    ECB,X'20'                CHECK FOR SUCCESSFUL COMPLETION
         BCR   7,RETURN                BRANCH IF COMPLETION SUCCESSFUL
         LA    RFIVE,12                SET ERROR CODE FOR PERMANENT
*                                         I/O ERROR
         B     MISTAKE
*
* THIS SECTION CLEANS UP AFTER LAST PASS FOR A MULTI-VOLUME REQUEST OR
* A MULTI-DD CARD REQUEST BY RESETTING THE LAST SEVEN BYTES OF THE
* DDNAME TO BLANKS.  IT UPDATES THE ISAM ALLOCATE INTERFACE IN THE
* TIOT IF THE CURRENT ALLOCATED DATA SET IS NOT THE LAST OF THE
* CONCATENATED DD CARDS.  RTEN POINTS TO THE CURRENT TIOT ENTRY.
*
ALLOUT   SR    RFIVE,RFIVE              SET RETURN CODE- SUCCESSFUL
         L     RTWO,HOWMUCH             TOTAL QTY TO ALLOCATE    Y02080
         LH    RTHREE,PRIQTY            PICK UP QUANTITY TO BE ALLOC-
         SR    RTWO,RTHREE                 ATED ON THIS VOLUME
         BNP   ALLOUTE                  BRANCH IF ALLOC DONE   @ZA02216
         LA    RFIVE,20                 SET ERROR CODE FOR QUANTITY NOT
*                                       AVAILABLE
         B     MISTAKE
ALLOUTE  EQU   *                        BRANCH LABEL             Y02134
         L     RTHREE,ADSABLST          GET DSAB LIST ADDRESS    Y02134
         L     RTWO,SCNDDSAB(,RTHREE)   RTWO HAS 2ND DSAB ADDR   Y02134
         LTR   RTWO,RTWO                IS THERE A SECOND DD     Y02134
         BZ    FINAL                    BRANCH IF CURRENT IS 1ST Y02134
*                                       AND ONLY DD CARD         Y02134
TWODD    EQU   *                        AT LEAST TWO DD CARDS    Y02134
         L     RTHREE,THRDDSAB(,RTHREE) RTHREE HAS 3RD DSAB ADDR Y02134
         USING DSAB,RTWO                                         Y02134
         L     RTWO,DSABTIOT            LOAD 2ND TIOT ENTRY ADDR Y02134
         DROP  RTWO                                              Y02134
         LTR   RTHREE,RTHREE            TEST FOR A 3RD DD CARD   Y02134
         BZ    ONLY2DDS                 BRANCH IF NO             Y02134
THREEDD  EQU   *                        THERE ARE 3 DD CARDS     Y02134
         USING DSAB,RTHREE                                       Y02134
         L     RTHREE,DSABTIOT          LOAD 3RD TIOT ENTRY ADDR Y02134
         DROP  RTHREE                                            Y02134
         CR    RTEN,RTHREE              TEST IF CURRENT IS 3RD   Y02134
         BE    THIRDOF3                 BRANCH IF YES            Y02134
         CR    RTEN,RTWO                TEST IF CURRENT IS 2ND   Y02134
         BNE   FIRSTOF3                 BRANCH IF NO             Y02134
*
* IF THE CURRENT TIOT ENTRY IS NOT THE LAST OF THE CONCATENATED
* DD CARDS, THIS SECTION UPDATES THE ISAM ALLOCATE INTERFACE IN
* THE TIOT FOR THE REMAINING CONCATENATED DD CARD(S).
*
SCNDOF3  EQU   *                        THE CURRENT TIOT         Y02134
*                                       ENTRY IS THE 2ND OF 3    Y02134
         MVC   VOLSQCTR(TWO,RTHREE),VOLSQCTR(RTWO) MOVE THE VOL  Y02134
*                                       SEQ COUNT INTO 3RD TIOT  Y02134
         B     FINAL                    CONTINUE                 Y02134
FIRSTOF3 EQU   *                        THE CURRENT TIOT         Y02134
*                                       ENTRY IS THE 1ST OF 3    Y02134
         MVC   ISAMSTAT(SEVEN,RTHREE),ISAMSTAT(RTWO) MOVE THE    Y02134
*                                       STATUS FROM THE SECOND   Y02134
*                                       TIOT ENTRY TO THE 3RD    Y02134
         NI    ISAMSTAT(RTHREE),X'FF'-SECPASS RESET 2ND ENTRY SW Y02134
         B     FINAL                    CONTINUE                 Y02134
*
* THIS SECTION RESETS THE LAST SEVEN BYTES OF THE DDNAME TO BLANKS.
*
ONLY2DDS EQU   *                        THERE ARE ONLY 2 DD      Y02134
*                                       CARDS FOR THE DATASET    Y02134
         CR    RTEN,RTWO                TEST IF CURRENT IS 2ND   Y02134
         BNE   FINAL                    BRANCH IF NOT            Y02134
         B     SCNDOF2                  BLANK LAST DD ENTRY      Y02134
THIRDOF3 EQU   *                        THE CURRENT TIOT         Y02134
*                                       ENTRY IS THE LAST OF 3   Y02134
         MVC   ISAMSTAT(SEVEN,RTEN),BLANKS  RESTORE LAST DDNAME  Y02134
*                                       TO BLANKS                Y02134
SCNDOF2  EQU   *                        THE CURRENT TIOT ENTRY   Y02134
*                                       IS THE LAST OF 2 OR 3    Y02134
         MVC   ISAMSTAT(SEVEN,RTWO),BLANKS  RESTORE SECOND       Y02134
*                                       DDNAME TO BLANKS         Y02134
FINAL    LA    DRBAK,FINAL2             SET UP RETURN ADDRESS
FINAL1   MVC   MJELNAME(8),VTOCNAME     MOVE QUEUE NAME TO WORK AREA
         MVI   DEQAREA,X'FF'            SET LAST ENTRY CODE
         SR    RONE,RONE                ZERO REGISTER            Y02080
         STH   RONE,DEQAREA+TWO         CLEAR FOR DEQ MACRO      Y02080
         TM    TYPEFLG,SMCDONE          IS SET MUST COMPLETE ON
         BZ    NOSMC                    BRANCH IF NO
*
* LINK TO DEQ ROUTINE TO RELEASE MUST COMPLETE AND DEQ THE VTOC
* ON LAST VOLUME ALLOCATED.
*
         DEQ   (MJELNAME,MIELNAME,6,SYSTEMS),RMC=STEP,MF=(E,DEQAREA)
*
         NI    DSMADTB2,X'FF'-DSMVTOCR-DSMSMCE  RESET ENQ'ED SWS Y02144
         NI    TYPEFLG,X'7F'            CLEAR SMC INDICATOR
         BR    DRBAK
*
* LINK TO DEQ ROUTINE TO DEQ THE VTOC ON LAST VOLUME ALLOCATED.
*
NOSMC    DEQ   (MJELNAME,MIELNAME,6,SYSTEMS),MF=(E,DEQAREA)
*
         NI    DSMADTB2,X'FF'-DSMVTOCR  RESET VTOC ENQ'ED SWITCH Y02144
         BR    DRBAK                    RETURN                   Y02080
*
* IF AN I/O ERROR OCCURRED, THIS SECTION ISSUES MESSAGE IEC603I.
*
FINAL2   EQU   *                        TEST FOR I/O ERROR       Y02080
         CH    RFIVE,IOCODE             TEST FOR I/O ERROR       Y02078
         BNE   RPSTEST                  BRANCH IF NO I/O ERROR   Y02078
         MVC   DSCBF1(MSGEND-ERRMSG),ERRMSG  MOVE IN MESSAGE     Y02078
         L     RUCB,DEB+UCBADDR         LOAD UCB ADDRESS         Y02078
         USING UCB,RUCB                 UCB ADDRESSABILITY       Y02078
         MVC   DSCBF1+UUU(L'UCBNAME),UCBNAME  MOVE IN EBCDIC UCB Y02078
*                                       CHANNEL/UNIT ADDRESS     Y02078
         MVC   DSCBF1+VOLSER(L'SRTEVOLI),SRTEVOLI  MOVE IN VOLID Y02078
         WTO   MF=(E,DSCBF1)            ISSUE ERROR MESSAGE      Y02078
*
RPSTEST  EQU   *                        TEST FOR RPS             Y02078
         TM    DSMADTB1,DSMRPSAP        TEST IF RPS APPENDAGE    YM3048X
                                        IGG019EK WAS LOADED      YM3048
         BNO   CONTINUE                 BRANCH IF NOT            YM3048
         L     RONE,DEB+APDIS           POINT TO APPENDAGE TABLE S20201
         MVC   DEB+APDIS(FOURCH),AVTDS(RONE)  RESTORE AVT PTR    Y02080
         NI    DSMADTB1,X'FF'-DSMRPSAP  RESET RPS APP LOADED SW  Y02144
*
* THIS SECTION FREES THE WORK AREA AND RETURNS CONTROL TO SCHEDULER
*
CONTINUE EQU   *                                                 S20201
         LR    REIGHT,RFIVE             SAVE THE RETURN CODE     Y02080
         IECRES FREE,PREFIX=FIRST,A=(RWORK)  FREE WORK AREAS     Y02080
         USING CVT,RFTEN                CVT ADDRESSABILITY       Y02080
         L     RFTEN,CVTPTR             LOAD CVT ADDRESS         Y02080
         L     RETURN,CVTEXPRO          LOAD EXIT PROLOGUE ADDR  Y02080
         LR    RFTEN,REIGHT             RETURN CODE IN REG 15    Y02080
         RETURN ,                                                Y02080
*
* FOR EACH DD CARD CONCATENATED TO THE FIRST ISAM DATA SET DD CARD,
* THIS SECTION RESETS THE LAST SEVEN BYTES OF THE DDNAME TO BLANKS.
*
MISTAKE  EQU   *                        RESET INDICATORS IN TIOT Y02134
         L     RTHREE,ADSABLST          GET DSAB LIST ADDRESS    Y02134
RESETIOT EQU   *                                                 A40449
         L     RTWO,SCNDDSAB(RTHREE)    GET SECOND/THIRD DSAB    Y02134
         LTR   RTWO,RTWO                IS THERE ONE             Y02134
         BZ    FINAL                    BRANCH IF NOT            Y02134
         USING DSAB,RTWO                                         Y02134
         L     RTWO,DSABTIOT            GET TIOT ENTRY ADDRESS   Y02134
         MVC   ISAMSTAT(SEVEN,RTWO),BLANKS  BLANK OUT DDNAME     Y02134
         LA    RTHREE,FOUR(,RTHREE)     INCREMENT DSAB LIST PTR  Y02134
         B     RESETIOT                 GET THIRD DSAB           Y02134
*
* CONSTANTS
*
IOCODE   DC    H'12'                    I/O ERROR CODE           O19117
NOSPACE  DC    H'20'                    NO SPACE ERROR CODE      A40449
ERRMSG   WTO   'IEC603I VTOC ERRORS MAY EXIST ON UUU,VOLSER,0',  Y02078X
               ROUTCDE=(4,10),DESC=4,MF=L                        Y02078
MSGEND   EQU   *                        MSG DELIMITER            Y02078
BLANKS   DC    CL7' '                   SEVEN BLANKS             Y02134
         DS    0F                       FULLWORD BOUNDARY        Y02078
CHANNEL  EQU   *
*CCW1
         DC    X'31'                         SEARCH ID EQUAL
         DC    AL3(0+VTOCADR-FIRST)
         DC    X'4000'
         DC    H'5'
*CCW2
         DC    X'08'                         TIC TO SEARCH ID EQUAL
         DC    AL3(0+CCW1-FIRST)
         DC    F'0'
*CCW3
         DC    X'05'                         WRITE DATA
         DC    AL3(0+IECSDSF4-FIRST)
         DC    X'4000'
         DC    H'96'
*CCW4
         DC    X'31'                         SEARCH ID EQUAL
         DC    AL3(0+VTOCADR-FIRST)
         DC    X'4000'
         DC    H'5'
*CCW5
         DC    X'08'                         TIC TO SEARCH ID RQUAL
         DC    AL3(0+CCW4-FIRST)
         DC    F'0'
*CCW6
         DC    X'06'                         READ DATA
         DC    AL3(0+IECSDSF4-FIRST)
         DC    X'1000'
         DC    H'96'
VTOCNAME DC    CL8'SYSVTOC '            MAJOR QUEUE NAME
*
* TABLE OF MODULE IDS AND ENTRY POINT ADDRESSES
*
         XCTLTABL ID=(MVOLMOD,I8),SVC=032,LENGTH=,BRT=YES        Y02080
         SPACE 2                                                 Y02134
         IECDSECS CVT,DSAB,UCB,EXPAND=YES  CVT, UCB, DSAB DSECTS Y02134
JFCBKEN  DSECT
         IEFJFCBN
         EJECT                                                   Y02080
DSCBWKAR IECALLWA EP,F4,D1=(1),D2=(5),ADT=YES ALLOCATE WORK AREA XM2969
DEQAREA  EQU   DS4IDFMT                 DEQ TABLE AREA
MJELNAME EQU   DS4IDFMT+16              MAJOR RESOURCE NAME        AAAA
         END
