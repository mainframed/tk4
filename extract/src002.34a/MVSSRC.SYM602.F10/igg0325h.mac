 TITLE 'IGG0325H - DADSM - UPDATE FORMAT 4 DSCB AND RETURN'      Y02080
IGG0325H CSECT
*
*MODULE NAME = IGG0325H
*
*DESCRIPTIVE NAME = DADSM - UPDATE FORMAT 4 DSCB AND RETURN
*
*COPYRIGHT = NONE
*
*CHANGE ACTIVITY = SEE BELOW
*
*          RELEASE 16 DELETIONS
*1462044000,045200                                                 AAAA
*          RELEASE 17 DELETIONS
*          RELEASE 18 DELETIONS
*          RELEASE 19 DELETIONS
*0783027600,028600-028800,030200,031600-033400,034600,042000,    O19117
*0783044400,045600,049000,050200-054000                          O19117
*          RELEASE 20 DELETIONS
*3033046600                                                      S20201
*          RELEASE 21 DELETIONS
*1197002600,004800-005000,019600-019800,020200-020400,064200     A37199
*1197000230,000600-001800,004200,005400,006400,008400-008800,    S21042
*1197082460                                                      S21042
*1197017800,021400,022800,023200,024409-024427,025600-026200,    S21042
*1197027700,028000-028540,030000,032600,034600,037200,038200-    S21042
*1197038400,040200-040400,048000,057600,058000,058400,059800,    S21042
*1197083000                                                      S21042
*0000000950,006280-006520,027612,027741-027744,027753,033800,    A46776
*0000034800-036400,036800,037100,037800,038100,038600,054200-    A46776
*0000055000,058400-059800,061800,080100,082520-082800            A46776
*0000039200,046600,057110-057200                                SA49351
*          RELEASE 22 DELETIONS
*          VS2 RELEASE 02 DELETIONS/CHANGES
*0000000030-000200,000470-000500,001250,001550-001750,002600-    Y02080
*0000003000,004800-005200,006200,007400-007800,008400-008600,    Y02080
*0000009200,010000,018200,020000-020300,024429,024490,027000,    Y02080
*0000027648-027654,027663-027699,027693-027702,027711,027726,    Y02080
*0000027732,027900-028100,029000,033800,034600-034700,035400,    Y02080
*0000037100,037420-037580,038200,038400,038600,039200-040800,    Y02080
*0000043900,044100,045100,045300,045600-045620,045644-045652,    Y02080
*0000045654-045655,045690-045700,046580-046600,047000,056970-    Y02080
*0000057040,058000,058400-082000,082040-082160                   Y02080
*0000082020-082040                                               Y02078
*0000045400                                                      Y02144
*0000024445-024454,024467-024470,045650-045660.057410            YM3048
*0000020300-020400,059700                                        XM2969
*0000045670-045730                                               YM3997
*
*STATUS CHANGE LEVEL 005
*
*OZ28204  ACQUIRE DIRECTORY SPACE FOR MSS VIRTUAL VOLUMES       OZ28204
*OZ28204  ADDITIONS AND CHANGES                                 OZ28204
*008100,022500,024425,024592-024596,035860,037400-037460,       OZ28204
*038440-038720,482800-485800                                    OZ28204
*
*FUNCTION/OPERATION: THIS MODULE CONVERTS THE CCHHR OF THE FORMAT 1   *
*   DSCB TO A TTR AND STORES IT IN THE JFCB FOR TASK SCHEDULER        *
*   ALLOCATION REQUESTS OR IN THE PARTIAL DSCB FOR UTILITY SPACE      *
*   ALLOCATION REQUESTS. IT ALSO CONVERTS THE CCHHR OF A DUPLICATE    *
*   NAME DSCB FOR UTILITY SPACE ALLOCATION REQUESTS. THIS MODULE      *
*   WRITES A FORMAT 0 DSCB, IF REQUIRED, AND WRITES THE UPDATED       *
*   FORMAT 4 DSCB INTO THE VTOC. IF DIRECTORIES ARE REQUESTED, IT     *
*   SETS UP THE DEB FOR CVOL TO WRITE DIRECTORIES. OTHERWISE, IT      *
*   FREES ANY WORK AREAS AND RETURNS.  FOR ERROR CONDITIONS, IT LOADS *
*   THE ERROR CODE INTO REGISTER 15 BEFORE RETURNING.  AN RMC/DEQ OR  *
*   A DEQ IS PERFORMED BEFORE A BRANCH TO IGG0CLF2 (CVOL) OR A RETURN.*
*   IF AN I/O ERROR HAS OCCURRED IN AN ALLOCATE FUNCTION, THIS MODULE *
*   ISSUES MESSAGE IEC603I WITH CODE=0 TO THE OPERATOR.               *
*                                                                     *
*ENTRY POINTS:                                                        *
*        IGG0325H - ENTRY IS MADE FROM IGG0325E ON ZERO QUANTITY      *
*   REQUESTS, FROM IGG0325G ON ALL SPACE REQUESTS, AND ON AN ERROR    *
*   CONDITION FROM IGG0325T OR FROM ANY ALLOCATE MODULE (EXCEPT ISAM  *
*   ALLOCATE AND VIO ALLOCATE) THAT ENCOUNTERED THE ERROR.            *
*                                                                     *
*INPUT: WORK AREA USED BY PREVIOUS DADSM MODULES, THE UPDATED         *
*   FORMAT 4 DSCB IN THE WORK AREA, AND A CHANNEL PROGRAM TO WRITE    *
*   OUT THE UPDATED FORMAT 4 DSCB AND A ZERO RECORD IF REQUIRED.      *
*   ALSO ON ERROR CONDITIONS THE ERROR CODE PASS REGISTER (8) WILL    *
*   CONTAIN THE APPROPRIATE CODE FOR THE ERROR ENCOUNTERED. IF        *
*   DIRECTORIES WERE REQUESTED THE LOW CCHH FROM THE FIRST EXTENT     *
*   ALLOCATED TO THIS REQUEST WILL BE IN THE WORK AREA.               *
*   IF A DUPLICATE NAME DSCB WAS FOUND, THE WORK AREA WILL CONTAIN    *
*   THE CCHHR OF THE NEXT DSCB. ALSO, IF A FORMAT 1 DSCB WAS CREATED, *
*   THE WORK AREA WILL CONTAIN THE CCHHR OF THIS F1 DSCB. IF AN SMC   *
*   WAS PERFORMED, THE SET MUST COMPLETE INDICATOR WILL BE ON.        *
*                                                                     *
*OUTPUT: UPDATED FORMAT 4 DSCB AND A ZERO RECORD DSCB IF REQUIRED.    *
*   ON DIRECTORY REQUESTS THE WORK AREA WILL CONTAIN A DEB WITH THE   *
*   REQUIRED DATA FOR CVOL.  REGISTERS 1 AND 2 WILL ALSO CONTAIN      *
*   ADDITIONAL DATA FOR CVOL.  THE JFCB OR THE PARTIAL DSCB WILL      *
*   CONTAIN THE TTR OF THE F1 DSCB FOR BOTH SCHEDULER AND UTILITY     *
*   SPACE ALLOCATION REQUESTS AND THE TTR OF ANY DUPLICATE NAME DSCB  *
*   FOR UTILITY SPACE REQUESTS.                                       *
*                                                                     *
*EXTERNAL ROUTINES:                                                   *
*        EXCP(0) - READ FROM OR WRITE TO DIRECT ACCESS DEVICE         *
*        WAIT(1) - WAIT ON EVENT CONTROL BLOCK                        *
*        IECRES  - FREE WORK AREA(S), BRANCH TO ANOTHER MODULE        *
*        WTO(35) - WRITE MESSAGE TO OPERATOR                          *
*        DEQ(48) - RELEASE MUST COMPLETE AND DEQ THE VTOC             *
*        MSS(126)- ACQUIRE VIRTUAL VOLUME SPACE                OZ28204*
*                                                                     *
*EXITS:                                                               *
*   NORMAL - CLEAR REGISTER 15 AND RETURN TO THE EXIT PROLOG ADDRESS  *
*            BRANCH TO IGG0CLF2 (CVOL) TO BUILD REQUESTED DIRECTORIES *
*   ERROR  - PUT ERROR CODE INTO REGISTER 15 AND RETURN VIA CVTEXPRO  *
*                                                                     *
*   ERROR CODES THAT CAN BE ISSUED BY MODULE IGG0325H:                *
*                                                                     *
*   0C - PERMANENT I/O ERROR                                          *
*                                                                     *
*TABLES/WORK AREAS: ALLOCATE WORK AREA DESCRIBED BY MACRO 'IECALLWA'  *
*                                                                     *
*              ***************************************                *
*              *             DADSM TABLE             *                *
*              ***************************************                *
*                                                                     *
*              ***************************************                *
*              *        *         *                  *                *
*              *  TYPE  *  NO OF  *     USED HOLE    *                *
*              *  FLAG  * ENTRIES *      COUNTER     *                *
*              *        *         *                  *                *
*              ***************************************                *
*              *                  *                  *                *
*              *       RTA        *      RTA+1       *                *
*              *                  *                  *                *
*              ***************************************                *
*              *                  *                  *                *
*              *       RTA        *      RTA+1       *                *
*              *                  *                  *                *
*              ***************************************                *
*              *                  *                  *                *
*              *       RTA        *      RTA+1       *                *
*              *                  *                  *                *
*              ***************************************                *
*              *                  *                  *                *
*              *       RTA        *      RTA+1       *                *
*              *                  *                  *                *
*              ***************************************                *
*              *                  *                  *                *
*              *       RTA        *      RTA+1       *                *
*              *                  *                  *                *
*              ***************************************                *
*                                                                     *
*              TYPEFLG  =  02 - BPAM DIRECTORIES REQUESTED.           *
*                                                                     *
*                       =  40 - USER LABELS REQUESTED                 *
*                                                                     *
*                       =  80 - SET MUST COMPLETE IS ACTIVE           *
*                                                                     *
*              NO OF ENTRIES = NUMBER OF ENTRIES WITHIN DADSM TABLE.  *
*                                                                     *
*              RTA = THE STARTING TRACK OF THE ALLOCATED EXTENT.      *
*                                                                     *
*              RTA+1 = THE STARTING TRACK OF THE NEXT AVAILABLE EXTENT*
*                                                                     *
*ATTRIBUTES: REENTRANT                                                *
*                                                                     *
*NOTES:                                                               *
*   OTHER MACROS USED                                                 *
*   CVT - CVT MACRO EXPANSION                                         *
*   IECSDSL1 - BUILD DSCB'S                                           *
*   IECALLWA - ALLOCATE WORK AREA MACRO EXPANSION                     *
*   IEFJFCBN - BUILD JFCB                                             *
*                                                                     *
*                                                                     *
*STORAGE: PROGRAM CODE - LESS THAN 1024 BYTES                         *
*   WORK AREA - AS DEFINED IN THE IECALLWA MACRO                 XM2969
*   RPS WORK AREA - AS DEFINED IN THE IECRPS MACRO               XM2969
*                                                                     *
*
*REGISTER USAGE-
*
RZERO    EQU   0                  REGISTER ZERO                  S21042
RONE     EQU   1                  REGISTER ONE                   S21042
R1       EQU   1                  XCTL TO CVOL REGISTER
R2       EQU   2                  XCTL TO CVOL REGISTER
RTWO     EQU   2                  REGISTER TWO                   S21042
RTTR     EQU   2                  POINTER TO TTR FIELD           S21042
ALTRWKA  EQU   3                  ALTERNATE WORKAREA POINTER     S21042
RWORK    EQU   3                  WORK REGISTER                  S21042
R3       EQU   3                  XCTL TO CVOL REGISTER
R4       EQU   4                  XCTL TO CVOL REGISTER
R5       EQU   5                  XCTL TO CVOL REGISTER
DSAVREG  EQU   5                  PO DIRECTORY REQUEST SAVE REG OZ28204
R6       EQU   6                  HOLD ERROR CODE REGISTER
R7       EQU   7                        WORK REGISTER            O19117
RCHP     EQU   7                  CHANNEL PROGRAM START POINTER  S21042
RUCBPTR  EQU   7                        UCB POINTER              Y02078
RERRPASS EQU   8                        ERROR PASS REGISTER
RERRSAVE EQU   9                  ERROR CODE SAVE REGISTER       S21042
RJFCB    EQU   11                 JFCB POINTER
RBASE    EQU   12                 BASE REGISTER
RWKA     EQU   13                 WORK AREA POINTER.
RBAK     EQU   14                 RETURN REGISTER
RWRK     EQU   15                 WORK REGISTER
RERR     EQU   15                 ERROR CODE REGISTER
RCVT     EQU   15                 POINTER TO CVT                 S21042
*
* OTHER EQUATES
*
ZERO     EQU   0                        VALUE ZERO               S21042
ONE      EQU   1                        CONSTANT OF 1           OZ28204
TWO      EQU   2                        CONSTANT OF 2            A46776
K3       EQU   3                        CONSTANT OF 3            Y02080
K4       EQU   4                        CONSTANT OF 4            XM2969
FOURCH   EQU   4                        CONSTANT NUMBER 4        S20201
K12      EQU   12                       CONSTANT OF 12           Y02080
APDIS    EQU   28                       DISP. TO AVT ADDR IN DEB S20201
DEBSCCHH EQU   38                       DISP. TO STARTING CCHH   Y02080
AVTDS    EQU   120                      DISP. TO AVT PTR. IN     S20201
*                                       W.A.                     S20201
DIRFBIT  EQU   X'04'                    VTOC INTERRUPT BIT       O19117
UCBADDR  EQU   32                       DISP. TO UCB ADDR IN DEB Y02078
UUU      EQU   37                       DISP. TO UUU IN MESSAGE  Y02078
VOLSER   EQU   41                       DISP. TO VOLSER IN MSG   Y02078
ZEROFLAG EQU   X'00'                    ZERO FLAGS FOR READ      XM2969
WRTCCW   EQU   X'0D'                    WRITE CCW CODE           XM2969
READCCW  EQU   X'0E'                    READ CCW CODE            XM2969
CYLBOUND EQU   X'C0'                    CYLINDER ALLOCATION     OZ28204
CYLROUND EQU   X'01'                    ROUND ALLOCATION        OZ28204
VIRTDASD EQU   X'08'                    VIRTUAL DASD BIT        OZ28204
*
* * * * * * * * * * * * *
*
*
* * * * * * * * * * * * *
*
         BALR  RBASE,0
         USING START,RBASE
         USING ALLOCWKA,RWKA            WORK AREA ADDRESSABILITY Y02080
         USING JFCBFLD,RJFCB
         USING CVT,RCVT                                          S21042
*
START    EQU   *                                                 O19117
         XI    DS4VTOCI,DIRFBIT         RESET/SET DIRF BIT       O19117
         LR    RERRSAVE,RERRPASS        SAVE ERROR CODE          S21042
         CH    RERRPASS,DUPNAME         TEST IF DUP NAME DSCB    S21042
         BNE   NOERRTST                 BRANCH IF NOT            S21042
         LTR   RJFCB,RJFCB              TEST FOR PARTIAL DSCB    A46776
         BP    NOERRTST                 BRANCH IF NOT            S21042
         SR    RWORK,RWORK              CLEAR REGISTER           S21042
         IC    RWORK,IDAREA+4           GET RECORD NUMBER        S21042
         BCT   RWORK,SAVEREC            DECREMENT RECORD NUMBER  S21042
         IC    RWORK,DS4DEVDT           SET RECORD NUMBER TO     S21042
*                                       MAX NUMBER OF DSCB'S/TRK S21042
SAVEREC  EQU   *                                                 S21042
         STC   RWORK,IDAREA+4                                    S21042
         MVC   CCHHR(5),IDAREA          MOVE IN CCHHR            S21042
         B     CONVERT                  GO CONVERT CCHHR TO TTR  S21042
NOERRTST EQU   *                                                 S21042
         LTR   RERRPASS,RERRPASS        TEST FOR ERROR CODE OF 0 S21042
         BNZ   TESTDIRF                 BRANCH IF NOT            S21042
*
* CONVERT THE F1 DSCB CCHHR OR THE DUPLICATE NAME CCHHR TO A TTR
* USING THE RESIDENT CONVERSION ROUTINE.
*
CONVERT  EQU   *                                                 S21042
         LA    R1,DEB                   POINTER TO DEB           S21042
         LA    R2,MBBCCHHR              POINTER TO MBBCCHHR      S21042
         XC    MBBCCHHR,MBBCCHHR        CLEAR CONVERT AREA       Y02080
         MVC   MBBCCHHR+K3(L'CCHHR),CCHHR  MOVE IN F1 CCHHR      Y02080
         STM   RERRSAVE,RWKA,IECREGSV+K12 SAVE REGISTERS         Y02080
         LR    ALTRWKA,RWKA             GET POINTER TO WORKAREA  S21042
         DROP  RWKA                                              S21042
         USING ALLOCWKA,ALTRWKA         WORK AREA ADDRESSABILITY Y02080
         L     RCVT,CVTPTR              LOAD CVT BASE            S21042
         L     RCVT,CVTPRLTV            LOAD TTR CONVERT ROUTINE S21042
*                                       BASE                     S21042
         BALR  RBAK,RCVT                CONVERT CCHHR            S21042
         LM    RERRSAVE,RWKA,IECREGSV+K12 RESTORE REGISTERS      Y02080
         DROP  ALTRWKA                                           S21042
         USING ALLOCWKA,RWKA            WORK AREA ADDRESSABILITY Y02080
         ST    RZERO,FULLWORD                                    S21042
         DROP  RCVT                                              S21042
         LTR   RJFCB,RJFCB              TEST FOR PARTIAL DSCB    A46776
         BP    NOTPDSCB                 BRANCH IF NO             A46776
         LA    RTTR,DSCBTTR             POINT TO TTR FIELD       S21042
         B     STORETTR                 GO MOVE IN TTR           S21042
NOTPDSCB EQU   *                                                 A46776
         LA    RTTR,JFCBDSCB            POINT TO TTR FIELD       S21042
STORETTR EQU   *                                                 S21042
         MVC   0(3,RTTR),FULLWORD       MOVE IN TTR              S21042
         LTR   RERRSAVE,RERRSAVE        TEST FOR ERROR CODE OF 0 S21042
         BE    TSTZERO                  BRANCH IF YES
TESTDIRF EQU   *                                                 S21042
         TM    DS4VTOCI,DIRFBIT         TEST RESULTS             O19117
         BO    ERREXIT                  BR IF PREVIOUS INTERRUPT O19117
         CH    RERRPASS,IOCODE          Q - I/O ERROR            O19117
         BE    ERREXIT                  IF YES, BR               O19117
         B     WRITEF4                  GO WRITE F4              O19117
*
* WRITE A ZERO RECORD IF NECESSARY.
*
TSTZERO  EQU   *                        TEST TO WRITE AN F0      Y02080
         TM    ACNVSW,AF5ZERO           DOES F5 WITH ALL ZERO    XM2969
*                                       EXTENTS EXIST            XM2969
         BNO   READPREV                 NO, BYPASS READING       XM2969
*                                       PREVIOUS F5              XM2969
         TM    ASWITCH,FRSTF5           IS FIRST F5 IN CORE      XM2969
*                                       INDICATOR SET            XM2969
         BO    READPREV                 BRANCH IF NOT SET        XM2969
         MVC   IDAREA,ALLCCHHR          MOVE SEARCH ARGUEMENT    XM2969
         MVI   CCW11+K4,ZEROFLAG        ZERO FLAGS FOR READ      XM2969
         LA    RCHP,CCW9                ADDR OF CHAN PGM START   XM2969
         BAL   R2,CHANNLGO              BAL TO READ F5           XM2969
         MVC   FMTINAD,DS5PTRDS         SAVE CCHHR OF NEXT F5    XM2969
         XC    DS5PTRDS,DS5PTRDS        ZERO CHAIN POINTER       XM2969
         MVI   CCW11,WRTCCW             CHANGE READ CCW TO WRITE XM2969
         BAL   R2,CHANNLGO              BAL TO WRITE BACK F5     XM2969
         MVI   CCW11,READCCW            RESTORE READ CCW         XM2969
         B     SETADDR                  CONTINUE                 XM2969
READPREV EQU   *                        BRANCH LABEL             XM2969
         TM    ASWITCH,WRZERO           IS WRITE F0 DSCB PENDING Y02080
         BZ    WRITEF4                  NO - CONTINUE TESTING
SETADDR  EQU   *                        BRANCH LABEL             XM2969
         MVC   IDAREA(5),FMTINAD        SET UP ADDR OF RCD TO BE ZEROED
         XC    DS1DSNAM(140),DS1DSNAM   CLEAR OUTPUT AREA
         MVI   CCW11+4,X'10'            TURN ON SKIP FLAG
         LA    RCHP,CCW6                START CHAN PROG AT CCW6  S21042
         BAL   R2,CHANNLGO              GO WRITE AN F0           O19117
         LH    RWRK,DS4DSREC            GET NBR OF AVAIL HOLES
         LA    RWRK,1(RWRK)             INCREMENT BY ONE
         STH   RWRK,DS4DSREC            RESET HOLE COUNT
*
* THIS ROUTINE SETS UP CHANNEL PROGRAM AND WRITES F4 DSCB
*
WRITEF4  EQU   *                                                 O19117
         MVC   IDAREA(5),VTOCADR        START SEEK/SRCH ON VTOC  O19117
         LM    R2,R7,XCCW1              LOAD 1ST 3 CCW'S         O19117
         ALR   R2,RWKA                  RESOLVE                  O19117
         ALR   R4,RWKA                    CORE                   O19117
         ALR   R6,RWKA                      ADDRESSES            O19117
         STM   R2,R7,CCW1               STORE 1ST 3 CCW'S        O19117
         LM    R4,R7,XCCW5              LOAD LAST 2 CCW'S        O19117
         ALR   R4,RWKA                  RESOLVE CORE             O19117
         ALR   R6,RWKA                    ADDRESSES              O19117
         STM   R2,R7,CCW4               STORE LAST 3 CCW'S       O19117
         LA    RCHP,CCW1                START CHAN PROG AT CCW1  S21042
         BAL   R2,CHANNLGO              GO WRITE F4              O19117
*
* FOR A DIRECTORY REQUEST, THIS SECTION PREPARES TO BRANCH TO CVOL.
*
TSTDIR   TM    TYPEFLG,X'02'            ARE DIRECTORIES REQUESTED
         BZ    GOODEND                 BRANCH IF NO
         BAL   R2,CVOLDEQ               GO DEQ VTOC BEFORE XCTL  O19117
         MVC   DEB+DEBSCCHH(FOURCH),SLOWCCHH  MOVE THE STARTING  Y02080
*                                       CCHH TO THE DEB FOR CVOL A46776
         XR    RWORK,RWORK             ZERO WORK REG            OZ28204
         LTR   RJFCB,RJFCB             TEST FOR PARTIAL DSCB     A46776
         BP    LOADDQTY                BRANCH IF NO              A46776
*
* UTILITY SPACE REQUEST - PICK UP DIRECTORY QUANTITY FROM PARTIAL DSCB
*
         L     RONE,PDDIRQTY            LOAD NUMBER OF DIRECTORY Y02080
*                                       BLOCKS REQUESTED         S21042
         IC    RWORK,PD1SCALO           LOAD ALLOCATION TYPE    OZ28204
         B     CK4MSSV                                          OZ28204
*
* NOT A UTILITY REQUEST - PICK UP DIRECTORY QUANTITY FROM THE JFCB
*
LOADDQTY EQU   *                                                 A46776
         L     RONE,JFCBDQTY            LOAD NUMBER OF DIRECTORY Y02080
*                                       BLOCKS REQUESTED         S21042
         SRL   RONE,8                   POSITION THIS NUMBER     Y02080
         IC    RWORK,JFCBCTRI           LOAD ALLOCATION TYPE    OZ28204
CK4MSSV  EQU   *                                                OZ28204
*      CHECK FOR A MSS VIRTUAL VOLUME AND ISSUE AN ACQUIRE      OZ28204
*      ORDER FOR THE DIRECTORY EXTENTS IF MSS.                  OZ28204
         L     RUCBPTR,DEB+UCBADDR      POINT TO UCB            OZ28204
         USING UCB,RUCBPTR                                      OZ28204
         TM    UCBTBYT2,VIRTDASD        MSS VIRTUAL VOLUME?     OZ28204
         BNO   LOADPTR                  NO, BY PASS ACQUIRE     OZ28204
         LR    DSAVREG,RONE             SAVE NUM OF DIRC BLKS   OZ28204
         LA    RONE,DSCBF1              POINT TO ACQUIRE LIST   OZ28204
         USING ACQLST,RONE                                      OZ28204
         XC    ACQLST(ACQLTH),ACQLST    ZERO ACQUIRE LIST       OZ28204
         MVI   ACQLST+3,ACQLTH          LENGTH OF LIST          OZ28204
         MVC   ARVOL,UCBVOLI            MOVE IN VOLSER          OZ28204
         MVI   ARNMEXTS,ONE             SET TO ONE EXTENT       OZ28204
         MVI   AROP,ACQOP               SET ACQUIRE OP CODE     OZ28204
         STC   RWORK,AREXT              PREPARE TO TEST ALLOC   OZ28204
*                                       TYPE CYL OR TRK BOUNDRY OZ28204
         TM    AREXT,CYLBOUND           CYLINDER BOUNDRY ?      OZ28204
         BO    INHIBIT                  YES, INHIBIT STAGE      OZ28204
         TM    AREXT,CYLROUND           CYLINDER BOUNDRY ?      OZ28204
         BO    INHIBIT                  YES, INHIBIT STAGE      OZ28204
         B     COMPEXT                  NO, STAGE DATA          OZ28204
INHIBIT  EQU   *                                                OZ28204
         OI    ARFLAG,INHFLG            SET INHIBIT STAGE       OZ28204
COMPEXT  EQU   *                                                OZ28204
*      COMPUTE THE CYLINDERS REQUIRED TO CONTAIN                OZ28204
*      DIRECTORY REQUEST.                                       OZ28204
         DROP  RUCBPTR                  REMOVE UCB PTR          OZ28204
         MVC   AREXT(2),SLOWCCHH        INIT BEGINING AND ENDINGOZ28204
         MVC   AREXT+2(2),SLOWCCHH      TO THE SAME CYLINDER    OZ28204
         XR    R4,R4                    ZERO WORK REG           OZ28204
         IC    R4,DS4DEVDB              R4=DIRC BLKS PER TRACK  OZ28204
         CR    DSAVREG,R4               REQUESTED DIRC BLKS GT  OZ28204
*                                       THAN DIRC BLKS PER TRK? OZ28204
         BNH   DOACQ                    NO , GO ISSUE ACQUIRE   OZ28204
*      COMPUTE TTR OF DIRECTORY                                 OZ28204
         XR    R2,R2                    ZERO WORK REG           OZ28204
         LR    R3,DSAVREG               R3=NUMBER OF DIRC REQ.  OZ28204
         DR    R2,R4                    R3=TT OF DIREC EXTENT   OZ28204
         LTR   R2,R2                    R4=REMAINDER ?          OZ28204
         BZ    STOREXT                  NO , ON TRK BOUNDRY     OZ28204
         LA    R3,1(R3)                 ROUND TO NEXT TRACK     OZ28204
STOREXT  EQU   *                                                OZ28204
*      CONVERT TTR TO MBBCCHHR                                  OZ28204
         LR    RZERO,R3                 R0=XXTT                 OZ28204
         SLL   RZERO,16                 R0=TTRN                 OZ28204
         LA    R2,AREXT+4               R2=POINTER TO MBBCCHHR  OZ28204
         LA    RONE,DEB                 R1=POINTER TO DEB       OZ28204
         STM   RERRSAVE,RWKA,IECREGSV+K12 SAVE REGS             OZ28204
         LR    ALTRWKA,RWKA             USE ALTRN WORK AREA REG OZ28204
         DROP  RWKA                                             OZ28204
         USING ALLOCWKA,ALTRWKA                                 OZ28204
         L     RCVT,CVTPTR              POINT CONVERT ROUTINE   OZ28204
         USING CVT,RCVT                                         OZ28204
         L     RCVT,CVTPCNVT                                    OZ28204
         BALR  RBAK,RCVT                GO TO CONVERT ROUTINE   OZ28204
         LM    RERRSAVE,RWKA,IECREGSV+K12 RESTORE REGS          OZ28204
         DROP  ALTRWKA                                          OZ28204
         USING ALLOCWKA,RWKA            RETURN TO WORK REG      OZ28204
         DROP  RCVT                                             OZ28204
         LA    RONE,DS1DSNAM            POINT TO ACQUIRE LIST   OZ28204
         MVC   AREXT+2(2),AREXT+7       SAVE ENDING CC IN LIST  OZ28204
         XC    AREXT+4(8),AREXT+4       ZERO WORK AREA USED     OZ28204
DOACQ    EQU   *                                                OZ28204
         ICBACREL  TYPE=ACQ,MF=(E,(1))  ISSUE ACQUIRE           OZ28204
*     NO TEST IS MADE FOR SUCCESSFUL COMPLETION.FORMATTING      OZ28204
*     OF DIRECTORY WILL CYLINDER FAULT IF UNSUCESSFUL.          OZ28204
         DROP  RONE                                             OZ28204
         LR    RONE,DSAVREG             RESTORE NUMBER OF REQ.  OZ28204
*                                       DIRECTORY BLOCKS        OZ28204
LOADPTR  EQU   *                                                 A46776
         SR    R2,R2                    GET NBR OF DIR/BLKS PER TRACK
         IC    R2,DS4DEVDB
         IECRES LOAD,EXTPR=(RWKA),MODNM=CVOLNAME,BRANCH=DIRECT   Y02080
*
* SUCCESSFUL ALLOCATION EXIT
*
GOODEND  SR    RERRPASS,RERRPASS        CLEAR ERROR HOLD REGISTER
*
ERREXIT  EQU   *                                                 O19117
         LA    R2,RETURN                RETURN REG EQUAL         O19117
*                                       FREEMAIN                 O19117
CVOLDEQ  MVC   MJELNAME(8),VTOCNAME     MOVE MAJOR QUE NAME TO WORKAREA
         MVI   DEQAREA,X'FF'            SET LAST ENTRY CODE
         SR    R1,R1                    CLEAR OPTION AND RTN FIELDS
         STH   R1,DEQAREA+2
         TM    TYPEFLG,SMCDONE          IS SET MUST COMP SWITCH ON
         BZ    NOSMC                    BRANCH IF NO
*
* LINK TO THE DEQ ROUTINE TO RELEASE MUST COMPLETE AND DEQ THE VTOC
*
         DEQ   (MJELNAME,MIELNAME,6,SYSTEMS),RMC=STEP,MF=(E,DEQAREA)
*
         NI    DSMADTB2,X'FF'-DSMVTOCR-DSMSMCE  RESET ENQ'ED SWS Y02144
         BR    R2                                                O19117
*
* LINK TO THE DEQ ROUTINE TO DEQ THE VTOC
*
NOSMC    DEQ   (MJELNAME,MIELNAME,6,SYSTEMS),MF=(E,DEQAREA)
*
         NI    DSMADTB2,X'FF'-DSMVTOCR  RESET VTOC ENQ'ED SWITCH Y02144
         BR    R2                       RETURN                   Y02080
*
* THIS SECTION ISSUES MESSAGE IEC603I IF AN I/O ERROR OCCURRED.
*
RETURN   EQU   *                        TEST FOR I/O ERROR       Y02080
         CH    RERRPASS,IOCODE          TEST FOR I/O ERROR       Y02078
         BNE   RPSTEST                  BRANCH IF NO I/O ERROR   Y02078
         MVC   DSCBF1(MSGEND-ERRMSG),ERRMSG  MOVE IN MESSAGE     Y02078
         L     RUCBPTR,DEB+UCBADDR      LOAD UCB ADDRESS         Y02078
         USING UCB,RUCBPTR              UCB ADDRESSABILITY       Y02078
         MVC   DSCBF1+UUU(L'UCBNAME),UCBNAME  MOVE IN EBCDIC     Y02078
*                                       CHANNEL/UNIT ADDRESS     Y02078
         MVC   DSCBF1+VOLSER(L'SRTEVOLI),SRTEVOLI  MOVE IN VOLID Y02078
         WTO   MF=(E,DSCBF1)            ISSUE ERROR MESSAGE      Y02078
*
RPSTEST  EQU   *                        TEST FOR RPS             Y02078
         TM    DSMADTB1,DSMRPSAP        TEST IF RPS APPENDAGE    YM3048X
                                        IGG019EK WAS LOADED      YM3048
         BNO   CONTINUE                 BRANCH IF NOT            YM3048
         L     RONE,DEB+APDIS           POINT TO APPENDAGE TABLE S20201
         MVC   DEB+APDIS(FOURCH),AVTDS(RONE) RESTORE DEB AVT PTR YM3997
         NI    DSMADTB1,X'FF'-DSMRPSAP  RESET RPS APP LOADED SW  Y02144
*
* FREE WORK AREA IF SUCCESSFUL ALLOCATION OR IF UNABLE TO COMPLETE
* ALLOCATION
*
CONTINUE EQU   *                                                 S20201
         IECRES FREE,PREFIX=FIRST,A=(RWKA)  FREE WORK AREAS      Y02080
         USING CVT,RCVT                 CVT ADDRESSABILITY       Y02080
         L     RCVT,CVTPTR              LOAD CVT ADDRESS         Y02080
         L     RBAK,CVTEXPRO            LOAD EXIT PROLOG ADDRESS Y02080
         LR    RERR,RERRPASS            LOAD ERROR REGISTER
         RETURN ,                       RETURN                   Y02080
*
* THIS SECTION LINKS TO IOS FOR ALL I/O OPERATIONS
*
CHANNLGO MVI   ECB,X'00'
         ST    RCHP,IOB+16              STORE START OF CHAN PROG S21042
         MVC   SEEK+3(4),IDAREA
         EXCP  IOB
         WAIT  ECB=ECB
         TM    ECB,X'20'                TEST FOR A PERMANENT I/O ERROR
         BNO   IOERR                    IF I/O ERROR, BR         O19117
         LTR   RERRPASS,RERRPASS        WAS THIS A FORCE F4      O19117
*                                       WRITE                    O19117
         BCR   8,R2                     IF NOT, BR               O19117
         B     ERREXIT                  YES, TAKE ERROR EXIT     O19117
IOERR    EQU   *                                                 O19117
*
* ERROR - PERMANENT I/O ERROR
*
         LA    RERRPASS,X'0C'           SET ERROR CODE 'PERM I/O ERROR
         B     ERREXIT
*
* * * * * * * * * * * * *
*
***   CONSTANTS
*
* * * * * * * * * * * * *
*
ERRMSG   WTO   'IEC603I VTOC ERRORS MAY EXIST ON UUU,VOLSER,0',  Y02078X
               ROUTCDE=(4,10),DESC=4,MF=L                        Y02078
MSGEND   EQU   *                        MSG DELIMITER            Y02078
VTOCNAME DC    CL8'SYSVTOC'             MAJOR QUEUE NAME
IOCODE   DC    H'12'                    I/O ERROR CODE           O19117
DUPNAME  DC    H'4'                     DUP NAME ERROR CODE      S21042
*
* CHANNEL PROGRAM SKELETON
*
         DS    0F                                                O19117
XCCW1    DC    X'31'                    SEARCH ID EQUAL          O19117
         DC    AL3(IDAREA-FIRST)                                 O19117
         DC    X'4000'                                           O19117
         DC    H'5'                                              O19117
XCCW2    DC    X'08'                    TIC                      O19117
         DC    AL3(CCW1-FIRST)                                   O19117
         DC    F'0'                                              O19117
XCCW3    DC    X'05'                    WRITE DATA               O19117
         DC    AL3(DS4IDFMT-FIRST)                               O19117
         DC    X'4000'                                           O19117
         DC    H'96'                                             O19117
*XCCW4         (WILL BE DUPLICATED FROM XCCW1)
XCCW5    DC    X'08'                    TIC                      O19117
         DC    AL3(CCW4-FIRST)                                   O19117
         DC    F'0'                                              O19117
XCCW6    DC    X'06'                    READ DATA/SKIP (WRITE    O19117
*                                       CK)                      O19117
         DC    AL3(DS4IDFMT-FIRST)                               O19117
         DC    X'1000'                                           O19117
         DC    H'96'                                             O19117
*
* TABLE OF MODULE NAMES AND ENTRY POINT ADDRESSES
*
         XCTLTABL ID=(CVOLNAME,IGG0CLF2),SVC=032,LENGTH=,BRT=YES Y02080
         EJECT                                                   Y02080
DSCBWKAR IECALLWA EP,F4,D1=(1),D2=(5),ADT=YES ALLOCATE WORK AREA XM2969
DEQAREA  EQU   ADSCBF4                  DEQUEUE PARAMETER LIST   Y02080
MJELNAME EQU   DEQAREA+16               MAJOR ELEMENT NAME       Y02080
         SPACE 2                                                 Y02078
         IECDSECS CVT,UCB,EXPAND=YES    CVT AND UCB DSECTS       Y02078
JFCBFLD  DSECT
         IEFJFCBN
         EJECT                                                   Y02080
*
* * * * * * * * * * * * *
*
***   DSECT FOR THE PARTIAL DSCB PASSED TO ALLOCATE BY IEHMOVE   A46776
*
* * * * * * * * * * * * *
*
         ORG   INFMJFCB
PD1DSNAM DS    CL44                DATA SET NAME
PD1FMTID DS    CL1                 FORMAT IDENTIFIER
PD1DSSN  DS    CL6                 DATA SET SERIAL NUMBER
PD1VOLSQ DS    XL2                 VOLUME SEQUENCE NUMBER
PD1CREDT DS    XL3                 CREATION DATE
PD1EXPDT DS    XL3                 EXPIRATION DATE
PD1NOEPV DS    XL1                 NUMBER OF EXTENTS ON VOLUME
PD1NOBDB DS    XL1                 NUMBER OF BYTES USED IN LAST
*                                     DIRECTORY BLOCK
         DS    XL1                 RESERVED
PD1SYSCD DS    CL13                SYSTEM CODE
         DS    XL7                 RESERVED
PD1DSORG DS    XL2                 DATA SET ORGANIZATION
PD1RECFM DS    XL1                 RECORD FORMAT
PD1OPTCD DS    XL1                 OPTION CODE
PD1BLKL  DS    XL2                 BLOCK LENGTH
PD1LRECL DS    XL2                 RECORD LENGTH
PD1KEYL  DS    XL1                 KEY LENGTH
PD1RKP   DS    XL2                 RELATIVE KEY POSITION
PD1DSIND DS    XL1                 DATA SET INDICATORS
PD1SCALO DS    XL4                 SECONDARY ALLOCATION
PD1LSTAR DS    XL3                 LAST USED TRACK AND BLOCK ON TRACK
PD1TRBAL DS    XL2                 BYTES REMAINING ON LAST TRACK USED
         DS    XL2                 RESERVED
PD1EXT1  DS    XL1                 EXTENT TYPE INDICATOR
         DS    XL2                 UNUSED
PDPRIQTY DS    F                   PRIMARY SPACE REQUEST IN TRACKS
PDDIRQTY DS    F                   NUMBER OF DIRECTORY BLOCKS
DSCBTTR  EQU   PD1SYSCD            TTR FIELD IN PARTIAL DSCB     A46776
         EJECT                                                  OZ28204
ACQLST   DSECT                     MSS VOLUME ACQUIRE LIST      OZ28204
ACQOP    EQU   X'02'               ACQUIRE OP CODE              OZ28204
ACQLTH   EQU   96                  LENGTH OF ACQUIRE ORDER      OZ28204
INHFLG   EQU   X'20'               INHIBIT STAGE FLAG           OZ28204
         ICBACREL  TYPE=ACQ,MF=L                                OZ28204
         END   IGG0325H                                          S21042
