         TITLE 'IGG0290F - SCRATCH AND RENAME - VOLUME MOUNTING' Y02134
IGG0290F CSECT
         ENTRY IGG0300F                                          Y02134
IGG0300F EQU   IGG0290F                 ALIAS                    Y02134
*          VS2 SU24  CHANGES/DELETIONS                         @G24LSFS
*789955                                                        @G24LSFS
*          VS2 RELEASE 01 DELETIONS
*0000                                                            YM2930
*          VS2 RELEASE 02 DELETIONS/CHANGES
*0000004500,005600-005700,030000,070000-072000,082000-084000,    Y02080
*0000088000-092000,132000-140000,164000,168000-170000,174000,    Y02080
*0000198600-199200,222800,228000-230000,252000,272000-298000,    Y02080
*0000306000,306840,312000,316000,342500-354000,356600-357200,    Y02080
*0000378000-380000,398000-406000,412000,424000,442000-454000,    Y02080
*0000464000,468000,537200,537900-557800,558400-670000,676000-    Y02080
*0000687000,694000-700000,704000                                 Y02080
*0000000300-002000,026000-706000                                 Y02134
*0000058000,157000-158000,177000,406000,926000,929000,932000-    YM1238
*0000933000                                                      YM1238
*0000724000,914000                                               YM2887
*0000702000-705000,795000-796000,800000-801000                   Y02146
*0000798000                                                      YM5390
*0000955000                                                      YM6554
*0000226000,596000-624000,803000-804000,826000,831000-832000     YM7307
*0000                                                            YM7890
*0000                                                            YM8501
*          VS2 RELEASE 03 DELETIONS/CHANGES                    @ZM30032
*0000                                                          @Y301STG
*0000415450,653200                                             @ZA04505
*0000387000                                                    @ZA05596
*
*
*          VS2 RELEASE 037 CHANGES
*          VS2 RELEASE 03.818 DELETIONS/CHANGES
*A544667-544668                                                @ZA20579
*0000                                                          @G18MSBS
*
*
*          RELEASE 20 DELETIONS
*0343308000                                                      S20016
*0343306980,307750,535200-535500                                 TS1611
*          RELEASE 21 DELETIONS
*1194034000-066000,102000-144000,168000,178000,236000,538000,    A41439
*1194558000,686000-688000                                        A41439
*0000344000                                                     SA52595
*          RELEASE 21.7 DELETIONS
*0000010000-024000,634000-646000                                SA56400
*0000052000,426000-428000,438000-440000,446000-450000,500000-   SA53147
*0000506000,522000-526000,538400-557200                         SA53147
*
***********************************************************************
*
* MODULE NAME = IGG0290F (ALIAS IGG0300F)
*
* DESCRIPTIVE MODULE NAME = SCRATCH AND RENAME VOLUME MOUNTING ROUTINE
*
* COPYRIGHT = NONE
*
* STATUS CHANGE LEVEL = 004
*
* FUNCTION =
*
*      THIS MODULE HAS THREE FUNCTIONS - DEMOUNTING, MOUNTING, AND
*      VERIFYING A VOLUME - AS INDICATED BY THE SETTING OF THREE
*      BITS (DEMNTSW, MOUNTSW, AND VERIFYSW) IN BYTE SISW2 IN THE
*      SCRATCH OR RENAME WORK AREA.  VALID COMBINATIONS OF THESE
*      BITS ARE:  DEMOUNTSW ALONE, DEMOUNTSW AND MOUNTSW BOTH SET,
*      AND VERIFYSW ALONE.
*
*      IF DEMOUNTSW IS SET, THIS MODULE FIRST TESTS THAT THE UNIT
*      IS ELIGIBLE FOR DEMOUNTING.  IF NOT, ERROR CODE X'04' IS
*      RETURNED TO THE MODULE WHICH PASSED CONTROL TO IGG0290F.
*
*      IF BOTH DEMOUNTSW AND MOUNTSW ARE SET, IT ISSUES AN EXCLUSIVE
*      ENQ ON SYSZVOLS.  IF THE RETURN CODE INDICATES THAT THE TASK
*      PREVIOUSLY HAD CONTROL OF THE RESOURCE, THE ENQ IS REISSUED
*      TO OBTAIN EXCLUSIVE CONTROL.  IF THE RETURN CODE FROM EITHER
*      ENQ INDICATES THAT THE RESOURCE IS UNAVAILABLE FOR EXCLUSIVE
*      CONTROL, ERROR CODE X'08' IS RETURNED TO THE MODULE WHICH
*      PASSED CONTROL TO IGG0290F (BEFORE ATTEMPTING TO DEMOUNT THE
*      CURRENT VOLUME.)
*
*      IF DEMNTSW IS SET, THIS MODULE THEN ISSUES DEMOUNT MESSAGE
*      IEC502E FOR THE OPERATOR TO DEMOUNT THE CURRENT VOLUME.
*      IF THE CURRENT VOLUME IS A VIRTUAL MSS  VOLUME, THE
*      MESSAGE TO THE OPERATOR IS BYPASSED AND AN ICBMNTDE MACRO
*      (SVC 126) MESSAGE IS SENT TO THE MSS  SUB SYSTEM INSTEAD.
*      THE MSS  SUB SYSTEM WILL THEN DEMOUNT THE VIRTUAL PACK.
*      (SVC 126) MESSAGE IS SENT TO THE MSS  SUB SYSTEM INSTEAD.
*      THE MSS  SUB SYSTEM WILL THEN DEMOUNT THE VIRTUAL PACK.
*      IF THE MONITOR SPACE COMMAND IS ACTIVE AND/OR SMF VOLUME
*      ACCOUNTING INFORMATION IS REQUESTED, IT CALLS LSPACE (SVC 78)
*      TO ADD SPACE INFORMATION TO THE MESSAGE AND/OR TO WRITE AN
*      SMF RECORD TYPE 19.  IT ALSO CALLS VOLSTAT (SVC 91) TO WRITE
*      A VOLUME STATISTICS RECORD TO SYS1.LOGREC.  IF BIT VOLDEQ IS
*      SET IN BYTE SISW1, IT ALSO DEQ'S THE VOLUME FROM THE SYSZVOLS
*      RESOURCE.
*
*      IF MOUNTSW IS SET, THIS MODULE THEN SETS BIT UCBMOUNT IN
*      THE UCB AND ISSUES MOUNT MESSAGE IEC601D.  IF THE CALLER OF
*      SCRATCH OR RENAME IS A TSO TASK, MESSAGE IEC108I IS ISSUED
*      TO HIS TERMINAL.  THE OPERATOR CAN REPLY EITHER 'U' (TO USE
*      THE VOLUME) OR 'M' (TO SKIP MOUNTING THIS VOLUME) TO MESSAGE
*      IEC601D.  IF THE OPERATOR CHOOSES TO SKIP THE VOLUME, THIS
*      MODULE RESETS BIT UCBMOUNT AND RETURNS ERROR CODE X'08' TO
*      THE MODULE WHICH PASSED CONTROL TO IGG0290F.
*
*      IF THE MOUNTSW IS SET, BUT THE DEVICE IS FOUND TO BE
*      AN MSS  SUBSYSTEM PACK  THEN THE ACTION IN THE PRECEDING
*      PARAGRAPH IS BYPASSED.  INSTEAD A REQUEST IS ISSUED TO
*      JES3(IF THE UCB INDICATES A JES3 MANAGED UNIT) FOR VOLUME
*      AWARENESS.  THEN AN ICBMNTDE MACRO(SVC126) IS ISSUED TO
*      THE SYBSYSTEM AND THE VIRTUAL VOLUME REQUESTED WILL BE
*      MOUNTED BY THE SUBSYSTEM.
*
*      IF THE OPERATOR REPLIES 'U' OR IF JUST VERIFYSW IS SET,
*      SYSEVENT DONTSWAP IS INVOKED TO PREVENT MEMORY SWAPOUT
*      WHILE WAITING TO READ THE VOLUME LABEL (THIS PREVENTS
*      WAIT IF THE OPERATOR DECIDES TO CANCEL THE JOB), THIS
*      MODULE THEN RESETS BIT UCBMOUNT, AND
*      VERIFIES THAT THE CORRECT VOLUME HAS BEEN MOUNTED.  IT THEN
*      CONVERTS THE CCHHR OF THE FORMAT 4 DSCB TO A TTR0 AND STORES
*      IT AND THE VOLSER IN THE UCB.  IT THEN RETURNS CONTROL TO THE
*      APPROPRIATE SCRATCH OR RENAME MODULE WITH A RETURN CODE OF 0.
*
* NOTES = (SEE BELOW)
*
*      DEPENDENCIES =
*
*           THE FIRST 364 BYTES OF THE SCRATCH AND RENAME WORK AREAS
*           (ADDRESSED RESPECTIVELY BY MAPPING MACROS IECSCRWA AND
*           IECRENWA) ARE MAPPED IDENTICALLY - 8 WORD PREFIX,20 WORD
*           EXTENDED PREFIX, 4 WORD DADSM WTG TABLE, 96 BYTE DSCB
*           AREA, AND 140 BYTE DSCB AREA.  THE 294 BYTES FROM LABELS
*           'WKADEB' THROUGH 'PDSNAME' ARE ALSO MAPPED IDENTICALLY.
*
*      RESTRICTIONS = NONE
*
*      REGISTER CONVENTIONS =
*
*           REGISTER 2 POINTS TO THE DEB.
*           REGISTER 3 POINTS TO THE VOLUME SERIAL NUMBER.
*           REGISTER 4 CONTAINS THE LAST FOUR CHARACTERS OF THE
*           RETURN MODULE NAME.
*           REGISTER 5 CONTAINS THE NUMBER OF VOLUMES REMAINING
*           TO BE SCRATCHED OR RENAMED.
*           REGISTER 8 POINTS TO THE CURRENT ENTRY IN THE VOLUME LIST.
*           REGISTER 9 POINTS TO THE CVT.
*           REGISTER 11 POINTS TO THE UCB.
*           REGISTER 12 IS THE BASE REGISTER.
*           REGISTER 13 POINTS TO THE EXTENDED PREFIX PORTION OF THE
*           SCRATCH OR RENAME WORK AREA.
*
*      PATCH LABEL = IHB0028D
*
* MODULE TYPE = CONTROL
*
*      PROCESSOR = ASSEMBLER
*
*      MODULE SIZE = LESS THAN 4096 BYTES
*
*      ATTRIBUTES = REENTRANT, REFRESHABLE, ENABLED, PRIVILEGED
*
* ENTRY POINT =
*
*      IGG0290F - ENTERED FROM A SCRATCH MODULE
*      IGG0300F - ENTERED FROM A RENAME MODULE
*
*      PURPOSE = SEE FUNCTION
*
*      LINKAGE =
*
*           THIS MODULE IS BRANCHED TO BY USE OF THE IECRES MACRO
*           FROM MODULES IGG0290D, IGG0290E, IGG03001, AND IGG03002.
*
* INPUT =
*
*      REGISTER 2 POINTS TO THE DEB IN THE SCRATCH OR RENAME WORK AREA.
*      REGISTER 3 POINTS TO THE VOLSER OF THE VOLUME TO BE MOUNTED (IF
*      A VOLUME IS TO BE MOUNTED AND/OR VERIFIED) OR TO THE VOLSER OF
*      THE VOLUME CURRENTLY MOUNTED (IF A VOLUME IS TO BE DEMOUNTED
*      AND DEQ'ED ONLY).
*      REGISTER 4 CONTAINS THE LAST FOUR CHARACTERS OF THE RETURN
*      MODULE NAME.
*      REGISTER 5 CONTAINS THE NUMBER OF VOLUMES REMAINING TO BE
*      SCRATCHED OR RENAMED.
*      REGISTER 8 POINTS TO THE CURRENT ENTRY IN THE VOLUME LIST.
*      REGISTER 9 POINTS TO THE CVT.
*      REGISTER 10 POINTS TO THE SCRATCH OR RENAME PARAMETER LIST.
*      REGISTER 11 POINTS TO THE UCB.
*      REGISTER 13 POINTS TO THE EXTENDED PREFIX PORTION OF THE
*      SCRATCH OR RENAME WORK AREA.
*
* OUTPUT =
*
*      THE FOLLOWING MESSAGES MAY BE ISSUED:
*
*      TO DEMOUNT A VOLUME IF THE MONITOR SPACE COMMAND IS ACTIVE -
*
*           IEC502E XX UUU,VOLSER,SPACE=CCCC,TTTT,AAAA/YYYY,ZZZZ
*           IEC502E JOBNAME,STEPNAME
*
*      WHERE XX CAN BE EITHER K (KEEP) OR RK (RETAIN KEEP) FOR A
*           PRIVATE VOLUME OR EITHER D (DELETE) OR RD (RETAIN DELETE)
*           FOR A PUBLIC VOLUME.
*           UUU IS THE CHANNEL/UNIT ADDRESS.
*           VOLSER IS THE VOLUME SERIAL NUMBER.
*           CCCC IS THE TOTAL NUMBER OF FREE CYLINDERS ON THE VOLUME.
*           TTTT IS THE TOTAL NUMBER OF UNALLOCATED TRACKS IN ADDITION
*           TO THE FREE CYLINDERS.
*           AAAA IS THE NUMBER OF EXTENTS IN THE FORMAT 5 DSCB(S).
*           YYYY IS THE NUMBER OF CYLINDERS IN THE LARGEST EXTENT OF
*           UNALLOCATED SPACE ON THE VOLUME.
*           ZZZZ IS THE NUMBER OF TRACKS IN ADDITION TO THE CYLINDERS
*           IN THE LARGEST EXTENT OF UNALLOCATED SPACE.
*
*      TO DEMOUNT A VOLUME IF THE MONITOR SPACE COMMAND IS NOT ACTIVE
*      OR TO REJECT A VOLUME -
*
*           IEC502E XX UUU,VOLSER,JOBNAME,STEPNAME
*
*      WHERE XX CAN BE K, RK, D, RD, OR R (REJECT).
*
*      TO MOUNT A VOLUME -
*
*           IEC601D M UUU,VOLSER,JOBNAME,STEPNAME - REPLY U OR M.
*
*      THE OPERATOR CAN REPLY EITHER U (TO MOUNT AND USE THE VOLUME)
*      OR M (TO SKIP MOUNTING THIS VOLUME).
*
*      TO TELL A TERMINAL THAT A VOLUME MUST BE MOUNTED -
*
*           IEC108I OPERATOR ACTION HAS BEEN REQUESTED FOR YOUR
*           DATA SET.
*
* NOTE: NO MOUNT OR DEMOUNT MESSAGES ARE ISSUED TO THE OPERATOR
*      IF THE MOUNT OR DEMOUNT IS FOR AN MSS  SUBSYSTEM VIRTUAL
*      DEVICE.  IN THIS CASE, MESSAGES TO A TSO TEMINAL WILL
*      ALSO BE BYPASSED.
*
*        -IF AN ERROR REASON CODE IS RETURNED FROM THE MSS
*         (DIS) MOUNT THE REASON CODE IS PRINTED IN MSG
*         IEC666I JJJJJJJJ,VOLSER,DDD MSS FAILURE IN (DIS)-
*         MOUNT. CODE=HHH -WHERE HHH EQUALS THE REASON CODE
*
*
*      THE FOLLOWING REGISTERS ARE RETURNED TO THE CALLING MODULE -
*
*      REGISTER 5 CONTAINS THE NUMBER OF VOLUMES REMAINING TO BE
*      SCRATCHED OR RENAMED.
*      REGISTER 8 POINTS TO THE CURRENT ENTRY IN THE VOLUME LIST.
*      REGISTER 9 POINTS TO THE CVT.
*      REGISTER 10 POINTS TO THE SCRATCH OR RENAME PARAMETER LIST.
*      REGISTER 11 POINTS TO THE UCB.
*      REGISTER 13 POINTS TO THE EXTENDED PREFIX PORTION OF THE
*      SCRATCH OR RENAME WORK AREA.
*      REGISTER 14 CONTAINS ONE OF THE FOLLOWING RETURN CODES:
*      00 - VOLUME SUCCESSFULLY DEMOUNTED, MOUNTED, AND/OR VERIFIED
*      04 - VOLUME COULD NOT BE DEMOUNTED
*      08 - VOLUME COULD NOT BE MOUNTED
*      0C - I/O ERROR OCCURRED WHEN READING THE VOLUME LABEL
*
* EXITS - NORMAL = BRANCH VIA IFG019RA (THE OPEN/CLOSE/EOV/DADSM
*      RESIDENT ROUTINE) WITH A RETURN CODE OF ZERO TO THE MODULE
*      THAT PASSED CONTROL TO IGG0190F.
*
* EXITS - ERROR = BRANCH VIA IFG019RA WITH AN ERROR RETURN CODE (AS
*      DESCRIBED UNDER 'OUTPUT') TO THE MODULE THAT PASSED CONTROL TO
*      IGG0290F.
*
* EXTERNAL REFERENCES = (SEE BELOW)
*
*      ROUTINES = EXCP    - EXECUTES A CHANNEL PROGRAM (SVC 0)
*                 WAIT    - WAITS ON AN EVENT CONTROL BLCOK (SVC 1)
*                 DEQ     - DEQ'S FROM SYSZVOLS (SVC 48)
*                 ENQ     - ENQ'S ON SYSZVOLS (SVC 56)
*                 WTO     - WRITES A MESSAGE TO THE OPERATOR (SVC 35)
*                 WTOR    - WRITES A MESSAGE WITH REPLY (SVC 35)
*                 LSPACE  - BUILDS AN SMF RECORD 19 AND/OR ADDS SPACE
*                           INFORMATION TO A DEMOUNT MESSAGE (SVC 78)
*                 VOLSTAT - WRITES A VOLUME STATISTICS RECORD TO
*                           SYS1.LOGREC (SVC 91)
*                 TPUT    - ISSUES A MESSAGE TO A TERMINAL (SVC 93)
*                 IECRES  - LOADS/BRANCHES TO ANOTHER MODULE
*                 IOSGEN  - SETS THE NOT READY BIT IN THE UCB
*                 IEFSSREQ- ISSUES A REQUEST TO JES3 FOR VOLUME
*                           AWARENESS FOR VIRTUAL VOLUMES.
*                 ICBMNTDE- TO MOUNT OR DEMOUNT A VIRTUAL VOLUME.
*                           AWARENESS FOR VIRTUAL VOLUMES.
*                 ICBMNTDE- TO MOUNT OR DEMOUNT A VIRTUAL VOLUME.
*                 SYSEVENT- PREVENTS/ENABLES MEMORY SWAPOUT
*
*      DATA AREAS = SCRATCH OR RENAME VOLUME LIST
*
*      CONTROL BLOCKS = ASCB
*                       BASE
*                       CVT
*                       DSAB
*                       JSCB
*                       PSA
*                       PSCB
*                       SMCA
*                       TCB
*                       TIOT
*                       UPT
*                       UCB
*                       JCT
*                       JESCT
*                       SCT
*                       JCT
*                       JESCT
*                       SCT
*
* TABLES = SCRATCH OR RENAME WORK AREA
*
* MACROS = IECDSECS - EXPANDS THE ASCB, BASE, CVT, DSAB, JSCB, PSA,
*                     PSCB, SMF, TCB, TIOT, UPT, AND UCB DSECTS
*          IECDIOCM - EXPANDS THE IOS COMMUNICATIONS AREA
*          IECRENWA - EXPANDS THE RENAME WORK AREA
*          IECPDSCT - EXPANDS PROBLEM DETERMINATION MSG DSECT
*          IECRES   - LOADS AND BRANCHES TO THE CALLING MODULE
*          IEFAJCTB - EXPANDS THE JOB CONTROL TABLE
*          IEFASCTB - EXPANDS THE STEP CONTROL TABLE
*          IEFJESCT - EXPANDS THE JES CONTROL TABLE
*          IEFJESCT - EXPANDS THE JES CONTROL TABLE
*          XCTLTABL - BUILDS A LIST OF MODULE NAMES AND ADDRESSES
*
* CHANGE ACTIVITY =
*
*      SEE THE 'CHANGES/DELETIONS' SECTION FOLLOWING THE CSECT.
*
***********************************************************************
         EJECT                                                   Y02134
***********************************************************************
*
* REGISTER EQUATES -
*
R0       EQU   0                        EQUATE FOR REGISTER 0    Y02134
R1       EQU   1                        EQUATE FOR REGISTER 1    Y02134
R2       EQU   2                        EQUATE FOR REGISTER 2    Y02134
RDEB     EQU   2                        POINTER TO WKADEB        Y02134
R3       EQU   3                        EQUATE FOR REGISTER 3    Y02134
RVOLSER  EQU   3                        POINTER TO VOL SER       Y02134
R4       EQU   4                        EQUATE FOR REGISTER 4    Y02134
RNAME    EQU   4                        CONTAINS RETURN MOD NAME Y02134
VOLCTR   EQU   5                        NUMBER OF REMAINING VOLS Y02134
R6       EQU   6                        EQUATE FOR REGISTER 6    Y02134
RDSAB    EQU   6                        POINTER TO THE DSAB      Y02134
RTCB     EQU   6                        POINTER TO THE TCB       Y02134
R7       EQU   7                        EQUATE FOR REGISTER 7    Y02134
RTIOT    EQU   7                        POINTER TO THE TIOT      Y02134
VOLISTX  EQU   8                        CURRENT VOLIST ENTRY PTR Y02134
R9       EQU   9                        EQUATE FOR REGISTER 9    Y02134
RCVT     EQU   9                        POINTER TO THE CVT       Y02134
RCODE    EQU   10                       RETURN CODE REGISTER     Y02134
PL       EQU   10                       PARAMETER LIST POINTER   Y02134
RSAVE    EQU   10                       POINTER TO SAVED REGS  @G18MSBS
RUCB     EQU   11                       UCB POINTER              Y02134
RBASE    EQU   12                       BASE REGISTER            Y02134
RWKAREA  EQU   13                       WORK AREA ADDRESS        Y02134
RETURN   EQU   14                       RETURN CODE REGISTER     Y02134
R14      EQU   14                       EQUATE FOR REGISTER 14   Y02134
RET      EQU   14                       EQUATE FOR REGISTER 14 @G18MSBS
R15      EQU   15                       EQUATE FOR REGISTER 15   Y02134
RF       EQU   15                       EQUATE FOR REGISTER 15 @G18MSBS
         SPACE 1                                                 Y02134
*
* OTHER EQUATES -
*
K0       EQU   0                        CONSTANT OF ZERO         Y02134
K1       EQU   1                        CONSTANT OF 1            Y02134
K2       EQU   2                        CONSTANT OF 2            Y02134
K3       EQU   3                        CONSTANT OF 3            Y02134
K4       EQU   4                        CONSTANT OF 4            Y02134
K5       EQU   5                        CONSTANT OF 5            Y02134
K6       EQU   6                        CONSTANT OF 6            Y02134
K7       EQU   7                        CONSTANT OF 7            Y02134
K8       EQU   8                        CONSTANT OF 8            Y02134
K9       EQU   9                        CONSTANT OF 9            Y02134
K12      EQU   12                       CONSTANT OF 12           Y02134
K24      EQU   24                       CONSTANT OF 24           YM7890
RTNCODE4 EQU   4                        RETURN CODE OF 4         Y02134
RTNCODE8 EQU   8                        RETURN CODE OF 8         Y02134
RTNCODEC EQU   12                       RETURN CODE OF 12        Y02134
LSPACE   EQU   78                       LSPACE SVC               Y02134
VOLSTAT  EQU   91                       VOLSTAT SVC              Y02134
DEMNTUUU EQU   15                       OFFSET TO UUU IN DEMOUNT Y02134
*                                       MESSAGE                  Y02134
MOUNTUUU EQU   22                       OFFSET TO UUU IN MOUNT   Y02134
*                                       MESSAGE                  Y02134
SPACELN  EQU   29                       LENGTH-1 OF LSPACE INFO  Y02134
REJECTJN EQU   25                       OFFSET TO JOB NAME IN    Y02134
*                                       REJECT MESSAGE           Y02134
DEMNTJN1 EQU   26                       OFFSET TO JOB NAME IN    Y02134
*                                       ONE LINE DEMOUNT MESSAGE Y02134
DEMNTJN2 EQU   76                       OFFSET TO JOB NAME IN    Y02134
*                                       TWO LINE DEMOUNT MESSAGE Y02134
HIORDER  EQU   X'80'                    HI-ORDER BIT OF A BYTE @G18MSBS
SWAPREFX EQU   16                       PREFIX OF QUEUE ELEMENT@G18MSBS
MOUNTJN  EQU   33                       OFFSET TO JOB NAME IN    Y02134
*                                       MOUNT MESSAGE            Y02134
SMFFLAG  EQU   X'80'                    SMF FLAG FOR LSPACE      Y02134
LASTNTRY EQU   X'80'                    LAST ENTRY INDICATOR     Y02134
GOODIO   EQU   X'20'                    TEST FOR SUCCESSFUL I/O  Y02134
BLANK    EQU   X'40'                    EBCDIC BLANK             Y02134
CHARD    EQU   C'D'                     EBCDIC CHARACTER D       Y02134
CHARK    EQU   C'K'                     EBCDIC CHARACTER K       Y02134
CHARR    EQU   C'R'                     EBCDIC CHARACTER R       Y02134
REPLYM   EQU   C'M'                     REPLY OF M TO MOUNT MSG  Y02134
REPLYU   EQU   C'U'                     REPLY OF U TO MOUNT MSG  Y02134
*
D0       EQU   0                        DISPLACEMENT OF ZERO   @Y30LSTG
D2       EQU   2                        DISPLACEMENT OF ONE    @Y30LSTG
D3       EQU   3                        DISPLACEMENT OF THREE  @Y30LSTG
D4       EQU   4                        DISPLACEMENT OF FOUR   @Y30LSTG
D6       EQU   6                        DISPLACEMENT OF SIX    @Y30LSTG
D8       EQU   8                        DISPLACEMENT OF EIGHT  @Y30LSTG
D11      EQU   11                       DISPLACEMENT OF ELEVEN @Y30LSTG
D14      EQU   14                       DISPLACEMENT OF FOURTEN@Y30LSTG
D17      EQU   17                       DISPLA OF SEVENTEEN    @Y30LSTG
D24      EQU   24                       LENGTH OF TWENTY-FOUR  @Y30LSTG
D43      EQU   43                       DISP OF FORTY-THREE    @Y30LSTG
D46      EQU   46                       DISP OF FORTY-SIX      @Y30LSTG
D58      EQU   58                       DISP OF FIFTY-EIGHT    @Y30LSTG
D60      EQU   60                       DISP OF SIXTY          @Y30LSTG
D72      EQU   72                       SEVENTY-TWO            @Y30LSTG
L2       EQU   2                        LENGTH OF TWO          @Y30LSTG
L3       EQU   3                        LENGTH OF THREE        @Y30LSTG
L6       EQU   6                        LENGTH OF SIX          @Y30LSTG
L8       EQU   8                        LENGTH OF EIGHT        @Y30LSTG
L18      EQU   18                       LENGTH OF EIGHTEEN     @Y30LSTG
L19      EQU   19                       LENGTH OF NINETEEN     @Y30LSTG
L28      EQU   28                       LENGTH OF TWENTY-EIGHT @ZM33363
TWO40    EQU   240                      VALUE OF 2-HUNDRED 40  @Y30LSTG
DECZR0   EQU   X'F0'                    DECIMAL ZERO           @Y30LSTG
***********************************************************************
         SPACE 1                                                 Y02134
         BALR  RBASE,K0                 ESTABLISH ADDRESSABILITY Y02134
         USING MNT00100,RBASE           CSECT ADDRESSABILITY     Y02134
         USING RENAMWKA,RWKAREA         WORK AREA ADDRESSABILITY Y02134
         USING WKADEB,RDEB              IOB & CCW ADDRESSABILITY Y02134
         USING UCB,RUCB                 UCB ADDRESSABILITY       Y02134
         USING CVT,RCVT                 CVT ADDRESSABILITY       Y02134
         EJECT                                                   Y02134
*
* THIS SECTION DETERMINES IF THE UCB IS ELIGIBLE FOR DEMOUNTING.
*
MNT00100 EQU   *                        TEST IF DEMOUNTABLE      Y02134
         TM    SISW2,VERIFYSW           TEST IF THE VOLUME IS TO Y02134
*                                       BE MERELY VERIFIED       Y02134
         BO    MNT04000                 BRANCH IF YES            Y02134
         TM    UCBDMCT,UCBDMC           CHECK FOR ANY USERS      Y02134
         BNZ   MNT01550                 BRANCH IF YES - VOLUME   Y02134
*                                       CANNOT BE DEMOUNTED      Y02134
         TM    UCBSTAT,UCBSYSR+UCBRESV+UCBPRES  TEST IF SYSRES,  Y02134
*                                       RESERVED, OR PERMANENT   Y02134
         BNZ   MNT01550                 BRANCH IF ANY OR ALL -   Y02134
*                                       VOL CANNOT BE DEMOUNTED  Y02134
         TM    UCBSTAB,UCBBSVL          TEST IF SHARED VOLUME    Y02134
         BZ    MNT01550                 BRANCH IF YES - VOLUME   Y02134
*                                       CANNOT BE DEMOUNTED      Y02134
         TM    UCBSTAT,UCBONLI          TEST IF ONLINE           Y02134
         BZ    MNT01550                 BRANCH IF NOT - VOLUME   Y02134
*                                       CANNOT BE DEMOUNTED      Y02134
*
* IF A VOLUME IS TO BE MOUNTED, THIS SECTION ISSUES AN EXCLUSIVE ENQ
* DIRECTED TO THE INITIATOR'S TCB WITH A MAJOR RESOURCE NAME OF
* SYSZVOLS AND THE VOLUME SERIAL NUMBER AS THE MINOR RESOURCE NAME.
* IF THE RETURN CODE INDICATES THAT THE TASK PREVIOUSLY HAD CONTROL
* OF THE RESOURCE, THE ENQ IS REISSUED TO OBTAIN EXCLUSIVE CONTROL.
*
MNT00200 EQU   *                        ISSUE ENQ RET=USE        Y02134
         TM    SISW2,MOUNTSW            SEE IF VOL TO BE MOUNTED Y02134
         BNO   MNT00400                 BRANCH IF NOT            Y02134
         MVC   ENQAREA(ENQLTH1),ENQLIST1  MOVE IN PARAMETER LIST Y02134
         MVC   MJNAME(L'SYSZVOLS),SYSZVOLS  MAJOR RESOURCE NAME  Y02134
         L     RTCB,TCBADDR             LOAD TCB ADDRESS         Y02134
         USING TCB,RTCB                 TCB ADDRESSABILITY       Y02134
         L     RTCB,TCBJSCB             LOAD JSCB ADDRESS        Y02134
         USING IEZJSCB,RTCB             JSCB ADDRESSABILITY      Y02134
         L     RTCB,JSCBACT             GET ACTIVE JSCB ADDRESS  YM2887
         L     RTCB,JSCBTCBP            INITIATOR TCB ADDRESS    Y02134
         ENQ   (MJNAME,(RVOLSER)),TCB=(RTCB),RET=USE,            Y02134X
               MF=(E,ENQAREA)           ISSUE ENQ ON SYSZVOLS    Y02134
         LTR   R15,R15                  TEST RETURN CODE         Y02134
         BNZ   MNT00250                 BRANCH IF ENQ FAILED     Y02134
         OI    SISW1,VOLENQ             SET SYSZVOLS ENQ SWTCH @ZA05596
         B     MNT00300                 GO TEST IF MOUNT PENDING Y02134
*                                                                Y02134
MNT00250 EQU   *                        ISSUE ENQ RET=CHNG       Y02134
         CLI   ENQAREA+K3,RTNCODE8      TEST IF TASK ALREADY     Y02134
*                                       ENQ'ED ON THIS VOL SER   Y02134
         BNE   MNT01650                 BRANCH IF NOT            Y02134
         MVC   ENQAREA(ENQLTH2),ENQLIST2  MOVE IN PARAMETER LIST Y02134
         ENQ   (MJNAME,(RVOLSER)),TCB=(RTCB),RET=CHNG,           Y02134X
               MF=(E,ENQAREA)           REISSUE THE ENQ          Y02134
         LTR   R15,R15                  TEST RETURN CODE         Y02134
         BNZ   MNT01650                 BRANCH IF NOT SUCCESSFUL Y02134
*                                                                Y02134
MNT00300 EQU   *                        TEST IF MOUNT PENDING    Y02134
         TM    UCBDMCT,UCBMOUNT         TEST IF MOUNT PENDING    Y02134
         BO    MNT04000                 BRANCH IF YES TO VERIFY  Y02134
*                                       THE VOLUME               Y02134
*
* IF A VOLUME IS CURRENTLY MOUNTED, THIS SECTION ISSUES DEMOUNT
* MESSAGE IEC502E.
*
MNT00400 EQU   *                        TEST FOR NOTHING MOUNTED Y02134
         CLC   UCBVOLI,ZEROS            TEST FOR NO VOL SER      Y02134
         BE    MNT01000                 BRANCH, NOTHING MOUNTED  Y02134
         CLC   UCBVOLI,BLANKS           TEST FOR BLANK VOL SER   Y02134
         BE    MNT01000                 BRANCH, NOTHING MOUNTED  Y02134
         BAL   RETURN,MNT11000          LINK TO MOVE IN DEMOUNT  Y02134
*                                       MESSAGE AND ISSUE SVC 78 Y02134
*                                       (LSPACE) AND SVC 91      Y02134
* IF THE VOLUME IS ON A VIRTUAL MSS  SYSTEM, THE DEMOUNT       @Y30LSTG
* IS INTERCEPTED AND A MSG SENT TO MSS  INSTEAD.               @Y30LSTG
*                                                              @Y30LSTG
         TM    UCBTBYT2,UCBRVDEV        IS IT A VIRTUAL DEVICE @Y30LSTG
         BNO   MNT00420                 NO, DO ORDINARY DEMOUNT@Y30LSTG
         BAL   RETURN,MNT11230          GO SETUP DEMOUNT MSG   @Y30LSTG
         BAL   RETURN,MNT11260          GO ASK FOR A DEMOUNT   @Y30LSTG
         BAL   RETURN,MNT11300          GO CLEAR OUT UCB       @ZA04505
         B     MNT01000                 SKIP NORMAL MSG        @Y30LSTG
MNT00420 EQU   *                        BRANCH TO LABEL          Y02134
         MVC   MSGINDIC(K2),BLANKS      CLEAR XX FIELD           Y02134
         TM    UCBJBNR,UCBMONT          TEST FOR DISP=PASS       Y02134
         BNO   MNT00430                 BRANCH IF NOT            Y02134
         MVI   MSGINDIC,CHARR           MOVE IN PASS INDICATOR   Y02134
*                                                                Y02134
MNT00430 EQU   *                        TEST FOR PRIVATE         Y02134
         TM    UCBSTAB,UCBBPRV          TEST FOR PRIVATE         Y02134
         BNO   MNT00440                 BRANCH IF NOT            Y02134
         MVI   MSGINDIC+K1,CHARK        MOVE IN KEEP INDICATOR   Y02134
         B     MNT00470                 GO COMPLETE THE MESSAGE  Y02134
*                                                                Y02134
MNT00440 EQU   *                        TEST FOR PUBLIC          Y02134
         TM    UCBSTAB,UCBBPUB          TEST FOR PUBLIC          Y02134
         BNO   MNT00450                 BRANCH IF NOT            Y02134
         MVI   MSGINDIC+K1,CHARD        MOVE IN DELETE INDICATOR Y02134
         B     MNT00470                 GO COMPLETE THE MESSAGE  Y02134
*                                                                Y02134
MNT00450 EQU   *                        TEST IF DISP SPECIFIED   Y02134
         CLI   MSGINDIC+K1,BLANK        TEST IF DISP SPECIFIED   Y02134
         BNE   MNT00470                 BRANCH IF SPECIFIED      Y02134
         CLI   MSGINDIC,CHARR           CHECK FOR PASS           Y02134
         BNE   MNT00460                 BRANCH - MUST BE REJECT  Y02134
         MVI   MSGINDIC+K1,CHARD        SET INDICATOR TO RD      Y02134
         B     MNT00470                 BRANCH TO FINISH MESSAGE Y02134
*                                                                Y02134
MNT00460 EQU   *                        SET INDICATOR TO REJECT  Y02134
         MVI   MSGINDIC,CHARR           SET INDICATOR TO REJECT  Y02134
*                                                                Y02134
MNT00470 EQU   *                        MOVE IN UUU AND VOLSER   Y02134
         LA    R1,F1DSCB+DEMNTUUU       POINT TO UUU AND VOLSER  Y02134
         BAL   RETURN,MNT11200          LINK TO GET UUU & VOLSER Y02134
*                                                                Y02134
         BAL   RETURN,MNT11300          LINK TO CLEAR UCB        Y02134
*                                                                Y02134
         LA    R1,F1DSCB+DEMNTJN1       POINT TO JOB NAME FOR    Y02134
*                                       ONE LINE DEMOUNT MSG     Y02134
         CLC   F1DSCB(K4),DMNTMSG2      TEST IF TWO LINE MSG     Y02134
         BNE   MNT00480                 BRANCH IF NOT            Y02134
         LA    R1,F1DSCB+DEMNTJN2       POINT TO JOB NAME FOR    Y02134
*                                       TWO LINE DEMOUNT MSG     Y02134
*                                                                Y02134
MNT00480 EQU   *                        MOVE IN JOB & STEP NAMES Y02134
         BAL   RETURN,MNT11400          LINK TO GET JOB NAME AND Y02134
*                                       STEP NAME FOR MESSAGE    Y02134
*                                                                Y02134
         BAL   RETURN,MNT11500          LINK TO ISSUE WTO        Y02134
*                                                                Y02134
*
* THIS SECTION LINKS TO A SUBROUTINE THAT ISSUES A DEQ FROM SYSZVOLS
* IF THE VOLUME BEING DEMOUNTED MUST BE DEQ'ED.
*
MNT01000 EQU   *                        LINK TO DEQ TEST         Y02134
         BAL   RETURN,MNT10000          LINK TO TEST IF VOLUME   Y02134
*                                       MUST BE DEQ'ED           Y02134
*
* IF THE RETURN CODE FROM THE ENQ ON SYSZVOLS INDICATED THAT THE
* TASK DID NOT PREVIOUSLY HAVE CONTROL OF THE RESOURCE, THIS SECTION
* INITIALIZES VARIOUS FIELDS FOR A SUBSEQUENT DEQ.
*
MNT01400 EQU   *                        SEE IF VOL TO BE MOUNTED Y02134
         TM    SISW2,MOUNTSW            SEE IF VOL TO BE MOUNTED Y02134
         BNO   MNT01500                 BRANCH IF NOT            Y02134
         TM    SISW1,VOLENQ             TEST IF VOLUME ENQ'ED    Y02134
*                                       BY FIRST ENQ             Y02134
         BNO   MNT03000                 BRANCH IF NOT            Y02134
         OI    SISW1,VOLDEQ             SET VOL TO BE DEQ'ED SW  Y02134
         NI    SISW1,X'FF'-VOLENQ       RESET VOL ENQ'ED SWITCH  Y02134
         MVC   VOLMINOR,K0(RVOLSER)     SAVE VOL SER FOR DEQ     Y02134
         B     MNT03000                 GO MOUNT THE VOLUME      Y02134
*
* THIS SECTION LOADS THE APPROPRIATE RETURN CODE.  AFTER LINKING TO
* A SUBROUTINE THAT ISSUES A DEQ FROM SYSZVOLS IF NECESSARY, THIS
* SECTION RETURNS TO THE CALLING MODULE.
*
MNT01500 EQU   *                        NO ERROR                 Y02134
         XR    RCODE,RCODE              RETURN CODE OF ZERO      Y02134
         B     MNT02000                 GO RETURN                Y02134
*                                                                Y02134
MNT01550 EQU   *                        UNABLE TO DEMOUNT VOLUME Y02134
         LA    RCODE,RTNCODE4           RETURN CODE OF FOUR      Y02134
         TM    SISW2,MOUNTSW+VERIFYSW   SEE IF VOL TO BE MOUNTED Y02134
*                                       AND/OR VERIFIED          Y02134
         BNZ   MNT02000                 YES, GO RETURN           Y02134
         B     MNT01800                 NO, DEMOUNT ONLY - GO    Y02134
*                                       TEST IF VOL TO BE DEQ'ED Y02134
*                                                                Y02134
MNT01600 EQU   *                        OPERATOR CAN'T MOUNT VOL Y02134
         BAL   RETURN,MNT10000          TEST IF VOL TO BE DEQ'ED Y02134
*                                                                Y02134
MNT01650 EQU   *                        UNABLE TO MOUNT VOLUME   Y02134
         LA    RCODE,RTNCODE8           RETURN CODE OF EIGHT     Y02134
         B     MNT02000                 GO RETURN                Y02134
*                                                                Y02134
MNT01700 EQU   *                        I/O ERROR                Y02134
         LA    RCODE,RTNCODEC           RETURN CODE OF TWELVE    Y02134
         TM    SISW2,VERIFYSW           TEST IF VERIFY ONLY      Y02134
         BO    MNT02000                 YES, GO RETURN           Y02134
*                                                                Y02134
MNT01800 EQU   *                        LINK TO DEQ TEST         Y02134
         BAL   RETURN,MNT10000          LINK TO DEQ TEST         Y02134
*                                                                Y02134
MNT02000 EQU   *                        SET UP RETURN NAME       Y02134
         LA    R7,SCRATCH1              POINT TO NAME IGG0290D   Y02134
         CL    RNAME,SCRATCH1+K4        TEST IF RETURN NAME      Y02134
         BE    MNT02100                 BRANCH IF YES            Y02134
*                                                                Y02134
         LA    R7,SCRATCH2              POINT TO NAME IGG0290E   Y02134
         CL    RNAME,SCRATCH2+K4        TEST IF RETURN NAME      Y02134
         BE    MNT02100                 BRANCH IF YES            Y02134
*                                                                Y02134
         LA    R7,RENAME1               POINT TO NAME IGG03001   Y02134
         CL    RNAME,RENAME1+K4         TEST IF RETURN NAME      Y02134
         BE    MNT02100                 BRANCH IF YES            Y02134
*                                                                Y02134
         LA    R7,RENAME2               POINT TO NAME IGG03002   Y02134
*                                                                Y02134
MNT02100 EQU   *                        RETURN TO SCRATCH/RENAME Y02134
         NI    SISW2,X'FF'-VIRTMNT      TURN OFF VIRT MNT BIT  @Y30LSTG
         LR    RETURN,RCODE             RETURN CODE IN REG 14    Y02134
         L     RCVT,CVTPTR              RELOAD CVT ADDRESS       Y02134
         L     PL,OLDPLPTR              RELOAD PARM LIST ADDRESS Y02134
         IECRES LOAD,EXTPR=(RWKAREA),MODNM=(R7),BRANCH=DIRECT    Y02134
         EJECT
         SPACE 3
***********************************************************************
*
* THIS SECTION ISSUES MOUNT MESSAGE IEC601D UNLESS THE DEVICE  @Y30LSTG
* IS AN MSS  SUBSYSTEM PACK.  IF IT IS A VIRTUAL VOLUME, ISSUE @G18MSBS
* A REQUEST TO JES3( IF THE UCB INDICATES A JES3 MANAGED UNIT) @G18MSBS
* FOR VOLUME AWARENESS.  AND THEN ISSUE THE ICBMNTDE MACRO     @G18MSBS
* (SVC 126) WHICH WILL INTERFACE WITH THE SUBSYSTEM TO MOUNT   @G18MSBS
* THE VIRTUAL VOLUME.                                          @G18MSBS
*
***********************************************************************
*
MNT03000 EQU   *                        ISSUE IEC601D            Y02134
         BAL   RETURN,MNT11600          LINK TO SET MOUNT BIT    Y02134
*                                       AND TO PUT VOLSER IN UCB Y02134
*                                                                Y02134
         TM    UCBTBYT2,UCBRVDEV        IS IT A VIRTUAL DEVICE @Y30LSTG
         BNO   MNT03010                 NO, ALL SHOULD WORK    @ZM30032
         SPACE 1                                               @G18MSBS
         TM    UCBJBNR,UCBJES3          DOES JES3 MANAGE UNIT? @G18MSBS
         BNO   MNT03005                 NO, BYPASS JES3 ROUTINE@G18MSBS
         EJECT
*                                                              @G18MSBS
***************************************************************@G18MSBS
*                                                              @G18MSBS
*      **************************                              @G18MSBS
*      * JES3 INTERFACE ROUTINE *                              @G18MSBS
*      **************************                              @G18MSBS
*                                                              @G18MSBS
*        ON ENTRY -                                            @G18MSBS
*                                                              @G18MSBS
*        THIS ROUTINE WILL BUILD THE SSOB HEADER AND EXTENSION @G18MSBS
*        PARAMETER LIST AND PASS IT TO JES3 VIA THE MACRO:     @G18MSBS
*        'IEFSSREQ', OFFERING THE VOLUME CHOICE TO JES3 FOR    @G18MSBS
*        APPROVAL.  UPON RETURN FROM JES3 THE 'SSOBRETN' FIELD @G18MSBS
*        IS TESTED FOR ZERO.  ZERO ALLOWS SCRATCH/RENAME TO    @G18MSBS
*        CONTINUE AND A NON-ZERO INDICATES JES3 DETERMINED     @G18MSBS
*        THAT VOLUME COULD NOT BE USED BY THIS JOB NOW. THE    @G18MSBS
*        RETURN CODE WILL BE SET TO EIGHT AND RETURN BACK TO   @G18MSBS
*        THE CALLER OF SCRATCH/RENAME.                         @G18MSBS
***************************************************************@G18MSBS
*                                                              @G18MSBS
        SPACE
         STM   R0,RET,RENAMWKA          SAVE REGISTERS         @G18MSBS
         LR    RSAVE,RWKAREA            SAVE SAVEAREA PTR      @G18MSBS
         LA    R1,VTOCDSCB              GET THE WORK AREA      @G18MSBS
         USING SSOB,R1                  ESTAB ADDRESSABILITY   @G18MSBS
         XC    SSOB(SSOBSIZE),SSOB      CLEAR OUT WORK AREA    @G18MSBS
         ST    R1,DASSOBAD              SAVE PARM POINTER      @G18MSBS
*                                       SET HI-ORDER BIT TO    @G18MSBS
         OI    DASSOBAD,HIORDER         REQUEST THE SSOB MACRO @G18MSBS
         MVC   SSOBID,DASSOBID          ID OF SSOB             @G18MSBS
         MVI   SSOBLEN+K1,SSOBHSIZ      LENGTH OF SSOBHEADER   @G18MSBS
         MVI   SSOBFUNC+K1,SSOBOEOV     SET FUNCTION ID        @G18MSBS
*                                       INDICATE FINAL REQUEST @G18MSBS
         OI    SSMOFLG1,SSMOFINL+SSMOSCR FROM DADSM SCRATCH    @G18MSBS
        SPACE
         LA    RF,SSMOBGN               GET FUNCTION DEPENDENT @G18MSBS
         ST    RF,SSOBINDV              AREA PTR AND SAVE IT   @G18MSBS
         MVI   SSMOLEN+K1,SSMOSIZE      LENGTH OF EXTENSION    @G18MSBS
         L     RTCB,TCBADDR             LOAD TCB ADDRESS       @G18MSBS
         USING TCB,RTCB                 TCB ADDRESSABILITY     @G18MSBS
         L     RTCB,TCBJSCB             LOAD JSCB ADDRESS      @G18MSBS
         USING IEZJSCB,RTCB             JSCB ADDRESSABILITY    @G18MSBS
         L     RTCB,JSCBACT             GET ACTIVE JSCB ADDRESS@G18MSBS
         L     RF,JSCBJCT               GET JCT PTR FROM JSCB  @G18MSBS
         LA    RF,SWAPREFX+JCTJNAME-JCTABLE(,RF) GET PTR OF    @G18MSBS
*                                       JOB NAME               @G18MSBS
         ST    RF,SSMOPNAM              AND SAVE IT FOR SSOB   @G18MSBS
         L     RF,JSCSCT                GET SCT PTR FROM JSCB  @G18MSBS
         LA    RF,SWAPREFX+SCTSNUMB-SCTABLE(,RF) GET PTR OF    @G18MSBS
*                                       STEP NUMBER            @G18MSBS
         ST    RF,SSMOPSTN              AND SAVE IT FOR SSOB   @G18MSBS
         LA    RF,UCBNAME               GET PTR OF UNIT ADDRESS@G18MSBS
         ST    RF,SSMOPUAD              AND SAVE IT FOR SSOB   @G18MSBS
         LA    RF,UCBVOLI               GET PTR OF VOLSER      @G18MSBS
         ST    RF,SSMOPVOL              AND SAVE IT FOR SSOB   @G18MSBS
         LA    RWKAREA,SSOBSAVE         GET SAVE AREA FOR SSOB @G18MSBS
         LA    R1,DASSOBAD              GET ADDR OF PARM PTR   @G18MSBS
         DROP  R1,RTCB                                         @G18MSBS
        EJECT
*                                                              @G18MSBS
***************************************************************@G18MSBS
*                                                              @G18MSBS
**       PREPARE TO ISSUE MACRO: 'IEFSSREQ'                    @G18MSBS
*                                                              @G18MSBS
**       ON ENTRY:                                             @G18MSBS
*                                                              @G18MSBS
**       REGISTER  1 = POINTER TO SSOB PARAMETER LIST POINTER  @G18MSBS
**       REGISTER 10 = POINTER TO MY SAVE AREA TO RESTORE REGS @G18MSBS
**       REGISTER 13 = SAVE AREA TO BE USED BY JES3            @G18MSBS
*                                                              @G18MSBS
**       ON RETURN:                                            @G18MSBS
*                                                              @G18MSBS
**       REGISTER  0 = VOLATILE                                @G18MSBS
**       REGISTER  1 = POINTER TO SSOB PARAMETER LIST          @G18MSBS
**       REGISTER 10 = POINTER TO MY SAVE AREA TO RESTORE REGS @G18MSBS
**       REGISTER 15 = JES SSOB RETURN CODE(IGNORED-SEE BELOW) @G18MSBS
**       'SSOBRETN' CONTAINS A RETURN CODE:                    @G18MSBS
**                  0 = THIS VOLUME IS AVAILABLE FOR MOUNTING. @G18MSBS
**                  4 = JES3 HAS SOME OTHER USE FOR THAT VOL.  @G18MSBS
*                                                              @G18MSBS
***************************************************************@G18MSBS
*                                                              @G18MSBS
         IEFSSREQ                       ISSUE MACRO            @G18MSBS
        SPACE
         USING SSOB,R1                  ESTAB ADDRESSABILITY   @G18MSBS
         L     RF,SSOBRETN              GET JES3 FUNCT RET CODE@G18MSBS
         DROP  R1                                              @G18MSBS
         SPACE
         LM    R0,RET,K0(RSAVE)         RESTORE REGISTERS      @G18MSBS
         LTR   RF,RF                    TEST RET FROM IEFSSREQ @G18MSBS
         BNZ   MNT01600                 JES3 WON'T ALLOW THIS  @G18MSBS
*                                       VOLUME TO BE MOUNTED   @G18MSBS
         EJECT
MNT03005 EQU   *                        MSS MOUNT ROUTINE      @G18MSBS
         XC    VTOCDSCB(L28),VTOCDSCB   CLEAR OUT WORK AREA    @G18MSBS
         MVC   VTOCDSCB+D8(L6),UCBVOLI  MOVE IN VOLID          @G18MSBS
         MVC   VTOCDSCB+D14(L2),UCBCHA  MOVE IN CHAN AND UNIT  @G18MSBS
         LA    R1,VTOCDSCB              POINT TO THE AREA      @Y30LSTG
         ICBMNTDE TYPE=MNT,MF=(E,(1))   ISSUE THE MOUNT        @Y30LSTG
         LTR   R15,R15                  ANY ERRORS             @ZM30032
         BNZ   MSSERR                   YES, GO ISSUE MESSAGE  @ZM30032
         OI    SISW2,VIRTMNT            INDICATE VIRT.MNT      @Y30LSTG
         B     MNT04000                 BRANCH OUT             @ZM30032
MSSERR   LA    R7,VTOCDSCB+L28          BASE MSG REGISTER      @ZM33363
         USING PDDSECT,R7               BASE MESSAGE AREA      @Y30SLTG
         LR    R15,R4                   SAVE REG FOUR          @Y30LSTG
         LA    R1,VTOCDSCB              BASE REG ONE           @Y30LSTG
         L     RCVT,CVTPTR              INIT BASE              @Y30LSTG
         L     R4,CVTTCBP               LOCATE TCB PTRS        @Y30LSTG
         L     R4,D4(,R4)               GET CURRENT TCB        @Y30LSTG
         L     R4,TCBTIO-TCB(,R4)       GET TIOT START         @Y30LSTG
         MVC   PDMSGLNG(MSSMSGL),MSSMSG  PUT IN MSG            @Y30LSTG
         MVC   PDMSG+D8(L8),TIOCNJOB-TIOT(R4) ST JOBNAME       @Y30LSTG
         MVC   PDMSG+D24(L3),UCBNAME    SET DDD IN MSG         @Y30LSTG
         MVC   PDMSG+D17(L6),D8(R1)     SET VOLSER IN MSG      @Y30LSTG
         SLL   R0,D4                    SHIFT FOUR BITS        @Y30LSTG
         STH   R0,D8(R1)                RESET REASON CODE      @Y30LSTG
         UNPK  PDMSG+D58(L3),D8(L2,R1)  UNPACK REASON CODE     @Y30LSTG
         OI    PDMSG+D60,DECZR0         MAKE DIGIT VALID       @Y30LSTG
         TR    PDMSG+D58(L3),NUMTBLE    MAKE IT PRINTABLE      @Y30LSTG
         MVC   PDMSG+D43(L18),PDMSG+D46 OVERLAY DIS IN MSG     @Y30LSTG
         LR    R4,R15                   RESTORE REG FOUR       @Y30LSTG
         WTO   MF=(E,PDMSGLNG)          ISSUE WTO              @Y30LSTG
         XC    UCBVOLI,UCBVOLI          CLEAR UCB VOL SERIAL   @ZA20579
         BAL   RETURN,MNT11800          RESET MOUNT PENDING    @ZA20579
         B     MNT01600                 POST ERROR             @Y30LSTG
         DROP  R7                       DROP                   @Y30LSTG
MNT03010 EQU   *                                               @Y30LSTG
*                                                              @Y30LSTG
         BAL   RETURN,MNT11700          LINK TO TELL TERMINAL    Y02134
*                                                                Y02134
         MVC   VTOCDSCB(IEC601DL),IEC601D  MOVE IN MOUNT MESSAGE Y02134
         LA    R1,VTOCDSCB+MOUNTUUU     POINT TO UUU AND VOLSER  Y02134
         BAL   RETURN,MNT11200          LINK TO GET UUU & VOLSER Y02134
*                                                                Y02134
         LA    R1,VTOCDSCB+MOUNTJN      POINT TO JOB NAME FIELD  Y02134
         BAL   RETURN,MNT11400          LINK TO GET JOBNAME AND  Y02134
*                                       STEPNAME FOR MESSAGE     Y02134
*                                                                Y02134
MNT03020 EQU   *                        ISSUE WTOR               Y02134
         XC    ECB,ECB                  CLEAR THE ECB            Y02134
         MVI   FIELD1,K0                CLEAR THE REPLY AREA     Y02134
*                                                                Y02134
         WTOR  ,FIELD1,1,ECB,MF=(E,VTOCDSCB)  ISSUE WTOR         Y02134
*                                                                Y02134
         WAIT  ECB=ECB                  WAIT FOR COMPLETION      Y02134
*                                                                Y02134
         CLI   FIELD1,REPLYU            TEST IF MOUNTING VOLUME  Y02134
         BE    MNT04000                 BRANCH IF YES            Y02134
         CLI   FIELD1,REPLYM            TEST IF SKIPPING VOLUME  Y02134
         BNE   MNT03020                 BRANCH IF NOT            Y02134
*                                                                Y02134
MNT03040 EQU   *                        RESET MOUNT BIT          Y02134
         XC    UCBVOLI,UCBVOLI          CLEAR THE VOL SERIAL NBR Y02134
         BAL   RETURN,MNT11800          LINK TO RESET MOUNT BIT  Y02134
*                                                                Y02134
         B     MNT01600                 GO SET THE RETURN CODE   Y02134
*
* THIS SECTION BUILDS AND EXECUTES THE CHANNEL PROGRAM TO READ
* THE VOLUME LABEL FROM RECORD 3 OF TRACK 0.
*
MNT04000 EQU   *                        BUILD CHANNEL PROGRAM    Y02134
         XC    SEEK,SEEK                CLEAR IOB SEEK ADDRESS   Y02134
         MVI   SEEK+K7,K3               RECORD 3                 Y02134
         XC    CCW1(CCW4-CCW1),CCW1     CLEAR CCW AREA           Y02134
         LA    R15,SEEK+K3              SEARCH ID ADDRESS        Y02134
         ST    R15,CCW1                                          Y02134
         LA    R15,CCW1                 TIC ADDRESS              Y02134
         ST    R15,CCW2                                          Y02134
         LA    R15,VOLLABEL             READ AREA ADDRESS        Y02134
         ST    R15,CCW3                                          Y02134
         OC    CCW1(CCW4-CCW1),CHANPRG  OR IN CODES AND FLAGS    Y02134
         XC    VOLLABEL,VOLLABEL        CLEAR READ AREA          Y02134
*                                                                YM7890
*****************************************************************YM7890
*                                                                YM7890
*        IF MOUNT PENDING AND UNIT NOT READY MEMORY MAY          YM7890
*        NOT BE SWAPPED OUT, BECAUSE IF OPERATOR CANCELS         YM7890
*        THE JOB, THE JOB WILL NOT TERMINATE UNTIL THE           YM7890
*        MOUNT IS SATISFIED.                                     YM7890
*                                                                YM7890
*****************************************************************YM7890
*                                                                YM7890
         TM    UCBFL1,UCBNOTRD          TEST IF READY            YM7890
         BZ    MNT04050                 BR READY- BYPASS NOSWAP  YM7890
*                                                                YM7890
         SYSEVENT DONTSWAP              IN CASE OPERATOR CANCEL  YM7890
*                                                                YM7890
         SLL   R1,K24                   CLEAR HI 3 BYTES         YM7890
         LTR   R1,R1                    CHECK FOR ERROR          YM7890
         BNZ   MNT04050                 BRANCH IF ERROR          YM7890
*                                                                YM7890
         L     R15,IECRRPRM             GET PTR TO RECOVERY      YM8511
         SH    R15,SIXWORDS             BACK UP BY SIX WORDS     YM8511
         USING DSMADTW1,R15             R15 PTS TO RECOVERY WDS  YM8511
         OI    DSMADTB1,DSMASWAP        SET RECOVERY INDICATOR   YM8511
         DROP  R15                                               YM8511
*                                                                YM8511
         EXCP  IOB                      READ VOLUME LABEL        YM7890
*                                                                YM7890
         WAIT  ECB=ECB                  WAIT FOR I/O TO FINISH   YM7890
*                                                                YM8511
         L     R15,IECRRPRM             GET PTR TO RECOVERY      YM8511
         SH    R15,SIXWORDS             BACK UP BY SIX WORDS     YM8511
         USING DSMADTW1,R15             R15 PTS TO RECOVERY WDS  YM8511
         NI    DSMADTB1,X'FF'-DSMASWAP  RESET RECOVERY INDICATOR YM8511
         DROP  R15                                               YM8511
*                                                                YM7890
         SYSEVENT OKSWAP                ALLOW SWAPOUT-UNIT READY YM7890
*                                                                YM7890
         B     MNT04100                 BRANCH TO RESET MOUNT    YM7890
*                                                                YM7890
MNT04050 EQU   *                        READ LABEL - UNIT READY  YM7890
*                                                                Y02134
         EXCP  IOB                      READ VOLUME LABEL        Y02134
*                                                                Y02134
         WAIT  ECB=ECB                  WAIT FOR COMPLETION      Y02134
*                                                                Y02134
MNT04100 EQU   *                        RESET MOUNT PENDING      YM7890
*                                                                YM7890
         BAL   RETURN,MNT11800          LINK TO RESET MOUNT BIT  Y02134
*
MNT04160 EQU   *                        CHECK FOR I/O ERROR      Y02134
         TM    ECB,GOODIO               CHECK FOR I/O ERROR      Y02134
         BZ    MNT01700                 BRANCH IF I/O ERROR      Y02134
*                                                                Y02134
MNT04180 EQU   *                        TEST IF VOLUME MOUNTED   Y02134
         CLC   VOLSERNO,K0(RVOLSER)     TEST IF THE CORRECT      Y02134
*                                       VOLUME IS MOUNTED        Y02134
         BE    MNT05000                 BRANCH IF CORRECT        Y02134
         TM    SISW2,VIRTMNT            IS IT VIRTUAL MOUNT    @Y30LSTG
         BO    MNT01600                 YES CANT REISSUE MOUNT @ZM30032
*
* IF THE WRONG VOLUME WAS MOUNTED, THIS SECTION ISSUES A DEMOUNT
* MESSAGE IF THE UNIT IS DEMOUNTABLE.  IF THE VOLUME IS AN MSS @Y30LSTG
* SUBSYSTEM PACK, AND ICBMNTDE (SVC 126) MACRO IS SENT TO THE  @Y30LSTG
* MSS  SYSTEM AND THE PACK WILL BE DEMOUNTED.                  @Y30LSTG
*
MNT04200 EQU   *                        TEST IF DEMOUNTABLE      Y02134
         TM    SISW2,VERIFYSW           TEST IF VOLUME WAS TO BE Y02134
*                                       VERIFIED ONLY            Y02134
         BNO   MNT04300                 BRANCH IF VOLUME WAS TO@Y30LSTG
*                                       BE MNTED AND VERIFIED  @Y30LSTG
         TM    UCBDMCT,UCBDMC           CHECK FOR ANY USERS      Y02134
         BNZ   MNT01550                 BRANCH IF YES - VOLUME   Y02134
*                                       CANNOT BE DEMOUNTED      Y02134
         TM    UCBSTAT,UCBSYSR+UCBRESV+UCBPRES  TEST IF SYSRES   Y02134
*                                       RESERVED, OR PERMANENT   Y02134
         BNZ   MNT01550                 BRANCH IF ANY OR ALL -   Y02134
*                                       VOL CANNOT BE DEMOUNTED  Y02134
         TM    UCBSTAB,UCBBSVL          TEST IF SHARED VOLUME    Y02134
         BZ    MNT01550                 BRANCH IF YES - VOLUME   Y02134
*                                       CANNOT BE DEMOUNTED      Y02134
*                                                                Y02134
MNT04300 EQU   *                        MOVE IN REJECT MESSAGE   Y02134
*                                                              @Y30LSTG
* THIS CODE IS FOR THE MSS  DEMOUNT.                           @Y30LSTG
*                                                              @Y30LSTG
         TM    UCBTBYT2,UCBRVDEV        VIRT DASD REJECT       @Y30LSTG
         BNO   MNT04600                 NO, GO ON AS USUAL     @ZM30032
         BAL   RETURN,MNT11230          GO MAKE PARM LIST      @Y30LSTG
         MVC   F1DSCB+D8(L6),VOLSERNO   DO IT RIGHT            @Y30LSTG
         BAL   RETURN,MNT11260          GO ISSUE DMNT TO MSS   @Y30LSTG
         BAL   RETURN,MNT11300          GO CLEAR OUT UCB       @ZA04505
         B     MNT03000                 NO GO ISSUE MOUNT      @Y30LSTG
MNT04600 EQU   *                                               @Y30LSTG
         MVC   F1DSCB(REJECTL),RJECTMSG  MOVE IN REJECT MESSAGE  Y02134
         MVC   MSGINDIC+K2(L'UCBNAME),UCBNAME  MOVE CHANNEL/UNIT Y02134
*                                       ADDRESS INTO MESSAGE     Y02134
         MVC   MSGINDIC+K6(L'UCBVOLI),VOLSERNO  MOVE IN VOL SER  Y02134
*                                       OF VOLUME BEING REJECTED Y02134
         OC    MSGINDIC+K6(L'UCBVOLI),BLANKS  ENSURE UPPERCASE   Y02134
*                                                                Y02134
         BAL   RETURN,MNT11300          LINK TO CLEAR UCB        Y02134
*                                                                Y02134
         LA    R1,F1DSCB+REJECTJN       POINT TO JOB NAME FIELD  Y02134
         BAL   RETURN,MNT11400          LINK TO GET JOB NAME AND Y02134
*                                       STEP NAME FOR MESSAGE    Y02134
*                                                                Y02134
         BAL   RETURN,MNT11500          LINK TO ISSUE WTO        Y02134
*                                                                Y02134
         B     MNT03000                 GO ISSUE MOUNT MESSAGE   Y02134
*
* THIS SECTION CONVERTS THE CCHHR OF THE VTOC TO A TTR0 AND STORES
* IT AND THE VOLUME SERIAL NUMBER IN THE UCB.
*
MNT05000 EQU   *                        CONVERT VTOC CCHHR       Y02134
         NI    SISW2,X'FF'-VIRTMNT      TURN OFF MOUNT SW      @Y30LSTG
         MVC   SEEK+K3(K5),VOLVTOC      CCHHR OF THE VTOC        Y02134
         BAL   RETURN,MNT11900          LINK TO CONVERT ROUTINE  Y02134
*                                                                Y02134
         ST    R0,UCBVTOC               STORE VTOC TTR0 IN UCB   Y02134
         MVC   UCBVOLI,VOLSERNO         MOVE VOL SER INTO UCB    Y02134
*
* THIS SECTION UPDATES THE UCB VOLUME DISPOSITION.
*
MNT05100 EQU   *                        TEST FOR PASSED DATA     Y02134
         NI    UCBJBNR,X'FF'-UCBMONT    SET PASS TO ZERO         Y02134
         NI    UCBSTAB,X'FF'-UCBBPRV-UCBBPUB  SET PRIVATE AND    Y02134
*                                       PUBLIC TO ZERO           Y02134
         OI    UCBSTAT,UCBDADI          INDICATE SL VOLUME       Y02134
*                                                                Y02134
         L     RDSAB,DSABADDR           LOAD DSAB ADDRESS        Y02134
         USING DSAB,RDSAB               DSAB ADDRESSABILITY      Y02134
         TM    DSABFLG3,DSABPASS        TEST FOR PASSED DATA     Y02134
         BNO   MNT05120                 BRANCH IF NOT PASSED     Y02134
*                                                                Y02134
         OI    UCBJBNR,UCBMONT          SET PASS INDICATOR       Y02134
*                                                                Y02134
MNT05120 EQU   *                        TEST FOR PRIVATE DATA    Y02134
         L     RTIOT,DSABTIOT           LOAD TIOT ADDRESS        Y02134
         USING TIOENTRY,RTIOT           TIOT ADDRESSABILITY      Y02134
         TM    TIOESTTA,TIOSDSP1        TEST FOR PRIVATE DATA    Y02134
         BNO   MNT05140                 BRANCH IF NOT PRIVATE    Y02134
         OI    UCBSTAB,UCBBPRV          SET PRIVATE INDICATOR    Y02134
*                                                                Y02134
         L     R1,UCBEXTPT              LOAD UCB EXTENSION ADDR  Y02146
         OI    UCBFLP1-UCBCMEXT(R1),UCBNSRCH SET TO NON-SCRTH VOLY02146
*                                                                Y02134
MNT05140 EQU   *                        TEST FOR PUBLIC DATA     Y02134
         TM    TIOESTTA,TIOSDSP2        TEST FOR PUBLIC DATA     Y02134
         BNO   MNT01500                 BRANCH IF NOT PUBLIC     Y02134
         OI    UCBSTAB,UCBBPUB          SET PUBLIC INDICATOR     Y02134
         B     MNT01500                 GO RETURN                Y02134
         EJECT                                                   Y02134
*
* IF THIS MODULE ENQ'ED ON THE VOLUME SERIAL OF THE VOLUME BEING
* DEMOUNTED, THIS ROUTINE ISSUES A DEQ DIRECTED TO THE INITIATOR'S TCB.
*
MNT10000 EQU   *                        ISSUE DEQ                Y02134
         TM    SISW1,VOLDEQ             TEST IF VOL TO BE DEQ'ED Y02134
         BCR   14,RETURN                RETURN IF NOT            Y02134
         L     RTCB,TCBADDR             LOAD TCB ADDRESS         Y02134
         USING TCB,RTCB                 TCB ADDRESSABILITY       Y02134
         L     RTCB,TCBJSCB             LOAD JSCB ADDRESS        Y02134
         USING IEZJSCB,RTCB             JSCB ADDRESSABILITY      Y02134
         L     RTCB,JSCBACT             GET ACTIVE JSCB ADDRESS  YM2887
         L     RTCB,JSCBTCBP            INITIATOR TCB ADDRESS    Y02134
         MVC   ENQAREA(DEQLTH1),DEQLIST1  MOVE IN DEQ PARM LIST  Y02134
         MVC   MJNAME(L'SYSZVOLS),SYSZVOLS  MAJOR RESOURCE NAME  Y02134
         LR    R7,RETURN                SAVE RETURN ADDRESS      YM8501
*                                                                YM8501
         DEQ   (MJNAME,VOLMINOR),TCB=(RTCB),MF=(E,ENQAREA)  DEQ  Y02134
*                                                                YM8501
         LR    RETURN,R7                RESTORE RETURN ADDRESS   YM8501
         NI    SISW1,X'FF'-VOLDEQ       RESET VOL DEQ SWITCH     Y02134
         XC    VOLMINOR,VOLMINOR        ZERO MINOR RESOURCE NAME Y02134
         BR    RETURN                   RETURN                   Y02134
         EJECT                                                   Y02134
*
* THIS ROUTINE MOVES THE APPROPRIATE DEMOUNT MESSAGE INTO THE WORK
* AREA.  IF THE MONITOR SPACE COMMAND IS ACTIVE AND/OR IF AN SMF
* RECORD TYPE 19 MUST BE BUILT, IT ISSUES SVC 78 (LSPACE).  IT THEN
* ISSUES SVC 91 (VOLSTAT) TO RECORD THE VOLUME STATUS ON SYS1.LOGREC.
*
MNT11000 EQU   *                        MONITOR SPACE ACTIVE     Y02134
         L     RCVT,CVTPTR              LOAD CVT ADDRESS         Y02134
         L     R15,CVTMSLT              MASTER SCHED LINK TABLE  Y02134
         TM    BAMONITR-BASE(R15),BASPACE  MONITOR SPACE ACTIVE  Y02134
         BNO   MNT11020                 BRANCH IF NOT            Y02134
         MVC   F1DSCB(DMNTLN2),DMNTMSG2  MOVE IN THE TWO LINE    Y02134
*                                       DEMOUNT MESSAGE          Y02134
         MVI   SPACEMSG,BLANK           SET UP TO BLANK SPACE    Y02134
*                                       INFORMATION FIELD        Y02134
         MVC   SPACEMSG+K1(SPACELN),SPACEMSG  PROPAGATE BLANKS   Y02134
         LA    R1,SPACEMSG              POINT TO SPACE INFO      Y02134
         B     MNT11040                 GO TEST FOR SMF          Y02134
*                                                                Y02134
MNT11020 EQU   *                        PREPARE FOR LSPACE       Y02134
         MVC   F1DSCB(DMNTLN1),DMNTMSG1  MOVE IN DEMOUNT MESSAGE Y02134
         XR    R1,R1                    CLEAR REG FOR LSPACE     Y02134
*                                                                Y02134
MNT11040 EQU   *                        TEST FOR SMF SYSTEM      Y02134
         L     R15,CVTSMCA              GET SMCA ADDRESS         Y02134
         LTR   R15,R15                  TEST FOR SMF SYSTEM      Y02134
         BZ    MNT11060                 BRANCH IF NOT            Y02134
         USING SMCABASE,R15             SMCA ADDRESSABILITY      Y02134
         TM    SMCAOPT,SMCAVOL          IS VOL INFO REQUESTED    Y02134
         BZ    MNT11060                 BRANCH IF NOT            Y02134
*                                                                Y02134
         LA    R15,SMFFLAG              SET UP LSPACE SMF FLAG   Y02134
         SLL   R15,24                   MOVE FLAG TO BIT 0       Y02134
         OR    R1,R15                   PUT FLAG IN PARM REG     Y02134
*                                                                Y02134
MNT11060 EQU   *                        ISSUE LSPACE SVC         Y02134
         LTR   R1,R1                    IS LSPACE SVC REQUIRED   Y02134
         BZ    MNT11080                 BRANCH IF NOT            Y02134
*                                                                Y02134
         LR    R0,RUCB                  UCB ADDRESS IN REG 0     Y02134
         SVC   LSPACE                   ISSUE LSPACE SVC         Y02134
*                                                                Y02134
MNT11080 EQU   *                        ISSUE VOLSTAT SVC        Y02134
         LNR   R0,RUCB                  INDICATE A DA UCB        Y02134
         SVC   VOLSTAT                  ISSUE VOLSTAT SVC        Y02134
*                                                                Y02134
         BR    RETURN                   RETURN                   Y02134
         EJECT                                                   Y02134
*
* THIS ROUTINE FILLS IN THE UUU AND VOLSER FIELDS OF THE DEMOUNT
* AND MOUNT MESSAGES.  REGISTER 1 POINTS TO THE FIRST BYTE OF THE
* UUU (CHANNEL/UNIT ADDRESS) FIELD.
*
MNT11200 EQU   *                        FILL IN UUU AND VOLSER   Y02134
         MVC   K0(L'UCBNAME,R1),UCBNAME  MOVE CHANNEL/UNIT       Y02134
*                                       ADDRESS INTO MESSAGE     Y02134
         MVC   K4(L'UCBVOLI,R1),UCBVOLI  MOVE IN VOLUME SERIAL   Y02134
         OC    K4(L'UCBVOLI,R1),BLANKS  ENSURE UPPER CASE        Y02134
         BR    RETURN                   RETURN                   Y02134
*                                                              @Y30LSTG
* THIS ROUTINE CREATES AND FILLS IN THE DEMOUNT PARAMETER LIST @Y30LSTG
* IF A DEMOUNT MESSAGE IS TO BE ISSUED TO MSS .                @Y30LSTG
*                                                              @Y30LSTG
MNT11230 EQU   *                                               @Y30LSTG
         XC    F1DSCB(L28),F1DSCB       ZERO DEMNT MSG AREA    @ZM33363
         MVC   F1DSCB+D8(L6),UCBVOLI    MOVE IN THE VOLID      @Y30LSTG
         BR    RETURN                   GO BACK TO CALLER      @Y30LSTG
         SPACE 1                                               @Y30LSTG
*                                                              @Y30LSTG
* THIS ROUTINE ISSUES A DEMOUNT MESSAGE TO REMOVE A VIRTUAL    @Y30LSTG
* PACK FROM A VIRTUAL DRIVE.                                   @ZM30032
*                                                              @Y30LSTG
MNT11260 EQU   *                                               @Y30LSTG
         LA    R1,F1DSCB                POINT TO PARM LIST     @Y30LSTG
         ICBMNTDE TYPE=DMNT,VUA=UCBCHA, DEMOUNT THE PACK       @G24LSFS*
               MF=(E,(1))
         LTR   R15,R15                  ANY ERRORS             @Y30LSTG
         BNZ   MSSERR2                  YES, GO ISSSUE MESG    @Y30LSTG
         BR    RETURN                   RETURN NORMAL          @ZM30032
MSSERR2  LA    R7,F1DSCB+L28            BASE MSG REGISTER      @ZM33363
         USING PDDSECT,R7               BASE MESSAGE AREA      @Y30SLTG
         LR    R15,R4                   SAVE REG FOUR          @Y30LSTG
         LA    R1,F1DSCB                BASE REG ONE           @Y30LSTG
         L     RCVT,CVTPTR              INIT BASE              @Y30LSTG
         L     R4,CVTTCBP               LOCATE TCB PTRS        @Y30LSTG
         L     R4,D4(,R4)               GET CURRENT TCB        @Y30LSTG
         L     R4,TCBTIO-TCB(,R4)       GET TIOT START         @Y30LSTG
         MVC   PDMSGLNG(MSSMSGL),MSSMSG  PUT IN MSG            @Y30LSTG
         MVC   PDMSG+D8(L8),TIOCNJOB-TIOT(R4) ST JOBNAME       @Y30LSTG
         MVC   PDMSG+D24(L3),UCBNAME    SET DDD IN MSG         @Y30LSTG
         MVC   PDMSG+D17(L6),D8(R1)     SET VOLSER IN MSG      @Y30LSTG
         SLL   R0,D4                    SHIFT FOUR BITS        @Y30LSTG
         STH   R0,D8(R1)                RESET REASON CODE      @Y30LSTG
         UNPK  PDMSG+D58(L3),D8(L2,R1)  UNPACK REASON CODE     @Y30LSTG
         OI    PDMSG+D60,DECZR0         MAKE DIGIT VALID       @Y30LSTG
         TR    PDMSG+D58(L3),NUMTBLE    MAKE IT PRINTABLE      @Y30LSTG
         LR    R4,R15                   RESTORE REG FOUR       @Y30LSTG
         WTO   MF=(E,PDMSGLNG)          ISSUE WTO              @Y30LSTG
         B     MNT01550                 POST ERROR             @Y30LSTG
         DROP  R7                       DROP                   @Y30LSTG
         EJECT                                                   Y02134
*
* THIS ROUTINE SETS THE NOT READY BIT IN THE UCB AND THEN
* CLEARS THE UCB BEFORE DEMOUNTING A VOLUME.
*
MNT11300 EQU   *                        SET NOT READY            Y02134
*                                                                Y02134
         IOSGEN UCBFLG,UCB=(RUCB),VAR=ON,TABLE=UCBNRY  NOT READY YM5390
*                                                                Y02134
         L     R1,UCBEXTPT              LOAD UCB EXTENSION ADDR  Y02146
         NI    UCBFLP1-UCBCMEXT(R1),X'FF'-UCBNSRCH NON-SCRTH OFF Y02146
         XC    UCBVOLI,UCBVOLI          CLEAR VOL SER NUMBER     Y02134
         XC    UCBFSCT,UCBFSCT          CLEAR FILE COUNTERS      Y02134
         XC    UCBFSEQ,UCBFSEQ          CLEAR FILE SEQ COUNTERS  Y02134
         NI    UCBJBNR,X'FF'-UCBMONT    SET PASS TO ZERO         Y02134
         NI    UCBSTAB,X'FF'-UCBBPRV-UCBBPUB  SET PRIVATE AND    Y02134
*                                       PUBLIC TO ZERO           Y02134
         NI    UCBSTAT,X'FF'-UCBDADI    SET SL VOLUME TO ZERO    Y02134
         BR    RETURN                   RETURN                   Y02134
         EJECT                                                   Y02134
*
* THIS ROUTINE FILLS IN THE JOB NAME AND STEP NAME FIELDS OF THE
* DEMOUNT AND MOUNT MESSAGES.  REGISTER 1 POINTS TO THE FIRST BYTE
* OF THE JOB NAME FIELD.
*
MNT11400 EQU   *                        JOB AND STEP NAMES       Y02134
         L     RTCB,TCBADDR             LOAD CURRENT TCB ADDRESS Y02134
         USING TCB,RTCB                 TCB ADDRESSABILITY       Y02134
         L     RTIOT,TCBTIO             LOAD TIOT ADDRESS        Y02134
         MVC   K0(L'TIOCNJOB,R1),TIOCNJOB-TIOT(RTIOT)  JOB NAME  Y02134
         MVC   K9(K8,R1),TIOCSTEP-TIOT(RTIOT)  STEP NAME         Y02134
         BR    RETURN                   RETURN                   Y02134
         SPACE 1                                                 Y02134
*
* THIS ROUTINE ISSUES A WTO.
*
MNT11500 EQU   *                        ISSUE WTO SVC            Y02134
         XR    R0,R0                    CLEAR FOR MULTI-LINE WTO Y02134
         WTO   MF=(E,F1DSCB)            ISSUE WTO SVC            Y02134
*                                                                Y02134
         BR    RETURN                   RETURN                   Y02134
         SPACE 1                                                 Y02134
*
* THIS ROUTINE SETS THE MOUNT BIT IN THE UCB AND PUTS THE VOLUME
* SERIAL NUMBER OF THE VOLUME BEING MOUNTED IN THE UCB.
*
MNT11600 EQU   *                        SET THE MOUNT BIT        Y02134
         OI    UCBDMCT,UCBMOUNT         SET THE MOUNT BIT        Y02134
         MVC   UCBVOLI,K0(RVOLSER)      MOVE VOL SER INTO UCB    Y02134
         BR    RETURN                   RETURN                   Y02134
         EJECT                                                   Y02134
*
* THIS ROUTINE ISSUES A TPUT FOR MESSAGE IEC108I FOR A TSO TASK.
*
MNT11700 EQU   *                        SET UP MESSAGE IEC108I   Y02134
         L     R15,PSAAOLD-PSA          LOAD ASCB OLD POINTER    Y02134
         L     R15,ASCBTSB-ASCB(R15)    LOAD TSB ADDRESS         Y02134
         LTR   R15,R15                  TEST IF TSO TASK         Y02134
         BZ    MNT11740                 BRANCH IF NOT            Y02134
*                                                                Y02134
         MVC   VTOCDSCB(L'IEC108I),IEC108I  MOVE MSG TO BUFFER   Y02134
         LA    R0,L'IEC108I             LENGTH OF TSO MESSAGE    Y02134
         LA    R1,VTOCDSCB              POINT TO TSO MESSAGE     Y02134
         L     RTCB,TCBADDR             LOAD CURRENT TCB ADDRESS Y02134
         USING TCB,RTCB                 TCB ADDRESSABILITY       Y02134
         L     RTCB,TCBJSCB             LOAD JSCB ADDRESS        Y02134
         USING IEZJSCB,RTCB             JSCB ADDRESSABILITY      Y02134
         LH    R15,JSCBTJID             LOAD TJID                Y02134
         L     RTCB,JSCBPSCB            LOAD PSCB ADDRESS        Y02134
         USING PSCB,RTCB                PSCB ADDRESSABILITY      Y02134
         L     RTCB,PSCBUPT             LOAD UPT ADDRESS         Y02134
         USING UPT,RTCB                 UPT ADDRESSABILITY       Y02134
         TM    UPTSWS,UPTMID            TEST IF MSG ID REQUIRED  Y02134
         BO    MNT11720                 BRANCH IF YES            Y02134
         LA    R1,K8(R1)                ADJUST TSO MESSAGE PTR   Y02134
         SH    R0,IDLENGTH              ADJUST TSO MSG LENGTH    Y02134
*                                                                Y02134
MNT11720 EQU   *                        ISSUE TPUT SVC           Y02134
         SLL   R15,16                   SHIFT TJID TO HI ORDER   Y02134
         OR    R0,R15                   PUT TJID IN MSG LENGTH   Y02134
         TPUT  (1),(0),R                SEND MESSAGE TO TERMINAL Y02134
*                                                                Y02134
MNT11740 EQU   *                        RETURN LABEL             Y02134
         BR    RETURN                   RETURN                   Y02134
         EJECT                                                   Y02134
*
* THIS ROUTINE RESETS THE MOUNT BIT IN THE UCB.
*
MNT11800 EQU   *                        RESET THE MOUNT BIT      Y02134
         NI    UCBDMCT,X'FF'-UCBMOUNT   RESET THE MOUNT BIT      Y02134
         BR    RETURN                   RETURN                   Y02134
         SPACE 1                                                 Y02134
*
* THIS ROUTINE LINKS TO THE RESIDENT CONVERT ROUTINE TO CONVERT
* AN MBBCCHHR TO A TTR0, WHICH IS RETURNED IN REGISTER 0.
*
MNT11900 EQU   *                        LINK TO CONVERT ROUTINE  Y02134
         LA    R1,WKADEB                WORK AREA DEB ADDRESS    Y02134
         DROP  RDEB                                              Y02134
         USING WKADEB,R1                WORK AREA ADDRESSABILITY Y02134
         LA    R2,SEEK                  POINTER TO MBBCCHHR      Y02134
         LR    R7,RWKAREA               SAVE EXTENDED PREFIX ADR Y02134
         STM   R9,RETURN,IECREGSV+K12   SAVE REGISTERS 9 - 14    Y02134
         L     RCVT,CVTPTR              LOAD CVT ADDRESS         Y02134
         L     R15,CVTPRLTV             LOAD CONVERT ROUTINE ADR Y02134
         BALR  RETURN,R15               LINK TO CONVERT ROUTINE  Y02134
         LM    R9,RETURN,IECREGSV+K12-RENAMWKA(R7)  RESTORE REGS Y02134
         LA    RDEB,WKADEB              RESTORE WORK AREA ADDR   Y02134
         USING WKADEB,RDEB              WORK AREA ADDRESSABILITY Y02134
         BR    RETURN                   RETURN                   Y02134
         EJECT                                                   Y02134
*
***********************************************************************
*
* PROGRAM CONSTANTS
*
***********************************************************************
*
ENQLIST1 ENQ   (,,E,6,SYSTEM),RET=USE,MF=L  SYSZVOLS ENQ LIST    Y02134
ENQLTH1  EQU   *-ENQLIST1               LENGTH OF ENQ PARM LIST  Y02134
ENQLIST2 ENQ   (,,E,6,SYSTEM),RET=CHNG,MF=L  SYSZVOLS ENQ LIST   Y02134
ENQLTH2  EQU   *-ENQLIST2               LENGTH OF ENQ PARM LIST  Y02134
DEQLIST1 DEQ   (,,6,SYSTEM),TCB=0,MF=L  SYSZVOLS DEQ LIST        YM2887
DEQLTH1  EQU   *-DEQLIST1               LENGTH OF DEQ PARM LIST  Y02134
SYSZVOLS DC    CL8'SYSZVOLS'            SYSZVOLS MAJOR NAME      Y02134
ZEROS    DC    XL6'00'                  SIX BYTES OF ZEROS       Y02134
BLANKS   DC    CL6' '                   SIX BYTES OF BLANKS      Y02134
SIXWORDS DC    AL2(6*4)                 SIX FULLWORDS            YM8511
DASSOBID DC    CL4'SSOB'                SSOB HEADER ID         @G18MSBS
         SPACE 1                                                 Y02134
*
* MESSAGES ISSUED BY THIS MODULE
*
IEC108I  DC    C'IEC108I OPERATOR ACTION HAS BEEN REQUESTED FOR YOUR DAX
               TA SET'                                           Y02134
IDLENGTH DC    H'8'                     LENGTH OF MSG ID         Y02134
RJECTMSG WTO   'IEC502E R UUU,VOLSER,JOBNAME ,STEPNAME',         YM1238X
               DESC=2,ROUTCDE=4,MF=L                             Y02134
REJECTL  EQU   *-RJECTMSG               LENGTH OF REJECT MESSAGE Y02134
DMNTMSG1 WTO   'IEC502E XX UUU,VOLSER,JOBNAME ,STEPNAME',        YM1238X
               DESC=2,ROUTCDE=4,MF=L                             Y02134
DMNTLN1  EQU   *-DMNTMSG1               LENGTH OF DEMOUNT MSG    Y02134
DMNTMSG2 WTO   ('IEC502E XX UUU,VOLSER,SPACE=CCCC,TTTT,AAAA/YYYY,ZZZZ',X
               D),('IEC502E JOBNAME ,STEPNAME',DE),              YM1238X
               DESC=2,ROUTCDE=4,MF=L                             Y02134
DMNTLN2  EQU   *-DMNTMSG2               LENGTH OF DEMOUNT MSG    Y02134
IEC601D  WTOR  'IEC601D M UUU,VOLSER,JOBNAME ,STEPNAME - REPLY U OR M',X
               ,1,,DESC=2,ROUTCDE=4,MF=L                         Y02134
IEC601DL EQU   *-IEC601D                LENGTH OF MOUNT MESSAGE  Y02134
NUMTBLE  EQU   *-TWO40                  TRANSLATE TABLE        @Y30LSTG
         DC    C'0123456789ABCDEF'      REA CODE FOR VIRT DASD @Y30LSTG
MSSMSG   WTO   'IEC666I JJJJJJJJ,VOLSER,DDD MSS FAILURE IN DISMOUNT. COX
               DE=HHH   ',MF=L,DESC=4,ROUTCDE=(2,11)           @Y30LSTG
MSSMSGL  EQU   *-MSSMSG                 LENGTH OF MSG          @Y30LSTG
         SPACE 1                                                 Y02134
*
* CCW'S TO READ THE VOLUME LABEL
*
CHANPRG  DC    X'3100000040000005'      SEARCH ID EQUAL          Y02134
         DC    X'0800000000000000'      TIC *-8                  Y02134
         DC    X'0600000000000050'      READ 80 BYTES OF DATA    Y02134
         SPACE 1                                                 Y02134
*
* TABLE OF MODULE NAMES AND ENTRY POINT ADDRESSES
*
         XCTLTABL ID=(SCRATCH1,IGG0290D,SCRATCH2,IGG0290E,       Y02134X
               RENAME1,IGG03001,RENAME2,IGG03002),LENGTH=,       Y02134X
               BRT=YES                                           Y02134
         SPACE 2                                                 Y02134
         IECPDSCT                                              @Y30LSTG
         SPACE 2                                                 Y02134
         IECDSECS ASCB,BASE,CVT,DSAB,JSCB,PSA,PSCB,SMF,TCB,      Y02134X
               TIOT,UPT,UCB,EXPAND=YES                           YM6554
*
* THIS MACRO DESCRIBES THE COMMUNICATION AREA OF THE I/O SUPERVISOR.
*
         IECDIOCM                                                Y02134
         EJECT                                                   Y02134
*
* THIS MACRO EXPANDS THE RENAME WORK AREA, WHICH CAN ALSO BE USED
* TO ADDRESS PORTIONS OF THE SCRATCH WORK AREA.
*
WORKAREA IECRENWA EP,ADT=YES            RENAME WORK AREA         YM8511
MSGINDIC EQU   F1DSCB+12                OFFSET TO MSG INDICATOR  Y02134
SPACEMSG EQU   F1DSCB+26                OFFSET TO LSPACE INFO    Y02134
         SPACE 1                                                 Y02134
         ORG   VTOCDSCB                                          Y02134
VOLLABEL DS    0CL80                    VOLUME LABEL             Y02134
VOLLABI  DS    CL3                      LABEL IDENTIFIER         Y02134
VOLNO    DS    CL1                      VOLUME LABEL NUMBER      Y02134
VOLSERNO DS    CL6                      VOLUME SERIAL NUMBER     Y02134
VOLSEC   DS    CL1                      VOLUME SECURITY FLAG     Y02134
VOLVTOC  DS    CL5                      CCHHR OF THE VTOC        Y02134
         DS    CL5                      RESERVED                 Y02134
         DS    CL20                     RESERVED                 Y02134
VOLOWNER DS    CL10                     OWNER NAME AND ADDR CODE Y02134
         DS    CL29                     RESERVED                 Y02134
         EJECT
         IEFJSSOB MO,CONTIG=YES                                @G18MSBS
DASSOBAD DS    A                                               @G18MSBS
SSOBSAVE DS    10D                                             @G18MSBS
SSOBSIZE EQU   *-SSOB                                          @G18MSBS
         EJECT
         IEFJESCT                                              @G18MSBS
         EJECT
JCTABLE  DSECT                                                 @G18MSBS
         IEFAJCTB                                              @G18MSBS
         EJECT
SCTABLE  DSECT                                                 @G18MSBS
         IEFASCTB                                              @G18MSBS
         EJECT
         END   IGG0290F                                          Y02134
