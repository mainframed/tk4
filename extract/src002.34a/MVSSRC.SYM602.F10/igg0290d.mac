         TITLE 'IGG0290D - ERROR EXITS AND DEQUE ROUTINE.'
IGG0290D CSECT
*          RELEASE 16 DELETIONS
*1465                                                             5M51R
*1465                                                              AAAA
*          RELEASE 17 DELETIONS
*          RELEASE 18 DELETIONS                                       *
*          RELEASE 19 DELETIONS                                       *
*1184008000                                                      A25777
*1184002200,014400                                               S19028
* 018120-018126                                                  M4916
*1184013600-013800                                               M3097
*          RELEASE 20 DELETIONS                                       *
*1331                                                            S20201
*          RELEASE 21 DELETIONS                                       *
*1198000250,000778-000790,001600-001800,002100,006000-006200,    A43551
*1198018054-018060,018064-018066,018098,018104-018106,018176-    A43551
*1198018178,018186,019800,021000,031800                          A43551
*0000018210,018310,018600                                       SA49351
*          RELEASE 21.7 DELETIONS
*0000000490-000530,015600,019880-020860,028400                  SA56400
*          VS1 RELEASE 2 DELETIONS
*0000                                                           XM1038
*
*          VS2 RELEASE 02 DELETIONS/CHANGES
*0000000030-000150,000800-001000,002000-002240,002400,005650,    Y02080
*0000005900,006020-006040,006200-006300,006800-007060,009000,    Y02080
*0000012010-012100,012120,012600-012900,013100-013200,013840-    Y02080
*0000013920,014500-014600,015000-016200,018064,018070,018076,    Y02080
*0000018090-018102,018164,018170,018174,018176-018188,018194,    Y02080
*0000018235-018242,018244-018245,018270-018280,018294,018400-    Y02080
*0000019000,019820,020350,020980-030860,031000-031774            Y02080
*0000001900,001960,007120,017630,019200                          Y02078
*0000000754-431790                                               Y02082
*0000                                                            Y02134
*0000                                                            Y02144
*0000                                                            Y02132
*0000175000,179000-179100,210500,211500,212500,233500            YM1337
*0000063500,067000,248000-249000,259000                          YM3048
*0000018000,246000,250000-250100                                 YM3997
*0000179000,179100,260500,261500                                 YM2880
*0000178000                                                      YM2887
*          VS2 RELEASE 03 DELETIONS/CHANGES                    @ZM30032
*0000                                                          @Y30LSTG
*0000                                                          @ZA04736
*0000    EXTENDED MVS CVOL SUPPORT                             @Z40CSRC
*
*                                                              @G60ASBJ
*        SU60 DELETIONS/CHANGES                                @G60ASBJ
*A 253000,253600                                               @G60ASBJ
*D 253500,25A000                                               @G60ASBJ
*                                                              @G60ASBJ
* MODULE NAME - IGG0290D
*
* DESCRIPTIVE NAME - ERROR EXITS AND DEQUE ROUTINE
*
* COPYRIGHT - NONE
*
*
* CHANGE ACTIVITY - SEE DELETIONS/CHANGES FOLLOWING CSECT CARD
*STATUS CHANGE LEVEL 004
*
*STATUS CHANGE LEVEL 004
*
*FUNCTION - IF NO ERROR HAS OCCURRED WHEN PROCESSING THE CURRENT ENTRY
*          IN THE VOLUME LIST, THIS MODULE UPDATES THE HOLE COUNT IN
*          THE FORMAT 4 DSCB AND WRITES IT BACK TO THE VTOC.  IF THE
*          VTOC WAS ENQ'ED, THIS MODULE ISSUES A DEQ WITH THE RELEASE-
*          MUST-COMPLETE OPTION.  IF AN I/O ERROR OCCURRED WHILE
*          PROCESSING THE CURRENT ENTRY, THIS MODULE ISSUES MESSAGE
*          IEC603I WITH THE CODE = 0.  IF OVERLAPPING EXTENTS WERE
*          FOUND IN THE FORMAT 5 DSCB AND THE DIRF BIT WAS NOT SET
*          PREVIOUSLY IN THE FORMAT 4 DSCB, THIS MODULE ISSUES MESSAGE
*          IEC603I WITH THE CODE = 4 AND REWRITES THE FORMAT 4 DSCB
*          WITH THE DIRF BIT SET.
*          IF THE UNIT WAS DYNAMICALLY ALLOCATED, DYNAMIC ALLOCATION
*          IS CALLED TO DEALLOCATE THE UNIT.  IF THERE ARE MORE
*          VOLUMES TO BE PROCESSED, CONTROL IS RETURNED TO IGG0290E.
*          AFTER ALL THE VOLUMES HAVE BEEN PROCESSED, THIS MODULE
*          TESTS IF A VOLUME WAS ENQ'ED AND MOUNTED BY IGG0290F.
*          IF YES, CONTROL IS GIVEN TO IGG0290F TO DEMOUNT AND DEQ
*          THE VOLUME.  IF SCRATCH ENQ'ED ON THE DATA SET NAME OR
*          THE TIOT, CORRESPONDING DEQ'S ARE ISSUED.  IF DATA SET
*          RECORDING FOR SMF IS REQUIRED, THIS MODULE BUILDS AN SMF
*          RECORD TYPE 17.  IF THE RPS SIO APPENDAGE IGG019EK WAS
*          LOADED, IT DELETES IT.  IT THEN FREES THE WORK AREA(S)
*          AND RETURNS TO THE CALLER.
*          IN ADDITION, IF THE DATA SET BEING SCRATCHED RESIDES@Y30LSTG
*          ON VIRTUAL MSS  DASD, THE PARAMETER LIST BUILT IN   @Y30LSTG
*          IGG0299A IS USED TO RELINQUISH REAL DASD SPACE THAT @Y30LSTG
*          WAS ACQUIRED FOR THE DATA SET.                      @Y30LSTG
*
*ATTRIBUTES - REENTRANT
*
*ENTRIES - THE ONLY ENTRY TO THIS MODULE IS IGG0290D. ENTRY IS MADE
*          FROM ANY OF THE PRECEDING MODULES - IGC0002I, IGG0290E,
*          IGG0290A, IGG0299A, IGG0290B, OR IGG0290C.
*
*SUPERVISOR CALLS AND EXTERNAL ROUTINES IN THIS MODULE -
*          EXCP(0)  - WRITES TO A DIRECT ACCESS DEVICE
*          WAIT(1)  - WAITS ON AN EVENT CONTROL BLOCK
*          TIME(11) - OBTAINS THE CURRENT TIME AND DATE
*          WTO(35)  - WRITES A MESSAGE TO THE OPERATOR
*          DEQ(48)  - DEQ'S FROM SYSVTOC, SYSZTIOT, AND/OR SYSDSN
*          SMFWTM   - WRITES AN SMF RECORD (SVC 83)
*          DYNALLOC - DYNAMICALLY DEALLOCATES THE UNIT (SVC 99)
*          MODESET  - SWITCHES TO THE CALLER'S KEY
*          IECRES   - BRANCHES TO ANOTHER MODULE
*                   - OBTAINS/FREES WORK AREA(S)
*          ICBACREL - RELINQUISHES DASD SPACE FOR MSS          @Y30LSTG
*
*OTHER MACROS ISSUED -
*          IECDSECS - EXPANDS THE CVT, DSAB, JMR, JSCB, QDB, RB, SMF,
*                     SMFTCT, TCB, TIOT, AND UCB DSECTS
*          IECSDSL1 - ENABLES SYMBOLIC ADDRESSING OF DSCBS
*          IECSCRWA - SCRATCH WORK AREA EXPANSION
*          IEFJFCBN - ENABLES SYMBOLIC ADDRESSING OF THE JFCB
*          IEFZB4D0 - EXPANDS THE DYNAMIC ALLOCATION REQUEST BLOCK
*          IEFZB4D2 - EXPANDS THE DYNAMIC ALLOCATION TEXT UNIT KEYS
*          IFASMFR  - BUILDS AN SMF RECORD TYPE 17
*          XCTLTABL - BUILDS A LIST OF MODULE ID'S AND ADDRESSES
*
*INPUT -   AT ENTRY TO THIS MODULE REGISTER 13 POINTS TO THE WORK AREA.
*          REGISTER 8 POINTS TO THE CURRENT ENTRY IN THE VOLUME LIST.
*
*OUTPUT -  THE UPDATED FORMAT 4 DSCB IS WRITTEN OUT.  IF THE SCRATCH
*          FUNCTION IS COMPLETE FOR ALL VOLUMES IN THE VOLUME LIST,
*          CONTROL IS RETURNED TO THE CALLER WITH AN ERROR CODE IN
*          REGISTER 15.  IF THE DATA SET MUST STILL BE SCRATCHED
*          FROM ANOTHER VOLUME, CONTROL IS PASSED TO MODULE IGG0290E.
*          REGISTER 9 POINTS TO THE CVT, AND  REGISTER 10 POINTS TO
*          THE INPUT PARAMETER LIST.  REGISTER 13 STILL POINTS TO THE
*          SCRATCH WORK AREA, IN WHICH 'VOLNUM' CONTAINS THE NUMBER
*          OF VOLUMES REMAINING TO BE SCRATCHED AND 'VOLPTR' CONTAINS
*          THE ADDRESS OF THE NEXT ENTRY IN THE VOLUME LIST.
*          THE FOLLOWING INFORMATION IS PASSED TO IGG0290F:
*          REGISTER 2 POINTS TO THE DEB.
*          REGISTER 3 POINTS TO THE VOLUME SERIAL NUMBER.
*          REGISTER 4 CONTAINS THE CHARACTERS '290D'.
*          REGISTER 8 POINTS TO THE CURRENT VOLUME LIST ENTRY.
*          REGISTER 11 CONTAINS THE PRIMARY (DEMOUNTABLE) UCB ADDRESS.
*
*ERROR CONDITION - THE ONLY ERROR INDICATED IS PERMANENT I/O ERROR.
*
*STORAGE - PROGRAM CODE CSECT = LESS THAN 2048 BYTES
*          WORK AREA = 900 BYTES                               @Y30LSTG
*          RPS WORK AREA = 128 BYTES
*
*NOTES -   UNLIKE THE ALLOCATE, EXTEND, AND PARTIAL RELEASE FUNCTIONS
*          OF DADSM, IF THE DIRF BIT IS SET IN THE FORMAT 4 DSCB IN
*          THE SCRATCH WORK AREA, THIS INDICATES THAT THE DIRF BIT
*          WAS PREVIOUSLY SET IN THE FORMAT 4 DSCB.
*
* REGISTER EQUATES
*
R0       EQU   0                        REGISTER 0               Y02080
R1       EQU   1
RPARM    EQU   2                        PARAMETER LIST POINTER   Y02082
R2       EQU   2
RDSAB    EQU   2                        POINTER TO THE DSAB      Y02082
RWRK     EQU   3                        WORK REGISTER            Y02082
RTIOT    EQU   3                        POINTER TO THE TIOT      Y02082
R3       EQU   3
RTEXT    EQU   4                        POINTER TO TEXT UNIT     Y02082
R4       EQU   4
VOLCTR   EQU   5                        NUMBER OF VOLUMES        Y02082
R6       EQU   6
R7       EQU   7
R8       EQU   8
VOLISTX  EQU   8                        PTR TO VOLIST ENTRY      Y02082
R9       EQU   9
RCVT     EQU   9                                                 S19028
RA       EQU   10
RUCB     EQU   11                       UCB POINTER              Y02078
RBASE    EQU   12
RWKAREA  EQU   13
RETURN   EQU   14
R15      EQU   15
WORKREG2 EQU   15
*
* OTHER EQUATES
*
K0       EQU   0                        CONSTANT OF ZERO         Y02082
K1       EQU   1                        CONSTANT OF 1            Y02082
K2       EQU   2                        CONSTANT OF 2            Y02080
K3       EQU   3                        CONSTANT OF 3            Y02082
K4       EQU   4                        CONSTANT OF 4            Y02082
K6       EQU   6                        CONSTANT OF 6          @Y30LSTG
K8       EQU   8                        CONSTANT OF 8            Y02082
K12      EQU   12                       CONSTANT OF 12           Y02080
K16      EQU   16                       CONSTANT OF 16           YM1337
D3       EQU   3                        DISPLACEMENT OF THREE  @Y30LSTG
X00      EQU   X'00'                    ONE BYTE OF BINARY ZEROS Y02082
FW       EQU   4                        LENGTH OF A FULLWORD     Y02082
APDIS    EQU   28                       DISP. TO APPGS IN W.A.   S20201
APSVA    EQU   120                      DISP.TO AVT PTR IN W.A.  S20201
SMFREC17 EQU   17                       SMF RECORD TYPE 17       Y02082
CHNPGADR EQU   16                       OFFSET TO CCW STARTING   Y02082
*                                       ADDRESS IN THE IOB       Y02082
UCBADDR  EQU   32                       DISP. TO UCB ADDR IN DEB Y02080
UUU      EQU   37                       DISP. TO UUU IN MESSAGE  Y02078
VOLID    EQU   41                       DISP. TO VOLSER IN MSG   Y02078
CODE     EQU   48                       DISP. TO CODE IN MESSAGE Y02078
VOLSER   EQU   4                        OFFSET TO THE VOL SER IN Y02082
*                                       A VOLUME LIST ENTRY      Y02082
STATUS   EQU   11                       OFFSET TO VOLIST STATUS  Y02078
ENTRYL   EQU   12                       LENGTH OF VOLIST ENTRY   Y02078
RETCODE8 EQU   8                        RETURN CODE OF 8 TO USER Y02082
PSWDERR  EQU   X'02'                    PASSWORD ERROR           Y02082
IOERR    EQU   X'04'                    I/O ERROR IN VOLUME LIST Y02078
OPENDS   EQU   X'07'                    OPEN DATA SET ERROR      Y02082
CODE4    EQU   C'4'                     CODE 4 IN MESSAGE        Y02078
PERIOD   EQU   C'.'                     EBCDIC PERIOD            A43551
DIRFBIT  EQU   X'04'                    TURN DIRF BIT ON        SA56400
WRDATA   EQU   X'05'                    WRITE DATA COMMAND CODE  Y02082
GOODIO   EQU   X'20'                    TEST VALUE FOR GOOD I/O  Y02082
BLANK    EQU   C' '                     EBCDIC BLANK             YM2880
ACQPLN   EQU   96                       ACQUIRE LIST LENGHT    @Y30LSTG
RELINQ   EQU   X'04'                    RELINQUISH OP CODE     @Y30LSTG
INHIBDST EQU   X'20'                    INHIBIT DESTAGING      @Y30LSTG
OFF      EQU   X'FF'                    SWITCH RESETTING MASK  @Z40CSRC
*
         BALR  RBASE,0
         USING BEGIN,RBASE
         USING SCRTHWKA,RWKAREA         WORK AREA ADDRESSABILITY Y02080
         USING UCB,RUCB                 UCB ADDRESSABILITY       Y02078
         USING CVT,RCVT                                          S19028
*
* THIS SECTION UPDATES THE FORMAT 4 DSCB AND WRITES IT BACK.
*
BEGIN    EQU   *
         TM    SISW2,DEMNTSW            TEST IF ENTERED FROM     Y02134
*                                       IGG0290F AFTER DEMOUNT   Y02134
         BO    RETURNPT                 BRANCH IF YES            Y02134
         TM    STYPEFLG,VIOBIT          TEST IF A VIO DATA SET   Y02132
         BO    DEQTEST1                 BRANCH IF YES            Y02132
         TM    STYPEFLG,VTOCENQ         TEST IF VTOC ENQ'ED      Y02082
         BNO   TSTALLOC                 BRANCH IF NOT            Y02082
         CLI   STATUS(VOLISTX),X00      TEST IF ERROR ON VOLUME  Y02082
         BNE   TESTEXIT                 BRANCH IF YES            Y02082
         NI    SISW1,X'FF'-NOSMFRCD     RESET NO SMF RCD SWITCH  Y02082
*                                       SINCE AT LEAST ONE       Y02082
*                                       SCRATCH WAS SUCCESSFUL   Y02082
         TM    STYPEFLG,ERXTENT         WERE OVERLAPPING         Y02078
*                                       EXTENTS FOUND           SA56400
         BZ    NOERROR                  BRANCH IF NO            SA56400
         OI    DS4VTOCI,DIRFBIT         TURN ON DIRF BIT        SA56400
NOERROR  EQU   *                                                SA56400
         LH    WORKREG2,DS4DSREC                                 Y02082
         AH    WORKREG2,HOLENUM                                  Y02082
         STH   WORKREG2,DS4DSREC        UPDATE VTOC HOLE COUNT   Y02082
         MVC   OUTDSCB(L'VTOCDSCB),VTOCDSCB                      Y02082
         LA    WORKREG2,L'VTOCDSCB      WRITE 96 BYTES OF DATA   Y02082
         ST    WORKREG2,CCW8+K4         SET COUNT FIELD          Y02082
         MVI   CCW8,WRDATA              WRITE DATA COMMAND CODE  Y02082
         MVC   OUTCCHHR,VTOCADR         SET UP SEARCH ID ADDRESS Y02082
         MVC   SEEK+K3(L'VTOCADR),VTOCADR                        Y02082
         LA    WORKREG2,CCW6            MODIFY CCW PTR IN IOB    Y02082
         ST    WORKREG2,IOB+CHNPGADR                             Y02082
         MVI   ECB,X00                  CLEAR THE ECB            Y02082
         EXCP  IOB
         WAIT  ECB=ECB                  WAIT FOR COMPLETION
         TM    ECB,GOODIO               TEST FOR AN I/O ERROR    Y02082
         BO    TESTEXIT                 BRANCH IF NO I/O ERROR
         MVI   ERCODE+K1,RETCODE8       SET RETURN CODE          Y02082
         L     R1,TCBADDR               LOAD TCB ADDRESS         Y02078
         USING TCB,R1                   TCB ADDRESSABILITY       Y02078
         MODESET EXTKEY=RBT234,WORKREG=2  SWITCH TO CALLER'S KEY Y02078
         MVI   STATUS(VOLISTX),IOERR    SET ERROR CODE IN VOLIST Y02078
         MODESET EXTKEY=ZERO            RETURN TO KEY ZERO       Y02082
         DROP  R1                                                Y02078
*
* THIS SECTION DEQ'S THE VTOC.
*
TESTEXIT EQU   *
         L     RUCB,WKADEB+UCBADDR      LOAD UCB ADDRESS         Y02078
         MVC   MINAME(L'SRTEVOLI),SRTEVOLI  MINOR RESOURCE NAME  Y02082
         MVC   MJNAME(L'VTOCNAME),VTOCNAME  MAJOR RESOURCE NAME  Y02082
         MVI   ENQAREA,X'FF'            SET LAST ENTRY INDICATOR Y02082
         SR    R1,R1                    ZERO REGISTER            Y02080
         STH   R1,ENQAREA+K2            CLEAR FOR DEQ MACRO      Y02080
         DEQ   (MJNAME,MINAME,6,SYSTEMS),RMC=STEP,MF=(E,ENQAREA)
*
         NI    DSMADTB2,X'FF'-DSMVTOCR-DSMSMCE  RESET VTOC       Y02144
*                                       ENQ'ED SMC SWITCHES      Y02144
         NI    STYPEFLG,X'FF'-VTOCENQ   RESET VTOC ENQ'ED SWITCH Y02082
*
         CLI   STATUS(VOLISTX),PSWDERR  TEST IF PASSWORD ERROR   Y02082
         BE    PASSERR                  IF YES, BRANCH           M3097
*
* THIS SECTION ISSUES MESSAGE IEC603I WITH CODE=0 OR CODE=4.
*
TSTIOERR EQU   *                        TEST IF I/O ERROR        Y02078
         CLI   STATUS(VOLISTX),IOERR    TEST IF I/O ERROR ON VOL Y02078
         BNE   TSTOVLAP                 BRANCH IF NO I/O ERROR   Y02078
         LA    RETURN,ISSUEMSG          SET UP RETURN REGISTER   Y02078
         B     BUILDMSG                 GO BUILD MESSAGE IEC603I Y02078
TSTOVLAP EQU   *                        TEST DIRF BIT            Y02078
         TM    STYPEFLG,ERXTENT         TEST IF DIRF BIT SET DUE Y02078
*                                       TO OVERLAPPING EXTENTS   Y02078
         BO    SETREC                   BRANCH IF NOT          @Y30LSTG
*                                                              @Y30LSTG
* IF NO ERRORS, THIS ROUTINE RELINQUISHES VIRTUAL DASD SPACE   @Y30LSTG
*                                                              @Y30LSTG
         L     R1,RELQADR               POINT TO PARM LIST     @Y30LSTG
         LTR   R1,R1                    IS THERE ONE           @Y30LSTG
         BZ    TSTALLOC                 NO, RELINQ. UNNEEDED   @ZM30032
         CLI   K4(R1),RELINQ            VALID PARM LIST        @Y30LSTG
         BNE   TSTALLOC                 NO, AGAIN NOTHING TO DO@Y30LSTG
         MVC   K8(K6,R1),SRTEVOLI       MOVE IN VOLID          @Y30LSTG
         OI    K6(R1),INHIBDST          SET NO DESTAGING       @Y30LSTG
         MVI   K0(R1),X00               1ST BYTE NEEDS ZEROING @Y30LSTG
         MVI   D3(R1),ACQPLN            PUT LENGTH IN MSS MSG  @Y30LSTG
         ICBACREL TYPE=REL,MF=(E,(1))   RELINQUISH THE SPACE   @Y30LSTG
         B     TSTALLOC                 CONTINUE               @Y30LSTG
*   ERROR RETURNS ARE IGNORED AS THE POSTING OF THE INHIBIT    @Y30LSTG
*   SWITCH IS THE VALID OPERATION REQUIRED                     @Y30LSTG
SETREC   EQU   *                                               @Y30LSTG
         LA    RETURN,SETCODE           SET UP RETURN REGISTER   Y02078
BUILDMSG EQU   *                        BUILD ERROR MSG          Y02078
         MVC   INDSCB(MSGEND-ERRMSG),ERRMSG  MOVE IN MESSAGE     Y02078
         MVC   INDSCB+UUU(L'UCBNAME),UCBNAME  MOVE IN EBCDIC UCB Y02078
*                                       CHANNEL/UNIT ADDRESS     Y02078
         MVC   INDSCB+VOLID(L'SRTEVOLI),SRTEVOLI  MOVE IN VOLID  Y02078
         BR    RETURN                   CONTINUE                 Y02078
SETCODE  EQU   *                        SET CODE                 Y02078
         MVI   INDSCB+CODE,CODE4        SET CODE=4 IN MESSAGE    Y02078
ISSUEMSG EQU   *                        ISSUE MESSAGE            Y02078
         WTO   MF=(E,INDSCB)            ISSUE MESSAGE IEC603I    Y02078
*
* IF THE UNIT WAS DYNAMICALLY ALLOCATED, THIS SECTION BUILDS THE
* REQUEST BLOCK TO INTERFACE WITH DYNAMIC ALLOCATION (SVC 99) TO
* UNALLOCATE THE UNIT.
*
TSTALLOC EQU   *                        DYNAMIC ALLOCATN TEST    Y02082
         TM    SISW1,UCBALLOC           TEST IF UCB ALLOCATED    Y02082
*                                       BY DYNAMIC ALLOCATION    Y02082
         BNO   TESTLAST                 BRANCH IF NOT            Y02082
         LA    RPARM,VTOCDSCB           PARAMETER LIST BASE      Y02082
         USING S99RBP,RPARM                                      Y02082
         LA    RWRK,K4(,RPARM)          INCREMENT BY FOUR        Y02082
         ST    RWRK,S99RBPTR            SET UP REQUEST BLOCK PTR Y02082
         OI    S99RBPTR,S99RBPND        TURN ON HIGH ORDER BIT   Y02082
         LR    RPARM,RWRK               ADDRESS OF REQUEST BLOCK Y02082
         USING S99RB,RPARM              REQ BLOCK ADDRESSABILITY Y02082
         XC    S99RB(S99RBEND-S99RB),S99RB  ZERO REQ BLOCK CORE  Y02082
         MVI   S99RBLN,S99RBEND-S99RB   LENGTH OF REQUEST BLOCK  Y02082
         MVI   S99VERB,S99VRBUN         INDICATE UNALLOCATION    Y02082
         LA    RWRK,S99RBEND            ADDR OF TEXT POINTERS    Y02082
         ST    RWRK,S99TXTPP            PLACE IN REQUEST BLOCK   Y02082
         OI    S99FLG21,S99TIONQ        INDICATE TIOT ENQ'ED     Y02082
         LR    RPARM,RWRK               ADDRESS OF TEXT PTRS     Y02082
         USING S99TUPL,RPARM            TEXT PTRS ADDRESSABILITY Y02082
         LA    RTEXT,FW(,RPARM)         ADDRESS OF TEXT UNIT     Y02082
         ST    RTEXT,S99TUPTR           POINTER TO TEXT UNIT     Y02082
         OI    S99TUPTR,S99TUPLN        INDICATE LAST TEXT PTR   Y02082
         USING S99TUNIT,RTEXT           TEXT UNIT ADDRESSABILITY Y02082
         LH    RWRK,DDNAMEKY                                     Y02082
         STH   RWRK,S99TUKEY            DDNAME SPECIFIED         Y02082
         LA    RWRK,K1                                           Y02082
         STH   RWRK,S99TUNUM            INDICATE 1 DDNAME ENTRY  Y02082
         LA    RWRK,L'TIOEDDNM                                   Y02082
         STH   RWRK,S99TULNG            LENGTH OF TEXT PARM      Y02082
         L     RDSAB,DSABADDR           GET DSAB ADDRESS         Y02082
         USING DSAB,RDSAB               DSAB ADDRESSABILITY      Y02082
         L     RTIOT,DSABTIOT           LOAD TIOT ADDRESS        Y02082
         USING TIOENTRY,RTIOT           TIOT ADDRESSABILITY      Y02082
         MVC   S99TUPAR(L'TIOEDDNM),TIOEDDNM  COPY TIOT'S DDNAME Y02082
         LA    R1,VTOCDSCB              POINT TO REQUEST BLK PTR Y02082
         DYNALLOC                                                Y02082
*
         NI    DSMADTB1,X'FF'-DSMUCBAL  RESET UCB ALLOCATED BIT  Y02144
         NI    SISW1,X'FF'-UCBALLOC     RESET ALLOCATED BIT      Y02082
*
* THIS SECTION BRANCHES BACK TO IGG0290E IF THE DATA SET MUST BE
* SCRATCHED FROM ANOTHER VOLUME.
*
TESTLAST EQU   *                        TEST EXIT BIT            Y02082
         TM    STYPEFLG,EXITBIT         IS EXIT BIT SET          Y02082
         BO    DEQTEST1                 BRANCH IF SET            Y02082
         NI    STYPEFLG,X'FF'-ERXTENT-DOSPACK  TURN OFF OVERLAP  Y02080
*                                       INDICATOR AND DOS PACK   XM1038
*                                       INDICATOR                XM1038
         LA    R7,LOAD2                 POINT TO ID OF IGG0290E  Y02134
         L     R1,RELQADR               POINT TO PARM LIST     @Y30LSTG
         LTR   R1,R1                    IS THERE ONE           @Y30LSTG
         BZ    LOADEXIT                 NO. GO TO EXIT         @ZM30032
         XC    K0(ACQPLN,R1),K0(R1)     ZERO IT OUT IF IT EXIST@Y30LSTG
*
* THIS SECTION BRANCHES TO ANOTHER LOAD OF SCRATCH.
*
LOADEXIT EQU   *                        EXIT TO NEXT LOAD        Y02134
         L     RA,OLDPLPTR              GET PARAMETER LIST POINTER
         L     RCVT,CVTPTR              LOAD CVT ADDRESS         Y02080
         IECRES LOAD,EXTPR=(RWKAREA),MODID=(R7),BRANCH=DIRECT    Y02134
*
* THIS SECTION STORES THE PASSWORD ERROR CODE IN THE REMAINING VOLUME
* LIST ENTRIES.
*
PASSERR  EQU   *
         LH    VOLCTR,VOLNUM            NUMBER OF REMAINING VOLS Y02082
         LA    VOLCTR,K1(VOLCTR)        INCREMENT BY ONE         Y02082
         L     R1,TCBADDR               LOAD TCB ADDRESS         Y02078
         USING TCB,R1                   TCB ADDRESSABILITY       Y02078
         MODESET EXTKEY=RBT234,WORKREG=2  SWITCH TO CALLER'S KEY Y02078
ERRLOOP  EQU   *
         MVI   STATUS(VOLISTX),PSWDERR  SET PASSWORD ERROR       Y02078
         LA    VOLISTX,ENTRYL(VOLISTX)  POINT TO NEXT ENTRY      Y02078
         BCT   VOLCTR,ERRLOOP           BRANCH IF MORE VOLUMES   Y02082
         MODESET EXTKEY=ZERO            RETURN TO KEY ZERO       Y02082
         DROP  R1                                                Y02078
         MVI   ERCODE+K1,RETCODE8       SET RETURN CODE          Y02082
         B     TSTALLOC                 GO TEST IF ALLOCATED     Y02082
*
* IF A VOLUME WAS ENQ'ED AND MOUNTED BY IGG0290F, THIS SECTION
* BRANCHES TO IGG0290F TO DEMOUNT AND DEQ THE VOLUME.
*
DEQTEST1 EQU   *                        DEQ VPL TEST             Y02082
         TM    SISW1,VOLDEQ             TEST IF VOL TO BE DEQ'ED Y02082
         BNO   DEQTEST2                 BRANCH IF NOT            Y02082
         OI    SISW2,DEMNTSW            SET DEMOUNT SWITCH       Y02134
         LA    R2,WKADEB                DEB ADDRESS IN REG 2     Y02134
         L     RUCB,PRUCBPTR            UCB ADDRESS IN REG 11    Y02134
         LA    R3,SRTEVOLI              VOLSER ADDR IN REG 3     Y02134
         L     R4,RETNAME               RETURN MODULE NAME       Y02134
         LA    R7,MNTLOAD               POINT TO ID OF IGG0290F  Y02134
         B     LOADEXIT                 GO BRANCH TO IGG0290F    Y02134
RETURNPT EQU   *                        RETURN HERE              Y02134
         NI    SISW2,X'FF'-DEMNTSW      RESET DEMOUNT SWITCH     Y02134
*
* IF SCRATCH ENQ'ED ON THE DSNAME, THIS SECTION ISSUES A DEQ
* DIRECTED TO THE INITIATOR'S TCB.
*
DEQTEST2 EQU   *                        DEQ SYSDSN TEST          Y02082
         TM    SISW2,DSNENQCV           IS CVOL SYSDSN ENQ'D   @Z40CSRC
         BO    STRIPBLK                 YES, GO STRIP BLANKS   @Z40CSRC
DSNQTST  EQU   *                        REGULAR SYSDSN ENQ TST @Z40CSRC
         TM    SISW1,DSNENQ             TEST IF ENQ'ED ON SYSDSN Y02082
         BNO   DEQTEST3                 BRANCH IF NOT            Y02082
STRIPBLK EQU   *                        STRIP TRAILING BLANKS  @Z40CSRC
         LA    R4,PDSNAME+L'PDSNAME-K1  LAST BYTE OF DSNAME      YM2880
TESTDSN  EQU   *                        SEARCH FOR NON-BLANK     YM2880
         CLI   K0(R4),BLANK             IS THIS ONE A BLANK      YM2880
         BNE   GETLNGTH                 BRANCH IF NO TO COMPUTE  YM2880X
                                        LENGTH OF NAME FOR DEQ   YM2880
         BCT   R4,TESTDSN               TRY NEXT BYTE            YM2880
GETLNGTH EQU   *                        CALCULATE LENGTH         YM2880
*
* IF DSNENQCV IS ON INDICATING AN ENQ WAS DONE ON THE NAME
* SYSCTLG.VXXXXXX (WHERE XXXXXX IS THE VOLSER), ADD THE
* SUFFIX QUALIFIER TO THE SIMPLE NAME SYSCTLG.
*
         TM    SISW2,DSNENQCV           SYSCTLG.VXXXXXX ENQ'D? @Z40CSRC
         BNO   PASTCVOL                 NO, SKIP CVOL CODE     @Z40CSRC
         MVC   K1(L'CDOTV,R4),CDOTV     MOVE IN '.V'           @Z40CSRC
         MVC   K3(L'UCBVOLI,R4),VOLSER(VOLISTX) MV IN VOLSER   @Z40CSRC
         LA    R4,KDOTVVL(,R4)          ADJUST FOR LONGER NAME @Z40CSRC
PASTCVOL EQU   *                        BEYOND CVOL SUFFIXING  @Z40CSRC
         LA    R2,PDSNAME-K1            BYTE PRECEDING DSNAME    YM2880
         SR    R4,R2                    R4 HAS LENGTH FOR DEQ    YM2880
         L     R2,TCBADDR               LOAD TCB ADDRESS         Y02082
         USING TCB,R2                   TCB ADDRESSABILITY       Y02082
         L     R2,TCBJSCB               LOAD JSCB ADDRESS        Y02082
         USING IEZJSCB,R2               JSCB ADDRESSABILITY      Y02082
         L     R2,JSCBACT               GET ACTIVE JSCB ADDRESS  YM2887
         L     R2,JSCBTCBP              ADDR OF INITIATOR'S TCB  Y02082
         MVC   ENQAREA(DEQLTH1),DEQLIST1 MOVE IN DEQ PARM LIST   Y02082
         MVC   MJNAME(L'DSNMJR),DSNMJR  MAJOR RESOURCE NAME      Y02082
         DEQ   (MJNAME,PDSNAME,(R4)),TCB=(R2),MF=(E,ENQAREA)     YM2880
*                                       ISSUE DEQ                YM2880
         TM    SISW2,DSNENQCV           IS DSNAME SUFFIXED     @Z40CSRC
         BNO   NOSUFFIX                 NO, SKIP BLANKING      @Z40CSRC
         NI    SISW2,OFF-DSNENQCV       RESET SUFFIX FLAG      @Z40CSRC
         LA    R4,PDSNAME-KDOTVVL(R4)   POINT AT '.VXXXXXX'    @Z40CSRC
         MVC   K0(KDOTVVL,R4),KDOTVVL(R4)  BLANK OUT SUFFIX    @Z40CSRC
         B     DSNQTST                  GO CHECK FOR REG. ENQ  @Z40CSRC
NOSUFFIX EQU   *                        NO SUFFIX TO BLANK     @Z40CSRC
         NI    DSMADTB2,X'FF'-DSMDSNE   RESET DSN ENQ'ED SWITCH  Y02144
*
* IF SCRATCH ENQ'ED ON THE TIOT, THIS SECTION ISSUES A DEQ.
*
DEQTEST3 EQU   *                        DEQ TIOT TEST            Y02082
         TM    SISW1,TIOTENQ            TEST IF ENQ'ED ON TIOT   Y02082
         BNO   TESTSMF                  BRANCH IF NOT            Y02082
         MVC   ENQAREA(DEQLTH2),DEQLIST2  MOVE IN DEQ PARM LIST  Y02082
         MVC   MJNAME(L'TIOTMJR),TIOTMJR  MAJOR RESOURCE NAME    Y02082
         DEQ   (MJNAME,TIOTMNR),MF=(E,ENQAREA)  DEQ FROM TIOT    Y02082
*
         NI    DSMADTB2,X'FF'-DSMTIOTE  RESET TIOT ENQ'ED SWITCH Y02144
*
* THIS SECTION DETERMINES IF AN SMF RECORD TYPE 17 MUST BE BUILT.
*
TESTSMF  EQU   *                                                 S19028
         TM    SISW1,NOSMFRCD           TEST IF SCRATCH TOTALLY  Y02082
*                                       OR PARTIALLY SUCCESSFUL  Y02082
         BO    EXIT                     BRANCH IF NOT            Y02082
         L     RCVT,CVTPTR              LOAD CVT ADDRESS         Y02082
         L     R2,TCBADDR               LOAD TCB ADDRESS         Y02082
         USING TCB,R2                   TCB ADDRESSABILITY       Y02082
         L     R2,TCBTCT                POINTER TO TCB           Y02082
         LA    R2,0(R2)                 CLEAR HI-ORDER         @ZA04736
         LTR   R2,R2                    TEST FOR TCT             M4916
         BZ    EXIT                     BRANCH IF NO TCT         M4916
         USING SMFTCT,R2                TCT ADDRESSABILITY       Y02082
         L     R2,TCTJMR                LOAD JMR ADDRESS         Y02082
         LTR   R2,R2                    IS THERE A JMR           Y02082
         BZ    EXIT                     BRANCH IF NOT            Y02082
         USING JMRJOB,R2                JMR ADDRESSABILITY       Y02082
         L     RA,CVTSMCA                                        S19028
         LTR   RA,RA                    IS THIS AN SMF SYSTEM    S19028
         BZ    EXIT                     BRANCH IF NO             S19028
         USING SMCABASE,RA                                       S19028
         TM    SMCAOPT,SMCADSA          IS DS RECORDING DESIRED  S19028
         BNO   EXIT                     BRANCH IF NO             S19028
         L     R6,OLDPLPTR              GET PARM LIST POINTER    S19028
         L     R7,4(R6)                 DSNAME PTR WITH IND BYTE S19028
         L     VOLISTX,K12(R6)          VOLUME LIST POINTER      Y02082
*
* THE FOLLOWING SECTION TESTS IF SMF TYPE 17 RECORDS MUST BE BUILT
* FOR TEMPORARY DATA SETS.  THE CALLER MUST BE IN SUPERVISOR STATE,
* IN KEY 0, OR AUTHORIZED TO TEST THE SYSIN/SYSOUT BIT IN THE JFCB.
*
         STM   RETURN,R3,IECREGSV+K12   SAVE TESTAUTH REGISTERS  Y02082
         TESTAUTH STATE=YES,KEY=YES,FCTN=1,BRANCH=YES  TEST IF   Y02082X
                                        CALLER IS IN KEY 0 OR    Y02082X
                                        IN SUPERVISOR STATE      Y02082
         LR    R4,R15                   SAVE RETURN CODE         Y02082
         LM    RETURN,R3,IECREGSV+K12   RESTORE REGISTERS        Y02082
         LTR   R4,R4                    TEST IF ANY CASE TRUE    Y02082
         BNZ   TESTNAME                 BRANCH IF NOT            Y02082
         TM    4(R6),X'40'              WAS ENTRY FROM A RDR OR  S19028
*                                       A WTR                    S19028
         BO    EXIT                     BRANCH IF YES            S19028
         TM    4(R6),X'80'              WAS ENTRY FROM STEP TERM S19028
*                                       OR JOB TERM? IF SO, THE  S19028
*                                       DSNAME PTR POINTS TO THE S19028
*                                       JFCBDSNM                 S19028
         BNO   TESTNAME                 BRANCH IF NO             Y02082
         USING INFMJFCB,R7                                       S19028
         TM    JFCBTSDM,JFCSDS          IS THIS A SYSIN/SYSOUT   S19028
*                                       DATA SET                 S19028
         BO    EXIT                     BRANCH IF YES            S19028
         DROP  R7                                                YM1337
TESTNAME EQU   *                        TEST NAME                Y02082
         CLC   SYSTEMDS,PDSNAME         SYSTEM GENERATED NAME?   YM1337
         BNE   BUILDSMF                 BRANCH IF NOT            A43551
         CLC   SYSTIME,PDSNAME+K8       TEST FOR CHARACTERS '.T' YM1337
         BNE   BUILDSMF                 BRANCH IF NOT            A43551
         CLI   PDSNAME+K16,PERIOD       TEST IF A TEMPORARY DS   YM1337
         BNE   BUILDSMF                 BRANCH IF NOT            A43551
         TM    SMCAOPT,SMCATDS          TEST IF RCD 17 DESIRED   A43551
*                                       FOR TEMPORARY DATA SETS  A43551
         BNO   EXIT                     BRANCH IF NO             S19028
*
* THIS SECTION CALCULATES IF ANOTHER WORK AREA MUST BE OBTAINED
* TO BUILD THE SMF RECORD TYPE 17.
*
BUILDSMF EQU   *                                                 A43551
         LH    VOLCTR,K0(VOLISTX)       GET THE NUMBER OF VOLS   Y02082
         LA    VOLISTX,K2(VOLISTX)      POINT TO FIRST ENTRY     Y02082
         LA    R4,WKADEB-SMF17RV2       DETERMINE HOW MANY VOLS  Y02082
         SRL   R4,3                     WILL FIT IN THE WKAREA   Y02082
         LA    R3,SMF17HDR              POINT TO SMF RECORD      Y02080
         CR    R4,VOLCTR                WILL ALL VOLS FIT IN     Y02082
*                                       THE SCRATCH WORK AREA    Y02082
         BNL   COREOK                   BRANCH IF YES            S19028
         LR    R4,VOLCTR                CALCULATE CORE NEEDED    Y02082
         SLL   R4,K3                    VOL COUNT TIMES 8        Y02082
         LA    R4,SMF17RV2-SMF17HDR(R4)  SET UP TO GET CORE      Y02082
         IECRES GET,EXTPR=(RWKAREA),ID=SCWA,LV=(R4),             Y02082X
               STM=(R2,RETURN,IECREGSV+K12)                      Y02080
         LR    R3,R1                    GET WORK AREA ADDRESS    Y02082
         MODESET EXTKEY=ZERO            RETURN TO KEY ZERO AFTER Y02082X
                                        ISSUING THE IECRES GET   Y02082
         USING SMF17HDR,R3              ADDRESSABILITY           Y02082
*
* THIS SECTION BUILDS AN SMF RECORD TYPE 17.
*
COREOK   EQU   *                                                 A43551
         XC    SMF17HDR(6),SMF17HDR     ZERO RECORD HEADER       A43551
         MVI   SMF17RTY,SMFREC17        SET RECORD TYPE          Y02082
         TIME  BIN                                               S19028
         STM   R0,R1,SMF17TME+2         TIME OF RECORD           S19028
         MVC   SMF17TME(8),SMF17TME+2                            S19028
         MVC   SMF17SID(4),SMCASID                               S19028
         DROP  RA                                                S19028
         MVC   SMF17JBN(SMF17UID-SMF17JBN),JMRJOB  JOB NAME      Y02082
         MVC   SMF17UID,JMRUSEID        USER ID FIELD            Y02082
         DROP  R2                                                Y02082
         XC    SMF17RIN,SMF17RIN        ZERO RECORD INDICATORS   Y02082
         MVC   SMF17DSN,PDSNAME         NAME OF SCRATCHED DS     YM1337
         XC    SMF17RV1,SMF17RV1        ZERO RESERVED AREA       Y02082
         XR    R7,R7                                             S19028
         LA    R2,SMF17RV2                                       Y02082
         USING SMF17RV2,R2                                       Y02082
TESTVOL  EQU   *                        WAS SCRATCH SUCCESSFUL   Y02082
         CLI   STATUS(VOLISTX),X00      WAS SCRATCH SUCCESSFUL   Y02082
         BNE   NEXTVOL                  BRANCH IF NOT            Y02082
         XC    SMF17RV2,SMF17RV2                                 Y02082
         MVC   SMF17FVL,VOLSER(VOLISTX)  MOVE VOL SER TO RECORD  Y02082
         LA    R7,1(R7)                 INCREMENT VOLUME COUNT   S19028
         LA    R2,SMF17LNV(R2)          INCREMENT RECORD PTR     Y02082
NEXTVOL  EQU   *                        NEXT VOLUME              Y02082
         LA    VOLISTX,ENTRYL(VOLISTX)  INCREMENT VOL LIST PTR   Y02082
         BCT   VOLCTR,TESTVOL           BRANCH IF MORE VOLUMES   Y02082
         LTR   R7,R7                    IS VOLUME COUNT ZERO     S19028
         BZ    EXIT                     BRANCH IF YES            S19028
         STC   R7,SMF17NVL              NUMBER OF SCRATCHED VOLS S19028
         DROP  R2                                                Y02082
         SR    R2,R3                    GET RECORD LENGTH AND    Y02082
         STH   R2,SMF17LEN              STORE IN RECORD          Y02082
         DROP  R3                                                Y02082
         LR    R1,R3                    POINT TO SMF RECORD      Y02082
         SMFWTM (1)                     WRITE SMF RECORD TYPE 17 A43551
*
* THIS SECTION RESTORES THE DEB AVT POINTER IF AN RPS WORK AREA EXISTS.
*
EXIT     EQU   *
         TM    DSMADTB1,DSMRPSAP        TEST IF RPS APPENDAGE    YM3048X
                                        IGG019EK WAS LOADED      YM3048
         BNO   CONTINUE                 BRANCH IF NOT            YM3048
         L     R1,WKADEB+APDIS          POINT TO APPENDAGE TABLE Y02082
         MVC   WKADEB+APDIS(K4),APSVA(R1)  RESTORE DEB AVT PTR   Y02082
         NI    DSMADTB1,X'FF'-DSMRPSAP  RESET RPS APP LOADED BIT Y02144
*
* THIS SECTION FREES ALL WORK AREAS AND RETURNS TO THE CALLER.
*
CONTINUE EQU   *                                                 S20201
         LH    R8,ERCODE                PICK UP ERROR CODE       Y02080
         L     RA,SCRSAVE               SAVE R14 IN R10        @G60ASBJ
         IECRES FREE,PREFIX=FIRST,A=(RWKAREA)                    Y02080
         LR    RETURN,RA                RESTORE R14            @G60ASBJ
         LR    R15,R8                   LOAD ERROR CODE          Y02080
         RETURN ,                                                Y02080
*
*** CONSTANTS
*
ERRMSG   WTO   'IEC603I VTOC ERRORS MAY EXIST ON UUU,VOLSER,0',  Y02078X
               ROUTCDE=(4,10),DESC=4,MF=L                        Y02078
MSGEND   EQU   *                        END OF MSG               Y02078
VTOCNAME DC    CL8'SYSVTOC '
SYSTEMDS DC    C'SYS'                   EBCDIC CHARACTERS 'SYS'  A43551
SYSTIME  DC    C'.T'                    EBCDIC CHARACTERS '.T'   A43551
CDOTV    DC    C'.V'                    CVOL SUFFIX CONSTAN    @Z40CSRC
DEQLIST1 DEQ   (,,,SYSTEM),TCB=0,MF=L   SYSDSN DEQ LIST          YM2880
DEQLTH1  EQU   *-DEQLIST1               LENGTH OF DEQ LIST       Y02082
DEQLIST2 DEQ   (,,6,SYSTEM),MF=L        SYSZTIOT DEQ LIST        YM2880
DEQLTH2  EQU   *-DEQLIST2               LENGTH OF DEQ LIST       Y02082
         DS    0F                       FULLWORD BOUNDARY        Y02134
RETNAME  DC    CL4'290D'                RETURN NAME FOR IGG0290F Y02134
TIOTMJR  DC    CL8'SYSZTIOT'            TIOT MAJOR RESOURCE NAME Y02082
DSNMJR   DC    CL8'SYSDSN'              DSN MAJOR RESOURCE NAME  Y02082
DDNAMEKY DC    AL2(DUNDDNAM)            DDNAME UNALLOCATION KEY  Y02082
*
*** TABLE OF MODULE IDS AND ENTRY POINT ADDRESSES
*
         XCTLTABL ID=(LOAD2,0E,MNTLOAD,0F),SVC=029,LENGTH=,      Y02134X
               BRT=YES                                           Y02134
         SPACE 2                                                 Y02082
         IECDSECS CVT,DSAB,JMR,JSCB,QDB,RB,SMF,SMFTCT,TCB,TIOT,  Y02082X
               UCB,EXPAND=YES                                    Y02082
JFCB     DSECT                                                 , S19028
         IEFJFCBN                                              , S19028
         EJECT
WORKAREA IECSCRWA EP,F4,ADT=YES         SCRATCH WORK AREA        Y02144
         SPACE 2                                                 Y02080
         ORG   VTOCDSCB                                          Y02080
         IFASMFR   (17)                                          S19028
         EJECT                                                   Y02082
*
* DSECTS FOR INVOCATING DYNAMIC ALLOCATION (SVC 99)
*
         IEFZB4D0                       DYNAMIC ALLOCATION DSECT Y02082
         EJECT                                                   Y02082
         IEFZB4D2                       DYNAMIC ALLOCATION KEYS  Y02082
KDOTVVL  EQU   L'CDOTV+L'UCBVOLI        CVOL SUFFIX LENGTH     @Z40CSRC
         END   IGG0290D                                          A43551
