    TITLE      'IGG0553G - EXTEND - SPLIT OF IGG0553A'
IGG0553G CSECT
*
*MODULE NAME = IGG0553G
*
*DESCRIPTIVE NAME = EXTEND - SPLIT OF IGG0553A
*
*COPYRIGHT = NONE
*
*CHANGE ACTIVITY = SEE BELOW
*
*        RELEASE 21 NEW MODULE
*0000001930,003700-005200,007600-008200,009600,009800-011000,    M0130
*0000011800-013000,013400-013800,015200,015600-015800,016600,    M0130
*0000019200,020420,020470-020500,020600-020800,021400-021600,    M0130
*0000022000,043200-043600,044400-044600,045140,045180-046000,    M0130
*0000046400-047200,048600,053210,072000-072200,084100,084400,    M0130
*0000086400-087620,088400-091200,091800-092200                   M0130
*          RELEASE 21.7 DELETIONS/CHANGES
*0000000270,002230                                              SA61344
*          VS2 RELEASE 02 DELETIONS/CHANGES                           *
*0000000030-000150,000280-000290,003400,003700,004200,004600,    Y02080
*0000007200-008293,009400,010000,010400,011400,012200,013220-    Y02080
*0000014196,015000-015700,016000-018400,018800,019200-020000,    Y02080
*0000020400-020434,020490-020560,031000-040200,042000-043200,    Y02080
*0000043800,044500,045120-045360,045760-045960,047600-050000,    Y02080
*0000050800-051000,052000-052200,052600-053210,053290,053310,    Y02080
*0000054800,055200,055800,055920,056600,058400,059000-059400,    Y02080
*0000067400,069800,071200,084000-084200,084400,084600-085600,    Y02080
*0000086200-088000,089200,091800-315200                          Y02080
*0000                                                           YA03162
*                                                                     *
* STATUS CHANGE LEVEL 001                                             *
*                                                                     *
*SPLIT OF IGG0553A FOR RELEASE 21                                     *
*                                                                     *
*FUNCTION/OPERATION:  THIS MODULE CALCULATES THE NUMBER OF TRACKS     *
*   NEEDED TO EXTEND THE DATASET. IF THE MOUNTED VOLUME IS THE LAST   *
*   VOLUME FOR THE DATASET TO BE EXTENDED THE LAST TRACK OF THE LAST  *
*   EXTENT IS USED AS A PREFERRED TRACK ADDRESS FOR THE EXTENSION.    *
*   THE REQUEST AS GIVEN IN THE JFCB IS CONVERTED TO TRACKS.  IF THE  *
*   REQUEST IS OF THE AVERAGE-RECORD-LENGTH TYPE THE JFCBLKSI FIELD   *
*   IS USED TO DETERMINE THE AVERAGE RECORD LENGTH.  IF THE JFCBLKSI  *
*   FIELD IS ZERO, THE JFCBDRLH FIELD IS USED.  IF BOTH FIELDS ARE    *
*   ZERO, NO SPACE WILL BE GOTTEN.                                    *
*                                                                     *
*ENTRY POINTS:                                                        *
*        IGG0553G - ONLY ENTRY POINT TO THIS MODULE.  ENTRY IS        *
*        MADE FROM IGG0553A OR IGG0553F VIA A BRANCH.                 *
*                                                                     *
*INPUT:                                                               *
*   REGISTER 4 POINTS TO THE I/O SUPPORT WORK AREA, WHICH CONTAINS    *
*   THE FORMAT 4 DSCB.                                                *
*   REGISTER 5 POINTS TO THE USER'S DEB.                              *
*   REGISTER 11 POINTS TO THE EXTEND WORK AREA, WHICH CONTAINS THE    *
*   ADDRESSES OF THE FORMAT 4 AND FIRST FORMAT 5 DSCBS.               *
*   THE I/O SUPPORT WORK AREA IS LAID OUT AS FOLLOWS:                 *
*                                                                     *
*          BYTES          AREA                                        *
*         *******        ******                                       *
*          100           DSCB FIELD                                   *
*          176           JFCB                                         *
*            4           ECB                                          *
*           40           IOB                                          *
*           44           DEB                                          *
*            4           DCB                                          *
*           96           CCW'S                                        *
*                                                                     *
*OUTPUT:                                                              *
*   REGISTER 5 POINTS TO THE USER'S DEB.                              *
*   REGISTER 12 WILL CONTAIN THE AMOUNT OF SPACE REQUESTED.           *
*   REGISTER 13 WILL CONTAIN THE RTA OF THE PREFERRED EXTENT.         *
*                                                                     *
*EXTERNAL ROUTINES:                                                   *
*        IECRES - BRANCH TO OTHER MODULES                             *
*                                                                     *
*EXITS:                                                               *
*   NORMAL - BRANCH TO IGG0553B                                       *
*                                                                     *
*   ERROR  - BRANCH TO IGG0553E WITH THE ENTRY CODE IN REGISTER 13    *
*   ERRORS MAY BE: LESS THAN 3 UNUSED DSCB'S IN THE VTOC              *
*                                                                     *
*ATTRIBUTES: REENTRANT                                                *
*                                                                     *
*OTHER MACROS USED:                                                   *
*   CVT - TO REFERENCE THE CVT                                        *
*   IECEXTWA - TO DEFINE THE EXTEND WORK AREA                         *
*   IECDSECT - TO DEFINE THE I/O SUPPORT WORK AREA                    *
*   IECSDSL1 - TO SYMBOLICALLY ADDRESS DSCB'S                         *
*                                                                     *
*STORAGE:                                                             *
*   PROGRAM CODE CSECT - LESS THAN 1024 BYTES                         *
*   EXTEND WORK AREA - AS DEFINED IN THE IECEXTWA MACRO               *
*   RPS WORK AREA - AS DEFINED IN THE IECRPS MACRO                    *
*   I/O SUPPORT WORK AREA                                             *
*
*
         BALR  RBASE,K0                 ESTABLISH ADDRESSABILITY Y02080
         USING BEGIN,RBASE                                       Y02080
         USING EOVWKA,RCORE             EOV W/A ADDRESSABILITY   Y02080
         USING EXTNDWKA,EXWKA           WORK AREA ADDRESSABILITY Y02080
*
* REGISTER EQUATES
*
REG0     EQU   0                        EQUATE FOR REGISTER 0    Y02080
REG1     EQU   1                        EQUATE FOR REGISTER 1    Y02080
REG2     EQU   2                        WORK REGISTER            Y02080
RBASE    EQU   3                        BASE REGISTER            Y02080
RCORE    EQU   4                        EOV WORK AREA ADDRESS    Y02080
RDEB     EQU   5                        DEB POINTER              Y02080
FREEREGE EQU   6                        EVEN REGISTER            Y02080
FREEREGO EQU   7                        ODD REGISTER             Y02080
FREEREGA EQU   8
FREEREGB EQU   9
REG9     EQU   9                        EQUATE FOR REGISTER 9    Y02080
REG10    EQU   10                       WORK REGISTER            Y02080
EXWKA    EQU   11                       EXTEND WORK AREA POINTER Y02080
REQ      EQU   12                       SPACE REQUESTED REGISTER Y02080
RPREF    EQU   13                       PREFERRED RTA REGISTER   Y02080
REG13    EQU   13                       EQUATE FOR REGISTER 13   Y02080
RETNREG  EQU   14
RWRK     EQU   15                       WORK REGISTER            Y02080
*
* OTHER EQUATES
*
K0       EQU   0                        CONSTANT OF 0            Y02080
K1       EQU   1                        CONSTANT OF 1            Y02080
K2       EQU   2                        CONSTANT OF 2            Y02080
K3       EQU   3                        CONSTANT OF 3
K4       EQU   4                        CONSTANT OF 4            Y02080
K6       EQU   6                        CONSTANT OF 6            Y02080
K7       EQU   7                        CONSTANT OF 7
K8       EQU   8                        CONSTANT OF 8            Y02080
K12      EQU   12                       CONSTANT OF 12           Y02080
X04      EQU   X'04'                    CONSTANT OF X'04'
NUMEXT   EQU   16                                                M0130
FIREXT   EQU   36                                                M0130
TBOVHD   EQU   X'08'                    TWO BYTE OVERHEAD FLAG   S20201
*
* THIS SECTION TESTS IF THERE ARE 3 UNUSED DSCB'S IN THE VTOC.
*
BEGIN    EQU   *                                                 M0130
         LA    RWRK,K3                                           Y02080
         CH    RWRK,DS4DSREC            TEST FOR 3 HOLES IN VTOC Y02080
         BH    NOROOM                   BRANCH IF LESS THAN 3
*
* THIS SECTION CHECKS TO SEE IF A NEW VOLUME HAS BEEN MOUNTED AND IF SO
* THE CALCULATION OF THE LAST TRACK USED BY THE DATA SET IS OMITTED
*
         SR    FREEREGO,FREEREGO
         IC    FREEREGO,NUMEXT(RDEB)    COMPUTE ADDRESS OF LAST  Y02080
         BCTR  FREEREGO,K0                EXTENT IN THE DEB      Y02080
         SLL   FREEREGO,K4                                       Y02080
         LA    FREEREGE,FIREXT(RDEB)                             Y02080
         AR    FREEREGO,FREEREGE        SET UP EXTENT ADDRESS    Y02080
         LR    FREEREGA,FREEREGO                                 Y02080
         TM    DXEXTSW,X04              TEST FOR NEW VOLUME      Y02080
         BZ    COMPRTA                  BRANCH IF NOT            M0130
         SR    RPREF,RPREF              ZERO PREFERRED RTA REG   Y02080
         B     CYLTRKRC
*
* THIS SECTION BRANCHES TO ANOTHER MODULE.
*
NOROOM   EQU   *                                                 O19117
         LA    REG2,LASTLOAD            POINT TO ID OF IGG0553E  Y02080
         SR    REG13,REG13              CLEAR ERROR REGISTER     Y02080
         IC    REG13,DXEXTSW            INSERT CALLER CODE       Y02080
XCTLHERE EQU   *                                                 M0130
         IECRES LOAD,EXTPR=(EXWKA),MODID=(REG2),BRANCH=DIRECT    Y02080
*
* THE LAST TRACK USED IS OBTAINED FROM THE DEB AND CONVERTED FROM
* CCHH TO RTA. THIS RTA IS THE PREFERRED EXTENT.
*
COMPRTA  EQU   *                        BRANCH LABEL             Y02080
         LA    FREEREGO,K6(FREEREGO)    POINTER TO CCHH          Y02080
         USING CVT,RWRK                 CVT ADDRESSABILITY       Y02080
         L     RWRK,CVTPTR              LOAD CVT ADDRESS         Y02080
         STM   REG9,REG13,IECREGSV+K12  SAVE REGISTERS           Y02080
         LA    REG1,DXDEB               SET POINTER TO DEB       Y02080
         XC    DXCCW12,DXCCW12                                   Y02080
         MVC   DXCCW12+K3(K4),0(FREEREGO)  SET UP CCHH           Y02080
         LA    REG2,DXCCW12             MBBCCHHR POINTER         Y02080
         L     RWRK,CVTPRLTV            ADDRESS OF CONVERT RTN   Y02080
         LR    FREEREGE,EXWKA           SAVE EXTEND WKA ADDRESS  Y02080
         BALR  RETNREG,RWRK             LINK TO CONVERT ROUTINE  Y02080
         LM    REG9,REG13,IECREGSV+K12-EXTNDWKA(FREEREGE)        Y02080
         LR    RPREF,REG0               SET UP PREFERRED RTA     Y02080
         SRL   RPREF,16                 SET UP PREFEERED RTA     Y02080
         LA    RPREF,K1(RPREF)                                   Y02080
*
* TEST FOR PRIMARY QUANTITY IN RECORDS
*
CYLTRKRC EQU   *                        BRANCH LABEL             Y02080
         XC    ETTRLL,ETTRLL            ZERO LAST BLOCK PTR      Y02080
         MVC   ETTRLL+K3(K2),DS4DEVTK   MOVE TRACK SIZE TO LL    Y02080
         TM    JFCBCTRI,X'C0'           IS PRI QTY IN CYLINDERS
         BC    1,INCYLIND               BRANCH IF YES
         TM    JFCBCTRI,X'40'           IS PRI QTY IN RECORDS
         BC    1,INRECORD               BRANCH IF YES
         L     REQ,JFCBSQTY             REQUEST IS TRACKS        Y02080
         SRL   REQ,K8                                            Y02080
         B     CLEANUP
INCYLIND EQU   *                        BRANCH LABEL             Y02080
         L     REQ,JFCBSQTY             LOAD NUMBER OF CYLINDERS Y02080
         SRL   REQ,K8                                            Y02080
SETREQ   EQU   *                        CYLINDERS ARE MULTIPLIED Y02080
         MH    REQ,DS4DEVSZ+K2          BY TRACKS PER CYLINDER   Y02080
CLEANUP  EQU   *                        BRANCH LABEL             Y02080
         LA    REG2,NEXTLOAD            POINT TO ID OF IGG0553B  Y02080
         TM    DXEXTSW,X04              TEST FOR NEW VOLUME      Y02080
         BZ    XCTLHERE                 BRANCH IF NO
         TM    JFCBLTYP,X'08'           USER LABEL REQUEST         UL17
         BZ    XCTLHERE                 BRANCH IF NO               UL17
         TM    JFCDSORG,JFCORGDA        TEST IF BDAM LOAD MODE  YA03162
         BO    XCTLHERE                 BR YES, NO UL 2ND VOL   YA03162
         TM    JFCBCTRI,X'C0'           TEST FOR CYLINDER REQUEST  UL17
         BC    1,USERCYL                BRANCH IF YES              UL17
         TM    JFCBCTRI,X'41'           RECORDS ROUNDED REQUEST    UL17
         BO    USERCYL                  BRANCH IF YES              UL17
         LA    REQ,K1(REQ)              ADD UL TRACK TO REQUEST  Y02080
         B     XCTLHERE                 GO TO EXIT                 UL17
USERCYL  EQU   *                        BRANCH LABEL             Y02080
         AH    REQ,DS4DEVSZ+K2          ADD USER LABEL FOR CYL   Y02080
         B     XCTLHERE
INRECORD SR    FREEREGA,FREEREGA        SET UP CALLING SEQUENCE
         IC    FREEREGA,JFCKEYLE        PICK UP KEY LENGTH
         LH    FREEREGB,JFCBLKSI       PICK UP BLOCK LENGTH
         LTR   FREEREGB,FREEREGB        IS BLKSIZE AVAILABLE    SA61344
         BNZ   BLKSIZOK                 BRANCH IF YES           SA61344
         MVC   DXCCW12+K1(L'JFCBDRLH),JFCBDRLH  AVG RCD SIZE    SA61344
         L     FREEREGB,DXCCW12         LOAD AVERAGE RCD SIZE   SA61344
         LA    FREEREGB,0(FREEREGB)     CLEAR HIGH BYTE         SA61344
         LTR   FREEREGB,FREEREGB        AVG RCD SIZE AVAILABLE  SA61344
         BNZ   BLKSIZOK                 BRANCH IF YES           SA61344
         B     NOROOM                   UNABLE TO CALCULATE THE SA61344
*                                       SECONDARY SPACE         SA61344
*
* OVERHEAD IS CALCULATED IN THIS SECTION
*
BLKSIZOK EQU   *                                                SA61344
         STM   FREEREGE,FREEREGO,IECREGSV+K12  SAVE REGISTERS    Y02080
         SR    FREEREGO,FREEREGO        CLEAR REGISTERS
         LR    REG2,FREEREGO                                     Y02080
         LR    FREEREGE,FREEREGO
         IC    FREEREGO,DS4DEVI         OVERHEAD FOR AVERAGE RCD
         IC    REG2,DS4DEVL             OVERHEAD FOR LAST RCD    Y02080
         TM    DS4DEVFG,TBOVHD          TWO BYTE OVERHEAD        S20201
         BZ    NOTTB10                  BRANCH NO                S20201
         LH    FREEREGO,DS4DEVI         GET TWO BYTE OVERHEAD    S20201
         LR    REG2,FREEREGO            USE FOR LAST ALSO        Y02080
NOTTB10  EQU   *                                                 S20201
         LTR   FREEREGA,FREEREGA        TEST KEY LENGTH EQ TO ZERO
         BZ    CALOVHD                  CALCULATE NEW OVERHEAD
ADDKLDL  AR    FREEREGB,FREEREGA        ADD KEYLENGTH TO DATA LENGTH
         LR    REG1,FREEREGB                                     Y02080
         LH    FREEREGE,DS4DEVTL        PICK UP DEVICE TOLERANCE
         SR    FREEREGA,FREEREGA
         MR    FREEREGA,FREEREGE        (KL+DL)TOLERANCE FACTOR
         SRDA  FREEREGA,9
         AR    FREEREGB,FREEREGO        ADD OVERHEADD I - - TOTAL IS X
         LR    FREEREGA,FREEREGB        SET UP AVERAGE RCD LENGTH
         B     COMPLETE
CALOVHD  IC    FREEREGE,DS4DEVK         SUBTRACT OVERHEAD CONSTANT
         SR    REG2,FREEREGE             L-K FOR NEW OVERHEAD    Y02080
         SR    FREEREGO,FREEREGE        REPEAT FOR AVERAGE RCD
         B     ADDKLDL
COMPLETE EQU   *                        BRANCH LABEL             Y02080
         AR    REG1,REG2                                         Y02080
         LR    FREEREGB,REG1            SET UP LAST RCD LENGTH=Y Y02080
         LM    FREEREGE,FREEREGO,IECREGSV+K12  RESTORE REGISTERS Y02080
*
* TRKCAL ----  THIS ROUTINE CALCULATES HOW MANY RECORDS OF A GIVEN
* SIZE WILL FIT ON A TRACK.
* THE LENGTH IN BYTES OF AN AVERAGE RECORD AND OF A LAST RECORD ON A
* TRACK ARE TO BE IN REGISTERS 4(FREEREGA) AND 5(FREEREGB)
* RESPECTIVELY
* THE NUMBER OF BYTES PER TRACK FOR THE DEVICE IS OBTAINED FROM THE
* DEVICE ENTRY OF THE FORMAT4 DSCB.
* RECORDS PER TRACK = 1 + (TRACK LENGTH-Y) / X
* THE LENGTH OF A LAST RECORD ON A TRACK IS SUBTRACTED FROM THE TRACK
* LENGTH
*
         LH    FREEREGO,DS4DEVTK
         SR    FREEREGO,FREEREGB        SUBTRACT Y FROM TRACK LENGTH
         BNL   SHORTREC                 BR IF RECORD DOES NOT EXCEED TK
*
* RECORDS EXCEED TRACK LENGTH. USE AVERAGE RCD AND MULTIPLY BY
* SECONDARY QUANTITY FOR TOTAL. DIVIDE BY DS4DEVTK FOR TRACKS
*
         L     FREEREGO,JFCBSQTY        SET UP NUMBER OF RECORDS
         SRL   FREEREGO,8
         MR    FREEREGE,FREEREGA        AVERAGE RCD/SECONDARY QUANTITY
         LH    FREEREGA,DS4DEVTK       TRACK LENGTH FOR DIVIDE
         B     LONGREC                  GO FOR DIVIDE
*
* IF THE LAST RECORD DOES NOT EXCEED THE TRACK LENGTH THE REMAINDER
* IS DIVIDED BY THE AVERAGE RECORD LENGTH
*
SHORTREC SR    FREEREGE,FREEREGE
         DR    FREEREGE,FREEREGA        DIVIDE BY AVERAGE RCD LENGTH
         LR    FREEREGA,FREEREGO
         LA    FREEREGA,1(FREEREGA)     INCREMENT RESULT BY ONE
         L     FREEREGO,JFCBSQTY        PICK UP SECONDARY QTY.
         SRL   FREEREGO,8
LONGREC  SR    FREEREGE,FREEREGE
         DR    FREEREGE,FREEREGA        DETERMINE NO. OF TRACKS
         LTR   FREEREGE,FREEREGE           FOR PRIMARY QUANTITY = A
         BZ    DONTINC                  BRANCH IF ZERO REMAINDER
         LA    FREEREGO,1(FREEREGO)    ADD 1 IF REMAINDER EXISTS
DONTINC  EQU   *                        BRANCH LABEL             Y02080
         LR    REQ,FREEREGO             ADD A TO B               Y02080
*
* A TEST IS MADE TO SEE IF THE ROUND TO CYLINDER OPTION IS SPECIFIED
*
         TM    JFCBCTRI,X'01'           TEST FOR ROUND OPTION
         BZ    CLEANUP                  BRANCH IF NOT ROUND
*
* WHEN THE ROUND TO FULL CYLINDERS OPTION IS GIVEN, THE NUMBER OF
* TRACKS IS DIVIDED BY THE NUMBER OF TRACKS PER CYLINDER TO SEE IF
* THERE ANY EXCESS TRACKS, AND IF SO THE NUMBER OF CYLINDERS IS
* INCREMENTED
*
ROUNDUP  EQU   *                        BRANCH LABEL             Y02080
         LR    FREEREGB,REQ                                      Y02080
         SR    FREEREGA,FREEREGA       THE NUMBER OF TRACKS IS DIVIDED
         LH    FREEREGE,DS4DEVSZ+2        BY THE NUMBER OF TRACKS PER
         DR    FREEREGA,FREEREGE          CYLINDER
         LTR   FREEREGA,FREEREGA       IF THERE IS A TRACK REMAINDER
         BZ    DONOTINC                   INCREMENT THE CYLINDER COUNT
         LA    FREEREGB,1(FREEREGB)
DONOTINC EQU   *                        BRANCH LABEL             Y02080
         LR    REQ,FREEREGB            SAVE NUMBER OF CYLINDERS  Y02080
         B     SETREQ
*
* * * * *                     BEGINNING OF CONSTANTS          * * * * *
K16      DC    H'16'                                             M4027
*
* TABLE OF MODULE IDS AND ENTRY POINT ADDRESSES
*
XCTLTABL XCTLTABL ID=(NEXTLOAD,3B,LASTLOAD,3E),SVC=055,BRT=YES,  Y02080X
               LENGTH=                                           Y02080
* * * * *                     END OF CONSTANTS                * * * * *
         EJECT                                                   Y02080
CVT      DSECT
         CVT                                                     Y02080
         EJECT                                                   Y02080
WORKAREA IECEXTWA EP                    EXTEND WORK AREA         Y02080
         EJECT                                                   Y02080
EOVWKA   DSECT                          EOV WORK AREA DSECT      Y02080
         IECDSECT
         EJECT                                                   Y02144
         ORG   DXLBL
         IECSDSL1 (4)
         END   IGG0553G
