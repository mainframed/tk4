 TITLE 'IGG0325P - NON-SPLIT-CYLINDER DATA SET PROCESSING'
IGG0325P CSECT
*                                                                     *
* MODULE NAME = IGG0325P
*
* DESCRIPTIVE NAME = NON-SPLIT-CYLINDER DATA SET PROCESSING
*
* COPYRIGHT = NONE
*
* CHANGE ACTIVITY = SEE BELOW
*
*          RELEASE 19 DELETIONS                                       *
*2754154000-164000                                               A27358
*          RELEASE 20 DELETIONS                                       *
*3361064000,096000,404000-405000                                 A31534
*3361260000-267000                                               A29936
*3361114000,244000-246000,401000-403000,413000-416000            A30794
*3361007000-013000,036000,060000-061000,066000,171000-180000,    A31333
*3361195000-196000,247000,249000,285000,302000-304000,311000,    A31333
*3361331000-336000,341000-342000,351000,352000,372000,375000,    A31333
*3361381000                                                      A31333
*3361                                                            A36311
*          RELEASE 21 DELETIONS                                       *
*1191007000-013000,016000,022000,380000,389000,395000,421000,    M0124
*1191423000-432000,434000-444000,446000,447000,449000-472000,    M0124
*1191474000-476000,482000,485000,487000-489000,497000            M0124
*1191064000,136000,392000-393000,401800-402800                   S21040
*0000001000,310000                                               A53183
*          RELEASE 22 DELETIONS                                       *
*          VS2 RELEASE 02 DELETIONS/CHANGES                           *
*0000000100-000500,002350-002630,036000-037000,058000,059600-    Y02080
*0000060200,062000,064000,066400,068000,075000,082000,112000,    Y02080
*0000119000-135000,139000,151000,159600-160200,162600,175600,    Y02080
*0000187000,188040-188080,188160,188400,189000,201000,204000-    Y02080
*0000205000,209000,211000,228000,236000,236600,258000,269000-    Y02080
*0000270000,275000,277000,294000-295000,313000,315000,337000-    Y02080
*0000340000,348000,352400-355000,364000,368000,372000-372500,    Y02080
*0000376000-380000,382000-383000,391000-394000,397000-400000,    Y02080
*0000400500,400600-416200,418000-433000,446000-486000,492000-    Y02080
*0000504000,506000,509000-510000,512000-574000                   Y02080
*0000011000,013000,021000-024000,026000-032000,035000-042000,    Y02078
*0000045000-047000,050000-052000,060800-061400,063000,065200-    Y02078
*0000066200,091000,118500,135500,168200-168600,168950,236300,    Y02078
*0000366000-371000                                               Y02078
*          VS2 RELEASE 03.0 DELETIONS/CHANGES                         *
*0000                                                           ZA00008
*0000168380,168410,168440,168470,168490-168500,168520-168530,   ZA00117
*0000168550                                                     ZA00117
*                                                                     *
*STATUS CHANGE LEVEL 004
*                                                                     *
*                                                                     *
*  NAME IGG0325P                                                      *
*                                                                     *
* FUNCTION OPERATION  THIS MODULE READS THE DSCB'S IN THE VTOC ONE    *
*   AT A TIME. IT CONVERTS NON-SPLIT CYLINDER ALLOCATED EXTENTS TO    *
*   RTA/RTA+1 FORMAT AND SUBTRACTS THESE EXTENTS FROM THE FREE SPACE  *
*   IN THE RE-INITIALIZED FORMAT 5. AT ANY POINT IN THE PROCESSING    *
*   WHEN THE FREE SPACE EXISTS IN MORE THAN 26 EXTENTS, THE MODULE    *
*   BRANCHES TO IGG0325R TO SUBTRACT FUTURE ALLOCATED EXTENTS. THE    *
*   MODULE ALSO TESTS FOR SPLIT CYLINDER DATA SETS TO DETERMINE IF    *
*   FORMAT 6 PROCESSING MUST BE DONE.  WHENEVER IGG0325P READS A      *
*   FORMAT 5 DSCB, IT STORES ITS CCHHR IN A VTOC CONVERSION WORK      *
*   AREA EXTENSION, WHICH IS CHAINED FROM THE ALLOCATE WORK AREA.     *
*   IF MORE THAN ONE FORMAT 5 DSCB IS CREATED DURING THE PROCESS      *
*   OF SUBTRACTING THE NON-SPLIT CYLINDER DATA SET EXTENTS FROM       *
*   THE FREE SPACE, THE RE-INITIALIZED FORMAT 5 DSCB'S ARE STORED     *
*   IN A VTOC CONVERSION WORK AREA EXTENSION OR EXTENSIONS.  IF       *
*   THERE WAS ORIGINALLY ONLY ONE FORMAT 5 DSCB ON THE VOLUME,        *
*   ONLY ONE FORMAT 5 WAS REBUILT, AND THERE ARE NO SPLIT CYLINDER    *
*   DATA SET EXTENTS, THEN THE REBUILT FORMAT 5 IS IN 'DSCBF5.'       *
*   OTHERWISE, THE REBUILT FORMAT 5 DSCB(S) ARE STORED IN THE VTOC    *
*   CONVERSION WORK AREA EXTENSION(S).                                *
*                                                                     *
* ENTRY POINTS                                                        *
*        IGG0325P- FROM IGG0325Z TO BEGIN BUILDING FORMAT 5 DSCB'S    *
*                  FROM IGG0325R TO PLACE MORE ALLOCATED EXTENTS      *
*                                IN THE DADSMTABLE                    *
*                                                                     *
* INPUT  FROM IGG0325Z                                                *
*              REGISTER 13 POINTS TO THE ALLOCATE WORK AREA, WHICH    *
*              CONTAINS THE FIRST RE-INITIALIZED FORMAT 5 DSCB AND    *
*              THE FORMAT 4 DSCB. 'COUNT' CONTAINS THE CCHHR OF THE   *
*              THIRD DSCB IN THE VTOC.                                *
*                                                                     *
*        FROM  IGG0325R                                               *
*              REGISTER 13 POINTS TO THE ALLOCATE WORK AREA, WHICH    *
*              CONTAINS THE FORMAT 4 DSCB, THE LAST FORMAT 1 OR 3     *
*              DSCB READ, AND THE FIRST FORMAT 5 DSCB IN THE CHAIN.   *
*              'COUNT' CONTAINS THE CCHHR OF THE NEXT DSCB TO BE READ.*
*              REGISTER 1 IS ZERO TO INDICATE REENTRY FROM IGG0325R.  *
*                                                                     *
* OUTPUT  TO IGG0325R                                                 *
*              REGISTER 13 POINTS TO THE ALLOCATE WORK AREA, WHICH    *
*              CONTAINS THE FORMAT 4 DSCB, THE LAST FORMAT 1 OR 3     *
*              DSCB READ, AND THE FIRST FORMAT 5 DSCB.  THE SEEK      *
*              ADDRESS IN THE IOB CONTAINS THE CCHHR OF THE DSCB      *
*              BEING PROCESSED.  'CCW12' CONTAINS A TWO WORD          *
*              PARAMETER LIST CONSISTING OF THE NUMBER OF EXTENTS     *
*              IN THE DADSMTBL TO BE SUBTRACTED AND THE ADDRESS       *
*              OF THE FIRST EXTENT TO BE SUBTRACTED.                  *
*                                                                     *
* OUTPUT  TO IGG0325Q                                                 *
*              REGISTER 13 POINTS TO THE ALLOCATE WORK AREA.  THE     *
*              HIGH WATER MARK IN THE FORMAT 4 IDSCB IS NOW VALID.    *
*              'FMTOUTAD' CONTAINS THE CCHHR OF THE FIRST FORMAT 5.   *
*              THE FIRST FORMAT 5 DSCB HAS BEEN MOVED TO THE VTOC     *
*              CONVERSION WORK AREA EXTENSION.                        *
*                                                                     *
* OUTPUT TO  IGG0325T                                                 *
*              REGISTER 13 POINTS TO THE ALLOCATE WORK AREA.  IF      *
*              THERE WAS ORIGINALLY ONLY ONE FORMAT 5 DSCB ON THE     *
*              VOLUME AND ONLY ONE FORMAT 5 WAS REBUILT, THEN THE     *
*              REBUILT FORMAT 5 IS IN 'DSCBF5.'  OTHERWISE, THE       *
*              REBUILT FORMAT 5 DSCB(S) ARE LOCATED IN THE VTOC       *
*              CONVERSION WORK AREA EXTENSION(S).                     *
*                                                                     *
* EXTERNAL ROUTINES                                                   *
*        CVTPRLTV                                                     *
*        EXCP(0)                                                      *
*        WAIT(1)                                                      *
*                                                                     *
* EXITS  NORMAL  BRANCH TO IGG0325R TO BUILD 2ND...NTH F5 DSCB'S      *
*                BRANCH TO IGG0325Q TO SUBTRACT SPLIT CYLINDER        *
*                     DATA SETS FROM THE FORMAT 5 DSCB'S AND TO       *
*                     BUILD THE FIRST F6 DSCB                         *
*                BRANCH TO IGG0325T TO CONVERT F5 DSCB'S TO CORRECT   *
*                     FORMAT AND WRITE THEM TO THE VTOC AND TO WRITE  *
*                     THE UPDATED F4 DSCB TO THE VTOC                 *
*                                                                     *
*        ERROR  BRANCH TO IGG0325T                                    *
*                                                                     *
*        ERROR CODES ISSUED BY THIS MODULE -                          *
*                                                                     *
*        0C - PERMANENT I/O ERROR                                     *
*                                                                     *
*        94 - OVERLAPPING EXTENTS IN THE VTOC                         *
*                                                                     *
* TABLE/WORK AREAS - DADSM ALLOCATE WORK AREA                         *
*                                                                     *
* ATTRIBUTES REENTRANT                                                *
*                                                                     *
*                                                                     *
*                                                                     *
REGZERO  EQU   0                       WORK REGISTER
REENTRY  EQU   1                       INDICATES ENTRY FROM IGG0325R
REGONE   EQU   1                       WORK REGISTER
REGTWO   EQU   2                       WORK REGISTER
DADSMCTR EQU   3                       CTR EXTENTS IN DADSMTBL
REGTHREE EQU   3                       WORK REGISTER
FORMATCT EQU   4                       MAX ON EXTENTS IN DSCB
REGFOUR  EQU   4                       WORK REGISTER             Y02078
EXTPTR   EQU   5                       PTR TO EXTENT BEING CONVERTED
REGFIVE  EQU   5                       WORK REGISTER             Y02078
REGSIX   EQU   6                       WORK REGISTER
LSTF5CTR EQU   6                       NBR OF EXTENTS IN LAST F5 Y02080
REGSEVEN EQU 7                         WORKREGISTER
F5EXTCTR EQU   7                       NO OF EXTENT BEING COMPARED
REGEIGHT EQU   8                       WORK REGISTER
DADSMPTR EQU   8                       PTR TO EXTENT IN DADSMTBL
ALLOCADR EQU   8                       PTR TO EXTENT IN DADSMTBL
RERRPASS EQU   8                        ERROR CODE REGISTER      A31534
REGNINE  EQU   9                       WORK REGISTER
F5ADDR   EQU   9                       PTR TO EXTENT IN F5
REGTEN   EQU   10                      WORK REGISTER
RVTOCWKA EQU   11                      VTOC CONVERSION W/A PTR   Y02078
BASEREG  EQU   12                      BASE REGISTER
REGTHIRT EQU   13                      WORK REGISTER
RWKAREA  EQU   13                      BASE REGISTER TO WORK AREA
RETRNREG EQU   14                      RETURN REGISTER
REG14    EQU   14                      WORK REGISTER             Y02078
RCVT     EQU   15                      BASE REGISTER TO CVT
*                                                                     *
* OTHER EQUATES                                                       *
*                                                                     *
ZERO     EQU   0                        VALUE ZERO
ONE      EQU   1                        VALUE ONE
TWO      EQU   2                        VALUE TWO
THREE    EQU   3                        VALUE THREE
K4       EQU   4                        CONSTANT OF 4            Y02080
FOUR     EQU   4                        VALUE FOUR
FIVE     EQU   5                        VALUE FIVE
SIX      EQU   6                        VALUE SIX
SEVEN    EQU   7                        VALUE SEVEN
EIGHT    EQU   8                        VALUE EIGHT
THIRT    EQU   13                       VALUE THIRTEEN
SIXTEEN  EQU   16                       VALUE SIXTEEN
TWTYSIX  EQU   26                       VALUE TWENTY-SIX
F1ID     EQU   X'F1'                    F1 DSCB INDICATOR
F2ID     EQU   X'F2'                    F2 DSCB ID               Y02078
SPEXT    EQU   X'80'                    SPLIT CYL EXTENT INDICATOR
F3ID     EQU   X'F3'                    F3 DSCB INDICATOR
F5ID     EQU   X'F5'                    F5 DSCB ID               Y02078
F5INDIC  EQU   X'05'                    F5 DSCB INDICATOR
C32      EQU   32                       VALUE THIRTY-TWO
GOODIO   EQU   X'20'                    TEST VALUE FOR SUCCESSFUL I/O
ERRCD    EQU   X'0C'                    ERROR CODE FOR UNSUCCESSFUL I/O
VTOCERR  EQU   X'94'                    OVERLAPPING XTENTS ERROR Y02078
NOCC     EQU   X'00'                    NO COMMAND CHAINING      Y02080
CC       EQU   X'40'                    COMMAND CHAINING BIT     Y02080
TEN      EQU   10                       VALUE 10                 A31333
K12      EQU   12                       VALUE OF 12              Y02080
*                                                                     *
         BALR  BASEREG,ZERO             INT. BASE REGISTER
         USING *,BASEREG
         USING ALLOCWKA,RWKAREA         WORK AREA ADDRESSABILITY Y02080
         USING   CVT,RCVT
         USING IECPREFX,REGTEN          PREFIX ADDRESSABILITY    Y02078
         USING VTOCWKA,RVTOCWKA         WORK AREA ADDRESSABILITY Y02078
*
         LTR   REENTRY,REENTRY         IS THIS REENTRY
         BZ    RETURNPT                 BRANCH IF YES
*                                                                     *
* INITIALIZE CHANNEL PROGRAM TO READ F1 AND F3 DSCB'S                 *
*                                                                     *
         LA    DADSMCTR,FIVE            INT. CTR OF EXTENTS IN DADSMTBL
         LA    REGEIGHT,DADSMTBL
*                                                                     *
* TEST END OF VTOC SWITCH AND READ THE NEXT DSCB IN THE VTOC          *
*                                                                     *
TESTSW   EQU   *                        BRANCH LABEL             Y02080
         TM    ASWITCH,EOVSW            IS END OF VTOC SWITCH ON Y02080
         BO    FINISHF5                 BRANCH IF YES
         MVC   SEEKCYL(FIVE),COUNT      MOVE CCHHR OF NEXT DSCB TO IOB
*                                                                     *
* TEST FOR END OF VTOC.  IF THE DSCB TO BE READ IS THE LAST DSCB IN   *
* THE VTOC, REMOVE THE READ COUNT CCW FROM THE CHANNEL PROGRAM TO     *
* PREVENT COMMAND REJECTS (SEEKING OFF THE VOLUME) IF THERE IS NO     *
* COUNT FIELD AFTER THE VTOC.  SET EOV SWITCH IF LAST DSCB.           *
*                                                                     *
         CLC   SEEKCYL(FOUR),DS4VTOC6   Q - LAST TRACK IN VTOC   A27358
         BNE   NOTLAST                  IF NOT, BR               A27358
         CLC   SEEKREC(ONE),DS4DEVDT    Q - LAST RCD ON TRACK    A27358
         BNE   NOTLAST                  IF NOT, BR               A27358
         OI    ASWITCH,EOVSW            SET END OF VTOC SWITCH   Y02080
         MVI   CCW6+K4,NOCC             BREAK RD COUNT FROM      Y02080
*                                       CHAIN                    A27358
NOTLAST  EQU   *                                                 A27358
         BAL   RETRNREG,EXCHANPG        READ NEXT DSCB           A27358
         MVI   CCW6+K4,CC               RESTORE READ COUNT TO    Y02080
*                                       CHAIN                    A27358
*                                                                     *
* TEST FOR FORMAT ONE OR FORMAT 3 DSCB. IF FORMAT ONE CHECK FOR SPLIT *
* CYLINDER DATA SET,UPDATE HIGH WATER MARK                            *
*                                                                     *
F1ORF3   CLI   DS1FMTID,ZERO           IS THIS A ZERO DSCB       8M3261
         BNE   NOTF0                    BRANCH IF NOT            Y02078
         CLC   DS1DSNAM(FOUR),ZEROS     TEST FOR KEY OF ZEROS    Y02078
         BE    TESTSW                   BRANCH IF TRUE F0 DSCB   Y02078
DECRMENT EQU   *                        BRANCH LABEL             Y02078
         LH    REGONE,DS4DSREC          GET HOLE COUNT           Y02078
         BCTR  REGONE,ZERO              DECREMENT HOLE COUNT     Y02078
         STH   REGONE,DS4DSREC          SAVE UPDATED COUNT       Y02078
         B     TESTSW                   GO READ NEXT DSCB        Y02078
NOTF0    EQU   *                        BRANCH LABEL             Y02078
         CLI   DS1FMTID,F2ID            TEST FOR FORMAT 2 DSCB   Y02078
         BE    DECRMENT                 BRANCH IF FORMAT 2 DSCB  Y02078
         CLI   DS1FMTID,F5ID            TEST IF FORMAT 5 DSCB    Y02078
         BH    DECRMENT                 BRANCH IF F6 OR HIGHER   Y02078
         BL    CHECK1                   BRANCH IF F1 OR F3 DSCB  Y02078
         BAL   RETRNREG,PREFXADR        GO GET PREFIX ADDRESS    Y02078
NEXTADR1 EQU   *                        PREFIX ADDRESS IN REG 10 Y02078
         CLC   IECCORPS,IECCORP2        TEST FOR ANOTHER WKAREA  Y02078
         BE    NEWKAREA                 BRANCH IF NO EXTENSION   Y02078
         L     REGTEN,IECCORP2          LOAD ADDR OF NEXT PREFIX Y02078
         CLC   IECCORID,VCWKAID         TEST IF VTOC WORK AREA   Y02078
         BNE   NEXTADR1                 BRANCH IF NOT            Y02078
         XR    REGFOUR,REGFOUR          CLEAR FOR DIVISION       Y02078
         LH    REGFIVE,F5FNDCT          GET NUMBER OF FOUND F5'S Y02078
         D     REGFOUR,FW3              3 F5'S PER WKA EXTENSION Y02078
         LTR   REGFOUR,REGFOUR          TEST FOR AN EMPTY SLOT   Y02078
*                                       IN A WORK AREA EXTENSION Y02078
         BZ    TOPSLOT                  BR IF F5 IN TOP SLOT OF ZA00117
*                                       A VCWA. CHECK IF        ZA00117
*                                       PRESENT                 ZA00117
         CL    REGFIVE,ZEROS            TEST IF ONLY 1 EXTENSION Y02078
*                                       WITH FOUND F5 IN IT     ZA00117
         BE    LOADADDR                 BRANCH IF ONLY 1         Y02078
NEXTADR2 EQU   *                        LOOP TILL LAST VCWA     ZA00117
*                                       WITH FOUND F5S IN IT    ZA00117
         L     REGTEN,IECCORP2          LOAD ADDR OF NEXT PREFIX Y02078
         BCT   REGFIVE,NEXTADR2         LOAD ADDR OF LAST PREFIX Y02078
*                                       WITH FOUND F5S IN IT    ZA00117
LOADADDR EQU   *                        GET SLOT 1, 2, OR 3     ZA00117
         L     RVTOCWKA,IECCORAD        LOAD ADDR OF LAST WKAREA Y02078
         LA    REG14,VTCCHHR1           ADDR OF 1ST CCHHR SLOT  ZA00117
         LTR   REGFOUR,REGFOUR          FIRST ENTRY IN VCWA     ZA00117
         BZ    NXTCCHHR                 BRANCH IF YES           ZA00117
         LA    REG14,VTCNXTF5(REG14)    POINT TO 2ND SLOT       ZA00117
         BCT   REGFOUR,INCRPTR          FIND ADDR OF EMPTY SLOT  Y02078
NXTCCHHR EQU   *                        PUT CCHHR IN PROPER     ZA00117
*                                       SLOT                    ZA00017
         MVC   ZERO(FIVE,REG14),SEEKCYL MOVE IN FOUND F5 CCHHR   Y02078
         B     UPF5CNT                  GO INCREMENT F5FNDCT     Y02078
INCRPTR  EQU   *                        THIRD SLOT NEEDED       ZA00117
         LA    REG14,VTCNXTF5(REG14)    POINT TO 3RD CCHHR SLOT ZA00117
         B     NXTCCHHR                 GO MOVE IN THE CCHHR     Y02078
TOPSLOT  EQU   *                        IS VCWA AVAILABLE FOR   ZA00117
*                                       NEW FORMAT 5            ZA00117
         CLC   F5FNDCT,F5BLDCT          FOUND LESS THAN BUILT   ZA00117
         BL    NEXTADR2                 BR YES-SLOT AVAILABLE   ZA00117
NEWKAREA EQU   *                        BRANCH LABEL             Y02078
         BAL   RETRNREG,GETWKA          GO GET WKAREA EXTENSION  Y02078
         CLC   F5FNDCT,ONEF5            TEST IF ONLY 1 FOUND F5  Y02078
         BNE   MOVEF5AD                 BRANCH IF MORE THAN 1    Y02078
         MVC   VTCCHHR1,FMTOUTAD        MOVE IN CCHHR OF 1ST F5  Y02078
         MVC   VTCCHHR2,SEEKCYL         MOVE IN CCHHR OF 2ND F5  Y02078
         B     UPF5CNT                  GO INCREMENT F5FNDCT     Y02078
MOVEF5AD EQU   *                        BRANCH LABEL             Y02078
         MVC   VTCCHHR1,SEEKCYL         MOVE IN CCHHR OF THIS F5 Y02078
UPF5CNT  EQU   *                        BRANCH LABEL             Y02078
         LH    REGONE,F5FNDCT           GET NBR OF FOUND F5'S    Y02078
         LA    REGONE,ONE(REGONE)       INCREMENT THIS NUMBER    Y02078
         STH   REGONE,F5FNDCT           SAVE UPDATED COUNT       Y02078
         B     TESTSW                   GO READ NEXT DSCB        Y02078
CHECK1   EQU   *                        BRANCH LABEL             Y02078
         LH    REGONE,DS4DSREC         GET HOLE COUNT            8M3261
         BCTR  REGONE,REGZERO          DECREMENT HOLE OCUNT      8M3261
         STH   REGONE,DS4DSREC         SAVE NOW HOLE COUNT       8M3261
         CLI   DS1FMTID,F1ID           IS THIS A F1 DSCB         8M3261
         BNE   CHECK3                   BRANCH IF NO
         LA    REGONE,DS1EXT1           POINT TO FIRST EXTENT    A31333
         LA    REGTWO,DS1PTRDS          POINT TO END OF 3        A31333
*                                       EXTENTS                  A31333
TESTF1   EQU   *                                                 A31333
         CLI   ZERO(REGONE),SPEXT       TEST IF SPLIT CYL DATA   A31333
*                                       SET                      A31333
         BNE   MOVEPTR1                 BRANCH IF NOT SPLIT CYL  A31333
         OI    ASWITCH,SPCYLSW          SET SPLIT CYLINDER       Y02080
*                                       SWITCH                   A31333
MOVEPTR1 EQU   *                                                 A31333
         LA    REGONE,TEN(REGONE)       INCREMENT EXTENT POINTER A31333
         CR    REGONE,REGTWO            TEST IF 3 EXTENTS TESTED A31333
         BL    TESTF1                   BRANCH IF NOT            A31333
HIWATER  MVC   DS4HPCHR(FIVE),SEEKCYL   UPDATE HIGH WATER MARK
         LA    EXTPTR,DS1EXT1           INT PTR TO FIRST EXT
         CLI   ZERO(EXTPTR),ZERO        IS THIS A MODEL DSCB
         BE    TESTSW                   BRANCH IF YES
         LA    FORMATCT,THREE           INT CTR OF NO EXTENTS IN F1
         B     CONVERT                  BRANCH TO CONVERT EXTENTS
CHECK3   EQU   *                        BRANCH LABEL             Y02080
         CLI   DS3FMTID,F3ID            IS THIS F3 DSCB          Y02080
         BNE   TESTSW                   BRANCH IF NO
         LA    REGONE,DS3EXTNT          POINT TO FIRST F3 EXTENT Y02080
         LA    REGTWO,DS3FMTID          POINT TO END OF 4        Y02080
*                                       EXTENTS                  A31333
         LA    REGTEN,DS3PTRDS          POINT TO END OF 13       Y02080
*                                       EXTENTS                  A31333
TESTF3   EQU   *                                                 A31333
         CLI   ZERO(REGONE),SPEXT       TEST IF SPLIT CYL DATA   A31333
*                                       SET                      A31333
         BNE   MOVEPTR3                 BRANCH IF NOT            A31333
         OI    ASWITCH,SPCYLSW          SET SPLIT CYLINDER       Y02080
*                                       SWITCH                   A31333
MOVEPTR3 EQU   *                                                 A31333
         LA    REGONE,TEN(REGONE)       INCREMENT EXTENT POINTER A31333
         CR    REGONE,REGTWO            WAS 4TH F3 EXTENT JUST   A31333
*                                       TESTED                   A31333
         BNE   TESTALL                  BRANCH IF NOT            A31333
         LA    REGONE,ONE(REGONE)       INCREMENT PAST F3 ID     A31333
TESTALL  EQU   *                                                 A31333
         CR    REGONE,REGTEN            TEST IF 13 EXTENTS       A31333
*                                       TESTED                   A31333
         BL    TESTF3                   BRANCH IF NOT            A31333
         LA    EXTPTR,DS3EXTNT          POINT TO 1ST F3 EXTENT   Y02080
         LA    FORMATCT,THIRT           INT. CTR OF NO. EXTENTS IN F3
*                                                                     *
* CONVERT AN EXTENT FORM CCHH/CCHH TO RTA/RTA+1  USING THE RESIDENT   *
* CONVERSION ROUTINE                                                  *
*                                                                     *
CONVERT  EQU   *                                                 A31333
         CLI   ZERO(EXTPTR),SPEXT       TEST IF SPLIT CYL DATA   A31333
*                                       SET                      A31333
         BNE   BGINCONV                 BRANCH IF NOT            A31333
         LA    EXTPTR,TEN(EXTPTR)       POINT TO NEXT EXTENT     A31333
         B     CONVNEXT                 BRANCH TO CONVERT NEXT   A31333
*                                       EXTENT                   A31333
BGINCONV EQU   *                                                 A31333
         LA    REGSEVEN,TWO             SET LOOP CONTROL
         AR    EXTPTR,REGSEVEN          GET PTR TO BEGINNING CCHH
         LA    REGTWO,CCW12             GET PTR TO ADDR MBBCCHHR INPUT
         LA    REGONE,DEB               GET PTR TO DEB
ROUTINE  EQU   *                        BRANCH LABEL             Y02080
         STM   REGEIGHT,REGTHIRT,IECREGSV+K12  SAVE REGISTERS    Y02080
         LR    REGEIGHT,REGTHIRT
         DROP  REGTHIRT
         USING ALLOCWKA,REGEIGHT        WORK AREA ADDRESSABILITY Y02080
         MVC   CCW12+THREE(FOUR),ZERO(EXTPTR)  MOVE IN CCHH      Y02080
         L     RCVT,CVTPTR              LOAD CVT BASE
         L     RCVT,CVTPRLTV            LOAD TTR CONVERT RTN BASE
         BALR  RETRNREG,RCVT            CONVERT EXTENT
         LM    REGEIGHT,REGTHIRT,IECREGSV+K12  RESTORE REGISTERS Y02080
         DROP  REGEIGHT
         USING ALLOCWKA,RWKAREA         WORK AREA ADDRESSABILITY Y02080
         SRA   REGSEVEN,ONE             DECREMENT LOOP CONTROL
         SRL   REGZERO,SIXTEEN          POSITION IT FOR STORING
         LTR   REGSEVEN,REGSEVEN        HAS UPPER CCHH BEEN CONVERTED
         BNE   CONT                     BRANCH IF NO
         LA    REGSIX,ONE
         AR    REGZERO,REGSIX          ADD 1 TO UPPER CCHH TO MAKE IT
*                                       RTA+1                         *
CONT     STH   REGZERO,ZERO(DADSMPTR)   STORE RTA OR RTA+1
         LA    REGZERO,TWO
         AR    DADSMPTR,REGZERO         UPDATE PTR TO NEXT SPACE IN
*                                       DADSMTBL                      *
         LA    REGSIX,FOUR
         AR    EXTPTR,REGSIX            UPDATE PTR TO NEXT EXTENT TO
*                                       BE CONVERTED                  *
         LTR   REGSEVEN,REGSEVEN        HAS RTA+1 JUST BEEN STORED
         BNE   ROUTINE                  BRANCH IF NO
         STM   FORMATCT,EXTPTR,CCW11    SAVE PTR TO NEXT EXTENT  Y02080
*                                                                     *
* TEST DADSMCTR TO DETERMINE IF  5 EXTENTS HAVE BEEN CONVERTED AS YET *
* IF 5 EXTENTS HAVE BEEN CONVERTED,GO TO SUBTRACT ROUTINE. IF NOT     *
* CALCULATE ADDR OF NEXT EXTENT TO BE CONVERTED                       *
*                                                                     *
         BCT   DADSMCTR,CONVNEXT        BRANCH IF DADSMTBL NOT FULL
         BAL   RETRNREG,ORDER           GO TO ORDER AND SUB EXTENTS
RETURNPT EQU   *                        BRANCH LABEL             Y02080
         LM    FORMATCT,EXTPTR,CCW11    LOAD PTR TO NEXT EXTENT  Y02080
         LA    DADSMCTR,FIVE
         LA    DADSMPTR,DADSMTBL
         XC    DADSMTBL,DADSMTBL        ZERO DADSMTBL
CONVNEXT BCTR  FORMATCT,ZERO            DECREMENT CTR
         LTR   FORMATCT,FORMATCT        ARE THERE MORE EXTENTS IN F1
*                                       OR F3                         *
         BZ    TESTSW                   BRANCH IF NO
         CH    FORMATCT,EXTSIX          WAS PREVIOUS EXTENT NO.  A30794
*                                       6                        A30794
         BNE   TESTUSED                 BRANCH IF NO             A31333
         LA    EXTPTR,ONE(EXTPTR)
TESTUSED EQU   *                                                 A31333
         CLI   ZERO(EXTPTR),ZERO        IS NEW EXTENT UNUSED
         BE    TESTSW                   BRANCH IF YES
         B     CONVERT                  BRANCH TO CONVERT NEXT EXTENT
*                                                                     *
* PUT THE CONVERTED EXTENTS IN THE DADSMTBL INTO ASCENDING ORDER      *
*                                                                     *
ORDER    LA    REGNINE,FIVE
         SR    REGNINE,DADSMCTR         FIND NO. EXTENTS IN TABLE
         ST    REGNINE,PDLIST5          SAVE NUMBER OF EXTENTS   Y02080
NEXTPASS LA    REGTEN,DADSMTBL          GET ADDR. BEGINNING OF TABLE
COMPARE  EQU   *                                                 A29936
         LM    REGZERO,REGTWO,ZERO(REGTEN) PICK UP FIRST 3       A29936
*                                       EXTENTS                  A29936
         LTR   REGONE,REGONE            IS THERE ONLY ONE EXTENT A29936
         BZ    LASTXTNT                 BRANCH IF YES            A29936
         CR    REGZERO,REGONE           COMPARE TWO ADJACENT     A29936
*                                       EXTENTS                  A29936
         BL    UPDATPTR                 IF 1ST LOW, DO NOT       A29936
*                                       SWITCH                   A29936
         ST    REGONE,ZERO(,REGTEN)     SWITCH CONTENTS OF THE   A29936
         ST    REGZERO,FOUR(,REGTEN)    TWO ADJACENT EXTENTS     A29936
UPDATPTR EQU   *                                                 A29936
         AR    REGTEN,REGSIX            POINT TO NEXT EXTENT     A29936
LASTXTNT EQU   *                                                 A29936
         LTR   REGTWO,REGTWO            IS THIS THE LAST EXTENT  A29936
         BNZ   COMPARE                  BRANCH IF NO             A29936
         BCT   REGNINE,NEXTPASS         DECREMENT CTR OF NO OF PASSES
         L     REGTEN,PDLIST5           GET NBR EXTENTS IN TABLE Y02080
*                                                                     *
* THIS ROUTINE BUILDS THE FIRST FORMAT 5 DSCB. WHEN THE FIRST FORMAT  *
* 5 DSCB IS FULL, THE MERGE FLAG IS SET AND XCTL IS MADE TO THE       *
* SECOND LOAD TO SUBTRACT THE EXTENTS IN THE DADSMTBL.                *
*                                                                     *
         TM    ASWITCH,MERGESW          TEST IF MORE THAN ONE F5 Y02080
         BO    GOTOTRIL                 BRANCH IF YES
         LH    LSTF5CTR,LSTF5CT         GET NUMBER OF EXTENTS    Y02080
*                                       IN THE LAST F5 DSCB      Y02080
         LA    F5ADDR,DS5AVEXT          POINT TO BEGINNING F5
         LA    F5EXTCTR,ONE             CTR EXTS IN F5 BEING COMPARED
         LA    ALLOCADR,DADSMTBL        GET ADDR 1ST EXTENT TO BE SUBT
NEWCOMP  CLC   ZERO(TWO,ALLOCADR),ZERO(F5ADDR) COMPARE BEGIN EXTENTS
         BL    DSERROR                  BRANCH IF NEW EXTENT LOW
         BH    MORECK                   BRANCH IF NEW EXTENT HIGH
         CLC   TWO(TWO,ALLOCADR),TWO(F5ADDR) ARE ENDS EXTENTS EQUAL
         BH    DSERROR                  ERROR IF EXTENT TO BE    A31333
*                                       SUBTRACTED IS GREATER
*                                       THAN FREE EXTENT
         BL    REPLACE                  BRANCH IF LOWER          A31333
WIPEOUT  LA    REGONE,TWTYSIX
         SR    REGONE,F5EXTCTR          CALCULATE LOOP CONTROL
         BZ    ZERO26                   EQUAL TO LAST EXTENT    ZA00008
*                                       IN FORMAT 5             ZA00008
         LR    REGTHREE,F5ADDR
MOVEFLD  MVC   ZERO(FIVE,REGTHREE),FIVE(REGTHREE) WIPEOUT ALLOCATED EXT
         LA    REGTHREE,FIVE(REGTHREE)  GET ADDR NEXT E
         BCT   REGONE,MOVEFLD           DECREMENT LOOP CONTROL
ZERO26   EQU   *                                                ZA00008
         XC    F5EXT16(FIVE),F5EXT16    CLEAR 26TH EXTENT        Y02080
         BCTR  LSTF5CTR,ZERO            DECREMENT COUNTER        Y02080
CHNGPTR  LA    ALLOCADR,FOUR(ALLOCADR)  POINT TO NEXT EXTENT TO BE SUBT
         BCT   REGTEN,NEWCOMP           DECREMENT CTR EXTENT TO BE SUBT
         STH   LSTF5CTR,LSTF5CT         SAVE COUNTER OF EXTENTS  Y02080
*                                       IN LAST F5 BEING BUILT   Y02080
         ST    REGTEN,PDLIST5           ZERO OUT                 Y02080
         BR    RETRNREG                 RETURN TO OBTAIN VORE EXTENTS
REPLACE  MVC   ZERO(TWO,F5ADDR),TWO(ALLOCADR) UPDATE EXTENT BOUNDRIES
         B     CHNGPTR                  BRANCH TO POINT TO NEW F5 EXT
MORECK   CLC   ZERO(TWO,ALLOCADR),TWO(F5ADDR) IS EXT TO BE SUBT IN THIS
*                                       FREE EXTENT                   *
         BNL   POINTNXT                 BRANCH IF NO
         CLC   TWO(TWO,ALLOCADR),TWO(F5ADDR) COMPARE END EXTENT  A31333
*                                       TO BE SUBTRACTED TO END
*                                       OF FREE EXTENT
         BH    DSERROR                  ERROR IF HIGHER          A31333
         BL    TWOEXTS                  BRANCH IF LOWER          A31333
         MVC   TWO(TWO,F5ADDR),ZERO(ALLOCADR) UPDATE EXTENT BOUNDRY
         B     CHNGPTR                 BRANCH TO POINT TO NEW F5 EXT
POINTNXT LA    F5ADDR,FIVE(F5ADDR)      POINT TO NEXT FREE EXTENT
         LA    F5EXTCTR,ONE(F5EXTCTR)   UPDATE CTR EXTENT BEING
*                                       COMPARED                      *
         CLC   ZERO(FOUR,F5ADDR),ZEROS  IS NEW EXTENT ZERO       A53183
         BE    DSERROR                  ERROR IF EXTENT TO BE    A31333
*                                       SUBTRACTED IS NOT WITHIN
*                                       ANY FREE EXTENT
         B     NEWCOMP                   BRANCH TO START COMPARISON
TWOEXTS  EQU   *                        BRANCH LABEL             Y02080
         LA    LSTF5CTR,ONE(LSTF5CTR)   INCREMENT COUNT          Y02080
         LA    REGTWO,TWTYSIX
         CR    LSTF5CTR,REGTWO          IS LAST F5 FULL          Y02080
         BH    PREPPARM                 BRANCH IF YES
         LA    REGTWO,FIVE              LOAD INCREMENT REGISTER
         LA    REGTHREE,F5EXT15
MOVE     MVC   FIVE(FIVE,REGTHREE),ZERO(REGTHREE)  MOVE A FIELD
         CR    F5ADDR,REGTHREE          HAVE NECESSARY FIELDS BEEN
*                                       MOVED                         *
         BE    CONTIN                   BRANCH IF YES
         SR    REGTHREE,REGTWO          DECREMENT MOVE ADDR
         B     MOVE                     BRNACH TO MOVE NEXT FIELD
CONTIN   MVC   SEVEN(TWO,F5ADDR),TWO(F5ADDR) SET UP
         MVC   TWO(TWO,F5ADDR),ZERO(ALLOCADR)   TWO NEW
         MVC   FIVE(TWO,F5ADDR),TWO(ALLOCADR)        EXTENTS
         LA    F5EXTCTR,ONE(F5EXTCTR)
         LA    F5ADDR,FIVE(F5ADDR)      POINT TO NEXT FREE EXTENT
         B     CHNGPTR                  BRNACH TO POINT TO NEW F5 EXT
DSERROR  EQU   *                                                 A31333
         LA    RERRPASS,VTOCERR         SET ERROR CODE OF X'48'  A31333
         B     ERREXIT                  GO XCTL TO IGG0325H      A31333
*
* THIS SECTION SETS UP A PARAMETER LIST IN CCW12 FOR MODULE IGG0325R.
*
PREPPARM EQU   *                        BRANCH LABEL             Y02080
         ST    ALLOCADR,CCW12+K4        PUT ADDR NEXT EXTENT TO  Y02080
*                                       SUBTRACT IN PARM LIST    Y02080
         OI    ASWITCH,MERGESW          SET MULTIPLE F5 SWITCH   Y02080
         STH   LSTF5CTR,LSTF5CT         SAVE COUNTER OF EXTENTS  Y02080
*                                       IN LAST F5 DSCB          Y02080
         ST    REGTEN,CCW12             PUT NO. EXTENTS TO BE SUBTRACT
*                                       IN PARAMETER LIST             *
         BAL   RETRNREG,PREFXADR        GO GET PREFIX ADDRESS    Y02078
NEXTPTR  EQU   *                        PREFIX ADDRESS IN REG 10 Y02078
         CLC   IECCORPS,IECCORP2        TEST FOR ANOTHER WKAREA  Y02078
         BE    FIRSTWKA                 BRANCH IF NO EXTENSION   Y02078
         L     REGTEN,IECCORP2          LOAD ADDR OF NEXT PREFIX Y02078
         CLC   IECCORID,VCWKAID         TEST IF VTOC WORK AREA   Y02078
         BNE   NEXTPTR                  BRANCH IF NOT            Y02078
         L     RVTOCWKA,IECCORAD        LOAD ADDR OF EXTENSION   Y02078
INITWKA  EQU   *                        BRANCH LABEL             Y02078
         MVC   VTC1STF5,DSCBF5          MOVE IN FIRST REBUILT F5 Y02078
         ST    RVTOCWKA,LASTF5AD        INITIALIZE LAST F5 ADDR  Y02078
         B     BELOW                    BELOW
FIRSTWKA EQU   *                        BRANCH LABEL             Y02078
         BAL   RETRNREG,GETWKA          GO GET WKAREA EXTENSION  Y02078
         MVC   VTCCHHR1,FMTOUTAD        MOVE IN CCHHR OF 1ST F5  Y02078
         B     INITWKA                  MOVE IN 1ST REBUILT F5   Y02078
GOTOTRIL LA    ALLOCADR,DADSMTBL        PUT ADDR NEXT EXTENT TO BE
*                                       SUBTRACTED IN PARAMETER LIST  *
         ST    ALLOCADR,CCW12+K4                                 Y02080
         ST    REGTEN,CCW12             PUT NO. EXTENTS TO BE SUBT.
*                                       IN PARAMETER LIST             *
BELOW    EQU   *                                                 A31333
         MVI   CCW12,ZERO               SET BYTE TO SHOW ENTRY   A31333
*                                       FROM IGG0325P
         LA    REGTWO,NEXTLOAD          POINT TO ID OF IGG0325R  Y02080
         B     XCTLHERE                 BRNACH TO XCTL
*
* THIS ROUTINE CALCULATES THE ADDRESS OF THE INITIAL PREFIX AND
* RETURNS THIS ADDRESS IN REGISTER 10.
*
PREFXADR EQU   *                        BRANCH LABEL             Y02078
         LA    REGTEN,ZERO(RWKAREA)     INITIAL WORK AREA ADDR   Y02078
         LA    REGONE,IECEXTPR-IECPREFX  LENGTH OF PREFIX        Y02078
         SR    REGTEN,REGONE            CALCULATE PREFIX ADDRESS Y02078
         BR    RETRNREG                 RETURN                   Y02078
*
* THIS ROUTINE OBTAINS A VTOC CONVERSION WORK AREA EXTENSION AND
* RETURNS ITS ADDRESS IN REGISTER RVTOCWKA.
*
GETWKA   EQU   *                        BRANCH LABEL             Y02078
         IECRES GET,EXTPR=(RWKAREA),LV=VWALNGTH,ID=VCWA,         Y02078X
               STM=(REGTWO,RETRNREG,IECREGSV+K12)                Y02078
         LR    RVTOCWKA,REGONE          LOAD WKA EXTENSION ADDR  Y02078
         BR    RETRNREG                 RETURN                   Y02078
*
* THIS ROUTINE CALCULATES THE ADDRESS OF THE FIRST VTOC CONVERSION
* WORK AREA EXTENSION AND MOVES THE FIRST REBUILT FORMAT 5 INTO IT.
*
MOVEF5IN EQU   *                        BRANCH LABEL             Y02078
         BAL   RETRNREG,PREFXADR        GO GET PREFIX ADDRESS    Y02078
NXTPREFX EQU   *                        PREFIX ADDRESS IN REG 10 Y02078
         L     REGTEN,IECCORP2          LOAD ADDR OF NEXT PREFIX Y02078
         CLC   IECCORID,VCWKAID         TEST IF VTOC WORK AREA   Y02078
         BNE   NXTPREFX                 BRANCH IF NOT            Y02078
         L     RVTOCWKA,IECCORAD        LOAD ADDR OF EXTENSION   Y02078
         MVC   VTC1STF5,DSCBF5          MOVE IN FIRST REBUILT F5 Y02078
         ST    RVTOCWKA,LASTF5AD        INITIALIZE LAST F5 ADDR  Y02078
         BR    REGTWO                   RETURN                   Y02078
*                                                                     *
* END OF VOLUME ROUTINE                                               *
*                                                                     *
FINISHF5 LA    REGNINE,FIVE
         SR    REGNINE,DADSMCTR
         LTR   REGNINE,REGNINE          ARE EXTENTS TO BE SUBTRACTED
         BE    TESTSPL                 BRANCH IF NO
         BAL   RETRNREG,ORDER           BRNACH TO ORDER EXTENTS
*
* THIS SECTION DETERMINES THE NEXT MODULE TO RECEIVE CONTROL.
*
TESTSPL  EQU   *                        BRANCH LABEL             Y02080
         TM    ASWITCH,MERGESW          ARE NEW F5'S ON VOLUME   Y02080
         BO    SPTEST                   BRANCH IF YES
         TM    ASWITCH,SPCYLSW          TEST FOR SPLIT CYL D/S   Y02078
         BNO   ONEF5TST                 BRANCH IF NONE FOUND     Y02078
         LA    REGTWO,LOAD4             PREPARE RETURN ADDRESS   Y02078
         CLC   F5FNDCT,ONEF5            TEST IF ONLY 1 FOUND F5  Y02078
         BNE   MOVEF5IN                 BRANCH IF MORE THAN 1    Y02078
         BAL   RETRNREG,GETWKA          GO GET WKAREA EXTENSION  Y02078
         MVC   VTCCHHR1,FMTOUTAD        MOVE IN CCHHR OF 1ST F5  Y02078
         MVC   VTC1STF5,DSCBF5          MOVE IN 1ST REBUILT F5   Y02078
         ST    RVTOCWKA,LASTF5AD        INITIALIZE LAST F5 ADDR  Y02078
         B     LOAD4                    GO BRANCH TO IGG0325Q    Y02078
ONEF5TST EQU   *                        BRANCH LABEL             Y02078
         CLC   F5FNDCT,ONEF5            TEST IF ONLY 1 FOUND F5  Y02078
         BE    LOAD5                    IF ONLY 1 FOUND F5 AND 1 Y02078
*                                       REBUILT F5, DO NOT USE   Y02078
*                                       A WORK AREA EXTENSION    Y02078
         LA    REGTWO,LOAD5             PREPARE RETURN ADDRESS   Y02078
         B     MOVEF5IN                 MOVE IN 1ST REBUILT F5   Y02078
SPTEST   EQU   *                                                 A31333
         TM    ASWITCH,SPCYLSW          DO SPLIT CYL DATA SETS   Y02080
*                                       EXIST ON THIS VOLUME          *
         BO    LOAD4                    BRANCH IF YES
LOAD5    EQU   *                                                 A31333
         XR    RERRPASS,RERRPASS        CLEAR ERROR PASS REG     S21040
ERREXIT  EQU   *                        BRANCH LABEL             Y02080
         LA    REGTWO,LASTLOAD          POINT TO ID OF IGG0325T  Y02080
XCTLHERE EQU   *                        BRANCH LABEL             Y02080
        IECRES LOAD,EXTPR=(RWKAREA),MODID=(REGTWO),BRANCH=DIRECT Y02080
LOAD4    EQU   *                                                 A31333
         LA    REGONE,C32              MAKE REG 1 NOT ZERO SO    S21040
*                                      IGG0325Q WILL NOT THINK
*                                      THIS IS REENTRY FROM 325U
         LA    REGTWO,SPLITCYL          POINT TO ID OF IGG0325Q  Y02080
         B     XCTLHERE                 BRANCH TO XCTL
*
* THIS ROUTINE EXECUTES A CHANNEL PROGRAM.
*
EXCHANPG MVI   ECB,ZERO
         EXCP IOB
         WAIT   1,ECB=ECB
         TM    ECB,GOODIO
         BCR   1,RETRNREG               RETURN IF I/O SUCESSFUL  M0124
         LA    RERRPASS,ERRCD
         B     ERREXIT                  BRANCH TO IGG0325T       Y02080
*                                                                     *
*          CONSTANTS                                                  *
*                                                                     *
FW3      DC    F'3'                     3 F5'S PER WKA EXTENSION Y02078
ONEF5    DC    H'1'                     HALFWORD OF ONE          Y02078
VCWKAID  DC    C'VCWA'                  VTOC WORK AREA PREFIX ID Y02078
         DS    0H                       HALF-WORD BOUNDARY       A30794
EXTSIX   DC    X'0009'                  EXTENT SIX               A30794
*                                                                     *
* TABLE OF MODULE IDS AND ENTRY POINT ADDRESSES                       *
*                                                                     *
         XCTLTABL ID=(NEXTLOAD,5R,SPLITCYL,5Q,LASTLOAD,5T),      Y02080X
               SVC=032,LENGTH=,BRT=YES                           Y02080
         EJECT                                                   Y02080
CVT      DSECT
         CVT                                                     Y02080
         EJECT                                                   Y02080
DSCBWKAR IECALLWA EP,F4,D1=(1,3),D2=(5)  ALLOCATE WORK AREA      Y02080
DS4VTOC6 EQU   DS4VTOCE+6
F5EXT15  EQU   DSCBF5+124               EXTENT 15 IN F5 DSCB
F5EXT16  EQU   DSCBF5+129               EXTENT 16 IN F5 DSCB
CHNPGADR EQU   IOB+16                   ADDR. CHANNEL PROGRAM IN IOB
SEEKCYL  EQU   SEEK+3
SEEKREC  EQU   SEEK+7
ZEROS    EQU   CCW2+4                   FULL WORD OF ZERO
         EJECT                                                   Y02078
VWKAREA  IECVTCWA                       VTOC CONVERSION WORKAREA Y02078
         END
