         TITLE 'IGG020D0 - PARTIAL RELEASE - WRITE BACK FORMAT 1 DSCB'
IGG020D0 CSECT
*
*        CHANGES/DELETIONS FOR RELEASE 21.7
*0000037000-037500,064000-065500,065800-065900,066500-066600,    SM5456
*0000067500,068800-070000,077500-078000,088500-095000,095300-    SM5456
*0000095500,097600-097700,099000-102020,122520,125000-130000,    SM5456
*0000158100-158400,168500-171000                                 SM5456
*        VS2 RELEASE 02 DELETIONS/CHANGES
*0000016000,019000,024000,024440,029200,035000,036000,041000,    Y02080
*0000070600-070700,072500,081000-085000,086000,139000-139500,    Y02080
*0000141000-143500,175500-176000,176540,176600,182000-200000     Y02080
*0000000500,103200                                               Y02078
*0000                                                            Y02082
*
***********************************************************************
*                                                                     *
* MODULE NAME = IGG020D0                                              *
*                                                                     *
* DESCRIPTIVE NAME = PARTIAL RELEASE - WRITE BACK FORMAT 1 DSCB       *
*                                                                     *
* COPYRIGHT = NONE                                                    *
*                                                                     *
* STATUS = CHANGE LEVEL 000                                           *
*                                                                     *
* FUNCTION = THIS MODULE WRITES BACK THE UPDATED FORMAT 1 DSCB.       *
*            IT READS THE FIRST FORMAT 5 DSCB INTO THE RELEASE        *
*            WORK AREA AND SORTS THE DADSM EXTENT TABLE INTO          *
*            ASCENDING ORDER. IT ALSO INITIALIZES THE CHANNEL         *
*            PROGRAM FOR THE NEXT LOAD OF PARTIAL RELEASE.            *
*            IF THE DOS BIT OR DIRF BIT IS SET IN THE FORMAT 4 DSCB,  *
*            RELEASED SPACE IS NOT RETURNED TO THE FORMAT 5 DSCB(S).  *
*                                                                     *
* NOTES = THIS MODULE WAS CREATED IN OS RELEASE 21.7 AS A SPLIT       *
*         OF MODULE IGG020P2 DUE TO FIX FOR OS APAR 54549.            *
*                                                                     *
*    DEPENDENCIES = NONE                                              *
*                                                                     *
*    RESTRICTIONS = NONE                                              *
*                                                                     *
*    REGISTER-CONVENTIONS = REGISTER 9 POINTS TO THE DCB.             *
*                           REGISTER 11 POINTS TO PARTIAL RELEASE     *
*                           WORK AREA.                                *
*                           REGISTER 13 POINTS TO I/O SUPPORT         *
*                           WORK AREA.                                *
*                           REFER TO 'REGISTER USAGE' IN THE MAIN     *
*                           CODE FOR THE LIST OF SYMBOLS USED TO      *
*                           REPRESENT THE REGISTERS IN THIS MODULE.   *
*                                                                     *
*    PATCH AREA = IHB0006D                                            *
*                                                                     *
* MODULE TYPE = CONTROL                                               *
*                                                                     *
*    PROCESSOR = ASSEMBLER                                            *
*                                                                     *
*    MODULE SIZE = LESS THAN 1024 DECIMAL BYTES                       *
*                                                                     *
*    ATTRIBUTES = REENTRANT, TRANSIENT                                *
*                                                                     *
* ENTRY POINT = IGG020D0                                              *
*                                                                     *
*    PURPOSE = SEE FUNCTION                                           *
*                                                                     *
*    LINKAGE = ENTRY FROM 1ST LOAD OF PARTIAL RELEASE -               *
*              MODULE IGG020P1 - AND 2ND LOAD OF PARTIAL RELEASE -    *
*              MODULE IGG020P2 - VIA A BRANCH.                        *
*                                                                     *
*    INPUT = IF AN ERROR CONDITION OTHER THAN AN I/O                  *
*            WAS DETECTED IN EITHER IGG020P1 OR IGG020P2, CONTROL     *
*            IS PASSED DIRECTLY TO IGG020D0 TO WRITE BACK             *
*            THE FORMAT 1 DSCB AND IMMEDIATELY BRANCH TO              *
*            THE LAST LOAD OF PARTIAL RELEASE.  REGISTER 2            *
*            WILL CONTAIN ONE OF THE FOLLOWING TO INDICATE THIS:      *
*            ALL ZEROS = NO SPACE RELEASED - NO ERROR CODE RETURNED   *
*            X'02' = UNABLE TO FIND EXTENT IN FORMAT 1 -              *
*                    RETURN ERROR CODE                                *
*            X'04' = UNABLE TO FIND EXTENT IN FORMAT 3 -              *
*                    RETURN ERROR CODE                                *
*            X'08' = ANOTHER DCB IS OPEN TO THE DATA SET -            *
*                    RETURN ERROR CODE                                *
*            X'20' = NO ROOM IN THE VTOC - RETURN ERROR CODE          *
*            IF REGISTER 2 CONTAINS A NEGATIVE VALUE ON ENTRY, IT     *
*            INDICATES NO ERROR HAS OCCURRED, SO CONTINUE PROCESSING. *
*            REGISTER 11 POINTS TO THE RELEASE WORK AREA WHICH        *
*            CONTAINS A POINTER TO THE DCB, THE DATA PORTION OF       *
*            THE FORMAT 4 DSCB, THE ADDRESSES OF THE FORMAT 4 AND     *
*            THE FORMAT 5 DSCB'S, AND THE DADSM EXTENT TABLE,         *
*            WHOSE ENTRIES REPRESENT ALL THE SPACE BEING RELEASED     *
*            IN THE FORMAT 1 DSCB AND POSSIBLY THE FORMAT 3 DSCB.     *
*            REGISTER 13 POINTS TO THE I/O SUPPORT WORK AREA          *
*            WHICH CONTAINS THE UPDATED FORMAT 1 DSCB.                *
*                                                                     *
*    OUTPUT = THE UPDATED FORMAT 1 DSCB IS WRITTEN TO THE VTOC.       *
*             REGISTER 9 INDICATES NUMBER OF EXTENTS TO BE            *
*             RELEASED WHEN MODULE BRANCHES TO IGG020D1.              *
*             REGISTER 11 POINTS TO THE RELEASE WORK AREA WHICH       *
*             CONTAINS THE FORMAT 4 DSCB, THE FIRST FORMAT 5          *
*             DSCB AND THE DADSM EXTENT TABLE WHOSE ENTRIES HAVE      *
*             BEEN SORTED INTO ASCENDING ORDER. ITS ENTRIES           *
*             REPRESENT ALL THE SPACE BEING RELEASED FROM THE         *
*             FORMAT 1 DSCB AND POSSIBLY THE FORMAT 3 DSCB.           *
*             REGISTER 13 POINTS TO THE I/O SUPPORT WORK AREA         *
*             WHICH CONTAINS THE UPDATED FORMAT 1 DSCB AND THE        *
*             CHANNEL PROGRAM SET UP FOR THE NEXT LOAD OF             *
*             PARTIAL RELEASE.                                        *
*                                                                     *
*    EXIT-NORMAL = BRANCH TO IGG020D1                                 *
*                                                                     *
*    EXIT-ERROR = BRANCH TO IGG020P3 WITH ONE OF THE FOLLOWING        *
*                 ERROR CODES IN REGISTER 2:                          *
*                 X'10' - PERMANENT I/O ERROR                         *
*                 OR ONE OF THE ERROR CODES NOTED UNDER 'INPUT'.      *
*                                                                     *
* EXTERNAL REFERENCES = NONE                                          *
*                                                                     *
*    ROUTINES = EXCP (SVC 0) READ/WRITE DISK                          *
*               WAIT (SVC 1) WAIT FOR COMPLETION OF I/O               *
*               IECRES       TRANSFER CONTROL TO NEXT LOAD            *
*                                                                     *
*    DATA AREAS = NONE                                                *
*                                                                     *
*    CONTROL BLOCKS = JFCB, DEB, DCB                                  *
*                                                                     *
* TABLES = SEE DESCRIPTION OF THE PARTIAL RELEASE WORK AREA AND       *
*          I/O SUPPORT WORK AREAS AT THE END OF THE MODULE.           *
*                                                                     *
* MACROS = IECDSECT - TO DEFINE THE I/O SUPPORT WORK AREA.            *
*          (THIS MACRO INCLUDES IEFJFCBN TO DEFINE THE JFCB).         *
*          IECSDSL1 - TO DEFINE DSCB'S.                               *
*          IECPRLWA - TO DEFINE THE PARTIAL RELEASE WORK AREA.        *
*                                                                     *
* CHANGE ACTIVITY =                                                   *
*        SEE 'CHANGES/DELETIONS' SECTION JUST AFTER CSECT CARD.       *
*                                                                     *
*                                                                     *
***********************************************************************
*
*** REGISTER USAGE
*
R0       EQU   0                        WORK REGISTER
INDEX1   EQU   1                        CONTAINS ADDRESS OF ENTRY
*                                       IN DADSM TABLE
R1       EQU   1                        WORK REGISTER
R2       EQU   2                        WORK REGISTER
INDEX2   EQU   2                        CONTAINS ADDRESS OF ENTRY
*                                       IN DADSM TABLE
ERRETREG EQU   2                        ERROR RETURN CODE REGISTER
INDEX3   EQU   3                        CONTAINS NUMBER OF PASSES
*                                       THRU DADSM TABLE
R3       EQU   3                        WORK REGISTER
R4       EQU   4                        WORK REGISTER
EXTCTR   EQU   5                        CONTAINS NUMBER OF
*                                       EXTENTS IN DADSM TABLE
R5       EQU   5                        WORK REGISTER
R6       EQU   6                        WORK REGISTER
R7       EQU   7                        WORK REGISTER
R8       EQU   8                        WORK REGISTER
RDEB     EQU   8                        POINTS TO DEB PREFIX    SA54549
R9       EQU   9                        WORK REGISTER
RXUSRDCB EQU   9                        BASE FOR USERS DCB
RXUCB    EQU   10                       POINTER TO THE UCB
RXWKA    EQU   11                       BASE FOR WORK AREA
RXBASE   EQU   12                       BASE FOR MAIN PROGRAM
RXCLOWKA EQU   13                       BASE FOR CLOSE'S WORK AREA
RLINK    EQU   14                       SUBROUTINE LINK REGISTER
R15      EQU   15                       WORK REGISTER
*
*** OTHER EQUATES
*
IOERROR  EQU   X'10'                    PERMANENT I/O ERROR      A37199
*
SKIP     EQU   X'10'                    USED TO TURN SKIP ON    SA54549
READATA  EQU   X'06'                    READ DATA COMMAND       SA54549
SUPXFER  EQU   X'10'                    SUPPRESS TRANSFER       SA54549
DOS      EQU   X'80'                    TEST FOR DOS PACK       SA54549
DIRFBIT  EQU   X'04'                    DIRF BIT IN DS4VTOCI     Y02078
*
K0       EQU   0                        CONSTANT OF ZERO         Y02082
K1       EQU   1                        1 BYTE                  SA54549
K3       EQU   3                        3 BYTES                 SA54549
K4       EQU   4                        4 BYTES                 SA54549
K5       EQU   5                        5 BYTES                 SA54549
DATAL    EQU   96                       DATA LENGTH             SA54549
*
         BALR  RXBASE,0                 LOAD BASE REGISTER
         USING *,RXBASE                 BASE FOR MODULE         SA54549
         USING PRLSEWKA,RXWKA           WORK AREA ADDRESSABILITY Y02080
         USING IHADCB,RXUSRDCB          ESTABLISH BASE FOR USERS DCB
         USING CLOSEWKA,RXCLOWKA        ESTABLISH BASE FOR
*                                       CLOSE WORK AREA
*
*** THIS SECTION BUILDS THE CHANNEL PROGRAM TO WRITE BACK THE
*   FORMAT 1 DSCB.
*   THE FORMAT OF THE CHANNEL PROGRAM IS AS FOLLOWS:
*   SEARCH ID EQ
*   TIC *-8
*   WRITE DATA (F1 DSCB) (NO CHAIN / CHAIN, 96 BYTES)
*   SEARCH ID EQ
*   TIC *-8
*   READ DATA (F1 DSCB) (SUPPRESS TRANSFER, 96 BYTES)
*
BEGINA   EQU   *
         LR    R4,ERRETREG              SAVE ERROR CODE         SA54549
         L     R9,DCBSAVE               RELOAD USER'S DCB PNTR  SA54549
         L     RDEB,DCBDEBAD            GET DEB ADDRESS         SA54549
         LA    RDEB,K0(RDEB)            CLEAR HIGH-ORDER BYTE    Y02082
         SH    RDEB,SIXTEEN             GET DEB PREFIX ADDR     SA54549
         MVC   DXDAADDR+K3(K5),K3(RDEB) MOVE IN FORMAT 1 CCHHR  SA54549
         LA    R0,DXCCW1                MODIFY CCW PTR IN IOB   SA54549
         ST    R0,IOBSIOCC                                      SA54549
         LM    R0,R1,WRITECCW           PICK UP CCW ADDRESS      SM5456
         ALR   R0,RXCLOWKA              ADD DISPLACEMENT FOR     SM5456
*                                       I/O WORK AREA            SM5456
         STM   R0,R1,DXCCW3             STORE IN CCW             SM5456
         LA    R0,OUTDSCB               PICK UP ADDRESS OF      SA54549
*                                       WHERE TO READ IN DSCB   SA54549
         ST    R0,DXCCW6                STORE IN CCW             SM5456
         MVI   DXCCW6,READATA           MOVE IN READ DATA        SM5456
*                                       COMMAND                 SA54549
         LA    R0,DATAL                 GET DATA LENGTH         SA54549
         ST    R0,DXCCW6+K4             MOVE IN LENGTH           SM5456
         MVI   DXCCW6+K4,SUPXFER        MOVE IN SUPPRESS         SM5456
*                                       TRANSFER COMMAND        SA54549
         BAL   RLINK,EXECIO             WRITE BACK FORMAT 1     SA54549
*                                       DSCB                    SA54549
         LTR   R4,R4                    DID A PREVIOUS MODULE   SA54549
*                                       RETURN AN ERROR CODE    SA54549
         BNM   PRELEAVE                 BRANCH IF YES           SA54549
*
*** THIS SECTION CHECKS IF THE DOS BIT OR DIRF BIT IS SET AND IF THERE
*   IS ANY SPACE TO BE RELEASED
*
DOSCHECK EQU   *                                                SA54549
         SR    ERRETREG,ERRETREG        CLEAR ERROR CODE        SA54549
         TM    DS4VTOCI,DIRFBIT         TEST IF THE DIRF BIT WAS Y02078
*                                       PREVIOUSLY SET           Y02078
         BNO   LEAVE                    BRANCH IF PREVIOUSLY SET Y02078
         TM    DS4VTOCI,DOS             IS THIS A DOS PACK      SA54549
         BO    LEAVE                    BRANCH IF YES           SA54549
         SR    EXTCTR,EXTCTR            CLEAR EXTENT COUNTER    SA54549
         IC    EXTCTR,EXTNUM            GET NUMBER OF           SA54549
*                                       DADSMTBL ENTRIES        SA54549
         LTR   R9,EXTCTR                ANY ENTRIES IN DADSMTBL SA54549
*                                       (REGISTER 9 IS PASSED   SA54549
*                                       TO NEXT LOAD OF PARTIAL SA54549
*                                       RELEASE WITH NUMBER OF  SA54549
*                                       ENTRIES IN DADSM TABLE) SA54549
         BZ    LEAVE                    BRANCH IF NO            SA54549
*
*** THIS SECTION SORTS THE DADSM TABLE INTO ASCENDING RTA'S
*
NEWPASS  LA    INDEX1,ENTRIES           GET ADDRESS 1ST ENTRY
         LA    INDEX2,4(INDEX1)         GET ADDRESS 2ND ENTRY
         LA    INDEX3,K1                LOAD PASS COUNTER       SA54549
         BCT   EXTCTR,NEWPAIR           BRANCH IF MORE EXTENTS
         B     RELOCATE                 CHECK IF IT IS DOS PACK SA54549
NEWPAIR  CLC   0(2,INDEX1),0(INDEX2)    COMPARE 2 DADSM ENTRIES
         BH    EXCHANGE                 EXCHANGE IF 1ST IS HIGH
SHIFT    LR    INDEX1,INDEX2            IF LOW -POINT INDEX1 TO
*                                       HIGHER OF 2 ENTRIES
         LA    INDEX2,4(INDEX2)         POINT INDEX2 TO NEXT ENTRY
         CLR   INDEX3,EXTCTR            IS THIS THE END OF A PASS
         BE    NEWPASS                  YES- HIGHEST ENTRY IS THE
*                                       TOP OF THE TABLE-GET NEXT
*                                       ENTRY AND REPEAT PROCESS
         LA    INDEX3,1(INDEX3)         NO-INCREMENT PASS COUNTER
         B     NEWPAIR                  COMPARE HIGH TO NEXT ENTRY
EXCHANGE MVC   HOLDEXTS(4),0(INDEX1)    SAVE HIGH ENTRY
         MVC   0(4,INDEX1),0(INDEX2)    EXCHANGE HIGH AND LOW ENTRIES
         MVC   0(4,INDEX2),HOLDEXTS
         B     SHIFT                    POINT INDEX1 TO HIGH ENTRY
*
*** THIS SECTION RELOCATES THE CHANNEL PROGRAM FOR THE NEXT LOAD
*
RELOCATE EQU   *
         LM    R0,R7,CCWX1              PICK UP FIRST 4 CCWS    SA54549
         ALR   R2,RXWKA                 MODIFY READ COUNT ADDR   A39292
         ALR   R4,RXCLOWKA
         ALR   R6,RXCLOWKA
         STM   R0,R7,DXCCW1             RELOCATE FIRST 4 CCWS   SA54549
         LM    R0,R7,CCWX5              PICK UP NEXT 4 CCWS      SM5456
         ALR   R2,RXWKA                 RESOLVE CCW ADDRESSES    SM5456
         ALR   R4,RXCLOWKA                                       SM5456
         ALR   R6,RXWKA                 WRITE FROM OUTDSCB       SM5456
         STM   R0,R7,DXCCW5             RELOCATE NEXT 4 CCWS     SM5456
         LR    R0,R2                    PICK UP 9TH CCW          SM5456
         LR    R1,R3                                             SM5456
         LM    R2,R5,CCWX10             PICK UP LAST 2 CCWS      SM5456
         ALR   R2,RXCLOWKA              RESOLVE CCW ADDRESSES    SM5456
         ALR   R4,RXWKA                                          SM5456
         STM   R0,R5,DXCCW9             RELOCATE LAST 3 CCWS     SM5456
*
*** THIS SECTION READS THE FORMAT 5 DSCB INTO WORKING STORAGE
*
         MVC   OUTCCHHR(K5),DADSMADR                            SA54549
         MVC   DXDAADDR+K3(K5),DADSMADR SET SEEK ADDRRESS       SA54549
*                                       TO READ FORMAT 5        SA54549
         LA    R0,DXCCW9                MODIFY CCW POINTER      SA54549
*                                       IN IOB                  SA54549
         ST    R0,IOBSIOCC
         BAL   RLINK,EXECIO             READ IN FORMAT 5        SA54549
         OI    DXCCW11+K4,SKIP          TURN ON SKIP BIT        SA54549
*
*** THIS SECTION PREPARES FOR EXIT TO NEXT LOAD OF PARTIAL RELEASE
*
RELSPACE EQU   *
         LA    R3,NEXTXCTL              FETCH NEXT LOAD PNTR     O19117
         B     XCTL                                              O19117
*
*** THIS SECTION PREPARES FOR EXIT TO LAST LOAD OF PARTIAL RELEASE
*
PRELEAVE EQU   *                                                SA54549
         LR    ERRETREG,R4              RELOAD ERROR CODE REG   SA54549
LEAVE    EQU   *                                                 O19117
         LA    R3,PRECLOSE              POINT TO LAST LOAD ID    O19117
*
*** THIS SECTION BRANCHES TO ANOTHER LOAD OF PARTIAL RELEASE.
*
XCTL     EQU   *                                                 O19117
         IECRES LOAD,EXTPR=(RXWKA),MODID=(R3),BRANCH=DIRECT      Y02080
*
*** THIS SECTION INITIATES THE CHANNEL PROGRAM.
*
EXECIO   MVI   DXECB,0                  TURN OFF ECB FLAGS
         EXCP  DXIOB                    READ DSCB
         WAIT  ECB=DXECB                WAIT FOR COMPLETION
         TM    DXECB,X'20'              TEST FOR PERM I/O ERR
         BCR   5,RLINK                  RETURN IF NO I/O ERR.
         LA    ERRETREG,IOERROR         LOAD ERROR CODE          A37199
         B     LEAVE                    GO XCTL TO THE LAST LOAD A37199
*
*** CHANNEL PROGRAM
*
CHANPRG  DS    0F
CCWX1    DC    X'1A'                    READ HOME ADDRESS.
         DC    AL3(0)                   AREA ADDRESS
         DC    X'5000'                  CCW FLAGS
         DC    H'5'                     COUNT
CCWX2    DC    X'92'                    READ COUNT
         DC    AL3(DS3PTRDS-FIRST)                               A39292
         DC    X'6000'                  CCW FLAGS
         DC    H'5'                     BYTE COUNT
CCWX3    DC    X'29'                    SEARCH KEY EQUAL
         DC    AL3(4+DXCCW4-CLOSEWKA)
         DC    X'6000'                  CCW FLAGS
         DC    H'4'                     BYTE COUNT
CCWX4    DC    X'08'                    TIC TO CCW2
         DC    AL3(DXCCW2-CLOSEWKA)
         DC    F'0'
CCWX5    DC    X'03'                    NOP                      SM5456
         DC    XL3'00'                                           SM5456
         DC    X'2000'                  SLI FLAG                 SM5456
         DC    H'1'                     BYTE COUNT               SM5456
CCWX6    DC    X'31'                    SEARCH ID EQUAL
         DC    AL3(OUTCCHHR-FIRST)
         DC    X'4000'                  CCW FLAGS
         DC    H'5'                     BYTE COUNT
CCWX7    DC    X'08'                    TIC TO CCW6
         DC    AL3(DXCCW6-CLOSEWKA)
         DC    F'0'
CCWX8    DC    X'0D'                    WRITE KEY AND DATA
         DC    AL3(OUTDSCB-FIRST)                                A39292
         DC    X'4000'                  CCW FLAGS
         DC    H'140'                   BYTE COUNT
*CCWX9                                  SAME AS CCW6
CCWX10   DC    X'08'                    TIC TO CCW8
         DC    AL3(DXCCW9-CLOSEWKA)
         DC    F'0'
CCWX11   DC    X'0E'                    READ KEY AND DATA-MAY BE
         DC    AL3(INDSCB-FIRST)        USED AS A WRITE CHECK
         DC    X'0000'                  CCW FLAGS               SA54549
         DC    H'140'                   BYTE COUNT
WRITECCW EQU   *                        BEGINING OF WRITE CCW   SA54549
         DC    X'05'                    WRITE DATA              SA54549
         DC    AL3(DXDSCB-CLOSEWKA)     ADDRESS FORMAT 1 DSCB   SA54549
         DC    X'4000'                  CHAIN                   SA54549
         DC    H'96'                    BYTE COUNT              SA54549
*
*** CONSTANTS
*
TEN      DC    H'10'                                             A37199
SIXTEEN  DC    H'16'                    OFFSET INTO DEB PREFIX  SA54549
*
*** TABLE OF MODULE IDS AND ENTRY POINT ADDRESSES
*
         XCTLTABL ID=(NEXTXCTL,D1,PRECLOSE,P3),SVC=020,          Y02080X
               LENGTH=,BRT=YES                                   Y02080
         EJECT
*
*** RELEASE WORK AREA
*
WORKAREA IECPRLWA EP,F4,D2=(3)          RELEASE WORK AREA        Y02080
         EJECT                                                   Y02080
CVT      DSECT                          CVT DSECT                Y02080
         CVT                                                     Y02080
         EJECT
*
*** I/O SUPPORT WORK AREA
*
CLOSEWKA DSECT
         IECDSECT
         EJECT
         DCBD  DSORG=(PS)
*
         END
