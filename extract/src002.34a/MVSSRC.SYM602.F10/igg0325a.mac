     TITLE 'IGG0325A - DADSM - HOUSEKEEPING && DUPLICATE NAME SEARCH'
IGG0325A CSECT
*
*MODULE NAME = IGG0325A
*
*DESCRIPTIVE NAME = DADSM - HOUSEKEEPING & DUPLICATE NAME SEARCH
*
*COPYRIGHT = NONE
*
*CHANGE ACTIVITY = SEE BELOW
*
*          VS2 RELEASE 01 DELETIONS
*0000                                                            YM2930
*          VS2 RELEASE 02 DELETIONS/CHANGES
*0000000180,000220,000570,009200-009400,012800-013200,013600-    Y02080
*0000013900,016320-016400,016800,025000,032000,033600-034000,    Y02080
*0000035300,036200,040200,041880,041912,041920,049400-051200,    Y02080
*0000052030-052090,052120-052400,053800,061080,061320,064442-    Y02080
*0000064450,064463-064481,064571-064580,065000,067800,072000-    Y02080
*0000073000,073400-076720,082300,082780-083200,083300-084200,    Y02080
*0000089800-090200,090600-091600,092200-096000,099000,102000-    Y02080
*0000103400,104200,104400,107600,109810-134600                   Y02080
*0000010600-010800,049400-051200                                 Y02132
*0000003300,032600-032800,064481,064487,064553-064562,064750     Y02078
*0000000010-000050,061100,061240-061304                          Y02082
*0000                                                            Y02144
*0000                                                            Y02072
*          RELEASE 20 DELETIONS
*                                                                S20201
*          RELEASE 21 DELETIONS
*1192                                                            A37199
*1192033800,038600,039000-039100,040200,065600                   A40967
*1192006500-006600,006700-006800,007200-007300,011000-011200,    A42477
*1192033200-033400,034000,074600-074800,084200,113600            A42477
*1192                                                            M0123
*0000000080,007100,011400,016100,034300,064427-064436,064448-    A46776
*0000064451,065420-065440,065580,065660,065700-065720,072200,    A46776
*0000073000-073350,077400-077700,078200,082400-082600,082720-    A46776
*0000082760,083400,085200,085600,087000-089200,103700,104300-    A46776
*0000105700,109400-109800                                        A46776
*1192003000-005600,006100-006300,007000-007100,013600-013800,    S21016
*1192014200,016200-016400,024600,025200-031800,033800,063400-    S21016
*1192064400,064445-064454,064600-064800,082400,104850-104950     S21016
*          RELEASE 21.7 DELETIONS
*0000099200                                                     SA48172
*0000006900,047060,052010-052020,063200-064000,064409-064437,   SA56426
*0000064457,064472,064508-064517,064582-064589                  SA56426
*0000006160-006320,007500-007600,083400-084000,102000-103000    SA53147
*0000065760-065800                                              SA53153
*          RELEASE 22 DELETIONS
*
*STATUS CHANGE LEVEL 002
*FUNCTION  THIS MODULE LINKS TO THE RESIDENT DIRECT ACCESS            *
*   CONVERSION ROUTINE TO CONVERT THE 'TTR0' OF THE VTOC TO  ABSOLUTE *
*   ADDRESS.  IT LINKS TO THE ENQ ROUTINE TO ENQUE THE VTOC ON THIS   *
*   VOLUME.  IT INITIALIZES AND RELOCATES TO THE WORK AREA A CHANNEL  *
*   PROGRAM TO PERFORM THE FOLLOWING OPERATIONS.  READ IN THE DATA    *
*   PORTION OF THE FORMAT 4 DSCB, READ IN COUNT/KEY/DATA OF THE FIRST *
*   FORMAT 5 DSCB, AND PERFORM A DUPLICATE NAME SEARCH OF THE FORMAT 1*
*   DSCB'S IN THE VTOC.  IF A DUPLICATE NAME FORMAT 1 DSCB IS FOUND   *
*   IT READS THE DATA PORTION OF THE DUPLICATE NAME FORMAT 1 DSCB AND *
*   THE COUNT OF THE NEXT RECORD.  THIS MODULE TESTS THE FORMAT 4 DSCB*
*   TO DETERMINE IF THE DOS OR DIRF BIT IS SET.  IF SO, IT BRANCHES TO*
*   IGG0325Z, THE FIRST LOAD OF THE DOS/OS VTOC CONVERSION ROUTINE,   *
*   IN ORDER TO BUILD VALID FORMAT 5 AND FORMAT 6 DSCB'S. AFTER       *
*   REENTRY FROM THE DOS/OS VTOC CONVERSION ROUTINE, THE DUPLICATE    *
*   NAME SEARCH IS REINITIATED.                                       *
*                                                                     *
*ENTRY POINTS:                                                        *
*        IGG0325A - ENTRY IS MADE FROM IGC0003B (SPACE ALLOCATION     *
*        INITIAL LOAD) OR REENTRY FROM IGG0325T (THE LAST LOAD OF     *
*        THE VTOC CONVERSION ROUTINES) TO CONTINUE ALLOCATION OF      *
*        SPACE ON A DIRECT ACCESS VOLUME.                             *
*                                                                     *
*INPUT:                                                               *
*        REGISTER 4 HAS AN ENTRY INDICATOR: IF ZERO, IT INDICATES     *
*        REENTRY FROM THE DOS/OS VTOC CONVERSION ROUTINE MODULES.     *
*        REGISTER 7 POINTS TO THE UCB, IF ENTERED FROM IGC0003B.      *
*        REGISTER 11 WILL POINT TO THE JFCB OR PARTIAL DSCB.          *
*        REGISTER 13 WILL POINT TO ALLOCATE'S WORK AREA.              *
*                                                                     *
*OUTPUT: WHEN A NON ERROR TRANSFER IS MADE TO ANOTHER MODULE,         *
*   THE WORK AREA WILL CONTAIN THE DATA PORTION OF THE FORMAT 4       *
*   DSCB, THE KEY AND DATA OF THE FIRST FORMAT 5 DSCB, AND THE        *
*   ABSOLUTE ADDRESS OF THE FORMAT 4 AND FORMAT 5 DSCB'S.  IF A       *
*   DUPLICATE NAME FORMAT 1 DSCB WAS FOUND THE WORK AREA WILL ALSO    *
*   CONTAIN THE DATA PORTION OF THE DUPLICATE NAME FORMAT 1 DSCB AND  *
*   THE COUNT OF THE NEXT RECORD.                                     *
*   REGISTER 11 WILL POINT TO THE JFCB OR PARTIAL DSCB.               *
*   REGISTER 13 WILL POINT TO THE WORK AREA.                          *
*                                                                     *
*EXTERNAL ROUTINES:                                                   *
*        CVTPCNVT - TTR CONVERSION ROUTINE                            *
*        EXCP(0) - READ FROM OR WRITE TO DIRECT ACCESS DEVICE         *
*        WAIT(1) - WAIT ON EVENT CONTROL BLOCK                        *
*        RESERVE(56) - ENQUEUE THE VTOC                               *
*                                                                     *
*EXITS:                                                               *
*   NORMAL - BRANCH TO IGG0325B FOR NON-ISAM SPACE ALLOCATION         *
*            BRANCH TO IGG0325Z FOR DOS/OS VTOC CONVERSION            *
*            BRANCH TO IGG032I1 FOR ISAM ALLOCATION                   *
*   ERROR  - BRANCH TO IGG0325H WITH CODE IN ERROR CODE PASS REGISTER *
*                                                                     *
*   ERROR CODES THAT CAN BE ISSUED BY THIS MODULE:                    *
*                                                                     *
*   04 - DSNAME OF REQUEST ALREADY EXISTS ON THIS VOLUME - INITIAL    *
*   ALLOCATION NOT POSSIBLE UNDER THE NAME GIVEN                      *
*                                                                     *
*   0C - PERMANENT I/O ERROR                                          *
*                                                                     *
*   18 - AVERAGE RECORD LENGTH SPECIFIED IN REQUEST IS GREATER THAN   *
*   65,535 BYTES IN LENGTH                                            *
*                                                                     *
*   34 - INVALID JFCB OR PARTIAL DSCB POINTER                         *
*                                                                     *
*   40 - INVALID USER LABEL REQUEST                                   *
*                                                                     *
*   A4 - ALLOCATION TERMINATED DUE TO DOS STACKED PACK FORMAT         *
*                                                                     *
*                                                                     *
*TABLES/WORK AREAS: DADSM WORK AREA DESCRIBED BY MACRO 'IECALLWA'     *
*                                                                     *
*              ***************************************                *
*              *             DADSM TABLE             *                *
*              ***************************************                *
*                                                                     *
*              ***************************************                *
*              *        *         *                  *                *
*              *  TYPE  *  NO OF  *     USED HOLE    *                *
*              *  FLAG  * ENTRIES *      COUNTER     *                *
*              *        *         *                  *                *
*              ***************************************                *
*              *                  *                  *                *
*              *       RTA        *      RTA+1       *                *
*              *                  *                  *                *
*              ***************************************                *
*              *                  *                  *                *
*              *       RTA        *      RTA+1       *                *
*              *                  *                  *                *
*              ***************************************                *
*              *                  *                  *                *
*              *       RTA        *      RTA+1       *                *
*              *                  *                  *                *
*              ***************************************                *
*              *                  *                  *                *
*              *       RTA        *      RTA+1       *                *
*              *                  *                  *                *
*              ***************************************                *
*              *                  *                  *                *
*              *       RTA        *      RTA+1       *                *
*              *                  *                  *                *
*              ***************************************                *
*                                                                     *
*              TYPEFLG  =  02 - BPAM DIRECTORIES REQUESTED.           *
*                                                                     *
*                       =  40 - USER LABELS REQUESTED                 *
*                                                                     *
*                       =  80 - SET MUST COMPLETE IS ACTIVE           *
*                                                                     *
*              NO OF ENTRIES = NUMBER OF ENTRIES WITHIN DADSM TABLE.  *
*                                                                     *
*              RTA = THE STARTING TRACK OF THE ALLOCATED EXTENT.      *
*                                                                     *
*              RTA+1 = THE STARTING TRACK OF THE NEXT AVAILABLE EXTENT*
*                                                                     *
*ATTRIBUTES: REENTRANT                                                *
*                                                                     *
*OTHER MACROS USED:                                                   *
*   IECALLWA - ALLOCATE WORK AREA EXPANSION                           *
*   IECSDSL1 - BUILD DSCB'S                                           *
*   IEFJFCBN - BUILD JFCB                                             *
*   IECDSECS - EXPANDS THE CVT, RRPL, AND UCB DSECTS                  *
*                                                                     *
*STORAGE: PROGRAM CODE - LESS THAN 1024 BYTES                         *
*   WORK AREA - AS DEFINED IN THE IECALLWA MACRO                      *
*   RPS WORK AREA - AS DEFINED IN THE IECRPS MACRO                    *
*                                                                     *
*NOTE: THIS MODULE IS NEW IN RELEASE 20.1.                            *
*
*REGISTER USAGE-
*
         BALR  BASEREG,0
         USING BEGINA,BASEREG
         USING ALLOCWKA,RWKAREA         WORK AREA ADDRESSABILITY Y02080
         USING JFCBKEN,RJFCBPTR
         USING UCB,RUCBPTRL
         USING CVT,RCVT
*
REGZERO  EQU   0
REGONE   EQU   1
REGTWO   EQU   2
FREEREGE EQU   2                        EVEN WORK REGISTER
REGTHREE EQU   3
FREEREGO EQU   3                        ODD WORK REGISTER
REGFOUR  EQU   4
REGFIVE  EQU   5
REGSIX   EQU   6
REGSEVEN EQU   7
RUCBPTRL EQU   7                        UCB POINTER
REGEIGHT EQU   8                        WORK REGISTER
RERRPASS EQU   8                        ERROR PASS REGISTER
REGNINE  EQU   9                        WORK REGISTER
REGTEN   EQU   10                       WORK REGISTER
RJFCBPTR EQU   11                       JFCB POINTER
BASEREG  EQU   12                       BASE REGISTER
RWKAREA  EQU   13                       WORK AREA POINTER
RBAK     EQU   14
RCVT     EQU   15
REGFTEN  EQU   15                       WORK REGISTER
*
* OTHER EQUATES
*
ZERO     EQU   0                        CONSTANT OF 0            A46776
TWO      EQU   2                                                   UL17
THREE    EQU   3                        CONSTANT OF 3            A46776
K4       EQU   4                        CONSTANT OF 4            Y02080
FOUR     EQU   4                        CONSTANT OF 4            A46776
K12      EQU   12                       CONSTANT OF 12           Y02080
INVALPTR EQU   X'34'                    INVALID JFCB OR PARTIAL  A46776
*                                       DSCB POINTER             A46776
CHAINING EQU   X'40'                    CCW COMMAND CHAINING BIT Y02080
IOERR    EQU   X'0C'                    I/O ERROR RETURN CODE   SA53153
CSWAD    EQU   8                        CSW OFFSET IN IOB       SA53153
F1ID     EQU   C'1'                     F1 DSCB IDENTIFIER      SA53153
DSORGPO  EQU   X'02'                    DSORG = PO               A46776
DIRFBIT  EQU   X'04'                    VTOC INTERRUPT BIT       O19117
JFCBSUL  EQU   X'08'                    USER LABEL REQUEST       A42477
DOSBIT   EQU   X'80'                    DOS CONTAMINATION BIT   SA56426
DOSSTKPK EQU   X'10'                    DOS STACKED PACK BIT     Y02072
STACKERR EQU   X'A4'                    STACKED PACK RETURN CODE Y02072
READCMMD EQU   X'06'                    READ DATA COMMAND        Y02078
*
* * * * * * * * * * * * *
*
*
* * * * * * * * * * * * *
*
BEGINA   EQU   *                                                 O19117
         LTR   REGTEN,REGFOUR           ENTRY FOR DOS CONVERT
         BZ    MOVECHPG                 YES, GO BUILD CHAN PGM  SA56426
*
* THIS SECTION LINKS TO THE 'ADDRESS CONVERSION' ROUTINE TO GET THE
* VTOC ADDRESS.
*
         L     REGZERO,UCBVTOC          PICK UP THE VTOC TTR0    Y02080
         MVC   MIELNAME(6),SRTEVOLI     SAVE VOLUME SERIAL NUMBER  AAAA
         IC    REGZERO,ZEROS            CLEAR THE LOW ORDER BYTE Y02080
         STM   REGNINE,RWKAREA,IECREGSV+K12  SAVE REGISTERS      Y02080
         LA    REGTWO,SEEK              LOAD POINTER TO 8 BYTE AREA
         LA    REGONE,DEB               LOAD POINTER TO THE DEB
         LR    REGTHREE,RWKAREA         SAVE WORK AREA POINTER
         L     RCVT,CVTPTR              LOAD COMM VECTOR TABLE BASE
         L     RCVT,CVTPCNVT            LOAD CONVERSION ROUTINE BASE
         BALR  RBAK,RCVT                GO TO CONVERSION ROUTINE
         LM    REGNINE,RWKAREA,IECREGSV+K12-FIRST(REGTHREE)      Y02080
SETSEEK  MVC   VTOCADR(5),SEEK+3      SAVE VTOC ADDRESS            OC58
*
* THIS SECTION RELOCATES THE CHANNEL PROGRAM TO THE WORKAREA.  IT ALSO
* INITIALIZES IT TO READ IN THE FORMAT 4 DSCB AND PERFORM A
* DUPLICATE NAME SEARCH
*
*
MOVECHPG LM    REGZERO,REGNINE,CHANPROG  LOAD FIRST 5 CCW'S        OC58
         ALR   REGZERO,RWKAREA
         ALR   REGTWO,RWKAREA
         ALR   REGFOUR,RWKAREA
         ALR   REGSIX,RWKAREA
         ALR   REGEIGHT,RWKAREA
         STM   REGZERO,REGNINE,CCW1     STORE FIRST 5 CCW'S
         LM    REGZERO,REGSEVEN,CCWP6   LOAD CCW'S 6 THRU 9      O19117
         ALR   REGZERO,RWKAREA          RELOCATE                 O19117
         ALR   REGTWO,RJFCBPTR            CCW                    O19117
         ALR   REGFOUR,RWKAREA              DATA                 O19117
         ALR   REGSIX,RWKAREA                 ADDRESSES          O19117
         STM   REGZERO,REGSEVEN,CCW6    STORE CCW'S 6 THRU 9     O19117
         LM    REGFOUR,REGNINE,CCWP11   LOAD CCW'S 11 THRU 13    O19117
         ALR   REGFOUR,RWKAREA          RELOCATE                 O19117
         ALR   REGSIX,RWKAREA             CCW DATA               O19117
         ALR   REGEIGHT,RWKAREA             ADDRESSES            O19117
         STM   REGTWO,REGNINE,CCW10     STORE CCW'S 10 THRU 13   O19117
*
         LTR   REGTEN,REGTEN            Q - DOS CONVERT RE-ENTRY O19117
         BZ    EXECUTET                 IF YES, BR - SKIP ENQ    O19117
         L     RUCBPTRL,DEB+32          RELOAD UCB POINTER       O19117
*
* LINK TO ENQ ROUTINE TO ENQ THE VTOC ON THIS VOLUME.
*
         MVC   MJELNAME(8),VTOCNAME     MOVE MAJOR QUE NAME TO WORKAREA
         MVI   ENQAREA,X'FF'            SET LAST ENTRY CODE
         SR    REGONE,REGONE            CLEAR CODE AND RETURN FIELDS
         STH   REGONE,ENQAREA+2
*
       RESERVE (MJELNAME,MIELNAME,E,6,SYSTEMS),MF=(E,ENQAREA),   YM3022X
               UCB=DEB+32
*
         OI    DSMADTB2,DSMVTOCR        SET VTOC ENQ'ED SWITCH   Y02144
         L     REGONE,IECRRPRM          RECOVERY RTN LIST ADDR   Y02144
         USING RRPLIST,REGONE           PARM LIST ADDRESSABILITY Y02144
         LA    RUCBPTRL,ZERO(RUCBPTRL)  CLEAR HIGH ORDER BYTE    Y02144
         ST    RUCBPTRL,RRUCBPTR        SAVE THE UCB ADDRESS     Y02144
         DROP  REGONE                                            Y02144
*
* THIS SECTION LINKS TO IOS FOR ALL I/O OPERATIONS.
*
EXECUTET EXCP  IOB
         WAIT  1,ECB=ECB
         TM    ECB,X'20'               CHECK FOR PERMANENT I/O ERROR
         LA    RERRPASS,X'0C'           PREPARE FOR POSSIBLE     O19117
*                                       ERROR EXIT
         BNO   ERREXIT                  BR IF I/O ERROR          O19117
         MVC   DADSMADR(5),DSCBF5-8     SAVE 1ST F5 ADDR         M4523
         MVC   FMTOUTAD(5),DADSMADR     SETUP 1ST F5 OUTPUT ADDR M4523
*
* IF THE DIRFBIT WAS NOT PREVIOUSLY SET, OR IF THIS MODULE WAS NOT
* NOT REENTERED FROM THE VTOC CONVERSION ROUTINES, THIS SECTION
* MODIFIES THE CHANNEL PROGRAM TO REWRITE THE FORMAT 4 DSCB WITH
* THE DIRFBIT SET.
*
DIRFTEST EQU   *                                                SA56426
         TM    CCW3+K4,CHAINING         TEST IF LAST I/O WAS FOR Y02080
*                                       REWRITE OF FORMAT 4 DSCB Y02080
         BZ    DOSTEST                  BRANCH IF YES            Y02078
         MVC   CCW9+FOUR(FOUR),IOB+CSWAD  SAVE CSW COMMAND ADDR SA53153
         LTR   REGTEN,REGTEN            TEST IF RE-ENTRY        SA56426
*                                       FROM DOS CONVERT RTNS   SA56426
         BZ    DIRBYP3                  BRANCH IF YES            Y02078
         XI    DS4VTOCI,DIRFBIT         SET/RESET DIRF BIT       O19117
         LA    RERRPASS,STACKERR        SET STACKED PACK FLAG    Y02072
         TM    DS4VTOCI,DOSSTKPK        IS THIS A STACKED PACK?  Y02072
         BO    ERREXIT                  YES - ERROR              Y02072
         TM    DS4VTOCI,DIRFBIT         TEST RESULTS             O19117
         BZ    DIRBYP2                  BRANCH IF DIRF BIT      SA56426
*                                       PREVIOUSLY SET          SA56426
         MVI   CCW3,X'05'               SET WR DATA CMMD         O19117
         MVI   CCW3+4,X'00'             RESET CMMD CHAIN         O19117
         B     EXECUTET                 GO WRITE BACK F4         O19117
DIRBYP2  EQU   *                                                SA56426
         OI    ACNVSW,DIRFSET           INDICATE ENTRY TO VTOC   Y02078
*                                       CONVERT RTNS DUE TO DIRF Y02078
         B     GOCNVERT                 GO CONVERT THE VTOC      Y02078
*
* THIS SECTION TESTS IF THIS IS A DOS OR DOS-CONTAMINATED VOLUME.
* IF SO, IT BRANCHES TO THE DOS/OS VTOC CONVERSION ROUTINE.
*
DOSTEST  EQU   *                        BRANCH LABEL             Y02078
         MVI   CCW3,READCMMD            RESTORE READ DATA CMMD   Y02078
         TM    DS4VTOCI,DOSBIT          TEST IF DOS VOLUME      SA56426
         BZ    DIRBYP3                  NO, DO NOT CONVERT VTOC SA56426
GOCNVERT EQU   *                        BRANCH LABEL             Y02078
         OI    DS4VTOCI,DIRFBIT         SET DIRF BIT PRIOR TO   SA56426
*                                       GOING TO DOS CONVERT    SA56426
         OI    ACNVSW,ALLOCID           ENTRY TO VTOC CONVERT    Y02078
*                                       ROUTINES FROM ALLOCATE   Y02078
         LA    REGFOUR,ALLOC02          GO BRANCH TO IGG0325Z    Y02080
         B     XCTLHERE                 GO XCTL                 SA56426
*
* THIS SECTION DETERMINES IF A USER LABEL TRACK MUST BE ALLOCATED.
* IT ALSO DETERMINES IF A DUPLICATE NAME DSCB WAS FOUND.
*
DIRBYP3  EQU   *                                                SA56426
         LA    RERRPASS,X'40'           PREPARE FOR POSSIBLE     O19117
*                                       ERROR EXIT
         LTR   RJFCBPTR,RJFCBPTR        TEST FOR PARTIAL DSCB    A46776
         BM    PARTIAL                  BRANCH IF YES            A46776
         BZ    NOJFCB                   ERROR IF POINTER IS ZERO A46776
         TM    JFCBLTYP,X'08'           USER LABELS REQUESTED      UL17
         BZ    CKFORDUP                 BRANCH IF NO               UL17
         TM    JFCDSORG,X'82'           IS THIS IS OR PARTITIONED  UL17
         BNZ   ERREXIT                  IF YES, BR               O19117
         TM    JFCDSORG+1,X'80'         IS THIS GRAPHICS           UL17
         BNZ   ERREXIT                  IF YES, BR               O19117
         CLC   JFCBDQTY(THREE),ZEROS    CHECK FOR DIRECTORY      A46776
         BE    SETUSER                  BRANCH IF NOT            A40967
         B     ERREXIT                  NO LABELS IF PARTITIONED A40967
NOJFCB   EQU   *                                                 A46776
         LA    RERRPASS,INVALPTR        INVALID JFCB POINTER     A46776
         B     ERREXIT                  GO XCTL                  A46776
PARTIAL  EQU   *                                                 A46776
         TM    PD1EXT1,USRLBL           USER LABELS REQUESTED    A46776
         BZ    CKFORDUP                 BRANCH IF NO               UL17
         TM    PD1DSORG,DSORGPO         TEST FOR PARTITIONED     A46776
         BO    ERREXIT                  BRANCH IF DSORG = PO     A46776
SETUSER  OI    TYPEFLG,USRLBL           SET USER LABEL INDICATOR   UL17
CKFORDUP EQU   *                                                SA53153
         L     REGTWO,CCW9+FOUR         GET CSW COMMAND ADDRESS SA53153
*                                       FOR FIRST EXCP          SA53153
         LA    REGTWO,0(REGTWO)         CLEAR HIGH BYTE         SA53153
         LA    REGTHREE,CCW9            GET CCW9 ADDRESS        SA53153
         SR    REGONE,REGONE            PREPARE TO RESTORE CCW9 SA53153
         ST    REGONE,CCW9+FOUR         TO ITS ORIGINAL STATUS  SA53153
         CLR   REGTWO,REGTHREE          WAS DSNAME FOUND        SA53153
         BE    NODSNAME                 BRANCH IF NOT FOUND     SA53153
         CLI   F5OUT,F1ID               WAS A DUPLICATE NAME    SA53153
*                                       DSCB FOUND              SA53153
         BE    SAMENAME                 BRANCH IF YES           SA53153
         LA    RERRPASS,IOERR           LOAD I/O ERROR CODE     SA53153
         B     ERREXIT                  BAD VTOC EXIT           SA53153
NODSNAME EQU   *                                                SA53153
         MVI   ASWITCH,FRSTF5           SET FIRST F5 IN CORE     Y02080
*                                       SWITCH                   O19117
         LTR   RJFCBPTR,RJFCBPTR        TEST FOR PARTIAL DSCB    A46776
         BM    CYLTRKRC                 BRANCH IF PARTIAL DSCB   A46776
         B     CHEKDSET                 GO TEST FOR ZERO QTY REQ Y02080
*
* IF DUPLICATE NAME IS FOUND A TEST IS MADE FOR AN ISAM DATA SET
*
SAMENAME EQU   *                                                 A46776
         LTR   RJFCBPTR,RJFCBPTR        TEST FOR PARTIAL DSCB    A46776
         BM    NOTISAM                  BRANCH IF PARTIAL DSCB   A46776
*
* NOT A UTILITY SPACE ALLOCATION REQUEST - TEST FOR ISAM IN THE JFCB
*
OKHERE   TM    JFCDSORG,X'80'
         BO    ISAMALLO                 IF ISAM, BR              O19117
*
* NOT ISAM - ERROR EXIT
*
NOTISAM  LA    RERRPASS,X'04'           SET ERROR CODE 'DUPL NAME DSCB
         B     ERREXIT
*
* THIS SECTION CHECKS FOR ZERO QUANTITY
*
CHEKDSET L     FREEREGE,JFCBPQTY       PICK UP PRIMARY QUANTITY
         SRL   FREEREGE,8              POSITION
         SR    FREEREGO,FREEREGO
         CR    FREEREGE,FREEREGO       CHECK FOR ZERO QTY REQUEST
         BH    CHEKREC                  NOT ZERO QUANTITY
         TM    JFCDSORG,X'80'           DSORG=IS                  24988
         BO    ISAMALLO                 IF YES, BRANCH            24988
*
* TRANSFER CONTROL TO THE NEXT MODULE OF SPACE ALLOCATION
*
CYLTRKRC SR    RERRPASS,RERRPASS        CLEAR ERROR PASS REGISTER
         LA    REGFOUR,ALLOC01          POINT TO ID OF NEXT LOAD Y02080
         B     XCTLHERE                 GO XCTL                  S21016
ERREXIT  EQU   *                                                 S21016
         LA    REGFOUR,ALLOC03          BRANCH TO IGG0325H       Y02080
XCTLHERE EQU   *                                                 A46776
       IECRES LOAD,EXTPR=(RWKAREA),MODID=(REGFOUR),BRANCH=DIRECT Y02080
*
* THIS SECTION CHECKS FOR A RECORD LENGTH GREATER THAN 65,535 BYTES.
*
CHEKREC  TM    JFCBCTRI,X'C0'          IS THIS A CYLINDER REQUEST
         BO    ISAMTEST                 BRANCH IF YES            A46776
         TM    JFCBCTRI,X'40'          IS THIS AN AVERAGE RECORD REQ
         BZ    ISAMTEST                 BRANCH IF NO             A46776
         CLC   JFCBDRLH(1),ZEROS        IS THE RCD/LNGTH FIELD HIGH
*                                       BYTE EQUAL TO ZERO
         LA    RERRPASS,X'18'           PREPARE FOR POSSIBLE     O19117
*                                       ERROR EXIT
         BNE   ERREXIT                  BR IF RCD TOO LONG       O19117
*
* TEST FOR ISAM ON DUPLICATE NAME DSCB NOT FOUND
*
ISAMTEST EQU   *                                                 A46776
         TM    JFCDSORG,JFCORGIS        TEST FOR ISAM            A46776
         BZ    CYLTRKRC                 BRANCH IF NOT ISAM       A46776
*
* THIS SECTION RELOCATES THE FORMAT 1 DSCB FOR ISAM ALLOCATE.
*
ISAMALLO EQU   *
         MVC   DSMADTW4,ADSABLST        SAVE DSAB LIST ADDRESS   Y02144
         OI    DSMADTB1,DSMISAM         SET ISAM ALLOCATE BIT    Y02144
*
         MVC   CCW1(96),F5OUT          RELOCATE THE DATA PORTION OF
         MVC   DSCBF1+44(96),CCW1         THE DUPLICATE NAME DSCB
         LA    REGFOUR,ISAMLOAD         SET UP FOR 1ST ISAM LOAD Y02080
         B     XCTLHERE                 GO BRANCH TO IGG032I1    Y02080
*
***   CHANNEL PROGRAMS FOR SEEK VTOC AND SEARCH FOR SAME NAME   ***
*
CCWP1    CCW   X'31',SEEK+3-FIRST,X'40',5  SEARCH EQUAL ID FOR VTOCOC58
CCWP2    CCW   X'08',CCW1-FIRST,0,0          TIC BACK TO SEARCH
CCWP3    CCW   X'06',DS4IDFMT-FIRST,X'40',96 READ DATA FOR VTOC DSCB
CCWP4    CCW   X'9E',DSCBF5-8-FIRST,X'40',148  READ COUNT//KEY/DATA F5.
CCWP5    CCW   X'F1',DS4HPCHR-FIRST,X'40',5  TEST FOR LAST F1 DSCB
CCWP6    CCW   X'08',CCW10-FIRST,0,0         TIC TO SEARCH NAME EQUAL
CCWP7    CCW   X'A9',0,X'60',44         SKE ON DATA SET NAME     Y02080
CCWP8    CCW   X'06',F5OUT-FIRST,X'30',96  READ DATA (SKIP BIT) SA48172
*                                       OF LAST FORMAT 1 TO END SA48172
*                                       THE CHANNEL PROGRAM     SA48172
CCWP9    CCW   X'08',CCW12-FIRST,0,0         TIC TO READ DATA DSCB F1
*CCWP10        (WILL BE DUPLICATED FROM CCWP7)
CCWP11   CCW   X'08',CCW5-FIRST,0,0          TIC BACK TO SEARCH LAST F1
CCWP12   CCW   X'06',F5OUT-FIRST,X'40',96  READ DATA ON DUP NAME DSCB
CCWP13   CCW   X'12',COUNT-FIRST,X'20',5     READ COUNT OF NEXT DSCB F1
CHANPROG EQU   CCWP1
ZEROS    EQU   CCWP11+FOUR              FOUR BYTES OF ZEROS      A46776
*
* * * * * * * * * * * * *
*
***   CONSTANTS
*
* * * * * * * * * * * * *
*
VTOCNAME DC    CL8'SYSVTOC'             MAJOR QUEUE NAME
*
* TABLE OF MODULE IDS AND ENTRY POINT ADDRESSES
*
XCTLTABL XCTLTABL ID=(ALLOC01,5B,ALLOC02,5Z,ALLOC03,5H,          Y02080X
               ISAMLOAD,I1),SVC=032,LENGTH=,BRT=YES              Y02080
         EJECT                                                   Y02080
*
* * * * * * * * * * * * *
*
*
* * * * * * * * * * * * *
*
JFCBKEN  DSECT
         IEFJFCBN
         EJECT                                                   Y02080
*
* * * * * * * * * * * * *
*
***   DSECT FOR THE PARTIAL DSCB PASSED TO ALLOCATE BY IEHMOVE   A46776
*
* * * * * * * * * * * * *
*
         ORG   INFMJFCB
PD1DSNAM DS    CL44                DATA SET NAME
PD1FMTID DS    CL1                 FORMAT IDENTIFIER
PD1DSSN  DS    CL6                 DATA SET SERIAL NUMBER
PD1VOLSQ DS    XL2                 VOLUME SEQUENCE NUMBER
PD1CREDT DS    XL3                 CREATION DATE
PD1EXPDT DS    XL3                 EXPIRATION DATE
PD1NOEPV DS    XL1                 NUMBER OF EXTENTS ON VOLUME
PD1NOBDB DS    XL1                 NUMBER OF BYTES USED IN LAST
*                                     DIRECTORY BLOCK
         DS    XL1                 RESERVED
PD1SYSCD DS    CL13                SYSTEM CODE
         DS    XL7                 RESERVED
PD1DSORG DS    XL2                 DATA SET ORGANIZATION
PD1RECFM DS    XL1                 RECORD FORMAT
PD1OPTCD DS    XL1                 OPTION CODE
PD1BLKL  DS    XL2                 BLOCK LENGTH
PD1LRECL DS    XL2                 RECORD LENGTH
PD1KEYL  DS    XL1                 KEY LENGTH
PD1RKP   DS    XL2                 RELATIVE KEY POSITION
PD1DSIND DS    XL1                 DATA SET INDICATORS
PD1SCALO DS    XL4                 SECONDARY ALLOCATION
PD1LSTAR DS    XL3                 LAST USED TRACK AND BLOCK ON TRACK
PD1TRBAL DS    XL2                 BYTES REMAINING ON LAST TRACK USED
         DS    XL2                 RESERVED
PD1EXT1  DS    XL1                 EXTENT TYPE INDICATOR
         DS    XL2                 UNUSED
PDPRIQTY DS    F                   PRIMARY SPACE REQUEST IN TRACKS
PDDIRQTY DS    F                   NUMBER OF DIRECTORY BLOCKS
         SPACE 2                                                 Y02080
         IECDSECS CVT,RRPL,UCB,EXPAND=YES                        Y02144
DSCBWKAR IECALLWA EP,F4,ADT=YES         ALLOCATE WORK AREA       Y02144
ENQAREA  EQU   ADSCBF4                  ENQUEUE PARAMETER LIST   Y02080
MJELNAME EQU   ADSCBF4+16               MAJOR ELEMENT NAME       Y02080
         END
