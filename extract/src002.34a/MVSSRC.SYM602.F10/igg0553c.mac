 TITLE 'IGG0553C - EXTEND - F1,F3 DSCB ROUTINE'
IGG0553C CSECT
*
*MODULE NAME = IGG0553C
*
*DESCRIPTIVE NAME = EXTEND - F1, F3 DSCB ROUTINE
*
*COPYRIGHT = NONE
*
*CHANGE ACTIVITY = SEE BELOW
*
*          RELEASE 16 DELETIONS
*0095038400,038800-039200,045600                                  13559
*          RELEASE 17 DELETIONS
*1708000640,028600-028800,031400,032000,034800-035000.             UL17
*1708002800,005000-005200,017400-017600,047400-047600             19682
*          RELEASE 18 DELETIONS
*3478                                                             MCS
*3478018400,018800-019200,064600,065000                           22833
*          RELEASE 19 DELETIONS
*          RELEASE 20 DELETIONS
*1023015200,042200                                               M6099
*1023017300,037000-037200,051600-051800,058600,064800            S20016
*          RELEASE 21 DELETIONS
*1395001400-001800,002600-003800,005600-007200,008000-009000,    M0130
*1395009600-010000,017144-017152,017176,017200-017800,019000-    M0130
*1395019400,028000,030600,031200,031420,032000-032200,034940,    M0130
*1395043200,044600,046600-047000,056000,061000,062800,063200,    M0130
*1395070000-071000,071400-072000,072800-075400,076000-076400     M0130
*1395039800-042600                                               M0050
*1395010800-012400,025200,026200-027400,034660,034700,034740,    A40437
*1395034800,048800-050800,065600-065800,066600,078800            A40437
*1395056600,065450                                               M0184
*          RELEASE 22 DELETIONS
*          VS2 RELEASE 02 DELETIONS/CHANGES
*0000000030-000150,000520-000600,002200,002420,002580-002640,    Y02080
*0000003360-003999,004229,004800,005800,006200,007600,008600,    Y02080
*0000009200,009500-010396,012600-017000,017128-017484,018000-    Y02080
*0000018600,018900,020400-020800,020860,020900,021000,022600-    Y02080
*0000022800,023200-023800,024200-024800,025210-025240,025300,    Y02080
*0000028600-029200,029600-029800,030200-031100,031250,031270-    Y02080
*0000031280,031330-031340,031360-031420,031440-031800,032200-    Y02080
*0000034200,034600,034630,034660,034700-034880,034940,034980-    Y02080
*0000035000,035400,036800,036860-036920,037400-037800,038200-    Y02080
*0000038250,038400,038500-038562,038580-038586,042800-043200,    Y02080
*0000044600-044800,045200,045800,046200,047200-048600,052800-    Y02080
*0000052840,052920-053000,053600-053800,054200,056000,056400,    Y02080
*0000059200-059400,060400-062000,062280,062300,062600-063000,    Y02080
*0000065200-065400,065600,065800-072400,073600,076000-078400,    Y02080
*0000088600-298600                                               Y02080
*0000                                                            Y02082
*0000                                                            Y02144
*0000                                                            Y02083
*0000065450                                                      YM3995
*
*          VS2 RELEASE 03.0 DELETIONS/CHANGES                         *
*0000065450                                                     ZA00704
*          SU32 DELETIONS                                      @G32DSFS
*                                                              @G32DSMI
*                                                                     *
*STATUS CHANGE LEVEL 005
*
*FUNCTION/OPERATION: THIS MODULE CHECKS FOR NEW VOLUME OR LAST VOLUME *
*   EXTENSION. IF ON A NEW VOLUME A DSCB FORMAT 1 IS CREATED AND IF   *
*   NECESSARY A FORMAT 3. IF ON THE LAST VOLUME OF A DATA SET THE     *
*   FORMAT 1 AND FORMAT 3 ARE UPDATED. IF NO FORMAT 3 EXISTS BUT      *
*   ONE IS NEEDED, IT IS CREATED. IF THE OLD VOLUME WAS        @Z40RSGD
*   DEFINED TO RAC, THE NEW VOLUME IS DEFINED TO RAC AND THE   @Z40RSGD
*   RAC DEFINED BIT IS SET IN THE NEW FORMAT 1.                @Z40RSGD
*                                                                     *
*ENTRY POINT:                                                         *
*        IGG0553C - ENTRY IS MADE FROM IGG0553B VIA A BRANCH.         *
*                                                                     *
*INPUT:                                                               *
*   REGISTER 4 POINTS TO THE I/O SUPPORT WORK AREA WHICH CONTAINS THE *
*   FORMAT 4 DSCB.                                                    *
*   REGISTER 5 POINTS TO THE USER'S DEB.                              *
*   REGISTER 11 POINTS TO THE EXTEND WORK AREA, WHICH CONTAINS THE    *
*   DADSM EXTENT TABLE.                                               *
*   THE I/O SUPPORT WORK AREA IS LAID OUT AS FOLLOWS:                 *
*                                                                     *
*          BYTES          AREA                                        *
*         *******        ******                                       *
*          100            DSCB FIELD                                  *
*          176            JFCB                                        *
*            4            ECB                                         *
*           40            IOB                                         *
*           44            DEB                                         *
*            4            DCB                                         *
*           96            CCW'S                                       *
*                                                                     *
*OUTPUT:                                                              *
*   THE FORMAT 1 AND FORMAT 3 DSCB'S ARE CREATED OR UPDATED. THE      *
*   DADSM EXTENT TABLE IN THE EXTEND WORK AREA IS BUILT.              *
*                                                                     *
*EXTERNAL ROUTINES:                                                   *
*        EXCP(0) - READ OR WRITE DIRECT ACCESS DEVICE                 *
*        WAIT(1) - WAIT ON EVENT CONTROL BLOCK                        *
*        IECRES  - TRANSFER CONTROL TO OTHER LOADS                    *
*        STATUS  - SET STEP MUST COMPLETE IN THE TCB                  *
*                                                                     *
*EXITS:                                                               *
*   NORMAL - BRANCH TO IGG0553D                                       *
*                                                                     *
*   ERROR  - BRANCH TO IGG0553E WITH THE ENTRY CODE IN REGISTER 13    *
*   ERROR CONDITIONS ARE I/O ERROR AND TOO MANY EXTENTS (GREATER      *
*   THAN 16) FOR THIS VOLUME AND UNABLE TO RAC DEFINE A NEW    @Z40RSGD
*   VOLUME WHEN THE OLD VOLUME WAS RAC DEFINED.                @Z40RSGD
*                                                                     *
*ATTRIBUTES: REENTRANT                                                *
*                                                                     *
*OTHER MACROS USED:                                                   *
*   IECEXTWA - TO DEFINE THE EXTEND WORK AREA                         *
*   IECDSECT - TO DEFINE THE I/O SUPPORT WORK AREA                    *
*   IECSDSL1 - TO SYMBOLICALLY ADDRESS DSCB'S                         *
*   IECDSECS - TO EXPAND THE CVT, DSAB, IEZDEB, PSA, SCVT, TCB DSECTS *
*                                                                     *
*STORAGE: PROGRAM CODE CSECT - LESS THAN 2048 BYTES                   *
*         EXTEND WORK AREA - AS DEFINED IN THE IECEXTWA MACRO         *
*         RPS WORK AREA - AS DEFINED IN THE IECRPS MACRO              *
*         I/O SUPPORT WORK AREA                                       *
*
*
* REGISTER EQUATES
*
REG0     EQU   0                        EQUATE FOR REGISTER 0    Y02080
REG1     EQU   1                        EQUATE FOR REGISTER 1    Y02080
REG2     EQU   2                        EQUATE FOR REGISTER 2    Y02080
RBASE    EQU   3                        BASE REGISTER            Y02080
RCORE    EQU   4                        EOV WORK AREA POINTER    Y02080
REG5     EQU   5                        EQUATE FOR REGISTER 5    Y02080
RDEB     EQU   5                        DEB POINTER              Y02080
ALTWKA   EQU   6                        ALTERNATE WKA POINTER    Y02080
REG6     EQU   6                        EQUATE FOR REGISTER 6    Y02080
REG7     EQU   7                        EQUATE FOR REGISTER 7    Y02080
REG8     EQU   8                        EQUATE FOR REGISTER 8    Y02080
RTNREG2  EQU   8                        RETURN REGISTER          Y02080
REG9     EQU   9                        EQUATE FOR REGISTER 9    Y02080
RA       EQU   10                       EQUATE FOR REGISTER 10   Y02080
EXWKA    EQU   11                       EXTEND WORK AREA POINTER Y02080
RC       EQU   12                       EQUATE FOR REGISTER 12   Y02080
RD       EQU   13                       EQUATE FOR REGISTER 13   Y02080
RTNREG   EQU   14                       RETURN REGISTER          Y02080
RF       EQU   15                       EQUATE FOR REGISTER 15   Y02080
RCVT     EQU   15                       CVT POINTER              Y02080
*
* OTHER EQUATES
*
DS1CKDS  EQU   X'01'                    DSCB1 CHKPT D/S FLAG     Y02083
RDWOPSWD EQU   X'04'                    READ WITHOUT PASSWORD    M0130
NEWVOL   EQU   X'04'                    EXTEND ON NEW VOLUME     M0130
TWO      EQU   2                                                   UL17
ZERO     EQU   0                        CONSTANT OF 0            Y02080
K0       EQU   0                        CONSTANT OF 0            Y02080
K1       EQU   1                        CONSTANT OF 1            Y02080
STEPMC   EQU   1                        STATUS STEP MC INDICATOR Y02080
K3       EQU   3                        CONSTANT OF THREE        Y02080
FOUR     EQU   4                                                   UL17
K6       EQU   6                        CONSTANT OF SIX          Y02080
K12      EQU   12                       CONSTANT OF TWELVE       Y02080
K16      EQU   16                       CONSTANT OF SIXTEEN      Y02080
TRKALIGN EQU   X'01'                    EXTENT IS TRACK ALIGNED  Y02080
CYLALIGN EQU   X'81'                    EXTENT IS CYLINDER ALIGN Y02080
USRLABEL EQU   X'40'                    USER LABEL TRACK EXTENT  Y02080
RESBITS  EQU   X'13'                    JFCB DSORG RESERVE BITS  Y02080
DS1RACDF EQU   X'40'                    DS1DSIND-DATA SET IS   @Z40RSGD
*                                       RAC DEFINED            @Z40RSGD
RACERROR EQU   X'40'                    RAC ERROR              @Z40RSGD
*
         USING EOVWKA,RCORE             EOV W/A ADDRESSABILITY   Y02080
         USING EXTNDWKA,EXWKA           WORK AREA ADDRESSABILITY Y02080
         BALR  RBASE,K0                 CSECT ADDRESSABILITY     Y02080
         USING BEGIN,RBASE                                       Y02080
*
* IF THE TASK IS NOT ALREADY STEP MUST COMPLETE, THIS SECTION LINKS
* TO THE STATUS ROUTINE TO SET THE TASK STEP MUST COMPLETE.
*
BEGIN    EQU   *                        BRANCH LABEL             Y02080
         XC    F5DSCB,F5DSCB            CLEAR WORK AREA          Y02080
         XC    F3DSCB,F3DSCB            CLEAR WORK AREA          Y02080
         MVI   DXCCW5+4,X'30'          BREAK CHANNEL PGM AT CCW5  13559
         LA    RC,ENTRIES               PTR TO DADSMTBL ENTRIES  Y02080
         SR    RD,RD                                             Y02080
         IC    RD,ENTRYNUM              NO. OF DADSMTBL ENTRIES  Y02080
*
         STM   EXWKA,RTNREG,IECREGSV+K12  SAVE REGISTERS 11-14   Y02080
         LR    ALTWKA,EXWKA             SAVE WORK AREA ADDRESS   Y02080
         MODESET EXTKEY=ZERO            SWITCH TO KEY ZERO       Y02082X
                                        BEFORE THE SETLOCK       Y02082
GETLOCK  SETLOCK OBTAIN,TYPE=LOCAL,MODE=UNCOND,                  Y02080X
               RELATED=(LOCAL,IGG0553C(RLSELOCK))  OBTAIN LOCK   Y02080
         LM    EXWKA,RTNREG,IECREGSV+K12-EXTNDWKA(ALTWKA)        Y02080
*
         USING DEBBASIC,RDEB            DEB ADDRESSABILITY       Y02080
         L     REG1,DEBTCBAD            LOAD TCB ADDR FROM DEB   Y02080
         USING TCB,REG1                 TCB ADDRESSABILITY       Y02080
         TM    TCBFLGS2,TCBFJMC         TEST IF ALREADY SMC      Y02080
         BO    RLSELOCK                 BRANCH IF USER SMC       Y02080
         DROP  REG1                                              Y02080
*
         USING CVT,RCVT                 CVT ADDRESSABILITY       Y02080
         L     RCVT,CVTPTR              LOAD CVT ADDRESS         Y02080
         L     RCVT,CVTABEND            LOAD SECONDARY CVT PTR   Y02080
         USING SCVTSECT,RCVT            2NDRY CVT ADDRESSABILITY Y02080
         L     RCVT,SCVTSTAT            LOAD STATUS EP ADDRESS   Y02080
         LA    REG0,STEPMC              INDICATE STEP MC         Y02080
         XR    REG1,REG1                INDICATE STATUS SET      Y02080
*
         BALR  RTNREG,RCVT              LINK TO STATUS ROUTINE   Y02080
*
         OI    ETYPEFLG,RSMC            SET RESET SMC FLAG       Y02080
         OI    DSMADTB2,DSMSMCE         SET VTOC ENQ'ED SMC BIT  Y02144
*
RLSELOCK EQU   *                        BRANCH LABEL             Y02080
         STM   EXWKA,RTNREG,IECREGSV+K12  SAVE REGISTERS 11-14   Y02080
         LR    ALTWKA,EXWKA             SAVE WORK AREA ADDRESS   Y02080
         SETLOCK RELEASE,TYPE=LOCAL,                             Y02080X
               RELATED=(LOCAL,IGG0553C(GETLOCK))  RELEASE LOCK   Y02080
         MODESET EXTKEY=DATAMGT         RETURN TO DATAMGT KEY    Y02082
         LM    EXWKA,RTNREG,IECREGSV+K12-EXTNDWKA(ALTWKA)        Y02080
*
         USING CVT,RCVT                 CVT ADDRESSABILITY       Y02080
         TM    DXEXTSW,NEWVOL           TEST IF NEW VOLUME       Y02080
         BO    BUILDF1                  BRANCH IF NEW VOLUME     M0130
*
* MODIFY IOB AND CCW'S FOR READ OF FORMAT 1 DSCB
*
READF1   MVC   SEEK+3(5),COUNT          SET UP SEEK ADDRESS IN IOB
         BAL   RTNREG2,EXEC             GO FOR FORMAT 1 READ     Y02080
NOERROR  EQU   *                        BRANCH LABEL             Y02080
         SR    REG6,REG6                                         Y02080
         IC    REG6,DS1NOEPV            NUMBER OF EXTENTS        Y02080
         CLI   DS1EXT1,X'40'    IS 1ST XTENT UL EXTENT            22833
         BNE   NVL              NO                                22833
         LA    REG6,K1(REG6)            MAKE COUNT+1 FOR INTERNL Y02080
*                                       USE                       22833
         STC   REG6,DS1NOEPV                                     Y02080
NVL      EQU   *                                                  22833
         AR    REG6,RD                  ADD NUMBER OF EXTENTS IN Y02080
*                                       THE DADSM EXTENT TABLE   Y02080
*
* AFTER THE FORMAT 1 DSCB FOR THIS DATA SET IS READ IN THE NUMBER OF
* EXTENTS FOR THE EXISTING DATA SET PLUS THE NUMBER OF NEW EXTENTS IS
* COMPARED TO SIXTEEN. IF THE TOTAL IS GREATER THAN SIXTEEN A RETURN
* IS MADE TO EOV SINCE THERE CAN BE ONLY A MAXIMUM OF SIXTEEN EXTENTS
* FOR A DATA SET PER VOLUME.
*
         STC   REG6,ESWITCH             SAVE NUMBER OF EXTENTS   Y02080
         CLI   ESWITCH,K16              CHECK HOW MANY           Y02080
         BH    RETURN                   BR IF TO MANY EXTENTS
         IC    REG6,DS1NOEPV            NO. OF CURRENT EXTENTS   Y02080
         LR    REG5,REG6                EXTENT NUMBER OF THE     Y02080
*                                       FIRST NEW EXTENT         Y02080
         LA    RA,DS3EXTNT              START OF FIRST F3 EXTENT Y02080
         SH    REG6,THREE               SUBTRACT 3 IN CASE OF F3 Y02080
         BC    10,NOTF1                 BR IF NO MORE ROOM IN F1
         LR    REG6,REG5                RESTORE NO. OF EXTENTS   Y02080
         LA    RA,DS1EXT1               START OF FIRST F1 EXTENT Y02080
NOTF1    EQU   *                        BRANCH LABEL             Y02080
         MH    REG6,TEN                 TIMES EXTENT SIZE        Y02080
         AR    RA,REG6                  POINT TO 1ST FREE EXTENT Y02080
         CLI   DS1NOEPV,3               CHECK FOR EXISTING F3 DSCB
         BNH   CHKEXT                   BRANCH IF NO F3 EXISTS   A40437
*
* MODIFY IOB AND CCW'S TO READ THE EXISTING FORMAT 3 DSCB
*
         MVC   SEEK+3(5),DS1PTRDS       SET UP SEEK IN IOB       A40437
         MVC   COUNT2(5),DS1PTRDS                                A40437
         LA    REG8,IECSDSL3            POINT TO INPUT AREA      Y02080
         ST    REG8,DXCCW11                                      Y02080
         MVI   DXCCW11,X'0E'            INSERT READ OP CODE      A40437
         BAL   RTNREG2,EXEC             GO READ THE F3 DSCB      Y02080
         MVC   DXCCW11+1(3),DXCCW5+1    RESTORE F1 AREA ADDRESS  A40437
CHKEXT   CLI   DS1NOEPV,7               CHECK NO. OF EXTENTS
         BNL   STBUMP                   BR IF POINTER IS PAST FMTID FLD
         B     STARTIT                  CONVERT AND MOVE EXTENTS
*
* CONVERT AND MOVE EXTENTS TO FORMAT1 AND FORMAT3 DSCB AREAS.
* CHECK FOR EXISTANCE OF FORMAT 3 DSCB AND WRITE IT IF NECESSARY.
* ENTRY TO THIS ROUTINE IS AT STARTIT
*
STLOOP   EQU   *                        BRANCH LABEL             Y02080
         LA    RC,FOUR(RC)              POINT TO NEXT ENTRY      Y02080
STLOOP2  EQU   *                        BRANCH LABEL             Y02080
         LA    REG5,K1(REG5)            INCREMENT EXTENT NUMBER  Y02080
         LA    RA,L'DS1EXT1(RA)         INCREMENT EXTENT POINTER Y02080
         CH    REG5,THREE               CHECK NEW EXTENT SEQ NO. Y02080
         BNE   STCHKBMP                 BR IF NOT AT START OF F3 DSCB
         LA    RA,DS3EXTNT              SET TO START OF F3       Y02080
STCHKBMP EQU   *                        BRANCH LABEL             Y02080
         CH    REG5,SEVEN               CHECK NEW EXTENT SEQ NO. Y02080
         BNE   STARTIT                  BR IF NOT AT F3 FMTID FIELD
STBUMP   EQU   *                        BRANCH LABEL             Y02080
         LA    RA,K1(RA)                BUMP PAST FORMAT ID      Y02080
STARTIT  EQU   *                        BRANCH LABEL             Y02080
         STC   REG5,K1(RA)              EXTENT SEQ NO. IN DSCB   Y02080
         LH    REG0,0(RC)               GET LOW RTA              Y02080
         BAL   RTNREG2,RTACONVT         LINK TO CALL CONVERSION  Y02080
         MVC   TWO(FOUR,RA),DXCCW12+K3  MOVE IN LOW CCHH         Y02080
         TM    JFCDSORG,X'20'           IS IT CREATE DIRECT D.S.   UL17
         BC    1,NOUSERLB               YES,NO USER LABELS         UL17
         TM    JFCBLTYP,X'08'           USER LABEL REQUEST         UL17
         BZ    NOUSERLB                 BRANCH IF NO               UL17
         LTR   REG5,REG5                SEE IF USER LABEL EXTENT Y02080
         BP    NOUSERLB                 BRANCH IF NOT              UL17
         MVC   EPDLIST2(TWO),0(RC)      SAVE RTA                 Y02080
         LH    REG2,0(RC)               PICK UP RTA              Y02080
         TM    JFCBCTRI,X'C0'           IS THIS A CYLINDER REQUEST UL17
         BO    CYLREQ                   BRANCH IF YES              UL17
         TM    JFCBCTRI,X'41'           IS THIS RECORD ROUNDED     UL17
         BO    CYLREQ                   BR IF YES                  UL17
         LA    REG2,K1(REG2)            INCREMENT RTA            Y02080
         STH   REG2,0(RC)               STORE IN DADSM TABLE     Y02080
         B     UPCCHH                   MOVE CCHH TO DSCB          UL17
CYLREQ   AH    REG2,DS4DEVSZ+TWO        ADD TRKS/CYL TO RTA      Y02080
         STH   REG2,0(RC)               UPDATE RTA IN DADSMTBL   Y02080
         BCTR  REG2,K0                  DECREMENT FOR U.L. RTA   Y02080
         LR    REG0,REG2                SET UP RTA IN REGISTER   Y02080
         STH   REG2,EPDLIST2            UPDATE SAVED RTA         Y02080
         BAL   RTNREG2,RTACONVT         LINK TO CALL CONVERSION  Y02080
         MVC   TWO(FOUR,RA),DXCCW12+K3  USER LABEL LOW CCHH      Y02080
         B     UPCCHH                   GO TO MOVE CCHH TO DSCB    UL17
NOUSERLB EQU   *                        BRANCH LABEL             Y02080
         LH    REG0,TWO(RC)             PICK UP RTA+1            Y02080
         BCTR  REG0,K0                  DECREMENT FOR HIGH RTA   Y02080
         BAL   RTNREG2,RTACONVT         LINK TO CALL CONVERSION  Y02080
UPCCHH   EQU   *                                                 M0130
         MVC   K6(FOUR,RA),DXCCW12+K3   MOVE IN UPPER CCHH       Y02080
         MVI   0(RA),TRKALIGN           INDICATE NOT CYL ALIGNED Y02080
         LH    REG8,DS4DEVSZ+TWO        PICK UP TRACKS/CYLINDER  Y02080
         SR    REG6,REG6                                         Y02080
         LH    REG7,0(RC)               LOAD RTA                 Y02080
         DR    REG6,REG8                CHECK FOR CYLINDER ALIGN Y02080
         LTR   REG6,REG6                IF ZERO - ALIGNED        Y02080
         BP    NOTCYL                   BRANCH IF NOT ALIGNED    Y02080
         LH    REG7,TWO(RC)             LOAD RTA+1               Y02080
         DR    REG6,REG8                CHECK FOR CYLINDER ALIGN Y02080
         LTR   REG6,REG6                                         Y02080
         BP    NOTCYL                   BR IF NOT ALIGNED
         MVI   0(RA),CYLALIGN           INDICATE CYL ALIGNED     Y02080
NOTCYL   EQU   *                                                   UL17
         LA    REG1,NOUSER              SET UP BRANCH REGISTER   Y02080
         TM    JFCDSORG,X'20'           IS IT CREATE DIRECT D.S.   UL17
         BCR   1,REG1                   YES, THEN NO USER LABELS Y02080
         TM    JFCBLTYP,X'08'           TEST FOR USR LBL REQUEST   UL17
         BCR   8,REG1                   BRANCH IF NO             Y02080
         LTR   REG5,REG5                WAS IT USER LABEL EXTENT Y02080
         BCR   2,REG1                   BRANCH IF NO             Y02080
         MVI   0(RA),USRLABEL           MOVE IN USER LABEL TYPE  Y02080
         CLC   0(TWO,RC),TWO(RC)        WAS THIS U.L. ONLY       Y02080
         BCR   8,REG1                   BRANCH IF YES            Y02080
         LA    RD,K1(RD)                UPDATE NUMBER OF EXTENTS Y02080
         STC   RD,ESWITCH                                        Y02080
         BCT   RD,STLOOP2               GET NEXT EXTENT          Y02080
NOUSER   EQU   *                        BRANCH LABEL             Y02080
         BCT   RD,STLOOP                BR IF ANOTHER EXTENT     Y02080
         CLI   DS1EXT1,X'40'            IS THIS A USER LABEL EXTENTUL17
         BNE   PREPWRT                  BRANCH IF NO               UL17
         TM    DXEXTSW,NEWVOL           TEST IF NEW VOLUME       Y02080
         BZ    PREPWRT                  BRANCH IF NO               UL17
         MVC   ENTRIES(TWO),EPDLIST2    RESTORE USER LABEL RTA   Y02080
PREPWRT  EQU   *                        BRANCH LABEL             Y02080
         LA    REG6,DXCCW6                                       Y02080
         MVI   DXCCW11+4,X'10'          SET ON WRITE CHECK FLAG
         CLI   ESWITCH,K3               CHECK ACTUAL NBR EXTENTS Y02080
         BH    WROUTF3                  BR IF F3 DSCB TO BE WRITTEN
*
* SEARCH FOR SPACE IF NEEDED AND WRITE FORMAT 1
*
WRITEF1  EQU   *                                                  13559
         MVC   DS1PTRDS(5),COUNT2       CHAIN ADDRESS OF F3
         MVC   DS1NOEPV,ESWITCH         SET TOTAL NBR OF EXTENTS Y02080
         LA    REG6,COUNT                                        Y02080
         ST    REG6,LITTLE1             GET IDAREA ADDRESS       Y02080
         NC    COUNT+1(4),COUNT+1       CHK FOR EXISTING FORMAT  S20016
*                                       1                        S20016
         BNZ   GOTIDF1                  BRANCH IF FORMAT 1       S20016
*                                       EXISTS                   S20016
         LH    REG6,UHOLECTR            UPDATE USED HOLE COUNTER Y02080
         LA    REG6,K1(REG6)                                     Y02080
         STH   REG6,UHOLECTR                                     Y02080
         MVC   COUNT(5),VTOCADR         START SEARCH AT VTOC ADDRESS
         LA    REG6,DXCCW1                                       Y02080
         ST    REG6,IOBSIOCC            START SEARCH AT CCW1     Y02080
         MVC   DXCCW2+1(3),LITTLE1+1   IDAREA ADDRESS             13559
         MVC   SEEK+3(4),COUNT         IOB SEEK ADDRESS           13559
         BAL   RTNREG2,EXEC             SEARCH FOR FORMAT 0      Y02080
GOTIDF1  EQU   *                                                  13559
         LA    REG6,DXCCW6              START WRITE AT CCW6      Y02080
         ST    REG6,IOBSIOCC            STORE ENTRY POINT        Y02080
         SR    REG6,REG6                                         Y02080
         IC    REG6,DS1NOEPV            OBTAIN EXTENT COUNT      Y02080
         CLI   DS1EXT1,X'40'       IS 1ST XTENT UL EXTENT         22833
         BNE   MVL2                NO                             22833
         BCTR  REG6,K0                  REDUCE EXTENT COUNT BY 1 Y02080
         STC   REG6,DS1NOEPV                                     Y02080
MVL2     EQU   *                                                  22833
         MVC   SEEK+3(4),COUNT          INSERT SEEK ADDRESS IN IOB
         MVC   DXCCW6+1(3),LITTLE1+1
         MVC   DXCCW9+1(3),LITTLE1+1
         BAL   RTNREG2,EXEC             GO TO WRITE FORMAT 1     Y02080
         CLC   DS4HPCHR,COUNT           TEST IF NEW HIWATER MARK Y02080
         BNL   MODIFYCP                 BRANCH IF NOT            Y02080
         MVC   DS4HPCHR,COUNT           MOVE IN NEW HIWATER MARK Y02080
*
* THIS SECTION MAKES CHANGES IN THE CHANNEL PROGRAM FOR THE DADSM
* UPDATE LOADS.CCW1 THRU CCW5 WILL BE USED TO SEARCH FOR A DSCB HOLE.
* CCW6 THRU CCW 11 ARE USED FOR WRITING FORMAT 5 AND FORMAT 4.
* CCW9 THRU CCW 11 ARE USED TO READ FORMAT5 AND FORMAT4
*
MODIFYCP EQU   *                        BRANCH LABEL             Y02080
         LA    REG6,IDAREA              POINT TO IDAREA          Y02080
         ST    REG6,DXCCW1                                       Y02080
         MVI   DXCCW1,X'16'             RESTORE READ R0 COMMAND
         ST    REG6,DXCCW2              CHANGE PTR IN CCW2       Y02080
         MVI   DXCCW2,X'92'             RESTORE READ COUNT COMMAND
         ST    REG6,DXCCW6              CHANGE PTR IN CCW6       Y02080
         MVI   DXCCW6,X'31'             RESTORE SEARCH EQUAL COMMAND
         ST    REG6,DXCCW9              CHANGE PTR IN CCW6       Y02080
         MVI   DXCCW9,X'31'             RESTORE SEARCH EQUAL COMMAND
         LA    REG6,F5LOAD              POINT TO IGG0553D        Y02080
RETRNEX  EQU   *                        BRANCH LABEL             Y02080
         LA    REG2,ENTRIES             PTR TO DADSMTBL ENTRIES  Y02080
XCTLHERE EQU   *                        BRANCH LABEL             Y02080
         MVI   ESWITCH,ZERO             ZERO ESWITCH             Y02080
         IECRES LOAD,EXTPR=(EXWKA),MODID=(REG6),BRANCH=DIRECT    Y02080
*
* SEARCH FOR HOLE FOR FORMAT 3 AND WRITE IT OR JUST WRITE THE FORMAT 3
*
WROUTF3  EQU   *                                                 S20016
         NC    COUNT2+1(4),COUNT2+1     CHK FOR EXISTING FORMAT  S20016
*                                       3                        S20016
         BNZ   GOTIDF3                  BRANCH IF FORMAT 3       S20016
*                                       EXISTS                   S20016
         MVC   IECSDSL3(4),HEX3         4 HEX 03'S TO KEY
         MVI   DS3FMTID,X'F3'           FORMAT IDENTIFIER
         MVI   UHOLECTR+1,X'01'         UPDATE USED HOLE COUNTER
         MVC   COUNT2(5),VTOCADR        START SEARCH AT VTOC
         LA    REG6,DXCCW1                                       Y02080
         ST    REG6,IOBSIOCC            START SEARCH AT CCW1     Y02080
         MVC   SEEK+3(4),COUNT2        IOB SEEK ADDRESS           13559
         BAL   RTNREG2,EXEC             SEARCH FOR FORMAT 0      Y02080
         LA    REG6,DXCCW6              START WRITE AT CCW6      Y02080
GOTIDF3  EQU   *                        BRANCH LABEL             Y02080
         ST    REG6,IOBSIOCC            CHAN PROG START ADDRESS  Y02080
         MVC   SEEK+3(4),COUNT2        INSERT SEEK ADDR INTO IOB.
         MVC   DXCCW9+1(3),DXCCW2+1     POINTER TO COUNT2 FOR CCW
         LA    REG6,IECSDSL3            SET CCW TO FORMAT 3 ADDR Y02080
         ST    REG6,DXCCW8                                       Y02080
         MVI   DXCCW8,X'0D'             RESTORE COMMAND FOR WRITE
         BAL   RTNREG2,EXEC             GO TO WRITE FORMAT 3     Y02080
         MVC   DXCCW8+1(3),DXCCW5+1          RESTORE F1 ADDRESS.
         B     WRITEF1
*
* BUILD A FORMAT 1 FROM THE JFCB FOR A NEW VOLUME
*
BUILDF1  MVC   DS1DSNAM(44),JFCBDSNM    DATA SET NAME
         MVI   DS1FMTID,X'F1'           FORMAT ID
         MVC   DS1DSSN(6),JFCBVOLS      VOLUME SERIAL NUMBER
         MVC   DS1VOLSQ,DXVOLSEQ        MOVE IN NEW VOLUME SEQ   Y02080
*                                       NUMBER SET UP BY EOV     Y02080
         MVC   DS1CREDT(6),JFCBCRDT     CREATION AND EXPIRATION DATES
         STC   RD,ESWITCH               NBR OF DADSMTBL ENTRIES  Y02080
         MVC   DS1SYSCD(13),SYSCODE     MOVE IN SYSTEM CODE      M0184
         MVC   DS1DSORG(8),JFCDSORG     SET UP DATA SET ORGANIZATION
*                                       RECORD FORMAT
*                                       OPTION CODE
*                                       BLOCK LENGTH
*                                       RECORD LENGTH
         NI    DS1DSORG+K1,X'FF'-RESBITS CLEAR THE RESERVED BITS Y02082
         MVC   DS1KEYL(1),JFCKEYLE
         MVC   DS1RKP(2),JFCRKP         RELATIVE KEY POSITION
         MVC   DS1SCALO(4),JFCBCTRI     FLAG FIELD AND SECONDARY QTY
         MVC   DS1LSTAR(5),TTRLL
         L     RA,DXDSAB                FETCH DSAB ADDRESS       Y02083
         USING DSAB,RA                  DSAB ADDRESSABILITY      Y02083
         TM    DSABFLG4,DSABCKDS        IS THIS A CHKPT D/S      Y02083
         DROP  RA                                                Y02083
         BZ    NOCHKPT                  IF NOT, BRANCH           Y02083
         OI    DS1DSIND,DS1CKDS         SET CHKPT D/S FLAG       Y02083
NOCHKPT  EQU   *                        BRANCH LABEL             Y02083
         TM    JFCBIND2,JFCBSCTY        DOES JFCB SPEC SECURITY  S20016
         BZ    NOSECUR                  BR IF NOT SECURITY
         OI    DS1DSIND,X'10'           SET SECURITY INDICATOR IN F1
         TM    JFCBIND2,JFCBRWPW        DOES JFCB SPEC RWPW OPTN S20016
         BNO   NOSECUR                  BRANCH IF NO             S20016
         OI    DS1DSIND,RDWOPSWD        IND SECTY IS FOR WR ONLY S20016
NOSECUR  EQU   *                        BRANCH LABEL             Y02080
*   DEFINE NEW VOLUME TO ONLY IF OLD VOLUME WAS                @Z40RSGD
*   DEFINED.                                                   @Z40RSGD
         LR    RA,RDEB                  DEB PREFIX             @Z40RSGD
         SH    RA,LENDEBPX                                     @Z40RSGD
         USING DEBPREFX,RA                                     @Z40RSGD
         L     RA,DEBXTNP               DEB EXTENSION          @Z40RSGD
         USING DEBXTN,RA                                       @Z40RSGD
         TM    DEBXFLG1,DEBXDSSI        IS IT PROTECTED        @Z40RSGD
         BZ    RACDONE                  NO-NOT RAC DEFINED     @Z40RSGD
*   DEFINE NEW VOLUME                                          @Z40RSGD
         MVC   RACDEF(LRACDEF),MRACDEF  RACDEF PARM LIST       @Z40RSGD
         L     RF,DXUCBADR              UCB ADDRESSABILITY     @Z40RSGD
         RACDEF ENTITY=DS1DSNAM,                               @Z40RSGDX
               VOLSER=UCBVOLI-UCB(RF),                         @Z40RSGDX
               OLDVOL=DXOLDVOL,                                @G32DSMIX
               MF=(E,RACDEF)                                   @ZR0RSGD
         LTR   RF,RF                    RACDEF SUCCESSFUL      @Z40RSGD
         BZ    RACDEFOK                 YES-CONTINUE           @Z40RSGD
         OI    DXEXTSW,RACERROR         NO-INDICATE ERROR      @Z40RSGD
         B     RETURN                   EXIT                   @Z40RSGD
RACDEFOK OI    DS1DSIND,DS1RACDF        INDICATE DATA SET IS   @Z40RSGD
*                                       DEFINED                @Z40RSGD
RACDONE  EQU   *                        PROCESSING DONE        @Z40RSGD
*                                       SUCCESSFULLY           @Z40RSGD
         SR    REG5,REG5                1ST SEQ NUMBER IS ZERO   Y02080
         LA    RA,DS1EXT1               SET UP DSCB 1 ADDRESS    Y02080
         B     STARTIT
*
* THIS ROUTINE LINKS TO THE RESIDENT CONVERT ROUTINE-RTA/CCHH
*
RTACONVT EQU   *                        BRANCH LABEL             Y02080
         STM   REG9,RD,IECREGSV+K12     SAVE REGISTERS           Y02080
         SLL   REG0,16                  SHIFT RTA                Y02080
         LA    REG1,DXDEB               LOAD DEB ADDRESS         Y02080
         LA    REG2,DXCCW12             POINT TO RESULT AREA     Y02080
         L     RCVT,CVTPTR              LOAD CVT ADDRESS         Y02080
         L     RCVT,CVTPCNVT            LOAD CONVERT RTN ADDR    Y02080
         LR    ALTWKA,EXWKA             SAVE EXTEND WKA ADDRESS  Y02080
         BALR  RTNREG,RCVT              LINK TO CONVERSION RTN   Y02080
         LM    REG9,RD,IECREGSV+K12-EXTNDWKA(ALTWKA)             Y02080
         BR    RTNREG2                  RETURN                   Y02080
*
* THIS SECTION EXECUTES A CHANNEL PROGRAM.
*
EXEC     EQU   *                                                 A40437
         MVI   DXECB,X'00'              CLEAR ECB                A40437
         EXCP  DXIOB                                             A40437
         WAIT  1,ECB=DXECB                                       A40437
         TM    DXECB,X'20'              TEST FOR I/O ERROR       A40437
         BCR   1,RTNREG2                RETURN IF NO ERROR       Y02080
*
* THIS SECTION BRANCHES TO THE LAST LOAD.
*
RETURN   EQU   *                        BRANCH LABEL             Y02080
         SR    RD,RD                                             Y02080
         IC    RD,DXEXTSW               PICK UP CALLER CODE      Y02080
         LA    REG6,LASTLOAD            POINT TO IGG0553E        Y02080
         B     XCTLHERE                 GO XCTL                  M0130
*
* CONSTANTS
*
LENDEBPX DC    AL2(DEBBASIC-DEBPREFX)   LENGTH OF DEB PREFX    @Z40RSGD
THREE    DC    H'3'
SEVEN    DC    H'7'
TEN      DC    H'10'
HEX3     DC    X'03030303'
MRACDEF  RACDEF TYPE=ADDVOL,            RACDEF MASTER          @Z40RSGDX
               DSTYPE=N,                PARAMETER LIST         @Z40RSGDX
               MF=L                                            @Z40RSGD
LRACDEF  EQU   *-MRACDEF                LENGTH PARM LIST       @Z40RSGD
SYSCODE  DC    CL13'IBMOSVS2'           SYSTEM CODE             ZA00704
*
* TABLE OF MODULE IDS AND ENTRY POINT ADDRESSES
*
         XCTLTABL ID=(F5LOAD,3D,LASTLOAD,3E),SVC=055,            Y02080X
               LENGTH=,BRT=YES                                   Y02080
         SPACE 2                                                 Y02080
         IECDSECS CVT,DSAB,IEZDEB,PSA,SCVT,UCB,TCB,EXPAND=YES  @Z40RSGD
WORKAREA IECEXTWA EP,D1=(1),D2=(3),ADT=YES  EXTEND WORK AREA     Y02080
RACDEF   EQU   IECREGSV                 RACDEF PARM LIST       @Z40RSGD
COUNT    EQU   ECOUNT                   EQUATE FOR ECOUNT        Y02080
UHOLECTR EQU   EHOLECTR                 EQUATE FOR EHOLECTR      Y02080
VTOCADR  EQU   EVTOCADR                 EQUATE FOR EVTOCADR      Y02080
LITTLE1  EQU   EPDLIST1                 EQUATE FOR EPDLIST1      Y02080
IDAREA   EQU   EIDAREA                  EQUATE FOR EIDAREA       Y02080
TTRLL    EQU   ETTRLL                   EQUATE FOR ETTRLL        Y02080
         EJECT                                                   Y02080
EOVWKA   DSECT                          EOV WORK AREA            Y02080
         IECDSECT
         EJECT                                                   Y02080
         ORG   DXLBL
         IECSDSL1 (4)
*
SEEK     EQU   DXDAADDR
         END   IGG0553C                                          A40437
