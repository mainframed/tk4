         TITLE 'IGG0325W - FORMAT 6 DSCB CONVERSION'             Y02080
IGG0325W CSECT
*
* MODULE NAME = IGG0325W
*
* DESCRIPTIVE NAME = FORMAT 6 DSCB CONVERSION
*
* COPYRIGHT = NONE
*
* CHANGE ACTIVITY = SEE BELOW
*                                                                     *
*          VS2 RELEASE 02 DELETIONS/CHANGES                           *
*0000000300-002000,008000-012000,078000-080000,108000,112000,    Y02080
*0000116000-118000,138000,224000,278000,306000,310000-316000,    Y02080
*0000328000,329200,340000,344000-346000,418000-422000,428000-    Y02080
*0000430000,464000,468000-470000,478000-486000,500000,530000,    Y02080
*0000570000-574000,578000-582000,590000-596000,597000,598000-    Y02080
*0000630000,634000-660000,694000-770000,774000-788000,792000-    Y02080
*0000814000,818000,822000-826000,830000-956000                   Y02080
*0000052000-056000,062000-070000,076000-078000,086000-088000,    Y02078
*0000094000-098000,174000,244000-246000,254000,328600,434000     Y02078
*          RELEASE 22 DELETIONS                                       *
*          RELEASE 21 DELETIONS                                       *
*0982024000-034000,114000,400000-402000,558000,562000-566000,    M0124
*0982582000,624000-626000,644000,648000-666000,670000-692000,    M0124
*0982696000,702000-748000,752000-756000,770000,776000,780000-    M0124
*0982784000,798000                                               M0124
*0982118000,130000-132000,484000,572000-574000,608000-612000     S21040
*                                                                     *
*0000                                                            A36311
*STATUS CHANGE LEVEL 002
*                                                                     *
* FUNCTION/OPERATION  THIS MODULE ADDS THE FORMAT 6 EXTENTS, FIVE AT  *
* A TIME, TO THE DADSMTBL AND XCTL'S TO IGG0325R TO SUBTRACT THESE    *
* FORMAT 6 EXTENTS FROM THE FREE SPACE IN THE FORMAT 5 DSCB(S). WHEN  *
* ALL THE EXTENTS HAVE BEEN SUBTRACTED, THE FORMAT 6 DSCB'S ARE       *
* CONVERTED TO THEIR FINAL FORMAT AND WRITTEN INTO THE VTOC.          *
*                                                                     *
* ENTRY POINTS                                                        *
*        IGG0325W- FROM IGG0325Q TO SUBTRACT FORMAT 6 EXTENTS FROM    *
*                                FREE SPACE                           *
*                  FROM IGG0325R TO PLACE MORE ALLOCATED EXTENTS      *
*                                IN THE DADSM TABLE                   *
*                                                                     *
* INPUT  FROM IGG0325Q                                                *
*              REGISTER 13 WILL POINT TO THE ALLOCATE WORK AREA,      *
*              WHICH WILL CONTAIN THE FORMAT 4 DSCB AND THE FIRST     *
*              FORMAT 6 DSCB IN 'DSCBF5'.                             *
*                                                                     *
*        FROM IGG0325R                                                *
*              REGISTER 13 WILL POINT TO THE ALLOCATE WORK AREA,      *
*              WHICH WILL CONTAIN THE FORMAT 4 DSCB, THE FORMAT 6     *
*              DSCB CURRENTLY BEING PROCESSED IN 'DSCBF1', AND THE    *
*              FIRST FORMAT 5 DSCB IN 'DSCBF5'.  REGISTER 1 WILL BE   *
*              ZERO TO INDICATE REENTRY FROM IGG0325R.                *
*                                                                     *
* OUTPUT TO IGG0325R                                                  *
*              REGISTER 13 WILL POINT TO THE ALLOCATE WORK AREA,      *
*              WHICH WILL CONTAIN THE FORMAT 4 DSCB, THE FORMAT 6     *
*              DSCB BEING PROCESSED IN 'DSCBF1', AND THE FIRST FORMAT *
*              5 DSCB IN 'DSCBF5'.  'SEEKCYL' CONTAINS THE CCHHR OF   *
*              THE FORMAT 6 DSCB IN 'DSCBF1'.  CCW12 WILL CONTAIN A   *
*              TWO WORD PARAMETER LIST CONSISTING OF THE NUMBER       *
*              OF EXTENTS IN THE DADSMTABLE TO BE SUBTRACTED AND      *
*              THE ADDRESS OF THE FIRST EXTENT TO BE SUBTRACTED.      *
*                                                                     *
*         TO IGG0325T                                                 *
*              REGISTER 13 WILL POINT TO THE ALLOCATE WORK AREA,      *
*              WHICH WILL CONTAIN THE FORMAT 4 DSCB.  THE FORMAT 6    *
*              DSCB(S) IN THE VTOC ARE VALID.                         *
*                                                                     *
* EXTERNAL ROUTINES                                                   *
*        EXCP (0)                                                     *
*        WAIT (1)                                                     *
*                                                                     *
* EXITS NORMAL BRANCH TO IGG0325R TO SUBTRACT F6 EXTENTS FROM THE     *
*                               FREE SPACE IN THE FORMAT 5 DSCB(S)    *
*              BRANCH TO IGG0325T TO CONVERT F5 TO CORRECT FORMAT     *
*       ERROR  BRANCH TO IGG0325T                                     *
*                                                                     *
*       ERROR CODES ISSUED BY THIS MODULE -                           *
*                                                                     *
*       0C - PERMANENT I/O ERROR                                      *
*                                                                     *
* TABLES/WORKAREAS  DADSM WORKAREA                                    *
*                                                                     *
* ATTRIBUTES  REENTRANT                                               *
*                                                                     *
* NOTES - THIS MODULE WAS SPLIT FROM IGG0325Q IN RELEASE 20.          *
*                                                                     *
*                                                                     *
REGZERO  EQU   0                        WORK REGISTER
REGONE   EQU   1                        WORK REGISTER
PARMREG  EQU   1                        PTR TO PARAMETER LIST
REENTRY  EQU   1                        REENTRY FROM IGG0325R
REGTWO   EQU   2                        WORK REGISTER
REGTHREE EQU   3                        WORK REGISTER
DADSMCTR EQU   4                        NO OF DADSMTBL ENTRIES
DADSMPTR EQU   5                        PTR TO DADSMTBL ENTRY
NEXTPTR  EQU   6                        PTR TO NEXT F6 EXTENT
REGSIX   EQU   6                        WORK REGISTER
F6EXTCTR EQU   7                        CTR OF NO OF EXTENTS IN F6
RERRPASS EQU   8                        ERROR CODE REGISTER
F6PTR    EQU   8                        ADDR OF EXTENT IN F6
RTNREG2  EQU   9                        BRANCH REGISTER
REGTEN   EQU   10                       WORK REGISTER
RVTOCWKA EQU   11                       VTOC WORK AREA BASE REG  Y02078
BASEREG  EQU   12                       BASE REGISTER
RWKAREA  EQU   13                       WORK AREA BASE REGISTER
RETRNREG EQU   14                       BRANCH REGISTER
RCVT     EQU   15                       CVT BASE REGISTER
*                                                                     *
* OTHER EQUATES                                                       *
*                                                                     *
ZERO     EQU   0                        VALUE ZERO
ONE      EQU   1                        VALUE ONE
TWO      EQU   2                        VALUE TWO
THREE    EQU   3                        VALUE THREE
K4       EQU   4                        CONSTANT OF 4            Y02080
FOUR     EQU   4                        VALUE FOUR
FIVE     EQU   5                        VALUE FIVE
SIX      EQU   6                        VALUE SIX
SEVEN    EQU   7                        VALUE SEVEN
EIGHT    EQU   8                        VALUE EIGHT              A36311
C26      EQU   26                       VALUE 26
C32      EQU   32                       VALUE 32
C90      EQU   90                       VALUE 90
NOCC     EQU   X'00'                    NO COMMAND CHAINING      Y02080
HEX01    EQU   X'01'                    VALUE X'01'
GOODIO   EQU   X'20'                    TEST VALUE FOR SUCCESSFUL I/O
ERRCD    EQU   X'0C'                    ERROR CODE FOR UNSUCCESSFUL I/O
F6ID     EQU   X'F6'                    F6 ID
F6INDIC  EQU   X'06'                    F6 DSCB INDICATOR
READCOMD EQU   X'0E'                    READ COMMAND CODE
*                                                                     *
         BALR  BASEREG,ZERO             INITIALIZE BASE REGISTER
         USING *,BASEREG
         USING ALLOCWKA,RWKAREA         BASE FOR ALLOCATE W/A    Y02080
         USING CVT,RCVT                 BASE FOR CVT
*
         LTR   REENTRY,REENTRY          IS THIS REENTRY
         BZ    RETURNPT                 BRANCH IF YES
*
* THIS SECTION BUILDS THE DADSMTBL, FIVE ENTRIES AT A TIME, AND
* THEN GOES TO THE SUBTRACT ROUTINE TO SUBTRACT THESE EXTENTS
* FROM THE F5 DSCB(S).
*
BUILDTBL EQU   *
         MVC   DADSMADR(FIVE),DS4F6PTR  INIT PTR TO PRESENT F6
         MVC   SEEKCYL(FIVE),DS4F6PTR   MOVE ADDR FIRST F6 TO
*                                       CHANNEL PROGRAM
         MVC   DSCBF1,DSCBF5            MOVE FIRST F6 DSCB       Y02078
         LR    REGTWO,RWKAREA           LOAD WORK AREA ADDRESS   Y02078
         LA    REGONE,IECEXTPR-IECPREFX  LENGTH OF PREFIX        Y02078
         SR    REGTWO,REGONE            POINT TO PREFIX          Y02078
         USING IECPREFX,REGTWO          ADDRESSABILITY TO PREFIX Y02078
LOADADDR EQU   *                        BRANCH LABEL             Y02078
         L     RVTOCWKA,IECCORP2        LOAD NEXT PREFIX ADDRESS Y02078
         LR    REGTWO,RVTOCWKA          PREPARE TO TEST PREFIX   Y02078
         CLC   IECCORID,VCWKAID         TEST IF VTOC WORK AREA   Y02078
         BNE   LOADADDR                 IF NOT, LOAD NEXT ADDR   Y02078
         LA    RVTOCWKA,IECSTART-IECPREFX(RVTOCWKA)  INCREMENT   Y02078
*                                       TO WORK AREA BEGINNING   Y02078
         USING VTOCWKA,RVTOCWKA         VTOC W/A ADDRESSABILITY  Y02078
         MVC   DSCBF5,VTC1STF5          MOVE IN FIRST F5 DSCB    Y02078
         LA    DADSMCTR,FIVE            INITIALIZE NUMBER OF
*                                       ENTRIES IN DADSMTBL
         LA    DADSMPTR,DADSMTBL        POINT TO DADSMTBL
         LA    F6EXTCTR,C26             MAX NO EXTENTS IN F6
         LA    F6PTR,DS6AVEXT           POINT TO FIRST EXTENT
         LR    NEXTPTR,F6PTR
         LA    NEXTPTR,FIVE(NEXTPTR)    POINT TO NEXT EXTENT
         XC    DADSMTBL,DADSMTBL        ZERO DADSMTBL
ADDENTRY EQU   *
         MVC   ZERO(FOUR,DADSMPTR),ZERO(F6PTR) MOVE F6 EXTENT
*                                       INTO DADSMTBL
         STM   NEXTPTR,F6EXTCTR,CCW11   SAVE PTR TO NEXT EXTENT  Y02080
         BCT   DADSMCTR,MOVEAGIN        BRANCH IF DADSMTBL NOT FULL
*
* THIS ROUTINE PREPARES TO XCTL TO IGG0325R TO SUBTRACT THE
* FORMAT 6 EXTENTS FROM THE FREE SPACE IN THE FORMAT 5 DSCB'S.
*
SUBTRACT EQU   *
         LA    REGTWO,FIVE
         SR    REGTWO,DADSMCTR          GET NO OF EXTENTS IN DADSMTBL
         ST    REGTWO,CCW12             PUT NO OF EXTENTS TO BE
*                                       SUBTRACTED IN LIST
         LA    DADSMPTR,DADSMTBL        PUT ADDR OF NEXT EXTENT
*                                       TO BE SUBTRACTED IN
*                                       PARAMETER LIST
         ST    DADSMPTR,CCW12+K4                                 Y02080
         MVI   CCW12,HEX01              SHOW ENTRY FROM IGG0325W
         LA    REGTWO,MOD325R           POINT TO ID OF IGG0325R  Y02080
         B     XCTLHERE                 GO XCTL
*
* THIS SECTION RECEIVES CONTROL AFTER REENTRY FROM IGG0325R.
*
RETURNPT EQU   *
         LM    NEXTPTR,F6EXTCTR,CCW11   RESTORE NEXT EXTENT PTR  Y02080
         LA    DADSMCTR,FIVE            NO OF ENTRIES IN DADSMTBL
         LA    DADSMPTR,DADSMTBL-FOUR   POINT TO DADSMTBL
         XC    DADSMTBL,DADSMTBL        ZERO DADSMTBL
MOVEAGIN EQU   *
         BCT   F6EXTCTR,TESTUSED        ARE THERE MORE F6 XTENTS
         CLI   DS6PTRDS+FOUR,ZERO       IS THIS THE LAST F6      Y02080
         BE    FINISHED                 BRANCH IF YES
         MVC   DADSMADR,DS6PTRDS        UPDATE PTR TO PRESENT F6 Y02080
         MVC   SEEKCYL(FIVE),DS6PTRDS   MOVE ADDR OF NEW F6 TO   Y02080
*                                       CHANNEL PROGRAM
         BAL   RTNREG2,READTOF1         GO READ THIS F6
         LA    F6EXTCTR,C26             MAX NO EXTENTS IN F6
         LA    NEXTPTR,DS6AVEXT         POINT TO FIRST EXTENT
         B     CHNGPTR                  GO UPDATE POINTERS
TESTUSED EQU   *
         CLC   ZERO(FOUR,NEXTPTR),ZEROS IS NEW EXTENT UNUSED
         BNE   CHNGPTR                  BRANCH IF NO
FINISHED EQU   *
         LA    REGTWO,FIVE
         SR    REGTWO,DADSMCTR          GET NO OF EXTENTS IN DADSMTBL
         LTR   REGTWO,REGTWO            ARE EXTENTS TO BE SUBTRACTED
         BE    RD1STF6                  BRANCH IF NO
         B     SUBTRACT                 GO SUBTRACT THE EXTENTS
CHNGPTR  EQU   *
         LA    DADSMPTR,FOUR(DADSMPTR)  POINT TO NEXT FREE SPACE
*                                       IN DADSMTBL
         LR    F6PTR,NEXTPTR
         LA    NEXTPTR,FIVE(NEXTPTR)    POINT TO NEXT F6 EXTENT
         B     ADDENTRY                 GO ADD ENTRY TO DADSMTBL
RD1STF6  EQU   *
         CLC   DADSMADR,DS4F6PTR        TEST IF FIRST FORMAT 6   Y02078
*                                       IS CURRENTLY IN 'DSCBF1' Y02078
         BNE   READF6                   IF NO, GO READ 1ST F6    Y02078
         MVC   DSCBF5,DSCBF1            MOVE IN FIRST F6 DSCB    Y02078
         B     FINISHF6                 GO CONVERT THE FIRST F6  Y02078
READF6   EQU   *                        BRANCH LABEL             Y02078
         MVC   SEEKCYL(FIVE),DS4F6PTR   MOVE ADDR FIRST F6
*                                       INTO CHANNEL PROGRAM
         MVC   DADSMADR(FIVE),DS4F6PTR  INIT CURRENT F6 POINTER
         BAL   RTNREG2,READTOF5         READ FIRST F6
*
* AFTER ALL THE FORMAT 6 EXTENTS HAVE BEEN SUBTRACTED FROM THE FREE
* SPACE IN THE FORMAT 5 DSCB(S), THIS SECTION CONVERTS THE EXTENTS
* FROM THE RTA/RTA+1/DATA SET COUNT FORMAT TO THEIR FINAL FORMAT
* (RTA/NUMBER OF SHARED SYLINDERS/DATA SET COUNT) AND INSERTS THE
* F6 ID AFTER MOVING THE LAST 90 BYTES ONE BYTE TO THE RIGHT.
*
FINISHF6 EQU   *
         LA    F6PTR,DS5AVEXT           POINT TO FIRST F6 EXTENT
         LA    REGSIX,C26               GET MAX NO OF EXTENTS IN F6
         LH    REGONE,DS4TKCYL          GET NUMBER TRKS/CYL
CONVNXT  EQU   *
         MVC   PDLIST5(FOUR),ZERO(F6PTR)  MOVE TO FULLWORD       Y02080
         LH    REGTHREE,PDLIST5+TWO     GET RTA+1                Y02080
         SH    REGTHREE,PDLIST5         SUBTRACT RTA FROM RTA+1  Y02080
         SR    REGTWO,REGTWO            ZERO REGISTER
         DR    REGTWO,REGONE            DIVIDE TRKS BY TRKS/CYL
         STH   REGTHREE,PDLIST5+TWO                              Y02080
         MVC   TWO(TWO,F6PTR),PDLIST5+TWO  STORE NUMBER OF CYLS  Y02080
         LA    F6PTR,FIVE(F6PTR)        UPDATE EXTENT POINTER
         CLC   ZERO(TWO,F6PTR),ZEROS    TEST IF EXTENT UNUSED    Y02078
         BE    BGINMOVE                 BRANCH IF YES
         BCT   REGSIX,CONVNXT           BRANCH IF MORE EXTENTS TO BE
*                                       CONVERTED
BGINMOVE EQU   *
         LA    REGTWO,C90               SET UP LOOP CONTROL
         LA    REGONE,F5EXT16B          POINT TO LAST FIELD
MOVE     EQU   *
         MVC   ONE(ONE,REGONE),ZERO(REGONE) MOVE FIELDS TO PROPER PLACE
         BCTR  REGONE,ZERO              DECREMENT ADDRESS
         BCT   REGTWO,MOVE              BRANCH IF MORE FIELDS TO MOVE
         MVI   DS5FMTID,F6ID            MOVE IN F6 IDENTIFIER
         MVI   DSCBF5,F6INDIC           MOVE IN F6 INDICATOR
         MVC   DS5KID1(THREE),DSCBF5    PROPAGATE X'06' FIELD
         BAL   RTNREG2,WRITEF6          WRITE F6
         LH    REGONE,DS4DSREC          LOAD NUMBER OF F0 DSCB'S Y02078
         BCTR  REGONE,ZERO              DECREMENT BY ONE         Y02078
         STH   REGONE,DS4DSREC          SAVE NEW NUMBER OF F0'S  Y02078
         CLI   DS5PTRDS+FOUR,ZERO       IS THIS THE LAST F6      Y02080
         BE    XCTL325T                 BRANCH IF YES
         MVC   DADSMADR,DS5PTRDS        UPDATE PRESENT F6 PTR    Y02080
         MVC   SEEKCYL(FIVE),DS5PTRDS   MOVE ADDR OF NEXT F6 TO  Y02080
*                                       CHANNEL PROGRAM
         BAL   RTNREG2,READTOF5         READ NEXT F6
         B     FINISHF6                 GO TO CONVERT NEXT F6
*
* WRITE A FORMAT 6 DSCB.
*
WRITEF6  EQU   *
         LA    REGONE,CCW1
         ST    REGONE,CHNPGADR          GET ADDR WRITE CHANNEL PROGRAM
         MVI   CCW3+K4,NOCC             UNCHAIN READ FROM RD CNT Y02080
         MVC   SEEKCYL(FIVE),DADSMADR   MOVE IN ADDR OF F6
         MVI   DSCBF5,F6INDIC           PUT F6 INDICATOR IN RECORD
         BAL   RETRNREG,EXCHANPG        WRITE F6
         BR    RTNREG2                  RETURN
*
* THIS ROUTINE READS A F6 DSCB INTO EITHER THE F1 OR F5 AREA.
*
READTOF5 EQU   *
         LA    REGTHREE,DSCBF5          GET ADDR OF F5 AREA
         B     FURTHER                  SKIP NEXT INSTRUCTION
READTOF1 EQU   *
         LA    REGTHREE,DSCBF1          GET ADDR OF F1 AREA
FURTHER  EQU   *
         ST    REGTHREE,CCW6            STORE ADDR OF AREA RECEIVING F6
         MVI   CCW6+K4,NOCC             DON'T READ COUNT         Y02080
         LA    REGTHREE,CCW4
         ST    REGTHREE,CHNPGADR        SET CHANNEL PROGRAM TO READ
         MVI   CCW6,READCOMD            MOVE IN READ CODE
         BAL   RETRNREG,EXCHANPG        EXECUTE CHANNEL PROGRAM
         BR    RTNREG2                  RETURN
*
* THIS ROUTINE EXECUTES THE CHANNEL PROGRAM.
*
EXCHANPG EQU   *
         MVI   ECB,ZERO
         EXCP  IOB
         WAIT  1,ECB=ECB
         TM    ECB,GOODIO               WAS I/O SUCCESSFUL
         BCR   1,RETRNREG               RETURN IF I/O SUCESSFUL  M0124
         LA    RERRPASS,ERRCD           SET ERROR RETURN CODE
         B     ERREXIT                  BRANCH TO IGG0325T       Y02080
XCTL325T EQU   *                        BRANCH LABEL             Y02080
         XR    RERRPASS,RERRPASS        CLEAR ERROR PASS REG     Y02080
ERREXIT  EQU   *
         LA    REGTWO,LASTLOAD          POINT TO ID OF IGG0325T  Y02080
XCTLHERE EQU   *
        IECRES LOAD,EXTPR=(RWKAREA),MODID=(REGTWO),BRANCH=DIRECT Y02080
*
* CONSTANTS
*
VCWKAID  DC    CL4'VCWA'                VTOC WORK AREA PREFIX ID Y02078
*
* TABLE OF MODULE IDS AND ENTRY POINT ADDRESSES
*
         XCTLTABL ID=(MOD325R,5R,LASTLOAD,5T),SVC=032,BRT=YES,   Y02080X
               LENGTH=                                           Y02080
         EJECT                                                   Y02080
CVT      DSECT
         CVT                                                     Y02080
         EJECT                                                   Y02080
DSCBWKAR IECALLWA EP,F4,D1=(6),D2=(5)   ALLOCATE WORK AREA       Y02080
DS4TKCYL EQU   DS4DEVSZ+2
DS5KID1  EQU   DSCBF5+1
F5EXT16B EQU   DSCBF5+133               END OF EXTENT 16 IN F5 DSCB
CHNPGADR EQU   IOB+16                   ADDRESS CHANNEL PROGRAM IN IOB
SEEKCYL  EQU   SEEK+3
ZEROS    EQU   CCW2+4                   FULL WORD OF ZERO
         EJECT                                                   Y02078
VWKAREA  IECVTCWA                       CONVERSION WORK AREA     Y02078
         END
