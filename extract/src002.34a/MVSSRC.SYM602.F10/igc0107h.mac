         TITLE     'IGC0107H  -  DADSM  -  LSPACE 2ND LOAD'
IGC0107H CSECT
*          RELEASE 20 DELETIONS
*0364                                                            A35588
*0364012000-022000                                               20201
*          RELEASE 21 DELETIONS
*1195                                                            A38860
*1195032000-044000,060000-064000,108000,284000,456000,474000-    A40793
*1195478000,624000                                               A40793
*1195120000,214500-222000,520000,524000,532000,584200-585600,    A42182
*1195618000,650000-652000,656000,720000-726000,742000-748000     A42182
*0000572000                                                     SA49351
*          RELEASE 22 DELETIONS
*          VS2 RELEASE 02 DELETIONS/CHANGES
*0000002100-002800,041000-042000,082000-084000,090000-094000,    Y02080
*0000106000,113000,134000,152000,162000,184000,198000,204000,    Y02080
*0000210100-210800,211200,211400-211500,242000,246000,280100-    Y02080
*0000281500,288000,358000,364000-366000,376400-377200,379200,    Y02080
*0000571200-572000,576000,580000,604000-608000,620000-622000,    Y02080
*0000626000,638000-666000,698000-760000,764000-774000            Y02080
*0000                                                            Y02130
*0000035000-037000,210850-210860,378200-379000,379400            Y02145
*0000142000-144000,556000,564000,582000                          Y02078
*0000                                                            Y02144
*          VS2 RELEASE 037 CHANGES
*A210847-210861,379200-379900,456000-4576400                   @ZA16899
*C457200                                                       @ZA16899
*0000210861,379500                                             @ZA30235
* MODULE NAME - IGC0107H
*
* DESCRIPTIVE NAME -  DADSM  -  LSPACE 2ND LOAD
*
* COPYRIGHT - NONE
*
* CHANGE ACTIVITY - SEE DELETIONS/CHANGES FOLLOWING CSECT CARD
*
* STATUS CHANGE LEVEL 003
*
* FUNCTION/OPERATION
*    IGC0107H READS IN THE FORMAT 4 DSCB AND THE FIRST FORMAT 5
*    DSCB. AS IT READS IN ANY CHAINED FORMAT 5 DSCB'S, IT TOTALS
*    THE AVAILABLE SPACE LISTED IN THEM AND SAVES THE LARGEST EXTENT.
*    IF SMF HAS BEEN SPECIFIED, THE MODULE IDENTIFICATION IS SENSED
*    AND THE VOLUME LABEL IS READ.  IGC0107H THEN BUILDS AN SMF RECORD
*    TYPE 19 AND RECORDS IT ON THE SMF DATA SET.  IF 'DISPLAY SPACE'
*    INFORMATION IS REQUIRED, THE SPACE INFORMATION COLLLECTED
*    FROM THE FORMAT 5 DSCB(S) IS CONVERTED TO EBCDIC AND PLACED
*    IN THE CALLER'S MESSAGE AREA. AFTER DEQ'ING THE VTOC AND
*    FREEING THE WORK AREA, THIS MODULE RETURNS TO THE CALLER.
*
* ENTRY POINT
*    IGC0107H - THIS MODULE IS THE SECOND LOAD OF THE LSPACE      19028
*               ROUTINE (SVC 78) AND IS ENTERED FROM MODULE       19028
*               IGC0007H.                                         19028
*
* INPUT ON ENTRY TO THIS MODULE
*    REGISTER 7 POINTS TO THE UCB.
*    REGISTER 9 POINTS TO THE 'DISPLAY SPACE' MESSAGE AREA.
*    REGISTER 13 POINTS TO THE WORKAREA.
*
* OUTPUT
*    IF THE SPACE INFORMATION WAS REQUESTED, REGISTER ONE WILL    19028
*    CONTAIN THE POINTER TO THE CALLER'S MESSAGE AREA AND THE     19028
*    MESSAGE AREA WILL CONTAIN THE INFORMATION IN EBCDIC.         19028
*    REGISTER FIFTEEN WILL CONTAIN A RETURN CODE.                 19028
*
* EXTERNAL ROUTINES
*    CVTEXPRO      - EXIT PROLOG ADDRESS
*    IECRES        - FREE THE WORK AREA
*    EXCP (0)      - READ FROM DIRECT ACCESS DEVICE
*    WAIT (1)      - WAIT ON EVENT CONTROL BLOCK
*    DEQ (48)      - DEQUEUE THE VTOC
*    TIME (11)     - GET TIME OF DAY AND DATE                     19028
*    SMFWTM (83)   - WRITE TO THE SMF DATA SET                    19028
*    IEC0SCR1      - RPS SECTOR CONVERT ROUTINE
*
* EXITS
*    RETURN IS MADE TO THE CALLER VIA IN BOTH NORMAL AND
*    ERROR EXITS. REGISTER 15 WILL CONTAIN A RETURN CODE. IF AN
*    ERROR OCCURS AND THE CALLER IS 'DISPLAY SPACE', AN ERROR
*    MESSAGE IS PLACED IN THE CALLER'S MESSAGE AREA.
*
* RETURN CODES THAT CAN BE ISSUED BY LSPACE.
*    00 - SUCCESSFUL PROCESSING
*    04 - PERMANENT I/O ERROR
*    08 - NON-STANDARD OS VOLUME
*    0C - INVALID UCB POINTER OR NOT A DIRECT ACCESS VOLUME OR
*         UCB NOT READY
*    10 - UNSUCCESSFUL VALIDITY CHECK ON CALLER'S MESSAGE         19028
*         POINTER OR INVALID SMF INDICATOR                        19028
*
* TABLES/WORK AREAS
*    LSPACE WORK AREA DESCRIBED BY MACRO IECLSPWA
*
* ATTRIBUTES  REENTRANT
*
* OTHER MACROS USED
*    IECLSPWA - DEFINE THE LSPACE WORK AREA
*    IECSDSL1 - BUILD DSCB'S
*    IECDSECS - EXPANDS THE CVT, RB, TCB, AND UCB DSECTS
*    IEFSMCA  - BUILD THE SYSTEM MANAGEMENT CONTROL AREA          19028
*    IFASMFR  - BUILD AN SMF RECORD                               19028
*
*
*
         BALR  BASEREG,0
         USING BEGIN,BASEREG
         USING LSPACWKA,RWKAREA         WORK AREA ADDRESSABILITY Y02080
         USING UCB,RUCBPTRL
         USING CVT,RCVT
*
REGZERO  EQU   0
REGONE   EQU   1
RSMCA    EQU   1
REGTWO   EQU   2
REGTHREE EQU   3
REGFOUR  EQU   4
REGFIVE  EQU   5
REGSIX   EQU   6
REGSEVEN EQU   7
RUCBPTRL EQU   7                        UCB POINTER
REGEIGHT EQU   8
REGNINE  EQU   9
REGTEN   EQU   10
BASEREG  EQU   12                       BASE REGISTER
RWKAREA  EQU   13                       WORK AREA POINTER
RBAK     EQU   14
RCVT     EQU   15
RERRPASS EQU   15                       ERROR PASS REGISTER
*
* OTHER EQUATES
*
ZERO     EQU   X'00'                    ONE BYTE OF ZERO         A40793
DA2314   EQU   X'08'                    2314 UCBTYP4           @ZA16899
DA3330   EQU   X'09'                    3330 UCBTYP4           @ZA16899
DA3330B  EQU   X'0D'                    3330-11 UCBTYP4        @ZA30235
REMOVE01 EQU   X'3F'                    REMOVE BITS 0 AND 1      A40793
CHAINING EQU   X'40'                    COMMAND CHAINING         A40793
C1       EQU   1                        ONE CHARACTER            20201
FW       EQU   4                        BYTES IN A  FULL WORD    20201
C7       EQU   7                        DISP OF SEVEN            20201
K12      EQU   12                       CONSTANT OF 12           Y02080
CCWSTART EQU   16                       IOB CCW START ADR OFFSET Y02080
RECORD3  EQU   X'03'                    RECORD THREE             Y02080
MSGL     EQU   30                       MESSAGE LENGTH OF 30     Y02078
RPSMSK   EQU   X'10'                    TESTS RPS BIT IN UCB     20201
*
BEGIN    EQU   *
*
* THIS SECTION LINKS TO IOS FOR ALL I/O OPERATIONS
*
EXECUTET EXCP  IOB
         WAIT  1,ECB=ECB
         TM    ECB,X'20'                CHECK FOR PERMANENT I/O ERROR
         BO    WHERETO                  BRANCH IF NO ERROR
         OI    SWITCH,X'04'             SET ERROR CODE
         B     SETDEQ
WHERETO  EQU   *                        WHAT ARE WE DOING        Y02080
         TM    SWITCH,RDVOLABL          WAS READ FOR VOL LABEL   Y02080
         BO    BLDSMF                   BRANCH IF YES             19028
         TM    SWITCH,RD2NDF5           IS THIS THE FIRST F4&F5  Y02080
         BC    1,ADDIN                  BRANCH IF NOT
*
* THIS SECTION TESTS FOR VTOC NOT IN EXPECTED FORM
*
         TM    DS4VTOCI,X'80'           IS THIS BOS CONTAMINATED
         BC    8,SETNXT                 BRANCH IF NOT
         OI    SWITCH,X'08'             SET ERROR CODE
         BC    15,SETDEQ                GO TO ERROR EXIT
*
* THIS SECTION RELOCATES THE CHANNEL PROGRAM TO READ IN SUBSEQUENT
* FORMAT 5 DSCB'S.
*
SETNXT   LM    REGZERO,REGFIVE,CHANPROG LOAD NEXT THREE CCW'S
         ALR   REGZERO,RWKAREA
         ALR   REGTWO,RWKAREA
         ALR   REGFOUR,RWKAREA
         STM   REGZERO,REGFIVE,CCW1     STORE THREE CCW'S
*
* TOTAL UP THE AVAILABLE SPACE GIVEN IN THE FORMAT 5 DSCB.
*
ADDIN    EQU   *                        TOTAL UP THE SPACE       Y02080
         MVC   DS5FMTID(90),DS5FMTID+1  TAKE OUT 1 BYTE FORMAT ID
         LA    REGONE,26                SET UP ENTRY COUNTER
         LA    REGTWO,DS5AVEXT          SET UP ENTRY POINTER
         LM    REGTHREE,REGFIVE,SAVETOT LOAD PREVIOUS TOTALS
ADDIN1   OC    0(5,REGTWO),0(REGTWO)
         BE    CHECKOUT
         CLC   2(3,REGTWO),TEMPCTG      COMPARE EXTENT SIZE
         BC    12,ADDIN2                BRANCH IF SMALLER
         MVC   TEMPCTG(3),2(REGTWO)     SAVE THIS ONE IF LARGER
ADDIN2   SR    REGSIX,REGSIX
         IC    REGSIX,4(REGTWO)         ADD NUMBER OF TRACKS
         AR    REGFOUR,REGSIX           IN THIS EXTENT TO TOTAL
         IC    REGSIX,2(REGTWO)
         SLL   REGSIX,8
         IC    REGSIX,3(REGTWO)
         AR    REGTHREE,REGSIX          TO TOTAL
         LA    REGFIVE,1(REGFIVE)       BUMP NUMBER OF AREAS BY ONE
         LA    REGTWO,5(REGTWO)
         BCT   REGONE,ADDIN1
CHECKOUT STM   REGTHREE,REGFIVE,SAVETOT SAVE THIS F5'S TOTALS
         OC    DS5PTRDS(5),DS5PTRDS     CHECK CHAIN ADDRESS OF F5 DSCB
         BZ    TESTSMF                                            19028
         OI    SWITCH,RD2NDF5           INDICATE READING 2ND F5  Y02080
         MVC   SEEK+3(5),DS5PTRDS       UPDATE SEEK ADDRESS
         TM    UCBTBYT2,RPSMSK          DOES DEVICE HAVE RPS     Y02080
         BZ    EXECUTET                 NO, CONTINUE AS NORMAL   Y02080
         LA    REGTWO,THETA             GET THETA ADDRESS        Y02080
         XC    CCW2+FW(FW),CCW2+FW      CLEAR WORK WORD          Y02080
         MVC   CCW2+FW(C1),UCBTBYT4     GET DEVICE CODE          Y02080
         O     REGTWO,CCW2+FW           PUT IN HIGH BYTE         Y02080
         L     REGZERO,PARM0            PREPARE PARAMETERS       Y02080
         IC    REGZERO,SEEK+C7          GET RECORD NUMBER        Y02080
         L     RCVT,CVTPTR              GET CVT ADDR             Y02080
         L     RCVT,CVT0SCR1            GET SECTOR CNVT RTN ADDR Y02080
         STM   REGNINE,RWKAREA,REGSAVEA  SAVE REGISTERS          Y02080
         LR    REGTHREE,RWKAREA         SAVE WORK AREA ADDRESS   Y02080
         BALR  RBAK,RCVT                GET SECTOR VALUE         Y02080
         LM    REGNINE,RWKAREA,REGSAVEA-FIRST(REGTHREE)  RESTORE Y02080
         BC    15,EXECUTET
*
* THIS SECTION CONVERTS BINARY TO EBCDIC.
*
CONVERT  L     REGFOUR,0(REGTWO)
         CVD  REGFOUR,CVTAREA
         UNPK  CVTAREA(4),CVTAREA+4(4)
         OI    CVTAREA+3,X'F0'
         MVC   0(4,REGTWO),CVTAREA
         LA    REGTWO,4(REGTWO)
         BCT   REGONE,CONVERT
         BR    REGTHREE
TESTSMF  TM    SMFFLAG,X'C0'            TEST FOR SMF FUNCTIONS    19028
         BZ    INITCVT                  BRANCH IF NOT REQUIRED    19028
*
* THIS SECTION BUILDS THE CCW'S TO READ THE VOLUME LABEL.
*
         OI    SWITCH,RDVOLABL          SET BIT TO READ LABEL    Y02080
         LM    REGZERO,REGSEVEN,CCWPLBL GET CCWS TO READ THE      19028
*                                       VOLUME LABEL              19028
         XC    SEEK,SEEK                ZERO THE IOB SEEK FIELD  Y02080
         MVI   SEEK+C7,RECORD3          SET R TO 3 FOR LABEL     Y02080
         ALR   REGZERO,RWKAREA          SET ADDRESS PORTION OF    19028
         ALR   REGTWO,RWKAREA           THE CCWS                  19028
         ALR   REGFOUR,RWKAREA                                    19028
         ALR   REGSIX,RWKAREA                                     19028
         STM   REGZERO,REGSEVEN,CCW1    PUT CCWS IN WORKAREA      19028
         LA    REGONE,CCW1              POINT TO CCW1            Y02080
         ST    REGONE,IOB+CCWSTART      BEGIN CHAN PROG AT CCW1  Y02080
         MVI   SMF19IND+1,ZERO          ZERO SENSE AREA          A40793
         L     RUCBPTRL,DEB+32          RESTORE UCB POINTER       19028
         CLI   UCBTYP+3,DA2314          TEST DEVICE TYPE       @ZA16899
         BE    SENSE                    BRANCH IF 2314         @ZA16899
         CLI   UCBTYP+3,DA3330B         TEST DEVICE TYPE       @ZA16899
         BE    SENSE                    BRANCH IF 3330-11      @ZA30235
         CLI   UCBTYP+3,DA3330          TEST DEVICE TYPE       @ZA16899
         BNE   NOSENSE                  BRANCH IF NOT 3330     @ZA16899
SENSE    MVI   CCW3+FW,CHAINING         SET COMMAND CHAINING   @ZA16899
NOSENSE  B     EXECUTET                                        @ZA16899
*
* THIS SECTION BUILDS AND RECORDS AN SMF RECORD TYPE 19.
*
BLDSMF   MVC   SMF19HDR(6),SMFHDR       SMF RECORD HEADER         19028
         MVC   SMF19DSR(2),DS4DSREC     NUMBER OF FMT 0 DSCB'S   M4920
         L     RCVT,CVTPTR              SET CVT BASE              19028
         L     RSMCA,CVTSMCA            GET PTR FOR SMCA          19028
         USING SMCABASE,RSMCA                                     19028
         MVC   SMF19SID(4),SMCASID      MOVE SYSTEM ID TO RECORD  19028
         DROP  RSMCA                                              19028
         TIME  BIN                      GET THE TIME AND DATE     19028
         STM   REGZERO,REGONE,TEMPAREA                            19028
         MVC   SMF19TME(8),TEMPAREA                               19028
         MVC   TEMPAREA(8),DS4VTOCE+2   CALCULATE THE NUMBER OF   19028
         LH    REGZERO,TEMPAREA         DETERMINE THE NUMBER OF   19028
         MH    REGZERO,DS4DEVSZ+2       TRACKS IN THE VTOC        19028
         AH    REGZERO,TEMPAREA+2       AND COMPUTE THE NUMBER    19028
         LH    REGONE,TEMPAREA+4        OF DSCB'S THAT WILL FIT   19028
         MH    REGONE,DS4DEVSZ+2        IN THE VTOC.              19028
         AH    REGONE,TEMPAREA+6                                  19028
         SR    REGONE,REGZERO                                     19028
         LA    REGONE,1(REGONE)                                   19028
         XR    REGZERO,REGZERO                                    19028
         IC    REGZERO,DS4DEVDT                                   19028
         MR    REGZERO,REGZERO                                    19028
         ST    REGONE,TEMPAREA                                    19028
         XC    SMF19RV1(2),SMF19RV1     ZERO RESERVED AREA        19028
         MVC   SMF19VOL(6),VOLSERNO     VOLUME SERIAL NUMBER      19028
         MVC   SMF19OID(10),VOLOWNER    VOLUME OWNER IDENT        19028
         MVC   SMF19DEV(4),UCBTYP       DEVICE TYPE               19028
         MVC   SMF19VTC(5),VOLVTOC      VOLUME VTOC ADDRESS       19028
         MVC   SMF19VTI(1),DS4VTOCI     VTOC INDICATORS           19028
         MVC   SMF19NDS(2),TEMPAREA+2   NUMBER OF DSCB'S          19028
         MVC   SMF19NAT(2),DS4NOATK     NUMBER OF ALT TRACKS      19028
         MVC   SMF19SPC(2),SAVETOT+2    UNUSED SPACE (CCTT)       19028
         MVC   SMF19SPC+2(2),SAVETOT+6                            19028
         MVC   SMF19LEX(2),TEMPCTG      SIZE OF LARGEST FREE      19028
         MVI   SMF19LEX+2,X'00'         EXTENT (CC0T)             19028
         MVC   SMF19LEX+3(1),TEMPCTG+2                            19028
         MVC   SMF19NUE(2),SAVETOT+10   NUMBER OF FREE EXTENTS    19028
         XC    SMF19RV2(5),SMF19RV2     ZERO RESERVED AREA       A40793
         CLI   UCBTYP+3,DA2314          CHECK DEVICE TYPE      @ZA16899
         BNZ   NORESET                  DO NOT RESET           @ZA16899
         NI    SMF19IND+1,REMOVE01      REMOVE UNWANTED BITS     A40793
NORESET  MVC   SMF19CUU(2),UCBCHA                              @ZA16899
         LA    REGONE,SMFRCD19                                    19028
         SVC   83                       WRITE SMF RECORD TYPE 19  19028
         LA    REGONE,0(REGNINE)        SHOULD MSG BE RETURNED    19028
         LTR   REGONE,REGONE                                      19028
         BZ    SETDEQ                   BRANCH IF NO MSG          19028
*
* THIS SECTION CONVERTS THE SPACE INFORMATION AND MOVES IT
* TO THE CALLER'S MESSAGE AREA.
*
INITCVT  LA    REGONE,3                 COUNT FOR CYLTRK AREA
         LA    REGTWO,SAVETOT           POINT TO CYLTRK AREA
         BAL   REGTHREE,CONVERT         CONVERT THEM FOR PRINTING
         L     REGONE,TCBADDR           LOAD TCB ADDRESS         Y02078
         USING TCB,REGONE               TCB ADDRESSABILITY       Y02078
         MODESET EXTKEY=RBT234,WORKREG=2  SWITCH TO CALLER'S KEY Y02078
OUT      MVC   0(6,REGNINE),SPACEMSG
         MVC   6(4,REGNINE),SAVETOT
         MVI   10(REGNINE),C','
         MVC   11(4,REGNINE),SAVETOT+4
         MVI   15(REGNINE),C','
         MVC   16(4,REGNINE),SAVETOT+8
         MVI   20(REGNINE),C'/'
         MVI   25(REGNINE),C','
         MODESET EXTKEY=DATAMGT         RETURN TO DATAMGT KEY    Y02078
         LA    REGONE,2
         LA    REGTWO,SAVETOT
         XC    SAVETOT(8),SAVETOT       ZERO AREA BEFORE INITIALIZING
         MVC   SAVETOT+2(2),TEMPCTG     PUT NUMBER OF CYL IN AREA
         MVC   SAVETOT+7(1),TEMPCTG+2   PUT NO. OF TRKS IN LARGEST XTNT
         BAL   REGTHREE,CONVERT
         L     REGONE,TCBADDR           LOAD TCB ADDRESS         Y02078
         MODESET EXTKEY=RBT234,WORKREG=2  SWITCH TO CALLER'S KEY Y02078
         MVC   21(4,REGNINE),SAVETOT    CYLINDERS IN LARGEST EXTENT
         MVC   26(4,REGNINE),SAVETOT+4  TRACKS IN LARGEST EXTENT
         MODESET EXTKEY=DATAMGT         RETURN TO DATAMGT KEY    Y02078
         DROP  REGONE                                            Y02078
*
* SET UP FOR DEQ
*
SETDEQ   MVC   MJELNAME(8),VTOCNAME
         MVI   ENQAREA,X'FF'            SET LAST ENTRY           A42182
         SR    REGONE,REGONE
         STH   REGONE,ENQAREA+2                                  A42182
*
* LINK TO DEQ ROUTINE TO DEQ THE VTOC
*
         DEQ   (MJELNAME,MIELNAME,6,SYSTEMS),MF=(E,ENQAREA)      A42182
*
         NI    DSMADTB2,X'FF'-DSMVTOCR  RESET VTOC ENQ'ED SWITCH Y02144
*
* RESTORE POINTER TO CALLER'S INFORMATION AREA, SET RETURN CODE IN
* REGISTER 15, MOVE MESSAGE TO THE INFORMATION AREA AND RETURN TO
* THE CALLER.
*
         SR    REGTHREE,REGTHREE        GET RETURN CODE
         NI    SWITCH,X'0F'             USE AS TABLE INDEX FOR
         IC    REGTHREE,SWITCH          GETTING APPROPRIATE MESSAGE
         BC    15,TABLE(REGTHREE)
TABLE    BC    15,FREE
         BC    15,ERREXIT1
ERREXIT2 EQU   *                        ERRMSG2                  Y02078
         LA    REGFOUR,ERRMSG2          POINT TO ERROR MESSAGE   Y02078
EXIT     LA    REGONE,0(REGNINE)        SHOULD MSG BE RETURNED    19028
         LTR   REGONE,REGONE                                      19028
         BZ    FREE                     BRANCH IF NO              19028
         L     REGONE,TCBADDR           LOAD TCB ADDRESS         Y02078
         USING TCB,REGONE               TCB ADDRESSABILITY       Y02078
         MODESET EXTKEY=RBT234,WORKREG=2  SWITCH TO CALLER'S KEY Y02078
         MVC   0(MSGL,REGNINE),0(REGFOUR)  MOVE ERROR MESSAGE    Y02078
*                                       TO CALLER'S AREA         Y02078
         MODESET EXTKEY=DATAMGT         RETURN TO DATAMGT KEY    Y02078
         DROP  REGONE                                            Y02078
*
* FREE THE WORK AREA
*
FREE     EQU   *                                                SA49351
         LR    REGEIGHT,REGTHREE        SAVE RETURN CODE         Y02080
         IECRES FREE,PREFIX=FIRST,A=(RWKAREA)  FREE WORK AREA    Y02080
*
         MODESET EXTKEY=SUPR            RETURN TO SUPERVISOR KEY Y02082
         LR    REGONE,REGNINE
         USING CVT,RCVT                 CVT ADDRESSABILITY       Y02080
         L     RCVT,CVTPTR              LOAD CVT ADDRESS         Y02080
         L     RBAK,CVTEXPRO            LOAD EXIT PROLOG ADDRESS Y02080
         LR    RERRPASS,REGEIGHT        LOAD RETURN CODE         Y02080
         RETURN ,                       RETURN TO CALLER         Y02080
*
ERREXIT1 EQU   *                        ERRMSG1                  Y02078
         LA    REGFOUR,ERRMSG1          POINT TO ERROR MESSAGE   Y02078
         BC    15,EXIT
*
* CHANNEL PROGRAM FOR READING SUBSEQUENT FORMAT 5 DSCB'S
*
CHANPROG CCW   X'31',DSCBF5+135-FIRST,X'40',5 SEARCH EQUAL ID FOR DSCB5
         CCW   X'08',CCW1-FIRST,X'00',0      TIC BACK TO SEARCH
         CCW   X'8E',DSCBF5-FIRST,X'00',140   READ IN KEY/DATA
*                                                                 19028
* CHANNEL PROGRAM TO READ THE VOLUME LABEL                        19028
*                                                                 19028
CCWPLBL  EQU   *                        CCW TO READ VOL LBL      Y02080
         CCW   X'31',SEEK+3-FIRST,X'40',5  SEARCH                Y02080
         CCW   X'08',CCW1-FIRST,X'00',0  TIC BACK TO SEARCH      Y02080
         CCW   X'06',VOLLABEL-FIRST,X'00',80                      19028
         CCW   X'04',SMF19RV2+1-FIRST,X'20',5  SENSE CCW         A40793
*
* CONSTANTS AND MESSAGES
*
PARM0    DS    0F                       AREA TO BUILD PARAMETERS 20201
DD       DC    X'0060'                  DATA LENGTH              20201
K        DC    X'2C'                    KEY  LENGTH              20201
R        DC    X'0'                     RECORD NUMBER            20201
SMFHDR   DC    XL6'004400000013'        SMF RECORD HEADER        A40793
VTOCNAME DC    CL8'SYSVTOC'
*
SPACEMSG DC    C'SPACE='
ERRMSG1  DC    C'LSPACE-PERMANENT I/O ERROR    '
ERRMSG2  DC    C'LSPACE-NON-STANDARD OS VOLUME '
*
* EMPTY TABLE OF MODULE IDS AND ENTRY POINT ADDRESSES
*
         XCTLTABL SVC=078,LENGTH=,BRT=YES                        Y02080
         EJECT                                                   Y02080
WORKAREA IECLSPWA EP,F4,D1=(5),ADT=YES  LSPACE WORK AREA         Y02144
REGSAVEA EQU   IECREGSV+K12             REGISTER SAVE AREA       Y02080
TEMPAREA EQU   CVTAREA                  WORK AREA                Y02080
         SPACE 1                                                 Y02080
         ORG   DSCBF5                                             19028
VOLLABEL DS    CL80                                               19028
         ORG   VOLLABEL                                           19028
VOLLABI  DS    CL3                                                19028
VOLNO    DS    CL1                                                19028
VOLSERNO DS    CL6                                                19028
         DS    CL1                                                19028
VOLVTOC  DS    CL5                                                19028
         DS    CL5                                                19028
         DS    CL20                                               19028
VOLOWNER DS    CL10                                               19028
         DS    CL29                                               19028
         EJECT                                                   Y02080
         ORG   VOLLABEL+60                                        19028
SMFRCD19 EQU   *                                                  19028
         IFASMFR   (19)                                           19028
SMF19END EQU   *                        SMF RECORD TYPE 19 END   Y02080
         SPACE 2                                                 Y02080
         IECDSECS CVT,RB,TCB,UCB,EXPAND=YES  VARIOUS DSECTS      Y02078
         IEESMCA                                                  19028
         END
