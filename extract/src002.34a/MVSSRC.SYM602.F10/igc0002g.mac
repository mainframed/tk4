 TITLE 'IGC0002G - OBTAIN - ENTRY POINT - READ DSCB'
IGC0002G CSECT
*
*          VS2 RELEASE 037 CHANGES
*A0000                                                         @ZA17992
*          VS2 RELEASE 03 DELETIONS/CHANGES
*000023200-24000                                                Z30AAJC
*0000                                                            ZM0144
*          VS1 RELEASE 02 DELETIONS
*0000                                                            X02989
*0000                                                           XA00356
*0000049797                                                      XM0876
*0000                                                            XM1039
*          VS2 RELEASE 02 DELETIONS/CHANGES
*0000018600-019600                                               YM0117
*0000000971,003000-003200,004120-004400,008600,009000,016673,    Y02080
*0000016703,016766-016797,016859,016887-016889,016977,017200-    Y02080
*0000017400,017900-018120,018260,019790-020000,020400,021250-    Y02080
*0000021321,021346-021617,021640,021761-021850,021964,022600,    Y02080
*0000024600-026600,028260,031000,032600,033860,035400-036400,    Y02080
*0000037000,037600,045500,045600-046200,049700,049744-049900,    Y02080
*0000053730-053750,053770-053800,058800,060250-060340,060584-    Y02080
*0000060592,062000,063000,073420-080700                          Y02080
*0000000030-000150,005200,007000,008800,016885,017135,039400-    Y02078
*0000039600,042000-042200,043000,043400-043600,045400,046480,    Y02078
*0000046600,047800-048000,048400,055600,056200,056600-057200,    Y02078
*0000058400,063600-063800,064800,068400,069000-069200,070200,    Y02078
*0000072800-073000                                               Y02078
*0000                                                            Y02132
*0000                                                            Y02082
*0000021480                                                      YM1312
*0000005000-005200,050540-050700,060290-060300                   YM3996
*0000060260                                                      YM5902
*0000051420,053020,053140,053340                                 YM5378
*          RELEASE 22 DELETIONS
*          RELEASE 21 DELETIONS
*1195008600,009000,010480,016200-016400,038400,043600            A38860
*1195016010-016020                                               A37199
*1195037000,037600                                               A44463
*0000018400,020200,020600                                        A42200
*0000019800,045800                                              SA49351
*          RELEASE 21.7 DELETIONS
*0000011100-016270,016704-016750,016770-016794,016800-016811,   SA56452
*0000016874,016937,017000,017070,017800,020600-021000,021200,   SA56452
*0000021400,021600,021970-021998,022100,050000-053400,053691-   SA56452
*0000053709,053714-053719,080700                                SA56452
*0000043600                                                     SA56392
*0000                                                           SA56450
*0000042800,066400                                              SA48172
*          RELEASE 20 DELETIONS
*3510000250-000420,000500-000650,053800,079400                   S20201
*3510                                                            A34440
*3510051200-051780,051790                                        M0696
*          RELEASE 19 DELETIONS
*2741024440-024560                                               A25499
*
*MODULE NAME - IGC0002G
*
*DESCRIPTIVE NAME - OBTAIN - ENTRY POINT - READ DSCB
*
*COPYRIGHT - NONE
*
*CHANGE ACTIVITY - SEE DELETIONS/CHANGES FOLLOWING CSECT CARD
*
*STATUS CHANGE LEVEL 005
*
*ATTRIBUTES - REENTRANT
*
*FUNCTION - THIS MODULE IS THE ENTRY POINT TO OBTAIN. THIS MODULE HAS
*          TWO OPTIONS. THE SEARCH OPTION SEARCHES THE VTOC FOR THE
*          NAMED DSCB AND THE SEEK OPTION SEEKS TO THE APPROPRIATE
*          TRACK AND SEARCHES THAT TRACK FOR THE GIVEN CCHHR. THE
*          DATA PORTION OF THE DSCB IS READ FOR THE SEARCH OPTION;
*          THE KEY AND DATA PORTIONS ARE READ FOR THE SEEK OPTION.
*          FOR THE SEARCH OPTION WITH A VIO DATA SET, THE DATA
*          PORTION OF THE FORMAT 1 DSCB IS MOVED FROM THE VDSDSCB
*          INTO THE USER'S WORK AREA, AND THE CCHHR IS SET TO ZERO.
*          VSAM SUPPORT REQUIRES THAT IF A DSCB IS NOT FOUND IN  X02989
*          A SEARCH OPERATION AND IF VSAM IS ON THE VOLUME,      X02989
*          CONTROL IS TRANSFERRED TO MODULE IGC0102G TO TRY TO   X02989
*          FIND THE DATA SET WITHIN A VSAM DATA SPACE.OTHERWISE, X02989
*          A NORMAL ERROR EXIT IS EFFECTED.                      X02989
*
*ENTRIES - THE ONLY ENTRY POINT TO THIS MODULE IS IGC0002G. ENTRY IS
*          MADE UPON SVC 27 FROM ANY SYSTEM OR PROBLEM PROGRAM.
*
*SUPERVISOR CALLS AND MACROS. THE FOLLOWING ARE SUPERVISOR CALLS
*          ISSUED IN THE MACRO EXPANSIONS:
*                    EXCP(0) READ FROM DIRECT ACCESS DEVICE
*                    WAIT(1) WAIT ON EVENT CONTROL BLOCK
*                    ENQ(56) ENQ ON SYSZTIOT
*                    DEQ(48) DEQ FROM SYSZTIOT
*
*OTHER MACROS USED:
*          IECOBTWA - OBTAIN WORK AREA EXPANSION
*          IECSDSL1 - ENABLES SYMBOLIC ADDRESSING OF DSCBS
*          IDDVDSCB - EXPANDS THE VDSCB
*          IECDSECS - EXPANDS THE ASCB, CVT, DSAB, JSCB, PSA, QDB, RB,
*                     RRPL, TCB, TIOT, AND UCB DSECTS
*
*INPUT -   AT ENTRY TO THIS MODULE REG 1 POINTS TO THE PARAMETER LIST.
*          THE PARAMETER LIST CONSISTS OF FOUR WORDS:
*                    WORD 1 - SEARCH CODE = X'C1000000'
*                             SEEK CODE = X'C0800000'
*                    WORD 2 - SEARCH = ADDR OF DATA SET NAME
*                             SEEK = ADDR OF CCHHR OF DSCB
*                    WORD 3 - ADDR OF VOLUME SERIAL NUMBER
*                    WORD 4 - ADDR OF 140 BYTE WORK AREA.
*
*OUTPUT -  THE DSCB IS RETURNED IN THE GIVEN WORK AREA. IF THE OPTION
*          WAS SEARCH, THE DSCB WILL OCCUPY THE FIRST 96 BYTES OF THE
*          WORK AREA, AND THE CCHHR OF THE DSCB WILL BE THE NEXT 5
*          BYTES. IF THE OPTION WAS SEEK, THE DSCB WILL OCCUPY THE
*          FIRST 140 BYTES OF THE WORK AREA.
*          WHEN CONTROL IS PASSED TO IGC0102G, REGISTER 10 POINTS
*          TO THE PARAMETER LIST, AND REGISTER 13 POINTS TO THE
*          OBTAIN WORK AREA.
*
*STORAGE - PROGRAM CODE CSECT = LESS THAN 2048 BYTES
*          USER WORK AREA = 140 BYTES
*          OBTAIN WORK AREA = 580 BYTES
*
*ERROR CONDITIONS - UPON EXIT REGISTER 15 WILL CONTAIN THE COMPLETION
*          CODE OF OBTAIN AS FOLLOWS--
*              00 - SUCCESSFUL OBTAIN
*              04 - VOLUME NOT MOUNTED
*              08 - DSCB NOT FOUND IN VTOC
*              0C - PERMANENT I/O ERROR
*              10 - INVALID WORK AREA POINTER                     13395
*              14 - (SEEK MODE) ABSOLUTE TRACK ADDRESS IS OUT     13395
*                   OF THE BOUNDARY OF THE VTOC EXTENT.           13395
*
*RESTRICTIONS - NO SECURITY PROTECTION
*
*REGISTER USAGE:
*
R0       EQU   0                        REGISTER ZERO
R1       EQU   1                        REGISTER ONE
R2       EQU   2                        REGISTER TWO
RDSAB    EQU   2                        DSAB POINTER             Y02132
R3       EQU   3                        REGISTER THREE
P1       EQU   3                        REGISTER THREE
R4       EQU   4                        REGISTER FOUR
P2       EQU   4                        REGISTER FOUR
R5       EQU   5                        REGISTER FIVE
P3       EQU   5                        POINTER TO VOL SER
R6       EQU   6                        REGISTER SIX
P4       EQU   6                        PTR TO CALLER'S WORK AREA
R7       EQU   7                        REGISTER SEVEN
R8       EQU   8                        REGISTER EIGHT
RCVT     EQU   8                        CVT POINTER
R9       EQU   9                        REGISTER NINE
RWKAPTR  EQU   9                        OBTAIN WORK AREA POINTER
PL       EQU   10                       PARAMETER LIST POINTER
RUCB     EQU   11                       UCB POINTER
RB       EQU   11                       REGISTER ELEVEN          A37199
RBASE    EQU   12                       BASE REGISTER
RWKAREA  EQU   13                       PTR TO CALLER'S WORK AREA
RETURN   EQU   14                       RETURN REGISTER
RE       EQU   14                       REGISTER FOURTEEN        A37199
R15      EQU   15                       EQUATE FOR REGISTER 15   Y02080
WORKREG  EQU   15                       WORK REGISTER
*
* OTHER EQUATES
*
K0       EQU   0                        CONSTANT OF ZERO         YM0839
K1       EQU   1                        CONSTANT OF ONE          Y02078
ONE      EQU   1                        ONE CHARACTER            S20201
K2       EQU   2                        CONSTANT OF TWO          Y02132
K3       EQU   3                        CONSTANT OF THREE        YM8538
K4       EQU   4                        CONSTANT OF FOUR         YM0839
NONZERO  EQU   4                        NON-ZERO VALUE           YM1312
FW       EQU   4                        BYTES IN FULL WORD       S20201
C7       EQU   7                        CHARACTER OF SEVEN       S20201
K8       EQU   8                        CONSTANT OF EIGHT        Y02078
K12      EQU   12                       CONSTANT OF 12          SA56452
K16      EQU   16                       CONSTANT OF 16          SA56452
CSWAD    EQU   8                        CSW OFFSET IN IOB       SA56392
NOVSAM   EQU   X'00'                    NO VSAM ON VTOC FLAG     X02989
NOF1DSCB EQU   8                        NO F1 DSCB ERROR CODE    Y02132
CCHHRL   EQU   5                        LENGTH OF CCHHR          Y02078
VDATALN  EQU   81                       VDSDSCB DATA LENGTH      Y02132
DATAL    EQU   96                       DSCB DATA PORTION LENGTH Y02078
F1       EQU   C'1'                     FORMAT 1 DSCB ID         Y02078
F4       EQU   C'4'                     FORMAT 4 DSCB ID         Y02078
SEEKCODE EQU   X'80'                    SEEK OPTION CODE         Y02132
DOSBIT   EQU   X'80'                    DOS BIT IN DS4VTOCI      Y02078
DIRFBIT  EQU   X'04'                    DIRF BIT IN DS4VTOCI     Y02078
WKALNGTH EQU   139                      LENGTH-1 OF WORK AREA    Y02078
UNRELATD EQU   X'02'                    UNRELATED FLAG IN IOB   SA56452
RPSMSK   EQU   X'10'                    TESTS RPS BIT IN UCB    SA56452
SKIP     EQU   X'10'                    CCW SKIP FLAG           SA48172
SLI      EQU   X'20'                    CCW SUPPRESS LENGTH     SA48172
*                                       INDICATOR               SA48172
*
         BALR  RBASE,K0                 INITIALIZE BASE REGISTER Y02080
         USING BEGIN,RBASE              CSECT ADDRESSABILITY     Y02080
         USING OBTNWKA,RWKAPTR          WORK AREA ADDRESSABILITY Y02080
         USING UCB,RUCB
*
* THIS SECTION OBTAINS THE OBTAIN WORK AREA.
*
BEGIN    EQU   *                                                 Y01077
         LR    PL,R1                    PARAMETER LIST POINTER
         LM    P3,P4,K8(PL)             LOAD VOL SER NUMBER ADDR YM0117
*                                       AND WORK AREA ADDRESS    YM0117
         LA    RWKAREA,0(P4)            LOAD BASE REGISTER FOR   A42200
*                                       USER'S WORK AREA         A42200
         L     R9,K0(R4)                LOAD SVRB ADDRESS        Y02080
         IECRES GET,PREFIX=FIRST,LV=OWALNGTH,ID=OBWA,            Y02080X
               STM=(R2,RWKAREA,96(R9))  OBTAIN WORK AREA         Y02080
         LR    RWKAPTR,R1               LOAD BASE REGISTER FOR   A42200
*                                       OBTAIN'S WORK AREA       A42200
         MODESET EXTKEY=ZERO            RETURN TO KEY ZERO AFTER Y02082X
                                        ISSUING THE IECRES GET   Y02082X
                                        FOR CORE IN KEY 5        Y02082
*
         IECRES LOAD,EXTPR=(RWKAPTR),MODNM=FIRSTLD,BRANCH=NO     Y02080X
                                        LET OPTIONAL TRACE PRINT Y02080X
                                        THIS MODULE'S NAME       Y02080
*
         L     R1,IECRRPRM              RECOVERY RTN LIST ADDR   Y02144
         USING RRPLIST,R1               PARM LIST ADDRESSABILITY Y02144
         OI    RRFUNCTN,RRFOBTN         SET OBTAIN FUNCTION BIT  Y02144
         DROP  R1                                                Y02144
*
         ST    R4,TCBADDR               SAVE INPUT TCB ADDRESS   Y02078
         L     RCVT,CVTPTR              LOAD CVT ADDRESS         Y02080
         USING CVT,RCVT                 CVT ADDRESSABILITY       Y02080
*
* THIS SECTION CHECKS VALIDITY OF USER'S WORK AREA POINTER
*
VALCK    EQU   *                                                SA56452
         STM   RB,R3,IECREGSV+K12       SAVE REGISTERS 11-3      Y02080
         XR    R7,R7                    INITIALIZE REGISTER      Y02080
*
* IF THE CALLER IS IN PROTECT KEY OF ZERO OR IN SUPERVISOR
* STATE, THE WORK AREA DOES NOT HAVE TO BE CHECKED SINCE
* HE MAY USE ANY REGION IN MAIN STORAGE
*
         TESTAUTH FCTN=1,KEY=YES,STATE=YES,BRANCH=YES  TEST IF   Y02080X
                                        CALLER IS IN KEY 0 OR    Y02080X
                                        IN SUPERVISOR STATE      Y02080
         LTR   R15,R15                  TEST IF ANY TRUE         Y02080
         BZ    RESTORE                  BRANCH IF YES            Y02080
         LA    R1,K0(P4)                STARTING ADDRESS         Y02080
         LA    R2,WKALNGTH(R1)          ENDING ADDRESS           Y02080
OBTNLOCK SETLOCK OBTAIN,TYPE=LOCAL,MODE=UNCOND,                  Y02080X
               RELATED=(LOCAL,IGC0002G(RLSELOCK))  OBTAIN LOCK   Y02080
         LM    RB,RE,IECREGSV+K12       RESTORE REGISTERS 11-14  Y02080
         L     R15,CVT0VL00             VALIDITY CHECK RTN ADDR  Y02080
         BALR  RETURN,R15               LINK TO VALIDITY CHECK   Y02080
         BZ    RLSELOCK                 BRANCH IF VALID ADDRESS  YM1312
         LA    R7,NONZERO               INDICATE INVALID ADDRESS YM1312
*
RLSELOCK SETLOCK RELEASE,TYPE=LOCAL,                             Y02080X
               RELATED=(LOCAL,IGC0002G(OBTNLOCK))  RELEASE LOCK  Y02080
RESTORE  EQU   *                        RESTORE REGISTERS        Y02080
         LM    RB,R3,IECREGSV+K12       RESTORE REGISTERS 11-3   Y02080
         LTR   R7,R7                    TEST IF ADDRESS VALID    Y02080
         BZ    ADDROK                   BRANCH IF VALID          Y02080
         LA    WORKREG,K16              INSERT ERROR CODE       SA56452
         B     SETCODE                  BR TO STORE ERROR CODE  SA56452
PERMERR  EQU   *                                                SA56452
         LA    WORKREG,K12              LOAD ERROR CODE         SA56452
SETCODE  EQU   *                                                SA56452
         STH   WORKREG,ERCODE           STORE ERROR CODE        SA56452
         B     LEAVE                    RETURN TO CALLER        SA56452
*
* THIS SECTION SEARCHES THE UCB LOOKUP TABLE TO FIND THE UCB ADDRESS.
*
ADDROK   EQU   *                        FIND THE UCB ADDRESS     Y02080
         OC    0(6,P3),0(P3)            CHECK FOR ZERO VOL       A25499
*                                       SERIAL                   A25499
         BZ    ENDADDR                  DON'T LOOK FOR UCB IF 0  A25499
         TM    K1(PL),SEEKCODE          TEST FOR THE SEEK OPTION Y02132
         BO    UCBSRCH                  BRANCH IF SEEK SPECIFIED Y02132
*                                                                Y02132
         L     P2,K4(,PL)               LOAD DSNAME ADDRESS      Y02132
         ST    P2,DSMADTW3              SAVE DSNAME ADDRESS      Y02144
         CLC   BLANKS,K0(P3)            TEST FOR A VALID VIRTUAL Y02132
*                                       UCB VOLUME SERIAL NUMBER Y02132
         BNE   UCBSRCH                  BRANCH IF NOT BLANKS     Y02132
         CLC   CSYS,K0(P2)              TEST FOR CHARACTERS SYS  Y02132
*                                       IN BYTES 0-2 OF THE DSN  Y02132
         BNE   UCBSRCH                  BRANCH IF NOT A VIO DSCB Y02132
         CLC   CDOTT,K8(P2)             TEST FOR .T IN BYTES 8-9 Y02132
         BNE   UCBSRCH                  BRANCH IF NOT A VIO DSCB Y02132
         CLC   CDOTR,K16(P2)            TEST FOR CHARACTERS .R   Y02132
*                                       IN BYTES 16-17           Y02132
         BE    VIODSTST                 BRANCH IF A POSSIBLE     Y02132
*                                       VIRTUAL DATA SET         Y02132
*
UCBSRCH  EQU   *                        UCB SEARCH               Y02132
         L     RCVT,CVTPTR              SET UP CVT BASE REGISTER Y01077
         L     R2,CVTILK2               LOAD ADDR OF UCB ADDR TABLE
UCBLOOP  EQU   *
         SR    WORKREG,WORKREG          CLEAR SUBUCB POINTRR.
         SR    RUCB,RUCB                CLEAR UCB POINTER        ZM0144
         ICM   RUCB,3,0(R2)             LOAD 16 BIT UCB ADDR    Z30AAJC
         BZ    NEXTADDR                 BRANCH IF NO UCB        Z30AAJC
         CLM   RUCB,3,EXTCONST          LAST TABLE ENTRY?       Z30AAJC
         BE    ENDADDR                  BRANCH IF AT END OF TBL Z30AAJC
         TM    UCBTBYT3,UCB3DACC       TEST FOR DIRECT ACCESS
         BZ    NEXTADDR                BR IF NOT
         TM    SRTESTAT,SRTEONLI        SEE IF THIS UCB IS       A25499
*                                       ONLINE                   A25499
         BZ    NEXTADDR                 IF NOT, TRY NEXT UCB     A25499
         CLC   SRTEVOLI(6),0(P3)       COMPARE VOLUME SERIAL NUMBERS
         BE    PRIMUCB                 BR IF FOUND
NEXTADDR LA    R2,2(R2)                TRY NEXT ADDR IN UCB ADDR TABLE
         B     UCBLOOP
*
ENDADDR  LA    WORKREG,4               LOAD ERROR CODE
         B     SETCODE
*
*
PRIMUCB  EQU   *
         L     R1,IECRRPRM              RECOVERY RTN LIST ADDR   Y02144
         USING RRPLIST,R1               PARM LIST ADDRESSABILIT  Y02144
         ST    RUCB,RRUCBPTR            SAVE UCB ADDRESS         Y02144
         DROP  R1                                                Y02144
*
* BUILD DEB
*
BUILDEB  EQU   *                       BUILD DEB                 Y02144
         LA    R1,DEB                  BUILD DEB
         MVI   16(R1),X'01'            NUM OF EXTS = 1
         LA    R2,DCB
         ST    R2,24(R1)               INSERT DCB ADDR
         MVI   24(R1),X'0F'            INSERT DEB ID
         MVC   29(3,R1),CVTXAPG+1      INSERT IOS APPAD TAB ADDR
         MVI   28(R1),X'04'            INSERT EXT SCALE
         ST    RUCB,32(R1)             INSERT UCB ADDR
         MVI   32(R1),X'C0'            INSERT DEVICE MOD
         MVC   42(6,R1),EXTCONST       INSERT EXTS AND TK COUNT
         ST    R1,DEBPTR               BUILD DCB
*
* BUILD IOB
*
         OI    IOB,UNRELATD             SET UNRELATED BIT IN IOB A37199
         LA    R0,ECB
         ST    R0,IOB+4                 POINT TO ECB.
         TM    UCBTBYT2,RPSMSK          DOES DEVICE HAVE RPS     S20201
         BZ    NORPS1                   NO, CONTINUE AS NORMAL   S20201
         LA    R0,THETA                 GET ADDRESS OF THETA     S20201
         O     R0,RPSCCW                GET SET SECTOR COMMAND   S20201
*                                       CODE                     S20201
         ST    R0,CCW0                  BUILD SET SECT. CCW      S20201
         MVC   CCW0+FW(FW),RPSCCW+FW    BUILD FLAGS & LENGTH     S20201
         LA    R0,CCW0                  ADDR OF RPS CHANNEL PGM. S20201
         B     IOBCONT                  CONTINUE                 S20201
NORPS1   EQU   *                                                 S20201
         LA    R0,CCW1
IOBCONT  EQU   *                                                 S20201
         ST    R0,IOB+16                POINT TO START OF CHANL PRGRM.
         LA    R0,DCB
         ST    R0,IOB+20                POINT TO DCB.
         L     R0,SRTEFSCT              PICK UP TTR0 OF VTOC     Y02080
         L     WORKREG,CVTPCNVT        LOAD ADDR OF CONVERT ROUTINE
         LA    R2,SEEK                 PUT MBBCCHHR IN SEEK FIELD
         STM   RWKAPTR,RWKAREA,IECREGSV+K12  SAVE REGISTERS 9-13 Y02080
         LR    R3,RWKAPTR               SAVE WORK AREA POINTER.
         BALR  RETURN,WORKREG          CONVERT TTR0 TO MBBCCHHR
         LM    RWKAPTR,RWKAREA,IECREGSV+K12-OBTNWKA(R3)  RESTORE Y02080
         TM    0(PL),X'01'             TEST FOR SEARCH CODE
         BZ    SEEKDSCB                BR IF NOT
*
* THIS SECTION RELOCATES & EXECUTES THE SEARCH OPTION CHANNEL PROGRAM.
*
SRCHDSCB EQU   *                       BUILD CCW                 Y02144
         L     WORKREG,4(0,PL)          LOAD DSNAME PTR          YM8538
         CLC   F4NAME,0(WORKREG)        CHECK IF F4 ONLY         YM8538
         BNE   F1SEARCH                 GO READ FORMAT 1         YM8538
         LM    R0,R5,CHANPROG           CCWS TO READ F4 ONLY     YM8538
         AR    R0,RWKAPTR               RESOLVE CCW1 ADDRESS     YM8538
         AR    R2,RWKAPTR               RESOLVE CCW2 ADDRESS     YM8538
         AR    R4,RWKAPTR               RESOLVE CCW3 ADDRESS     YM8538
         LA    R5,0(0,R5)               CLEAR FLAGS              YM8538
         STM   R0,R5,CCW1               SETUP 3 CCWS             YM8538
         BAL   RETURN,EXECIO            READ F4 ONLY             YM8538
         CLI   DS4IDFMT,F4             TEST FOR FORMAT 4 ID      YM8538
         BNE   PERMERR                 BRANCH IF NOT             YM8538
         MVC   DS1FMTID(L'F4DSCB),F4DSCB MOVE F4 TO F1 BUFFER    YM8538
         MVC   NEWCCHHR,SEEK+K3         RETURN F4 CCHHR          YM8538
         B     RETPARMS                 BRANCH TO RETURN INFO    YM8538
*                                                                YM8538
F1SEARCH EQU   *                        SEARCH FOR F1 DSCB       YM8538
*                                                                YM8538
         LM    R0,R7,CHANPROG          LOAD CCW1 TO CCW4 CONSTANTS
         AR    R0,RWKAPTR               RESOLVE ADDR IN CCW1.
         AR    R2,RWKAPTR               RESOLVE ADDR IN CCW2.
         AR    R4,RWKAPTR              RESOLVE ADDR IN CCW3      Y02078
         AR    R6,RWKAPTR              RESOLVE ADDR IN CCW4      Y02078
         STM   R0,R7,CCW1              STORE CCW1 TO CCW4
         LA    WORKREG,CCW9-CCW1
         AR    R2,WORKREG              RESOLVE ADDR IN CCW5
         STM   R2,R3,CCW5              STORE CCW5
         LA    WORKREG,CCW11-CCW9
         AR    R2,WORKREG              RESOLVE ADDR IN CCW8
         STM   R2,R3,CCW8              STORE CCW8
         LM    R0,R7,CHANPROG+32       LOAD CCW9 TO CCW12 CONSTANTS
         L     WORKREG,4(0,PL)         LOAD ADDR OF DATA SET NAME
         LA    WORKREG,0(WORKREG)      CLEAR HIGH ORDER BYTE    SA56450
         AR    R0,WORKREG              RESOLVE ADDR IN CCW9
         AR    R2,RWKAPTR               RESOLVE ADDR IN CCW2.
         AR    R4,RWKAPTR              RESOLVE ADDR IN CCW11     Y02078
         AR    R6,RWKAPTR              RESOLVE ADDR IN CCW12     Y02078
         STM   R0,R7,CCW9              STORE CCW9 TO CCW 12
         STM   R0,R1,CCW6              STORE CCW6
         STM   R4,R5,CCW7              STORE CCW7               SA48172
         MVI   CCW7+4,SLI+SKIP         SET SLI AND SKIP BITS    SA48172
EXECUTE  BAL   RETURN,EXECIO           EXECUTE THE CHANNEL PROGRAM
         L     R2,IOB+CSWAD            GET CSW COMMAND ADDRESS  SA56392
         LA    R2,0(R2)                CLEAR HIGH BYTE          SA56392
         LA    R3,CCW8                 GET CCW8 ADDRESS         SA56392
         CLR   R2,R3                   DOES CSW POINT TO CCW8   SA56392
         BE    TESTDOS                 YES, FORMAT 1 DSCB       SA56392
*                                      WAS NOT FOUND            SA56392
         CLI   DS1FMTID,F1             TEST FOR FORMAT 1 ID      Y02078
         BNE   PERMERR                 BAD VTOC - I/O ERR CODE  SA56392
         SR    WORKREG,WORKREG         ZERO REG
         STC   WORKREG,ERCODE          TURN OFF FIRST PASS INDICATOR
         IC    WORKREG,NEWCCHHR+4      PICK UP R OF NEXT RECORD
         BCTR  WORKREG,R0              DECREMENT R BY ONE
         LTR   WORKREG,WORKREG         CHECK FOR DSCB LAST ON TRACK
         BNE   NOTLAST                 BR IF NOT
         IC    WORKREG,DS4DEVDT        SET R TO LAST DSCB ON THE TRACK
NOTLAST  STC   WORKREG,NEWCCHHR+4      STORE R OF DSCB READ INTO CORE
*                                                                YM8538
RETPARMS EQU   *                        UPDATE USER PARM LIST    YM8538
*                                                                YM8538
         L     R1,TCBADDR              LOAD TCB ADDRESS          Y02078
         USING TCB,R1                  TCB ADDRESSABILITY        Y02078
         MODESET EXTKEY=RBT234,WORKREG=2  SWITCH TO CALLER'S KEY Y02078
         MVC   K0(DATAL,RWKAREA),DS1FMTID  MOVE IN F1 DSCB DATA  Y02078
         MVC   DATAL(CCHHRL,RWKAREA),NEWCCHHR  MOVE IN CCHHR     Y02078
         MODESET EXTKEY=ZERO           RETURN TO KEY ZERO        Y02082
         DROP  R1                                                Y02078
*
* THIS SECTION FREES THE WORK AREA AND RETURNS TO THE CALLER.
*
LEAVE    EQU   *                        SAVE RETURN CODE         Y02080
         LH    R8,ERCODE                SAVE RETURN CODE         Y02080
         IECRES FREE,PREFIX=FIRST,A=(RWKAPTR)  FREE WORK AREA    Y02080
         USING CVT,WORKREG              CVT ADDRESSABILITY       Y02080
         L     WORKREG,CVTPTR           LOAD CVT ADDRESS         Y02080
         L     RETURN,CVTEXPRO          LOAD EXIT PROLOG ADDRESS Y02080
         LR    WORKREG,R8               RESTORE RETURN CODE      Y02080
         RETURN ,                       RETURN                   Y02080
         DROP  WORKREG                                           Y02080
*
* IF THE FORMAT 1 DSCB WAS NOT FOUND ON THE FIRST PASS, AND THE
* DOS OR DIRF BIT IS SET IN THE FORMAT 4 DSCB, THIS SECTION
* MODIFIES THE CHANNEL PROGRAM TO READ EVERY DSCB IN THE VTOC.
*
TESTDOS  EQU   *                       TEST IF DOS OR DIRF SET   Y02078
         TM    DS4VTOCI,DOSBIT+DIRFBIT  TEST IF DOS OR DIRF SET  Y02078
         BZ    NODSCB                  BR IF NOT
         TM    ERCODE,X'80'            TEST FOR SECOND PASS
         BO    NODSCB                  BR IF YES
         OI    ERCODE,X'80'            TURN ON FIRST PASS INDICATOR
         CLC   DS4HPCHR(4),DS4VTOCE+6   IS HIGH PRIME CCHH END OF VTOC.
         BNE   NEWHIGH                  BRANCH IF NOT            Y02078
         CLC   DS4HPCHR+K4(K1),DS4DEVDT  IS HIGH PRIME R THE     Y02078
*                                       LAST RECORD ON THE TRACK Y02078
         BE    NODSCB                   BRANCH IF YES.
NEWHIGH  EQU   *                       SET NEW HIGH              Y02078
         MVC   DS4HPCHR(4),DS4VTOCE+6     INSERT CCHH OF VTOE END.
         MVC   DS4HPCHR+4(1),DS4DEVDT  PICK UP NEW R
         MVI   CCW3+4,X'50'            STORE NEW CCW3
         B     EXECUTE
*
NODSCB   LA    WORKREG,8               LOAD ERROR CODE
         CLI   DS4AMCAT,NOVSAM         CHECK FOR NO VSAM ON VOL  X02989
         BE    SETCODE                 NORMAL PATH FOR ERROR     X02989
         LR    RWKAREA,RWKAPTR          W/A ADDR IN REGISTER 13  Y02080
         IECRES LOAD,EXTPR=(RWKAPTR),MODNM=VSAMMOD,BRANCH=DIRECT Y02080
*
* FOR A POSSIBLE VIO DATA SET, THIS SECTION FIRST ATTEMPTS TO ENQ
* ON THE TIOT.  IT THEN SEARCHES THE DSAB CHAIN FOR A TIOT ENTRY
* CONTAINING A VIRTUAL UCB WHOSE VDSDSCB CONTAINS THE DATA SET NAME
* BEING OBTAINED AND WHOSE VOLUME SERIAL MATCHES THE VOLUME SERIAL
* IN THE PARAMETER LIST.  IF A DSNAME/VOL SER MATCH IS FOUND, IT
* MOVES THE 81 BYTE DATA PORTION OF THE VDSDSCB INTO THE CALLER'S
* WORK AREA.  NOTE THAT IF THE ENQ RETURN CODE INDICATED THAT THE TASK
* PREVIOUSLY HAD CONTROL OF THE RESOURCE, NO SUBSEQUENT DEQ IS ISSUED.
*
VIODSTST EQU   *                        TEST FOR VIO DATA SET    Y02132
         L     RDSAB,TCBADDR            GET TCB ADDRESS          Y02132
         USING TCB,RDSAB                TCB ADDRESSABILITY       Y02132
         L     RDSAB,TCBJSCB            GET JSCB ADDRESS         Y02132
         USING IEZJSCB,RDSAB            JSCB ADDRESSABILITY      Y02132
         L     RDSAB,JSCBACT            GET ACTIVE JSCB ADDRESS  Y02132
         L     RDSAB,JSCDSABQ           GET DSAB QDB ADDRESS     Y02132
         ST    RDSAB,DSABQDB            SAVE FOR TIOT ENQ        Y02132
         L     WORKREG,CVTPTR           LOAD CVT ADDRESS         YM3996
         L     WORKREG,CVTTCBP-CVT(,WORKREG)  LOAD TCB/ASID PTRS YM3996
         L     WORKREG,K12(,WORKREG)    LOAD CURRENT ASID ADDR   YM3996
         USING ASCB,WORKREG             ASCB ADDRESSABILITY      YM3996
         MVC   TIOTMNR(K2),ASCBASID     MOVE ASID INTO ENQ MINOR YM3996
         DROP  WORKREG                                           YM3996
         MVC   MJELNAME(L'TIOTMJR),TIOTMJR  MAJOR RESOURCE NAME  Y02132
         MVC   ENQAREA(ENQLTH),ENQLIST  MOVE IN ENQ PARM LIST    Y02132
         ENQ   (MJELNAME,TIOTMNR),MF=(E,ENQAREA)  ISSUE ENQ      Y02132
*                                                                Y02132
         LTR   R15,R15                  TEST IF SUCCESSFUL ENQ   Y02132
         BNZ   DSABSRCH                 BRANCH IF NOT            Y02132
         OI    DSMADTB2,DSMTIOTE        SET TIOT ENQ'ED SWITCH   Y02144
         LA    R15,TIOTMNR              MINOR RESOURCE ADDRESS   Y02144
         ST    R15,DSMADTW2             SAVE MINOR RESOURCE ADDR Y02144
*
DSABSRCH EQU   *                        GET DSAB                 Y02132
         L     RDSAB,DSABQDB            GET DSAB QDB ADDR        Y02132
         USING QDB,RDSAB                QDB ADDRESSABILITY       Y02132
         L     RDSAB,QDBFELMP           GET PTR TO FIRST DSAB    Y02132
         USING DSAB,RDSAB               DSAB ADDRESSABILITY      Y02132
VDSABTST EQU   *                        IS THERE ANOTHER DSAB    Y02132
         LTR   RDSAB,RDSAB              IS THERE ANOTHER DSAB    Y02132
         LA    RB,UCBSRCH               SET UP RETURN ADDRESS    YM5378
         BZ    DEQTEST1                 BRANCH IF NOT            Y02132
         L     R3,DSABTIOT              GET TIOT ADDRESS         Y02132
         USING TIOENTRY,R3              TIOT ADDRESSABILITY      Y02132
         L     RUCB,TIOEFSRT-K1         GET UCB ADDRESS          Y02132
         LA    RUCB,K0(RUCB)            CLEAR HIGH BYTE          Y02132
         LTR   RUCB,RUCB                IS THERE A UCB ADDRESS   Y02132
         BZ    TRYNEXT                  BRANCH IF NOT            Y02132
         TM    UCBJBNR,UCBVRDEV         TEST IF A VIRTUAL UCB    Y02132
         BZ    TRYNEXT                  BRANCH IF NOT            Y02132
         CLC   SRTEVOLI,K0(P3)          COMPARE VOL SER'S        Y02132
         BNE   TRYNEXT                  BRANCH IF NOT EQUAL      Y02132
         LR    R3,RUCB                  VDSCB ADDRESS            Y02132
         USING VDSCB,R3                 VDSCB ADDRESSABILITY     Y02132
         CLC   VDSDSCB(L'DS1DSNAM),K0(P2)  COMPARE DSNAME'S      Y02132
         BE    SCRTHTST                 BRANCH IF EQUAL          Y02132
TRYNEXT  EQU   *                        GET NEXT DSAB            Y02132
         L     RDSAB,DSABFCHN           GET NEXT DSAB ADDRESS    Y02132
         B     VDSABTST                 GO TEST IT               Y02132
*
SCRTHTST EQU   *                        IS VDSCB SCRATCHED       Y02132
         CLC   VDSABSTT,EXTCONST        TEST IF VDSCB SCRATCHED  Y02132
         BNE   VIOBTAIN                 BRANCH IF NOT SCRATCHED  Y02132
         LA    WORKREG,NOF1DSCB         NO F1 DSCB ERROR CODE    Y02132
         STH   WORKREG,ERCODE           SAVE ERROR CODE          Y02132
         B     DEQTEST                  GO DEQ FROM THE TIOT     Y02132
*
VIOBTAIN EQU   *                        OBTAIN VIO DATA SET DSCB Y02132
         L     R1,TCBADDR               LOAD TCB ADDRESS         Y02132
         USING TCB,R1                   TCB ADDRESSABILITY       Y02132
         MODESET EXTKEY=RBT234,WORKREG=2  SWITCH TO CALLER'S KEY Y02132
         MVC   K0(VDATALN,RWKAREA),VDSDSCB+L'DS1DSNAM            Y02132
         XC    VDATALN(DATAL-VDATALN,RWKAREA),VDATALN(RWKAREA)   Y02132
*                                       CLEAR REST OF DATA AREA  Y02132
         XC    DATAL(CCHHRL,RWKAREA),DATAL(RWKAREA)              Y02132
*                                       CLEAR CCHHR AREA         Y02132
         MODESET EXTKEY=ZERO            RETURN TO KEY ZERO       Y02132
         DROP  R1                                                Y02132
*
DEQTEST  EQU   *                        DEQ TIOT                 Y02132
         LA    RB,LEAVE                 SET UP RETURN ADDRESS    YM5378
DEQTEST1 EQU   *                        TEST IF TIOT ENQ'ED      Y02132
         TM    DSMADTB2,DSMTIOTE        TEST IF TIOT ENQ'ED      Y02132
         BCR   8,RB                     BRANCH IF NOT            YM5378
         MVC   ENQAREA(DEQLTH),DEQLIST  MOVE IN DEQ PARM LIST    Y02132
         DEQ   (MJELNAME,TIOTMNR),MF=(E,ENQAREA)  ISSUE DEQ      Y02132
*
         NI    DSMADTB2,X'FF'-DSMTIOTE  RESET TIOT ENQ'ED BIT    Y02144
         BR    RB                       RETURN                   YM5378
*
* THIS SECTION EXECUTES A CHANNEL PROGRAM.
*
EXECIO   EQU   *                                                 S20201
         TM    UCBTBYT2,RPSMSK          DOES DEVICE HAVE RPS     S20201
         BZ    NORPS3                   NO, CONTINUE AS NORMAL   S20201
         LA    R2,THETA                 GET THETA ADDRESS        S20201
         XC    CCW2+FW(FW),CCW2+FW      CLEAR WORK WORD          S20201
         MVC   CCW2+FW(ONE),UCBTBYT4    GET DEVICE CODE          S20201
         O     R2,CCW2+FW               PUT IN HIGH BYTE         S20201
         L     R0,PARM0                 GET DDK FOR SCR          S20201
         IC    R0,SEEK+C7               GET RECORD NUMBER        S20201
         USING CVT,RCVT                 CVT ADDRESSABILITY       Y02080
         L     WORKREG,CVT0SCR1         GET ROUTINE ADDRESS      Y01077
         LR    R3,R9                    SAVE REGISTER            S20201
         STM   RWKAPTR,RETURN,IECREGSV+K12   SAVE REGISTERS 9-14 Y02080
         BALR  RETURN,WORKREG           GET SECTOR VALUE         S20201
         LM    RWKAPTR,RETURN,IECREGSV+K12-OBTNWKA(R3)  RESTORE  Y02080
NORPS3   EQU   *                                                 S20201
         EXCP  IOB                                               S20201
         WAIT  1,ECB=ECB
         TM    ECB,X'20'               TEST ECB FOR PERMANENT ERROR
         BZ    PERMERR                 BR IF ERROR
         BR    RETURN
*
* THIS SECTION READS THE FORMAT 4 DSCB AND ENSURES THAT THE USER'S
* CCHHR IS WITHIN THE EXTENTS OF THE VTOC.
*
SEEKDSCB LM    R0,R5,CHANPROG          LOAD CCW1 TO CCW3 CONSTANTS
         AR    R0,RWKAPTR               RESOLVE ADDR IN CCW1.
         AR    R2,RWKAPTR               RESOLVE ADDR IN CCW10.
         AR    R4,RWKAPTR               RESOLVE ADDR IN CCW3     Y02078
         STM   R0,R5,CCW1              STORE CCW1 TO CCW3
         MVI   CCW3+4,X'00'            TURN OFF COMMAND CHAIN BIT
         BAL   RETURN,EXECIO           EXECUTE THE CHANNEL PROGRAM
         CLI   DS4IDFMT,F4             TEST FOR FORMAT 4 ID      Y02078
         BNE   PERMERR                 BRANCH IF NOT             Y02078
FIX      LA    R1,DEB
         MVC   38(8,R1),DS4VTOCE+2     MOVE EXTENTS OF VTOC INTO DEB
         L     WORKREG,4(0,PL)         LOAD ADDR OF DATA SET CCHHR
         CLC   0(4,WORKREG),DS4VTOCE+2 CHECK FOR EXTENT WITHIN @ZA17992
         BC    4,ERROR20                VTOC                   @ZA17992
         CLC   0(4,WORKREG),DS4VTOCE+6  CHECK ABSOLUTE TRACK ADDR 13395
         BC    2,ERROR20               BRANCH IF OUT OF VTOC EXT  13395
         CLC   SEEK+K3(CCHHRL),0(WORKREG) CHECK IF F4 WANTED     YM8538
         BNE   READF1                   BRANCH TO READ F1 DSCB   YM8538
         MVC   DS1DSNAM,F4NAME          FORMAT 4 DSNAME          YM8538
         MVC   DS1FMTID(L'F4DSCB),F4DSCB F4 DSCB TO RETURN AREA  YM8538
         B     RETPARM2                 RETURN TO CALLER         YM8538
*                                                                YM8538
READF1   EQU   *                        READ FORMAT 1 DSCB       YM8538
*                                                                YM8538
         MVC   SEEK+3(5),0(WORKREG)    MOVE DATA SET CCHHR TO IOS SEEK
         LM    R0,R1,CHALPROG          LOAD CCW3 CONSTANT
         AR    R0,RWKAPTR              RESOLVE ADDR IN CCW3      Y02078
*
* THIS SECTION RELOCATES AND EXECUTES THE SEEK OPTION CHANNEL PROGRAM.
*
         STM   R0,R1,CCW3              STORE CCW3
         BAL   RETURN,EXECIO           EXECUTE THE CHANNEL PROGRAM
*                                                                YM8538
RETPARM2 EQU   *                        RETURN PARMS TO CALLER   YM8538
*                                                                YM8538
         L     R1,TCBADDR               LOAD TCB ADDRESS         Y02078
         USING TCB,R1                   TCB ADDRESSABILITY       Y02078
         MODESET EXTKEY=RBT234,WORKREG=2  SWITCH TO CALLER'S KEY Y02078
         MVC   K0(L'OBTDSCB,RWKAREA),OBTDSCB  MOVE IN DSCB       Y02078
         MODESET EXTKEY=ZERO            RETURN TO KEY ZERO       Y02082
         DROP  R1                                                Y02078
         B     LEAVE
ERROR20  EQU   *                                                  13395
         LA    WORKREG,20              CCHH OUT OF VTOC EXTENT    13395
         B     SETCODE                 BRANCH TO RETURN           13395
*
*** PROGRAM CONSTANTS
*
CSYS     DC    CL3'SYS'                 CONSTANT OF 'SYS'        Y02132
CDOTT    DC    CL2'.T'                  CONSTANT OF '.T'         Y02132
CDOTR    DC    CL2'.R'                  CONSTANT OF '.R'         Y02132
BLANKS   DC    CL6' '                   SIX EBCDIC BLANKS        Y02132
F4NAME   DC    0XL44'04',44X'04'        FORMAT 4 DSNAME          YM8549
DEQLIST  DEQ   (,,6,SYSTEM),MF=L        DEQ PARAMETER LIST       YM5902
DEQLTH   EQU   *-DEQLIST                LENGTH OF DEQ PARM LIST  Y02132
TIOTMJR  DC    CL8'SYSZTIOT'            TIOT MAJOR RESOURCE NAME Y02132
ENQLIST  ENQ   (,,S,6,SYSTEM),RET=HAVE,MF=L  ENQ PARAMETER LIST  Y02132
ENQLTH   EQU   *-ENQLIST                LENGTH OF ENQ PARM LIST  Y02132
EXTCONST DC    X'FFFFFFFF7FFF'         LAST CCHH AND NUM OF TKS
RPSCCW   DS    0F                       SET SECTOR COMMAND       S20201
         DC    X'23'                    COMMAND CODE             S20201
         DC    X'000000'                ADDR                     S20201
         DC    X'4000'                  FLAGS                    S20201
         DC    H'1'                     COUNT                    S20201
PARM0    DS    0F                       AREA TO BUILD PARAMETERS S20201
DD       DC    X'0060'                  DATA LENGTH              S20201
K        DC    X'2C'                    KEY  LENGTH              S20201
R        DC    X'0'                     RECORD NUMBER            S20201
*
*** THE FOLLOWING CHANNEL PROGRAM SEARCHES THE VTOC FOR THE NAMED DSCB.
*** THE NAMED DSCB IS READ INTO THE INPUT AREA.
*
CHANPROG DS    0F
*CCW1
         DC    X'31'                   SEARCH EQUAL ID (SEEK+3)
         DC    AL3(3+SEEK-OBTNWKA)     SEEK ADDR                 Y02080
         DC    X'4000'
         DC    H'5'
*CCW2
         DC    X'08'                   TIC TO CCW1
         DC    AL3(0+CCW1-OBTNWKA)     TIC TO ADDR               Y02080
         DC    F'0'
*CCW3
         DC    X'06'                    READ DATA OF F4 DSCB     Y02078
         DC    AL3(0+F4DSCB-OBTNWKA)    DATA ADDR                Y02078
         DC    X'4000'
         DC    H'96'
*CCW4
         DC    X'F1'                    SEARCH HI OR EQ LST FMT1.
         DC    AL3(0+DS4HPCHR-OBTNWKA)  SEARCH ADDR              Y02078
         DC    X'4000'
         DC    H'5'
*CCW5                                   TO BE CONSTRUCTED
*                                       TIC TO CCW9
*CCW6                                   TO BE CONSTRUCTED
*                                       SEARCH EQUAL KEY (DS NAME)
*CCW7                                   TO BE CONSTRUCTED
*                                       READ DATA OF THE LAST   SA48172
*                                       FORMAT 1 WITH THE SKIP  SA48172
*                                       BIT SET IN ORDER TO END SA48172
*                                       THE CHANNEL PROGRAM     SA48172
*CCW8                                   TO BE CONSTRUCTED
*                                       TIC TO CCW11
*CCW9
         DC    X'29'                   SEARCH EQUAL KEY (DS NAME)
         DC    AL3(0)
         DC    X'6000'
         DC    H'44'
*CCW10
         DC    X'08'                   TIC TO CCW4
         DC    AL3(0+CCW4-OBTNWKA)     TIC TO ADDR               Y02078
         DC    F'0'
*CCW11
         DC    X'06'                    READ DATA OF F1 DSCB     Y02078
         DC    AL3(DS1FMTID-OBTNWKA)    DATA ADDR                Y02078
         DC    X'4000'
         DC    H'96'
*CCW12
         DC    X'12'                   READ COUNT (NEWCCHHR)
         DC    AL3(0+NEWCCHHR-OBTNWKA) DATA ADDR                 Y02078
         DC    X'2000'
         DC    H'5'
*
*** THE FOLLOWING CHANNEL PROGRAM SEARCHES A TRACK AND READS A DSCB
*** INTO THE INPUT AREA.
*
CHALPROG DS    0F
*CCW1                                   TO BE CONSTRUCTED
*                                       SEARCH EQUAL ID (SEEK+3)
*CCW2                                   TO BE CONSTRUCTED
*                                       TIC TO CCW1
*CCW3
         DC    X'0E'                    READ KEY & DATA OF DSCB  Y02078
         DC    AL3(OBTDSCB-OBTNWKA)     DATA ADDR                Y02078
         DC    X'0000'
         DC    H'140'
*
* TABLE OF MODULE NAMES AND ENTRY POINT ADDRESSES
*
         XCTLTABL ID=(FIRSTLD,IGC0002G,VSAMMOD,IGC0102G),        Y02080X
               SVC=027,LENGTH=,BRT=YES                           Y02080
         SPACE 2                                                 Y02080
         IECDSECS CVT,DSAB,JSCB,QDB,PSA,RB,RRPL,TCB,TIOT,UCB,    Y02132X
               ASCB,                                             YM3996X
               EXPAND=YES                                        Y02132
WORKAREA IECOBTWA EP,F4,D1=(1),ADT=YES  OBTAIN WORK AREA         Y02144
         EJECT                                                   Y02132
         IDDVDSCB                       VDSCB MAPPING MACRO      Y02132
         END
