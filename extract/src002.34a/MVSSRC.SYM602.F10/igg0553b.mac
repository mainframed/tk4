 TITLE 'IGG0553B - EXTEND - SPACE SEARCH ROUTINE'
IGG0553B CSECT
*
*MODULE NAME = IGG0553B
*
*DESCRIPTIVE NAME = EXTEND - SPACE SEARCH ROUTINE
*
*COPYRIGHT = NONE
*
*CHANGE ACTIVITY = SEE BELOW
*
*          RELEASE 22 DELETIONS                                       *
*          RELEASE 21 DELETIONS                                       *
*1394075440-075560,095000                                        M0050
*1394000400,000600,001600-004200,012000,013800-015200,016000-    M0130
*1394016400,016800-018000,018400,018800,023600,033400,043200-    M0130
*1394043400,046800,049600-050200,051200-051600,076000,087600,    M0130
*1394088400-089400,090200-092800,093400-093800,096600            M0130
*0000032000-032200,038400                                        M2281
*0000034800                                                     SA54497
*          RELEASE 20 DELETIONS                                       *
*1463                                                            M6099
*        VS2 RELEASE 02 DELETIONS/CHANGES                             *
*0000000030-000150,002360,002460,003800-004934,013400,014200,    Y02080
*0000014800,015600,016700-018680,019000-023400,024520-029800,    Y02080
*0000030800,032000-033400,033800-034400,035800-036200,036600,    Y02080
*0000037000-038800,040200-041000,041400-042000,043000-044400,    Y02080
*0000045400-045800,046200,047200-048000,049000,050400,052000,    Y02080
*0000052400-052600,053000,054000-054200,056000,056400-056800,    Y02080
*0000057200-058000,058400-059400,059800-060000,061000,062200-    Y02080
*0000062800,064000-066200,066600-067400,067800-068000,068600-    Y02080
*0000069200,069600-072000,072400-073200,073600-074200,074600-    Y02080
*0000074800,075260,075600,076200-076400,077400,078400,079400,    Y02080
*0000080400,081200,082200,083200,084000,085000,086000,086800,    Y02080
*0000087400,087600-089800,091000,093400-756400                   Y02080
*                                                                     *
* STATUS CHANGE LEVEL  003
*                                                                     *
*FUNTION/OPERATION:                                                   *
*   THIS MODULE READS IN THE FIRST FORMAT 5 DSCB AND SEARCHES IT      *
*   FOR AVAILABLE EXTENTS THAT WILL FILL THE SECONDARY REQUEST. A     *
*   PUSH DOWN LIST OF UP TO FIVE ENTRIES IS BUILT UNTIL AN AVAILABLE  *
*   EXTENT IS FOUND THAT IS EQUAL OR GREATER THAN THE REQUEST. THE    *
*   EXTENT CLOSEST IN SIZE TO THE REQUEST IS HELD AND NEW EXTENTS ARE *
*   COMPARED AGAINST THIS EXTENT. WHEN THE PREFERRED EXTENT IS        *
*   ENCOUNTERED AND THERE IS SUFFICIENT SPACE TO FULFILL THE REQUEST, *
*   THE PREFERRED EXTENT IS ENTERED IN THE DADSM EXTENT TABLE. IF     *
*   ANOTHER FORMAT 5 DSCB EXISTS, IT IS ALSO SEARCHED FOR SPACE.      *
*   AFTER ALL AVAILABLE EXTENTS HAVE BEEN SEARCHED, THE DADSM TABLE   *
*   IS BUILT FROM THE HELD QUANTITY, IF ONE EXISTS. IF THERE IS NO    *
*   HELD QUANTITY, THE PUSHDOWN LIST IS EXAMINED TO SEE IF TWO OR     *
*   MORE EXTENTS CAN SATISFY THE REQUEST. THE EXTENT OR EXTENTS THAT  *
*   SATISFY THE REQUEST ARE ENTERED IN THE DADSM EXTENT TABLE.        *
*                                                                     *
*ENTRY POINT:                                                         *
*   IGG0553B - ENTRY IS MADE FROM IGG0553G VIA A BRANCH.              *
*                                                                     *
*INPUT:                                                               *
*   REGISTER 4 POINTS TO THE I/O SUPPORT WORK AREA.                   *
*   REGISTER 5 POINTS TO THE USER'S DEB.                              *
*   REGISTER 11 POINTS TO THE EXTEND WORK AREA.                       *
*   REGISTER 12 CONTAINS THE AMOUNT OF SPACE REQUESTED.               *
*   REGISTER 13 CONTAINS THE PREFERRED RTA.                           *
*   THE I/O SUPPORT WORK AREA IS LAID OUT AS FOLLOWS:                 *
*                                                                     *
*          BYTES          AREA                                        *
*         *******        ******                                       *
*          100            DSCB FIELD                                  *
*          176            JFCB                                        *
*            4            ECB                                         *
*           40            IOB                                         *
*           44            DEB                                         *
*            4            DCB                                         *
*           96            CCW'S                                       *
*                                                                     *
*OUTPUT:                                                              *
*   REGISTER 4 POINTS TO THE I/O SUPPORT WORK AREA.                   *
*   REGISTER 5 POINTS TO THE USER'S DEB.                              *
*   REGISTER 11 POINTS TO THE EXTEND WORK AREA, WHICH CONTAINS THE    *
*   DADSM EXTENT TABLE.                                               *
*                                                                     *
*              ***************************************                *
*              *             DADSM TABLE             *                *
*              ***************************************                *
*                                                                     *
*              ***************************************                *
*              *        *         *                  *                *
*              *  TYPE  *  NO OF  *     USED HOLE    *                *
*              *  FLAG  * ENTRIES *      COUNTER     *                *
*              *        *         *                  *                *
*              ***************************************                *
*              *                  *                  *                *
*              *       RTA        *      RTA+1       *                *
*              *                  *                  *                *
*              ***************************************                *
*              *                  *                  *                *
*              *       RTA        *      RTA+1       *                *
*              *                  *                  *                *
*              ***************************************                *
*              *                  *                  *                *
*              *       RTA        *      RTA+1       *                *
*              *                  *                  *                *
*              ***************************************                *
*              *                  *                  *                *
*              *       RTA        *      RTA+1       *                *
*              *                  *                  *                *
*              ***************************************                *
*              *                  *                  *                *
*              *       RTA        *      RTA+1       *                *
*              *                  *                  *                *
*              ***************************************                *
*                                                                     *
*              NO OF ENTRIES = NUMBER OF ENTRIES WITHIN DADSM TABLE.  *
*                                                                     *
*              RTA = THE STARTING TRACK OF THE ALLOCATED EXTENT.      *
*                                                                     *
*              RTA+1 = THE STARTING TRACK OF THE NEXT AVAILABLE EXTENT*
*                                                                     *
*                                                                     *
*                                                                     *
*EXTERNAL ROUTINES:                                                   *
*        EXCP(0) - READ OR WRITE DIRECT ACCESS DEVICE                 *
*        WAIT(1) - WAIT ON EVENT CONTROL BLOCK                        *
*        IECRES - BRANCH TO ANOTHER MODULE                            *
*                                                                     *
*EXITS:                                                               *
*   NORMAL - BRANCH TO IGG0553C                                       *
*                                                                     *
*   ERROR  - BRANCH TO IGG0553E WITH THE ENTRY CODE IN REGISTER 13    *
*   ERROR CONDITIONS ARE I/O ERROR OR QUANTITY NOT AVAILABLE.         *
*                                                                     *
*ATTRIBUTES: REENTRANT                                                *
*                                                                     *
*OTHER MACROS USED:                                                   *
*   IECDSECT - TO DEFINE THE I/O SUPPORT WORK AREA                    *
*   IECEXTWA - TO DEFINE THE EXTEND WORK AREA                         *
*   IECSDSL1 - TO SYMBOLICALLY ADDRESS DSCB'S                         *
*   CVT - TO DEFINE THE CVT                                           *
*                                                                     *
*STORAGE: PROGRAM CODE CSECT - LESS THAN 1024 BYTES                   *
*         EXTEND WORK AREA - AS DEFINED IN THE IECEXTWA MACRO         *
*         RPS WORK AREA - AS DEFINED IN THE IECRPS MACRO              *
*         I/O SUPPORT WORK AREA                                       *
*                                                                     *
         BALR  RBASE,K0                 CSECT ADDRESSABILITY     Y02080
         USING BEGIN,RBASE                                       Y02080
         USING EOVWKA,RCORE             EOV W/A ADDRESSABILITY   Y02080
         USING EXTNDWKA,EXWKA           WORK AREA ADDRESSABILITY Y02080
*
* REGISTER EQUATES
*
REG0     EQU   0                        EQUATE FOR REGISTER 0    Y02080
RHH      EQU   0                        NUMBER OF EXTRA TRACKS   Y02080
REG1     EQU   1                        EQUATE FOR REGISTER 1    Y02080
RCC      EQU   1                        NUMBER OF CYLINDERS      Y02080
ALTWKA   EQU   1                        ALTERNATE EXTEND WKA PTR Y02080
RIN      EQU   2                        INPUT F5 POINTER         Y02080
RBASE    EQU   3                        BASE REGISTER            Y02080
RCORE    EQU   4                        EOV WORK AREA POINTER    Y02080
RDEB     EQU   5                        DEB POINTER              Y02080
RCTR     EQU   6                        COUNTER                  Y02080
RTA1     EQU   6                        EVEN REGISTER            Y02080
REG6     EQU   6                        EQUATE FOR REGISTER 6    Y02080
REG7     EQU   7                        EQUATE FOR REGISTER 7    Y02080
RTA      EQU   7                        ODD REGISTER             Y02080
REG8     EQU   8                        EQUATE FOR REGISTER 8    Y02080
ROUT     EQU   8                        CONVERTED F5 POINTER     Y02080
TBLPTR   EQU   8                        DADSMTBL ENTRY POINTER   Y02080
REG9     EQU   9                        EQUATE FOR REGISTER 9    Y02080
PDLPTR   EQU   9                        PUSHDOWN LIST POINTER    Y02080
RA       EQU   10                       EQUATE FOR REGISTER 10   Y02080
RTRK     EQU   10                       NUMBER OF TRKS/CYLINDER  Y02080
EXWKA    EQU   11                       EXTEND WORK AREA POINTER Y02080
RC       EQU   12                       EQUATE FOR REGISTER 12   Y02080
REQ      EQU   12                       SPACE REQUESTED          Y02080
REG13    EQU   13                       EQUATE FOR REGISTER 13   Y02080
RPREF    EQU   13                       PREFERRED RTA            Y02080
RE       EQU   14                       EQUATE FOR REGISTER 14   Y02080
RCNT     EQU   14                       COUNTER OF EXTENTS       Y02080
RF       EQU   15                       EQUATE FOR REGISTER 15   Y02080
RHOLD    EQU   15                       HELD QUANTITY INDICATOR  Y02080
*
* OTHER EQUATES
*
ONEENTRY EQU   X'01'                    ONE ENTRY IN DADSM TABLE M0130
K0       EQU   0                        CONSTANT OF 0            Y02080
K1       EQU   1                        CONSTANT OF 1            Y02080
K2       EQU   2                        CONSTANT OF 2            Y02080
K3       EQU   3                        CONSTANT OF 3            Y02080
K4       EQU   4                        CONSTANT OF 4            Y02080
K5       EQU   5                        CONSTANT OF 5            Y02080
K24      EQU   24                       CONSTANT OF 24           Y02080
K26      EQU   26                       CONSTANT OF 26           Y02080
K27      EQU   27                       CONSTANT OF 27           Y02080
K48      EQU   48                       CONSTANT OF 48           Y02080
K72      EQU   72                       CONSTANT OF 72           Y02080
K108     EQU   108                      CONSTANT OF 108          Y02080
*
* THIS SECTION RELOCATES THE CHANNEL PROGRAM FOR
* READING IN THE DSCB FORMAT 5.
*
BEGIN    EQU   *                        BRANCH LABEL             M0130
         SR    RHOLD,RHOLD              CLEAR HOLD SWITCH        Y02080
         MVC   DS5PTRDS,EDADSMAD        MOVE IN FIRST F5 ADDRESS Y02080
         LR    ALTWKA,EXWKA             SAVE BASE REGISTER       Y02080
         LM    REG6,EXWKA,CHANPRO       PICK UP FIRST 3 CCW'S    Y02080
         ALR   REG6,ALTWKA                                       Y02080
         ALR   REG8,ALTWKA                                       Y02080
         ALR   RA,RCORE                                          Y02080
         STM   REG6,EXWKA,DXCCW1        STORE FIRST 3 CCW'S      Y02080
         LM    REG6,EXWKA,CHANPRO+K24   PICK UP NEXT 3 CCW'S     Y02080
         ALR   REG6,RCORE                                        Y02080
         ALR   REG8,ALTWKA                                       Y02080
         ALR   RA,ALTWKA                                         Y02080
         STM   REG6,EXWKA,DXCCW4        STORE NEXT 3 CCW'S       Y02080
         LM    REG6,EXWKA,CHANPRO+K48   PICK UP NEXT 3 CCW'S     Y02080
         ALR   REG6,RCORE                                        Y02080
         ALR   REG8,ALTWKA                                       Y02080
         ALR   RA,RCORE                                          Y02080
         STM   REG6,EXWKA,DXCCW7        STORE NEXT 3 CCW'S       Y02080
         LM    REG6,REG9,CHANPRO+K72    PICK UP LAST 2 CCW'S     Y02080
         ALR   REG6,RCORE                                        Y02080
         ALR   REG8,ALTWKA                                       Y02080
         STM   REG6,REG9,DXCCW10        STORE LAST 2 CCW'S       Y02080
         LR    EXWKA,ALTWKA                                      Y02080
         LA    REG6,DXCCW9                                       Y02080
         ST    REG6,IOBSIOCC                                     Y02080
READF5   EQU   *                        BRANCH LABEL             Y02080
         MVC   DXDAADDR+K3(K5),DS5PTRDS  INSERT SEEK ADDRESS     Y02080
         LR    REG6,RHOLD               SAVE HOLD SWITCH         Y02080
         EXCP  DXIOB
         WAIT  1,ECB=DXECB
         TM    DXECB,X'20'              TEST FOR PERMANENT I/O ERROR
         BC    8,UNSUCFUL
         LR    RHOLD,REG6               RESTORE HELD QTY SWITCH  Y02080
*
* THIS SECTION CONVERTS THE EXTENTS IN THE FORMAT 5 FROM XXYYZ TO
* RTA - NO. OF TRACKS
*
SETUP    MVC   READIN+44(90),READIN+45 CLOSE UP EXTENTS OVER ID
         SR    RHH,RHH                  CLEAR NUMBER OF TRACKS   Y02080
         LA    RIN,READIN+K4            SET UP INPUT F5 POINTER  Y02080
         LR    ROUT,RIN                 POINTER TO CONVERTED F5  Y02080
         LA    RCNT,K26                 NUMBER OF INPUT EXTENTS  Y02080
TESTZERO EQU   *                        BRANCH LABEL             Y02080
         LH    RTRK,DS4DEVSZ+K2         NUMBER OF TRACKS/CYL     Y02080
         SR    RTA1,RTA1                INITIALIZE FOR DIVISION  Y02080
         CLC   0(K2,RIN),ZEROS          CHECK FOR A ZERO EXTENT  Y02080
         BE    INCRIN                   BRANCH IF ZERO TO UPDATE PTRS.
         MVC   ENEXTADR,0(RIN)          SET XXYYZ ON HALFWORD    Y02080
         LH    RCC,ENEXTADR+K2                                   Y02080
         MH    RCC,DS4DEVSZ+K2          MULTIPLY YY BY TRKS/CYL  Y02080
         LH    RTA,ENEXTADR                                      Y02080
         TM    JFCBCTRI,X'C0'           TEST FOR CYLINDER REQUEST
         BO    CYLRND                   BRANCH IF CYLINDER REQ  SA54497
         TM    JFCBCTRI,JFCBAVR+JFCROUND TEST FOR ROUND REQUEST SA54497
         BNO   ADD                      BRANCH IF NOT ROUND     SA54497
*
* IF REQUEST IS IN CYLINDERS MUST ROUND RTA UP TO CYLINDER BOUNDARY
* IF NECESSARY
*
CYLRND   EQU   *                        BRANCH LABEL            SA54497
         DR    RTA1,RTRK                DIVIDE RTA BY TRKS/CYL   Y02080
         MH    RTA,DS4DEVSZ+K2          FIND NO. OF FULL CYLS    Y02080
         LTR   RTA1,RTA1                TEST IF ON CYL BOUNDARY  Y02080
         BZ    ADDA                     BRANCH IF YES
         AR    RTA,RTRK                 ROUND UP TO NEXT CYL     Y02080
         B     ADDA
ADD      EQU   *                        BRANCH LABEL             Y02080
         IC    RHH,ENEXTADR+K4          PICK UP SURPLUS TRACKS   Y02080
         AR    RCC,RHH                  ADD SURPLUS NO. OF TRKS  Y02080
ADDA     EQU   *                        BRANCH LABEL             Y02080
         STH   RCC,K2(ROUT)             STORE CONVERTED EXTENT   Y02080
         STH   RTA,0(ROUT)                                       Y02080
         LA    ROUT,K4(ROUT)            INCREMENT POINTERS       Y02080
INCRIN   EQU   *                        BRANCH LABEL             Y02080
         LA    RIN,K5(RIN)                                       Y02080
         BCT   RCNT,TESTZERO            GO CONVERT NEXT EXTENT   Y02080
         XC    0(K27,ROUT),0(ROUT)      CLEAR REST OF AREA       Y02080
         LA    RIN,DS5AVEXT             SET UP INPUT F5 POINTER  Y02080
         B     BEGINHRE
*
* THIS SECTION CHECKS FOR INDEX POINTING TO THE 26 TH EXTENT OF A DSCB
* FORMAT 5. IF NOT THE INDEX IS UPDATED TO THE NEXT DADSM ENTRY AND
* THE SEARCH CONTINUES. IF AT THE END A TEST IS MADE TO SEE IF ANOTHER
* FORMAT 5 IS PRESENT. IF SO THE NEW FORMAT 5 IS READ IN AND THE SEARCH
* IS CONTINUED.
*
TESTLOC  EQU   *                        BRANCH LABEL             Y02080
         LA    RA,READIN+K108           TEST FOR LAST EXTENT     Y02080
         CR    RA,RIN                                            Y02080
         BNE   BEGSURCH                 BRANCH IF MORE EXTENTS
         OC    DS5PTRDS,DS5PTRDS        TEST IF ANOTHER F5       Y02080
         BZ    FILLIT                   BRANCH IF NO MORE F5'S   Y02080
         B     READF5                   READ ANOTHER FORMAT 5
*
* I/O ERROR AND EXIT ROUTINES
*
UNSUCFUL EQU   *                        BRANCH LABEL             Y02080
         SR    REG13,REG13              ZERO REGISTER            Y02080
         IC    REG13,DXEXTSW            PICK UP CALLER CODE      Y02080
         LA    REG6,LASTLOAD            POINT TO ID OF IGG0553E  Y02080
XCTLHERE EQU   *                        BRANCH LABEL             Y02080
         IECRES LOAD,EXTPR=(EXWKA),MODID=(REG6),BRANCH=DIRECT    Y02080
*
* THIS SECTION BEGINS THE SEARCH BY TRYING TO FIND AN EXTENT ADJACENT
* TO THE DATA SET
*
BEGSURCH EQU   *                        BRANCH LABEL             Y02080
         LA    RIN,K4(RIN)              UPDATE SEARCH POINTER    Y02080
BEGINHRE EQU   *                        COMPARE THE PREFERRED    Y02080
         CH    RPREF,0(RIN)             RTA TO THE F5 RTA        Y02080
         BNE   CHECKOUT                 BRANCH NOT EQUAL
         CH    REQ,K2(RIN)              TEST IF QTY AVAILABLE    Y02080
         BH    QTNOTAVL                 NOT AVAILABLE
*
* WHEN THERE IS SUFFICIENT SPACE AT THE PREFERRED RTA TO FULFILL
* THE REQUEST, THE PREFERRED EXTENT IS ENTERED IN THE DADSM TABLE.
*
ONEDADSM EQU   *                        BRANCH LABEL             Y02080
         LH    REG6,0(RIN)              ADD REQUESTED QUANTITY   Y02080
         AR    REG6,REQ                 TO BEGINNING RTA         Y02080
         STH   REG6,K2(RIN)                                      Y02080
         MVC   ENTRIES(K4),0(RIN)       SET UP TABLE             Y02080
         MVI   ENTRYNUM,ONEENTRY        ONE DADSM ENTRY          Y02080
         B     BUILDONE
*
* WHEN RTA IS NOT AVAILABLE THE QUANTITY IS CHECKED
*
CHECKOUT EQU   *                        BRANCH LABEL             Y02080
         CH    REQ,K2(RIN)              TEST QUANTITY AVAILABLE  Y02080
         BH    QTNOTAVL                 QUANTITY TOO SMALL
         BL    COMPARE                  QUANTITY TOO LARGE
         CH    RPREF,0(RIN)             COMPARE RTA'S            Y02080
         BNH   ONEDADSM                 BRANCH IF RTA AVAILABLE IS NOT
*                                        LOWER THAN REQUESTED RTA
*
* THIS SECTION COMPARES THE NEW EXTENT WITH THE HELD EXTENT TO
* DETERMINE WHICH EXTENT IS THE CLOSEST IN SIZE TO THE ORIGINAL
* REQUEST.
*
COMPARE  EQU   *                        BRANCH LABEL             Y02080
         LTR   RHOLD,RHOLD              TEST FOR QUANTITY HELD   Y02080
         BZ    NOHOLD                   BRANCH IF NOT HELD
         L     REG6,0(RIN)              COMPARE NEW QTY TO HELD  Y02080
         CLC   K2(K2,RIN),EPDLIST1+K2   IS NEW QUANTITY LOWER    Y02080
         BH    TESTLOC                  BRANCH IF NOT CLOSER TO REQUEST
         ST    REG6,EPDLIST1            IF CLOSER - NEW QUANTITY Y02080
         B     TESTLOC                   BECOMES HELD QUANTITY
*
* NO QUANTITY IS BEING HELD SO THIS ONE IS THE FIRST TO BE EQUAL OR
* GREATER THAN THE REQUEST AND BECOMES THE HELD QUANTITY
*
NOHOLD   EQU   *                        BRANCH LABEL             Y02080
         MVC   EPDLIST1,0(RIN)          SET UP THE HELD QUANTITY Y02080
         LA    RHOLD,K1(RHOLD)          SET HELD QUANTITY SWITCH Y02080
         B     TESTLOC
*
* THIS SECTION IS ENTERED WHEN THE QUANTITY IS LESS THAN THE REQUEST
* FIRST A TEST IS MADE TO CHECK FOR A HELD QUANTITY. IF ONE EXISTS THE
* NEW EXTENT IS DISREGARDED AND A RETURN IS MADE TO EXAMINE THE NEXT
* EXTENT. IF THERE IS NO HELD QUANTITY THE PUSH DOWN LIST MUST BE
* EXAMINED.
*
QTNOTAVL EQU   *                        BRANCH LABEL             Y02080
         LTR   RHOLD,RHOLD              TEST FOR HELD QUANTITY   Y02080
         BP    TESTLOC                  BRANCH IF QUANTITY HELD
CHEKLIST EQU   *                        BRANCH LABEL             Y02080
         L     REG9,0(RIN)              LOAD CONVERTED EXTENT    Y02080
         LH    REG0,K2(RIN)             LOAD NUMBER OF TRACKS    Y02080
         CH    REG0,EPDLIST5+K2         COMPARE PUSHDOWN ENTRY   Y02080
         BNH   TESTLOC                  BRANCH IF NEW QUANTITY LOW
FINDSLOT EQU   *                        BRANCH LABEL             Y02080
         MVC   EPDLIST5,0(RIN)          EXCHANGE NEW QTY WITH    Y02080
*                                       SMALLEST IN LIST         Y02080
         LA    REG6,K4                  SET INDEX DECREMENT      Y02080
         LA    REG7,EPDLIST4            SET LOCATION INDEX       Y02080
COMPTEST EQU   *                        BRANCH LABEL             Y02080
         CH    REG0,K2(REG7)            COMPARE NEXT ENTRY       Y02080
         BNH   TESTLOC                  BRANCH IF NEW QUANTITY LOW
EXCHANGE EQU   *                        BRANCH LABEL             Y02080
         LR    REG8,REG7                                         Y02080
         AR    REG8,REG6                                         Y02080
         MVC   0(K4,REG8),0(REG7)       EXCHANGE ENTRIES         Y02080
         ST    REG9,0(REG7)                                      Y02080
         LA    REG8,EPDLIST1            CHECK IF AT END OF THE   Y02080
         CR    REG8,REG7                PUSH DOWN LIST           Y02080
         BE    TESTLOC                  BRANCH IF END
         SR    REG7,REG6                DECREMENT LOCATION INDEX Y02080
         B     COMPTEST                 GO COMPARE AGAIN         Y02080
*
* TESTS ARE MADE HERE TO DETERMINE IF WITH ONE OR MORE EXTENTS THE
* REQUEST CAN BE FILLED ON THIS VOLUME.
*
FILLIT   EQU   *                        BRANCH LABEL             Y02080
         LTR   RHOLD,RHOLD              TEST FOR HELD QUANTITY   Y02080
         BZ    PICKLIST                 BRANCH IF NO HELD QUANTITY
*
* AN EXTENT IS AVAILABLE THAT IS EQUAL TO OR GREATER THAN REQUEST.  THE
* QUANTITY REQUESTED IS ADDED TO STARTING RTA TO DETERMINE ENDING RTA+1
*
         AH    REQ,EPDLIST1             HELD QTY IS AVAILABLE    Y02080
         STH   REQ,ENTRIES+K2           BUILD DADSM EXTENT TABLE Y02080
         MVC   ENTRIES(K2),EPDLIST1     SET UP TABLE             Y02080
         MVI   ENTRYNUM,ONEENTRY        ONE DADSM ENTRY          Y02080
         B     BUILDONE
*
* THE PUSH DOWN LIST IS EXAMINED FOR UP TO 5 EXTENTS WHICH TOGETHER CAN
* FILL THE REQUEST
*
PICKLIST EQU   *                        BRANCH LABEL             Y02080
         MVC   ENTRIES(K4),EPDLIST1     MOVE IN RTA1/RTA2 OF     Y02080
*                                       THE LARGEST EXTENT       Y02080
         LH    REG6,ENTRIES             CONVERT FIRST ENTRY      Y02080
         AH    REG6,ENTRIES+K2                                   Y02080
         STH   REG6,ENTRIES+K2                                   Y02080
         SR    RCTR,RCTR                INITIALIZE COUNTER       Y02080
         LA    TBLPTR,ENTRIES+K4        POINT TO ADDRESS OF NEXT Y02080
         LA    PDLPTR,EPDLIST2          TABLE ENTRY & PUSH DOWN  Y02080
         LA    RF,K4                    INITIALIZE TO TEST CTR   Y02080
         LH    REG7,EPDLIST1+K2         PICK UP LARGEST QUANTITY Y02080
AGAIN    EQU   *                        BRANCH LABEL             Y02080
         AH    REG7,K2(PDLPTR)          ADD QUANTITY TO SUM      Y02080
         LA    RCTR,K1(RCTR)            INCREMENT COUNTER        Y02080
         CR    REG7,REQ                 COMPARE TO REQUEST       Y02080
         BNL   COMBINEX                 COMBINE WHEN SUFFICIENT
         LH    REG13,0(PDLPTR)          PICK UP RTA              Y02080
         AH    REG13,K2(PDLPTR)         ADD NUMBER OF TRACKS     Y02080
         STH   REG13,K2(PDLPTR)         CALCULATE ENDING RTA+1   Y02080
         MVC   0(K4,TBLPTR),0(PDLPTR)   MOVE EXTENT TO DADSMTBL  Y02080
         CR    RCTR,RF                  TEST CTR FOR 5TH EXTENT  Y02080
         BE    UNSUCFUL                 IF NOT SUFFICIENT - GET OUT
         LA    TBLPTR,K4(TBLPTR)        INCREMENT POINTERS       Y02080
         LA    PDLPTR,K4(PDLPTR)                                 Y02080
         B     AGAIN                    ENTER LOOP AGAIN
COMBINEX BH    SUBEXCES                 IF QUANTITY EXACT RTA'S OF LAST
         LH    REG13,0(PDLPTR)          EXTENT USED ARE MOVED TO Y02080
         AH    REG13,K2(PDLPTR)         THE DADSM EXTENT TABLE   Y02080
         STH   REG13,K2(PDLPTR)                                  Y02080
         MVC   0(K4,TBLPTR),0(PDLPTR)   MOVE EXTENT TO THE       Y02080
         B     SORTLIST                    DADSM TABLE
SUBEXCES EQU   *                        BRANCH LABEL             Y02080
         SR    REG7,REQ                 IF QUANTITY IS IN EXCESS Y02080
         LH    REG13,K2(PDLPTR)         THEN MUST ADJUST         Y02080
         SR    REG13,REG7               ENDING RTA + 1           Y02080
         AH    REG13,0(PDLPTR)                                   Y02080
         STH   REG13,K2(PDLPTR)         MOVE EXTENT USED INTO    Y02080
         MVC   0(K4,TBLPTR),0(PDLPTR)   THE DADSM TABLE          Y02080
SORTLIST EQU   *                        BRANCH LABEL             Y02080
         LA    PDLPTR,ENTRIES           PTR TO DADSMTBL ENTRIES  Y02080
         LA    RCTR,K1(RCTR)            INCREMENT COUNTER        Y02080
         STC   RCTR,ENTRYNUM            NUMBER OF EXTENTS        Y02080
         BCTR  RCTR,K0                  DECREMENT EXT COUNT      Y02080
         LR    REG7,RCTR                NUMBER OF SCANS NEEDED   Y02080
INNERLP  EQU   *                        BRANCH LABEL             Y02080
         LH    REG8,0(PDLPTR)           COMPARE BEGINNING RTA'S  Y02080
         CH    REG8,K4(PDLPTR)          OF THE TWO EXTENTS       Y02080
         BNH   INORDER                  BRANCH IF IN ORDER
         L     REG8,0(PDLPTR)           IF SECOND LARGER MUST    Y02080
         MVC   0(K4,PDLPTR),K4(PDLPTR)  EXCHANGE EXTENTS         Y02080
         ST    REG8,K4(PDLPTR)                                   Y02080
INORDER  EQU   *                        BRANCH LABEL             Y02080
         BCTR  RCTR,K0                  DECREMENT EXT COUNT      Y02080
         LTR   RCTR,RCTR                TEST FOR END OF SCAN     Y02080
         BZ    SCANEND                  BRANCH IF SCAN COMPLETE
         LA    PDLPTR,K4(PDLPTR)        IF NOT, INCR LOCATION    Y02080
         B     INNERLP                     INDICATOR AND CONTINUE
SCANEND  EQU   *                        BRANCH LABEL             Y02080
         BCTR  REG7,K0                  DECREMENT EXT COUNT      Y02080
         LTR   REG7,REG7                TEST FOR END OF SORT     Y02080
         BZ    BUILDONE                 BRANCH IF SORT COMPLETE
         LA    PDLPTR,ENTRIES           IF NOT AT END OF SORT,   Y02080
*                                       THEN RESET THE LOCATION  Y02080
         LR    RCTR,REG7                INDICATOR AND COUNTER    Y02080
         B     INNERLP                  RE-ENTER LOOP
*
* THIS SECTION PREPARES TO BRANCH TO THE NEXT LOAD.
*
BUILDONE EQU   *                        BRANCH LABEL
         LA    REG6,BUILDLD             POINT TO ID OF IGG0553C  Y02080
         B     XCTLHERE
*
* CONSTANTS
*
*
* CHANNEL PROGRAM
*
         DS    0F
CHANPRO  EQU   *
*CCW1
         DC    X'16'                    READ R0
         DC    AL3(COUNT2-EXTNDWKA)     BUFFER ADDR              Y02080
         DC    X'7000'
         DC    H'4'
*CCW2
         DC    X'92'                    READ COUNT - CCHHR
         DC    AL3(0+COUNT2-EXTNDWKA)   BUFFER ADDR              Y02080
         DC    X'6000'
         DC    H'5'
*CCW3
         DC    X'A9'                    SEARCH KEY EQUAL
         DC    AL3(DXCCW4+4-DXLBL)      SEARCH ARG ADDR          Y02080
         DC    X'6000'
         DC    H'4'
*CCW4
         DC    X'08'                    TIC BACK TO READ
         DC    AL3(0+DXCCW2-DXLBL)      TIC ADDR                 Y02080
ZEROS    DC    F'0'
*CCW5
         DC    X'06'                    READ DATA
         DC    AL3(F5DSCB-EXTNDWKA)     BUFFER ADDR              Y02080
         DC    X'7000'
         DC    H'8'
*CCW6
         DC    X'31'                    SEARCH EQUAL I.D.
         DC    AL3(0+COUNT2-EXTNDWKA)   SEARCH ARG ADDR          Y02080
         DC    X'4000'
         DC    H'5'
*CCW7
         DC    X'08'                    TIC BACK TO SEARCH
         DC    AL3(0+DXCCW6-DXLBL)      TIC ADDR                 Y02080
         DC    F'0'
*CCW8
         DC    X'0D'                    WRITE KEY AND DATA
         DC    AL3(0+F5DSCB-EXTNDWKA)   BUFFER ADDR              Y02080
         DC    X'4000'
         DC    H'140'
*CCW9
         DC    X'31'                    SEARCH EQUAL I.D.
         DC    AL3(DXDAADDR+3-DXLBL)    SEARCH ARG ADDR          Y02080
         DC    X'4000'
         DC    H'5'
*CCW10
         DC    X'08'                    TIC BACK TO SEARCH
         DC    AL3(0+DXCCW9-DXLBL)      TIC ADDR                 Y02080
         DC    F'0'
*CCW11
         DC    X'0E'                    READ KEY AND DATA
         DC    AL3(0+F5DSCB-EXTNDWKA)   BUFFER ADDR              Y02080
         DC    H'0'
         DC    H'140'
*
* TABLE OF MODULE IDS AND ENTRY POINT ADDRESSES
*
         XCTLTABL ID=(BUILDLD,3C,LASTLOAD,3E),SVC=055,           Y02080X
               LENGTH=,BRT=YES                                   Y02080
         EJECT                                                   Y02080
WORKAREA IECEXTWA EP,D1=(5)             EXTEND WORK AREA         Y02080
READIN   EQU   F5DSCB                   EQUATE FOR F5DSCB AREA   Y02080
         EJECT                                                   Y02080
EOVWKA   DSECT                                                   Y02080
         IECDSECT
         EJECT                                                   Y02144
         ORG   DXLBL
         IECSDSL1 (4)
         EJECT                                                   Y02080
CVT      DSECT                          CVT DSECT                Y02080
         CVT                                                     Y02080
         END
