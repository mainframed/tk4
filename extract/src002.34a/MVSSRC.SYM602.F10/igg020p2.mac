 TITLE 'IGG020P2 - PARTIAL RELEASE - UPDATE F1,F3'
IGG020P2 CSECT
*          RELEASE 19 DELETIONS                                       *
*1023029100-029200,038200,038600,056000-061400                   O19117
*1023024000,024720                                               A25991
*1023                                                            S19028
*1023045024                                                      M4896
*          RELEASE 20 DELETIONS                                       *
*0224045096,045132                                               A34973
*0224067200                                                      A35327
*          RELEASE 21 DELETIONS                                       *
*0980000210,006000,016600-016800,031800,032200,032800-033000,    A39292
*0980034400,035600,044200,051200,052800,065200,069600,078600-    A39292
*0980079400                                                      A39292
*0980000600-002600,004400-007000,007800,008800,009200-009600,    A37199
*0980010000-010600,011000,011400,012000,012400,012800-013200,    A37199
*0980015000,015400-015800,016200,018600-019000,021680-021920,    A37199
*0980022200,023400,029630,045102,045400,046800,050200-050600,    A37199
*0980060600,063200-063800,073200,073600,076800,077920,079600-    A37199
*0980079900                                                      A37199
*          RELEASE 21.7 DELETIONS
*0000004300-004600,005300-005700,006300-006800,008000-008200,   SA54549
*0000008600,011600,016000,016300,017400,018200,018500,019400,   SA54549
*0000020060-020120,028500-028600,029070,029420-029560,029700-   SA54549
*0000029840,041600-044000,045048,045066,045084,045186-055800,   SA54549
*0000062500,063200-071840,072000,072250-072350,073200-073400,   SA54549
*0000007400,075400-076000,080400-080800                         SA54549
*          VS2 RELEASE 02 DELETIONS/CHANGES
*0000000300,002400-003000,004000,005900-006000,006300-006400,    Y02080
*0000006590,017600,020000,021000,044200,044600,045730,057020-    Y02080
*0000057022,057600-060800,072000-072200,072242,072250-072360,    Y02080
*0000072600,072800-078000                                        Y02080
*0000000030-000150                                               Y02082
*0000                                                            YM4670
*          VS2 RELEASE 03 ADDITIONS
*0000                                                          @ZA07622
*0000                                                          @ZA12266
*MODULE NAME - IGG020P2
*
*DESCRIPTIVE NAME - PARTIAL RELEASE - UPDATE F1,F3
*
*COPYRIGHT - NONE
*
*CHANGE ACTIVITY - SEE DELETIONS/CHANGES FOLLOWING CSECT CARD
*
*STATUS CHANGE LEVEL 011
*
*FUNCTION - THIS MODULE RELEASES ANY UNUSED SPACE THAT WAS
*        ALLOCATED IN THE DATA SET'S FORMAT 3 DSCB.
*
*ENTRIES - THE ENTRY POINT TO THIS MODULE IS IGG020P2. ENTRY IS
*        MADE FROM IGG020P1 VIA A BRANCH.
*
*SUPERVISOR CALLS
*        SVC   EXCP (0) READ/WRITE DISK
*        SVC   WAIT (1) WAIT FOR COMPLETION OF I/O
*
*INPUT - REGISTER 5 CONTAINS THE NUMBER OF EXTENTS IN THE DADSM TABLE.
*        REGISTER 11 POINTS TO THE RELEASE WORK AREA WHICH CONTAINS
*        THE DATA PORTION OF THE FORMAT 4 DSCB, AND THE DADSM EXTENT
*        TABLE, WHOSE ENTRIES REPRESENT THE SPACE BEING RELEASED
*        FROM THE FORMAT 1 DSCB.
*        REGISTER 13 POINTS TO THE I/O SUPPORT WORK AREA, WHICH
*        CONTAINS THE UPDATED FORMAT 1 DSCB.
*
*OUTPUT - THE UPDATED FORMAT 3 IS WRITTEN TO THE VTOC. IF ALL THE
*        EXTENTS IN THE FORMAT 3 WERE RELEASED, A FORMAT 0 DSCB IS
*        WRITTEN OVER THE FORMAT 3 DSCB. REGISTER 11 POINTS TO THE
*        RELEASE WORK AREA WHICH CONTAINS THE DADSM EXTENT TABLE,
*        WHOSE ENTRIES REPRESENT ALL THE SPACE BEING RELEASED
*        FROM THE FORMAT 1 AND FORMAT 3 DSCB'S. REGISTER 13 POINTS
*        TO THE I/O SUPPORT WORK AREA, WHICH CONTAINS THE
*        UPDATED FORMAT 1 DSCB.
*
*STORAGE - PROGRAM CODE CSECT    = LESS THAN 1024 BYTES
*          RELEASE WORK AREA     = 620 BYTES
*          RPS WORK AREA         = 128 BYTES
*          I/O SUPPORT WORK AREA
*
*EXITS -
*        NORMAL - BRANCH TO IGG020D0 WITH NEGATIVE VALUE IN REGISTER 2.
*        ERROR  - IF AN I/O ERROR OCCURRED, BRANCH TO IGG020P3
*                 WITH X'10' ERROR CODE IN REGISTER 2.
*                 BRANCH TO IGG020D0 TO WRITE BACK THE FORMAT 1
*                 WITH ONE OF THE FOLLOWING CODES IN REGISTER 2:
*                 X'04' - UNABLE TO FIND EXTENT IN THE FORMAT 3 DSCB
*                 ALL ZEROS - NO SPACE TO BE RELEASED FROM FORMAT 1
*                         AND/OR FORMAT 3
*
*REGISTER USAGE
R0       EQU   0
ERRETREG EQU   2
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
RXUSRDCB EQU   9                       BASE FOR USERS DCB
RXUCB    EQU   10                      POINTER TO THE UCB
RXWKA    EQU   11                      BASE FOR WORK AREA
RXBASE   EQU   12                      BASE FOR MAIN PROGRAM
RXCLOWKA EQU   13                      BASE FOR CLOSE'S WORK AREA
RLINK    EQU   14                      SUBROUTINE LINK REGISTER
R15      EQU   15
*
*** OTHER EQUATES
*
NOTINF3  EQU   X'04'                    EXTENT NOT FOUND ERROR   A37199
IOERROR  EQU   X'10'                    I/O ERROR CODE           A37199
CC       EQU   X'40'                    CCW COMMAND CHAINING     YM4670
WRITEKD  EQU   X'0D'                    WRITE KEY AND DATA       YM4670
KEYDATA  EQU   X'0E'                    READ KEY AND DATA       SA54549
*                                       COMMAND                 SA54549
F3IND    EQU   X'FF'                    TEST FOR FORMAT 3       SA54549
*
K4       EQU   4                        OFFSET INTO FIELD       SA54549
K7       EQU   7                        OFFSET INTO FIELD       SA54549
*
         BALR  RXBASE,0                LOAD BASE REGISTER
         USING *,RXBASE                 BASE FOR MODULE         SA54549
         USING CVT,R15                      CVT BASE REGISTER.
         USING PRLSEWKA,RXWKA           WORK AREA ADDRESSABILITY Y02080
         USING IHADCB,RXUSRDCB         ESTABLISH BASE FOR USERS DCB
         USING CLOSEWKA,RXCLOWKA       ESTABLISH BASE FOR CLOS WKAREA
*
*
*** THIS SECTION CHECKS IF THERE IS A FORMAT 3 TO BE READ IN
*
CHECKF3  EQU   *                                                SA54549
         MVI   DXCCW3,WRITEKD           PREPARE TO WRITE A F3    YM4670
         MVI   DXCCW3+K4,CC             CHAIN THE WRITE CCW'S TO YM4670X
                                        THE WRITE CHECK CCW'S    YM4670
         TM    OUTCCHHR+K4,F3IND        IS THERE A FORMAT 3     SA54549
         BZ    FIEXT02                  NO, BRANCH TO TEST      SA54549X
                                        IF EXTENTS RELEASED     SA54549
*
*** THIS SECTION MODIFIES THE CHANNEL PROGRAM TO READ THE FORMAT 3 DSCB
*
READFMT3 EQU   *
         LA    R0,DXCCW4                MODIFY CCW PTR IN IOB   SA54549
         ST    R0,IOBSIOCC                                      SA54549
         LA    R0,OUTDSCB               PICK UP ADDRESS OF      SA54549
*                                       WHERE TO READ IN DSCB   SA54549
         ST    R0,DXCCW6                MODIFY READ CCW         SA54549
         MVI   DXCCW6,KEYDATA           READ KEY AND DATA       SA54549
*                                       COMMAND                 SA54549
         LH    R0,DSCBLNTH              MOVE IN LENGTH          SA54549
         ST    R0,DXCCW6+K4             MODIFY FLAGS AND COUNT  SA54549
         BAL   RLINK,EXECIO             READ FMT 3 DSCB         SA54549
*
*** THIS SECTION DETERMINES WHETHER THE EXTENT THAT CONTAINS THE LAST
*   TRACK WRITTEN IS LOCATED IN THE FORMAT 1 OR FORMAT 3 DSCB. IF THE
*   LAST TRACK IS IN THE FORMAT 3 DSCB, IT LOCATES THE PROPER EXTENT.
*
BEGINA   SR    R3,R3                        CLEAR DEB EXT REG.
         LA    R7,OUTDSCB-26                SET UP FMT 3 DSCB SCAN REG.
         LA    R8,OUTDSCB+135               SET UP FMT 3 END PTR.
         IC    R3,USEXTNUM              PICK UP M OF MBBCCHHR    Y02080
         CLI   INCCHHR+4,X'FF'              WAS EXTNT IN FMT 1 DSCB.
         BE    F1EXTGO                      YES
         MH    R3,TEN
         AR    R7,R3                        LOCATE PROPER EXT IN F1.
         CLI   USEXTNUM,K7              IS EXTENT IN KEY OF F3   Y02080
         BL    INKEY                        YES
         LA    R7,1(R7)                     NO- INCL IDENTIFIER BIT.
INKEY    MVI   0(R7),X'81'                  TURN ON CYL ALGNMENT BIT.
         TM    JFCBCTRI,X'C0'           CYLINDER REQUEST?        A37199
         BO    ALIGNED                      BR-IF CYLINDER REQ.
         TM    JFCBCTRI,X'41'           RECORD ROUNDED REQUEST?  A37199
         BO    ALIGNED                      BR-IF REC ROUNDED REQ.
         MVI   0(R7),X'01'                  TURN OFF CYL ALGNMENT BIT.
GETFDAD  MVC   MBBCCHHR+3(4),DCBFDAD+3      INSERT NEW CCHH       18924
ALIGNED  LA    R8,OUTDSCB+135               RESET FMT 3 END PTR.
*
*** THIS SECTION RELEASES ALL UNUSED EXTENTS FROM THE FORMAT 3 DSCB.
*
LSTRACK  CLC   6(4,R7),MBBCCHHR+3           IS THIS END OF EXTENT.
UPDATE   LA    R7,10(R7)                UPDATE SCAN REGISTER     A25991
         BE    CHECKID                      YES-CHECK FOR IDENTIFIER.
         SH    R7,TEN                       RESET SCAN REG.
         CLC   MBBCCHHR+3(4),2(R7)      IS CCHH IN THIS EXTENT.   17305
         BL    NOEXTNT                  BRANCH IF NOT-TO ERROR.
         MVC   HOLD+4(4),6(R7)              SAVE PREVIOUS CCHH.
         EX    0,LSTRACK       CCHH WITHIN EXTENT                 18924
         BH    WITHIN          YES,CONTINUE                       18924
         EX    0,GETFDAD  FALL BACK TO LAST TRK ACT USED          18924
         EX    0,LSTRACK        IS CCHH OF LAST TRK WITHIN       A25991
*                                       EXTENT                   A25991
         BL    NOEXTNT          NO, IT IS BEYOND END OF EXTENT   A25991
         B     UPDATE           BR TO PERFORM ADDITIONAL TESTS   A25991
WITHIN   EQU   *                                                  18924
         MVC   HOLD(4),MBBCCHHR+3           SAVE NEW CCHH OF EXT USED.
         MVC   6(4,R7),MBBCCHHR+3           INSERT NEW CCHH.
* THIS CODE WILL SET EXTENT INDICATOR FOR PROPER BOUNDS
         CLI   5(R7),0                      EXT START ON CYL   @ZA12266
         BNZ   RESETIND                     RESET IND          @ZA12266
         LH    R4,DS4DEVSZ+2                GET TRK/CYL        @ZA12266
         LH    RLINK,8(,R7)                 GET END HH         @ZA12266
         BCTR  R4,0                         SET TO LAST TRK    @ZA12266
         CR    R4,RLINK                     END ON CYL         @ZA12266
         BZ    GO                           CONVERT            @ZA12266
RESETIND MVI   0(R7),X'01'                  RESETIND           @ZA12266
GO       BAL   RLINK,CVTORTA                CONVERT RELSED CCHH TO RTA.
*                                                              @ZA12266
         LH    RLINK,ENTRIES                                      12693
         LA    RLINK,1(RLINK)          INCREMENT FIRST ENTRY      12693
         STH   RLINK,ENTRIES           FOR AVAIL SPACE INDICATION 12693
         B     CHECKID                      CHECK FOR IDENTIFIER FIELD.
CHECKLST CLR   R7,R8                        IS THIS THE LAST EXTENT
         BE    TSTWRITE                     YES-TEST FOR ALTERED FMT 3.
TESTZERO CLI   0(R7),0                      NO-IS NEXT EXTNT ZERO
         BE    TSTWRITE                     YES-TEST FOR ALTERED FMT 3.
         MVC   HOLD(8),2(R7)                GET EXT FOR DADSM UPDATE.
         XC    0(10,R7),0(R7)               CLEAR F1 RELEASED EXTENT.
         BAL   RLINK,CVTORTA                CONVERT EXTEXT TO RTA FOR
CHECKID  CLI   0(R7),X'F3'                  IS THIS IDENTIFIER FLD.
         BNE   CHECKLST                     NO-CHECK FOR LAST EXT.
         LA    R7,1(R7)                     YES-SKIP IDENTIFIER FLD.
         B     CHECKLST                     CHECK FOR LAST EXTENT.
F1EXTGO  EQU   *
         MVC   DXDAADDR+3(5),OUTCCHHR  SET SEARCH ID TO FORMAT 3  14634
         LA    R7,30(R7)               SET SCAN REG TO FIRST EXT  14634
         B     TESTZERO                                           14634
FIEXT02  LTR   R5,R5                   ANY EXTENTS RELEASED       14634
         BNZ   UPDATEF1                 YES, BR TO UPDATE FMT 1 SA54549
         SR    ERRETREG,ERRETREG        CLEAR ERROR CODE        SA54549
         B     WRITF1                   PREPARE TO XCTL TO      SA54549
*                                       IGG020D0                SA54549
*
* * PREPARE FOR ERROR EXIT
*
NOEXTNT  EQU   *                                                 O19117
         LA    ERRETREG,NOTINF3         LOAD ERROR CODE          A37199
         B     WRITF1                                           SA54549
*
*** THIS SECTION SETS UP LINKAGE TO THE CVT ROUTINE,WHICH CONVERTS
*   EXTENTS FROM CCHH TO RTA.
*
CVTORTA  LA    R4,2                         INSERT COUNT
RTACVT   L     R15,CVTPTR
         L     R15,CVTPRLTV                 SET UP BASE  FOR CVT ROUT.
         MVC   MBBCCHHR+3(4),HOLD           MOVE CCHH TO BE CONVERTED.
         LA    R2,MBBCCHHR                  POINT TO CCHH AREA.
CCHHLINK EQU   *                                                 A39292
         STM   R9,R15,SAVEREGS          SAVE VOLATILE REGISTERS  A39292
         NI    CONVCON+1,0              CLEAR CONSTANT FOR FLOP.
         LR    R3,RXWKA                 SAVE WORKAREA REGISTER   A39292
         LA    R1,DXDEB                     PICK UP DEB PTR.
CVTLINK1 BALR  RLINK,R15                    CONVERT CCHH TO RTA.
         LM    RXWKA,RXBASE,8+SAVEREGS-FIRST(R3)  RESTORE RLSE   A39292
*                                       WORKAREA AND BASE REGS   A39292
         SRL   R0,16
         AH    R0,CONVCON
         STH   R0,0(R6)                     STORE RTA INTO DADSNTBL.
         LA    R6,2(R6)                     INCREMENT DADSMTBL PTR.
         XI    CONVCON+1,1                  FLIPFLOP RTA INCREMENT.
         BCT   R4,FLIPFLOP                  CONTINUE IF COUNT NOT ZERO.
         LM    R9,R15,SAVEREGS-FIRST(R3)  RESTORE VOLATILE REGS  A39292
         BR    RLINK                        RETURN.
FLIPFLOP EQU   *
         MVC   MBBCCHHR+3(4),HOLD+4         PICK UP NEXT EXTENT.
         LA    R5,1(R5)                     INCREMENT ENTRY NO IN TBL.
         LA    R7,10(R7)                    UPDATE F1 EXTENT PTR.
         L     R15,24+SAVEREGS-FIRST(R3)  RESTORE CVT POINTER    A39292
         B     CVTLINK1                     LOOP TO CONVERT NEXT EXTNT
*
*** THIS SECTION MODIFIES THE CHANNEL PROGRAM TO WRITE A ZERO RECORD
*   WHENEVER ALL EXTENTS ARE RELEASED FROM THE FORMAT 3 DSCB.
*
WRITEZRO TM    OUTCCHHR+4,X'FF'  IS THERE A FMT 3 DSCB            22833
         BZ    TESTCHGE  NO-TEST FOR ALTERED F1                   22833
         LA    R0,DXCCW2+4                  YES-SET UP CHNL PRG WRT 0.
         ST    R0,DXCCW3                    MODIFY WRITE AREA ADDR.
         MVI   DXCCW3,X'0D'                 AND OP CODE.
         MVI   DXCCW3+4,X'60'               TURN ON S.I.L.I. FLAG.
         MVI   DXCCW3+7,X'04'               MODIFY COUNT.
         LH    R1,HOLENUM               FETCH HOLE COUNT         O19117
         LA    R1,1(R1)
         STH   R1,HOLENUM               STORE UPDATED HOLE COUNT O19117
*                                           TO REFLECT THE DELETION
*                                           OF A FORMAT 3 DSCB.
         B     WRTDSCB                      WRITE FMT 0 OVER FMT 3 DSC
TSTWRITE CLI   INCCHHR+4,X'FF'              WAS LAST EXT USED IN FMT 1.
         BE    WRITEZRO                     CLEAR FMT 3 DSCB AREA .
TESTCHGE LTR   R5,R5                        WAS EXTNT FOUND.
         BZ    NOEXTNT                      NO-ERROR.
WRTDSCB  LA    R0,DXCCW1                    MODIFY CCW PTR IN IOB.
         ST    R0,IOBSIOCC
         MVI   DXCCW6+4,X'10'               TURN ON SKIP FLAG.
         BAL   RLINK,EXECIO             *** WRITE DSCB.
         MVI   DXCCW3+4,X'40'               RESTORE CMND CHN.
         MVI   DXCCW3+7,X'8C'               RESTORE COUNT.
*
*** THIS SECTION UPDATES THE FORMAT 1 DSCB WITH THE NUMBER OF
*   EXTENTS REMAINING ON THE VOLUME FOR THIS DATA SET.
*
UPDATEF1 EQU   *                        UPDATE FORMAT 1 DSCB    SA54549
         L     R9,DCBSAVE               RESTORE USER'S DCB PTR   Y02080
         STC   R5,EXTNUM                    SAVE EXT NO IN DADSMTBL.
         XR    R1,R1                                             S19028
         XR    R3,R3                                             S19028
         IC    R1,DSCNOEXT              GET NUM OF EXTENTS       S19028
         IC    R3,USEXTNUM              PICK UP M OF MBBCCHHR    Y02080
         CLI   DSCEXTYP,X'40'      TEST,BR IF THERE IS A          22833
         BE    ULABELS             USER LABEL EXTENT              22833
         LA    R3,1(R3)                     GET NO OF EXT LEFT IN DSCB
ULABELS  EQU   *                                                  22833
         STC   R3,DSCNOEXT                  INSERT NEW NUMBER OF EXTS.
*
*** THIS SECTION CHECKS IF SMF IS SPECIFIED
*
         SR    R1,R3                    NUM OF EXTNTS RELEASED   S19028
         SLL   R1,24                    FOR SMF                  S19028
         L     R2,CVTPTR                GET CVT ADDR             S19028
         L     R2,0(,R2)                GET ADDR OF TCB ADDR     M4896
         L     R2,4(R2)                 GET TCB ADDR             S19028
         L     R2,164(R2)               GET TCT ADDR             S19028
         LA    R2,0(R2)                 CLEAR HI-ORD           @ZA07622
         LTR   R2,R2                    TEST FOR SMF SYSTEM      S19028
         BZ    NEXTLOAD                 BRANCH IF NOT SMF       SA54549
         L     R2,12(R2)                GET TCTIOT ADDR          S19028
         LTR   R3,R2                    TEST FOR TCTIOT          S19028
         BZ    NEXTLOAD                 BRANCH IF NOT TCTIOT    SA54549
         LA    R3,8(R3)                 ADDR OF TCTIOT LOOKUP    S19028
         TM    PTYPEFLG,FROMCR          TEST IF ENTRY FROM C/R   Y02082
         BO    NXTEXT                   BRANCH IF FROM CHKPT/RST Y02082
         MODESET EXTKEY=ZERO            SWITCH TO KEY ZERO       Y02082
NXTEXT   OC    0(4,R3),0(R3)            TEST FOR TABLE END       S19028
         BZ    NEXTLOAD                 BRANCH IF END           SA54549
         CLC   0(2,R3),DCBTIOT          CHECK FOR CORRECT ENTRY  S19028
         BE    CORRECT                  IF CORRECT ENTRY,        A34973
*                                       CONTINUE                 A34973
         LA    R3,4(R3)                 IF NOT POINT TO NEXT     A34973
*                                       ENTRY                    A34973
         B     NXTEXT                   B TO TEST NEXT ENTRY     A34973
CORRECT  EQU   *                                                 A34973
         LH    R3,2(R3)                 GET TCTIOT DISPLACEMENT  S19028
         AR    R2,R3                    GET ENTRY ADDR           S19028
         XR    R3,R3                                             S19028
         IC    R3,2(R2)                 GET NUM OF UCB ENTRIES   S19028
         SLL   R3,3                     MULT BY ENTRY SIZE OF 8  A34973
         LA    R2,4(R2,R3)              GET ADDR OF SPACE RLSD   S19028
*                                       FIELD IN THE TCTIOT
         LA    R3,ENTRIES               PTR TO FIRST DADSM ENTRY S19028
NEXTRTA  AH    R1,2(R3)                 ADD RTA+1                S19028
         SH    R1,0(R3)                 SUBTRACT RTA             S19028
         LA    R3,4(R3)                 INCREMENT DADSM PTR      S19028
         BCT   R5,NEXTRTA               TEST FOR LAST EXTENT     S19028
         ST    R1,0(R2)                 STORE DATA IN TCTIOT     S19028
         TM    PTYPEFLG,FROMCR          TEST IF ENTRY FROM C/R   Y02082
         BO    NEXTLOAD                 BRANCH IF FROM CHKPT/RST Y02082
         MODESET EXTKEY=DATAMGT         RETURN TO DATAMGT KEY    Y02082
*
*** THIS SECTION SETS UP TO BRANCH TO NEXT LOAD OF PARTIAL RELEASE.
*
NEXTLOAD EQU   *
         LNR   ERRETREG,RXBASE          LOAD NEGATIVE VALUE     SA54549
WRITF1   EQU   *                                                SA54549
         LA    R3,NEXTXCTL              FETCH NEXT LOAD POINTER  O19117
*
*** THIS SECTION BRANCHES TO ANOTHER LOAD OF PARTIAL RELEASE.
*
XCTL     EQU   *                                                 O19117
         IECRES LOAD,EXTPR=(RXWKA),MODID=(R3),BRANCH=DIRECT      Y02080
*
*** THIS SECTION INITIATES THE CHANNEL PROGRAM.
*
EXECIO   MVI   DXECB,0                      TURN OFF ECB FLAGS.
         EXCP  DXIOB                        READ DSCB
         WAIT  ECB=DXECB                    WAIT FOR COMPLETION.
         TM    DXECB,X'20'                  TEST FOR PERM I/O ERR.
         BCR   5,RLINK                      RETURN IF NO I/O ERR.
         LA    ERRETREG,IOERROR         LOAD ERROR CODE          A37199
LEAVE    EQU   *                                                SA54549
         LA    R3,PRECLOSE              POINT TO LAST LOAD ID   SA54549
         B     XCTL                                             SA54549
*
*** CONSTANTS
*
TEN      DC    H'10'                                             A37199
DSCBLNTH DC    H'140'                   LENGTH FOR READ KEY AND SA54549
*                                       DATA COMMAND            SA54549
*
*** TABLE OF MODULE IDS AND ENTRY POINT ADDRESSES
*
         XCTLTABL ID=(NEXTXCTL,D0,PRECLOSE,P3),SVC=020,          Y02080X
               LENGTH=,BRT=YES                                   Y02080
         EJECT                                                  SA54549
*
*** CVT DSECT
*
CVT      DSECT
         CVT                            CVT DSECT                Y02080
         EJECT                                                  SA54549
*
*** RELEASE WORK AREA
*
WORKAREA IECPRLWA EP,F4                 RELEASE WORK AREA        Y02080
SAVEREGS EQU   IECREGSV+12              REGISTER SAVE AREA       Y02080
         EJECT                                                  SA54549
*
*** I/O SUPPORT WORK AREA
*
CLOSEWKA DSECT
         IECDSECT
         EJECT                                                  SA54549
         DCBD  DSORG=(PS)
         END
