         TITLE 'IGG019EK - RPS SIO, PGFX, CE, AND XE APPENDAGES' Y02080
IGG019EK CSECT
*0000216000,258000,582000                                        M5885
*0000426000                                                      M4741
*0000336000                                                      M6287
*          VS2 RELEASE 02 DELETIONS/CHANGES
*0000000600-004000,272000,278400,279200-281000,340200,341200,    Y02080
*0000634090,634450-634540,634990,635530-635620,635710-635800,    Y02080
*0000635890,644000-656000,692000-716000,762000-786000            Y02080
*0000278800-282000,286000,340400-341000,634020-635920            YM3048
*0000                                                           YA03681
*
***********************************************************************
*                                                                     *
*                                                                     *
*                  - - - APPENDAGES - - -                             *
*                                                                     *
* MODULE NAME     - IGG019EK                                          *
*                                                                     *
* DESCRIPTIVE NAME- RPS SIO, CE, AND XE APPENDAGES                    *
*                                                                     *
* COPYRIGHT       - NONE                                              *
*                                                                     *
* CHANGE ACTIVITY - SEE DELETIONS/CHANGES FOLLOWING THE CSECT CARD.   *
*                                                                     *
* ATTRIBUTES      - REENTRANT, REFRESHABLE, DISABLED, SUPERVISORY     *
*                                                                     *
* ENTRY POINTS    - IGG019EK: RPS SIO APPENDAGE                       *
*                 - IGG019EK+8: RPS CHANNEL END APPENDAGE             *
*                 - IGG019EK+12: RPS ABNORMAL END APPENDAGE           *
*                                                                     *
* NORMAL EXITS    - RETURN TO IOS VIA A BR 14 INSTRUCTION             *
*                                                                     *
* ERROR EXITS     - NONE                                              *
*                                                                     *
* SVCS ISSUED     - NONE                                              *
*                                                                     *
* INPUT PARAMETERS- THE REGISTER SETTINGS WILL BE AS SPECIFIED IN     *
*                   THE SYSTEM PROGRAMMER'S GUIDE (FORM C28-6550)     *
*                   UNDER THE HEADING OF 'APPENDAGES'. THE ADDRESS    *
*                   OF THE USER'S CHANNEL PROGRAM WILL BE OBTAIN-     *
*                   ED BY REFERENCING THE IOBSTART FIELD OF THE       *
*                   IOB ADDRESSED BY REGISTER 2. THE RPS WORK         *
*                   AREA WILL BE LOCATED BY REFERENCING THE DEB-      *
*                   APPAD FIELD OF THE DEB ADDRESSED BY REGISTER      *
*                   3. THE UCB WILL BE ADDRESSED USING THE CON-       *
*                   TENTS OF REGISTER 7.                              *
*                                                                     *
* FUNCTIONS       -                                                   *
*                                                                     *
*        RPS SIO APPENDAGE:                                           *
*                                                                     *
*        1. IS ENTERED FROM IOS JUST PRIOR TO ISSUING THE SIO IN-     *
*        STRUCTION,                                                   *
*        2. EFFECTIVELY INSERTS SET SECTOR CCWS AT THE START OF,      *
*        AND AT APPROPRIATE POINTS WITHIN THE USER'S CHANNEL PRO-     *
*        GRAM,                                                        *
*        3. INTERFACES WITH A RESIDENT 'R-TO-THETA CONVERT ROU-       *
*        TINE' TO OBTAIN APPROPRIATE THETA VALUES,                    *
*        4. PLACES POINTERS TO THE RPS CHANNEL END AND ABNORMAL       *
*        END APPENDAGES IN THE AVT IN RPS'S WORK AREA TO SCHEDULE     *
*        THESE ROUTINES FOR EXECUTION AFTER I/O HAS BEEN PERFORMED.   *
*                                                                     *
*        RPS ABNORMAL END APPENDAGE:                                  *
*                                                                     *
*        1. IS ENTERED FROM IOS A) BEFORE ATTEMPTING ERROR CORREC-    *
*        TION WHEN AN I/O ERROR IS DETECTED OR B) AFTER A PERMA-      *
*        NENT I/O ERROR IS ENCOUNTERED,                               *
*        2. TESTS THE ECB POINTED TO BY THE IOB TO DETERMINE IF       *
*        THE ENTRY WAS DUE TO CASE A OR B,                            *
*        3. IF CASE A (CC=X'7F') RETURNS IMMEDIATELY TO IOS,          *
*        4. IF CASE B (CC=X'4X') FALLS THROUGH TO THE RPS CHANNEL     *
*        END APPENDAGE.                                               *
*                                                                     *
*        RPS CHANNEL END APPENDAGE:                                   *
*                                                                     *
*        1. IS ENTERED A) FROM IOS UPON NORMAL COMPLETION OF AN I/O   *
*        OPERATION OR B) FROM THE RPS ABNORMAL END APPENDAGE,         *
*        2. RESTORES THE USERS CHANNEL PROGRAM TO ITS STATE PRIOR     *
*        TO RPS START I/O APPENDAGE EXECUTION,                        *
*        3. PLACES POINTERS TO A BR 14 INSTRUCTION IN THE AVT IN      *
*        RPS'S WORK AREA SO THAT THE RPS CHANNEL END AND ABNORMAL     *
*        END APPENDAGES WILL NOT BE REEXECUTED UNTIL AFTER THE        *
*        RPS SIO APPENDAGE HAS PERFORMED ITS FUNCTION.                *
*                                                                     *
***********************************************************************
*
***********************************************************************
*    REGISTER AND OTHER EQUATES                                       *
**********************************************************************
*
FGMR1    EQU   0                        USED IN FREE-& GETMAINS
REG02    EQU   2                        NOT USED
REG09    EQU   9                        NOT USED
REG10    EQU   10                       NOT USED
REG11    EQU   11                       NOT USED
REG12    EQU   12                       NOT USED
RETRG    EQU   14                       RETURN REGISTER
BASER    EQU   15                       BASE REG. FOR THIS MODULE
C0       EQU   0                        CONSTANT NUMBER
C4       EQU   4                        CONSTANT NUMBER
C8       EQU   8                        CONSTANT NUMBER
REG13    EQU   13                       CE APPG. WORK REG.
OAVT     EQU   120                      DISP. TO PTR TO OLD AVT IN W.A.
RSADS    EQU   96                       DISP. TO REG SAVE AREA
REG06    EQU   6                        WORK REG FOR SIO
PARM0    EQU   0                        PARAMETER REGISTER
PARM2    EQU   2                        PARAMETER REG. FOR CONVERT
IOBAD    EQU   2                        POINTER TO IOB
DEBAD    EQU   3                        POINTER TO DEB
HITIC    EQU   5                        ADDR OF HIGHEST TIC
USRCCW   EQU   7                        USER CHAN.PGM.PTR
UCBAD    EQU   7                        POINTER TO UCB
CVTPTR   EQU   16                       ADDR OF POINTER TO CVT
RPCCW    EQU   13                       RPS CHAN.PGM. PTR
REG14    EQU   14                       WORK REG.
REG15    EQU   15                       WORK REG.
ECBDIS   EQU   4                        DISP. OF ECB IN IOB
FW       EQU   4                        BYTES IN 1 FULL WORD
RDS      EQU   4                        DISP TO R IN CCHHR
LGTH     EQU   22                       DISP TO BYTE COUNT
DW       EQU   8                        BYTES IN ONE DOUBLE WD.
DCCHN    EQU   X'C0'                    DATA AND COMMAND CHAIN   M5885
*                                       BITS                     M5885
TFW      EQU   12                       BYTES IN 3 FULL WORDS
SAVWD1   EQU   12                       2ND WORD OF 1ST TIC
TDW      EQU   16                       BYTES IN 2 DOUBLE WORDS
IOBST    EQU   16                       DISP. OF IOBSTART FIELD
D17      EQU   17                       DISPLACEMENT OF 17
D18      EQU   18                       DISPLACEMENT OF 18
D19      EQU   19                       DISPLACEMENT OF 19
SAVWD2   EQU   20                       2ND WORD OF 2ND TIC
TH1DS    EQU   20                       DISP TO 1ST THETA
DAMSK    EQU   X'20'                    MASK FOR D.A. BIT IN UCB
SSCODE   EQU   X'23'                    SET SECTOR COMMAND CODE
CHPGMDS  EQU   24                       DISP TO CH. PGM. IN RPSWA
SHFT3    EQU   24                       BITS FOR THREE-BYTE SHIFT
SIDECD   EQU   X'31'                    SIDE COMMAND CODE
COMP     EQU   X'7F'                    ECB COMPLETION CODE
SCRDS    EQU   232                      DISP TO CONVERT ADDR
BIT256   EQU   X'01'                    BIT WITH VALUE OF 256
ADRMSK   EQU   X'03'                    MASK FOR LOW TWO BITS
TICCW    EQU   X'08'                    COMMAND CODE OF TIC
RPSMSK   EQU   X'10'                    MASK FOR RPS BIT IN UCB
SECSA    EQU   RSADS+12                 S. A. FOR SECOND 3 REG.S
*
***********************************************************************
* THIS SECTION TESTS ENTRY CONDITIONS AND INITIALIZES REGISTERS       *
***********************************************************************
*
         USING DEB,DEBAD
         USING IGG019EK,BASER
         B     SIO                      BRANCH AROUND I.D.
         USING IGG019EK+C4,BASER        CE ADDRESSABILITY        YM3048
         B     CE                       BRANCH TO CHAN END APPG
         USING IGG019EK+C8,BASER        XE ADDRESSABILITY        YM3048
XE       EQU   *
*
***********************************************************************
*        ABNORMAL END APPENDAGE                                       *
***********************************************************************
*
         TM    ECBDIS(IOBAD),COMP       TEST COMPLETION CODE
         BO    C0(RETRG)                7F, RETURN TO IOS
*
***********************************************************************
*        CHANNEL END APPENDAGE                                        *
***********************************************************************
*
CE       EQU   *
         BALR  BASER,FGMR1              RE ESTABLISH BASE REG
         USING *,BASER                  USING FOR HERE
         L     REG10,IOBST(IOBAD)       GET START OF USER CCW'S
         LA    REG10,C0(REG10)          CLEAR HIGH ORDER BYTE
NOADD    EQU   *
         CLI   C0(REG10),TICCW          IS CCW A 'TIC'
         BE    IFTIC                    YES,INCREMENT HITIC
         CLI   C0(REG10),SSCODE         IS CCW A 'SET SECTOR'
         BE    IFSS                     YES, MUST BE REMOVED
         TM    C4(REG10),DCCHN          DATA/COMMAND CHN. BITS ON
         BZ    ENDCE                    BRANCH NO                M6287
         AH    REG10,ONEDW              STEP TO NEXT CCW
         B     NOADD                    SKIP INCREMENT
IFTIC    EQU   *
         L     REG12,C0(REG10)          GET ADDRESS IN TIC
         LA    REG12,C0(REG12)          CLEAR COMMAND CODE
         CR    REG12,REG10              IS IT A TIC *-8
         BH    FOLLOW                   NO, GO FOLLOW THIS TIC
         AH    REG10,ONEDW              STEP TO NEXT CCW
         B     NOADD                    GO ON TO NEXT CCW TEST
FOLLOW   EQU   *
         L     REG10,C0(REG10)          LOOK AT NEXT CCW
         B     NOADD                    GO ON TO NEXT CCW TEST
IFSS     EQU   *
         L     REG11,C8(REG10)          TIC POINTS TO ORIG. CCW
         MVC   C0(C8,REG10),C0(REG11)      ORIGINAL CCW RESTORED
         ST    REG10,C8(REG10)          YES, POINT TIC TO TIC-8
         MVI   C8(REG10),TICCW          INSERT COMMAND CODE
         AH    REG10,TWODW              INCREMENT THRU USER CCWS
         AH    REG11,DCTDWDW            INCREMENT THRU RPS  CCWS
         B     NOADD                    GO ON TO NEXT CCW
ENDCE    EQU   *
         L     REG10,DEBAPPAD           GET PTR TO RPSWA
         L     REG13,OAVT(REG10)        GET ADDR OF OLD AVT
         MVC   TFW(C4,REG10),TFW(REG13) RESTORE OLD CE PTR
         MVC   TDW(C4,REG10),TDW(REG13) RESTORE OLD XE PTR
         BR    RETRG                    RETURN TO IOS
*
***********************************************************************
*        START I/O APPENDAGE                                          *
***********************************************************************
*
SIO      EQU   *
         USING IGG019EK,BASER
         TM    D18(UCBAD),DAMSK
         BZ    C0(RETRG)                NO, RETURN TO IOS
         TM    D17(UCBAD),RPSMSK
         BZ    C0(RETRG)                NO, RETURN TO IOS
         L     RPCCW,DEBAPPAD           POINTER TO RPS WORK AREA
         STM   PARM0,REG02,RSADS(RPCCW) SAVE REGISTERS
         STM   HITIC,UCBAD,SECSA(RPCCW) SAVE REGISTERS
         IC    REG06,D19(UCBAD)
         SLL   REG06,SHFT3              PUT IN HIGH ORDER BYTE
         L     USRCCW,IOBST(IOBAD)      GET START OF USER'S CCWS
         LA    USRCCW,0(USRCCW)         CLEAR THE HIGH BYTE     YA03681
         CLI   C0(USRCCW),SSCODE        1ST CCW A SET SECTOR
         BE    RPSEND                   YES,RETURN TO IOS        M4741
         LA    PARM2,TH1DS(RPCCW)       POINTER TO FIRST THETA
         OR    PARM2,REG06              PUT DEV. CODE IN PARM.REG.
         LA    RPCCW,CHPGMDS(RPCCW)     POINTER TO RPS CCW CHAIN
         O     RPCCW,TICCW2             PUT X'08' INTO HIGH BYTE
         XR    HITIC,HITIC              CLEAR HIGH TIC ADDR REG.
*
***********************************************************************
* MODIFY THE CCW CHAINS                                               *
***********************************************************************
*
         CLI   C0(USRCCW),SIDECD        1ST CCW A SEARCH ON I.D.
         BNE   INCREMNT                 NO, SKIP THETA CONVERT
MODIFY   EQU   *
         MVC   C0(TDW,RPCCW),C0(USRCCW) MOVE 2 CCWS TO RPS CHAIN
         L     REG06,C0(USRCCW)         GET SIDE'S DATA ADDRESS
SCRPREP  EQU   *
*
* THE FOLLOWING INSTRUCTION TESTS THE BYTE COUNT OF THE CCW FOLLOWING
* THE SIDE AND TIC IN ORDER TO DETERMINE THE KEY AND DATA LENGTH TO
* BE PASSED TO THE SECTOR CONVERT ROUTINE. CATALOG'S VOLUME CONTROL
* BLOCKS AND INDICES HAVE DATA LENGTHS OF 256, ALL OTHER I/O OPERATIONS
* IN CATALOG AND DADSM ARE PERFORMED ON DSCB'S HAVING A MAXIMUM I/O
* BYTE COUNT OF 148 (96 BYTES DATA, 44 BYTES KEY, AND 8 BYTES COUNT)
*
         TM    LGTH(USRCCW),BIT256      BYTE COUNT = 256 OR MORE
         BO    LONGBLK                  YES, PASS INDEX/VCB DD&K
         L     PARM0,DSCBCT             NO, PASS DSCB'S DD & K
         B     GETR                     SKIP LONGBLK
LONGBLK  EQU   *
         L     PARM0,IVCBCT             INDEX & VOL.CONT.BLK DD&K
GETR     EQU   *
         IC    PARM0,RDS(REG06)         PUT R INTO PARAMETER REG.
         ST    RETRG,SAVWD1(RPCCW)      SAVE RETURN AND BASE AD-
         ST    BASER,SAVWD2(RPCCW)      *DRESSES IN TICS
         L     REG15,CVTPTR             CVT ADDRESS
         L     REG15,SCRDS(REG15)       SECTOR CONVERT RTN ADDR
         BALR  REG14,REG15              CONVERT R TO THETA
         L     RETRG,SAVWD1(RPCCW)      RESTORE
         L     BASER,SAVWD2(RPCCW)      REGISTERS
         ST    PARM2,C0(USRCCW)         POINT SET SECTOR TO THETA
         MVI   C0(USRCCW),SSCODE        SET SECTOR COMMAND CODE
         MVC   FW(FW,USRCCW),SSFLGL     SET FLGS & LENGTH FOR SS
         ST    RPCCW,DW(USRCCW)         BUILD TIC TO RPS CHAIN
         CLI   DW(RPCCW),TICCW          IS NEXT RPCCW A TIC
         BNE   NEXTTIC                  NO, SKIP WORK ON THIS TIC
         O     USRCCW,TICCW2            PUT X'08' IN HI BYTE
         ST    RPCCW,DW(RPCCW)          POINT RPCCW+8 TO RPCCW
NEXTTIC  EQU   *
         AH    USRCCW,TWODW             POINT TO CCW AFTER TIC
         ST    USRCCW,TDW(RPCCW)        BUILD TIC TO USER'S CHPGM
         CLI   C0(USRCCW),TICCW         IS NEXT CCW A TIC
         BNE   INCREMNT                 NO, SKIP TIC ADJUSTMENT
         MVC   TDW(C4,RPCCW),C0(USRCCW) POINT RPS TIC TO NEW ADDR
INCREMNT EQU   *
         STC   PARM2,TFW(RPCCW)         GET LOW BYTE OF THETA ADR
         TM    TFW(RPCCW),ADRMSK        HAVE 3 THETA'S BEEN USED
         BO    FINISH                   YES, FINISH OFF & RETURN
         AH    RPCCW,DCTDWDW            STEP RPS CCW POINTER
         AH    PARM2,ONE                STEP THETA'S POINTER
         TM    FW(USRCCW),DCCHN         MORE CCWS                M6287
         BZ    FINISH                   BRANCH NO                M6287
USRSTEP  EQU   *
         AH    USRCCW,ONEDW             STEP USER CCW POINTER
*
***********************************************************************
* MAKE TESTS TO DECIDE NEXT ACTION: MODIFY CHANNEL PROGRAM AT THIS    *
* POINT, LOOK AT NEXT CCW, OR RETURN ?                                *
***********************************************************************
*
         CLI   C0(USRCCW),TICCW         IS CCW A TIC
         BNE   NOTTIC                   NO, SKIP TIC WORK
         L     REG06,C0(USRCCW)         GET TIC DATA ADDRESS
         CR    HITIC,REG06              ADDR GREATER THAN HITIC
         BH    USRSTEP                  NO, LOOK AT NEXT CCW
         LR    HITIC,REG06              UPDATE HITIC
         B     USRSTEP                  NOW LOOK AT NEXT CCW
NOTTIC   EQU   *
         CLI   C0(USRCCW),SIDECD        IS CCW A SEARCH ON I.D. ?
         BE    MODIFY                   YES, MODIFY CHANNEL PGM.
         TM    FW(USRCCW),DCCHN         CMD. OR DATA CHAIN ON    M5885
         BNZ   USRSTEP                  YES, LOOK AT NEXT CCW
FINISH   EQU   *
         L     REG10,DEBAPPAD           GET ADDR OF RPSWA
         LM    PARM0,REG02,RSADS(REG10) LOAD REGISTERS
         LM    HITIC,UCBAD,SECSA(REG10) LOAD REGISTERS
         XR    REG09,REG09              ZERO REG .
         L     REG11,FW(REG10)          GET ADDR OF THIS APPENDAGE
         LA    REG11,FW(REG11)          GET ADDR OF CE APPENDAGE YM3048
         ST    REG11,TFW(REG10)         PUT INTO NEW AVT
         LA    REG11,FW(REG11)          GET ADDR OF XE APPENDAGE
         ST    REG11,TDW(REG10)         PUT INTO NEW AVT
         BR    RETRG                    RETURN TO IOS            M4741
RPSEND   LM    HITIC,UCBAD,SECSA(RPCCW) LOAD REGISTERS           M4741
         BR    RETRG                    RETURN TO IOS
*
***********************************************************************
*        CONSTANTS                                                    *
***********************************************************************
*
         DS    0F
TICCW2   DC    X'08000000'              COMMAND CODE OF TIC
SSFLGL   DC    X'60000001'              FLAG & LENGTH FOR SET SECT.
DSCBCT   DC    X'00602C00'              DSCB DD & K
IVCBCT   DC    X'01000800'              INDEX & VCB DD & K
ONE      DC    H'1'                     ONE
ONEDW    DC    H'8'                     EIGHT
TWODW    DC    H'16'                    SIXTEEN
DCTDWDW  DC    H'24'                    TWENTY-FOUR
*
***********************************************************************
*        END OF APPENDAGES DELIMITER                                  *
***********************************************************************
*
         XCTLTABL BRT=YES,PATCH=0,LENGTH=                        Y02080
         EJECT                                                   Y02080
*
*        WORK AREA'S DEFINITIONS                                      *
***********************************************************************
*
*** DEB AREA DEFINITION
         DSECT                                                   Y02080
DEB      DS    0F                       START OF DEB
DEBTCBAD DS    F                        TCB ADDRESS
DEBAMIND DS    0X                       AM INDICATOR
DEBDEBAD DS    F                        NEXT DEB ADDRESS
         DS    2F                       IRB AND SYS PURGE CHAIN
DEBNMEXT DS    0X                       NO. OF EXTENTS
         DS    2F                       USER PURGE & PURGE ECB ADDR
DEBDCBAD DS    F                        DCB ADDRESS
DEBAPPAD DS    F                        APPENDAGE ADDRESS
DEBDVMOD DS    0X                       DEVICE MODIFIER MASK
DEBUCBAD DS    F                        UCB ADDRESS
DEBBINUM DS    H                        BIN NUMBER
DEBSTRCC DS    H                        START CYLINDER
DEBSTRHH DS    H                        START HEAD
DEBENDCC DS    H                        END CYLINDER
DEBENDHH DS    H                        END HEAD
DEBNMTRK DS    H                        NUMBER OF TRACKS
*
*** RPS WORK AREA DEFINITION
RPSWA    DSECT
RPSWAST  DS    0D                       START OF WORK AREA
RPEOEPTR DS    F                        EOE APG. POINTER
RPSIOPTR DS    F                        SIO APG. POINTER
RPPCIPTR DS    F                        PCI APG. POINTER
RPCEPTR  DS    F                        CE APG. POINTER
RPXEPTR  DS    F                        XE  APG. POINTER
THETA1   DS    CL1                      THETA VALUE 1
THETA2   DS    CL1                      THETA VALUE 2
THETA3   DS    CL1                      THETA VALUE 3
RPSWASIZ DS    CL1                      SIZE/8 OF RPS WORK AREA
RPCCW1   DS    D                        1ST USER CCW
RPCCW2   DS    D                        2ND USER CCW
RPCCW3   DS    D                        TIC BACK TO USER'S C.P.
RPCCW4   DS    D                        1ST EMBEDDED USER SIDE
RPCCW5   DS    D                        USER CCW FOLLOWING SIDE
RPCCW6   DS    D                        TIC BACK TO USER'S C.P.
RPCCW7   DS    D                        2ND EMBEDDED USER SIDE
RPCCW8   DS    D                        USER CCW FOLLOWING SIDE
RPCCW9   DS    D                        TIC BACK TO USER'S C.P.
REGSVAR  DS    4D                       SAVE AREA FOR SIO REGS
RPSWAEND EQU   *                        END OF RPS WORK AREA     Y02080
*
         END
