        TITLE 'IGG0325T - F5 DSCB COMPLETION, F4 DSCB UPDATING' SA56426
IGG0325T CSECT
*
* MODULE NAME = IGG0325T
*
* DESCRIPTIVE NAME = F5 DSCB COMPLETION, F4 DSCB UPDATING
*
* COPYRIGHT = NONE
*
* CHANGE ACTIVITY = SEE BELOW
*
*          RELEASE 19 DELETIONS                                       *
*          RELEASE 20 DELETIONS                                       *
*1296052000,098000,398000-400000                                 A31534
*1296004000,180000,190000,256000,322000,392000-394000,408000     S20201
*          RELEASE 21 DELETIONS                                       *
*1190178400                                                      M0130
*1190050000-052000                                               S21040
*1190020000-021200,036000,049000,320800,344000,348000-350000,    M0124
*1190364000,418000,422000-440000,444000-464000,468000,474000-    M0124
*1190520000,524000-528000,540000,546000,550000-554000,568000     M0124
*          RELEASE 21.7 DELETIONS                                     *
*0000002000,176000,188000,306000-308000,310000,320800,356000,   SA56426
*0000394000-407000,408500,410000-412000                         SA56426
*          RELEASE 22 DELETIONS                                       *
*          VS2 RELEASE 02 DELETIONS/CHANGES                           *
*0000000300-001500,005008-005628,046000,049000-052000,060000,    Y02080
*0000118000-132000,136000,142000,148000-152000,156000,160000-    Y02080
*0000162000,166000,172000-178600,186000,214000,262000,266000-    Y02080
*0000270000,278000,288000-290000,308100,308400-309200,310000-    Y02080
*0000310500,311500-311600,316000-318000,320400-320800,328000,    Y02080
*0000352000-352600,354000-363000,374000,384000-390500,392000,    Y02080
*0000394000-434000,466000-558000,562000-584000,588000,592000-    Y02080
*0000724000                                                      Y02080
*0000                                                            Y02082
*0000010000-014000,024000-028000,188100-188200,189000,196000-    Y02078
*0000200000,266000-276000,292000,309200,364500-382000,591000-    Y02078
*0000591500                                                      Y02078
*0000308400                                                      Y02144
*          VS2 RELEASE 3 CHANGES/DELETIONS
*0000270300-270600                                             @ZA05618
*                                                                     *
*STATUS CHANGE LEVEL 003
*                                                                     *
* FUNCTION/OPERATION  THIS MODULE CONVERTS THE FORMAT 5 DSCB'S TO     *
* FINAL FORM AND WRITES THEM IN THE VTOC.  IF THERE WAS ORIGINALLY    *
* ONLY ONE FORMAT 5 DSCB ON THE VOLUME, ONLY ONE FORMAT 5 WAS REBUILT,*
* AND THERE ARE NO SPLIT CYLINDER DATA SET EXTENTS, THEN THE REBUILT  *
* FORMAT 5 IS IN 'DSCBF5' IN THE ALLOCATE WORK AREA.  OTHERWISE, THE  *
* REBUILT FORMAT 5 DSCB(S) ARE STORED IN THE VTOC CONVERSION WORK     *
* AREA EXTENSIONS(S).  IF THERE WERE ORIGINALLY MORE FORMAT 5 DSCB'S  *
* ON THE VOLUME THAN THERE ARE REBUILT FORMAT 5 DSCB'S, THIS MODULE   *
* ZEROS OUT THE EXTRA FORMAT 5 DSCB'S.  IF THE DOS BIT IS SET, THIS   *
* MODULE RESETS IT AND SETS BIT 58.4 IN THE FORMAT 4 DSCB.  IF THE    *
* VTOC CONVERSION ROUTINES WERE ENTERED BECAUSE THE DIRF BIT IS SET,  *
* THIS MODULE SETS BIT 58.6 IN THE FORMAT 4 DSCB.  IT THEN WRITE THE  *
* UPDATED FORMAT 4 DSCB TO THE VTOC.  IF THE VTOC CANNOT BE CONVERTED,*
* THIS MODULE ISSUES MESSAGE IEC602I WITH A CODE OF 0, 4, OR 8.       *
*                                                                     *
* ENTRY POINTS                                                        *
*        IGG0325T - UNDER NORMAL CONDITIONS, ENTRY IS MADE FROM       *
*        IGG0325P OR IGG0325W. ENTRY IS ALSO MADE FROM ANY OF THE     *
*        CONVERT ROUTINE MODULES WHEN AN ERROR IS ENCOUNTERED.        *
*                                                                     *
* INPUT  REGISTER 13 POINTS TO THE ALLOCATE WORK AREA, WHICH CONTAINS *
*        THE FORMAT 4 DSCB.  REGISTER 8 IS ZERO FOR A NORMAL ENTRY OR *
*        CONTAINS AN ERROR CODE.                                      *
*                                                                     *
* OUTPUT  VALID FORMAT 5 DSCB'S AND AN UPDATED FORMAT 4 DSCB WILL     *
*         EXIST IN THE VTOC. REGISTER 4 WILL BE ZERO TO INDICATE      *
*         REENTRY TO IGG0325A.                                        *
*                                                                     *
* EXTERNAL ROUTINES                                                   *
*        EXCP(0)                                                      *
*        WAIT(1)                                                      *
*        WTO(35) - WRITE A MESSAGE TO THE OPERATOR                    *
*                                                                     *
* EXITS NORMAL  BRANCH TO IGG0325A IF CALLED BY ALLOCATE OR TO        *
*               IGG0553F IF CALLED BY EXTEND                          *
*       ERROR   BRANCH TO IGG0325H IF CALLED BY ALLOCATE OR TO        *
*               IGG0553F IF CALLED BY EXTEND                          *
*                                                                     *
* TABLES/WORKAREAS DADSM WORKAREA                                     *
*                                                                     *
* ATTRIBUTES   REENTRANT                                              *
*                                                                     *
*                                                                     *
*                                                                     *
*                                                                     *
REGONE   EQU   1                       WORK REGISTER
REGTWO   EQU   2                       WORK REGISTER
REGTHREE  EQU  3                       WORK REGISTER
REGFOUR  EQU   4                       WORK REGISTER
REGFIVE  EQU   5                       WORK REGISTER
REGSIX  EQU   6                        WORK REGISTER
F0CTR    EQU   6                        NUMBER OF F0'S TO WRITE  Y02078
REGSEVEN EQU   7                       WORK REGISTER
RUCBPTR  EQU   7                       UCB POINTER
RF5BLDCT EQU   8                        NBR OF F5'S STILL TO BE  Y02078
*                                       CONVERTED AND WRITTEN    Y02078
RERRPASS EQU   8                        ERROR CODE REGISTER      A31534
RTNREG2  EQU   9                       BRANCH REGISTER
REGTEN   EQU   10                       WORK REGISTER            Y02078
RJFCBPTR EQU   11                      JFCB POINTER
RVTOCWKA EQU   11                       VTOC WORK AREA ADDRESS   Y02078
BASEREG  EQU   12                      BASE REGISTER
RWKAREA  EQU   13                      WORK AREA BASE REGISTER
RETRNREG EQU   14                     BRANCH REGISTER
RCVT     EQU   15                      CVT BASE REGISTER
*
* OTHER EQUATES
*
ZERO     EQU   0                        VALUE ZERO
ONE      EQU   1                        VALUE ONE
TWO      EQU   2                        VALUE TWO
THREE    EQU   3                        VALUE THREE
K4       EQU   4                        CONSTANT OF 4            Y02080
FOUR     EQU   4                        VALUE FOUR
FIVE     EQU   5                        VALUE FIVE
SIX      EQU   6                        VALUE SIX
EIGHT    EQU   8                        VALUE EIGHT
K12      EQU   12                       TWELVE BYTES             Y02080
F5INDIC  EQU   X'05'                    F5 DSCB INDICATOR
GOODIO   EQU   X'20'                    TEST VALUE FOR SUCCESSFUL I/O
ERRCD    EQU   X'0C'                    ERROR CODE FOR UNSUCCESSFUL I/O
C26      EQU   26
C140     EQU   140
NOCC     EQU   X'00'                    NO COMMAND CHAINING      Y02080
CC       EQU   X'40'                    COMMAND CHAINING BIT     Y02078
SLI      EQU   X'20'                    SUPPRESS INCORRECT LNGTH Y02078
SKIP     EQU   X'10'                    SUPPRESS DATA XFER BIT   Y02078
WRDATA   EQU   X'05'                    WRITE DATA COMMAND CODE  Y02078
WRITCOMD EQU   X'0D'                    WRITE KEY AND DATA       Y02078
READCOMD EQU   X'0E'                    READ COMMAND CODE
C90      EQU   90                      VALUE 90
F5ID     EQU   X'F5'                   F5 ID
DOSBIT   EQU   X'80'                    DOS BIT IN DS4VTOCI      Y02080
DIRFBIT  EQU   X'04'                    DIRF BIT IN DS4VTOCI     Y02078
DIRFCNVT EQU   X'02'                    DIRF CONVERTED BIT       Y02078
UCBADDR  EQU   32                       DISP. TO UCB ADDR IN DEB Y02078
UUU      EQU   34                       DISP. TO UUU IN MESSAGE  Y02078
VOLSER   EQU   38                       DISP. TO VOLSER IN MSG   Y02078
CODE     EQU   45                       DISP. TO CODE IN MESSAGE Y02078
CODE4    EQU   C'4'                     CODE 4 IN MESSAGE        Y02078
CODE8    EQU   C'8'                     CODE 8 IN MESSAGE        Y02078
VTOCERR  EQU   X'48'                    RETURN CODE FOR EXTEND   Y02078
BITON    EQU   X'08'                   USED TO TURN BIT 58.4 ON
C96      EQU   96                      VALUE 96
*
* REGISTER USAGE
*
         BALR  BASEREG,ZERO             INITIALIZE BASE REGISTER
         USING *,BASEREG
         USING ALLOCWKA,RWKAREA         BASE FOR ALLOCATE W/A    Y02080
*
* THIS SECTION DETERMINES IF THE FIRST FORMAT 5 DSCB IS LOCATED
* IN A VTOC WORK AREA EXTENSION.
*
         LTR   RERRPASS,RERRPASS        IS THERE AN ERROR        S21040
         BP    ERREXIT                  YES, DO WE GO TO EXTEND  S21040
WKAEXTST EQU   *                        BRANCH LABEL             Y02078
         XC    DADSMADR,DADSMADR        INITIALIZE CHAIN CCHHR   Y02078
         LH    RF5BLDCT,F5BLDCT         LOAD NBR OF REBUILT F5'S Y02078
         CH    RF5BLDCT,F5FNDCT         TEST IF EQUAL TO NUMBER  Y02078
*                                       OF FOUND F5'S            Y02078
         BH    FINDF0                   BRANCH IF THERE ARE MORE Y02078
*                                       REBUILT THAN FOUND F5'S  Y02078
         BL    FINDLAST                 BRANCH IF THERE ARE MORE Y02078
*                                       FOUND THAN REBUILT F5'S  Y02078
         CH    RF5BLDCT,ONEF5           TEST IF ONLY 1 F5 BUILT  Y02078
         BNE   FINDLAST                 BRANCH IF MORE THAN ONE  Y02078
         TM    ASWITCH,SPCYLSW          TEST FOR SPLIT CYLINDER  Y02078
*                                       DATA SET EXTENTS IN VTOC Y02078
         BO    FINDLAST                 BRANCH IF YES            Y02078
*
* THIS SECTION CONVERTS EACH FORMAT 5 RTA/RTA+1 TO AN XXYYZ EXTENT.
*                                                                     *
         MVC   DADSMADR,FMTOUTAD        GET 1ST F5 ADDRESS
CONVNEXT EQU   *                        BRANCH LABEL             Y02078
         LA    REGTWO,C26               MAXIMUM NBR OF EXTENTS   Y02078
         LA    REGTHREE,DS5AVEXT        GET PTR TO CONVERTED AREA
         LH    REGONE,DS4TKCYL          GET NUM TRKS/CYL
NEXTEXT  SR    REGFOUR,REGFOUR          CLEAR HIGH DIVIDE REGISTERS
         SR    REGSIX,REGSIX
         MVC   DSCBF5(FOUR),ZERO(REGTHREE)
         LH    REGFIVE,DSCBF5           GET RTA
         LH    REGSEVEN,DSCBF5+TWO      GET RTA+1                Y02080
         DR    REGFOUR,REGONE           CONVERT RTA TO CYL/TRK
         DR    REGSIX,REGONE            CONVERT RTA+1 TO CYL/TRK
         SR    REGSEVEN,REGFIVE         CALCULATE CYL DIFFERENCE
         SR    REGSIX,REGFOUR
         LTR   REGSEVEN,REGSEVEN        IS CYL DIFFERENCE ZERO
         BZ    STORE                    BRANCH IF YES
         LTR   REGFOUR,REGFOUR          IS TRK DIFFERENCE ZERO
         BZ    STORE                    BRANCH IF YES
         AR    REGSIX,REGONE            ADD TRK CONSTANT TO TRK DIFF
         BCTR  REGSEVEN,ZERO            DECREMENT NO OF CYLS
STORE    STH   REGSEVEN,DSCBF5          STORE NUM FULL CYLS
         MVC   TWO(TWO,REGTHREE),DSCBF5 STORE NO FULL CYLS
         STC   REGSIX,FOUR(REGTHREE)    STORE NO EXCESS TRKS
         LA    REGTHREE,FIVE(REGTHREE)  HAVE ALL EXTENTS BEEN CONVERTED
         BCT   REGTWO,NEXTEXT           BRANCH IF NO
         LA    REGTWO,C90               SET UP LOOP CONTROL
         LA    REGONE,F5EXT16B          GET ADDR OF START OF MOVE
MOVEMORE MVC   ONE(ONE,REGONE),ZERO(REGONE) MOVE A FIELD
         BCTR  REGONE,ZERO              UPDATE MOVE ADDR
         BCT   REGTWO,MOVEMORE          BRANCH IF MORE FIELDS TO MOVE
*
* THIS SECTION INSERTS THE FORMAT 5 INDICATORS AND WRITES THE
* CONVERTED FORMAT 5 DSCB'S.
*
         MVI   DS5FMTID,F5ID            INSERT F5 INDICATOR
         MVI   DSCBF5,F5INDIC           INSERT F5 INDICATOR
         MVC   DSCBF5+ONE(THREE),DSCBF5  PROPAGATE F5 INDICATOR  Y02080
         BAL   RTNREG2,WRITEF5          WRITE F5 JUST CONVERTED
         LH    REGONE,DS4DSREC          LOAD COUNT OF F0 DSCBS   Y02078
         BCTR  REGONE,ZERO              DECREMENT BY ONE         Y02078
         STH   REGONE,DS4DSREC          SAVE UPDATED COUNT       Y02078
         BCT   RF5BLDCT,MOVEF5IN        GO GET THE NEXT F5 DSCB  Y02078
         B     F0TEST                   TEST IF F0 TO BE WRITTEN Y02078
*
* THIS SECTION FINDS FORMAT 0 DSCB'S OVER WHICH REBUILT FORMAT 5
* DSCB'S WILL BE WRITTEN.
*
FINDF0   EQU   *                        BRANCH LABEL             Y02078
         LR    F0CTR,RF5BLDCT           LOAD NBR OF REBUILT F5'S Y02078
         SH    F0CTR,F5FNDCT            NUMBER OF F0'S TO FIND   Y02078
         L     RVTOCWKA,LASTF5AD        ADDR OF LAST REBUILT F5  Y02078
         USING VTOCWKA,RVTOCWKA         WORK AREA ADDRESSABILITY Y02078
         LA    REGONE,CCW4                                       Y02078
         ST    REGONE,CHNPGADR          ADDR OF CHAN PROG IN IOB Y02078
         MVC   SEEKCYL(FIVE),FMTOUTAD   BEGIN SEARCH AT F5 CCHHR Y02078
         MVI   CCW6+FOUR,CC+SKIP        CHAIN READ TO READ COUNT Y02078
         MVI   CCW7+FOUR,CC+SLI         CHAIN RD COUNT TO SKE    Y02078
         MVI   CCW8+FOUR,CC+SLI         SET SLI BIT IN SKE       Y02078
NEXTF0   EQU   *                        BRANCH LABEL             Y02078
         BAL   RETRNREG,EXCHANPG        EXECUTE THE CHANNEL PROG Y02078
         MVC   VTCCHHR1,COUNT           MOVE CCHHR OF F0 TO WKA  Y02078
         MVC   SEEKCYL(FIVE),COUNT      INIT. FOR ANOTHER SEARCH Y02078
         LH    REGONE,F5FNDCT           NBR OF FOUND F5 CCHHR'S  Y02078
         LA    REGONE,ONE(REGONE)       INCREMENT BY ONE         Y02078
         STH   REGONE,F5FNDCT           SAVE UPDATED COUNT       Y02078
         L     RVTOCWKA,VTCBPTR1        ADDRESS OF PRECEDING F5  Y02078
         BCT   F0CTR,NEXTF0             BRANCH IF MORE TO FIND   Y02078
*
* THIS SECTION MOVES THE FORMAT 5 DSCB POINTED TO BY REGISTER
* RVTOCWKA FROM A VTOC CONVERSION WORK AREA EXTENSION INTO THE
* MAIN ALLOCATE WORK AREA.
*
FINDLAST EQU   *                        BRANCH LABEL             Y02078
         L     RVTOCWKA,LASTF5AD        LOAD LAST F5 DSCB ADDR   Y02078
MOVEF5IN EQU   *                        BRANCH LABEL             Y02078
         MVC   DSCBF5,VTC1STF5          MOVE F5 DSCB POINTED TO  Y02078
*                                       BY RVTOCWKA INTO DSCBF5  Y02078
         MVC   DS5PTRDS,DADSMADR        MOVE IN CHAINING ADDRESS Y02078
         MVC   DADSMADR,VTCCHHR1        CCHHR OF THIS F5         Y02078
         L     RVTOCWKA,VTCBPTR1        ADDRESS OF PRECEDING F5  Y02078
         B     CONVNEXT                 GO CONVERT THE FORMAT 5  Y02078
*
* THIS SECTION TESTS IF FORMAT 0 DSCB'S MUST BE WRITTEN OVER ANY
* FORMAT 5 DSCB'S (FOUND DURING THE SEARCH THROUGH THE VTOC IN MODULE
* IGG0325P) WHICH WERE NOT WRITTEN OVER WITH REBUILT FORMAT 5 DSCB'S.
*
F0TEST   EQU   *                        BRANCH LABEL             Y02078
         CLC   F5BLDCT,F5FNDCT          TEST IF F0'S TO WRITE    Y02078
         BNL   TURNOFF                  DO NOT BRANCH IF MORE    Y02078
*                                       FOUND F5'S THAN REBUILT  Y02078
         LH    F0CTR,F5FNDCT            LOAD NBR OF FOUND F5'S   Y02078
         SH    F0CTR,F5BLDCT            CALCULATE NUMBER OF F0'S Y02078
         L     RVTOCWKA,LASTF5AD        ADDRESS OF LAST F5       Y02078
         SR    REGFOUR,REGFOUR          CLEAR FOR DIVISION       Y02078
         LH    REGFIVE,F5BLDCT          GET NUMBER OF BUILT F5'S Y02078
         D     REGFOUR,FW3              3 F5'S PER WKA EXTENSION Y02078
         LTR   REGFOUR,REGFOUR          TEST IF THE LAST REBUILT Y02078
*                                       F5 WAS THE THIRD IN THE  Y02078
*                                       WORK AREA EXTENSION      Y02078
         BZ    NEXTWKA                  BRANCH IF YES            Y02078
         LA    REGTWO,VTCNXTF5          LENGTH OF A F5 SLOT    @ZA05618
         AR    RVTOCWKA,REGTWO          POINT TO NEXT ADDRESS  @ZA05618
WRITEF0  EQU   *                        BRANCH LABEL             Y02078
         MVC   SEEKCYL(FIVE),VTCCHHR1   MOVE CCHHR INTO IOB      Y02078
         LA    REGONE,ONE                                        Y02078
         STH   REGONE,CCW3+SIX                                   Y02078
         MVI   CCW3+FOUR,SLI            SET SLI BIT IN WRITE K&D Y02078
         LA    REGONE,ZEROS                                      Y02078
         ST    REGONE,CCW3              WRITE 140 BYTES OF ZEROS Y02078
         MVI   CCW3,WRITCOMD            WRITE KEY AND DATA COMD  Y02078
         LA    REGONE,CCW1                                       Y02078
         ST    REGONE,CHNPGADR          ADDR OF CHAN PROG IN IOB Y02078
         BAL   RETRNREG,EXCHANPG        EXECUTE THE CHANNEL PROG Y02078
         BCTR  F0CTR,ZERO               DECREMENT COUNTER        Y02078
         LTR   F0CTR,F0CTR              TEST IF ALL WRITTEN      Y02078
         BZ    TURNOFF                  BRANCH IF ALL WRITTEN    Y02078
         LA    REGFOUR,ONE(,REGFOUR)                             Y02078
         C     REGFOUR,FW3              TEST IF NEXT CCHHR IS IN Y02078
*                                       THE SAME WORK AREA       Y02078
         BNE   SAMEWKA                  BRANCH IF YES            Y02078
         SR    REGFOUR,REGFOUR                                   Y02078
NEXTWKA  EQU   *                        BRANCH LABEL             Y02078
         LA    REGTEN,VTC3RDF5-VTC1STF5  LENGTH OF 2 F5 SLOTS    Y02078
         SR    RVTOCWKA,REGTEN          ADDR OF WKAREA BEGINNING Y02078
         LR    REGTEN,RVTOCWKA                                   Y02078
         LA    REGONE,IECEXTPR-IECPREFX  LENGTH OF PREFIX        Y02078
         SR    REGTEN,REGONE            CALCULATE PREFIX ADDRESS Y02078
         USING IECPREFX,REGTEN          PREFIX ADDRESSABILITY    Y02078
         L     REGTEN,IECCORP2          ADDRESS OF NEXT PREFIX   Y02078
         L     RVTOCWKA,IECCORAD        LOAD ADDR OF WORK AREA   Y02078
         B     WRITEF0                  GO WRITE A F0 DSCB       Y02078
SAMEWKA  EQU   *                        BRANCH LABEL             Y02078
         LA    RVTOCWKA,VTCNXTF5(RVTOCWKA)  POINT TO NEXT SLOT   Y02078
         B     WRITEF0                  GO WRITE A F0 DSCB       Y02078
*
* THIS SECTION TURNS OFF THE DOS BIT AND WRITES BACK THE FORMAT 4 DSCB
* BEFORE RETURNING.
*
TURNOFF  EQU   *                        BRANCH LABEL             Y02080
         TM    DS4VTOCI,DOSBIT          TEST IF DOS BIT SET      Y02078
         BNO   DIRFTEST                 BRANCH IF NOT            Y02078
         NI    DS4VTOCI,X'FF'-DOSBIT    TURN OFF DOS BIT IN F4   Y02080
         OI    DS4VTOCI,BITON           TURN ON CONVERTED BIT
DIRFTEST EQU   *                        BRANCH LABEL             Y02078
         TM    ACNVSW,DIRFSET           TEST IF ENTRY FOR DIRF   Y02078
         BNO   WRITEF4                  BRANCH IF NOT            Y02078
         OI    DS4VTOCI,DIRFCNVT        SET DIRF CONVERTED BIT   Y02078
WRITEF4  EQU   *                        BRANCH LABEL             Y02078
         MVI   DS4NOEXT,X'01'           UPDATE NO. OF EXTENTS FIELD
         LA    REGONE,IECSDSF4          GET ADDR OF F4 IN WORK AREA
         ST    REGONE,CCW3              MOVE F4 ADDR TO CHANNEL PROGRAM
         LA    REGONE,C96               GET LENGTH F4 DATA
         ST    REGONE,CCW3+FOUR         STORE DATA LENGTH IN     Y02080
*                                       CHANNEL PROGRAM          Y02080
         MVI   CCW3,WRDATA              PUT WRITE DATA COMMAND   Y02078
*                                       IN CHANNEL PROGRAM            *
         LA    REGONE,CCW1              GET ADDR BEGINNING CHANNEL
*                                       PROGRAM                       *
         ST    REGONE,CHNPGADR
         MVC   SEEKCYL(FIVE),VTOCADR    MOVE ADDR F4 TO CHANNEL PROGRAM
         BAL   RETRNREG,EXCHANPG        WRITE NEW F4 DATA
         XR    RERRPASS,RERRPASS        CLEAR ERROR CODE REG     Y02078
         TM    ACNVSW,EXTENDID          DID WE COME FROM EXTEND  Y02080
         BNO   AROUND                   NO, GO TO ALLOCATE       S21040
EXTNDBCK EQU   *                                                 S21040
         CH    RERRPASS,IOCODE          TEST FOR ANY VTOC ERROR  Y02078
         BNH   LDWKADDR                 BRANCH IF NO ERROR OR AN Y02078
*                                       I/O ERROR RETURN CODE    Y02078
         LA    RERRPASS,VTOCERR         RETURN X'48' TO EXTEND   Y02078
LDWKADDR EQU   *                        BRANCH LABEL             Y02078
         L     REGFOUR,ALLADTRL         LOAD EOV WORK AREA ADDR  Y02144
         MVC   WTGMODNM(K12),EXTEND     MOVE IN MODULE NAME AND  Y02080
*                                       ENTRY POINT ADDRESS      Y02080
         B     FREEWKAS                 GO FREE ANY WORK AREAS   Y02078
AROUND   EQU   *                                                 S21040
         XC    CCW1(ATIOTPTR-CCW1),CCW1  CLEAR CCW'S             Y02080
         XC    TTRLL(VTOCADR-TTRLL),TTRLL  CLEAR CCHHR'S EXCEPT  Y02080
*                                       VTOCADR (F4 DSCB CCHHR) SA56426
         XC    DADSMADR(MIELNAME-DADSMADR),DADSMADR              Y02080
*                                       CLEAR REST OF CCHHR'S    Y02080
         XC    DSCBF1(C140),DSCBF1     CLEAR F1 AREA
         XC    DSCBF5(C140),DSCBF5     CLEAR F5 AREA
         XC    DADSMTBL(ACNVSW-DADSMTBL),DADSMTBL                Y02080
*                                       CLEAR REST OF WORK AREA  Y02080
         SR    REGFOUR,REGFOUR          INITIALIZE REENTRY REGISTER
         LA    REGTWO,ALLOCATE          POINT TO ID OF IGG0325A  Y02080
         B     XCTLHERE                                          M0124
*
* THIS ROUTINE WRITES A FORMAT 5 DSCB.
*
WRITEF5  LA    REGONE,CCW1              GET ADDR WRITE CHANNEL PROGRAM
         ST    REGONE,CHNPGADR
         MVI   CCW3+K4,NOCC             UNCHAIN WRITE FROM SID   Y02080
         MVC   SEEKCYL(FIVE),DADSMADR   GET WHERE TO WRITE ADDR
         BAL   RETRNREG,EXCHANPG        WRITE F5
         BR    RTNREG2                  RETURN
*
* THIS ROUTINE EXECUTES A CHANNEL PROGRAM.
*
EXCHANPG MVI   ECB,ZERO                 INITIALIZE ECB
         EXCP  IOB                      DO I/O
         WAIT  1,ECB=ECB
         TM    ECB,GOODIO               WAS I/O SUCCESSFUL
         BCR   1,RETRNREG               RETURN IF I/O SUCCESSFUL M0124
         LA    RERRPASS,ERRCD           LOAD ERROR CODE
         B     NOERRMSG                 GO TEST THE RETURN       Y02078
*
* IF THE VTOC COULD NOT BE CONVERTED, THIS SECTION ISSUES MESSAGE
* IEC602I WITH A CODE OF 0, 4, OR 8.
*
ERREXIT  EQU   *                        BRANCH LABEL             Y02080
         CH    RERRPASS,IOCODE          TEST FOR I/O ERROR CODE  Y02078
         BE    NOERRMSG                 BRANCH IF YES            Y02078
         MVC   DSCBF1(MSGEND-ERRMSG),ERRMSG  MOVE IN MESSAGE     Y02078
         L     RUCBPTR,DEB+UCBADDR      LOAD UCB ADDRESS         Y02078
         USING UCB,RUCBPTR              UCB ADDRESSABILITY       Y02078
         MVC   DSCBF1+UUU(L'UCBNAME),UCBNAME  MOVE IN EBCDIC UCB Y02078
*                                       CHANNEL/UNIT ADDRESS     Y02078
         MVC   DSCBF1+VOLSER(L'SRTEVOLI),SRTEVOLI  MOVE IN VOLID Y02078
         CH    RERRPASS,OVERLAP         TEST IF OVERLAP ERROR    Y02078
         BE    ISSUEMSG                 BRANCH IF YES            Y02078
         MVI   DSCBF1+CODE,CODE4        INITIALIZE FOR CODE = 4  Y02078
         CH    RERRPASS,DOSERR          TEST IF DOS SPLIT CYL    Y02078
*                                       OVERLAPPING EXTENT ERROR Y02078
         BE    ISSUEMSG                 BRANCH IF YES            Y02078
         MVI   DSCBF1+CODE,CODE8        ASSUME RERRPASS = X'9C'  Y02078
*                                       AND SET CODE = 8 IN MSG  Y02078
ISSUEMSG EQU   *                        BRANCH LABEL             Y02078
         WTO   MF=(E,DSCBF1)            ISSUE ERROR MESSAGE      Y02078
         NI    DS4VTOCI,X'FF'-DIRFBIT   RESET DIRF BIT TO        Y02078
*                                       PREVENT IGG0325H FROM    Y02078
*                                       REWRITING THE F4 DSCB    Y02078
NOERRMSG EQU   *                        BRANCH LABEL             Y02078
         TM    ACNVSW,EXTENDID          DID WE COME FROM EXTEND  Y02080
         BO    EXTNDBCK                 YES, GO TO EXTEND        S21040
         LA    REGTWO,ERRLOAD           POINT TO ID OF IGG0325H  Y02080
XCTLHERE EQU   *
         L     RJFCBPTR,AJFCBPTR        RESTORE JFCB ADDRESS     Y02078
         MODESET EXTKEY=ZERO            RETURN TO KEY ZERO       Y02082
         MVC   WTGMODNM+SIX(TWO),ZERO(REGTWO)  MOVE IN MODULE ID Y02080
         MVC   WTGMODEP+ONE(THREE),TWO(REGTWO)  MOVE IN EPA      Y02080
FREEWKAS EQU   *                        BRANCH LABEL             Y02078
         LA    REGONE,IECEXTPR-IECPREFX  LENGTH OF PREFIX        Y02078
         LA    REGTEN,ZERO(RWKAREA)     INITIAL WORK AREA ADDR   Y02078
         SR    REGTEN,REGONE            CALCULATE PREFIX ADDRESS Y02078
         USING IECPREFX,REGTEN          PREFIX ADDRESSABILITY    Y02078
NXTPREFX EQU   *                        BRANCH LABEL             Y02078
         CLC   IECCORPS,IECCORP2        TEST IF ONLY ALLOCATE    Y02078
*                                       OR EXTEND AND ALLOCATE   Y02078
*                                       WORK AREAS AND/OR RPS    Y02078
*                                       WORK AREAS EXIST         Y02078
         BE    LOADMOD                  YES, DO NOT FREE WKAREA  Y02078
         L     REGTEN,IECCORP2          LOAD NEXT PREFIX ADDRESS Y02078
         CLC   IECCORID,VCWKAID         TEST IF VTOC CONVERSION  Y02078
*                                       WORK AREA EXTENSION      Y02078
         BNE   NXTPREFX                 BRANCH IF NOT            Y02078
         L     REGTEN,IECCORAD          LOAD ADDRESS OF WKAREA   Y02078
         IECRES FREE,PREFIX=YES,A=(REGTEN),                      Y02078X
               STM=(REGONE,RWKAREA,IECREGSV+K12)  FREE WORK AREA Y02078
         B     FREEWKAS                 GO TEST IF MORE TO FREE  Y02078
LOADMOD  EQU   *                        BRANCH LABEL             Y02080
         IECRES LOAD,EXTPR=(RWKAREA),BRANCH=DIRECT               Y02080
*
* CONSTANTS
*
IOCODE   DC    H'12'                    I/O ERROR                Y02078
ONEF5    DC    H'1'                     HALFWORD OF ONE          Y02078
OVERLAP  DC    X'0094'                  OVERLAPPING EXTENTS ERR  Y02078
DOSERR   DC    X'0098'                  DOS OVERLAPPING ERROR    Y02078
FW3      DC    F'3'                     3 F5 SLOTS PER WORK AREA Y02078
VCWKAID  DC    CL4'VCWA'                VTOC WORK AREA ID        Y02078
ERRMSG   WTO   'IEC602I VTOC NOT CONVERTED ON UUU,VOLSER,0',     Y02078X
               ROUTCDE=(4,10),DESC=4,MF=L                        Y02078
MSGEND   EQU   *                        MSG DELIMITER            Y02078
*
* TABLE OF MODULE IDS AND ENTRY POINT ADDRESSES
*
         XCTLTABL ID=(ALLOCATE,5A,ERRLOAD,5H,EXTEND,IGG0553F),   Y02080X
               SVC=032,LENGTH=,BRT=YES                           Y02080
         SPACE 2                                                 Y02078
         IECDSECS CVT,UCB,EXPAND=YES    CVT AND UCB DSECTS       Y02078
DSCBWKAR IECALLWA EP,F4,D2=(5)          ALLOCATE WORK AREA       Y02080
ZEROS    EQU   CCW2+4                   FULL WORD OF ZEROS       Y02078
DS4TKCYL EQU   DS4DEVSZ+2
F5EXT16B EQU   DSCBF5+133               END OF EXTENT 16 IN F5 DSCB
CHNPGADR EQU   IOB+16                   ADDR. CHANNEL PROGRAM IN IOB
SEEKCYL  EQU   SEEK+3
         EJECT                                                   Y02078
VWKAREA  IECVTCWA                       VTOC WORK AREA           Y02078
         END
