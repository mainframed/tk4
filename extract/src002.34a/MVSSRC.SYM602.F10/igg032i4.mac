 TITLE 'IGG032I4 - ISAM ALLOCATE - UPDATES AND BUILDS F1,F2,F3'
IGG032I4 CSECT
*
*MODULE NAME = IGG032I4
*
*DESCRIPTIVE NAME = ISAM ALLOCATE - UPDATES AND BUILDS F1, F2, AND F3
*
*COPYRIGHT = NONE
*
*CHANGE ACTIVITY = SEE BELOW
*
*          RELEASE 16 DELETIONS
*1492092800                                                        AAAA
*1492                                                             13559
*          RELEASE 17 DELETIONS
*          RELEASE 18 DELETIONS
*          RELEASE 19 DELETIONS
*          RELEASE 20 DELETIONS
*          RELEASE 21 DELETIONS
*1194042600-043000,043400-043600,044000-044200,046200-046400,    A42162
*1194047000,047400-047600,048000-048200,058800,061200-061400,    A42162
*1194071400-071600,081580-081600                                 A42162
*1194003600,004000,016600,017600-018200,034000-034600,041800-    A38860
*1194042000,062000,062600-063400,076400,077200-077600,081800-    A38860
*1194083000,095200                                               A38860
*          RELEASE 22 DELETIONS
*          VS2 RELEASE 02 DELETIONS/CHANGES
*0000000030-000150,001100-001200,002800,003600,006000,006600-    Y02080
*0000007000,008000,017200,017700-018000,024200-024320,025200,    Y02080
*0000025600,062000-062400,063000-063300,070200,074800,077300,    Y02080
*0000082520,083250-083400,083800,084200-097400,097800            Y02080
*0000                                                            Y02144
*0000006600,074400-075800,081920-083200,092400-092600,093200-    YM3995
*0000097800                                                      YM3995
*
*STATUS CHANGE LEVEL 005
*FUNCTION - THIS MODULE UPDATES AN EXISTING FORMAT 1 DSCB AND IF
*           NECESSARY UPDATES A FORMAT 2 DSCB AND BUILDS A DSCB FORMAT
*           3 OR UPDATES A FORMAT 3 DSCB. THE DSCB'S CREATED OR UPDATED
*           ARE WRITTEN OUT AND CONTROL IS TRANSFERED TO MODULE
*           IGG032I6.
*
*ENTRIES - THE ONLY ENTRY POINT TO THIS MODULE IS IGG032I4. ENTRY IS
*          MADE FROM THE THIRD LOAD OF ISAM ALLOCATE VIA A BRANCH.
*
*
*INPUT - AT ENTRY TO THIS MODULE REG 11 POINTS TO A JFCB, REGISTER 13
*        POINTS TO THE ALLOCATE WORK AREA WHICH CONTAINS A PTR TO THE
*        ENTRY IN THE TIOT FOR THIS ALLOCATE AND THE ADDRESS OF THE
*        FORMAT 1 DSCB, THE VTOCADR, AND DADSMADR.
*
*OUTPUT - UPON TRANSFERRING TO THE NEXT MODULE IGG032I6, THE WORK AREA
*         WILL CONTAIN A SORTED DADSM TABLE, THE VTOCADR AND DADSMADR
*
*
*EXTERNALLY REFERENCED ROUTINES - THE FOLLOWING ARE SUPERVISOR CALLS
*   ISSUED IN MACRO EXPANSIONS
*   EXCP(0) READ FROM OR WRITE TO DIRECT ACCESS DEVICE
*   WAIT(1) WAIT ON EVENT CONTROL BLOCK
*   OTHER MACROS ISSUED - IEFJFCBN - BUILDS JFCB
*                         IECALLWA - ALLOCATE WORK AREA EXPANSION
*                         IECSDSL1 - BUILDS DSCBS
*                         IECDSECS - EXPANDS CVT, PSA, SCVT DSECTS
*
*EXITS - NORMAL - BRANCH TO IGG032I6
*        ERROR  - BRANCH TO IGG032I7 WITH ERROR CODE IN REGISTER 5
*
*              0C   PERMANENT I/O ERROR
*
*
*TABLES/WORK AREAS = DADSM WORK AREA DESCRIBED BY MACRO IECALLWA
*
*              ***************************************
*              *             DADSM TABLE             *
*              ***************************************
*
*              ***************************************
*              *        *         *                  *
*              *  TYPE  *  NO OF  *     USED HOLE    *
*              *  FLAG  * ENTRIES *      COUNTER     *
*              *        *         *                  *
*              ***************************************
*              *                  *                  *
*              *       RTA        *      RTA+1       *
*              *                  *                  *
*              ***************************************
*              *                  *                  *
*              *       RTA        *      RTA+1       *
*              *                  *                  *
*              ***************************************
*              *                  *                  *
*              *       RTA        *      RTA+1       *
*              *                  *                  *
*              ***************************************
*              *                  *                  *
*              *       RTA        *      RTA+1       *
*              *                  *                  *
*              ***************************************
*              *                  *                  *
*              *       RTA        *      RTA+1       *
*              *                  *                  *
*              ***************************************
*
*
*              TYPE FLAG = 02 - BPAM DIRECTORIES REQUESTED.
*
*                        = 40 - USER LABELS REQUESTED
*
*                        = 80 - SET MUST COMPLETE IS ACTIVE
*
*
*              NO OF ENTRIES = NUMBER OF ENTRIES WITHIN DADSM TABLE.
*
*
*              RTA = THE STARTING TRACK OF THE ALLOCATED EXTENT.
*
*
*              RTA+1 = THE STARTING TRACK OF THE NEXT AVAILABLE EXTENT.
*
*
*ATTRIBUTES - REENTRANT
*
*STORAGE - PROGRAM CODE CSECT = LESS THAN 2048 BYTES
*          WORK AREA = AS DEFINED IN THE IECALLWA MACRO
*          RPS WORK AREA = AS DEFINED IN THE IECRPS MACRO
*
*
*REGISTER USAGE
*              REGISTERS 0-9 WORK REGISTERS
*              REGISTER 10 I TIOT SET UP TABLE POINTER
*              REGISTER 11 - JFCB POINTER
*              REGISTER 12 - BASE REGISTER
*              REGISTER 13 - WORK AREA POINTER
*              REGISTER 14 SUBROUTINE LINKAGE AND WORK REGISTER
*              REGISTER 15 WORK REGISTER
***** REGISTER NAMES *****
RZERO    EQU   0
RONE     EQU   1
RTWO     EQU   2
RTHREE   EQU   3
RFOUR    EQU   4
RFIVE    EQU   5
RSIX     EQU   6
RSEVEN   EQU   7
REIGHT   EQU   8
RNINE    EQU   9
DRBAK   EQU   9
RTEN     EQU   10
RJFCB    EQU   11
RBASE    EQU   12
RWORK    EQU   13
RETURN   EQU   14
RFTEN    EQU   15
*
* OTHER EQUATES
*
TWO      EQU   2                        CONSTANT OF TWO          Y02080
STEPMC   EQU   1                        STATUS STEP MC INDICATOR YM3995
K12      EQU   12                       CONSTANT OF 12           YM3995
*
         BALR  RBASE,0
         USING GAMMA,RBASE
         USING ALLOCWKA,RWORK           WORK AREA ADDRESSABILITY Y02080
         USING JFCBKEN,RJFCB
*
GAMMA    EQU   *                        BRANCH LABEL             Y02080
         MVC   DS1DSNAM,JFCBDSNM        MOVE IN DATA SET NAME    Y02080
* THIS SECTION DETERMINES THE NUMBER OF EXISTING EXTENTS
F1EXISTS SR    RTWO,RTWO                CHECK NUMBER OF EXISTING
         IC    RTWO,DS1NOEPV               EXTENTS IN DSCB FORMAT 1
         LA    RONE,3
         LR    RFIVE,RTWO               DETERMINE THE NEXT EXTENT
*                                          SEQUENCE NUMBER TO BE USED
         CR    RTWO,RONE                CHECK NUMBER OF EXISTING
*                                          EXTENTS
         BH    F3EXISTS                 BRANCH IF MORE THAN 3 EXTENTS -
*                                          DSCB FORMAT 3 EXISTS
         BE    F3BUILD                  BRANCH IF 3 EXTENTS - DSCB F 3
*                                          MUST BE BUILT
* THIS SECTION DETERMINES HOW MUCH ROOM EXISTS IN FORMAT.
DETPOSIT SR    RSIX,RSIX                ADD TO NUMBER OF ENTRIES THE
         IC    RSIX,ENTRYNUM               NUMBER OF NEW ENTRIES
         LR    RFOUR,RTWO
         LR    RONE,RSIX
         AR    RSIX,RTWO
         STC   RSIX,DS1NOEPV            UPDATE NUMBER OF EXTENTS
         LA    RTWO,ENTRIES             SET POINTER TO DADSM TABLE
         LA    RSIX,2
         CR    RSIX,RFOUR               CHECK TO SEE IF ONE OR  MORE
*                                          EXTENTS EXIST IN F1
         BH    TWOSPACE                 BRANCH IF ROOM FOR TWO EXTENTS
         LA    RTHREE,DS1EXT3           SET POINTER TO LAST F1 EXTENT
         LA    RNINE,1                  SET UP EXTENT COUNTER
         B     STRTLOOP
TWOSPACE LA    RTHREE,DS1EXT2           SET POINTER TO THE 2ND F1
*                                          EXTENT
         LA    RNINE,2                  SET UP EXTENT COUNTER
* THIS SECTION FILLS IN THE EXTENTS IN THE FORMAT 1
STRTLOOP BAL   RETURN,LOOP
         BCTR  RONE,0                  REDUCE NUMBER OF NEW EXTENTS
         LTR   RONE,RONE                CHECK FOR LAST DADSM TABLE
*                                          ENTRY
         BZ    ENTER                    BRANCH IF LAST ENTRY
         BCTR  RNINE,0                  REDUCE NUMBER OF AVAILABLE
*                                          HOLES
         LTR   RNINE,RNINE              CHECK FOR LAST F1 EXTENT
         BZ    LABEL                    BRANCH IF LAST F1 EXTENT
         B     STRTLOOP
* THIS SECTION DETERMINES THE TYPE OF REQUEST
F3BUILD  LA    RTWO,ENTRIES             SET UP POINTER TO DADSM TABLE
         SR    RONE,RONE
         LR    RNINE,RONE              ADD TO THE NUMBER OF EXISTING
         IC    RONE,ENTRYNUM              ENTRIES THE NUMBER OF NEW
         IC    RNINE,DS1NOEPV              EXTENTS
         AR    RNINE,RONE
         STC   RNINE,DS1NOEPV           UPDATE NUMBER OF ENTRIES
LABEL    LA    RTHREE,DS3EXTNT         SET UP POINTER TO F3 EXTENT
         XC    SAVEADR+5(5),SAVEADR+5  ZERO OUT THE F3 POINTER AREA IF
*                                         NO F3 HAS PREVIOUSLY EXISTED
         XC    F5IN(140),F5IN           CLEAR OUT F3 AREA
* THIS SECTION FILLS IN THE FORMAT 3 EXTENTS
BUILDF3  BAL   RETURN,LOOP
         BCTR  RONE,0                   REDUCE NUMBER OF NEW EXTENTS
         LTR   RONE,RONE                CHECK FOR LAST EXTENT
         BZ    DONE                     BRANCH IF LAST DADSM TABLE
*                                          ENTRY
         LA    RSIX,DS3FMTID            PICK UP FORMAT IDENTIFIER
*                                          ADDRESS
         CR    RSIX,RTHREE              CHECK FOR F3 POINTER AT FORMAT
*                                          IDENTIFIER
         BNE   BUILDF3                  BRANCH IF NOT AT FORMAT IDEN-
*                                          TIFIER
         LA    RTHREE,1(RTHREE)         HOP OVER FORMAT IDENTIFIER
         B     BUILDF3
* THIS SECTION BUILDS A FORMAT 3
DONE     MVI   DS3FMTID,X'F3'           SET UP FORMAT IDENTIFIERS
         MVI   IECSDSL3,X'03'
         MVC   IECSDSL3+1(3),IECSDSL3
        BAL   DRBAK,WRITEF3            WRITE OUT DSCB F3
         OC    DS1PTRDS(5),DS1PTRDS     CHECK F1 CHAIN FIELD FOR
*                                          POINTER TO ANOTHER DSCB
         BC    7,UPDATF2               BRANCH IF THERE IS A FORMAT 2
         MVC   DS1PTRDS(5),SAVEADR+5    PUT POINTER TO F3 IN F1
ENTER    EQU   *                                                 A38860
         BAL   DRBAK,WRITEF1            WRITE OUT THE F1 DSCB    A38860
         B     XCTLDADS                 XCTL TO IGG032I6         A38860
* THIS SECTION READS IN A FORMAT 2 DSCB
UPDATF2  LA    RTWO,CCW9
         ST    RTWO,IOB+16              START CHANNEL PROGRAM AT A42162
*                                       CCW9                     A42162
         MVI   CCW11+4,X'00'                                     A42162
         LA    RTWO,DS1PTRDS            PICK UP POINTER TO ADDRESS OF
         ST    RTWO,CCW9                FORMAT 2 TO SEARCH ON    A42162
         MVI   CCW9,X'31'               RESTORE SID CODE         A42162
         LA    RTWO,F5IN                PICK UP ADDRESS OF AREA IN
         ST    RTWO,CCW11               WHICH F2 IS TO BE READ   A42162
         MVI   CCW11,X'0E'              READ COMMAND CODE        A42162
         MVC   SEEK+3(5),DS1PTRDS       UPDATE THE SEEK ADDRESS
        BAL   DRBAK,EXEC
         MVC   DS2PTRDS(5),SAVEADR+5    SET F2 CHAIN ADDRESS TO F3
*                                          ADDRESS
         MVC   TTRLL(5),DS1PTRDS        SET UP F2 ADDRESS
        BAL   DRBAK,WRITEF2            WRITE OUT DSCB F2
         B     ENTER
* THIS SECTION MODIFIES THE CHANNEL PROGRAM FOR A READ OF A F2 OR F3
F3EXISTS LA    RTWO,F5IN                PICK UP ADDRESS OF AREA TO BE
         ST    RTWO,CCW11               WRITTEN INTO             A42162
         MVI   CCW11,X'0E'              READ COMMAND CODE        A42162
         MVI   CCW11+4,X'00'                                     A42162
         MVC   SAVEADR+5(5),DS1PTRDS    PICK UP ADDRESS IN F1 CHAIN
         LA    RTWO,DS1PTRDS               FIELD
MODCHAN  EQU   *                                                 A42162
         MVC   SEEK+3(5),0(RTWO)           MODIFY SEEK FIELD
         ST    RTWO,CCW9                SEARCH ADDR FOR F2 OR F3 A42162
         MVI   CCW9,X'31'               RESTORE SID CODE         A42162
         LA    RTWO,CCW9
         ST    RTWO,IOB+16              START CHANNEL PROGRAM AT A42162
*                                       CCW 9                    A42162
        BAL   DRBAK,EXEC
         CLI   DS2FMTID,X'F3'           CHECK FOR A DSCB FORMAT 3
         BE    ISAF3                    BRANCH IF THIS A FORMAT 3
         LA    RTWO,DS2PTRDS            PICK UP CHAIN ADDRESS FROM F2
         MVC   SAVEADR+5(5),DS2PTRDS    PICK UP NEW ADDRESS FOR SEEK
         B     MODCHAN
* THIS SECTION LOCATES THE FIRST AVAILABLE F3 EXTENT
ISAF3    SR    RSEVEN,RSEVEN
         LR    RSIX,RSEVEN
         IC    RSEVEN,DS1NOEPV          PICK UP NUMBER OF EXISTING
         BCTR  RSEVEN,0                    EXTENTS AND DETERMINE
*                                          POSITION IN F3
         BCTR  RSEVEN,0                 REDUCE BY 3 FOR THOSE EXTENTS
         BCTR  RSEVEN,0                    IN THE FORMAT 1
         LA    RONE,10
         MR    RSIX,RONE
         LA    RTHREE,DS3EXTNT          LOCATE NEXT ENTRY IN DSCB
         AR    RTHREE,RSEVEN               FORMAT 3
         CLI   DS1NOEPV,X'07'           CHECK FOR THE SEVENTH EXTENT -
*                                          AT FORMAT IDENTIFIER
         BL    OKAY                     NOT TO FORMAT IDENTIFIER YET
         LA    RTHREE,1(RTHREE)         INCREMENT TO ADJUST FOR FORMAT
*                                          IDENTIFIER
*
OKAY     LA    RTWO,ENTRIES             SET UP DADSM TABLE POINTER
         SR    RFOUR,RFOUR
         IC    RFOUR,ENTRYNUM           PICK UP NUMBER OF NEW ENTRIES
* THIS SECTION FILLS IN F3 EXTENTS
ENTRLOOP BAL   RETURN,LOOP
         BCTR  RFOUR,0                  REDUCE NUMBER OF DADSM TABLE
*                                          ENTRIES
         LTR   RFOUR,RFOUR              CHECK FOR LAST ENTRY
         BZ    THRU                     BRANCH IF LAST ENTRY
         LA    RSIX,DS3FMTID            PICK UP FORMAT IDENTIFIER
*                                          ADDRESS
         CR    RTHREE,RSIX              CHECK FOR F3 POINTER AT FORMAT
*                                          IDENTIFIER
         BNE   ENTRLOOP                 BRANCH IF NOT AT FORMAT IDEN-
         LA    RTHREE,1(RTHREE)         HOP OVER FORMAT IDENTIFIER
         B     ENTRLOOP
*
THRU    BAL   DRBAK,WRITEF3            WRITE OUT DSCB F3
         SR    RFOUR,RFOUR              UPDATE F1 NUMBER OF EXTENTS
         LR    RFIVE,RFOUR                 FIELD
         IC    RFOUR,ENTRYNUM           PICK UP NUMBER OF NEW EXTENTS
         IC    RFIVE,DS1NOEPV           PICK UP NUMBER OF OLD EXTENTS
         AR    RFIVE,RFOUR
         STC   RFIVE,DS1NOEPV           UPDATE NUMBER OF EXTENTS ON
*                                          THIS VOLUME
        BAL   DRBAK,WRITEF1            WRITE OUT DSCB F1
*
* THIS SECTION RELOCATES THE CHANNEL PROGRAM FOR THE FORMAT 5 UPDATING
* OF IGG032I6.
*
XCTLDADS LA    RTWO,TTRLL               PICK UP ADDRESS OF FIELD TO
         ST    RTWO,PDLIST                 SEARCH ON
         MVC   CCW1+1(3),PDLIST+1
         MVC   CCW2+1(3),PDLIST+1
         MVC   CCW6+1(3),PDLIST+1
         MVC   CCW9+1(3),PDLIST+1
         LA    RTWO,DS1DSNAM            PICK UP ADDRESS OF AREA TO
         ST    RTWO,PDLIST                 WRITE INTO
         MVC   CCW5+1(3),PDLIST+1
         MVC   CCW8+1(3),PDLIST+1
         LA    RTWO,140(RTWO)           PICK UP ADDRESS OF AREA  FROM
         ST    RTWO,CCW11               WHICH TO WRITE           A42162
         MVI   CCW11,X'0E'              READ COMMAND CODE        A42162
         MVC   SEEK+3(5),DADSMADR       UPDATE THE SEEK ADDRESS TO
*                                          POINT TO THE FIRST F5
         LA    RTWO,NEXTLOAD            BRANCH TO IGG032I6       Y02080
*
* THIS SECTION BRANCHES TO ANOTHER MODULE.
*
XCT      EQU   *                                                 A38860
         IECRES LOAD,EXTPR=(RWORK),MODID=(RTWO),BRANCH=DIRECT    Y02080
*
*THIS SECTION SET UP AND BRANCHES TO CONVERT ROUTINE TO CONVERT FROM
*RTA TO CCHH
LOOP     MVC   0(1,RTHREE),TYPE        SET EXTENT TYPE
         STC   RFIVE,1(RTHREE)         SET EXTENT SEQUENCE NUMBER
         LA    RFIVE,1(RFIVE)
         L     RSIX,CVTPTR             PICK UP CVT POINTER
         USING CVT,RSIX
         STM   RZERO,RTWO,DS1DSNAM      SAVE REGISTERS ZERO THRU TWO
         STM   RNINE,RETURN,DS1DSNAM+12   AND NINE THRU 15 BEFORE
*                                         BRANCHING TO THE CONVERT RTN
         L     RFTEN,CVTPCNVT          CONVERT ROUTINE ADDRESS
         LA    RONE,DEB                R1=DEB ADDRESS
         LH    RZERO,0(RTWO)           R0=RTN TO BE CONVERTED
         SLL   RZERO,16
         LA    RTWO,DS1DSNAM+36        R2=ADDRESS OF 8 BYTE RESULT AREA
         LR    REIGHT,RWORK            SAVE AREA ADDRESS
         BALR  RETURN,RFTEN
         MVC   2(4,RTHREE),39+DS1DSNAM-FIRST(REIGHT)
         L     RSEVEN,8+DS1DSNAM-FIRST(REIGHT) CONVERT RTA+1 IN THE
         LH    RZERO,2(RSEVEN)            SAME MANNER AS RTA
         BCTR  RZERO,0
         SLL   RZERO,16
         L     RFTEN,CVTPCNVT           PICK UP CONVERT ROUTINE ADDRESS
         BALR  RETURN,RFTEN
         MVC   6(4,RTHREE),39+DS1DSNAM-FIRST(REIGHT)
         LM    RZERO,RTWO,DS1DSNAM-FIRST(REIGHT)
         LM    RNINE,RETURN,DS1DSNAM+12-FIRST(REIGHT)
         LA    RTHREE,10(RTHREE)       UPDATE DSCB POINTER
         LA    RTWO,4(RTWO)            UPDATE EXTENT POINTER
         BR    RETURN
* THIS SECTION SETS UP THE CHANNEL PROGRAM
WRITEF1  LA    RFOUR,SAVEADR            PICK UP ADDRESS OF SEARCH
*                                          ADDRESS FOR FORMAT 1
         LA    RTHREE,DS1DSNAM          PICK UP ADDRESS OF F1    Y02080
         MVC   SEEK+3(5),SAVEADR       UPDATE THE SEEK ADDRESS
         MVC   DS1DSNAM(44),JFCBDSNM   RESET DSNAME PORTION OF F1 AFTER
*                                      USING IT FOR THE CONVERT RTN
GOTID1   LA    RTWO,CCW6                UPDATE STARTING ADDRESS OF
*                                          CHANNEL PROGRAM
         ST    RTWO,IOB+16              START OF CHANNEL PROGRAM A42162
         ST    RFOUR,PDLIST             STORE ADDRESS OF SEARCH ADDRESS
         MVC   CCW2+1(3),PDLIST+1
         MVC   CCW6+1(3),PDLIST+1
         MVC   CCW9+1(3),PDLIST+1
         ST    RTHREE,PDLIST            STORE ADDRESS OF AREA TO BE
*                                          WRITTEN FROM
         MVC   CCW5+1(3),PDLIST+1
         MVC   CCW8+1(3),PDLIST+1
         MVC   CCW11+1(3),PDLIST+1
         MVI   CCW11+4,X'10'            SET SUPPRESS DATA XFER   A42162
* THIS SECTION INITIATES THE CHANNEL PROGRAM TO SEARCH FOR HOLES IN THE
* VTOC AND WRITE OUT THE DSCB'S
EXEC     TM    TYPEFLG,SMCDONE          IS SET MUST COMPLETE ON
         BO    CLRECB                   BRANCH IF YES
*
* LINK TO THE STATUS ROUTINE TO PERFORM A STEP MUST COMPLETE
*
         STM   RJFCB,RETURN,IECREGSV+K12  SAVE REGISTERS 11-14   YM3995
         LR    RTWO,RWORK               SAVE WORK AREA ADDRESS   YM3995
GETLOCK  SETLOCK OBTAIN,TYPE=LOCAL,MODE=UNCOND,                  YM3995X
               RELATED=(LOCAL,IGG032I4(RLSELOCK))  OBTAIN LOCK   YM3995
         LM    RJFCB,RETURN,IECREGSV+K12-ALLOCWKA(RTWO)          YM3995
*
         USING CVT,RFTEN                CVT ADDRESSABILITY       YM3995
         L     RFTEN,CVTPTR             LOAD CVT ADDRESS         YM3995
         L     RFTEN,CVTABEND           LOAD SECONDARY CVT PTR   YM3995
         USING SCVTSECT,RFTEN           2NDRY CVT ADDRESSABILITY YM3995
         L     RFTEN,SCVTSTAT           LOAD STATUS EP ADDRESS   YM3995
         DROP  RFTEN                                             YM3995
         LA    RZERO,STEPMC             INDICATE STEP MC         YM3995
         XR    RONE,RONE                INDICATE STATUS SET      YM3995
*
         BALR  RETURN,RFTEN             LINK TO STATUS ROUTINE   YM3995
*
RLSELOCK EQU   *                        RELEASE THE LOCK         YM3395
         STM   RJFCB,RETURN,IECREGSV+K12  SAVE REGISTERS 11-14   YM3995
         LR    RTWO,RWORK               SAVE WORK AREA ADDRESS   YM3995
         SETLOCK RELEASE,TYPE=LOCAL,                             YM3995X
               RELATED=(LOCAL,IGG032I4(GETLOCK))  RELEASE LOCK   YM3995
         LM    RJFCB,RETURN,IECREGSV+K12-ALLOCWKA(RTWO)          YM3995
*
         OI    DSMADTB2,DSMSMCE         SET ENQ'ED SMC SWITCH    Y02144
         OI    TYPEFLG,SMCDONE          TURN ON SMC INDICATOR
CLRECB   EQU   *                                                 A38860
         MVI   ECB,X'00'                CLEAR ECB                A38860
         EXCP  IOB
         WAIT  1,ECB=ECB
         TM    ECB,X'20'                TEST FOR A PERMANENT I/O ERROR
         BCR   7,DRBAK                  RETURN IF OK             A38860
         LA    RFIVE,12                                          A38860
         LA    RTWO,ERROR               BRANCH TO IGG032I7       Y02080
         B     XCT                                               A38860
*
* THIS SECTION SETS UP TO WRITE F2 AND F3 DSCB'S AND TO FIND F0 DSCB'S.
*
WRITEF2  LA    RFOUR,TTRLL              PICK UP ADDRESS OF SEARCH
*                                          ADDRESS FOR FORMAT 2
         LA    RTHREE,F5IN              PICK UP ADDRESS OF AREA FOR F2
         MVC   SEEK+3(5),TTRLL          UPDATE THE SEEK ADDRESS
         B     GOTID1
WRITEF3  LA    RFOUR,SAVEADR+5          PICK UP ADDRESS OF SEARCH
*                                          ADDRESS FOR FORMAT 3
         LA    RTHREE,F5IN              PICK UP ADDRESS OF AREA FOR F3
         OC    SAVEADR+5(5),SAVEADR+5   CHECK FOR THE EXISTENCE OF A
*                                          DSCB FORMAT 3
         BZ    CHANGE2                  BRANCH IF THERE IS NO FORMAT 3
         MVC   SEEK+3(5),SAVEADR+5      UPDATE THE SEEK ADDRESS
         B     GOTID1
CHANGE2  LA    RTWO,CCW1                PICK UP STARTING POINT FOR
*                                          CHANNEL PROGRAM
         IC    RONE,UHOLECTR+1
         LA    RONE,1(RONE)
         STC   RONE,UHOLECTR+1          UPDATE THE NUMBER OF DSCB'S
*                                          CREATED
         MVC   SEEK+3(4),VTOCADR       SEEK TO START OF VTOC      13559
         ST    DRBAK,CCW7+4            SAVE RETURN ADDRESS        13559
         ST    RTWO,IOB+16             START SEARCH AT CCW1       13559
         ST    RFOUR,CCW2              SET UP SEARCH ADDRESS      13559
         MVI   CCW2,X'92'              RESTORE COMMAND CODE       13559
         BAL   DRBAK,EXEC              SEARCH FOR FORMAT 0        13559
         L     DRBAK,CCW7+4            RESTORE RETURN ADDRESS     13559
         MVC   SEEK+3(4),SAVEADR+5     SEEK TO FOUND FORMAT 0     13559
         B     GOTID1                   CHANNEL PROGRAM WILL     A42162
*                                       START AT CCW6            A42162
*
* TABLE OF MODULE IDS AND ENTRY POINT ADDRESSES
*
         XCTLTABL ID=(NEXTLOAD,I6,ERROR,I7),SVC=032,LENGTH=,     Y02080X
               BRT=YES                                           Y02080
         SPACE 2                                                 Y02080
         IECDSECS CVT,PSA,SCVT,EXPAND=YES                        YM3995
JFCBKEN  DSECT
         IEFJFCBN
         EJECT                                                   Y02080
DSCBWKAR IECALLWA EP,F4,D1=(1),D2=(2,3),ADT=YES  WORK AREA       Y02144
ENTRYNUM EQU   AENTRYNM                 AENTRYNM EQUATE          Y02080
ENTRIES  EQU   AENTRIES                 AENTRIES EQUATE          Y02080
         END
