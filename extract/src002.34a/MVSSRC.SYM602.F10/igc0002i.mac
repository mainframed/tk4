 TITLE 'IGC0002I - DADSM SCRATCH - ENTRY POINT'
IGC0002I CSECT                                                   A53147
*
*          VS1 RELEASE 02 DELETIONS
*0000                                                           OX00362
*          RELEASE 18 DELETIONS
*1652026800                                                       21421
*1652                                                             MCS
*          RELEASE 19 DELETIONS
*          RELEASE 20 DELETIONS
*0953020200,055200,064200                                        S20201
*0953025400,026200,026400,060800,061700-062000                   A33080
*0953                                                            M0688
*0953000100,000260,002400-003800,004400,005400-005600,008400-    M1889
*0953008800,013400-015000,026430-026600,072800,074400            M1889
*          RELEASE 21 DELETIONS
*1198004400-004600,014100,015800,086000-086600                   A37199
*1198                                                            A42478
*0000023200                                                     SA49351
*          RELEASE 21.7 DELETIONS
*0000000230,001600,020062,020068-020086,020092,020400-020600,    A53147
*0000040000,054800-055050,055080-055190,055500,063600-064120,    A53147
*0000066900,067200-069200,071600-074320                          A53147
*0000021800-021900,026010,026210,026350,028800-029400,029420,   SA56369
*0000021800-021900,026010,026210,026350,028800-029400,029420,   SA56369
*0000029550-029700,030000-030600,031400-031905,031920,031930,   SA56369
*0000031945,031960-031975,060500,063200,074440,081800-083000,   SA56369
*0000085600-085800,087400-087800                                SA56369
*0000021000,026020-026350,056000                                SA57158
*0000061100                                                     SA57621
*          VS2 RELEASE 02 DELETIONS/CHANGES
*0000000280,000289,000620-000800,001000,001400-001600,004800-    Y02080
*0000005000,006200-006400,006800-007000,008400-008600,013200-    Y02080
*0000013500,014100-014400,015400,019100,020050-020051,020078-    Y02080
*0000020085,020092,020150-020216,021200,021980-022120,022160-    Y02080
*0000023200,023600-023800,025100-025200,025600-025800,026208-    Y02080
*0000026220,026227-026276,026282-026284,026290-026292,026294-    Y02080
*0000026590,026653,026716,028670-028830,029500-029550,031910-    Y02080
*0000031955,031985,032400,032800,047000-054600,055060,055200-    Y02080
*0000055600,055800,056200,057410-057510,060400,060520-060580,    Y02080
*0000062300,063900,064250-065400,066033-066066,066200-066800,    Y02080
*0000070400,071500-088200                                        Y02080
*0000000030-000150                                               Y02078
*0000001600-003400,005200-007800,008200-009800,012900-013700,    Y02082
*0000014100-015600,020053-020100,021940,026240,026370-026390,    Y02082
*0000026603-028660,028870,030000-030200,032000,032600-060200,    Y02082
*0000063400-064200                                               Y02082
*0000                                                            Y02144
*0000027170,067000-067200                                        Y02132
*0000026390                                                      YM1312
*0000                                                            YM1241
*0000033700,034300,036820-036825,036840,036850,064990-065000     YM1337
*0000026730-026770,026870-026930,028170,038900,068100-068200,    YM1063
*0000071800-072000                                               YM1063
*0000035300                                                      YM3960
*0000025300-025500                                               YM5399
*0000024120-024140                                               YM5748
*0000028900,029020,039400,040800                                 YM7036
*0000034300,035300,036770,067900                                 YM2880
*0000                                                            YM2887
*0000                                                            YM6992
*0000065280                                                      YM7831
*
*0000    EXTENDED MVS CVOL SUPPORT                             @Z40CSRC
*
*        SU60 ADDITIONS/DELETIONS                              @G60ASBJ
*A 020116,021700,023200,023280-123340                          @G60ASBJ
*C 000842,001400,004400,023240                                 @G60ASBJ
*D 004600                                                      @G60ASBJ
*                                                              @G60ASBJ
*MODULE NAME - IGC0002I
*
*DESCRIPTIVE NAME - DADSM SCRATCH                              @G60ASBJ
*
*COPYRIGHT - NONE
*
*CHANGE ACTIVITY - SEE DELETIONS/CHANGES FOLLOWING CSECT CARD
*
*STATUS CHANGE LEVEL 007
*
*ATTRIBUTES - REENTRANT
*
*FUNCTION - THIS MODULE IS THE  BEGINNING  OF  SCRATCH. IT FIRST
*          OBTAINS THE SCRATCH WORK AREA.  THEN IT ISSUES AN ENQ
*          ON THE TIOT (UNLESS THE TIOT WAS ALREADY ENQ'ED UPON BY
*          DYNAMIC DELLOCATION).  IF THIS RESOURCE IS UNAVAILABLE,
*          THE DATA SET IS IN THE PROCESS OF BEING OPENED OR CLOSED
*          AND THEREFORE CANNOT BE SCRATCHED.
*          THIS MODULE THEN ATTEMPTS TO FIND A PRIMARY (DEMOUNTABLE)
*          UCB ADDRESS.  IF A UCB ADDRESS WAS SUPPLIED BY THE CALLER,
*          THE TIOT IS SEARCHED TO SEE IF THE UNIT IS ALLOCATED TO
*          THE JOB.  IF ALLOCATED, THE UNIT IS TESTED TO DETERMINE
*          IF ITS ASSOCIATED VOLUME CAN BE DEMOUNTED.  IF NO UCB
*          ADDRESS WAS SUPPLIED OR IF THE SUPPLIED ADDRESS IS EITHER
*          NOT ALLOCATED OR NOT DEMOUNTABLE, THE TIOT IS SEARCHED TO
*          SEE IF ANY VOLUME IN THE VOLUME LIST IS MOUNTED ON A UNIT
*          ALLOCATED TO THE JOB.  IF A VOLUME IS MOUNTED, ITS UCB IS
*          TESTED FOR DEMOUNTABILITY.  IF A UCB IS FOUND THAT IS BOTH
*          ALLOCATED AND DEMOUNTABLE, IT BECOMES THE PRIMARY UCB.
*          BEFORE BRANCHING TO THE NEXT LOAD OF SCRATCH, THIS MODULE
*          ISSUES AN ENQ ON THE DATA SET NAME.  IF THE RETURN CODE
*          INDICATES THAT THE TASK PREVIOUSLY HAD CONTROL OF THE
*          RESOURCE, THE ENQ IS REISSUED TO OBTAIN EXCLUSIVE CONTROL
*          AND A BIT IS SET TO INDICATE A DEB SEARCH MUST BE DONE
*          IN MODULE IGG0290A TO ENSURE NO ONE IS CURRENTLY OPEN TO
*          THE DATA SET.  IF THE RETURN CODE FROM EITHER ENQ INDICATES
*          THAT THE RESOURCE IS UNAVAILABLE, THE DATA SET IS OPEN AND
*          THEREFORE CANNOT BE SCRATCHED.
*          FOR A POSSIBLE VIO DATA SET, THE MODULE SEARCHES THE DSAB
*          CHAIN FOR A TIOT ENTRY CONTAINING A VIRTUAL UCB WHOSE
*          VDSDSCB CONTAINS THE DATA SET NAME BEING SCRATCHED AND
*          WHOSE VOLUME SERIAL NUMBER IN THE VDSUCB MATCHES THE FIRST
*          VOLUME SERIAL IN THE VOLUME LIST.  IF A DSNAME/VOL SER
*          MATCH IS FOUND, IT ISSUES VSCRATCH (IF THE DATA SET HAD
*          BEEN OPENED) AND ZEROS THE DATA PORTION OF THE VDSDSCB.
*
*ENTRIES - THE ONLY ENTRY POINT TO THIS MODULE IS IGC0002I. ENTRY IS
*          MADE FROM IGG029DU.                                @G60ASBJ
*
*SUPERVISOR CALLS AND EXTERNAL ROUTINES USED BY THIS MODULE -
*          ENQ(56)  - ENQ'S ON SYSZTIOT AND SYSDSN
*          TESTAUTH - DETERMINES IF THE CALLER IS AUTHORIZED,
*                     IN SUPERVISOR STATE, OR IN KEY ZERO
*          SETLOCK  - OBTAINS/RELEASES THE LOCAL MEMORY LOCK
*          MODESET  - SWITCHES TO THE CALLER'S KEY
*          IEC0VL00 - CHECKS THE VALIDITY OF THE VOLUME LIST ADDRESS
*          VSCRATCH - DELETES VBP CONTROL BLOCKS, FREES PAGING SPACE
*          IECRES   - OBTAINS THE WORK AREA
*                   - BRANCHES TO OTHER SCRATCH MODULES
*
*OTHER MACROS ISSUED -
*          IDDVDSCB - EXPANDS THE VDSCB
*          IECDSECS - EXPANDS THE CVT, DSAB, JSCB, PSA, QDB, RB,
*                     RRPL, TIOT, TCB, AND UCB DSECTS
*          IECSCRWA - EXPANDS THE SCRATCH WORK AREA
*          XCTLTABL - BUILDS A LIST OF MODULE NAMES AND ADDRESSES
*
*INPUT -   AT ENTRY TO THIS MODULE REGISTER 1 POINTS TO THE PARAMETER
*          LIST.  REGISTER 0 MAY POINT TO A UCB, OR IT MAY CONTAIN NO
*          ADDRESS. THE PARAMETER LIST CONSISTS OF 4 WORDS:
*          WORD 1 - CHECK PURGE DATE = X'41004000'
*                 - OVERRIDE PURGE DATE = X'41005000'
*                   BIT 0 OF THE 2ND OPTION BYTE IS SET BY DYNAMIC
*                   UNALLOCATION AND BY JOB AND STEP TERMINATION
*                   TO INDICATE NOT TO INVOKE DYNAMIC ALLOCATION.
*                   BIT 0 OF THE THIRD OPTION BYTE IS SET BY
*                   DYNAMIC UNALLOCATION TO INDICATE THAT THE
*                   TIOT HAS ALREADY BEEN ENQ'ED.
*          WORD 2 - ADDRESS OF THE DATA SET NAME
*                   IF BIT 0 IS SET, ENTRY IS FROM JOB TERMINATION
*                   OR FROM STEP TERMINATION.  IF BIT 1 IS SET, ENTRY
*                   IS FROM THE READER OR WRITER.
*          WORD 3 - NOT USED
*          WORD 4 - ADDRESS OF THE VOLUME LIST
*          THE VOLUME LIST IS VARIABLE IN LENGTH. THE FIRST TWO BYTES
*          INDICATE THE NUMBER OF 12 BYTE VOLUME ID FIELDS WHICH
*          FOLLOW. A 12 BYTE VOLUME ID FIELD CONTAINS A FOUR BYTE
*          DEVICE CODE, SIX BYTE VOLUME SERIAL NUMBER, ONE BYTE SPARE,
*          AND ONE BYTE USED FOR RETURNING THE STATUS OF THE DATA SET
*          ON THAT VOLUME AS FOLLOWS:
*                    BINARY 0 - SUCCESSFUL SCRATCH
*                    BINARY 1 - DSCB NOT FOUND IN VTOC
*                    BINARY 2 - PASSWORD DIFFICULITY
*                    BINARY 3 - UNEXPIRED PURGE DATE
*                    BINARY 4 - PERMANENT I/O ERROR
*                    BINARY 5 - APPROPRIATE UCB UNAVAILABLE
*                    BINARY 6 - OPERATOR UNABLE TO MOUNT VOLUME
*                    BINARY 7 - DATA SET IS OPEN
*
*OUTPUT -  WHEN CONTROL IS TRANSFERED TO THE NEXT MODULE, REGISTER 13
*          POINTS TO THE WORK AREA, WHICH CONTAINS THE ADDRESS OF A
*          PRIMARY UCB IN 'PRUCBPTR' (IF ONE IS AVAILABLE).  ALSO,
*          'VOLNUM' CONTAINS THE NUMBER OF VOLUMES IN THE VOLUME
*          LIST, AND 'VOLPTR' CONTAINS THE ADDRESS OF THE FIRST ENTRY
*          IN THE VOLUME LIST.  REGISTER 9 POINTS TO THE CVT, AND
*          REGISTER 10 POINTS TO THE INPUT PARAMETER LIST.
*
*ERROR CONDITIONS - UPON RETURN TO THE CALLER, REGISTER 15 CONTAINS
*          ONE OF THE FOLLOWING VALUES TO INDICATED THE COMPLETION
*          STATUS OF THE SCRATCH FUNCTION:
*          00 - SUCCESSFUL COMPLETION
*          04 - NO UNIT AVAILABLE FOR MOUNTING ANY OF THE VOLUMES IN
*               THE VOLUME LIST.  THE STATUS BYTE OF EVERY ENTRY IN
*               THE VOLUME LIST IS SET TO X'05'.
*          08 - UNABLE TO SCRATCH ON ONE OR MORE VOLUMES.  THIS CODE
*               IS ACCOMPANIED BY A SETTING OF THE STATUS BYTE OF
*               EACH VOLUME LIST ENTRY AS DESCRIBED UNDER 'INPUT'.
*          12 - THE NUMBER OF VOLUMES IN THE VOLUME LIST WAS ZERO,
*               OR THE VOLUME LIST WAS NOT IN THE USER'S REGION.
*
*STORAGE - PROGRAM CODE = LESS THAN 2048 BYTES
*          WORK AREA = 896 BYTES
*
*ERROR CONDITIONS IN THIS MODULE - THE NUMBER OF VOLUMES IN THE VOLUME
*          LIST IS ZERO, THE VOLUME LIST ADDRESS IS NOT IN THE USER'S
*          REGION, THE DATA SET IS CURRENTLY OPEN, OR THE VIO DSCB
*          CANNOT BE FOUND.
*
*
*REGISTER USAGE:
R0       EQU   0
R1       EQU   1
RUCBSAVE EQU   1                        CALLER'S UCB ADDRESS     Y02082
R2       EQU   2
RDSAB    EQU   2                        POINTER TO THE DSAB      Y02082
R3       EQU   3
RTIOT    EQU   3                        POINTER TO THE TIOT      Y02082
P1       EQU   3
REND     EQU   4                        POINTER TO END OF ENTRY  Y02082
R4       EQU   4
P2       EQU   4
R5       EQU   5
VOLCTR   EQU   5
P4       EQU   6
R7       EQU   7
R8       EQU   8
VOLISTX  EQU   8
SVRBREG  EQU   8                        SVRB ADDRESS             Y02080
R9       EQU   9
RCVT     EQU   9
PL       EQU   10
RUCB     EQU   11
RBASE    EQU   12
RWKAREA  EQU   13
RETURN   EQU   14
R15      EQU   15                       EQUATE FOR REGISTER 15   Y02080
WORKREG  EQU   15
*
*OTHER EQUATES
*
K0       EQU   0                        CONSTANT OF ZERO         Y02082
K1       EQU   1                        CONSTANT OF 1            Y02082
C1       EQU   1                        CONSTANT OF 1            S20201
K2       EQU   2                        CONSTANT OF 2            Y02082
K3       EQU   3                        CONSTANT OF 3            Y02082
K8       EQU   8                        CONSTANT OF 8            Y02132
K4       EQU   4                        CONSTAND OF 4            Y02082
NONZERO  EQU   4                        NON-ZERO VALUE           YM1312
K12      EQU   12                       CONSTANT OF 12           Y02082
K16      EQU   16                       CONSTANT OF 16           Y02132
K44      EQU   44                       CONSTANT OF 44         @G60ASBJ
APD      EQU   29                       DISP. TO APPG PTR IN DEB Y02082
RTNCODE8 EQU   8                        RETURN CODE OF 8         Y02082
BADLIST  EQU   12                       BAD VOL LIST ERROR CODE  Y02082
VOLSER   EQU   4                        OFFSET TO VOLUME SERIAL  Y02082
*                                       IN A VOLUME LIST ENTRY   Y02082
STATUS   EQU   11                       OFFSET TO VOLIST STATUS  Y02082
ENTRYL   EQU   12                       VOLIST ENTRY LENGTH      Y02082
VDATALN  EQU   81                       VDSDSCB DATA LENGTH      Y02132
DYNUNALL EQU   X'80'                    DYNAMIC UNALLOCATION BIT Y02082
OPENDS   EQU   X'07'                    OPEN DATA SET ERROR CODE Y02082
NOF1     EQU   X'01'                    NO F1 DSCB ERROR CODE    Y02132
IOERR    EQU   X'04'                    VSCRATCH (I/O) ERR CODE  YM6992
BLANK    EQU   C' '                     EBCDIC BLANK             YM2880
OFF      EQU   X'FF'                    MASK TO RESET SWITCHES @Z40CSRC
*
         BALR  RBASE,R0
         USING *,RBASE                                          SA57158
         USING SCRTHWKA,RWKAREA         WORK AREA ADDRESSABILITY Y02080
         USING UCB,RUCB
         USING CVT,RCVT
         USING RBBASIC,SVRBREG          SVRB ADDRESSABILITY    @G60ASBJ
*
* THIS SECTION OBTAINS THE SCRATCH WORK AREA.
*
BEGIN    EQU   *                                                 Y01077
         LPR   PL,R1                    SAVE PARAMETER LIST PTR  Y02080
         LR    RUCB,R0                  SAVE POSSIBLE UCB ADDR   Y02080
         L     RCVT,CVTPTR              LOAD CVT ADDRESS         Y02080
         L     SVRBREG,0(R4)            LOAD SVRB ADDRESS        Y02080
         ST    RETURN,RBEXSAVE+K44      SAVE REGISTER 14       @G60ASBJ
         IECRES GET,PREFIX=FIRST,LV=SWALNGTH,ID=SCWA,            Y02080X
               STM=(R2,RBASE,RBEXSAVE)  OBTAIN WORK AREA       @G60ASBJ
         LR    RWKAREA,R1               LOAD BASE REG FOR WORK AREA
         L     RETURN,RBEXSAVE+K44      RESTORE REGISTER 14    @G60ASBJ
         ST    RETURN,SCRSAVE           SAVE R14 IN SCRATCH    @G60ASBJX
                                        WORKAREA               @G60ASBJ
         DROP SVRBREG                                          @G60ASBJ
         MODESET EXTKEY=ZERO            RETURN TO KEY ZERO AFTER Y02082X
                                        ISSUING THE IECRES GET   Y02082
*
         IECRES LOAD,MODNM=LOAD1,EXTPR=(RWKAREA),BRANCH=NO       Y02080X
                                        LET OPTIONAL TRACE PRINT Y02080X
                                        THIS MODULE'S NAME       Y02080
*
         L     R1,IECRRPRM              RECOVERY RTN LIST ADDR   Y02144
         USING RRPLIST,R1               PARM LIST ADDRESSABILITY Y02144
         OI    RRFUNCTN,RRFSCRTH        SET SCRATCH FUNCTION BIT Y02144
         DROP  R1                                                Y02144
*
         ST    R4,TCBADDR               SAVE INPUT TCB POINTER   Y02078
         ST    PL,OLDPLPTR              STORE PARAMETER LIST POINTER
         OI    SISW1,NOSMFRCD           SET NO SMF RECORD SWITCH YM5399X
                                        UNTIL AT LEAST ONE       YM5399X
                                        SCRATCH IS SUCCESSFUL    YM5399
         L     P1,K12(,PL)              LOAD ADDRESS OF VOL LIST YM5399
         LRA   P1,K0(,P1)               LOAD REAL ADDRESS OF     YM5399X
                                        VOLUME LIST              YM5399
         BC    5,ERREXIT                BR IF INVALID (INVALID   YM5748
*                                       SEGMENT TABLE ENTRY OR   YM5748
*                                       INVALID SEGMENT OR PAGE  YM5399
*                                       TABLE LENGTH)            YM5399
         LM    P1,P4,0(PL)              LOAD PARAMETER LIST
*                                            P1 = SCRATCH OPTIONS
*                                            P2 = DATA SET NAME ADDR
*                                            P3 = SPARE
*                                            P4 = VOLUME LIST ADDR
         MVC   PDSNAME,K0(P2)           SAVE DSNAME IN PROTECTED YM1337
*                                       WORK AREA                YM1337
         LA    P2,PDSNAME               GET ADDR OF PROTECTED    YM1337
*                                       DSNAME                   YM1337
         ST    P2,DSMADTW3              SAVE ADDRESS OF DSNAME   Y02144
         MVC   VOLNUM,0(P4)             MOVE NUMBER OF VOLUMES   Y02080
         LH    VOLCTR,VOLNUM            LOAD NBR OF VOLS IN LIST Y02080
         LTR   VOLCTR,VOLCTR            IS LIST EMPTY
         BNP   ERREXIT                  BRANCH IF YES           SA56369
*
* THIS SECTION CHECKS IF THE VOLUME LIST ADDRESS IS WITHIN THE USER'S
* REGION.  IF THE CALLER IS IN SUPERVISOR STATE OR IN KEY ZERO, THEN
* THE VOLUME LIST ADDRESS DOES NOT HAVE TO BE CHECKED.
*
VALCK    EQU   *                                                SA57158
         TESTAUTH FCTN=1,KEY=YES,STATE=YES,BRANCH=YES  TEST IF   Y02080X
                                        CALLER IS IN KEY 0 OR    Y02080X
                                        IN SUPERVISOR STATE      Y02080
         LTR   R15,R15                  TEST IF ANY TRUE         Y02080
         BZ    BYPASS                   BRANCH IF YES           SA57158
*
* THIS SECTION CALCULATES THE STARTING AND ENDING ADDRESSES OF
* THE AREA TO BE CHECKED
*
         MH    VOLCTR,TWELVE            12 TIMES NUMBER OF      SA57158
*                                       VOLUMES                 SA57158
         LA    VOLCTR,C1(VOLCTR)        ADD ONE                 SA57158
         LA    R1,0(P4)                 RELOAD VOLIST ADDRESS    Y02080
         LR    R2,R1                    GET STARTING ADDRESS     Y02080
         AR    R2,VOLCTR                GET ADDRESS OF LAST      Y02080
*                                       BYTE OF VOLIST          SA57158
         STM   RUCB,P1,IECREGSV+K12     SAVE REGISTERS 11-3      Y02080
         LR    R7,RWKAREA               SAVE WORK AREA ADDRESS   Y02080
         XR    R5,R5                    INITIALIZE REGISTER      YM1241
OBTNLOCK SETLOCK OBTAIN,TYPE=LOCAL,MODE=UNCOND,                  Y02080X
               RELATED=(LOCAL,IGC0002I(RLSELOCK))  OBTAIN LOCK   Y02080
         LM    RUCB,RETURN,IECREGSV+K12-SCRTHWKA(R7)  RESTORE    Y02080
         L     R4,TCBADDR               TCB ADDRESS IN REG 4     Y02078
         L     R15,CVT0VL00             VALIDITY CHECK RTN ADDR  Y02080
         BALR  RETURN,R15               LINK TO VALIDITY CHECK   Y02080
         BZ    RLSELOCK                 BRANCH IF VALID ADDRESS  YM1312
         LA    R5,NONZERO               INDICATE INVALID ADDRESS YM1312
*
RLSELOCK SETLOCK RELEASE,TYPE=LOCAL,                             Y02080X
               RELATED=(LOCAL,IGC0002I(OBTNLOCK))  RELEASE LOCK  Y02080
         LM    RUCB,P1,IECREGSV+K12-SCRTHWKA(R7)  RESTORE REGS   Y02080
         LTR   R5,R5                    TEST IF ADDRESS VALID    Y02080
         BNZ   ERREXIT                  BRANCH IF NOT VALID      Y02080
         LH    VOLCTR,VOLNUM            GET VOLUME COUNT         Y02080
*
* THIS SECTION ATTEMPTS TO ENQ ON THE TIOT, UNLESS SCRATCH HAS BEEN
* CALLED BY DYNAMIC UNALLOCATION (IF THE CALLER IS IN KEY ZERO AND BIT
* 0 OF OPTION BYTE 3 OF THE FIRST WORD OF THE PARAMETER LIST IS SET).
*
BYPASS   EQU   *                        ENQ ON THE TIOT          Y02082
         L     RDSAB,TCBADDR            LOAD TCB ADDRESS         Y02082
         USING TCB,RDSAB                TCB ADDRESSABILITY       Y02082
         L     RDSAB,TCBJSCB            GET JSCB ADDRESS         Y02082
         USING IEZJSCB,RDSAB            JSCB ADDRESSABILITY      Y02082
         L     RDSAB,JSCBACT            GET ACTIVE JSCB ADDRESS  Y02082
         L     RDSAB,JSCDSABQ           GET DSAB QDB ADDRESS     Y02082
         ST    RDSAB,DSABQDB            SAVE FOR TIOT ENQ        Y02082
         TESTAUTH FCTN=1,KEY=YES,BRANCH=YES  TEST IF CALLER IS   YM1063X
                                        AUTHORIZED OR KEY ZERO   YM1063
*                                                                YM1063
         LTR   R15,R15                  TEST IF EITHER TRUE      YM1063
         BNZ   FIRSTENQ                 BRANCH IF NOT TO ENQ     Y02082
         TM    K2(PL),DYNUNALL          TEST IF ENTERED FROM     Y02082X
                                        DYNAMIC UNALLOCATION     Y02082
         BO    SKIPENQ                  BRANCH IF YES            Y02082
*                                                                Y02082
FIRSTENQ EQU   *                        FIRST ENQ                Y02082
         L     R2,CVTTCBP               LOAD TCB/ASID POINTERS   YM1063
         L     R2,K12(,R2)              LOAD CURRENT ASID ADDR   YM1063
         USING ASCB,R2                  ASCB ADDRESSABILITY      YM1063
         MVC   TIOTMNR(K2),ASCBASID     MOVE ASID INTO ENQ MINOR YM1063
*                                       RESOURCE NAME            YM1063
         DROP  R2                                                YM1063
         MVC   MJNAME(L'TIOTMJR),TIOTMJR  MAJOR RESOURCE NAME    Y02082
         MVC   ENQAREA(ENQLTH1),ENQLIST1  MOVE IN PARAMETER LIST Y02082
         ENQ   (MJNAME,TIOTMNR),MF=(E,ENQAREA)  ENQ ON SYSZTIOT  Y02082
         LTR   R15,R15                  TEST IF SUCCESSFUL ENQ   Y02082
         BZ    SETENQSW                 BRANCH IF YES            Y02082
*
         LA    VOLISTX,K2(P4)           ADDRESS OF FIRST VOLUME  Y02082
OPENERR  EQU   *                        OPEN DATA SET ERROR      Y02082
         LA    R3,OPENDS                OPEN DATA SET ERROR CODE Y02132
         B     SETERRCD                 GO STORE THE STATUS BYTE Y02132
NOF1DSCB EQU   *                        NO F1 DSCB ERROR         Y02132
         LA    R3,NOF1                  NO F1 DSCB ERROR CODE    Y02132
SETERRCD EQU   *                        SET ERROR CODE           Y02132
         L     R1,TCBADDR               LOAD TCB ADDRESS         Y02078
         USING TCB,R1                   TCB ADDRESSABILITY       Y02078
         MODESET EXTKEY=RBT234,WORKREG=2  SWITCH TO CALLER'S KEY Y02078
ERRLOOP  EQU   *                        BEGIN LOOP               Y02082
         STC   R3,STATUS(VOLISTX)       SET ERROR CODE IN VOLIST Y02132
         LA    VOLISTX,ENTRYL(VOLISTX)  NEXT VOL LIST ENTRY ADDR Y02082
         BCT   VOLCTR,ERRLOOP           BRANCH IF MORE VOLUMES   Y02082
         MODESET EXTKEY=ZERO            RETURN TO KEY ZERO       Y02082
         LA    VOLISTX,K2(P4)           RELOAD ADDR OF 1ST VOL   Y02082
         LA    WORKREG,RTNCODE8         ERROR RETURN CODE        Y02082
         B     SETCODE                  GO SAVE ERROR CODE       Y02082
*
SETENQSW EQU   *                        SET ENQ SWITCH           Y02082
         LA    R1,TIOTMNR               MINOR RESOURCE NAME ADDR Y02144
         ST    R1,DSMADTW2              SAVE MINOR ENQ NAME ADDR Y02144
         OI    DSMADTB2,DSMTIOTE        SET TIOT ENQ'ED SWITCH   Y02144
         OI    SISW1,TIOTENQ            SET TIOT ENQ'ED SWITCH   Y02082
SKIPENQ  EQU   *                        ENQ SKIPPED              Y02082
         LA    VOLISTX,K2(P4)           ADDRESS OF FIRST VOLUME  Y02082
         LTR   RUCBSAVE,RUCB            TEST FOR PASSED UCB ADDR Y02082
         BZ    FINDUNIT                 BRANCH IF ZERO           Y02082
         LA    RUCBSAVE,K0(RUCBSAVE)    CLEAR HIGH BYTE          Y02082
         TM    UCBJBNR,UCBVRDEV         TEST IF A VIRTUAL UCB    Y02132
         BZ    USERSUCB                 BRANCH IF NOT            Y02132
         ST    RUCB,PRUCBPTR            SAVE INPUT UCB ADDRESS   Y02132
         B     VIODSTST                 BRANCH TO VIO D/S TEST   Y02132
*
* IF THE USER SUPPLIED A UCB ADDRESS, THIS SECTION SEARCHES THE
* TIOT TO DETERMINE IF THE UNIT IS ALLOCATED TO THE JOB.  IF IT IS
* ALLOCATED, IT BECOMES THE PRIMARY UCB IF IT IS DEMOUNTABLE.
*
USERSUCB EQU   *                        USER SUPPLIED UCB ADDR   Y02082
         L     RDSAB,DSABQDB            LOAD DSAB QDB ADDRESS    Y02082
         USING QDB,RDSAB                QDB ADDRESSABILITY       Y02082
         L     RDSAB,QDBFELMP           GET FIRST DSAB ADDRESS   Y02082
         USING DSAB,RDSAB               DSAB ADDRESSABILITY      Y02082
UDSABTST EQU   *                        DSAB TEST                Y02082
         LTR   RDSAB,RDSAB              IS THERE ANOTHER DSAB    Y02082
         BZ    FINDUNIT                 BRANCH IF NOT            Y02082
         L     RTIOT,DSABTIOT           LOAD TIOT ADDRESS        Y02082
         USING TIOENTRY,RTIOT           TIOT ADDRESSABILITY      Y02082
         SR    REND,REND                ZERO REGISTER FOR IC     Y02082
         IC    REND,TIOELNGH            LENGTH OF TIOT ENTRY     Y02082
         AR    REND,RTIOT               ADDRESS OF FIRST BYTE    Y02082
*                                       PAST THE TIOT ENTRY      Y02082
         LA    RTIOT,TIOEFSRT-K1        ADDR OF FIRST UCB ADDR   Y02082
UNEXTUCB EQU   *                        END OF ENTRY             Y02082
         CR    RTIOT,REND               TEST IF AT END OF ENTRY  Y02082
         BNL   UNXTDSAB                 BRANCH IF YES            Y02082
         L     RUCB,K0(,RTIOT)          GET UCB ADDRESS          Y02082
         LA    RUCB,K0(RUCB)            CLEAR HIGH BYTE          Y02082
         LTR   RUCB,RUCB                IS THERE A UCB ADDRESS   Y02082
         BZ    UINCRPTR                 BRANCH IF NOT            YM1063
         CR    RUCB,RUCBSAVE            TEST IF EQUAL TO THE     Y02082
*                                       USER-SUPPLIED UCB ADDR   Y02082
         BE    LINKTEST                 BRANCH IF YES            Y02082
UINCRPTR EQU   *                                                 YM1063
         LA    RTIOT,L'TIOESTTB+L'TIOEFSRT(RTIOT)  INCREMENT TO  Y02082
*                                       NEXT UCB ADDRESS IN TIOT Y02082
         B     UNEXTUCB                 GO TRY NEXT UCB ADDRESS  Y02082
*
UNXTDSAB EQU   *                        NEXT DSAB                Y02082
         L     RDSAB,DSABFCHN           GET NEXT DSAB ADDRESS    Y02082
         B     UDSABTST                 GO TEST IT               Y02082
*
* THIS SECTION LINKS TO A ROUTINE THAT DETERMINES IF THE UNIT IS
* ELIGIBLE FOR DEMOUNTING.
*
LINKTEST BAL   RETURN,TESTUCB           GO TEST DEMOUNTABILITY  SA56369
         LTR   RUCB,RUCB                TEST IF DEMOUNTABLE     SA56369
         BZ    FINDUNIT                 BRANCH IF NOT           SA56369
         ST    RDSAB,PRDSABAD           SAVE PRIMARY DSAB ADDR   Y02082
         ST    RUCB,PRUCBPTR            SAVE PRIMARY UCB ADDRESS Y02082
         L     RETURN,DSABTIOT          LOAD TIOT ENTRY ADDRESS  YM7036
         LA    RETURN,TIOEJFCB-TIOENTRY(,RETURN)  POINT TO JFCB  YM7036X
                                        PREFIX ADDRESS           YM7036
         IECRES LOCJFCB,(RETURN)        LOCATE THE JFCB          YM7036
         CLC   PDSNAME,JFCBDSNM-INFMJFCB(RETURN)  CHECK IF       YM7036X
                                        THIS IS THE RIGHT TIOT   YM7036X
                                        ENTRY FOR THIS DSNAME    YM7036
         BE    FOUNDUCB                 BRANCH IF CORRECT        YM7036
         OI    SISW2,UCBFNDSW           SET THE UCB FOUND SWITCH YM7036
         B     UINCRPTR                 GO TRY NEXT TIOT ENTRY   YM7036
*
* THIS SECTION TESTS IF THE UCB IS DEMOUNTABLE.
*
TESTUCB  EQU   *
         TM    SRTESTAB,SRTEBSVL        TEST FOR SHARED VOLUME
         BZ    WRONGUCB                 BR IF SHARED
         TM    UCBTBYT3,UCB3DACC        TEST IF DIRECT ACCESS    Y02082
         BZ    WRONGUCB                 BRANCH IF NOT            Y02082
         TM    UCBDMCT,UCBDMC           TEST IF DATAMGT COUNT=0  Y02082
         BNZ   WRONGUCB                 BRANCH IF NOT            Y02082
         TM    SRTESTAT,SRTEONLI        TEST FOR ONLINE DEVICE  SA56369
         BZ    WRONGUCB                 BRANCH IF NOT ONLINE    SA56369
         TM    SRTESTAT,SRTERESV+SRTEPRES+SRTESYSR  TEST FOR RESERVED,
*                                       PERMANENTLY RESIDENT, OR
*                                       SYSTEM RESIDENT VOLUME
         BNZ   WRONGUCB                 BRANCH IF YES           SA56369
         BR    RETURN                   RETURN IF UCB IS OKAY   SA56369
WRONGUCB EQU   *                                                SA56369
         SR    RUCB,RUCB                DISCARD THE UCB ADDRESS SA56369
         BR    RETURN                   RETURN                  SA56369
*
* THIS SECTION INITIALIZES REGISTERS AND COUNTERS BEFORE
* BRANCHING TO IGG0290E.
*
NOPRIUCB EQU   *                        SET FLAG                 Y02082
         OI    STYPEFLG,NOMNTDEV        TURN ON NO DEVICE BIT    Y02082
FOUNDUCB LA    VOLISTX,2(P4)            LOAD ADDR OF FIRST VOLUME
         LH    VOLCTR,VOLNUM            LOAD NBR OF VOLS IN LIST Y02080
         ST    VOLISTX,VOLPTR           SAVE ADDR OF 1ST ENTRY   Y02082
         NI    SISW2,X'FF'-UCBFNDSW     TURN OFF UCB FOUND SWTCH YM7036
*
* THIS SECTION ISSUES AN EXCLUSIVE ENQ DIRECTED TO THE INITIATOR'S
* TCB WITH A MAJOR RESOURCE NAME OF SYSDSN AND THE DATA SET NAME AS
* THE MINOR RESOURCE NAME. IF THE RETURN CODE INDICATES THE TASK
* PREVIOUSLY HAD CONTROL OF THE RESOURCE, THE ENQ IS REISSUED AND
* A BIT IS SET TO INDICATE A DEB SEARCH MUST BE DONE TO ENSURE NO
* ONE IS CURRENTLY OPEN TO THE DATA SET.
*
         MVC   ENQAREA(ENQLTH2),ENQLIST2  MOVE IN PARAMETER LIST Y02082
         MVC   MJNAME(L'DSNMJR),DSNMJR  MAJOR RESOURCE NAME      Y02082
         LA    P2,PDSNAME+L'PDSNAME-K1  LAST BYTE OF DSNAME      YM2880
TESTDSN  EQU   *                        SEARCH FOR NON-BLANK     YM2880
         CLI   K0(P2),BLANK             IS THIS ONE A BLANK      YM2880
         BNE   GETLNGTH                 BRANCH IF NO TO COMPUTE  YM2880X
                                        LENGTH OF NAME FOR ENQ   YM2880
         BCT   P2,TESTDSN               TRY NEXT BYTE            YM2880
GETLNGTH EQU   *                        CALCULATE LENGTH         YM2880
         LA    R2,PDSNAME-K1            BYTE PRECEDING DSNAME    YM2880
         SR    P2,R2                    P2 HAS LENGTH FOR ENQ    YM2880
         L     R2,TCBADDR               LOAD TCB ADDRESS         Y02082
         USING TCB,R2                   TCB ADDRESSABILITY       Y02082
         L     R2,TCBJSCB               GET JSCB ADDRESS         Y02082
         USING IEZJSCB,R2               JSCB ADDRESSABILITY      Y02082
         L     R2,JSCBACT               GET ACTIVE JSCB ADDRESS  YM2887
         L     R2,JSCBTCBP              INITIATOR TCB ADDRESS    Y02082
         ENQ   (MJNAME,PDSNAME,,(P2)),TCB=(R2),RET=USE,MF=(E,ENQAREA)  X
                                                                 YM2880
         LTR   R15,R15                  TEST RETURN CODE         Y02082
         BNZ   ERRTEST                  BRANCH IF ENQ FAILED     Y02082
*
         TM    SISW2,DSNENQCV           SUFFIXED CVOL NAME ?   @Z40CSRC
         BO    CLRCVOLN                 YES, GO CLEAR SUFFIX   @Z40CSRC
         OI    DSMADTB2,DSMDSNE         SET DSN ENQ'ED SWITCH    Y02144
         OI    SISW1,DSNENQ             SET ENQ ON SYSDSN SWITCH Y02082
         B     CHKCVOL                  GO CHK FOR A CVOL      @Z40CSRC
ERRTEST  EQU   *                        IS DSN ALREADY ENQ'ED    Y02082
         CLI   ENQAREA+K3,RTNCODE8      TEST IF TASK ALREADY     Y02082
*                                       ENQ'ED ON SYSDSN         Y02082
         BNE   CLRERRCV                 BRANCH IF NOT          @Z40CSRC
         TM    SISW2,DSNENQCV           DID CVOL ENQ FAIL      @Z40CSRC
         BO    CLRERRC2                 YES, FAIL REQUEST      @Z40CSRC
         MVC   ENQAREA(ENQLTH3),ENQLIST3  MOVE IN PARAMETER LIST Y02082
         ENQ   (MJNAME,PDSNAME,,(P2)),TCB=(R2),RET=CHNG,MF=(E,ENQAREA) X
                                                                 YM2880
         LTR   R15,R15                  TEST RETURN CODE         Y02082
         BNZ   CLRERRCV                 BR IF NOT SUCCESSFUL   @Z40CSRC
         OI    SISW1,DEBSRCH            DEB SEARCH REQUIRED SW   Y02082
*
* HAVING JUST SUCCESSFULLY ENQUEUED ON SYSDSN, CHECK FOR SCRATCHING
* A CVOL (DSNAME='SYSCTLG').  IF SO, DO AN ADDITIONAL ENQ USING
* THE NAME SYSCTLG.VXXXXXX WHERE XXXXXX IS THE VOLSER.
* THIS WILL CHECK FOR USERS OPEN TO THE CVOL VIA SVC 26 (CATALOG
* MANAGEMENT) AND SVC 22 (OPENJ).
* IF THE CVOL IS ALREADY ENQUEUED UPON, FAIL THE REQUEST.
* A DEB SEARCH WOULD BE INSUFFICIENT, SINCE SVC 26 USES A DUMMY
* DEB THAT IS NOT ON THE DEB CHAIN.
*
CHKCVOL  EQU   *                        CHECK FOR A CVOL       @Z40CSRC
         CLC   PDSNAME(L'CSYSCTNM),CSYSCTNM SCRATCHING A CVOL  @Z40CSRC
         BNE   NEXTLOAD                 NO, GO TO NEXT LOAD    @Z40CSRC
         OI    SISW2,DSNENQCV           FLAG SUFFIXED CVOL ENQ @Z40CSRC
         LA    P2,PDSNAME(P2)           POINT BEYOND DSNAME    @Z40CSRC
         MVC   K0(L'CDOTV,P2),CDOTV     MOVE IN '.V'           @Z40CSRC
         MVC   L'CDOTV(L'UCBVOLI,P2),VOLSER(VOLISTX) MV VOLSER @Z40CSRC
         LA    P2,KDOTVVL-K1(,P2)       ADDR OF LAST CHAR      @Z40CSRC
         B     GETLNGTH                 GO ENQ ON SYSCTLG.V... @Z40CSRC
NEXTLOAD EQU   *                        GO TO IGG0290E           Y02082
         LA    R2,LOAD2                 POINT TO IGG0290E        Y02080
         B     CONTINUE                 GO BRANCH TO IGG0290E    Y02082
*
* CLEAR SUFFIX QUALIFIER FROM CVOL NAME
*
CLRCVOLN EQU   *                        CLEAR CVOL NAME        @Z40CSRC
         LA    P2,PDSNAME-KDOTVVL(P2)   POINT AT '.VXXXXXX'    @Z40CSRC
         MVC   K0(KDOTVVL,P2),KDOTVVL(P2) BLANK OUT SUFFIX     @Z40CSRC
         B     NEXTLOAD                 GO TO NEXT LOAD        @Z40CSRC
CLRERRCV EQU   *                        CVOL CLRING FOR ERRS   @Z40CSRC
         TM    SISW2,DSNENQCV           IS DSNAME SUFFIXED     @Z40CSRC
         BNO   OPENERR                  NO, GO TO ERROR RTN    @Z40CSRC
CLRERRC2 EQU   *                        CLEAR SUFFIX           @Z40CSRC
         NI    SISW2,OFF-DSNENQCV       SHOW NOTHING TO DEQ    @Z40CSRC
         LA    P2,PDSNAME-KDOTVVL(P2)   POINT AT '.VXXXXXX'    @Z40CSRC
         MVC   K0(KDOTVVL,P2),KDOTVVL(P2) BLANK OUT SUFFIX     @Z40CSRC
         B     OPENERR                  GO TO ERROR ROUTINE    @Z40CSRC
*
* THIS SECTION SEARCHES THE TIOT TO SEE IF ANY VOLUME IN THE VOLUME
* LIST IS MOUNTED ON A UNIT ALLOCATED TO THE JOB.  IF MOUNTED, IT
* LINKS TO A ROUTINE THAT DETERMINES IF THE UNIT IS DEMOUNTABLE.
*
FINDUNIT EQU   *                        FIND A UNIT              Y02082
         TM    SISW2,UCBFNDSW           TEST IF A UCB WAS FOUND  YM7036
         BO    FOUNDUCB                 BRANCH IF YES            YM7036
         OC    VOLSER(L'SRTEVOLI,VOLISTX),VOLSER(VOLISTX)  TEST  Y02082
*                                       FOR VALID VOLUME SERIAL  Y02082
         BZ    NOUNIT                   BRANCH IF ZERO           Y02082
         CLC   VOLSER(L'SRTEVOLI,VOLISTX),BLANKS  TEST FOR VALID Y02132
*                                       VIRTUAL UCB VOL SER      Y02132
         BNE   NOVIOVOL                 BRANCH IF NOT            Y02132
         CLC   CSYS,PDSNAME             CHECK FOR CHARACTERS SYS YM1337
*                                       IN BYTES 0-2 OF THE DSN  Y02132
         BNE   NOVIOVOL                 BRANCH IF NOT            Y02132
         CLC   CDOTT,PDSNAME+K8         TEST FOR .T IN BYTES 8-9 YM1337
         BNE   NOVIOVOL                 BRANCH IF NOT            Y02132
         CLC   CDOTR,PDSNAME+K16        CHECK FOR CHARACTERS .R  YM1337
*                                       IN BYTES 16-17           Y02132
         BE    VIODSTST                 BRANCH IF YES TO TEST IF Y02132
*                                       A VIO DATA SET           Y02132
*
NOVIOVOL EQU   *                        GET FIRST DSAB ADDRESS   Y02132
         L     RDSAB,DSABQDB            LOAD DSAB QDB ADDRESS    Y02082
         USING QDB,RDSAB                QDB ADDRESSABILITY       Y02082
         L     RDSAB,QDBFELMP           GET FIRST DSAB ADDRESS   Y02082
         USING DSAB,RDSAB               DSAB ADDRESSABILITY      Y02082
DSABTEST EQU   *                        DSAB TEST                Y02082
         LTR   RDSAB,RDSAB              IS THERE ANOTHER DSAB    Y02082
         BZ    NOUNIT                   BRANCH IF NOT            Y02082
         L     RTIOT,DSABTIOT           GET TIOT POINTER         Y02082
         USING TIOENTRY,RTIOT           TIOT ADDRESSABILITY      Y02082
         SR    REND,REND                ZERO REGISTER FOR IC     Y02082
         IC    REND,TIOELNGH            LENGTH OF TIOT ENTRY     Y02082
         AR    REND,RTIOT               ADDRESS OF TIOT ENTRY    Y02082
*                                       PAST THE TIOT ENTRY      Y02082
         LA    RTIOT,TIOEFSRT-K1        ADDR OF FIRST UCB ADDR   Y02082
NEXTUCB  EQU   *                        GET NEXT UCB             Y02082
         CR    RTIOT,REND               TEST IF AT END OF ENTRY  Y02082
         BNL   NEXTDSAB                 BRANCH IF YES            Y02082
         L     RUCB,K0(,RTIOT)          GET UCB ADDRESS          Y02082
         LA    RUCB,K0(RUCB)            CLEAR HIGH BYTE          Y02082
         LTR   RUCB,RUCB                IS THERE A UCB ADDRESS   Y02082
         BZ    NXTENTRY                 BRANCH IF NOT            YM1063
         CLC   SRTEVOLI,VOLSER(VOLISTX)  COMPARE VOL SER NUMBERS Y02082
         BNE   NXTENTRY                 BRANCH IF NOT EQUAL      Y02082
*
         BAL   RETURN,TESTUCB           TEST IF UCB DEMOUNTABLE  Y02082
*
         LTR   RUCB,RUCB                TEST IF DEMOUNTABLE      Y02082
         BZ    NXTENTRY                 BRANCH IF NOT            YM7036
         ST    RDSAB,PRDSABAD           SAVE PRIMARY DSAB ADDR   YM7036
         ST    RUCB,PRUCBPTR            SAVE PRIMARY UCB ADDRESS YM7036
         L     RETURN,DSABTIOT          LOAD TIOT ENTRY ADDRESS  YM7036
         LA    RETURN,TIOEJFCB-TIOENTRY(,RETURN)  POINT TO JFCB  YM7036X
                                        PREFIX ADDRESS           YM7036
         IECRES LOCJFCB,(RETURN)        LOCATE THE JFCB          YM7036
         CLC   PDSNAME,JFCBDSNM-INFMJFCB(RETURN)  CHECK IF       YM7036X
                                        THIS IS THE RIGHT TIOT   YM7036X
                                        ENTRY FOR THIS DSNAME    YM7036
         BE    FOUNDUCB                 BRANCH IF CORRECT        YM7036
         OI    SISW2,UCBFNDSW           SET THE UCB FOUND SWITCH YM7036
NXTENTRY EQU   *                        NEXT ENTRY               Y02082
         LA    RTIOT,L'TIOESTTB+L'TIOEFSRT(,RTIOT)  INCREMENT TO Y02082
*                                       NEXT UCB ADDRESS IN TIOT Y02082
         B     NEXTUCB                  GO TEST NEXT UCB ADDRESS Y02082
*
NEXTDSAB EQU   *                        NEXT DSAB                Y02082
         L     RDSAB,DSABFCHN           GET NEXT DSAB ADDRESS    Y02082
         B     DSABTEST                 GO TEST IT               Y02082
NOUNIT   EQU   *                        NO UNIT                  Y02082
         BCT   VOLCTR,TRYNEXT           TEST IF MORE VOLUMES     Y02082
         TM    SISW2,UCBFNDSW           TEST IF A UCB WAS FOUND  YM7036
         BO    FOUNDUCB                 BRANCH IF YES            YM7036
         B     NOPRIUCB                 BRANCH IF NOT            Y02082
TRYNEXT  EQU   *                        NEXT VOLUME LIST ENTRY   Y02082
         LA    VOLISTX,ENTRYL(VOLISTX)  POINT TO NEXT ENTRY      Y02082
*                                       IN THE VOLUME LIST       Y02082
         B     NOVIOVOL                 GO TRY NEXT VOLUME       YM7036
*
* THIS SECTION PREPARES TO BRANCH TO THE LAST LOAD OF SCRATCH.
*
ERREXIT  EQU   *                                                SA56369
         LA    WORKREG,BADLIST          ERROR CODE FOR EMPTY    SA57261
*                                       VOLUME LIST OR INVALID  SA57261
*                                       VOLUME LIST ADDRESS     SA57261
SETCODE  EQU   *                        SET CODE                 Y02082
         STH   WORKREG,ERCODE          STORE ERROR CODE IN VOL LIST
         OI    STYPEFLG,EXITBIT         SET EXIT BIT             Y02082
         LA    R2,LASTLOAD              POINT TO IGG0290D        Y02080
*
* THIS SECTION BRANCHES TO ANOTHER LOAD OF SCRATCH.
*
CONTINUE EQU   *                        GO TO NEXT LOAD          Y02082
         MVC   WKADEB+APD(K3),CVTXAPG+K1  POINT DEB TO IOS AVT   Y02082
         L     PL,OLDPLPTR              GET PARAMETER LIST PTR   Y02082
         IECRES LOAD,EXTPR=(RWKAREA),MODNM=(R2),BRANCH=DIRECT    Y02082
*
* THIS SECTION SEARCHES THE DSAB CHAIN FOR A TIOT ENTRY CONTAINING
* A VIRTUAL UCB WHOSE VDSDSCB CONTAINS THE DATA SET NAME BEING
* SCRATCHED AND WHOSE VOLUME SERIAL NUMBER IN THE VDSUCB MATCHES
* THE FIRST VOLUME SERIAL IN THE VOLUME LIST. IF A DSNAME/VOL SER
* MATCH IS FOUND, IT ISSUES VSCRATCH AND ZEROS THE VDSDSCB.
*
VIODSTST EQU   *                        FIND VIO DSN             Y02132
         OI    STYPEFLG,VIOBIT          SET VIO BIT              Y02132
         L     RDSAB,DSABQDB            GET DSAB QDB ADDRESS     Y02132
         USING QDB,RDSAB                QDB ADDRESSABILITY       Y02132
         L     RDSAB,QDBFELMP           GET PTR TO FIRST DSAB    Y02132
         USING DSAB,RDSAB               DSAB ADDRESSABILITY      Y02132
VDSABTST EQU   *                        DSAB TEST                Y02132
         LTR   RDSAB,RDSAB              IS THERE ANOTHER DSAB    Y02132
         BZ    NOMATCH                  BRANCH IF NOT            Y02132
         L     RTIOT,DSABTIOT           GET TIOT ADDRESS         Y02132
         USING TIOENTRY,RTIOT           TIOT ADDRESSABILITY      Y02132
         L     RUCB,TIOEFSRT-K1         GET UCB ADDRESS          Y02132
         LA    RUCB,K0(RUCB)            CLEAR HIGH BYTE          Y02132
         LTR   RUCB,RUCB                IS THERE A UCB ADDRESS   Y02132
         BZ    VNXTDSAB                 IF NO, TRY NEXT DSAB     Y02132
         TM    UCBJBNR,UCBVRDEV         TEST IF A VIRTUAL UCB    Y02132
         BZ    VNXTDSAB                 BRANCH IF NOT            Y02132
         CLC   SRTEVOLI,VOLSER(VOLISTX)  COMPARE VOL SER'S       Y02132
         BNE   VNXTDSAB                 BRANCH IF NOT EQUAL      Y02132
         LR    R7,RUCB                  VDSCB ADDRESS            Y02132
         USING VDSCB,R7                 VDSCB ADDRESSABILITY     Y02132
         CLC   DS1DSNAM,PDSNAME         COMPARE DSNAME'S         YM1337
         BNE   VNXTDSAB                 BRANCH IF NOT EQUAL      Y02132
VIOSCRTH EQU   *                        TEST COUNT               Y02132
         CLI   DSABOPCT,K0              IS DSAB OPEN COUNT ZERO  Y02132
         BNE   OPENERR                  BRANCH IF NOT            Y02132
         CLI   UCBDMCT,K0               IS UCB DATAMGT COUNT 0   Y02132
         BNE   OPENERR                  BRANCH IF NOT            Y02132
         L     R1,VDSDSPCT              GET PTR TO DSPCT         Y02132
         LTR   R1,R1                    DOES IT EQUAL ZERO       Y02132
         BZ    NOVSCRTH                 BRANCH IF YES            Y02132
*
         XC    VDSDSPCT,VDSDSPCT        ZERO VDSDSPCT PTR        YM7831
*
         STM   RUCB,RETURN,IECREGSV+K12  SAVE REGISTERS 11-14    Y02132
         LR    R2,RWKAREA               SAVE WORK AREA ADDRESS   Y02132
OBTNLCK2 SETLOCK OBTAIN,TYPE=LOCAL,MODE=UNCOND,                  Y02132X
               RELATED=(LOCAL,IGC0002I(RLSELCK2))  OBTAIN LOCK   Y02132
         LM    RUCB,RETURN,IECREGSV+K12-SCRTHWKA(R2)  RESTORE    Y02132
         VSCRATCH DSPCT=(1)             ISSUE VSCRATCH MACRO     Y02132X
                                        REGISTER 13 POINTS TO    Y02132X
                                        THE 18 WORD SAVE AREA    Y02132
*                                                                YM6992
         LR    R1,R15                   SAVE VSCRATCH RETURN CDE YM6992
         STM   RUCB,RETURN,IECREGSV+K12  SAVE REGISTERS 11-14    Y02132
         LR    R2,RWKAREA               SAVE WORK AREA ADDRESS   Y02132
RLSELCK2 SETLOCK RELEASE,TYPE=LOCAL,                             Y02080X
               RELATED=(LOCAL,IGC0002I(OBTNLCK2))  RELEASE LOCK  Y02080
         LM    RUCB,RETURN,IECREGSV+K12-SCRTHWKA(R2)  RESTORE    Y02132
         LTR   R1,R1                    TEST VSCRATCH RETURN CDE YM6992
         BZ    NOVSCRTH                 BRANCH IF NO ERROR       YM6992
         L     R1,TCBADDR               LOAD TCB ADDRESS         YM6992
         USING TCB,R1                   TCB ADDRESSABILITY       YM6992
         MODESET EXTKEY=RBT234,WORKREG=2  SWITCH TO CALLER'S KEY YM6992
         MVI   STATUS(VOLISTX),IOERR    I/O ERROR CODE           YM6992
         MODESET EXTKEY=ZERO            RETURN TO KEY ZERO       YM6992
         DROP  R1                                                YM6992
         BCT   VOLCTR,VIOERR            ERROR IF MORE VOLUMES    YM6992
         LA    WORKREG,RTNCODE8         SCRATCH ERROR CODE       YM6992
         B     SETCODE                  GO BRANCH TO LAST LOAD   YM6992
*                                                                YM6992
NOVSCRTH EQU   *                        ZERO OUT THE VIO DSCB    Y02132
         XC    DS1FMTID(VDATALN),DS1FMTID  ZERO OUT THE VIO DSCB Y02132X
                                        EXCEPT THE DSNAME FIELD  Y02132
         XR    R1,R1                    ZERO REGISTER            Y02132
         BCTR  R1,K0                    DECREMENT TO MINUS 1     Y02132
         STH   R1,VDSABSTT              INDICATE D/S SCRATCHED   Y02132
         DROP  R7                                                Y02132
         NI    SISW1,X'FF'-NOSMFRCD     RESET NO SMF RECORD BIT  Y02132
         L     R1,TCBADDR               LOAD TCB ADDRESS         Y02132
         USING TCB,R1                   TCB ADDRESSABILITY       Y02132
         MODESET EXTKEY=RBT234,WORKREG=2  SWITCH TO CALLER'S KEY Y02132
         MVI   STATUS(VOLISTX),K0       SUCCESSFUL SCRATCH CODE  Y02132
         MODESET EXTKEY=ZERO            RETURN TO KEY ZERO       Y02132
         DROP  R1                                                Y02132
         BCT   VOLCTR,VIOERR            ERROR IF MORE VOLUMES    Y02132
         XR    WORKREG,WORKREG          SUCCESSFUL SCRATCH CODE  Y02132
         B     SETCODE                  GO BRANCH TO LAST LOAD   Y02132
VIOERR   EQU   *                        SET ERROR CODE           Y02132
         LA    VOLISTX,ENTRYL(VOLISTX)  NEXT VOL LIST ENTRY ADDR Y02132
         B     NOF1DSCB                 GO SET AN ERROR CODE OF  Y02132
*                                       X'01' FOR OTHER ENTRIES  Y02132
VNXTDSAB EQU   *                        NEXT                     Y02132
         L     RDSAB,DSABFCHN           GET NEXT DSAB ADDRESS    Y02132
         B     VDSABTST                 GO TEST IT               Y02132
NOMATCH  EQU   *                        NO VIO DSN MATCH         Y02132
         OC    PRUCBPTR,PRUCBPTR        TEST FOR INPUT UCB ADDR  Y02132
         BZ    NOF1DSCB                 BRANCH IF NONE           Y02132
         NI    STYPEFLG,X'FF'-VIOBIT    RESET VIO BIT            Y02132
         XC    PRUCBPTR,PRUCBPTR        CLEAR INVALID UCB ADDR   Y02132
         B     NOVIOVOL                 BRANCH TO NON-VIO LOGIC  Y02132
*
*** PROGRAM CONSTANTS
*
TWELVE   DC    H'12'                                             A33080
CSYSCTNM DS    0CL8                     CVOL DSNAME 'SYSCTLG ' @Z40CSRC
CSYS     DC    CL3'SYS'                 CONSTANT OF 'SYS'        Y02132
         DC    C'CTLG '                 REST OF 'SYSCTLG '     @Z40CSRC
CDOTT    DC    CL2'.T'                  CONSTANT OF '.T'         Y02132
CDOTR    DC    CL2'.R'                  CONSTANT OF '.R'         Y02132
CDOTV    DC    C'.V'                    CONSTANT OF '.V'       @Z40CSRC
BLANKS   DC    CL6' '                   SIX EBCDIC BLANKS        Y02132
TIOTMJR  DC    CL8'SYSZTIOT'            TIOT MAJOR RESOURCE NAME Y02082
DSNMJR   DC    CL8'SYSDSN'              DSN MAJOR RESOURCE NAME  Y02082
ENQLIST1 ENQ   (,,E,6,SYSTEM),RET=HAVE,MF=L  TIOT ENQ PARM LIST  Y02082
ENQLTH1  EQU   *-ENQLIST1               LENGTH OF ENQ PARM LIST  Y02082
ENQLIST2 ENQ   (,,E,,SYSTEM),RET=USE,MF=L  1ST DSN ENQ LIST      YM2880
ENQLTH2  EQU   *-ENQLIST2               LENGTH OF ENQ PARM LIST  Y02082
ENQLIST3 ENQ   (,,E,,SYSTEM),RET=CHNG,MF=L  2ND DSN ENQ LIST     YM2880
ENQLTH3  EQU   *-ENQLIST3               LENGTH OF ENQ PARM LIST  Y02082
*
*** TABLE OF MODULE NAMES AND ENTRY POINT ADDRESSES
*
         XCTLTABL ID=(LOAD1,IGC0002I,LOAD2,IGG0290E,LASTLOAD,    Y02080X
               IGG0290D),SVC=029,LENGTH=,BRT=YES                 Y02080
         SPACE 2                                                 Y02080
         IECDSECS ASCB,CVT,DSAB,JSCB,PSA,QDB,RB,RRPL,TIOT,TCB,   YM1063X
               UCB,EXPAND=YES                                    YM1063
WORKAREA IECSCRWA EP,ADT=YES            SCRATCH WORK AREA        Y02144
         EJECT                                                   Y02132
         IDDVDSCB                       VDSCB EXPANSION          Y02132
         EJECT                                                   Y02132
         ORG   VDSDSCB                                           Y02132
         IECSDSL1 (1)                   FORMAT 1 DSCB EXPANSION  Y02132
         EJECT                                                   YM7036
         IEFJFCBN                       JFCB DSECT               YM7036
KDOTVVL  EQU   L'CDOTV+L'UCBVOLI        LENGTH OF CVOL SUFFIX  @Z40CSRC
         END
