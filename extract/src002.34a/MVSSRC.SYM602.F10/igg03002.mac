 TITLE 'IGG03002 - RENAME - LOAD 2 - LOCATE VOLUME TO BE PROCESSED'
IGG03002 CSECT
*
*          RELEASE 17 DELETIONS                                       *
*1675006000,012000                                                19894
*        RELEASE 18 DELETIONS                                         *
*3476192000-198000,330000,342000,396000,402000,708000-711000       DM0Q
*3476005700,447000-450000,591000,603000                           20326
*3476                                                             21488
*          RELEASE 19 DELETIONS                                       *
*1291318000,447000,645000-663000,924000                          S19028
*          RELEASE 20 DELETIONS                                       *
*3450390000                                                      S20016
*3450388200,389300,770000-770500                                 TS1611
*3450234000,249000,325200-325300,660000                          S20201
*          RELEASE 21 DELETIONS                                       *
*1195006000,033000,090000-099000,111000,195000,540000-543000,    A38860
*1195549000-552000,681000-684000,769000-769500,902000            A38860
*1195267000-270000,276000,679000,768500                          A37199
*1195                                                            A44427
*1195                                                            A36775
*0000665400                                                     SA49351
*          RELEASE 21.7 DELETIONS
*0000192300-193800,658200-664800,688000                         SA53147
*          RELEASE 22 DELETIONS                                       *
*0000063000-066000,072000-081000,102000-105000,110000-112000,    Y02080
*0000117000,194100,207000,216000,259000,294000,315000-324000,    Y02080
*0000325200-325500,330000-340500,342500,348000,354000-357000,    Y02080
*0000375000,387000,396000,402000,468000,474000-489000,499400,    Y02080
*0000510000-516000,546000-548000,579000-606000,657900-669000,    Y02080
*0000682000,768300-768900,771000-897000,901000-903000,912000-    Y02080
*0000918000,923000                                               Y02080
*0000                                                            Y02078
*0000021000-108000,112000,117000,166000,169000,191200-192000,    Y02082
*0000229000-231000,285000-669000,687500-926600                   Y02082
*0000                                                            Y02134
*0000                                                            Y02144
*0000290000-291000,325000,361000,388000,501000                   YM1337
*0000324000,364000-366000                                        YM1063
*0000364500-365000                                               YM7377
*0000349400                                                      YM7515
*0000344000,573000                                               YM7036
*
*          VS2 RELEASE 03 DELETIONS/CHANGES                           *
*0000352000                                                    @ZA01347
*
*          VS2 RELEASE 04 DELETIONS/CHANGES                           *
*0000353000                                                    @ZA05225
*
*MODULE NAME - IGG03002
*
*DESCRIPTIVE NAME - RENAME - LOAD 2 - LOCATE VOLUME TO BE PROCESSED
*
*COPYRIGHT - NONE
*
*CHANGE ACTIVITY - SEE DELETIONS/CHANGES FOLLOWING CSECT CARD
*
* STATUS CHANGE LEVEL 004
*
*ATTRIBUTES - REENTRANT
*
*FUNCTION - AFTER BUILDING THE DEB/DCB AND THE IOB, THIS MODULE
*          SEARCHES THE TIOT TO SEE IF THE CURRENT VOLUME IN THE
*          VOLUME LIST IS MOUNTED ON A UNIT ALLOCATED TO THE JOB.
*          IF THE VOLUME IS NOT MOUNTED ON AN ALLOCATED UNIT, IT
*          SEARCHES THE UCB LOOKUP TABLE TO DETERMINE IF THE VOLUME
*          IS MOUNTED ANYWHERE.  IF IT IS MOUNTED, DYNAMIC ALLOCATION
*          IS CALLED TO TRY TO ALLOCATE THE UNIT.  IF THE VOLUME IS
*          NOT MOUNTED ANYWHERE OR IF THE UNIT CANNOT BE DYNAMICALLY
*          ALLOCATED, AND THERE IS A PRIMARY UCB, CONTROL IS GIVEN TO
*          THE SCRATCH/RENAME DEMOUNT/MOUNT/VERIFY ROUTINE TO MOUNT
*          THE VOLUME.  IF THERE IS NO PRIMARY UCB OR IF THE VOLUME
*          CANNOT BE MOUNTED, THE NEXT VOLUME IN THE VOLUME LIST IS
*          PROCESSED.  IF THE VOLUME WAS FOUND TO BE MOUNTED ON AN
*          ALLOCATED UNIT, AFTER THE UNIT CONTAINING THE VOLUME IS
*          DYNAMICALLY ALLOCATED, AFTER THE VOLUME IS MOUNTED ON THE
*          PRIMARY UCB, OR AFTER ALL THE VOLUME LIST ENTRIES HAVE BEEN
*          PROCESSED, CONTROL IS GIVEN TO THE THIRD LOAD OF RENAME.
*
*ENTRIES - THE ONLY ENTRY POINT TO THIS MODULE IS IGG03002.  ENTRY IS
*          MADE FROM IGC00030, IGG03001, OR IGG0300F.
*
*SUPERVISOR CALLS AND EXTERNAL ROUTINES -
*          DYNALLOC - DYNAMICALLY ALLOCATES THE UNIT (SVC 99)
*          MODESET  - SWITCHES TO THE CALLER'S KEY
*          IECRES   - BRANCHES TO OTHER RENAME MODULES
*
*OTHER MACROS ISSUED -
*          IECDSECS - EXPANDS THE CVT, DSAB, QDB, RB, TCB, TIOT,
*                     AND UCB DSECTS
*          IECRENQA - EXPANDS THE RENAME WORK AREA
*          IEFZB4D0 - EXPANDS THE DYNAMIC ALLOCATION REQUEST BLOCK
*          IEFZB4D2 - EXPANDS THE DYNAMIC ALLOCATION TEST UNIT KEYS
*          XCTLTABL - BUILDS A LIST OF MODULE IDS AND ADDRESSES
*
*INPUT -   AT ENTRY TO THIS MODULE REGISTER 9 POINTS TO THE CVT.
*          REGISTER 10 POINTS TO THE PARAMETER LIST.  REGISTER 13
*          POINTS TO THE WORK AREA, IN WHICH 'VOLNUM' CONTAINS THE
*          NUMBER OF VOLUMES REMAINING TO BE RENAMED, AND 'VOLPTR'
*          CONTAINS THE ADDRESS OF THE CURRENT ENTRY IN THE VOLUME
*          LIST.
*          IF REENTRY FROM IGG0300F, REGISTER 14 CONTAINS ONE OF
*          THE FOLLOWING RETURN CODES:
*          00 - VOLUME WAS SUCCESSFULLY MOUNTED AND/OR VERIFIED
*          04 - VOLUME COULD NOT BE DEMOUNTED
*          08 - VOLUME COULD NOT BE MOUNTED
*          0C - I/O ERROR OCCURRED WHEN READING THE VOLUME LABEL
*          ALSO, REGISTER 5 CONTAINS THE NUMBER OF VOLUMES REMAINING
*          TO BE RENAMED, AND REGISTER 8 POINTS TO THE CURRENT ENTRY
*          IN THE VOLUME LIST.
*
*OUTPUT -  REGISTER 8 POINTS TO THE CURRENT ENTRY IN THE VOLUME LIST.
*          REGISTER 9 POINTS TO THE CVT, AND REGISTER 10 POINTS TO
*          THE PARAMETER LIST.  REGISTER 13 POINTS TO THE WORK AREA,
*          IN WHICH THE DEB CONTAINS THE UCB ADDRESS OF THE VOLUME
*          WHOSE DATA SET IS TO BE RENAMED.
*          THE FOLLOWING ADDITIONAL INFORMATION IS PASSED TO IGG0300F:
*          REGISTER 2 POINTS TO THE DEB.
*          REGISTER 3 POINTS TO THE VOLUME SERIAL NUMBER.
*          REGISTER 4 CONTAINS THE CHARACTERS '3002'.
*          REGISTER 11 CONTAINS THE PRIMARY (DEMOUNTABLE) UCB ADDRESS.
*
*STORAGE - PROGRAM CODE CSECT = LESS THAN 1024 BYTES
*          WORK AREA = 676 BYTES
*
*ERROR CONDITIONS - NO DEVICE AVAILABLE FOR MOUNTING THE VOLUME
*
*RESTRICTIONS - NONE
*
*REGISTER USAGE:
R0       EQU   0
R1       EQU   1
R2       EQU   2
RPARM    EQU   2                        PTR TO SVC 99 PARM LIST  Y02082
RDSAB    EQU   2                        POINTER TO THE DSAB      Y02082
R3       EQU   3
P1       EQU   3
RWRK     EQU   3                        WORK REGISTER            Y02082
RTIOT    EQU   3                        POINTER TO THE TIOT      Y02082
R4       EQU   4
RTEXT    EQU   4                        POINTER TO TEXT UNIT     Y02082
REND     EQU   4                        POINTER TO END OF ENTRY  Y02082
P2       EQU   4
R5       EQU   5
R6       EQU   6                                                 S20016
VOLCTR   EQU   5
P4       EQU   6
R7       EQU   7
VOLISTX  EQU   8
R9       EQU   9
RCVT     EQU   9
PL       EQU   10
RUCB     EQU   11
RBASE    EQU   12
RWKAREA  EQU   13
RETURN   EQU   14
WORKREG  EQU   15
RTNCODE  EQU   15                       RETURN CODE REGISTER     Y02082
*
* OTHER EQUATES
*
K0       EQU   0                        CONSTANT OF ZERO         Y02082
K1       EQU   1                        CONSTANT OF 1            Y02082
K2       EQU   2                        CONSTANT OF 2            Y02082
K3       EQU   3                        CONSTANT OF 3            Y02082
K4       EQU   4                        CONSTANT OF 4            Y02080
K6       EQU   6                        CONSTANT OF 6            Y02082
K7       EQU   7                        CONSTANT OF 7            Y02082
K8       EQU   8                        CONSTANT OF 8            Y02082
K12      EQU   12                       CONSTANT OF 12           Y02082
FW4      EQU   16                       LENGTH OF 4 FW'S         Y02082
STATEXTL EQU   8                        STATUS TEXT UNIT LENGTH  Y02082
UNITEXTL EQU   10                       UNIT TEXT UNIT LENGTH    Y02082
VOLTEXTL EQU   12                       VOLSER TEXT UNIT LENGTH  Y02082
RTNCODE4 EQU   4                        ERROR RETURN CODE OF 4   Y02082
RTNCODE8 EQU   8                        ERROR RETURN CODE OF 8   Y02082
VOLSER   EQU   4                        OFFSET TO VOLUME SERIAL  Y02082
*                                       IN A VOLUME LIST ENTRY   Y02082
STATUS   EQU   11                       OFFSET TO STATUS BYTE    Y02082
ENTRYL   EQU   12                       VOL LIST ENTRY LENGTH    Y02082
UCBADDR  EQU   32                       DISP. TO UCB ADDR IN DEB Y02082
DEVMOD   EQU   X'C0'                    DEB DEVICE MODIFIER      Y02082
IOERR    EQU   X'04'                    I/O ERROR IN VOL LIST    Y02082
NODEVERR EQU   X'05'                    NO DEVICE ERROR          Y02082
NOMNTERR EQU   X'06'                    VOLUME NOT MOUNTED ERROR Y02082
STATOLD  EQU   X'01'                    DISP=OLD INDICATOR       Y02082
AVTDISP  EQU   29                       DISPLACEMENT TO THE AVT SA53147
*                                       ADDRESS IN THE DEB      SA53147
DEBSIZE  EQU   48                       SIZE OF THE DEB         SA53147
UNRELATD EQU   X'02'                    UNRELATED FLAG IN IOB    A37199
*
         BALR  RBASE,R0
         USING BEGIN,RBASE
         USING RENAMWKA,RWKAREA         WORK AREA ADDRESSABILITY Y02080
         USING UCB,RUCB
         USING CVT,RCVT
*
BEGIN    L     RCVT,CVTPTR
         TM    SISW2,DEMNTSW+MOUNTSW+VERIFYSW  TEST IF RETURN    Y02134
*                                       FROM DEMOUNT, MOUNT,     Y02134
*                                       AND/OR VERIFY FUNCTION   Y02134
         BNZ   RETURNPT                 BRANCH IF YES            Y02134
*
*** BUILD CONTROL BLOCKS
*
         XC    WKADEB(DEBSIZE),WKADEB   ZERO THE DEB             Y02082
         MVC   WKADEB+AVTDISP(K3),CVTXAPG+K1  INSERT IOS AVT ADR Y02082
         LA    R1,WKADEB                BUILD THE DEB            Y02082
         MVI   16(R1),X'01'                  NUM OF EXTS = 1
         LA    R2,DCB
         ST    R2,24(R1)                     INSERT DCB ADDR
         MVI   24(R1),X'0F'                  INSERT DEB ID
         MVI   28(R1),X'04'                  INSERT EXT SCALE
         MVC   42(6,R1),EXTCONST             INSERT EXTS AND TK COUNT
         ST    R1,DEBPTR                BUILD DCB
         OI    IOB,UNRELATD             SET UNRELATED BIT IN IOB A37199
         LA    WORKREG,ECB              BUILD IOB
         ST    WORKREG,IOB+4                 INSERT ECB ADDR
         LA    RETURN,CCW1                                       A37199
         LA    WORKREG,DCB
         STM   RETURN,WORKREG,IOB+16    CCW AND DCB ADDRESSES    A37199
NEXTVOL  EQU   *                        NEXT VOLUME              Y02082
         LH    VOLCTR,VOLNUM            LOAD NUMBER OF VOLUMES LEFT
         L     VOLISTX,VOLPTR           LOAD VOLUME LIST INDEX
         L     R1,TCBADDR               LOAD TCB ADDRESS         Y02078
         USING TCB,R1                   TCB ADDRESSABILITY       Y02078
         MODESET EXTKEY=RBT234,WORKREG=2  SWITCH TO CALLER'S KEY Y02078
         MVI   STATUS(VOLISTX),K0       CLEAR STATUS BYTE IN     Y02082
*                                       THIS VOLUME LIST ENTRY   Y02082
         DROP  R1                                                Y02082
         MODESET EXTKEY=ZERO            RETURN TO KEY ZERO       Y02082
         MVC   PVOLSER,VOLSER(VOLISTX)  SAVE VOLSER IN WRK AREA  YM1337
         OC    PVOLSER,PVOLSER          TEST FOR VALID VOLSER    YM1337
         BZ    NODEVICE                 NO UNIT IF ZERO          Y02082
*
* THE FOLLOWING SECTIONS TRY TO FIND THE UCB ADDRESS OF THE CURRENT
* VOLUME IN THE VOLUME LIST. FIRST, IT SEARCHES THE TIOT TO SEE IF
* THE VOLUME IS MOUNTED ON A UNIT ALLOCATED TO THE JOB. IF NOT, IT
* SEARCHES THE UCB LOOKUP TABLE TO DETERMINE IF THE VOLUME IS MOUNTED
* ANYWHERE. IF IT IS MOUNTED, DYNAMIC ALLOCATION IS CALLED TO TRY TO
* ALLOCATE THE UNIT. IF THE VOLUME IS NOT MOUNTED ANYWHERE OR IF IT
* CANNOT BE DYNAMICALLY ALLOCATED, THE VOLUME WILL BE MOUNTED ON THE
* PRIMARY UCB, IF ONE EXITS.
*
FINDUCB  EQU   *                        FIND UCB                 Y02082
         L     RDSAB,DSABQDB            LOAD DSAB QDB ADDRESS    Y02082
         USING QDB,RDSAB                QDB ADDRESSABILITY       Y02082
         L     RDSAB,QDBFELMP           GET FIRST DSAB ADDRESS   Y02082
         USING DSAB,RDSAB               DSAB ADDRESSABILITY      Y02082
DSABTEST EQU   *                        DSAB TEST                Y02082
         LTR   RDSAB,RDSAB              IS THERE ANOTHER DSAB    Y02082
         BZ    TABLSRCH                 BRANCH IF NOT            Y02082
         L     RTIOT,DSABTIOT           GET TIOT POINTER         Y02082
         USING TIOENTRY,RTIOT           TIOT ADDRESSABILITY      Y02082
         SR    REND,REND                ZERO REGISTER FOR IC     Y02082
         IC    REND,TIOELNGH            LENGTH OF TIOT ENTRY     Y02082
         AR    REND,RTIOT               ADDRESS OF FIRST BYTE    Y02082
*                                       PAST THE TIOT ENTRY      Y02082
         LA    RTIOT,TIOEFSRT-K1        ADDR OF FIRST UCB ADDR   Y02082
UCBTEST  EQU   *                        UCB TEST                 Y02082
         CR    RTIOT,REND               TEST IF AT END OF ENTRY  Y02082
         BNL   NEXTDSAB                 BRANCH IF YES            Y02082
         L     RUCB,K0(,RTIOT)          GET UCB ADDRESS          Y02082
         LA    RUCB,K0(RUCB)            CLEAR HIGH BYTE          Y02082
         LTR   RUCB,RUCB                IS THERE A UCB ADDRESS   Y02082
         BZ    NXTENTRY                 BRANCH IF NOT            YM1063
         CLC   SRTEVOLI,PVOLSER         COMPARE VOL SER NUMBERS  YM1337
         BNE   NXTENTRY                 BRANCH IF NOT EQUAL      Y02082
         CLC   UCBTBYT3(K2),K2(VOLISTX)  COMPARE DEVICE TYPES    Y02082
         BNE   NXTENTRY                 BRANCH IF NOT EQUAL      Y02082
         TM    UCBTBYT3,UCB3DACC        TEST IF DIRECT ACCESS    Y02082
         BZ    NXTENTRY                 BRANCH IF NOT            Y02082
         TM    SRTESTAT,SRTEONLI        TEST IF ONLINE           Y02082
         BO    FOUNDUCB                 BRANCH IF ONLINE         Y02082
NXTENTRY EQU   *                        NEXT UCB                 Y02082
         LA    RTIOT,L'TIOESTTB+L'TIOEFSRT(,RTIOT)  INCREMENT TO Y02082
*                                       NEXT UCB ADDRESS IN TIOT Y02082
         B     UCBTEST                  GO TEST NEXT UCB ADDRESS Y02082
NEXTDSAB EQU   *                        NEXT DSAB                Y02082
         L     RDSAB,DSABFCHN           GET NEXT DSAB ADDRESS    Y02082
         B     DSABTEST                 GO TEST IT               Y02082
FOUNDUCB EQU   *                        FOUND IT                 Y02082
         ST    RDSAB,DSABADDR           SAVE DSAB ADDRESS        Y02082
         ST    RUCB,WKADEB+UCBADDR      SAVE UCB ADDRESS IN DEB  Y02082
         MVI   WKADEB+UCBADDR,DEVMOD    INSERT DEVICE MODIFIER   Y02082
         L     RETURN,DSABTIOT          LOAD TIOT ENTRY ADDRESS  YM7036
         LA    RETURN,TIOEJFCB-TIOENTRY(,RETURN)  POINT TO JFCB  YM7036X
                                        PREFIX ADDRESS           YM7036
         IECRES LOCJFCB,(RETURN)        LOCATE THE JFCB          YM7036
         CLC   PDSNAME,JFCBDSNM-INFMJFCB(RETURN)  CHECK IF       YM7036X
                                        THIS IS THE RIGHT TIOT   YM7036X
                                        ENTRY FOR THIS DSNAME    YM7036
         BE    MOUNTED                  BRANCH IF CORRECT        YM7036
         OI    SISW2,UCBFNDSW           SET THE UCB FOUND SWITCH YM7036
         B     NXTENTRY                 GO TRY NEXT TIOT ENTRY   YM7036
*
* THIS SECTION SEARCHES THE UCB LOOKUP TABLE TO DETERMINE IF THE
* VOLUME IS MOUNTED ANYWHERE.
*
TABLSRCH EQU   *                        UCB LOOKUP TABLE SEARCH  Y02082
         TM    SISW2,UCBFNDSW           TEST IF A UNIT WAS FOUND YM7036
         BNO   LDLOOKUP                 BRANCH IF NOT            YM7515
         L     RUCB,WKADEB+UCBADDR      LOAD FOUND UCB ADDRESS   YM7515
         LA    RUCB,K0(RUCB)            CLEAR HIGH BYTE          YM7515
         B     MOUNTED                  GO TEST IF VERIFIED      YM7515
LDLOOKUP EQU   *                        LOAD UCB LOOKUP TABL ADR YM7515
         L     R2,CVTILK2               GET LOOKUP TABLE ADDRESS Y02082
UCBLOOP  EQU   *                        NEXT                     Y02082
         SR    RUCB,RUCB                CLEAR REG              @ZA01347
         ICM   RUCB,K3,K0(R2)           GET UCB ADDR           @ZA01347
         CLM   RUCB,K3,EXTCONST         TEST IF AT END OF TBL  @ZA05225
         BE    ENDADDR                  BRANCH IF YES            Y02082
         LTR   RUCB,RUCB                UCB TABLE MAY HAVE HOLES Y02082
         BZ    NEXTADDR                 BRANCH IF NO ADDRESS     Y02082
         TM    UCBTBYT3,UCB3DACC        TEST IF DIRECT ACCESS    Y02082
         BZ    NEXTADDR                 BRANCH IF NOT            Y02082
         TM    SRTESTAT,SRTEONLI        TEST IF ONLINE           Y02082
         BZ    NEXTADDR                 BRANCH IF NOT ONLINE     Y02082
         CLC   SRTEVOLI,PVOLSER         COMPARE VOL SER NUMBERS  YM1337
         BNE   NEXTADDR                 BRANCH IF NOT EQUAL      Y02082
         CLC   UCBTBYT3(K2),K2(VOLISTX)  COMPARE DEVICE TYPES    Y02082
         BNE   NEXTADDR                 BRANCH IF NOT EQUAL      YM1063
         TM    UCBSTAB,UCBBSVL          TEST IF NONSHAREABLE     YM7377
         BZ    ALLOCVOL                 BRANCH IF SHAREABLE TO   YM7377X
                                        TRY TO ALLOCATE THE UNIT YM1063
         B     ENDADDR                  DON'T TRY TO DYNAMICALLY YM1063
*                                       ALLOCATE IF NONSHAREABLE YM1063
NEXTADDR EQU   *                        GET NEXT ADDR            Y02082
         LA    R2,K2(R2)                PT TO NEXT ADDR IN TABLE Y02082
         B     UCBLOOP                  GO TRY NEXT UCB ADDRESS  Y02082
*
ENDADDR  EQU   *                        NO MORE                  Y02082
         L     RUCB,PRUCBPTR            LOAD PRIMARY UCB ADDRESS Y02082
         LTR   RUCB,RUCB                IS THERE A PRIMARY UCB   Y02082
         BZ    NODEVICE                 BRANCH IF NO PRIMARY UCB Y02082
         CLC   UCBTBYT3(K2),K2(VOLISTX)  COMPARE DEVICE TYPES    Y02082
         BNE   NODEVICE                 BRANCH IF NOT EQUAL      Y02082
*
* THIS SECTION PREPARES TO BRANCH TO IGG0300F TO DEMOUNT (IF REQUIRED)
* AND MOUNT AND/OR VERIFY THE NEXT VOLUME.
*
MOUNTVOL EQU   *                        MOUNT A VOLUME           Y02134
         ST    RUCB,WKADEB+UCBADDR      MODIFY UCB ADDR IN DEB   Y02134
         MVI   WKADEB+UCBADDR,DEVMOD    INSERT DEVICE MODIFIER   Y02134
         OI    SISW2,DEMNTSW+MOUNTSW    SET DEMOUNT & MOUNT SW   Y02134
         MVC   DSABADDR,PRDSABAD        INITIALIZE DSAB ADDRESS  Y02134
VERIFY   EQU   *                        VERIFY A VOLUME          Y02134
         LA    R2,WKADEB                DEB ADDR IN REGISTER 2   Y02134
         LA    R3,PVOLSER               VOLSER ADDR IN REG 3     YM1337
         L     R4,RETNAME               RETURN NAME IN REG 4     Y02134
         LA    R7,MNTLOAD               POINT TO ID OF IGG0300F  Y02134
         B     LOADEXIT                 GO BRANCH TO IGG0300F    Y02134
*
* THIS SECTION TESTS THE RETURN CODE FROM THE DEMOUNT/MOUNT ROUTINE.
*
RETURNPT EQU   *                        RETURN HERE              Y02134
         NI    SISW2,X'FF'-DEMNTSW-MOUNTSW-VERIFYSW  RESET SW    Y02134
         B     TESTERR(RETURN)          RETURN CODE BRANCH TABLE Y02134
TESTERR  EQU   *                        TEST FOR ERROR           Y02134
         B     MOUNTED                  VOLUME MOUNTED/VERIFIED  Y02134
         B     NODEVICE                 UNSUCCESSFUL DEMOUNT     Y02134
         B     NOMOUNT                  MOUNT/VERIFY FAILED      Y02134
         B     PERMERR                  I/O ERROR OCCURRED       Y02134
*
* THIS SECTION PREPARES TO BRANCH TO THE NEXT LOAD OF RENAME.
*
MOUNTED  EQU   *                        VOLUME MOUNTED           Y02134
         NI    SISW2,X'FF'-UCBFNDSW     TURN OFF UCB FOUND SWTCH YM7036
         TM    UCBDMCT,UCBMOUNT         TEST MOUNT BIT           Y02134
         BNO   VERIFIED                 BRANCH IF VERIFIED       Y02134
         OI    SISW2,VERIFYSW           SET VERIFY SWITCH        Y02134
         B     VERIFY                   BRANCH TO VERIFY         Y02134
VERIFIED EQU   *                        VOLUME VERIFIED          Y02134
         NI    RTYPEFLG,X'FF'-NOMNTDEV  RESET THE 'NO DEVICE FOR Y02134
*                                       MOUNTING' SWITCH AS AT   Y02134
*                                       LEAST ONE VOL IS MOUNTED Y02134
         BCT   VOLCTR,MOREVOLS          TEST FOR LAST VOLUME     Y02082
         OI    RTYPEFLG,EXITBIT         SET EXIT BIT IF LAST VOL Y02082
MOREVOLS EQU   *                        MORE VOLUMES             Y02082
         LA    R1,ENTRYL(VOLISTX)       NEXT VOL LIST ENTRY ADDR Y02082
         ST    R1,VOLPTR                SAVE NEXT ENTRY ADDRESS  Y02082
         STH   VOLCTR,VOLNUM            SAVE UPDATED VOL COUNT   Y02082
NEXTLOAD EQU   *                        SET FOR NEXT LOAD        Y02082
         LA    R7,LOAD3                 POINT TO ID OF IGG03001  Y02082
*
* THIS SECTION BRANCHES TO ANOTHER LOAD OF RENAME.
*
LOADEXIT EQU   *                        EXIT TO NEXT LOAD        Y02082
         IECRES LOAD,EXTPR=(RWKAREA),MODID=(R7),BRANCH=DIRECT    Y02082
*
* THIS SECTION SETS AN APPROPRIATE ERROR CODE IN THE STATUS BYTE
* OF THE CURRENT VOLUME LIST ENTRY. IF THE LAST VOLUME HAS BEEN
* PROCESSED, CONTROL IS PASSED TO THE NEXT LOAD OF RENAME. IF NOT,
* THE NEXT VOLUME LIST ENTRY IS PROCESSED.
*
PERMERR  EQU   *                        I/O ERROR                Y02082
         LA    R3,IOERR                 I/O ERROR                Y02082
         B     SETCODE                  BRANCH TO SET ERROR CODE Y02082
NODEVICE EQU   *                        NO UNIT FOR MOUNTING     Y02082
         LA    R3,NODEVERR              NO UNIT FOR MOUNTING ERR Y02082
         B     SETCODE                  BRANCH TO SET ERROR CODE Y02082
NOMOUNT  EQU   *                        VOLUME NOT MOUNTED       Y02082
         LA    R3,NOMNTERR              VOLUME NOT MOUNTED ERROR Y02082
SETCODE  EQU   *                        SET ERROR CODE           Y02082
         L     R1,TCBADDR               LOAD TCB ADDRESS         Y02078
         USING TCB,R1                   TCB ADDRESSABILITY       Y02078
         MODESET EXTKEY=RBT234,WORKREG=2  SWITCH TO CALLER'S KEY Y02078
         DROP  R1                                                Y02078
         STC   R3,STATUS(VOLISTX)       PUT ERROR CODE IN VOLIST Y02078
         MODESET EXTKEY=ZERO            RETURN TO KEY ZERO       Y02082
         BCT   VOLCTR,NOTLAST           TEST IF LAST VOLUME      Y02082
         OI    RTYPEFLG,EXITBIT         SET EXIT BIT IF LAST VOL Y02082
NOTLAST  EQU   *                        NOT LAST                 Y02082
         LA    R1,ENTRYL(VOLISTX)       NEXT VOL LIST ENTRY ADDR Y02082
         ST    R1,VOLPTR                SAVE NEXT ENTRY ADDR     Y02082
         STH   VOLCTR,VOLNUM            SAVE UPDATED VOL COUNT   Y02082
         LA    RTNCODE,RTNCODE8         NORMAL ERROR RETURN CODE Y02082
         STH   RTNCODE,ERCODE           SAVE ERROR RETURN CODE   Y02082
         TM    RTYPEFLG,EXITBIT         TEST IF LAST VOLUME      Y02082
         BZ    NEXTVOL                  BRANCH IF NOT TO PROCESS Y02082
*                                       THE NEXT VOLUME          Y02082
         TM    RTYPEFLG,NOMNTDEV        TEST IF AN ERROR CODE OF Y02082
*                                       4 MUST BE RETURNED TO    Y02082
*                                       CALLER IN REGISTER 15    Y02082
         BZ    NEXTLOAD                 BRANCH IF NOT            Y02082
         MVI   ERCODE+K1,RTNCODE4       SET RETURN CODE TO 4     Y02082
         B     NEXTLOAD                 GO BRANCH TO IGG03001    Y02082
*
* THIS SECTION BUILDS THE REQUEST BLOCK TO INTERFACE WITH DYNAMIC
* ALLOCATION (SVC 99) TO TRY TO ALLOCATE THE UNIT ON WHICH THE
* CURRENT VOLUME IN THE VOLUME LIST IS MOUNTED.
*
ALLOCVOL EQU   *                        TRY TO ALLOCATE THE UNIT Y02082
         LA    RPARM,VTOCDSCB           PARAMETER LIST BASE      Y02082
         USING S99RBP,RPARM                                      Y02082
         LA    RWRK,K4(,RPARM)          INCREMENT BY FOUR        Y02082
         ST    RWRK,S99RBPTR            SET UP REQUEST BLOCK PTR Y02082
         OI    S99RBPTR,S99RBPND        TURN ON HIGH ORDER BIT   Y02082
         LR    RPARM,RWRK               ADDRESS OF REQUEST BLOCK Y02082
         USING S99RB,RPARM              REQ BLOCK ADDRESSABILITY Y02082
         XC    S99RB(S99RBEND-S99RB),S99RB  ZERO REQ BLOCK CORE  Y02082
         MVI   S99RBLN,S99RBEND-S99RB   LENGTH OF REQUEST BLOCK  Y02082
         MVI   S99VERB,S99VRBAL         INDICATE ALLOCATION      Y02082
         LA    RWRK,S99RBEND            ADDR OF TEXT POINTERS    Y02082
         ST    RWRK,S99TXTPP            PLACE IN REQUEST BLOCK   Y02082
         OI    S99FLG11,S99NOMNT        INDICATE THAT VOL MUST   Y02082
*                                       BE MOUNTED IF ALLOCATED  Y02082
         OI    S99FLG21,S99TIONQ+S99NORES  INDICATE TIOT ENQ'ED  Y02082
*                                       AND NO D/S RESERVATION   Y02082
         LR    RPARM,RWRK               ADDRESS OF TEXT POINTERS Y02082
         USING S99TUPL,RPARM            TEXT PTR ADDRESSABILITY  Y02082
*
* THIS SECTION BUILDS THE VOLSER TEXT UNIT.
*
         LA    RTEXT,FW4(,RPARM)        BASE FOR TEXT UNIT       Y02082
         ST    RTEXT,S99TUPTR           PTR TO VOLSER TEXT UNIT  Y02082
         USING S99TUNIT,RTEXT           TEXT UNIT ADDRESSABILITY Y02082
         LH    RWRK,VOLSERKY            INDICATE VOLSER TEXT     Y02082
         STH   RWRK,S99TUKEY                                     Y02082
         LA    RWRK,K1                  ONE TEXT PARM            Y02082
         STH   RWRK,S99TUNUM                                     Y02082
         LA    RWRK,L'SRTEVOLI          LENGTH OF VOLSER         Y02082
         STH   RWRK,S99TULNG                                     Y02082
         MVC   S99TUPAR(L'SRTEVOLI),PVOLSER  GET VOLSER          YM1337
*
* THIS SECTION BUILDS THE UNIT TEXT UNIT.
*
         LA    RTEXT,VOLTEXTL(,RTEXT)   INCREMENT BASE           Y02082
         ST    RTEXT,S99TUPTR+K4        POINT TO UNIT TEXT UNIT  Y02082
         LH    RWRK,UNITKEY             INDICATE UNIT SPECIFIED  Y02082
         STH   RWRK,S99TUKEY                                     Y02082
         LA    RWRK,K1                  ONE TEXT PARM            Y02082
         STH   RWRK,S99TUNUM                                     Y02082
         LA    RWRK,L'UCBNAME           LENGTH OF UCBNAME        Y02082
         STH   RWRK,S99TULNG                                     Y02082
         MVC   S99TUPAR(L'UCBNAME),UCBNAME  GET UNIT NUMBER      Y02082
*
* THIS SECTION BUILDS THE STATUS TEXT UNIT.
*
         LA    RTEXT,UNITEXTL(,RTEXT)   UPDATE TEXT UNIT BASE    Y02082
         ST    RTEXT,S99TUPTR+K8        PTR TO STATUS TEXT UNIT  Y02082
         LH    RWRK,STATUSKY                                     Y02082
         STH   RWRK,S99TUKEY            INDICATE STATUS TEXT     Y02082
         LA    RWRK,K1                                           Y02082
         STH   RWRK,S99TUNUM            ONE TEXT PARM            Y02082
         STH   RWRK,S99TULNG            LENGTH OF TEXT PARM      Y02082
         MVI   S99TUPAR,STATOLD         INDICATE DISP=OLD        Y02082
*
* THIS SECTION BUILDS THE RETURN DDNAME TEXT UNIT.
*
         LA    RTEXT,STATEXTL(,RTEXT)   INCREMENT TEXT UNIT BASE Y02082
         ST    RTEXT,S99TUPTR+K12       PTR TO DDNAME TEXT UNIT  Y02082
         LH    RWRK,RTNDDKEY            INDICATE RETURN DDNAME   Y02082
         STH   RWRK,S99TUKEY            SPECIFICATION            Y02082
         LA    RWRK,K1                  ONE TEXT PARM            Y02082
         STH   RWRK,S99TUNUM                                     Y02082
         LA    RWRK,L'TIOEDDNM                                   Y02082
         STH   RWRK,S99TULNG            LENGTH OF DDNAME         Y02082
         XC    S99TUPAR(L'TIOEDDNM),S99TUPAR  CLEAR FOR DDNAME   Y02082
         OI    S99TUPTR+K12,S99TUPLN    INDICATE END OF LIST     Y02082
         LA    R1,VTOCDSCB              POINT TO REQ BLOCK PTR   Y02082
         DYNALLOC                       ISSUE DYNAMIC ALLOCATION Y02082
         LTR   RTNCODE,RTNCODE          TEST IF SUCCESSFUL       Y02082
         BNZ   ENDADDR                  BRANCH IF NOT            Y02082
*
* THIS SECTION SEARCHES THE DSAB CHAIN TO FIND THE ADDRESS OF THE DSAB
* WHICH POINTS TO THE TIOT ENTRY CONTAINING THE DYNAMICALLY ALLOCATED
* DDNAME. RTEXT STILL POINTS TO THE DDNAME TEXT UNIT.
*
         L     RDSAB,DSABQDB            LOAD DSAB QDB ADDRESS    Y02082
         USING QDB,RDSAB                QDB ADDRESSABILITY       Y02082
         L     RDSAB,QDBFELMP           GET FIRST DSAB ADDRESS   Y02082
         USING DSAB,RDSAB               DSAB ADDRESSABILITY      Y02082
FINDDSAB EQU   *                        DSAB TEST                Y02082
         LTR   RDSAB,RDSAB              IS THERE ANOTHER DSAB    Y02082
         BZ    ENDADDR                  BRANCH IF NOT            Y02082
         L     RTIOT,DSABTIOT           GET TIOT POINTER         Y02082
         USING TIOENTRY,RTIOT           TIOT ADDRESSABILITY      Y02082
         CLC   TIOEDDNM,S99TUPAR        TEST IF CORRECT DSAB     Y02082
         BE    FOUNDSAB                 BRANCH IF YES            Y02082
         L     RDSAB,DSABFCHN           GET NEXT DSAB ADDRESS    Y02082
         B     FINDDSAB                 CONTINUE SEARCH          Y02082
FOUNDSAB EQU   *                        GOT ONE                  Y02082
         OI    SISW1,UCBALLOC           SET UCB ALLOCATED BIT    Y02082
*
         OI    DSMADTB1,DSMUCBAL        SET UCB ALLOCATED BIT    Y02144
         ST    RDSAB,DSMADTW4           SAVE DSAB ADDRESS        Y02144
         ST    RDSAB,DSABADDR           SAVE DSAB ADDRESS        YM7036
         ST    RUCB,WKADEB+UCBADDR      SAVE UCB ADDRESS IN DEB  YM7036
         MVI   WKADEB+UCBADDR,DEVMOD    INSERT DEVICE MODIFIER   YM7036
         B     MOUNTED                  CONTINUE WITH RENAME     YM7036
*
*** PROGRAM CONSTANTS
*
EXTCONST DC    X'FFFFFFFF7FFF'         LAST CCHH AND NUM OF TKS
         DS    0F                       FULLWORD BOUNDARY        Y02134
RETNAME  DC    CL4'3002'                RETURN NAME FOR IGG0300F Y02134
VOLSERKY DC    AL2(DALVLSER)            VOLSER TEXT UNIT KEY     Y02082
UNITKEY  DC    AL2(DALUNIT)             UNIT TEXT UNIT KEY       Y02082
STATUSKY DC    AL2(DALSTATS)            STATUS TEXT UNIT KEY     Y02082
RTNDDKEY DC    AL2(DALRTDDN)            RETURN DDNAME KEY        Y02082
*
*** TABLE OF MODULE IDS AND ENTRY POINT ADDRESSES
*
         XCTLTABL ID=(LOAD3,01,MNTLOAD,0F),SVC=030,LENGTH=,      Y02082*
               BRT=YES                                           Y02082
         SPACE 2                                                 Y02082
         IECDSECS CVT,DSAB,QDB,RB,TCB,TIOT,UCB,EXPAND=YES        Y02082
WORKAREA IECRENWA EP,ADT=YES            RENAME WORK AREA         Y02144
         EJECT                                                   Y02082
*
* DSECTS FOR INVOCATING DYNAMIC ALLOCATION (SVC 99)
*
         IEFZB4D0                       DYNAMIC ALLOCATION DSECT Y02082
         EJECT                                                   Y02082
         IEFZB4D2                       DYNAMIC ALLOCATION KEYS  Y02082
         EJECT                                                   YM7036
         IEFJFCBN                       JFCB DSECT               YM7036
         END
