 TITLE 'IGG020P1 - PARTIAL RELEASE - ENTRY POINT'
IGG020P1 CSECT
*          RELEASE 16 DELETIONS
*1463                                                             5M03B
*1463077400,077600                                                 AAAA
*1463                                                             17305
*          RELEASE 17 DELETIONS
*6039000900,001600-002200,060000,069400.                           DM0D
*6039029200                                                       18924
*6039019000                                                       19689
*          RELEASE 18 DELETIONS
*3479                                                             MCS
*          RELEASE 19 DELETIONS
*0946019400,029000,037300,048600-050600,057800-060200            O19117
*0946041200,043320                                               A25991
*          RELEASE 20 DELETIONS
*          RELEASE 21 DELETIONS
*0980001000-001200,001500-003000,004800-005000,005400-005800,    A37199
*0980006200-006400,006800-007600,008400,009000,009800-010200,    A37199
*0980010800,011400,011800,012200-012400,013000,014200,014600-    A37199
*0980015000,017400-017600,018200,019200,020000,024800-025200,    A37199
*0980028000,031200,034800-035000,037160-037240,042200-043000,    A37199
*0980048300,050800,064200-064400,069800,070800,071200,074400,    A37199
*0980075520                                                      A37199
*0980000260,006400,015600-015800,018900-019000,021200,029400,    A39292
*0980036200,044600,053600,054000,054600-054800,056200,057400,    A39292
*0980066400,069600-069800,076200-077000                          A39292
*          RELEASE 21.7 DELETIONS
*0000002400,005300-005600,006900-007400,016400,024200-024400,   SA54549
*0000025280,027000,028200,029360,029460,037000,037320-037480,   SA54549
*0000045000,046400-048400,063200,064200,068800,069480-069640,   SA54549
*0000071600,073000-073600                                       SA54549
*0000018200,022400-023800,050200-050400,063000,067400-067600    SA53147
*0000069100                                                     SA53147
*          RELEASE 22 DELETIONS
*          VS2 RELEASE 01 DELETIONS
*0000                                                            YM2930
*0000026500,026660-026708                                        YM4398
*          VS2 RELEASE 02 DELETIONS/CHANGES
*0000000280,000304-000308,000740-000800,003200-003400,004400,    Y02080
*0000005820,006200-006400,006800-006900,007100,015300,015630-    Y02080
*0000015650,016600,017600,018100-018200,018450-018550,019300-    Y02080
*0000019700,025320,025360-025680,025800,026100,026480,026720,    Y02080
*0000029300,036350,036450,036550,039400,048800,049800-050800,    Y02080
*0000066400,067600,068000,069200-069416,069440,069480,070200,    Y02080
*0000070400-075600                                               Y02080
*0000017200,019900-020080,026140,069580-070220,077900-078200     Y02144
*0000000030-000150,005820,019860,019900,020100,026480,026660-    Y02082
*0000026740,028600-028800,029100-031000,066200,066600            Y02082
*0000027000-027100                                               YM3912
*0000029240-029250,029830-029840,069060,069100                   YM2880
*0000                                                            YM2887
*0000                                                            YM5371
*0000030710-030730,030770-030790                                 YM5709
*0000026130,026148-026156                                        YM6546
*0000020020,026100-026110,069500                                 YM3997
*0000029980,030130,030820                                        YM4653
*0000004700,018800,029320,029880-029890,029940,029960-029970,    YM8507
*0000030000,030030-030050,030110-030130,030210,030910-030920,    YM8507
*0000078400                                                      YM8507
*
*
*STATUS CHANGE LEVEL 010
*
*MODULE NAME - IGG020P1
*
*DESCRIPTIVE NAME - PARTIAL RELEASE - ENTRY POINT
*
*COPYRIGHT - NONE
*
*CHANGE ACTIVITY - SEE DELETIONS/CHANGES FOLLOWING CSECT CARD
*
*FUNCTION - THIS MODULE RELEASES ANY UNUSED SPACE THAT WAS
*        ALLOCATED IN THE DATA SET'S FORMAT 1 DSCB.
*
*DEPENDENCIES - REGISTER 7 ON ENTRY FROM RESTART MUST POINT TO   YM8507
*        THE RESTART WORKAREA MAPPED BY IEEVRSWA, DSECT NAME     YM8507
*        RSTWA. LABEL RSINT POINTS TO THE FIRST OF ALL THE DEBS  YM8507
*        BEING REESTABLISHED FOR THE TASK.                       YM8507
*
*ENTRIES - THE ENTRY POINT TO THIS MODULE IS IGG020P1. ENTRY IS
*        MADE FROM CLOSE MODULE IFG0202E OR FROM THE REPOSITION I/O
*        FUNCTION OF CHECKPOINT/RESTART.
*
*SUPERVISOR CALLS
*        SVC   EXCP (0)  READ/WRITE DISK
*        SVC   WAIT (1)  WAIT FOR COMPLETION OF I/O
*
*INPUT - REGISTER 2 POINTS TO THE USER'S DCB FOR RESTART, AND    YM8507
*        TO THE COPY DCB FOR CLOSE. REGISTER 4 POINTS TO         YM8507
*        THE I/O SUPPORT WORK AREA WHICH CONTAINS THE DATA PORTION
*        OF THE FORMAT 1 DSCB. REGISTER 10 POINTS TO THE UCB.
*        REGISTER 7 POINTS TO RESTART WORKAREA IF FROM RESTART.  YM8507
*
*OUTPUT - REGISTER 11 POINTS TO THE RELEASE WORK AREA WHICH CONTAINS
*        THE DATA PORTION OF THE FORMAT 4 DSCB, AND THE DADSM
*        EXTENT TABLE, WHOSE ENTRIES REPRESENT THE SPACE BEING
*        RELEASED FROM THE FORMAT 1 DSCB.
*        REGISTER 13 POINTS TO THE I/O SUPPORT WORK AREA, WHICH
*        CONTAINS THE PARTIALLY UPDATED FORMAT 1 DSCB.
*        IF THIS MODULE IS BRANCHING TO IGG020P2, REGISTER 5 WILL
*        CONTAIN THE NUMBER OF ENTRIES IN THE DADSM TABLE.
*
*STORAGE - PROGRAM CODE CSECT    = LESS THAN 2048 BYTES
*          RELEASE WORK AREA     = 620 BYTES
*          RPS WORK AREA         = 128 BYTES
*          I/O SUPPORT WORK AREA
*
*EXITS -
*        NORMAL - BRANCH TO IGG020P2
*        ERROR  - IF AN I/O ERROR OCCURRED, BRANCH TO IGG020P3
*                 WITH X'10' ERROR CODE IN REGISTER 2.
*                 BRANCH TO IGG020D0 TO WRITE BACK THE FORMAT 1
*                 WITH ONE OF THE FOLLOWING CODES IN REGISTER 2:
*                 X'00' - NO EXTENTS BEING RELEASED
*                 X'02' - UNABLE TO FIND EXTENT IN THE FORMAT 1 DSCB
*                 X'08' - ANOTHER DCB IS OPEN TO THE DATA SET
*                 X'20' - NO ROOM IN THE VTOC
*
*REGISTER USAGE
R0       EQU   0
RX1      EQU   1                       WORK
R1       EQU   1
ERRETREG EQU   2
R2       EQU   2
RX2      EQU   2                       WORK
R3       EQU   3
RX4      EQU   4                       WORK
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
RXUSRDCB EQU   9                       BASE FOR USERS DCB
RXUCB    EQU   10                      POINTER TO THE UCB
RXWKA    EQU   11                      BASE FOR WORK AREA
RXBASE   EQU   12                      BASE FOR MAIN PROGRAM
RXCLOWKA EQU   13                      BASE FOR CLOSE'S WORK AREA
RLINK    EQU   14                      SUBROUTINE LINK REGISTER
R15      EQU   15
*
*** OTHER EQUATES
*
K0       EQU   0                        CONSTANT OF 0            Y02082
K1       EQU   1                        CONSTANT OF 1            Y02082
K2       EQU   2                        CONSTANT OF 2            Y02080
K3       EQU   3                        CONSTANT OF 3            Y02082
K4       EQU   4                        4 BYTES                 SA54549
K5       EQU   5                        CONSTANT OF 5            Y02082
K6       EQU   6                        6 BYTES                 SA54549
K12      EQU   12                       CONSTANT OF 12           Y02082
SVRBEXSA EQU   96                       SVRB EXTENDED SAVEAREA   YM8507
RTNCODE8 EQU   8                        ERROR RETURN CODE OF 8   Y02082
DS1EXTUL EQU   X'40'                    USER LABEL EXTENT        Y02082
ALLBITS  EQU   X'FF'                    MASK OF ALL ONES         Y02082
BLANK    EQU   C' '                     EBCDIC BLANK             YM2880
DEBDADEV EQU   X'04'                    DEB EXTENT SCALE FOR DA  Y02082
DIRFBIT  EQU   X'04'                    VTOC INTERRUPT BIT       O19117
NOTINF1  EQU   X'02'                    EXTENT NOT FOUND ERROR   A37199
OPENDS   EQU   X'08'                    OPEN DATA SET ERROR      Y02082
IOERROR  EQU   X'10'                    I/O ERROR CODE           A37199
NOROOM   EQU   X'20'                    NO ROOM IN VTOC ERROR    A37199
*
         BALR  RXBASE,0                      LOAD BASE REGISTER
         USING *,RXBASE                 BASE FOR MODULE         SA54549
         USING CVT,R15                      CVT BASE REGISTER.
         USING PRLSEWKA,RXWKA           WORK AREA ADDRESSABILITY Y02080
         USING IHADCB,RXUSRDCB         ESTABLISH BASE FOR USERS DCB
         USING CLOSEWKA,RXCLOWKA       ESTABLISH BASE FOR CLOS WKAREA
         USING UCB,RXUCB                UCB ADDRESSABILITY       Y02144
*
*** THIS SECTION OBTAINS CORE FOR THE PARTIAL RELEASE WORK AREA.
*
BEGINA   LA    RXCLOWKA,0(RX4)              GET PTR TO CLOSE WORKAREA.
         IECRES GET,PREFIX=FIRST,LV=PWALNGTH,ID=PRWA,            Y02080X
               STM=(RX2,RXCLOWKA,DXCCW1)  OBTAIN WORK AREA       Y02080
         LR    RXWKA,RX1                     LOAD GETMAIN AREA BASE REG
         MVC   WTGMODNM,FIRSTLD         MOVE IN MODULE NAME      YM5371
*
         MODESET EXTKEY=ZERO            SWITCH TO KEY 0 BEFORE   Y02082X
                                        SAVING REGISTERS IN SVRB Y02082
         BAL   RLINK,FINDSVRB               GET POINTER TO SVRB.
         STM   RX2,RXUCB,SVRBEXSA(R15)  SAVE REGISTERS FOR CLOSE YM8507
         LR    RXUSRDCB,RX2                  LOAD USERS DCB ADR.IN BASE
         ST    RXUSRDCB,DCBSAVE         SAVE USER'S DCB POINTER SA54549
         LTR   R4,R4                    ENTERED FROM            SA54549
*                                       CHECKPOINT/RESTART      SA54549
         BP    SETKEY5                  BRANCH IF NO             Y02082
         OI    DADSMTBL,FROMCR          TURN INDICATOR ON       SA54549
*
         IECRES LOAD,MODNM=FIRSTLD,EXTPR=(RXWKA),BRANCH=NO       Y02080X
                                        LET OPTIONAL TRACE PRINT Y02080X
                                        THIS MODULE'S NAME       Y02080
         B     SETFNCTN                 INITIALIZE THE RECOVERY  Y02144
*                                       ROUTINE PARAMETER LIST   Y02144
SETKEY5  EQU   *                        RETURN TO DATAMGT KEY    Y02082
         MODESET EXTKEY=DATAMGT         RETURN TO DATAMGT KEY IF Y02082X
                                        ENTERED FROM CLOSE       Y02082
*
*** THIS SECTION INITIALIZES THE RECOVERY ROUTINE PARAMETER LIST.
*
SETFNCTN EQU   *                        INITIALIZE LIST          Y02144
         L     RX1,IECRRPRM             RECOVERY RTN LIST ADDR   Y02144
         USING RRPLIST,RX1              PARM LIST ADDRESSABILITY Y02144
         OI    RRFUNCTN,RRFPRLSE        SET RELEASE FUNCTION BIT Y02144
         ST    RXUCB,RRUCBPTR           SAVE UCB ADDRESS         Y02144
         DROP  RX1                                               Y02144
*
*** THIS SECTION INITIALIZES THE CHANNEL PROGRAM.
*
CHANPROG EQU   *                                                SA54549
         XC    DXDAADDR(2),DXDAADDR         CLEAR FIRST TWO BYTES OF SK
         LM    R0,R5,CCWX1                  PICK UP FIRST 3 CCWS.
         ALR   R0,RXCLOWKA                  MODIFY ADDR OF CCWS IN
*                                           CLOSE WORK AREA.
         ALR   R2,RXCLOWKA                  CLOSE AREA HAS CCW'S.
         ALR   R4,RXWKA                 WRITE FROM OUTDSCB       A39292
         STM   R0,R5,DXCCW1                 STORE FIRST 6 CCW'S.
         LA    R2,DXCCW4                    MODIFY TIC ADDRESS.
         ST    R2,IOBSIOCC                  INSERT STARTING CCW IN IOB.
         STM   R0,R5,DXCCW4                 STORE LAST 3 OF 6 CCW'S.
         MVI   DXCCW5,X'08'                 INSERT TIC OP CODE.
*   BUILD CCW6 AND CCW7 TO READ F4 DSCB AND COUNT OF FIRST F5 DSCB
         LM    R0,R3,CCWX6              LOAD CCWS 6-7           SA53147
         ALR   R0,RXWKA                 RELOCATE READ AREA ADDR SA53147
         ALR   R2,RXWKA                 RELOCATE READ IN AREA   SA53147
*                                       FOR READ COUNT          SA53147
         STM   R0,R3,DXCCW6             STORE CCW6 AND CCW7     SA53147
         LA    R2,DXDAADDR                  PICK UP PTR TO SAVE AREA.
*
*** THIS SECTION GETS THE TTR OF THE VTOC FROM THE UCB AND CONVERTS
*   IT TO AN MBBCCHHR. AFTER ENQ'ING ON THE VTOC, IT READS THE
*   FORMAT 4 DSCB AND DETERMINES IF THERE IS AN UNUSED DSCB IN THE
*   VTOC (IN CASE ONE IS NEEDED FOR AN ADDITIONAL FORMAT 5 DSCB).
*
         LA    R0,SRTEVOLI             R0 = POINTER TO VOLUME LABELAAAA
         ST    R0,DXCCW12              SET RNAME POINTER IN ENQ LISAAAA
         L     R0,SRTEFSCT              LOAD TTR OF F4 CCHHR     Y02080
         MVC   DXDEBSCC(K4),CCWX2+K4    MOVE BEGINNING CCHH     SA54549
*                                       INTO DEB                SA54549
         MVC   DXDEBECC(K6),ECCHHTRK    MOVE ENDING CCHH AND    SA54549
*                                       NUMBER OF TRACKS INTO   SA54549
*                                       DEB                     SA54549
         BAL   RLINK,CCHHCVT            *** CONVERT TTR0 TO MBBCCHHR.
         TM    UCBTBYT2,UCB2OPT3        TEST FOR RPS DEVICE     SA53147
         BNO   SKIPRPS                  BRANCH IF NOT RPS       SA53147
         LA    R3,DXDEB                 PTR TO DEB IN CLOSE     SA53147
*                                       WORK AREA               SA53147
         IECRPS RDEB=R3,WKREG1=R6,EXTPR=RXWKA  INITIALIZE FOR    YM3997X
                                        RPS PROCESSING           YM3997
*
         MVC   WTGMODNM,FIRSTLD         RESTORE MODULE NAME      YM5371
*
         TM    DADSMTBL,FROMCR          TEST IF ENTRY FROM C/R   Y02082
         BO    SKIPRPS                  BRANCH IF C/R            YM6546
         MODESET EXTKEY=DATAMGT         RETURN TO KEY 5 IF CLOSE YM6546
*
SKIPRPS  EQU   *                                                SA53147
         MVC   MJNAME,VTOCNAME         INSERT MAJOR Q N AME
         SR    R1,R1                    ZERO REGISTER            Y02080
         STH   R1,ENQAREA+K2            CLEAR FOR RESERVE MACRO  Y02080
         MVI   ENQAREA,255             INDICATE LAST Q AREA
        RESERVE (MJNAME,,E,6,SYSTEMS),SMC=STEP,MF=(E,ENQAREA),   YM3022X
               UCB=DXDEB+32
*
         OI    DSMADTB2,DSMVTOCR+DSMSMCE  SET VTOC ENQ'ED SMC    Y02144
*
         DROP  RXUCB                                             Y02082
         USING CVT,R15
         BAL   RLINK,RDDSCBT            *** READ VTOC DSCB
         LH    R6,DS4DSREC                  GET VTOC HOLE COUNTER.
         LTR   R6,R6                        IS THERE A HOLE IN VTOC.
         BP    SETVTOC                      YES-GO TO SAVE VTOC ADDR.
         LA    ERRETREG,NOROOM          NO, SET ERROR CODE       A37199
         B     WRITF1                   EXIT TO WRITE           SA54549
*                                       BACK FORMAT 1           SA54549
*
*** THIS SECTION WRITES BACK THE FORMAT 4 DSCB WITH THE DIRF BIT SET.
*
SETVTOC  EQU   *                                                 O19117
         MVC   VTOCADR(5),DXDAADDR+3    SAVE VTOC DSCB ADDRESS   O19117
         XI    DS4VTOCI,DIRFBIT         SET/RESET DIRF BIT       O19117
         TM    DS4VTOCI,DIRFBIT         TEST RESULTS             O19117
         BZ    SKPWRT                   BR IF PREVIOUS INTERRUPT O19117
         MVI   DXCCW6,X'05'             SET UP DATA CMMD         O19117
         MVI   DXCCW6+4,X'00'           RESET CMMD CHAIN         O19117
         BAL   RLINK,RDDSCBT            GO WRITE BACK F4         O19117
*
*** THIS SECTION VERIFIES THAT NO OTHER JOB IS OPEN TO THE DATA SET
*   AND THAT THE CURRENT JOB HAS NO OTHER DCB OPEN TO THE DATA SET.
*
SKPWRT   EQU   *                                                 O19117
         SR    ERRETREG,ERRETREG        ZERO RETURN CODE REG     Y02082
         CLI   DSCNOEXT,K0              CHECK NUMBER OF EXTENTS  Y02082
         BE    WRITF1                   BRANCH IF MODEL DSCB     Y02082
         L     R15,CVTPTR               LOAD CVT ADDRESS         Y02082
         L     R15,CVTTCBP              LOAD TCB/ASCB POINTERS   Y02082
         L     RX2,K4(,R15)             LOAD CURRENT TCB ADDRESS Y02082
         USING TCB,RX2                  TCB ADDRESSABILITY       Y02082
         TM    PTYPEFLG,FROMCR          TEST IF ENTRY FROM C/R   Y02082
         BNO   TIOTENQ                  BRANCH IF FROM CLOSE     Y02082
*
*** FOR ENTRY FROM CHKPT/RESTART, THIS SECTION ISSUES AN EXCLUSIVE
*   ENQ DIRECTED TO THE INITIATOR'S TCB WITH A MAJOR RESOURCE NAME OF
*   SYSDSN AND THE DATA SET NAME AS THE MINOR RESOURCE NAME.  IF THE
*   RETURN CODE INDICATES THAT THE TASK PREVIOUSLY HAD CONTROL OF THE
*   RESOURCE, THEN THE ENQ IS REISSUED IN A SUCCEEDING SECTION OF CODE.
*
DSNENQ1  EQU   *                        ENQ DSNAME               Y02082
         LA    RX4,JFCBDSNM             POINT TO DATA SET NAME   Y02082
         L     R3,TCBJSCB               LOAD JSCB ADDRESS        Y02082
         USING IEZJSCB,R3               JSCB ADDRESSABILITY      Y02082
         L     R3,JSCBACT               GET ACTIVE JSCB ADDRESS  YM2887
         L     R3,JSCBTCBP              INITIATOR'S TCB ADDRESS  Y02082
         DROP  R3                                                Y02082
         MVC   MJNAME(L'SYSDSN),SYSDSN  MAJOR RESOURCE NAME      Y02082
         MVC   ENQAREA(ENQLTH1),ENQLIST1  MOVE IN ENQ PARM LIST  Y02082
         LA    R5,JFCBDSNM+L'JFCBDSNM-K1  LAST BYTE OF DSNAME    YM2880
TESTDSN1 EQU   *                        SEARCH FOR NON-BLANK     YM2880
         CLI   K0(R5),BLANK             IS THIS ONE A BLANK      YM2880
         BNE   GETLNTH1                 BRANCH IF NO TO COMPUTE  YM2880X
                                        LENGTH OF NAME FOR ENQ   YM2880
         BCT   R5,TESTDSN1              TRY NEXT BYTE            YM2880
GETLNTH1 EQU   *                        CALCULATE LENGTH         YM2880
         LA    R6,JFCBDSNM-K1           BYTE PRECEDING DSNAME    YM2880
         SR    R5,R6                    R5 HAS LENGTH FOR ENQ    YM2880
         ENQ   (MJNAME,(RX4),,(R5)),TCB=(R3),RET=USE,            YM2880X
               MF=(E,ENQAREA)           ENQ EXCLUSIVELY ON DSN   YM2880
*
         LTR   R15,R15                  TEST RETURN CODE         Y02082
         BNZ   ERRTEST                  BRANCH IF NOT SUCCESSFUL Y02082
*
         OI    DSMADTB2,DSMDSNE         SET DSN ENQ'ED SWITCH    Y02082
         ST    RX4,DSMADTW3             SAVE POINTER TO DSNAME   Y02082
         B     DEBSRCH                  SEARCH DEB CHAIN         YM8507
ERRTEST  EQU   *                        ERROR TEST               Y02082
         CLI   ENQAREA+K3,RTNCODE8      TEST IF TASK ALREADY     Y02082
*                                       ENQ'ED ON SYSDSN         Y02082
         BE    DSNENQ2                  BRANCH IF YES            Y02082
OPENERR  EQU   *                        OPEN DATA SET ERROR      Y02082
         LA    ERRETREG,OPENDS          OPEN DATA SET ERROR      Y02082
         B     WRITF1                   GO REWRITE THE F1 DSCB   Y02082
*
*** FOR ENTRY FROM CLOSE, THIS SECTION ISSUES AN ENQ RET=CHNG WITH A
*   MAJOR NAME OF SYSZTIOT AND A MINOR NAME COMPOSED OF THE ASID/DSAB
*   QDB ADDRESS.  IT THEN READS THE KEY OF THE FORMAT 1 DSCB, SINCE
*   JFCBDSNM MAY NOT CONTAIN THE DATA SET NAME.
*
TIOTENQ  EQU   *                        ENQ TIOT                 Y02082
         L     RX1,K12(,R15)            LOAD CURRENT ASCB ADDR   Y02082
         USING ASCB,RX1                 ASCB ADDRESSABILITY      Y02082
         MVC   MINAME(L'ASCBASID),ASCBASID  ASID TO MINOR NAME   Y02082
         DROP  RX1                                               Y02082
         L     RX1,TCBJSCB              LOAD JSCB ADDRESS        Y02082
         USING IEZJSCB,RX1              JSCB ADDRESSABILITY      Y02082
         L     RX1,JSCBACT              ACTIVE JSCB ADDRESS      Y02082
         MVC   MINAME+K2(L'JSCDSABQ),JSCDSABQ  DSAB QDB ADDRESS  Y02082
         DROP  RX1                                               Y02082
         MVC   MJNAME(L'SYSZTIOT),SYSZTIOT  MAJOR RESOURCE NAME  Y02082
         MVC   ENQAREA(ENQLTH),ENQLIST  MOVE IN ENQ PARM LIST    Y02082
         ENQ   (MJNAME,MINAME),MF=(E,ENQAREA),RET=CHNG           Y02082X
                                        ENQ EXCLUSIVELY ON TIOT  Y02082
*
         LTR   R15,R15                  TEST RETURN CODE         Y02082
         BNZ   OPENERR                  BRANCH IF NOT SUCCESSFUL Y02082
         L     RX1,DCBDEBAD             LOAD DEB ADDRESS         Y02082
         LA    RX1,K0(RX1)              CLEAR HIGH-ORDER BYTE    Y02082
         SH    RX1,SIXTEEN              GET DEB PREFIX ADDRESS   Y02082
         MVC   DXDAADDR+K3(K5),K3(RX1)  MOVE IN FORMAT 1 CCHHR   Y02082
         LA    R0,DXCCW1                                         Y02082
         ST    R0,IOBSIOCC              MODIFY CCW PTR IN IOB    Y02082
         BAL   RLINK,RDDSCBT            GO READ F1 KEY AND DATA  Y02082
         LA    R5,DS1DSNAM+L'DS1DSNAM-K1  LAST BYTE OF DSNAME    YM2880
TESTDSN2 EQU   *                        SEARCH FOR NON-BLANK     YM2880
         CLI   K0(R5),BLANK             IS THIS ONE A BLANK      YM2880
         BNE   GETLNTH2                 BRANCH IF NO TO COMPUTE  YM2880X
                                        LENGTH OF NAME FOR ENQ   YM2880
         BCT   R5,TESTDSN2              TRY NEXT BYTE            YM2880
GETLNTH2 EQU   *                        CALCULATE LENGTH         YM2880
         LA    R6,DS1DSNAM-K1           BYTE PRECEDING DSNAME    YM2880
         SR    R5,R6                    R5 HAS LENGTH FOR ENQ    YM2880
         LA    RX4,DS1DSNAM             POINT TO DATA SET NAME   Y02082
         L     R3,TCBJSCB               LOAD JSCB ADDRESS        Y02082
         USING IEZJSCB,R3               JSCB ADDRESSABILITY      Y02082
         L     R3,JSCBACT               GET ACTIVE JSCB ADDRESS  YM2887
         L     R3,JSCBTCBP              INITIATOR'S TCB ADDRESS  Y02082
         DROP  R3                                                Y02082
*
*** THIS SECTION ISSUES AN ENQ RET=CHNG ON SYSDSN.  REGISTER 4 POINTS
*   TO THE DSN, AND REGISTER 3 CONTAINS THE INITIATOR'S TCB ADDRESS.
*
DSNENQ2  EQU   *                        ENQ EXCLUSIVELY ON DSN   Y02082
         MVC   MJNAME(L'SYSDSN),SYSDSN  MAJOR RESOURCE NAME      Y02082
         MVC   ENQAREA(ENQLTH2),ENQLIST2  MOVE IN ENQ PARM LIST  Y02082
         ENQ   (MJNAME,(RX4),,(R5)),TCB=(R3),RET=CHNG,           YM2880X
               MF=(E,ENQAREA)           ENQ EXCLUSIVELY ON DSN   YM2880
*
         LTR   R15,R15                  TEST RETURN CODE         Y02082
         BNZ   OPENERR                  BRANCH IF NOT SUCCESSFUL Y02082
*                                       SEARCH THE DEBS IF C/R   Y02082
*
*** THIS SECTION SEARCHES THE JOB'S DEBS TO ENSURE THAT THE CURRENT
*   JOB HAS NO OTHER DCB OPEN TO THE DATA SET.
*
*        IF CALLED FROM CLOSE, WE WILL INSPECT EACH DEB IN       YM8507
*        THE JOB STEP'S DEB TABLE.                               YM8507
*        IF CALLED FROM EITHER CLOSE OR RESTART THE DEB CHAIN    YM8507
*        WILL BE SEARCHED (TCB DEB CHAIN FOR CLOSE, RESTART'S    YM8507
*        DEB CHAIN IF RESTART).                                  YM8507
*
DEBSRCH  EQU   *                        SEARCH DEBS              Y02082
         MODESET EXTKEY=ZERO            SWITCH TO KEY ZERO       Y02082
*
         STM   RXWKA,RLINK,IECREGSV+K12  SAVE SETLOCK REGISTERS  Y02082
         LR    R3,RXWKA                 SAVE WORK AREA BASE      YM8507
GETLOCK  SETLOCK OBTAIN,TYPE=LOCAL,MODE=UNCOND,  PREVENT DEBCHK  Y02082*
               RELATED=(DEBTBL,IGG020P1(FREELOCK))               Y02082
         LM    RXWKA,RLINK,IECREGSV+K12-PRLSEWKA(R3)  RESTORE    YM8507
*
         TM    PTYPEFLG,FROMCR          TEST FOR RESTART         YM8507
         BZ    DTBLSRCH                 BR IF NO-SEARCH DEBTABLE YM8507
         L     R15,SVRBPTR              GET SVRB ADDRESS         YM8507
         L     R15,SVRBEXSA+(R7-R2)*K4(R15) GET RESTART'S R7     YM8507
         USING RSTWA,R15                RESTART WORKAREA         YM8507
         LA    R1,RSINT-K4              ADDR-4 OF FIRST DEB      YM8507
         DROP  R15                                               YM8507
         OI    PTYPEFLG,DCHNSRCH        SET CHAIN SEARCH ACTIVE  YM8507
         B     DEBCHAIN                 SEARCH CHAIN             YM8507
DTBLSRCH EQU   *                        SEARCH DEB TABLE         YM8507
         L     R1,TCBJSCB               LOAD JSCB ADDRESS        YM8507
         USING IEZJSCB,R1               JSCB ADDRESSABILITY      YM8507
         L     R1,JSCBDBTB              ADDR OF DEB TABLE        YM8507
         LTR   R1,R1                    IS ADDR ZERO             Y02082
         BZ    NOPENDS                  BRANCH IF YES            Y02082
         LH    R2,K0(,R1)               LAST ENTRY OFFSET        Y02082
         AR    R2,R1                    ADDRESS OF LAST ENTRY    Y02082
NXTDEB   EQU   *                        NEXT DEB                 Y02082
         TM    PTYPEFLG,DCHNSRCH        IS CHAIN SEARCH ACTIVE   YM8507
         BZ    NXTABLE                  BRANCH IF NO             YM8507
DEBCHAIN EQU   *                        NEXT DEB IN CHAIN        YM8507
         USING DEBBASIC,R3                                       YM8507
         ICM   R3,B'0111',DEBDEBAD-DEBBASIC+K1(R1) GET NEXT DEB  YM8507
         BZ    NOPENDS2                 NO MORE DEBS             YM8507
         LR    R1,R3                    SAVE DEB ADDRESS         YM8507
         TM    PTYPEFLG,FROMCR          TEST TO SEE IF, IN FACT, YM8507
*                                       RESTART CALLED US        YM8507
         BO    CRDCBDEB                 CHECK FOR CURRENT DEB    YM8507
         L     RXUSRDCB,DCBSAVE         COPIED DCB FROM CLOSE    YM8507
         CLM   R3,B'0111',DCBDEBAD+K1   CURRENT DEB              YM8507
         BE    DEBCHAIN                 BR IF YES-GET NEXT DEB   YM8507
         B     CHKDEB                   BR IF NO-CHECK DEB       YM8507
CRDCBDEB EQU   *                        RESTART DCB IS USER'S    YM8507
         CLC   DEBDCBAD+K1(K3),DCBSAVE+K1 CURRENT DEB            YM8507
         BE    DEBCHAIN                 BR IF YES-GET NEXT DEB   YM8507
         B     CHKDEB                   CHECK THE DEB            YM8507
NXTABLE  EQU   *                        NEXT DEB IN TABLE        YM8507
         LA    R1,K4(,R1)               POINT TO NEXT ENTRY      YM8507
         CR    R1,R2                    END OF DEB TABLE         YM8507
         BE    NOPENDS                  BRANCH IF YES            YM8507
         OC    K0(K4,R1),K0(R1)         EMPTY ENTRY              Y02082
         BZ    NOPENDS                  BRANCH IF YES            Y02082
         TM    K3(R1),ALLBITS           DELETED ENTRY            Y02082
         BO    NXTDEB                   BRANCH IF YES            Y02082
*                                       R1 POINTS TO A FULLWORD  Y02082
*                                       CONTAINING A DEB POINTER Y02082
         L     R3,K0(,R1)               LOAD DEB ADDRESS         Y02082
CHKDEB   EQU   *                        CHECK THE DEB            YM8507
         LA    R4,DEBBASIC-DEBAMTYP     OFFSET BACK TO DEBAMTYP  Y02082
         LCR   R4,R4                    MAKE OFFSET NEGATIVE     Y02082
         AR    R4,R3                    R4 POINTS TO DEBAMTYP    Y02082
         USING DEBAMTYP,R4                                       Y02082
         CLI   DEBAMTYP,K0              AMTYP ABSENT             Y02082
         BE    AMVALID                  BRANCH IF YES--VALID     Y02082
         CLC   DEBAMTYP,AMBPAM          BPAM DEB                 Y02082
         BE    AMVALID                  BRANCH IF YES--VALID     Y02082
         CLC   DEBAMTYP,AMBDAM          BDAM DEB                 Y02082
         BE    AMVALID                  BRANCH IF YES            Y02082
         CLC   DEBAMTYP,AMSAM           SAM DEB                  Y02082
         BE    AMVALID                  BRANCH IF YES            Y02082
         CLC   DEBAMTYP,AMEXCP          EXCP DEB                 Y02082
         BE    AMVALID                  BRANCH IF YES            Y02082
         CLC   DEBAMTYP,AMISAM          ISAM DEB                 Y02082
         BNE   NXTDEB                   BRANCH IF NO-THIS DEB    Y02082
*                                       NEED NOT BE CHECKED      Y02082
         CLI   DEBNMEXT,K1              0 OR ONE EXTENT IN ISAM  Y02082
*                                       DEB                      Y02082
         BNH   NXTDEB                   INVALID ISAM DEB         Y02082
         DROP  R4                                                Y02082
         SR    R4,R4                    ZERO FOR IC              Y02082
         IC    R4,DEBNMEXT              INSERT NUMBER OF EXTENTS Y02082
         BCTR  R4,K0                    REDUCE BY ONE            Y02082
         LA    R3,DEBNMTRK-DEBUCBAD+L'DEBNMTRK(,R3)  SKIP FIRST  Y02082
*                                       EXTENT FOR ISAM          Y02082
         B     CHKEXTNT                 GO TO CHECK EACH EXTENT  Y02082
AMVALID  EQU   *                        VALID AM TYPE            Y02082
         CLI   DEBNMEXT,K0              ARE THERE ANY EXTENTS    Y02082
         BZ    NXTDEB                   BRANCH IF NO             Y02082
         CLI   DEBEXSCL,DEBDADEV        IS EXTENT SCALE 4 FOR DA Y02082
         BNE   NXTDEB                   BRANCH IF NO             Y02082
         L     R4,DEBSUCBA              GET UCB ADDRESS          Y02082
         USING UCBOB,R4                                          Y02082
         LA    R4,K0(,R4)               CLEAR HIGH BYTE          Y02082
         LTR   R4,R4                    IS THERE A UCB           Y02082
         BZ    NXTDEB                   BRANCH IF NO             Y02082
         TM    UCBTBYT3,UCB3DACC        IS IT DIRECT ACCESS      Y02082
         BZ    NXTDEB                   BRANCH IF NO             Y02082
         DROP  R4                                                Y02082
         SR    R4,R4                    ZERO FOR IC              Y02082
         IC    R4,DEBNMEXT              NUMBER OF EXTENTS        Y02082
CHKEXTNT EQU   *                        CHECK EXTENT             Y02082
         LA    R3,DEBBASND              INCREMENT PAST BASIC     Y02082
*                                       SECTION OF DEB           Y02082
         USING DEBDASD,R3                                        Y02082
CMPREXTS EQU   *                        CHECK CCHH               Y02082
         CLC   DEBUCBA,DXDEBUCB+K1      TEST IF SAME UCB         YM4653
         BNE   NEXTXTNT                 BRANCH IF NOT            YM4653
         CLI   DSCEXTYP,DS1EXTUL        IS FIRST EXTENT USER LBL Y02082
         BNE   NOTUL                    BRANCH IF NOT            Y02082
         CLC   DEBSTRCC(L'DEBSTRCC+L'DEBSTRHH),DSCEXT1+K2  IS    YM5709X
                                        THE DEB'S STARTING CCHH  YM5709X
                                        EQUAL TO THE DSCB'S      YM5709
         BE    NOPRLSE                  BRANCH IF YES            Y02082
         B     NEXTXTNT                 GET NEXT EXTENT          Y02082
NOTUL    EQU   *                        NOT USER LBL EXTENT      Y02082
         CLC   DEBSTRCC(L'DEBSTRCC+L'DEBSTRHH),DSCLOWLM  IS      YM5709X
                                        THE DEB'S STARTING CCHH  YM5709X
                                        EQUAL TO THE DSCB'S      YM5709
         BE    NOPRLSE                  BRANCH IF YES            Y02082
NEXTXTNT EQU   *                        NEXT EXTENT              Y02082
         LA    R3,DEBNMTRK+L'DEBNMTRK   INCREMENT TO NEXT EXTENT YM4653
         BCT   R4,CMPREXTS              TRY NEXT EXTENT          Y02082
         B     NXTDEB                   NO MORE EXTENTS          Y02082
NOPRLSE  EQU   *                        OPEN DATA SET ERROR      Y02082
         LA    R3,OPENERR               SET UP BRANCH ADDRESS AS Y02082
*                                       THE DATA SET IS OPEN AND Y02082
*                                       CANNOT BE RELEASED       Y02082
         B     FREELOCK                 FREE THE LOCAL LOCK      Y02082
NOPENDS  EQU   *                        GET BRANCH ADDR          Y02082
         TM    PTYPEFLG,DCHNSRCH        DEB CHAIN SEARCH ACTIVE  YM8507
         BO    NOPENDS2                 BR IF YES-SEARCHING DONE YM8507
         L     R3,CVTPTR                CVT ADDR                 YM8507
         L     R3,CVTTCBP-CVT(,R3)      TCB POINTERS             YM8507
         L     R3,K4(,R3)               TCB ADDRESS              YM8507
         LA    R1,TCBDEB-TCB-K4(,R3)    ADDRESS-4 OF FIRST DEB   YM8507
         OI    PTYPEFLG,DCHNSRCH        SET CHAIN SEARCH ACTIVE  YM8507
         B     DEBCHAIN                 START DEB CHAIN SEARCH   YM8507
NOPENDS2 EQU   *                        ABSOLUTELY NO OPEN D.S.  YM8507
         LA    R3,GETFDAD               SET UP BRANCH ADDRESS    YM8507
*
FREELOCK EQU   *                        RELEASE LOCAL LOCK       YM8507
         NI    PTYPEFLG,ALLBITS-DCHNSRCH CHAIN SEARCH INACTIVE   YM8507
         LR    R1,RXWKA                 SAVE WORK AREA BASE      Y02082
         STM   RXWKA,RLINK,IECREGSV+K12  SAVE SETLOCK REGISTERS  Y02082
         SETLOCK RELEASE,TYPE=LOCAL,    RELEASE LOCAL LOCK       Y02082*
               RELATED=(DEBTBL,IGG020P1(GETLOCK))                Y02082
         LM    RXWKA,RLINK,IECREGSV+K12-PRLSEWKA(R1)  RESTORE    Y02082
         TM    PTYPEFLG,FROMCR          TEST IF ENTRY FROM C/R   YM8507
         BOR   R3                       BRANCH IF C/R-STAY KEY 0 YM8507
         MODESET EXTKEY=DATAMGT         RETURN TO DATAMGT KEY    Y02082
         BR    R3                       RETURN                   Y02082
*
GETFDAD  MVC   MBBCCHHR+K3(K4),DCBFDAD+K3  INSERT NEW CCHH       Y02082
         MVC   USEXTNUM,DCBFDAD         SAVE M OF USER'S DCBFDAD Y02082
         LA    R6,ENTRIES                                        Y02082
         SR    R5,R5                    CLEAR COUNT REGISTER     Y02082
         TM    JFCBCTRI,JFCBCYL         TEST IF A CYLINDER REQ   Y02082
         BO    ROUNDCYL                 BRANCH IF A CYL REQUEST  Y02082
         TM    JFCBCTRI,JFCBAVR+JFCROUND  TEST IF A ROUNDED      Y02082
*                                       RECORD REQUEST           Y02082
         BNO   SAVEFMT3                 BRANCH IF NOT            Y02082
*
*** THIS SECTION CHECKS TO SEE IF THE LAST TRACK WRITTEN WAS CYLINDER
*   ALIGNED. IF NOT, IT ROUNDS THE EXTENT TO BE RELEASED TO THE
*   NEXT CYLINDER.
*
ROUNDCYL LA    R4,1                         YES-SET CONVERSION COUNT.
         MVC   HOLD(4),DCBFDAD+3            GET LAST ACTIVE TRACK.
         BAL   RLINK,RTACVT             *** CONVERT CCHH TO RTA
         LH    R9,ENTRIES                   PICK UP LAST ACTIVE TRK RTA
         SR    R8,R8                        CLEAR REG FOR DIVISION.
         LH    R0,DS4DEVSZ+2                PICK UP NO OF TRKS/CYL.
         DR    R8,R0                        DETERMINE CYL ALIGNMENT
         BCTR  R0,0                         SET R0 TO LAST TRACK OF CYL
         CLR   R0,R8                        IS REMAINDER EQ TO LAST TRK
         BE    SAVEFMT3                     YES-EXTENT IS CYL ALIGNED.
         OI    DADSMTBL,X'01'               TURN ON ROUND BIT.
         MH    R9,DS4DEVSZ+2                CONVERT CYL'S TO TRKS.
         AR    R0,R9                        ROUND RTA TO CYL ALGNMENT
         SLL   R0,16                        POSITION TT00.
         LA    R2,MBBCCHHR                  GET POINTER TO SAVE AREA.
*
*** THIS SECTION DETERMINES WHETHER THE EXTENT THAT CONTAINS THE
*   LAST TRACK WRITTEN IS LOCATED IN THE FORMAT 1 OR FORMAT 3 DSCB.
*
         BAL   RLINK,CCHHCVT                CONVERT ROUNDED EXT TO CCHH
SAVEFMT3 MVC   DXDAADDR+3(5),DSCNEXT        SAVE
         MVC   OUTCCHHR(5),DSCNEXT           CHAIN ADDRESS
         LA    R6,ENTRIES                   RESTORE DADSMTBL PTR.
         L     R9,DCBSAVE               RESTORE USER'S DCB PTR   A39292
         CLI   DSCEXTYP,X'40'           TEST FOR USER LABEL.
         BNE   NOLABEL                  BR-IF NO USER LABEL EXTENT.
         IC    R8,USEXTNUM              GET M OF DCBFDAD         Y02080
         LA    R8,1(R8)                 ACCOUNT FOR USER LABEL EXTENT.
         STC   R8,USEXTNUM              SAVE UPDATED M           Y02080
NOLABEL  EQU   *
         CLI   USEXTNUM,K2              IS EXTENT IN F1 OR F3    Y02080
         BNH   F1RUTINE                     ENTRY IS IN F1-PROCESS IT.
         CLI   DSCNEXT+4,X'00'              OTHERWISE TEST FOR F3.
         BNE   OUT                      GO READ F3 IF PRESENT   SA54549
F1EXNOER EQU   *                                                 O19117
         LA    ERRETREG,NOTINF1         SET ERROR CODE           A37199
WRITF1   EQU   *                                                SA54549
         LA    R3,D0                    PREPARE TO XCTL TO      SA54549
*                                       IGG020D0 TO WRITE       SA54549
*                                       BACK FORMAT 1           SA54549
         B     XCTL                                             SA54549
*
***  THIS LOCATES THE PROPER EXTENT IN THE FORMAT 1 DSCB CONTAINING THE
*    LAST TRACK WRITTEN,AND THEN TESTS FOR CYLINDER TYPE ALLOCATION.
*
F1RUTINE XC    DSCNEXT(5),DSCNEXT           CLEAR CHAIN ADDR OF F1.
         MVI   INCCHHR+4,X'FF'              INDICATE EXTENT IS IN F1.
         LA    R7,DSCEXTYP
         LA    R8,DSCEXT2+10                SET UP END OF EXTNTS PTR.
         SR    R3,R3                        CLEAR REGISTER.
         IC    R3,USEXTNUM              GET M OF DCBFDAD         Y02080
         MH    R3,TEN
         AR    R7,R3                        LOCATE PROPER EXT IN F1.
         TM    JFCBCTRI,X'C0'           WAS THIS A CYLINDER       17305
*                                       REQUEST                   17305
         BO    CYLREQ                       BRANCH IF CYLINDER.
         TM    JFCBCTRI,X'41'           WAS ROUNDED RECORD        17305
*                                       REQUESTED                 17305
         BO    CYLREQ                       BR-IF REC ROUNDED REQ.
         MVI   0(R7),X'01'                  NO-TURN OFF CYL ALGNMNT BIT
CYLREQ   CLC   6(4,R7),MBBCCHHR+3           CHECK FOR LAST TRK OF EXT.
UPDATE   LA    R7,10(R7)      UPDATE SCAN REG TO NEXT EXTENT     A25991
         BE    CHECKLST                     CHECK FOR LAST TRK IF LAST
         SH    R7,TEN                       DECREMENT SCAN REG IF NOT
*                                           LAST TRACK OF EXTENT.
         CLC   MBBCCHHR+3(4),2(R7)      IS CCHH IN THIS EXTENT.   17305
         BL    F1EXNOER                 BR TO ERROR IF NOT.
         MVC   HOLD+4(4),6(R7)              SAVE PREVIOUS CCHH.
         EX    0,CYLREQ        CCHH WITHIN EXTENT                 18924
         BH    WITHIN          YES,CONTINUE                       18924
         EX    0,GETFDAD  FALL BACK TO LAST TRK ACT USED          18924
         EX    0,CYLREQ       IS CCHH OF LAST TRK WITHIN EXTENT  A25991
         BL    F1EXNOER       NO, IT IS BEYOND END-OF-EXTENT     A25991
         B     UPDATE         BRANCH TO TRY AGAIN                A25991
*
*** THIS SECTION SETS UP THE CCHH PARAMETERS TO BE CONVERTED TO
*   STARTING RTA AND ENDING RTA+1. IT THEN PLACES THESE RTA'S IN
*   THE EXTENT TABLE.
*
WITHIN   EQU   *                                                  18924
         MVC   6(4,R7),MBBCCHHR+3           INSERT NEW CCHH.
         MVC   HOLD(4),MBBCCHHR+3           SAVE NEW CCHH OF EXT USED.
         BAL   RLINK,CVTORTA                CONVERT RELSED CCHH TO RTA.
         LH    R9,ENTRIES
         LA    R9,1(R9)                     INCREMENT FIRST ENTRY
         STH   R9,ENTRIES                   FOR AVAIL INDICATION.
         L     R9,DCBSAVE               RESTORE USER'S DCB PTR   A39292
CHECKLST CLR   R7,R8                        IS THIS THE LAST EXTENT
         BE    OUT                      YES - GO TO NEXT LOAD   SA54549
*                                       OF PARTIAL RELEASE      SA54549
         CLI   0(R7),0                      NO-IS THIS EXTNT ZERO
         BE    OUT                          YES-XCTL TO NEXT LOAD
         MVC   HOLD(8),2(R7)                GET EXT FOR DADSM UPDATE.
         XC    0(10,R7),0(R7)               CLEAR F1 RELEASED EXTENT.
         BAL   RLINK,CVTORTA                CONVERT EXTEXT TO RTA FOR
         B     CHECKLST                     CHECK FOR LAST EXTENT.
*
* * THIS SECTION BRANCHES TO ANOTHER LOAD OF PARTIAL RELEASE.
*
OUT      EQU   *                                                 O19117
         LA    R3,NEXTXCTL              FETCH NEXT LOAD PNTR     O19117
XCTL     EQU   *                                                 O19117
         IECRES LOAD,EXTPR=(RXWKA),MODNM=(R3),BRANCH=DIRECT      Y02080
*
*** THIS SECTION SETS UP LINKAGE TO THE CVT ROUTINE.
*
CCHHCVT  L     R15,CVTPTR
         USING CVT,R15                                           YM8507
         L     R15,CVTPCNVT                 SET UP BASE AND ENTRY-CVT
         LA    R4,1                         INSERT LOOP COUNT.
         LA    R6,ENTRIES                   PICK UP DADSM TABLE PTR.
         B     CCHHLINK                     GO TO CONVERT RTA TO CCHH.
CVTORTA  LA    R4,2                         INSERT COUNT
RTACVT   L     R15,CVTPTR
         L     R15,CVTPRLTV                 SET UP BASE  FOR CVT ROUT.
         MVC   MBBCCHHR+3(4),HOLD           MOVE CCHH TO BE CONVERTED.
         LA    R2,MBBCCHHR                  POINT TO CCHH AREA.
CCHHLINK EQU   *                                                 A39292
         STM   R9,R15,SAVEREGS          SAVE REGISTERS           A39292
         NI    CONVCON+1,X'00'         ZERO RTA INCREMENT
         LR    R3,RXWKA                 SAVE WORKAREA REGISTER   A39292
         LA    R1,DXDEB                     PICK UP DEB PTR.
CVTLINK1 BALR  RLINK,R15                    CONVERT CCHH TO RTA.
         LM    RXWKA,RXBASE,8+SAVEREGS-FIRST(R3)  RESTORE RLSE   A39292
*                                       WORKAREA AND BASE REGS   A39292
         SRL   R0,16
         AH    R0,CONVCON
         STH   R0,0(R6)                     STORE RTA INTO DADSNTBL.
         LA    R6,2(R6)                     INCREMENT DADSMTBL PTR.
         XI    CONVCON+1,1                  FLIPFLOP RTA INCREMENT.
         BCT   R4,FLIPFLOP                  CONTINUE IF COUNT NOT ZERO.
         LM    R9,R15,SAVEREGS-FIRST(R3)  RESTORE VOLATILE REGS  A39292
         BR    RLINK                        RETURN.
FLIPFLOP EQU   *
         MVC   MBBCCHHR+3(4),HOLD+4         PICK UP NEXT EXTENT.
         LA    R5,1(R5)                     INCREMENT ENTRY NO IN TBL.
         LA    R7,10(R7)                    UPDATE F1 EXTENT PTR.
         L     R15,24+SAVEREGS-FIRST(R3)  RESTORE CVT POINTER    A39292
         B     CVTLINK1                     LOOP TO CONVERT NEXT EXTNT
*
*** THIS LOCATES THE SUPERVISOR REQUEST BLOCK OF WHICH ITS EXTENSION
*   IS USED AS A REGISTER SAVE AREA.
*
FINDSVRB L     R15,CVTPTR                   PICK UP CVT POINTER.
         L     R15,CVTTCBP                  GET POINTER TO TCB ADDR.
         L     R15,4(R15)                   GET POINTER TO CURRENT TCB.
         L     R15,0(R15)                   PICK UP SVRB ADDR.
         ST    R15,SVRBPTR                  SAVE POINTER TO SVRB.
         BR    RLINK                        SAVE VOL REGS IN SVRB.
*
*** THIS SECTION INITIATES THE CHANNEL PROGRAM.
*
RDDSCBT  EQU   *                                                SA53147
         EXCP  DXIOB                        READ DSCB
         WAIT  ECB=DXECB                    WAIT FOR COMPLETION.
         TM    DXECB,X'20'                  TEST FOR PERM I/O ERR.
         BCR   5,RLINK                      RETURN IF NO I/O ERR.
         LA    ERRETREG,IOERROR         LOAD I/O ERROR CODE      A37199
LEAVE    EQU   *                                                SA54549
         LA    R3,PRECLOSE              PREP FOR LAST LOAD      SA54549
         B     XCTL                                             SA54549
*
*** CHANNEL PROGRAM
*
         DS    0F
CCWX1    DC    X'31'
         DC    AL3(DXDAADDR+3-CLOSEWKA)
         DC    X'4000'
         DC    H'5'
CCWX2    DC    X'08'
         DC    AL3(DXCCW1-CLOSEWKA)
         DC    F'0'
CCWX3    DC    X'0E'                    READ F1 KEY AND DATA     Y02082
         DC    AL3(OUTDSCB-PRLSEWKA)    DATA ADDR                Y02080
         DC    H'0'                     NO COMMAND CHAINING      Y02082
         DC    H'140'
*CCWX4                                      SAME AS CCWX1.
*CCWX5                                      TIC TO CCW4
CCWX6    DC    X'06'                    READ DATA COMMAND       SA53147
         DC    AL3(IECSDSL4-PRLSEWKA)   ADDR OF READ AREA        Y02080
         DC    X'4000'                  COMMAND CHAINING        SA53147
         DC    H'96'                    LENGTH OF 96 BYTES      SA53147
CCWX7    DC    X'92'                        READ COUNT FOR F5 DSCB.
         DC    AL3(DADSMADR-PRLSEWKA)   ADDRESS OF F5 CCHHR      Y02080
         DC    X'2000'
         DC    H'5'
*
*** CONSTANTS
*
TEN      DC    H'10'
SIXTEEN  DC    H'16'                    SIXTEEN BYTES            Y02082
ECCHHTRK DC    4X'FF'                   ENDING CCHH MOVED INTO  SA54549
*                                       DEB ALONG WITH NEXT TWO SA54549
*                                       CONSTANTS               SA54549
         DC    X'7F'                    1ST BYTE OF TRACKS TO   SA54549
*                                       BE MOVED INTO DEB       SA54549
ENABLE   DC    X'FF'                    ENABLE INTERRUPTS PLUS  SA54549
*                                       USED AS LAST BYTE OF    SA54549
*                                       TRACKS MOVED INTO DEB   SA54549
SYSZTIOT DC    CL8'SYSZTIOT'            TIOT MAJOR RESOURCE NAME Y02082
SYSDSN   DC    CL8'SYSDSN'              DSN MAJOR RESOURCE NAME  Y02082
VTOCNAME DC    CL8'SYSVTOC '
ENQLIST  ENQ   (,,E,6,SYSTEM),RET=CHNG,MF=L  TIOT ENQ PARM LIST  Y02082
ENQLTH   EQU   *-ENQLIST                LENGTH OF ENQ PARM LIST  Y02082
ENQLIST1 ENQ   (,,E,,SYSTEM),RET=USE,MF=L  FIRST DSN ENQ LIST    YM2880
ENQLTH1  EQU   *-ENQLIST1               LENGTH OF ENQ PARM LIST  Y02082
ENQLIST2 ENQ   (,,E,,SYSTEM),RET=CHNG,MF=L  2ND DSN ENQ LIST     YM2880
ENQLTH2  EQU   *-ENQLIST2               LENGTH OF ENQ PARM LIST  Y02082
*
*        DEB ACCESS METHOD TYPE VALUES
*
AMBPAM   DEBCHK AM=BPAM,MF=L            BPAM AMTYPE              Y02082
AMBDAM   DEBCHK AM=BDAM,MF=L            BDAM AMTYPE              Y02082
AMSAM    DEBCHK AM=SAM,MF=L             SAM AMTYPE               Y02082
AMEXCP   DEBCHK AM=EXCP,MF=L            EXCP AMTYPE              Y02082
AMISAM   DEBCHK AM=ISAM,MF=L            ISAM AMTYPE              Y02082
*
*** TABLE OF MODULE NAMES AND ENTRY POINT ADDRESSES
*
         XCTLTABL ID=(FIRSTLD,IGG020P1,NEXTXCTL,IGG020P2,        Y02080X
               D0,IGG020D0,PRECLOSE,IGG020P3,RPS19EK,IGG019EK),  YM3997X
               SVC=020,LENGTH=,BRT=YES                           Y02080
         SPACE 2                                                 Y02144
         IECDSECS ASCB,CVT,IEZDEB,JSCB,PSA,TCB,RRPL,UCB,         Y02082X
               EXPAND=YES                                        Y02082
*
*** RELEASE WORK AREA
*
WORKAREA IECPRLWA EP,F4,D2=(1),ADT=YES  RELEASE WORK AREA        Y02144
SAVEREGS EQU   IECREGSV+12              REGISTER SAVE AREA       Y02080
         EJECT                                                  SA54549
*
*** I/O SUPPORT WORK AREA
*
CLOSEWKA DSECT
         IECDSECT
MINAME   EQU   DXCCW8+K2                ENQ MINOR NAME           Y02082
MJNAME   EQU   DXCCW9                   ENQ MAJOR NAME           Y02082
ENQAREA  EQU   DXCCW11                                             AAAA
         EJECT
         DCBD  DSORG=(PS)
         EJECT
         IEEVRSWA VER=3                 RESTART WORKAREA         YM8507
         END
