*/*
         MACRO
&NAME    IEDDCT    &REG=1,&FLD=,&LEN=4
         LCLA      &NDX
         LCLB      &KGT1,&LGT6,&L1LT0
         LCLC      &OPS,&MLEN
.*A000000-999999                                               @Y17XAYO
.*A003820-004060,005650-005850                                 @Y17XAMO
.*C001700,002600,002900,003400,003800,006500-007060            @Y17XAMO
&NDX     SETA      2
&NAME    SR        &REG,&REG      CLEAR FOR IC
         IC        &REG,TRMCHCIN  PICK UP DCT INDEX
         BCTR      &REG,0         DECREMENT FOR MULTIPLY
         MH        &REG,AVTDCTLN  GET DCT ADDRESS OFFSET
         A         &REG,AVTCSTCS  GET ADDRESS OF DCT ENTRY
         AIF       ('&FLD' EQ '').DONE   NO MOVE ->
         AIF       ('&LEN' GT '1').NON0LEN CHECK FOR 0 LEN SPEC
         MNOTE     0,'ZERO/ONE LENGTH MOVE SPECIFIED - NO MOVE DONE'
         AGO       .DONE  EXIT                                 @Y17XAMO
* THE FOLLOWING SEVEN LINES DETERMINE IF THE &LEN SPEC IS A SYMBOL OR
* A CONSTANT, AND IF A CONSTANT THAT THE VALUE IS SIX OR LESS.
.NON0LEN ANOP
&MLEN    SETC      '&LEN'
&KGT1    SETB      (K'&LEN GT 1)
&LGT6    SETB      ('&LEN' GT '6')
&L1LT0   SETB      ('&LEN'(1,1) LT '0')
         AIF   ((&KGT1 AND &L1LT0) OR (NOT(&KGT1 OR &LGT6))).LENOK
&MLEN    SETC      '4'                                         @Y17XAMO
.LENOK   AIF       ('&FLD'(K'&FLD,1) EQ ')').BASED
         MVC       &FLD.(&MLEN),0(&REG) MOVE THE DCT ENTRY
         AGO       .CHECK                                      @Y17XAMO
.BASED   AIF       ('&FLD'(&NDX,1) EQ '(').FOUND
&NDX     SETA      &NDX+1
         AIF       (&NDX LT K'&FLD).BASED
         MNOTE     12,'INVALID ''TO'' OPERAND SPECIFIED: &FLD'
         AGO       .DONE  EXIT                                 @Y17XAMO
.FOUND   ANOP
&OPS     SETC     '&FLD'(1,&NDX).'&MLEN'.','.'&FLD'(&NDX+1,K'&FLD-&NDX)
         MVC       &OPS,0(&REG)
*                                MOVE THE DCT ENTRY
.CHECK   TM        TRMSTATE,TRMPREF   IS THERE A PREFIX?       @Y17XAMO
         BNO       $DCT&SYSNDX   NO ->                         @Y17XAMO
         CLI       TRMTYPE,TRMLUNT   SNA LOGICAL UNIT?         @Y17XAMO
         BNE       $DCT&SYSNDX  NO ->                          @Y17XAMO
         ICM       &REG,7,TRMSIBPT PICK UP THE SIB ADDRESS     @Y17XAMO
         TM        SIBSTAT0-IEDSIBD(&REG),X'7C' LUTYPE 0,1,2,3 @Y17XAMO
         BNZ       $DCT&SYSNDX  NO ->                          @Y17XAMO
         TM        SIBSTAT0-IEDSIBD(&REG),X'03' LUTYPE 0 OR 3? @Y17XAMO
         BNM       #DCT&SYSNDX  YES ->                         @Y17XAMO
         TM        SIBSTAT0-IEDSIBD(&REG),X'02' IS IT LUTYPE 2 @Y17XAMO
         BO        #DCT&SYSNDX  YES ->                         @Y17XAMO
$DCT&SYSNDX LA        &REG,&FLD       PICK UP TARGET ADDRESS   @Y17XAMO
         B         @DCT&SYSNDX  SKIP AROUND 3270 BIT SETTING   @Y17XAMO
#DCT&SYSNDX LA        &REG,&FLD       PICK UP TARGET ADDRESS   @Y17XAMO
         OI        DCTBYTE1-IEDDCT(&REG),DCT3270    SET 3270 BIT
@DCT&SYSNDX DS        0H
         AGO       .DONE                             */
*%IEDDCT: MACRO KEYS(REG,FLD,LEN);
*         DCL (REG,FLD,LEN)    CHAR;
*         DCL (MREG,MFLD,MLEN,SLEN) CHAR;
*         DCL FLEN             FIXED;
*         MREG='?'××MACLABEL××' '××MACNAME××' '××MACKEYS××';';
*         ANS(COMMENT(MREG)) SKIP  COL(MACCOL);
*         IF REG(1)='' THEN
*            ANS('/* THE ''REG'' KEYWORD MUST BE SPECIFIED */')  SKIP
*            MSG('/* THE ''REG'' KEYWORD MUST BE SPECIFIED */');
*         ELSE DO;
*              MREG=REG(1);   MFLD=FLD(1);
* ANS('/* SET UP ADDRESSABILITY TO THE DCT ENTRY */') COL(9);
* ANS(MACLABEL××MREG××'=AVTCSTCS+(TRMCHCIN-1)*AVTDCTLN;') SKIP COL(10);
*              IF MFLD^='' THEN DO; /* MOVE REQUESTED            */
*              MLEN=LEN(1);
*              IF MLEN='' THEN MLEN='4';
*              ELSE IF INDEX('0123456789',MLEN(1:1))^=0 THEN DO;
*              FLEN=FIXED(MLEN); IF FLEN>6 THEN MLEN='4';
*              ELSE IF FLEN<2 THEN MLEN='4';                 END;
*              IF INDEX(MFLD,'(')=0 THEN SLEN='(1:'××MLEN××')';
*              ELSE SLEN='';
* ANS('/* MOVE THE DCT ENTRY TO THE SPECIFIED WORKAREA */') COL(9);
* ANS(MFLD××SLEN××' = '××MREG××'->IEDDCT(1:'××MLEN××');') SKIP COL(10);
* ANS('/* SET POINTER TO THE TARGET ADDRESS */') COL(9);
* ANS(MREG××'=ADDR('××MFLD××'); /*SET UP PTR*/') SKIP COL(15);
* ANS('/* IS THE DCT FOR AN LU WITH 3270 DATA STREAM */') COL(9);
* ANS('IF (TRMPREF=''1''B  & TRMTYPE=TRMLUNT & (TRMSIBPT->SIBLUTYP = '××
*'''0000000''B × TRMSIBPT->SIBLUTYP=''0000010''B × TRMSIBPT->SIBLUTYP'××
*' = ''0000011''B)) THEN') COL(10);
* ANS('/* YES - THEN SET THE DCT3270 BIT IN THE COPY MADE */') COL(9);
* ANS(MREG××'->DCT3270 = ''1''B; /* SET 3270 BIT */') SKIP COL(15);
*              END; END;
*%END IEDDCT;                         /*
.DONE    ANOP
         MEND                                */
