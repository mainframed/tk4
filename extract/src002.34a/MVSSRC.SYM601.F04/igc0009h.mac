         TITLE 'IGC0009H SVC 98 (PROTECT) FIRST LOAD - PASSWORD DATA SEX
               T MAINTENANCE/INITIALIZATION'
IGC0009H CSECT
*
*        VS2 RELEASE 037 CHANGES
*A074000,074600,075200,089500,094000,095200,116000,118000      @ZA26556
*A145000,164090-173450,173810,180000,183000,209000-210000      @ZA26556
*A173540                                                       @ZA25398
*A210800,392000-393800,436500,469100,469200,578500,579000      @ZA26556
*A588500,627000,627500                                         @ZA26556
*C141500,511000,513000                                         @ZA26556
*
*        VS2-3.0 SU
*
*0000007500,140500-141600,145100,469500                        @ZA03174
*
*0000007500,070000,141500,361000,365000,367000,370000-371000   @ZA11617
*0000373000,383000,419000,423000-425000                        @ZA11617
*
*A 461500                                                      @YL026TA
*A 461600                                                      @YL026TA
*A 057500,146500,224500,239500,288500,478500,577500-579441     @YL026TA
*C 006000,147000,224000,239000,269000,288000,478000            @YL026TA
*D 145500-145600,437500-437700                                 @YL026TA
*C 145500                                                        YM2088
*C 424000                                                      @ZA01374
*D 333000                                                      @ZA02233
*C 462000                                                      @Z30RSTA
*A 462500                                                      @Z30RSTA
*C 070000,361000,365000,367000,370000-371000,373000-383000     @ZA06424
*C 419000,423000                                               @ZA06424
*A 478700-478800,371010-371030,423010-423020                   @ZA06424
***********************************************************************
*                                                                     *
* STATUS -- OS/VS2 RELEASE 037                                 @ZA26556
*                                                                     *
* FUNCTION -- FIRST LOAD OF THE PASSWORD DATA SET MAINTENANCE ROUTINE *
*    1. INITIALIZE A WORK AREA WITH A DCB, DEB, ECB, IOB, AND CCWS.   *
*    2. READ THE PASSWORD DATA SET DSCB FROM THE SYSTEM RESIDENCE     *
*    DEVICE.  THE DATA SET IS FLAGGED AS NEWLY ALLOCATED IF THE DSCB  *
*    DS1LSTAR FIELD IS ZEROED.  THE PASSWORD DATA SET WILL NOT BE     *
*    SEARCHED FOR EXISTING ENTRIES IF THIS FIELD IS ZERO.             *
*    3. VALIDATE PARAMETER LIST PASSED TO SVC 98.                     *
*                                                                     *
* ENTRY POINTS --                                                     *
*         IGC0009H - ENTRY POINT OF FIRST LOAD, ENTERED WHEN SVC 98   *
*    ISSUED                                                           *
*                                                                     *
* INPUT -- REGISTER 1 POINTS TO A PARAMETER LIST WHICH REQUESTS THE   *
*    DIFFERENT FUNCTIONS OF SVC 98.                                   *
*                                                                     *
* OUTPUT --                                                           *
*    REGISTER RW POINTS TO WORK AREA                                  *
*    REGISTER RPARM POINTS TO INPUT PARAMETER LIST                    *
*    REGISTER RA CONTAINS ENTRY CODE FOR IGC0109H (SECOND LOAD SVC 98)*
*    REGISTER RB PASSES RETURN CODE BETWEEN LOADS OF SVC 98           *
*                                                                     *
* EXTERNAL REFERENCES --                                              *
*         IECPCNVT - BRANCH TO THE RESIDENT DATA MANAGEMENT CONVERT   *
*    ROUTINE TO CONVERT A RELATIVE DIRECT ACCESS DEVICE ADDRESS (TTR) *
*    TO AN ACTUAL DIRECT ACCESS DEVICE ADDRESS (MBBCCHHR)             *
*                                                                     *
* EXITS, NORMAL -- XCTL TO IGC0109H TO PROCESS PASSWORD DATA SET      *
*                                                                     *
* EXITS, ERROR -- XCTL TO IGC0109H WITH NON-ZERO RETURN CODE          *
*    08  DATA SET NAME/PASSWORD NOT IN PASSWORD DATA SET              *
*    16  INCOMPLETE OR INCORRECT PARAMETER LIST                       *
*    44  PASSWORD DATA SET NOT ON THE SYSTEM RESIDENCE VOLUME         *
*                                                                     *
* TABLES/WORK AREAS --                                                *
*    WORKD - A CONTIGUOUS AREA DEFINED BY IECDSECT FOR I/O CONTROL    *
*    BLOCKS FOLLOWED BY A LOCAL WORK AREA FOR COMMUNICATION AND       *
*    BUFFER SPACE                                                     *
*    PARMD - A DSECT TO MAP THE INPUT PARAMETER LIST                  *
*    WORKSPACE - A DSECT TO MAP THE LOCATE AND OBTAIN WORKAREAS       *
*                                                                     *
* ATTRIBUTES -- REENTRANT, READ ONLY, PRIVILEGED, ENABLED             *
*                                                                     *
* CHARACTER CODE DEPENDENCY -- CHARACTER CODE INDEPENDENT             *
*                                                                     *
* NOTES --                                                            *
*    DESCRIBED IN DADSM LOGIC MANUAL, SY26-3787.               @YL026TA
*                                                                     *
***********************************************************************
         EJECT
*
*        EQUATED SYMBOLS
*
NOSEEK   EQU   X'18'               FILE MASK-NO SEEKS
CLRLL    EQU   X'2B'               TO CLEAR DSNAME BUFFER TO BLANKS
HLEN     EQU   X'2C'               MAXIMUM DSNAME LENGTH
NEWMASK  EQU   X'00'               IDENTIFY FIRST TIME INITIALIZATION
FLAGIOB  EQU   X'42'               IOB FLAG / COMMAND CHAIN-UNRELATED
DEBID    EQU   X'0F'               IDENTIFY AS A DEB
CCWL     EQU   48                  CCW LIST LENGTH             @ZA11617
DATALL   EQU   X'50'               PASSWORD ENTRY DATA LENGTH
DSCBLL   EQU   96                  DSCB DATA FIELD LENGTH
KEYLL    EQU   X'34'               PASSWORD ENTRY KEY LENGTH
AUTH     EQU   X'80'               INDICATE AUTHORIZED USER    @ZA26556
MPENQ    EQU   X'80'               MPDS ENQUEUE FLAG
HSTR     EQU   X'4D'               MAX STRING LENTH            @ZA26556
REPASS   EQU   X'40'               MASK INDICATES REPLACE OF PASSWORD
SECPASS  EQU   X'20'               VALIDITY CHECK SECOND PASS  @ZA26556
ENQF     EQU   X'20'               TO SET DFLAG1- INDICATE ENQUEUES
OBTNCORE EQU   X'08'               OBTAIN CORE TO BE FREED
ADD      EQU   X'01'               ENTRY CODE FOR ADD
REPLACE  EQU   X'02'               ENTRY CODE FOR REPLACE
LIST     EQU   X'04'               ENTRY CODE FOR LIST
TTR      EQU   X'05'               ENTRY CODE FOR TTR
PCOD0    EQU   X'00'               PROTECTION CODE '00'          A43379
PCOD3    EQU   X'03'               PROTECTION CODE '03'          A43379
LO       EQU   4                   TEST FOR LOW COMPARE          A43379
USERSP   EQU   251                 SUBPOOL UNDER USERS PROTECT KEY
HI       EQU   2                   TEST FOR HIGH COMPARE
ZR       EQU   8                   TEST FOR EQUAL COMPARE
ZERO     EQU   0                   ZERO
N1       EQU   1                   ONE
N2       EQU   2                   TWO
N3       EQU   3                   THREE
N4       EQU   4                   FOUR
N5       EQU   5                   FIVE                        @ZA26556
N6       EQU   6                   SIX
N7       EQU   7                   SEVEN
N8       EQU   8                   EIGHT
N9       EQU   9                   NINE
N12      EQU   12                  TWELVE                      @ZA26556
N16      EQU   16                  SIXTEEN
N17      EQU   17                  SEVENTEEN
N24      EQU   24                  TWENTY-FOUR
N28      EQU   28                  TWENTY-EIGHT                @ZA26556
RNLENGTH EQU   44                  RNAME LENGTH FOR ENQUEUE ON DSCB
L132     EQU   132                 LENGTH OF KEY AND DATA FIELDS
BLANK    EQU   C' '                BLANK
HFF      EQU   X'FF'               PASSWORD ENTRY DELETION CHARACTER
*
*        RETURN CODES FOR ALL CONDITIONS  (NORMAL OR ERROR)
*
C4       EQU   4                   RETURN CODE  4
C8       EQU   8                   RETURN CODE  8
C12      EQU   12                  RETURN CODE 12
C16      EQU   16                  RETURN CODE 16
C20      EQU   20                  RETURN CODE 20
C24      EQU   24                  RETURN CODE 24
C28      EQU   28                  RETURN CODE 28
C32      EQU   32                  RETURN CODE 32
C36      EQU   36                  RETURN CODE 36
C40      EQU   40                  RETURN CODE 40
C44      EQU   44                  RETURN CODE 44
C48      EQU   48                  RETURN CODE 48
*
*        REGISTER EQUATES
*
RP0      EQU   0                   PARAMETER REGISTER
R0       EQU   0                   WORK REGISTER               @ZA26556
RP1      EQU   1                   PARAMETER REGISTER
RP2      EQU   2                   PARAMETER REGISTER
R2       EQU   2                   WORK REGISTER
R4       EQU   4                   WORK REGISTER               @ZA26556
RCVT     EQU   3                   CVT ADDRESS
RPARM    EQU   4                   ADDRESS OF CALLERS PARAMETER LIST
RW       EQU   5                   ADDRESS OF WORK AREA (WORKD)
RUCB     EQU   6                   UCB ADDRESS
RWK6     EQU   6                   WORK REGISTER
RWK7     EQU   7                   WORK REGISTER
RWK8     EQU   8                   WORK REGISTER
RWK9     EQU   9                   WORK REGISTER
RA       EQU   10                  WORK REGISTER
RB       EQU   11                  WORK REGISTER
RBASE    EQU   12                  BASE REGISTER
RD       EQU   13                  UNUSED REGISTER
RE       EQU   14                  RETURN LINKAGE
RF       EQU   15                  LINKAGE/RETURN CODE
         EJECT
         USING WORKD,RW            ESTABLISH ADDRESSABILITY FOR DSECT
         USING CVTTCBP,RCVT        ESTABLISH ADDRESSABILITY FOR CVT
         USING SRT,RUCB            ESTABLISH ADDRESSABILITY FOR UCB
         BALR  RBASE,RP0           GET ADDRESS WHERE LOADED
         USING *,RBASE             ESTABLISH ADDRESSABILITY FOR PROGRAM
         B     BRID                BRANCH AROUND ID            @ZA03174
         DC    C'IGC0009H'         MODULE ID                   @ZA03174
         DC    X'7339'             DATE  12/05/77              @ZA26556
BRID     EQU   *                                               @ZA03174
         LR    RA,RE               SAVE RETURN ADDRESS
*        REGISTER 3 CONTAINS CVT ADDRESS AT ENTRY
*        REGISTER 4 CONTAINS TCB ADDRESS AT ENTRY
         LR    RB,RPARM            SAVE TCB POINTER FOR VALIDATION
         LR    RWK7,RP1            PARAMETER LIST POINTER      @ZA26556
INITCOR  LA    RP0,WKSIZE          WORK AREA SIZE
*        GET WORK AREA STORAGE                                 @YL026TA
         GETMAIN R,LV=(0),RELATED=(SP0-2K,IGC0209H(NOOBTAIN))  @YL026TA
         LR    RW,RP1              BASE FOR WORK AREA WORKD
         ST    RB,TCBADDR          SAVE TCB ADDRESS FOR MFT ENQUEUE
         XC    COMMD(DCLEAR),COMMD CLEAR PORTION OF WORK AREA
         XC    DFOXKEY(L132),DFOXKEY CLEAR BUFFER
         L     RUCB,CVTILK2        ADDRESS OF UCB LOOKUP TABLE
         ST    RUCB,DUCBADDR       SAVE FOR UCB SCAN IN IGC0209H
         ST    RA,DSAVR            SAVE RETURN ADDRESS IN WORK AREA
         MVC   JFCBELNM(WTOLL),FULMSG SET UP WTO MESSAGE IN WORK AREA
*
*        VALIDATE PARAMETER LIST PASSED BY REQUESTOR. THE DATA SET
*        NAME AND PASSWORD ARE MOVED INTO AN I/O BUFFER.
*
         MVI   DKEY,BLANK          BLANK KEY
         MVC   DKEY+N1(CLRLL),DKEY CLEAR DSNAME/PASSWORD AREA
         LA    RA,PRMER010         ERROR ROUTINE ADDRESS
         STM   R0,RE,REGSAVE       SAVE REGS FOR AUTHORITY CK  @ZA26556
         TESTAUTH FCTN=1,KEY=YES,STATE=YES,BRANCH=YES          @ZA26556
         LTR   RF,RF               USER AUTHORIZED?            @ZA26556
         LM    R0,RE,REGSAVE       RESTORE REGS                @ZA26556
         BZ    SKIPLOCK            YES,NO NEED TO GET LOCK     @ZA26556
OBTLOCK  SETLOCK OBTAIN,TYPE=LOCAL,MODE=UNCOND,                @ZA26556X
               RELATED=(LOCAL,IGC0009H(RLSELOCK))              @ZA26556
         LM    R0,RE,REGSAVE       RESTORE REGS                @ZA26556
         B     CONTINUE                                        @ZA26556
SKIPLOCK EQU   *                                               @ZA26556
         OI    DFLAG2,AUTH         SET AUTHORIZED FLAG         @ZA26556
CONTINUE EQU   *                                               @ZA26556
         CLI   0(RWK7),TTR         IS ENTRY CODE TOO LARGE     @ZA26556
         BH    PRMER010            ERROR EXIT                  @ZA26556
         CLI   0(RWK7),ADD         IS ENTRY CODE TO SMALL      @ZA26556
         BL    PRMER010            ERROR EXIT                  @ZA26556
         CLI   0(RWK7),N3          ENTRY CODE EQU THREE        @ZA26556
         BZ    SETLTH5             YES,GO SET PARM LIST LTH    @ZA26556
         BL    SETLTH7             LOW,SET LTH TO SEVEN WDS    @ZA26556
         LA    RWK8,N3             SET LTH TO THREE WDS        @ZA26556
         B     TESTPARM            GO VALCK PARM LIST          @ZA26556
SETLTH5  EQU   *                                               @ZA26556
         LA    RWK8,N5             SET LTH TO FIVE WDS         @ZA26556
         B     TESTPARM            GO VALCK PARM LIST          @ZA26556
SETLTH7  EQU   *                                               @ZA26556
         LA    RWK8,N7             SET LTH TO SEVEN WDS        @ZA26556
TESTPARM EQU   *                                               @ZA26556
         SLL   RWK8,N2             MULTIPLY BY 4               @ZA26556
         LR    RWK6,RWK8           SAVE LENGTH OF PARMLIST     @ZA26556
         AR    RWK8,RWK7           COMPUTE ENDING ADDRESS      @ZA26556
         BCTR  RWK8,ZERO           REDUCE BY 1 FOR ACTUAL END  @ZA26556
         TM    DFLAG2,AUTH         IS USER AUTHORIZED?         @ZA26556
         BO    NOVALCK             SKIP VALIDITY CHECK         @ZA26556
         BAL   RWK9,VALCKRTN       GO VALIDITY CHECK PARMLIST  @ZA26556
NOVALCK  EQU   *                                               @ZA26556
         BCTR  RWK6,ZERO           DECREMENT FOR MOVE          @ZA26556
         EX    RWK6,MOVEPARM       GO GET PARM LIST            @ZA26556
         CLI   DSNLL,HLEN          IS DSN LTH VALID?           @ZA26556
         BH    PRMER010            NO,ERROR EXIT               @ZA26556
         CLI   DSNLL,ZERO          IS DSN LTH ZERO?            @ZA26556
         BZ    PRMER010            YES,ERROR                   @ZA26556
         CLI   ENCODE,N3           IS ENTRY CODE GTR THAN TWO? @ZA26556
         BNL   TEST2               SKIP PROT CODE CHECK        @ZA26556
         CLI   PCODE,PCOD3         IS PROT CODE TOO LARGE?     @ZA26556
         BH    PRMER010            YES,GO TO ERROR EXIT        @ZA26556
         CLI   STRINGL,HSTR        IS STRING LTH TOO GREAT?    @ZA26556
         BH    PRMER010            YES,ERROR                   @ZA26556
TEST2    EQU   *                                               @ZA26556
         TM    DFLAG2,AUTH         IS USER AUTHORIZED?         @ZA26556
         BO    COPYDSN             GO MOVE DSNAME AND PASSWORD @ZA26556
         LA    RB,DBUF             GET ADDRESS FIRST PARM WD   @ZA26556
         XR    RWK8,RWK8           CLEAR WORK REG              @ZA26556
         XR    RWK7,RWK7           CLEAR WORK REG              @ZA26556
         CLI   ENCODE,N4           IS ENTRY CODE LESS THAN FOUR@ZA26556
         BL    ECLFOUR             YES,BRANCH                  @ZA26556
         LA    RWK6,N3             SET CNTR                    @ZA26556
NEXTWORD EQU   *                                               @ZA26556
         ICM   RWK7,N7,N1(RB)      GET A PARMLIST PTR          @ZA26556
         LTR   RWK7,RWK7           IS THERE AN ADDRESS?        @ZA26556
         BZ    PRMER010            NO,ERROR                    @ZA26556
         BAL   RWK9,VALCKRTN       GO CHECK ADDRESS            @ZA26556
         LA    RB,N4(,RB)          INCREMENT TO NEXT WD        @ZA26556
         BCT   RWK6,NEXTWORD       REDUCE CNTR                 @ZA26556
         B     COPYDSN             GO GET DSNAME AND PASSWORD  @ZA26556
ECLFOUR  EQU   *                                               @ZA26556
         LA    RWK6,N4             SET CNTR                    @ZA26556
NEXTWD   EQU   *                                               @ZA26556
         LA    RB,N4(,RB)          SET TO SECOND WORD          @ZA26556
         ICM   RWK7,N7,N1(RB)      GET A PARMLIST ADDRESS      @ZA26556
         LTR   RWK7,RWK7           IS THERE AN ADDRESS?        @ZA26556
         BZ    GETNEXT             NO GO TO NEXT WORD          @ZA26556
         TM    DFLAG2,SECPASS      IS THIS SECOND PASS?        @ZA26556
         BO    VALCK               YES,GO TO VAL CK            @ZA26556
         C     RWK6,ONES           IS CNTR EQUAL TO ONE?       @ZA26556
         BZ    VOLUMECK            GO TEST FOR MULTIPLE VOLS   @ZA26556
VALCK    EQU   *                                               @ZA26556
         BAL   RWK9,VALCKRTN       GO CHECK ADDRESS            @ZA26556
         XR    RWK8,RWK8           CLEAR REG                   @ZA26556
         XR    RWK7,RWK7           CLEAR REG                   @ZA26556
GETNEXT  EQU   *                                               @ZA26556
         BCT   RWK6,NEXTWD         GO PROCESS NEXT ENTRY       @ZA26556
         CLI   ENCODE,N3           IS ENTRY EQUAL TO THREE?    @ZA26556
         BZ    COPYDSN             GO GET DSNAME AND PASSWORD  @ZA26556
         TM    DFLAG2,SECPASS      IS THIS SECOND TIME?        @ZA26556
         BO    COPYDSN             GO GET DSNAME AND PASSWORD  @ZA26556
         OI    DFLAG2,SECPASS      SET SECOND PASS SWITCH      @ZA26556
         LA    RWK6,N2             SET CNTR EQUAL TO TWO       @ZA26556
         B     NEXTWD              GO PROCESS NEXT ENTRY       @ZA26556
VOLUMECK EQU   *                                               @ZA26556
         CLI   VOLNUM,N1           MOVE THAN ONE VOLUME?       @ZA26556
         BNH   VALCK               NO, GO TEST ADDRESS         @ZA26556
         IC    RWK8,VOLNUM         GET NUMBER OF VOLUMES       @ZA26556
         MH    RWK8,SIX            MULTIPLE BY SIX             @ZA26556
         AR    RWK8,RWK7           COMPUTE ENDING ADDRESS      @ZA26556
         BCTR  RWK8,ZERO           REDUCE BY 1 FOR ACTUAL END  @ZA26556
         B     VALCK               GO CK ADDRESS               @ZA26556
VALCKRTN EQU   *                                               @ZA26556
         LR    RP1,RWK7            SET PARM REG                @ZA26556
         LR    RP2,RWK8            SET SECOND PARM REG         @ZA26556
         L     R4,TCBADDR          GET TCB ADDRESS             @ZA26556
         L     RF,CVT0VL00         LOAD RESIDENT VALCK ADDRESS @ZA26556
         BALR  RE,RF               GO CHECK ADDRESSES          @ZA26556
         BNZ   PRMER010            NOT A VALID ADDRESS,ERROR   @ZA26556
         BR    RWK9                RETURN TO CALLER            @ZA26556
COPYDSN  EQU   *                                               @ZA26556
         L     RWK8,DSN            GET ADDRESS OF DSNAME       @ZA25398
         SRDA  RWK8,N24            SHIFT OUT ADDRESS TO NEXT REGISTER
         SRA   RWK9,N8             SET UP ADDRESS IF NOT ZERO
         BZ    PRMER010            IF ZERO GO TO ERROR EXIT    @ZA26556
         BCTR  RWK8,ZERO           DECREMENT LENGTH FOR EXECUTE
         EX    RWK8,MOVDSN         MOVE DSNAME TO BUFFER/SEARCH FIELD
         L     RWK9,CURRENT        CURRENT PASSWORD
         CLI   ENCODE,ADD          IS THIS AN ADD REQUEST
         BE    CHK001              YES, CURRENT PASSWORD NOT REQUIRED
         LTR   RWK9,RWK9           IS CURRENT PW ADDR SPECIFIED
         BZ    PRMER010            GO TO ERROR EXIT            @ZA26556
CHK001   CLI   ENCODE,REPLACE      REPLACE/ADD SPECIFIED
         BH    CHK010              GO PROCESS DELETE ENTRY
         L     RWK8,NEW            GET NEW PASSWORD ADDRESS
         LA    RWK8,ZERO(RWK8)     CLEAR HIGH ORDER BYTE
         LTR   RWK8,RWK8           IS NEW PASSWORD ADDRESS SPECIFIED
         BZ    PRMER010            GO TO ERROR EXIT            @ZA26556
         CLI   ENCODE,ADD          IS ADD SPECIFIED
         BE    CHK005              GO MOVE IN NEW PASSWORD
         CLC   ZERO(N8,RWK8),ZERO(RWK9) IS A NEW PASSWORD SPECIFIED
         BE    CHK010              IF NOT, MOVE IN CURRENT NEW PASSWORD
         OI    DFLAG2,REPASS       SET FLAG FOR SECOND LOAD
CHK005   LR    RWK9,RWK8           ADDRESS NEW PASSWORD TO REGISTER
CHK010   MVC   DKEY8(N8),ZERO(RWK9) MOVE PASSWORD TO BUFFER
         IC    RWK8,ENCODE         SET UP ENTRY CODE FOR BRANCH
         SLL   RWK8,N2             TABLE IN NEXT LOAD OF THE SVC
         STC   RWK8,DCODTAB        SAVE FOR ENTRY TO IGC0109H
         TM    DFLAG2,AUTH         AUTHORIZED USER?            @ZA26556
         BO    NORLSE              BRANCH,NO LOCK TO RELEASE   @ZA26556
         STM   R0,RF,REGSAVE       SAVE REGS                   @ZA26556
RLSELOCK SETLOCK RELEASE,TYPE=LOCAL,                           @ZA26556X
               RELATED=(LOCAL,IGC0009H(OBTLOCK))               @ZA26556
         LM    R0,RF,REGSAVE        RESTORE REGS               @ZA26556
*
*        INITIALIZE ENQUEUE PARAMETER LIST
*
NORLSE   EQU   *                                               @ZA26556
         MVC   DMPDSQN(N17),MPDSQN MOVE NAMES TO ENQUEUE BUFFERS
         MVC   DMPDS+N9(HLEN-N9),DMPDS+N8 CLEAR RNAME BUFFER
         MVC   DENQLST(N4),ENQMP   ENQ OPTIONS TO ENQ LIST
         LA    RWK8,DMPDSQN        QNAME POINTER
         ST    RWK8,DENQLST+N4     QNAME ADDRESS TO ENQ LIST
         LA    RWK8,DKEY           ADDRESS OF KEY
         ST    RWK8,DENQLST+N8     RNAME POINTS TO DSN OF REQUEST
         LA    RP1,DENQLST         POINT TO ENQUEUE LIST
*
*        ENQ ON  MPDS FOR DSN
*
CHK020   ENQ   MF=(E,(1)),RELATED=(SYSZPSWD-USERDATA,IGC0109H(RET040)) *
                                                               @YL026TA
         CLI   ENCODE,ADD          ADD REQUESTED
         BNE   TABREC              IF NOT, SKIP SECOND ENQUEUE
         CLC   ZERO(N9,RWK8),MPDS  IS THE PASSWORD D/S BEING PROTECTED
         BE    INITEQE             BYPASS SECOND ENQ IF  IT IS
*
*        ENQ ON PASSWORD DATA SET IF AN ADD IS REQUESTED TO PREVENT
*        ACCESS BY MULTIPLE ADD REQUESTS.
*
         LA    RWK8,DMPDS          ADDRESS OF PASSWORD DATA SET NAME
         ST    RWK8,DENQLST+N8     RNAME POINTS TO MPDS DSNAME
         LA    RP1,DENQLST         POINT TO ENQUEUE LIST
*
*        ENQUEUE ON PASSWORD DATA SET WITH MODIFIED PARAMETER LIST
*
CHK030   ENQ   MF=(E,(1)),RELATED=(SYSZPSWD-PASSWORD,IGC0109H(RET030),I*
               GC0209H(SKIPDQ))                                @YL026TA
         B     TABREC              GO FIND TRACK CONSTANT
INITEQE  OI    DFLAG1,MPENQ        SPECIFY ONLY ONE ENQ DONE
*
*
*        ROUTINE TO FIND SYSRES DEVICE RECORD CAPACITY CONSTANT
*        CONSTANT IS BASED ON FIXED LENGTH KEYED RECORDS OF 132 BYTES.
*
TABREC   OI    DFLAG1,ENQF         INDICATE ENQUEUES DONE FOR DEQUEUE
         L     RUCB,CVTSYSAD       ADDRESS OF SYSRES DEVICE UCB
         MVC   OBTVOL(N6),SRTEVOLI SYSRES VOLUME SERIAL FOR OBTAIN
         LA    RWK7,TABR           DETERMINE TYPE AND SAVE
         LA    RWK9,TABREND        RECORDS PER TRACK CONSTANT
         LA    RWK8,N2             LENGTH OF EACH TABLE ENTRY
TABRC1   CLC   ZERO(N1,RWK7),UCBTBYT4 IS UCB TYPE CODE IN THE TABLE
         BE    TABRC2              TYPE FOUND IN TABLE
         BXLE  RWK7,RWK8,TABRC1    CONTINUE SEARCH IF MORE ENTRIES
         B     PRMER030            RETURN-WITH ERROR
TABRC2   MVC   DRECAP(N1),N1(RWK7) SAVE RECORD/TRACK CAPACITY
*
*        BEGIN INITIALIZATION FOR ACCESSING MPDS DSCB
*
         LA    RP0,WRKSIZE         WORK AREA FOR OBTAIN/LOCATE
         LA    RP1,USERSP          GETMAIN UNDER USERS PROTECT KEY
         SLL   RP1,N24             SHIFT TO HIGH ORDER BYTE
         OR    RP0,RP1             REQUEST SUBPOOL 251
*
*        GET WORK AREA FOR OBTAIN/LOCATE
*        THE AREA WILL HAVE THE USERS PROTECTION KEY
*
         GETMAIN R,LV=(0),RELATED=(SP251-2K,IGC0209H(FREECORE))
         OI    DFLAG1,OBTNCORE     INDICATE OBTAIN CORE TO BE FREED
         LR    R2,RP1              SAVE ADDRESS FOR OBTAIN OF DSCB
         MVC   OBTPTR(CAMEND-CAMFLAG),CAMFLAG INITIALIZE CAMLIST
         LA    RA,OBTDSN           ADDRESS OF FULLY QUALIFIED MPDS DSN
         ST    RA,OBTAIN2          DSNAME POINTER
         LA    RA,OBTVOL           VOLUME SER POINTER
         ST    RA,OBTAIN3          OBTAIN VOLUME
         ST    R2,OBTAIN4          OBTAIN WORK AREA
*
*        OBTAIN MPDS DSCB FROM SYSRES VTOC
*
         OBTAIN OBTPTR             DO OBTAIN-DSCB TO DXDSCB AREA
         LTR   RF,RF               WAS OBTAIN SUCCESSFUL
         BNZ   PRMER030            GO TO ERROR EXIT- OBTAIN CODE IN RF
         MVC   DXDSCB(DSCBLL),ZERO(R2) MOVE DSCB TO I/O WORK AREA
*
*        INITIALIZE WORK AREA FOR I/O
*
         XC    DXECB(CLRECB),DXECB CLEAR I/O WORK AREA         @YL026TA
         XC    DXDCB(CLEARIO),DXDCB CLEAR I/O WORK AREA        @YL026TA
         XC    DXCCW(CLEARIO1),DXCCW CLEAR I/O WORKAREA        @YL026YB
         LA    RA,DXDEB            DEB ADDRESS
         ST    RA,DXDCBDEB         DEB ADDRESS TO DCB
         LA    RA,DXDCB            DCB ADDRESS
         ST    RA,DXDEBDCB         DCB ADDRESS TO DEB
         ST    RA,IOBWGHT          DCB ADDRESS TO IOB
         L     RUCB,CVTSYSAD-CVT(RCVT) SYSRES DEVICE UCB ADDRESS
         ST    RUCB,DXDEBUCB       UCB ADDRESS TO DEB
         L     RA,CVTXAPG          IOS APPENDAGE TABLE
         ST    RA,DXDEBAPP         APPENDAGE TABLE TO DEB
         LA    RA,DXECB            ECB ADDRESS
         ST    RA,IOBECBPT         ECB ADDRESS TO IOB
         MVI   IOBFLAG1,FLAGIOB    SET IOB FLAGS
         MVI   DXDEBUSR,N1         NO. OF EXTENTS=1
         MVI   DXDEBID,DEBID       MASK TO IDENTIFY AS A DEB
         MVI   DXDEBMOD,NOSEEK     SET FILE MASK FOR NOSEEK X'18'
         OC    DXDEBSCC(N8),DSCLOWLM MOVE PASSWORD D/S EXTENTS TO DEB
         OC    DXDEBNTR(N2),NTRK   MAXIMUM TRACKS FOR DEB
         MVI   DCKDLL,KEYLL        KEY LENGTH FOR WRT C-K-D
         MVI   DCKDLL+N2,DATALL    DATA LENGTH FOR WRT C-K-D
         MVI   DFOXKEY,HFF         SET UP BYTE OF FFS
         MVC   DFOXKEY+N1(KEYLL-N1),DFOXKEY SET UP 52 BYTES OF FFS
*
*        TEST FOR FIRST TIME INTO PASSWORD DATA SET
*
         CLC   DSCLSTAR(N3),DXCCW1 IS TTR FIELD ZEROED
         BNE   INITC0              NO- ASSUME PRIOR ACTIVITY IN MPDS
         MVI   DCODTAB,NEWMASK     FIRST TIME, SET CODE FOR NEW ADD
         MVC   DMPEND+N3(N4),DSCLOWLM  LIMIT SEARCH TO ONE TRACK
         CLI   ENCODE,ADD          YES-  IS FIRST REQUEST AN ADD
         BE    INITC1              GO SET UP CHANNEL PROGRAMS
         LA    RF,C8               DATA SET CONTAINS NO ENTRIES
         B     ERETURN             ERROR, RETURN
*
*        CONVERT TTR TO MBBCCHHR
*        USE TTR (CONVERTED) AS UPPER EXTENT OF MPDS
*
INITC0   LA    RP1,DXDEB           DEB ADDRESS
         OC    DSAVT(N3),DSCLSTAR  TTR TO ZEROED FULL WORD
         L     RP0,DSAVT           TTRN WITH ZERO (N) FIELD
         LA    RP2,DMPEND          FOR MBBCCHHR
         L     RF,CVTPCNVT-CVT(RCVT) GET ADDRESS OF CONVERT
         STM   RWK9,RD,DXJBF       SAVE REGISTERS IN WORKD
         BALR  RE,RF               GO TO CONVERT ROUTINE
         LM    RWK9,RD,DXJBF       RESTORE REGISTERS
*
*        INITIALIZE CHANNEL PROGRAMS  ADDRESS FIELDS
*        TWO CHANNEL PROGRAMS ARE USED, A SEARCH PROGRAM TO LOCATE
*        THE CORRECT PASSWORD ENTRY AND READ THE DATA FIELD
*        AND AN UPDATE CHANNEL PROGRAM TO WRITE AN UPDATED OR
*        A NEW ENTRY TO THE PASSWORD DATA SET.
*
*        SEARCH CHANNEL PROGRAM
*
*              SEARCH ID EQ                 DXCCW1
*              TIC                          DXCCW2
*              READ COUNT                   DXCCW3
*              SEARCH KEY EQ                DXCCW4
*              TIC                          DXCCW5
*              READ DATA                    DXCCW6
*
*        WRITE UPDATE CHANNEL PROGRAM
*
*              SEARCH ID EQ                 DXCCW7
*              TIC                          DXCCW8
*              WRITE CNT-KEY-DATA/KEY-DATA  DXCCW9
*              SEARCH ID EQ                 DXCCW10
*              TIC                          DXCCW11
*              READ DATA (VALIDATE)         DXCCW12
*
INITC1   LA    RA,DXDAADDR+N3      IOB SEEK ADDRESS
         ST    RA,DXCCW1           IOB SEEK ADDRESS TO S-ID (SEARCH)
         ST    RA,DXCCW7           IOB SEEK TO S-ID (UPDATE)   @ZA11617
         LA    RA,DXCCW1           ADDRESS OF FIRST CCW
         ST    RA,DXCCW2           SEARCH ID ADDRESS TO TIC
         LA    RA,DXCCW3           ADDRESS OF THIRD CCW
         ST    RA,DXCCW5           READ COUNT ADDRESS TO TIC   @ZA11617
         LA    RA,DATA             ADDRESS OF DATA BUFFER
         ST    RA,DXCCW6           DATA BUF ADR TO READ CCW    @ZA11617
         LA    RA,DCNT             ADDRESS OF COUNT BUFFER
         ST    RA,DXCCW3           CNT BUFFER ADDRESS FOR READ COUNT
         ST    RA,DXCCW9           CNT BUF ADR TO WRT C-K-D    @ZA11617
         ST    RA,DXCCW10          CNT BUF ADR TO S-ID         @ZA11617
         LA    RA,DKEY             ADDRESS OF KEY
         ST    RA,DXCCW4           KEY/DATA BUF ADR TO S-K-EQ  @ZA11617
         ST    RA,DXCCW12          KEY/DATA BUF ADR TO READ    @ZA11617
         LA    RA,DXCCW7           ADDRESS OF SEVENTH CCW      @ZA11617
         ST    RA,DXCCW8           SEARCH ID ADDRESS TO TIC    @ZA11617
         LA    RA,DXCCW10          ADDRESS OF SRCH CCW         @ZA11617
         ST    RA,DXCCW11          SEARCH KEY ADDRESS TO TIC   @ZA11617
         OC    DXCCW(CCWL),CCWBASE BASIC SEARCH/READ (6 CCWS)  @ZA11617
         OC    DXCCW7(N16),CCWSRCH SEARCH ID/TIC KNOWN CCHHR   @ZA11617
         OC    DXCCW9(N8),CCWWRT   WRITE KEY/DATA              @ZA11617
         OC    DXCCW10(N16),CCWSRCH SEARCH ID/TIC VALIDATE     @ZA11617
         OC    DXCCW12(N8),CCWVAL  READ VALIDATE               @ZA11617
         XR    RF,RF               CLEAR REGISTER FOR EXIT
         B     ERETURN             GO TO SECOND LOAD VIA XCTL
         EJECT
*
*        EXIT ROUTINE- AN EXIT IS MADE TO THE NEXT LOAD OF THE
*        SVC VIA AN XCTL. A NON-ZERO RETURN CODE IS IN REGISTER RB
*        IF AN ERROR HAS OCCURRED IN THIS LOAD.
PRMER010 LA    RF,C16              PARAMETER LIST ERROR
         TM    DFLAG2,AUTH         AUTHORIZED USER?            @ZA26556
         BO    EXITNOW             BRANCH,NO LOCK TO RELEASE   @ZA26556
         STM   R0,RF,REGSAVE       SAVE REGS                   @ZA26556
RLSELKER SETLOCK RELEASE,TYPE=LOCAL,                           @ZA26556X
               RELATED=(LOCAL,IGC0009H(OBTLOCK))               @ZA26556
         LM    R0,RF,REGSAVE        RESTORE REGS               @ZA26556
EXITNOW  B     ERETURN             ERROR, RETURN               @ZA26556
PRMER030 LA    RF,C44              PASSWORD DATA SET NOT FOUND
ERETURN  XR    RA,RA               CLEAR REGISTER
         IC    RA,DCODTAB          SET UP FOR ENTRY INTO IGC0109H
         LR    RB,RF               SAVE THE RETURN CODE
*
*        SET UP FOR XCTL TO IGC0109H
*        THE WORK AREA (DXJBF) IS USED AS AN XCTL LIST
*
         LA    R2,DXJBF            XCTL PARAMETER LIST ADDRESS
         MVC   ZERO(N8,R2),NXLOAD  MOVE IN NAME OF NEXT LOAD
         LA    RF,N8(R2)           ADDRESS OF LIST POINTER FOR XCTL
*
*        XCTL TO NEXT LOAD OF SVC 98
*
         XCTL  EPLOC=(R2),DCB=0,SF=(E,(15)) XCTL
*
         EJECT
*
*        CONSTANTS
*
         DS    0D                  ALIGN DOUBLE WORD
*
*        CCW LIST FOR ALL EXCP I/O OPERATIONS
*
CCWBASE  DS    0CL48               BASIC SEARCH CCWS           @ZA11617
*
CCWSRCH  DC    X'3100000060000005' SEARCH ID EQUAL
         DC    X'0800000000000000' TIC
         DC    X'9200000060000008' READ COUNT M/T              @ZA11617
         DC    X'A900000040000034' SEARCH KEY EQ (DSN+PW)      @ZA11617
         DC    X'0800000000000000' TIC                         @ZA06424
*
*        READ  FOR 80 BYTE DATA FIELD
CCWRD    DC    X'8600000020000050' READ DATA (CHAIN FROM S-K-EQ)
*
*        WRITE - UPDATE IN PLACE OR ADD ON , WRITE EOF
CCWWRT   DC    X'1D0000006000008C' WRITE COUNT-KEY-DATA
*
*        VALIDATE WRITES
CCWVAL   DC    X'0E00000030000050' READ DATA (VALIDATE)
*
MOVDSN   MVC   DKEY(ZERO),ZERO(RWK9) MOVE FOR DSNAME
MOVEPARM MVC   PARMWDS(ZERO),ZERO(RWK7)  MOVE PARMLIST         @ZA26556
*
*        CAMLST PARAMETER LIST
*
CAMFLAG  CAMLST SEARCH,0,0,0
CAMEND   EQU   *                   END OF CAMLIST
*
*        ENQUEUE/DEQUEUE CONSTANTS
*
ENQMP    DC    X'FF2C4000'         ENQUEUE/DEQUEUE CONSTANT
*        KEEP NEXT TWO FIELDS TOGETHER, MOVED BY ONE INSTRUCTION
*        NOTE - EXTRA BLANK REQUIRED, USED TO CLEAR BUFFER
MPDSQN   DC    CL8'SYSZPSWD'       QNAME FOR ALL ENQUEUES
MPDS     DC    CL9'PASSWORD '      NAME OF PASSWORD DATA SET
         EJECT
*
*        THIS TABLE CONTAINS A 2 BYTE FIELD FOR EACH SYSTEM RESIDENCE
*        DEVICE SUPPORTED. THE FIRST BYTE IS THE UCB ID FOR DEVICE
*        THE SECOND BYTE IS THE NUMBER OF 132 BYTE KEYED RECORDS THAT
*        WILL FIT ON EACH TRACK (MINUS 1 RECORD).
*
TABR     EQU   *                   LOOK UP TABLE FOR DEVICE CONSTANT
         DC    X'0819'             2314    ID/RECORD CAPACITY
*                                  2319 SAME AS 2314
         DC    X'0612'             2305-1  ID/RECORD CAPACITY
         DC    X'0722'             2305-2  ID/RECORD CAPACITY
         DC    X'0D27'             3330-1  ID/RECORD CAPACITY  @YL026TA
         DC    X'0A15'             3340    ID/RECORD CAPACITY  @YL026TA
         DC    X'0927'             3330    ID/RECORD CAPACITY  @Z30RSTA
TABREND  DC    X'0B2F'             3350    ID/RECORD CAPACITY  @Z30RSTA
*
*        END OF TABLE LOOKUP TABLE
*
FULMSG   WTO   'IEC021I NO SPACE IN PASSWORD DATA SET',MF=L,ROUTCDE=(9)X
               ,DESC=4             WTO
WTOLL    EQU   *-FULMSG            LENGTH OF WTO MESSAGE
NXLOAD   DC    C'IGC0109H'         SECOND LOAD - SVC 98
NTRK     DC    X'7FFF'             MAXIMUM TRACKS FOR DEB
ONES     DC    A(1)                VALUE OF ONE                @ZA26556
SIX      DC    X'0006'             CONSTANT FOR VOLSERS        @ZA26556
PATCH    DC    20X'00'             PATCH AREA                  @ZA03174
         SPACE 3
         EJECT
*        THIS WORK AREA IS USED BY ALL LOADS OF THE PROTECT SVC.
*        ANY CHANGES TO IT MUST BE MADE TO ALL LOADS.
WORKD    DSECT
*
         IECDSECT
CLEARIO  EQU   DXCCW-DXDCB         LENGTH FOR CLEARING I/O BLOCKS
CLEARIO1 EQU   *-DXCCW             LENGTH FOR CLEARING I/O BLOCKS
CLRECB   EQU   DXDCB-DXECB         LENGTH FOR CLEARING I/O BLOCKS
*
COMMD    EQU   *                   CONTINUE DSECT
*
*        WORK AREA FOR MPDS COMMUNICATION AND BUFFERING
*        A CONTINUATION OF DSECT (WORKD)
*
*
DSAVR    DS    F                   SAVE AREA FOR RETURN REGISTER
DSAVT    DS    F                   MPDS LAST BLOCK SAVE AREA
DUCBADDR DS    F                   ADDRESS OF UCB LOOKUP TABLE
DPMODE   DS    CL1                 PROTECTION MODE BYTE SAVE AREA
DRECAP   DS    CL1                 SYSRES DEVICE-RECORD/TRK CAPACITY
DCODTAB  DS    CL1                 ENTRY CODE SAVE AREA FOR NEXT LOAD
*
*        DFLAG - COMMUNICATES STATUS OF DSCB UPDATING REQUIREMENTS
*
DFLAG1   DS    BL1                 COMMUNICATION FLAG
*        COMMUNICATION FLAG (DFLAG1) IN COMMD WORK AREA
*              1 BYTE  (DFLAG1)
*                   BIT  0  =1 MPDS IS ENQ TO ADD OWN P/W PROTECTION
*                        1  =1 INDICATES I/O ERROR
*                        2  =1 INDICATES ENQUEUED IN 1ST - 2ND LOADS
*                        3  =1 PROTECTED DATA SET-SET BIT 3 IN DSCB
*                        4  =1 OBTAIN CORE TO BE FREED
*                        5  =1 NO PASSWORD TO READ-SET BIT 5 IN DSCB
*                        6  =1 UPDATE DSCB OF DATA SET WITH PROTECTION
*                              SPECIFIED IN BITS 3 AND 5
*                        7  =1 UPDATE MPDS DSCB FROM CORE IMAGE IN
*                              WORK AREA (WORKD) AND DEQ ON MPDS
*
DFLAG2   DS    BL1                 COMMUNICATION FLAG
*              1 BYTE (DFLAG2)
*                   BIT 0  =1 INDICATES AUTHORIZED USER        @ZA26556
*                   BIT 1  =1 INDICATES NEW PASSWORD WITH REPLACE
*                   BIT 2  =1 SECOND PASS SWITCH FOR VALCKRTN  @ZA26556
*                   BIT 3     RESERVED
*                   BIT 4     RESERVED
*                   BIT 5     RESERVED
*                   BIT 6     RESERVED
*                   BIT 7     RESERVED
*
         DS    0CL11               COUNT FIELD FOR I/O
DMPEND   DS    CL8                 MBBCCHHR OF LAST RECORD WRITTEN
DCKDLL   DS    CL3                 KEY LENGTH/DATA LENGTH
*
*        EXCP BUFFER  AREA FOR COUNT KEY DATA READ/WRITE
*
         DS    0F                  ALIGN WORD
         DS    CL1                 ALIGN FOR MBBCCHHR
DMBB     DS    CL3                 MBB FIELD FOR CONVERT ROUTINE
         DS    0CL8                COUNT FIELD FOR I/O
DCNT     DS    CL4                 CCHH PORTION OF COUNT
DRECNO   DS    CL1                 RECORD NUMBER
DKEYLEN  DS    CL1                 KEY LENGTH FOR FOR WRITE
DDATALEN DS    CL2                 DATA LENGTH FOR WRITE
DKEY     DS    CL44                EXCP- 44 BYTE KEY (DSN)
DKEY8    DS    CL8                 EXCP- 8 BYTE EXTENSION (PASSWORD)
DATA     DS    0CL80               DATA FIELD
DUSE     DS    CL2                 USE COUNT
DMODE    DS    BL1                 PROTECTION MODE BYTE FIELD
DAT77    DS    CL77                USER DATA (77 BYTES OPTIONAL DATA)
DSAVLL   EQU   *-DCNT              COUNT-KEY-DATA FIELD LENGTH
DCLEAR   EQU   *-COMMD             WORK AREA TO BE CLEARED TO ZERO
DFOXKEY  DS    52C                 52 BYTE KEY OF FF
         DS    80C                 80 BYTE DATA FIELD
*
*        OBTAIN LIST
*
OBTPTR   CAMLST SEARCH,OBTDSN,OBTVOL,OBTWRK
         ORG   OBTPTR              LABEL OBTAIN FIELDS
OBTAIN1  DS    F                   OBTAIN CAMLST FIRST FULL WORD
OBTAIN2  DS    F                   OBTAIN CAMLST SECOND FULL WORD
OBTAIN3  DS    F                   OBTAIN CAMLST THIRD FULL WORD
OBTAIN4  DS    F                   OBTAIN CAMLST FOURTH FULL WORD
         ORG
*
*        ENQUEUE PARAMETER LIST
*
DENQLST  ENQ   (DMPDSQN,DMPDS,E,HLEN,SYSTEM),MF=L
*        NAME LIST FOR ENQUEUE/DEQUEUE AND OBTAIN
DMPDSQN  DS    0CL52               ONE FIELD FOR SETTING UP IN WORKD
         DS    CL8                 QNAME
DMPDS    DS    CL44                RNAME
OBTDSN   EQU   DMPDS               DSNAME OF MASTER PASSWORD DATA SET
OBTVOL   DS    CL6                 VOLUME SERIAL OF MPDS DSCB VOLUME
DREPSAV  DS    CL140               SAVEAREA FOR PASSWORD ENTRY
LOCNAME  CAMLST NAME,DKEY,,LOCAREA LOCATE CAMLST MACRO
         ORG   LOCNAME             LABEL LOCATE CAMLST FIELDS
LOCNAME1 DS    F                   1ST WORD LOCATE CAMLST
LOCNAME2 DS    F                   2ND WORD LOCATE CAMLST
LOCNAME3 DS    F                   3RD WORD LOCATE CAMLST
LOCNAME4 DS    F                   4TH WORD LOCATE CAMLST
         ORG
USECOUNT DS    F                   SAVE POINTER TO USER COUNT HERE
*  KEEP ENQ AND DEQ LISTS TOGETHER.
ENQLIST  ENQ   (QNAME,RNAME,E,RNLENGTH,SYSTEM),RET=USE,MF=L ENQUEUE
DEQLIST  DEQ   (QNAME,RNAME,RNLENGTH,SYSTEM),RET=HAVE,MF=L DEQUEUE
*  NOTE - MFT ENQUEUE WITH QNAME EQUAL TO SYSDSN REQUIRES TCB
*         ADDRESS IMMEDIATELY FOLLOWING THE QNAME.
QNAME    DS    CL8                 QNAME BUFFER
TCBADDR  DS    F                   TCB ADDRESS FOR MFT ENQUEUE
RNAME    DS    CL44                RNAME BUFFER
OLDFLAG  DS    CL1                 OLD PROTECTION STATUS
NEWFLAG  DS    CL1                 NEW PROTECTION STATUS
*  WORK AREA NEEDED FOR ALLOCATION                             @YL026TA
DAPARM   DS    F                   POINTER TO REQUEST BLOCK    @YL026TA
DARQBLK  DS    0CL20               DYNAMIC ALLOCATION REQUEST          *
                                   BLOCK                       @YL026TA
DABLKLN  DS    CL1                 LENGTH OF BLOCK             @YL026TA
DAVERB   DS    CL1                 VERB CODE FOR REQUEST       @YL026TA
DAFLGS1  DS    CL2                 FLAGS                       @YL026TA
DAERRCD  DS    CL2                 ERROR RETURN CODE           @YL026TA
DAINFO   DS    CL2                 INFORMATIONAL RETURN CODE   @YL026TA
DATXTPT  DS    CL4                 ADDRESS OF TEXT POINTERS    @YL026TA
         DS    CL4                 RESERVED                    @YL026TA
DAFLGS2  DS    CL4                 FLAGS                       @YL026TA
* TEXT POINTERS                                                @YL026TA
DATP1    DS    F                                               @YL026TA
DATP2    DS    F                                               @YL026TA
DATP3    DS    F                                               @YL026TA
DATP4    DS    F                                               @YL026TA
DATP5    DS    F                                               @YL026TA
*  TEXT UNITS FOR ALLOCATION AND UNALLOCATION                  @YL026TA
DADSNM   DS    0CL50               DSNAME TEXT UNIT            @YL026TA
         DS    CL2                 KEY                         @YL026TA
         DS    CL2                 NUMBER                      @YL026TA
DADSL    DS    CL2                 LENGTH                      @YL026TA
DADSN    DS    CL44                DATA SET NAME               @YL026TA
DASTAT   DS    0CL7                STATUS TEXT UNIT            @YL026TA
         DS    CL2                 KEY                         @YL026TA
         DS    CL2                 NUMBER                      @YL026TA
         DS    CL2                 LENGTH                      @YL026TA
         DS    CL1                 STATUS INDICATOR FIELD      @YL026TA
DAUNIT   DS    0CL9                UNIT NAME TEXT UNIT         @YL026TA
         DS    CL2                 KEY                         @YL026TA
         DS    CL2                 NUMBER                      @YL026TA
         DS    CL2                 LENGTH                      @YL026TA
DAUNAM   DS    CL3                 UNIT NAME FIELD             @YL026TA
DADDNM   DS    0CL14               DDNAME TEXT UNIT            @YL026TA
         DS    CL2                 KEY                         @YL026TA
         DS    CL2                 NUMBER                      @YL026TA
         DS    CL2                 LENGTH                      @YL026TA
         DS    CL8                 DDNAME FIELD                @YL026TA
DAVOL    DS    0CL12               VOLUME TEXT UNIT            @YL026TA
         DS    CL2                 KEY                         @YL026TA
         DS    CL2                 NUMBER                      @YL026TA
         DS    CL2                 LENGTH                      @YL026TA
DAVOLSR  DS    CL6                 VOLUME SERIAL FIELD         @YL026TA
*
REGSAVE  DS    18F                 REGSAVE AREA                @ZA26556
RCVTSAVE EQU   REGSAVE+N12         CVT SAVEAREA                @ZA26556
*              PARAMETER LIST FOR ALL REQUESTS
*
*
*              COMMON  SEGMENT FOR ALL REQUESTS
*
PARMWDS  DS    0FL7                                            @ZA26556
ENCODE   DS    0BL1                ENTRY CODE
DBUF     DS    CL4                 ADDRESS OF BUFFER FOR CODE 04/05
DSNLL    DS    0CL1                DSN LENGTH
DSN      DS    CL4                 ADDRESS OF DATA SET NAME
CURRENT  DS    CL4                 ADDRESS OF CURRENT PASSWORD
*
*              ADD / REPLACE / DELETE COMMON SEGMENT
*
CONTROL  DS    CL4                 ADDRESS OF CONTROL PASSWORD
VOLNUM   DS    0CL1                NUMBER OF VOLUMES SPECIFIED
VOLIST   DS    CL4                 ADDRESS OF VOLUME LIST
*
*              ADD / REPLACE  COMMON SEGMENT
*
PCODE    DS    0BL1                PROTECTION CODE
NEW      DS    CL4                 ADDRESS OF NEW PASSWORD
STRINGL  DS    0CL1                STRING LENGTH
STRING   DS    CL4                 ADDRESS OF DATA STRING
*
WKSIZE   EQU   *-WORKD             SIZE OF PROGRAM WORKAREA
*
*        END OF MAPPING OF DSECT WORKAREA
         EJECT
*
WRKSPACE DSECT
*
*  OBTAIN WORKAREA AND LOCATE WORKAREA
*
OBTWRK   DS    CL96                DSCB
OBTCCH   DS    CL5                 CCHHR
         DS    CL249               OBTAIN WORK SPACE
         DS    0D                  ALIGN LOCATE WORKAREA
LOCAREA  DS    CL265               LOCATE WORK SPACE (VOLUME LIST)
WRKSIZE  EQU   *-WRKSPACE          SIZE OF LOCATE AND OBTAIN WORKAREA
*
         EJECT
SRT      DSECT
         IEFUCBOB
         EJECT
CVT      DSECT
         CVT
         EJECT                                                 @ZA26556
         IHAPSA                                                @ZA26556
         END
