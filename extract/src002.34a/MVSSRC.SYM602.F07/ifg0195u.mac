         TITLE 'IFG0195U                      PASSWORD CHECK ROUTINE'
IFG0195U CSECT
***********************************************************************
*                                                                     *
*        VS2 RELEASE 04 DELETIONS/CHANGES                             *
*0000                                                          @ZA02896
*                                                                     *
* MODULE NAME = IFG0195U (OS/VS2)                                     *
*                                                                     *
* DESCRIPTIVE NAME = PASSWORD CHECK ROUTINE                           *
*                                                                     *
* COPYRIGHT = NONE                                                    *
*                                                                     *
* STATUS = RELEASE 2, LEVEL 0                                         *
*                                                                     *
* FUNCTION =                                                          *
*                                                                     *
*        1. SEARCHES PASSWORD DATA SET FOR MATCHING KEY               *
*           DSNAME + PASSWORD (52 BYTES).                             *
*                                                                     *
*        2. UPDATE USE COUNT IN PASSWORD RECORD AND WRITE IT BACK.    *
*                                                                     *
*        3. IF AN UNRECOVERABLE ERROR OCCURS OR IF THE OPERATOR       *
*           IS UNABLE TO MAKE A CORRECT RESPONSE IN TWO TRIES,        *
*           MESSAGES IEC117I AND IEC118I ARE ISSUED TO TERMINAL TASKS.*
*                                                                     *
* NOTES = ALL SEARCHES TO THE PASSWORD DATA SET ARE PROTECTED VIA ENQ.*
*         THE ENQ ON THE DATA SET NAME IS NOT THE SAME AS THE         *
*         SHARE/EXCLUSIVE ENQ ON THE DSNAME DONE BY THE JOB           *
*         SCHEDULER, SINCE THE MAJOR NAME IS DIFFERENT. THE ENQ       *
*         IS DONE TO PREVENT SIMULTANEOUS UPDATE (BY DIFFERENT        *
*         TASKS) OF THE PASSWORD RECORD USE COUNT AND SO LOSING       *
*         COUNT. THIS WOULD NEITHER BE LIKELY NOR CRITICAL.           *
*                                                                     *
*         NOTE THAT WRONG LENGTH IS SUPPRESSED WHEN READING THE       *
*         PASSWORD RECORD BECAUSE MANY USERS HAVE INCORRECTLY         *
*         CREATED THIS DATA SET WITH A BLOCK SIZE OF 132 INSTEAD      *
*         OF WITH A BLOCK SIZE OF 80 WITH KEY OF 44 + 8 = 52.         *
*         THE KEY IS NOT SUPPOSED TO BE INCLUDED IN THE BLOCK SIZE.   *
*                                                                     *
*    DEPENDENCIES =                                                   *
*                                                                     *
*            CLASS ONE CHARACTER CODE.  THE EBCDIC CHARACTER CODE     *
*            WAS USED FOR ASSEMBLY.  THE MODULE MUST BE REASSEMBLED   *
*            IF A DIFFERENT CHARACTER SET IS USED FOR EXECUTION.      *
*                                                                     *
*    RESTRICTIONS = NONE                                              *
*                                                                     *
*    REGISTER CONVENTIONS = SEE IECEQU MACRO.                         *
*                                                                     *
*    PATCH LABEL = SEE XCTLTABL MACRO.                                *
*                                                                     *
* MODULE TYPE = CONTROL (OPEN, CLOSE, EOV DATA MANAGEMENT)            *
*                                                                     *
*    PROCESSOR = ASSEMBLER XF                                         *
*                                                                     *
*    MODULE SIZE = SEE EXTERNAL SYMBOL DICTIONARY OR LOC FIELD ON     *
*                  ORG STATEMENT AT END OF LISTING                    *
*                                                                     *
*    ATTRIBUTES = REENTRANT, REFRESHABLE,READ-ONLY, ENABLED,          *
*                 PRIVILEGED, SUPERVISOR STATE, DATA MANAGEMENT KEY,  *
*                 LINK PACK AREA RESIDENT/PAGEABLE                    *
*                                                                     *
* ENTRY POINT = IFG0195U FROM IFG0195V OR READPSWD.                   *
*                                                                     *
*    PURPOSE = TO READ A PASSWORD RECORD AND VALIDATE THAT IT IS      *
*              CONSISTENT WITH THE PROCESSING MODE REQUESTED.         *
*                                                                     *
*    LINKAGE =                                                        *
*        IECRES LOAD,MODNM=IFG0195U,PREFIX=WTGPREFX,BRANCH=DIRECT     *
*                                                                     *
* INPUT = AS FOLLOWS -                                                *
*                                                                     *
*        R4 = ADDRESSABILITY TO FORCORE-LIKE WORK AREA.               *
*        R6 = ADDRESSABILITY TO WTG TABLE.                            *
*        RD = ADDRESSABILITY TO SECCORE WORK AREA                     *
*                                                                     *
*        IF RET (REG 14) HAS 0 ON ENTRY A PASSWORD MUST BE AT MYREPLY *
*        IF RET (REG 14) HAS 4 ON ENTRY, A PASSWORD RECORD            *
*        IS AT THE 132 BYTE AREA BEGINNING AT MYDSN.                  *
*                                                                     *
* OUTPUT = AS FOLLOWS -                                               *
*                                                                     *
*        PASSWORD RECORD UPDATED IN PASSWORD DATA SET.                *
*        PASSWORD TTR UPDATED IN DSAB.                                *
*                                                                     *
* EXIT-NORMAL = IFG0195T - PASSWORD SUPPLIED WAS OK.                  *
*               READPSWD - PASSWORD WAS INCORRECT/ TRY COUNT NOT 2.   *
*               SECLOADA - PASSWORD SUPPLIED WAS OK.                  *
*                                                                     *
* EXIT-ERROR =  IFG0195T - PASSWORD SUPPLIED WAS INCORRECT.           *
*               SECLOADA - PASSWORD SUPPLIED WAS INCORRECT.           *
*                                                                     *
* EXTERNAL REFERENCES = SEE BELOW                                     *
*                                                                     *
*    ROUTINES = READPSWD.                                             *
*                                                                     *
*    DATA AREAS = IECDSECS SECCORE,MAIN,WTG,PREFX,RRPL                *
*                                                                     *
*    CONTROL BLOCK = CVT,ASCB,TCB,JSCB,PSCB,DSAB,UCB,UPT,DEB,DCB.     *
*                                                                     *
* TABLES = SEE XCTLTABL MACRO.                                        *
*                                                                     *
* MACROS = IECRES,EXCP,WAIT,ENQ,DEQ,MODESET,TPUT,                     *
*          XCTLTABL,IECDSECS,IECEQU.                                  *
*                                                                     *
* CHANGE ACTIVITY = NEW RELEASE (LEVEL 0)                             *
*                                                                     *
***********************************************************************
         EJECT
*
         USING FORCORE,RCORE            DEFINE BASE TO MAIN WORK AREA
         USING WTG,RWTG                 DEFINE BASE TO WTG TABLE
         USING SECCORE,RD               DEFINE BASE TO SECURITY WK AREA
*
         BALR  RBASE,R0                 ESTABLISH BASE REGISTER
         USING *,RBASE                  DEFINE BASE REGISTER
*
         B     PCR00000(RET)            BRANCH TO BRANCH TABLE   Y02134
*                                                                Y02134
PCR00000 EQU   *                        BRANCH TABLE             Y02134
*                                                                Y02134
         B     PCR01000                 (0) SEARCH PSWD DATA SET Y02134
         B     PCR02000                 (4) WRITE BACK PSWD REC  Y02134
         B     PCR09500                 (8) ISSUE ERROR MESSAGE  Y02134
         EJECT
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
*        SEARCH PASSWORD DATA SET FOR ENTRY                      Y02134
*        WITH MATCHING KEY - DSNAME + PASSWORD                   Y02134
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
PCR01000 EQU   *                        SEARCH FOR PASSWORD REC  Y02134
*                                                                Y02134
         XC    MYDAADDR,MYDAADDR        SET MBBCCHHR TO ZEROES   Y02134
         MVC   MYDAADDR+K3(K4),MYDEBSCC SET CCHH FROM START EXT  Y02134
*
*****************************************************************Y02134
*                                                                Y02134
*        CONSTRUCT CHANNEL PROGRAM TO SEARCH THE PASSWORD DATA SET
*        FOR DSNAME+PASSWORD AND READ THE DATA FROM THAT RECORD.
*
*****************************************************************Y02134
*                                                                Y02134
*        SEARCH ID EQ
*        TIC *-8
*        READ COUNT  MT
*        SEARCH KEY EQ
*        TIC *-16
*        READ DATA
*
*****************************************************************Y02134
*                                                                Y02134
         XC    MYCCW,MYCCW              CLEAR CHANNEL PROG AREA  Y02134
         LA    R1,MYDAADDR+K3           SEARCH ID ADDR           Y02134
         ST    R1,DXCCW1
         LA    R1,DXCCW1                TIC *-8
         ST    R1,DXCCW2
         LA    R1,DXCCW7                READ COUNT IN WORK AREA  Y02134
         ST    R1,DXCCW3
         LA    R1,MYDSN                 SEARCH KEY EQ ADDR       Y02134
         ST    R1,DXCCW4
         LA    R1,DXCCW3                TIC *-16
         ST    R1,DXCCW5
         LA    R1,MYLOGFLD              READ DATA AREA           Y02134
         ST    R1,DXCCW6
         OC    DXCCW1(K6*K8),PCRCCW01   CMND, FLAGS, AND LENGTH  Y02134
         EJECT
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
*        ESTABLISH ENQ/DEQ LIST IN SECURITY WORK AREA            Y02134
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
         TM    WTGPATHS,X01             TEST SCRATCH/RENAME      Y02134
         BO    PCR01100                 BRANCH TO BYPASS ENQ     Y02134
*                                                                Y02134
         MVC   MYSYSDSN,MYDSN           DSNAME TO ENQ AREA       Y02134
         L     R1,IECRRPRM-IECEXTPR(,RD) LOAD RECOVERY PARM LIST Y02134
         OI    RRFLAGS2-RRPLIST(R1),RRFENQPW PSWD ENQ IND        Y02134
*                                                                Y02134
         MVC   MYENQSW1(K12),ENQLIST    ENQ LIST TO WORK AREA    Y02134
*                                                                Y02134
         ENQ   (,MYSYSDSN),MF=(E,MYENQSW1) ENQ ON DSNAME         Y02134
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
*        READ PASSWORD ENTRY                                     Y02134
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
PCR01100 EQU   *                        READ IN RECORD           Y02134
*
         EXCP  MYIOB                    READ PASSWORD ENTRY      Y02134
*
         WAIT  ECB=MYECB                WAIT FOR READ COMPLETE   Y02134
*
         XC    MYREPLY,MYREPLY          HIDE PASSWORD            Y02134
*                                                                Y02134
         MVC   DXDAADDR+K3(K5),DXCCW7   PASSWORD CCHHR DA ADDR   Y02134
*                                                                Y02134
         NI    DCBIFLGS-IHADCB+MYDCB,ALLBITS-DCBIFPIO CLEAR ERR  YM8526
*                                                                YM8526
         TM    MYECB,ECBNOERR           TEST IF I/O ERROR        Y02134
         BNZ   PCR01200                 BRANCH IF NO I/O ERROR   YM1353
         EJECT
*                                                                YM1353
*****************************************************************YM1353
*                                                                YM1353
*        DEQ DSNAME FOR ERROR PROCESSING                         YM1353
*                                                                YM1353
*****************************************************************YM1353
*                                                                YM1353
         BAL   RET,PCR20000             LINK TO DEQ              YM1353
         LA    R0,K22                   LOAD ERROR CODE          YM1353
         B     PCR09000                 BRANCH, ISSUE ERROR MSG  YM1353
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
*        CHECK THAT THE MODE INDICATOR IN PASSWORD RECORD        Y02134
*        CORRESPONDS TO MODE OF USE OF THE DATA SET              Y02134
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
PCR01200 EQU   *                        TEST FOR PROPER MODE     YM1353
*                                                                YM1353
         TM    MYMODE,K1                IS MODE READ AND WRITE   Y02134
         BO    PCR02000                 BRANCH IF FULL ACCESS    Y02134
*                                                                Y02134
OUTPUT   EQU   X'0E'                    NOT INPUT OR RDBACK      Y02134
         TM    MYDEBSYS,OUTPUT          OPEN FOR INPUT OR RDBACK Y02134
         BZ    PCR02000                 BRANCH CONSISTENT MODE   YM1353
*                                                                YM1353
*****************************************************************YM1353
*                                                                YM1353
*        DEQ DSNAME FOR ERROR PROCESSING                         YM1353
*                                                                YM1353
*****************************************************************YM1353
*                                                                YM1353
         BAL   RET,PCR20000             LINK TO DEQ              YM1353
         LA    R0,K22                   LOAD ERROR CODE          YM1353
         B     PCR09100                 BRANCH, ISSUE ERROR MSG  YM1353
         EJECT
*
***********************************************************************
*
*        UPDATE PASSWORD USE COUNT AND WRITE BACK THE RECORD     Y02134
*
*****************************************************************Y02134
*                                                                Y02134
PCR02000 EQU   *                        UPDATE RECORD            Y02134
*                                                                Y02134
         LA    R1,K1                    GET INCREMENTING FACTOR  Y02134
         AH    R1,MYCNTR                INCREMENT USE CNT BY ONE Y02134
         STH   R1,MYCNTR                STORE IT BACK            Y02134
*
         XC    MYCCW,MYCCW              CLEAR CHANNEL PROG AREA  Y02134
         LA    R1,MYDAADDR+K3           SEARCH ID ADDR           Y02134
         ST    R1,DXCCW1                                         Y02134
         LA    R1,DXCCW1                TIC *-8                  Y02134
         ST    R1,DXCCW2                                         Y02134
         LA    R1,MYLOGFLD              POINT TO UPDATE RECORD   Y02134
         ST    R1,DXCCW3                STORE UPDATED RECORD PTR Y02134
         OC    DXCCW1(K3*K8),PCRCCW02   CMDS, FLAGS, LENGTHS     Y02134
*
*****************************************************************Y02134
*                                                                Y02134
*        WRITE THE UPDATED PASSWORD ENTRY BACK
*
*****************************************************************Y02134
*                                                                Y02134
         EXCP  MYIOB                    WRITE BACK DATA          Y02134
*
         WAIT  ECB=DXECB                WAIT FOR I/O TO COMPLETE Y02134
*                                                                YM1353
         BAL   RET,PCR20000             LINK TO DEQ              YM1353
*                                                                YM1353
         NI    DCBIFLGS-IHADCB+MYDCB,ALLBITS-DCBIFPIO CLEAR ERR  YM8526
         LA    R0,K22                   LOAD ERROR CODE          Y02134
         TM    MYECB,ECBNOERR           TEST IF I/O ERROR        Y02134
         BZ    PCR09500                 BR IF YES                Y02134
         EJECT
*
***********************************************************************
*
*        UPDATE THE TTR OF THE PASSWORD RECORD IN THE DSAB       Y02134
*
*        CONVERT MBBCCHHR DISK ADDRESS TO TTR
*
*****************************************************************Y02134
*                                                                Y02134
         STM   R1,RF,MYPREFIX           SAVE REGS                Y02134
         L     RF,CVTPTR                GET CVT ADDR
         LA    R1,MYDEB                 POINT TO SECURITY WORK DEB
         LA    R2,MYDAADDR              POINT TO MBBCCHHR FIELD  Y02134
         L     RF,CVTPRLTV-CVT(,RF)     GET ADDR OF IECPRLTV ROUTINE
         BALR  RET,RF                   BR TO CONVERT MBBCCHHR TO TTR
*                                                                Y02134
         L     RD,WTGPREFX              LOAD PREFIX POINTER      Y02134
         LA    RD,IECSTART-IECPREFX(,RD) POINT TO EXT PREFIX     Y02134
         LM    R1,RF,MYPREFIX           RESTORE REGS             Y02134
*                                                                Y02134
         L     R1,MYDSAB                LOAD DSAB ADDRESS        Y02134
*                                                                Y02134
         MODESET EXTKEY=SCHED           SCHEDULER KEY            Y02134
*                                                                Y02134
         ST    R0,DSABPTTR-DSAB(,R1)    PUT PSWD TTR IN DSAB     Y02080
*                                                                Y02134
         MODESET EXTKEY=DATAMGT         RESET TO DM KEY          Y02134
*                                                                Y02134
         B     PCR09950                 RETURN TO CALLER         Y02134
         EJECT
*
***********************************************************************
*
*        INVALID PASSWORD                                        Y02134
*                                                                Y02134
*****************************************************************Y02134
*
PCR09000 EQU   *                        INVALID PASSWORD MSG     Y02134
*
         CLI   MYCODE2,K0               CHECK IF USER ERROR    @ZA02896
         BE    PCR09900                 BRANCH IF NOT USER ERR @ZA02896
*
         LA    R1,PCRMSG01              INVALID PASSWORD         Y02134
         BAL   RET,PCR10000             LINK TO ISSUE MESSAGE    Y02134
         B     PCR09900                 BRANCH TO READPSWD       Y02134
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
*        PASSWORD=READ ONLY AND OUTPUT REQUESTED                 Y02134
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
PCR09100 EQU   *                        INCORRECT MODE MSG       Y02134
*                                                                Y02134
         CLI   MYCODE2,K0               CHECK IF USER ERROR    @ZA02896
         BE    PCR09900                 BRANCH IF NOT USER ERR @ZA02896
*
         LA    R1,PCRMSG02              PASSWORD=READ ONLY       Y02134
         BAL   RET,PCR10000             LINK TO ISSUE MESSAGE    Y02134
         B     PCR09900                 BRANCH TO READPSWD       Y02134
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
*        I/O ERROR ACCESSING PASSWORD DATA SET                   Y02134
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
PCR09500 EQU   *                        I/O ERROR MSG            Y02134
*                                                                Y02134
         LA    R1,PCRMSG01              INVALID PASSWORD/I/O ERR Y02134
         BAL   RET,PCR10000             LINK TO ISSUE MESSAGE    Y02134
         B     PCR09950                 BRANCH TO RETURN ERROR   Y02134
         EJECT
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
*        GIVE OPERATOR TWO TRIES BEFORE RETURNING ERROR          Y02134
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
PCR09900 EQU   *                        CHECK TRY COUNT          Y02134
*                                                                Y02134
         CLI   MYCODE2,K2               ALREADY TWO TRIES        Y02134
         BE    PCR09940                 BRANCH IF ALREADY TWO    Y02134
*                                                                Y02134
         IECRES LOAD,MODNM=READPSWD,BRCODE=K0,                   Y02134X
               PREFIX=WTGPREFX,BRANCH=DIRECT                     Y02134
*                                                                Y02134
PCR09940 EQU   *                        RETURN ERROR CODE        Y02134
*                                                                Y02134
         STC   R0,MYERRCOD              STORE ERROR CODE         Y02134
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
*        RETURN TO CALLER                                        Y02134
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
PCR09950 EQU   *                        RETURN                   Y02134
*                                                                Y02134
         XR    RET,RET                  CLEAR RETURN OFFSET      Y02134
         IC    RET,MYRETCOD             GET RETURN OFFSET        Y02134
*                                                                Y02134
         IECRES LOAD,MODNM=MYRETMOD,BRCODE=(14),                 Y02134X
               PREFIX=WTGPREFX,BRANCH=DIRECT                     Y02134
         EJECT
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
*        SEND MESSAGE TO TSO TERMINAL ONLY                       Y02134
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
PCR10000 EQU   *                        MESSAGE SUBROUTINE       Y02134
*                                                                Y02134
         STM   R0,RF,MYPREFIX           SAVE REGS                Y02134
*                                                                Y02134
         L     RF,MYASCB                LOAD CURRENT ASCB ADDR   Y02134
         L     RF,ASCBTSB-ASCB(,RF)     LOAD TSB ADDRESS         Y02134
         LTR   RF,RF                    TEST FOR TERMINAL TASK   Y02134
         BZ    PCR12000                 BRANCH IF NOT            Y02134
*                                                                Y02134
         LH    RC,K0(R1)                GET LENGTH OF MESSAGE    Y02134
         STCM  RC,B'0011',MYMCSFLG      SAVE LENGTH              Y02134
         BCTR  RC,R0                    DECREMENT FOR EX INST    Y02134
         EX    RC,MOVE                  MVC  MYMIEC(0),2(1)      Y02134
         LA    R1,MYMIEC                POINT TO TSO MSG         Y02134
*                                                                Y02134
         L     RF,MYJSCB                GET JSCB ADDRESS         Y02134
         MVC   MYMSIZE(L'JSCBTJID),JSCBTJID-IEZJSCB(RF) ID       Y02134
         L     RF,JSCBPSCB-IEZJSCB(,RF) GET THE PSCB ADDRESS     Y02134
         LTR   RF,RF                    PSCB INITIALIZED         Y02134
         BZ    PCR11000                 DEFAULT TPUT W/MSG ID    Y02134
*                                                                Y02134
         L     RF,PSCBUPT-PSCB(,RF)     GET UPT ADDRESS          Y02134
         MODESET EXTKEY=ZERO            SWITCH TO KEY ZERO       YM6725
         TM    UPTSWS-UPT(RF),UPTMID    IS MESSAGE ID REQUIRED   Y02134
         MODESET EXTKEY=DATAMGT         RETURN TO DATAMGT KEY    YM6725
         BO    PCR11000                 BRANCH IF REQUIRED       Y02134
*                                                                Y02134
         LH    RC,MYMCSFLG              LOAD MESSAGE LENGTH      Y02134
         LA    R0,L'MYMIEC              LOAD ID LENGTH           Y02134
         SR    RC,R0                    GET NEW MESSAGE LENGTH   Y02134
         STCM  RC,B'0011',MYMCSFLG      SAVE LENGTH              Y02134
         LA    R1,MYMNOID               POINT PASSED MSG ID      Y02134
*                                                                Y02134
PCR11000 EQU   *                        ISSUE MESSAGE            Y02134
*                                                                Y02134
         ICM   R0,B'1111',MYMSIZE       LOAD ID AND LENGTH       Y02134
         TPUT  (1),(0),R                SEND MSG TO TERMINAL     Y02134
*                                                                Y02134
PCR12000 EQU   *                        RETURN                   Y02134
*                                                                Y02134
         LM    R0,RF,MYPREFIX           RESTORE REGISTERS        Y02134
         BR    RET                      RETURN                   Y02134
MOVE     MVC   MYMIEC(K0),K2(R1)        MOVE MESSAGE TO BUFFER   Y02134
         EJECT
*                                                                YM1353
*****************************************************************YM1353
*                                                                YM1353
*        DEQ PASSWORD ENTRY                                      YM1353
*                                                                YM1353
*****************************************************************YM1353
*                                                                YM1353
PCR20000 EQU   *                        DEQ SUBROUTINE           YM1353
*                                                                YM1353
         TM    WTGPATHS,X01             TEST SCRATCH/RENAME      YM1353
         BCR   K1,RET                   BRANCH TO BYPASS DEQ     YM1353
*                                                                YM1353
         DEQ   MF=(E,MYENQSW1)          DEQ PREVIOUS ENQ ON DSN  YM1353
*                                                                YM1353
         L     R1,IECRRPRM-IECEXTPR(,RD) LOAD RECOVERY PARM LIST YM1353
         NI    RRFLAGS2-RRPLIST(R1),ALLBITS-RRFENQPW ENQ IND OFF YM1353
*                                                                YM1353
         BR    RET                      RETURN                   YM1353
*                                                                YM1353
         EJECT
*
***********************************************************************
*
*        CONSTANTS
*
*****************************************************************Y02134
*                                                                Y02134
*        CCWS TO SEARCH PASSWORD DATA SET FOR PASSWORD RECORD    Y02134
*                                                                Y02134
PCRCCW01 CCW   CCWSCHID,K0,CCWCMDCH,K5  SEARCH ID EQUAL          Y02134
         CCW   CCWTIC,K0,K0,K0          TIC *-8                  Y02134
         CCW   CCWMTRDC,K0,CCWCMDCH,K8  READ COUNT  MT           Y02134
         CCW   CCWSCHKE,K0,CCWCMDCH,L'MYDSN+L'MYREPLY SCH KEY EQ Y02134
         CCW   CCWTIC,K0,K0,K0          TIC *-16                 Y02134
         CCW   CCWRDDA,K0,CCWSILI,L'MYLOGFLD READ DATA           Y02134
*                                                                Y02134
*        CCWS TO UPDATE THE PASSWORD RECORD IN PASSWORD DATA SET Y02134
*                                                                Y02134
PCRCCW02 CCW   CCWSCHID,K0,CCWCMDCH,K5  SEARCH ID EQUAL          Y02134
         CCW   CCWTIC,K0,K0,K0          TIC *-8                  Y02134
         CCW   CCWWRTDA,K0,CCWSILI,L'MYLOGFLD WRITE DATA-UPDATE  Y02134
*
ENQLIST  ENQ   (SYSZPSWD,,E,L'JFCBDSNM,SYSTEM),MF=L              Y02134
SYSZPSWD DC    C'SYSZPSWD'              MAJOR ENQ/DEQ NAME       Y02134
*                                                                Y02134
PCRMSG01 DC    AL2(PCRMSG1E-*-2)        LENGTH OF MESSAGE 1      Y02134
OSQ2870K DC    C'IEC117I DATA SET CANNOT BE USED - PASSWORD INVALID' H
PCRMSG1E EQU   *                        END OF MESSAGE 1         Y02134
PCRMSG02 DC    AL2(PCRMSG2E-*-2)        LENGTH OF MESSAGE 2      Y02134
OSQ2880K DC    C'IEC118I READ ONLY DATA SET CANNOT BE OPENED FOR OUTPUTX
               '
PCRMSG2E EQU   *                        END OF MESSAGE 2         Y02134
*
         XCTLTABL ID=(,READPSWD),BRT=YES,LENGTH=                 Y02080
*
         IECDSECS CVT,ASCB,TCB,JSCB,PSCB,UPT,UCB,DSAB,           Y02134X
               DCB,DEB,MAIN,SECCORE,WTG,PREFX,RRPL,EXPAND=YES    Y02134
         IECEQU ,                       DEFINE EQUATES
         END
