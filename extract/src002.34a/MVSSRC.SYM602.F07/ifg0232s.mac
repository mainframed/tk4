         TITLE 'IFG0232S - TCLOSE TAPE POSITIONING FUNCTION'
IFG0232S CSECT
*
***********************************************************************
*                                                                     *
*          VS1 RELEASE 2.6 DELETIONS/CHANGES                          *
*0000852000                                                     XA01996
*          VS2 RELEASE 02 DELETIONS/CHANGES                           *
*0000136000,430000-438000,962000,972000                          YM1272
*          RELEASE 21.7 DELETIONS/CHANGES                             *
*0000412000                                                     SA57250
*0000392000,444000,892000                                       SA53865
*          RELEASE 21 DELETIONS/CHANGES                               *
*0000550000                                                      M0011
*                                                                     *
* MODULE NAME - IFG0232S                                              *
*                                                                     *
* DESCRIPTIVE NAME - TCLOSE TAPE POSITIONING                          *
*                                                                     *
* COPYRIGHT - NONE                                                    *
*                                                                     *
* CHANGE ACTIVITY - SEE DELETIONS/CHANGES FOLLOWING THE CSECT CARD.   *
*                                                                     *
* STATUS CHANGE LEVEL 000                                             *
*                                                                     *
* FUNCTION -                                                          *
*    THIS MODULE CONTAINS THE 'TCLOSE TAPE POSITIONING FUNCTION'.     *
*    REFER TO THE FOLLOWING 'FUNCTION PROLOG' FOR DETAILS.            *
*                                                                     *
* ENTRY POINTS -                                                      *
*         IFG0232S                                                    *
*                                                                     *
* INPUT -                                                             *
*    ENTERED IN DATA MANAGEMENT KEY                              Y02082
*    REGISTER 14 ('RET') WILL CONTAIN ONE OF THE BRANCH TABLE OFFSETS *
*    LISTED BELOW, THE BRANCH TABLE BEING USED TO DETERMINE AT WHICH  *
*    POINT PROCESSING IS TO BEGIN -                                   *
*       0 - NORMAL ENTRY TO EITHER WRITE A TAPE MARK TO DELIMIT       *
*           THE DATA SET AND THEN TO REPOSITION IT (OUTPUT) OR        *
*           JUST TO REPOSITION IT (INPUT).                            *
*    REFER TO THE FOLLOWING 'FUNCTION PROLOG' FOR ADDITIONAL INPUT.   *
*                                                                     *
* OUTPUT -                                                            *
*    EXIT IN DATA MANAGEMENT KEY                                 Y02082
*    REFER TO THE FOLLOWING 'FUNCTION PROLOG'.                        *
*                                                                     *
* EXTERNAL REFERENCES -                                               *
*         IFG019RA - O/C/EOV/DADSM COMMON FUNCTION ROUTINE TO WAIT    *
*                    AND BRANCH.                                      *
*                                                                     *
* EXITS, NORMAL -                                                     *
*    REFER TO THE FOLLOWING 'FUNCTION PROLOG'.                        *
*                                                                     *
* EXITS, ERROR -                                                      *
*         IFG0230P - WHEN AN ERROR CONDITION OCCURS IN THIS MODULE.   *
*                    REFER TO THE FOLLOWING 'FUNCTION PROLOG'.        *
*                                                                     *
* TABLES/WORK AREAS -                                                 *
*    REFER TO THE FOLLOWING 'FUNCTION PROLOG'.                        *
*                                                                     *
* ATTRIBUTES -                                                        *
*    REENTRANT, REFRESHABLE, READ-ONLY, ENABLED, PRIVILEGED           *
*                                                                     *
* CHARACTER CODE DEPENDENCY -                                         *
*    CLASS ONE CHARACTER CODE DEPENDENCY - THE EBCDIC CHARACTER CODE  *
*    WAS USED FOR ASSEMBLY.  THE MODULE MUST BE REASSEMBLED IF A      *
*    DIFFERENT CHARACTER SET IS USED FOR EXECUTION.                   *
*                                                                     *
* NOTES -                                                             *
*    REFER TO THE FOLLOWING 'FUNCTION PROLOG'.                        *
*                                                                     *
***********************************************************************
*
         BALR  RBASE,0                  ESTABLISH ADDRESSABILITY
         USING *,RBASE
         USING FORCORE,RCORE
         USING IHADCB,RDCB
         USING UCB,RUCB
         USING DEBBASIC,RDEB            ADDRESSABILITY TO DEB    YM1272
         USING WTG,RWTG
*
         B     TCT14000(RET)            DETERMINE TYPE OF ENTRY TO MOD
TCT14000 EQU   *
         B     TCT14200                 BR TO COMPLETE TAPE PROCESSING
         EJECT
         SPACE 1
***********************************************************************
*                                                                     *
*                          FUNCTION PROLOG                            *
*                                                                     *
***********************************************************************
*                                                                     *
* FUNCTION NAME - 'TCLOSE TAPE POSITIONING FUNCTION'                  *
*                                                                     *
* (STATUS) -                                                          *
*    NOT APPLICABLE                                                   *
*                                                                     *
* FUNCTION -                                                          *
*    1. FOR OUTPUT DATA SETS, TWO TAPE MARKS ARE WRITTEN TO DELIMIT   *
*       THE DATA SET FOLLOWING THE DATA SET LABELS (STANDARD LABEL    *
*       TAPE) OR FOLLOWING THE DATA (NO-LABEL TAPE). IF A 'TAPE       *
*       INDICATE' (I.E., END-OF-TAPE) CONDITION IS DETECTED WHEN      *
*       THE FIRST TAPE MARK IS WRITTEN FOR A NO-LABEL TAPE DATA SET   *
*       WITH A POSITIONING OPTION OF 'LEAVE', THE FORCE END-OF-VOLUME *
*       (FEOV) MACRO IS ISSUED TO CONTINUE THE DATA SET ON ANOTHER    *
*       VOLUME, AND PROCESSING CONTINUES.                             *
*       THE USER'S DCB ADDRESS IS PLACED BACK IN THE DEB AND THE Y02082
*       CALLER'S PROTECT KEY IS ASSUMED BEFORE THE FEOV IS       Y02082
*       ISSUED. UPON RETURN THE DEB IS VERIFIED USING THE DEBCHK Y02082
*       MACRO, THE DEB IS POINTED TO THE PROTECTED COPY OF THE   Y02082
*       DCB, AND THE COPIED DCB IS POINTED TO THE NEW DEB.       Y02082
*    2. THE DATA SET IS REPOSITIONED ACCORDING TO THE OPTION          *
*       SPECIFIED IN THE TCLOSE MACRO- IF 'LEAVE' WAS SPECIFIED,      *
*       THE DATA SET IS POSITIONED TO ITS 'LOGICAL END'; IF 'REREAD'  *
*       WAS SPECIFIED, TO ITS 'LOGICAL BEGINNING'.                    *
*       A. THE 'LOGICAL BEGINNING' OF A DATA SET OPENED FOR OUTPUT OR *
*          READ FORWARD IS THE INTERBLOCK GAP PRECEDING THE FIRST     *
*          DATA RECORD; THE 'LOGICAL BEGINNING' OF A DATA SET OPENED  *
*          FOR 'RDBACK' IS THE INTERBLOCK GAP FOLLOWING THE LAST DATA *
*          RECORD.                                                    *
*       B. THE 'LOGICAL END' OF A DATA SET OPENED FOR OUTPUT OR READ  *
*          FORWARD IS THE INTERBLOCK GAP FOLLOWING THE LAST DATA      *
*          RECORD; THE 'LOGICAL END' OF A DATA SET OPENED FOR 'RDBACK'*
*          IS THE INTERBLOCK GAP PRECEDING THE FIRST DATA RECORD.     *
*    3. THE BLOCK COUNT IN THE DCB IS UPDATED TO REFLECT THE POSITION *
*       OF THE TAPE. IF THE TAPE IS POSITIONED AT ITS PHYSICAL        *
*       BEGINNING, THE BLOCK COUNT IS SET TO ZERO. IF THE TAPE IS     *
*       POSITIONED AT ITS PHYSICAL END, THEN THE BLOCK COUNT CAN ONLY *
*       BE UPDATED FOR DATA SETS ON STANDARD LABEL TAPE. THIS IS      *
*       DONE BY READING TRAILER LABEL 1, CONVERTING THE BLOCK COUNT   *
*       THEREIN TO BINARY, AND STORING IT IN THE DCB.                 *
*                                                                     *
* ENTRY POINTS -                                                      *
*    REFER TO THE PRECEDING MODULE PROLOG.                            *
*                                                                     *
* INPUT -                                                             *
*    1. REGISTER CONTENTS AT ENTRY ARE AS FOLLOWS-                    *
*       'RDCB' - ADDRESS OF THE DCB BEING PROCESSED;                  *
*       'RCORE'- ADDRESS OF THE WORK AREA FOR THAT DCB;               *
*       'RES'  - ADDRESS OF THE OPEN/CLOSE/EOV RESIDENT ROUTINE;      *
*       'RWTG' - ADDRESS OF THE WHERE-TO-GO TABLE;                    *
*       'RPARC'- ADDRESS OF THE CURRENT TCLOSE PARAMETER LIST ENTRY;  *
*       'RWTGC'- ADDRESS OF THE CURRENT WHERE-TO-GO TABLE ENTRY;      *
*       'RTIOT'- ADDRESS OF THE CURRENT TIOT ENTRY FOR THE DCB;       *
*       'RUCB' - ADDRESS OF THE CURRENT UCB FOR THE DCB;              *
*       'RDEB' - ADDRESS OF THE DCB'S DEB;                            *
*    2. WORK AREA INDICATORS-                                    M0011*
*       'DXCCW7' - X'FF' - INDICATES THAT DEFERRED USER LABEL    M0011*
*                          PROCESSING OCCURRED.                  M0011*
*                                                                     *
* OUTPUT -                                                            *
*    1. AT EXIT TO MODULE IFG0232Z-                                   *
*       REGISTER CONTENTS ARE THE SAME AS AT ENTRY EXCEPT THAT        *
*          REGISTER 'RET' CONTAINS A BRANCH TABLE OFFSET OF '4';      *
*       CONTROL BLOCK FIELDS-                                         *
*          'DCBBLKCT' - UPDATED AS SPECIFIED IN (3) ABOVE;            *
*       TAPE POSITION- AS SPECIFIED IN (2) ABOVE.                     *
*    2. AT EXIT TO PROBLEM DETERMINATION MODULE IFG0230P-             *
*       REGISTER CONTENTS ARE THE SAME AS AT ENTRY EXCEPT THAT        *
*          REGISTER 'R0' CONTAINS ONE OF THE FOLLOWING INTERNAL       *
*          ERROR CODES-                                               *
*          125 (717/0C)- ERROR WRITING 1ST TAPE MARK AFTER DATA SET;  *
*          126 (717/0C)- ERROR WRITING 2ND TAPE MARK AFTER DATA SET;  *
*          127 (117/10)- ERROR BACKSPACING PAST SECOND TAPE MARK;     *
*          128 (117/10)- ERROR BACKSPACING PAST 1ST TAPE MARK, WHICH  *
*              FOLLOWS THE DATA SET LABELS (STANDARD LABEL TAPE)      *
*              OR THE DATA (NO-LABEL TAPE);                           *
*          129 (117/18)- ERROR FORWARD SPACING PAST TAPE MARK         *
*              PRECEDING DATA;                                        *
*          130 (117/1C)- ERROR BACKSPACING PAST TAPE MARK             *
*              PRECEDING DATA;                                        *
*          134 (117/20)- ERROR BACKSPACING PAST TRAILER LABELS;       *
*          135 (117/24)- ERROR FORWARD SPACING PAST DATA;             *
*          136 (117/28)- ERROR BACKSPACING PAST TAPE MARK FOLLOWING   *
*              DATA;                                                  *
*          137 (117/14)- ERROR REWINDING OR BACKSPACING PAST DATA;    *
*          138 (717/10)- ERROR READING TRAILER LABEL 1 IN ORDER TO    *
*              UPDATE THE BLOCK COUNT IN THE DCB;                     *
*          139 (117/08)- ERROR FORWARD SPACING PAST TAPE MARK         *
*              PRECEDING DATA;                                        *
*          140 (117/2C)- ERROR BACKSPACING PAST TAPE MARK FOLLOWING   *
*              DATA WHEN FEOV IS TO BE ISSUED.                        *
*                                                                     *
* EXTERNAL REFERENCES -                                               *
*    REFER TO THE PRECEDING MODULE PROLOG.                            *
*                                                                     *
* EXITS, NORMAL -                                                     *
*         IFG0232Z- TO PERFORM TCLOSE FINAL PROCESSING.               *
*                                                                     *
* EXITS, ERROR -                                                      *
*    REFER TO THE PRECEDING MODULE PROLOG.                            *
*                                                                     *
* TABLES/WORK AREAS -                                                 *
*    THE TCLOSE WORK AREA AND THE WHERE-TO-GO (WTG)                   *
*    TABLE ARE DESCRIBED BY THE DSECTS AT THE END OF THE LISTING.     *
*                                                                     *
* ATTRIBUTES -                                                        *
*    REFER TO THE PRECEDING MODULE PROLOG.                            *
*                                                                     *
* CHARACTER CODE DEPENDENCY -                                         *
*    REFER TO THE PRECEDING MODULE PROLOG.                            *
*                                                                     *
* NOTES -                                                             *
*                                                                     *
***********************************************************************
*
TCT14200 EQU   *
         TM    DCBOFLGS,DCBOWRIT        WAS OUTPUT PERFORMED-
         BNO   TCT15200                 ..BR IF NOT TO POSITION TAPE
*
*****          WRITE TWO TAPE MARKS AND BACKSPACE OVER LAST
*****         ( FOR LABELED TAPE, TAPE MARKS FOLLOW LABELS )
*****         ( FOR UNLABELED TAPE, TAPE MARKS FOLLOW DATA )
*
TCT14400 EQU   *
         MVC   DXCCW1,AWTMCCWM          INITLZ CCW TO WRITE TAPE MARK
         MVI   DXCCW1+K4,CCWCMDCH+CCWSILI  CHAIN A NOP, SET SILI BIT ON
         MVC   DXCCW2,AWTMCCWM          INITLZ SECOND CCW
         MVI   DXCCW2,CCWNOP            MAKE SECOND CCW A NOP
         LA    RD,TABD125               LOAD INTERNAL ERROR CODE
         OI    DXATALL,DXATFC           FORCE CLOSE ON ERROR     YM7099
         BAL   RC,TCT20500              WRITE TM AFTER TRAILER  SA53865
*                                       LABEL (SL) OR AFTER DATA (NL)
         TM    IOBSTAT0,CSWUNITX        IS 'TAPE INDICATE' ON-
         BNO   TCT14800                 ..BR IF NOT TO CONTINUE
         TM    SRTESTAT,UCBDADI         IS THE TAPE LABELED-
         BO    TCT14800                 ..BR IF YES- THIS CONDITION
*                                       ..HAS BEEN TAKEN CARE OF FOR SL
         TM    SRTESTAB,UCBBSTR         IS THE TAPE LABELED AL-  YM7099
         BO    TCT14800                 ..BR YES- THIS CONDITION YM7099
*                                       ..ALREADY HANDLED FOR AL YM7099
         TM    DEBOFLGS,DEBOFRLS        IS THIS AN EMULATION TAPE-
         BO    TCT14800                 ..BRANCH IF YES
         TM    PLISTOPT(RPARC),PLISTLV  WAS 'LEAVE' SPECIFIED-
         BM    TCT14800                 ..BRANCH IF REREAD      SA57250
*
*****          ISSUE END-OF-VOLUME- USER HAS RUN OUT OF SPACE
*****          AND MAY WISH TO CONTINUE WRITING DATA
*
TCT14600 EQU   *
         MVI   DXCCW1,CCWBSF            INITLZ CCW FOR BACKSPACE CMD
         LA    RD,TABD140               'TCLOSE' INTERNAL ERROR CODE
         BAL   RC,TCT20400              BACKSPACE PAST TAPE MARK
         NI    DXATALL,X'FF'-DXATFC     RESET FORCE CLOSE INDIC  YM7099
*
         IECRES INIT,DCBCOPY=FRWKAR,STM=(0,14,WTGPREFX)          YM1272*
                                        UPDATE USER'S COPY       YM1272
         MVC   DEBDCBAD+K1(L'DEBDCBAD-K1),DXUDCBAD+K1  POINT DEB YM1272*
                                        TO USER'S DCB FOR FEOV   YM1272
         LR    R1,RDEB                  GET DEB ADDRESS          YM1272
         LA    RF,DEBBASIC-DEBXTNP      NEGATIVE OFFSET TO DEB   YM1272
*                                       EXTENSION ADDRESS        YM1272
         SR    R1,RF                    POINT R1 TO DEB PREFIX   YM1272
         L     RF,DEBXTNP-DEBXTNP(,R1)  LOAD DEB EXTENSION ADDR  YM1272
         NI    DEBXFLG1-DEBXTN(RF),X'FF'-DEBXCDCB  DEBDCBAD      YM1272
*                                       POINTS TO THE USER'S DCB YM1272
*
         MODESET KEYADDR=DXUKEY,WORKREG=1  GET IN USER KEY       YM1272
*
         L     R1,DXUDCBAD              GET USER'S DCB AGAIN     YM1272
         FEOV  (1)                      ISSUE FEOV FOR NEXT VOL  YM1272
*
         MODESET EXTKEY=DATAMGT         RETURN TO DM KEY         YM1272
         IECRES INIT,DCBCOPY=TOWKAR,STM=(0,14,WTGPREFX)          YM1272X
                                        UPDATE THE COPIED DCB    YM1272
         L     R1,DXUDCBAD              GET PTR TO USER'S DCB    YM1272
         DEBCHK (1)                     CHECK DEB                YM1272
*
         LR    RDEB,R1                  GET VERIFIED DEB POINTER YM1272
         STCM  RDEB,B'0111',DCBDEBAD+K1  UPDATE DCB COPY'S DEBAD YM1272
         MVC   DEBDCBAD+K1(L'DEBDCBAD-K1),DXPDCBAD+K1  POINT     YM1272*
                                        DEB TO COPIED DCB        YM1272
         LA    RF,DEBBASIC-DEBXTNP      NEGATIVE OFFSET TO DEB   YM1272
*                                       EXTENSION ADDRESS        YM1272
         SR    R1,RF                    POINT R1 TO DEB PREFIX   YM1272
         L     RF,DEBXTNP-DEBXTNP(,R1)  LOAD DEB EXTENSION ADDR  YM1272
         OI    DEBXFLG1-DEBXTN(RF),DEBXCDCB  INDICATE DEBDCBAD   YM1272
*                                       POINTS TO THE COPIED DCB YM1272
*
*        PURGE ANY OUTSTANDING I/O (THERE SHOULD BE NONE)        YM1272
*
         LA    R1,DXCCW4                BUILD PURGE PARM LIST    YM1272
         USING PURGPRM,R1               PARM LIST ADDRESSABILITY YM1272
         ST    RDEB,PURGDEB             STORE DEB TO BE PURGED   YM1272
         MVI   PURGOPT,B'10100000'      SET OPTION BITS          YM1272
         XC    PURGECB,PURGECB          CLEAR COMPLETION CODE    YM1272
         LA    R0,DEBUSRPG              SET POINTER TO IOB CHAIN YM1272
         ST    R0,PURGCHN                                        YM1272
         XC    PURGMVM,PURGMVM          CLEAR 4TH WORD OF LIST   YM1272
         PURGE (1)                      PURGE ALL IOBS, THIS DEB YM1272
*
         LA    R1,DXCCW4                ADDRESS PURGE PARM LIST  YM1272
         LA    R1,PURGECB-PURGPRM(R1)   POINT TO PURGE ECB       YM1272
         WAIT  ECB=(1)                  WAIT TILL PURGE COMPLETE YM1272
*
         DROP  R1                                                YM1272
         B     TCT14400                 BR TO PERFORM TRAILER LABEL
*                                       ..PROCESSING ON NEW VOLUME
TCT14800 EQU   *
         OI    DXATOUTA,DXATTTM1        SHOW 1 OF 2 TM'S WRITTEN Y02144
         LA    RD,TABD126               LOAD INTERNAL ERROR CODE
         BAL   RC,TCT20500              WRITE SECOND TAPE MARK  SA53865
         OI    DXATOUTA,DXATTTM2        SHOW 2 OF 2 TM'S WRITTEN Y02144
TCT15000 EQU   *
         MVC   DXCCW1,ABSFCCW           INITLZ CCW FOR BSF
         LA    RD,TABD127               LOAD INTERNAL ERROR CODE
         BAL   RC,TCT20400              BACKSPACE PAST LAST TM
         B     TCT16000                 BRANCH TO POSITION TAPE
TCT15200 EQU   *
         TM    DEBOFLGS,DEBOFEOF        HAS AN EOF BEEN READ BY BSAM-
         L     RET,DCBIOBA              LOAD IOB ADDR
         LA    RET,K0(,RET)             ZERO HIGH ORDER BYTE
         BNO   TCT15400                 ..BR IF EOF NOT READ
         NI    DCBIFLGS,X'FF'-DCBIFPIO  RESET ERROR INDICATION CAUSED
*                                       ..BY READING TAPE MARK
TCT15400 EQU   *
         LR    RD,RET                   SAVE IOB ADDRESS
TCT15600 EQU   *
         MODESET KEYADDR=DXUKEY,WORKREG=12 IOB KEY               Y02082
         TM    K20(RD),K1               DOES THIS IOB INDICATE THAT
*                                       ..A TAPE MARK WAS READ-
         BNO   TCT15800                 ..BRANCH IF NOT
         NI    K20(RD),X'FF'-K1         ..ELSE RESET IOB TAPE MARK IND
         MODESET EXTKEY=DATAMGT         DCB COPY KEY             Y02082
         OI    DCBOFLGS,DCBOEOF         TURN ON TAPE MARK IND IN DCB
         MODESET KEYADDR=DXUKEY,WORKREG=12 IOB KEY               Y02082
TCT15800 EQU   *
         L     RD,K0(,RD)               LOAD NEXT IOB ADDRESS
         LA    RD,K0(,RD)               ZERO HIGH ORDER BYTE
         CR    RD,RET                   IS THIS THE LAST IOB-
         BNE   TCT15600                 ..BRANCH IF NOT
         MODESET EXTKEY=DATAMGT         DCB COPY KEY             Y02082
*
***********************************************************************
*              TAPE POSITIONING                                       *
***********************************************************************
*
TCT16000 EQU   *
         MVC   DXCCW1,ABSFCCW           INITLZ CCW FOR POSITIONING
         TM    JFCBLTYP,JFCBAL+JFCSL    IS AL OR SL SPECIFIED-
         BZ    TCT16800                 ..BR IF NOT TO POS NL TAPE
*
*****          POSITIONING FOR LABELED TAPE
*
*                        READ FORWARD (EODS) AND OUTPUT
*                             LEAVE     BSF(2)
*                             REREAD    BSF(3) + FSF
*                        READ FORWARD (NOT EODS)
*                             LEAVE     FSF + BSF
*                             REREAD    BSF + FSF
*                        READ BACKWARD (EODS)
*                             LEAVE     FSF (IF NOT BOT) + FSF
*                             REREAD    FSF (IF NOT BOT) + FSF(2) + BSF
*                        READ BACKWARD (NOT EODS)
*                             LEAVE     BSF + FSF
*                             REREAD    FSF + BSF
TCT16200 EQU   *
         TM    DCBOFLGS,DCBORDBK        IS DCB OPENED FOR 'RDBACK'-
         BO    TCT16400                 ..BR IF YES TO CHK POSITIONING
         TM    DEBOFLGS,DEBOFEOF        WAS OUTPUT PERFORMED OR  M0011
*                                       ..EOF READ-              M0011
         BNO   TCT17800                 ..BR IF NOT- BALANCE OF POS
*                                       ..IS LIKE NO-LABEL TAPE
         TM    DXCCW7,ALLBITS           WAS THERE DEFERRED USER  M0011
*                                       ..LABEL PROCESSING       M0011
         BO    TCT17600                 ..BR IF YES- ALREADY     M0011
*                                       ..POSITIONED BEHIND TAPE M0011
         LA    RD,TABD128               LOAD INTERNAL ERROR CODE
         BAL   RC,TCT20400              BYPASS TAPE MARK FOLLOWING
*                                       ..TRAILER LABELS
         B     TCT17600                 BRANCH- BALANCE OF POSITIONING
*                                       ..IS LIKE NO-LABEL TAPE
*
*****          POSITIONING FOR LABELED READ-BACKWARD TAPE
*
TCT16400 EQU   *
         TM    DEBOFLGS,DEBOFEOF        WAS AN EOF (OR BOT) READ-
         BO    TCT16600                 ..BR IF YES TO TEST FOR BOT
         TM    PLISTOPT(RPARC),PLISTLV  IS DISPOSION 'LEAVE'-
         BNM   TCT18200                 ..BR IF YES- POS TO BEGINNING
         B     TCT18000                 ..ELSE POSITION TO END OF DATA
TCT16600 EQU   *
         MVI   DXCCW1,CCWFSF            INITLZ CCW FOR FSF COMMAND
         LA    RD,TABD129               LOAD INTERNAL ERROR CODE
         BAL   RC,TCT20400              BYPASS TAPE MARK FOLLOWING HDRS
         TM    PLISTOPT(RPARC),PLISTLV  IS DISPOSITION 'LEAVE'-
         BNM   TCT19200                 ..BR IF YES- POS COMPLETE
         B     TCT18000                 ..ELSE BR TO POS AT END OF DATA
*
*****          POSITIONING FOR UNLABELED TAPE
*
*                        READ FORWARD (EODS) AND OUTPUT
*                             LEAVE     BSF
*                             REREAD    BSF(2) + FSF (IF NOT AT BOT)
*                        READ FORWARD (NOT EODS)
*                             LEAVE     FSF + BSF
*                             READ      BSF + FSF (IF NOT AT BOT)
*                        READ BACKWARD (EODS)
*                             LEAVE     FSF (IF NOT AT BOT)
*                             REREAD    FSF (IF NOT AT BOT) +FSF + BSF
*                        READ BACKWARD (NOT EODS)
*                             LEAVE     BSF + FSF (IF NOT AT BOT)
*                             REREAD    FSF +BSF
*
TCT16800 EQU   *
         TM    DCBOFLGS,DCBORDBK        IS DCB OPENED FOR 'RDBACK'-
         BNO   TCT17400                 ..BRANCH IF NOT
         TM    DEBOFLGS,DEBOFEOF        WAS AN END-OF-FILE READ-
         BNO   TCT17200                 ..BR IF NOT TO CHK DISPOSITION
         MVI   DXCCW1,CCWFSF            INITLZ CCW FOR FSF COMMAND
         TM    DCBOFLGS,DCBOEOF         IS WAS A TAPE MARK READ-
         BNO   TCT17000                 ..BR IF NOT- TAPE IS AT
*                                       ..LOAD POINT
         LA    RD,TABD130               LOAD INTERNAL ERROR CODE
         BAL   RC,TCT20400              BYPASS TAPE MARK PRECEDING DATA
TCT17000 EQU   *
         TM    PLISTOPT(RPARC),PLISTLV  IS DISPOSITION 'LEAVE'-
         BNM   TCT19200                 ..BR IF YES- POS COMPLETE
         B     TCT18000                 ..ELSE BR TO POS AT END OF DATA
TCT17200 EQU   *
         TM    PLISTOPT(RPARC),PLISTLV  IS DISPOSITION 'LEAVE'-
         BNM   TCT18200                 ..BR IF YES- POS AT BEGINNING
         B     TCT18000                 ..ELSE POSITION TO END OF DATA
*
*****          POSITIONING FOR UNLABELED READ-FORWARD/OUTPUT TAPE
*
TCT17400 EQU   *
         TM    DEBOFLGS,DEBOFEOF        WAS AN EOF (OR BOT) READ-
         BNO   TCT17800                 ..BR IF NOT TO CHK DISPOSITION
TCT17600 EQU   *
         MVI   DXCCW1,CCWBSF            INITLZ CCW FOR BSF COMMAND
         LA    RD,TABD134               LOAD INTERNAL ERROR CODE
         BAL   RC,TCT20400              BYPASS TAPE MARK FOLLOWING DATA
         TM    PLISTOPT(RPARC),PLISTLV  IS DISPOSITION 'LEAVE'-
         BNM   TCT19200                 ..BR IF YES- POS COMPLETE
         B     TCT18200                 ..ELSE POS TO BEGINNING OF DATA
TCT17800 EQU   *
         TM    PLISTOPT(RPARC),PLISTLV  IS DISPOSITION 'LEAVE'-
         BM    TCT18200                 ..BR IF NOT TO POS AT BEGINNING
TCT18000 EQU   *
         MVI   DXCCW1,CCWFSF            INITLZ CCW FOR FSF COMMAND
         LA    RD,TABD135               LOAD INTERNAL ERROR CODE
         BAL   RC,TCT20400              FWD SPACE TO END OF DATA SET
         TM    JFCBLTYP,JFCBAL+JFCSL    IS AL OR SL SPECIFIED-
         BNZ   TCT18600                 ..BR IF YES TO UPDATE BLK CT
         MVI   DXCCW1,CCWBSF            INITLZ CCW FOR BSF COMMAND
         LA    RD,TABD136               LOAD INTERNAL ERROR CODE
         BAL   RC,TCT20400              BYPASS TAPE MARK FOLLOWING DATA
         B     TCT19200                 ..BR- POSITIONING IS COMPLETE
TCT18200 EQU   *
         MVI   DXCCW1,CCWBSF            INITLZ CCW FOR BSF COMMAND
         CLC   SRTEFSCT(K2),ABSFCCW+K6  IS THE DESIRED DATA SET THE
*                                       ..THE FIRST ONE ON THE TAPE-
         BNE   TCT18400                 ..BR IF NOT TO BACKSPACE
         MVI   DXCCW1,CCWREW            ..YES- INITLZ FOR REWIND CMD
*                                       ..( FASTER THAN BACKSPACE )
TCT18400 EQU   *
         LA    RD,TABD137               LOAD INTERNAL ERROR CODE
         BAL   RC,TCT20400              POS TAPE TO BEGINNING OF DATA
         B     TCT19200                 BR- NOW POS IN FRONT OF DATA
*
*****          SET UP BLOCK COUNT FROM TRAILER LABEL
*
TCT18600 EQU   *
         LA    RC,DXLBL                 PLACE ADDRESS OF LABEL BUFFER
         ST    RC,DXCCW1                ..INTO READ COMMAND OPERAND
         MVI   DXCCW1,CCWRDTAP          INITLZ CCW FOR READ COMMAND
         MVC   DXCCW1+K4(K4),ARDCCW     SET LNG EQ 80, NO FLAGS
         TM    SRTESTAB,UCBBSTR         WERE ANSI LABELS SPECIFIED-
         BNO   TCT18800                 ..BR IF NOT TO READ LABEL
         MVI   DXCCW1+K4,CCWSILI        TURN ON SILI BIT IN CCW
TCT18800 EQU   *
         LA    RD,TABD138               LOAD INTERNAL ERROR CODE
         BAL   RC,TCT20400              READ FILE TRAILER LABEL 1
         TM    SRTESTAB,UCBBSTR         WERE ANSI LABELS SPECIFIED-
         BNO   TCT19000                 ..BR IF NOT- BYPASS TRANSLATION
*
*****          TRANSLATE ANSI LABEL DATA
*
         XLATE DXLBL,K80                TRANSLATE FILE TRAILER LABEL 1
TCT19000 EQU   *
         PACK  DXCCW5,FL1BLKCT          PACK THE LABEL BLOCK COUNT AND
         CVB   RC,DXCCW5                ..CONVERT IT TO BINARY, THEN
         ST    RC,DCBBLKCT              ..STORE IT IN THE DCB
         MVC   DXCCW1,ABSFCCW           INITLZ CCW FOR BSF COMMAND
         B     TCT18400                 BYPASS TAPE MARK FOLLOWING DATA
*
*                   SEE IF TAPE WAS LEFT AT FRONT OF DATA SET
*                        IF SO,IT IS NECESSARY TO DETERMINE IF
*                        THE TAPE IS AT BEGINNING OF TAPE SO
*                        THAT THE TAPE MAY BE LEFT IN FRONT OF
*                        THE FIRST DATA RECORD.
*                             THIS IS DONE HERE RATHER THAN
*                        IMMEDIATELY FOLLOWING THE BSF TO
*                        GAIN CHANNEL OVERLAP IF TCLOSEING
*                        MULTIPLE DCBS.
*
TCT19200 EQU   *
         TM    DCBOFLGS,DCBORDBK        IS DCB OPENED FOR 'RDBACK'-
         BNO   TCT19400                 ..BR IF NOT TO CHK FOR OUTPUT
         TM    DEBOFLGS,DEBOFEOF        WAS END-OF-FILE READ-
         BO    TCT20000                 ..BR IF YES- POS COMPLETE
         TM    PLISTOPT(RPARC),PLISTLV  IS DISPOSITION 'LEAVE'-
         BM    TCT20000                 ..BR IF NOT- POS COMPLETE
         B     TCT19600                 ..ELSE BR TO CHK FOR ADD'L POS
TCT19400 EQU   *
         TM    PLISTOPT(RPARC),PLISTLV  IS DISPOSITION 'LEAVE'-
         BNM   TCT20000                 ..BR IF YES- POS COMPLETE
TCT19600 EQU   *
         XC    DCBBLKCT,DCBBLKCT        ZERO OUT BLOCK COUNT IN THE DCB
         TM    JFCBLTYP,JFCBAL+JFCSL    IS AL OR SL SPECIFIED-
         BNZ   TCT19800                 ..BRANCH IF EITHER ONE
         LA    RC,K1                    INITLZ REGISTER TO ONE
         LH    RF,SRTEFSCT              PICK UP FILE SEQ COUNT FROM UCB
         CR    RC,RF                    ARE WE AT FILE SEQUENCE ONE-
         BNE   TCT19800                 ..BRANCH IF NO          XA01996
         TM    JFCBLTYP,JFCBLTM         IS LTM SPECIFIED        XA01996
         BZ    TCT20200                 BRANCH IF NO -          XA01996
*                                       ..POSITIONING COMPLETE  XA01996
         LA    RC,DXLBL                 GET READ ADDRESS        XA01996
         ST    RC,DXCCW1                STORE IN CCW            XA01996
         MVI   DXCCW1,CCWRDTAP          MOVE IN READ OP         XA01996
         BAL   RC,TCT20500              GO READ A RECORD        XA01996
         TM    IOBSTAT0,CSWUNITX        WAS A TAPE MARK READ    XA01996
         BO    TCT20200                 BRANCH IF YES -         XA01996
*                                       ..POSITIONING COMPLETE  XA01996
         MVI   DXCCW1,CCWBSR            MOVE IN BSP RECORD      XA01996
         BAL   RC,TCT20500              GO EXECUTE BACKSPACE    XA01996
         B     TCT20200                 GO EXIT                 XA01996
*
TCT19800 EQU   *
         MVI   DXCCW1,CCWFSF            INITLZ CCW FOR FSF COMMAND
         LA    RD,TABD139               'TCLOSE' INTERNAL ERROR CODE
         BAL   RC,TCT20400              BYPASS TAPE MARK PRECEDING DATA
TCT20000 EQU   *
         MVI   DXCCW1,CCWNOP            INITLZ CCW FOR NOP COMMAND
         BAL   RC,TCT20400              WAIT FOR ANY PENDING DEVICE END
         SPACE 3
***********************************************************************
*              EXIT                                                   *
***********************************************************************
*
TCT20200 EQU   *
         OI    DXATGENS,DXATDISP        VOLUME DISPOSITION DONE  Y02144
         IECRES LOAD,MODID=MOD2S2Z,BRCODE=K4,BRANCH=QUEUED       Y02080
*                                  INVOKE IECRES MACRO TO 'XCTL' Y02080
         SPACE 3
***********************************************************************
*              SUBROUTINES                                            *
***********************************************************************
*
*****          CLOSED SUBROUTINES TO PERFORM I/O OPERATIONS     SA53865
*
*        ENTRY TO THE ROUTINE IS VIA A 'BAL RC,TCT20400'
*
TCT20400 EQU   *
         OI    DXATALL,DXATFC           FORCE CLOSE ON ERROR     YM7099
         EXCP  DXIOB                    INITIATE I/O OPERATION
         IECRES WAIT                    INVOKE IECRES MACRO TO 'WAIT'
         B    TCT20550                  BR TO TEST COMPLETION   SA53865
*
*        ENTRY TO THE ROUTINE IS VIA A 'BAL RC,TCT20500'        SA53865
*
TCT20500 EQU  *                                                 SA53865
         EXCP  DXIOB                    INITIATE I/O OPERATION  SA53865
         IECRES WAIT                    INVOKE MACRO TO 'WAIT'  SA53865
         TM    IOBSTAT0,CSWUNITX        TEST FOR TAPE INDICATE  SA53865
         BZ    TCT20550                 BRANCH IF NOT           SA53865
         MVI   DXECB,ECBCOD7F           INDICATE SUCCESSFUL     SA53865
*                                       COMPLETION FOR FEOV     SA53865
         BR   RC                        RETURN TO CALLER        SA53865
*
TCT20550 EQU   *                                                SA53865
         TM    DXECB,ECBNOERR           TEST FOR SUCCESSFUL COMPLETION-
         BCR   1,RC                     ..BR IF YES- RETURN TO CALLER
         TM    IOBSENS1,UCBLDPT         IS THE TAPE AT LOAD POINT-
         BCR   1,RC                     ..BR IF YES- RETURN TO CALLER
*                                       ..ELSE DROP THRU TO ERROR RTN
         EJECT
***********************************************************************
*              ERROR PROCESSING                                       *
***********************************************************************
*
TCT20600 EQU   *
         DMABCOND (RD),ABEND2S          EXIT TO PROB DETERMINATION RTN
         SPACE 2
***********************************************************************
*              CONSTANTS                                              *
***********************************************************************
*
AWTMCCWM CCW   X'1F',0,X'20',80         WR TAPE MARK COMMAND AND LENGTH
ABSFCCW  CCW   X'2F',0,X'20',1          BACKSPACE FILE, SILI FLAG ON
ARDCCW   DC    X'00000050'              NO FLAGS, LENGTH EQ 80 CHARACTE
         SPACE 2
***********************************************************************
*              XCTL TABLE                                             *
***********************************************************************
*
XCTLTB2S XCTLTABL ID=(ABEND2S,0P,MOD2S2Z,2Z),SVC=023,            Y02080X
               BRT=YES,LENGTH=                                   Y02080
         SPACE 2
         IECDSECS DCB,ACB,                                             C
               CVT,IEZDEB,PREFX,                                 YM1272C
               UCB,                                                    C
               MAIN,                                                   C
               WTG,                                                    C
               EXPAND=YES
PURGPRM  DSECT                          PURGE PARAMETER LIST     Y02082
PURGDEB  DS    0F                       DEB TO BE PURGED         Y02082
PURGOPT  DS    C                        OPTION BITS              Y02082
         DS    AL3                      DEB POINTER              Y02082
PURGECB  DS    0F                       USE THIS FIELD FOR ECB   Y02082
         DS    C                        COMPLETION CODE          Y02082
PURGTCB  DS    AL3                      TCB POINTER (0=CURRENT)  Y02082
PURGCHN  DS    AL4                      CHAIN OF IOBS            Y02082
PURGMVM  DS    F                        4TH WORD FOR MVM         Y02082
         EJECT
         IECEQU IEZDEB=YES              EQUATES                  YM1272
         END
