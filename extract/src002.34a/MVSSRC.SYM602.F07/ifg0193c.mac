         TITLE 'IFG0193C                      OMODVOL1 - STANDARD LABELX
               EDITING ROUTINE'                                  Y02134
IFG0193C CSECT
         ENTRY OMODVOL1                 ALIAS ENTRY POINT        Y02134
OMODVOL1 EQU   IFG0193C                 USER ENTRY POINT         Y02134
***********************************************************************
*                                                                     *
* MODULE NAME = IFG0193C (OS/VS2)                                     *
*                                                                     *
* DESCRIPTIVE NAME = OMODVOL1 - STANDARD LABEL EDITING ROUTINE        *
*                                                                     *
* COPYRIGHT = NONE                                                    *
*                                                                     *
* STATUS = RELEASE 2, LEVEL 0                                         *
*                                                                     *
* FUNCTION =                                                          *
*                                                                     *
*        REGISTERS ARE INITIALIZED FROM REG 5 & 6 TO PROCESS EACH DCB *
*        THAT REQUIRES LABEL PROCESSING AS INDICATED BY JFCBMASK+5    *
*                                                                     *
*        1. DETERMINES PROCESSING TO BE PERFORMED:                    *
*           A. FOR SPECIFIC REQUESTS - SETS REJECT VOLUME INDICATOR   *
*           ON (UCBDMCT BIT 0) AND GOES TO PROCESS NEXT DCB. IF       *
*           THE LABEL IS CHANGED ON A TAPE WHICH IS SPECIFICALLY      *
*           REQUESTED, THIS MAY CAUSE PROBLEMS IN SUBSEQUENT          *
*           STEPS WHICH REFER TO THE TAPE BY ITS ORIGINAL LABEL.      *
*                                                                     *
*           B. FOR NON-SPECIFIC REQUESTS - CHECKS FOR LABEL OR        *
*           DENSITY CONFLICTS;                                        *
*                                                                     *
*           C. IF A LABEL CONFLICT EXISTS AND A SL IS TO BE           *
*           DESTROYED, WRITES A TAPE MARK, RESETS THE LABEL           *
*           DESTROY BIT JFCSLDES IN THE JFCBMASK+5 FIELD,             *
*           AND GOES TO PROCESS THE NEXT DCB.                         *
*                                                                     *
*           D. IF A LABEL CONFLICT EXISTS AND A SL IS TO BE           *
*           CREATED, CREATES A VOL1 LABEL, RESETS THE LABEL           *
*           CREATE BIT JFCSLCRE IN THE JFCBMASK+5 FIELD,              *
*           AND GOES TO PROCESS THE NEXT DCB.                         *
*                                                                     *
*           E. FOR DENSITY CONFLICTS, RESOLVES THE DENSITY            *
*           CONFLICT, RESETS THE DENSITY CONFLICT BIT                 *
*           JFCDUAL IN THE JFCBMASK+5 FIELD,                          *
*           AND GOES TO PROCESS THE NEXT DCB.                         *
*                                                                     *
*           F. IF A SITAUTION IS ENCOUNTERED WHERE IT BECOMES         *
*           DESIREABLE TO HAVE A NEW TAPE MOUNTED, SET THE            *
*           UCBMOUNT BIT IN THE UCBDMCT FIELD OF THE UCB.             *
*                                                                     *
* NOTES = IF THE JFCSLDES BIT IS NOT RESET IN THE JFCBMASK+5          *
*         THE LABEL ON THE TAPE WILL BE DESTROYED                     *
*         WHEN YOU RETURN TO IGG0190A.                                *
*       * IF THE JFCSLCRE BIT IS NOT RESET IN THE JFCBMASK+5          *
*         A LABEL WILL BE CREATED ON THE TAPE                         *
*         WHEN YOU RETURN TO IGG0190A.                                *
*       * IF THE JFCDUAL BIT IS NOT RESET IN THE JFCBMASK+5           *
*         THIS ROUTINE WILL NOT BE REENTERED IF A SUBSEQUENT          *
*         DENSITY CONFLICT OCCURS IN LABEL VERIFICATION.              *
*       * THE LABELS ARE ALWAYS REWRITTEN AT THE DENSITY              *
*         INDICATED IN THE DXDEBMOD FIELD OF THE WORK AREA.           *
*       * BOTH JFCSLDES AND JFCSLCRE MAY BE ON IF SWITCHING           *
*         FROM SL TO AL OR AL TO SL.                                  *
*       * **NOTE** IGG0190A WILL CHECK FOR PASSWORD PROTECTION @G32DSMI
*         OR RACF PROTECTION ON THE TAPE PRIOR TO PERFORMING   @G32DSMI
*         THE LABEL EDITING ACTION REQUESTED. A SECURITY       @G32DSMI
*         FAILURE WILL CAUSE NO EDITING ACTION TO OCCUR.       @G32DSMI
*                                                                     *
*    DEPENDENCIES =                                                   *
*            CLASS ONE CHARACTER CODE.  THE EBCDIC CHARACTER CODE     *
*            WAS USED FOR ASSEMBLY.  THE MODULE MUST BE REASSEMBLED   *
*            IF A DIFFERENT CHARACTER SET IS USED FOR EXECUTION.      *
*            THIS ROUTINE USES REGISTER CONVENTIONS AND WORK AREAS    *
*            DEFINED FOR OPEN/CLOSE/EOV ROUTINES.                     *
*            OPEN/CLOSE/EOV DEPEND ON THIS ROUTINE FOR UPHOLDING      *
*            THE SECURITY AND THE INTEGRITY OF THE MACHINE.           *
*                                                                     *
*    RESTRICTIONS = NONE                                              *
*                                                                     *
*    REGISTER CONVENTIONS =                                           *
*            R2 POINTS TO DCB                                         *
*            R4 POINTS TO OPEN WORK AREA                              *
*            R5 POINTS TO THE RESIDENT ROUTINE                        *
*            R6 POINTS TO THE WTG TABLE                               *
*            R7 POINTS TO THE CURRENT PARAMETER LIST ENTRY            *
*            R8 POINTS TO THE CURRENT WTG TABLE ENTRY                 *
*            R9 POINTS TO THE DD ENTRY IN THE TIOT                    *
*            R10 POINTS TO THE UCB                                    *
*                                                                     *
*    PATCH LABEL = SEE NEXT TO LAST LABEL BEFORE ORG STATEMENT AT END *
*                  OF LISTING.                                        *
*                                                                     *
* MODULE TYPE = CONTROL (OPEN, CLOSE, EOV DATA MANAGEMENT)            *
*                                                                     *
*    PROCESSOR = ASSEMBLER XF                                         *
*                                                                     *
*    MODULE SIZE = SEE EXTERNAL SYMBOL DICTIONARY OR LOC FIELD ON     *
*                  ORG STATEMENT AT END OF LISTING                    *
*                                                                     *
*    ATTRIBUTES = REENTRANT, REFRESHABLE,READ-ONLY, ENABLED,          *
*                 PRIVILEGED, SUPERVISOR STATE, KEY 0,                *
*                 LINK PACK AREA RESIDENT/PAGEABLE                    *
*                                                                     *
* ENTRY POINT = IFG0193C                                              *
*               OMODVOL1                                              *
*                                                                     *
*    PURPOSE = TO RESOLVE LABEL AND DENSITY CONFLICTS IN A MANNER     *
*              APPROPRIATE WITH THE OPERATING CHARACTERISTICS         *
*              PARTICULAR TO THE INSTALLATION.                        *
*                                                                     *
*    LINKAGE = AS FOLLOWS -                                           *
*        LA    RPAR,PARMLIST            GET PARM LIST POINTER         *
*        LA    RWTG,WTG                 GET WTG TABLE POINTER         *
*        MODESET EXTKEY=ZERO            KEY 0                         *
*        IECRES LOAD,MODNM=OMODVOL1,BRANCH=DIRECT,PREFIX=WTGPREFX     *
*                                                                     *
* INPUT = RWTG (REG 6) POINTS TO WTG TABLE,                           *
*         RPAR (REG 5) POINTS TO USER PARMLIST.                       *
*                                                                     *
* OUTPUT = THE JFCBMASK+5 BITS MUST BE SET AS STATED ABOVE.           *
*          UCBDMCT - UCBMOUNT SET ON IF VOLUME DISMOUNT DESIRED.      *
*                                                                     *
* EXIT-NORMAL = IGG0190A (IFG0193D) LABEL EDITOR AND DESTROY          *
*               STANDARD LABEL                                        *
*                                                                     *
* EXIT-ERROR = NONE                                                   *
*                                                                     *
* EXTERNAL REFERENCES = SEE BELOW                                     *
*                                                                     *
*    ROUTINES = IGG0190A,IFG019RA.                                    *
*                                                                     *
*    DATA AREAS = WTG TABLE, OPEN WORK AREA.                          *
*                SEE PARAMETERS OF THE IECDSECS MACRO.                *
*                                                                     *
*    CONTROL BLOCK = UCB, JFCB.                                       *
*                SEE PARAMETERS OF THE IECDSECS MACRO.                *
*                                                                     *
* TABLES = SEE XCTLTABL MACRO.                                        *
*                                                                     *
* MACROS = IECDSECS,IECEQU,IECRES,XCTLTABL.                           *
*                                                                     *
* CHANGE ACTIVITY = LEVEL 1                                           *
*                                                                     *
*      VS2 SU32  DELETIONS                                            *
*                                                              @G32DSMI
*                                                                     *
***********************************************************************
         EJECT
*                  SYMBOLIC REGISTER USAGE
*
*
*                  ESTABLISH ADDRESSABILITY
         BALR  RBASE,R0                 SET BASE REGISTER
         USING *,RBASE
         USING FORCORE,RCORE            WORK AREA
         USING WTG,RWTG                 WHERE-TO-GO TABLE        Y02080
         USING WTGENTRY,RWTGC           RWTGC POINTS TO CURRENT  Y02134
         USING USERPRML,RPARC           RPARC POINTS TO CURRENT  Y02134
         USING IHADCB,RDCB              DCB
         USING UCBOB,RUCB               UCB
         USING TIOENTRY,RTIOT           TIOT ENTRY
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
*        INITIALIZE REGISTERS                                    Y02134
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
         LR    RPARC,RPAR               POINT TO FIRST ENTRY     Y02134
         LA    RWTGC,WTGENTRY-WTG(,RWTG) POINT TO FIRST ENTRY    Y02134
*                                                                Y02134
OMO01000 EQU   *                        PROCESS DCB IN THIS LOAD Y02134
*                                                                Y02134
         CLC   WTGIDTTR(K2),ID3C3C      CHECK IF RIGHT DCB       Y02134
         BNE   OMO09000                 BRANCH IF ENTRY NOT SAME Y02134
*                                                                Y02134
         L     RDCB,DXDCBLST            LOAD DCB POINTER         Y02134
         L     RCORE,WTGCORE-K1         LOAD WORK AREA POINTER   Y02134
         L     RTIOT,DXDSAB             LOAD POINTER TO DSAB     Y02134
         L     RTIOT,DSABTIOT-DSAB(,RTIOT) GET TIOENTRY POINTER  Y02134
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
*        GET UCB ADDRESS FROM WORK AREA                          Y02134
*        FIRST ENTRY IN TIOT MAY NOT BE VALID                    Y02134
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
         L     RUCB,DXUCBADR            GET CURRENT UCB ADDRESS  Y02134
*
*****************************************************************Y02134
*                                                                Y02134
*CHECK JFCB MASK TO DETERMINE IF PROCESSING IS REQUIRED ON THIS DCB
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
         TM    JFCBMASK+K5,LBLERR       TEST FOR CONFLICT IN LABEL
         BNZ   OMO02000                 BRANCH IF LABEL CONFLICT Y02134
*                                                                Y02134
         TM    JFCBMASK+K5,JFCDUAL      TEST IF CONFLICT IN DENS Y02134
         BNO   OMO08000                 BRANCH IF NO CONFLICT    Y02134
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
*        DENSITY CONFLICTS TO BE RESOLVED HERE                   Y02134
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
*        THE FOLLOWING TWO INSTRUCTIONS, IF ACTIVATED, WILL      YM3819
*        CAUSE A SPECIFICALLY REQUESTED TAPE TO BE REJECTED      YM3819
*        WHEN A DENSITY CONFLICT OCCURS. IF THE TAPE IS          YM3819
*        REMOUNTED BY THE OPERATOR WITHOUT TAMPERING WITH        YM3819
*        THE LABEL, THE TAPE WILL BE ACCEPTED AND THIS ROUTINE   YM3819
*        WILL NOT RECEIVE CONTROL AGAIN. THE LABEL ON THE TAPE   YM3819
*        WILL BE REWRITTEN AT THE USER REQUESTED DENSITY.        YM3819
*                                                                YM3819
*        IF THE FOLLOWING TWO INSTRUCTIONS ARE NOT ACTIVATED     YM3819
*        THE LABEL ON THE TAPE WILL BE REWRITTEN AT THE USER     YM3819
*        REQUESTED DENSITY WITHOUT AN INTERVENING DISMOUNT.      YM3819
*                                                                YM3819
*****************************************************************YM3819
*                                                                YM3819
         CLC   DXVOLSR2,SCRTCH          CHECK IF SCRATCH REQUEST YM3819
*        BNE   OMO08000                 BRANCH IF NOT SCRTCH     YM3819
*        FOLLOWING INSTRUCTION MAY BE ZAPPED TO 4770             YM3819
         NOP   OMO08000                 BRANCH IF NOT SCRTCH     YM3819
*                                                                YM3819
*****************************************************************YM3819
*                                                                YM3819
* INSERT YOUR INSTRUCTIONS TO RESOLVE DENSITY CONFLICTS,         Y02134
* AND REMOVE ASTERISKS TO ACTIVATE THE NEXT INSTRUCTION          Y02134
* AND REASSEMBLE THIS MODULE.                                    Y02134
*                                                                Y02134
******** NI    JFCBMASK+K5,ALLBITS-JFCDUAL SET DENSITY RESOLVED  Y02134
*                                                                Y02134
         B     OMO09000                 BRANCH TO GET NEXT ENTRY Y02134
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
*        RESOLVE LABEL CONFLICTS HERE                            Y02134
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
OMO02000 EQU   *                        CHECK LABEL CONFLICT
*                                                                Y02134
         TM    JFCBMASK+K5,JFCSLDES     DESTROY SL LABEL         Y02134
         BNO   OMO04000                 NO, CHECK IF CREATE      YM4647
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
*        DESTROY STANDARD LABEL HERE                             Y02134
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
OMO03000 EQU   *                        USER ROUTINE HERE        Y02134
*                                                                Y02134
*****************************************************************YM3819
*                                                                YM3819
*        THE FOLLOWING TWO INSTRUCTIONS, IF ACTIVATED, WILL      YM3819
*        CAUSE A SPECIFICALLY REQUESTED TAPE TO BE REJECTED      YM3819
*        WHEN A LABEL CONFLICT OCCURS (NL REQUESTED & SL         YM3819
*        MOUNTED).                                               YM3819
*                                                                YM3819
*        IF THE FOLLOWING TWO INSTRUCTIONS ARE NOT ACTIVATED     YM3819
*        THE LABEL ON THE TAPE WILL BE REWRITTEN AT THE USER     YM3819
*        REQUESTED LABEL TYPE WITHOUT AN INTERVENING DISMOUNT,   YM3819
*        REGARDLESS IF THE REQUEST IS SPECIFIC OR NOT.           YM3819
*                                                                YM3819
*****************************************************************YM3819
*                                                                YM3819
         CLC   DXVOLSR2,SCRTCH          CHECK IF SCRATCH REQUEST YM3819
*        BNE   OMO08000                 BRANCH IF NOT SCRTCH     YM3819
*        FOLLOWING INSTRUCTION MAY BE ZAPPED TO 4770             YM3819
         NOP   OMO08000                 BRANCH IF NOT SCRTCH     YM3819
*                                                                YM3819
*****************************************************************YM3819
*                                                                YM3819
* INSERT YOUR INSTRUCTIONS TO DESTROY THE VOLUME LABEL           Y02134
* ACCORDING TO PUBLICATION GC28-6680,                            Y02134
* AND REMOVE ASTERISKS TO ACTIVATE THE NEXT INSTRUCTION,         Y02134
* AND REASSEMBLE THIS MODULE.                                    Y02134
*                                                                Y02134
******** NI    JFCBMASK+K5,ALLBITS-JFCSLDES SET DESTROY SL OFF   Y02134
*                                                                Y02134
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
*        CREATE STANDARD LABEL HERE                              Y02134
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
OMO04000 EQU   *                        USER ROUTINE HERE        Y02134
*                                                                YM4647
         TM    JFCBMASK+K5,JFCSLCRE     CREATE SL LABEL          YM4647
         BNO   OMO09000                 NO, RETURN TO REVERIFY   YM4647
*                                                                Y02134
*****************************************************************YM3819
*                                                                YM3819
*        THE FOLLOWING TWO INSTRUCTIONS WILL                     YM3819
*        CAUSE A SPECIFICALLY REQUESTED TAPE TO BE REJECTED      YM3819
*        WHEN A LABEL CONFLICT OCCURS (SL REQUESTED & NL         YM3819
*        MOUNTED).                                               YM3819
*                                                                YM3819
*        IF THE FOLLOWING TWO INSTRUCTIONS ARE NOT ACTIVATED     YM4647
*        THE LABEL ON THE TAPE WILL BE REWRITTEN AT THE USER     YM4647
*        REQUESTED LABEL TYPE WITHOUT AN INTERVENING DISMOUNT,   YM4647
*        REGARDLESS IF THE REQUEST IS SPECIFIC OR NOT.           YM4647
*                                                                YM3819
*****************************************************************YM3819
*                                                                YM3819
         CLC   DXVOLSR2,SCRTCH          CHECK IF SCRATCH REQUEST YM3819
*        BNE   OMO08000                 BRANCH IF NOT SCRTCH     YM4647
*        FOLLOWING INSTRUCTION MAY BE ZAPPED TO 4770             YM4647
         NOP   OMO08000                 BRANCH IF NOT SCRTCH     YM4647
*                                                                YM3819
*****************************************************************YM3819
*                                                                YM3819
* INSERT YOUR INSTRUCTIONS TO CREATE THE VOLUME LABEL            Y02134
* ACCORDING TO PUBLICATION GC28-6680,                            Y02134
* AND REMOVE ASTERISKS TO ACTIVATE THE NEXT INSTRUCTION,         Y02134
* AND REASSEMBLE THIS MODULE.                                    Y02134
*                                                                Y02134
******** NI    JFCBMASK+K5,ALLBITS-JFCSLCRE SET CREATE SL OFF    Y02134
*                                                                Y02134
         B     OMO09000                 BRANCH TO GET NEXT ENTRY Y02134
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
*        REJECT MOUNTED VOLUME HERE                              Y02134
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
OMO08000 EQU   *                        REJECT VOLUME HERE       Y02134
*                                                                Y02134
         OI    UCBDMCT,UCBMOUNT         SET REJECT ON            Y02134
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
*        GET NEXT ENTRY IN PARM LIST                             Y02134
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
OMO09000 EQU   *                        GET NEXT DCB             Y02134
*                                                                Y02134
         TM    DXDCBLST,LASTNTRY        TEST FOR LAST ENTRY      Y02134
         LA    RPARC,DXDCBLST+L'DXDCBLST GET NEXT ENTRY          Y02134
         LA    RWTGC,WTGENTRY+L'WTGENTRY GET NEXT ENTRY          Y02134
         BNO   OMO01000                 BR TO PROCESS NEXT ENTRY Y02134
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
*        RETURN TO IGG0190A                                      Y02134
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
         IECRES LOAD,MODNM=IDRETURN,PREFIX=WTGPREFX,             Y02134X
               BRANCH=DIRECT                                     Y02134
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
*        CONSTANTS                                               Y02134
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
ID3C3C   DC    CL2'3C'                  ID OF OMODVOL1/EMODVOL1  Y02134
SCRTCH   DC    CL6'SCRTCH'              NON SPECIFIC REQUEST     Y02134
*                                                                Y02134
         XCTLTABL ID=(IDRETURN,IGG0190A),BRT=YES,LENGTH=         Y02134
*                                                                Y02134
         IECDSECS CVT,TCB,DSAB,TIOT,DCB,UCB,MAIN,WTG,PREFX,      Y02134X
               EXPAND=YES                                        Y02134
         IECEQU
         END
