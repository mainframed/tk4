         TITLE 'IFG0195G                      OPEN - DIRECT ACCESS 213 A
               ABEND EXIT'                                       Y02134
         COPY  LCGASMSW
IFG0195G CSECT
***********************************************************************
*                                                                     *
* MODULE NAME = IFG0195G (OS/VS2)                                     *
*                                                                     *
* DESCRIPTIVE NAME = DIRECT ACCESS 213 EXIT                           *
*                                                                     *
* COPYRIGHT = NONE                                                    *
*                                                                     *
* STATUS = RELEASE 2, LEVEL 0                                         *
*                                                                     *
* FUNCTION =                                                          *
*        1. DECREMENT THE DATA MANAGEMENT COUNT IN THE UCB.           *
*                                                                     *
*        2. IF A DCB EXIT (X'09') EXISTS, DO NOT ISSUE THE 213 ABEND. *
*        INSTEAD, TRANSFER CONTROL TO THE LAST LOAD OF OPEN (IFG0198N)*
*        TO PERFORM CLEAN-UP AND GIVE CONTROL TO CALLER.              *
*                                                                     *
*        3. IF A DECB EXIT (X'09') DOES NOT EXIST, TRANSFER CONTROL   *
*        PROBLEM DETERMINATION (IFG0190P) TO ISSUE 213 ABEND.         *
*                                                                     *
* NOTES = SEE BELOW                                                   *
*                                                                     *
*    DEPENDENCIES =                                                   *
*            CLASS ONE CHARACTER CODE.  THE EBCDIC CHARACTER CODE     *
*            WAS USED FOR ASSEMBLY.  THE MODULE MUST BE REASSEMBLED   *
*            IF A DIFFERENT CHARACTER SET IS USED FOR EXECUTION.      *
*                                                                     *
*    RESTRICTIONS = NONE                                              *
*                                                                     *
*    REGISTER CONVENTIONS =                                           *
*            R2 POINTS TO DCB                                         *
*            R4 POINTS TO OPEN WORK AREA                              *
*            R5 POINTS TO THE RESIDENT ROUTINE                        *
*            R6 POINTS TO THE WTG TABLE                               *
*            R7 POINTS TO THE CURRENT PARAMETER LIST ENTRY            *
*            R8 POINTS TO THE CURRENT WTG TABLE ENTRY                 *
*            R9 POINTS TO THE DD ENTRY IN THE TIOT                    *
*            R10 POINTS TO THE UCB                                    *
*                                                                     *
*    PATCH LABEL = SEE NEXT TO LAST LABEL BEFORE ORG STATEMENT AT END *
*                  OF LISTING.                                        *
*                                                                     *
* MODULE TYPE = CONTROL (OPEN, CLOSE, EOV DATA MANAGEMENT)            *
*                                                                     *
*    PROCESSOR = ASSEMBLER XF                                         *
*                                                                     *
*    MODULE SIZE = SEE EXTERNAL SYMBOL DICTIONARY OR LOC FIELD ON     *
*                  ORG STATEMENT AT END OF LISTING                    *
*                                                                     *
*    ATTRIBUTES = REENTRANT, REFRESHABLE,READ-ONLY, ENABLED,          *
*                 PRIVILEGED, SUPERVISOR STATE, DATA MANAGEMENT KEY,  *
*                 LINK PACK AREA RESIDENT/PAGEABLE                    *
*                                                                     *
* ENTRY POINT = IFG0195G                                              *
*                                                                     *
*    PURPOSE = PROVIDES DCB EXIT LIST BYPASS FOR 213 ABEND            *
*                                                                     *
*    LINKAGE = IECRES LOAD,BRANCH=QUEUED,MODID=ID5G,BRCODE=0          *
*                                                                     *
* INPUT = AS FOLLOWS -                                                *
*                                                                     *
*        RDCB (REG 2) = CONTENTS OF CURRENT PARMLIST ENTRY            *
*                       CONTAINING DCB POINTER                        *
*        RCORE (REG 4) = OPEN/EOV WORK AREA POINTER                   *
*        REG 5 = POINTER TO RESIDENT ROUTINE                          *
*        RWTG (REG 6) = POINTER TO OPEN/EOV WTG TABLE                 *
*        RPARC (REG 7) = POINTER TO CURRENT ENTRY IN PARMLIST         *
*        RWTGC (REG 8) = POINTER TO CURRENT WTG ENTRY                 *
*        RTIOT (REG 9) = POINTER TO CURRENT TIOT ENTRY                *
*        RUCB (REG 10) = POINTER TO FIRST UCB                         *
*                                                                     *
* OUTPUT = SAME AS INPUT.                                             *
*                                                                     *
* EXIT-NORMAL = IFG0198N TO SKIP 213 ABEND.                           *
*                                                                     *
* EXIT-ERROR = IFG0190P TO ISSUE 213 ABEND.                           *
*                                                                     *
* EXTERNAL REFERENCES = SEE BELOW                                     *
*                                                                     *
*    ROUTINES = IFG0194A,IFG0195A,IFG0198N,IFG0190P.                  *
*                                                                     *
*    DATA AREAS = MAIN WORKAREA, WTG TABLE, PREFIX.                   *
*                                                                     *
*    CONTROL BLOCK = CVT,TCB,TIOT,UCB,DCB.                            *
*                                                                     *
* TABLES = SEE WTG TABLE AND XCTLTABL.                                *
*                                                                     *
* MACROS = IECDSECS,IECEQU,MODESET,IECRES,XCTLTABL,DMABCOND.          *
*                                                                     *
* CHANGE ACTIVITY = NEW RELEASE (LEVEL 0)                             *
*                                                                     *
***********************************************************************
         EJECT
         IECDSECS CVT,TCB,TIOT,UCB,DCB,MAIN,PREFX,WTG            Y02134
*
         IECEQU ,                       DEFINE EQUATES
*
         USING IHADCB,RDCB              DEFINE BASE TO USER'S DCB
         USING FORCORE,RCORE            DEFINE BASE TO MAIN WORK AREA
         USING WTG,RWTG                 RWTG POINTS TO WTG TABLE Y02134
         USING TIOENTRY,RTIOT           RTIOT POINTS TO CURRENT  Y02134
         USING UCBOB,RUCB               DEFINE BASE TO MAIN UCB
*                                                                Y02134
         BALR  RBASE,R0                 ESTABLISH ADDRESSABILITY Y02134
         USING *,RBASE                  DEFINE BASE REGISTER     Y02134
*
***********************************************************************
*
*  CHECK FOR DCB EXIT. IF NONE, GO THROUGH NORMAL PROBLEM
*  DETERMINATION ERROR RECOVERY, WHICH WILL USUALLY RESULT IN A
*  213 ABEND, BECAUSE NO RECOVERY WAS SPECIFIED BY THE USER.
*
***********************************************************************
*
         TM    DCBMACRF,DCBMEXCP        CHECK FOR EXCP
         BZ    ODA92600                 BR IF NO
         TM    DCBMACRF,DCBMFOUN        CHECK FOR FOUNDATION EXTENSION
         BZ    ODA93400                 BR IF NOT PRESENT
*
ODA92600 EQU   *                        CHECK FOR EXIT LIST
*
         L     R1,DCBEXLST              IS ADDR OF EXIT LIST 0 OR 1
         LA    R1,0(,R1)
         LA    R0,K1
         CR    R1,R0
         BNH   ODA93400                 BR IF NO LIST SPECIFIED
*
         MODESET KEYADDR=DXUKEY,WORKREG=12 USER KEY TO LOOK AT   Y02082*
                                        EXIT LIST                Y02082
*
ODA92800 EQU   *                        CHECK FOR EXIT LIST ENTRY
*
         CLI   0(R1),XL213ABD           CHECK FOR ACTIVE X'09' DCB EXIT
         BE    ODA93000                 BR IF YES
         CLI   0(R1),XL213ABD+LASTNTRY  CHECK FOR ACTIVE X'89' DCB EXIT
         BE    ODA93000                 BR IF YES
         TM    0(R1),LASTNTRY           IS THIS END OF LIST
         LA    R1,K4(,R1)               INCREMENT TO NEXT ENTRY IN LIST
         BZ    ODA92800                 BR IF NO
*
         B     ODA93400                 BR TO PROBLEM DET / ABEND
*
***********************************************************************
*
*  SINCE A 213 EXIT WAS FOUND, GO TO THE LAST OPEN LOADS TO CLEAN UP.
*  THEY WILL FREEMAIN THE WORK AREAS AND RETURN TO THE CALLER OF OPEN.
*  OTHER DCB'S MAY CONTINUE TO BE OPENED.
*  NO 213 ABEND WILL BE ISSUED.  ANY ADDRESS IN THE EXIT LIST ENTRY
*  IS NOT USED.  CONTROL WILL RETURN TO THE NORMAL RETURN ADDRESS
*  OF THE CALLER OF OPEN.  HE SHOULD THEN TEST THE DCBOFLGS OPEN BIT.
*  THE DCB IS LEFT IN A PARTIALLY OPEN STATE AND SO CAN NOT BE
*  USED AGAIN WITHOUT COMPLETE REINITIALIZATION BY THE USER.
*
***********************************************************************
*
ODA93000 EQU   *                        BYPASS 213 ABEND
*
         MODESET EXTKEY=DATAMGT         RESET TO DM KEY          Y02080
         NI    DCBOFLGS,X'FF'-DCBOBUSY  CLEAR DCB BUSY BIT TO STOP OPEN
*
*                                       XCTL TO END OF OPEN      Y02080
         IECRES LOAD,MODID=ODA9980X,BRCODE=4,BRANCH=QUEUED       Y02080
*
***********************************************************************
*
*  XCTL TO IFG0190P PROBLEM DETERMINATION MODULE.
*  THERE, IF THE USER HAS A X'11' EXIT, HE MAY RECOVER FROM THIS
*  213 ABEND BY SPECIFYING A NEW VOLUME SERIAL NUMBER TO USE.
*  IF SO, PROBLEM DETERMINATION WILL XCTL TO IFG0194E TO TRY TO
*  RECOVERY WITH THE NEW VOLUME SERIAL NUMBER.
*  PD WILL HAVE PLACED THE NEW VOLUME SERIAL NUMBER SPECIFIED BY THE
*  USER IN DXVOLSR2.
*
***********************************************************************
*
ODA93400 EQU   *                        GO ISSUE 213 ABEND
*
         MODESET EXTKEY=DATAMGT         DATA MANAGEMENT KEY      Y02082
OABD030  EQU   30                       FORMAT 1 DSCB MISSING IN VTOC
         MVC   DXRETMOD,ODA9930X        SET UP RETURN            Y02134
         MVC   DXRETCOD,K24             SET UP RETURN OFFSET     Y02134
         MVC   DXSAVMOD,ODA9940X        SET UP RETURN MOD FOR 4A Y02134
         MVI   DXSAVOFF,K0              SET UP RET OFFSET FOR 4A Y02134
         DMABCOND OABD030,ODA9920X      INVOKE PROB DET          Y02080
*
***********************************************************************
*
*        CONSTANTS
*
***********************************************************************
*
         XCTLTABL ID=(ODA9920X,0P,ODA9930X,4A,ODA9940X,5A,       Y02134X
               ODA9980X,8N),BRT=YES,LENGTH=                      Y02134
*
         IECDSECS EXPAND=YES            EXPAND DESIRED DSECTS HERE
*
         END
