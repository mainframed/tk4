         TITLE 'IFG0195K                      OPEN - TAPE FILE LABEL 2 P
               PROCESSING'                                       Y02134
IFG0195K CSECT
***********************************************************************
*                                                                     *
* MODULE NAME = IFG0195K (OS/VS2)                                     *
*                                                                     *
* DESCRIPTIVE NAME = TAPE FILE LABEL 2 PROCESSING                     *
*                                                                     *
* COPYRIGHT = NONE                                                    *
*                                                                     *
* STATUS = RELEASE 2, LEVEL 0                                         *
*                                                                     *
* FUNCTION =                                                          *
*        1. WAIT FOR REPLY TO CHECK EXPIRATION DATE MESSAGE AND USE   *
*        OPERATOR'S REPLY TO DETERMINE WHETHER OR NOT TO USE THE TAPE.*
*                                                                     *
*        2. IF THE TAPE IS SECURITY PROTECTED, AN INDICATOR IS SET SO *
*        THE PASSWORD WILL BE CHECKED LATER. AN ASCII TAPE CANNOT BE  *
*        USED IF ITS SECURITY BYTE IS NON-BLANK BUT DOES NOT INDICATE *
*        SECURITY.                                                    *
*                                                                     *
*        3. READ FILE LABEL 2.                                        *
*                                                                     *
*        4. MERGE THE FOLLOWING FIELDS FROM THE LABEL TO THE JFCB:    *
*              RECFM                                                  *
*              BLKSIZE                                                *
*              LRECL                                                  *
*              BUFOFF (FOR ASCII)                                     *
*              BLOCKING ATTRIBUTE                                     *
*              CARRIAGE CONTROL CHARACTER SET                         *
*              TAPE RECORDING TECHNIQUE                               *
*                                                                     *
*        5. POSITION TAPE TO FIRST DATA RECORD.                       *
*                                                                     *
* NOTES = SEE BELOW                                                   *
*                                                                     *
*    DEPENDENCIES =                                                   *
*            CLASS ONE CHARACTER CODE.  THE EBCDIC CHARACTER CODE     *
*            WAS USED FOR ASSEMBLY.  THE MODULE MUST BE REASSEMBLED   *
*            IF A DIFFERENT CHARACTER SET IS USED FOR EXECUTION.      *
*                                                                     *
*    RESTRICTIONS = NONE                                              *
*                                                                     *
*    REGISTER CONVENTIONS =                                           *
*            R2 POINTS TO DCB                                         *
*            R4 POINTS TO OPEN WORK AREA                              *
*            R5 POINTS TO THE RESIDENT ROUTINE                        *
*            R6 POINTS TO THE WTG TABLE                               *
*            R7 POINTS TO THE CURRENT PARAMETER LIST ENTRY            *
*            R8 POINTS TO THE CURRENT WTG TABLE ENTRY                 *
*            R9 POINTS TO THE DD ENTRY IN THE TIOT                    *
*            R10 POINTS TO THE UCB                                    *
*                                                                     *
*    PATCH LABEL = SEE THIRD LABEL BEFORE ORG STATEMENT AT END        *
*                  OF LISTING.                                        *
*                                                                     *
* MODULE TYPE = CONTROL (OPEN, CLOSE, EOV DATA MANAGEMENT)            *
*                                                                     *
*    PROCESSOR = ASSEMBLER XF                                         *
*                                                                     *
*    MODULE SIZE = SEE EXTERNAL SYMBOL DICTIONARY OR LOC FIELD ON     *
*                  ORG STATEMENT AT END OF LISTING                    *
*                                                                     *
*    ATTRIBUTES = REENTRANT, REFRESHABLE,READ-ONLY, ENABLED,          *
*                 PRIVILEGED, SUPERVISOR STATE, DATA MANAGEMENT KEY,  *
*                 LINK PACK AREA RESIDENT/PAGEABLE                    *
*                                                                     *
* ENTRY POINT = IFG0195K ROM IFG0195H TAPE INPUT HEADER LABEL         *
*               VERIFICATION                                          *
*                                                                     *
*    PURPOSE = SEE FUNCTION                                           *
*                                                                     *
*    LINKAGE =                                                        *
*        FROM IFG0195H:                                               *
*           IECRES LOAD,MODID=ID5H5K,BRCODE=K0,BRANCH=QUEUED          *
*                                                                     *
*        FROM IFG0194J:                                               *
*           IECRES BRANCH=QUEUED USING DXRETCOD AND DXRETMOD SET IN   *
*           IFG0195K.                                                 *
*                                                                     *
*        FROM IFG0195T:                                               *
*           IECRES BRANCH=QUEUED USING DXRETCOD AND DXRETMOD SET IN   *
*           IFG0195K.                                                 *
*                                                                     *
* INPUT =                                                             *
*        TAPE LABEL 1 VERIFIED.                                       *
*                                                                     *
* OUTPUT =                                                            *
*        TAPE POSITIONED TO FIRST DATA RECORD.                        *
*                                                                     *
* EXIT-NORMAL = IFG0196J JFCB TO DCB MERGE                            *
*               IFG0195N TAPE INPUT USER LABEL PROCESSING             *
*               IFG0195T OPEN/EOV PASSWORD SECURITY INITIALIZATION    *
*               IFG0194J ERROR DISMOUNT MESSAGE                       *
*                                                                     *
* EXIT-ERROR =                                                        *
*    IFG0190P PROBLEM DETERMINATION - RETURN TO IFG0194A              *
*    EXIT WITH ONE OF THE FOLLOWING INTERNAL CODES--                  *
*          9 - 613 ABEND - I/O ERROR ON CONTROL                       *
*         11 - 613 ABEND - I/O ERROR READING LABEL.                   *
*         12 - 613 ABEND - INVALID LABEL READ.                        *
*         20 - 913 ABEND - ASCII SECURITY BYTE NOT BLANK AND NOT      *
*                          INDICATING SECURITY.                       *
*                                                                     *
* EXTERNAL REFERENCES = SEE BELOW                                     *
*                                                                     *
*    ROUTINES = IFG019RA                                              *
*                                                                     *
*    DATA AREAS = STANDARD                                            *
*                                                                     *
*    CONTROL BLOCK = DCB, DSAB, JFCB, ACB                             *
*                                                                     *
* TABLES = STANDARD                                                   *
*                                                                     *
* MACROS = IECEQU, IECRES, DMABCOND, MODESET, EXCP, XCTLTABL, IECDSECS*
*                                                                     *
* CHANGE ACTIVITY = NEW RELEASE (LEVEL 0)                             *
*                                                                     *
*0000522000                                                     SA62427
***********************************************************************
         EJECT
         IECEQU
CHARC    EQU   C'C'                     CHARACTER C              Y02083
*
         USING SRT,RUCB                 ADDRESS OF SRT
         USING FORCORE,RCORE            ADDRESS OF WORK AREA
         USING WTG,RWTG                                          Y02080
*
         BALR  RBASE,0                  SET BASE REG
         USING *,RBASE
*
         B     OTA30400(RET)            GO TO PROPER ENTRY
OTA30400 EQU   *                        BRANCH TABLE
         B     OTA30800                 GO TO SECURITY ENTRY
         B     OTA31100                 BRANCH TO PROB DET       Y02134
         B     OTA32150                 CHKPT VOL SECURE REPLY   YM7006
         B     OTA31400                 RETURN FROM SECURITY     YM7059
         SPACE 1
*                   CHECK DATA SET SECURITY
         SPACE 1
OTA30800 EQU   *                        SECURITY ENTRY
         TM    UCBSTAB,UCBBSTR          IS THIS AN ANSI LABEL
         BZ    OTA31200                 NO, GO TO STANDARD CHECK
         CLC   FL1SYSCD(K5),SYSCODE     CHECK IF OS/360 CREATED
         BNE   OTA30900                 NO, GO TEST SECURITY
         OI    DXCCW3,OSLAB             SET BIT FOR NEXT MODULE
OTA30900 EQU   *                        TEST SECURITY
         CLI   FL1FSEC,BLANK            ANSI ACCESSIBILITY BLANK
         BE    OTA31400                 YES, OK TO USE
         CLI   FL1FSEC,FL1SECTY         PASSWORD PROTECT INDICATED
         BE    OTA31200                 YES, CONTINUE SECURITY CHECK
         CLI   FL1FSEC,FL1WRSEC         WR ONLY PASSWORD PROTECT A53145
         BE    OTA31200                 YES, CONT SECURITY CHK   A53145
*
*   MOUNTED ANSI VOLUME CANNOT BE USED. DISCONTINUE PROCESSING
*   OF THIS DCB
OTA31000 EQU   *
*   INITIALIZE FOR OPEN TAPE ERROR DEMOUNT FUNCTION              M0034
*                                                                Y02134
OABD020  EQU   20                       INTERNAL ABEND CODE      M0034
         LA    R0,OABD020               PASS INT CODE-UNUSEABLE  M0034
*                                       ANSI VOLUME              M0034
         LA    R1,K2                    PASS MSG MODIFIER CHAR   M0034
         LA    RET,K4                   BRANCH TABLE ENTRY FOR   M0034
*                                       ERROR DEMOUNT FUNCTION   M0034
         MVC   DXRETMOD,ID5K5K          RETURN LOAD              Y02134
         MVI   DXRETCOD,K4              RETURN OFFSET            Y02134
*                                                                Y02134
         IECRES LOAD,MODID=ID5K4J,BRCODE=K8,BRANCH=QUEUED        Y02134
         EJECT
* ***
* ***          NOTIFY OPERATOR THAT JOB IS BEING TERMINATED
* ***          DUE TO UNRECOVERABLE I/O ERROR WHILE ATTEMPTING
* ***          TO DO A CONTROL OPERATION
* ***
OTA31100 EQU   *                        PROBLEM DETERMINATION ENTRY
*                                                                Y02134
         MVC   DXSAVMOD,ID5K5B          SAVE RETURN LOAD         Y02134
         MVI   DXRETCOD,K0              RETURN OFFSET            Y02134
*                                                                Y02134
         LA    RET,K24                  RETRY ENTRY OFFSET       Y02134
         DMABCOND (0),ID5K0P,RETURN=ID5K4A
OTA31200 EQU   *                        CONT. SECURITY CHECK
         NI    JFCBIND2,CLRSCTY         CLR SECURITY BITS IN JFCB
         CLI   FL1FSEC,FL1SECTY         IS SECTY IND FOR RD AND WR
         BE    OTA31300                 BR IF YES FOR BOTH RD + WR
         CLI   FL1FSEC,FL1WRSEC         IS SECTY IND FOR WR ONLY
         BNE   OTA31400                 NO, GO TO RD FILE LABEL 2
         OI    JFCBIND2,JFCBRWPW        IND SECTY IS FOR WR ONLY
         TM    K0(RPARC),DEBOPOUT-DEBOPRBK  IS THIS INPUT OR READBACK
         BZ    OTA31400                 BRANCH IF YES
OTA31300 EQU   *                        BOTH RD AND WR
         OI    JFCBIND2,JFCBSCTY
         OI    JFCBMASK+K4,X'84'        SET JFCB MOD AND PASSWORD
*                                                                YM7059
*        GO TO IFG0195T FOR PASSWORD PROCESSING                  YM7059
*                                                                YM7059
         LA    RF,ID5K5T                SECURITY PROCESSING MOD  YM7059
         LA    RET,K0                   INDICATE ENTRY FOR TAPE  YM7059
         MVC   DXRETMOD,ID5K5K          SET UP RETURN MOD        YM7059
         MVI   DXRETCOD,K12             SET UP RETURN OFFSET     YM7059
         B     OTA34500                 GO TO NEXT LOAD          YM7059
*                                                                YM7059
*        READ FILE LABEL 2                                       YM7059
*                                                                YM7059
OTA31400 EQU   *                        READ FILE LABEL 2
         BAL   RB,OTA34700              GO READ LABEL
         BO    OTA31800                 BRANCH IF NO ERROR
         TM    IOBSTAT0,CSWUNITX        UNIT EXCEPTION CAUSE ERROR
         BO    OTA31500                 YES, BRANCH
OABD011  EQU   11                       INTERNAL ABEND CODE      M0034
         LA    R0,OABD011               INT CODE-I/O ERR ON READ M0034
         B     OTA31100                 GO TO PROBLEM DETERMINATION
         SPACE 1
*                   POSITION TAPE TO FIRST DATA RECORD
         SPACE 1
OTA31500 EQU   *                        POSITION TAPE - FIRST DATA
         MVC   DXCCW1,ABSFCCW           SET UP CCW TO BACKSPACE FILE
OTA31600 EQU   *                        GO ISSUE I/O
         BAL   RB,OTA34700              GO PERFORM NON-DATA OPERATION
OABD009  EQU   9                        INTERNAL ABEND CODE      M0034
         LA    R0,OABD009               INT CODE-I/O ERR ON      M0034
*                                       CNTRL                    M0034
         BZ    OTA31100                 IF I/O ERROR BRANCH
         MVI   DXCCW1,CCWNOP            ISSUE NOP
         BAL   RB,OTA34700              WAIT FOR BSF
         B     OTA33900                 GO TO NEXT LOAD
         SPACE 1
OTA31800 EQU   *                        NO ERROR ON I/O
         TM    UCBSTAB,UCBBSTR          IS THIS AN ANSI LABEL
         BZ    OTA31900                 NO, CONTINUE
         XLATE DXLBL,K80                YES, TRANSLATE LABEL DATE
OTA31900 EQU   *                        NOT ANSI LABEL
OABD012  EQU   12                       INTERNAL ABEND CODE      M0034
         LA    R0,OABD012               INT CODE-INVALID LABEL   M0034
         CLC   FL1LABI,AHDR             HDR LABEL
         BE    OTA32100                 YES, GO CHECK TYPE
         CLC   FL1LABI,AEOV             EOV LABEL
         BE    OTA32100                 YES, GO CHECK TYPE
         CLC   FL1LABI,AEOF             EOF LABEL
         BE    OTA32100                 YES, GO CHECK TYPE
         CLC   FL1LABI,UHL              USER HEADER LABEL
         BE    OTA32000                 YES, BRANCH
         CLC   FL1LABI,UTL              USER TRAILER LABEL
         BNE   OTA31100                 GO TO PROBLEM DETERMINATION
OTA32000 EQU   *                        USER HEADER LABEL
         MVI   DXCCW1,CCWBSR            BACKSPACE RECORD
         B     OTA31600                 GO ISSUE I/O
         EJECT
OTA32100 EQU   *                        CHECK LABEL TYPE
         SPACE 1
*                   DETERMINE IF THIS IS A FILE LABEL 2
         SPACE 1
         CLI   FL1NO,CHAR2              IS THIS FILE LABEL 2
         BNE   OTA31100                 NO, GO TO PROBLEM DETERMI
         SPACE 1
         CLI   FL2DSIND,CHARC           CHECK POINT DATA SET     Y02083
         BNE   OTA32200                 BRANCH IF NOT            YM7006
         L     RB,DXDSAB                ADR OF DSAB              YM7006
         USING DSAB,RB                                           YM7006
         TM    DSABFLG4,DSABCKDS+DSABCKVL DSAB CHKPT SECRTY IND  YM7006
         BO    OTA32200                 BRANCH IF ALREADY SECURE YM7006
*                                                                YM7006
*****************************************************************YM7006
*                                                                YM7006
*        CHECK IF RESTART VOL SECURE                             YM7006
*                                                                YM7006
*****************************************************************YM7006
*                                                                YM7006
OTA32130 EQU   *                        IS CHKPT VOLUME SECURE   YM7006
*                                                                YM7006
         XC    DXWORK1,DXWORK1          CLEAR WTOR REPLY AREA    YM7006
         LA    R1,DXWORK1               POINT TO REPLY AREA      YM7006
         LA    R0,K3                    SET REPLY LENGTH         YM7006
         SLL   R0,K24                   MOVE LENGTH TO HI BYTE   YM7006
         OR    R1,R0                    SET UP WTOR PARM         YM7006
*                                                                YM7006
         MVC   DXRETMOD,ID5K5K          SET RETURN NAME & EPA    YM7006
         MVI   DXRETCOD,K8              SET BR TABLE RETURN CODE YM7006
         IECRES LOAD,MODID=ID5K4J,BRCODE=K56,BRANCH=QUEUED       YM7006
*                                                                YM7006
*****************************************************************YM7006
*                                                                YM7006
*        RETURN HERE WITH OPERATOR REPLY                         YM7006
*                                                                YM7006
*****************************************************************YM7006
*                                                                YM7006
OTA32150 EQU   *                        WTOR RETURN              YM7006
*                                                                YM7006
         IECRES WAIT                    WAIT FOR REPLY           YM7006
*                                                                YM7006
         OC    DXWORK1,BLANKS           FOLD REPLY TO UPPER CASE YM7006
         CLC   KYES,DXWORK1             CHECK CORRECT REPLY      YM7006
         BE    OTA32170                 BRANCH TO SET CODE       YM7006
         CLC   KNO,DXWORK1              CHECK CORRECT REPLY      YM7006
         BE    OTA32180                 BRANCH TO SET CODE       YM7006
         B     OTA32130                 RE ISSUE MESSAGE         YM7006
*                                                                YM7006
OTA32170 EQU   *                        DSAB SECURITY INDICATOR  YM7006
*                                                                YM7006
         MODESET EXTKEY=SCHED                                    Y02083
         OI    DSABFLG4,DSABCKDS+DSABCKVL DSAB CHKPT SECRTY IND  YM7006
         MODESET EXTKEY=DATAMGT                                  Y02083
         B     OTA32200                 MERGE HDR2 LABEL         YM7006
*                                                                YM7006
OTA32180 EQU   *                        DSAB SECURITY INDICATOR  YM7006
*                                                                YM7006
         MODESET EXTKEY=SCHED                                    Y02083
         NI    DSABFLG4,ALLBITS-DSABCKDS-DSABCKVL NO CHKPT SCRTY YM7006
         MODESET EXTKEY=DATAMGT                                  Y02083
         DROP  RB
         EJECT
OTA32200 EQU   *                        MERGE FROM FILE LABEL 2  YM7006
         TM    JFCBTSDM,JFCNDSCB        CHECK FULL JFCB SWITCH
         BO    OTA33900                 BR IF ON, NOT TO MERGE
*                   MERGE FROM FILE LABEL 2
         TM    UCBSTAT,UCBDADI          SL TAPE MOUNTED
         BO    OTA32300                 YES, BRANCH
*                                       IF NOT SL, ASSUME AL
         TM    DXCCW3,OSLAB             DID OS/360 CREATE LABEL
         BZ    OTA33900                 NO, BYPASS MERGE
         NI    DXCCW3,X'FF'-UCBDADI     TURN OFF LABEL INDICATOR
         SPACE 1
OTA32300 EQU   *                        SL TAPE
         TM    JFCRECFM,ALLBITS-DCBRECKY RECFM SPEC IN JFCB?    SA65463
         BNZ   OTA33200                 YES,DON'T MERGE FIELD   SA65463
         CLI   FL2RECFM,C'F'            NO,IS RECORD FORM SPEC AS F
         BC    6,OTA32400               NO,GO SEE IF SPEC AS UNKNOWN
         OI    JFCRECFM,JFCFIX          YES, SET RECFM AS FIXED
         B     OTA32700                 BRANCH TO CHECK BLOCKING
OTA32400 EQU   *                        CHECK RECORD FORMAT = UNKNOWN
         CLI   FL2RECFM,CHARU           IS RECORD FORMAT UNKNOWN
         BC    6,OTA32500               NO,RECORD FORMAT MUST BE V
         OI    JFCRECFM,DCBRECUN        YES,SET RECFM TO U-FORMAT
         B     OTA33000                 RECORD FORMAT MERGE IS CO
OTA32500 EQU   *                        RECORD FORMAT MUST BE V
         CLI   FL2RECFM,DCBRECUN+DCBRECCA  TEST FOR D TYPE FORMAT
         BNE   OTA32600                 NO, GO SET VARIABLE TYPE
         OI    JFCRECFM,JFCVARD         SET REFM AS D
         B     OTA33000                 RECORD FORMAT MERGE COMPLETE
OTA32600 EQU   *                        SET TYPE VARIABLE
         OI    JFCRECFM,JFCVAR          SET RECORD FORMAT TO V
*
*                   BLOCKING ATTRIBUTE
OTA32700 EQU   *                        CHECK BLOCKING
*
         TM    JFCRECFM,JFCRFB+JFCRFS   IS BLK ATTRIB SPECIFIED IN
         BC    5,OTA33000               YES, DO NOT MERGE
         CLI   FL2BLKA,BLANK            IS BLOCKING ATTRIBUTE BLANK
         BE    OTA33000                 YES, INDIC NOT BLKD OR SPND
         CLI   FL2BLKA,C'B'             IS BLOCKING ATTRIBUTE B
         BNE   OTA32800                 NO, BR TO CHK FOR SPANNED
         OI    JFCRECFM,JFCRFB          SET TO BLOCKED
         B     OTA33000                 MERGE COMPLETE
OTA32800 EQU   *                        CHECK FOR SPANNED
         CLI   FL2BLKA,C'S'             IS BLOCKING ATTRIBUTE S
         BNE   OTA32900                 NO, BR TO CHK FOR BLKD & SPND
         OI    JFCRECFM,JFCRFS          SET BLKA TO SPANNED
         B     OTA33000                 BLK ATTRIB MERGE COMPLETE
OTA32900 EQU   *                        CHECK FOR BLKD AND SPND
         OI    JFCRECFM,JFCRFB+JFCRFS   SET BLK ATTRIB TO BLK & SPAND
         SPACE 1
*                   CARRIAGE CONTROL CHARACTER SET
         SPACE 1
OTA33000 EQU   *                        CARRIGAE CONTROL CHAR SET
         TM    JFCRECFM,DCBRECCA+DCBRECCM  IS CARRIAGE CONTROL CHARACTE
         BC    5,OTA33200               YES,DO NOT MERGE THIS FIELD
         CLI   FL2CNTRL,CHARA           ASA CARRIAGE CONTROL CHAR
         BC    6,OTA33100               NO,GO SEE IF MACHINE CODE CHAR
         OI    JFCRECFM,DCBRECCA        YES,SET JFCB FOR ASA CHAR
OTA33100 EQU   *                        IS IST MACHINE CODE
         CLI   FL2CNTRL,CHARM           MACHINE CODE CONTROL CHAR
         BC    6,OTA33200               NO,THIS FIELD NOT TO BE MERGED
         OI    JFCRECFM,DCBRECCM        YES,SET MACHINE CODE IN JFCB
         SPACE 1
*                   BLOCK LENGTH AND LOGICAL RECORD LENGTH
         SPACE 1
OTA33200 EQU   *                        CHECK BLOCK LENGTH
         CLC   JFCBLKSI,AZERO           IS BLOCK LENGTH SPEC IN JFCB
         BC    6,OTA33300               YES,DO NOT MERGE THIS FIELD
         PACK  DXCCW2,FL2BLKL           NO,CONVERT IT FROM UNPACKED
         CVB   RB,DXCCW2                CONVERT IT TO BINARY
         STH   RB,JFCBLKSI              PLACE BLOCK LENGTH INTO JFCB
OTA33300 EQU   *                        CHECK LOGICAL RECORD LENGTH
         CLC   JFCLRECL,AZERO           IS LOGICAL REC LENGTH SPE
         BC    6,OTA33500               YES,DO NOT MERGE LRECL
         CLC   FL2LRECL(K5),HRCKF9      DOES LABEL INDIC GRTR 32K
         BNE   OTA33400                 NO, CONTINUE MERGE
         OI    JFCLRECL,X'80'           SET JFCB TO INDIC GRTR 32K
         B     OTA33500                 BR, MERGE COMPLETE
OTA33400 EQU   *                        CONTINUE MERGE
         PACK  DXCCW2,FL2LRECL          NO,CONVERT IT FROM UNPACKED
         CVB   RB,DXCCW2                CONVERT IT TO BINARY
         STH   RB,JFCLRECL              PLACE LOG REC LENGTH IN JFCB
         SPACE 1
*                   TAPE RECORDING TECHNIQUE
         SPACE 1
OTA33500 EQU   *                        TAPE RECORDING TECHNIQUE
         CLI   JFCTRTCH,K0              IS RECORDING TECH IN JFCB
         BC    6,OTA33600               YES,DO NOT MERGE TO JFCB
         CLI   FL2TRTCH,BLANK           NO,RECORDING TECH IN LABEL
         BE    OTA33600                 NO,NO MERGING TO BE PERFORMED
         CLI   FL2TRTCH,C'C'            YES,WAS DATA CONVERSION USED
         MVI   JFCTRTCH,JFCCONV         SET BITS TO IND DATA CONV
         BZ    OTA33600                 YES, MERGE IS COMP
         CLI   FL2TRTCH,CHART           BCDIC TO EBCDIC TRANSLATION
         MVI   JFCTRTCH,JFCTRAN         SET BITS TO IND TRANSLATION
         BZ    OTA33600                 YES,TAPE RECORD TECH MERGE
         MVI   JFCTRTCH,JFCEVEN         NO,SET BITS FOR EVEN PARITY
         CLI   FL2TRTCH+K1,CHART        BOTH EVEN PARITY + TRANSL
         BC    6,OTA33600               NO,TAPE RECORD TECH MERGE COMP
         MVI   JFCTRTCH,DCBTRTET        YES,ALSO SET TRANSLATION BITS
*
*                  BUFFER OFFSET (ANSI ONLY)
*
OTA33600 EQU   *                        BUFFER OFFSET
         TM    UCBSTAB,UCBBSTR          TEST FOE ANSI LABEL
         BZ    OTA33900                 NO, CHECK FOR USER LABELS
         TM    JFCBUFOF,DCBUSAON        BUFOFF = L SPECIFIED
         BNO   OTA33800                 NO, CHECK BUFF. OFFSET
         OI    JFCBUFOF,K4              SET BUFF. OFFSET = 4
OTA33800 EQU   *                        CONT. BUFF OFF TEST
         TM    JFCBUFOF,X'FF'-DCBUSAON  TEST IF LENGTH SPECIFIED
         BM    OTA33900                 YES, DO NOT MERGE
         PACK  DXCCW2,FL2BUFOF          PACK BUFFER OFFSET LENGTH
         CVB   RB,DXCCW2                CONVERT TO BINARY
         STC   RB,DXCCW2                TEMP. STORE OFFSET LENGTH
         OC    JFCBUFOF,DXCCW2          SET BUFF. OFFSET LENGTH
*  IF TAPE HAS 6250 BPI, CHECK LABEL FOR ID OF CREATING TAPE UNIT.
*  IF PRESENT,PLACE ID IN THE UCB EXTENSION.
         CLI   UCBTBYT4,UCB3400         TEST FOR 3400 DRIVE      YM1491
         BNE   OTA33900                 NO,BYPASS TAPE UNIT ID   YM1491
         CLI   FL2ID+K4,HEX40           CHECK FOR BLANK FIELD     99223
         BE    OTA33850                 BRANCH IF BLANK           99223
************************************************************** @ZA10282
*           RETRIEVE THE LOW ORDER 4 DIGITS OF THE TAPE      * @ZA10282
*           DRIVE SERIAL NUMBER, CONVERT IT TO BINARY        * @ZA10282
*           AND PLACE IT IN THE UCB EXTENTION.               * @ZA10282
************************************************************** @ZA10282
         PACK  DXCCW2,FL2ID+1(K4)       RETRIEVE FROM LBL      @ZA10282
         CVB   RB,DXCCW2                CONVERT TO HEX         @ZA10282
         B     OTA33870
OTA33850 EQU   *
         SR    RB,RB                    CLEAR RB               @ZA10282
OTA33870 EQU   *
         L     RC,UCBXTN                GET EXT ADDRESS           99223
         MODESET EXTKEY=SUPR            UCB KEY                   99223
         USING UCBMT,RC                                           99223
         STH   RB,UCBCTD                PUT ID IN THE UCB EXTENT  99223
         DROP  RC                                                 99223
         MODESET EXTKEY=DATAMGT         DM  KEY                   99223
*
OTA33900 EQU   *                        USER LABEL TEST
         TM    JFCBLTYP,JFCBUL          WAS USER LABEL SPECIFIED
         BNO   OTA34100                 NO, DO NOT CHECK FRO USR LBLS
         LA    RF,ID5K5N                USER LABEL MOD IDTTR
         B     OTA34500                 GO TO USER LABEL MOD
*                   POSITION TAPE TO FIRST DATA RECORD
         SPACE 1
OTA34100 EQU   *                        POSITION TAPE
         MVC   DXCCW1,ABSFCCW           SET UP CCW TO BACKSPACE FILE
         TM    0(RPARC),DEBOPIO         DCB BEING OPENED FOR READBACK
         BM    OTA34300                 YES,GO BYPASS TM PRECEDING
         TM    K0(RPARC),DEBOPOIN       OUTPUT OR OUTIN
         BO    OTA34300                 YES, BRANCH
         MVI   DXCCW1,CCWFSF            BYPASS TM FOLLOWING HDRS
         SPACE 1
         SPACE 1
OTA34300 EQU   *                        OUTPUT OR OUTIN
         BAL   RB,OTA34700              GO PERFORM NON-DATA OPERATION
         LA    R0,OABD009               INT CODE-I/O ERR ON      M0034
*                                       CNTRL                    M0034
         BC    12,OTA31100              IF I/O ERROR,GO NOTIFY THE
         MVI   DXCCW1,CCWNOP            ISSUE NOP
         BAL   RB,OTA34700              WAIT FOR FSF OR BSF
         LA    R0,OABD009               INT CODE-I/O ERR ON      M0034
*                                       CNTRL                    M0034
         BC    12,OTA31100              I/O ERROR, NOTIFY OPERATOR
         SPACE 1
*                   EXIT TO NEXT LOAD
         SPACE 1
         LA    RET,K0
         LA    RF,ID5K6J                MERGE MODULE IDTTR
OTA34500 EQU   *                        GO TO NEXT LOAD
         IECRES LOAD,MODID=(RF),BRCODE=(RET),BRANCH=QUEUED       Y02080
*
*                   CLOSED SUBROUTINE TO ISSUE I/O OPERATIONS
         SPACE 1
*                        IT IS ASSUMED UPON ENTRY THAT ALL CONTROL
*                             BLOCKS HAVE ALREADY BEEN SET UP
         SPACE 1
*                             BAL  RA,HRCK80
*                             RETURN
         SPACE 1
OTA34700 EXCP  DXIOB                    ISSUE I/O OPERATION
         IECRES WAIT
         TM    DXECB,ECBNOERR           IS THERE AN UNUSUAL I/O COND
         BCR   15,RB                    RETURN TO CALLER
         SPACE 1
*                   CONSTANTS
         SPACE 1
         DS    0F
ABSFCCW  DC    X'2F00000020000001'      BACKSPACE FILE
AZERO    DC    X'0000'                  ZERO
AHDR     DC    C'HDR'                   HEADER
AEOV     DC    C'EOV'                   END OF VOLUME
AEOF     DC    C'EOF'                   END OF FILE
UHL      DC    C'UHL'                   USER HEADER LABEL
UTL      DC    C'UTL'                   USER TRAILER LABEL
HRCKF9   DC    X'F9F9F9F9F9'            HEADER LABEL CHECK FOR GT 32K
SYSCODE  DC    C'OS360'                 SYSTEM CODE FOR ANSI
KYES     DC    C'YES'                   CHKPT SECURE VOL REPLY   YM7006
KNO      DC    C'NO'                    CHKPT SECURE VOL REPLY   YM7006
BLANKS   DC    CL4' '                   FOLD REPLY BUFFER        YM7006
*
XCTL5K   XCTLTABL ID=(ID5K4J,4J,ID5K4A,4A,ID5K5N,5N,ID5K0P,0P,   Y02080X
               ID5K6J,6J,ID5K5T,5T,ID5K5B,5B,ID5K5K,5K),         Y02080X
               BRT=YES,LENGTH=                                   Y02080
         IECDSECS UCB,MAIN,DCB,WTG,PREFX,DSAB,EXPAND=YES         Y02083
         END
