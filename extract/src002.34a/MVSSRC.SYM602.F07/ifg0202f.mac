         TITLE 'IFG0202F - VOLUME DISPOSITION'
IFG0202F CSECT
***********************************************************************
*                                                                     *
*                                                                     *
*          RELEASE 21 DELETIONS/CHANGES                               *
*0000                                                            M1767
*0000018000,025000,028000,031000,087000,094000-166000,187000,    A38013
*0000192000-200000,488000,495000,608000,625000                   A38013
*                                                               SA55570
*                                                                     *
* MODULE NAME = IFG0202F                                              *
*                                                                     *
* DESCRIPTIVE NAME = VOLUME DISPOSITION                               *
*                                                                     *
* COPYRIGHT = NONE                                                    *
*                                                                     *
* STATUS CHANGE LEVEL 000                                             *
*                                                                     *
* FUNCTION -                                                          *
*    THIS MODULE CONTAINS THE FUNCTION(S) OR PART(S) OF FUNCTION(S)-- *
*    CLOSE TAPE VOLUME DISPOSITION FUNCTION.                          *
*    REFER TO THE FOLLOWING 'FUNCTION PROLOG(S)' FOR DETAILS.         *
*                                                                     *
* ENTRY POINTS -                                                      *
*         IFG0202F - ENTRY POINT VIA AN XCTL FROM THE FOLLOWING--     *
*             INITIAL ENTRY--                                         *
*                IFG0200Y - PROCESS CLOSE TAPE VOLUME DISPOSITION     *
*                IFG0202A   FUNCTION.                                 *
*             SECOND ENTRY--                                          *
*                IFG0202B - PROCESS CLOSE TAPE VOLUME DISPOSITION     *
*                           FUNCTION.                                 *
*             THIRD ENTRY--                                           *
*                IFG0200Z - PROCESS CLOSE TAPE VOLUME DISPOSITION     *
*                           FUNCTION.                                 *
*             FOURTH ENTRY--                                          *
*                IFG0202H - PROCESS CLOSE TAPE VOLUME DISPOSITION     *
*                           FUNCTION.                                 *
*                                                                     *
* INPUT -                                                             *
*    ENTERED IN DATA MANAGEMENT KEY                              Y02082
*    REFER TO THE FOLLOWING 'FUNCTION PROLOG(S)'.                     *
*                                                                     *
* OUTPUT -                                                            *
*    EXIT IN DATA MANAGEMENT KEY                                 Y02082
*    DATA MANAGEMENT COUNT DECREMENTED                           YM6559
*    REFER TO THE FOLLOWING 'FUNCTION PROLOG(S)'.                     *
*                                                                     *
* EXTERNAL REFERENCES -                                               *
*         IFG019RA - OPEN/CLOSE/EOV RESIDENT ROUTINE FOR XCTL AND     *
*                    WAIT.                                            *
*    REFER TO THE FOLLOWING 'FUNCTION PROLOG(S)'.                     *
*                                                                     *
* EXITS, NORMAL -                                                     *
*    EXIT VIA THE RESIDENT ROUTINE XCTL TO THE FOLLOWING--            *
*         IFG0202H - PROCESS CLOSE/EOV SMF DATA SET FUNCTION.         *
*         IFG0202J - PROCESS CLOSE FINAL STRING.                      *
*    REFER TO THE FOLLOWING 'FUNCTION PROLOG(S)'.                     *
*                                                                     *
* EXITS, ERROR -                                                      *
*    EXIT IS TO THE PROBLEM DETERMINATION MODULE IFG0200P, IF AN      *
*    ABEND SITUATION OCCURS IN THIS MODULE.  REFER TO THE FOLLOWING   *
*    'FUNCTION PROLOG(S)'.                                            *
*                                                                     *
* TABLES/WORK AREAS -                                                 *
*    REFER TO THE FOLLOWING 'FUNCTION PROLOG(S)'.                     *
*                                                                     *
* ATTRIBUTES -                                                        *
*    REENTRANT, REFRESHABLE, READ-ONLY, ENABLED, PRIVILEGED           *
*                                                                     *
* CHARACTER CODE DEPENDENCY -                                         *
*    CLASS ONE CHARACTER CODE DEPENDENCY - THE EBCDIC CHARACTER CODE  *
*    WAS USED FOR ASSEMBLY.  THE MODULE MUST BE REASSEMBLED IF A      *
*    DIFFERENT CHARACTER SET IS USED FOR EXECUTION.                   *
*                                                                     *
* NOTES -                                                             *
*    REFER TO THE FOLLOWING 'FUNCTION PROLOG(S)'.                     *
*                                                                     *
***********************************************************************
         EJECT
         USING IHADCB,RDCB
         USING FORCORE,RCORE
         USING TCB,RET
         USING UCBOB,RUCB
         USING CVT,RD
         USING TIOENTRY,RTIOT
         USING DEB,RDEB
         USING WTG,RWTG                                          A38013
         USING WTGENTRY,RWTGC                                    A38013
         BALR  RBASE,0                  ESTABLISH BASE REGISTER
         USING *,RBASE
*
         MVI   DXCCW5+4,K0              ZERO INDICATOR USED      M1767
*                                       IN DETERMINING IF        M1767
*                                       CLOSE SHOULD ABEND       M1767
         B     CTA06600(RET)            USE BRANCH TABLE
CTA06600 EQU   *
         B     CTA06800                 ENTRY FOR VOL DISP.
         B     CTA08800                 ENTRY FROM NSL RTN
         B     CTA06700                 ENTRY FROM SL RTN        A38013
         B     CTA06850                 RETURN FROM SMF RTN      A38013
         SPACE 3                                                 A38013
CTA06700 EQU   *                                                 A38013
         IECRES WAIT                    WAIT FOR TRL2
         TM    IOBSTAT0,CSWUNITX        UNIT EXCEPTION
         BO    CTA06750                 BRANCH IF YES TO CONTINUEY02144
         TM    DXECB,ECBNOERR           TEST FOR PERM I/O ERROR
         BC    12,CTA10600              BRANCH IF ERROR
CTA06750 EQU   *                        INDICATE LABELS WRITTEN  Y02144
         OI    DXATOUTA,DXATTRL2        SHOW TRL2 WRITTEN        Y02144
         EJECT
*                                                                     *
*                          FUNCTION PROLOG                            *
*                                                                     *
***********************************************************************
*                                                                     *
* FUNCTION NAME -                                                     *
*    CLOSE TAPE VOLUME DISPOSITION FUNCTION.                          *
*                                                                     *
* (STATUS) -                                                          *
*    NOT APPLICABLE                                                   *
*                                                                     *
* FUNCTION -                                                          *
*    INTERFACE WITH SMF DATA SET FUNCTION IF THE DATA SET IS NOT      *
*    SYSIN OR SYSOUT.                                                 *
*    ISSUE SVC 91 (VOLSTAT) IF THE VOLUME STATISTICS FEATURE IS IN    *
*    EFFECT.                                                          *
*    DETERMINE DISPOSITION SPECIFIED AND POSITION TAPE ACCORDINGLY.   *
*    IF THERE IS NO CLOSE DISPOSITION, THE DISPOSITION IN THE TASK    *
*    INPUT/OUTPUT TABLE IS TAKEN.  THE TAPE IS REWOUND OR REWOUND AND *
*    UNLOADED AND AN APPROPRIATE MESSAGE IS ISSUED. IF NO TIOT        *
*    DISPOSITION IS SPECIFIED, A LEAVE DISPOSITION IS ASSUMED.        *
*                                                                     *
* ENTRY POINTS -                                                      *
*    ENTERED FROM THE FOLLOWING--                                     *
*    CLOSE TAPE ACCESS METHOD EXECUTOR INTERFACE FUNCTION.            *
*    CLOSE TAPE STANDARD TRAILER LABEL FUNCTION.                      *
*    CLOSE TAPE STANDARD USER LABEL FUNCTION.                         *
*    CLOSE/EOV SMF DATA SET FUNCTION.                                 *
*    CLOSE TAPE NONSTANDARD LABEL FUNCTION.                           *
*                                                                     *
* INPUT -                                                             *
*    A POINTER TO EACH OF THE FOLLOWING--                             *
*       CURRENT PARAMETER LIST ENTRY- RPARC                           *
*       DD ENTRY IN THE TIOT- RTIOT                                   *
*       WTG TABLE- RWTG                                               *
*       CURRENT WTG TABLE ENTRY- RWTGC                                *
*       DCB- RDCB                                                     *
*       WORK AREA- RCORE                                              *
*       RESIDENT ROUTINE- RES                                         *
*       UCB- RUCB                                                     *
*       DEB- RDEB                                                     *
*                                                                     *
* OUTPUT -                                                            *
*    TAPE POSITIONED ACCORDING TO DISPOSITION.                        *
*                                                                     *
* EXTERNAL REFERENCES -                                               *
*    REFER TO THE PRECEEDING MODULE PROLOG.                           *
*                                                                     *
* EXITS, NORMAL -                                                     *
*    REFER TO THE PRECEEDING MODULE PROLOG.                           *
*    CLOSE FINAL STRING.                                              *
*                                                                     *
* EXITS, ERROR -                                                      *
*    REFER TO THE PRECEEDING MODULE PROLOG.                           *
*    EXIT WITH THE FOLLOWING INTERNAL CODE--                          *
*       65 - 214 ABEND - ERROR IN POSITIONING THE TAPE.               *
*                                                                     *
* TABLES/WORK AREAS -                                                 *
*    THE OPEN, CLOSE, OR EOV WORK AREA AND THE WHERE-TO-GO (WTG)      *
*    TABLE ARE DESCRIBED BY THE DSECTS AT THE END OF THE LISTING.     *
*                                                                     *
* ATTRIBUTES -                                                        *
*    REFER TO THE PRECEEDING MODULE PROLOG.                           *
*                                                                     *
* CHARACTER CODE DEPENDENCY -                                         *
*    REFER TO THE PRECEEDING MODULE PROLOG.                           *
*                                                                     *
* NOTES -                                                             *
*    NOT APPLICABLE                                                   *
*                                                                     *
***********************************************************************
         EJECT
CTA06800 EQU   *                                                 A38013
         TM    WTGPATHS,WTGSMF          SMF REQUESTED            A38013
         BZ    CTA06850                 BRANCH IF NOT            A38013
         SR    RET,RET                  SET BRANCH OFFSET        A38013
         LA    RF,SMFMOD                SET UP FOR XCTL TO SMF   A38013
         MVC   DXRETMOD,VOLDPMOD        MOVE IN RETURN IDTTR     A38013
         B     CTA09525                 BRANCH TO SMF STRING     YM6559
         SPACE 3                                                 A38013
CTA06850 EQU   *                                                 A38013
         L     RDEB,DCBDEBAD            DEB ADDRESS
         MVC   DXCCW1(K16),ABSFCCW      SET UP TWO CCW'S
         MVC   DXDEBUCB(K6),DEBUCBAD    USER'S UCB ADDR TO CLOSE DEB
         TM    UCBFL1,UCBNOTRD          IS DEVICE READY
         BO    CTA09400                 NO, BRANCH TO FINAL PROCESSING
*
         MODESET EXTKEY=SUPR            UCB/TIOT KEY             Y02082
         NI    UCBWGT,X'FF'-UCBREW      INSURE REWIND BIT OFF    Y02080
         MODESET EXTKEY=DATAMGT         DATA MANAGEMENT KEY      Y02082
         TM    DCBOFLGS,DCBOWRIT        IS TRAILER SWITCH ON
         BNO   CTA07000                 BR IF TRLR SWITCH OFF
         EJECT
*
*****   OUTPUT TAPE PROCESSING
*
*
*        WRITE TWO TAPE MARKS
*        FOR STANDARD LABEL, TAPE MARKS FOLLOW LABEL
*        FOR UNLABELED TAPE, TAPE MARKS FOLLOW DATA
*
         MVI   DXCCW1,CCWWTM            SET UP CCW FOR WTM
         MVI   DXCCW1+K4,CCWCMDCH+CCWSILI  CHAIN A NOP
         MVI   DXCCW2,CCWNOP            SET CCW FOR NOP
         LA    RD,K1                    LOAD COUNT FOR BSF
         BAL   RC,CTA10200              WRITE FIRST TM
         OI    DXATOUTA,DXATTTM1        SHOW 1 OF 2 TM'S WRITTEN Y02144
         BAL   RC,CTA10200              WRITE SECOND TM
         OI    DXATOUTA,DXATTTM2        SHOW 2 OF 2 TM'S WRITTEN Y02144
         LA    RD,K1                    LOAD COUNT FOR BSF
         BAL   RC,CTA09550              BRANCH TO EXECUTE CHANNEL PGM
*
         TM    PLISTOPT(RPARC),PLISTLV  CK FOR LEAVE DISPOSITION
         BO    CTA08700                 BRANCH IF LEAVE          YM7526
         TM    PLISTOPT(RPARC),PLISTRRD CK FOR REREAD DISPSITION YM7526
         BO    CTA06900                 BRANCH IF REREAD
         TM    PLISTOPT(RPARC),CLOSEREW TEST FOR REWIND          YM7526
         BO    CTA09300                 BRANCH IF REWIND         YM7526
         TM    UCBJBNR,UCBMONT          TEST FOR PASS            YM7526
         BO    CTA08700                 BRANCH IF PASS           YM7526
         B     CTA09300                 BRANCH IF DISP/FREE      YM7526
*
CTA06900 EQU   *
*
         LA    RC,K1                    SET COUNT TO ONE         YM7526
         CH    RC,UCBFSCT               CHECK SEQ FOR FIRST FILE
         BNL   CTA09300                 BR IF ON FIRST           YM7526
         B     CTA08300                 BRANCH IF NOT FIRST FILE
         EJECT
*
*****     INPUT TAPE PROCESSING
*
CTA07000 EQU   *
*
         TM    PLISTOPT(RPARC),PLISTLV  CK FOR LEAVE DISPOSITION
         BO    CTA07100                 BR IF LEAVE SPECIFIED
         TM    PLISTOPT(RPARC),PLISTRRD  CK FOR REREAD DISPOSITION
         BO    CTA07900                 BRANCH IF REREAD
         TM    PLISTOPT(RPARC),CLOSEREW TEST FOR REWIND          YM7526
         BO    CTA09300                 BRANCH IF REWIND SPECFD  YM7526
         TM    UCBJBNR,UCBMONT          TEST FOR DISP=PASS       YM7526
         BNO   CTA09300                 BRANCH IF NOT PASS       YM7526
*
**********************************************************************
*
*****         LEAVE SPECIFIED
*
CTA07100 EQU   *
*
         TM    DCBOFLGS,DCBORDBK        TEST FOR READ BACKWARD
         BO    CTA07600                 BR IF READ BACKWARD
         TM    DEBOFLGS,DEBOFEOF        TEST IF DEB EOF ON
         BO    CTA08700                 YES, BR OUT
*
*****     IF TAPE MARK SENSED, 0 FSF FOR UNLABELED TAPE
*****                          1 FSF FOR STANDARD LABELED TAPE
*****     IF TAPE MARK NOT SENSED, 1 FSF FOR UNLABELED TAPE
*****                              2 FSF FOR STANDARD LABELED TAPE
*
         SR    RD,RD                    SET COUNT TO ZERO
         TM    DCBOFLGS,DCBOEOF         CK FOR A TAPE MARK
         BO    CTA07300                 BR IF TM SENSED
*
CTA07200 EQU   *
*
         LA    RD,K1(RD)                INCREMENT COUNT BY 1
*
CTA07300 EQU   *
*
         TM    JFCBLTYP,JFCBAL+JFCSL    TEST FOR SL AND AL
         BZ    CTA07500                 BR IF NOT SL OR AL
         LA    RD,K1(RD)                INCREMENT COUNT BY 1
CTA07400 EQU   *
         LA    RC,CTA08700              SET RETURN ADDR FOR I/O RTN
         B     CTA09600                 BR TO FSF RTN
*
CTA07500 EQU   *                        POSITION TAPE
*
         LTR   RD,RD                    IS COUNT ZERO
         BZ    CTA08700                 BR IF YES, DO NOT GO TO FSF
         B     CTA07400                 BR TO FSF RTN
*
*****          READ BACKWARDS
*
CTA07600 EQU   *
*
         TM    DEBOFLGS,DEBOFEOF        TEST IF DEB EOF ON
         BO    CTA07800                 YES, BR TO CK LABEL
         TM    DCBOFLGS,DCBOEOF         WAS A TAPE MARK READ
         BO    CTA07700                 YES, BR TO CK LABEL
         LA    RD,K1                    SET COUNT TO 1
         MVI   DXCCW5+K4,K1             SET SW NOT TO ABEND AFTER NOP
         BAL   RC,CTA09550              BR TO BSF RTN
*
CTA07700 EQU   *
*
         TM    JFCBLTYP,JFCBAL+JFCSL    TEST FOR SL AND AL
         BZ    CTA08500                 BRANCH IF NOT STD OR ANSI
         TM    IOBSENS1,UCBLDPT         CK FOR BOT
         BO    CTA10400                 BR TO ABEND-POSITIONING ERROR
         LA    RD,K1                    SET COUNT TO 1
         B     CTA08400                 BR TO BSF RTN
*
*****         EOF BIT ON
CTA07800 EQU   *
         TM    JFCBLTYP,JFCBAL+JFCSL    TEST FOR SL AND AL
         BNZ   CTA09400                 YES, BRANCH OUT
         TM    DCBOFLGS,DCBOEOF         WAS A TAPE MARK READ
         BO    CTA08600                 YES, BR TO POSITION TAPE
         B     CTA09400                 BRANCH TO DO NOT PROCESS
*
**********************************************************************
*
*****          REREAD SPECIFIED
*
CTA07900 EQU   *
*
         TM    DCBOFLGS,DCBORDBK        IS IT READ BACKWARD
         BO    CTA08200                 BRANCH IF YES
         LA    RC,K1                    SET COUNT TO ONE
         CH    RC,UCBFSCT               CHECK SEQ FOR FIRST FILE
         BE    CTA09300                 BRANCH IF FIRST FILE     YM7526
         TM    DEBOFLGS,DEBOFEOF        TEST IF DEB EOF ON
         BO    CTA08300                 YES, BR TO CK LABEL
*
*****          NOT FIRST FILE
*
*****     IF TAPE MARK SENSED, 2 BSF FOR UNLABELED TAPE
*****                          3 BSF FOR STANDARD LABELED TAPE
*****     IF TAPE MARK NOT SENSED, 1 BSF FOR UNLABELED TAPE
*****                              2 BSF FOR STANDARD LABELED TAPE
*
*
         LA    RD,K1                    SET COUNT TO 1
         TM    DCBOFLGS,DCBOEOF         WAS A TAPE MARK READ
         BZ    CTA08000                 BR IF NO TAPE MARK WERE READ
         LA    RD,K1(RD)                INCREMENT COUNT BY 1
*
CTA08000 EQU   *
*
         TM    JFCBLTYP,JFCBAL+JFCSL    TEST FOR SL AND AL
         BZ    CTA08100                 BR IF NOT SL OR AL
*
*
         LA    RD,K1(RD)                INCREMENT COUNT BY 1
CTA08100 EQU   *
         B     CTA08400                 BR TO BSF RTN
*
*****          READ BACKWARDS
*
CTA08200 EQU   *
*
*
*****     IF TAPE MARK SENSED, 2 FSF FOR UNLABELED TAPE
*****                          3 FSF FOR STANDARD LABELED TAPE
*****     IF TAPE MARK NOT SENSED, 1 FSF FOR UNLABELED TAPE
*****                              2 FSF FOR STANDARD LABELED TAPE
*
         LA    RD,K1                    SET COUNT TO 1
         TM    DCBOFLGS,DCBOEOF         WAS A TAPE MARK READ
         BNO   CTA07300                 BR IF NOT
         B     CTA07200                 BR TO CHECK LABEL
         EJECT
         SPACE 1
*
*****          FUNCTIONS USED BY BOTH INPUT AND OUTPUT PROCESSING
*
CTA08300 EQU   *
*
*****     2 BSF FOR UNLABELED TAPE
*****     4 BSF FOR STANDARD LABELED TAPE
*
         LA    RD,K2                    SET COUNT TO 2
         TM    JFCBLTYP,JFCBAL+JFCSL    TEST FOR SL AND AL
         BZ    CTA08400                 BR IF NOT SL OR AL
         LA    RD,K2(RD)                INCREMENT COUNT BY 2
CTA08400 EQU   *
         MVI   DXCCW5+K4,K1             SET SW NOT TO ABEND AFTER NOP
         BAL   RC,CTA09550              BRANCH TO BSF ROUTINE
CTA08500 EQU   *
         TM    IOBSENS1,UCBLDPT         CK FOR LOAD POINT
         BO    CTA09400                 BRANCH IF AT LOAD POINT
CTA08600 EQU   *
         LA    RD,K1                    SET COUNT TO 1
         LA    RC,CTA09400              SET RETURN ADDR FOR I/O RTN
         B     CTA09600                 GO TO FSF RTN
*
*        INCREMENT FILE SEQUENCE NUMBER IN SRT
*
CTA08700 EQU   *
*
         LH    RF,UCBFSEQ               GET FILE SEQUENCE NUMBER
         LA    RF,K1(,RF)               INCREMENT FIL SEQ COUNTER
         MODESET EXTKEY=SUPR            UCB/TIOT KEY             Y02082
         STH   RF,UCBFSEQ               PUT BACK UPDATED FILE SEQUENCE
         LH    RF,UCBFSCT               GET FIL SEQ COUNT
         LA    RF,K1(,RF)               INCR FIL SEQ COUNT
         STH   RF,UCBFSCT               PUT BACK UPDATED FIL SEQ COUNT
         MODESET EXTKEY=DATAMGT         DATA MANAGEMENT KEY      Y02082
         B     CTA09400                 BRANCH TO FINAL ROUTINE
*
CTA08800 EQU   *
*
*        TEST FOR CLOSE REWIND OPTION-USASI COBOL
*
         TM    PLISTOPT(RPARC),CLOSEREW  CHECK FOR REWIND OPTION
         BO    CTA09300                 BRANCH IF REWIND         YM7526
         TM    UCBJBNR,UCBMONT          TEST FOR DISP=PASS       YM7526
         BO    CTA09400                 BRANCH IF PASS           YM7526
*
*****          FIRST FILE
*
CTA09300 EQU   *
*
         MODESET EXTKEY=ZERO            UCB KEY                  YM7834
*                                                                YM7834
         XC    UCBFSCT,UCBFSCT          FORCE REWIND             YM7834
         XC    UCBFSEQ,UCBFSEQ          CLEAR FILE COUNTERS      YM7834
*                                                                YM7834
         MODESET EXTKEY=DATAMGT         DM KEY                   YM7834
*                                                                YM7834
         MVC   DXVOLSR1,UCBVOLI         SETUP VOLSER             YM7526
         MVC   DXRETMOD,FINALMOD        RETURN TO FINAL LOADS    Y02134
         MVI   DXRETCOD,K0              SET UP RETURN OFFSET     Y02134
         LA    RET,K4                   LOAD VOLUME DISP OFFSET  Y02134
         LA    RF,ID2F4A                LOAD VOL DISP ROUTINE    Y02134
         B     CTA09500                 BRANCH TO XCTL
*
CTA09400 EQU   *
         LA    RF,FINALMOD              LOAD ADDR OF FINAL LOAD
         XR    RET,RET                  SET ENTRY OFFSET         YM7526
CTA09500 EQU   *
         MODESET EXTKEY=SUPR            UCB KEY                  YM6559
         MVI   UCBDMCT,K0               ZERO DATA MANAGEMENT CT  YM6559
*                                       BEFORE GOING TO 4A OR 2J YM6559
         MODESET EXTKEY=DATAMGT         RETURN TO D. M. KEY      YM6559
         OI    DXATEOV,DXATNVOL         INDICATOR TO RECOVERY    YM6559
CTA09525 EQU   *                        TRANSFER CONTROL         YM6559
         IECRES LOAD,MODID=(RF),BRCODE=(RET),BRANCH=QUEUED       Y02080
*                                     XCTL TO WTOMOD OR FINALMOD Y02080
         EJECT
         SPACE 1
**********                    MACRO
*
         SPACE 1
*          CLOSE SUBROUTINES TO BUILD CHANNEL PROGRAMS
*
*        ENTRY TO THE ROUTINE IS AS FOLLOWS-
*           BAL   RC,CTA09600       OR
*           BAL   RC,CTA09550
*        REGISTERS MUST BE INITIALIZED AS FOLLOWS-
*           REG D  COUNT FOR THE OPERATION
*        RETURN TO THE CALLING ROUTINE TAKES PLACE AS FOLLOWS-
*           BR    RC
*
*        THE CONDITION CODE WILL HAVE BEEN SET TO '1' IF AN I/O
*        ERROR HAS OCCURED.
*        IN CASE OF ERROR CODITION, THIS ROUTINE WILL PASS
*        CONTROL TO ERROR PROCESSING.
*        ALL CONTROL OPERATION WILL RETURN TO CALLER VIA NOP ROUTINE
         SPACE 1
CTA09550 EQU   *
         MVC   DXCCW1,ABSFCCW           SET UP BSF CCW
         B     CTA09700                 GO EXCP
CTA09600 EQU   *
         MVC   DXCCW1,AFSFCCW           SET UP FSF CCW
CTA09700 EQU   *
         ST    RC,DXCCW5                SAVE RETURN ADDR
CTA09800 EQU   *
         BAL   RC,CTA10100              GO TO EXECUTE CHANNEL PROGRAM
         BNO   CTA10400                 BRANCH TO ERROR PROCESSING
         BCT   RD,CTA09800              REPEAT THE CHANNEL PROGRAM
         MVI   DXCCW1,CCWNOP            SET UP NOP CCW
         BAL   RC,CTA10100              GO TO EXECUTE CHANNEL PROGRAM
         BNO   CTA10400                 BRANCH TO ERROR PROCESSING
CTA09900 EQU   *
         NI    DXCCW5+K4,X'FF'-K1       TURN OFF BYPASS ABEND BIT
         L     RC,DXCCW5                RESTORE RETURN ADDR
         BR    RC                       RETURN TO CALLER OF THIS SUBRTN
         EJECT
         SPACE 1
**********                 MACRO                             **********
*                                                                     *
         SPACE 1
*               CLOSED SUBROUTINE TO ISSUE I/O OPERATIONS
*                      AND TEST ONLY FOR I/O ERROR
*  ( IT IS ASSUMED THAT ALL CONTROL BLOCKS HAVE ALREADY BEEN SET UP )
*
*        ENTRY TO THE SUBROUTINE IS AS FOLLOWS-
*           BAL   RC,CTA10100
*        RETURN TO THE CALLING ROUTINE TAKES PLACE AS FOLLOWS-
*           BR    RC
*
*        THE CONDITION CODE WILL HAVE BEEN SET TO '1' IF AN I/O
*        ERROR HAS OCCURRED.
         SPACE 1
CTA10100 EQU   *
         EXCP  DXIOB                    INITIATE I/O OPERATION
         IECRES WAIT                    WAIT FOR I/O TO COMPLETE
         TM    DXECB,ECBNOERR           TEST FOR UNUSUAL I/O CONDITION
         BR    RC                       RETURN TO CALLER OF SUBROUTINE
         SPACE 1
*                                                                     *
**********                                                   **********
         EJECT
         SPACE 1
**********                MACROS                             **********
*                                                                     *
         SPACE 1
*            CLOSED SUBROUTINE TO ISSUE I/O OPERATIONS
*            AND TEST FOR UNIT EXCEPTION AND I/O ERROR
*            NOTE THAT THIS ROUTINE IS CALLED ONLY TO WRITE     SA56478
*            TAPEMARKS AT THE END OF A DATA SET, AND FOR THIS   SA56478
*            REASON ONLY TREATS UNIT EXCEPTION (END-OF-TAPE)    SA56478
*            AS A NORMAL ACCEPTABLE CONDITION.                  SA56478
*  ( IT IS ASSUMED THAT ALL CONTROL BLOCKS HAVE ALREADY BEEN SET UP )
*
*        ENTRY TO THE ROUTINE IS AS FOLLOWS-
*           BAL   RC,CTA10200
*        RETURN TO THE CALLING ROUTINE TAKES PLACE AS FOLLOWS-
*           BR    RC             RETURN IF UNIT EXCEPTION       SA56478
*        OR
*           BR    RC             RETURN IF NO UNIT EXCEPTION
*
*        IF AN I/O ERROR HAS OCCURED THIS ROUTINE GOES TO THE   SA56478
*        PROBLEM DETERMINATION EXIT FOR THIS MODULE.            SA56478
         SPACE 1
CTA10200 EQU   *
         EXCP  DXIOB                    INITIATE I/O OPERATION
         IECRES WAIT                    WAIT FOR I/O TO COMPLETE
         TM    IOBSTAT0,CSWUNITX        TEST FOR UNIT EXCEPTION CAUSED
         BCR   K1,RC                    END-OF-TAPE - BR IF YES SA56478
         TM    DXECB,ECBNOERR           TEST FOR UNUSUAL I/O CONDITION
         BCR   K1,RC                    RETURN IF NO ERROR      SA56478
* IF I/O ERROR HAS OCCURED, FALL THRU TO CTA10300               SA56478
         SPACE 1
*                                                                     *
**********                                                   **********
         EJECT
         SPACE 1
***********************************************************************
*                                                                     *
*                        ERROR PROCESSING                             *
*                                                                     *
***********************************************************************
         SPACE 1
CTA10300 EQU   *
         LA    R0,CABD062               ERR- WRITE TAPE MARK
         B     CTA10500                 GO TO ERROR EXIT
CTA10400 EQU   *
         TM    DXCCW5+K4,K1             IS SW NOT TO ABEND SET ON
         BO    CTA09900                 YES, RETURN TO CALLER
         LA    R0,CABD065               ERR- POSITIONING
*****     SET RETURN ADDRESS TO FINAL PROCESSING
CTA10500 EQU   *
         DMABCOND (0),PDMOD,RETURN=FINALMOD  ABEND MACRO
CTA10600 EQU   *
         LA    R0,CABD063               ERR- WRITE TRL2
         SR    RET,RET                  SET UP FOR BRANCH TABLE
         DMABCOND (0),PDMOD             ABEND MACRO
         EJECT
         SPACE 1
***********************************************************************
*                                                                     *
*                       CONSTANTS                                     *
*                                                                     *
***********************************************************************
         SPACE 1
ABSFCCW  DC    X'2F00000020000001'      BACKSPACE FILE, SILI FLAG ON
AFSFCCW  DC    X'3F00000020000001'      FORWARD SPACE FILE, SILI ON
         XCTLTABL ID=(FINALMOD,2J,ID2F4A,4A,PDMOD,0P,SMFMOD,2H,  A38013*
               VOLDPMOD,2F),SVC=020,BRT=YES,LENGTH=              Y02080
         IECDSECS CVT,TCB,TIOT,DCB,DEB,UCB,MAIN,WTG,EXPAND=YES
         IECEQU
         END
