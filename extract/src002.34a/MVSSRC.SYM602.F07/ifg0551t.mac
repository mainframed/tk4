         TITLE 'IFG0551T - EOV TAPE OUTPUT VOLUME DISPOSITION'
         COPY  LCGASMSW
IFG0551T CSECT
         ENTRY IGG0550F                                          Y02080
IGG0550F EQU   IFG0551T                 ALLIAS ENTRY POINT       Y02080
***********************************************************************
*                                                                     *
*           VS2 RELEASE 03 DELETIONS/CHANGES                          *
*0000                                                          @ZA13589
*616327                                                        @ZA19713
*                                                                     *
*           VS2 RELEASE 02 DELETIONS/CHANGES                          *
*                                                                     *
*                                                                     *
***********************************************************************
*                                                                     *
* MODULE NAME = IFG0551T (OS/VS2)                                     *
*                                                                     *
* DESCRIPTIVE NAME = TAPE OUTPUT VOLUME DISPOSITION                   *
*                                                                     *
* COPYRIGHT = NONE                                                    *
*                                                                     *
* STATUS = RELEASE 2, LEVEL 0                                         *
*                                                                     *
* FUNCTION =                                                          *
*        1) IF SMF SPECIFIED, TRANSFER CONTROL TO IFG0553B WITH       *
*        THIS MODULE AS RETURN MODULE AND A BRANCH RETURN = 4         *
*        2) WRITE A TAPE MARK UNLESS NSL SPECIFIED.                   *
*        3) WRITE A SECOND TAPE MARK IF ASCII LABELS AND (BSF)        *
*        POSITION BETWEEN THE 2 TAPE MARKS.                           *
*        4) CLEAR BLOCK COUNT FIELD IN DCB.                           *
*        5) PERFORM VOLUME DISPOSITION FUNCTION IF MORE THAN ONE      *
*        UNIT IS ALLOCATED TO THE DATA SET:                           *
*        REWIND - REWIND THE TAPE.                                    *
*        LEAVE - INCREMENT BOTH THE FSCT AND FSEQ.                    *
*        REREAD - IF PHYSICAL FILE COUNT IS 1, A REWIND IS ISSUED.    *
*        6) TRANSFER CONTROL TO IFG0554A TO MOUNT/VERIFY NEXT VOLUME  *
*        (BRANCH CODE = 0). SET RETURN MODULE TO IFG0552B (BRANCH     *
*        CODE = 0).                                                   *
*                                                                     *
* NOTES = SEE BELOW                                                   *
*                                                                     *
*    DEPENDENCIES =                                                   *
*            CLASS ONE CHARACTER CODE.  THE EBCDIC CHARACTER CODE     *
*            WAS USED FOR ASSEMBLY.  THE MODULE MUST BE REASSEMBLED   *
*            IF A DIFFERENT CHARACTER SET IS USED FOR EXECUTION.      *
*                                                                     *
*    RESTRICTIONS = NONE                                              *
*                                                                     *
*    REGISTER CONVENTIONS =                                           *
*            R2 POINTS TO DCB                                         *
*            R4 POINTS TO OPEN WORK AREA                              *
*            R5 POINTS TO THE RESIDENT ROUTINE                        *
*            R6 POINTS TO THE WTG TABLE                               *
*            R7 POINTS TO THE CURRENT PARAMETER LIST ENTRY            *
*            R8 POINTS TO THE CURRENT WTG TABLE ENTRY                 *
*            R9 POINTS TO THE DD ENTRY IN THE TIOT                    *
*            R10 POINTS TO THE UCB                                    *
*                                                                     *
*    PATCH LABEL = SEE THIRD FROM LAST LABEL BEFORE ORG STATEMENT AT  *
*                  END OF LISTING.                                    *
*                                                                     *
* MODULE TYPE = CONTROL (OPEN, CLOSE, EOV DATA MANAGEMENT)            *
*                                                                     *
*    PROCESSOR = ASSEMBLER XF                                         *
*                                                                     *
*    MODULE SIZE = SEE EXTERNAL SYMBOL DICTIONARY OR LOC FIELD ON     *
*                  ORG STATEMENT AT END OF LISTING                    *
*                                                                     *
*    ATTRIBUTES = REENTRANT, REFRESHABLE,READ-ONLY, ENABLED,          *
*                 PRIVILEGED, SUPERVISOR STATE, DATA MANAGEMENT KEY,  *
*                 LINK PACK AREA RESIDENT/PAGEABLE                    *
*                                                                     *
* ENTRY POINT =                                                       *
*        IFG0551T                                                     *
*        IGG0550F - ALIAS                                             *
*                                                                     *
*    PURPOSE = SEE FUNCTION                                           *
*                                                                     *
*    LINKAGE =                                                        *
*        THIS MODULE IS TRANSFERRED CONTROL THROUGH THE IECRES-LOAD   *
*        MACRO INSTRUCTION.                                           *
*                                                                     *
* INPUT =                                                             *
*        GIVEN CONTROL IN PROTECT KEY 5.                              *
*        REGISTER 2 POINTS TO THE COPIED DCB.                         *
*        DEBDCBAD POINTS TO THE COPIED DCB.                           *
*        REGISTER 4 POINTS TO THE EOV WORKAREA                        *
*                                                                     *
* OUTPUT =                                                            *
*        THE NEXT MODULE IS GIVEN CONTROL IN PROTECT KEY 5 WITH       *
*        REGISTER 2 POINTING TO THE COPIED DCB,                       *
*        DEBDCBAD POINTING TO THE COPIED DCB,                         *
*        AND REGISTER 4 POINTING TO THE EOV WORKAREA,                 *
*                                                                     *
* EXIT-NORMAL =                                                       *
*        IFG0554A - MOUNT/VERIFY, BRANCH CODE = 0                     *
*           RETURN TO IFG0552B, BRANCH CODE = 0                       *
*        IFG0553B - SMF PROCESSING, BRANCH CODE = 0                   *
*           RETURN TO IFG0551T, BRANCH CODE = 4                       *
*                                                                     *
* EXIT-ERROR =                                                        *
*        IFG0550P - PROBLEM DETERMINATION, POSITIONING ERROR,         *
*                   INTERNAL CODE = 144, ABEND 637                    *
*                                                                     *
* EXTERNAL REFERENCES = SEE BELOW                                     *
*                                                                     *
*    ROUTINES =                                                       *
*        IFG019RA THROUGH THE IECRES MACRO.                           *
*                                                                     *
*    DATA AREAS =                                                     *
*        EOV WORKAREA.                                                *
*                                                                     *
*    CONTROL BLOCK =                                                  *
*        DEB                                                          *
*        JFCB                                                         *
*        UCB                                                          *
*                                                                     *
* TABLES =                                                            *
*                                                                     *
* MACROS =                                                            *
*        MODESET                                                      *
*        IECRES LOAD                                                  *
*        EXCP                                                         *
*        WAIT                                                         *
*        DMABCOND                                                     *
*                                                                     *
* CHANGE ACTIVITY =                                                   *
*        SEE CHANGES/DELETIONS SECTION JUST AFTER CSECT CARD.         *
*                                                                     *
***********************************************************************
         EJECT
         USING FORCORE,RCORE
         USING WTG,RWTG                 BASE FOR WTG TABLE       Y02080
         USING UCB,RUCB
         USING TIOENTRY,RTIOT
         USING DEB,RDEB
         USING IHADCB,RDCB
*
***********************************************************************
*              TAPE OUTPUT EOV PROCESSING STARTS HERE
***********************************************************************
*
         BALR  RBASE,0                  ESTABLISH BASE
         USING *,RBASE
*                                                                M0110
*****    DETERMINE TYPE OF ENTRY TO MODULE                       M0110
*                                                                M0110
         B     ETO04930(RET)            BR TO DETEMINE ENTRY     M0110
ETO04930 EQU   *                        BRANCH TABLE             M0110
         B     ETO04960                 BR IF NORMAL ENTRY       M0110
         B     ETO05100                 BR IF RETURN FROM SMF    M0110
*                                                                M0110
ETO04960 EQU   *                        NORMAL ENTRY             M0110
         L     RDCB,DXUDCBAD            GET PTR TO USER'S DCB    Y02082
         MODESET KEYADDR=DXUKEY,WORKREG=1 ASSUME USER'S KEY      Y02082
         OC    DCBOFLGS,DCBOFLGS        TOUCH THE USER'S MEMORY  Y02082
         LA    R0,ALLBITS-DCBOBUSY      ISOLATE BUSY BIT MASK    YM3005
         IC    R1,DCBOFLGS              GET CALLER'S OFLGS       YM3005
         MODESET EXTKEY=DATAMGT         RESUME DATA MGT KEY      Y02082
         L     RDCB,DXPDCBAD            GET PTR TO COPIED DCB    Y02082
         OR    R0,R1                    ISOLATE CALLER BUSY BIT  YM3005
         IC    R1,DCBOFLGS              GET COPIED DCB OFLGS     YM3005
         NR    R1,R0                    UPDTE BUSY BIT TO CALLER YM3005
         STC   R1,DCBOFLGS              UPDATE DCBOFLGS IN COPY  Y02082
         TM    JFCBLTYP,JFCNSL          IS NSL TAPE SPECIFIED-
         BNO   ETO05000                 ..BRANCH IF NOT
         MVI   DXECB,K32                RESTORE DXECB          @ZA13589
         LR    RES,R8                   RESTORE RESIDENT ROUTINE ADDR
         MVC   DXXMODNM,NAME551T        RESTORE TRUE MODULE NAME M0110
*                                       ..TO EOV WTG TABLE       M0110
*
ETO05000 EQU   *                        NSL NOT SPECIFIED
         TM    DXXPATHS,WTGSMF          IS SMF REQUIRED          A38013
         BNO   ETO05100                 BR IF NOT- BYPASS SMF    M0110
*
         LA    RET,K4                   SET RETURN INDCATOR
         MVC   DXRETMOD,MOD1T           MOD NAME FOR RETURN
         LA    R7,MOD0I1T               SET UP FOR XCTL
         LA    RWTGC,DXXENTRY           SET R8 FOR RES RTN       Y02134
         LA    RWTG,DXXAREA             POINT TO EOV WTG         Y02134
         IECRES LOAD,MODID=(R7),BRANCH=QUEUED GO TO NEXT MODULE  Y02080
*                                                                Y02134
ETO05100 EQU   *                        WRITE TAPE MARK
         MVC   DXCCW1(K16),WTMK         CCW TO WORKAREA
         TM    JFCBLTYP,JFCNSL          NON-STD LABELS SPECIFIED
         BO    ETO05400                 YES DON'T WRITE TAPE MARK
         OI    DXATALL,DXATFC           FORCE CLOSE ON ERROR     YM7099
         BAL   R8,ETO06800              WRITE TAPE MARK          A52335
         OI    DXATOUTA,DXATTTM1        TRAILING TM WRITTEN      Y02144
         TM    JFCBLTYP,JFCBAL          ASCII STD LABEL          A52335
         BO    ETO05150                 YES, BRANCH TO WTM       A52335
         TM    JFCBLTYP,JFCNL           WAS NL SPECIFIED
         BZ    ETO05200                 NO, CONTINUE
*
*    ASCII VOLUMES MUST HAVE TWO TAPE MARKS AT EOV.              A52335
*
         TM    DCBOPTCD,DCBOPTQ         ASCII UNLABELED TAPE     A52335
         BZ    ETO05200                 NO, CONTINUE
ETO05150 EQU   *                        WRITE 2ND TAPE MARK      A52335
         BAL   R8,ETO06800              WRITE TAPE MARK
         OI    DXATOUTA,DXATTTM2        2ND TRAILING TM WRITTEN  Y02144
         MVI   DXCCW1,CCWBSF            SET UP BACKSPACE CCW     A52335
         BAL   R8,ETO06800              BACKSPACE 1 TM           A52335
ETO05200 EQU   *                        CHAIN BIT OFF
         L     RUCB,DXUCBADR            GET UCB ADDR             Y02134
         MODESET EXTKEY=SUPR            UCB KEY                  Y02082
         NI    UCBWGT,X'FF'-UCBREW      RESET TSK/SCD RWND BIT   Y02080
         MODESET EXTKEY=DATAMGT         DATA MANAGEMENT KEY      Y02082
ETO05400 EQU   *                        DETERMINE DISPOSITION
*                                                                A46383
*****    DETERMINE THE DISPOSITION OF THE VOLUME AT EOV          A46383
*                                                                A46383
         TM    DCBMACRF,DCBMEXCP        USING EXCP
         BZ    ETO05500                 NO, GO
         TM    DCBMACRF+K1,DEPEND       DEVICE SECT PRESENT
         BZ    ETO05600                 NO, GO AROUND
ETO05500 XC    K12(K4,RDCB),K12(RDCB)   CLEAR BLK COUNT
*                                                                Y02134
ETO05600 EQU   *                        SET UP CHANNEL PROGRAM   Y02134
*                                                                Y02134
         MVI   DXCCW1+K4,CCWSILI        RESET CHAIN COMMAND FLAG
         MVC   DXCCW7+K4(K4),UCBFSCT    SAVE FSCT AND FSEQ
         TM    JFCBLTYP,JFCNSL          NSL SPEC
         BO    ETO06500                 YES,GET VOL SER          Y02134
*              IS ONLY 1 UNIT ALLOCATED TO DATA SET
         CLI   DXNOUNIT+K1,K1           ONE UNIT
         BE    ETO06400                 YES, SKIP POSITIONING    YM7526
*
ETO05700 EQU   *                        TEST FOR FEOV OPTIONS
*
*****  TEST FOR ANY FEOV OPTIONS REWIND/LEAVE
*
         TM    PLISTOPT(RPARC),DEBOPLV  WAS REWIND SPECIFIED     YM7526
         BM    ETO06400                 YES,GO ISSUE EXCP FOR
*                                       REWIND
         BO    ETO06300                 GO CONTINUE
*                                       PROCESSING-LEAVE
         TM    PLISTOPT(RPARC),PLISTLV  IS LEAVE SPECIFIED       YM7526
         BO    ETO06300                 YES,GO
*                                       IS REREAD DISPOSITION SPECIFIED
         BZ    ETO06200                 NO, GO                   YM7526
*
*  POSITION TAPE FOR REREAD.
*  IF THIS IS THE FIRST PHYSICAL FILE ON THIS VOLUME, A REWIND
*  CAN BE SUBSTITUTED FOR 2 OR 4 BSF'S.  IN ADDITION TO BEING
*  PHYSICALLY FASTER, IT ALLOWS THE NEXT VOLUME ON UCB(2) TO BE
*  USED IMMEDIATELY.  NONE OF THE BSF'S ARE OVERLAPPED WITH ANYTHING.
*
         CLC   UCBFSCT(K2),CON0101      IS PHYSICAL FILE COUNT 1
         BE    ETO06400                 BR IF YES TO REWIND
*
         MVI   DXCCW1,CCWBSF            SET UP BACKSPACE CCW
*
*              UNLABELED TAPES MUST BACKSPACE 2 TAPEMARKS FOR REREAD
*              STD-LABEL TAPES MUST BACKSPACE 4 TAPEMARKS FOR REREAD
*
         TM    JFCBLTYP,JFCBAL+JFCSL    STD LABELS
         BM    ETO05800                 YES, BACKSPACE FOUR
         B     ETO06000                 NO, BACKSPACE TWO        A52335
ETO05800 EQU   *                        STD LABEL POSITIONING
*
*              BACKSPACE FIRST 2 TAPEMARKS FOR STD LABELED TAPE
*
         BAL   R8,ETO06800              GO DO BACKSPACE
         BAL   R8,ETO06800              GO DO BACKSPACE
*
*              BACKSPACE 2 TAPEMARKS FOR UNLABELED, OR LAST 2 OF 4
*              TAPEMARKS FOR STD LABELED
*
ETO06000 BAL   R8,ETO06800              DO BACK SPACE 1 OF 2
         BAL   R8,ETO06800              DO BACKSPACE 2 OF 2
*
*              DID LAST BACKSPACE FILE ENCOUNTER LOAD POINT ON TAPE
*
         MVI   DXCCW1,CCWNOP            NOP OP CODE
         BAL   R8,ETO06800              DO BACK SPACE
         TM    IOBSENSE+K1,UCBLDPT      AT LOAD POINT
         BO    ETO06500                 YES,GO
         MVI   DXCCW1,CCWFSF            SPACE 1 TAPEMARK TO BE POSITION
*                                       PROPERLY
         BAL   R8,ETO06800              DO BACKSPACE
         B     ETO06500                 BR TO PREPARE FOR XCTL
ETO06200 EQU   *                        TEST FOR PASS            YM7526
*                                                                YM7526
*        TEST FOR DISP=PASS                                      YM7526
*                                                                YM7526
         TM    UCBJBNR,UCBMONT          TEST FOR PASSED          YM7526
         BNO   ETO06400                 BRANCH IF NOT PASS       YM7526
*
*              ADD 1 TO FILE COUNT AND FILE NUMBER ON THIS VOLUME
*
ETO06300 EQU   *                        INCREMENT FSCT AND FSEQ
*
         L     RWTG,UCBFSCT             GET FILE SEQ COUNT
         A     RWTG,CON0101             INCREMENT FSCT AND FSEQ
         MODESET EXTKEY=SUPR            UCB KEY                  Y02082
         ST    RWTG,UCBFSCT             SAVE FSCT AND FSEQ
         MODESET EXTKEY=DATAMGT         DATA MANAGEMENT KEY      Y02082
         B     ETO06500                 GO EXIT FROM THIS MOD
ETO06400 EQU   *                        FORCE REWIND             YM7526
*                                                                YM7834
         MODESET EXTKEY=ZERO            UCB KEY                  YM7834
*                                                                YM7834
         XC    UCBFSCT,UCBFSCT          FORCE REWIND             YM7834
         XC    UCBFSEQ,UCBFSEQ          CLEAR FILE COUNTERS      YM7834
*                                                                YM7834
         MODESET EXTKEY=DATAMGT         DM KEY                   YM7834
*
ETO06500 EQU   *                        ZERO DATA MGT COUNT
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
*        SET DATA MANAGEMENT COUNT TO ZERO ON CURRENT VOLUME     Y02134
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
         MODESET EXTKEY=SUPR            UCB KEY                  Y02134
*                                                                Y02134
         MVI   UCBDMCT,K0               SET COUNT TO ZERO        Y02134
*                                                                Y02134
         MODESET EXTKEY=DATAMGT         RESET KEY TO DM          Y02134
         OI    DXATEOV,DXATNVOL         INDIC CURR UNIT INACTIVE YM6520
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
*        INCREMENT VOLUME SEQUENCE COUNT FOR NEXT VOLUME         Y02134
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
         LH    R1,DXVOLSEQ              GET CURRENT VOL SEQ      Y02134
         LA    R1,K1(R1)                UPDATE VOL SEQ COUNT     Y02134
         STH   R1,DXVOLSEQ              SAVE COUNT               Y02134
*                                                                Y02134
         STH   R1,JFCBVLSQ              SAVE COUNT IN JFCB     @ZA19713
         MVC   DXRETMOD,ID1T2B          SET MOUNT/VERIFY RETURN  Y02134
         MVI   DXRETCOD,K0              BRANCH OFFSET TO ZERO    Y02134
*
         LA    RWTGC,DXXENTRY           SET R8 FOR RES RTN       Y02134
         LA    RWTG,DXXAREA             POINT TO EOV WTG
         IECRES LOAD,MODID=ID1T4A,BRCODE=K0,BRANCH=QUEUED        Y02134
*
*              ENTER VIA BAL  R8,EXECUTE
*
ETO06800 EQU   *                        I/O ROUTINE
         EXCP  DXIOB
         WAIT  ,ECB=DXECB
         TM    IOBSTAT0,CSWUNITX        IS THERE A UNIT EXCEPTION ERROR
         BNO   ETO06900                 NO,GO CHECK IF ANY I/O ERROR
         TM    IOBSTAT0,IOERR           TEST FOR UNIT CHK ALSO  SA58747
         BO    ETO06900                 YES,CHK ANY I/O ERROR   SA58747
         OI    DXECB,ECBCOD7F           IGNORE ERROR
ETO06900 EQU   *                        CHECK FOR I/O ERROR
         TM    DXECB,ECBNOERR           IO ERROR
         BNO   ETO07500                 YES, GO CHECK FOR LOAD POINT
         BR    R8                       RETURN
*
***********************************************************************
*
*
ETO07500 EQU   *                        CHECK IF AT LOAD POINT
         TM    IOBSENS1,UCBLDPT         TAPE AT LOAD POINT (BOT)
         BCR   1,R8                     YES, RETURN TO CALLER
         MODESET EXTKEY=DATAMGT         DATA MANAGEMENT KEY      Y02082
         LA    RWTGC,DXXENTRY           SET R8 FOR RES RTN
         LA    RWTG,DXXAREA             POINT TO EOV WTG
         DMABCOND 144,MODPD1T           ABEND 637
*
*                   CONSTANTS
*
         DS    0F
CON0101  DC    X'00010001'              PHYSICAL FILE COUNT
WTMK     DC    X'1F00000060000001'      WRITE TM,COMMAND CHAIN,SILI,CNT
         DC    X'0300000020000001'      NOP CCW
*
XCTLTA1T XCTLTABL ID=(ID1T4A,4A,ID1T2B,2B,                       Y02134X
               MODPD1T,0P,MOD1T,1T,MOD0I1T,3B),TAG=NAME551T,     Y02080X
               SVC=055,BRT=YES,LENGTH=                           Y02080
         IECDSECS TIOT,DCB,DEB,UCB,MAIN,WTG,PREFX,EXPAND=YES     Y02080
         IECEQU
         END
