         TITLE 'IFG019TR - OPEN/CLOSE/EOV OPTIONAL TRACE ROUTINE'
         COPY  LCGASMSW
IFG019TR CSECT
         ENTRY IFG020TR,IFG023TR,IFG055TR                        Y02080
IFG020TR EQU   IFG019TR                 ALLIAS ENTRY POINT       Y02080
IFG023TR EQU   IFG019TR                 ALLIAS ENTRY POINT       Y02080
IFG055TR EQU   IFG019TR                 ALLIAS ENTRY POINT       Y02080
***********************************************************************
*                                                                     *
*            VS2 RELEASE 037 DELETIONS/CHANGES                        *
*0000                                                          @AZ25627
*                                                                     *
*                                                                     *
***********************************************************************
* MODULE NAME = IFG019TR (OS/VS2)                                     *
*                                                                     *
* DESCRIPTIVE NAME = OPEN/CLOSE/EOV OPTIONAL TRACE ROUTINE            *
*                                                                     *
* COPYRIGHT = NONE                                                    *
*                                                                     *
* STATUS = RELEASE 2, LEVEL 0                                         *
*                                                                     *
* FUNCTION =                                                          *
*        1. GETS POINTER TO TRACE AREA FROM WORK AREA.                *
*                                                                     *
*        2. TRACES THE FOLLOWING VIA THE GTRACE MACRO:                *
*           A. DCB (IF VSAM OR AN ACB, THEN THE GTF FORMAT ID AND     *
*                   TRACE CODE IS CHANGED TO REFLECT VSAM ACB).       *
*           B. DSCB                                                   *
*           C. JFCB                                                   *
*           D. CCW'S                                                  *
*           E. WTG TABLE IF NOT EOV                                   *
*                                                                     *
*        3. RETURNS TO CALLING ROUTINE (IFG019RA).                    *
*                                                                     *
* NOTES = SEE BELOW                                                   *
*                                                                     *
*    DEPENDENCIES =                                                   *
*            CLASS ONE CHARACTER CODE.  THE EBCDIC CHARACTER CODE     *
*            WAS USED FOR ASSEMBLY.  THE MODULE MUST BE REASSEMBLED   *
*            IF A DIFFERENT CHARACTER SET IS USED FOR EXECUTION.      *
*                                                                     *
*    RESTRICTIONS = NONE                                              *
*                                                                     *
*    REGISTER CONVENTIONS =                                           *
*            R2 POINTS TO DCB                                         *
*            R4 POINTS TO OPEN/CLOSE/EOV WORK AREA                    *
*            R5 POINTS TO THE RESIDENT ROUTINE                        *
*            R6 POINTS TO THE WTG TABLE                               *
*            R7 POINTS TO THE CURRENT PARAMETER LIST ENTRY            *
*            R8 POINTS TO THE CURRENT WTG TABLE ENTRY                 *
*            R9 POINTS TO THE DD ENTRY IN THE TIOT                    *
*            R10 POINTS TO THE UCB                                    *
*                                                                     *
*    PATCH LABEL = SEE THIRD LABEL BEFORE ORG STATEMENT AT END        *
*                  OF LISTING.                                        *
*                                                                     *
* MODULE TYPE = CONTROL (OPEN, CLOSE, EOV DATA MANAGEMENT)            *
*                                                                     *
*    PROCESSOR = ASSEMBLER XF                                         *
*                                                                     *
*    MODULE SIZE = SEE EXTERNAL SYMBOL DICTIONARY OR LOC FIELD ON     *
*                  ORG STATEMENT AT END OF LISTING                    *
*                                                                     *
*    ATTRIBUTES = REENTRANT, REFRESHABLE, READ-ONLY, ENABLED,         *
*                 PRIVILEGED, SUPERVISOR STATE, DATA MANAGEMENT KEY,  *
*                 LINK PACK AREA RESIDENT/PAGEABLE                    *
*                                                                     *
* ENTRY POINT = IFG019TR FOR OPEN                                     *
*               IFG020TR FOR CLOSE                                    *
*               IFG023TR FOR TCLOSE                                   *
*               IFG055TR FOR EOV                                      *
*                                                                     *
*    PURPOSE = SEE FUNCTION                                           *
*                                                                     *
*    LINKAGE = POINTER TO TRACE AREA IN WORK AREA.                    *
*                                                                     *
* INPUT = RDCB (R2) = POINTER TO USERS DCB                            *
*         RCORE (R4) = POINTER TO WORK AREA                           *
*         RWTG (R6) = POINTER TO WTG TABLE                            *
*         RWTGC (R8) = POINTER TO CURRENT ENTRY IN WTG TABLE          *
*         RTIOT (R9) = POINTER TO TRACE PARAMETER LIST                *
*                                                                     *
* OUTPUT = TRACE INFORMATION IN BUFFER                                *
*                                                                     *
* EXIT-NORMAL = IFG019RA                                              *
*                                                                     *
* EXIT-ERROR = NONE                                                   *
*                                                                     *
* EXTERNAL REFERENCES = SEE BELOW                                     *
*                                                                     *
*    ROUTINES = GTRACE MONITOR CALL                                   *
*                                                                     *
*    DATA AREAS = OPEN/CLOSE/EOV WORK AREA                            *
*                 TRACE BUFFER                                        *
*                                                                     *
*    CONTROL BLOCKS = ACB                                             *
*                     DCB                                             *
*                     DSCB                                            *
*                     JFCB                                            *
*                                                                     *
* TABLES = NONE                                                       *
*                                                                     *
* MACROS = GTRACE                                                     *
*          IECDSECS                                                   *
*          XCTLTABL                                                   *
*                                                                     *
* CHANGE ACTIVITY = NEW RELEASE (LEVEL 0)                             *
*                                                                     *
***********************************************************************
         EJECT
R0       EQU   0                        WORK/PARAMETER REG
R1       EQU   1                        WORK/PARAMETER REG
RDCB     EQU   2                        PTR TO DCB
RBASE    EQU   3                        BASE REG
RCORE    EQU   4                        PTR TO O/C/EOV WKAREA
RES      EQU   5                        PTR TO RESIDENT RTN
RWTG     EQU   6                        PTR TO WTG TABLE
RPARC    EQU   7                        PTR TO PARALIST CURRENT ENTRY
RWTGC    EQU   8                        PTR TO WTG TABLE CURRENT ENTRY
RTIOT    EQU   9                        PTR TO TIOT ENTRY
RUCB     EQU   10                       PTR TO UCB
RB       EQU   11                       WORK/PARAMETER REG
RC       EQU   12                       WORK/PARAMETER REG
RD       EQU   13                       WORK/PARAMETER REG
RET      EQU   14                       WORK/PARAMETER REG
RF       EQU   15                       WORK/PARAMETER REG
R6       EQU   RWTG                     REGISTER 6               YM2167
R7       EQU   RPARC                    REGISTER 7               YM2167
R8       EQU   RWTGC                    REGISTER 8               YM2167
R9       EQU   RTIOT                    REGISTER 9               YM2167
R10      EQU   RUCB                     REGISTER 10              YM2167
R11      EQU   RB                       REGISTER 11              YM2167
R12      EQU   RC                       REGISTER 12              YM2167
R13      EQU   RD                       REGISTER 13              YM2167
R14      EQU   RET                      REGISTER 14              YM2167
R15      EQU   RF                       REGISTER 15              YM2167
ZERO     EQU   0                        WORK VALUE OF 0          YM2167
COMMUTAB EQU   16                       PTR TO CVT               YM2167
LIMIT    EQU   100                      CONSTANT OF 100          YM2167
REGSAVE  EQU   156                      CONSTANT OF 156 USED TO  YM2167
*                                       ADDRESS A TEMPORARY SAVE YM2167
*                                       AREA IN THE BUFFER       YM2167
*                                       TO SAVE REGISTERS        YM2167
EXCPAM   EQU   X'80'                    EXCP ACCESS METHOD FLAG  X02989
TWO      EQU   2                        CONSTANT OF TWO          X02989
FOUR     EQU   4                        WORK VALUE
EIGHT    EQU   8                        WORK VALUE
ORG      EQU   0                        WORK VALUE
NINE     EQU   9                        WORK VALUE
ONE      EQU   1                        WORK VALUE
MDCBMXLN EQU   183                      MAXIMUM LN TO XFER FOR DCB/ACB
WKAREA1  EQU   72                       TRACE CODE FOR START OF WKAREA
WKAREA2  EQU   76                       TRACE CODE FOR 2ND PART OF
*                                       O/C/EOV WKAREA
WKAREA3  EQU   80                       TRACE CODE FOR THIRD PART OF
*                                       O/C/EOV WKAREA
WKAREA4  EQU   84                       TRACE CODE FOR FOURTH PART OF
*                                       O/C/EOV WKAREA
WKAREA5  EQU   100                      TRACE CODE FOR FIFTH     YM7364
*                                       PART OF O/C/EOV WKAREA   YM7364
WTGCOD   EQU   X'60'                    TRACE CODE FOR WTG TABLE
DCBTCODE EQU   40                       TRACE CODE FOR DCB
EQUAL    EQU   8                        BRANCH CONDITION
         EJECT
         BALR  RC,R0                    USE REG 12 AS BASE SO AS
*                                       NOT TO DESTROY REG 3
         USING *,RC
         USING FORCORE,RCORE
         USING WTG,RWTG
         L     R1,WTGPREFX              GET PTR TO PREFIX        Y02080
         USING IECPREFX,R1                                       Y02080
         L     R1,IECRESWA              GET ADDRESS OF WORK AREA Y02080
         USING RWARESWA,R1                                       Y02080
         L     R1,RWATRBUF              PTR TO TRACE BUFFER      Y02080
         DROP  R1                                                Y02080
***************************************************************  X02989
*  THIS SECTION TESTS FOR A VSAM ACB.  IF ONE IS PRESENT, THE    X02989
*  GTF FORMAT APPENDAGE ID AND TRACE CODE ARE CHANGED TO REFLECT X02989
*  A VSAM ACB.  AFTER THE ACB IS VALIDITY CHECKED AND THE TRACE  X02989
*  BUFFER WRITTEN, THE FORMAT ID AND TRACE CODE ARE RESTORED     X02989
*  TO THEIR ORGINAL STATUS.                                      X02989
***************************************************************  X02989
         USING IHADCB,RDCB              DEFINE BASE TO DCB       X02989
         TM    JFCDSRG2,JFCORGAM        IS THIS A VSAM DATA SET  XM0856
         BNO   PDF80020                 BRANCH IF NO             XM0856
         TM    DCBDSORG+ONE,ACBDORGA    IS CONTROL BLOCK AN ACB  X02989
         BNO   PDF80020                 BRANCH IF NO             X02989
         USING IFGACB,RDCB              DEFINE BASE FOR ACB      X02989
         MVC   TWO(L'VFID,R1),VFID      MOVE FORMAT ID FOR VSAM  X02989
*                                       INTO TRACE BUFFER        X02989
         LH    RF,ACBLENG               LD LENGTH OF ACB         X02989
         STH   RF,ORG(R1)               ST LENGTH IN TRACE BFR   X02989
         IC    RF,ACBID                 LOAD ACB ID              X02989
         STC   RF,EIGHT(,R1)            STORE ID IN TRACE BFR    X02989
         B     PDF80030                 MOVE DCB/ACB TO WORD AREAX02989
         USING IHADCB,RDCB              REDEFINE BASE TO DCB     X02989
PDF80020 EQU   *                        DETERMINE DCB/ACB LENGTH X02989
         LH    RF,DXUDCBPL              LOAD DCB PREFIX LN       Y02080
         AH    RF,DXUDCBML              ADD DCB LN               Y02080
         STH   RF,ORG(R1)               SAVE ACB/DCB LENGTH
         MVI   EIGHT(R1),DCBTCODE       MOVE DCB CODE TO BUFFER
*
***********************************************************************
*        THIS SECTION DETERMINES THE LENGTH OF THE ACB OR DCB TO BE   *
*        MOVED INTO THE TRACE BUFFER.  IF IS IS LESS THAN 183 BYTES,  *
*        THE MAXIMUM LENGTH THAT CAN BE TRACED, THEN THE ENTIRE DCB   *
*        OR ACB IS MOVED INTO THE TRACE BUFFER AND THE LENGTH IS      *
*        PLACED IN THE FIRST TWO BYTES OF THE TRACE BUFFER.  IF THE   *
*        ACB/DCB IS LONGER THAN 183 BYTES, THEN ONLY 183 BYTES ARE    *
*        MOVED INTO THE TRACE BUFFER AND 184 IS PLACED IN THE FIRST   *
*        TWO BYTES OF THE BUFFER.                                     *
***********************************************************************
*
PDF80030 EQU   *                        MOVE ACB/DCB TO BUFFER
         LA    RF,MDCBMXLN              SET MAX LN TO TRANSFER   Y02080
         CH    RF,ORG(,R1)              COMP MAX LN TO DCB SIZE  Y02080
         BNH   PDF80050                 BRANCH TO USE MAX LENGTH Y02080
         LH    RF,ORG(,R1)              USE ACTUAL SIZE DCB/ACB  Y02080
         LTR   RF,RF                    ACTUAL SIZE '0' ?      @AZ25627
         BH    PDF80050                 YES? USE IT            @AZ25627
         LA    RF,MDCBMXLN              SET TO MAX LENGTH      @AZ25637
PDF80050 EQU   *                        MOVE TO BUFFER, SET LN   Y02080
         BCTR  RF,ZERO                  DECREMENT FOR MOVE INSTR Y02080
         EX    RF,PDF80115              MOVE DCB INTO BUFFER     Y02080
         LA    RF,TWO(,RF)              RESTORE TRACE DATA LNG   YM3945
         BAL   RD,PDF80010              TRACE DCB
         MVC   TWO(L'FID,R1),FID        RESTORE FORMAT ID IN     X02989
*                                       CASE MODULE ENTERED FOR  X02989
*                                       VSAM ACB OR AMB          X02989
         LA    RF,DXJBF-DXDSCB+ONE      LNG OF 1ST PART OF WKAREA
*                                       TO BE TRACED
         MVI   EIGHT(R1),WKAREA1        MOVE CODE TO BUFFER
         MVC   NINE(DXJBF-DXDSCB,R1),DXDSCB
*                                       MOVE DSCB FROM WKAREA TO BUFFER
         BAL   RD,PDF80010              TRACE DSCB
         LA    RF,JFCBEND-DXJBF+ONE     LNG OF JFCB
         MVI   EIGHT(R1),WKAREA2        MOVE CODE TO BUFFER
         MVC   NINE(JFCBEND-DXJBF,R1),DXJBF
*                                       MOVE JFCB TO BUFFER
         BAL   RD,PDF80010              TRACE JFCB
         LA    RF,DXCCW-DXECB+ONE       LNG OF ECB, IOB, DEB
         MVI   EIGHT(R1),WKAREA3        MOVE CODE TO WKAREA
         MVC   NINE(DXCCW-DXECB,R1),DXECB
*                                       MOVE ECB, IOB, DEB TO BUFFER
         BAL   RD,PDF80010              TRACE ECB, IOB, DEB
         LA    RF,DXWORK-DXCCW+ONE      LENGTH OF CCWS, REGISTER YM7364
*                                       SAVE AREA, MODULE IDS    YM7364
         MVI   EIGHT(R1),WKAREA4        MOVE CODE TO BUFFER
         MVC   NINE(DXWORK-DXCCW,R1),DXCCW  MOVE CCWS+REG SA+IDS YM7364
         BAL   RD,PDF80010              GO TRACE CCWS+REG SA+IDS YM7364
         LA    RF,DXLBL+DSECTSIZ-DXWORK+ONE  LNG OF EXTENDED WA, YM7364
*                                       PROTECTED CTRL BLK PTRS, YM7364
*                                       RECOVERY RTN AUDIT TRAIL YM7364
         MVI   EIGHT(R1),WKAREA5        MOVE CODE TO BUFFER      YM7364
         MVC   NINE(DXLBL+DSECTSIZ-DXWORK,R1),DXWORK MOVE EXT WA YM7364
*                                       PROT CB PTS, AUDIT TRAIL YM7364
         BAL   RD,PDF80010              GO TRACE EXT WA+PTRS+TRL YM7364
         LA    RF,WTGENTRY-WTG+EIGHT+ONE  LNG OF WTG TABLE TRACE
         MVI   EIGHT(R1),WTGCOD         MOVE CODE OF WTGTBL TO BUFFER
         LA    RB,DXXAREA               PTR TO EOV WTG TABLE     YM7364
         CR    RWTG,RB                  ARE WE IN EOV            YM7364
         LR    RB,RWTGC                 PT TO NON-EOV WTG ENTRY  YM7364
         BNE   PDF80060                 BR NO TO MOVE WTG TO BFR YM7364
         LA    RB,DXXENTRY-DXXAREA(,RWTG) PT TO EOV WTG ENTRY    YM7364
PDF80060 EQU   *
         MVC   NINE(WTGENTRY-WTG,R1),WTG  MOVE WTG TBL BASE TO BUFFER
         MVC   WTGENTRY-WTG+NINE(EIGHT,R1),ORG(RB)               YM7364
*                                       MOVE CURRENT ENTRY TO BUFFER
         BAL   RD,PDF80010              TRACE WTG TABLE
         BR    RET                      RETURN TO RESIDENT RTN
*
*        FOLLOWING MVC USED BY EX INSTRUCTION
PDF80115 MVC   NINE(ZERO,R1),ORG(RDCB)  USED BY EX INSTR         Y02080
*
PDF80010 EQU   *                        ISSUE GTRACE MACRO
         STH   RF,ORG(,R1)              SET LNG OF TRACE DATA    YM3945
         GTRACE ID=4095,PAGEIN=YES,MF=(E,(1))  TRACE DATA IN BFR YM7364
         BR    RD                       RETURN TO CALLER
FID      DC    X'00FF'                  FORMAT ID FOR O/C/EOV    XM0478
VFID     DC    X'00F9'                  FORMAT ID FOR VSAM       XM0478
         XCTLTABL BRT=YES,LENGTH=                                Y02080
         IECDSECS CVT,MAIN,PREFX,WTG,DCB,ACB,PSA,EXPAND=YES      Y02080
         END
