         TITLE 'IFG0196K/OPEN - JFCB TO DCB MERGE,  TELECOMMUNICATIONS'
         COPY  LCGASMSW
IFG0196K CSECT
***********************************************************************
*                                                                     *
* MODULE NAME = IFG0196K (OS/VS2)                                     *
*                                                                     *
* DESCRIPTIVE NAME = OPEN JFCB TO DCB MERGE, TELECOMMUNICATIONS       *
*                                                                     *
* COPYRIGHT = NONE                                                    *
*                                                                     *
* STATUS = RELEASE 2, LEVEL 0                                         *
*                                                                     *
* FUNCTION =                                                          *
*        MERGE JFCB TO DCB FIELDS FOR TELECOMMUNICATIONS AND GRAPHICS *
*        DATA SETS.                                                   *
*                                                                     *
* NOTES = SEE BELOW                                                   *
*                                                                     *
*    DEPENDENCIES =                                                   *
*            CLASS ONE CHARACTER CODE.  THE EBCDIC CHARACTER CODE     *
*            WAS USED FOR ASSEMBLY.  THE MODULE MUST BE REASSEMBLED   *
*            IF A DIFFERENT CHARACTER SET IS USED FOR EXECUTION.      *
*                                                                     *
*    RESTRICTIONS = NONE                                              *
*                                                                     *
*    REGISTER CONVENTIONS =                                           *
*            R2 POINTS TO DCB                                         *
*            R4 POINTS TO OPEN WORK AREA                              *
*            R5 POINTS TO THE RESIDENT ROUTINE                        *
*            R6 POINTS TO THE WTG TABLE                               *
*            R7 POINTS TO THE CURRENT PARAMETER LIST ENTRY            *
*            R8 POINTS TO THE CURRENT WTG TABLE ENTRY                 *
*            R9 POINTS TO THE DD ENTRY IN THE TIOT                    *
*            R10 POINTS TO THE UCB                                    *
*                                                                     *
*    PATCH LABEL = SEE NEXT TO LAST LABEL BEFORE ORG STATEMENT AT END *
*                  OF LISTING.                                        *
*                                                                     *
* MODULE TYPE = CONTROL (OPEN, CLOSE, EOV DATA MANAGEMENT)            *
*                                                                     *
*    PROCESSOR = ASSEMBLER XF                                         *
*                                                                     *
*    MODULE SIZE = SEE EXTERNAL SYMBOL DICTIONARY OR LOC FIELD ON     *
*                  ORG STATEMENT AT END OF LISTING                    *
*                                                                     *
*    ATTRIBUTES = REENTRANT, REFRESHABLE,READ-ONLY, ENABLED,          *
*                  PRIVILEGED, SUPERVISOR STATE, DATA MANAGEMENT KEY, *
*                  LINK PACK AREA RESIDENT/PAGEABLE                   *
*                                                                     *
* ENTRY POINT =                                                       *
*        IFG0196K FROM IFG0196J                                       *
*                                                                     *
*    PURPOSE = SEE FUNCTION                                           *
*                                                                     *
*    LINKAGE =                                                        *
*        IECRES  LOAD,MODID=ID6J6K,BRANCH=QUEUED                      *
*                                                                     *
* INPUT = A JFCB IN EACH WORK AREA                                    *
*                                                                     *
* OUTPUT =                                                            *
*        MERGED FIELDS IN THE COMPIED DCB, AND AN INDICATION OF WHICH *
*        FIELDS WERE MERGED SAVED IN JFCBMASK.                        *
*                                                                     *
* EXIT-NORMAL = IFG0196L                                              *
*                                                                     *
* EXIT-ERROR = NONE                                                   *
*                                                                     *
* EXTERNAL REFERENCES = SEE BELOW                                     *
*                                                                     *
*    ROUTINES = IFG019RA                                              *
*                                                                     *
*    DATA AREAS = NONE                                                *
*                                                                     *
*    CONTROL BLOCK = DCB,JFCB, WTG TABLE                              *
*                                                                     *
* TABLES = NONE                                                       *
*                                                                     *
* MACROS = IECDSECS, IECRES, XCTLTABLE                                *
*                                                                     *
* CHANGE ACTIVITY = NEW RELEASE (LEVEL 0)                             *
*                                                                     *
***********************************************************************
         EJECT
         IECDSECS MAIN,WTG              DEFINE DESIRED DSECTS TO BE    X
                                        EXPANDED AT THE END      S22024
*
         IECEQU ,                       DEFINE DESIRED EQUATES
*
         USING IHADCB,RDCB              DEFINE BASE TO USER'S DCB
         USING FORCORE,RCORE            DEFINE BASE TO MAIN WORK AREA
         USING WTG,RWTG                 ESTABLISH ADDRESSABILITY Y02080
*
         BALR  RBASE,0                  ESTABLISH BASE REGISTER
         USING *,RBASE                  DEFINE BASE REGISTER
*
         SR    R1,R1                    POINT TO BSAM IN MERGE TABLE
***********************************************************************
*
*        DETERMINE ACCESS METHOD
*
***********************************************************************
         TM    DCBDSORG+K1,DCBORGGR     TEST FOR GRAPHICS ACCESS METHOD
         BO    OMG23400                 BR IF GRAPHICS
*
         TM    DCBDSORG,DCBORGCX        TEST IF DSORG=CX
         BO    OMG24500                 BR IF YES
*
         TM    DCBDSORG+K1,DCBORGTX     IS IT TCAM LINE GROUP
         BO    OMG24000                 BRANCH IF YES
*
         TM    DCBDSORG+K1,DCBORGTQ     IS IT TCAM MSG QUEUE
         BO    OMG23600                 BRANCH IF YES
*
         TM    DCBDSORG+K1,DCBDSGTR     IS IT TCAM 3705          S22024
         BO    OMG24450                 YES, CHECK TCAM DSORG    S22024
*
         TM    DCBDSORG,DCBORGPS        IS IT PHYSICAL SEQ
         BO    OMG23200                 BR IF YES, TCAM DUMMY PS
*
         TM    DCBDSORG,DCBORGCQ        IS IT COMM DIRECT ACCESS GROUP
         BO    OMG26900                 BR IF YES, BYPASS MERGE
*
         TM    DCBDSORG,DCBORGMQ        IS IT PROCESS PGM MESSAGE QUEUE
         BO    OMG23700                 BR IF YES
*
         DC    H'0'                     CAN NOT FALL THRU TESTS
*
OMG23200 EQU   *                        TCAM DUMMY PS
         NC    DCBBLKSI,DCBBLKSI        IS BLKSIZE ZERO          A44903
         BNZ   OMG23300                 BRANCH IF NOT ZERO       A44903
         MVC   DCBBLKSI,JFCBLKSI        MERGE BLKSIZE            A44903
         OI    JFCBMASK+K2,X'10'        SET BLKSIZE MERGE MASK   A44903
OMG23300 EQU   *                        BLKSIZE NOT ZERO         A44903
         NC    DCBLRECL,DCBLRECL        IS LRECL=0
         BNZ   OMG25000                 BR IF NO TO MAIN MERGE
         MVC   DCBLRECL,JFCLRECL        MERGE LRECL
         OI    JFCBMASK+K3,X'02'        SET LRECL MERGE MASK
         B     OMG25000                 BR TO MAIN MERGE
*
OMG23400 EQU   *                        GRAPHICS  DSORG=GS
         CLI   32(RDCB),K0              IS GNCP FIELD =0
         BNE   OMG26900                 BR IF NO
         MVC   32(1,RDCB),JFCBFALN      **** MERGE GNCP ****
         OI    JFCBMASK+K2,X'01'        SET GNCP BIT IN MERGE MASK
         B     OMG26900                 BR TO BYPASS MERGE
*
OMG23600 EQU   *                        TCAM MESSAGE QUEUE
*
***********************************************************************
*                                                                     *
*   FOR TCAM WITH DSORG = TQ, THRESH AND BLKSIZE ARE HANDLED WITH
*   UNIQUE MERGE.
*                                                                     *
***********************************************************************
*
         NC    DCBBLKSI,DCBBLKSI        IS BLKSIZE ZERO          S22027
         BNZ   OMG23650                 BRANCH IF NOT ZERO       S22027
         MVC   DCBBLKSI,JFCBLKSI        MERGE BLKSIZE            S22027
         OI    JFCBMASK+K2,X'10'        SET BLKSIZE MERGE MASK   S22027
OMG23650 EQU   *                        BLOCKSIZE NOT ZERO       S22027
*
         CLI   32(RDCB),K0              IS THRESH ZERO IN DCB
         BNE   OMG25000                 BRANCH IF NO
         MVC   32(1,RDCB),INFMJFCB+113  MERGE THRESH
         OI    JFCBMASK+K3,X'08'        INDICATE THRESH MERGED
         B     OMG25000                 BR TO MAIN MERGE
*
OMG23700 EQU   *                        DSORG=MQ  G,P ONLY
         NC    24(2,RDCB),24(RDCB)      IS SOWA=0
         BNZ   OMG23800                 BR IF SOWA NOT =0
         MVC   24(2,RDCB),JFCSOWA       MERGE SOWA
         OI    JFCBMASK+K3,X'10'        SET SOWA MERGE MASK
*
OMG23800 EQU   *                        SOWA NOT ZERO
         CLI   DCBRECFM,K0              IS RECFM = 0 IN DCB
         BNE   OMG23900                 BR IF RECFM NOT = 0
         MVC   DCBRECFM,JFCRECFM        MERGE RECORD FORMAT
         OI    JFCBMASK+K2,X'04'        SET RECFM IN MERGE MASK
*
OMG23900 EQU   *                        RECFM NOT ZERO
         LA    R1,K5                    POINT TO BUFNO IN MERGE
         B     OMG25000                 BR TO MAIN MERGE
*
***********************************************************************
*
* FOR TCAM WITH DSORG = TX, BUFMAX, PCI, RESERVE AND BUFSIZE FIELDS ARE
* HANDLED WITH A UNIQUE JFCB TO DCB MERGE.
*
***********************************************************************
*
OMG24000 EQU   *                        DSORG = TX
         CLI   25(RDCB),K0              IS PCI FIELD ZERO
         BNE   OMG24100                 BRANCH IF NON-ZERO
         MVC   25(1,RDCB),INFMJFCB+107  MERGE PCI
         OI    JFCBMASK+K1,X'20'        INDICATE PCI MERGED
OMG24100 EQU   *                        PCI NOT ZERO
         CLI   28(RDCB),K0              IS BUFMAX FIELD ZERO
         BNE   OMG24200                 BRANCH IF NON-ZERO
         MVC   28(1,RDCB),INFMJFCB+106  MERGE BUFMAX
         OI    JFCBMASK+K2,X'01'        INDICATE BUFMAX MERGED
OMG24200 EQU   *                        CHECK BUFSIZE
         OC    54(2,RDCB),54(RDCB)      IS BUFSIZE FIELD ZERO
         BNZ   OMG24300                 BRANCH IF NON-ZERO
         MVC   54(2,RDCB),INFMJFCB+102  MERGE BUFSIZE
         OI    JFCBMASK+K2,X'10'        INDICATE BUFSIZE MERGED
OMG24300 EQU   *                        BUFSIZE NOT ZERO
         OC    56(4,RDCB),56(RDCB)      IS RESERVE FIELD ZERO
         BNZ   OMG24400                 BR IF NO
         MVC   56(4,RDCB),INFMJFCB+108  MERGE RESERVE
         OI    JFCBMASK+K1,X'08'        INDICATE RESERVE MERGED
OMG24400 EQU   *                        RESERVE FIELD NOT ZERO
         B     OMG24600                 BRANCH TO MERGE
*
***********************************************************************
*                                                                     *
*        FOR TCAM DSORG = TR, IPLTXID IS HANDLED WITH A UNIQUE   S22024
*        JFCB TO DCB MERGE.                                      S22024
*                                                                     *
***********************************************************************
OMG24450 EQU   *                        CHECK TCAM DSORG         S22024
         CLI   DCBIPLTX,BLANK           IS IT TR                 S22024
         BNE   OMG26900                 NO, GO TO THE NEXT MODULES22024
         MVC   DCBIPLTX,JFCIPLTX        MERGE TO THE DCB         S22024
         OI    JFCBMASK+K1,X80          INDICATE MERGE           S22024
         B     OMG26900                 GO TO NEXT MODULE        S22024
*
OMG24500 EQU   *                        DSORG=CX  G,P OR R,W
         LA    R1,K3                    POINT TO BTAM IN MERGE TABLE
*
         TM    DCBMACRF,DCBMREAD        TEST IF BTAM READ
         BO    OMG25000                 BR IF YES
         TM    DCBMACRF+K1,DCBMWRIT     TEST IF BTAM WRITE
         BO    OMG25000                 BR IF YES
*
***********************************************************************
*                                       MUST BE QTAM BY DEFAULT
***********************************************************************
*
OMG24600 EQU   *                        MERGE
         CLI   24(RDCB),K0              IS INTVL=0
         BNE   OMG24700                 BR IF INTVL NOT=0
         MVC   24(1,RDCB),JFCINTVL      MERGE INTVL
         OI    JFCBMASK+K3,X'04'        SET INTVL MERGE MASK
OMG24700 EQU   *                        INTVL NOT ZERO
         CLI   32(RDCB),K0              IS CPRI=0
         BNE   OMG25000                 BR IF CPRI NOT=0
         MVC   32(1,RDCB),JFCCPRI       MERGE CPRI
         OI    JFCBMASK+K3,X'08'        SET CPRI MERGE MASK
*
***********************************************************************
*
*        JFCB TO DCB MERGE ROUTINE
*
***********************************************************************
OMG25000 EQU   *                        JFCB TO DCB MERGE
         LR    R0,RDCB                  DCB WORK REGISTER
         LA    RET,OMG2720K             DCB DISP PTR FOR MERGE
         LA    RF,OMG2800K              JFCB DISP PTR FOR MERGE
         LA    RD,OMG2840K              FIELD LENGTHS FOR JFCB-DCB MRG
         AR    RF,R1                    POINT TO
         AR    RET,R1                   PROPER
         AR    RD,R1                    ENTRY
         SR    RUCB,RUCB                CLEAR MASK REGISTER
*
OMG25200 EQU   *                        JFCB TO DCB MERGE CONTINUED
         L     RCORE,WTGCORE-K1-WTGENTRY(,RWTGC)  RELOAD DCB WORK ADDR
         LA    RCORE,DXJBF              ADDRESS OF JFCB
         SR    RC,RC                    CLEAR REGISTER
         IC    RC,0(,RET)               PICK UP DCB DISP
         AR    RDCB,RC                  POINT TO DCB FIELD
         IC    RC,0(,RF)                PICK UP JFCB DISP
         AR    RCORE,RC                 POINT TO JFCB FIELD
         IC    RC,0(,RD)                PICK UP FIELD LENGTH-1
         CLI   0(RET),X'24'             IS THIS FIELD RECFM
         BE    OMG26600                 BR IF YES
         EX    RC,OMG26000              ****EXECUTE COMPARE****
         BNZ   OMG25600                 BRANCH IF FIELD NOT ZERO
OMG25400 EQU   *                        EX INSTR FOR MERGE
         EX    RC,OMG26200              ****EXECUTE THE MERGE****
         O     RUCB,OMG2700K            OR A BIT INTO THE MASK REGISTER
*
OMG25600 EQU   *                        CONTINUE MERGE
         LA    RET,K1(RET)              STEP DCB DISP POINTER
         TM    0(RET),X'FF'             TEST IF AT END OF MERGE
         BZ    OMG26700                 BRANCH IF AT END OF MERGE
         BO    OMG26400                 BRANCH IF END OF A.M. MERGE
         LA    RF,K1(RF)                STEP JFCB DISP POINTER
         LA    RD,K1(RD)                STEP FIELD LENGTH POINTER
OMG25800 EQU   *                        SHIFT MERGE MASK TO LEFT
         SLL   RUCB,K1                  SHIFT THE MASK TO THE LEFT 1
         LR    RDCB,R0                  RESET DCB POINTER
         B     OMG25200                 START THRU MERGE LOOP AGAIN
*
OMG26000 NC    0(*-*,RDCB),0(RDCB)      ****CHECK FOR ZERO FIELD****
OMG26200 OC    0(*-*,RDCB),0(RCORE)     ****MERGE****
*
OMG26400 EQU   *                        END OF A.M. MERGE
         LA    RF,K1(RF)                STEP RF BY ONE
         IC    RC,0(,RF)                PICK UP SHIFT FACTOR
         SLL   RUCB,0(RC)               SHIFT MASK LEFT ONE
         LA    RET,OMG2720K             RESET POINTERS
         LA    RF,OMG2800K
         LA    RD,OMG2840K
         B     OMG25800                 BR TO SHIFT MERGE MASK
*
OMG26600 EQU   *                        RECFM TEST
         TM    0(RDCB),X'FF'-DCBRECKY   IS ANY VALUE IN RECFM
         BZ    OMG25400                 BR IF NO
         B     OMG25600                 BR RECFM NON-ZERO
*
OMG26700 EQU   *                        END OF MERGE
         L     RDCB,0(,RPARC)           RELOAD DCB ADDR
         L     RCORE,WTGCORE-K1-WTGENTRY(,RWTGC)  RELOAD DCB WORK ADDR
*
         SLL   RUCB,K6                  FINAL ALIGN MERGE MASK BITS
         O     RUCB,JFCBMASK            OR WITH EXISTING MERGE BITS
         ST    RUCB,JFCBMASK            STORE IN JFCB MASK FIELD
*
**********************************************************************
*
OMG26900 EQU   *                        MERGE COMPLETE, NEXT MODULE
         MVI   DXATOPEN,DXATMODE        SHOW DCB MERGE DONE      Y02144
         IECRES XCTL,ID6K6L             XCTL TO NEXT MODULE
*
OMG2700K DC    F'1'                     REG LOW ORDER BIT MASK
**********************************************************************
         EJECT
**********************************************************************
*
*
*
*        THE FOLLOWING ARE DISPLACEMENTS WITHIN THE DCB
*
*
*
*        OF FIELDS WHICH WILL BE MERGED TO THE DCB FROM THE JFCB
*
*
*
**********************************************************************
OMG2720K EQU   *
         DC    FL1'52'                  OPTCOD
         DC    FL1'36'                  RECFM
         DC    FL1'18'                  DEN
OMG2780K EQU   *
         DC    FL1'32'                  BFTEK OR BFALN
         DC    FL1'24'                  BUFL
         DC    FL1'20'                  BUFNO
         DC    X'00'                    ****END OF TABLE****
**********************************************************************
         SPACE 3
**********************************************************************
*
*
*
*        THE FOLLOWING ARE THE DISPLACEMENTS OF FIELDS IN THE
*
*
*
*        JFCB WHICH WILL BE MERGED INTO THE DCB
*
*
*
**********************************************************************
OMG2800K EQU   *
         DC    FL1'101'                 OPTCOD OFFSET
         DC    FL1'100'                 RECFM OFFSET
         DC    FL1'94'                  DEN OFFSET
         DC    FL1'89'                  BFTEK-BALN OFFSET
         DC    FL1'90'                  BUFL OFFSET
         DC    FL1'88'                  BUFNO OFFSET
         DC    X'00'                    END OF TABLE**********
**********************************************************************
         EJECT
**********************************************************************
*
*
*
*        THE FOLLOWING ARE THE LENGTHS-1 OF THE FIELDS
*
*
*
*        WHICH ARE MERGED FROM THE JFCB TO THE DCB
*
*
*
**********************************************************************
OMG2840K EQU   *
         DC    FL1'0'                   OPTCOD LGH
         DC    FL1'0'                   RECFM LGH
         DC    FL1'0'                   DEN LGH
         DC    FL1'0'                   BFTEK-BALN LGH
         DC    FL1'1'                   BUFL LGH
         DC    FL1'0'                   BUFNO LGH
         DC    X'00'                    ****END OF TABLE****
*
         XCTLTABL ID=(ID6K6L,6L),BRT=YES,LENGTH=                 Y02080
*
         DCBD  DSORG=(PS,IS,DA,PO,BS,TQ,TR)  INCLUDE 3705        S22024
*
         IECDSECS EXPAND=YES            EXPAND DESIRED DSECTS HERE
*
         END
