         TITLE 'IFG0199D - OPEN/CLOSE/EOV ABEND INTERPRETATION AND RECO*
               VERY TRACE ROUTINE'
         COPY  LCGASMSW
IFG0199D CSECT
         ENTRY IFG0209D
         ENTRY IFG0239D
         ENTRY IFG0559D
IFG0209D EQU   IFG0199D                 ALIAS ENTRY POINT        Y02080
IFG0239D EQU   IFG0199D                 ALIAS ENTRY POINT        Y02080
IFG0559D EQU   IFG0199D                 ALIAS ENTRY POINT        Y02080
***********************************************************************
*                   VS2 RELEASE 037 CHANGES                           *
*D85798002,86250002                                            @ZA32987
*                                                                     *
***********************************************************************
* MODULE NAME = IFG0199D (OS/VS2)                                     *
*                                                                     *
* DESCRIPTIVE NAME = OPEN/CLOSE/EOV ABEND INTERPRETATION AND RECOVERY *
*                    TRACE ROUTINE                                    *
*                                                                     *
* COPYRIGHT = NONE                                                    *
*                                                                     *
* STATUS = RELEASE 2, LEVEL 0                                         *
*                                                                     *
* FUNCTION =                                                          *
*        1. USE THE PROBLEM DETERMINATION CODE AS A BRANCH TABLE      *
*        OFFSET TO DETERMINE THE DATA TO BE TRACED.  DATA IS          *
*        TRACED BY ISSUING THE GRACE MACRO.  REGISTER D IS USED       *
*        FOR THE BRANCH TABLE OFFSET.  THE INFORMATION TRACED IS:     *
*           CODE      DATA TRACED                                     *
*           0  (0)    EXIT TO NEXT MODULE                             *
*           4  (4)    IOB, CCW'S                                      *
*           8  (8)    TIOT DD ENTRY, UCB'S                            *
*           12 (C)    JFCB VOLID'S                                    *
*           16 (10)   TAPE LABEL                                      *
*           20 (14)   JFCB DSN, TAPE LABEL                            *
*           24 (18)   DA VOLUME LABEL                                 *
*           28 (1C)   DSCB(S)                                         *
*           32 (20)   EXIT TO NEXT MODULE                             *
*           36 (24)   EXIT TO NEXT MODULE                             *
*           40 (28)   DCB                                             *
*           44 (2C)   EXIT TO NEXT MODULE                             *
*           48 (30)   EXIT TO NEXT MODULE                             *
*           52 (34)   DCB, DSCB                                       *
*           56 (38)   JFCB ELEMENT NAME                               *
*           60 (3C)   DCB EXIT LIST                                   *
*           64 (40)   DCB, TAPE LABEL                                 *
*           68 (44)   JFCB DSNAME, DCB                                *
*           72 (48)   JFCB DSNAME                                     *
*           76 (4C)   JFCB VOLID'S, DSCB                              *
*                                                                     *
*        2. EXITS TO NEXT MODULE IN PROBLEM DETERMINATION.            *
*                                                                     *
* NOTES = SEE BELOW                                                   *
*                                                                     *
*    DEPENDENCIES =                                                   *
*            CLASS ONE CHARACTER CODE.  THE EBCDIC CHARACTER CODE     *
*            WAS USED FOR ASSEMBLY.  THE MODULE MUST BE REASSEMBLED   *
*            IF A DIFFERENT CHARACTER SET IS USED FOR EXECUTION.      *
*                                                                     *
*    RESTRICTIONS = NONE                                              *
*                                                                     *
*    REGISTER CONVENTIONS =                                           *
*            R2 POINTS TO DCB                                         *
*            R4 POINTS TO OPEN WORK AREA                              *
*            R5 POINTS TO THE RESIDENT ROUTINE                        *
*            R6 POINTS TO THE WTG TABLE                               *
*            R7 POINTS TO THE CURRENT PARAMETER LIST ENTRY            *
*            R8 POINTS TO THE CURRENT WTG TABLE ENTRY                 *
*            R9 POINTS TO THE DD ENTRY IN THE TIOT                    *
*            R10 POINTS TO THE UCB                                    *
*                                                                     *
*    PATCH LABEL = SEE NEXT TO LAST LABEL BEFORE ORG STATEMENT AT END *
*                  OF LISTING.                                        *
*                                                                     *
* MODULE TYPE = CONTROL (OPEN, CLOSE, EOV DATA MANAGEMENT)            *
*                                                                     *
*    PROCESSOR = ASSEMBLER XF                                         *
*                                                                     *
*    MODULE SIZE = SEE EXTERNAL SYMBOL DICTIONARY OR LOC FIELD ON     *
*                  ORG STATEMENT AT END OF LISTING                    *
*                                                                     *
*    ATTRIBUTES = REENTRANT, REFRESHABLE,READ-ONLY, ENABLED,          *
*                  PRIVILEGED, SUPERVISOR STATE, DATA MANAGEMENT KEY, *
*                  LINK PACK AREA RESIDENT/PAGEABLE                   *
*                                                                     *
* ENTRY POINT = IFG0199D FOR OPEN                                     *
*               IFG0209D FOR CLOSE                                    *
*               IFG0239D FOR TCLOSE                                   *
*               IFG0559D FOR EOV                                      *
*                                                                     *
*    PURPOSE = SEE FUNCTION                                           *
*                                                                     *
*    LINKAGE = R1 POINTS TO PROBLEM DETERMINATION WORK AREA.          *
*              R2,R4-R10 SAME AS WHEN PROBLEM DETERMINATION CALLED.   *
*              PROBLEM DETERMINATION CODE IN WORK AREA.               *
*                                                                     *
* INPUT = SEE FUNCTION AND LINKAGE.                                   *
*                                                                     *
* OUTPUT = TRACE DATA.                                                *
*          TRACE CODE IN PROBLEM DETERMINATION WORK AREA.             *
*                                                                     *
* EXIT-NORMAL = IFG0199E FOR OPEN                                     *
*               IFG0209E FOR CLOSE                                    *
*               IFG0239E FOR TCLOSE                                   *
*               IFG0559E FOR EOV                                      *
*                                                                     *
* EXIT-ERROR = NONE                                                   *
*                                                                     *
* EXTERNAL REFERENCES = SEE BELOW                                     *
*                                                                     *
*    ROUTINES = IFG019RA                                              *
*               LOCK ROUTINE                                          *
*               GTRACE MONITOR CALL                                   *
*                                                                     *
*    DATA AREAS = PROBLEM DETERMINATION WORK AREA                     *
*                 OPEN/CLOSE/EOV WORK AREA                            *
*                                                                     *
*    CONTROL BLOCKS = CVT                                             *
*                     DCB                                             *
*                     DSCB                                            *
*                     IOB                                             *
*                     JFCB                                            *
*                     SVRB                                            *
*                     TCB                                             *
*                     TIOT                                            *
*                     UCB                                             *
*                                                                     *
* TABLES = DCB EXIT LIST                                              *
*                                                                     *
* MACROS = GTRACE                                                     *
*          IECDSECS                                                   *
*          IECPDSCT                                                   *
*          IECRES LOAD                                                *
*          MODESET                                                    *
*          SETLOCK                                                    *
*          XCTLTABL                                                   *
*                                                                     *
* CHANGE ACTIVITY = NEW RELEASE (LEVEL 0)                             *
*                                                                     *
***********************************************************************
         EJECT
R0       EQU   0                        WORK/PARAMETER REG
R1       EQU   1                        WORK/PARAMETER REG
RDCB     EQU   2                        PTR TO DCB
RBASE    EQU   3                        BASE REG
RCORE    EQU   4                        O/C/EOV WKAREA PTR
RWTG     EQU   6                        PTR TO WTG TABLE
RPARC    EQU   7                        PTR TO PARALIST CURRENT ENTRY
RWTGC    EQU   8                        PTR TO WTG TABLE CURRENT ENTRY
RTIOT    EQU   9                        PTR TO TIOT ENTRY
RUCB     EQU   10                       PTR TO UCB
RB       EQU   11                       WORK/PARAMETER REG
RC       EQU   12                       WORK/PARAMETER REG
RD       EQU   13                       WORK/PARAMETER REG
RET      EQU   14                       WORK/PARAMETER REG
RF       EQU   15                       WORK/PARAMETER REG
ONE      EQU   1                        WORK VALUE
ORG      EQU   0                        WORK VALUE
IOBLNG   EQU   44                       LNG OF IOB IN O/C/EOV WKAREA
CCWLNG   EQU   8                        LNG OF CCW
TIC      EQU   X'08'                    CHANNEL COMMAND CODE OF TIC
FOUR     EQU   4                        WORK VALUE
ZERO     EQU   0                        WORK VALUE
TWO      EQU   2                        WORK VALUE
UCBLNG   EQU   60                       LNG OF DA UCB
DEXITLNG EQU   4                        LNG OF DCB EXIT LIST ENTRY
JVOLLNG  EQU   30                       LNG OF JFCBVOLS FIELD
LBLLNG   EQU   80                       LNG OF TAPE LABEL
DSNLNG   EQU   44                       MAXIMUM LNG OF DSNAME
DSCBLNG  EQU   96                       LNG OF F1 DSCB
CDCHN    EQU   X'C0'                    CCW FLAGS INDICATING CHAINING
FIDLNG   EQU   2                        LNG OF FORMAT ID
CURRTCB  EQU   4                        CURRENT TCB ADDR OFFSET  Y02082
IOBCOD   EQU   4                        TRACE CODE FOR IOB
CCWCOD   EQU   32                       TRACE CODE FOR CCWS
TIOCOD   EQU   8                        TRACE CODE FOR TIOT ENTRY
UCBCOD   EQU   36                       TRACE CODE FOR UCB
JVOLCOD  EQU   12                       TRACE CODE FOR JFCBVOLS
TLBLCOD  EQU   16                       TRACE CODE FOR TAPE LABEL
JDSNCOD  EQU   20                       TRACE CODE FOR JFCB DSNAME
VLBLCOD  EQU   24                       TRACE CODE FOR VOLUME LABEL
DSCBCOD  EQU   28                       TRACE CODE FOR F1 DSCB
DCBCOD   EQU   40                       TRACE CODE FOR DCB
JELNMCOD EQU   52                       TRACE CODE FOR JFCBELNM
DEXITCOD EQU   56                       TRACE CODE FOR DCBEXLST ENTRY
ELNMLNG  EQU   8                        LNG OF JFCBELNM
LAST     EQU   X'80'                    BIT IN DCB EXIT LAST INDICATING
*                                       LAST ENTRY
THREE    EQU   3                        WORK VALUE
F2HXCODE EQU   X'02'                    F2 DSCB FORMAT ID
F3HXCODE EQU   X'03'                    F3 DSCB FORMAT ID
F1CODE   EQU   X'F1'                    F1 DSCB FORMAT ID
F2DSCBCD EQU   X'58'                    TRACE CODE FOR F2 DSCB
F3DSCBCD EQU   X'5C'                    TRACE CODE FOR F3 DSCB
F23DSCLN EQU   44                       LNG OF FIRST PART OF F2 AND F3
*                                       DSCBS THAT ARE TRACED
F3COD2   EQU   X'68'                    TRACE CODE FOR LAST 96 BYTES OF
*                                       F3 DSCB
         BALR  RBASE,R0                 ESTABLISH ADDRESSABILITY
         USING *,RBASE
         USING FORCORE,RCORE
         USING PDDSECT,R1
         USING TIOT,RTIOT
         USING IHADCB,RDCB
         USING WTG,RWTG                                         XM8309
         MVC   PDTRCFID(FIDLNG),OCEFID  MOVE FORMAT ID TO BUFFER
         LA    RF,PDTRCDAT              PTR TO DATA ADDR
         ST    RF,PDDATAAD              STORE IN BUFFER
         SR    RF,RF                    ZERO WKREG
         SR    RD,RD                    ZERO WKREG
         IC    RD,PDCODE                PUT TRACE CODE IN REG
         B     PDF03020(RD)             USE TO REFERENCE BRANCH TABLE
PDF03020 EQU   *                        BRANCH TABLE
         B     PDF03820                 NO TRACE, CHECK FOR ABEND EXIT
*                                       CODE 00
         B     PDF03040                 TRACE IOB, CCWS
*                                       CODE 04
         B     PDF03060                 TRACE TIOT ENTRY, UCBS
*                                       CODE 08
         B     PDF03100                 TRACE JFCB VOLIDS
*                                       CODE 0C
         B     PDF03140                 TRACE TAPE LABEL
*                                       CODE 10
         B     PDF03160                 TRACE JFCB DSN FLD, TAPE LABEL
*                                       CODE 14
         B     PDF03120                 TRACE DA VOLUME LABEL
*                                       CODE 18
         B     PDF03180                 TRACE DSCB
*                                       CODE 1C
         B     PDF03820                 NO TRACE, CHECK FOR ABEND EXIT
*                                       CODE 20
         B     PDF03820                 NO TRACE, CHECK FOR ABEND EXIT
*                                       CODE 24
         B     PDF03200                 TRACE DCB
*                                       CODE 28
         B     PDF03820                 NO TRACE, CHECK FOR ABEND EXIT
*                                       CODE 2C
         B     PDF03820                 NO TRACE, CHECK FOR ABEND EXIT
*                                       CODE 30
         B     PDF03220                 TRACE DCB, DSCB
*                                       CODE 34
         B     PDF03240                 TRACE JFCB ELEM NAME
*                                       CODE 38
         B     PDF03260                 TRACE DCB EXIT LIST
*                                       CODE 3C
         B     PDF03280                 TRACE DCB, TAPE LABEL
*                                       CODE 40
         B     PDF03300                 TRACE DCB, JFCB DSNAME
*                                       CODE 44
         B     PDF03320                 TRACE JFCB DSNAME
*                                       CODE 48
         B     PDF03340                 TRACE JFCB VOLIDS, DSCB
*                                       CODE 4C
PDF03040 EQU   *                        TRACE IOB, CCWS
         BAL   RB,PDF03360              MOVE IOB FR WKAREA TO TRACE BFR
         BAL   RB,PDF03800              GO TO TRACE RTN
         BAL   RB,PDF03380              MOVE CCWS TO TRACE BUFFER
         LA    RB,PDF03820              LOAD RETURN ADDRESS IN REG
         B     PDF03800                 BRANCH TO TRACE RTN
PDF03060 EQU   *                        TRACE TIOT ENTRY, UCBS
         BAL   RB,PDF03460              MOVE TIOT TO TRACE BUFFER
         BAL   RB,PDF03800              GO TO TRACE RTN
         LR    R0,RUCB                  SAVE UCB REG
         LA    RET,TIOEFSRT-ONE-TIOENTRY(RTIOT)  PTR TO UCB IN TIOT
         SR    RC,RC                    ZERO WORK REG
         IC    RC,TIOELNGH-TIOENTRY(RTIOT)  PUT LNG OF ENTRY IN REG
         AR    RC,RTIOT                 POINT TO END OF ENTRY
PDF03080 L     RUCB,ORG(RET)            SET UP RUCB FOR UCB TRACE RTN
         BAL   RB,PDF03500              MOVE UCB TO TRACE RTN
         BAL   RB,PDF03800              TRACE UCB                M0144
         LA    RET,FOUR(RET)            POINT TO NEXT DEVICE ENTRY
         CR    RET,RC                   END OF DD ENTRY
         BNE   PDF03080                 NO
         LR    RUCB,R0                  RESTORE UCB REG
         B     PDF03820                 GO TO EXIT FROM MODULE   M0144
PDF03100 EQU   *                        TRACE JFCB VOLUME IDS
         BAL   RB,PDF03560              GO TO TRACE VOLIDS FROM JFCB
         LA    RB,PDF03820              LOAD RETURN ADDRESS
         B     PDF03800                 GO TO TRACE RTN
PDF03120 EQU   *                        TRACE DA VOLUME LABEL
         BAL   RB,PDF03620              MOVE VOL LABEL TO TRACE BUFFER
         LA    RB,PDF03820              SET RETURN ADDRESS
         B     PDF03800                 GO TO TRACE RTN
PDF03140 EQU   *                        TRACE TAPE LABEL
         BAL   RB,PDF03580              MOVE TAPE LABEL TO TRACE BUFFER
         LA    RB,PDF03820              LOAD RETURN ADDRESS
         B     PDF03800                 GO TO TRACE RTN
PDF03160 EQU   *                        TRACE JFCB DSN, TAPE LABEL
         BAL   RB,PDF03640              MOVE DSN FROM JFCB TO BUFFER
         BAL   RB,PDF03800              GO TO TRACE RTN
         B     PDF03140                 TRACE TAPE LABEL
PDF03180 EQU   *                        SET DSCB TRACE CODE
         MVI   PDTRCDAT,DSCBCOD         MOVE TRACE CODE TO BUFFER
PDF03182 EQU   *                        TRACE FORMAT 1 DSCB
         CLI   DSCFMTID,F1CODE          FORMAT 1 DSCB
         BNE   PDF03185                 NO, CHECK FOR F2 OR F3 DSCB
         BAL   RB,PDF03660              MOVE DSCB TO BUFFER
         BAL   RB,PDF03800              GO TO TRACE RTN
         NC    DSCCORE+ONE(THREE),DSCCORE+ONE  ANOTHER DSCB IN CORE
         BZ    PDF03820                 NO, EXIT
         L     RCORE,DSCCORE            POINT TO NEXT DSCB
PDF03185 EQU   *                        CHK DSCB FORMAT TYPE
         CLI   ORG(RCORE),F2HXCODE      IS IT A FORMAT 2 DSCB
         MVI   PDTRCDAT,F2DSCBCD        MOVE F2 DSCB CODE TO BUFFER
         BE    PDF03190                 GO TO TRACE F2 DSCB
         CLI   ORG(RCORE),F3HXCODE      IS IT A FORMAT 3 DSCB
         MVI   PDTRCDAT,F3DSCBCD        MOVE TRACE CODE TO BUFFER
         BE    PDF03190                 GO TO TRACE F3 DSCB
*                                       IF NOT F2 OR F3 DSCB, IT MUST
*                                       BE ANOTHER F1
         MVI   PDTRCDAT,DSCBCOD         MOVE CODE TO BUFFER
         CLI   ORG(RCORE),F1CODE        IS DSNAME PREFIX PRESENT?
         BE    PDF03180                 NO, GO TRACE F1 DSCB
         LA    RCORE,DSNLNG(RCORE)      BUMP PAST PREFIX
*                                       NOW CHECK TO SEE WHETHER OR
*                                       NO IT IS A VALID DSCB OR THE
*                                       WKAREA HAS BEEN CLOBBERRED
         CLI   ORG(RCORE),F1CODE        IS IT A F1 DSCB W/O PREFIX
         BNE   PDF03820                 NO, EXIT WITHOUT TRACING
         B     PDF03180                 GO TRACE F1 DSCB
PDF03190 EQU   *                        TRACE FORMAT 2 OR 3 DSCB
         BAL   RB,PDF03670              GO MOVE F2 OR F3 DSCB TO BFR
         BAL   RB,PDF03800              TRACE IT
         MVI   PDTRCDAT,F2HXCODE        MOVE CODE FOR 2ND HALF OF F2
*                                       DSCB TO TRACE BUFFER
         LA    RCORE,F23DSCLN(,RCORE)   BUMP PTR TO DSCB 2ND HALF
         CLI   PDTRCDAT,F2HXCODE        TRACING FORMAT 2 DSCB
         BE    PDF03182                 YES, USE F1 TRACE RTN TO TRACE
*                                       2ND HALF OF F2 DSCB
         MVI   PDTRCDAT,F3COD2          MOVE CODE FOR 2ND HALF OF F3
*                                       DSCB TO TRACE BUFFER
         B     PDF03182                 USE F1 TRACE RTN TO TRACE
*                                       2ND HALF OF F3 DSCB
PDF03200 EQU   *                        TRACE DCB
         BAL   RB,PDF03680              MOVE DCB TO BUFFER
         LA    RB,PDF03820              SET RETURN ADDR
         B     PDF03800                 GO TO TRACE RTN
PDF03220 EQU   *                        TRACE DCB, DSCB
         BAL   RB,PDF03680              MOVE DCB TO BUFFER
         BAL   RB,PDF03800              GO TO TRACE RTN
         B     PDF03180                 TRACE DSCB
PDF03240 EQU   *                        TRACE JFCB ELEMENT NAME
         BAL   RB,PDF03700              MOVE JFCB ELEM NAME TO BFR
         LA    RB,PDF03820              SET RETURN ADDR
         B     PDF03800                 GO TO TRACE RTN
PDF03260 EQU   *                        TRACE DCB EXIT LIST
         BAL   RB,PDF03720              MOVE EXLST TO BUFFER
         LA    RB,PDF03820              SET RETURN ADDRESS
         B     PDF03800                 GO TO TRACE RTN
PDF03280 EQU   *                        TRACE DCB, TAPE LABEL
         BAL   RB,PDF03680              MOVE DCB TO BUFFER
         BAL   RB,PDF03800              GO TO TRACE RTN
         B     PDF03140                 TRACE TAPE LABEL
PDF03300 EQU   *                        TRACE JFCB DSNAME, DCB
         BAL   RB,PDF03640              TRACE JFCB DSNAME
         BAL   RB,PDF03800              GO TO TRACE RTN
         B     PDF03200                 TRACE DCB
PDF03320 EQU   *                        TRACE JFCB DSNAME
         BAL   RB,PDF03640              TRACE JFCB DSNAME
         LA    RB,PDF03820              SET RETURN ADDRESS
         B     PDF03800                 GO TO TRACE RTN
PDF03340 EQU   *                        TRACE JFCB VOLIDS, DSCB
         BAL   RB,PDF03560              TRACE JFCB VOLIDS
         BAL   RB,PDF03800              GO TO TRACE RTN
         B     PDF03180                 TRACE DSCB
PDF03360 EQU   *                        MOVE IOB TO TRACE BUFFER
         MVI   PDTRCDAT,IOBCOD          MOVE TRACE CODE TO BUFFER
         MVC   PDTRCDAT+ONE(IOBLNG),DXIOB  TRACE IOB FROM WKAREA
         LA    RF,IOBLNG+ONE            LENGTH OF TRACE DATA
         BR    RB                       RETURN
PDF03380 EQU   *                        SET UP TO MOVE CCWS TO BUFFER
*                                       THE FOLLOWING ROUTINE MOVES
*                                       CCWS FROM THE O/C/EOV WKAREA
*                                       TO THE TRACE BUFFER.
*                                       IF A CHAIN BIT IS ON IN THE
*                                       CCW, OR THE COMMAND CODE IS
*                                       A TIC, THEN IT IS ASSUMED
*                                       THAT ANOTHER CCW FOLLOWS THIS
*                                       ONE.
*                                       AS EACH CCW IS MOVED TO THE
*                                       BUFFER, THE COUNT IS UPDATED.
         LA    RF,CCWLNG+ONE            LNG OF A CCW
         LA    RC,DXCCW1                PTR TO FIRST CCW
         LA    RET,PDTRCDAT+ONE         PTR TO BUFFER
PDF03400 EQU   *                        MOVE CCW TO TRACE BUFFER
         MVC   ORG(CCWLNG,RET),ORG(RC)  MOVE CCW TO BUFFER
         TM    ORG(RC),TIC              IS CCW A TIC
         BO    PDF03420                 YES, THERE MUST BE MORE CCWS
         TM    FOUR(RC),CDCHN           COMMAND OR DATA CHAIN
         BZ    PDF03440                 BRANCH IF NO MORE CCWS TO TRACE
PDF03420 EQU   *                        INCR POINTERS, COUNT
         LA    RC,CCWLNG(RC)            BUMP CCW PTR
         LA    RET,CCWLNG(RET)          BUMP BUFFER PTR
         LA    RF,CCWLNG(RF)            BUMP LNG COUNT
         B     PDF03400                 GO MOVE CCW TO BUFFER
PDF03440 EQU   *                        SET CCW TRACE CODE
         MVI   PDTRCDAT,CCWCOD          MOVE CODE TO BUFFER
         BR    RB                       RETURN TO CALLER
PDF03460 EQU   *                        CHK TIOT DD ENTRY LENGTH
         IC    RF,ORG(,RTIOT)           PUT LNG OF DD ENTRY IN   M0144
*                                       REG                      M0144
         LA    R0,((L'PDTRCDAT-ONE)/FOUR)*FOUR  SET LENGTH FOR  OY02805
*                                       MAXIMUM WORDS IN BUFFER OY02805
         CR    RF,R0                    DD ENTRY FIT IN BUFFER  OY02805
         BNH   PDF03470                 BR IF YES               OY02805
         LR    RF,R0                    USE SMALLER BUFFER SIZE OY02805
PDF03470 EQU   *                        MOVE TIOT ENTRY TO BFR  OY02805
         BCTR  RF,ZERO                  REDUCE BY ONE FOR EX
         EX    RF,PDF03480              MOVE TIOT ENTRY TO BUFFER
         LA    RF,TWO(RF)               RESTORE LNG AND ADD ONE FOR COD
         MVI   PDTRCDAT,TIOCOD          MOVE TRACE CODE TO BUFFER
         BR    RB                       RETURN TO CALLER
PDF03480 MVC   PDTRCDAT+ONE(ZERO),TIOENTRY-TIOENTRY(RTIOT) MOVE TIOT   *
                                        ENTRY TO BUFFER
PDF03500 EQU   *                        MOVE UCB TO BUFFER
         MVI   PDTRCDAT,UCBCOD          MOVE TRACE CODE TO BUFFER
         LA    RF,UCBLNG+ONE            UCB LENGTH
         MVC   PDTRCDAT+ONE(UCBLNG),ORG(RUCB)  MOVE UCB TO BUFFER
         BR    RB                       RETURN TO CALLER
PDF03560 EQU   *                        MOVE JFCB VOL IDS TO BUFFER
         MVC   PDTRCDAT+ONE(JVOLLNG),JFCBVOLS  MOVE VOLIDS TO BUFFER
         MVI   PDTRCDAT,JVOLCOD         MOVE TRACE CODE TO BUFFER
         LA    RF,JVOLLNG+ONE           PUT LNG IN REG
         BR    RB                       RETURN
PDF03580 EQU   *                        SET TAPE LABEL TRACE CODE
         MVI   PDTRCDAT,TLBLCOD         MOVE TRACE CODE TO BUFFER
PDF03600 EQU   *                        MOVE TAPE LABEL TO BUFFER
         MVC   PDTRCDAT+ONE(LBLLNG),DXLBL
         LA    RF,LBLLNG+ONE            LNG OF TRACE DATA
         BR    RB                       RETURN
PDF03620 EQU   *                        SET VOLUME LABEL TRACE CODE
         MVI   PDTRCDAT,VLBLCOD         MOVE TRACE CODE TO BUFFER
         B     PDF03600                 GO MOVE LABEL TO BUFFER
PDF03640 EQU   *                        MOVE DSNAME TO BUFFER
         MVC   PDTRCDAT+ONE(DSNLNG),JFCBDSNM
         LA    RF,DSNLNG+ONE            PUT LNG IN REG
         MVI   PDTRCDAT,JDSNCOD         MOVE TRACE CODE TO BUFFER
         BR    RB                       RETURN
PDF03660 EQU   *                        MOVE DSCB TO BUFFER
         MVC   PDTRCDAT+ONE(DSCBLNG),DXDSCB
         LA    RF,DSCBLNG+ONE           PUT LNG IN REG
         BR    RB                       RETURN
PDF03670 EQU   *                        MOVE F2 OR F3 DSCB TO BUFFER
         MVC   PDTRCDAT+ONE(F23DSCLN),DXDSCB
         LA    RF,F23DSCLN+ONE          PUT LNG IN REG
         BR    RB                       RETURN
PDF03680 EQU   *                        SET DCB TRACE CODE
         MVI   PDTRCDAT,DCBCOD          MOVE TRACE CODE TO BUFFER
         LH    RF,DXUDCBPL              LOAD DCB PREFIX LENGTH   YM1297
         AH    RF,DXUDCBML              ADD DCB LENGTH           YM1297
         STH   RF,ORG(,R1)              SAVE ACB/DCB LENGTH      YM3945
         LA    RF,L'PDTRCDAT-ONE        SET MAX LNG TO TRANSFER  YM3945
         CH    RF,ORG(,R1)              COMP MAX LNG TO DCB SIZE YM3945
         BNH   PDF03685                 BR TO USE MAX LENGTH     YM3945
         LH    RF,ORG(,R1)              USE ACTUAL SIZE DCB/ACB  YM3945
PDF03685 EQU   *                        SET LNG                  YM3945
         BCTR  RF,R0                    SET RF FOR EXECUTE INSTR YM1115
         EX    RF,PDF03690              MOVE DCB TO BUFFER       YM1297
         LA    RF,TWO(RF)               RESTORE TRACE DATA LNG   YM1297
         BR    RB                       RETURN
PDF03690 MVC   PDTRCDAT+ONE(ZERO),ORG(RDCB) MOVE DCB TO BUFFER   YM1297
PDF03700 EQU   *                        MOVE JFCB ELEMENT NAME
         MVI   PDTRCDAT,JELNMCOD        MOVE TRACE CODE TO BUFFER
         MVC   PDTRCDAT+ONE(ELNMLNG),JFCBELNM
*                                       MOVE ELEMENT NAME TO BUFFER
         LA    RF,ELNMLNG+ONE           DATA LENGTH
         BR    RB                       RETURN
PDF03720 EQU   *                        SET USER KEY
         CLC   DCBEXLST+ONE(THREE),EXITCHK DCB PT TO EXIT LIST  OY02805
         BNH   PDF03820                 BR IF NO, NO TRACE      OY02805
         MVI   PDTRCDAT,DEXITCOD        MOVE TRACE CODE TO BFR   YM5352
         L     RC,CVTPTR                ADDRESS OF CVT           Y02082
         USING CVT,RC                                            Y02082
         L     RC,CVTTCBP               TCB POINTERS ADDRESS     Y02082
         L     RC,CURRTCB(,RC)          CURRENT TCB ADDRESS      Y02082
         USING TCB,RC                   USING FOR MODESET        Y02082
         MODESET EXTKEY=RBT234,WORKREG=12 USER KEY               Y02082
         L     RC,DCBEXLST              PTR TO USER EXIT LIST
         LA    R0,((L'PDTRCDAT-ONE)/FOUR)*FOUR  SET LENGTH FOR  OY02805
*                                       MAXIMUM WORDS IN BUFFER OY02805
         LA    RET,DEXITLNG             LNG OF ENTRY
PDF03740 TM    ORG(RC),LAST             LAST ENTRY
         BO    PDF03760                 YES
         CR    RET,R0                   EXIT LIST FIT IN BUFFER OY02805
         BNL   PDF03760                 BR IF BUFFER NOW FULL   OY02805
         LA    RC,FOUR(RC)              POINT TO NEXT ENTRY
         LA    RET,FOUR(RET)            BUMP LENGTH
         B     PDF03740                 GO CHECK FOR LAST ENTRY IN LIST
PDF03760 EQU   *                        SET DCB EXIT CODE
         LA    RF,ONE(RET)              LNG OF DATA
         BCTR  RET,R0                   SUBTRACT ONE FOR EX
         MODESET EXTKEY=SUPR            SETLOCK KEY              Y02082
         LR    RC,RF                    SAVE RF                  Y02082
         L     RF,WTGPREFX              PREFIX ADDR              Y02082
         STM   RB,RET,IECREGSV-IECPREFX(RF)                      Y02082
PDF03762 EQU   *                        MOVE DCB EXIT LIST       Y02082
         SETLOCK OBTAIN,TYPE=LOCAL,MODE=UNCOND, GET LOCAL LOCK   Y02082*
               RELATED=(FREEMAIN,IFG0199D(PDF03764))             Y02082
         LM    RB,RET,IECREGSV-IECPREFX(RF) RESTORE REGS         Y02082
         LR    RF,RC                    RESTORE RF               Y02082
         L     RC,CVTPTR                ADDRESS OF CVT           Y02082
         USING CVT,RC                                            Y02082
         L     RC,CVTTCBP               TCB POINTERS ADDRESS     Y02082
         L     RC,CURRTCB(,RC)          CURRENT TCB ADDRESS      Y02082
         USING TCB,RC                   USING FOR MODESET        Y02082
         MODESET EXTKEY=RBT234,WORKREG=12 USER KEY               Y02082
         DROP  RC                                                Y02082
         L     RC,DCBEXLST              PTR TO EXIT LIST        OY02805
         MODESET EXTKEY=SUPR            KEY 0 FOR MOVE           Y02082
         EX    RET,PDF03780             MOVE EXIT LIST TO BUFFER
         LR    RC,RF                    SAVE RF                  Y02082
         L     RF,WTGPREFX              WTG PREFIX               Y02082
         STM   RB,RET,IECREGSV-IECPREFX(RF) SAVE REGS            Y02082
PDF03764 EQU   *                        RELEASE LOCK, SET KEY 5  Y02082
         SETLOCK RELEASE,TYPE=LOCAL,    RELEASE LOCAL LOCK       Y02082*
               RELATED=(FREEMAIN,IFG0199D(PDF03762))             Y02082
         LM    RB,RET,IECREGSV-IECPREFX(RF) RESTORE REGS         Y02082
         LR    RF,RC                    RESTORE RF               Y02082
         MODESET EXTKEY=DATAMGT         KEY 5                    Y02082
         BR    RB                       RETURN
PDF03780 MVC   PDTRCDAT+ONE(ZERO),ORG(RC) MOVE EXIT LIST
PDF03800 EQU   *                        ISSUE GTRACE MACRO
         STH   RF,PDTRCLNG              STORE LNG OF TRACE DATA IN BFR
         GTRACE ID=4095,PAGEIN=YES,MF=(E,(1))  ISSUE TRACE MACRO YM7364
         BR    RB                       RETURN
PDF03820 EQU   *                        RESTORE WORK AREA PT
         L     RCORE,WTGCORE-WTGENTRY-ONE(RWTGC)
*                                       RESTORE RCORE IN CASE IT WAS
*                                       DESTROYED
         IECRES LOAD,PREFIX=WTGPREFX,BRANCH=DIRECT,              Y02080X
               MODID=EXITID                                      Y02080
*                                       GO TO EXIT ROUTINE       Y02080
OCEFID   DC    X'00FF'                  TRACE FORMAT ID
EXITCHK  DC    AL3(1)                   ADDR TO CHK IF DCB EXIT OY02805
*                                       LIST EXISTS             OY02805
         XCTLTABL ID=(EXITID,9E),BRT=YES,LENGTH=                 Y02080
         IECPDSCT
         IECDSECS MAIN,RB,TCB,TIOT,CVT,DCB,WTG,PREFX,            Y02080*
               DSCB2,DSCB3,PSA,EXPAND=YES                        Y02082
         END
