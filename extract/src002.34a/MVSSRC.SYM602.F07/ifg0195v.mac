         TITLE 'IFG0195V                      TSO AUTOMATIC SECURITY PAS
               SSWORD'                                           Y02134
IFG0195V CSECT
***********************************************************************
*                                                                     *
*             VS2 RELEASE 037 DELETIONS/CHANGES                       *
*0000743000                                                    @ZA10842
*                                                                     *
*                                                                     *
* MODULE NAME = IFG0195V (OS/VS2)                                     *
*                                                                     *
* DESCRIPTIVE NAME = TSO AUTOMATIC SECURITY PASSWORD ROUTINE          *
*                                                                     *
* COPYRIGHT = NONE                                                    *
*                                                                     *
* STATUS = RELEASE 2, LEVEL 0                                         *
*                                                                     *
* FUNCTION =                                                          *
*                                                                     *
*        1. SETS UP CONTROL BLOCKS IN THE SECURITY WORK AREA          *
*           TO DO I/O TO THE PASSWORD DATA SET ON SYSRES.             *
*                                                                     *
*        2. IF THE DSCB POINTED BY INPUT REGISTER 4 INDICATES         *
*           DSORG=VSAM, CONTROL IS PASSED TO IDA0192G.                *
*                                                                     *
*        3. F4 DSCB OF SYSRES IS OBTAINED TO READ F1 OF THE           *
*           PASSWORD DATA SET WITH THE PROPER EXTENTS FOR THE VTOC.   *
*                                                                     *
*        4. THE F1 DSCB OF THE PASSWORD DATA SET IS READ TO GET       *
*           THE PROPER EXTENTS FOR READING THE PASSWORD DATA SET.     *
*                                                                     *
*        5. THE INPUT DSAB IS INSPECTED TO DETERMINE IF A TTR FOR     *
*           THE PASSWORD RECORD EXISTS, INDICATING THAT THE PASSWORD  *
*           WAS PREVIOUSLY REQUESTED. IF THE TTR EXISTS, THE          *
*           PASSWORD RECORD IS READ, THE DSNAME IS COMPARED TO THE    *
*           INPUT DSNAME AND THE PROCESSING MODE IS CHECKED.          *
*           IF THE PASSWORD RECORD SATISFIES THE REQUEST, CONTROL     *
*           IS PASSED TO IFG0195U TO UPDATE THE PASSWORD RECORD.      *
*                                                                     *
*        6. IF THE TTR IS NOT PRESENT OR IF THE PASSWORD RECORD       *
*           TESTS FAIL, THE MASTER LOGON PASSWORD IN THE TSB IS       *
*           MOVED TO THE PASSWORD AREA TO BE USED IN IFG0195U         *
*           TO SEARCH THE PASSWORD DATA SET.                          *
*                                                                     *
*        7. IF NO MASTER LOGON PASSWORD EXISTS, CONTROL PASSES        *
*           TO READPSWD TO GET A PASSWORD.                            *
*                                                                     *
* NOTES = ALL SEARCHES TO THE PASSWORD DATA SET ARE PROTECTED VIA ENQ.*
*                                                                     *
*    DEPENDENCIES =                                                   *
*                                                                     *
*            CLASS ONE CHARACTER CODE.  THE EBCDIC CHARACTER CODE     *
*            WAS USED FOR ASSEMBLY.  THE MODULE MUST BE REASSEMBLED   *
*            IF A DIFFERENT CHARACTER SET IS USED FOR EXECUTION.      *
*                                                                     *
*    RESTRICTIONS = NONE                                              *
*                                                                     *
*    REGISTER CONVENTIONS = SEE IECEQU MACRO.                         *
*                                                                     *
*    PATCH LABEL = SEE XCTLTABL MACRO.                                *
*                                                                     *
* MODULE TYPE = CONTROL (OPEN, CLOSE, EOV DATA MANAGEMENT)            *
*                                                                     *
*    PROCESSOR = ASSEMBLER XF                                         *
*                                                                     *
*    MODULE SIZE = SEE EXTERNAL SYMBOL DICTIONARY OR LOC FIELD ON     *
*                  ORG STATEMENT AT END OF LISTING                    *
*                                                                     *
*    ATTRIBUTES = REENTRANT, REFRESHABLE,READ-ONLY, ENABLED,          *
*                 PRIVILEGED, SUPERVISOR STATE, DATA MANAGEMENT KEY,  *
*                 LINK PACK AREA RESIDENT/PAGEABLE                    *
*                                                                     *
* ENTRY POINT = IFG0195V FROM IFG0195T OR SECLOADA.                   *
*                                                                     *
*    PURPOSE = TO BYPASS REQUESTING A PASSWORD IF IT HAS ALREADY      *
*              BEEN REQUESTED OR IF A MASTER LOGON PASSWORD           *
*              IS AVAILABLE.                                          *
*                                                                     *
*    LINKAGE =                                                        *
*        IECRES LOAD,MODNM=IFG0195V,PREFIX=WTGPREFX,BRANCH=DIRECT     *
*                                                                     *
* INPUT = AS FOLLOWS -                                                *
*                                                                     *
*        R4 = ADDRESSABILITY TO FORCORE-LIKE WORK AREA.               *
*        R6 = ADDRESSABILITY TO WTG TABLE.                            *
*        RD = ADDRESSABILITY TO SECCORE WORK AREA                     *
*                                                                     *
* OUTPUT = FINISH WORK AREA INITIALIZATION.                           *
*                                                                     *
*        PASSWORD RECORD READ IF TTR IN DSAB.                         *
*        PASSWORD IN WORK AREA IF MASTER LOGON PASSWORD PRESENT.      *
*                                                                     *
* EXIT-NORMAL = IFG0195T - GOOD RETURN FROM VSAM.                     *
*               IFG0195U - READ OR UPDATE PASSWORD RECORD.            *
*               READPSWD - GET PASSWORD FROM OPERATOR OR TERMINAL.    *
*               SECLOADA - GOOD RETURN FROM VSAM.                     *
*                                                                     *
* EXIT-ERROR =  IFG0195U TO ISSUE ERROR MESSAGE.                      *
*                                                                     *
* EXTERNAL REFERENCES = SEE BELOW                                     *
*                                                                     *
*    ROUTINES = IDA0192G,IFG0195U,READPSWD.                           *
*                                                                     *
*    DATA AREAS = IECDSECS SECCORE,MAIN,WTG,PREFX,RRPL                *
*                                                                     *
*    CONTROL BLOCK = CVT,ASCB,TCB,JSCB,TSB,DSAB,UCB,DSCB4,ACB,DCB.    *
*                                                                     *
* TABLES = SEE XCTLTABL MACRO.                                        *
*                                                                     *
* MACROS = IECRES,EXCP,WAIT,ENQ,DEQ,XCTLTABL,IECDSECS,IECEQU.         *
*                                                                     *
* CHANGE ACTIVITY = NEW RELEASE (LEVEL 0)                             *
*        VS2 RELEASE 03 DELETIONS/CHANGES                             *
*0000742000                                                     ZA00111
*                                                                     *
***********************************************************************
         EJECT
*
         USING FORCORE,RCORE            DEFINE BASE TO MAIN WORK AREA
         USING WTG,RWTG                 DEFINE BASE TO XCTL/WTG TABLE
         USING SECCORE,RD               DEFINE BASE TO SECURITY WK AREA
*
         BALR  RBASE,R0                 ESTABLISH BASE REGISTER
         USING *,RBASE                  DEFINE BASE REGISTER
         EJECT
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
*        SET UP CONTROL BLOCK POINTERS FOR I/O OPERATIONS        Y02134
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
         L     RC,CVTPTR                GET CVT ADDR             Y02134
         L     RF,CVTZDTAB-CVT(,RC)     GET ADDR I/O DEVICE TBL  Y02134
         L     RET,CVTSYSAD-CVT(,RC)    GET ADDR OF SYSRES UCB   Y02134
         ST    RET,MYDEBUCB             SAVE SYSRES UCBAD        Y02134
         L     R1,CVTSVDCB-CVT(,RC)     GET ADDR OF SVCLIB DCB   Y02134
         L     R1,DCBDEBAD-IHADCB(,R1)  GET ADDR OF SVCLIB DEB   Y02134
         L     R0,MYDEBSYS              SAVE ATTRIBUTES          Y02134
         MVC   MYDEBDEB(K32),K4(R1)     COPY SVCLIB DEB          Y02134
         MVC   MYDEBAPP,CVTXAPG-CVT(RC) DUMMY APP VECTOR TABLE   Y02134
         MVI   MYDEBMOD,K0              SET FILE MASK TO ZERO    YM6841
         MVI   MYDEBUSR,K1              DEFINE DEB AS 1 EXTENT   Y02134
         STCM  R0,B'1000',MYDEBSYS      RESTORE ATTRIBUTES       Y02134
*                                                                Y02134
         XC    MYDEBSCC(K4),MYDEBSCC    SET EXTENT TO COVER DISK Y02134
*                                                                Y02134
         SR    R1,R1                    CLEAR REG FOR IC         Y02134
         IC    R1,UCBTBYT4-UCB(,RET)    GET UCB DEVICE TYPE      Y02134
         IC    R1,K0(R1,RF)             INDEX TO DEVICE OFFSET   Y02134
         AR    RF,R1                    ADDR OF DEVICE ENTRY     Y02134
         MVC   MYDEBECC(K4),K0(RF)      DEVICE TABLE MAX EXTENT  Y02134
*                                                                Y02134
         MVC   MYDEBNTR,TASTRK          SET TRACKS TO MAXIMUM    Y02134
*                                                                Y02134
         LA    R1,MYDCB                 POINT TO DCB             Y02134
         ST    R1,IOBDCBPT-K1           SAVE DCB IN IOB          Y02134
         ST    R1,MYDEBDCB              SAVE DCB IN DEB          Y02134
*                                                                Y02134
         LA    R1,MYECB                 LOAD ECB POINTER         Y02134
         ST    R1,IOBECBPT              SAVE ECB POINTER         Y02134
*                                                                Y02134
         LA    R1,MYDEB                 LOAD DEB POINTER         Y02134
         ST    R1,MYDCBDEB              SAVE DEB ADDRESS         Y02134
*                                                                Y02134
         MVI   MYDEBMOD,K0              FILE MASK TO ALLOW SEEKS Y02134
         MVI   MYDEBID,DEBIDENT         DEB ID                   Y02134
*                                                                Y02134
         LA    RF,MYCCW                 CCW START ADDRESS        Y02134
         ST    RF,IOBSIOCC              TO SECURITY IOB          Y02134
*                                                                Y02134
         L     R1,CVTPTR                LOAD CVT ADDRESS         Y02134
         L     R1,CVTTCBP-CVT(,R1)      LOAD TCB/ASCB PTRS       Y02134
         MVC   MYASCB,K12(R1)           GET CURRENT ASCB         Y02134
         EJECT
*                                                                Y02134
*****************************************************************Y02134
*                                                                X02989
*        IF THE FORMAT 1 DSCB INDICATES A VSAM DATA SPACE,       X02989
*        BRANCH TO THE THE VSAM SECURITY INTERFACE MODULE        X02989
*        TO VERIFY SECURITY FOR THE DATA SPACE.                  X02989
*                                                                X02989
*****************************************************************Y02134
*                                                                Y02134
         CLI   MYDSCB,CHAR1             CHECK FOR DA             Y02134
         BNE   TAS03000                 BRANCH IF NOT F1         Y02134
         TM    DSCFILTY+K1,ACBDORGA     IS DATA SET VSAM         YM1389
         BNO   TAS03000                 BRANCH IF NO             X02989
*                                                                Y02134
         LA    RB,MYDSN                 LOAD POINTER TO DSNAME   Y02134
*                                                                Y02134
         IECRES LOAD,MODNM=IDA0192G,BRCODE=TAS02000,             Y02134X
               PREFIX=WTGPREFX,BRANCH=DIRECT                     Y02134
*                                                                Y02134
TAS02000 EQU   *                        RETURN FROM VSAM         Y02134
*                                                                Y02134
         LTR   RF,RF                    TEST RETURN CODE         XM0856
         BZ    TAS10000                 BR IF SUCCESSFUL         X02989
         LA    R0,K22                   LOAD VSAM ERROR CODE     Y02134
         B     TAS09000                 SECURITY FAILED          X02989
         EJECT
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
*        GET VTOC ADDRESS FROM SYSRES UCB FOR READING FORMAT 4   Y02082
*        DSCB IN ORDER TO DETERMINE THE HIGHEST RECORD IN THE    Y02082
*        VTOC                                                    Y02082
*                                                                Y02134
*        CONVERT TTR TO MBBCCHHR FORM                            Y02134
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
TAS03000 EQU   *                        GO CONVERT TTR           Y02134
*                                                                Y02134
         L     RUCB,MYDEBUCB            GET ADDR OF SYSRES UCB   Y02134
         USING UCBOB,RUCB               POINTS TO SYSRES UCB     Y02134
*                                                                Y02134
         STM   R0,RF,MYPREFIX           SAVE REGS                Y02134
*                                                                Y02134
         XR    R0,R0                    CLEAR REG FOR ICM INST   Y02134
         ICM   R0,B'1110',UCBVTOC       GET VTOC TTRN FROM UCB   Y02134
*                                                                Y02134
         LA    R1,MYDEB                 WORK AREA DEB            Y02134
         LA    R2,MYDAADDR              MBBCCHHR RESULT FIELD    Y02134
         L     RF,CVTPTR                GET CVT ADDR             Y02134
         L     RF,CVTPCNVT-CVT(,RF)     ADDR OF IECPCNVT ROUTINE Y02134
         BALR  RET,RF                   CONVERT TTR TO MBBCCHHR  Y02134
*                                                                Y02134
         L     RD,WTGPREFX              LOAD PREFIX POINTER      Y02134
         LA    RD,IECSTART-IECPREFX(,RD) POINT TO EXT PREFIX     Y02134
         LM    R0,RF,MYPREFIX           RESTORE REGISTERS        Y02134
         EJECT
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
*        CONSTRUCT CHANNEL PROGRAM TO READ FORMAT 4 DSCB         Y02082
*                                                                Y02082
*        SEARCH ID EQUAL                                         Y02082
*        TIC *-8                                                 Y02082
*        SEARCH KEY EQUAL (4 BYTES - SLI BIT ON)                 Y02082
*        NOP (COUNT 1, SLI BIT ON)                               Y02082
*        READ DATA (80 BYTES - SLI BIT ON)                       Y02082
*                                                                Y02082
*****************************************************************Y02134
*                                                                Y02134
         XC    MYCCW,MYCCW              CLEAR CCW AREA           Y02134
         LA    RF,MYDAADDR+K3           SEARCH ADDR              Y02082
         ST    RF,DXCCW1                                         Y02082
         LA    RF,DXCCW1                TIC *-8                  Y02082
         ST    RF,DXCCW2                                         Y02082
         LA    RF,MYDSCB                GET F4 READ AREA ADDRESS Y02134
         MVC   MYDSCB(L'F4KEY),F4KEY    MOVE F4 KEY TO WORK AREA Y02134
         ST    RF,DXCCW3                SEARCH KEY               Y02082
         ST    RF,DXCCW5                READ DATA                Y02082
         OC    DXCCW1(K5*K8),TASCCW01   OR IN CMD, FLGS, LENGTHS Y02082
*                                                                Y02134
         EXCP  MYIOB                    READ FORMAT 4            Y02082
*                                                                Y02134
         WAIT  ECB=MYECB                WAIT FOR COMPLETION      Y02082
*                                                                Y02134
         NI    DCBIFLGS-IHADCB+MYDCB,ALLBITS-DCBIFPIO CLEAR ERR  YM8526
         LA    R0,K21                   PASSWORD I/O ERROR CODE  Y02134
         TM    MYECB,ECBNOERR           I/O ERROR                Y02082
         BZ    TAS09000                 BR IF I/O ERROR          Y02082
         CLI   MYDSCB,CHAR4             FORMAT 4 READ            Y02082
         BNE   TAS09000                 BRANCH IF NOT            Y02082
         EJECT
*                                                                Y02082
*****************************************************************Y02134
*                                                                Y02134
*        CONSTRUCT CHANNEL PROGRAM TO READ F1 DSCB               Y02134
*        OF THE PASSWORD DATA SET.                               Y02134
*                                                                Y02082
*****************************************************************Y02134
*                                                                Y02134
*DXCCW1  SEARCH ID EQUAL                                         Y02082
*DXCCW2  TIC *-8                                                 Y02082
*DXCCW3  SEARCH ID EQ MULTI-TRACK (CCHHR OF LAST RECORD IN VTOC) Y02082
*DXCCW4  TIC DXCCW8                                              Y02082
*DXCCW5  SEARCH KEY EQ (PASSWORD DATA SET NAME)                  Y02082
*DXCCW6  NOP (COUNT = 1, SLI BIT ON)                             Y02082
*DXCCW7  TIC DXCCW10                                             Y02082
*DXCCW8  SEARCH KEY EQUAL (PASSWORD DATA SET NAME)               Y02082
*DXCCW9  TIC DXCCW3                                              Y02082
*DXCCW10 READ DATA (80 BYTES, SLI BIT ON)                        Y02082
*                                                                Y02082
*****************************************************************Y02134
*                                                                Y02134
         XC    MYCCW,MYCCW              CLEAR CCW AREA           Y02134
         LA    RF,MYDAADDR+K3           SEARCH ADDR              Y02082
         ST    RF,DXCCW1                                         Y02082
         LA    RF,DXCCW1                TIC *-8                  Y02082
         ST    RF,DXCCW2                                         Y02082
         LA    RF,DXCCW11               HIGH VTOC ADDR           Y02082
*                                                                Y02134
         MVC   DXCCW11(K4),DS4VTOCE+K6-DSCB4(RCORE) HIGH CCHH    Y02134
         MVC   DXCCW11+K4(K1),DS4DEVDT-DSCB4(RCORE) DSCBS/TRK    Y02134
*                                                                Y02134
         ST    RF,DXCCW3                STORE IN CCW             Y02082
         LA    RF,DXCCW8                TIC ADDR IF SEARCH ID =  Y02082
         ST    RF,DXCCW4                STORE IN CCW4            Y02082
         LA    RF,MYSYSDSN              PASSWORD DSN ADDR        Y02082
*                                                                Y02134
         MVC   MYSYSDSN(L'PASSWORD),PASSWORD NAME + BLANK        Y02134
         MVC   MYSYSDSN+L'PASSWORD(L'JFCBDSNM-L'PASSWORD),MYSYSDSN+L'PAS
               SSWORD-K1                PROPAGATE THE BLANK      Y02134
*                                                                Y02134
         ST    RF,DXCCW5                STORE IN CCW5            Y02082
         ST    RF,DXCCW8                STORE IN CCW8            Y02082
         LA    RF,DXCCW10               TIC ADDR IF SEARCH KEY = Y02082
         ST    RF,DXCCW7                STORE IN CCW7            Y02082
         LA    RF,DXCCW3                TIC TO CCW3              Y02134
         ST    RF,DXCCW9                STORE IN CCW9            Y02134
         LA    RF,MYDSCB                READ DATA ADDR           Y02082
         ST    RF,DXCCW10               STORE IN CCW10           Y02082
         OC    DXCCW1(K10*K8),TASCCW02  OR IN CMDS, FLAGS, LTHS  Y02082
         EJECT
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
*        READ FORMAT 1 OF PASSWORD DATA SET                      Y02134
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
         EXCP  MYIOB                    SEARCH SYSRES VTOC       Y02134
*                                                                Y02134
         WAIT  ECB=MYECB                WAIT FOR READ COMPLETE   Y02134
*                                                                Y02134
         NI    DCBIFLGS-IHADCB+MYDCB,ALLBITS-DCBIFPIO CLEAR ERR  YM8526
         LA    R0,K21                   PASSWORD I/O ERROR CODE  Y02134
         TM    MYECB,ECBNOERR           TEST IF I/O ERROR        Y02134
         BZ    TAS09000                 BR IF I/O ERROR          Y02082
*                                                                Y02134
         LA    R1,DXCCW10+K8            NORMAL ENDING CSW ADDR   Y02082
         CLM   R1,B'0111',IOBCOMAD+K1   DID CHAN PGM END OK      Y02082
         BNE   TAS09000                 BRANCH IF NOT            Y02082
         CLI   MYDSCB,CHAR1             FORMAT 1 READ            Y02082
         BNE   TAS09000                 BRANCH IF NOT            Y02082
*                                                                Y02134
         MVC   MYDEBSCC(K8),DSCLOWLM    PASSWORD EXTENT TO DEB   Y02134
*                                                                Y02134
         CLI   DSCEXTYP,X80+X01         TEST FOR CYLINDER EXTENT Y02134
         BE    TAS04000                 BR IF YES                Y02134
*                                                                Y02134
TRACK    EQU   X'18'                    FILE MASK TRACK ALLOCATION
         MVI   MYDEBMOD,TRACK           INHIBIT HEAD, CYL SEEKS  Y02134
*                                                                Y02134
***********************************************************************
*                                                                     *
*        CHECK FOR TTR IN DSAB                                        *
*                                                                     *
***********************************************************************
*                                                                Y02134
TAS04000 EQU   *                        PREVIOUS PASSWORD REQ    Y02134
*
         L     R1,MYDSAB                GET PTR TO CURRENT DSAB  Y02080
         L     R0,DSABPTTR-DSAB(,R1)    GET PSWD RECORD TTR      Y02080
         LTR   R0,R0                    CHECK FOR TTR EXISTENCE  Y02080
         BZ    TAS05100                 BR IF NO                 Y02080
*                                                                Y02134
         MVC   MYSYSDSN,MYDSN           SAVE DSNAME              Y02134
         EJECT
*
***********************************************************************
*
*        CONVERT TTR OF PASSWORD RECORD TO MBBCCHHR              Y02134
*
*****************************************************************Y02134
*                                                                Y02134
         STM   R0,RF,MYPREFIX           SAVE REGS                Y02134
         LA    R1,MYDEB                 POINT TO SECURITY WORK DEB
         LA    RDCB,MYDAADDR            POINT TO MBBCCHHR RESULT FIELD
         L     RF,CVTPTR                GET CVT ADDR
         L     RF,CVTPCNVT-CVT(,RF)     GET ADDR OF IECPCNVT ROUTINE
         BALR  RET,RF                   LINK TO CONVERT ROUTINE
*                                                                Y02134
         L     RD,WTGPREFX              LOAD PREFIX POINTER      Y02134
         LA    RD,IECSTART-IECPREFX(,RD) POINT TO EXT PREFIX     Y02134
         LM    R0,RF,MYPREFIX           RESTORE REGS             Y02134
*
***********************************************************************
*
*        CONSTRUCT CHANNEL PROGRAM TO READ KEY AND               Y02134
*        DATA OF RECORD POINTED TO BY DSAB TTR.                  Y02134
*
*        SEARCH ID EQ
*        TIC *-8
*        READ KEY+DATA, KEY TO MYDSN + MYREPLY (PASSWORD)        Y02134
*
*****************************************************************Y02134
*                                                                Y02134
         XC    MYCCW,MYCCW              CLEAR CCW AREA           Y02134
         LA    RF,MYDAADDR+K3           SEARCH ADDR              Y02082
         ST    RF,DXCCW1                                         Y02082
         LA    RF,DXCCW1                TIC *-8                  Y02082
         ST    RF,DXCCW2                                         Y02082
         LA    RF,MYDSN                 READ KEY + DATA ADDR     Y02134
         ST    RF,DXCCW3                                         Y02134
         OC    DXCCW1(K3*K8),TASCCW03   OR IN CMND,FLAGS, LENGTH Y02134
         EJECT
*
*****************************************************************Y02134
*                                                                Y02134
*        ESTABLISH ENQ/DEQ LIST IN SECURITY WORK AREA
*
*****************************************************************Y02134
*                                                                Y02134
         TM    WTGPATHS,X01             TEST SCRATCH/RENAME      Y02134
         BO    TAS04500                 BRANCH TO BYPASS ENQ     Y02134
*                                                                Y02134
         CLI   MYCODE1,K1               CHECK FOR EOV            Y02134
         BE    TAS04500                 BRANCH TO BYPASS ENQ     Y02134
*
         L     R1,IECRRPRM-IECEXTPR(,RD) LOAD RECOVERY PARM LIST Y02134
         OI    RRFLAGS2-RRPLIST(R1),RRFENQPW PSWD ENQ IND        Y02134
*                                                                Y02134
         MVC   MYENQSW1(K12),ENQLIST    ENQ LIST TO WORK AREA    Y02134
*
         ENQ   (,MYSYSDSN),MF=(E,MYENQSW1) ENQ ON DSNAME         Y02134
*
*****************************************************************Y02134
*                                                                Y02134
*        READ PASSWORD ENTRY
*
*****************************************************************Y02134
*                                                                Y02134
TAS04500 EQU   *                        READ WITH DSAB TTR       Y02134
*                                                                Y02134
         EXCP  MYIOB                    READ KEY AND DATA OF RCD Y02134
*
         WAIT  ECB=MYECB                WAIT ON READ TO COMPLETE Y02134
*
         XC    MYREPLY,MYREPLY          HIDE PASSWORD BY ZEROING Y02134
*
         NI    DCBIFLGS-IHADCB+MYDCB,ALLBITS-DCBIFPIO CLEAR ERR  YM8526
         TM    MYECB,ECBNOERR           CHECK FOR I/O ERROR      Y02134
         BZ    TAS05000                 BR IF YES                Y02134
*
         CLC   MYDSN,MYSYSDSN           ARE DSNAMES SAME         Y02134
         BNE   TAS05000                 CHECK MASTER PASSWORD    Y02134
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
*        CHECK THAT THE MODE INDICATOR IN PASSWORD RECORD        Y02134
*        CORRESPONDS TO MODE OF USE OF THE DATA SET              Y02134
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
         LA    RET,K4                   LOAD ENTRY OFFSET        Y02134
         TM    MYMODE,K1                IS MODE READ AND WRITE   Y02134
         BO    TAS10100                 BRANCH IF FULL ACCESS    Y02134
*                                                                Y02134
OUTPUT   EQU   X'0E'                    NOT INPUT OR RDBACK      Y02134
         TM    MYDEBSYS,OUTPUT          OPEN FOR INPUT OR RDBACK Y02134
         BZ    TAS10100                 BRANCH CONSISTENT MODE   Y02134
         EJECT
*
***********************************************************************
*
*        DEQ UNSUCCESSFUL PASSWORD RECORD                        Y02134
*
*****************************************************************Y02134
*                                                                Y02134
TAS05000 EQU   *                        DEQ DATA SET             Y02134
*                                                                Y02134
         MVC   MYDSN,MYSYSDSN           RESTORE DSN              Y02134
         L     R1,MYDSAB                GET POINTER TO DSAB      Y02134
         MODESET EXTKEY=SCHED           SET TO SCHED KEY         Y02134
         XC    DSABPTTR-DSAB(L'DSABPTTR,R1),DSABPTTR-DSAB(R1) 0  Y02134
         MODESET EXTKEY=DATAMGT         RESET TO DM KEY          Y02134
*                                                                Y02134
         TM    WTGPATHS,X01             TEST FOR SCRATCH/RENAME  Y02134
         BO    TAS05100                 BRANCH TO BYPASS DEQ     Y02134
*                                                                Y02134
         CLI   MYCODE1,K1               CHECK FOR EOV            Y02134
         BE    TAS05100                 BRANCH TO BYPASS DEQ     Y02134
*
         DEQ   MF=(E,MYENQSW1)          DEQ PREVIOUS ENQ ON DSNAME
*                                                                Y02134
         L     R1,IECRRPRM-IECEXTPR(,RD) LOAD RECOVERY PARM LIST Y02134
         NI    RRFLAGS2-RRPLIST(R1),ALLBITS-RRFENQPW SET ENQ OFF Y02134
*
***********************************************************************
*                                                                     *
*        CHECK MASTER LOGON PASSWORD                             Y02134
*                                                                     *
***********************************************************************
*
TAS05100 EQU   *                        CHECK LOGON PASSWORD     Y02134
*                                                                Y02134
         L     R1,MYASCB                LOAD ASCB POINTER        Y02134
         L     RF,ASCBTSB-ASCB(,R1)     LOAD TSB POINTER         Y02080
         LTR   RF,RF                    CHECK IF TSB INITIALIZED Y02134
         BZ    TAS10200                 BRANCH IF NOT TSO        Y02134
         MODESET EXTKEY=TCAM            SWITCH TO KEY OF TSB     YM6211
         CLI   TSBPSWD-TSB(RF),BLANK    CHECK FOR LOGON PASSWORD Y02080
         BE    TAS10200                 BR IF NO PASSWORD FOUND  Y02080
         CLI   TSBPSWD-TSB(RF),K0       CHECK FOR LOGON PASSWORD Y02080
         BE    TAS10200                 BR IF NO PASSWORD FOUND  Y02080
*
         LM    RET,RF,TSBPSWD-TSB(RF)   LOAD MASTER PASSWORD    ZA00111
         MODESET EXTKEY=DATAMGT         RETURN TO DATAMGT KEY    YM6211
         STM   RET,RF,MYREPLY           PSWD TO SEARCH AREA     ZA00111
         LA    RET,K0                   LOAD ENTRY OFFSET        Y02134
         B     TAS10150                 BR TO READ PSWD REC    @ZA10842
         EJECT
*
***********************************************************************
*
*        AN UNCORRECTABLE ERROR HAS OCCURRED                     Y02134
*
*****************************************************************Y02134
*                                                                Y02134
TAS09000 EQU   *                        I/O ERROR                Y02134
*
         STC   R0,MYERRCOD              SAVE ERROR CODE          Y02134
         LA    RET,K8                   LOAD ENTRY OFFSET        Y02134
         B     TAS10100                 ISSUE ERROR MSG          Y02134
         EJECT
*
*****************************************************************Y02134
*                                                                Y02134
*        VSAM PASSWORD CHECKING COMPLETED NORMALLY               Y02134
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
TAS10000 EQU   *                        RETURN TO CALLER         Y02134
*                                                                Y02134
         XR    RET,RET                  CLEAR RETURN OFFSET      Y02134
         IC    RET,MYRETCOD             GET RETURN OFFSET        Y02134
*                                                                Y02134
         IECRES LOAD,MODNM=MYRETMOD,BRCODE=(14),                 Y02134X
               PREFIX=WTGPREFX,BRANCH=DIRECT                     Y02134
         EJECT
*
*****************************************************************Y02134
*                                                                Y02134
*        GO TO NEXT LOAD                                         Y02134
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
TAS10100 EQU   *                        GO UPDATE DSCB           Y02134
*                                                                Y02134
         CLI   MYCODE1,K1               CHECK FOR EOV            Y02134
         BE    TAS10000                 DON'T REWRITE PASSWORD   Y02134
*                                                                Y02134
TAS10150 EQU   *                                               @ZA10842
*                                                              @ZA10842
         IECRES LOAD,MODNM=IFG0195U,BRCODE=(14),                 Y02134X
               PREFIX=WTGPREFX,BRANCH=DIRECT                     Y02134
         EJECT
*
*****************************************************************Y02134
*                                                                Y02134
*        GO READ A PASSWORD                                      Y02134
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
TAS10200 EQU   *                        GET PASSWORD             Y02134
*                                                                Y02134
         MODESET EXTKEY=DATAMGT         RETURN TO DATAMGT KEY    YM6211
         IECRES LOAD,MODNM=READPSWD,                             Y02134X
               PREFIX=WTGPREFX,BRANCH=DIRECT                     Y02134
         EJECT
*
***********************************************************************
*
*        CONSTANTS
*
*****************************************************************Y02134
*                                                                Y02134
*        CCWS TO READ FORMAT 4 OF SYSRES                         Y02134
*                                                                Y02134
TASCCW01 CCW   CCWSCHID,K0,CCWCMDCH,K5  SEARCH ID EQ             Y02134
         CCW   CCWTIC,K0,K0,K0          TIC *-8                  Y02134
         CCW   X80+CCWSCHKE,K0,CCWSILI+CCWCMDCH,L'F4KEY S KEY EQ Y02134
         CCW   CCWNOP,K0,CCWSILI,K1     NOP                      Y02082
         CCW   CCWRDDA,K0,CCWSILI,K80   READ DATA-SILI 80 BYTES  Y02134
*                                                                Y02134
*        CCWS TO READ FORMAT 1 OF PASSWORD DATA SET              Y02134
*                                                                Y02134
TASCCW02 CCW   CCWSCHID,K0,CCWCMDCH,K5  SEARCH ID EQ             Y02134
         CCW   CCWTIC,K0,K0,K0          TIC *-8                  Y02134
         CCW   CCWMTSID,K0,CCWCMDCH+CCWSILI,K5                   Y02082
         CCW   CCWTIC,K0,K0,K0                                   Y02082
         CCW   CCWSCHKE,K0,CCWCMDCH+CCWSILI,L'JFCBDSNM           Y02134
         CCW   CCWNOP,K0,CCWSILI,K1                              Y02082
         CCW   CCWTIC,K0,K0,K0                                   Y02082
         CCW   CCWSCHKE,K0,CCWCMDCH+CCWSILI,L'JFCBDSNM           Y02134
         CCW   CCWTIC,K0,K0,K0                                   Y02082
         CCW   CCWRDDA,K0,CCWSILI,K80                            Y02082
*
*        CCWS TO READ PASSWORD RECORD FROM TTR IN DSAB           Y02134
*                                                                Y02134
TASCCW03 CCW   CCWSCHID,K0,CCWCMDCH,K5  SEARCH ID EQ             Y02134
         CCW   CCWTIC,K0,K0,K0          TIC *-8                  Y02134
         CCW   CCWRDKEY,K0,CCWSILI,L'MYDSN+L'MYREPLY+L'MYLOGFLD  Y02134
*                                                                Y02134
TASTRK   DC    X'7FFF'                  MAX TRACKS IN EXTENT     Y02134
F4KEY    DC    XL4'04040404'            FORMAT 4 KEY             Y02082
PASSWORD DC    C'PASSWORD '             DSNAME OF PASSWORD DS    Y02134
*                                                                Y02134
ENQLIST  ENQ   (SYSZPSWD,,E,L'JFCBDSNM,SYSTEM),MF=L              Y02134
SYSZPSWD DC    C'SYSZPSWD'              MAJOR ENQ/DEQ NAME       Y02134
*
         XCTLTABL ID=(,IDA0192G,,IFG0195U,,READPSWD),            Y02134X
               BRT=YES,LENGTH=                                   Y02134
*
         IECDSECS CVT,ASCB,TCB,JSCB,TSB,DSAB,UCB,DSCB4,          Y02080X
               ACB,DCB,DEB,MAIN,SECCORE,PREFX,WTG,RRPL,          Y02080X
               EXPAND=YES                                        Y02134
         IECEQU ,                       DEFINE EQUATES
*
         END
