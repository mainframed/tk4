         TITLE 'IFG0551P  EOV TAPE OUTPUT TRAILER LABEL 1 PREPARATION'
         COPY  LCGASMSW
IFG0551P CSECT
***********************************************************************
*                                                                     *
*                                                                     *
*          VS2 RELEASE 02 DELETIONS                                   *
*                                                                     *
*                                                                     *
***********************************************************************
*                                                                     *
* MODULE NAME = IFG0551P (OS/VS2)                                     *
*                                                                     *
* DESCRIPTIVE NAME = TAPE OUTPUT TRAILER LABEL 1 PREPARATION          *
*                                                                     *
* COPYRIGHT = NONE                                                    *
*                                                                     *
* STATUS = RELEASE 2, LEVEL 0                                         *
*                                                                     *
* FUNCTION =                                                          *
*        1) WRITE TAPE MARK BETWEEN DATA AND TRAILER 1.               *
*        2) CONSTRUCT TRAILER LABEL 1 FROM INFORMATION IN             *
*        THE JFCB, DCB, UCB, AND DEB.                                 *
*        3) IF ASCII WAS SPECIFIED, TRANSLATE THE LABEL               *
*        FROM EBCDIC TO ASCII.                                        *
*        4) ISSUE EXCP TO WRITE TRAILER LABEL 1.                      *
*        5) PASS CONTROL TO IFG0551R (551R WILL WAIT ON               *
*        COMPLETION OF THE EXCP)                                      *
*        6) IF AN I/O ERROR OCCURRED WHILE WRITING THE TAPE           *
*        MARK, PROBLEM DETERMINATION IS GIVEN CONTROL (IFG0550P)      *
*        WITH AN INTERNAL CODE OF 141.                                *
*                                                                     *
* NOTES = SEE BELOW                                                   *
*                                                                     *
*    DEPENDENCIES =                                                   *
*            CLASS ONE CHARACTER CODE.  THE EBCDIC CHARACTER CODE     *
*            WAS USED FOR ASSEMBLY.  THE MODULE MUST BE REASSEMBLED   *
*            IF A DIFFERENT CHARACTER SET IS USED FOR EXECUTION.      *
*                                                                     *
*    RESTRICTIONS = NONE                                              *
*                                                                     *
*    REGISTER CONVENTIONS =                                           *
*            R2 POINTS TO DCB                                         *
*            R4 POINTS TO OPEN WORK AREA                              *
*            R5 POINTS TO THE RESIDENT ROUTINE                        *
*            R6 POINTS TO THE WTG TABLE                               *
*            R7 POINTS TO THE CURRENT PARAMETER LIST ENTRY            *
*            R8 POINTS TO THE CURRENT WTG TABLE ENTRY                 *
*            R9 POINTS TO THE DD ENTRY IN THE TIOT                    *
*            R10 POINTS TO THE UCB                                    *
*                                                                     *
*    PATCH LABEL = SEE THIRD FROM LAST LABEL BEFORE ORG STATEMENT AT  *
*                  END OF LISTING.                                    *
*                                                                     *
* MODULE TYPE = CONTROL (OPEN, CLOSE, EOV DATA MANAGEMENT)            *
*                                                                     *
*    PROCESSOR = ASSEMBLER XF                                         *
*                                                                     *
*    MODULE SIZE = SEE EXTERNAL SYMBOL DICTIONARY OR LOC FIELD ON     *
*                  ORG STATEMENT AT END OF LISTING                    *
*                                                                     *
*    ATTRIBUTES = REENTRANT, REFRESHABLE,READ-ONLY, ENABLED,          *
*                 PRIVILEGED, SUPERVISOR STATE, DATA MANAGEMENT KEY,  *
*                 LINK PACK AREA RESIDENT/PAGEABLE                    *
*                                                                     *
* ENTRY POINT = IFG0551P                                              *
*                                                                     *
*    PURPOSE = SEE FUNCTION                                           *
*                                                                     *
*    LINKAGE =                                                        *
*        THIS MODULE IS TRANSFERRED CONTROL THROUGH THE IECRES-LOAD   *
*        MACRO INSTRUCTION.                                           *
*                                                                     *
* INPUT =                                                             *
*        GIVEN CONTROL IN PROTECT KEY 5.                              *
*        REGISTER 2 POINTS TO THE COPIED DCB.                         *
*        DEBDCBAD POINTS TO THE COPIED DCB.                           *
*        REGISTER 4 POINTS TO THE EOV WORKAREA                        *
*                                                                     *
* OUTPUT =                                                            *
*        THE NEXT MODULE IS GIVEN CONTROL IN PROTECT KEY 5 WITH       *
*        REGISTER 2 POINTING TO THE COPIED DCB,                       *
*        DEBDCBAD POINTING TO THE COPIED DCB,                         *
*        AND REGISTER 4 POINTING TO THE EOV WORKAREA,                 *
*                                                                     *
* EXIT-NORMAL =                                                       *
*        IFG0551R - BRANCH CODE = 0                                   *
*                                                                     *
* EXIT-ERROR =                                                        *
*        IFG0550P - I/O ERROR WRITING TAPE MARK,INT CODE=141          *
*                                                                     *
* EXTERNAL REFERENCES = SEE BELOW                                     *
*                                                                     *
*    ROUTINES =                                                       *
*        IFG019RA THROUGH THE IECRES MACRO.                           *
*                                                                     *
*    DATA AREAS =                                                     *
*        EOV WORKAREA.                                                *
*                                                                     *
*    CONTROL BLOCK =                                                  *
*        CVT                                                          *
*        DEB                                                          *
*        JFCB                                                         *
*        UCB                                                          *
*        TCB                                                          *
*                                                                     *
* TABLES =                                                            *
*                                                                     *
* MACROS =                                                            *
*        MODESET                                                      *
*        DMABCOND                                                     *
*        IECRES LOAD                                                  *
*        EXCP                                                         *
*        WAIT                                                         *
*        XLATE                                                        *
*                                                                     *
* CHANGE ACTIVITY =                                                   *
*        SEE CHANGES/DELETIONS SECTION JUST AFTER CSECT CARD.         *
*                                                                     *
***********************************************************************
         EJECT
         SPACE 1
         SPACE 1
*                   ADDRESSABILITY
         SPACE 1
         BALR  RBASE,0                  ESTABLISH BASE
         USING *,RBASE
         USING FORCORE,RCORE
         USING WTG,RWTG                 BASE FOR WTG TABLE       Y02080
         USING IHADCB,RDCB
         USING DEB,RDEB
         USING SRT,RUCB
         SPACE 1
*                   WRITE TAPE MARK PRECEDING TRAILER LABELS
         SPACE 1
         MVC   DXCCW1(K16),AWTMCCW      SET UP CCW TO WRITE TAPE MARK
*                                       AND NOP COMMAND CHAIN
         BAL   R7,ETO01600              WRITE TM BETWEEN DATA AND LABEL
         BNO   ETO01800                 IF I/O ERROR,GO NOTIFY THE OPER
*                                       IF I/O ERROR, GO ABEND
         OI    DXATOUTA,DXATDATM        TM WRITTEN AFTER DATA    Y02144
         MVI   DXCCW1+K4,CCWSILI        RESET CHAIN COMMAND FLAG IN CCW
*
*                        PREPARE TRAILER LABEL 1
*                   CONSTRUCT END OF VOLUME LABEL 1
         SPACE 1
         MVC   FL1LABI,AEOV0E           SET FILE LABEL ID TO EOV
         MVI   FL1NO,CHAR1              SET FILE LABEL NUMBER TO 1
         SPACE 1
*                   DETERMINE 17 LEAST SIGNIFICANT NON-BLANK
*                   CHARACTERS IN DATA SET NAME IN JFCB
         SPACE 1
         LA    R7,JFCBDSNM+K27          STARTING ADDR OF DSN TO WRK REG
ETO00200 CLI   K16(R7),BLANK            IS THE CHARACTER BLANK
         BNE   ETO00300                 NO,GO MOVE DSN TO LABEL
         BCT   R7,ETO00200              BRANCH IF NOT BLANK
ETO00300 LA    R1,JFCBDSNM              POINT TO START OF DSN
         CLR   R1,R7                    IS DSN LONGER THAN 17 CHAR
         BL    ETO00400                 BR, YES
         LR    R7,R1                    SET TO START OF DSN
ETO00400 MVC   FL1ID,K0(R7)             DSN TO LABEL
         ST    R7,DXCCW4                SAVE FOR HDR ROUTINE
         SPACE 1
*                   GENERATION NUMBER AND VERSION NUMBER OF GENERATION
         SPACE 1
ETO00500 MVI   FL1GNO,BLANK             SET GENERATION NUMBER AND VER
         MVC   FL1GNO+K1(K44),FL1GNO    NUMBER OF GENERATION TO BLANKS
         CLI   JFCBELNM+K1,BLANK        GENERATION DATA SET
         BE    ETO00800                 NO,GO SET UP FILE SERIAL NUMBER
         LA    R7,JFCBDSNM+K43          POINT TO END OF DSN
ETO00600 CLI   K0(R7),BLANK             DOES DSN START HERE
         BNE   ETO00700                 YES,GO FIND GEN NO.
         BCT   R7,ETO00600              BR TO FIND START OF DSN
ETO00700 LA    RD,K9                    SUBTRACT 9 FROM REGISTER
         SLR   R7,RD                    CONTAINING LAST CHAR IN DSNAM
         MVC   FL1GNO,K3(R7)            R7 NOW POINTS TO END OF DSN
         MVC   FL1VNG,K8(R7)            LESS GEN NO. ( GNNNNVNN )
         ST    R7,DXCCW4+K4             SAVE FOR HDR LBL PROCESSING
*                   FILE SERIAL,VOLUME SEQUENCE AND FILE SEQUENCE
         SPACE 1
ETO00800 EQU   *                        SET UP FILE SER
         MVC   FL1FILSR,JFCBVOLS        FILE SER IS 1ST VOL SER OF DS
         LH    R7,JFCBVLSQ              PICK UP VOLUME SEQ NUM OF VOL 1
         LTR   R7,R7                    IS IT ZERO
         BZ    ETO00900                 YES,VOL SEQ NUM IN DEB CORRECT
         BCTR  R7,R0                    NO, -1 AND ADD VOLUME SEQ NUM
*                                      IN DEB SINCE VOL SEQ NUM IN
ETO00900 AH    R7,DEBTVLSQ              DEB = 1 FOR VOL 1 OF DATA SET
         CVD   R7,DXCCW2                CONVERT FROM BI TO PACKED DEC
         UNPK  FL1VOLSQ,DXCCW2          VOLUME SEQUENCE NUM IN EBCDIC
         OI    FL1VOLSQ+K3,ZONEOF       REMOVE SIGN BITS
         LH    R7,UCBFSEQ               PICK UP FILE SEQUENCE NUMBER
         CVD   R7,DXCCW2                CONVERT FROM BI TO PACKED DEC
         UNPK  FL1FILSQ,DXCCW2          FILE SEQ NUM IN EBCDIC TO LBL
         OI    FL1FILSQ+K3,ZONEOF       REMOVE SIGN BITS
         SPACE 1
*                   CREATION DATE
         SPACE 1
         SR    R7,R7                    CLEAR WORK REGISTER
         IC    R7,JFCBCRDT              YEAR OF CREATION IS 1 BYTE
         CVD   R7,DXCCW2                CONVERT YEAR TO DECIMAL
         MVI   FL1CREDT,BLANK           LABEL TO BE OF FORM  BYYDDD
         UNPK  FL1CREDT+K1(K2),DXCCW2   PLACE YEAR IN EBCDIC INTO LABEL
         OI    FL1CREDT+K2,ZONEOF       REMOVE SIGN BITS
         IC    R7,JFCBCRDT+K1           PICK UP THE TWO CHARACTERS OF
         SLA   R7,K8                    THE DAY OF CREATION
         IC    R7,JFCBCRDT+K2           FROM THE JFCB
         CVD   R7,DXCCW2                CONVERT DAY TO DECIMAL
         UNPK  FL1CREDT+K3(K3),DXCCW2   CREATION DAY IN EBCDIC TO LBL
         OI    FL1CREDT+K5,ZONEOF       REMOVE SIGN BITS
         SPACE 1
*                   EXPIRATION DATE
         SPACE 1
         SR    R7,R7                    CLEAR WORK REGISTER
         IC    R7,JFCBXPDT              PICK UP YR OF EXPIRATION IN BI
         CVD   R7,DXCCW2                CONVERT YEAR TO DECIMAL
         MVI   FL1EXPDT,BLANK           LABEL FIELD TO BE OF FORM  BYYD
         UNPK  FL1EXPDT+K1(K2),DXCCW2   PLACE YEAR IN EBCDIC INTO LABEL
         OI    FL1EXPDT+K2,ZONEOF       REMOVE SIGN BITS
         LH    R7,JFCBXPDT+K1           PICK UP DAY OF EXPIRATION
         CVD   R7,DXCCW2                CONVERT DAY TO DECIMAL
         UNPK  FL1EXPDT+K3(K3),DXCCW2   DAY IN EBCDIC TO TRAILER LABEL
         OI    FL1EXPDT+K5,ZONEOF       REMOVE SIGN BITS
         SPACE 1
*                   BLOCK COUNT
         SPACE 1
         L     R7,DCBBLKCT              PICK UP BLK COUNT FROM THE DCD
         CVD   R7,DXCCW2                CONVERT BLOCK COUNT TO DECIMAL
         UNPK  FL1BLKCT,DXCCW2          BLOCK COUNT IN EBCDIC TO LABEL
         OI    FL1BLKCT+K5,ZONEOF       REMOVE SIGN BITS
         TM    DCBMACRF,DCBMEXCP        IS THE PROBLEM PGM USING EXCP
         BNO   ETO01000                 NO,BLOCK COUNT IN DCB IS VALID
         TM    DCBMACRF+K1,DEPEND       TAPE DEPENDENT PORTION EXIST
         BC    5,ETO01000               YES,ASSUME BLK CNT IN DCB VALID
         MVI   FL1BLKCT,K0              NO,SET BLOCK COUNT IN LABEL TO
         MVC   FL1BLKCT+K1(K5),FL1BLKCT  SINCE FLD NOT PRESENT IN DCB
         SPACE 1
*                   DATA SET SECURITY
         SPACE 1
ETO01000 MVI   FL1FSEC,ZONEOF           RESET DATA SET SECURITY IND
         TM    JFCBIND2,JFCBSCTY        DOES THE JFCB SPECIFY SECURITY
         BNO   ETO01100                 NO,DO NOT SET INDICATOR
         MVI   FL1FSEC,CHAR1            YES,SET DATA SET SECURITY IND
         TM    JFCBIND2,JFCBRWPW        DOES JFCB SPEC RWPW OPTION
         BNO   ETO01100                 NO, DO NOT SET INDICATOR
         MVI   FL1FSEC,FL1WRSEC         IND SECTY IS FOR WR ONLY
         SPACE 1
*                   SYSTEM CODE AND RESERVED FIELDS
         SPACE 1
ETO01100 EQU   *                        SYSTEM CODE
         MVI   FL1RES,BLANK             PAD THE REST OF THE LABEL
         MVC   FL1RES1,FL1RES           WITH BLANKS - 7 CHARACTERS
         SPACE 1
*                   SET UP CHANNEL PROGRAM TO WRITE LABEL
         SPACE 1
*                                 PLACE ADDRESS OF LABEL AREA IN
         ST    RCORE,DXCCW1             OBTAINED CORE INTO CCW
         MVI   DXCCW1,CCWWRTAP          DITTO FOR WRITE OPERATION CODE
         MVC   DXCCW1+K4(K4),AWRCCW     SILI FLAG, 80 CHARACTERS
*
*   IF CREATING A USASI LABEL,MAKE THE REQUIRED CHANGES IN THE
*   LABEL FIELDS AND TRANSLATE THE LABEL TO USASI BEFORE WRITING.
*
         TM    JFCBLTYP,JFCBAL          IS THIS A USASCI LABEL
         BZ    ETO01300                 NO, GO WRITE A LABEL
         TM    JFCBIND2,JFCBSCTY        JFCB SPECIFY SECURITY
         BO    ETO01200                 YES, SET SYSTEM CODE
         MVI   FL1FSEC,BLANK            NO, SET SECURITY BLANK
ETO01200 MVC   FL1SYSCD(K5),SYSCODE     SET USAUI SYSTEM CODE
         XLATE DXLBL,K80,TO=A           TRANSLATE LABEL TO ASCII
         B     ETO01400                 BRANCH TO WRITE LABEL
         SPACE 1
*                   WRITE END OF VOLUME FILE LABEL 1
ETO01300 EQU   *                        WRITE EOV1
         MVC   FL1SYSCD,VSSYSCD         INSERT SYSTEM CODE       XM1201
*                                       INTO TRAILER             XM1201
ETO01400 EQU   *                        WRITE LABEL
         SPACE 1
         EXCP  DXIOB                    WRITE EOV1 NEXT MOD WILL CHK IT
         SPACE 1
ETO02300 EQU   *                        EXIT ROUTINE
*                   EXIT
         LA    RWTGC,DXXENTRY           SET R8 FOR RES TRN
         LA    RWTG,DXXAREA             POINT TO EOV WTG
         SR    RET,RET                  ZERO RETURN REG
         IECRES LOAD,MODID=MOD1R1P,BRANCH=QUEUED                 Y02080
*                                       GO TO NEXT MODULE        Y02080
*                   CLOSED SUBROUTINE TO ISSUE I/O OPERATIONS
         SPACE 1
*                        IT IS ASSUMED THAT ALL CONTROL BLOCKS
*                        HAVE ALREADY BEEN SET UP
         SPACE 1
*                             BAL  R7,PRTR50
*                             RETURN
         SPACE 1
ETO01600 EQU   *                        I/O SUBROUTINE
         EXCP  DXIOB                    INITIATE I/O OPERATION
         WAIT  ,ECB=DXECB               WAIT FOR COMPLETION OF I/O OPER
         TM    IOBSTAT0,CSWUNITX        IS THERE A UNIT EXCEPTION ERROR
         BNO   ETO01700                 NO,GO CHECK IF ANY I/O ERROR
         TM    IOBSTAT0,IOERR           TEST FOR UNIT CHK ALSO  SA58747
         BO    ETO01700                 YES, CHK ANY I/O ERROR  SA58747
         OI    DXECB,ECBCOD7F           IGNORE ERROR
ETO01700 EQU   *                        UNIT CHECK
         TM    DXECB,ECBNOERR           IS THERE AN UNUSUAL I/O COND
         BR    R7                       RETURN TO CALLER
* ***
* ***          NOTIFY OPERATOR THAT JOB IS BEING TERMINATED
* ***          DUE TO I/O ERROR
* ***
* ***
ETO01800 EQU   *                        ABEND
         LA    RWTGC,DXXENTRY           SET R8 FOR RES RTN
         LA    RWTG,DXXAREA             POINT TO EOV WTG
         DMABCOND 141,MODPD1P           GO TO PROB DETER. 137 ABEND
         SPACE 1
*                   CONSTANTS
         SPACE 1
AWRCCW   DC    X'20000050'              SILI FLAG FOR 80 CHARACTERS
SEN60E   DC    X'20000006'              SENSE 6 BYTES - SILI
ATRTCH   DC    C' CET'                  CONVERSION,EVEN PARITY,TRAN
         DS    0H
AGRTR32K DC    X'8000'                  VALUE IN LABEL WHEN LRECLGT 32K
AWTMCCW  CCW   X'1F',0,X'60',80         WRITE TAPE MARK COUNT FOR WRITE
         CCW   X'03',0,X'20',1          NOP CCW FOR COMMAND CHAIN TO WT
AEOV0E   DC    C'EOV'                   FILE LABEL ID FOR EOV
ARECFM   EQU   *-1                      RECFM TRANSLATE TABLE
         DC    C'VFU'                   VARIABLE,FIXED,UNKNOWN REC FMT
ABLKA    DC    C' SBR'                  BLK AATTRB TRANSLATE TABLE
SYSCODE  DC    C'OS360'                 USASCI SYSTEM CODE
VSSYSCD  DC    C'IBM OS/VS 370'         VS SYSTEM CODE           XM1201
XCTLTA1P XCTLTABL SVC=055,ID=(MOD1R1P,1R,MODPD1P,0P),BRT=YES,    Y02080X
               LENGTH=                                           Y02080
         IECDSECS CVT,TCB,DCB,UCB,MAIN,DEB,TIOT,WTG,PREFX,       Y02080*
               EXPAND=YES                                        Y02080
         IECEQU
         END
