         TITLE 'IFG0202H - CLOSE/EOV SMF ROUTINE'
IFG0202H CSECT
         ENTRY IFG0553B                                          Y02080
IFG0553B EQU   IFG0202H                 ALLIAS ENTRY POINT       Y02080
***********************************************************************
*                                                                     *
*          VS2 RELEASE 03 DELETIONS/CHANGES                           *
*0000636000,638000,648000,650000                                Z30AAJC
*0000                                                          @ZA05458
*0000                                                          @ZA07184
*0000362000                                                    @ZA14716
*          VS1 RELEASE 01 DELETIONS
*0000                                                            XM5543
*          VS1 RELEASE 02 DELETIONS                                   *
*0000699500                                                      XM0454
*                                                                     *
*          RELEASE 21 DELETIONS/CHANGES                               *
*                                                                M1771
*0000372000-374000                                               M0143
*                                                                A39153
*                                                                A41413
*                                                                A43244
*                                                                A38013
*                                                                A44480
*0000384000,386000,390000,392000                                 A52390
*                                                                     *
* MODULE NAME = IFG0202H                                              *
*                                                                     *
* DESCRIPTIVE NAME = CLOSE/EOV SMF ROUTINE                            *
*                                                                     *
* COPYRIGHT = NONE                                                    *
*                                                                     *
* STATUS CHANGE LEVEL 000                                             *
*                                                                     *
* FUNCTION -                                                          *
*    THIS MODULE CONTAINS THE FUNCTION(S) OR PART(S) OF FUNCTION(S)-- *
*    CLOSE/EOV SMF RECORD BUILDER FUNCTION.                           *
*    REFER TO THE FOLLOWING 'FUNCTION PROLOG(S)' FOR DETAILS.         *
*                                                                     *
* ENTRY POINTS -                                                      *
*         IFG0202H - ENTRY POINT VIA AN XCTL FROM THE FOLLOWING--     *
*             INITIAL ENTRY--                                         *
*                IFG0202B - PROCESS CLOSE/EOV SMF RECORD BUILDER      *
*                IFG0202E   FUNCTION.                                 *
*                IFG0202F                                             *
*             SECOND ENTRY--                                          *
*                IFG0551T - PROCESS CLOSE/EOV SMF RECORD BUILDER      *
*                IFG0552R   FUNCTION.                                 *
*                IFG0552T                                             *
*                IFG0552X                                             *
*                IFG0553P                                             *
*                IFG0554P                                             *
*             EITHER ENTRY--                                          *
*                IFG0RR0B - PROCESS CLOSE/EOV SMF RECORD BUILDER      *
*                                                                     *
* INPUT -                                                             *
*    REFER TO THE FOLLOWING 'FUNCTION PROLOG(S)'.                     *
*                                                                     *
* OUTPUT -                                                            *
*    REFER TO THE FOLLOWING 'FUNCTION PROLOG(S)'.                     *
*                                                                     *
* EXTERNAL REFERENCES -                                               *
*         IFG019RA - OPEN/CLOSE/EOV RESIDENT ROUTINE FOR XCTL AND     *
*                    WAIT.                                            *
*    REFER TO THE FOLLOWING 'FUNCTION PROLOG(S)'.                     *
*                                                                     *
* EXITS, NORMAL -                                                     *
*    EXIT VIA THE RESIDENT ROUTINE XCTL TO THE FOLLOWING--            *
*         IFG0202I - CONTINUE PROCESSING CLOSE/EOV SMF RECORD BUILDER *
*                    FUNCTION. (DIRECT ACCESS)                        *
*         IFG0202J - PROCESS CLOSE FINAL STRING.                      *
*         IFG0202B - PROCESS CLOSE TAPE FUNCTION.                     *
*         IFG0202F                                                    *
*         IFG0551T - PROCESS EOV TAPE OUTPUT FUNCTION.                *
*         IFG0552R - PROCESS EOV TAPE INPUT FUNCTION.                 *
*         IFG0552T                                                    *
*         IFG0552X                                                    *
*         IFG0553P - PROCESS EOV DIRECT ACCESS INPUT FUNCTION.        *
*         IFG0554P - PROCESS EOV DIRECT ACCESS OUTPUT FUNCTION.       *
*         IFG0RROB - FORCE CLOSE PROCESSING                           *
*    REFER TO THE FOLLOWING 'FUNCTION PROLOG(S)'.                     *
*                                                                     *
* EXITS, ERROR -                                                      *
*    EXIT IS TO THE PROBLEM DETERMINATION MODULE IFG0200P, IF AN      *
*    ABEND SITUATION OCCURS IN THIS MODULE.  REFER TO THE FOLLOWING   *
*    'FUNCTION PROLOG(S)'.                                            *
*                                                                     *
* TABLES/WORK AREAS -                                                 *
*    REFER TO THE FOLLOWING 'FUNCTION PROLOG(S)'.                     *
*                                                                     *
* ATTRIBUTES -                                                        *
*    REENTRANT, REFRESHABLE, READ-ONLY, ENABLED, PRIVILEGED           *
*                                                                     *
* CHARACTER CODE DEPENDENCY -                                         *
*    CLASS ONE CHARACTER CODE DEPENDENCY - THE EBCDIC CHARACTER CODE  *
*    WAS USED FOR ASSEMBLY.  THE MODULE MUST BE REASSEMBLED IF A      *
*    DIFFERENT CHARACTER SET IS USED FOR EXECUTION.                   *
*                                                                     *
* NOTES -                                                             *
*    THIS MODULE HAS AN ALIAS NAME OF IFG0553B.                       *
*    REFER TO THE FOLLOWING 'FUNCTION PROLOG(S)'.                     *
*                                                                     *
***********************************************************************
         EJECT
         USING IHADCB,RDCB
         USING FORCORE,RCORE
         USING CVT,RD
         USING TCB,RET
         USING UCBOB,RUCB
         USING TIOENTRY,RTIOT
         USING DEB,RDEB
         USING SMF14RCD,R7
         USING WTG,RWTG
         BALR  RBASE,0                  ESTABLISH BASE REGISTER
         USING *,RBASE
         L     RF,DXATCOM3              GET RRPLIST PTR          Y02144
         USING RRPLIST,RF                                        Y02144
         OI    RRFLAGS1,RRFSMF          INDICATE SMF IN CONTROL  Y02144
         DROP  RF                                                Y02144
         OI    DXATDACC,DXATSMF         INDICATE SMF PROCESSING  YM4614*
                                        STARTED FOR THE DATA SET YM4614
         B     CCM01700(RET)            USE BRANCH TABLE
CCM01700 EQU   *
         B     CCM01900                 ENTRY FROM CLOSE
*        ENTRY FROM EOV ( OFFSET 4 )
         EJECT
*                                                                     *
*                          FUNCTION PROLOG                            *
*                                                                     *
***********************************************************************
*                                                                     *
* FUNCTION NAME -                                                     *
*    CLOSE/EOV SMF RECORD BUILDER FUNCTION.                           *
*                                                                     *
* (STATUS) -                                                          *
*    NOT APPLICABLE                                                   *
*                                                                     *
* FUNCTION -                                                          *
*    THIS FUNCTION IS CALLED BY CLOSE/EOV WHEN THE SYSTEM MANAGEMENT  *
*    FACILITIES DATA SET INFORMATION IS DESIRED. THIS FUNCTION BUILDS *
*    THE TYPE 14/15 SMF RECORD AND ISSUE AN SVC 83 TO HAVE THE RECORD *
*    WRITTEN ON THE SYS1.MAN DATA SET.                                *
*    IF ENTRY WAS FROM CLOSE, THIS FUNCTION CALCULATES THE CORE NEEDED*
*    FOR UCB SEGMENTS FOR ALL VOLUMES TO BUILD THE SMF RECORD.        *
*    A GETMAIN IS ISSUED FOR THE CORE REQUIRED TO BUILD THE RECORD.   *
*                                                                     *
* ENTRY POINTS -                                                      *
*    ENTERED FROM THE FOLLOWING--                                     *
*    CLOSE SMF INTERFACE FUNCTIONS.                                   *
*    EOV SMF INTERFACE FUNCTIONS.                                     *
*                                                                     *
* INPUT -                                                             *
*    ENTERED IN DATA MANAGEMENT KEY                              Y02082
*    A POINTER TO EACH OF THE FOLLOWING--                             *
*       CURRENT PARAMETER LIST ENTRY- RPARC                           *
*       DD ENTRY IN THE TIOT- RTIOT                                   *
*       WTG TABLE- RWTG                                               *
*       CURRENT WTG TABLE ENTRY- RWTGC                                *
*       DCB- RDCB                                                     *
*       WORK AREA- RCORE                                              *
*       RESIDENT ROUTINE- RES                                         *
*       UCB- RUCB                                                     *
*       DEB- RDEB                                                     *
*                                                                     *
* OUTPUT -                                                            *
*    EXIT IN DATA MANAGEMENT KEY                                 Y02082
*    AN SMF RECORD IS WRITTEN FOR TAPE OR D. A. DATA SETS.            *
*                                                                     *
* EXTERNAL REFERENCES -                                               *
*    REFER TO THE PRECEEDING MODULE PROLOG.                           *
*                                                                     *
* EXITS, NORMAL -                                                     *
*    REFER TO THE PRECEEDING MODULE PROLOG.                           *
*    CLOSE/EOV FUNCTIONS THAT CALLED THIS FUNCTION.                   *
*                                                                     *
* EXITS, ERROR -                                                      *
*    REFER TO THE PRECEEDING MODULE PROLOG.                           *
*                                                                     *
* TABLES/WORK AREAS -                                                 *
*    THE OPEN, CLOSE, OR EOV WORK AREA AND THE WHERE-TO-GO (WTG)      *
*    TABLE ARE DESCRIBED BY THE DSECTS AT THE END OF THE LISTING.     *
*                                                                     *
* ATTRIBUTES -                                                        *
*    REFER TO THE PRECEEDING MODULE PROLOG.                           *
*                                                                     *
* CHARACTER CODE DEPENDENCY -                                         *
*    REFER TO THE PRECEEDING MODULE PROLOG.                           *
*                                                                     *
* NOTES -                                                             *
*    NOT APPLICABLE                                                   *
*                                                                     *
***********************************************************************
         EJECT
         LA    R0,K320                  LOAD MIN GETMAIN SIZE
         B     CCM02000                 BRANCH TO GET CORE
CCM01900 EQU   *
*
*   THIS SECTION OF CODE IS EXECUTED WHEN ENTRY IS FROM CLOSE.
*   IT WILL DETERMINE IF ADDITIONAL CORE IS REQUIRED TO BUILD THE SMF
*   RECORD. THIS CODE IS ENTERED FOR EACH DCB IN THE CLOSE WTG TABLE.
*
         LA    R0,K320                  LOAD MIN GETMAIN SIZE
         TM    DCBMACRF,DCBMEXCP        IS IT EXCP               A39153
         BZ    CCM01920                 BRANCH IF NOT            A39153
         TM    DCBMACRF,DCBMCOM         COMMON INTERFACE PRESENT A39153
         BZ    CCM01925                 BRANCH IF NOT          @ZA14716
CCM01920 EQU   *                                                 A39153
         TM    DCBDSRG2,DCBDSGTQ        TEST FOR TCAM MSG QUEUE SA50816
         BO    CCM01923                 BRANCH IF YES           SA50816
         TM    DCBDSORG,DCBORGIS+DCBORGDA+DCBORGPO  IS/DA/PO DATA SET
         BZ    CCM02000                 BRANCH IF NO
CCM01923 EQU   *                        TCAM MSG QUEUE          SA50816
         XR    RET,RET                  CLEAR REG
         TM    DCBDSORG,DCBORGDA        BDAM                    YA03180
         BNO   CCM01924                 BRANCH IF NOT           YA03180
         TM    DCBMACRF,DCBMEXCP        EXCP                    YA03180
         BO    CCM01925                 BDAM-EXCP, USE DEBNMEXT YA03180
         B     CCM01930                 USE TIOT IF NOT         YA03180
CCM01924 EQU   *                        TEST FOR ISAM           YA03180
         TM    DCBDSORG,DCBORGIS        IS IT AN ISAM DATA SET  SA54605
         BNO   CCM01930                 BRANCH IF NOT ISAM      SA54605
         TM    DCBMACRF,DCBMEXCP        IS IT EXCP               M1771
         BZ    CCM01926                 BRANCH IF NOT EXCP      YA03180
CCM01925 EQU   *                        USE NUMBER OF EXTENTS   YA03180
         IC    RET,DEBNMEXT             GET NUMBER OF EXTENTS    M1771
         B     CCM01980                 GO DECREMENT THIS NUMBER M1771
CCM01926 EQU   *                                                YA03180
         IC    RET,DEBNPEE              GET NO. OF PRIME EXTENTS M1771
         LA    RET,K2(RET)              ADD 1 UCB SEGMENT EACH   M1771
*                                       FOR INDEX AND OVERFLOW   M1771
         B     CCM01980                 GO DECREMENT THIS NUMBER M1771
CCM01930 EQU   *                                                 M1771
         LR    RF,RTIOT                 SAVE TIOT ENTRY PTR      A41413
         XR    R1,R1                    CLEAR REG                A41413
CCM01940 EQU   *                                                 A41413
         IC    R1,TIOELNGH              GET LEN OF THIS ENTRY    A52390
         AR    RTIOT,R1                 POINT TO NEXT ENTRY      A52390
* CALCLULATE NUMBER OF UNITS FOR THIS DD ENTRY                   A52390
         SH    R1,DDBASLEN              SUBTR DD ENTRY BASE LEN  A52390
         SRL   R1,K2                    DIV BY 4 BYTES PER UCB   A52390
*                                       ENTRY TO GET NO. UNITS   A52390
         AR    RET,R1                   ADD ONTO TOTAL NO.       A41413
         TM    DCBDSORG,DCBORGPO        IS IT BPAM              SA54605
         BNO   CCM01960                 BRANCH IF NO            SA54605
         CLI   TIOELNGH,X'00'           TEST IF END OF TIOT      A41413
         BE    CCM01960                 BRANCH IF END OF TIOT    A41413
         CLI   TIOEDDNM,BLANK           CHECK NEXT DDNAME        A41413
         BE    CCM01940                 BRANCH TO GET NEXT ENTRY A41413
CCM01960 EQU   *                                                 A41413
         LR    RTIOT,RF                 RESTORE TIOT ENTRY PTR   A41413
CCM01980 EQU   *                                                 A41413
         BCTR  RET,0                    DECREASE COUNT BY ONE
         MH    RET,UCBSIZE              MULTIPLY BY SIZE OF UCB
*                                       SEGMENT IN SMF RECORD
         AR    R0,RET                   ADD UCB SEG TO SIZE      A41413
CCM02000 LR    RET,R0                   SAVE AMOUNT FOR FREEMAIN
         L     R1,WTGPREFX              GET BASE PREFIX ADDR     YM4614
         LA    RF,IECREGSV-IECPREFX(,R1) ADDR OF REG SAVE AREA   YM4614
         CLC   IECCORID-IECPREFX(K4,R1),RECCORID  RECOVERY ENTRY YM4614
         LR    R1,RF                    GET EXTENDED PREFIX ADDR YM4614
         BNE   CCM02050                 BR IF NOT RECOVERY ENTRY YM4614
         L     RF,8(,RF)                RECOVERY REG SAVE AREA   YM4614
CCM02050 IECRES GET,LV=(RET),PREFIX=YES, GET CORE FOR SMF RECORD YM4614*
               STM=(2,14,0(RF)),A=(R1),ID=SFWA                   YM4614
         LR    R7,R1                    SET RECORD BASE
*
*   THIS SECTION BUILDS THE JFCB SEGMENT AND THE DEVICE INDEPENDENT
*   DCB/DEB SEGMENT OF THE SMF RECORD
*
         MVC   SMFJFCB1,INFMJFCB        COPY THE JFCB
         TM    DCBMACRF,DCBMEXCP        IS IT EXCP               A39153
         BZ    CCM02140                 BRANCH IF NOT            A39153
         TM    DCBMACRF,DCBMCOM         COMMON INTERFACE PRESENT A39153
         BZ    CCM02100                 BRANCH IF NOT            A39153
         MVC   SMFDCBOR,DCBDSORG        DCBDSORG FIELD           A39153
CCM02100 TM    DCBMACRF,DCBMFOUN        FOUNDATION EXT. PRESENT  A39153
         BZ    CCM02120                 BRANCH IF NOT            A39153
         MVC   SMFDCBRF,DCBRECFM        DCBRECFM FIELD           A39153
CCM02120 TM    DCBMACRF,DCBMAPP         APPENDAGES PRESENT       A39153
         BZ    CCM02200                 BRANCH IF NOT            A39153
         B     CCM02160                 MOVE IN DCBOPTCD         A39153
CCM02140 EQU   *                                                 A39153
         MVC   SMFDCBOR,DCBDSORG        DCBDSORG FIELD
         MVC   SMFDCBRF,DCBRECFM        DCBRECFM FIELD
CCM02160 EQU   *                                                 A39153
         MVC   SMFDCBOP,DCBOPTCD        DCBOPTCD FIELD
CCM02200 EQU   *                                                 A39153
         MVC   SMFDCBMF,DCBMACRF        DCBMACRF FIELD
         MVC   SMFDCBFL,DCBOFLGS        DCBOFLGS FIELD
         MVC   SMFDEBFL,DEBOFLGS        DEBOFLGS FIELD
         MVC   SMFDEBOP,DEBOPATB        DEBOPATB FIELD
         MVC   SMFTIOT(SMFJFCB1-SMFTIOT),0(RTIOT)  TIOT ENTRY FOR THIS
*                                       DCB
         TM    DCBMACRF,DCBMEXCP        IS THIS EXCP
         BO    CCM02250                 BRANCH IF YES
         TM    DCBDSORG,DCBORGPS        IS THIS A PS DATA SET
         BNO   CCM02300                 BRANCH IF NO
         TM    DCBCIND2,DCBCNCHS        IS THIS CHAIN SCHED.   @ZA05458
         BNO   CCM02250                 BRANCH IF NO           @ZA05458
         OI    SMFJFCB1+JFCOPTCD-INFMJFCB,JFCPCIBT    SET      @ZA05458
*                                       CHAIN SCHEDULING       @ZA05458
CCM02250 EQU   *
         LA    R1,K4                    SET UP FOR TAPE OFFSET   A43244
         TM    UCBTBYT3,UCB3DACC        IS THIS A DA DEVICE      A43244
         BZ    CCM02280                 BRANCH TO GET DEBVOLSQ   A43244
CCM02260 EQU   *                                                 A43244
         IC    R1,DEBNMEXT              GET NUMBER OF EXTENTS
         SLL   R1,4                     MULT BY EXTENT SECTION SIZE
CCM02280 EQU   *                                                 A43244
         LA    R1,DEBDVMOD-DEB(R1,RDEB)  GET ADDR OF DEBVOLSQ
         MVC   SMFDEBVL,0(R1)           DEBVOLSQ FIELD
CCM02300 EQU   *
         ST    RDCB,REGSAVE             SAVE DCB POINTER
         STM   RCORE,RET,REGSAVE+K4     SAVE REGISTERS
         L     RD,CVTPTR                GET POINTER TO CVT
         L     RET,CVTTCBP              PTR TO TCB
         L     RET,K4(RET)              PTR TO CURRENT TCB
         L     RF,TCBTCT-TCB(RET)       GET PTR TO TCT
         USING SMFTCT,RF
         MVC   SMF14JBN+K10(K4),TCTJMR  SAVE ADDR OF JMR
         L     R1,TCTIOTBL              GET PTR TO TCT TIOT
         DROP  RF
         USING TCTTIOT,R1
         LA    RET,K4                   SET UP INDEX
CCM02400 LA    RET,K4(RET)              INCREMENT TABLE PTR
         LA    RF,0(RET,R1)             PTR TO LOOK UP ENTRY
         CLC   0(K4,RF),ZERO            CHECK FOR END OF TABLE   YM3762
         BNZ   CCM02500                 BRANCH IF NOT END
         LM    RCORE,RET,REGSAVE+K4     RESTORE REGISTERS
         B     CCM04800                 BRANCH TO FREEMAIN
CCM02500 CLC   0(K2,RF),DCBTIOT         CHECK FOR CORRECT ENTRY
         BNE   CCM02400                 BRANCH IF NOT
         LH    RF,K2(RF)                GET ENTRY DISPLACEMENT
         AR    RF,R1                    GET ENTRY ADDR
         ST    RF,REGSAVE+K48           SAVE TCTUCBP
         DROP  R1
         TM    UCBJBNR,UCBVRDEV         TEST FOR VIO DATA SET   YM3859
         BZ    CCM02550                 BRANCH IF NOT           YM3859
         OI    SMF14RIN,SMF14VIO        SET VIO BIT IN SMF REC  YM3859
CCM02550 EQU   *                        TEST DEVICE TYPE        YM3859
         TM    UCBTBYT3,UCB3DACC        IS THIS A DA DEVICE
         BO    CCM05000                 BRANCH IF YES
*
*   THIS SECTION BUILDS THE DCB/DEB EXTENSION, THE UCB SEGMENT
*   FOR DATA SETS ON TAPE DEVICES.
*
         USING TCTUCBP,RF
         LM    RCORE,RF,REGSAVE+K4      RESTORE REGISTERS
         XC    REGSAVE(K52),REGSAVE     CLEAR REGSAVE CORE       A39153
         TM    DCBMACRF,DCBMEXCP        IS IT EXCP               A39153
         BZ    CCM05050                 BRANCH IF NOT            A39153
         TM    DCBMACRF+1,DCBMDEV       DEV. INTERFACE PRESENT   A39153
         BZ    CCM05070                 BRANCH IF NOT            A39153
CCM05050 EQU   *                                                 A39153
         MVC   SMFDCBBL,DCBBLKCT        DCBBLKCT FIELD
CCM05070 EQU   *                                                 A39153
         MVC   SMFDSSNO,UCBSQC          DATA SET SERIAL NUMBER
         MVC   SMFUCBCH(K2),UCBCHA      UCBCHA FIELD
         NI    SMFUCBCH,X'3F'           MASK OFF BITS 0 AND 1    A44480
         MVC   SMFUCBTY,UCBTYP          UCBTYP FIELD
         MVC   SMFSRTEV,UCBVOLI         UCBVOLI FIELD
         MVC   SMFSRTES,UCBSTAB         UCBSTAB FIELD
         LA    RUCB,0(,RUCB)            CLEAR HIGH ORDER BYTE    A38013
         XR    R0,R0                    CLEAR REG                A38013
         IC    R0,TCTSCTR               SET LOOP CONTROL         A38013
         LR    R1,RF                    POINT TO FIRST UCB      Z30AAJC
CCM05080 CLM   RUCB,3,0(R1)             TEST FOR CORRECT UCB    Z30AAJC
         BE    CCM05090                 BRANCH IF CORRECT        A38013
         LA    R1,K8(R1)                INCREMENT INDEX          A38013
         BCT   R0,CCM05080              TEST FOR LAST ENTRY      A38013
         B     CCM04450                 BRANCH IF LAST           A38013
CCM05090 EQU   *                        GET EXCP COUNT          Z30AAJC
         MVC   SMFEXCP,K4(R1)           GET EXCP COUNT IN TCTIOT
CCM04450 EQU   *                                                 A38013
         MVC   SMFSRTEF(K4),UCBFSCT     UCBFSCT AND UCBFSEQ FIELDS
         MVI   SMF14NUC,K1              SET NUMBER OF UCB'S TO 1
SMFRCDLN EQU   X'124'                   SMF RECORD LENGTH        XM5543
         LA    RF,SMFRCDLN              SET RECORD LENGTH        XM5543
         STH   RF,SMF14LEN              STORE LENGTH IN RECORD
*
*   THE ESV EXTENSION IS NOT SUPPORTED FOR REL 21
*
*   THIS SECTION FILLS IN THE REMAINING FIELDS AND ISSUES THE
*   SMFWTM MACRO TO WRITE THE RECORD ON THE SYS1.MAN DATA SET.
*
*     CHECK FOR A TEMPORARY DATA SET BY EXAMINING THE  DSN      SA60779
         CLC   JFCBDSNM(K3),SYSDS2H     SYSTEM GENERATED NAME?  SA60779
         BNE   CCM04500                 BRANCH, NOT A TEMP DS   SA60779
         CLC   JFCBDSNM+K8(K2),SYSTME2H  TEST FOR .T            SA60779
         BNE   CCM04500                 BRANCH, NOT A TEMP DS   SA60779
         CLI   JFCBDSNM+K16,PERIOD      TEST FOR '.'            SA60779
         BNE   CCM04500                 BRANCH, NOT A TEMP DS   SA60779
         OI    SMF14RIN,SMF14TDS        SET TEMP INDICATOR
CCM04500 EQU   *
         MVI   SMF14RTY,K14             SET RECORD TYPE TO 14
         TM    SMFDEBOP,DEBOPOUT-DEBOPRBK  TEST FOR INPUT RDBACK DS
         BZ    CCM04600                 BRANCH IF INPUT/RDBACK
         OI    SMF14RTY,K15             SET RECORD TYPE TO 15
CCM04600 TIME  BIN                      ISSUE TIME SVC
         STM   R0,R1,SMF14TME+K2        SAVE REGS
         MVC   SMF14TME(K8),SMF14TME+K2  MOVE IN TIME
         L     R1,SMF14JBN+K10          GET PTR TO JMR
         USING JMRJOB,R1
         MVC   SMF14JBN(SMF14UID-SMF14JBN),JMRJOB  JMRJOB FIELD
         MVC   SMF14UID,JMRUSEID        JMRUSEID FIELD
         MVC   SMF14SID(K4),JMRCPUID    JMRCPUID FIELD
         MVI   SMF14SDC,SMF14UCB-SMFDCBDE  DCBDEBSZ FIELD
         MVI   SMF14SUC,SMF14UCE-SMF14UCB  UCBSEGSZ FIELD        XM0454
         OC    DXREGE,DXREGE            TEST FOR CLOSE ENTRY
         BZ    CCM04700                 BRANCH IF YES
         OI    SMF14RIN,SMF14EOV        SET EOV BIT
CCM04700 EQU   *                        POINT TO SMF RECORD
         SMFWTM (R7)                    WRITE SMF RECORD
CCM04800 EQU   *
         L     R1,WTGPREFX              GET BASE PREFIX ADDR     YM4614
         LA    RF,IECREGSV-IECPREFX(,R1) ADDR OF REG SAVE AREA   YM4614
         CLC   IECCORID-IECPREFX(K4,R1),RECCORID  RECOVERY ENTRY YM4614
         BNE   CCM04850                 BR IF NOT RECOVERY ENTRY YM4614
         L     RF,8(,RF)                RECOVERY REG SAVE AREA   YM4614
CCM04850 EQU   *                                                 YM4614
         LR    R1,R7                    SET UP FOR FREEMAIN
         IECRES FREE,A=(1),PREFIX=YES,STM=(0,14,0(RF))           YM4614*
                                        FREE SMF RECORD AREA     YM4614
         L     RF,DXATCOM3              GET RRPLIST PTR          YM4614
         USING RRPLIST,RF                                        YM4614
         L     R1,WTGPREFX              GET BASE PREFIX ADDR     YM4614
         CLC   IECCORID-IECPREFX(K4,R1),RECCORID  RECOVERY ENTRY YM4614
         BNE   CCM04900                 BR IF NOT RECOVERY ENTRY YM4614
         NI    RRFLAGS1,X'FF'-RRFSMF    RESET SMF IN CONTROL FLG YM4614
         MVC   WTGMODNM,RECMOD          MOVE RECOVERY MOD NAME   YM4614
         L     RF,RRFWORK               RECOVERY SUBRTN SAVEAREA YM4614
         L     RET,IECREGSV-IECEXTPR+K4*RET(,RF) GET RETURN ADDR YM4614
         ST    RET,WTGMODEP             SET EPA TO RETURN ADDR   YM4614
         LM    R0,RD,0(RF)              GET RECOVERY SUBRTN REGS YM4614
         IECRES LOAD,EXTPR=(RF),BRANCH=DIRECT RETURN TO IFG0RR0B YM4614
*
CCM04900 LM    RTIOT,RET,DXREGSAV       RESTORE CALLER'S         YM4614
         LM    R0,R1,DXREG0             REGISTERS
         LTR   RET,RET                  ENTRY FROM CLOSE
         BNZ   CCM04950                 BRANCH IF NOT
         LA    RET,K12                  RETURN FOR CLOSE RTN     A38013
         CLC   DXRETMOD(2),FINALMOD     RETURNING TO IFG0202J
         BNE   CCM04950                 NO, XCTL TO DXRETMOD
         NI    RRFLAGS1,X'FF'-RRFSMF    RESET SMF IN CONTROL FLG Y02144
         IECRES LOAD,MODID=FINALMOD,BRCODE=(RET),BRANCH=QUEUED   Y02080
*                                       XCTL TO FINALMOD         Y02080
CCM04950 EQU   *
*                                       CLEAR TTR TO ENABLE      XM0454
*                                       XCTLING BY NAME          XM0454
         NI    RRFLAGS1,X'FF'-RRFSMF    RESET SMF IN CONTROL FLG Y02144
         IECRES LOAD,MODID=DXRETMOD,BRCODE=(RET),BRANCH=QUEUED   Y02080
*                                   XCTL TO FINAL MOD OR EOV RTN Y02080
CCM05000 EQU   *
         L     R1,WTGPREFX              GET BASE PREFIX ADDR     YM4614
         LA    RF,IECREGSV-IECPREFX(,R1) ADDR OF REG SAVE AREA   YM4614
         CLC   IECCORID-IECPREFX(K4,R1),RECCORID  RECOVERY ENTRY YM4614
         BNE   CCM05010                 NO, GO TEST FOR CLOSE  @ZA07184
         L     RF,8(,RF)                RECOVERY REG SAVE AREA @ZA07184
         MVC   WTGMODNM+4(K2),CLOSESVC  MOVE IN C'20'          @ZA07184
         LR    R1,R7                    PASS REG 7 TO REG 1    @ZA07184
         B     CCM05020                 RECOV. BRANCH DIRECT   @ZA07184
CCM05010 EQU   *                                               @ZA07184
         LM    RTIOT,RET,DXREGSAV       RESTORE CALLERS        @ZA07184
         L     R0,DXREG0                REGISTERS              @ZA07184
         CLI   WTGMODNM+4,X'F2'         ENTRY FROM CLOSE?      @ZA07184
         MVC   WTGMODNM+4(K2),CLOSESVC  MOVE IN C'20'          @ZA07184
         LR    R1,R7                    PASS REG 7 TO REG 1    @ZA07184
         BNE   CCM05020                 ENTRY NOT FROM CLOSE   @ZA07184
*                                       BRANCH DIRECT          @ZA07184
         IECRES LOAD,MODID=SMFDAMOD,BRCODE=(RET),BRANCH=QUEUED @ZA07184
CCM05020 IECRES LOAD,MODID=SMFDAMOD,EXTPR=(RF),BRANCH=DIRECT   @ZA07184
         EJECT
         SPACE 1
**********************************************************************
*                                                                    *
*              CONSTANTS                                             *
*                                                                    *
**********************************************************************
         SPACE 1
ZERO     DC    F'0'                     CONSTANT ZERO            YM3762
DDBASLEN DC    Y(TIOESTTB-TIOENTRY)     TIOT ENTRY BASE LEN      A52390
UCBSIZE  DC    H'24'                    SIZE OF UCB SECTION
CLOSESVC DC    C'20'                    SVCCODE FOR CLOSE
SYSDS2H  DC    C'SYS'                   SYSTEM DS INDICATOR     SA60779
SYSTME2H DC    C'.T'                    TIME FOR TEMP DS        SA60779
RECCORID DC    C'RR0A'                  RECOVERY CORE ID         YM4614
         XCTLTABL ID=(SMFDAMOD,2I,FINALMOD,2J,RECMOD,IFG0RR0B),  YM4614*
               SVC=020,BRT=YES,LENGTH=                           YM4614
         IECDSECS CVT,TCB,TIOT,SMF,SMFRCD,SMFTCT,JMR,DCB,DEB,UCB,WTG,  *
               PREFX,MAIN,RRPL,EXPAND=YES                        Y02144
         IECEQU
         END
