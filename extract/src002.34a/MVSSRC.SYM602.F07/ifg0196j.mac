         TITLE 'IFG0196J                      OPEN - JFCB TO DCB MERGE'
         COPY  LCGASMSW
IFG0196J CSECT
***********************************************************************
*        VS 2 RELEASE 02 DELETIONS/CHANGES                            *
*                                                                     *
*A449100-450000                                              @ZA21109 *
*0000                                                        @ZA02660 *
*                                                                     *
* MODULE NAME = IFG0196J (OS/VS2)                                     *
*                                                                     *
* DESCRIPTIVE NAME = OPEN - JFCB TO DCB MERGE                         *
*                                                                     *
* COPYRIGHT = NONE                                                    *
*                                                                     *
* STATUS = RELEASE 2, LEVEL 0                                         *
*                                                                     *
* FUNCTION =                                                          *
*        1. RESTORE WORK DEB FIRST MAIN UCB ADDRESS FOR DIRECT ACCESS *
*        DATA SETS.                                                   *
*                                                                     *
*        2. MERGE JFCB TO DCB FIELDS FOR SAM, PAM, DAM, ISAM, EXCP    *
*        DATA SETS.                                                   *
*                                                                     *
* NOTES = SEE BELOW                                                   *
*                                                                     *
*    DEPENDENCIES =                                                   *
*            CLASS ONE CHARACTER CODE.  THE EBCDIC CHARACTER CODE     *
*            WAS USED FOR ASSEMBLY.  THE MODULE MUST BE REASSEMBLED   *
*            IF A DIFFERENT CHARACTER SET IS USED FOR EXECUTION.      *
*                                                                     *
*    RESTRICTIONS = NONE                                              *
*                                                                     *
*    REGISTER CONVENTIONS =                                           *
*            R2 POINTS TO DCB                                         *
*            R4 POINTS TO OPEN WORK AREA                              *
*            R5 POINTS TO THE RESIDENT ROUTINE                        *
*            R6 POINTS TO THE WTG TABLE                               *
*            R7 POINTS TO THE CURRENT PARAMETER LIST ENTRY            *
*            R8 POINTS TO THE CURRENT WTG TABLE ENTRY                 *
*            R9 POINTS TO THE DD ENTRY IN THE TIOT                    *
*            R10 POINTS TO THE UCB                                    *
*                                                                     *
*    PATCH LABEL = SEE NEXT TO LAST LABEL BEFORE ORG STATEMENT AT END *
*                  OF LISTING.                                        *
*                                                                     *
* MODULE TYPE = CONTROL (OPEN, CLOSE, EOV DATA MANAGEMENT)            *
*                                                                     *
*    PROCESSOR = ASSEMBLER XF                                         *
*                                                                     *
*    MODULE SIZE = SEE EXTERNAL SYMBOL DICTIONARY OR LOC FIELD ON     *
*                  ORG STATEMENT AT END OF LISTING                    *
*                                                                     *
*    ATTRIBUTES = REENTRANT, REFRESHABLE,READ-ONLY, ENABLED,          *
*                  PRIVILEGED, SUPERVISOR STATE, DATA MANAGEMENT KEY, *
*                  LINK PACK AREA RESIDENT/PAGEABLE                   *
*                                                                     *
* ENTRY POINT =                                                       *
*        IFG0196J R14 = 0                                             *
*                                                                     *
*    PURPOSE = SEE FUNCTION                                           *
*                                                                     *
*    LINKAGE =                                                        *
*        FROM IFG0194A:                                               *
*           MVC WTGIDDTR,DXSAVMOD                                     *
*           XR  RET,RET                                               *
*           IC  RET,DXSAVMOD                                          *
*           IECRES LOAD,BRANCH=QUEUED                                 *
*                                                                     *
*        FROM IFG0195A:                                               *
*           IECRES LOAD,MODID=ODA1950X,BRCODE=0,BRANCH=QUEUED         *
*                                                                     *
*        FROM IFG0195B:                                               *
*           LA  RF,ID5B6J                                             *
*           IECRES LOAD,MODID=(RF),BRCODE=(RET),BRANCH=QUEUED         *
*                                                                     *
*        FROM IFG0195C:                                               *
*           IECRES LOAD,MODID=ID5C6J,BRANCH=QUEUED                    *
*                                                                     *
*        FROM IFG0195D:                                               *
*           MVC  WTGIDTTR,MERGE                                       *
*           LA   RET,K0                                               *
*           IECRES LOAD,BRANCH=QUEUED                                 *
*                                                                     *
*        FROM IFG0195J:                                               *
*           IECRES LOAD,MODID=ID5J6J,BRCODE=K0,BRANCH=QUEUED          *
*                                                                     *
*        FROM IFG0195K:                                               *
*           LA  RET,K0                                                *
*           LA  RF,ID5K6J                                             *
*           IECRES LOAD,MODID=(RF),BRCODE=(RET),BRANCH=QUEUED         *
*                                                                     *
*        FROM IFG0195N:                                               *
*           LA  RET,K0                                                *
*           LA  RF,ID5N6J                                             *
*           IECRES LOAD,MODID=(RF),BRCODE=(RET),BRANCH=QUEUED         *
*                                                                     *
* INPUT =                                                             *
*        A JFCB IN EACH OF THE DCB WORK AREAS.                        *
*                                                                     *
* OUTPUT =                                                            *
*        MERGED FIELDS IN THE USER'S DCB, AND AN INDICATION OF WHICH  *
*        FIELDS WERE MERGED SAVED IN JFCBMASK.                        *
*                                                                     *
* EXIT-NORMAL =                                                       *
*        IFG0196L DCB EXIT                                            *
*        IFG0196K JFCB TO DCB MERGE TELECOMMUNICATIONS                *
*                                                                     *
* EXIT-ERROR =                                                        *
*        IFG0190P 013 ABEND                                           *
*                                                                     *
* EXTERNAL REFERENCES = SEE BELOW                                     *
*                                                                     *
*    ROUTINES = NONE                                                  *
*                                                                     *
*    DATA AREAS = NONE                                                *
*                                                                     *
*    CONTROL BLOCK = NONE                                             *
*                                                                     *
* TABLES = NONE                                                       *
*                                                                     *
* MACROS = IECRES, DMABCOND, XCTLTABL, IECDSECS                       *
*                                                                     *
* CHANGE ACTIVITY = NEW RELEASE (LEVEL 0)                             *
*                                                                     *
***********************************************************************
         EJECT
         IECDSECS CVT,TIOT,UCB,         DEFINE DESIRED DSECTS TO BE    X
               DCB,MAIN,WTG,PREFX       EXPANDED AT THE END      Y02080
*
         IECEQU ,                       DEFINE EQUATES
*
         USING IHADCB,RDCB              DEFINE BASE TO USER'S DCB
         USING FORCORE,RCORE            DEFINE BASE TO MAIN WORK AREA
         USING WTG,RWTG                 DEFINE BASE TO WTG TABLE Y02080
         USING TIOENTRY,RTIOT           DEFINE BASE TO TIOT DD ENTRY
         USING UCBOB,RUCB               DEFINE BASE TO MAIN UCB
*
         BALR  RBASE,0                  ESTABLISH BASE REGISTER
         USING *,RBASE                  DEFINE BASE REGISTER
*
***********************************************************************
*
*  DIRECT ACCESS MULTI-VOLUME ISAM, BDAM, MQ, AND PO REQUIRES
*  REINITIALIZATION OF THE WORK DEB MAIN UCB ADDR FOR UCB
*  ATTRIBUTE TESTING PURPOSES.
*  SINCE PS ALWAYS ROTATES THE TIOT DD ENTRY UCB POINTERS SO THAT
*  THE CURRENT UNIT IS FIRST, DOING THIS REINITIALIZATION FOR
*  EVERY DSORG WILL DO NO HARM.
*  (NOTE THAT THE WORK DEB EXTENT IS NOT BEING REINITIALIZED YET.)
*
         TM    JFCBMASK+K4,JFCMNULL     TEST IF NULL DATA SET
         BO    OMG10600                 BR IF YES
         TM    TIOELINK,TIOESYIN+TIOESYOT SPOOLED DATA SET?      Y02120
         BM    OMG10950                 BRANCH IF YES            Y02120
*
         L     RUCB,DXUCBADR            LOAD UCB ADDRESS         Y02134
         TM    UCBTBYT3,UCB3DACC        IS IT DIRECT ACCESS
         BNO   OMG10600                 BR IF NO
*
         LA    RUCB,0(RUCB)             CLEAR HIGH ORDER BYTE
         ST    RUCB,DXDEBUCB            STORE MAIN UCB ADDR IN WRK DEB
*
***********************************************************************
*
* INITIALIZE THE DEVT AND DVTBL FIELDS FOR DA EXCP DCB'S HERE.
* WHEN ACCESS METHODS ARE USED, THE A.M. EXECUTORS INITIALIZE
* THESE FIELDS.
*
OMG10600 EQU   *                        INIT DEVT, DVTB FIELDS
         TM    DCBMACRF,DCBMEXCP        IS THIS EXCP
         BZ    OMG10800                 BR IF NOT EXCP
*
         TM    JFCBMASK+K4,JFCMNULL     TEST IF NULL DATA SET
         BO    OMG12200                 BR IF YES TO ABEND
*
         TM    DCBMACRF,DCBMFOUN        IS DCB F.B.E. PRESENT
         BZ    OMG11200                 BR IF NO
*
         TM    UCBTBYT3,UCB3DACC        IS IT DIRECT ACCESS
         BNO   OMG10800                 BR IF NO
*
         MVC   DCBDEVT,UCBTBYT4         SET UP DEVTYPE FIELD
         OC    DCBDEVT,UCBTBYT3         OR IN DEVICE CLASS
*
         L     RC,CVTPTR                GET CVT ADDR
         L     RC,CVTZDTAB-CVT(,RC)     GET DEVICE CONSTANT TABLE ADDR
         SR    R1,R1                    CLEAR WORK REGISTER
         IC    R1,UCBTBYT4              GET DEVICE CHARACTERISTIC
         IC    R1,0(R1,RC)              ADD ID TO TABLE ADDRESS
         LA    RC,0(R1,RC)              ADD CONST AT THIS LOCATION
         IC    RB,DCBDVTBL              SAVE POSSIBLE RECRD NUMBER
         ST    RC,DCBDVTBL              SET UP DEVICE TABLE FIELD
         STC   RB,DCBDVTBL              RESTORE RECORD NUMBER
*
***********************************************************************
*
*  MERGE JFCB FUNC TO DCB FUNC IF 3525 UNIT RECORD DEVICE.       S21088
*                                                                S21088
OMG10800 EQU   *                        JFCB TO DCB MERGE
*
         TM    JFCBMASK+K4,JFCMNULL     TEST IF NULL DATA SET    S21088
         BO    OMG10950                 BR IF YES                S21088
*                                                                S21088
         CLI   UCBTBYT3,UCB3UREC        IS DEVICE UNIT RECORD    S21088
         BNE   OMG10950                 BR IF NO                 S21088
*                                                                S21088
         CLI   UCBTBYT4,X'0C'           IS DEVICE 3525           S21088
         BNE   OMG10950                 BR IF NO                 S21088
*                                                                S21088
         TM    DCBMACRF,DCBMEXCP        IS THIS EXCP DCB         S21088
         BZ    OMG10900                 BR IF NO                 S21088
*                                                                S21088
         TM    DCBMACRF,DCBMDI5W+DCBMDI4W+DCBMDI3W+DCBMDI1W      S21088
         BZ    OMG10950                 BR IF NO DCB FUNC FIELD  S21088
*                                                                S21088
OMG10900 EQU   *                        NOT EXCP                 S21088
         CLI   DCBFUNC,K0               IS DCB FUNC SPECIFIED    S21088
         BNE   OMG10950                 BR IF YES                S21088
*                                                                S21088
         MVC   DCBFUNC,JFCFUNC          MERGE FUNC FROM JFCB     S21088
         OI    JFCBMASK+K3,X'08'        INDICATE FUNC MERGED     S21088
*                                                                S21088
OMG10950 EQU   *                        MERGE CONTINUED          S21088
*
         MVI   DXATOPEN,DXATMODS        SHOW DCB MERGE STARTED   Y02144
         TM    DCBMACRF,DCBMEXCP        CHECK FOR EXCP
         BZ    OMG11600                 BR IF NOT EXCP
OMG11200 EQU   *                        NO DCB F.B.E.
         LA    R1,K13                   POINT TO FBE IN MERGE TABLE
         TM    DCBMACRF,DCBMAPP         IS EXCP INTERFACE PRESENT
         BO    OMG11400                 BR IF YES, OPTCD INCLUDED
         LA    R1,K14                   NO, DO NOT MERGE OPTCD
OMG11400 EQU   *                        OPTCD INCLUDED
         TM    DCBMACRF,DCBMFOUN        IS F.B.E. PRESENT
         BO    OMG15000                 BR IF YES TO MERGE
         B     OMG16900                 BR TO BYPASS MERGE
*
***********************************************************************
*
* IF GRAPHICS OR TELEPROCESSING, BYPASS THE PROCESSING IN THIS MODULE.
*
OMG11600 EQU   *                        NOT EXCP
*        TEST FOR GRAPHICS, TCAM, OR 3705 CU                     S22024
         TM    DCBDSORG+K1,DCBORGGR+DCBORGTQ+DCBORGTX+DCBDSGTR   S22024
         BNZ   OMG11800                 BR IF GRAPHICS, TCAM, 3705 CU  C
                                                                 S22024
         TM    DCBDSORG,DCBORGCX+DCBORGCQ+DCBORGMQ
         BNZ   OMG11800                 BRANCH IF TP
         TM    DCBDSORG,DCBORGPS        IS THIS PS
         BNO   OMG12000                 BRANCH IF NO
         TM    TIOELINK,TIOEQNAM        IS THIS A TCAM DUMMY
         BNO   OMG12000                 BRANCH IF NO
OMG11800 EQU   *                        GO TO JFCB TO DCB MERGE TELECOM
*
         IECRES LOAD,MODID=ID6J6K,BRANCH=QUEUED                  Y02080
*                                       XCTL TO GX,TELCOM MODULE Y02080
*
***********************************************************************
*
*  NULL DATA SET IS ONLY SUPPORTED FOR DSORG=PS OR MQ.
*  FOR MQ IT IS NOT LOGICALLY A DUMMY, SINCE IT WILL BE LINKED
*  UP WITH A DATA SET SUPPORTED BY A MASTER QTAM PROGRAM.
*
OMG12000 EQU   *                        NULL DATA SET
         TM    JFCBMASK+K4,JFCMNULL     TEST IF NULL DATA SET
         BNO   OMG12400                 BR IF NOT
         TM    DCBDSORG,DCBORGMQ        IS IT DSORG=MQ           A43751
         BO    OMG12400                 BR IF YES                A43751
         TM    DCBDSORG,DCBORGPS        IS IT DSORG=PS           A43751
         BZ    OMG12200                 BR IF NO TO PROB DET     A43751
         TM    DCBMACRF+K1,DCBMWRIT+DCBMLOAD  IS BDAM LOAD MODE  A43751
         BNO   OMG12400                 BR IF NO, JUST PS        A43751
*
***********************************************************************
OMG12200 EQU   *                        ABEND
OABD043  EQU   43                       NULL DATA SET NOT SUPPORTED
*                                       FOR EXCP, AND ONLY FOR
*                                       DSORG=PS OR MQ
         DMABCOND OABD043,ID6J0P        XCTL TO PROBLEM DET / ABEND
*
***********************************************************************
OMG12400 EQU   *                        DSORG TESTS
         TM    DCBDSORG,DCBORGPS        IS IT PHYSICAL SEQ
         BO    OMG12600                 BR IF YES
*
         TM    DCBDSORG,DCBORGIS        IS IT INDEXED SEQ
         BO    OMG14000                 BR IF YES
*
         TM    DCBDSORG,DCBORGDA        IS IT DIRECT ACCESS
         LA    R1,K8                    POINT TO DAM IN MRG TABLE
         BO    OMG15000                 BR IF YES
*
         TM    DCBDSORG,DCBORGPO        IS IT PARTITIONED ORGANIZATION
         BO    OMG13600                 BR IF YES
*
*                                       NO DSORG SPECIFIED,
*                                       DEFAULT TO PS
*
***********************************************************************
*
OMG12600 EQU   *                        PHYSICAL SEQUENTIAL
         TM    JFCBMASK+K4,JFCMNULL     CHECK FOR NULL DATA SET
         BZ    OMG12800                 BR IF NO
*
         TM    TIOELINK,TIOTTERM        IS THIS TSO
         BNO   OMG13400                 BR IF NO
*
         MVI   DCBDEVT,DCBDEVTM         SET DEVICE TYPE TO X'4F'
*
         CLI   DCBBUFNO,K0              IS BUFNO SPECIFIED IN DCB
         BNE   OMG13400                 BR IF YES
*
         CLI   JFCBUFNO,K0              IS BUFNO SPECIFIED IN DD CARD
         BNE   OMG13400                 BR IF YES
*
         MVI   DCBBUFNO,K1              SET DCB BUFNO TO 1
         B     OMG13400                 BR TO CONT NULL DATA SET
*
***********************************************************************
*
*   THIS SECTION TESTS THE JFCBUFOF BYTE TO DETERMINE THE STATUS
*   OF THE OPTIONAL BUFFER OFFSET FIELD FOR ASCII'S DATA SETS. THE
*   CORRESPONDING BITS IN THE DCB ARE MERGED IF NOT PREVIOUSLY SET.
*
OMG12800 EQU   *
         TM    TIOELINK,TIOESYIN+TIOESYOT SPOOLED DATA SET?      Y02120
         BM    OMG13400                 BRANCH IF YES            Y02120
         TM    UCBTBYT3,UCB3TAPE        TEST IF MAGNETIC TAPE
         BZ    OMG13400                 BR IF NO
*
         TM    DCBUSASI,DCBBLBP         IS DCB FIELD ZERO
         BO    OMG13000                 NO, BYPASS MERGE
         TM    JFCBUFOF,DCBUSAON        BLK LENGTH IN JFCB
         BZ    OMG13000                 NO, BYPASS MERGE
         OI    DCBUSASI,DCBBLBP         SET BUF OF =BLK LNG
         OI    JFCBMASK,DCBUSAON        INDICATE MERGE IN JFCBMASK
*
OMG13000 TM    DCBBUFOF,X'FF'-DCBUSAON  CHECK DCB FOR BUFFER OFFSET
         BNZ   OMG13400                 BYPASS BUFOFF CHECK
         MVC   DCBBUFOF,JFCBUFOF        SET BUFFER OFFSET LENGTH
         NI    DCBBUFOF,X'FF'-DCBUSAON  TURN OFF BIT 0
         OI    JFCBMASK+K1,JFCMBFOF     SET BUFOFF MERGE MASK
*
***********************************************************************
*
OMG13400 EQU   *                        CONTINUE MERGE TESTS
         LA    R1,K5                    POINT TO QSAM IN MRG TAB
*
         TM    DCBMACRF,DCBMGET         TEST IF QSAM GET
         BO    OMG15000                 BR IF YES
         TM    DCBMACRF+K1,DCBMPUT      TEST IF QSAM PUT
         BO    OMG15000                 BR IF YES
*
*        THIS IS PS BSAM OR PO BPAM
OMG13600 EQU   *
         NC    DCBLRECL,DCBLRECL        IS LRECL=0
         BNZ   OMG13800                 BR IF NO
         MVC   DCBLRECL,JFCLRECL        MERGE LRECL
         OI    JFCBMASK+K3,X'02'        SET LRECL MERGE MASK
OMG13800 EQU   *                        LRECL NOT 0
         LA    R1,K10                   POINT TO BSAM IN MERGE TABLE
         B     OMG15000                 BR TO MAIN MERGE
*
OMG14000 EQU   *                        ISAM
         LA    R1,K0                    POINT TO ISAM IN MRG TABLE
         TM    DCBMACRF+K1,DCBMPMOV+DCBMLOC  IS IT QISAM LOAD MODE
         BNZ   OMG15000                 BR IF YES
*
         L     RF,DSCCORE               PTR TO FORMAT 2 DSCB
         MVC   DCBNCRHI,DS2NOBYT(RF)    MOVE TO DCB THE
*                                       NUMBER OF BYTES NEEDED
*                                       FOR HIGHEST LEVEL INDEX
DS2NOBYT EQU   X'40'                    DS2NOBYT OFFSET IN DSCB
*
         NC    DCBBLKSI,DCBBLKSI        IS DCBBLKSI=0         @ZA02660
         BNZ   OMG15000                 BRANCH IF NO          @ZA02660
         MVC   DCBBLKSI,JFCBLKSI        MERGE BLKSIZE
         OI    JFCBMASK+K2,X'10'        INDICATE MERGE DONE
*
***********************************************************************
*
*        JFCB TO DCB MERGE ROUTINE
*
*  NOTE - THE HIARCHY FIELD DOES NOT PARTICIPATE IN THE FORWARD
*        MERGE IN THE MANNER OF OTHER FIELDS.
*  THE FOLLOWING INSTRUCTIONS SAVE THE BYTE CONTAINING THE
*  HIERARCHY SPECIFICATION (INDICATED BY BITS 0 AND 5) SO THAT
*  AFTER THE MERGE IS COMPLETED, THE HIERARCHY INDICATION CAN
*  BE RESTORED, AND ALLOWS THE MERGE OF DCBBFTEK AND DCBBFALN
*  FIELDS TO BE INDEPENDENT OF THE HIERARCHY SPECIFICATION.
*
OMG15000 EQU   *                        JFCB TO DCB MERGE
         MVC   DXCCW11(K1),DCBBFTEK     SAVE DCB HIERARCHY BITS
         NI    DCBBFTEK,X'FF'-DCBH0-DCBH1  ZERO DCB HIERARCHY BITS
*
*
         LR    R0,RDCB                  DCB WORK REGISTER
         LA    RET,OMG1720K             DCB DISP PTR FOR MERGE
         LA    RF,OMG1800K              JFCB DISP PTR FOR MERGE
         LA    RD,OMG1840K              FIELD LENGTHS FOR JFCB-DCB MRG
         AR    RF,R1                    POINT TO
         AR    RET,R1                   PROPER
         AR    RD,R1                    ENTRY
         SR    RUCB,RUCB                CLEAR MASK REGISTER
*
OMG15200 EQU   *                        MERGE NEXT FIELD
         L     RCORE,WTGCORE-K1-WTGENTRY(,RWTGC)  RELOAD DCB WORK ADDR
         LA    RCORE,DXJBF              ADDRESS OF JFCB
         SR    RC,RC                    CLEAR REGISTER
         IC    RC,0(,RET)               PICK UP DCB DISP
         AR    RDCB,RC                  POINT TO DCB FIELD
         IC    RC,0(,RF)                PICK UP JFCB DISP
         AR    RCORE,RC                 POINT TO JFCB FIELD
         IC    RC,0(,RD)                PICK UP FIELD LENGTH-1
         CLI   0(RET),X'24'             IS THIS FIELD RECFM
         BE    OMG16600                 BR IF IT IS RECFM
         EX    RC,OMG16000              ****EXECUTE COMPARE****
         BNZ   OMG15600                 BRANCH IF FIELD NOT ZERO
OMG15400 EQU   *                        NO RECFM
         EX    RC,OMG16200              ****EXECUTE THE MERGE****
         O     RUCB,OMG1700K            OR A BIT INTO THE MASK REGISTER
*
OMG15600 EQU   *                        RECFM NON-ZERO
         LA    RET,K1(RET)              STEP DCB DISP POINTER
         TM    0(RET),X'FF'             TEST IF AT END OF MERGE
         BZ    OMG16700                 BRANCH IF AT END OF MERGE
         BO    OMG16400                 BRANCH IF END OF A.M. MERGE
         LA    RF,K1(RF)                STEP JFCB DISP POINTER
         LA    RD,K1(RD)                STEP FIELD LENGTH POINTER
OMG15800 EQU   *                        SHIFT MERGE MASK
         SLL   RUCB,K1                  SHIFT THE MASK TO THE LEFT 1
         LR    RDCB,R0                  RESET DCB POINTER
         B     OMG15200                 BR TO MERGE NEXT FIELD
*
OMG16000 NC    0(*-*,RDCB),0(RDCB)      ****CHECK FOR ZERO FIELD****
OMG16200 OC    0(*-*,RDCB),0(RCORE)     ****MERGE****
*
OMG16400 EQU   *                        END OF AM MERGE
         LA    RF,K1(RF)                STEP RF BY ONE
         IC    RC,0(,RF)                PICK UP SHIFT FACTOR
         SLL   RUCB,0(RC)               SHIFT MASK LEFT ONE
         LA    RET,OMG1740K             RESET POINTERS
         LA    RF,OMG1820K
         LA    RD,OMG1860K
         B     OMG15800                 BR TO SHIFT MERGE MASK
*
OMG16600 EQU   *                        RECFM
         TM    0(RDCB),X'FF'-DCBRECKY   IS ANY VALUE IN RECFM
         BZ    OMG15400                 BR IF NO
         B     OMG15600                 BR RECFM NON-ZERO
*
OMG16700 EQU   *                        END OF MERGE
         L     RDCB,0(,RPARC)           RELOAD DCB ADDR
         L     RCORE,WTGCORE-K1-WTGENTRY(,RWTGC)  RELOAD DCB WORK ADDR
*
*  TO INSURE BFTEK/BFALN CAME FROM SAME SOURCE AND TAKE DOUBLEWORD
*  DEFAULT IF NOT SPECIFIED FOR BFALN AND RESET HIERARCHY BITS 0,5 TO
*  PRE-MERGE SETTING.
OMG16710 EQU   *                                               @ZA21109
         TM    DXCCW11,X'7B'            BFALN/BFTEK SPEC IN DCB?ZA21109
         BZ    OMG16730                 NO, GO CHK JFCB        @ZA21109
         TM    DXCCW11,X'03'            BFALN SPECIFIED IN DCB?@ZA21109
         BNZ   OMG16740                 YES, USE IT            @ZA21109
OMG16720 OI    DXCCW11,X'02'            SET DEFAULT            @ZA21109
         B     OMG16740                                        @ZA21109
OMG16730 EQU   *                        CHECK JFCB             @ZA21109
         TM    JFCBFALN,X'7B'           BFALN/BFTECK SPEC IN JFCB?21109
         BZ    OMG16720                 GO SET DEFAULT         @ZA21109
         OC    DXCCW11,JFCBFALN         USE JFCB FIELDS        @ZA21109
         TM    JFCBFALN,X'03'           BUFF ALGN SPECIFIED IN JFCB?
         BZ    OMG16720                 TAKE DEFAULT IF NOT    @ZA21109
OMG16740 EQU   *
         NI    DCBBFTEK,X'FF'-JFCBH1    INSURES HIARCHY NOT MERGED
         OC    DCBBFTEK,DXCCW11         RESTORES ONLY HIARCHY FLAGS
*
*
         TM    DCBRECFM,DCBRECKY        IS KEY LENGTH SPECIFIED
         BO    OMG16800                 BR IF YES
         CLI   DCBKEYLE,K0              IS KEY LENGTH=0
         BNE   OMG16800                 BR IF NO
         MVC   DCBKEYLE,JFCKEYLE        MERGE KEY LENGTH
         OI    JFCBMASK+K3,X'20'        SET KEY LENGTH MERGE MASK
OMG16800 EQU   *                        KEY LENGTH NOT ZERO
*
         SLL   RUCB,K6                  FINAL ALIGN MERGE MASK BITS
         O     RUCB,JFCBMASK            OR WITH EXISTING MERGE BITS
         ST    RUCB,JFCBMASK            STORE IN JFCB MASK FIELD
         MVI   DXATOPEN,DXATMODE        SHOW DCB MERGE DONE      Y02144
*
***********************************************************************
*
OMG16900 EQU   *
         IECRES LOAD,MODID=ID6J6L,BRANCH=QUEUED                        C
                                        XCTL TO NEXT MODULE      Y02080
*
OMG1700K DC    F'1'                     REG LOW ORDER BIT MASK
**********************************************************************
         EJECT
**********************************************************************
*
*
*
*        THE FOLLOWING ARE DISPLACEMENTS WITHIN THE DCB
*
*
*
*        OF FIELDS WHICH WILL BE MERGED TO THE DCB FROM THE JFCB
*
*
*
**********************************************************************
OMG1720K EQU   *
*                   ****ISAM SECTION OF MERGE TABLE FOLLOWS****
         DC    FL1'54'                  ISAM NTM
         DC    FL1'55'                  ISAM CYLOF
         DC    FL1'60'                  ISAM RKP
         DC    FL1'112'                 ISAM DUBFN
         DC    FL1'72'                  ISAM NCP
         DC    FL1'84'                  ISAM-QSAM EROPT
         DC    FL1'82'                  ISAM-QSAM LRECL
         DC    X'FF'                    ****DIVISION LINE*****
         DC    FL1'81'                  BDAM LIMCT
         DC    X'FF'                    ****DIVISION LINE****
*                   ****BSAM SECTION OF MERGE TABLE FOLLOWS****
         DC    FL1'72'                  BSAM NCP
         DC    X'FF'                    *****DIVISION LINE****
*                                       COMMON SECTION FOLLOWS
OMG1740K EQU   *
         DC    FL1'62'                  BLOCK SIZE
OMG1760K EQU   *
         DC    FL1'52'                  OPTCOD
         DC    FL1'36'                  RECFM
         DC    FL1'18'                  DEN
OMG1780K EQU   *
         DC    FL1'32'                  BFTEK OR BFALN
         DC    FL1'24'                  BUFL
         DC    FL1'20'                  BUFNO
         DC    X'00'                    ****END OF TABLE****
**********************************************************************
         EJECT
**********************************************************************
*
*
*
*        THE FOLLOWING ARE THE DISPLACEMENTS OF FIELDS IN THE
*
*
*
*        JFCB WHICH WILL BE MERGED INTO THE DCB
*
*
*
**********************************************************************
OMG1800K EQU   *
*                   ****ISAM SECTION OF MERGE TABLE FOLLOWS****
         DC    FL1'107'                 ISAM NTM OFFSET
         DC    FL1'110'                 ISAM CYLOF OFFSET
         DC    FL1'108'                 ISAM RKP OFFSET
         DC    FL1'111'                 ISAM DUBFN OFFSET
         DC    FL1'106'                 ISAM NCP OFFSET
         DC    FL1'92'                  ISAM-QSAM EROPT OFFSET
         DC    FL1'104'                 ISAM-QSAM LRECL OFFSET
         DC    X'02'                    MASK SHIFT FACTOR AFTER ISAM
*                   ****BDAM SECTION OF MERGE TABLE FOLLOWS****
         DC    FL1'95'                  BDAM LIMCT OFFSET
         DC    X'01'                    MASK SHIFT FACTOR AFTER DAM
*                   ****BSAM SECTION OF MERGE TABLE FOLLOWS****
         DC    FL1'106'                 BSAM NCP OFFSSET
         DC    X'00'                    MASK SHIFT FACTOR AFTER BSAM
OMG1820K EQU   *
         DC    FL1'102'                 BLOCKSIZE OFFSET
         DC    FL1'101'                 OPTCOD OFFSET
         DC    FL1'100'                 RECFM OFFSET
         DC    FL1'94'                  DEN OFFSET
         DC    FL1'89'                  BFTEK-BALN OFFSET
         DC    FL1'90'                  BUFL OFFSET
         DC    FL1'88'                  BUFNO OFFSET
         DC    X'00'                    END OF TABLE**********
**********************************************************************
         EJECT
**********************************************************************
*
*
*
*        THE FOLLOWING ARE THE LENGTHS-1 OF THE FIELDS
*
*
*
*        WHICH ARE MERGED FROM THE JFCB TO THE DCB
*
*
*
**********************************************************************
OMG1840K EQU   *
*                   ****ISAM SECTION OF MERGE TABLE FOLLOWS****
         DC    FL1'0'                   ISAM NTM LGH
         DC    FL1'0'                   ISAM CYLOF LGH
         DC    FL1'1'                   ISAM RKP LGH
         DC    FL1'0'                   ISAM DUBFN LGH
         DC    FL1'0'                   ISAM NCP LGH
         DC    FL1'0'                   ISAM-QSAM EROPT LGH
         DC    FL1'1'                   ISAM-QSAM LRECL LGH
         DC    X'FF'                    ****DIVISION LINE****
*                   ****BDAM SECTION OF MERGE TABLE FOLLOWS****
         DC    FL1'2'                   BDAM LIMCT LGH
         DC    X'FF'                    ****DIVISION LINE****
*                   ****BSAM SECTION OF MERGE TABLE FOLLOWS****
         DC    FL1'0'                   BSAM NCP LGH
         DC    X'FF'                    ****DIVISION LINE*******
OMG1860K EQU   *
         DC    FL1'1'                   BLOCK SIZE LENGTH
         DC    FL1'0'                   OPTCOD LGH
         DC    FL1'0'                   RECFM LGH
         DC    FL1'0'                   DEN LGH
         DC    FL1'0'                   BFTEK-BALN LGH
         DC    FL1'1'                   BUFL LGH
         DC    FL1'0'                   BUFNO LGH
         DC    X'00'                    ****END OF TABLE****
***********************************************************************
         EJECT
*
         XCTLTABL ID=(ID6J0P,0P,ID6J6L,6L,ID6J6K,6K),            Y02080X
               BRT=YES,LENGTH=                                   Y02080
*
         IECDSECS EXPAND=YES            EXPAND DESIRED DSECTS HERE
*
         END
