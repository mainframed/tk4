         TITLE 'IFG0195C/OPEN - UNLABELED TAPE POSITIONING'
IFG0195C CSECT
***********************************************************************
*                                                                     *
*                                                                     *
*        VS2 RELEASE 040 DELETIONS/CHANGES                            *
*0000298000,486000                                             @ZA02547
*        VS2 RELEASE 020 DELETIONS/CHANGES                            *
*0000                                                           ZA01328
*                                                                     *
*                                                                     *
* MODULE NAME = IFG0195C (OS/VS2)                                     *
*                                                                     *
* DESCRIPTIVE NAME =  UNLABELED TAPE POSITIONING                      *
*                                                                     *
* COPYRIGHT = NONE                                                    *
*                                                                     *
* STATUS = RELEASE 2, LEVEL 0                                         *
*                                                                     *
* FUNCTION =                                                          *
*    1. ZERO THE DCB BLOCK COUNT.                                     *
*                                                                     *
*    2. VERIFY THAT THE UCB FILE SEQUENCE NUMBER IS NOT ZERO. IF ZERO,*
*       SET IT TO ONE.                                                *
*                                                                     *
*    3. POSITION THE TAPE TO THE DESIRED DATA SET BY COMPARING THE    *
*       SEQUENCE NUMBER OF THE DESIRED DATA SET, IN THE JFCB, AGAINST *
*       THE CURRENT SEQUENCE NUMBER, IN THE UCB. THE TAPE IS MOVED    *
*       FORWARD OR BACKWARD AS NEEDED TO REACH THE DESIRED DATA SET.  *
*                                                                     *
* NOTES = SEE BELOW                                                   *
*                                                                     *
*    DEPENDENCIES =                                                   *
*            CLASS ONE CHARACTER CODE.  THE EBCDIC CHARACTER CODE     *
*            WAS USED FOR ASSEMBLY.  THE MODULE MUST BE REASSEMBLED   *
*            IF A DIFFERENT CHARACTER SET IS USED FOR EXECUTION.      *
*                                                                     *
*    RESTRICTIONS = NONE                                              *
*                                                                     *
*    REGISTER CONVENTIONS =                                           *
*            R2 POINTS TO DCB                                         *
*            R4 POINTS TO OPEN WORK AREA                              *
*            R5 POINTS TO THE RESIDENT ROUTINE                        *
*            R6 POINTS TO THE WTG TABLE                               *
*            R7 POINTS TO THE CURRENT PARAMETER LIST ENTRY            *
*            R8 POINTS TO THE CURRENT WTG TABLE ENTRY                 *
*            R9 POINTS TO THE DD ENTRY IN THE TIOT                    *
*            R10 POINTS TO THE UCB                                    *
*                                                                     *
*    PATCH LABEL = SEE NEXT TO LAST LABEL BEFORE ORG STATEMENT AT END *
*                  OF LISTING.                                        *
*                                                                     *
* MODULE TYPE = CONTROL (OPEN, CLOSE, EOV DATA MANAGEMENT)            *
*                                                                     *
*    PROCESSOR = ASSEMBLER XF                                         *
*                                                                     *
*    MODULE SIZE = SEE EXTERNAL SYMBOL DICTIONARY OR LOC FIELD ON     *
*                  ORG STATEMENT AT END OF LISTING                    *
*                                                                     *
*    ATTRIBUTES = REENTRANT, REFRESHABLE,READ-ONLY, ENABLED,          *
*                  PRIVILEGED, SUPERVISOR STATE, DATA MANAGEMENT KEY, *
*                  LINK PACK AREA RESIDENT/PAGEABLE                   *
*                                                                     *
* ENTRY POINT = IFG0195C                                              *
*                                                                     *
*    PURPOSE = SEE FUNCTION                                           *
*                                                                     *
*    LINKAGE =                                                        *
*        FROM THE TAPE INITIALIZATION ROUTINES:                       *
*           IECRES BRANCH=QUEUED                                      *
*           THE START OF THE LINKAGE IS IN IFG0193A WHICH SETS UP     *
*           DXRETMOD AND DXRETCOD.                                    *
*                                                                     *
* INPUT = STANDARD                                                    *
*                                                                     *
* OUTPUT = THE TAPE IS POSITIONED TO THE REQUESTED FILE.              *
*                                                                     *
* EXIT-NORMAL = IFG0196J                                              *
*                                                                     *
* EXIT-ERROR =                                                        *
*        IFG0190P PROBLEM DETERMINATION                               *
*           9 - 613 ABEND - I/O ERROR POSITIONING TAPE                *
*           10 - A13 ABEND - BACKSPACED INTO LOAD POINT DUE TO        *
*                            A INCORRECT UCB FILE SEQ NO.             *
*                                                                     *
* EXTERNAL REFERENCES = SEE BELOW                                     *
*                                                                     *
*    ROUTINES = IFG019RA                                              *
*                                                                     *
*    DATA AREAS = NONE                                                *
*                                                                     *
*    CONTROL BLOCK = DCB, UCB, JFCB                                   *
*                                                                     *
* TABLES = STANDARD                                                   *
*                                                                     *
* MACROS = MODESET, IECRES, EXCP, XCTLTABL, IECDSECS, IECEQU,         *
*          DMABCOND                                                   *
*                                                                     *
* CHANGE ACTIVITY = NEW RELEASE (LEVEL 0)                             *
*                                                                     *
***********************************************************************
         EJECT
         BALR  RBASE,R0                 SET BASE REGISTER
         USING *,RBASE                  ESTABLISH ADDRESSABILITY
         USING FORCORE,RCORE            WORKAREA ADDRESSABILITY
         USING WTG,RWTG                 WTG TABLE ADDRESSABILITY Y02080
         USING IHADCB,RDCB              DCB ADDRESSABILITY
         USING TIOENTRY,RTIOT           TIOT ADDRESSABILITY
         USING UCBOB,RUCB               UCB ADDRESSABILITY
         SPACE 1
         TM    DCBMACRF,DCBMEXCP        IS IT EXCP
         BNO   OTA50100                 NO, BRANCH
         TM    DCBMACRF+K1,DEPEND       DOES DEVICE PORTION EXIST
         BZ    OTA50200                 NO, BRANCH
OTA50100 EQU   *                        NOT EXCP
         XC    DCBBLKCT,DCBBLKCT        ZERO OUT BLOCK COUNT IN DCB
OTA50200 EQU   *                        NO DEVICE POERTION
         CLC   UCBFSEQ,OP2KONE          IS THE FILE SEQ NUMBER IN SRT
         BH    OTA50400                 BR, GREATER THAN ONE   @ZA02547
         BE    OTA50300                 BR IF EQ ONE           @ZA02547
         MODESET EXTKEY=SUPR            UCB KEY                  Y02082
         MVI   UCBFSCT+K1,K1            SET FILE SEQ COUNT TO ONE
         MVI   UCBFSEQ+K1,K1            SET FILE SEQ NUMBER TO ONE
         MODESET EXTKEY=DATAMGT         DATA MANAGEMENT KEY      Y02082
OTA50300 EQU   *                      FILE SEQ NO. IN UCB EQ 1 @ZA02547
         TM    JFCBLTYP,JFCBLTM         LEADING TAPE MARK?     @ZA02547
         BZ    OTA50400                 BR IF NO               @ZA02547
         BAL   RD,OTA52600              BR TO BYPASS TAPE MARK @ZA02547
OTA50400 EQU   *                                               @ZA02547
         TM    0(RPARC),X'0E'           PRELIMINARY TO RDBACK TEST
         BNZ   OTA51500                 IF ANY BITS ON, NOT RDBACK
         TM    0(RPARC),DEBOPRBK        READ BACKWARD?
         BNO   OTA51500                 BR IF NO
         SPACE 1
*
*                   SET UP FOR READ BACKWARD AND DISP=MOD POSITIONING
         SPACE 1
OTA50600 EQU   *                        READ BACKWARD AND DISP = MOD
         LH    RC,JFCBFLSQ              JFCB FIL SEQ
         LA    RC,K1(K0,RC)             INCREMENT JFCB FIL SEQ BY ONE
         LH    RD,UCBFSEQ               GET CURRENT POSITION OF TAPE
         CR    RC,RD                    JFCB+1=SRT
         BE    OTA51100                 BRANCH IF THEY ARE EQUAL
         BH    OTA51200                 BRANCH IF JFCB HIGH
         SPACE 1
*        THE FOLLOWING CODING WILL HANDLE THE CASE WHERE JFCB FIL
*        SEQ CTR +1 IS LESS THAN SRT FIL SEQ CTR
         SPACE 1
         BCTR  RC,0                     DECREMENT COUNT BY ONE
         SR    RD,RC                    SUBTRACT JFCB CTR FROM SRT CTR
OTA50700 EQU   *                        SET UP FOR BSF
         MVC   DXCCW1,BSFCCW            SET UP FOR BSF
OTA50800 EQU   *                        TEST LOOP COMPLETION
         BAL   RB,OTA52900              GO EXECUTE BACKSPACE
         BCT   RD,OTA50800              TEST LOOP COMPLETION
         MVI   DXCCW1,CCWNOP            SET UP CCW TO ISSUE NOP
         BAL   RB,OTA53000              GO EXECUTE A NOP
         TM    IOBSENSE+K1,UCBLDPT      IS TAPE AT LOAD POINT
         BO    OTA52700                 YES,ERROR UNABLE TO POSITION
OTA50900 EQU   *                        UPDATE SEQ COUNT AND NO.
         MODESET EXTKEY=SUPR            UCB KEY                  Y02082
         MVC   UCBFSEQ,JFCBFLSQ         SET SEQ NUM IN SRT FROM JFCB
         MVC   UCBFSCT,OP2KONE          SET FILE SEQ. COUNT TO ONE
         MODESET EXTKEY=DATAMGT         DATA MANAGEMENT KEY      Y02082
         CLI   JFCBNVOL,K1              IS THIS MULTI REEL
         BH    OTA52500                 BRANCH IF YES
         MODESET EXTKEY=SUPR            UCB KEY                  Y02082
         MVC   UCBFSCT,JFCBFLSQ         SET FILE SEQ COUNT IN SRT
         MODESET EXTKEY=DATAMGT         DATA MANAGEMENT KEY      Y02082
         B     OTA52500                 GO TO MERGE 196J
         SPACE 1
*                   POSITION LAST VOLUME OF MULTI-REEL READ BACK D.S.
         SPACE 1
OTA51000 EQU   *                        LAST VOL OF MULT-REEL READ BACK
         MVC   DXCCW1,FSFCCW            SET UP FORWARD SPACE FILE
         BAL   RB,OTA52900              POSITION IN BACK OF DATA SET
         MVI   DXCCW1,CCWBSF            SET UP CCW TO BACKSPACE FILE
         BAL   RB,OTA52900              POSITION TO LAST RECORD
         MODESET EXTKEY=SUPR            UCB KEY                  Y02082
         MVC   UCBFSEQ,JFCBFLSQ         SET LOGICAL FILE SEQUENCE NUM
         MODESET EXTKEY=DATAMGT         DATA MANAGEMENT KEY      Y02082
         B     OTA52500                 POSITIONING COMPLETE
*
OTA51100 EQU   *                        MULTIREEL
         LA    RD,K1                    SET LOOP COUNT=1
         B     OTA50700                 GO EXECUTE BSF
         SPACE 1
*        THE FOLLOWING CODING WILL HANDLE JFCB FIL SEQ CTR+1 GREATER
*        THAN SRT FIL SEQ CTR
         SPACE 1
OTA51200 EQU   *                        JFCB FILE SEQ GREATER THAN UCB
         CLI   JFCBNVOL,K1              IS THIS MULTI REEL
         BH    OTA51000                 BRANCH IF YES
         BCTR  RC,0                     DECREMENT COUNT BY ONE
         SR    RC,RD                    SUBTRACT SRT CTR FROM JFCB CTR
         LTR   RD,RD                    IS SRT COUNT=0
         BZ    OTA51300                 YES, BRANCH
         LA    RC,K1(K0,RC)             ADD ONE TO LOOP COUNT
OTA51300 EQU   *                        UCB COUNT = 0
         MVC   DXCCW1,FSFCCW            SET UP FOR FSF
OTA51400 EQU   *                        TEST LOOP COMPLETION
         BAL   RB,OTA52900              GO EXECUTE FSF
         BCT   RC,OTA51400              TEST LOOP COMPLETION
         MVI   DXCCW1,CCWBSF            SET UP CCW TO BACKSPACE FILE
         BAL   RB,OTA52900              GO EXECUTE BSF
         B     OTA50900                 GO UPDATE SEQ COUNT AND NUMBER
*
OTA51500 EQU   *                        NOT READ BACKWARD
         TM    0(RPARC),X'04'           TEST FOR INPUT
         BZ    OTA51600                 YES, BYPASS MOD
         TM    JFCBIND2,JFCOLD          CK FOR MOD
         BZ    OTA50600                 BR IF YES
OTA51600 EQU   *                        INPUT, BYPASS MOD
         MVC   DXCCW1,FSFCCW            SET FORWARD SPACE FILE
OTA51700 EQU   *                        DETERMINE POSITIONING REQUIRED
         CLC   UCBFSEQ,JFCBFLSQ         DETERMINE POSITIONING REQUIRED
         BH    OTA51900                 BRANCH IF SRT HI
         BE    OTA51800                 BR IF EQ               @ZA02547
         SPACE 1
*        THIS CODE WILL HANDLE THE CASE OF JFCB FILE SEQUENCE
*        NUMBER HIGHER THAN SRT FILE SEQUENCE NUMBER
         SPACE 1
         L     RD,UCBFSCT               GET FILE SEQ COUNT AND NUMBER
         A     RD,ANUM1                 ADD 1 TO SEQ COUNT AND NUMBER
         MODESET EXTKEY=SUPR            UCB KEY                  Y02082
         ST    RD,UCBFSCT               STORE UPDATED SEQ CNT & NO
         MODESET EXTKEY=DATAMGT         DATA MANAGEMENT KEY      Y02082
         BAL   RB,OTA52900              GO EXECUTE FSF
         B     OTA51700                 BRANCH BACK IF COMPLETED
OTA51800 EQU   *                                               @ZA02547
         TM    JFCBLTYP,JFCBLTM        LEADING TAPE MARK?      @ZA02547
         BZ    OTA52500                NO, BRANCH-DONE         @ZA02547
         CLC   JFCBFLSQ,OP2KONE        JFCB FILE SEQ EQ 1?     @ZA02547
         BNE   OTA52500                NO, BRANCH-DONE         @ZA02547
         TM    K0(RPARC),DEBOPOIN      OUTPUT OR OUTIN?        @ZA02547
         BNO   OTA52500                NO, BRANCH-DONE         @ZA02547
         LA    RB,OTA52500             SET RETURN ADDR FROM REW@ZA02547
         B     OTA52000                BR TO REWIND            @ZA02547
         SPACE 1
*        THE FOLLOWING CODE WILL POSITION A TAPE IF SRT FILE SEQUENCE
*        IS GREATER THAN JFCB FILE SEQUENCE
         SPACE 1
OTA51900 EQU   *                        POSITION FOR UCB REATER JFCB
         LA    RB,K1
         CH    RB,JFCBFLSQ              CK FOR FIL SEQ 1 IN JFCB
         BNE   OTA52200                 BR IF NO
         LA    RB,OTA52100              SET RETURN FROM REW
OTA52000 EQU   *                        REWIND
         MVC   DXCCW1,REWCCWC           SET CCW FOR REWIND
         MODESET EXTKEY=SUPR            UCB KEY                  Y02082
         MVC   UCBFSEQ,JFCBFLSQ         SET FIL SEQ TO 1 IN SRT
         MVC   UCBFSCT,JFCBFLSQ
         MODESET EXTKEY=DATAMGT         DATA MANAGEMENT KEY      Y02082
         B     OTA52900                 GO REWIND
OTA52100 EQU   *                        RETURN FROM REWIND
         TM    JFCBLTYP,JFCBLTM         TEST LEADING TAPE MARK BIT
         BZ    OTA52500                 BRANCH IF NOT SET
         TM    K0(RPARC),DEBOPOIN       OUTPUT OR OUTIN
         BO    OTA52500                 YES, BRANCH
         LA    RD,OTA52500              SET RETURN FROM BYPASS RTN
         B     OTA52600                 GO BYPASS LEADING TAPE MARK
OTA52200 EQU   *                        JFCB FILE SEQ NOT ONE
         MVI   DXCCW1,CCWBSF            BACKSPACE FILE CCW
         BAL   RB,OTA52900              BRANCH TO EXCP*****
OTA52300 EQU   *                        BACKSPACE
         L     RD,UCBFSCT               GET FILE SEQ COUNT AND NUMBER
         S     RD,ANUM1                 DECREMENT SEQ COUNT AND NUMBER
         MODESET EXTKEY=SUPR            UCB KEY                  Y02082
         ST    RD,UCBFSCT               STORE FILE SEQ CNT & NO
         MODESET EXTKEY=DATAMGT         DATA MANAGEMENT KEY      Y02082
         MVI   DXCCW1,CCWBSF            BACKSPACE FILE CCW
         BAL   RB,OTA52900              GO EXECUTE BSF
         MVI   DXCCW1,CCWNOP            SET CCW TO ISSUE NOP
         CLC   UCBFSEQ,JFCBFLSQ         WAS THIS THE LAST BACKSPACE
         BE    OTA52400                 YES,GO FORWARD SPACE IF NEEDED
         BAL   RB,OTA53000              SEE IF TAPE IS AT LOAD POINT
         TM    IOBSENSE+K1,UCBLDPT      IS THE TAPE AT LOAD POINT
         BNO   OTA52300                 NO, PERFORM ANOTHER BACKSPACE
         B     OTA52700                 YES,ERROR - UNABLE TO POSITION
OTA52400 EQU   *                        FORWARD SPACE
         BAL   RB,OTA53000              SEE IF TAPE IS AT LOAD POINT
         TM    IOBSENSE+K1,UCBLDPT      TAPE AT LOAD POINT
         BO    OTA52500                 YES, BRANCH
         MVI   DXCCW1,CCWFSF            FORWARD SPACE FILE CCW
         BAL   RB,OTA52900              GO EXECUTE FSF
         SPACE
*                                                                Y02080
*        GO SEE IF THIS IS THE LAST DCB IN OPEN LIST             Y02080
*                                                                Y02080
OTA52500 EQU   *                        BRANCH QUEUED TO IFG0196J
         IECRES LOAD,MODID=MERGE,BRANCH=QUEUED                   Y02080
         SPACE 1
*  BYPASS ROUTINE - READ ONE RECORD. TEST TO SEE IF TAPE MARK WAS
*                    READ, IF SO, TAPE IS IN PROPER POSITION.
*                    IF TAPE MARK WAS NOT READ, BACKSPACE ONE RECORD.
*
OTA52600 EQU   *                        BYPASS ROUTINE
         MVC   DXCCW1,BSFCCW            MOVE IN A CCW
         LA    RB,DXLBL                 GET READ ADDRESS
         ST    RB,DXCCW1                STORE IN CCW
         MVI   DXCCW1,CCWRDTAP          MOVE IN READ OP
         MVI   DXCCW1+K7,K12            SET READ LENGTH TO 12-  ZA01328
*                                       THE MINIMUM READ LENGTH ZA01328
         BAL   RB,OTA53000              GO READ A RECORD
         TM    IOBSTAT0,CSWUNITX        WAS A TAPE MARK READ
         BCR   1,RD                     YES, RETURN
         MVC   DXCCW1,BSFCCW            MOVE IN A CCW           ZA01328
         MVI   DXCCW1,CCWBSR            MOVE IN BACKSPACE RECORD
         BAL   RB,OTA52900              GO EXECUTE BACKSPACE
         BR    RD                       RETURN
*
         SPACE 1
*                   ERROR - SPECIFIED FILE SEQUENCE NUMBER NOT ON TAPE
         SPACE 1
OTA52700 EQU   *                        FILE SEQ NO. NOT ON TAPE
         LA    R0,K10                   INT CODE - BACKSPACE TO LD PNT
OTA52800 EQU   *                        GO TO PROBLEM DETERM
         DMABCOND (0),PROBDET
*
OTA52900 EQU   *                        BSP
         EXCP  DXIOB                    INITIATE I/O OPERATION
         IECRES WAIT
         TM    DXECB,ECBNOERR           CK FOR PERMANET I/O ERROR
         BCR   1,RB                     RETURN IF NO ERROR
OTA52950 LA    R0,K9                    INT CODE - POSITIONING ERROR
         B     OTA52800                 GO TO PROBLEM DETERMINATION
*                                  CLOSE - CALL ABNORMAL END OF TASK
OTA53000 EQU   *                        EXEC EXCP
         EXCP  DXIOB                    ISSUE I/O OPERATION
         IECRES WAIT
         BR    RB                       RETURN
         EJECT
*                   CONSTANTS
         SPACE 1
BSFCCW   DC    X'2F00000020000001'
FSFCCW   DC    X'3F00000020000001'
REWCCWC  DC    X'0700000020000001'
OP2KONE  DC    X'0001'                  CONSTANT ONE
         DS    0F
ANUM1    DC    X'00010001'              MASK TO UPDATE SEQ CNT AND NO
         EJECT
         SPACE 1
         EJECT
*
XCTLTABL XCTLTABL ID=(MERGE,6J,PROBDET,0P,ID5C5C,5C),            Y02083X
               BRT=YES,LENGTH=                                   Y02080
*
         IECDSECS TIOT,DCB,UCB,CVT,MAIN,WTG,PREFX,EXPAND=YES     Y02083
         IECEQU
         END
