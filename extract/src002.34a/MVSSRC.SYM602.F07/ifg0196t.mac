         TITLE 'IFG0196T/OPEN - OUTPUT TAPE HEADER LABEL PREPARATION'
IFG0196T CSECT
*                                                                     *
*        VS2 SU32 DELETIONS
*                                                              @G32DSMI
*        VS2 RELEASE 04 CHANGES/DELETIONS
*381957-382392,493500,493600                                   @ZA02210
*0000182000-188000                                             @ZA09620
*0000                                                          @ZA08326
*0000                                                          @ZA13576
*0000                                                          @ZA19724
*A408500,409500                                                @ZA26229
*C381957,382286                                                @ZA26229
* MODULE NAME = IFG0196T (OS/VS2)                                     *
*                                                                     *
* DESCRIPTIVE NAME = TAPE OUTPUT HEADER LABEL PROPARATION             *
*                                                                     *
* COPYRIGHT = NONE                                                    *
*                                                                     *
* STATUS = RELEASE 2, LEVEL 0                                         *
*                                                                     *
* FUNCTION =                                                          *
*        1. WAIT FOR VOLUME LABEL REWRITE INITIATED IN PREVIOUS MOD.  *
*                                                                     *
*        2. CONSTRUCT FILE LABEL 1 USING INFORMATION FROM THE JFCB    *
*        AND UCB AND WRITE IT OUT.                                    *
*                                                                     *
*        3. CONSTRUCT FILE LABEL 2 USING INFORMATION FROM THE JFCB    *
*        AND TIOT AND WRITE IT OUT. BEFORE WRITING OUT HEADER LABEL   *
*        TWO INFORMATION, CHECK FOR CHECKPOINT DATA SET.  IF IT IS    *
*        A CHECKPOINT DATA SET, TEST IF IT IS SECURE OR CAN BE MADE   *
*        SECURE.  TURN ON CHECKPOINT DATA SET AND VOLUME INDICATORS   *
*        IF THE VOLUME CAN BE MADE SECURE, OR SET THE CHECKPOINT      *
*        DATA SET INDICATOR IF IT IS ALREADY SECURE.                  *
*                                                                     *
*        1B. FOR A NO-WAIT ENTRY, IF PROTECT WAS SPECIFIED,           *
*        4. IF USER LABELS ARE TO BE PROCESSED, TRANSFER CONTROL TO   *
*        THE OUTPUT USER LABEL MODULE (IFG0196U).OTHERWISE, WRITE     *
*        TAPE MARK FOLLOWING THE HEADER LABEL.                        *
*                                                                     *
*        5. IF AN I/O ERROR OCCURS DURING THE WRITING OF THE LABELS,  *
*        AND THE DATA SET IS THE FIRST ON THE VOLUME, AN ATTEMPT TO   *
*        RECOVER IS MADE WHENEVER POSSIBLE.                           *
*                                                                     *
* NOTES = SEE BELOW                                                   *
*                                                                     *
*    DEPENDENCIES =                                                   *
*            CLASS ONE CHARACTER CODE.  THE EBCDIC CHARACTER CODE     *
*            WAS USED FOR ASSEMBLY.  THE MODULE MUST BE REASSEMBLED   *
*            IF A DIFFERENT CHARACTER SET IS USED FOR EXECUTION.      *
*                                                                     *
*    RESTRICTIONS = NONE                                              *
*                                                                     *
*    REGISTER CONVENTIONS =                                           *
*            R2 POINTS TO DCB                                         *
*            R4 POINTS TO OPEN WORK AREA                              *
*            R5 POINTS TO THE RESIDENT ROUTINE                        *
*            R6 POINTS TO THE WTG TABLE                               *
*            R7 POINTS TO THE CURRENT PARAMETER LIST ENTRY            *
*            R8 POINTS TO THE CURRENT WTG TABLE ENTRY                 *
*            R9 POINTS TO THE DD ENTRY IN THE TIOT                    *
*            R10 POINTS TO THE UCB                                    *
*                                                                     *
*    PATCH LABEL = SEE NEXT TO LAST LABEL BEFORE ORG STATEMENT AT END *
*                  OF LISTING.                                        *
*                                                                     *
* MODULE TYPE = CONTROL (OPEN, CLOSE, EOV DATA MANAGEMENT)            *
*                                                                     *
*    PROCESSOR = ASSEMBLER XF                                         *
*                                                                     *
*    MODULE SIZE = SEE EXTERNAL SYMBOL DICTIONARY OR LOC FIELD ON     *
*                  ORG STATEMENT AT END OF LISTING                    *
*                                                                     *
*    ATTRIBUTES = REENTRANT, REFRESHABLE,READ-ONLY, ENABLED,          *
*                 PRIVILEGED, SUPERVISOR STATE, DATA MANAGEMENT KEY,  *
*                 LINK PACK AREA RESIDENT/PAGEABLE                    *
*                                                                     *
* ENTRY POINT = IFG0196T FROM IFG0196Q TAPE OUTPUT DATA CHECK AND     *
*                                       REWRITE VOLUME LABEL OR       *
*                             IFG0196N TAPE OUTPUT SECURITY           *
*                                                                     *
*    PURPOSE = SEE FUNCTION                                           *
*                                                                     *
*    LINKAGE =                                                        *
*        FROM IFG0196Q:                                               *
*          LA    RET,K0  OR K4                                        *
*          IECRES LOAD,MODID=ID6Q6T,BRCODE=(14),BRANCH=QUEUED         *
*                                                                     *
*        FROM IFG0196N:                                               *
*          SR    RET,RET                                              *
*          LA    RF,ID6N6T                                            *
*          IECRES LOAD,MODID=(RF),BRCODE=(RET),BRANCH=QUEUED          *
*                                                                     *
* INPUT = TAPE POSITIONED FOR LABEL WRITING                           *
*                                                                     *
* OUTPUT = TAPE HEADER LABELS WRITTEN                                 *
*                                                                     *
* EXIT-NORMAL = IFG0196U - TAPE OUTPUT USER LABEL MODULE              *
*               IFG0194A - VOLUME/UNIT SELECTION                      *
*               IFG0194J - MESSAGE MODULE                             *
*                                                                     *
* EXIT-ERROR = NONE                                                   *
*                                                                     *
* EXTERNAL REFERENCES = SEE BELOW                                     *
*                                                                     *
*    ROUTINES = IFG019RA                                              *
*                                                                     *
*    DATA AREAS = NONE                                                *
*                                                                     *
*    CONTROL BLOCK = CVT, DCB, DSAB, JFCB, UCB                        *
*                                                                     *
* TABLES = STANDARD                                                   *
*                                                                     *
* MACROS = IECRES, MODESET, EXCP, XLATE, XCTLTABL, IECDSECS, IECEQU   *
*                                                                     *
* CHANGE ACTIVITY = NEW RELEASE (LEVEL 0)                             *
*                                                                     *
***********************************************************************
         EJECT
         SPACE 1
*                   ADDRESSABILITY
         SPACE 1
         BALR  RBASE,0                  LOAD BASE REGISTER
         USING *,RBASE
*
         USING FORCORE,RCORE
         USING WTG,RWTG                 WHERE-TO-GO TABLE        Y02080
         USING IHADCB,RDCB
         USING SRT,RUCB
         USING TIOENTRY,RTIOT
         USING CVT,RB
         SPACE 1
         B     OTA42900(RET)            GO TO PROPER ENTRY
OTA42900 EQU   *                        BRANCH TABLE
         B     OTA43100                 NO WAIT ENTRY
         B     OTA42950                 WAIT ENTRY             @G32DSMI
         B     OTA45230                 CHK PT SEC ENTRY         Y02083
*                                                              @G32DSMI
OTA42950 EQU   *                        NO WAIT ENTRY          @G32DSMI
*                                                              @G32DSMI
         BAL   RB,OTA46000              CHECK IF RACDEF NEEDED @G32DSMI
*
*        THE FOLLOWING WAIT CODE IS PART OF THE REWRITE VOLUME LABEL
*        FUNCTION. REFER TO THE PRECEEDING MODULE PROLOG FOR DETAILS.
*
OTA43000 EQU   *                        WAIT ENTRY
         LA    RC,K15                   613-10 ABEND ON I/O ERR  YM8520
         IECRES WAIT
         TM    DXECB,ECBNOERR           I/O ERROR
         BO    OTA43100                 NO, BRANCH
         TM    IOBSTAT0,CSWUNITX        UNIT EXCEPTION
         BNO   OTA45900                 NO, BRANCH
OTA43100 EQU   *                        NO WAIT ENTRY
*                                                                     *
         NI    JFCBMASK+K4,ALLBITS-DS1WRSEC RESET SECURITY IND   YM7530
*                                                                YM7530
         ST    RCORE,DXCCW1             RESET CCW1
         MVC   DXCCW1+4(4),ARDCCW6T     NO FLAG, 80 CHARACTERS
*                   CONSTRUCT HEADER LABEL 1
         SPACE 1
         MVC   FL1LABI(4),AHDR1P        SET IDENTIFIER TO HDR1
         SPACE 1
*                                                              @ZA09620
*      DETERMINE 17 LEAST SIGNIFICANT NON-BLANK                @ZA09620
*      CHARACTERS IN DATA SET NAME IN JFCB                     @ZA09620
*                                                              @ZA09620
         LA    RD,JFCBDSNM     STARTING ADDR OF JFCB TO REG    @ZA09620
         LA    RF,JFCBDSNM+27  GET ADDR OF FIRST POSSIBLE      @ZA09620
OTA43400 EQU   *               SIGNIFICANT CHARACTER           @ZA09620
         CLI   K16(RF),BLANK   IS CHARACTER BLANK              @ZA09620
         BNE   OTA43500        NO, BRANCH                      @ZA09620
         BCT   RF,OTA43400     DECR PTR AND BR TO TEST         @ZA09620
OTA43500 EQU   *               NEXT CHAR TO LEFT               @ZA09620
         CLR   RF,RD           IS LEFT POINTER WITHIN FIELD    @ZA09620
         LA    RD,L'FL1ID(,RF) POINT TO FIRST BLANK            @ZA09620
         BNL   OTA43600        BR TO BUILD DSNAME              @ZA09620
*                                                              @ZA09620
*      PLACE DSNAME FROM JFCB INTO FILE IDENTIFICATION         @ZA09620
*                                                              @ZA09620
         MVC   FL1ID,JFCBDSNM  INSERT FIRST 17 CHARACTERS      @ZA09620
         B     OTA43700        GO SET UP GENERATION NUMBER     @ZA09620
OTA43600 EQU   *                                               @ZA09620
         MVC   FL1ID,0(RF)     USE LAST 17 LEAST SIG           @ZA09620
*                                                              @ZA09620
*                   GENERATION NUMBER AND VERSION NUMBER
*                                                              @ZA09620
         SPACE 1
OTA43700 EQU *                                                 @ZA09620
         MVI   FL1GNO,BLANK             SET GEN NUMBER AND VER
         MVC   FL1GNO+1(44),FL1GNO      NUMBER OF GENERATION AND THE
*                                       REST OF THE LABEL TO BLANKS
         CLI   JFCBELNM+1,BLANK         GENERATION DATA SET
         BE    OTA43800                 NO,GO SET UP FILE SERIAL
         LA    RC,8                     LEN OF GEN&VER NUMBER  @ZA09620
         SLR   RD,RC                    POINT TO GEN NUMBER    @ZA09620
         MVC   FL1GNO,K1(RD)            MOVE IN GEN NUMBER     @ZA09620
         MVC   FL1VNG,K6(RD)            MOVE IN VERSION NUMBER @ZA09620
         SPACE 1
*                   FILE SERIAL,VOLUME SEQUENCE AND FILE SEQUENCE
         SPACE 1
OTA43800 MVC   FL1FILSR,UCBSQC          FILE SERIAL NUMBER
         LH    RB,JFCBVLSQ              GET VOLUME SEQUENCE NUMBER
         LTR   RB,RB                    IS IT ZERO
         BNZ   OTA43900                 NO,GO TO CONVERT IT
         LA    RB,1(RB)                 YES,INITIALIZE IT TO ONE
OTA43900 CVD   RB,DXCCW2                CONVERT TO PACKED DECIMAL
         UNPK  FL1VOLSQ,DXCCW2          VOLUME SEQ NUMBER TO EBCDIC
         OI    FL1VOLSQ+3,ZONEOF        REMOVE SIGN BITS
         LH    RB,JFCBFLSQ              GET FILE SEQUENCE NUMBER
         CVD   RB,DXCCW2                CONVERT TO PACKED DECIMAL
         UNPK  FL1FILSQ,DXCCW2          FILE SEQUENCE NUM TO LABEL
         OI    FL1FILSQ+3,ZONEOF        REMOVE SIGN BITS
         SPACE 1
*                   CREATION DATE
         SPACE 1
         SR    RB,RB                    CLEAR WORK REGISTER
         IC    RB,JFCBCRDT              GET YEAR OF CREATION
         CVD   RB,DXCCW2                CONVERT YEAR TO DECIMAL
         UNPK  FL1CREDT+1(K2),DXCCW2    PLACE YEAR IN EBCDIC INTO
         OI    FL1CREDT+2,ZONEOF        REMOVE SIGN BITS
         IC    RB,JFCBCRDT+1            PICK UP THE TWO CHARACTERS OF
         SLA   RB,8                     THE DAY OF CREATION
         IC    RB,JFCBCRDT+2            FROM THE JFCB
         CVD   RB,DXCCW2                CONVERT DAY TO DECIMAL
         UNPK  FL1CREDT+3(K3),DXCCW2    CREATION DAY IN EBCDIC TO
         OI    FL1CREDT+5,ZONEOF        RESET SIGN BITS
         SPACE 1
*                   EXPIRATION DATE
         SPACE 1
         SR    RB,RB                    CLEAR WORK REGISTER
         IC    RB,JFCBXPDT              PICK UP YEAR OF EXPIRATION
         CVD   RB,DXCCW2                CONVERT YEAR TO DECIMAL
         UNPK  FL1EXPDT+1(K2),DXCCW2    PLACE YEAR IN EBCDIC INTO
         OI    FL1EXPDT+2,ZONEOF        RESET SIGN BITS
         LH    RB,JFCBXPDT+1            PICK UP DAY OF EXPIRATION
         CVD   RB,DXCCW2                CONVERT DAY TO DECIMAL
         UNPK  FL1EXPDT+3(K3),DXCCW2    EXPIRATION DAY IN EBCDIC TO
         OI    FL1EXPDT+5,ZONEOF        RESET SIGN BITS
         SPACE 1
*                   BLOCK COUNT
         SPACE 1
         MVI   FL1FSEC,CHAR0            RESET SECURITY INDICATOR
         MVC   FL1BLKCT(6),FL1FSEC      EBCDEC ZERO BLOCK COUNT
         SPACE 1
*                   DATA SET SECURITY
         SPACE 1
         TM    JFCBIND2,JFCBSCTY        DOES JFCB SPECIFY SECURITY
         BNO   OTA44100                 NO GO CHECK ASCII
         MVI   FL1FSEC,FL1SECTY         YES,SET DATA SET SECURITY IND
         TM    JFCBIND2,JFCBRWPW        DOES JFCB SPECIFY RWPW OPTION
         BNO   OTA44100                 NO, DO NOT SET INDICATOR
         MVI   FL1FSEC,FL1WRSEC         IND SECTY IS FOR WR ONLY
         SPACE 1
*                   SYSTEM CODE AND RESERVED FIELDS
OTA44100 EQU   *                        TEST SYSTEM CODE, RESERVED FLD
         TM    JFCBLTYP,JFCBAL          ANSI LABEL SPECIFIED
         BZ    OTA44300                 NO, GO SET SYSTEM CODE
         MVC   FL1SYSCD(K5),ANSICD      MOVE SYSTEM CODE TO LABEL
         TM    JFCBIND2,JFCBSCTY        IS SECURITY INDICATED
         BO    OTA44200                 YES, GO TRANSLATE LABEL
         MVI   FL1FSEC,BLANK            NO, SET BYTE TO BLANK
OTA44200 EQU   *                        TRANSLATE LABEL
         BAL   RC,OTA45850              GO PERFORM TRANSLATE
         B     OTA44400                 BRANCH TO SET CHANNEL PROGRAM
         SPACE 1
OTA44300 MVC   FL1SYSCD,VSSYSC          INSERT SYSTEM CODE       XM1056
*                                       INTO HEADER
         SPACE 1
*                   SET UP CHANNEL PROGRAM TO WRITE LABEL
OTA44400 EQU   *                        BUILD WRITE LABEL CHAN PROG
         SPACE 1
         MVI   DXCCW1,CCWWRTAP          DITTO FOR WRITE OPERATION CODE
         MVI   DXCCW1+4,CCWSILI         SET SILI FLAG ON
         SPACE 1
*                   WRITE HEADER LABEL 1
         LA    RC,K15                   INT CODE - I/O ERROR WRITE LBL
         BAL   RB,OTA45800              GO TO PERFORM WRITE OPERATION
         BO    OTA44500                 BRANCH IF NO I/O ERROR
         TM    IOBSTAT0,CSWUNITX        UNIT EXCEPTION CAUSE ERROR
         BZ    OTA45900                 NO, BRANCH
OTA44500 EQU   *                        NO I/O ERROR
*
*                   SET UP TO WRITE HEADER LABEL 2
*
         MVC   FL1LABI,AHDR1P           MOVE IN HDR
         MVI   FL1NO,CHAR2              SET FILE LABEL IDENTIFIER TO 2
         MVI   FL2TRTCH,BLANK           SET PART OF LABEL TO
         MVC   FL2TRTCH+1(K45),FL2TRTCH  BLANKS - 47 CHARACTERS
         SPACE 1
*                  BLOCKING ATTRIBUTE
         SPACE 1
         SR    RB,RB                    CLEAR WORK REGISTER
         IC    RB,JFCRECFM              PICK UP RECORD FORMAT
         SRL   RB,3                     BITS 0,1 CONTAIN BLK ATTRIB
         STC   RB,FL2BLKA               TO PREPARE FOR TRANSLATE, ZERO
         NI    FL2BLKA,X'03'            ALL BUT BITS 0,1
         TR    FL2BLKA,ABLKA            TRANSLATE TO S, B, R OR BLANK
*
*                  RECORD FORMAT
*
         SRL   RB,3                     BITS 0,1 CONTAIN RECORD FORMAT
         STC   RB,FL2RECFM              F=BIT 0  V=BIT 1  U=BITS 0,1
*   DEFAULT TO RECFM = D IF NEITHER BIT IS ON
         TR    FL2RECFM,ARECFM          TRANSLATE TO  V,F,D OR U
         SPACE 1
*                   DENSITY
         SPACE 1
         MVI   FL2DEN,K4                SET DEN=4                 99223
         CLI   JFCDEN,DCBD6250          6250 SPECIFIED            99223
         BE    OTA44550                 YES,SET ZONEBITS          99223
         IC    RB,JFCDEN                PICK UP DENSITY FROM THE JFCB
         SRL   RB,6                     BITS 0 AND 1 CONTAIN DENSITY
         STC   RB,FL2DEN                00=LOW 01=MEDIUM 02=HIGH
OTA44550 EQU   *                        SET ZONE BITS
         OI    FL2DEN,DENSET            SET DENSITY TO EBCDIC 0,1 OR 2
         SPACE 1
*                   TAPE RECORDING TECHNIQUE
         SPACE 1
         IC    RB,JFCTRTCH              PICK UP TAPE RECORDING TECH
         SRL   RB,4                     13=C ,23=E,2B=ET,3B=T,O=BLANK
         STC   RB,FL2TRTCH              PLACE FIRST CHAR OF TRTCH INTO
         TR    FL2TRTCH(1),ATRTCH       THE LABEL AS BLANK,C,E OR T
         CLI   JFCTRTCH,JFCTREV         IS IT EVEN PARITY AND TRA
         BC    6,OTA44600               NO,THE SECOND CHAR S/B BLANK
         MVI   FL2TRTCH+1,CHART         SET SECOND CHAR TO TRANSLATE
         SPACE 1
*                   BLOCK SIZE AND LOGICAL RECORD LENGTH
         SPACE 1
OTA44600 LH    RB,JFCBLKSI              PICK UP BLOCK LENGTH
         CVD   RB,DXCCW2                CONVERT BLOCK LENGTH TO D
         UNPK  FL2BLKL,DXCCW2           BLOCK LENGTH IN EBCDIC TO
         OI    FL2BLKL+4,ZONEOF         RESET SIGN BITS
         LH    RB,JFCLRECL              PICK UP LOGICAL RECORD LENGTH
         CH    RB,AGRTR32K              IS LRECL INDIC GRTR 32K
         BE    OTA44700                 BR TO SET LABEL TO 9'S
         CVD   RB,DXCCW2                CONVERT TO DECIMAL
         UNPK  FL2LRECL,DXCCW2          LOGICAL RECORD LEN TO LABEL
         OI    FL2LRECL+4,ZONEOF        RESET SIGN BITS
         B     OTA44800                 BR TO CONTINUE MERGE
OTA44700 EQU   *                        SET LRECL TO 9'S
         MVI   FL2LRECL,X'F9'           SET LRECL TO 9'S
         MVC   FL2LRECL+1(K4),FL2LRECL  INDICATING GRTR 32K
OTA44800 EQU   *                        CONTINUE MERGE
         SPACE 1
*                   FILE POSITION AND JOB/STEP IDENTIFICATION
         SPACE 1
         L     RB,CVTPTR                GET CVT ADDRESS
         L     RB,CVTTCBP-CVT(RB)       GET TCB POINTERS
         L     RB,K4(RB)                GET TCB ADDRESS
         L     RB,TCBTIO-TCB(RB)        GET TIOT ADDRESS
         SPACE
OTA44900 MVI   FL2FILP,C'0'             SET FILE POSITION TO ZERO
         MVC   FL2JOBD,TIOCNJOB-TIOT(RB)  MOVE IN JOB NAME
         MVI   FL2JSSP,SLASH            (INSERT SLASH)
         MVC   FL2STEPD,TIOCSTEP-TIOT(RB)  MOVE IN STEP NAME
         SPACE 1
*                   CARRIAGE CONTROL CHARACTER SET
         SPACE 1
         TM    JFCRECFM,JFCMAC          JFCB SPECIFY MACHINE CONTROL
         BC    12,OTA45000              NO,GO SEE IF ASA CONTROL CHAR
         MVI   FL2CNTRL,CHARM           SET MACHINE CODE CONTROL CHAR
OTA45000 TM    JFCRECFM,JFCASA          JFCB SPECIFY ASA CONTROL CHAR
         BNO   OTA45100                 NO, GO CHECK ASCII LABEL
         MVI   FL2CNTRL,CHARA           SET CONTROL CHARACTER TO ASA
         SPACE 1
*
OTA45100 EQU   *                        CHECK ASII LABEL
         TM    JFCBLTYP,JFCBAL          ANSI LABLE SPECIFIED
         BZ    OTA45200                 NO, CHECK FOR T.U. ID     99223
*   GET BUFFER OFFSET LENGTH FROM JFCB AND CONVERT IT FOR LABEL 2
         MVC   DXCCW2(K1),JFCBUFOF      GET BUFFER OFFSET LENGTH
         NI    DXCCW2,X'FF'-JFCBFOFL    TURN OFF BIT ZERO
         SR    RB,RB                    ZERO OUT REGISTER
         IC    RB,DXCCW2                GET BUFFER LENGTH IN REG
         CVD   RB,DXCCW2                CONVERT TO DECIMAL
         UNPK  FL2BUFOF,DXCCW2          PLACE IN LABEL AREA
         OI    FL2BUFOF+K1,ZONEOF       RESET SIGN BITS
         BAL   RC,OTA45850              GO PERFORM TRANSLATE
         B     OTA45210                 GO TEST FOR CHKPT        Y02083
OTA45200 EQU   *                        CHECK FOR T.U. ID
         CLI   UCBTBYT4,UCB3400         TEST FOR 3400 DRIVE      YM1491
         BNE   OTA45210                 NO, GO TEST FOR CHKPT    YM1491
         L     RC,UCBEXTPT              GET PT TO NEW UCB BLK    Y02146
         CLI   UCBSNSCT-UCBCMEXT(RC),K24 24 SENSE BYTES          Y02146
         BNE   OTA45210                 NO, GO TEST FOR CHKPT    Y02083
         L     RC,UCBXTN                LOAD UCB EXT ADDRS        99223
         USING UCBMT,RC                                           99223
         LH    RB,UCBCTD                GET TAPE UNIT ID          99223
         N     RB,SNMASK                CLEAR HI ORDER BITS    @ZA08326
         DROP  RC                                                 99223
         CVD   RB,DXCCW2                CONVERT TO DECIMAL     @ZA08326
         UNPK  FL2ID,DXCCW2             PLACE IN LABEL         @ZA13576
         OI    FL2ID+K4,ZONEOF          STRIP SIGN             @ZA13576
***********************************************************************
* GET THE MODEL NO. CODE FROM THE SENSE INFORMATIONS AND
* PLACE THE MODEL NO. CHARACTER INTO THE SERIAL NO.
         LA    RC,DXCCW2                GET CCW2 ADDR          @ZA26229
         MVC   DXCCW1+K4(K4),CCW6TK7    SILI FLAG,7 SNS BYTES  @ZA02210
         ST    RC,DXCCW1                PUT AREA ADDR INTO CCW @ZA02210
         MVI   DXCCW1,CCWSENSE          SNS CMD CODE INTO CCW  @ZA02210
         BAL   RB,OTA45800              GO ISSUE SENSE COMMAND @ZA02210
         LA    RC,K15                   INT CODE- I/O ERROR WRT@ZA02210
         BZ    OTA45900                 NO, GO REJECT VOLUME   @ZA02210
         IC    RC,DXCCW2+K6             GET MOD NMBR FROM SNS  @ZA26229
         SLL   RC,K28                   CLEAR REG EXCEPT FOR   @ZA13576
         SRL   RC,K28                   HALF BYTE MOD. NO.     @ZA13576
         IC    RC,CHARTBL(RC)           GET CHAR. FROM TABLE   @ZA13576
         STC   RC,FL2ID                 MOD. NO. TO LABEL      @ZA13576
*               RESET CCW TO WRITE LABEL                       @ZA13576
         ST    RCORE,DXCCW1             RESET CCW1             @ZA02210
         MVC   DXCCW1+K4(K4),ARDCCW6T   SILI FLAG, 80 CHARS    @ZA02210
         MVI   DXCCW1,CCWWRTAP          WRITE OPERATION CODE   @ZA02210
***********************************************************************
OTA45210 EQU   *                        BEGIN TEST FOR CHKPT D/S Y02083
         L     R1,DXDSAB                ADR OF DSAB              Y02083
         USING DSAB,R1                                           Y02083
         TM    DSABFLG4,DSABCKDS        THIS A CHK PT DATA SET   Y02083
         BNO   OTA45260                 NO                       Y02083
         TM    DSABFLG4,DSABCKVL        VOL ALREADY SECURE       Y02083
         BO    OTA45245                 YES                      Y02083
***********************************************************************
*        ASK THE OPERATOR IF THE VOLUME CAN BE MADE SECURE            *
***********************************************************************
OTA45220 EQU   *                        INSTR MUST FOLLOW        Y02083
         MVC   DXRETMOD,ID6T6T          SET RTN NAME & ADDRESS   Y02083
         MVI   DXRETCOD,K8              SET BR TAB RETURN VALUE  Y02083
         XC    DXWORK1,DXWORK1          ZERO REPLY(WTOR) AREA    Y02083
         LA    R1,DXWORK1               POINT TO REPLY AREA      Y02083
         LA    R0,K3                    SET REPLY LENGTH         Y02083
         SLL   R0,K24                   IN HI ORDER BYTE         Y02083
         OR    R1,R0                    SET UP WTOR PARM         Y02083
         IECRES LOAD,MODID=ID6T4J,BRCODE=K52,BRANCH=QUEUED       Y02083
***********************************************************************
*              TEST HIS REPLY                                         *
***********************************************************************
OTA45230 EQU   *                        INSTR MUST FOLLOW        Y02083
         IECRES WAIT                    WAIT FOR REPLY           Y02083
         L     R1,DXDSAB                DSAB ADR                 Y02083
         OC    DXWORK1(K3),BLANKS       FOLD REPLY TO UPPER CASE Y02083
         CLC   KYES,DXWORK1             REPLY YES                Y02083
         BE    OTA45240                 YES                      Y02083
         CLC   KNO,DXWORK1              REPLY NO                 Y02083
         BE    OTA45250                 YES                      Y02083
         B     OTA45220                 NEITHER ISSUE MSG AGAIN  Y02083
***********************************************************************
*              UPDATE DSAB INDICATORS                                 *
***********************************************************************
OTA45240 EQU   *                        INSTR MUST FOLLOW        Y02083
         MODESET EXTKEY=SCHED                                    Y02083
         OI    DSABFLG4,DSABCKVL        SET DSAB SECURE VOL IND  Y02083
         MODESET EXTKEY=DATAMGT                                  Y02083
OTA45245 EQU   *                        FLAG CHKPT D/S           Y02083
         MVI   FL2DSIND,FL2CKDS         SET CHK PT IND IN LABEL  Y02083
         B     OTA45260                 WRITE HDR 2 LABEL        Y02083
OTA45250 EQU   *                        INSTR MUST FOLLOW        Y02083
         MODESET EXTKEY=SCHED                                    Y02083
         NI    DSABFLG4,X'FF'-DSABCKDS  RESET CK PT DATA SET IND Y02083
         MODESET EXTKEY=DATAMGT                                  Y02083
         DROP  R1                                                Y02083
         SPACE 1
*
*                   WRITE HEADER LABEL 2
*
         SPACE 1
OTA45260 EQU   *                        INSTR MUST FOLLOW        Y02083
         LA    RC,K15                   INT CODE - I/O ERROR WRITE LBL
         BAL   RB,OTA45800              GO WRITE FILE LABEL 2
         BO    OTA45300                 BRANCH IF NO ERROR
         TM    IOBSTAT0,CSWUNITX        IS ERROR UNIT EXCEPTION
         BZ    OTA45900                 NO, GO REJECT VOLUME
*
* BYPASS USER LABEL PROCESSING IF SYSOUT TAPE
*
OTA45300 EQU   *                        BYPASS USER LABEL IF SYSOUT
         SPACE
*                   SEE IF USER LABELS ARE TO BE PROCESSED
         SPACE
         TM    JFCBLTYP,JFCBUL          WAS USER LABELS SPECIFIED
         BNO   OTA45400                 NO, DO NOT PROCESS USER LABELS
*                                                                Y02134
         IECRES LOAD,MODID=ID6T6U,BRCODE=K0,BRANCH=QUEUED        Y02134
         EJECT
*                   WRITE TAPE MARK FOLLOWING HEADER LABELS
         SPACE 1
OTA45400 EQU   *                        WRITE TAPE MARK
         MVI   DXCCW1,CCWWTM            SET UP CCW TO WRITE TAPE MARK
         MVI   DXCCW1+4,CCWSILI+CCWCMDCH  CHAIN A NOP
         MVC   DXCCW2,DXCCW1            INSURE A VALID CCW     @ZA26229
         MVI   DXCCW2,CCWNOP            SET CCW FOR NOP
         MVI   DXCCW2+K4,K0             INSURE NO CHAINING     @ZA26229
         LA    RC,K16                   INT CODE - I/O ERROR WRITE TM
         BAL   RB,OTA45800              GO TO WRITE TAPE MARK
         BO    OTA45500                 BRANCH IF NO ERROR
         TM    IOBSTAT0,CSWUNITX        IS ERROR UNIT EXCEPTION
         BZ    OTA45900                 NO, GO REJECT VOLUME
OTA45500 EQU   *                        NO ERROR WRITING TAPE MARK
         OI    DXATOUTA,DXATHDTM        TM WRITTEN AFTER HDR LBL Y02144
*                                                                YM8526
         CLC   JFCBFLSQ,ONE6T           CHECK FILE SEQ ONE       YM8526
         BH    OTA45600                 BRANCH IF GT ONE         YM8526
*                                                                Y02134
         MVC   DXRETMOD,ID6T6V          SET UP RETURN LOAD       Y02134
         MVI   DXRETCOD,K8              SET UP RETURN OFFSET     Y02134
*                                                                Y02134
         IECRES LOAD,MODID=ID6T4J,BRCODE=K48,BRANCH=QUEUED       Y02134
*                                                                YM8526
OTA45600 IECRES LOAD,MODID=ID6T6V,BRCODE=K8,BRANCH=QUEUED        YM8526
         EJECT
*
         SPACE
*                   CLOSED SUBROUTINE TO ISSUE I/O OPERATIONS
         SPACE 1
*                        IT IS ASSUMED THAT ALL CONTROL BLOCKS
*                        HAVE ALREADY BEEN SET UP
         SPACE 1
*                             BAL  RA,PRHR50
*                             RETURN
         SPACE 1
OTA45800 EQU   *                        I/O SUBROUTINE
         EXCP  DXIOB                    ISSUE I/O OPERATION
         IECRES WAIT
         TM    DXECB,ECBNOERR           IS THERE AN UNUSUAL CONDITION
         BR    RB                       RETRN TO CALLER
         EJECT
*    TRANSLATE LABEL DATA BEFORE WRITING
OTA45850 EQU   *                        TRANSLATE LABEL ROUTINE
         XLATE DXLBL,K80,TO=A
         BR    RC                       RETURN TO CALLER
         EJECT
OTA45900 EQU   *                        ASK FOR NEW VOLUME
         NI    DCBIFLGS,X'FF'-DCBIFPIO  TURN OFF ERROR FLAGS IN
*                                       DCB TO ISSUE REWIND
*                                       AND/OR UNLOAD COMMAND
*                                                                Y02134
         LR    R0,RC                    LOAD ERROR CODE          Y02134
*                                                                Y02134
         MVC   DXCALLID,ID6T6T          SETUP ERROR ID           YM7530
         MVC   DXRETMOD,ID6T6N          SET UP RETURN LOAD       Y02134
         MVI   DXRETCOD,K0              SET UP RETURN OFFSET     Y02134
*                                                                Y02134
         IECRES LOAD,MODID=ID6T4A,BRCODE=K16,BRANCH=QUEUED       Y02134
         EJECT                                                 @G32DSMI
*                                                              @G32DSMI
***************************************************************@G32DSMI
*                                                              @G32DSMI
*        ISSUE RACDEF DEFINE IF THE VOLUME IS ELIGIBLE TO BE   @G32DSMI
*        UPDATED TO RACF OR NEWLY DEFINED TO RACF.             @G32DSMI
*                                                              @G32DSMI
***************************************************************@G32DSMI
*                                                              @G32DSMI
OTA46000 EQU   *                        CHK IF RACDEF NEEDED   @G32DSMI
*                                                              @G32DSMI
         TM    JFCFLGS1,JFCBPROT        CHK FOR AUTOMATIC RACF @G32DSMI*
                                        DATA SET PROTECTION    @G32DSMI
         BZR   RB                       RETURN IF NOT          @G32DSMI
*                                                              @G32DSMI
OTA46100 EQU   *                        ISSUE RACDEF           @G32DSMI
         MVC   DXCCW5(RACDLNTH),RACDLIST  MOVE IN RACDEF LIST  @G32DSMI
*                                                              @G32DSMI
         RACDEF ENTITY=UCBVOLI,MF=(E,DXCCW5) ISSUE RACDEF      @G32DSMI
*                                                              @G32DSMI
         LTR   RF,RF                    TEST IF SUCCESSFUL     @G32DSMI
         BNZ   OTA46300                 BRANCH IF NOT          @G32DSMI
         NI    JFCFLGS1,X'FF'-JFCBPROT  TURN OFF PROTECT       @G32DSMI
         L     RC,DXDEBXAD              ADDRESS OF DEB EXTN    @G32DSMI
         USING DEBXTN,RC                                       @G32DSMI
         OI    DEBXFLG1,DEBXDSSI        TURN ON RACF INDICATOR @G32DSMI
         DROP  RC                                              @G32DSMI
*                                                              @G32DSMI
OTA46200 EQU   *                        RETURN                 @G32DSMI
*                                                              @G32DSMI
         BR    RB                       RETURN                 @G32DSMI
*                                                              @G32DSMI
OTA46300 EQU   *                        ISSUE DMABCOND         @G32DSMI
*
         LA    R0,OABD245               CAUSE 913-44 ABEND     @G32DSMI
         LA    RC,K4                    NOT DEFINED RETURN CODE@G32DSMI
         CR    RF,RC                    RETURN CODE = 4        @G32DSMI
         BE    OTA46400                 YES                    @G32DSMI
         LA    R0,OABD248               CAUSE 913-4C ABEND     @G32DSMI
*                                                              @G32DSMI
OTA46400 EQU   *                        ISSUE DMABCOND         @G32DSMI
         DMABCOND (R0),ID6T0P           ABEND                  @G32DSMI
         EJECT
*                   CONSTANTS
         SPACE 1
         DS    0H
RACDLIST RACDEF TYPE=DEFINE,CLASS='TAPEVOL',MF=L  RACDEF       @G32DSMI
RACDLNTH EQU   *-RACDLIST               LENGTH OF PARM LIST    @G32DSMI
ONE6T    DC    H'1'                     HALFWORD CONSTANT ONE    YM8526
AGRTR32K DC    X'8000'                  VALUE IN LABEL WHEN LRECLGT
ARDCCW6T DC    X'20000050'              SILI FLAG, 80 CHARACTERS
AHDR1P   DC    C'HDR1'
CCW6TK7  DC    X'20000007'              CCW LENGTH CONSTANT    @ZA02210
         DS    0F                                              @ZA08326
SNMASK   DC    X'0000FFFF'              SER NUMBER MASK        @ZA08326
CHARTBL  DC    C'0003570000046800'      CHARACTER TABLE        @ZA19724
         DS    0F
ANSICD   DC    C'OS360'                 ANSI SYSTEM CODE
ARECFM   EQU   *                        RECFM TRANSLATE TABLE
OABD248  EQU   248                      CAUSE 913-4C ABEND     @G32DSMI
OABD245  EQU   245                      CAUSE 913-44 ABEND     @G32DSMI
*                                  TYPE D ADDED FOR ASCII
         DC    C'DVFU'                  DEC,VARIABLE,FIXED,UNKNOWN
ATRTCH   DC    C' CET'                  BLANK,CONVERSION,EVEN PAR
ABLKA    DC    C' SBR'                  BLK ATTRB TRANSLATE TABLE
VSSYSC   DC    C'IBM OS/VS 370'         VS SYSTEM CODE           XM1056
BLANKS   DC    C'   '                   3 BLANK CHARS            Y02083
KYES     DC    C'YES'                   YES REPLY MASK           Y02083
KNO      DC    C'NO '                   NO REPLY MASK            Y02083
         EJECT
*                                                                Y02083
*              EQUATES                                           Y02083
*                                                                Y02083
         SPACE 1
FL2CKDS  EQU   C'C'                     FL2DSIND CHK PT IND      Y02083
         EJECT
XCTL6T   XCTLTABL ID=(ID6T6N,6N,ID6T6V,6V,ID6T4A,4A,ID6T4J,4J,   Y02134X
               ID6T0P,0P,                                      @G32DSMI*
               ID6T6T,6T,ID6T6U,6U),BRT=YES,LENGTH=              Y02083
         IECDSECS CVT,TCB,TIOT,DCB,UCB,MAIN,WTG,DSAB,            Y02083*
               JSCB,RACVT,IEZDEB,                              @Z40RSDS*
               EXPAND=YES                                      @Z40RSDS
         IECEQU IEZDEB=YES
         END
