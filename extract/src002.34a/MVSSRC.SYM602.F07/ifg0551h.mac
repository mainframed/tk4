         TITLE 'IFG0551H DETERMINES DEVICE TYPE, INITIALIZES WORK AREA'
         COPY  LCGASMSW
IFG0551H CSECT
***********************************************************************
*                                                                     *
*                                                                     *
*          VS2 RELEASE 02 DELETIONS                                   *
*          VS2 RELEASE 03 DELETIONS                                   *
*          VS2 SU32 DELETIONS
*                                                              @G32DSMI
*0000526500                                                    @ZA01336
*0000                                                          @ZA09149
*0000483628                                                    @ZA12186
*A000162441    SYSROUTE OX13642                                @ZA13468
*48398701                                                      @ZA18494
***********************************************************************
*                                                                     *
* MODULE NAME = IFG0551H (OS/VS2)                                     *
*                                                                     *
* DESCRIPTIVE NAME = DEVICE TYPE DETERMINATION                        *
*                                                                     *
* COPYRIGHT = NONE                                                    *
*                                                                     *
* STATUS = RELEASE 2, LEVEL 0                                         *
*                                                                     *
* FUNCTION =                                                          *
*        THE PRINCIPLE FUNCTION OF THIS MODULE IS THE DETERMINATION   *
*        OF THE NEXT MODULE TO RECEIVE CONTROL.                       *
*                                                                     *
*        1) IF SMF IS REQUIRED, SETS SMF INDICATOR (WTGSMF) IN        *
*        DXXPATHS.                                                    *
*        2) DETERMINES VOLUME SEQUENCE NUMBER (DXVOLSER) FOR VOLUME   *
*        SERIAL NUMBER IN DXVOLSR1 (UCBVOLI) EXCEPT FOR CONCATENATION *
*        (ALREADY SET UP) AND/OR UNIT RECORD.                         *
*        3) DETERMINES DEVICE TYPE (MAG TAPE, DA, UNIT RECORD) AND    *
*        TRANSFER CONTROL TO PROPER ROUTINE DEPENDING UPON THE OPEN   *
*        OPTIONS (OUPUT, INPUT, ETC.).                                *
*        4) IF CONTROL IS TO BE PASSED TO THE CALLER'S NSL ROUTINES,  *
*        A DCB COPY IS PERFORMED FROM EOV'S WORK AREA TO THE CALLER'S *
*        STORAGE.  IF NSL IS SPECIFIED, THE CALLER'S EXIT LIST IS     *
*        CHECKED TO DETERMINE IF NSL PROCESSING IS TO BE DEFERRED     *
*        UNTIL CLOSE TIME. (X'0D)                                     *
*        5) IF CONCATENATION IS IN PROGRESS FOR DA/MAG TAPE, THE MOUNT*
*        MOUNT/VERIFY FUNCTION (IFG0554A) IS GIVEN CONTROL INDI-      *
*        CATING A RETURN MODULE OF IFG0553F AND A BRANCH CODE OF 0.   *
*        6) FOR UNIT RECORD, CONTROL IS PASSED TO IFG0552X WITH A     *
*        BRANCH CODE OF 0 FOR NORMAL PROCESSING OR A BRANCH CODE OF 16*
*        FOR CONCATENATION.                                           *
*        7) FOR DIRECT ACCESS, ONE OF 4 ROUTINES IS GIVEN CONTROL:    *
*        DA OUTPUT (FEOV) - IFG0554N                                  *
*        DA OUTPUT - IFG0554P                                         *
*        DA INPUT - IFG0553P                                          *
*        DA INPUT (REPOSITIONING) - IFG0554B                          *
*        8) FOR MAGNETIC TAPE, ONE OF 6 ROUTINES IS GIVEN CONTROL:    *
*        TAPE OUTPUT (NL,BLP) - IFG0551T                              *
*        TAPE OUTPUT (SL,AL) - IFG0551P                               *
*        TAPE OUTPUT (NSL) - NSLETRLO                                 *
*        TAPE INPUT (NSL) - NSLETRLI, NOT DEFERRED UNTIL CLOSE        *
*        TAPE INPUT (NL,FEOV,NSL) - IFG0552R, BRANCH CODE = 4         *
*        TAPE INPUT (SL,AL) - IFG0552R, BRANCH CODE = 0               *
*        9) IF IFG0551H IS UNABLE TO LOCATE A JFCB EXTENSION,         *
*        PROBLEM DETERMINATION IS CALLED (IFG0550P) WITH AN INTER-    *
*        NAL CODE OF 197.                                             *
*                                                                     *
* NOTES = SEE BELOW                                                   *
*                                                                     *
*    DEPENDENCIES =                                                   *
*            CLASS ONE CHARACTER CODE.  THE EBCDIC CHARACTER CODE     *
*            WAS USED FOR ASSEMBLY.  THE MODULE MUST BE REASSEMBLED   *
*            IF A DIFFERENT CHARACTER SET IS USED FOR EXECUTION.      *
*                                                                     *
*    RESTRICTIONS = NONE                                              *
*                                                                     *
*    REGISTER CONVENTIONS =                                           *
*            R2 POINTS TO DCB                                         *
*            R4 POINTS TO OPEN WORK AREA                              *
*            R5 POINTS TO THE RESIDENT ROUTINE                        *
*            R6 POINTS TO THE WTG TABLE                               *
*            R7 POINTS TO THE CURRENT PARAMETER LIST ENTRY            *
*            R8 POINTS TO THE CURRENT WTG TABLE ENTRY                 *
*            R9 POINTS TO THE DD ENTRY IN THE TIOT                    *
*            R10 POINTS TO THE UCB                                    *
*                                                                     *
*    PATCH LABEL = SEE THIRD FROM LAST LABEL BEFORE ORG STATEMENT AT  *
*                  END OF LISTING.                                    *
*                                                                     *
* MODULE TYPE = CONTROL (OPEN, CLOSE, EOV DATA MANAGEMENT)            *
*                                                                     *
*    PROCESSOR = ASSEMBLER XF                                         *
*                                                                     *
*    MODULE SIZE = SEE EXTERNAL SYMBOL DICTIONARY OR LOC FIELD ON     *
*                  ORG STATEMENT AT END OF LISTING                    *
*                                                                     *
*    ATTRIBUTES = REENTRANT, REFRESHABLE,READ-ONLY, ENABLED,          *
*                 PRIVILEGED, SUPERVISOR STATE, DATA MANAGEMENT KEY,  *
*                 LINK PACK AREA RESIDENT/PAGEABLE                    *
*                                                                     *
* ENTRY POINT = IFG0551H                                              *
*                                                                     *
*    PURPOSE = SEE FUNCTION                                           *
*                                                                     *
*    LINKAGE =                                                        *
*        THIS MODULE IS TRANSFERRED CONTROL THROUGH THE IECRES-LOAD   *
*        MACRO INSTRUCTION.                                           *
*                                                                     *
* INPUT =                                                             *
*        GIVEN CONTROL IN PROTECT KEY 5.                              *
*        REGISTER 2 POINTS TO THE COPIED DCB.                         *
*        DEBDCBAD POINTS TO THE COPIED DCB.                           *
*        REGISTER 4 POINTS TO THE EOV WORKAREA                        *
*                                                                     *
* OUTPUT =                                                            *
*        THE NEXT MODULE IS GIVEN CONTROL IN PROTECT KEY 5 WITH       *
*        REGISTER 2 POINTING TO THE COPIED DCB,                       *
*        DEBDCBAD POINTING TO THE COPIED DCB,                         *
*        AND REGISTER 4 POINTING TO THE EOV WORKAREA,                 *
*                                                                     *
* EXIT-NORMAL =                                                       *
*        IFG0554A - TAPE/DA CONCAT TO MOUNT/VERIFY                    *
*        IFG0552X - UNIT RECORD NORMAL, BRANCH CODE = 0               *
*        IFG0552X - UNIT RECORD CONCATENATION, BRANCH CODE = 16       *
*        IFG0554N - DA OUPUT (FEOV) - BRANCH CODE = 0                 *
*        IFG0554P - DA OUPUT, BRANCH CODE = 0                         *
*        IFG0553P - DA INPUT, BRANCH CODE = 0                         *
*        IFG0554B - DA INPUT (REPOSITIONING), BRANCH CODE = 0         *
*        IFG0551T - TAPE OUPUT (NL,BLP), BRANCH CODE = 0              *
*        IFG0551P - TAPE OUTPUT (SL,AL), BRANCH CODE = 0              *
*        IFG0552R - TAPE INPUT (NL,BLP), BRANCH CODE = 4              *
*        IFG0552R - TAPE INPUT (SL,AL), BRANCH CODE = 0               *
*        NSLETRLO - OUTPUT NSL                                        *
*        NSLETRLI - INPUT NSL                                         *
*                                                                     *
* EXIT-ERROR =                                                        *
*        IFG0550P - JFCB EXTENSION NOT FOUND, 837 ABEND-INT CODE=197  *
*                                                                     *
* EXTERNAL REFERENCES = SEE BELOW                                     *
*                                                                     *
*    ROUTINES =                                                       *
*        IFG019RA THROUGH THE IECRES MACRO.                           *
*                                                                     *
*    DATA AREAS =                                                     *
*        EOV WORKAREA.                                                *
*                                                                     *
*    CONTROL BLOCK =                                                  *
*        CVT                                                          *
*        DEB                                                          *
*        JFCB                                                         *
*        UCB                                                          *
*        TIOT                                                         *
*                                                                     *
* TABLES =                                                            *
*                                                                     *
* MACROS =                                                            *
*        MODESET                                                      *
*        DMABCOND                                                     *
*        IECRES LOAD                                                  *
*        IECRES INIT                                                  *
*        IECRES LOCJFCB                                               *
*                                                                     *
* CHANGE ACTIVITY =                                                   *
*        SEE CHANGES/DELETIONS SECTION JUST AFTER CSECT CARD.         *
*                                                                     *
***********************************************************************
         EJECT
EABD197  EQU   197                      INTERNAL CODE            YM1336
*                                                                     *
         USING UCB,RUCB
         USING FORCORE,RCORE
         USING WTG,RWTG                                          Y02080
         USING TIOENTRY,RTIOT
         USING DEB,RDEB
         USING IHADCB,RDCB
*
         BALR  RBASE,0                  ESTABLISH BASE
         USING *,RBASE
         OI    DCBOFLGS,DCBOBUSY        SET I/O SUPPORT BIT      Y02080
         L     RDCB,DXUDCBAD            USER DCB ADDRESS         YM7883
         MODESET KEYADDR=DXUKEY,WORKREG=15 ASSUME USER KEY       YM7883
         OI    DCBOFLGS,DCBOBUSY        SET BUSY BIT ON          YM7883
         MODESET EXTKEY=DATAMGT         RETURN TO D.M. KEY       YM7883
         L     RDCB,DXPDCBAD            DCB COPY ADDRESS         YM7883
         TM    DXXPATHS,WTGSMF          HAS TEST FOR SMF BEEN    A38013
*                                       ..MADE PREVIOUSLY-       A38013
         BO    EIN13400                 YES, DO NOT TEST AGAIN,  A38013
*                                       INSTEAD, BRANCH TO TURN  A38013
*                                       OFF SMF INDICATOR        A38013
         TM    JFCBTSDM,JFCSDS          SYSIN OR SYSOUT          A40990
         BO    EIN13450                 YES, BYPASS SMF          A40990
         L     RF,CVTPTR                                         A38013
         USING CVT,RF                                            A38013
         L     R7,CVTSMCA               GET PTR TO SMCA          A38013
         LTR   R7,R7                    TEST FOR SMF SYSTEM      A38013
         BZ    EIN13450                 BRANCH IF NO SMF         A38013
         USING SMCABASE,R7                                       A38013
         TM    SMCAOPT,SMCADSA          TEST FOR DATA SET INFO   A38013
         BNO   EIN13450                 BRANCH IF NOT REQUIRED   A38013
         TM    SMCAMISC,SMCAUSER+SMCAMAN                         A38013
*                                       ARE IBM RECORDS REQUIRED A38013
         BNO   EIN13450                 BRANCH IF NO             A38013
         DROP  R7                                                A38013
         L     R7,CVTTCBP                                        A38013
         L     R7,K4(R7)                GET TCB PTR              A38013
         L     R7,TCBTCT-TCB(R7)        GET TCT PTR              A38013
         LA    R7,0(R7)                 CLEAR HIGH ORDER BYTE  @ZA13468
         LTR   R7,R7                    TEST FOR TCT             A38013
         BZ    EIN13450                 BR IF NOT- BYPASS SMF    A38013
         OI    DXXPATHS,WTGSMF          IND SMF PROCESSING REQ'D A38013
         B     EIN13450                 CONTINUE PROCESSING      A38013
EIN13400 EQU   *                        TURN OFF SMF INDICATOR
         NI    DXXPATHS,X'FF'-WTGSMF    TURN OFF SMF INDICATOR   A38013
*                                       THIS WILL OCCUR ONLY ON  A38013
*                                       CONCATENATION WITH LIKE  A38013
*                                       ATTRIBUTES, WHEN THIS    A38013
*                                       MODULE CAN BE ENTERED    A38013
*                                       TWICE.                   A38013
EIN13450 EQU   *                        SMF NOT REQUIRED         A38013
*
         TM    UCBTBYT3,UCB3UREC        IS DEVICE UNIT RECORD   SA55590
         BO    EIN15300                 BRANCH IF UNIT RECORD   SA55590
         TM    DCBOFLGS,DCBOCON         IS CONCATENATION IN PROGRESS
         BO    EIN15300                 BR IF ALREADY SET UP     Y02134
         MVC   DXVOLSR1,UCBVOLI         MOVE IN CURRENT VOL SER  Y02134
         MVC   DXOLDVOL,UCBVOLI         RACF CURRENT VOL SER   @G32DSMI
         XR    RF,RF                    SET COUNT TO 0           Y02134
         LA    RET,JFCBEXAD             POINT TO EXT PTR         Y02134
         LA    RD,JFCBVOLS              POINT TO VOL SERS        Y02134
         LA    RC,K5                    SET COUNT AT FIVE        Y02134
*                                                                Y02134
EIN13600 EQU   *                        LOCATE VOLUME SERIAL     Y02134
*                                                                Y02134
         LA    RF,K1(RF)                INCREMENT BY ONE         Y02134
         CLC   DXVOLSR1,JFCBVOLS-JFCBVOLS(RD) CHECK FOR VOL SER  Y02134
         BE    EIN15000                 BRANCH IF FOUND          Y02134
         LA    RD,L'DXVOLSR1(RD)        INCREMENT BY 6           Y02134
         BCT   RC,EIN13600              LOOP IF MORE VOLS        Y02134
         CLC   JFCBEXAD-JFCBEXAD(L'JFCBEXAD,RET),ONEHZERO        YM3135
         BNE   EIN13700                 BRANCH TO FIND EXTN      YM3135
*                                                                Y02134
         DMABCOND EABD197,ID1H0P        PROBLEM DETERMINATION    YM1336
*                                                                Y02134
EIN13700 EQU   *                        LOCATE JFCB/EXTENSION    Y02134
*                                                                Y02134
         IECRES LOCJFCB,(RET)           LOCATE JFCB              Y02134
*                                                                Y02134
         LA    RD,K4(RET)               POINT TO VOL SERS        Y02134
         LA    RC,K15                   SET COUNT TO 15          Y02134
         B     EIN13600                 BRANCH, CHECK MORE VOLS  Y02134
*                                                                Y02134
*                                                                Y02134
EIN15000 EQU   *                        FOUND VOLSER             Y02134
*                                                                Y02134
         STH   RF,DXVOLSEQ              SAVE SEQ NO.             Y02134
*                                                                Y02134
EIN15300 EQU   *                        CHECK FOR MAG TAPE
*                        CHECK IF DEVICE IS MAG TAPE
         TM    UCBTBYT3,DCBDEVTP        IS IT TAPE DEVICE
         BO    EIN15600                 YES, GO
*
*              TEST DEVICE TYPE FOR DIRECT ACCESS INDICATOR
         TM    UCBTBYT3,DCBDEVDA        IS IT DIRECT ACCESS
         BO    EIN16700                 YES, GO
*                                                                Y02134
EIN15550 EQU   *                        NOT TAPE/DA
*                                                                Y02134
         MVC   DXXIDTTR,ID1H2X          EOD/CONCATENATION LOAD   Y02134
*                                                                Y02134
         LA    RET,K16                  BR TABLE OFFSET FOR
*                                       ..CONCATENATION          M0015
         TM    DCBOFLGS,DCBOCON         IS THERE CONCATENATION-
         BO    EIN17600                 ..BR IF YES TO EXIT
         B     EIN17500                 ..ELSE BR TO LOAD BR
*                                       ..TABLE OFFSET FOR EOD   M0015
*
*              IS THIS INPUT OR OUTPUT MAG TAPE DATA SET
*
EIN15600 MVC   DXDEBMOD(K4),DEBDVMOD    DENS,MODE,UCB ADDR OF PP DEB
         NI    DXDEBMOD,DX6250          REMOVE PROB PROG TRTCH    99223
         TM    UCBTBYT2,UCB3TAPE        CHK FOR 7 TRK
         BZ    EIN15700                 BRANCH IF NO
         NI    DXDEBMOD,DXTRTMSK        TURN OFF 6250 BPI BIT     99223
EINTRANS EQU   X'2B'                    INDIC TRANSLATE MODE
         OI    DXDEBMOD,EINTRANS        SET TRANSLATE MODE
EIN15700 EQU   *                        CHECK FOR INPUT
*                        CHECK IF DATA SET IS INPUT
         TM    DCBOFLGS,DCBOWRIT        IF WRITE BIT ON,TREAT AS OUTPUT
         BO    EIN16500                 YES, GO
*
*              IF EOVC BIT IS ON, CONCATENATION IS IN PROGRESS
*
*                        CHECK CONCATENATION INDICATOR
         TM    DCBOFLGS,DCBOCON         CONCAT IN PROGRESS
         BO    EIN17700                 YES, GO
*
*              WAS EOV CALLED BY FEOV EXECUTOR
*
EIN15750 MVC   DXXIDTTR,ID1H2R          TAPE INPUT LOAD          Y02134
*                                                                Y02134
         LA    RET,K4                   NL,NSL,FEOV,SYSIN OFFSET Y02134
*                                                                Y02134
         TM    DCBMACRF,DCBMEXCP        TEST FOR EXCP           SA59750
         BO    EIN15800                 BR IF EXCP              SA59750
         TM    DCBCIND2,DCBC2FEO        CHECK FOR FEOV
         BO    EIN17600                 YES, GO
*                        DETERMINE IF LABELS SPECIFIED
*              IS INPUT TAPE LABELED
*
EIN15800 TM    JFCBLTYP,JFCBLP+JFCNL    IS DATA SET UNLABELED
         BM    EIN17600                 YES, GO
*
*              TEST INPUT LABELED TAPE FOR STANDARD INDICATOR
*
         TM    JFCBLTYP,JFCBAL+JFCSL    IS LABEL STANDARD OR ANSI
         BNZ   EIN17500                 YES, GO
         MVC   DXCCW11(L'DXVOLSEQ),DXVOLSEQ MOVE SEQ NO TO WA    Y02134
*                                                                Y02134
         TM    DCBOFLGS,DCBORDBK        TEST FOR READ BACKWARDS  Y02134
         BO    EIN15900                 BRANCH IF READBACK       Y02134
*                                                                Y02134
         CLC   JFCBNVOL,DXVOLSEQ+K1     CHECK FOR NSL EOF        Y02134
         BH    EIN16100                 NOT EOF, GO TO NSLETRLI  Y02134
         B     EIN15950                 BRANCH, PROCESS NSL EOF  Y02134
*                                                                Y02134
EIN15900 EQU   *                        READBACK PROCESS         Y02134
*                                                                Y02134
         CLI   DXVOLSEQ+K1,K1           LAST VOLUME READBACK     Y02134
         BH    EIN16100                 NOT EOF, GO TO NSLETRLI  Y02134
*                                                                Y02134
EIN15950 EQU   *                        NSL EOF                  Y02134
*                                                                Y02134
         L     R1,DXDSABAD              GET CURRENT DSAB       @ZA01336
         USING DSAB,R1                                         @ZA01336
         L     R1,DSABFCHN              GET NEXT DSAB(CONC)    @ZA01336
         LTR   R1,R1                    IS THERE ANOTHER ONE?  @ZA01336
         BZ    EIN15975                 NO- NO CONCATENATION   @ZA01336
*                                       BR TO SEE IF DEFER EXIT@ZA01336
         L     R1,DSABTIOT              GET NEXT TIOT ENTRY    @ZA01336
         CLI   TIOEDDNM-TIOENTRY(R1),BLANK CONCATENATION?      @ZA01336
         BNE   EIN15975                 BR IF NOT BLANK (NO    @ZA01336
*                                       CONCATENATION)- SEE IF @ZA01336
*                                       DEFER EXIT IS SPECIFIED@ZA01336
         TM    TIOELINK-TIOENTRY(R1),TIOTSPOL SPOOLED DATA SET?@ZA01336
         BM    EIN15975                 YES, BR TO TEST FOR    @ZA01336
*                                       DEFER EXIT SPECIFIED   @ZA01336
         TM    DCBOFLGS,DCBOUNLK        UNLIKE ATTRIBUTES?     @ZA01336
         BO    EIN15975                 YES, BR TO TEST FOR    @ZA01336
*                                       DEFER EXIT SPECIFIED   @ZA01336
         L     RF,TIOESTTB-TIOENTRY(,R1) GET NEXT UCB          @ZA01336
         LA    RF,K0(RF)                CLEAR HIGH ORDER BYTE  @ZA01336
         LTR   RF,RF                    IS THIS A DUMMY D/S?   @ZA01336
         BNZ   EIN16100                 NO, BR TO NSLETRLI     @ZA01336
EIN15975 EQU   *                                               @ZA01336
         L     RF,DCBEXLST              GET EXIT LIST ADDR TO
*                                       CHECK
         LA    RF,K0(K0,RF)             FOR LABEL PROCESSING DEFERRED
         LTR   RF,RF                    IS THERE AN EXIT LIST ADDRESS
         BZ    EIN16100                 BR IF NOT XCTL TO NSLETRI
         MODESET KEYADDR=DXUKEY,WORKREG=12 USER EXIT LIST KEY    Y02082
EIN16000 EQU   *                        CHECK EXIT LIST
         CLI   K0(RF),XLDEFNSL          EXIT LIST CODE BYTE EQ
*                                       X'0D'
         BE    EIN17600                 YES,DEFER UNTIL CLOSE    Y02134
*                                                                Y02134
         CLI   K0(RF),XLDEFNSL+LASTNTRY  EXIT LIST CODE BYTE EQ
*                                       X'8D'
         BE    EIN17600                 YES,DEFER UNTIL CLOSE    Y02134
*                                                                Y02134
         TM    K0(RF),LASTNTRY          LAST CODE BYTE ON LIST
         LA    RF,K4(K0,RF)             INCREMENT TO NEXT ENTRY
         BC    12,EIN16000              NO,GO CHECK NEXT FOR
*                                       X'0D'
EIN16100 EQU   *                        YES,XCTL TO NSL MODULE
         MODESET EXTKEY=DATAMGT         EOV KEY                  Y02082
*
*        LOADING OF USER'S NON-STANDARD INPUT TRAILER LABEL HANDLER
*
         MVC   DXXMODNM(K12),ID1HNSLI   MOVE IN NSLETRLI         Y02134
         LA    RET,K8                   BRANCH TABLE OFFSET FOR RETURN
*                                       MOD IFG0552R (ALIAS IGG0550B)
EIN16200 EQU   *                        NSL
*                                                                Y02134
         MODESET EXTKEY=SUPR            UCB KEY                  Y02082
         NI    UCBWGT,ALLBITS-UCBREW    INSURE REWIND BIT IS OFF Y02080
         MODESET EXTKEY=DATAMGT         DATA MANAGEMENT KEY      Y02082
*                                                                Y02082
* COPY THE DCB FROM THE WORK AREA TO USER'S STORAGE              Y02082
*                                                                Y02082
         IECRES INIT,DCBCOPY=FRWKAR,STM=(3,14,WTGPREFX)          Y02082
*                                                                Y02082
         LR    R8,RES                   SAVE RES PTR
         LR    RES,RDEB                 PUT DEB IN R5 FOR NSL
         L     RDCB,DXPDCBAD            POINT TO COPIED DCB      YM5702
         OI    DXATALL,DXATFC           FORCE CLOSE ON ERROR     YM7099
         MODESET EXTKEY=ZERO            ASSUME KEY ZERO          YM2866
*                                                                Y02134
         IECRES LOAD,PREFIX=DXXPREFX,BRANCH=DIRECT               Y02080
*
*                        CHECK STANDARD LABEL INDICATOR
*              TEST FOR STD LABELS INDICATOR IN JFCB
EIN16500 TM    JFCBLTYP,JFCBAL+JFCSL    IS LABEL STANDARD OR ANSI
         BNZ   EIN16600                 YES, GO
*
*              TEST FOR NON-STD LABELS INDICATOR IN JFCB
*                                                                Y02134
         TM    JFCBLTYP,JFCNSL          ARE LABELS NON-STD
         BNO   EIN16550                 PROCESS NL,BLP           Y02134
*                                                                Y02134
         MVC   DXXMODNM(K12),ID1HNSLO   MOVE IN NSLETRLO         Y02134
         OI    DXATOUTA,DXATNSL         SET NSL RTN GIVEN CONTROLY02144
         L     RF,DXATCOM3              GET RRPLIST PTR          Y02144
         USING RRPLIST,RF                                        Y02144
         OI    RRFLAGS1,RRFNSL          SET NSL GIVEN CONTROL    Y02144
         SR    RET,RET                  BR ENTRY RETURN FOR 551T YM5315
         B     EIN16200                 BRANCH TO NSL ROUTINE    Y02134
*
*              LOADING OF TAPE OUTPUT EOV MODULE
*
EIN16550 EQU   *                        NL/BLP OUTPUT TAPE       Y02134
*                                                                Y02134
         MVC   DXXIDTTR,ID1H1T          NL OR BLP TAPE OUTPUT    Y02134
         B     EIN17500                 BRANCH TO NEXT LOAD      Y02134
*
*              LOADING OF TAPE OUTPUT TRAILER LABEL PREPARATION
*
EIN16600 EQU   *                        SL/AL OUTPUT TAPE        Y02134
*                                                                Y02134
         MVC   DXXIDTTR,ID1H1P          SL OR AL TAPE OUTPUT     Y02134
         B     EIN17500                 BRANCH TO NEXT LOAD      Y02134
*                                                                Y02134
EIN16700 EQU   *                        TEST FOR CONCATENATION   Y02134
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
*        TEST FOR CONCATENATION IN PROGRESS                      Y02134
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
         TM    DCBOFLGS,DCBOCON         CONCATNATION IN PROGRESS Y02134
         BNO   EIN16800                 BRANCH NOT CONCATENATION Y02134
*                                                                Y02134
         MVC   DXRETMOD,ID1H3X          SET UP RETURN MODULE     Y02134
         XC    DXRETCOD,DXRETCOD        ZERO RETURN MOD'S CODE   YM3929
         B     EIN17900                 BRANCH TO MOUNT VERIFY   Y02134
*
*      DETERMINE IF DIRECT ACCESS DATA SET IS INPUT OR OUTPUT
*                                                                Y02134
EIN16800 SR    RD,RD                    DA EOV INIT ENTRY OFFSET Y02134
         TM    DCBOFLGS,DCBOWRIT        HAS THE WRITE BIT BEEN SET
         BO    EIN17100                 YES, GO
*                                                                     *
***** LOAD THE REPOSITIONING MODULE                               *****
*                                                                     *
         TM    DCBMACRF,DCBMEXCP        IS IT EXCP DCB           A47776
         BO    EIN16900                 YES GO TO DA PROCESSING  A47776
         TM    DCBIOBAD,DCBIODEB        TEST FURTHER
         BZ    EIN16900                 NOT REPOSITIONING
*                                       FFEOV,BR
         MVC   DXXIDTTR,ID1H4B          SET UP FOR REPOSITIONING Y02134
         B     EIN17500                 GO TO REPOSITIONING LOAD Y02134
*                                                                Y02134
*
*              LOADING OF DA INPUT EOV MODULE
*                                                                Y02134
EIN16900 EQU   *                        DA INPUT EOV
*
         MVC   DXXIDTTR,ID1H3P          DA INPUT LOAD            Y02134
         B     EIN17500                 BRANCH TO DA INPUT       Y02134
*
*                        CHECK IF FEOV ISSUED
*                                                                Y02134
*              DID CONTROL COME FROM FEOV EXECUTOR
EIN17100 EQU   *                                                SA59750
         TM    DCBMACRF,DCBMEXCP        TEST FOR EXCP            YM6540
         BO    EIN17200                 BR IF EXCP               YM6540
         TM    DCBCIND2,DCBC2FEO        FEOV?
         BO    EIN17300                 YES, GO
*
*              LOADING OF DA OUTPUT EOV MODULE
*                                                                Y02134
EIN17200 EQU   *                        DA OUTPUT EOV
*
         MVC   DXXIDTTR,ID1H4P          DA OUTPUT LOAD           Y02134
         B     EIN17500                 BR TO DA OUTPUT PROCESSING
*
*              LOADING OF DA OUTPUT FEOV MODULE
*
EIN17300 EQU   *                        DA OUTPUT EOV            Y02134
*                                                                Y02134
         MVC   DXXIDTTR,ID1H4N          DA FEOV LOAD             Y02134
         B     EIN17500                 BR TO DA FEOV PROCESSING
*                                                                Y02134
EIN17500 EQU   *                        DA FEOV
*                                                                Y02134
         SR    RET,RET                  ZERO REG 14
EIN17600 EQU   *                        EXIT                     Y02082
         MODESET EXTKEY=DATAMGT         EOV KEY                  Y02082
         LA    RWTGC,DXXENTRY           SET R8 FOR RES RTN       Y02134
         IECRES LOAD,BRANCH=QUEUED      GO TO NEXT LOAD          Y02134
*                                                                Y02134
EIN17700 EQU   *                        CONCATENATION TAPE       Y02134
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
*        TAPE CONCATENATION - SET UP RETURN NAME                 Y02134
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
         MVC   DXRETMOD,ID1H3F          RETURN TO 553F           Y02134
         XC    DXRETCOD,DXRETCOD        ZERO RETURN MOD'S CODE   YM3929
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
*        SET UP FOR MOUNT VERIFY DURING CONCATENATION            Y02134
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
EIN17900 EQU   *                        MOUNT/VERIFY             Y02134
*                                                                Y02134
         MVC   DXVOLSEQ,JFCBVLSQ        MOVE VOLUME SEQ TO WA    Y02134
*                                                                Y02134
         CLC   JFCBNVOL,DXVOLSEQ+K1     NO. OF VOLS & VOL SEQ    Y02134
         BL    EIN17910                 BRANCH IF VOLSEQ GT VOLS Y02134
*                                                                Y02134
         CLI   DXVOLSEQ+K1,K0           TEST FOR VOL SEQ OF ZERO Y02134
*                                                              @ZA18494
         BH    EIN17920                 BRANCH NOT ZERO, USE IT  Y02134
*                                                                Y02134
EIN17910 EQU   *                        VOLSEQ > NO. OF VOLUMES  Y02134
*                                                                Y02134
         MVC   DXVOLSEQ,EOVDC10U        START WITH FIRST VOL     Y02134
*                                                                Y02134
EIN17920 EQU   *                        VOLSEQ NOT ZERO          Y02134
*                                                                Y02134
         LA    RWTGC,DXXENTRY           SET UP FOR RES           Y02134
         IECRES LOAD,MODID=ID1H4A,BRCODE=K0,BRANCH=QUEUED        Y02134
*                                                                Y02134
         EJECT
*                                                                Y02134
*****************************************************************Y02134
*
*                   CONSTANTS
*
CON40U   DC    H'4'                     HWD CONSTANT OF 4
CON60U   DC    H'6'                     HWD CONSTANT OF 6
EOVDC10U DC    H'1'                     HWD CONSTANT OF 1
EOVDC30U DC    H'3'                     HWD CONSTANT OF 3
EOVDC50U DC    H'5'                     HWD CONSTANT OF 5
EOVDC70U DC    H'7'                     HWD CONSTANT OF 7
ONEHZERO DC    X'000000'                COMPARE ADDRESS TO ZERO  YM3135
CHNOCT0U DC    X'4000000006'            CHAIN, 0 COUNT, READ OP
CHCTTI0U DC    X'6000000508'            CHAIN,SLI,5 COUNT,TIC OP
ANDCON   DC    F'4095'                  FWD CONSTANT OF 4095
         DC    X'FF'                    1 BYTE CONSTANT OF HEX 'FF'
IGG0U    DC    CL6'IFG055'              FIRST 6 BYTES OF MODULE NAMES
*
         XCTLTABL ID=(ID1H2R,2R,ID1H1P,1P,ID1H1T,1T,ID1H3P,3P,   Y02134X
               ID1H4P,4P,ID1H2X,2X,ID1H4N,4N,ID1H4B,4B,          Y02134X
               ID1H4A,4A,ID1H0P,0P,ID1H3X,3X,ID1H3F,3F,          Y02134X
               ID1HNSLI,NSLETRLI,ID1HNSLO,NSLETRLO),             Y02134X
               SVC=055,BRT=YES,LENGTH=                           Y02134
*                                                                Y02134
         IECDSECS CVT,TCB,DCB,UCB,DEB,MAIN,TIOT,WTG,SMF,PREFX,   Y02080*
               RRPL,DSAB,EXPAND=YES                            @ZA01336
TIOTSPOL EQU   X'06'               SPOOLED DATA SET INDICATOR  @ZA01336
         IECEQU
         END
