         TITLE 'IFG0198N/OPEN - WORK AREA FREEMAIN'
         COPY  LCGASMSW
IFG0198N CSECT
***********************************************************************
*                                                                     *
*        VS2 RELEASE 999 DELETIONS/CHANGES                            *
*0000                                                          @ZA01349
*A274000-274600                                                @ZA25895
*        VS2 RELEASE 03 DELETIONS/CHANGES                             *
*0000399700,399800,403158,403170                               @ZA02546
*                                                                     *
* MODULE NAME = IFG0198N (OS/VS2)                                     *
*                                                                     *
* DESCRIPTIVE NAME = WORK AREA FREEMAIN                               *
*                                                                     *
* COPYRIGHT = NONE                                                    *
*                                                                     *
* STATUS = RELEASE 2, LEVEL 0                                         *
*                                                                     *
* FUNCTION =                                                          *
*        1. IF OUTPUT, SET LAST OPERATION SWITCH TO WAS-A-WRITE SO    *
*        THAT TRAILER LABELS WILL BE WRITTEN BY EOV/CLOSE.            *
*                                                                     *
*        2. RESTORE OPEN PARAMETER LIST DCB OPEN OPTIONS FOR DCB'S    *
*        THAT ARE NO LONGER BEING OPENED.                             *
*                                                                     *
*        3. SYNCHRONIZE PROCESSING ALL DCB'S TO THIS MODULE, AND      *
*        CEASE PROCESSING IN PARALLEL WITH THE RESIDENT ROUTINE.      *
*                                                                     *
*        4. IF QSAM, PARALLEL DCB PROCESSING IS REQUESTED, COMPLETE   *
*        THE PARALLEL DATA ACCESS BLOCK--PDAB.                        *
*
*        5. CLEAR THE WTG TABLE ENTRY FOR EACH DCB EXCEPT WHERE       *
*        DEFERRED ABENDS OR CONCATENATION ARE INVOLVED.               *
*                                                                     *
*        6. UPDATE DSAB'S FOR THE DATA SET AND FOR ALL CONCATENATED   *
*        DSAB'S FOR ISAM OR BPAM INPUT/UPDATE:                        *
*          A. SET DSAB OPEN BIT IN DSABFLG2;                          *
*          B. INCREMENT DSABOPCT FIELD.                               *
*                                                                     *
*        7. TEST FOR A CHECK POINT DATA SET SECURITY INTERFACE.  IF IT*
*        EXISTS, UPDATE THE USER'S DCB TO THE STATUS OF THE PROTECTED *
*        DCB AND PLACE THE ADDRESS OF THE SECURITY INTERFACE ERROR    *
*        ROUTINE IN THE READ/WRITE AND CHECK ROUTINE ADDRESS SLOTS OF *
*        THE USER'S DCB.                                              *
*                                                                     *
*        8. IF DEFERRED ABEND SITUATION, RESTORE REGISTERS AND ISSUE  *
*        ABEND.                                                       *
*                                                                     *
*        9. FREEMAIN THE WTG TABLE. THIS FREES ALL WORKAREAS AS WELL. *
*                                                                     *
* NOTES = SEE BELOW                                                   *
*                                                                     *
*    DEPENDENCIES =                                                   *
*            CLASS ONE CHARACTER CODE.  THE EBCDIC CHARACTER CODE     *
*            WAS USED FOR ASSEMBLY.  THE MODULE MUST BE REASSEMBLED   *
*            IF A DIFFERENT CHARACTER SET IS USED FOR EXECUTION.      *
*                                                                     *
*    RESTRICTIONS = NONE                                              *
*                                                                     *
*    REGISTER CONVENTIONS =                                           *
*            R2 POINTS TO DCB                                         *
*            R4 POINTS TO OPEN WORK AREA                              *
*            R5 POINTS TO THE RESIDENT ROUTINE                        *
*            R6 POINTS TO THE WTG TABLE                               *
*            R7 POINTS TO THE CURRENT PARAMETER LIST ENTRY            *
*            R8 POINTS TO THE CURRENT WTG TABLE ENTRY                 *
*            R9 POINTS TO THE DD ENTRY IN THE TIOT                    *
*            R10 POINTS TO THE UCB                                    *
*                                                                     *
*    PATCH LABEL = SEE NEXT TO LAST LABEL BEFORE ORG STATEMENT AT END *
*                  OF LISTING.                                        *
*                                                                     *
* MODULE TYPE = CONTROL (OPEN, CLOSE, EOV DATA MANAGEMENT)            *
*                                                                     *
*    PROCESSOR = ASSEMBLER XF                                         *
*                                                                     *
*    MODULE SIZE = SEE EXTERNAL SYMBOL DICTIONARY OR LOC FIELD ON     *
*                  ORG STATEMENT AT END OF LISTING                    *
*                                                                     *
*    ATTRIBUTES = REENTRANT, REFRESHABLE,READ-ONLY, ENABLED,          *
*                 PRIVILEGED, SUPERVISOR STATE, DATA MANAGEMENT KEY,  *
*                 LINK PACK AREA RESIDENT/PAGEABLE                    *
*                                                                     *
* ENTRY POINT = IFG0198N FROM IFG0196W, ACCESS METHOD EXECUTOR RETURN *
*                        OR IFG0196X, LOAD EXCP APPEN, WRITE BACK JFCB*
*                                                                     *
*    PURPOSE = SEE FUNCTION                                           *
*                                                                     *
*    LINKAGE =                                                        *
*        FROM IFG0196W:                                               *
*           LA    RET,K8                                              *
*           IECRES LOAD,MODID=ID6W8N,BRCODE=(RET),BRANCH=QUEUED       *
*                                                                     *
*        FROM IFG0196X:                                               *
*           IECRES LOAD,MODID=ID6X8N,BRCODE=0,BRANCH=QUEUED           *
*                                                                     *
* INPUT = STANDARD                                                    *
*                                                                     *
* OUTPUT =                                                            *
*   OPEN RETURN CODE IN REGISTER 15 -                                 *
*      CODE IS LARGEST OF ALL DCB CODES IN A PARALLEL DCB OPEN        *
*      0 - ALL ENTRIES IN PARAMETER LIST OPENED CORRECTLY.            *
*      4 - ALL ENTRIES IN PARAMETER LIST HAVE SUCCESSFULLY            *
*          COMPLETED OPEN, BUT ONE OR MORE ENTRIES HAS A              *
*          WARNING MESSAGE.                                           *
*      8 - ONE OR MORE ENTRIES IN PARAMETER LIST WAS NOT              *
*          OPENED SUCCESSFULLY.  THE ENTRIES WITH ERRORS              *
*          WERE RESTORED TO THEIR PRE-OPEN STATUS.                    *
*     12 - ONE OR MORE ENTRIES WAS NOT OPENED SUCCESSFULLY.           *
*          THE ENTRIES WITH ERRORS WERE NOT RESTORED, AND             *
*          CANNOT BE REOPENED WITHOUT RESTORATION.                    *
*                                                                     *
* EXIT-NORMAL = RETURN TO CALLER OF OPEN                              *
*                                                                     *
* EXIT-ERROR = ABEND                                                  *
*                                                                     *
* EXTERNAL REFERENCES = SEE BELOW                                     *
*                                                                     *
*    ROUTINES = STANDARD                                              *
*                                                                     *
*    DATA AREAS = STANDARD                                            *
*                                                                     *
*    CONTROL BLOCK = STANDARD                                         *
*                                                                     *
* TABLES = STANDARD                                                   *
*                                                                     *
* MACROS = IECDSECS, IECEQU, DMABCOND, MODESET, IECRES, XCTLTABL      *
*                                                                     *
* CHANGE ACTIVITY = NEW RELEASE (LEVEL 0)                             *
*                                                                     *
***********************************************************************
         EJECT
         IECDSECS CVT,TCB,RB,DCB,ACB,MAIN,PREFX,WTG,TIOT,IEZDEB, Y02134*
               DSAB,PSA,RRPL,PDAB                               YL03123
         IECEQU AOS=YES,IEZDEB=YES
*
TIOTSPOL EQU   X'06'                    JES SPOOL D.S. INDICATORS     $
         USING TIOENTRY,RTIOT           DEFINE BASE TO TIOT DD ENTRY
*
         USING IHADCB,RDCB              DEFINE BASE TO USER'S DCB
         USING FORCORE,RCORE            DEFINE BASE TO MAIN WORK AREA
         USING WTG,RWTG                 DEFINE BASE TO XCTL/WTG TABLE
*
         BALR  RBASE,0                  ESTABLISH BASE REGISTER
         USING *,RBASE                  DEFINE BASE REGISTER
*
         B     OFN60200(RET)            BRANCH TO INDICATED FUNCTION
*
OFN60200 B     OFN60600                 RET=0 STILL PROCESSING   YM1342
         B     OFN70000                 RET=4 DCB IS NOT BEING OPENED
         B     OFN70400                 RET=8 RE-SYNCH RETURN
*
OFN60600 EQU   *                        STILL PROCESSING ENTRY
*
***********************************************************************
*
*  IF OUTPUT MODE, SET LAST OPERATION WAS A WRITE INDICATOR
*  IN DCB FOR EOV/CLOSE, SO THAT TRAILER LABELS WILL BE WRITTEN.
*
         NI    DCBOFLGS,X'FF'-DCBOWRIT  CLEAR WRITE SWITCH IN DCB
         L     RDCB,DXUDCBAD            ADDRESS USER DCB         Y02082
         MODESET KEYADDR=DXUKEY,WORKREG=1 ASSUME USER KEY        Y02082
* REPEAT DCB FIELD CHANGES                                       Y02082
         NI    DCBOFLGS,X'FF'-DCBOWRIT  CLEAR WRT SWITCH IN DCB  Y02082
         MODESET EXTKEY=DATAMGT         RESUME DATA MGT KEY      Y02082
         L     RDCB,DXPDCBAD            ADDRESS COPY OF DCB      Y02082
         TM    0(RPARC),PLISTOIN        IS OPEN FOR OUTPUT OR OUTIN
         BNO   OFN70400                 BR IF NO TO SYNCH DCB'S
         OI    DCBOFLGS,DCBOWRIT        SET WRITE SW ON FOR EOV/CLOSE
         L     RDCB,DXUDCBAD            ADDRESS USER DCB         Y02082
         MODESET KEYADDR=DXUKEY,WORKREG=1 ASSUME USER KEY        Y02082
* REPEAT DCB FIELD CHANGES                                       Y02082
         OI    DCBOFLGS,DCBOWRIT        SET WRITE SW ON FOR C/E  Y02082
         MODESET EXTKEY=DATAMGT         RESUME DATA MGT KEY      Y02082
         L     RDCB,DXPDCBAD            ADDRESS COPY OF DCB      Y02082
         B     OFN70400                 BR TO SYNCH DCB'S
*
***********************************************************************
*
*  RESTORE THE OPEN PARAMETER LIST DCB OPEN OPTION FOR DCB'S THAT
*  ARE NO LONGER BEING OPENED.
*  THE SAME FUNCTION IS PERFORMED IN IFG0196X FOR DCB'S THAT ARE
*  STILL BEING OPENED.
*
OFN70000 EQU   *                        DCB NOT BEING OPENED ENTRY
         TM    JFCBMASK+K6,JFCINOP+JFCOUTOP  WAS AN OVERRIDE USED
         BNO   OFN70400                 BRANCH IF NO
*
         TM    0(RPARC),PLISTOUT        IS OPEN FOR OUTPUT (OUTIN)
         BZ    OFN70200                 BR IF NO, IT WAS INPUT
*                                       ASSUME LABEL=(,,,OUT) AND
*                                       CHANGE THE PARAMETER LIST
         NI    0(RPARC),X'FF'-PLISTOP1+PLISTOIN  ENTRY BACK TO OUTIN
         NI    JFCBMASK+K6,X'FF'-JFCINOP  RESTORE JFCB LABEL PARM
         TM    DCBMACRF,DCBMEXCP        CHECK FOR EXCP          SA61146
         BO    OFN70400                 BR IF EXCP              SA61146
         MVC   DCBOFFSR,DCBOFFSW        EQUATE READ WITH WRITE CCW
         L     RDCB,DXUDCBAD            ADDRESS USER DCB         Y02082
         MODESET KEYADDR=DXUKEY,WORKREG=1 ASSUME USER KEY        Y02082
* REPEAT DCB FIELD CHANGES                                       Y02082
         MVC   DCBOFFSR,DCBOFFSW        EQUATE READ WITH WRT CCW Y02082
         MODESET EXTKEY=DATAMGT         RESUME DATA MGT KEY      Y02082
         L     RDCB,DXPDCBAD            ADDRESS COPY OF DCB      Y02082
         B     OFN70400                 BR TO CONTINUE
*
OFN70200 EQU   *                        FIRST,SEE IF BDAM      @ZA25895
         TM    DCBDSORG,DCBDSGDA        IS DCB FOR BDAM?       @ZA25895
         BNO   OFN70205                 NO,BRANCH              @ZA25895
         OI    0(RPARC),PLISTUPD        YES,CHANGE TO UPDATE   @ZA25895
         B     OFN70210                 GO RESET JFCB LABEL PRM@ZA25895
OFN70205 OI    0(RPARC),PLISTIO         CHANGE ENTRY BACK TO IN@ZA25895
OFN70210 NI    JFCBMASK+K6,X'FF'-JFCOUTOP  RESTORE JFCB LABEL P@ZA25895
         TM    DCBMACRF,DCBMEXCP        TEST FOR EXCP           SA61146
         BO    OFN70400                 BR IF EXCP              SA61146
         MVC   DCBOFFSW,DCBOFFSR        EQUATE WRITE WITH READ CCW
         L     RDCB,DXUDCBAD            ADDRESS USER DCB         Y02082
         MODESET KEYADDR=DXUKEY,WORKREG=1 ASSUME USER KEY        Y02082
* REPEAT DCB FIELD CHANGES                                       Y02082
         MVC   DCBOFFSW,DCBOFFSR        EQUATE WRITE WITH RD CCW Y02082
         MODESET EXTKEY=DATAMGT         RESUME DATA MGT KEY      Y02082
         L     RDCB,DXPDCBAD            ADDRESS COPY OF DCB      Y02082
*
***********************************************************************
*
*  SYNCHRONIZE ALL DCB'S TO THIS POINT AND THEN CEASE PROCESSING
*  IN PARALLEL WITH THE RESIDENT ROUTINE.
*
OFN70400 EQU   *                        SYNCH DCBS
         IECRES SYNCHDCB                SYNCHRONIZE THE DCB'S TO HERE
*
***********************************************************************
*
*  LOOP THROUGH THE DCB/ACB PARAMETER LIST. DO THE FOLLOWING,
*  AS APPROPRIATE:
*    (1) SET A RETURN CODE;
*    (2) SET THE OPEN BIT, RESET THE BUSY BIT;
*    (3) SET THE DSAB OPEN BIT;
*    (4) INCREMENT THE DSAB OPEN COUNT;
*    (5) CLEAR THE WTG TABLE ENTRY EXCEPT FOR
*        ... A DCB/ACB HAVING A DELAYED ABEND PENDING;
*        ... A DCB/ACB HAVING AN IGNORED ABEND PENDING;
*        ... A DCB WITH CONCATENATION IN PROGRESS;
*        ... A NON-SPOOLED ACB WITH CONCATENATION IN PROGRESS.
*
         L     RPAR,WTGPREFX            GET PTR TO WTG PREFIX    Y02080
         USING IECPREFX,RPAR                                     Y02080
         L     RPAR,IECUPRML            GET DCB PARM LIST ADDR   Y02080
         DROP  RPAR                                              Y02080
         LR    RPARC,RPAR               ADDR OF FIRST DCB PARM ENTRY
         USING USERPRML,RPARC                                    Y02080
         LA    RWTGC,WTGENTRY           ADDR OF FIRST WTG ENTRY
         USING WTGENTRY,RWTGC           DEFINE BASE TO WTG ENTRY
         MVI   WTGLNG,K0                INIT OPEN RTN CODE TO 0  Y02080
*
OFN72200 EQU   *                        PROCESS DCB
         L     RCORE,WTGCORE-K1         LOAD MAIN WORK AREA ADDR
         LA    RCORE,0(RCORE)           CLEAR HIGH ORDER BYTE
         LTR   RCORE,RCORE              TEST IF DCB HAS WORK AREA
         BNZ   OFN72400                 BR IF YES
*
         LA    R1,K8                    RET CODE = 8             YM3011
         BAL   RB,OFN72500              GO SET RET CODE          YM3011
         B     OFN74500                 BR TO PROCESS NEXT DCB   YM1342
*
OFN72400 EQU   *                        DCB HAS WORK AREA
         L     RDCB,PLISTDCB(,RPARC)    LOAD CURRENT DCB ADDR    YM3011
*
**********************************************************************
*
*    TEST FOR VTAM ACB                                           YM3011
*
*        DO NOT CHECK FOR EXCP BEFORE CHECKING DSORG           @ZA02546
*        BECAUSE THE DDNAME MAY BE THERE AND THE DSORG         @ZA02546
*        WILL BE ZERO IF NOT PRESENT BECAUSE IT IS A COPY.     @ZA02546
         TM    DCBDSRG2,ACBDORGA        IS CONTROL BLOCK ACB     YM3011
         BZ    OFN72405                 NO, CAN'T BE VTAM        YM3011
         USING IFGACB,RACB              ADDRESSABILITY TO ACB    YM3011
         CLI   ACBAMETH,ACBVTAM         IS THIS A VTAM ACB       YM3011
         BE    OFN72430                 YES, CHECK ERROR CODE    YM3011
*
OFN72405 EQU   *                        CHECK FOR DELAYED ABEND  YM3011
         TM    JFCBMASK+K4,JFCMABND     DELAYED ABEND PENDING    Y02144
         BO    OFN74500                 BR IF YES                YM1342
         TM    DXATOPEN,DXATIGN         IGNORED ABEND PENDING    Y02144
         BO    OFN74500                 BR IF YES                YM1342
*
         L     RTIOT,DXTIOTAD           GET CURRENT TIOT ENTRY   YM1342
*                                                                X02989
*  TEST FOR VSAM ACB'S AND SET RETURN CODES                      X02989
*                                                                X02989
         TM    JFCDSRG2,JFCORGAM        IS THIS A VSAM DATA SET  XM1037
         BNO   OFN72460                 BR IF NO                 Y02134
*        DO NOT CHECK FOR EXCP BEFORE CHECKING DSORG           @ZA02546
*        BECAUSE THE DDNAME MAY BE THERE AND THE DSORG         @ZA02546
*        WILL BE ZERO IF NOT PRESENT BECAUSE IT IS A COPY.     @ZA02546
         TM    ACBDSORG+K1,ACBDORGA     IS CONTROL BLOCK AN ACB  X02989
         BNO   OFN72460                 BR IF NO                 YM1342
         TM    ACBOFLGS,ACBOPEN         IS ACB OPEN              X02989
         BNO   OFN72425                 BR IF NO                 X02989
         CLI   ACBERFLG,K0              ARE ERROR FLAGS SET      X02989
         BE    OFN73810                 BR IF NO, GO SET DSAB    YM1342
         LA    R1,K4                    SET RET CODE = 4         YM3011
         BAL   RB,OFN72500              GO SET RET CODE          YM3011
         B     OFN73810                 BR TO SET DSAB           YM1342
         USING IHADCB,RDCB              REDEFINE BASE TO DCB     X02989
OFN72425 EQU   *                        ACB NOT OPEN             X02989
         LA    R1,K8                    SET RET CODE = 8         YM3011
         BAL   RB,OFN72480              ENSURE OPEN BIT OFF    @ZA01349
         BAL   RB,OFN72500              GO SET RET CODE          YM3011
         B     OFN73980                 BR, VSAM ACB NOT OPEN    YM1342
*
***********************************************************************
*
*    TEST FOR VTAM ERRORS AND SET RETURN CODE                    YM3011
*
OFN72430 EQU   *                        TEST FOR VTAM ERRORS     YM3011
         USING IFGACB,RACB              DETERMINE BASE FOR ACB   YM3011
         TM    ACBOFLGS,ACBIOSFG        IS BUSY BIT ON           YM3011
         BO    OFN72440                 YES, TEST ERROR FLG      YM3011
         LA    R1,K8                    RET CODE = 8             YM3011
         BAL   RB,OFN72480              ENSURE OPEN BIT OFF    @ZA01349
         BAL   RB,OFN72500              GO SET RET CODE          YM3011
         MVC   ACBDDNM,DDNINIT          INITIALIZE DD NAME       YM3011
         L     RACB,DXUDCBAD            USERS ACB                YM3011
         MODESET KEYADDR=DXUKEY,WORKREG=1 USERS KEY              YM3011
*   REPEAD FIELD CHANGES
         MVC   ACBDDNM,DDNINIT          INIT DDNAME              YM3011
         MODESET EXTKEY=DATAMGT         RETURN TO DM DEY         YM3011
         L     RACB,DXPDCBAD            COPIED DCB               YM3011
         B     OFN73980                 GO TEST FOR DEB EXT      YM3011
*
OFN72440 EQU   *                        BUSY BIT ON, TEST ERR FLGYM3011
         CLI   ACBERFLG,K0              ERROR FLAG ON            YM3011
         BNE   OFN72450                 YES, SET RET CODE = 4    YM3011
         B     OFN72455                 NO, OPEN ON, BUSY OFF    YM3011
*
OFN72450 EQU   *                        BUSY ON, ERROR FLAGS ON  YM3011
         LA    R1,K4                    RET CODE = 4             YM3011
         BAL   RB,OFN72500              GO SET RET CODE          YM3011
         B     OFN72455                 CLEAR WTG TABLE ENTRY    YM3011
*
OFN72455 EQU   *                        TURN ON OPEN, BUSY OFF   YM3011
         USING IHADCB,RDCB                                       YM3011
         XI    DCBOFLGS,DCBOPEN+DCBOBUSY OPEN ON BUSY OFF        YM3011
         L     RDCB,DXUDCBAD            USERS DCB                YM3011
         MODESET KEYADDR=DXUKEY,WORKREG=1                        YM3011
*     REPEAT DCB FIELD CHANGES
         XI    DCBOFLGS,DCBOPEN+DCBOBUSY OPEN ON BUSY OFF        YM3011
         MODESET EXTKEY=DATAMGT         SESUME DATA MGT KEY      YM3011
         L     RDCB,DXPDCBAD            COPIED DCB ADDR          YM3011
         B     OFN74000                 CLEAR WTG TABLE ENTRY    YM3011
*                                                                YM1342
*   ON ENTRY TO THIS MODULE THE OPEN BIT CAN BE ON FOR           YM1342
*   A VSAM ACB AND AN ISAM-VSAM CI DCB.  A VSAM ACB              YM1342
*   WILL NOT PASS THROUGH THE FOLLOWING TEST.                    YM1342
*                                                                YM1342
OFN72460 EQU   *                        NOT MSAM ACB             Y02134
         TM    DCBOFLGS,DCBOPEN         CHECK FOR OPENED DCB     YM1342
         BO    OFN73810                 BR IF OPEN               YM1342
*
         TM    DCBOFLGS,DCBOLOCK+DCBOBUSY  CHECK LOCK AND BUSY BITS
         BO    OFN72600                 BR IF STILL OPENING
*
         MVI   WTGLNG,K12               SET RETURN CODE          Y02080
         B     OFN73980                 BR IF NOT STILL OPENING
*
***********************************************************************
*
*    CLOSED SUBROUTINE TO ENSURE OPEN BIT IS OFF IF THIS
*    DCB IS NOT OPENED SUCCESSFULLY (IE THE ERROR RETURN CODE IS
*    GREATER THAN 8 AND A WORK AREA WAS OBTAINED).
*
OFN72480 EQU   *                                               @ZA01349
         NI    DCBOFLGS,X'FF'-DCBOPEN   ENSURE OPEN BIT OFF    @ZA01349
         L     RDCB,DXUDCBAD            ADDRESS USER DCB       @ZA01349
         MODESET KEYADDR=DXUKEY,WORKREG=15 ASSUME USER KEY     @ZA01349
         NI    DCBOFLGS,X'FF'-DCBOPEN   ENSURE OPEN BIT OFF    @ZA01349
         MODESET EXTKEY=DATAMGT         RESUME DATA MGT KEY    @ZA01349
         L     RDCB,DXPDCBAD            ADDRESS COPY OF DCB    @ZA01349
         BR    RB                       RETURN                 @ZA01349
*
***********************************************************************
*
*    CLOSED SUBROUTINE TO DETERMINE IF PRESENT RET CODE IS       YM3011
*    GREATER THAN PREVIOUS.  IF IT IS, IT REPLACES PREVIOUS.  IF YM3011
*    NOT, THE PREVIOUS RET CODE IS LEFT.  PREVIOUS RET CODE IS   YM3011
*    IN CORE LOCATION WTGLNG AND PRESENT CODE IS IN REG 1        YM3011
*
OFN72500 EQU   *                        SET RET CODE             YM3011
         CLM   R1,K1,WTGLNG             PRESENT RC LARGER PREV.  YM3011
         BLR   RB                       NO, DON'T SET RET CODE   YM3011
         STC   R1,WTGLNG                YES, SET RET CODE        YM3011
         BR    RB                       RETURN                   YM3011
*
OFN72600 EQU   *                        STILL OPENING
         XI    DCBOFLGS,DCBOPEN+DCBOBUSY  SET OPEN BIT ON, BUSY BIT OFF
         L     RDCB,DXUDCBAD            ADDRESS USER DCB         Y02082
         MODESET KEYADDR=DXUKEY,WORKREG=1 ASSUME USER KEY        Y02082
* REPEAT DCB FIELD CHANGES                                       Y02082
         XI    DCBOFLGS,DCBOPEN+DCBOBUSY  SET OPEN, BUSY BITS    Y02082
         MODESET EXTKEY=DATAMGT         RESUME DATA MGT KEY      Y02082
         L     RDCB,DXPDCBAD            ADDRESS COPY OF DCB      Y02082
*
*  QSAM PARALLEL INPUT PROCESSING OCCURS HERE.  A REQUEST FOR QPIP
*  IS RECOGNIZED BY A DCB EXIT LIST CODE OF X'12'.  THE ADDRESS OF THE
*  DCB IS ADDED TO A QUEUE OF DCB ADDRESSES IN A PARALLEL DATA ACCESS
*  BLOCK.  THE ADDRESS OF THE PDAB IS OBTAINED FROM THE DCB EXIT LIST.
*  WHEN SPACE IS AVAILABLE IN THE PDAB, THE DCB ADDRESS IS ADDED TO THE
*  QUEUE; THE PARALLEL GET ROUTINE IS LOADED; AND THE DCB IS MARKED
*  OPEN FOR QPIP--DCBPOPEN IS SET TO 1.
*
         TM    DCBOFLGS,DCBOPEN         OPENED FOR PROCESSING    YM1343
         BZ    OFN73810                 BR NO                    YM1343
         TM    DCBMACF1,DCBMEXCP        USING EXCP               YM1343
         BO    OFN73810                 BR YES                   YM1343
         TM    DCBDSRG2,DCBACBM         CONTROL BLOCK AN ACB     YM1343
         BO    OFN73810                 BR YES                   YM1343
         TM    DCBDSRG1,DCBORGPS+DCBORGPO CAPABLE OF QSAM        YM1343
         BZ    OFN73810                 BR NO                    YM1343
         TM    DCBMACF1,DCBMGET         USING QSAM GET           YM1343
         BZ    OFN73810                 BR NO                    YM1343
         TM    DCBQSWS,DCBPOPEN         ALREADY OPEN FOR QPIP    YM1343
         BO    OFN73810                 BR YES                   YM1343
         TM    0(RPARC),PLISTM01        INPUT PROCESSING         YM1343
         BO    OFN73810                 BR NO                    YM1343
         TM    TIOELINK,TIOESYIN        SPOOLED INPUT            YM1343
         BO    OFN73810                 BR YES                   YM1343
         L     RF,TIOESTTB                                       YM1343
         N     RF,OFN7745K              DUMMY DATA SET           YM1343
         BZ    OFN73810                 BR YES                   YM1343
         TM    DCBRECFM,DCBRECVR+DCBRECST SPANNED RECORDS        YM1343
         BO    OFN73810                 BR YES                   YM1343
         TM    DCBBFTEK,DCBBFTE         EXCHANGE BUFFERING       YM1343
         BO    OFN73810                 BR YES                   YM1343
         TM    DCBRECFM,DCBRECTK        TRK OVERFLOW             YM1343
         BO    OFN73810                 BR YES                   YM1343
         TM    DCBMACF1,DCBMGMOV+DCBMLOC MOVE OR LOCATE MODE     YM1343
         BZ    OFN73810                 BR NO                    YM1343
         L     RD,DCBEXLST                                       YM1343
         N     RD,OFN7745K              EXIT LIST PRESENT        YM1343
         BZ    OFN73810                 BR NO                    YM1343
         L     RDCB,DXUDCBAD            ADDRESS USER DCB         Y02082
         MODESET KEYADDR=DXUKEY,WORKREG=1 ASSUME USER KEY        Y02082
OFN72720 EQU   *                        CHECK ENTRY              YM1343
         CLI   0(RD),XLQPIP             PARALLEL INPUT PROC      YM1343
         BE    OFN72730                 BR YES                   YM1343
         CLI   0(RD),XLQPIP+LASTNTRY    PARALLEL INPUT PROC      YM1343
         BE    OFN72730                 BR YES                   YM1343
         TM    0(RD),LASTNTRY           LAST EXLST ENTRY         YM1343
         BO    OFN72900                 BR YES                   YM1343
         LA    RD,K4(,RD)               STEP TO NEXT ENTRY       YM1343
         B     OFN72720                 GO CHECK NEXT ENTRY      YM1343
OFN72730 EQU   *                        PARALLEL INPUT PROCESS   YM1343
         L     RET,0(,RD)               LOCATE PDAB              YM1343
         USING IHAPDAB,RET                                       YM1343
         LH    RD,PDANODCB              GET CURRENT NO. DCB'S    YM1343
         AH    RD,OFN7740K              ADD THIS ONE             YM1343
         BNP   OFN72900                 BR IF COUNT INVALID      YM1343
         LH    RF,PDAMAXCB              GET MAX ENTRIES          YM1343
         LTR   RF,RF                    POSITIVE NUMBER          YM1343
         BNP   OFN72900                 BR NO                    YM1343
         CR    RD,RF                    THIS ENTRY EXCEED MAX    YM1343
         BH    OFN72900                 BR YES                   YM1343
         MODESET EXTKEY=DATAMGT         RESUME DATA MGT KEY      Y02082
         L     RDCB,DXPDCBAD            ADDRESS COPY OF DCB      Y02082
         IECRES LOAD,MODNM=OFN7760N,BRANCH=NO,PREFIX=WTGPREFX    YM1307
         OI    DCBQSWS,DCBPOPEN         COMPLETED PARALLEL OPEN  YM1343
         L     RDCB,DXUDCBAD            ADDRESS USER DCB         Y02082
         MODESET KEYADDR=DXUKEY,WORKREG=1 ASSUME USER KEY        Y02082
         STH   RD,PDANODCB              UPDATE NO. OF DCB'S      YM1343
         SLL   RD,K2                    LENGTH OF DCB LIST       YM1343
         LA    RDCB,0(,RDCB)                                     YM1343
         ST    RDCB,PDADCBAL-K4(RD)     ADD DCB ADDR TO LIST     YM1343
         LA    RB,PDADCBAL-K4(RD)       ADDR OF LAST DCB ENTRY   YM1343
         LR    RC,RB                    ALLOW WAIT ON FIRST REQ  YM1343
         LA    RA,K4                    BXLE INCR FOR IGG019JD   YM1343
         STM   RA,RD,PDADCBAI           UPDATE PDAB PARAMETERS   YM1343
         MVC   PDAGRTNA,WTGMODEP        ADD ENTRY POINT TO PDAB  YM1343
         OI    DCBQSWS,DCBPOPEN         COMPLETED PARALLEL OPEN  YM1343
OFN72900 EQU   *                        LAST EXLST ENTRY
         MODESET EXTKEY=DATAMGT         RESUME DATA MGT KEY      Y02082
         L     RDCB,DXPDCBAD            ADDRESS COPY OF DCB      Y02082
*                                                                Y02082
*        SET DSAB OPEN BIT ON AND INCREMENT OPEN COUNT IN DSAB.  Y02134
*        DO THE SAME IN ALL CONCATENATED DSABS.                  Y02134
*        THIS IS NOT DONE IF CONCATENATION OF UNLIKE ATTRIBUTES  Y02134
*        IS OCCURRING.                                           Y02134
*
OFN73810 EQU   *                        DSAB OPEN BIT PROCESS    Y02082
         TM    DCBMACRF,DCBMEXCP        IS IT EXCP               Y02134
         BO    OFN73815                 YES AVOID ACB TEST       Y02134
         TM    DCBDSRG2,ACBDORGA        IS IT AN ACB             Y02134
         BO    OFN73815                 YES, DON'T TEST SPOOL DCBY02134
         TM    TIOELINK,TIOTSPOL        IS IT SPOOLED DCB        Y02134
         BZ    OFN73815                 NO, GO INCREMENT DSAB    YM1111
         TM    DCBOFLGS,DCBOCON         CONCATENATION            YM1111
         BZ    OFN74000                 BR NO TO ZERO WTG ENTRY  YM1111
         B     OFN74500                 BYPASS ZEROING WTG ENTRY YM1111
OFN73815 EQU   *                        INCREMENT DSAB           Y02134
         L     RC,DXDSABAD              FIRST DSAB ADDRESS       Y02134
         USING DSAB,RC                                           Y02134
         LTR   RC,RC                    IS THERE A DSAB          Y02134
         BZ    OFN74000                 BRANCH IF NO             YM1342
         MODESET EXTKEY=SCHED           ASSUME SCHED KEY         Y02082
         OI    DSABFLG2,DSABOPEN        TURN ON OPEN BIT IN      Y02134
*                                       FIRST DSAB               Y02134
         MODESET EXTKEY=DATAMGT         ASSUME DATA MGT KEY      Y02082
         TM    DCBOFLGS,DCBOCON         CONCATENATION            Y02134
         BZ    OFN73820                 BRANCH IF NO             YM1342
         TM    DCBMACRF,DCBMEXCP        IS ACCESS METHOD EXCP    YM1342
         BO    OFN74500                 BR IF YES, NOT AN ACB    YM1342
         TM    DCBDSRG2,ACBDORGA        IS CONTROL BLOCK AN ACB  YM1342
         BZ    OFN74500                 BR NO                    YM1342
         TM    TIOELINK,TIOTSPOL        CHK IF SPOOLED ACB       YM1342
         BZ    OFN74500                 BR NO                    YM1342
         NI    DCBOFLGS,X'FF'-DCBOCON   INSURE EOVC BIT OFF      YM1342
         L     RDCB,DXUDCBAD            ADDRESS USER DCB         YM1342
         MODESET KEYADDR=DXUKEY,WORKREG=1 ASSUME USER KEY        YM1342
* REPEAT DCB FIELD CHANGES                                       YM1342
         NI    DCBOFLGS,X'FF'-DCBOCON   INSURE EOVC BIT OFF      YM1342
         MODESET EXTKEY=DATAMGT         RESUME DATA MGT KEY      YM1342
         L     RDCB,DXPDCBAD            ADDRESS COPY OF DCB      YM1342
         B     OFN74000                 GO ZERO WTG ENTRY        YM1342
OFN73820 EQU   *                        NO CONCATENATION         Y02134
         OI    DXATOPEN,DXATDSIN        DSAB COUNT INCR STARTED  Y02144
         L     RF,DSABOPCT-K2           OPEN COUNT 2ND HWORD     Y02134
OFN73860 EQU   *                        INCREMENT DSAB COUNT     Y02134
         LR    R0,RF                    LOAD TO MANIPULATE       Y02134
         SRDL  R0,K16                   COUNT IN HIGH 2 BYTES    Y02134
*                                       OF REG 1                 Y02134
         SRA   R1,K16                   PROPOGATE SIGN BYTE      Y02134
         LTR   R1,R1                    IS IT NEGATIVE?          Y02134
         MODESET EXTKEY=SCHED           DSAB KEY                 Y02082
         BNL   OFN73880                 BRANCH IF NO             Y02134
         SR    R1,R1                    SET TO ZERO              Y02134
         B     OFN73900                 BRANCH TO INCREMENT      Y02134
OFN73880 EQU   *                        COUNT TOO LARGE?         Y02134
         C     R1,=F'32767'             COMPARE TO X'7FFF'       Y02134
         BNL   OFN73920                 DON'T INCREMENT IF HIGH  Y02134
*                                       OR EQUAL                 Y02134
OFN73900 EQU   *                        INCREMENT COUNT          Y02134
         LA    R1,K1(,R1)               INCREMENT DSAB OPEN      Y02134
*                                       COUNT BY ONE             Y02134
         SLL   R1,K16                   PUT IN HIGH 2 BYTES OF 1 Y02134
         SLDL  R0,K16                   R0 IS UPDATED FULLWORD   Y02134
         CS    RF,R0,DSABOPCT-K2        SWAP UPDATED VALUE IN    Y02134
         BNE   OFN73860                 TRY AGAIN IF ALTERED     Y02134
         MODESET EXTKEY=DATAMGT         DATA MANAGEMENT KEY      Y02082
OFN73920 EQU   *                        INCREMENT OPEN COUNT IN  Y02134
*        SUBSEQUENT CONCATENATED DSABS
         L     RC,DSABFCHN              GET NEXT DSAB            Y02134
         LTR   RC,RC                    IS THERE A NEXT ONE      Y02134
         BZ    OFN73930                 GET OUT IF NOT           Y02134
         L     RTIOT,DSABTIOT           GET TIOT ENTRY ADDR      Y02134
         CLI   TIOEDDNM,C' '            IS THERE A DDNAME?       Y02134
         BE    OFN73820                 BRANCH IF NO             Y02134
OFN73930 EQU   *                        DSABS COMPLETE           Y02144
         OI    DXATOPEN,DXATDFIN        DSAB COUNT INCR FINISHED Y02144
         EJECT
*                                                                Y02083
*****************************************************************Y02083
*                                                                Y02083
*  AT THIS POINT TEST TO SEE IF A CHECKPOINT DATA SET SECURITY   Y02083
*  INTERFACE EXISTS.  IF SO, UPDATE THE USER'S DCB TO THE        Y02083
*  STATUS OF THE PROTECTED DCB AND PLACE THE ADDRESS OF THE      Y02083
*  SECURITY-INTERFACE ERROR ROUTINE IN THE READ/WRITE AND        Y02083
*  CHECK ROUTINE ADDRESS SLOTS OF THE USER'S DCB.                Y02083
*  IF A SECURITY INTERFACE EXISTS, THERE ARE THREE VERSIONS OF   YM3806
*  THE DCB THAT EXIST: THE USER'S DCB IN USER'S KEY, THE COPIED  YM3806
*  DCB IN KEY 5, AND THE CHECKPOINT DCB REPRESENTING THE USER    YM3806
*  DCB IN KEY ZERO. AT THIS POINT THE TRUE USER'S DCB KEY OF     YM3806
*  USER WILL BE UPDATED FROM THE TRUE COPIED DCB KEY 5.          YM3806
*                                                                Y02083
         L     RC,DXDSAB                FETCH DSAB ADDRESS       YM1342
         USING DSAB,RC                  ADDRESS DSAB             Y02083
         TM    DSABFLG4,DSABCKVL        SECURE CHK PT VOL REF    YM3827
         BNO   OFN74000                 NO, CONT NORMAL PROC     YM3827
         TM    DSABFLG4,DSABCKSI        SECURITY INTERFACE EXIST Y02083
         BZ    OFN74000                 IF NOT, BRANCH           YM1441
         DROP  RC                       DSAB ADDRESSABILITY      Y02083
         L     RC,DXUDCBAD              GET KEY ZERO DCB ADDR    YM3806
         L     RDCB,DXPDCBAD            GET COPIED DCB ADDR      YM3806
         LA    RF,K4                    BACKUP PROT. DCB ADDR BY Y02083
         SR    RC,RF                    4 TO GET USER'S DCB ADDR Y02083
         MODESET EXTKEY=ZERO            GET IN KEY ZERO          YM3806
         L     RC,K0(,RC)               GET TRUE USER'S DCB ADDR Y02083
         ICM   RB,B'0111',DCBEXLSA-IHADCB(RC) SAVE USER'S EX LST YM3863
         AH    RDCB,DXUDCBPL            PNT PAST PROT. DCB PREFX Y02083
         AH    RC,DXUDCBPL              PNT PAST USER DCB PREFX  Y02083
         LH    RF,DXUDCBML              GET DCB MOVE LENGTH      Y02083
         BCTR  RF,R0                    ADJUST FOR MOVE INSTR    Y02083
         L     R1,DXTCBADR              FETCH USER TCB ADDRESS   Y02083
         USING TCB,R1                   ADDRESS TCB              Y02083
         MODESET KEYADDR=TCBPKF,WORKREG=1 USER KEY               Y02083
         DROP  R1                       TCB ADDRESSABILITY       Y02083
         EX    RF,OFNMOVER              UPDATE USER'S DCB        Y02083
         SH    RC,DXUDCBPL              RESTORE PT TO USER'S DCB YM3863
         STCM  RB,B'0111',DCBEXLSA-IHADCB(RC) RESTORE USER EX LSTYM2874
         MODESET EXTKEY=DATAMGT         BACK TO D/M KEY          Y02083
         SH    RC,DXUDCBPL              POINT TO USER DCB PREFX  Y02083
         IECRES LOAD,MODNM=OFN19SI,BRANCH=NO,PREFIX=WTGPREFX     YM1342
         L     R0,WTGMODEP              GET S/I ERROR RTN EPA    YM1342
         L     R1,DXTCBADR              FETCH USER TCB ADDRESS   Y02083
         USING TCB,R1                   ADDRESS TCB              Y02083
         MODESET KEYADDR=TCBPKF,WORKREG=1 USER KEY               Y02083
         DROP  R1                       TCB ADDRESSABILITY       Y02083
         STCM  R0,K7,DCBREAD+K1-IHADCB(RC) DROP INTO USER'S DCB  Y02083
         STCM  R0,K7,DCBCHECK+K1-IHADCB(RC) READ/WRITE AND CHECK Y02083
*                                       ROUTINE ADDRESS SLOTS    Y02083
         MODESET EXTKEY=DATAMGT         BACK TO D/M KEY          Y02083
         L     RDCB,DXPDCBAD            RESTORE COPIED DCB ADDR  Y02083
         B     OFN74000                 CONTINUE                 YM3011
*
***********************************************************************
*
*    TEST FOR DEB EXTENSION.  IF ONE EXISTS, FREE IT.  IN ERROR  YM3011
*    CONDITION AT THIS POINT.  ERROR FROM THE EXECUTORS.         YM3011
*
OFN73980 EQU   *                        IF DEB EXT, FREE IT      YM3011
         L     R1,DXDEBXAD              GET DEB EXT ADDR         YM3011
         LTR   R1,R1                    DOES DEB EXT EXIST       YM3011
         BZ    OFN74000                 NO, CONTINUE             YM3011
*                                       YES, FREE DEB            YM3011
         USING DEBXTN,R1                                         YM3011
         LH    R0,DEBXLNGH              GET DEB EXT LENGTH       YM3011
         IECRES FREE,LV=(R0),A=(R1),PREFIX=NO,                         C
               STM=(2,14,WTGPREFX)      FREE DEB EXTENSION       YM3011
         B     OFN74000                 CONTINUE                 YM3011
         EJECT
OFN74000 EQU   *                        CLEAR WTG TABLE ENTRY    YM1342
         XC    WTGENTRY,WTGENTRY        CLEAR WTG TABLE ENTRY    YM1342
*                                                                Y02083
*****************************************************************Y02083
*                                                                Y02083
*  CHECK FOR END OF DCB WORKAREA FREEMAIN LOOP                   Y02083
*                                                                Y02083
OFN74500 EQU   *                        CONTINUE                 YM1342
         TM    PLISTOPT(RPARC),LASTNTRY CHECK FOR LAST DCB ENTRY
         LA    RPARC,K4(RPARC)          ADVANCE TO NEXT DCB ENTRY
         LA    RWTGC,L'WTGENTRY(RWTGC)  ADVANCE TO NEXT WTG ENTRY
         BZ    OFN72200                 NO, GO PROCESS NEXT DCB
*
***********************************************************************
*
*  SEE IF THERE IS A DCB LEFT THAT HAS A DEFERRED ABEND PENDING.
*
         LR    RPARC,RPAR               ADDR OF FIRST DCB PARM ENTRY
         LA    RWTGC,WTGENTRY-WTG(,RWTG)  ADDR OF FIRST WTG ENTRY
         L     RC,WTGPREFX              GET WTG PREFIX PTR       Y02144
         L     RC,IECRRPRM-IECPREFX(,RC) GET RRPLIST ADDRESS     Y02144
         USING RRPLIST,RC                                        Y02144
*
OFN75400 EQU   *                        CHECK FURTHER FOR DELAYED ABEND
         L     RCORE,WTGCORE-K1         LOAD MAIN WORK AREA ADDR
         LA    RCORE,0(RCORE)           CLEAR HIGH ORDER BYTE
         LTR   RCORE,RCORE              TEST IF DCB HAS WORK AREA
         BZ    OFN75500                 BR IF NO, ADDR CLEARED   Y02144
         TM    JFCBMASK+K4,JFCMABND     DELAYED ABEND DCB?       Y02144
         BO    OFN75600                 YES, GO ISSUE ABEND      Y02144
         TM    DXATOPEN,DXATIGN         IGNORED ABEND DCB?       Y02144
         BZ    OFN76000                 NO, BRANCH TO PROCESSING Y02082
*                                       FOR UNLIKE CONCATENATION Y02082
         CLC   RRMLRTRY,RRFWORK         PRIOR IGNORED ABEND DCB  Y02144
         BNE   OFN75500                 YES,GO CHK IF LAST ENTRY Y02144
         STM   RPARC,RWTGC,RRMLRTRY     SAVE CURR DCB ENTRY ADDR Y02144
*                                       AND CURR WTG ENTRY ADDR  Y02144
***********************************************************************
*
*  CHECK FOR END OF LOOP LOOKING FOR A DELAYED/IGNORED ABEND.
*
OFN75500 EQU   *                        END OF LOOP CHECK        Y02144
         TM    PLISTOPT(RPARC),LASTNTRY CHECK FOR LAST DCB ENTRY Y02144
         LA    RPARC,K4(RPARC)          ADVANCE TO NEXT DCB ENTRY
         LA    RWTGC,L'WTGENTRY(RWTGC)  ADVANCE TO NEXT WTG ENTRY
         BZ    OFN75400                 BR NO TO CHK FURTHER FOR Y02144
*                                       A DELAYED ABEND DCB      Y02144
         CLC   RRMLRTRY,RRFWORK         IGNORED ABEND PENDING    Y02144
         BE    OFN76400                 NO, BR TO FREE WTG TABLE Y02144
         LM    RPARC,RWTGC,RRMLRTRY     GET CURR DCB ENTRY ADDR  Y02144
*                                       AND CURR WTG ENTRY ADDR  Y02144
         LA    RCORE,OFN76400           GET MAINLINE RETRY ADDR  Y02144
         ST    RCORE,RRMLRTRY           SAVE MAINLINE RETRY ADDR Y02144
         XC    RRFWORK,RRFWORK          CLEAR RRPLIST WORK WORD  Y02144
         L     RCORE,WTGCORE-K1         LOAD MAIN WORK AREA ADDR Y02144
*
***********************************************************************
*
*  DO A DELAYED/IGNORED ABEND ON THIS DCB.  RESTORE REGISTERS FIRST.
*
OFN75600 EQU   *                        DELAY/IGNORE ABEND       Y02144
         L     RDCB,DXUDCBAD            LOAD CURRENT DCB ADDR    YM5924
         LM    RTIOT,RET,DXREG9         RESTORE REGISTERS 9-14
         L     R0,DXREG0                RESTORE REGISTER 0
*
         SR    RF,RF                    LOAD PROBLEM DETERMINATION
         IC    RF,DXRETCOD              RETURN CODE IN REGISTER 15
*
         LH    R1,DXABCODE              LOAD THE ABEND CODE      Y02134
         SRL   R1,K4                    ALIGN RIGHT              Y02134
         ABEND (1),DUMP,,SYSTEM,        SYSTEM ABEND WITH A DUMP Y02080*
               DUMPOPT=SNAPLIST
*
***********************************************************************
*
*  SPECIAL PROCESSING FOR EOV CONCATENATION WITH UNLIKE ATTRIBUTES,
*  WHICH CAN ONLY OCCUR IF OPENING ONLY ONE DCB VIA BR FROM EOV. Y02082
*  THE LOGIC BEGINNING AT LABEL 'OFN76000' CAN ONLY BE ENTERED   Y02082
*  IF EOV CONCATENATION IS IN PROCESS.                           Y02082
*
OFN76000 EQU   *                        EOV CONATENATION PROCESSING
*
*  TURN OFF THE EOVC BIT FOR ALL EXCEPT A QSAM GET THAT WENT
*  THROUGH EOV (NOT FEOV).  THE QSAM GET SUBROUTINE WILL TEST IT AND
*  TURN IT OFF.  IT MUST BE OFF BEFORE THE DCB GOES TO EOV/FEOV AGAIN.
         L     RDCB,PLISTDCB(,RPAR)     LOAD DCB ADDR
         TM    DCBMACRF,DCBMEXCP        IS THIS AN EXCP DCB      A40495
         BO    OFN76050                 BR IF YES, NEVER FEOV   SA58096
         TM    DCBCIND2,DCBC2FEO        CHECK FOR FEOV           A40495
         BZ    OFN76100                 BR IF NO                 A40495
         NI    DCBCIND2,X'FF'-DCBC2FEO  RESET FEOV BIT           A40495
OFN76050 EQU   *                        NEVER FEOV              SA58096
         NI    DCBOFLGS,ALLBITS-DCBOCON RESET EOVC BIT          SA58096
         L     RDCB,DXUDCBAD            LOAD USERS DCB          ZA30837
         MODESET KEYADDR=DXUKEY,WORKREG=1 ASSUME USER KEY       ZA30837
         NI    DCBCIND2,X'FF'-DCBC2FEO  RESET FEOV BIT          ZA30837
         NI    DCBOFLGS,ALLBITS-DCBOCON RESET EOVC BIT          ZA30837
         MODESET EXTKEY=DATAMGT         RESUME DATA MGT KEY     ZA30837
         L     RDCB,DXPDCBAD            ADDRESS COPY DCB        ZA30837
         B     OFN76400                                         SA58096
OFN76100 EQU   *                                                 A40495
         TM    DCBMACRF,DCBMGET         CHECK FOR QSAM GET
         BO    OFN76200                 BR IF YES
         TM    DCBMACRF,DCBMREAD        CHECK FOR BSAM READ
         BO    OFN76200                 BR IF YES
OFN76150 EQU   *                        NOT FEOV                 A40495
         NI    DCBOFLGS,X'FF'-DCBOCON   RESET EOVC BIT
         L     RDCB,DXUDCBAD            ADDRESS USER DCB         Y02082
         MODESET KEYADDR=DXUKEY,WORKREG=1 ASSUME USER KEY        Y02082
* REPEAT DCB FIELD CHANGES                                       Y02082
         NI    DCBOFLGS,X'FF'-DCBOCON   RESET EOVC BIT           Y02082
         MODESET EXTKEY=DATAMGT         RESUME DATA MGT KEY      Y02082
         L     RDCB,DXPDCBAD            ADDRESS COPY OF DCB      Y02082
         TM    DCBMACRF,DCBMEXCP        IS ACCESS METHOD EXCP    YM1342
         BO    OFN76200                 YES, EXCP
         TM    DCBDSORG+K1,ACBDORGA     IS CONTROL BLOCK AN ACB
         BO    OFN76400                 YES, BR TO FREE WTG TABLE
OFN76200 EQU   *                        QSAM GET
*
*  INTERCHANGE REGISTERS 0-8 FROM THE SVRB 0-15 GENERAL REGISTER
*  SAVE AREA AND THE USER'S SAVE AREA POINTED TO BY REGISTER 13.
*  NOTE THAT THE ACCESS METHOD ROUTINES SAVED REGISTERS 0-8 IN
*  THE REGISTER 13 SAVE AREA TWO FULL WORDS FURTHER AWAY THAN IS
*  NORMAL FOR A REGISTER 13 SAVE AREA.  THE EOV SVC ROUTINES
*  INTERCHANGED THESE REGISTERS SO THAT THE SVRB WOULD HAVE THE
*  USER'S REGISTERS FOR SYNCH EXITS TO THE USER DURING EOV,
*  CLOSE, AND OPEN.
*
         L     RET,CVTPTR               GET CVT ADDR             Y02082
         L     RET,CVTTCBP-CVT(,RET)    GET ADDR TCB POINTERS    Y02082
         L     RET,K4(,RET)             GET CURRENT TCB ADDR     Y02082
         L     RET,TCBRBP-TCB(,RET)     GET ADDR OF SVRB         Y02082
         USING RBSECT,RET               DEFINE BASE TO SVRB      Y02082
*                                                                Y02082
         L     RD,RBGRS13               GET REG 13 SAVE AREA POINTER
         L     RF,WTGPREFX              GET PREFIX               Y02082
         STM   R0,RET,IECREGSV-IECPREFX(RF)    SAVE REGS ALTERED Y02082
*                                       BY SETLOCK               Y02082
         MODESET EXTKEY=SUPR            KEY ZERO FOR SETLOCK     Y02082
         SETLOCK OBTAIN,TYPE=LOCAL,MODE=UNCOND, GET THE LOCAL    Y02082*
               RELATED=(FREEMAIN,IFG0198N(OFN76300))             Y02082
         LM    R0,RET,IECREGSV-IECPREFX(RF) RESTORE REGS         Y02082
         MODESET KEYADDR=DXUKEY,WORKREG=12 USER KEY              Y02082
         OC    K28(K36,RD),K28(RD)      MAKE SURE IT'S STILL IN  Y02082
*                                       USER'S CORE              Y02082
         MODESET EXTKEY=SUPR            KEY ZERO FOR XC'S        Y02082
         XC    RBGRS0(K36),K28(RD)      INTERCHANGE REGISTERS 0-8
         XC    K28(K36,RD),RBGRS0       BY TRIPLE EXCLUSIVE ORING
         XC    RBGRS0(K36),K28(RD)      THE TWO FIELDS
         MODESET EXTKEY=DATAMGT         DATA MANAGEMENT KEY      Y02082
         STM   R0,RET,IECREGSV-IECPREFX(RF) SAVE REGS            Y02082
         MODESET EXTKEY=SUPR            KEY 0 FOR SETLOCK        Y02082
OFN76300 SETLOCK RELEASE,TYPE=LOCAL,    RELEASE LOCAL LOCK       Y02082*
               RELATED=(FREEMAIN,IFG0198N(OFN76200))             Y02082
         MODESET EXTKEY=DATAMGT         DATA MANAGEMENT KEY      Y02082
         LM    R0,RET,IECREGSV-IECPREFX(RF) RESTORE REGS         Y02082
*
OFN76400 EQU   *                        FREE WTG TABLE
         SR    R9,R9                    SAVE THE OPEN RETURN     YM0868
         IC    R9,WTGLNG                CODE                     Y02080
*
***********************************************************************
*
*        FREEMAIN THE WHERE-TO-GO TABLE
*
         IECRES FREE,A=(RWTG),PREFIX=WTG FREEMAIN WTG TABLE CORE Y02080
         LR    RF,R9                    LOAD THE OPEN RETRN CODE Y02080
*                                                                Y02080
*        RETURN TO CALLER                                        Y02080
*                                                                Y02080
         IECRES EXIT                    RETURN                   Y02080
*                                                                Y02080
*
         DROP  RWTGC                                             Y02080
         EJECT
*
***********************************************************************
*
*        CONSTANTS
*
OFNMOVER MVC   K0(K0,RC),K0(RDCB)       OBJECT OF EXECUTE INS.   Y02083
OFN7800K DC    H'8'                     LENGTH RESIDENT WK AREA PREFIX
OFN7740K DC    H'1'                                             YL03123
OFN7745K DC    A(X'FFFFFC')                                     YL03123
DDNINIT  DC    X'FF00000000000000'      INITIAL VALUE FOR DDNAME YM3011
*
SNAPLIST SNAP  SDATA=TRT,MF=L           READ ONLY SNAP LIST FOR  Y02144*
                                        ABEND OPTION DUMPOPT     Y02144
*
         XCTLTABL ID=(OFN19SI,IGG019SI,OFN7760N,IGG019JD),       YM1342*
               BRT=YES,LENGTH=                                   Y02080
*
         IECDSECS TCB,EXPAND=YES        EXPAND DSECTS HERE       Y02083
         END
