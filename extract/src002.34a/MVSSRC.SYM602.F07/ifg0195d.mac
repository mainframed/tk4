         TITLE 'IFG0195D                      OPEN - NSL INPUT'
IFG0195D CSECT
         ENTRY IGG0190B                                          Y02080
IGG0190B EQU   IFG0195D                 ALIAS ENTRY POINT        Y02080
***********************************************************************
*                                                                     *
*              VS2 RELEASE 03 DELETIONS/CHANGES                       *
*                                                                     *
*000403000,626100                                              @ZA03195
*0000                                                          @ZA04224
*0000                                                          @ZA13589
*                                                                     *
* MODULE NAME = IFG0195D (OS/VS2)                                     *
*                                                                     *
* DESCRIPTIVE NAME = OPEN NSL INPUT                                   *
*                                                                     *
* COPYRIGHT = NONE                                                    *
*                                                                     *
* STATUS = RELEASE 2, LEVEL 0                                         *
*                                                                     *
* FUNCTION =                                                          *
*        1. INTERFACE WITH USER SUPPLIED NSL INPUT ROUTINE(NSLOHDRI). *
*                                                                     *
*        2. SYNCHRONIZE ALL NSL DCB'S, AND SET UP THE NECESSARY       *
*        CONDITIONS FOR PARALLEL PROCESSING PRIOR TO EXITING TO THE   *
*        NLS ROUTINE.                                                 *
*                                                                     *
*        3. ON RETURN FROM THE NSL ROUTINE, RESET TO PROCESS SERIALLY *
*        VIA THE RESIDENT ROUTINE.                                    *
*                                                                     *
*        4. CHECK THE UCBMOUNT BIT IN THE UCBDMCT FIELD OF THE UCB.   *
*        IF IT IS SET, THE VOLUME IS REJECTED.                        *
*                                                                     *
* NOTES = SEE BELOW                                                   *
*                                                                     *
*    DEPENDENCIES =                                                   *
*            CLASS ONE CHARACTER CODE.  THE EBCDIC CHARACTER CODE     *
*            WAS USED FOR ASSEMBLY.  THE MODULE MUST BE REASSEMBLED   *
*            IF A DIFFERENT CHARACTER SET IS USED FOR EXECUTION.      *
*                                                                     *
*    RESTRICTIONS = NONE                                              *
*                                                                     *
*    REGISTER CONVENTIONS =                                           *
*            R2 POINTS TO DCB                                         *
*            R4 POINTS TO OPEN WORK AREA                              *
*            R5 POINTS TO THE RESIDENT ROUTINE                        *
*            R6 POINTS TO THE WTG TABLE                               *
*            R7 POINTS TO THE CURRENT PARAMETER LIST ENTRY            *
*            R8 POINTS TO THE CURRENT WTG TABLE ENTRY                 *
*            R9 POINTS TO THE DD ENTRY IN THE TIOT                    *
*            R10 POINTS TO THE UCB                                    *
*                                                                     *
*    PATCH LABEL = SEE NEXT TO LAST LABEL BEFORE ORG STATEMENT AT END *
*                  OF LISTING.                                        *
*                                                                     *
* MODULE TYPE = CONTROL (OPEN, CLOSE, EOV DATA MANAGEMENT)            *
*                                                                     *
*    PROCESSOR = ASSEMBLER XF                                         *
*                                                                     *
*    MODULE SIZE = SEE EXTERNAL SYMBOL DICTIONARY OR LOC FIELD ON     *
*                  ORG STATEMENT AT END OF LISTING                    *
*                                                                     *
*    ATTRIBUTES = REENTRANT, REFRESHABLE,READ-ONLY, ENABLED,          *
*                  PRIVILEGED, SUPERVISOR STATE, DATA MANAGEMENT KEY, *
*                  LINK PACK AREA RESIDENT/PAGEABLE                   *
*                                                                     *
* ENTRY POINT = IFG0195D                                              *
*                                                                     *
*    PURPOSE = SEE FUNCTION                                           *
*                                                                     *
*    LINKAGE =                                                        *
*        FROM THE TAPE INITIALIZATION ROUTINE:                        *
*           IECRES BRANCH=QUEUED                                      *
*        START OF LINKAGE IS IN IFG0193A WHICH SETS UP DXRETMOD       *
*        AND DXRETCOD.                                                *
*                                                                     *
* INPUT = STANDARD                                                    *
*                                                                     *
* OUTPUT =                                                            *
*        IEC101A M - MOUNT MESSAGE IF VOLUME IS REJECTED              *
*                                                                     *
* EXIT-NORMAL =                                                       *
*        IFG0196J                                                     *
*        NSLOHDRI                                                     *
*                                                                     *
* EXIT-ERROR =                                                        *
*        IFG0194A WITH A 413 - 04 ABEND                               *
*                                                                     *
* EXTERNAL REFERENCES = SEE BELOW                                     *
*                                                                     *
*    ROUTINES = IFG019RA                                              *
*                                                                     *
*    DATA AREAS = NONE                                                *
*                                                                     *
*    CONTROL BLOCK = UCB, DCB                                         *
*                                                                     *
* TABLES = NONE                                                       *
*                                                                     *
* MACROS = MODESET, IECEQU, IECDSECS, IECRES, XCTLTABL                *
*                                                                     *
* CHANGE ACTIVITY = NEW RELEASE (LEVEL 0)                             *
*                                                                     *
***********************************************************************
         EJECT
         IECEQU
*                                                                     *
         USING FORCORE,RCORE
         USING TIOENTRY,RTIOT
         USING IHADCB,RDCB
         USING UCBOB,RUCB
         USING WTG,RWTG
         USING WTGENTRY,RWTGC           CURRENT WTG ENTRY        Y02134
         USING DXDCBLST,RPARC           CURRENT PARMLIST POINTER Y02134
*
         BALR  RBASE,0                  ESTABLISH ADDRESSABILITY
         USING *,RBASE
         MODESET EXTKEY=DATAMGT         RETURN TO DATA MGMT KEY  Y02082
*
         B     OTA60000(RET)            GO TO PROPER ENTRY
OTA60000 EQU   *                        BRANCH TABLE
         NOP   0                        ENTRY NOT USED           Y02134
         B     OTA60500                 NORMAL RETURN PROCESSING
         B     OTA60200                 INITAL NSL RETURN
         B     OTA60100                 INITAL ENTRY
*                                                                Y02134
OTA60100 EQU   *                        BRANCH TABLE INITIAL ENTRY
         STM   RTIOT,RUCB,DXXCTL        SAVE REGS FOR RETURN FROM NSL
         ST    RPARC,DXPRPARC           SAVE CURRENT PARM PTR    Y02082
*                                                                Y02082
* COPY THE DCB FROM THE WORK AREA TO USER'S STORAGE              Y02082
*                                                                Y02082
         IECRES INIT,DCBCOPY=FRWKAR,STM=(3,14,WTGPREFX)          Y02082
*
         MODESET EXTKEY=SUPR            UCB KEY                @ZA04224
*        MOUNT BIT OFF = NSL ALREADY VERIFIED                  @ZA04224
         OI    UCBDMCT,UCBMOUNT         INDICATE REVERIFY      @ZA04224
         MODESET EXTKEY=DATAMGT         DATA MANAGEMENT KEY    @ZA04224
*                                                                Y02082
         IECRES SYNCHDCB
*                                                                Y02134
         LR    RD,RES                   SAVE RESIDENT PTR FOR RETURN
*                                                                Y02082
         L     RPAR,WTGPREFX            GET PTR TO PREFIX        Y02080
         USING IECPREFX,RPAR                                     Y02080
         L     RPAR,IECUPRML            GET POINTER TO PARM LIST YM5702
*                                       OF COPIED DCBS           YM5702
         LR    RPARC,RPAR               GET START OF PARAM LIST  Y02082
         LA    RET,K8                   RE-ENTRY FORM NSL
         MVC   WTGMODNM,NSLOHDRI        NSL ROUTINE NAME
         MODESET EXTKEY=SUPR            KEY 0 TO NSLOHDRI        Y02082
         IECRES LOAD,PREFIX=WTGPREFX,BRANCH=DIRECT               Y02080
*
OTA60200 EQU   *                        INITIAL NSL RETURN
*                                                                Y02134
         MVI   DXECB,K32                RESTORE DXECB          @ZA13589
         MVC   WTGMODNM,OPENIDB         RESTORE IFG019 NAME
         LA    RWTGC,WTGENTRY-WTG(,RWTG) POINTER TO FIRST ENTRY  Y02134
         L     RF,WTGPREFX              LOAD POINTER TO PREFIX   Y02134
         L     RPAR,IECUPRML-IECPREFX(,RF) LOAD PTR TO PARM LIST Y02134
         LR    RPARC,RPAR               GET FIRST NSL PARM PTR   Y02082
*                                                                Y02134
OTA60300 EQU   *                        PROCESS DCB
*                                                                Y02134
         CLC   WTGIDTTR(K2),RETID       CHECK FOR THIS MODULE    Y02134
         BNE   OTA60350                 BRANCH IF WRONG MODULE   Y02134
*                                                                Y02134
         L     RCORE,WTGCORE-K1         GET WORK AREA PTR        Y02134
         L     RTIOT,DXTIOTAD           GET CURRENT TIOT PTR     Y02134
         L     RUCB,DXUCBADR            GET CURRENT UCB ADDRESS  Y02134
         STM   RTIOT,RUCB,DXREG9        STORE FOR QUEUED PROCESS Y02134
*                                                                Y02134
         L     RDCB,DXUDCBAD            GET PTR TO USER'S DCB    Y02082
         MODESET KEYADDR=DXUKEY,WORKREG=1 ASSUME USER'S KEY      Y02082
         OC    DCBOFLGS,DCBOFLGS        TOUCH USER'S CORE        Y02082
         LA    R0,ALLBITS-DCBOBUSY      SET MASK FOR BUSY BIT    YM3005
         IC    R1,DCBOFLGS              GET USER'S DCBOFLGS      Y02082
         MODESET EXTKEY=DATAMGT         DATA MGT KEY             YM5311
         OR    R0,R1                    COMBINE COPIED DCBOFLGS  YM3005
         L     RDCB,DXPDCBAD            WITH USER'S DCBOFLGS     YM3005
         IC    R1,DCBOFLGS              MAKING COPIED BUSY BIT   YM3005
         NR    R1,R0                    SAME AS USER'S BUSY BIT  YM3005
         STC   R1,DCBOFLGS              UPDATE COPIED DCBOFLGS   Y02082
*                                                                Y02134
         LA    RET,K4                   ENTRY TO THIS LOAD AFTER Y02134
*                                       FIRST DCB HAS COMPLETED  Y02134
         ST    RET,DXREGE               SAVE ENTRY VALUE         Y02134
*                                                                Y02134
OTA60350 EQU   *                        CHECK FOR LAST DCB       Y02134
*                                                                Y02134
         TM    DXDCBLST,LASTNTRY        IS THIS THE LAST DCB     Y02134
         LA    RPARC,DXDCBLST+L'DXDCBLST GET NEXT PARM POINTER   Y02134
         LA    RWTGC,WTGENTRY+L'WTGENTRY GET NEXT ENTRY POINTER  Y02134
         BNO   OTA60300                 NO, BRANCH               Y02134
*                                                                Y02134
         LR    RPARC,RPAR               GET FIRST NSL PARM PTR   Y02082
         LA    RWTGC,WTGENTRY-WTG(,RWTG) POINTER TO FIRST ENTRY  Y02134
         LR    RES,RD                   RESTORE RESIDENT POINTER
*
OTA60400 EQU   *                        EXIT PREPARATION         Y02134
*                                                                Y02134
         CLC   WTGIDTTR(K2),RETID       CHECK FOR THIS MODULE    Y02134
         BE    OTA60450                 BRANCH IF RIGHT MODULE   Y02134
*                                                                Y02134
         TM    DXDCBLST,LASTNTRY        IS THIS THE LAST DCB     Y02134
         LA    RPARC,DXDCBLST+L'DXDCBLST GET NEXT PARM POINTER   Y02134
         LA    RWTGC,WTGENTRY+L'WTGENTRY GET NEXT ENTRY POINTER  Y02134
         BNO   OTA60400                 NO, BRANCH               Y02134
         B     OTA62300                 BRANCH TO EXIT           Y02134
*                                                                Y02134
OTA60450 EQU   *                        GET POINTERS             Y02134
*                                                                Y02134
         L     RDCB,DXDCBLST            LOAD DCB POINTER         Y02134
         L     RCORE,WTGCORE-K1         GET WORK AREA PTR        Y02134
         L     RTIOT,DXTIOTAD           GET CURRENT TIOT PTR     Y02134
         L     RUCB,DXUCBADR            GET CURRENT UCB ADDRESS  Y02134
*                                                                Y02134
OTA60500 EQU   *                        CHECK MOUNT SW           Y02134
*                                                                Y02134
         TM    UCBDMCT,UCBMOUNT         CK FOR MOUNT SW FOR NSL
         BNO   OTA62200                 BR IF NOT ON             Y02134
*                                                                Y02134
OTA60600 EQU   *                        SET UP RETURN TO IFG0195D
*                                                                Y02134
         MVI   DXRETCOD,K12             SET UP RETURN OFFSET     Y02134
         MVC   DXRETMOD,RETID           SAVE RETURN MODULE       Y02134
*                                                                Y02134
         MODESET EXTKEY=SUPR            UCB KEY                  Y02082
         NI    UCBDMCT,ALLBITS-UCBMOUNT SET NSL REJECT OFF     @ZA03195
         XC    UCBVOLI,UCBVOLI          CLEAR UCB                Y02134
         MODESET EXTKEY=DATAMGT         DATA MANAGEMENT KEY      Y02082
*                                                                Y02134
         LA    R0,K0                    ABEND 413-04 ABEND     @ZA03195
         IECRES LOAD,MODID=ID5D4A,BRCODE=K16,BRANCH=QUEUED       Y02134
*                                                                Y02134
OTA62200 EQU   *                        NEXT MODULE IFG0196J     Y02134
*                                                                Y02134
         MVC   WTGIDTTR,MERGE           MOVE IN NEXT ID          Y02134
         LA    RET,K0                   LOAD BRANCH OFFSET       Y02134
*                                                                Y02134
OTA62300 EQU   *                        BRANCH QUEUED
*                                                                Y02134
         IECRES LOAD,BRANCH=QUEUED                               Y02134
*
         EJECT
*
         XCTLTABL ID=(MERGE,6J,RETID,5D,ID5D4A,4A,               Y02134X
               ,NSLOHDRI),TAG=OPENIDB,                           Y02134X
               BRT=YES,LENGTH=                                   Y02080
*
         IECDSECS CVT,TCB,TIOT,DCB,UCB,MAIN,WTG,PREFX,EXPAND=YES Y02134
         END
