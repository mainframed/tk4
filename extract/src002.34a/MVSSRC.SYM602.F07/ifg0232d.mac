         TITLE 'IFG0232D - TCLOSE DIRECT ACCESS PROCESSING'      S21940
IFG0232D CSECT                                                   S21940
*
***********************************************************************
*                                                                     *
*         VS2 RELEASE 03 DELETIONS/CHANGES                            *
*0000657500                                                     ZA01505
*0000                                                          @ZA02192
*
*         VS1 RELEASE 02 DELETIONS                                    *
*                                                                     *
*0000                                                            XM1011
*         VS2 RELEASE 02 DELETIONS/CHANGES                            *
*0000273750                                                     YA03128
*         RELEASE 21.7 DELETIONS/CHANGES                              *
*0000128500-128900,135000,198000,198600-200000,274600,414000-   SA58019
*0000415000,530000,534600-536000                                SA58019
*0000612000,614000                                              SA57255
*         RELEASE 21 DELETIONS/CHANGES                                *
*                                                                A35953
*0000304800,648000                                               S21940
*D017200,129000-142000,195000,264100-271100,476000              SA48558
*                                                                     *
* MODULE NAME - IFG0232D                                              *
*                                                                     *
* DESCRIPTIVE NAME - TCLOSE DIRECT ACCESS PROCESSING                  *
*                                                                     *
* COPYRIGHT - NONE                                                    *
*                                                                     *
* CHANGE ACTIVITY - SEE DELETIONS/CHANGES FOLLOWING THE CSECT CARD.   *
*                                                                     *
* STATUS CHANGE LEVEL 000                                             *
*                                                                     *
* FUNCTION -                                                          *
*    THIS MODULE CONTAINS THE FOLLOWING FUNCTIONS-                    *
*    1. TCLOSE DIRECT ACCESS INPUT FUNCTION.                          *
*    2. TCLOSE DIRECT ACCESS OUTPUT FUNCTION, PART I.                 *
*                                                                     *
* ENTRY POINTS -                                                      *
*         IFG0232D - REFER TO 'INPUT' FOR A LIST OF CALLING MODULES   *
*                    AND THEIR RESPECTIVE ENTRY CONDITIONS.           *
*                                                                     *
* INPUT -                                                             *
*    ENTERED IN DATA MANAGEMENT KEY                              Y02082
*    REGISTER 14 ('RET') WILL CONTAIN ONE OF THE BRANCH TABLE OFFSETS *
*    LISTED BELOW, THE BRANCH TABLE BEING USED TO DETERMINE AT WHICH  *
*    POINT PROCESSING IS TO BEGIN-                                    *
*       0 - ENTRY FROM IGC0002C TO TCLOSE A DATA SET ON A             *
*           DIRECT ACCESS DEVICE.                                     *
*    REFER TO THE FOLLOWING 'FUNCTION PROLOGS' FOR ADDITIONAL INPUT.  *
*                                                                     *
* OUTPUT -                                                            *
*    EXIT IN DATA MANAGEMENT KEY                                 Y02082
*    REFER TO THE FOLLOWING 'FUNCTION PROLOGS'.                       *
*                                                                     *
* EXTERNAL REFERENCES -                                               *
*         IECPCNVT - A CLOSED SUBROUTINE- RESIDENT IN THE NUCLEUS-    *
*                    WHICH CONVERTS A RELATIVE TRACK ADDRESS AND      *
*                    CONCATENATION NUMBER, 'TTRN', TO AN ABSOLUTE     *
*                    TRACK ADDRESS, 'MBBCCHHR', UTILIZING THE         *
*                    FOLLOWING REGISTER CONVENTIONS-                  *
*                    REGISTER  CONTENTS AT ENTRY     CONTENTS AT EXIT *
*                      0       TTRN TO BE CONVERTED  DESTROYED        *
*                      1       DEB ADDRESS           TRANSPARENT      *
*                      2       ADDR OF 8-BYTE AREA   TRANSPARENT      *
*                              FOR MBBCCHHR                           *
*                      3-8     IGNORED               TRANSPARENT      *
*                      9-13    IGNORED               DESTROYED        *
*                      14      RETURN ADDRESS        TRANSPARENT      *
*                      15      ENTRY POINT ADDRESS   ERROR CODE       *
*                    THE ERROR CODE WILL BE '0' IF THE CONVERSION IS  *
*                    SUCCESSFUL AND '1' IF THE 'TT' EXCEEDS THE       *
*                    NUMBER OF EXTENTS ASSIGNED TO THE DATA SET.      *
*         IECPRLTV - A CLOSED SUBROUTINE- RESIDENT IN THE NUCLEUS-    *
*                    WHICH CONVERTS AN ABSOLUTE TRACK ADDRESS,        *
*                    'MBBCCHHR', TO A RELATIVE TRACK ADDRESS, 'TTR0', *
*                    UTILIZING THE FOLLOWING REGISTER CONVENTIONS-    *
*                    REGISTER  CONTENTS AT ENTRY     CONTENTS AT EXIT *
*                      0       IGNORED               RESULTANT TTR0   *
*                      1       DEB ADDRESS           TRANSPARENT      *
*                      2       ADDR OF MBBCCHHR      DESTROYED        *
*                              TO BE CONVERTED                        *
*                      3-8     IGNORED               TRANSPARENT      *
*                      9-13    IGNORED               DESTROYED        *
*                      14      RETURN ADDRESS        TRANSPARENT      *
*                      15      ENTRY POINT ADDRESS   '0'              *
*         IFG019RA - OPEN/CLOSE/EOV RESIDENT ROUTINE FOR XCTL AND     *
*                    WAIT.                                            *
*                                                                     *
* EXITS, NORMAL -                                                     *
*    REFER TO THE FOLLOWING 'FUNCTION PROLOGS'.                       *
*                                                                     *
* EXITS, ERROR -                                                      *
*         IFG0230P - WHEN AN ERROR CONDITION OCCURS IN THIS MODULE.   *
*                    REFER TO 'OUTPUT' IN THE FOLLOWING 'FUNCTION     *
*                    PROLOGS' FOR A LIST OF ERROR CONDITIONS.         *
*                                                                     *
* TABLES/WORK AREAS -                                                 *
*    REFER TO THE FOLLOWING 'FUNCTION PROLOGS'.                       *
*                                                                     *
* ATTRIBUTES -                                                        *
*    REENTRANT, REFRESHABLE, READ-ONLY, ENABLED, PRIVILEGED           *
*                                                                     *
* CHARACTER CODE DEPENDENCY -                                         *
*    CLASS ONE CHARACTER CODE DEPENDENCY - THE EBCDIC CHARACTER CODE  *
*    WAS USED FOR ASSEMBLY.  THE MODULE MUST BE REASSEMBLED IF A      *
*    DIFFERENT CHARACTER SET IS USED FOR EXECUTION.                   *
*                                                                     *
* NOTES -                                                             *
*    REFER TO THE FOLLOWING 'FUNCTION PROLOG'.                        *
*                                                                     *
***********************************************************************
         EJECT                                                   S21940
*                                                                S21940
         BALR  RBASE,0                  ESTABLISH ADDRESSABILITY S21940
         USING *,RBASE                                           S21940
*                                                                S21940
         USING FORCORE,RCORE                                     S21940
         USING WTG,RWTG                                          Y02080
         USING IHADCB,RDCB                                       S21940
         USING IHADEB,RDEB                                       S21940
         USING TIOENTRY,RTIOT                                    S21940
         USING UNITABLE,R1                                       S21940
         USING UCB,RUCB                                          Y02132
*
*****    DETERMINE TYPE OF ENTRY TO MODULE-
*****    ALTHOUGH THERE IS ONLY A SINGLE ENTRY POINT TO THIS
*****    MODULE AT THIS TIME, THE BRANCH TABLE IS UTILIZED TO PERMIT
*****    ADDITION OF NEW ENTRY POINTS AT A LATER DATE
*
         B     TCD00050(RET)            DETERMINE TYPE OF ENTRY  S21940
TCD00050 EQU   *                                                 S21940
         B     TCD00100                 BR TO PROCESS DA         S21940
         EJECT
***********************************************************************
*                                                                     *
*                          FUNCTION PROLOG                            *
*                                                                     *
***********************************************************************
*                                                                     *
* FUNCTION NAME - 'TCLOSE DIRECT ACCESS INPUT FUNCTION'               *
*                                                                     *
* (STATUS) -                                                          *
*    NOT APPLICABLE                                                   *
*                                                                     *
* FUNCTION -                                                          *
*    1. TEST THE DCB FOR VALID ATTRIBUTES FOR BOTH INPUT AND OUTPUT   *
*       DATA SETS - IT MUST BE OPENED FOR PHYSICAL SEQUENTIAL         *
*       (DSORG=PS) OR PARTITIONED (DSORG=PO) PROCESSING.              *
*       PROCESSING IS TERMINATED IF EITHER CONDITION IS NOT MET.      *
*    2. READ THE FORMAT 1 (F1) DATA SET CONTROL BLOCK (DSCB) FOR      *
*       BOTH INPUT AND OUTPUT PROCESSING.                             *
*    3. A DATA SET ON A DIRECT ACCESS DEVICE IS 'REPOSITIONED' BY     *
*       MODIFYING THE 'DCBFDAD' AND IOB SEEK FIELDS AS INDICATED      *
*       BELOW FOR THE DATA SET ORGANIZATIONS SHOWN.  USE OF TCLOSE    *
*       FOLLOWING INPUT IS NOT PERMITTED FOR DCBS OPENED TO A MEMBER  *
*       OF A PARTITIONED DATA SET.  IT IS ALSO RESTRICTED TO PHYSICAL *
*       SEQUENTIAL DCBS WHICH ARE USED TO PROCESS THE DATA SET        *
*       ORGANIZATIONS SHOWN BELOW:                                    *
*       'REREAD'-                                                     *
*          PS, DA, IS, UNDEFINED (I.E., A VTOC), AND PO NOT OPENED TO *
*             A MEMBER- POINT TO THE BEGINNING OF THE DATA SET USING  *
*             THE FIRST EXTENT IN THE DEB. NOTE THAT THIS POSITIONS   *
*             A PO DATA SET TO THE DIRECTORY.                         *
*       'LEAVE'-                                                      *
*          PS AND PO NOT OPENED TO A MEMBER- POINT TO THE FILE MARK   *
*             THAT DENOTES THE END OF THE DATA SET USING 'DSCLSTAR'   *
*             AND UPDATE 'DCBTRBAL' FROM 'DSCLSTAR' TO REFLECT THE    *
*             NUMBER OF BYTES REMAINING ON THE TRACK.                 *
*       PROCESSING IS TERMINATED FOR ALL OTHER CONDITIONS.            *
*                                                                     *
* ENTRY POINTS -                                                      *
*    REFER TO THE PRECEDING MODULE PROLOG.                            *
*                                                                     *
* INPUT -                                                             *
*    1. REGISTER CONTENTS AT ENTRY ARE AS FOLLOWS-                    *
*       'RDCB' - ADDRESS OF THE DCB BEING PROCESSED.                  *
*       'RCORE'- ADDRESS OF THE WORK AREA FOR THAT DCB.               *
*       'RES'  - ADDRESS OF THE OPEN/CLOSE/EOV RESIDENT ROUTINE.      *
*       'RWTG' - ADDRESS OF THE WHERE-TO-GO TABLE.                    *
*       'RPARC'- ADDRESS OF THE CURRENT TCLOSE PARAMETER LIST ENTRY.  *
*       'RWTGC'- ADDRESS OF THE CURRENT WHERE-TO-GO TABLE ENTRY.      *
*       'RTIOT'- ADDRESS OF THE CURRENT TIOT ENTRY FOR THE DCB.       *
*       'RUCB' - ADDRESS OF THE CURRENT UCB FOR THE DCB.              *
*       'RDEB' - ADDRESS OF THE DCB'S DEB.                            *
*                                                                     *
* OUTPUT -                                                            *
*    1. AT EXIT TO IFG0232Z-                                          *
*       REGISTER CONTENTS ARE THE SAME AS AT ENTRY EXCEPT THAT        *
*          REGISTER 'RET' CONTAINS A BRANCH TABLE OFFSET OF '4'.      *
*    2. AT EXIT TO IFG0230P (PROBLEM DETERMINATION)-                  *
*       REGISTER CONTENTS ARE THE SAME AS AT ENTRY EXCEPT THAT        *
*          REGISTER 'R0' CONTAINS ONE OF THE FOLLOWING INTERNAL       *
*          ERROR CODES (THE CORRESPONDING ABEND AND RETURN CODES ARE  *
*          INDICATED IN PARENTHESES)-                                 *
*          132 (317/04)- ERROR READING THE DSCB.                      *
*          142 (---/00)- MESSAGE ONLY- DCB IS NOT PHYSICAL            *
*                        SEQUENTIAL OR PARTITIONED.                   *
*          143 (---/04)- MESSAGE ONLY- DCB IS OPENED FOR INPUT TO     *
*                        A MEMBER OF A PARTITIONED DATA SET.          *
*                        PROCESSING COULD NOT BE PERFORMED.           *
*          144 (---/08)- MESSAGE ONLY- 'LEAVE' OPTION SPECIFIED FOR   *
*                        AN INVALID DATA SET ORGANIZATION.            *
*                        PROCESSING COULD NOT BE PERFORMED.           *
*       WORK AREA INDICATORS-                                         *
*          'DXCCW1'   - CONTAINS A X'FF' WHEN PROCESSING HAS BEEN     *
*                       TERMINATED ON THE CORRESPONDING DCB.          *
*          'DXCALLID' - CONTAINS THE 2-BYTE ID OF THIS MODULE.        *
*          'DXRETMOD' - CONTAINS THE 5-BYTE ID AND TTR OF THE FINAL   *
*                       LOAD OF TCLOSE WHEN PROCESSING HAS BEEN       *
*                       TERMINATED ON A DCB.                          *
*                                                                     *
* EXTERNAL REFERENCES -                                               *
*    REFER TO THE PRECEDING MODULE PROLOG.                            *
*                                                                     *
* EXITS, NORMAL -                                                     *
*         IFG0232Z - TO PERFORM TCLOSE FINAL PROCESSING.              *
*                                                                     *
* EXITS, ERROR -                                                      *
*    REFER TO THE PRECEDING MODULE PROLOG.                            *
*                                                                     *
* TABLES/WORK AREAS -                                                 *
*    THE TCLOSE WORK AREA AND THE WHERE-TO-GO (WTG)                   *
*    TABLE ARE DESCRIBED BY THE DSECTS AT THE END OF THE LISTING.     *
*                                                                     *
* ATTRIBUTES -                                                        *
*    REFER TO THE PRECEDING MODULE PROLOG.                            *
*                                                                     *
* CHARACTER CODE DEPENDENCY -                                         *
*    REFER TO THE PRECEDING MODULE PROLOG.                            *
*                                                                     *
* NOTES -                                                             *
*                                                                     *
***********************************************************************
         EJECT                                                   S21940
*
*****    INSURE THAT THE DCB HAS PROPER ATTRIBUTES FOR TCLOSE   SA48558
*
TCD00100 EQU   *                                                 S21940
         LA    R1,TABD142               CODE FOR INVALID DCB    SA48558
         NC    DCBDSORG,DCBDSORG        ANY DSORG SPECIFIED?   @ZA02192
         BZ    TCD00105                 NO, ASSUME 'PS'        @ZA02192
         TM    DCBDSORG,DCBORGPS+DCBORGPO  IS DCB 'PS' OR 'PO'  SA58019
         BZ    TCD01560                 ..BR IF NOT- ERROR      SA48558
*                                                               SA48558
*****    PREPARE TO READ THE FORMAT 1 (F1) DSCB                 SA48558
*                                                               SA48558
TCD00105 EQU   *                                               @ZA02192
         TM    UCBJBNR,UCBVRDEV         TEST FOR VIO UCB         Y02132
         BNO   TCD00120                 BR IF NOT                Y02132
TCD00110 EQU   *                        READ VIO DSCB FROM SWA   YM7525
         MVC   DXDSCB(DSCEXT2-DSCFMTID),VDSDSCB-VDSUCB+L'JFCBDSNM(RUCB)
*                                                                Y02132
         OC    DXDSCB(DSCEXT2-DSCFMTID),DXDSCB IS DSCB ZERO?     Y02132
         LA    R1,TABD132               INTERNAL I/O ERROR CODE  Y02132
         BZ    TCD01600                 BR IF YES, I/O ERROR     Y02132
*                                                                Y02132
         XC    DSCEXT2(DSCBEND-DSCEXT2),DSCEXT2 ZERO REMAIN CORE Y02132
*                                                                Y02132
         B     TCD00140                 RESUME PROCESSING        Y02132
*                                                                Y02132
*****    BUILD THE WORK AREA DEB AND THE CHANNEL PROGRAM         Y02132
*                                                                Y02132
TCD00120 EQU   *                        BUILD THE WORK AREA DEB  Y02132
         LA    RC,K16                   SUBTRACT 16 FROM BASE OF DEB
         SR    RDEB,RC                  TO ESTABLISH BASE OF VTOC TRK
         MVC   DXDEBBIN(K2),K1(RDEB)    MOVE BIN NR IN WK DEB
         XC    DXDEBSCC(K4),DXDEBSCC    SET BEGINNING CCHH TO ZERO
         MVC   DXDEBECC(K6),MAXEXTNT    SET ENDING CCHH AND NO.  S21940
*                                       ..OF TRACKS TO MAXIMUM   S21940
*                                       ..POSSIBLE               S21940
         MVC   DXDAADDR+K1(K7),K1(RDEB)  PLACE SEARCH ADDRESS IN SEEK
         AR    RDEB,RC                  POINT BACK TO BASIC DEB  S21940
         LA    RD,DXDAADDR+K3           PLACE ADDRESS OF CCHHR   S21940
         ST    RD,DXCCW1                ..INTO FIRST CCW         S21940
         MVI   DXCCW1,CCWSCHID          SET CCW1 TO 'SEARCH      S21940
*                                       ..ID EQUAL'              S21940
         LA    RD,DXCCW1                PLACE ADDRESS OF SEARCH  S21940
         ST    RD,DXCCW2                ..CCW INTO TIC CCW       S21940
         MVC   DXCCW1+K4(K5),ACCW1CD    SET CMD CHAIN, SILI IND, S21940
*                                       ..AND LENGTH EQUAL 5 IN  S21940
*                                       ..CCW1, MAKE CCW2 A TIC  S21940
         LA    RD,DXDSCB                PLACE ADDR FOR DSCB      S21940
         ST    RD,DXCCW3                ..INTO THE READ DATA CCW S21940
         MVC   DXCCW2+K4(K5),ACCW2C     SET CMD CHAIN IN CCW2,   S21940
*                                       ..MAKE CCW3 'READ DATA'  S21940
         MVC   DXCCW3+K4(K4),ACCW3C     SET LNG EQ 96 IN CCW3    S21940
         BAL   RC,TCD01500              GO TO PERFORM READ OF DSCB
*
         LA    R1,TABD132               INTERNAL ERROR CODE      S21940
         BNO   TCD01600                 BR IF I/O ERROR          S21940
*                                                                Y02132
TCD00140 EQU   *                        SET DSCB READ INDICATOR  Y02132
         OI    DXATDACC,DXATRDDB        DSCB READ                Y02144
*                                                               SA48558
*****    DETERMINE THE NEXT REQUIRED PROCESSING                 SA48558
*                                                               SA48558
TCD00150 EQU   *
         TM    DCBOFLGS,DCBOWRIT        WAS OUTPUT PERFORMED-    S21940
         BO    TCD00800                 ..BR IF YES              S21940
*
*****    TEST FOR VALID OPTIONS FOLLOWING INPUT                 SA58019
*                                                               SA58019
         LA    R1,TABD142               LOAD INVALID DCB CODE   SA58019
         NC    DCBDSORG,DCBDSORG        ANY DSORG SPECIFIED?   @ZA02192
         BZ    TCD00152                 NO, ASSUME 'PS'        @ZA02192
         TM    DCBDSORG,DCBORGPS        IS DCB DSORG 'PS'       SA58019
         BZ    TCD01560                 ..BRANCH IF NOT - ERROR SA58019
TCD00152 EQU   *                                               @ZA02192
         LA    R1,TABD143               INVALID POSITIONING     SA58019
*                                       ..OPTION CODE           SA58019
         TM    JFCBIND1,JFCPDS          IS DCB OPEN TO A MEMBER SA58019
*                                       ..OF A PO DATA SET      SA58019
         BO    TCD00170                 ..YES, BR TO CHECK EOF  YA03128
*                                                               SA58019
         TM    PLISTOPT(RPARC),PLISTLV  IS DISPOSITION 'LEAVE'-  S21940
         BM    TCD00200                 ..BRANCH IF NOT         SA58019
*
*****    PROCESS DATA SETS WITH DISPOSITION OF 'LEAVE'-
*****    UPDATE THE DCB FULL DIRECT ADDRESS ('DCBFDAD') TO POINT
*****    TO THE 'FILE MARK' AND UPDATE THE DCB TRACK BALANCE
*****    ('DCBTRBAL') TO REFLECT THE NUMBER OF BYTES
*****    REMAINING ON THE TRACK
*
         LA    R1,TABD144               CODE FOR 'LEAVE' OPTION SA48558
*                                       ..WITH INVALID DSORG    SA48558
         NC    DSCFILTY,DSCFILTY        ANY DSORG SPECIFIED?   @ZA02192
         BZ    TCD00155                 NO, ASSUME 'PS'        @ZA02192
         TM    DSCFILTY,DS1ORGPS+DS1ORGPO IS DSORG VALID-       SA48558
         BZ    TCD01560                 ..BR IF NOT- ERROR      SA48558
*                                                               SA48558
TCD00155 EQU   *                                               @ZA02192
         MVC   DCBTRBAL,DSCLSTAR+K3     NUMBER OF BYTES
*                                       REMAINING ON TRACK
         MVI   DSCLSTAR+K3,K0           RESET N OF TTRN
         MVC   DXCCW5(K4),DSCLSTAR      PLACE THE TTR0 OF LAST TRACK OF
         L     R0,DXCCW5                DATA SET USED IN PARAMETER REG
         LR    R1,RDEB                  DEB ADDR INTO PARM REG
         LA    RDCB,DCBFDAD             ADDRESS FOR MBBCCHHR TO PARAM
*                                       REGISTER
         STM   RTIOT,RDEB,DXCCW5        SAVE REGISTERS           S21940
         USING CVT,RF                   ESTABLISH CVT BASE       S21940
         L     RF,CVTPTR                LOAD CVT ADDRESS         S21940
         L     RF,CVTPCNVT              LOAD CONVERT RTN ADDR    S21940
         BALR  RET,RF                   CONVERT TTR TO MBBCCHHR  S21940
         LM    RTIOT,RDEB,DXCCW5        RESTORE REGISTERS        S21940
         L     RDCB,0(RPARC)            RESTORE ADDRESS OF CURRENT DCB
         B     TCD00300                 GO SET CURRENT POSITION IN BSAM
*
TCD00170 EQU   *                        TEST IF EOF REACHED     YA03128
         TM    DCBOFLGS,DCBOEOF         HAS EOF BEEN REACHED    YA03128
         BO    TCD00600                 ..YES, GO TO FINAL LOAD YA03128
         B     TCD01560                 ..NO, ISSUE ERROR MSG   YA03128
         EJECT                                                   S21940
*
*****    PROCESS DATA SETS WITH DISPOSITION OF 'REREAD'-
*****    INITIALIZE THE DCB FULL DIRECT ADDRESS ('DCBFDAD') TO
*****    POINT TO THE BEGINNING OF THE FIRST EXTENT OF THE
*****    DATA SET AND THE DCB TRACK BALANCE ('DCBTRBAL')
*****    TO REFLECT A FULL TRACK
*
TCD00200 EQU   *
         XC    DCBFDAD(K8),DCBFDAD      ZERO DISK ADDRESS
         MVC   DCBFDAD+K1(K6),DEBBINUM  SET BBCCHH TO START OF EXTENT
*                                       M,R = 0
         L     RC,DCBDVTBL              PICK UP BASE OF DEVICE TABLE
         MVC   DCBTRBAL,UNITTRLN-UNITABLE(RC)  SET REMAINING BYTES=MAX
*
*****    UPDATE THE IOB SEEK ADDRESS TO CORRESPOND TO 'DCBFDAD'
*
TCD00300 EQU   *
         L     RD,DCBIOBA               ADDR OF PREVIOUS IOB
         LA    RD,0(,RD)                CLEAR HI ORDER BYTE
         LR    R0,RD                    SAVE ADDR OF 1ST FOR COMPARE
         SR    RC,RC                    CLEAR REGISTER
         LR    R1,RD                    COPY IOB/ICB ADDR
         TM    DCBCIND2,DCBOEOF         IS PCI USED
         BO    TCD00400                 YES, BRANCH
         LA    R1,K8(RD)                ADJUST IOB FOR NORMAL SCHED
         LA    RC,K8                    OFFSET IN IOB FOR NORMAL SCHED
TCD00400 EQU   *                        UPDATE FIRST ICB/IOB     Y02082
         MODESET KEYADDR=DXUKEY,WORKREG=14  IOB IN USER KEY      Y02082
         MVC   K32(K8,R1),DCBFDAD       UPDATE FIRST ICB/IOB     Y02082
*
TCD00500 EQU   *
         L     RD,0(,RD)                ADDR OF NEXT ICB/IOB IN CHAIN
         LA    RD,0(,RD)                CLEAR HIGH ORDER BYTE
         LA    R1,0(RC,RD)              FORM IOB OR ICB ADDR
         MVC   K32(K8,R1),DCBFDAD       UPDATE ICB/IOB FDAD
*
         CR    RD,R0                    IS THIS LAST IN CHAIN
         BNE   TCD00500                 ..BR IF NOT- UPDATE      S21940
*                                       ..NEXT IOB IN CHAIN      S21940
         MODESET EXTKEY=DATAMGT         TCLOSE KEY               Y02082
TCD00600 EQU   *                                                 S21940
         LA    RF,MOD2D2Z               PT TO ID/TTR OF TCLOSE   S21940
*                                       ..FINAL FUNCTION         S21940
         LA    RET,K4                   LOAD BR TABLE OFFSET FOR S21940
*                                       ..TCLOSE FINAL FUNCTION  S21940
         B     TCD01400                 ..AND BRANCH TO EXIT     S21940
         EJECT
***********************************************************************
*                                                                     *
*                          FUNCTION PROLOG                            *
*                                                                     *
***********************************************************************
*                                                                     *
* FUNCTION NAME - 'TCLOSE DIRECT ACCESS OUTPUT FUNCTION', PART I      *
*                                                                     *
* (STATUS) -                                                          *
*    NOT APPLICABLE                                                   *
*                                                                     *
* FUNCTION -                                                          *
*    1. THE DSCB IS UPDATED TO REFLECT THE ADDRESS OF THE LAST        *
*       RECORD WRITTEN AND THE NUMBER OF BYTES REMAINING ON THE       *
*       TRACK ('DSCLSTAR'), AND IS THEN REWRITTEN TO THE VTOC.        *
*       TCLOSE FOLLOWING OUTPUT IS RESTRICTED TO THE FOLLOWING        *
*       CONDITIONS:                                                   *
*       - PHYSICAL SEQUENTIAL DCBS PROCESSING PHYSICAL SEQUENTIAL     *
*         DATA SETS.                                                  *
*       - PHYSICAL SEQUENTIAL DCBS OPENED TO AN ENTIRE PARTITIONED    *
*         DATA SET, IN WHICH CASE THE 'REREAD' OPTION REPOSITIONS     *
*         THE USER TO THE DIRECTORY.                                  *
*       - PHYSICAL SEQUENTIAL DCBS OPENED TO A MEMBER OF A            *
*         PARTITIONED DATA SET, FOR WHICH 'LEAVE' IS THE ONLY         *
*         VALID POSITIONING OPTION.                                   *
*       - PARTITIONED DCBS PROCESSING PARTITIONED DATA SETS, FOR      *
*         WHICH 'LEAVE' IS THE ONLY VALID POSITIONING OPTION.         *
*       PROCESSING IS TERMINATED FOR ALL OTHER CONDITIONS.            *
*                                                                     *
* ENTRY POINTS -                                                      *
*    REFER TO THE PRECEDING MODULE PROLOG.                            *
*                                                                     *
* INPUT -                                                             *
*    1. REGISTER CONTENTS AT ENTRY ARE AS FOLLOWS-                    *
*       'RDCB' - ADDRESS OF THE DCB BEING PROCESSED.                  *
*       'RCORE'- ADDRESS OF THE WORK AREA FOR THAT DCB.               *
*       'RES'  - ADDRESS OF THE OPEN/CLOSE/EOV RESIDENT ROUTINE.      *
*       'RWTG' - ADDRESS OF THE WHERE-TO-GO TABLE.                    *
*       'RPARC'- ADDRESS OF THE CURRENT TCLOSE PARAMETER LIST ENTRY.  *
*       'RWTGC'- ADDRESS OF THE CURRENT WHERE-TO-GO TABLE ENTRY.      *
*       'RTIOT'- ADDRESS OF THE CURRENT TIOT ENTRY FOR THE DCB.       *
*       'RUCB' - ADDRESS OF THE CURRENT UCB FOR THE DCB.              *
*       'RDEB' - ADDRESS OF THE DCB'S DEB.                            *
*                                                                     *
* OUTPUT -                                                            *
*    1. AT EXIT TO IFG0232J-                                          *
*       REGISTER CONTENTS ARE THE SAME AS AT ENTRY EXCEPT THAT        *
*          REGISTER 'RET' CONTAINS A BRANCH TABLE OFFSET OF '0'.      *
*    1. AT EXIT TO IFG0232Z-                                          *
*       REGISTER CONTENTS ARE THE SAME AS AT ENTRY EXCEPT THAT        *
*          REGISTER 'RET' CONTAINS A BRANCH TABLE OFFSET OF '0'.      *
*    2. AT EXIT TO IFG0230P (PROBLEM DETERMINATION)-                  *
*       REGISTER CONTENTS ARE THE SAME AS AT ENTRY EXCEPT THAT        *
*          REGISTER 'R0' CONTAINS ONE OF THE FOLLOWING INTERNAL       *
*          ERRORS CODES (THE CORRESPONDING ABEND AND RETURN CODES ARE *
*          INDICATED IN PARENTHESES)-                                 *
*          133 (417/04)- ERROR WRITING THE DSCB.                      *
*          145 (---/0C)- MESSAGE ONLY- OUTPUT PROCESSING REQUESTED    *
*                        FOR AN INVALID DATA SET ORGANIZATION.        *
*                        PROCESSING COULD NOT BE PERFORMED.           *
*          151 (---/10)- MESSAGE ONLY- 'REREAD' OPTION SPECIFIED FOR  *
*                        AN INVALID DATA SET ORGANIZATION.            *
*                        PROCESSING COULD NOT BE PERFORMED.           *
*       WORK AREA INDICATORS-                                         *
*          'DXCCW1'   - CONTAINS A X'FF' WHEN PROCESSING HAS BEEN     *
*                       TERMINATED ON THE CORRESPONDING DCB.          *
*          'DXCALLID' - CONTAINS THE 2-BYTE ID OF THIS MODULE.        *
*          'DXRETMOD' - CONTAINS THE 5-BYTE ID AND TTR OF THE FINAL   *
*                       LOAD OF TCLOSE WHEN PROCESSING HAS BEEN       *
*                       TERMINATED ON A DCB.                          *
*                                                                     *
* EXTERNAL REFERENCES -                                               *
*    REFER TO THE PRECEDING MODULE PROLOG.                            *
*                                                                     *
* EXITS, NORMAL -                                                     *
*         IFG0232J - TO CREATE OUTPUT USER TRAILER LABELS.            *
*         IFG0232Z - TO WRITE A FILE MARK AND THEN PERFORM TCLOSE     *
*                    FINAL PROCESSING.                                *
*                                                                     *
* EXITS, ERROR -                                                      *
*    REFER TO THE PRECEDING MODULE PROLOG.                            *
*                                                                     *
* TABLES/WORK AREAS -                                                 *
*    THE TCLOSE WORK AREA AND THE WHERE-TO-GO (WTG)                   *
*    TABLE ARE DESCRIBED BY THE DSECTS AT THE END OF THE LISTING.     *
*                                                                     *
* ATTRIBUTES -                                                        *
*    REFER TO THE PRECEDING MODULE PROLOG.                            *
*                                                                     *
* CHARACTER CODE DEPENDENCY -                                         *
*    REFER TO THE PRECEDING MODULE PROLOG.                            *
*                                                                     *
* NOTES -                                                             *
*                                                                     *
***********************************************************************
         EJECT                                                   S21940
*
*****    TEST FOR VALID POSITIONING OPTIONS FOLLOWING OUTPUT    SA58019
*
TCD00800 EQU   *
         LA    R1,TABD145               CODE FOR INVALID DSORG  SA48558
         NC    DSCFILTY,DSCFILTY        ANY DSORG SPECIFIED?   @ZA02192
         BZ    TCD00840                 NO, ASSUME 'PS'        @ZA02192
         TM    DSCFILTY,DS1ORGPS        IS DSORG EQ 'PS'-       SA48558
         BO    TCD00840                 ..BRANCH IF YES         SA58019
         TM    DSCFILTY,DS1ORGPO        IS DATA SET ORG 'PO'    SA58019
         BZ    TCD01560                 ..BRANCH IF NOT - ERROR SA58019
         TM    DCBDSORG,DCBORGPO        IS DCB DSORG 'PO'       SA58019
         BO    TCD00820                 ..BRANCH IF YES - TEST  SA58019
*                                       ..FOR 'LEAVE' OPTION    SA58019
         TM    JFCBIND1,JFCPDS          IS DCB OPEN TO A MEMBER SA58019
*                                       ..OF A PO DATA SET      SA58019
         BZ    TCD00840                 ..BRANCH IF NOT - ALL   SA58019
*                                       ..OPTIONS ALLOWED       SA58019
TABD151  EQU   151                      TCLOSE INTERNAL CODE    SA58019
TCD00820 EQU   *                                                SA58019
         LA    R1,TABD151               LOAD CODE FOR 'REREAD'  SA58019
*                                       ..OPTION WITH INVALID   SA58019
*                                       ..DSORG                 SA58019
         TM    PLISTOPT(RPARC),PLISTLV  IS DISPOSITION 'LEAVE'  SA58019
         BM    TCD01560                 ..BRANCH IF NOT - ERROR SA58019
*                                                               SA58019
*****    CALCULATE THE 'TTR0' OF THE LAST BLOCK WRITTEN         SA58019
*                                                               SA58019
TCD00840 EQU   *                                                SA58019
         L     RUCB,DXUCBADR            LOAD UCB ADDR FROM TIOT  Y02082
         STM   RTIOT,RC,DXCCW5          SAVE REGISTERS
         LR    R1,RDEB                  ADDRESS OF USER'S DEB TO PARAM
         LA    RDCB,DCBFDAD             ADDRESS OF MBBCCHHR TO PARAM
         L     RC,CVTPTR                PICK UP ADDRESS OF CONVERT
*                                       ROUTINE
         L     RF,CVTPRLTV-CVT(0,RC)    FROM COMMUNICATION VECTOR TABLE
         BALR  RET,RF                   CONVERT MBBCCHHR TO TTR0
         LM    RTIOT,RC,DXCCW5          RESTORE REGISTERS
         L     RDCB,0(RPARC)            RESTORE ADDRESS OF
*                                       THE USER'S DCB
         ST    R0,DXCCW5                THE TTR OF LAST TRACK USED
         MODESET EXTKEY=SCHED           ASSUME KEY ZERO          Y02082
         NI    TIOERLOC+K1,CHARFF-LASTNTRY  ZERO HI ORDER BIT
         LTR   R0,R0                    NO DATA IF ZERO
         BZ    TCD00900                 BR-NO DATA EXISTS
         OI    TIOERLOC+K1,LASTNTRY     INDICATE DATA EXISTS
TCD00900 EQU   *
         MODESET EXTKEY=DATAMGT         DATA MANAGEMENT KEY      Y02082
         OI    DSCDSIND,LASTNTRY        SET LAST VOL INDICATOR  SA57255
         MVC   DSCLSTAR(K3),DXCCW5      SET LAST BLOCK PTR IN   SA57255
*                                       F1 DSCB                 SA57255
         MVC   DSCLSTAR+K3(K2),DCBTRBAL SET TRACK BALANCE       SA57255
*                                       IN F1 DSCB              SA57255
         OI    DXATDACC,DXATTRAK        LAST TRK USED AND TRK    Y02144
*                                       BALANCE FIELDS UPDATED   Y02144
         TM    UCBJBNR,UCBVRDEV         TEST FOR VIO UCB         Y02132
         BNO   TCD00930                 BR IF NOT                Y02132
*                                                                Y02132
         MODESET EXTKEY=SCHED           ASSUME SCHEDULER KEY     Y02132
*                                                                Y02132
*        WRITE DSCB OUT TO SWA                                   Y02132
*                                                                Y02132
         MVC   VDSDSCB-VDSUCB+L'JFCBDSNM(DSCEXT2-DSCFMTID,RUCB),DXDSCB
*                                                                Y02132
         MODESET EXTKEY=DATAMGT         DATA MANAGEMENT KEY      Y02132
*                                                                Y02132
         B     TCD00960                 RESUME PROCESSING        Y02132
*
*****    WRITE THE UPDATED DSCB TO THE VTOC
*
TCD00930 EQU   *                        WRITE UPDATED F1 DSCB    Y02132
         MVC   DXCCW4(K24),DXCCW1       APPEND WRITE CK CCW'S TO CP
         LA    RF,DXCCW4
         ST    RF,DXCCW5                TIC BACK TO CCW4
         MVI   DXCCW5,CCWTIC            RESTORE TIC OP
         OI    DXCCW3+K4,HEX40          CC FLAG ON IN WRITE CCW
         OI    DXCCW6+K4,DCBMRKEY       SKIP FLAG ON IN READ CCW
         MVI   DXCCW3,CCWWRTDA          WRITE DISK OPERATION CODE
         BAL   RC,TCD01500              GO TO WRITE THE DSCB
         LA    R1,TABD133               INTERNAL ERROR CODE      S21940
         BNO   TCD01600                 BR IF I/O ERROR          S21940
         OI    DXATDACC,DXATUPDB        UPDATED DSCB WRITTEN     Y02144
*
*****    RESTORE USER'S DEB INFORMATION TO WORK DEB
*
TCD00960 EQU   *                        UPDATE WORK AREA DEB     Y02132
         MVC   DXDEBUCB(K6),DEBUCBAD    MOVE USER'S UCB ADDR AND S21940
*                                       ..BIN NO. TO WORK DEB    S21940
*
*****    DETERMINE WHETHER OR NOT USER LABELS ARE TO BE PROCESSED
*
         LA    RF,MOD2D2Z               PT TO ID/TTR OF FILE     S21940
*                                       ..MARK MODULE            S21940
         TM    JFCBLTYP,JFCSUL          WAS 'SUL' SPECIFIED-     S21940
         BNO   TCD01200                 ..BR IF NOT- BYPASS LBLS S21940
         TM    JFCBTSDM,JFCSDS          IS DATA SET SYSOUT       S21940
         BO    TCD01200                 ..BY IF YES- BYPASS LBLS S21940
         L     RC,DCBEXLST              YES,PICK UP EXIT LIST FROM THE
         LA    RC,0(RC)                 ZERO OUT HI ORDER BYTE OF EXIT
         LTR   RC,RC                    IS THE EXIT LIST ZERO
         BZ    TCD01200                 ..BR IF YES- BYPASS LBLS S21940
         MODESET KEYADDR=DXUKEY,WORKREG=1 EXIT LIST IN USER KEY ZA01505
TCD01000 EQU   *                                                 S21940
         CLI   0(RC),XLOUTL             IS THIS THE CORRECT EXIT S21940
         BE    TCD01100                 ..BR IF YES- CONTINUE    S21940
         CLI   0(RC),XLOUTL+LASTNTRY    SEE IF LAST INDICATOR ALSO ON
         BE    TCD01100                 ..BR IF YES- CONTINUE    S21940
         TM    0(RC),LASTNTRY           NO,IS THIS THE LAST ENTRY IN EX
         LA    RC,K4(RC)                INCRE BASE REG TO POINT TO NEXT
         BNO   TCD01000                 ..BR IF NOT- TEST AGAIN  S21940
         B     TCD01200                 IGNORE USER LABEL EXIT   XM1011
TCD01100 EQU   *                                                 S21940
         L     RC,K0(,RC)               LOAD ENTRY ADDRESS AND   S21940
         LA    RD,K0(,RC)               ..CLEAR HIGH-ORDER BYTE  S21940
         LTR   RD,RD                    IS THE ENTRY ADDR ZERO-  S21940
         BZ    TCD01200                 ..BR IF YES- BYPASS LBLS S21940
         LA    RF,MOD2D2J               PT TO ID/TTR OF USER     S21940
*                                       ..LABEL MODULE           S21940
TCD01200 EQU   *                                                 S21940
         MODESET EXTKEY=DATAMGT         RET DATAMGT KEY          Y02082
         SR    RET,RET                  ZERO BR TABLE OFFSET     S21940
         EJECT
***********************************************************************
*              EXIT                                                   *
***********************************************************************
*
TCD01400 EQU   *
         IECRES LOAD,MODID=(RF),BRCODE=(RET),BRANCH=QUEUED       Y02080
         SPACE 2
***********************************************************************
*              SUBROUTINES                                            *
***********************************************************************
*
*****    CLOSED SUBROUTINE TO PERFORM I/O OPERATIONS
*****       ENTRY IS AS FOLLOWS-
*****          BAL   RC,TCD01500
*****       UPON RETURN TO THE CALLER, THE CONDITION CODE IS SET AS
*****       FOLLOWS-
*****          '0'- AN I/O ERROR OCCURRED
*****          '1'- THE I/O OPERATION COMPLETED WITHOUT ERROR
*
TCD01500 EQU   *
         EXCP  DXIOB
         IECRES WAIT
         TM    DXECB,ECBNOERR           IS THERE AN UNUSUAL I/O COND
         BCR   15,RC                    RETURN TO CALLER
         SPACE 2
***********************************************************************
*              ERROR PROCESSING                                       *
***********************************************************************
*
TCD01560 EQU   *                                                SA48558
         MVI   DXCCW1,ALLBITS           IND THAT PROCESSING     SA48558
*                                       ..HAS BEEN TERMINATED   SA48558
         MVC   DXRETMOD,MOD2D2Z         PLACE RETURN ID/TTR     SA48558
*                                       ..INTO SAVE AREA        SA48558
         LA    RET,K4                   LD BR TABLE OFFSET FOR  SA48558
*                                       ..TCLOSE FINAL FUNCTION SA48558
TCD01600 EQU   *
         DMABCOND (R1),ABEND2D          PROCESS THE ERROR        S21940
         EJECT
***********************************************************************
*              CONSTANTS                                              *
***********************************************************************
*
ACCW1CD  DC    X'6000000508'            COMM CHAIN,SILI,5 CHAR,TIC OPCD
ACCW2C   DC    X'4000000086'            COMMAND CHAIN,READ DATA OPCD
ACCW3C   DC    X'00000060'              96 CHARACTERS
MAXEXTNT DC    X'FFFFFFFF7FFF'          HIGHEST POSSIBLE CCHH    S21940
*                                       ..AND NO. OF TRKS ON A   S21940
*                                       ..DEVICE
         SPACE 2
***********************************************************************
*              XCTL TABLE                                             *
***********************************************************************
*
XCTLTB2D XCTLTABL ID=(ABEND2D,0P,MOD2D2J,2J,MOD2D2Z,2Z),SVC=023, Y02080X
               BRT=YES,LENGTH=                                   Y02080
         SPACE 2
         IECDSECS CVT,TCB,TIOT,DCB,DEB,MAIN,ICB,UNITTAB,WTG,     Y02080*
               PREFX,UCB,EXPAND=YES                              Y02132
         IDDVDSCB                                                Y02132
         IECEQU
         END
