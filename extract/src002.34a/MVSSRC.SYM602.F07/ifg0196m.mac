         TITLE 'IFG0196M/OPEN - DCB TO JFCB MERGE'
         COPY  LCGASMSW
IFG0196M CSECT
***********************************************************************
*             VS2037 DELETIONS/CHANGES                                *
*0000339000-343000                                             @ZA25724
*315000-316000                                                 @ZA14154
*D315000-31600                                                 @ZA33498
***********************************************************************
* MODULE NAME = IFG0196M (OS/VS2)                                     *
*                                                                     *
* DESCRIPTIVE NAME = DCB TO JFCB MERGE                                *
*                                                                     *
* COPYRIGHT = NONE                                                    *
*                                                                     *
* STATUS = RELEASE 2, LEVEL 0                                         *
*                                                                     *
* FUNCTION =                                                          *
*        1. MERGE DCB FIELDS TO JFCB FIELDS.  THE FIELDS MERGED ARE   *
*        A FUNCTION OF THE ACCESS METHOD USED.                        *
*                                                                     *
*        2. TEST FOR CONFLICTS BETWEEN ASCII AND EBCDIC DATA SET      *
*        SPECIFICATIONS.                                              *
*                                                                     *
* NOTES = SEE BELOW                                                   *
*                                                                     *
*    DEPENDENCIES =                                                   *
*            CLASS ONE CHARACTER CODE.  THE EBCDIC CHARACTER CODE     *
*            WAS USED FOR ASSEMBLY.  THE MODULE MUST BE REASSEMBLED   *
*            IF A DIFFERENT CHARACTER SET IS USED FOR EXECUTION.      *
*                                                                     *
*    RESTRICTIONS = NONE                                              *
*                                                                     *
*    REGISTER CONVENTIONS =                                           *
*            R2 POINTS TO DCB                                         *
*            R4 POINTS TO OPEN WORK AREA                              *
*            R5 POINTS TO THE RESIDENT ROUTINE                        *
*            R6 POINTS TO THE WTG TABLE                               *
*            R7 POINTS TO THE CURRENT PARAMETER LIST ENTRY            *
*            R8 POINTS TO THE CURRENT WTG TABLE ENTRY                 *
*            R9 POINTS TO THE DD ENTRY IN THE TIOT                    *
*            R10 POINTS TO THE UCB                                    *
*                                                                     *
*    PATCH LABEL = SEE NEXT TO LAST LABEL BEFORE ORG STATEMENT AT END *
*                  OF LISTING.                                        *
*                                                                     *
* MODULE TYPE = CONTROL (OPEN, CLOSE, EOV DATA MANAGEMENT)            *
*                                                                     *
*    PROCESSOR = ASSEMBLER XF                                         *
*                                                                     *
*    MODULE SIZE = SEE EXTERNAL SYMBOL DICTIONARY OR LOC FIELD ON     *
*                  ORG STATEMENT AT END OF LISTING                    *
*                                                                     *
*    ATTRIBUTES = REENTRANT, REFRESHABLE,READ-ONLY, ENABLED,          *
*                  PRIVILEGED, SUPERVISOR STATE, DATA MANAGEMENT KEY, *
*                  LINK PACK AREA RESIDENT/PAGEABLE                   *
*                                                                     *
* ENTRY POINT = IFG0196M FROM IFG0196L                                *
*                                                                     *
*    PURPOSE = SEE FUNCTION                                           *
*                                                                     *
*    LINKAGE =                                                        *
*        IECRES LOAD,ID6L6M,BRANCH=QUEUED                             *
*                                                                     *
* INPUT = STANDARD                                                    *
*                                                                     *
* OUTPUT = MERGED FIELDS IN THE JFCB IN THE DCB WORK AREA             *
*                                                                     *
* EXIT-NORMAL =                                                       *
*        IFG0196N TAPE OUTPUT SECURITY PROCESSING                     *
*        IFG0196P USER LABEL (ALIAS FOR IFG0555J)                     *
*        IFG0196O NSL OUTPUT                                          *
*        IFG0196V ACCESS METHOD DETERMINATION                         *
*                                                                     *
* EXIT-ERROR =                                                        *
*        IFG0190P PROBLEM DETERMINATION                               *
*           013 ABEND FOR MIXED ASCII AND NON-ASCII TAPE OPTIONS      *
*                                                                     *
* EXTERNAL REFERENCES = SEE BELOW                                     *
*                                                                     *
*    ROUTINES = NONE                                                  *
*                                                                     *
*    DATA AREAS = NONE                                                *
*                                                                     *
*    CONTROL BLOCK = NONE                                             *
*                                                                     *
* TABLES = NONE                                                       *
*                                                                     *
* MACROS = IECDSECS, MODESET, DMABCOND, IECRES, XCTLTABL, IECEQU      *
*                                                                     *
* CHANGE ACTIVITY = NEW RELEASE (LEVEL 0)                             *
*                                                                     *
***********************************************************************
         EJECT
         IECDSECS TIOT,UCB,DCB,MAIN,WTG,DSAB                     Y02134
         IECEQU ,                       DEFINE EQUATES
*
         USING IHADCB,RDCB              DEFINE BASE TO USER'S DCB
         USING FORCORE,RCORE            DEFINE BASE TO MAIN WORK AREA
         USING WTG,RWTG                 WHERE-TO-GO TABLE        Y02080
         USING TIOENTRY,RTIOT           DEFINE BASE TO TIOT DD ENTRY
         USING UCBOB,RUCB               DEFINE BASE TO MAIN UCB
*
         BALR  RBASE,0                  ESTABLISH BASE REGISTER
         USING *,RBASE                  DEFINE BASE REGISTER
*
         MVI   DXCCW11+K1,0             ZERO REVERSE MERGE FIELD Y02134
         BAL   RC,OMG47700              GO DETERMINE IF HIARCHY IS MEAN
         B     OMG41000                 RETURN HERE IF NOT MEANINGFUL
*  RETURN HERE IF HIARCHY IS MEANINGFUL.
         MVC   DXCCW11(K1),JFCBFTEK     PRESERVE SETTING OF HIARCHY
         NI    DXCCW11,JFCBH1           ONLY SAVE HIARCHY        A36911
         NI    JFCBFTEK,X'FF'-JFCBH1    FLAG,  THEN INSURE THAT
*  HIARCHY FLAG DOES NOT INTERFERE WITH MERGE OF BFTEK/BFALN
*  FROM DCB TO JFCB (WHEN INPUT/INOUT).
OMG41000 EQU   *                        MERGE PROCESS
         TM    JFCBTSDM,JFCNDCB         TEST IF DCB TO JFCB MERGE IS
         BO    OMG46500                 INHIBITED, BR IF YES
*
*
*        DCB/JFCB MERGE
*
         TM    DCBMACRF,DCBMEXCP        CHECK FOR EXCP
         BNO   OMG43400                 BR IF NOT EXCP
         LA    R1,K15                   POINT TO FBE IN MERGE TABLE
         TM    DCBMACRF,DCBMFOUN        IS F.B.E. PRESENT
         BZ    OMG46800                 BR NO, SKIP BACK MERGE
         TM    DCBMACRF,DCBMAPP         EXCP INTERFACE PRESENT
         BO    OMG44800                 BR IF YES TO MERGE
         LA    R1,K16                   DO NOT MERGE OPTCD FIELD
         B     OMG44800                 BRANCH TO MERGE
*
OMG43400 EQU   *                        NOT EXCP
         LA    RF,K5                    SET INDICATOR FOR BSAM
         TM    DCBMACRF,DCBMREAD        IS MACRF = READ
         BO    OMG43600                 BR IF YES, BSAM
         TM    DCBMACRF+K1,DCBMWRIT     IS MACRF = WRITE
         BO    OMG43600                 BR IF YES, BSAM
         SR    RF,RF                    SET INDICATOR FOR QSAM
OMG43600 EQU   *                        BSAM
         TM    DCBDSORG,DCBORGPS        IS IT PHYSICAL SEQUENTIAL
         BO    OMG43800                 BR IF YES
         TM    DCBDSORG,DCBORGPO        IS IT PARTITIONED
         BO    OMG44000                 BR IF YES, BPAM
         SR    R1,R1                    POINT TO ISAM IN MERGE TABLE
         TM    DCBDSORG,DCBORGIS        IS IT INDEXED SEQUENTIAL
         BO    OMG44800                 BR IF YES TO MERGE
         TM    DCBDSORG,DCBORGDA        IS IT DIRECT ACCESS ORG
         LA    R1,K8                    POINT TO DAM IN MRG TABLE
         BO    OMG44800                 BR IF YES
         TM    DCBDSORG,DCBORGCX        IS IT COMM LINE GROUP
         BO    OMG44200                 BR IF YES
         TM    DCBDSORG+K1,DCBORGTX     IS THIS TCAM LINE GROUP
         BO    OMG44200                 BR IF YES
         TM    DCBDSORG,DCBORGCQ        IS IT COMM D.A. GROUP
         BO    OMG46800                 BR IF YES, CQ ACCESS METHOD
         TM    DCBDSORG+K1,DCBORGTQ     IS THIS TCAM MSG QUEUE
         BO    OMG46800                 BR IF YES
         TM    DCBDSORG,DCBORGMQ        IS IT PROCESS PGM MSG QUEUE
         BO    OMG44600                 BR IF YES
         TM    DCBDSORG+K1,DCBORGTQ+DCBORGGR  IS IT TCAM TQ OR GRAPHIC
         BM    OMG44100                 BR IF YES
*
*                                       DEFAULT TO PS/PO
*
OMG43800 EQU   *                        PHYSICAL SEQUENTIAL
         LA    R1,K5                    POINT TO QSAM IN MERGE TABLE
         LTR   RF,RF                    READ/WRITE OR GET/PUT
         BZ    OMG44800                 BR IF GET/PUT
OMG44000 EQU   *                        BPAM
         LA    R1,K10                   POINT TO SAM AND PAM IN MRGTAB
         B     OMG44800                 BR TO MERGE
*
OMG44100 EQU   *                        TCAM TQ OR GRAPHIC
         LA    R1,K22                   PT TO GRAPHICS IN MERGE TABLE
         B     OMG44800                 BR IF MERGE
*
OMG44200 EQU   *                        TCAM LINE GROUP
         LA    R1,K18                   POINT TO TAM IN MERGE TABLE
         LTR   RF,RF                    READ/WRITE OR GET/PUT
         BNZ   OMG44800                 BR IF R/W TO MERGE
         TM    0(RPARC),PLISTM0C        CHECK FOR INPUT OR INOUT
         BNZ   OMG44300                 BR IF NO
         CLI   JFCINTVL,K0              IS INTVL=0
         BNE   OMG44400                 BR IF NO
OMG44300 EQU   *                        NOT INPUT OR INOUT
         MVC   JFCINTVL,K24(RDCB)       ****MERGE INTVL****
         MVI   DXCCW11+K1,K255          SET REVERSE MERGE FIELD  Y02134
OMG44400 EQU   *                        NOT INTVL
         TM    0(RPARC),PLISTM0C        CHECK FOR INPUT OR INOUT
         BNZ   OMG44500                 BR IF NO
         CLI   JFCCPRI,K0               IS CPRI=0
         BNE   OMG44800                 BR IF NO
OMG44500 EQU   *                        NOT INPUT OR INOUT
         MVC   JFCCPRI,K32(RDCB)        ****MERGE CPRI****
         MVI   DXCCW11+K1,K255          SET REVERSE MERGE FIELD  Y02134
         B     OMG44800                 BR TO MERGE
OMG44600 EQU   *                        PROCESS PGM MSG QUEUE
         LA    R1,K18                   POINT TO TAM IN MERGE TABLE
         LTR   RF,RF                    READ/WRITE OR GET/PUT
         BNZ   OMG44800                 BR IF READ/WRITE
         TM    0(RPARC),PLISTM0C        CHECK FOR INPUT OR INOUT
         BNZ   OMG44700                 BR IF NO
         NC    JFCSOWA,JFCSOWA          IS SOWA = 0
         BNZ   OMG44800                 BR IF NO
OMG44700 EQU   *                        NOT INPUT OR INOUT
         MVC   JFCSOWA,24(RDCB)         ****MERGE SOWA****
         MVI   DXCCW11+K1,K255          SET REVERSE MERGE FIELD  Y02134
*
OMG44800 EQU   *                        SOWA NOT ZERO
*
***********************************************************************
*
*        DCB TO JFCB MERGE ROUTINE
*
*
         LR    R0,RDCB                  DCB WORK REGISTER
         LA    RET,OMG4800K(R1)         DCB DISP PTR FOR MERGE  SA56418
         LA    RF,OMG4840K(R1)          JFCB DISP PTR FOR MERGE SA56418
         LA    RD,OMG4860K(R1)          FIELD LENGTHS FOR JFCB- SA56418
*                                       ..DCB MERGE             SA56418
*
OMG44900 EQU   *                        START LOOP PROCESS
         LA    RUCB,DXJBF               JFCB ADDRESS
         SR    RC,RC                    CLEAR REGISTER
         IC    RC,0(,RET)               PICK UP DCB DISP
         AR    RDCB,RC                  POINT TO DCB FIELD
         IC    RC,0(,RF)                PICK UP JFCB DISP
         AR    RUCB,RC                  POINT TO JFCB FIELD
         IC    RC,0(,RD)                PICK UP FIELD LENGTH-1
         TM    0(RPARC),PLISTM0C        CHECK FOR INPUT OR INOUT
         BNZ   OMG45200                 BR IF NO
OMG45000 EX    RC,OMG45100              CHECK FOR ZERO FIELD
         BNZ   OMG45600                 BR IF FIELD IS NOT ZERO
         B     OMG45400                 BR TO MERGE FIELD
*
OMG45100 NC    0(*-*,RUCB),0(RUCB)      CHECK FOR ZERO FIELD
*
OMG45200 EQU   *                        NOT INPUT OR INOUT
         CLI   0(RET),X'1A'             IS THIS FIELD DSORG
         BE    OMG45000                 BR IF IT IS DSORG
OMG45400 EQU   *                        MERGE FIELD
         EX    RC,OMG46200              ****EXECUTE THE MERGE****
         MVI   DXCCW11+K1,K255          SET REVERSE MERGE FIELD  Y02134
*
OMG45600 EQU   *                        FIELD NOT ZERO
         LA    RET,K1(RET)              STEP DCB DISP POINTER
         TM    0(RET),X'FF'             TEST IF AT END OF MERGE
         BZ    OMG46400                 BR IF AT END OF MERGE
         BO    OMG46300                 BRANCH IF END OF A.M. MERGE
         LA    RF,K1(RF)                STEP JFCB DISP POINTER
         LA    RD,K1(RD)                STEP FIELD LENGTH POINTER
OMG45800 EQU   *                        RESET DCB PT AND LOOP
         LR    RDCB,R0                  RESET DCB POINTER
         B     OMG44900                 BR TO START OF LOOP
*
OMG46200 MVC   0(*-*,RUCB),0(RDCB)      ****MERGE****
*
OMG46300 EQU   *                        END OF AM MERGE
         LA    RET,OMG4810K             RESET POINTERS
         LA    RF,OMG4850K
         LA    RD,OMG4870K
         B     OMG45800                 BR TO RESET DCB PTR AND LOOP
*
OMG46400 EQU   *                        END OF MERGE
         LR    RDCB,R0                  RESTORE DCB ADDRESS
*
OMG46500 EQU   *                        MERGE INHIBITED
         BAL   RC,OMG47700              GO DETERMINE IF HIARCHY IS MEAN
         B     OMG46700                 RETURN HERE IF NOT MEANINGFUL
*  RETURN HERE IF HIARCHY IS MEANINGFUL.
         NI    JFCBFTEK,X'FF'-DCBH0-DCBH1  INSURE THAT HIARCHY NOT
*              MERGED TO JFCB ALONG WITH BFTEK/BFALN.
         OC    JFCBFTEK,DXCCW11         RESET HIARCHY FLAG
         TM    DCBBFTEK,DCBH0+DCBH1     TEST,BR IF HIARCHY ALREADY
         BM    OMG46700                 EXISTS IN DCB
*  AT NO TIME SHOULD DCBBFTEK BITS 0,5 = 1,1.  IF THEY ARE SET 1,1
*  THEN THIS ERROR DEFAULTS TO HIARCHY=0 (UNLESS JFCBH1 IS ON).
         NI    DCBBFTEK,X'FF'-DCBH0-DCBH1
         TM    JFCBFTEK,JFCBH1          TEST,BR IF USER HAS NOT
         BZ    OMG46600                 REQUESTED HIARCHY=1 IN DD
         OI    DCBBFTEK,DCBH1           SET HIARCHY=1 IN DCB
         B     OMG46700                 BR TO CONTINUE
OMG46600 EQU   *                        HIARCHY = ONE
         OI    DCBBFTEK,DCBH0           SET HIARCHY=O IN DCB
OMG46700 EQU   *                        CONTINUE
         TM    DXCCW11+K1,K255          ANY REVERSE MERGE?       Y02134
         BZ    OMG46800                 BRANCH IF NO             Y02134
         TM    K0(RPARC),PLISTM0C       INPUT,INOUT, OR RDBACK?  Y02134
         BNZ   OMG46800                 BRANCH IF NO             Y02134
         L     RC,DXDSAB                LOAD CURRENT DSAB ADDR   Y02134
         USING DSAB,RC                                           Y02134
         MODESET EXTKEY=SCHED           GET INTO SCHEDULER KEY   Y02082
         OI    DSABFLG2,DSABIRM         SET INPUT REVERSE MERGE  Y02134
         MODESET EXTKEY=DATAMGT         RETURN TO DATA MGT KEY   Y02082
*                                       BIT IN DSAB              Y02134
         DROP  RC                                                Y02134
OMG46800 EQU   *                        NO REVERSE MERGE
*
***********************************************************************
*
         TM    TIOELINK,TIOESYIN+TIOESYOT SPOOLED DATA SET?      Y02120
         BM    OMG47600                 BRANCH IF YES            Y02120
         L     RUCB,TIOEFSRT-K1         RESTORE FIRST UCB ADDR
*
         TM    JFCBMASK+K4,JFCMNULL     TEST IF DUMMY DATA SET
         BO    OMG47600                 BR IF YES
*
* ABEND IF HAVE ASCII OPTCD=Q WHEN NOT TAPE, NOT DUMMY, NOT EXCP, M0943
* BUT PS OR PO (SAM)                                              M0943
*                                                                 M0943
         TM    UCBTBYT3,UCB3TAPE        TAPE DEVICE               M0943
         BO    OMG46860                 YES, GO SKIP ABEND        M0943
         TM    DCBMACRF,DCBMEXCP        EXCP DCB                  M0943
         BO    OMG46860                 YES, GO SKIP ABEND        M0943
         TM    DCBDSORG,DCBORGPO+DCBORGPS  PS OR PO (SAM)         M0943
         BZ    OMG46860                 NO, BR, OPTCD=Q TEST NOT  M0943
*                                       VALID                     M0943
         TM    DCBOPTCD,DCBOPTQ         OPTCD=Q                   M0943
         BO    OMG47100                 YES, ABEND, BAD OPTCD     M0943
*                                                                 M0943
OMG46860 EQU   *                        BACK MERGE SKIPPED        M0943
*
         TM    UCBTBYT3,UCB3DACC        TEST IF DIRECT ACCESS
         BO    OMG47500                 BR IF YES
*
         TM    UCBTBYT3,UCB3TAPE        TEST IF MAGNETIC TAPE
         BNO   OMG47600                 BR IF NOT TAPE
**********************************************************************
*
*  TEST FOR CONFLICTS BETWEEN ASCII AND EBCDIC DATA SET SPECIFICATIONS
*  AND MERGE DCBBUFOF AND DCBUSASI TO JFCBUFOF.
*
OMG46868 EQU   *                                               @ZA14154
         TM    DCBMACRF,DCBMEXCP        EXCP DCB
         BO    OMG47300                 YES, BRANCH
*
         TM    DCBOPTCD,DCBOPTQ         ASCII DATA SET, OPTCD=Q  A43865
         BZ    OMG47000                 NO, GO TEST FOR ASCII FLDS
*
         TM    JFCBLTYP,JFCSL           STANDARD LABEL WANTED
         BO    OMG47100                 BR IF YES - LABEL ERROR
*
         TM    DCBRECFM,DCBRECUN        U-FORMAT RECORDS
         BO    OMG46900                 BR IF YES
         TM    DCBRECFM,DCBRECVR        VARIABLE RECORDS SPEC.
         BO    OMG47100                 BR IF YES TO ERROR EXIT
*
OMG46900 CLI   DCBBUFOF,K99             BUFOF GREATER THAN 99
         BH    OMG47100                 BR IF YES TO ERROR
         TM    JFCBTSDM,JFCNDCB         JFCB MERGE INHIBITED   @ZA25724
         BO    OMG47300                 YES, GO SKIP MERGE     @ZA25724
         TM    PLISTOPT(RPARC),PLISTOIN OPEN FOR OUTPUT/INOUT  @ZA25724
         BNO   OMG46920                 NO, BR INPUT PROCESS   @ZA25724
         CLI   DCBBUFOF,K0              DOES BUFFER OFFSET = 0 @ZA25724
         BH    OMG46910                 NO-CONTINUE            @ZA25724
         TM    DCBUSASI,DCBBLBP         BUFOF=L SPECIFIED      @ZA25724
         BNO   OMG46930                 NO, MERGE BUFOF=NOT L  @ZA25724
*                                       TO JFCB                @ZA25724
OMG46905 EQU   *                                               @ZA25724
         TM    DCBRECFM,JFCVARD         D-TYPE RECORD FORMAT   @ZA25724
         BNO   OMG47100                 NO. BR TO ERROR EXIT   @ZA25724
*                                       BUFOF=L ONLY VALID FOR @ZA25724
*                                       D-TYPE RECORD FORMAT   @ZA25724
         B     OMG46940                 MERGE BUFOF=L TO JFCB  @ZA25724
*                                                              @ZA25724
OMG46910 TM    DCBUSASI,DCBBLBP         BUFOF = L SPECIFIED    @ZA25724
         BNO   OMG47100                 NO, BR TO ERROR EXIT     A43865
*                                       CAN'T HAVE NON-ZERO BUFOF
*                                       FOR OUTPUT UNLESS BUFOF=L.
*                                       IBM DOES NOT SUPPORT BUFOF
*                                       FOR OUTPUT (EXCEPT =L).
         B     OMG46905                 CHECK D-TYPE RECORDS   @ZA25724
*                                                              @ZA25724
OMG46920 EQU   *                        NOT OPENED FOR OUTPUT    A43865
         CLI   JFCBUFOF,K0              BUFOF IN JFCB            A43865
         BNE   OMG47300                 YES, GO SKIP MERGE       A43865
         TM    DCBUSASI,DCBBLBP         BUFOF=L SPECIFIED        A43865
         BO    OMG46940                 YES, GO SKIP MERGE     @ZA25724
*                                                              @ZA25724
OMG46930 EQU   *                                               @ZA25724
         MVC   JFCBUFOF,DCBBUFOF        MERGE BUFOF NOT =L       A43865
         B     OMG46960                 GO SET JFCB MODIFIED     A43865
*                                                              @ZA25724
OMG46940 EQU   *                        MERGE BUFOF=L            A43865
         MVI   JFCBUFOF,JFCBFOFL+K4     MERGE BUFOF=L AND 4 BYTE A43865
*                                       OFFSET TO JFCB           A43865
OMG46960 EQU   *                        SET JFCB MODIFIED        A43865
         OI    JFCBMASK+4,JFCMJMOD      SET JFCB MODIFIED        A43865
         B     OMG47300                 GO TO RESUME PROCESSING
*
*   CHECK FOR ASCII FIELD FOR EBCDIC DATA SETS
OMG47000 EQU   *                        CHECK FOR ASCII FIELDS
         TM    JFCBLTYP,JFCBAL          ASCII LABEL SPECIFIED
         BO    OMG47100                 YES, BRANCH - ERROR
*
         TM    DCBRECFM,DCBRECUN        IS RECFM U, F OR V      SA41465
         BNZ   OMG47300                 BRANCH IF YES           SA41465
*
         TM    DCBRECFM,DCBRECD         D-TYPE RECORD FORMAT    SA56418
         BNO   OMG47300                 NO, BR TO CONTINUE      SA56418
*
*   IF ANY CONFLICTS IN DCB ATTRIBUTES, PROCESSING OF THIS
*   DCB WILL BE DISCONTINUED AFTER GOING TO PROBLEM DETERMINATION
*   TO WRITE AN ERROR MESSAGE.
*
OMG47100 EQU   *                        GO TO PROB DETERM
OABD047  EQU   47                       MIXED EBCDIC AND ASCII
*                                       ATTRIBUTES OR INVALID  @ZA14154
*                                       TRTCH FOR MAGNETIC TAPE
         DMABCOND OABD047,ID6M0P        XCTL TO PROBLEM DET / ABEND
*
***********************************************************************
*
*  PROCESSING FOR MAGNETIC TAPE ONLY.  DETERMINE NEXT MODULE TO GO TO.
*
OMG47300 EQU   *
*
         TM    0(RPARC),X'07'           TEST IF OPEN FOR OUTPUT
         BNO   OMG47600                 BR IF NOT OUTPUT OR OUTIN
*
         TM    JFCBIND2,JFCOLD          IS THIS DISP=MOD (NOT NEW, OLD)
         BNO   OMG47600                 BR IF MOD
*
         TM    JFCBLTYP,JFCSL+JFCBAL    TEST IF LABEL=SL, SUL, AL, AUL
         BNZ   OMG47400                 BR IF YES
*
         TM    JFCBLTYP,JFCNSL          TEST IF LABEL=NSL
         BNO   OMG47600                 BR IF NOT NSL, ASSUME NL
*
         IECRES LOAD,MODID=ID6M6O,BRCODE=4,BRANCH=QUEUED         Y02080
*                                   XCTL TO NSL OUTPUT INTERFACE Y02080
*
OMG47400 EQU   *                        STANDARD LABEL
         IECRES LOAD,MODID=ID6M6N,BRCODE=0,BRANCH=QUEUED         Y02080
*                                 XCTL TO MAG TAPE OUTPUT SL MOD Y02080
*
***********************************************************************
*
*  PROCESSING FOR DIRECT ACCESS ONLY.  DETERMINE IF OUTPUT UHL.
*
OMG47500 EQU   *                        DIRECT ACCESS
         TM    JFCBMASK+K5,X'04'        TEST IF OUTPUT UL NEEDED
         BZ    OMG47600                 BR IF NO
*
         NI    JFCBMASK+K5,X'FF'-X'04'  CLEAR OPEN UL INDICATOR
*
         L     RC,DXCCW6                RESTORE EXIT ADDR FOR UL MOD
*
         MVC   DXRETMOD,ID6M6V          MODULE FOR UL TO XCTL TO NEXT
         IECRES LOAD,MODID=ID6M6P,BRCODE=0,BRANCH=QUEUED         Y02080
*                                  XCTL TO OUTPUT DA USER LABELS Y02080
*
***********************************************************************
*
OMG47600 EQU   *                        GO TO IFG0196V
         IECRES LOAD,MODID=ID6M6V,BRCODE=0,BRANCH=QUEUED         Y02080
*                                 XCTL TO SELECT EXECUTOR MODULE Y02080
*
***********************************************************************
*
OMG47700 EQU   *                        IS HIARCHY MEANINGFUL
         TM    DCBMACRF,DCBMEXCP        TEST IF EXCP
         BZ    OMG47800                 BR IF NO
         TM    DCBMACRF,DCBMFOUN        IS FBE PRESENT
         BCR   8,RC                     BR IF NO
OMG47800 EQU   *                        NOT EXCP
         TM    DCBDSORG,DCBORGIS+DCBORGDA+DCBORGPO  IS IT - IS, DA, PO
         BM    K4(,RC)                  BR IF YES
         TM    DCBDSORG,DCBORGPS        IS THIS PS
         BNO   OMG47900                 BR IF NO
         TM    TIOELINK,TIOEQNAM        IS THIS TCAM PS
         BCR   1,RC                     BR IF YES
         B     K4(,RC)                  BR, IT IS NORMAL PS
OMG47900 TM    DCBDSORG+K1,DCBORGGR+DCBORGTQ+DCBORGTX  IS IT - GR,TQ,TX
         BCR   4,RC                     BR IF YES
*  ELIMINATED ALL EXCEPT TAM.  FOR TAM, ONLY (CX)BTAM HAS
*  VALID HIARCHY FLAGS.
         TM    DCBMACRF,DCBMREAD        IS MACRF = READ
         BO    K4(,RC)                  BR IF YES, BTAM
         TM    DCBMACRF+K1,DCBMWRIT     IS MACRF = WRITE
         BO    K4(,RC)                  BR IF YES, BTAM
         BR    RC                       BR NO HIARCHY
*
**********************************************************************
         EJECT
**********************************************************************
*
*
*
*        THE FOLLOWING ARE DISPLACEMENTS WITHIN THE DCB
*
*
*
*        OF FIELDS WHICH WILL BE MERGED TO THE DCB FROM THE JFCB
*
*
*
**********************************************************************
OMG4800K EQU   *
*                   ****ISAM SECTION OF MERGE TABLE FOLLOWS****
         DC    FL1'54'                  ISAM NTM
         DC    FL1'55'                  ISAM CYLOF
         DC    FL1'60'                  ISAM RKP
         DC    FL1'112'                 ISAM DUBFN
         DC    FL1'72'                  ISAM NCP
         DC    FL1'84'                  ISAM-QSAM EROPT
         DC    FL1'82'                  ISAM-QSAM LRECL
         DC    X'FF'                    ****DIVISION LINE*****
*                   ****BDAM SECTION OF MERGE TABLE FOLLOWS****
         DC    FL1'81'                  BDAM LIMCT
         DC    X'FF'                    ****DIVISION LINE****
*                   ****BSAM SECTION OF MERGE TABLE FOLLOWS****
         DC    FL1'72'                  BSAM NCP
         DC    FL1'82'                  BSAM LRECL
         DC    X'FF'                    *****DIVISION LINE****
*                                       COMMON SECTION FOLLOWS
OMG4810K EQU   *
         DC    FL1'16'                  KEY LENGTH
         DC    FL1'62'                  BLOCK SIZE
OMG4820K EQU   *
         DC    FL1'52'                  OPTCOD
         DC    FL1'26'                  DSORG
         DC    FL1'18'                  DEN
OMG4830K EQU   *
         DC    FL1'32'                  BFTEK OR BFALN
         DC    FL1'24'                  BUFL
         DC    FL1'20'                  BUFNO
         DC    X'00'                    ****END OF TABLE****
*                   ****GRAPHICS SECTION OF MERGE TABLE FOLLOWS****
         DC    FL1'26'                  DSORG
         DC    FL1'32'                  GNCP
         DC    X'00'                    ****END OF GRAPHICS****
**********************************************************************
         EJECT
**********************************************************************
*
*
*
*        THE FOLLOWING ARE THE DISPLACEMENTS OF FIELDS IN THE
*
*
*
*        JFCB WHICH WILL BE MERGED INTO THE DCB
*
*
*
**********************************************************************
OMG4840K EQU   *
*                   ****ISAM SECTION OF MERGE TABLE FOLLOWS****
         DC    FL1'107'                 ISAM NTM OFFSET
         DC    FL1'110'                 ISAM CYLOF OFFSET
         DC    FL1'108'                 ISAM RKP OFFSET
         DC    FL1'111'                 ISAM DUBFN OFFSET
         DC    FL1'106'                 ISAM NCP OFFSET
         DC    FL1'92'                  ISAM-QSAM EROPT OFFSET
         DC    FL1'104'                 ISAM-QSAM LRECL OFFSET
         DC    X'FF'                    ****DIVISION LINE****
*                   ****BDAM SECTION OF MERGE TABLE FOLLOWS****
         DC    FL1'95'                  BDAM LIMCT OFFSET
         DC    X'FF'                    ****DIVISION LINE****
*                   ****BSAM SECTION OF MERGE TABLE FOLLOWS****
         DC    FL1'106'                 BSAM NCP OFFSSET
         DC    FL1'104'                 BSAM LRECL OFFSET
         DC    X'FF'                    ****DIVISION LINE****
OMG4850K EQU   *
         DC    FL1'93'                  KEY LENGTH OFFSET
         DC    FL1'102'                 BLOCKSIZE OFFSET
         DC    FL1'101'                 OPTCOD OFFSET
         DC    FL1'98'                  DSORG
         DC    FL1'94'                  DEN OFFSET
         DC    FL1'89'                  BFTEK-BALN OFFSET
         DC    FL1'90'                  BUFL OFFSET
         DC    FL1'88'                  BUFNO OFFSET
         DC    X'00'                    END OF TABLE**********
*                   ****GRAPHICS SECTION OF MERGE TABLE FOLLOWS****
         DC    FL1'98'                  DSORG
         DC    FL1'89'                  GNCP
         DC    X'00'                    END OF GRAPHICS
**********************************************************************
         EJECT
**********************************************************************
*
*
*
*        THE FOLLOWING ARE THE LENGTHS-1 OF THE FIELDS
*
*
*
*        WHICH ARE MERGED FROM THE JFCB TO THE DCB
*
*
*
**********************************************************************
OMG4860K EQU   *
*                   ****ISAM SECTION OF MERGE TABLE FOLLOWS****
         DC    FL1'0'                   ISAM NTM LGH
         DC    FL1'0'                   ISAM CYLOF LGH
         DC    FL1'1'                   ISAM RKP LGH
         DC    FL1'0'                   ISAM DUBFN LGH
         DC    FL1'0'                   ISAM NCP LGH
         DC    FL1'0'                   ISAM-QSAM EROPT LGH
         DC    FL1'1'                   ISAM-QSAM LRECL LGH
         DC    X'FF'                    ****DIVISION LINE****
*                   ****BDAM SECTION OF MERGE TABLE FOLLOWS****
         DC    FL1'2'                   BDAM LIMCT LGH
         DC    X'FF'                    ****DIVISION LINE****
*                   ****BSAM SECTION OF MERGE TABLE FOLLOWS****
         DC    FL1'0'                   BSAM NCP LGH
         DC    FL1'1'                   BSAM LRECL LGH
         DC    X'FF'                    ****DIVISION LINE*******
OMG4870K EQU   *
         DC    FL1'0'                   KEY LENGTH LENGHT
         DC    FL1'1'                   BLOCK SIZE LENGTH
         DC    FL1'0'                   OPTCOD LGH
         DC    FL1'1'                   DSORG
         DC    FL1'0'                   DEN LGH
         DC    FL1'0'                   BFTEK-BALN LGH
         DC    FL1'1'                   BUFL LGH
         DC    FL1'0'                   BUFNO LGH
         DC    X'00'                    ****END OF TABLE****
*                   ****GRAPHICS SECTION OF MERGE TABLE FOLLOWS****
         DC    FL1'1'                   DSORG
         DC    FL1'0'                   GNCP
         DC    X'00'                    ****END OF GRAPHICS****
***********************************************************************
         EJECT
*
         XCTLTABL ID=(ID6M0P,0P,ID6M6N,6N,ID6M6P,6P,             YM3076X
               ID6M6O,6O,ID6M6V,6V),BRT=YES,LENGTH=              Y02080
*
         IECDSECS EXPAND=YES            EXPAND DESIRED DSECTS HERE
*
         END
