         TITLE 'IFG0196O                      OPEN - NSL OUTPUT'
         COPY  LCGASMSW
IFG0196O CSECT
         ENTRY IGG0190R                                          Y02080
IGG0190R EQU   IFG0196O                 ALLIAS ENTRY POINT       Y02080
***********************************************************************
*                                                                     *
*                                                                     *
*        VS 02 RELEASE 03 DELETIONS/CHANGES                           *
*                                                                     *
*705000,706520                                                 @ZA03195
*0000                                                          @ZA04224
*0000708580-708584                                             @ZA02874
*0000                                                          @ZA13589
*D65224037-65244037,A627566                                    @ZA20775
*                                                                     *
*        VS 02 RELEASE 02 DELETIONS/CHANGES                           *
*0000689000                                                     ZA01502
*                                                                     *
* MODULE NAME = IFG0196O (OS/VS2)                                     *
*                                                                     *
* DESCRIPTIVE NAME = NSL OUTPUT                                       *
*                                                                     *
* COPYRIGHT = NONE                                                    *
*                                                                     *
* STATUS = RELEASE 2, LEVEL 0                                         *
*                                                                     *
* FUNCTION =                                                          *
*        1. INTERFACE WITH USER SUPPLIED NSL OUTPUT ROUTINE (NSLOHDRO)*
*                                                                     *
*        2. SYNCHRONIZE ALL DCB'S AND SET UP NECESSARY CONDITIONS TO  *
*        PROCESS IN PARALLEL BEFORE EXITING TO THE NSL ROUTINE.       *
*                                                                     *
*        3. ON RETURN FROM THE USER'S ROUTINE, RESET TO PROCESS       *
*        SERIALLY VIA THE RESIDENT ROUTINE.                           *
*                                                                     *
*        4. IF THE UCBMOUNT SWITCH IN THE UCBDMCT FIELD IS SET(BIT 0  *
*        AT OFFSET 35), REJECT THE TAPE VOLUME.                       *
*                                                                     *
* NOTES = SEE BELOW                                                   *
*                                                                     *
*    DEPENDENCIES =                                                   *
*            CLASS ONE CHARACTER CODE.  THE EBCDIC CHARACTER CODE     *
*            WAS USED FOR ASSEMBLY.  THE MODULE MUST BE REASSEMBLED   *
*            IF A DIFFERENT CHARACTER SET IS USED FOR EXECUTION.      *
*                                                                     *
*    RESTRICTIONS = NONE                                              *
*                                                                     *
*    REGISTER CONVENTIONS =                                           *
*            R2 POINTS TO DCB                                         *
*            R4 POINTS TO OPEN WORK AREA                              *
*            R5 POINTS TO THE RESIDENT ROUTINE                        *
*            R6 POINTS TO THE WTG TABLE                               *
*            R7 POINTS TO THE CURRENT PARAMETER LIST ENTRY            *
*            R8 POINTS TO THE CURRENT WTG TABLE ENTRY                 *
*            R9 POINTS TO THE DD ENTRY IN THE TIOT                    *
*            R10 POINTS TO THE UCB                                    *
*                                                                     *
*    PATCH LABEL = SEE NEXT TO LAST LABEL BEFORE ORG STATEMENT AT END *
*                  OF LISTING.                                        *
*                                                                     *
* MODULE TYPE = CONTROL (OPEN, CLOSE, EOV DATA MANAGEMENT)            *
*                                                                     *
*    PROCESSOR = ASSEMBLER XF                                         *
*                                                                     *
*    MODULE SIZE = SEE EXTERNAL SYMBOL DICTIONARY OR LOC FIELD ON     *
*                  ORG STATEMENT AT END OF LISTING                    *
*                                                                     *
*    ATTRIBUTES = REENTRANT, REFRESHABLE,READ-ONLY, ENABLED,          *
*                  PRIVILEGED, SUPERVISOR STATE, DATA MANAGEMENT KEY, *
*                  LINK PACK AREA RESIDENT/PAGEABLE                   *
*                                                                     *
* ENTRY POINT =                                                       *
*        IFG0196O AND IGG0190R                                        *
*                                                                     *
*    PURPOSE = SEE FUNCTION                                           *
*                                                                     *
*    LINKAGE =                                                        *
*        FROM IFG0196M:                                               *
*          IECRES LOAD,MODID=ID6M6O,BRCODE=4,BRANCH=QUEUED            *
*                                                                     *
*        FROM IFG0194A:                                               *
*          IECRES BRANCH=QUEUED USING DXRETMOD AND DXRETCOD SETUP IN  *
*          IFG0196O.                                                  *
*                                                                     *
*        FROM NSLOHDRO:                                               *
*          XCTL TO IGG0190R                                           *
*                                                                     *
* INPUT = STANDARD                                                    *
*                                                                     *
* OUTPUT =                                                            *
*        ON EXIT TO THE NSL ROUTINE THE FULL PARAMETER LIST IS PASSED *
*        FOR PARALLEL PROCESSING.                                     *
*        ON FINAL EXIT THE TAPES HAVE BEEN VERIFIED AND POSITIONED BY *
*        THE NSL ROUTINE.                                             *
*                                                                     *
* EXIT-NORMAL =                                                       *
*        IFG0196V                                                     *
*        IFG0194A                                                     *
*        NSLOHDRO                                                     *
*                                                                     *
* EXIT-ERROR = NONE                                                   *
*                                                                     *
* EXTERNAL REFERENCES = SEE BELOW                                     *
*                                                                     *
*    ROUTINES = IFG019RA                                              *
*                                                                     *
*    DATA AREAS = NONE                                                *
*                                                                     *
*    CONTROL BLOCK = CVT, DCB, JFCB, UCB                              *
*                                                                     *
* TABLES = STANDARD                                                   *
*                                                                     *
* MACROS = MODESET, IECRES, XCTLTABL, IECDSECS, IECIQU, ENQ           *
*                                                                     *
* CHANGE ACTIVITY = NEW RELEASE (LEVEL 0)                             *
*                                                                     *
***********************************************************************
         EJECT
*
         USING FORCORE,RCORE            WORK AREA
         USING TIOENTRY,RTIOT           TIOT ENTRY
         USING IHADCB,RDCB              DCB
         USING UCBOB,RUCB               UCB
         USING WTG,RWTG                 WHERE TO GO TABLE
         USING USERPRML,RPARC           PTR TO CURRENT ENTRY     Y02134
         USING WTGENTRY,RWTGC           PTR TO CURRENT ENTRY     Y02134
*
         BALR  RBASE,0                  SET BASE REGISTER
         USING *,RBASE
       MODESET EXTKEY=DATAMGT           GET IN DM KEY IF NOT     Y02082*
                                        THERE ALREADY            Y02082
*
         B     OTA70000(RET)            GO TO PROPER ENTRY
OTA70000 EQU   *                        BRANCH TABLE
         B     OTA70200                 RETURN FROM NSL
         B     OTA70100                 INITAL ENTRY
         B     OTA70500                 CHECK NSL RETURN
         SPACE 2
OTA70100 EQU   *                        INITAL ENTRY
         STM   RTIOT,RUCB,DXXCTL        SAVE REGS FOR RETURN FROM NSL
         ST    RPARC,DXPRPARC           SAVE CURRENT PARM PTR    Y02082
*                                                                Y02082
* COPY THE DCB(S) FROM THE WORK AREA TO USER'S STORAGE           Y02082
*                                                                Y02082
         IECRES INIT,DCBCOPY=FRWKAR,STM=(3,14,WTGPREFX)          Y02082
*                                                                Y02082
         MODESET EXTKEY=SUPR            UCB KEY                @ZA04224
*        MOUNT BIT OFF = NSL ALREADY VERIFIED                  @ZA04224
         OI    UCBDMCT,UCBMOUNT         INDICATE REVERIFY      @ZA04224
         MVC   DXCCW7(L'UCBVOLI),UCBVOLI SAVE VOL SERIAL       @ZA20775
         MODESET EXTKEY=DATAMGT         DATA MANAGEMENT KEY    @ZA04224
*
         OI    DXATOUTA,DXATNSL         SET NSL RTN GIVEN CONTRL Y02144
         IECRES SYNCHDCB
*                                                                Y02134
         LR    RD,RES                   SAVE RESIDENT PTR FOR RETURN
         L     RPAR,WTGPREFX            GET PTR TO PREFIX        Y02080
         USING IECPREFX,RPAR                                     Y02080
         L     RF,IECRRPRM              GET RRPLIST PTR          Y02144
         L     RPAR,IECUPRML            GET POINTER TO PARM LIST YM5702
*                                       OF COPIED DCB ADDRESSES  YM5702
         LA    RET,K0                   RE-ENTRY FORM NSL
         USING RRPLIST,RF                                        Y02144
         OI    RRFLAGS1,RRFNSL          SET NSL GIVEN CONTROL    Y02144
OTA70150 EQU   *                                               @ZA02874
         MODESET EXTKEY=SUPR            KEY ZERO TO NSLOHDRO     Y02082
*                                                                Y02134
         IECRES LOAD,MODNM=NSLOHDRO,PREFIX=WTGPREFX,             Y02134X
               BRANCH=DIRECT                                     Y02134
*                                                                Y02134
OTA70200 EQU   *                        RETRUN FROM NSL ENTRY
*                                                                Y02134
         MVI   DXECB,K32                RESTORE DXECB          @ZA13589
         MVC   WTGMODNM,OPENIDB         RESTORE IFG019 NAME      Y02134
         LA    RWTGC,WTGENTRY-WTG(,RWTG) POINTER TO FIRST ENTRY  Y02134
         L     RF,WTGPREFX              LOAD POINTER TO PREFIX   Y02134
         L     RPAR,IECUPRML-IECPREFX(,RF) LOAD PTR TO PARM LIST Y02134
         LR    RPARC,RPAR               GET FIRST NSL PARM PTR   Y02082
*                                                                Y02134
OTA70300 EQU   *                        PROCESS DCB              Y02134
*                                                                Y02134
         CLC   WTGIDTTR(K2),RETID       CHECK FOR THIS MODULE    Y02134
         BNE   OTA70350                 BRANCH IF WRONG MODULE   Y02134
*                                                                Y02134
         L     RCORE,WTGCORE-K1         GET WORK AREA PTR        Y02134
         L     RTIOT,DXTIOTAD           GET CURRENT TIOT PTR     Y02134
         L     RUCB,DXUCBADR            GET CURRENT UCB ADDRESS  Y02134
         STM   RTIOT,RUCB,DXREG9        STORE FOR QUEUED PROCESS Y02134
*                                                                Y02134
         L     RDCB,DXUDCBAD            GET PTR TO USER'S DCB    Y02082
         MODESET KEYADDR=DXUKEY,WORKREG=1 ASSUME USER'S KEY      Y02082
         OC    DCBOFLGS,DCBOFLGS        TOUCH THE USER'S MEMORY  Y02082
         LA    R0,ALLBITS-DCBOBUSY      SET MASK FOR BUSY BIT    YM3005
         IC    R1,DCBOFLGS              GET USER'S DCBOFLGS      Y02082
         MODESET EXTKEY=DATAMGT         RESUME DATA MGT KEY      Y02082
         OR    R0,R1                    COMBINE COPIED DCBOFLGS  YM3005
         L     RDCB,DXPDCBAD            WITH USER'S DCBOFLGS     YM3005
         IC    R1,DCBOFLGS              MAKING COPIED BUSY BIT   YM3005
         NR    R1,R0                    SAME AS USER'S BUSY BIT  YM3005
         STC   R1,DCBOFLGS              UPDATE DCBOFLGS IN COPY  Y02082
*                                                                Y02134
         LA    RET,K8                   ENTRY TO THIS LOAD AFTER Y02134
*                                       FIRST DCB HAS COMPLETED  Y02134
         ST    RET,DXREGE               SAVE ENTRY VALUE         Y02134
*                                                                Y02134
OTA70350 EQU   *                        WRONG MODULE             Y02134
*                                                                Y02134
         TM    DXDCBLST,LASTNTRY        IS THIS THE LAST DCB     Y02134
         LA    RPARC,DXDCBLST+L'DXDCBLST GET NEXT PARM POINTER   Y02134
         LA    RWTGC,WTGENTRY+L'WTGENTRY GET NEXT ENTRY POINTER  Y02134
         BNO   OTA70300                 NO, BRANCH               Y02134
*                                                                Y02134
         LR    RPARC,RPAR               GET FIRST NSL PARM PTR   Y02082
         LA    RWTGC,WTGENTRY-WTG(,RWTG) POINTER TO FIRST ENTRY ZA01502
         LR    RES,RD                   RESTORE RESIDENT POINTER Y02134
*                                                                Y02134
OTA70400 EQU   *                        NOT LAST DCB             Y02134
*                                                                Y02134
         CLC   WTGIDTTR(K2),RETID       CHECK FOR THIS MODULE    Y02134
         BE    OTA70450                 BRANCH IF RIGHT MODULE   Y02134
*                                                                Y02134
         TM    DXDCBLST,LASTNTRY        IS THIS THE LAST DCB     Y02134
         LA    RPARC,DXDCBLST+L'DXDCBLST GET NEXT PARM POINTER   Y02134
         LA    RWTGC,WTGENTRY+L'WTGENTRY GET NEXT ENTRY POINTER  Y02134
         BNO   OTA70400                 NO, BRANCH               Y02134
         B     OTA70900                 BRANCH TO EXIT           Y02134
*                                                                Y02134
OTA70450 EQU   *                        RIGHT MODULE             Y02134
*                                                                Y02134
         L     RDCB,DXDCBLST            LOAD DCB POINTER         Y02134
         L     RCORE,WTGCORE-K1         GET WORK AREA PTR        Y02134
         L     RTIOT,DXTIOTAD           GET CURRENT TIOT PTR     Y02134
         L     RUCB,DXUCBADR            GET CURRENT UCB ADDRESS  Y02134
*                                                                Y02134
OTA70500 EQU   *                        NSL MOUNT SW NOT ON      Y02134
*                                                                Y02134
         TM    UCBDMCT,UCBMOUNT         CK FOR MOUNT SW FOR NSL  Y02134
         BNO   OTA70800                 BR IF NOT ON             Y02134
         NI    DXATOUTA,X'FF'-DXATNSL   RESET NSL RTN CNTRL FLG  Y02144
*                                                                Y02134
         MODESET EXTKEY=SUPR            UCB KEY                  Y02082
         NI    UCBDMCT,ALLBITS-UCBMOUNT SET NSL REJECT OFF     @ZA03195
         MODESET EXTKEY=DATAMGT         DATA MANAGEMENT KEY      Y02082
*                                                                Y02134
OTA70700 EQU   *                        VOLUME REJECTED          Y02134
*                                                                Y02134
         LA    R0,K0                    VOL VERIFICATION ERROR @ZA03195
         MVI   DXRETCOD,K4              SET UP RETURN OFFSET     Y02134
         MVC   DXRETMOD,RETID           SAVE RETURN MODULE       Y02134
*                                                                Y02134
         IECRES LOAD,MODID=ID6O4A,BRCODE=K16,BRANCH=QUEUED       Y02134
*                                                                Y02134
OTA70800 EQU   *                        MOUNT SW FOR NSL NOT ON  Y02134
*                                                                Y02134
         CLC   DXVOLSR2,SCRTCH          CHECK FOR NONSPECIFIC    Y02134
         BNE   OTA70850                 BRANCH IF NOT SCRTCH     Y02134
*                                                                Y02134
         TM    JFCBMASK+K5,K1           L-NUMBER GENERATED?    @ZA02874
         BZ    OTA70825                 NO, BR                 @ZA02874
         CLC   UCBVOLI,DXCCW7           DID NSL ROUTINE        @ZA02874
*                                       CHANGE L-NUMBER TO     @ZA02874
*                                       SPECIFIC VOLUME SERIAL?@ZA02874
         BNE   OTA70815                 YES, BR                @ZA02874
         CLC   JFCBVOLS(L'UCBVOLI),DXCCW7 L-NUMBER CHANGED?    @ZA02874
         BE    OTA70850                 NO, BR                 @ZA02874
         MODESET EXTKEY=SUPR            SUPR KEY               @ZA02874
         MVC   UCBVOLI,JFCBVOLS         UPDATE UCB VOL SERIAL  @ZA02874
         MODESET EXTKEY=DATAMGT         DATA MANAGEMENT KEY    @ZA02874
OTA70815 EQU   *                                               @ZA02874
         NI    JFCBMASK+K5,ALLBITS-K1   RESET L-NO. GENERATED  @ZA02874
*                                       BIT IN JFCBMASK        @ZA02874
OTA70825 EQU   *                                               @ZA02874
*                                                                Y02134
         MVC   DXWORK1(K16),ENQLIST     INIT ENQ LIST            Y02134
         L     RF,DXJSCBAD              LOAD JSCB ADDRESS        Y02134
         L     R0,JSCBTCBP-IEZJSCB(,RF) INITIATOR TCB POINTER    Y02134
         ENQ   (,UCBVOLI),TCB=(0),RET=USE,MF=(E,DXWORK2),        Y02134X
               RELATED=(SYSZVOLS,IFG0194A(VUS02450))             Y02134
*                                                                Y02134
         LTR   RF,RF                    VOLUME ALREADY IN USE    Y02134
         BNZ   OTA70700                 BRANCH TO REJECT         Y02134
*                                                                Y02134
         MVC   JFCBVOLS(L'UCBVOLI),UCBVOLI UPDATE VOL SERS       Y02134
*                                                                Y02134
OTA70850 EQU   *                        PREPARE TO GO TO NEXT MODY02134
*                                                                Y02134
         MVC   WTGIDTTR,CONTINUE        MOVE IN NEXT ID          Y02134
         LA    RET,K8                   GET NEXT LOAD OFFSET     Y02134
*                                                                Y02134
OTA70900 EQU   *                        GO TO NEXT MODULE        Y02134
*                                                                Y02134
         IECRES LOAD,BRANCH=QUEUED                               Y02134
*                                                                Y02134
         EJECT
*                                                                Y02134
ENQLIST  DC    A(0)                     ENQLIST                  Y02134
         ENQ   (SYSZVOLS,,E,6,SYSTEM),TCB=,RET=USE,MF=L          Y02134
SYSZVOLS DC    CL8'SYSZVOLS'            ENQ NAME                 Y02134
SCRTCH   DC    CL6'SCRTCH'              NONSPECIFIC REQUEST      Y02134
*                                                                Y02134
         XCTLTABL ID=(CONTINUE,6V,RETID,6O,ID6O4A,4A,            Y02134X
               ,NSLOHDRO),TAG=OPENIDB,                           Y02134X
               BRT=YES,LENGTH=                                   Y02080
*                                                                Y02134
         IECDSECS CVT,TCB,JSCB,TIOT,DCB,UCB,MAIN,WTG,PREFX,      Y02134X
               RRPL,                                             Y02144X
               EXPAND=YES                                        Y02134
*
         IECEQU
         END
