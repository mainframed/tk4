         TITLE 'IFG0195J                      OPEN - DIRECT ACCESS USERX
                LABELS'                                          Y02134
IFG0195J CSECT
***********************************************************************
*                                                                     *
* MODULE NAME = IFG0195J (OS/VS2)                                     *
*                                                                     *
* DESCRIPTIVE NAME = OPEN - DIRECT ACCESS USER LABELS                 *
*                                                                     *
* COPYRIGHT = NONE                                                    *
*                                                                     *
* STATUS = RELEASE 2, LEVEL 0                                         *
*                                                                     *
* FUNCTION = DETERMINES IF INPUT USER LABELS PROCESSING               *
*            ON DIRECT ACCESS IS REQUIRED.                            *
*                                                                     *
* NOTES = SEE BELOW                                                   *
*                                                                     *
*    DEPENDENCIES =                                                   *
*            CLASS ONE CHARACTER CODE.  THE EBCDIC CHARACTER CODE     *
*            WAS USED FOR ASSEMBLY.  THE MODULE MUST BE REASSEMBLED   *
*            IF A DIFFERENT CHARACTER SET IS USED FOR EXECUTION.      *
*                                                                     *
*    RESTRICTIONS = NONE                                              *
*                                                                     *
*    REGISTER CONVENTIONS =                                           *
*            R2 POINTS TO DCB                                         *
*            R4 POINTS TO OPEN WORK AREA                              *
*            R5 POINTS TO THE RESIDENT ROUTINE                        *
*            R6 POINTS TO THE WTG TABLE                               *
*            R7 POINTS TO THE CURRENT PARAMETER LIST ENTRY            *
*            R8 POINTS TO THE CURRENT WTG TABLE ENTRY                 *
*            R9 POINTS TO THE DD ENTRY IN THE TIOT                    *
*            R10 POINTS TO THE UCB                                    *
*                                                                     *
*    PATCH LABEL = SEE NEXT TO LAST LABEL BEFORE ORG STATEMENT AT END *
*                  OF LISTING.                                        *
*                                                                     *
* MODULE TYPE = CONTROL (OPEN, CLOSE, EOV DATA MANAGEMENT)            *
*                                                                     *
*    PROCESSOR = ASSEMBLER XF                                         *
*                                                                     *
*    MODULE SIZE = SEE EXTERNAL SYMBOL DICTIONARY OR LOC FIELD ON     *
*                  ORG STATEMENT AT END OF LISTING                    *
*                                                                     *
*    ATTRIBUTES = REENTRANT, REFRESHABLE,READ-ONLY, ENABLED,          *
*                 PRIVILEGED, SUPERVISOR STATE, DATA MANAGEMENT KEY,  *
*                 LINK PACK AREA RESIDENT/PAGEABLE                    *
*                                                                     *
* ENTRY POINT = IFG0195J FROM IFG0195A                                *
*                                                                     *
*    PURPOSE = TO PASS CONTROL TO INPUT USER LABEL INTERFACE MODULE.  *
*                                                                     *
*    LINKAGE = IECRES LOAD,BRANCH=QUEUED,MODID=ID5J,BRCODE=0          *
*                                                                     *
* INPUT = AS FOLLOWS -                                                *
*                                                                     *
*        RDCB (REG 2) = CONTENTS OF CURRENT PARMLIST ENTRY            *
*                       CONTAINING DCB POINTER                        *
*        RCORE (REG 4) = OPEN/EOV WORK AREA POINTER                   *
*        REG 5 = POINTER TO RESIDENT ROUTINE                          *
*        RWTG (REG 6) = POINTER TO OPEN/EOV WTG TABLE                 *
*        RPARC (REG 7) = POINTER TO CURRENT ENTRY IN PARMLIST         *
*        RWTGC (REG 8) = POINTER TO CURRENT WTG ENTRY                 *
*        RTIOT (REG 9) = POINTER TO CURRENT TIOT ENTRY                *
*        RUCB (REG 10) = POINTER TO FIRST UCB                         *
*        RET (REG 14) = VALUE 0 = INITIAL ENTRY, 4 = RETURN FROM WTOR *
*                                                                     *
* OUTPUT = DXDEBSYS CONTAINS INPUT OR OUTPUT INDICATOR.               *
*          RC (REG 12) CONTAINS USER LABEL EXIT LIST ENTRY.           *
*                                                                     *
* EXIT-NORMAL = IFG0195F TO PROCESS USER LABELS                       *
*               IFG0196J TO SKIP USER LABEL PROCESSING.               *
*                                                                     *
* EXIT-ERROR = NONE                                                   *
*                                                                     *
* EXTERNAL REFERENCES = SEE BELOW                                     *
*                                                                     *
*    ROUTINES = IFG0195F,IFG0196J.                                    *
*                                                                     *
*    DATA AREAS = MAIN WORKAREA, WTG TABLE, PREFIX.                   *
*                                                                     *
*    CONTROL BLOCK = TIOT,UCB,DCB,DSAB.                               *
*                                                                     *
* TABLES = SEE WTG TABLE AND XCTLTABL.                                *
*                                                                     *
* MACROS = IECDSECS,IECEQU,MODESET,IECRES,XCTLTABL.                   *
*                                                                     *
* CHANGE ACTIVITY = NEW RELEASE (LEVEL 0)                             *
*                                                                     *
***********************************************************************
         EJECT
         IECDSECS TIOT,UCB,DCB,MAIN,WTG,PREFX,DSAB DEFINE        Y02134*
                                DSECTS TO BE EXPANDED AT THE END Y02080
*
         IECEQU ,                       DEFINE EQUATES
*
         USING IHADCB,RDCB              DEFINE BASE TO USER'S DCB
         USING FORCORE,RCORE            DEFINE BASE TO MAIN WORK AREA
         USING WTG,RWTG                 DEFINE BASE TO WTG TABLE Y02080
         USING TIOELNGH,RTIOT           DEFINE BASE TO TIOT DD ENTRY
         USING UCBOB,RUCB               DEFINE BASE TO MAIN UCB
*
         BALR  RBASE,R0                 ESTABLISH BASE REGISTER
         USING *,RBASE                  DEFINE BASE REGISTER
*
***********************************************************************
*
*  DETERMINE IF INPUT USER LABELS SHOULD BE PROCESSED
*
*  (OUTPUT USER LABELS WILL BE WRITTEN AFTER THE JFCB TO DCB MERGE
*  AND THE DCB USER EXIT.)
*
***********************************************************************
*
         TM    JFCBTSDM,JFCSDS          IS IT SYSIN OR SYSOUT
         BO    ODA21400                 YES, DON'T PROCESS UL'S
*
         TM    JFCBLTYP,JFCSUL          WAS SUL SPECIFIED
         BNO   ODA21400                 BR IF NO
*
         TM    DCBMACRF,DCBMEXCP        IS THIS AN EXCP DCB
         BZ    ODA20200                 NO, BR TO CK FOR USR LBLS
         TM    DCBMACRF,DCBMFOUN        YES, DOES FBE EXIST
         BZ    ODA21400                 BR IF NO, IGNORE USER LABELS
*
***********************************************************************
*
*        CHECK IF EXIT LIST SPECIFIED.                          SA65806
*
***********************************************************************
*
ODA20200 EQU   *                        VALIDITY CHECK EXIT LST SA65806
*
         CLC   DCBEXLST+K1(K3),ODA2500K IS THE EXIT LIST 0 OR 1 SA65806
         BNH   ODA21400                 BR IF YES               SA65806
*                                                               SA65806
         L     RC,DCBEXLST              GET EXIT LIST ADDRESS   SA65806
*                                                               SA65806
         MODESET KEYADDR=DXUKEY,WORKREG=15 EXIT LIST IN USER KEY YM4616
*                                                               SA65806
****************************************************************SA65806
*                                                               SA65806
*        IF DSORG=DA (BDAM) THE DATA SET MUST BE OLD.           SA65806
*        USER LABELS MAY BE READ OR UPDATED.                    SA65806
*                                                               SA65806
****************************************************************SA65806
*                                                               SA65806
         TM    DCBDSORG,DCBORGDA        IS DSORG DIRECT
         BO    ODA20400                 IF DA, GO PROCESS UHL   SA65806
*                                                               SA65806
         TM    DCBDSORG,DCBORGPS        IS DSORG PS             SA65806
         BNO   ODA21400                 EXIT IF NOT PS OR DA    SA65806
*                                       NOTE-PS INCLUDES        SA65806
*                                       BDAM - CREATE MODE      SA65806
*
         TM    0(RPARC),PLISTOIN        TEST FOR OUTPUT OR OUTIN
         BNO   ODA20400                 BR IF NOT OUTPUT OR OUTIN
*
         TM    JFCBIND2,JFCOLD          IS DISP=MOD (NOT NEW, OLD)
         BO    ODA21400                 BR IF NOT MOD, NO USER LABELS
         B     ODA20600                 PROCESS INPUT TRL LABEL SA65806
*                                                               SA65806
ODA20400 EQU   *                        USER HEADER LABEL EXIT  SA65806
*                                                               SA65806
         CLI   0(RC),XLIUHL             CHECK IF INPUT UHL EXIT SA65806
         BE    ODA21200                 BR IF YES               SA65806
         CLI   0(RC),XLIUHL+LASTNTRY    UHL AND LAST ENTRY      SA65806
         BE    ODA21200                 BR IF YES               SA65806
         TM    0(RC),LASTNTRY           IS IT LAST ENTRY        SA65806
         BO    ODA21400                 BR IF YES               SA65806
         LA    RC,K4(RC)                INCREMENT POINTER       SA65806
         B     ODA20400                 BR TO CHECK NEXT ENTRY  SA65806
*                                                               SA65806
ODA20600 EQU   *                        USER TRAILER LABEL EXIT SA65806
*                                                               SA65806
         CLI   0(RC),XLIUTL             CHECK IF INPUT UTL EXIT SA65806
         BE    ODA21200                 BR IF YES               SA65806
         CLI   0(RC),XLIUTL+LASTNTRY    UTL AND LAST ENTRY      SA65806
         BE    ODA21200                 BR IF YES               SA65806
         TM    0(RC),LASTNTRY           IS IT LAST ENTRY        SA65806
         BO    ODA20800                 BR IF YES               SA65806
         LA    RC,K4(RC)                INCREMENT POINTER       SA65806
         B     ODA20600                 BR TO CHECK NEXT ENTRY   YM2850
*                                                               SA65806
ODA20800 EQU   *                        DEFER USER LABEL EXIT   SA65806
*                                                               SA65806
         L     RC,DCBEXLST              GET EXIT LIST ADDRESS   SA65806
*                                                               SA65806
ODA21000 EQU   *                        DEFER USER LABEL EXIT   SA65806
*                                                               SA65806
         CLI   0(RC),XLDEFSL            CHECK IF DEFER UTL EXIT SA65806
         BE    ODA21200                 BR IF YES               SA65806
         CLI   0(RC),XLDEFSL+LASTNTRY   UTL AND LAST ENTRY      SA65806
         BE    ODA21200                 BR IF YES               SA65806
         TM    0(RC),LASTNTRY           IS IT LAST ENTRY        SA65806
         BO    ODA21400                 BR IF YES               SA65806
         LA    RC,K4(RC)                INCREMENT POINTER       SA65806
         B     ODA21000                 BR TO CHECK NEXT ENTRY  SA65806
*                                                               SA65806
ODA21200 EQU   *                        GO TO USER LABEL INTFCE SA65806
*                                                               SA65806
         L     RC,0(,RC)                GET EXLST DATA INTO R12 SA65806
*                                                               SA65806
         MODESET EXTKEY=DATAMGT         RETURN TO DM KEY        SA65806
*                                                               SA65806
         LA    RF,0(RC)                 ZERO HI-ORDER BYTE      SA65806
         LTR   RF,RF                    IS EXIT LIST ADDRESS 0  SA65806
         BZ    ODA21400                 BR IF YES               SA65806
*
         MVC   DXDEBSYS(K1),PLISTOPT(RPARC)  OPEN ATTRIBUTES IN WRK DEB
*
         MVC   DXRETMOD,ID5J6J          SAVE RETURN MODULE       Y02134
         IECRES LOAD,MODID=ID5J5F,BRCODE=K0,BRANCH=QUEUED        Y02134
*
***********************************************************************
*
ODA21400 EQU   *                        SKIP USER LABEL PROCESSING
*
         MODESET EXTKEY=DATAMGT         RETURN TO DATA MGMNT KEY Y02082
*
         IECRES LOAD,MODID=ID5J6J,BRCODE=K0,BRANCH=QUEUED        Y02080
*
***********************************************************************
*
*        CONSTANTS
*
***********************************************************************
*
ODA2500K DC    AL3(1)                   ADDR IF NO DCB EXIT LIST
*
         XCTLTABL ID=(ID5J5F,5F,ID5J6J,6J),BRT=YES,LENGTH=       Y02134
*
         IECDSECS EXPAND=YES            EXPAND DESIRED DSECTS HERE
*
         END
