         TITLE ' IFG0552N  RECOVER FROM INVALID TAPE '
IFG0552N CSECT
         ENTRY IGG0550H                                          Y02080
IGG0550H EQU   IFG0552N                 ALLIAS ENTRY POINT       Y02080
***********************************************************************
*                                                                     *
*                                                                     *
*          VS2 RELEASE 037 CHANGES                                    *
*A722313-722318,723610,723794-723814                           @ZA25563
*C712500,A713000-714000                                        @ZA29302
*
*          VS2 RELEASE 03 DELETIONS/CHANGES                           *
*                                                                     *
*717500,720923,731600-731920                                   @ZA03195
*0000719940-719960                                             @ZA02874
*0000                                                          @ZA13589
*          VS2 RELEASE 02 DELETIONS                                   *
*                                                                     *
*0000721974,708000,736000                                      @ZA02873
*0000                                                          @ZA02875
*                                                                     *
*                                                                     *
***********************************************************************
*                                                                     *
* MODULE NAME = IFG0552N (OS/VS2)                                     *
*                                                                     *
* DESCRIPTIVE NAME = RECOVER FROM INVALID TAPE                        *
*                                                                     *
* COPYRIGHT = NONE                                                    *
*                                                                     *
* STATUS = RELEASE 2, LEVEL 0                                         *
*                                                                     *
* FUNCTION =                                                          *
*        1) IF NOT ENTERED FOR NSL, CONTROL IS TRANSFERRED TO         *
*        IFG0554A (BR CODE OF 16) TO MOUNT/VERIFY WITH A RETURN MODULE*
*        SET UP AS IFG0552B (BR CODE OF 0).                           *
*        2) IF ENTERED FROM NSLEHDRO, THE UCBMOUNT BIT IS TESTED TO   *
*        VERIFY THAT THE NSL CHECK WAS OK. IF NOT OK, THE EXIT DES-   *
*        CRIBED IN (1) ABOVE IS TAKEN. IF THE NSL CHECK WAS OK,AN     *
*        ENQ ON THE VOLUME SERIAL IS ISSUED. IF THE RETURN FROM ENQ   *
*        INDICATES THAT THE VOLUME IS ALREADY IN USE, THE EXIT DES-   *
*        CRIBED IN (1) ABOVE IS TAKEN. OTHERWISE, THE JFCB IS UP-     *
*        DATED WITH THE NEW VOLUME SERIAL NUMBER, AND CONTROL IS      *
*        PASSED TO IFG0552F TO RETURN TO THE CALLER.                  *
*                                                                     *
* NOTES = SEE BELOW                                                   *
*                                                                     *
*    DEPENDENCIES =                                                   *
*            CLASS ONE CHARACTER CODE.  THE EBCDIC CHARACTER CODE     *
*            WAS USED FOR ASSEMBLY.  THE MODULE MUST BE REASSEMBLED   *
*            IF A DIFFERENT CHARACTER SET IS USED FOR EXECUTION.      *
*                                                                     *
*    RESTRICTIONS = NONE                                              *
*                                                                     *
*    REGISTER CONVENTIONS =                                           *
*            R2 POINTS TO DCB                                         *
*            R4 POINTS TO OPEN WORK AREA                              *
*            R5 POINTS TO THE RESIDENT ROUTINE                        *
*            R6 POINTS TO THE WTG TABLE                               *
*            R7 POINTS TO THE CURRENT PARAMETER LIST ENTRY            *
*            R8 POINTS TO THE CURRENT WTG TABLE ENTRY                 *
*            R9 POINTS TO THE DD ENTRY IN THE TIOT                    *
*            R10 POINTS TO THE UCB                                    *
*                                                                     *
*    PATCH LABEL = SEE THIRD FROM LAST LABEL BEFORE ORG STATEMENT AT  *
*                  END OF LISTING.                                    *
*                                                                     *
* MODULE TYPE = CONTROL (OPEN, CLOSE, EOV DATA MANAGEMENT)            *
*                                                                     *
*    PROCESSOR = ASSEMBLER XF                                         *
*                                                                     *
*    MODULE SIZE = SEE EXTERNAL SYMBOL DICTIONARY OR LOC FIELD ON     *
*                  ORG STATEMENT AT END OF LISTING                    *
*                                                                     *
*    ATTRIBUTES = REENTRANT, REFRESHABLE,READ-ONLY, ENABLED,          *
*                 PRIVILEGED, SUPERVISOR STATE, DATA MANAGEMENT KEY,  *
*                 LINK PACK AREA RESIDENT/PAGEABLE                    *
*                                                                     *
* ENTRY POINT =                                                       *
*        IFG0552N                                                     *
*        IFG0550H - ALIAS                                             *
*                                                                     *
*    PURPOSE = SEE FUNCTION                                           *
*                                                                     *
*    LINKAGE =                                                        *
*        THIS MODULE IS TRANSFERRED CONTROL THROUGH THE IECRES-LOAD   *
*        MACRO INSTRUCTION.                                           *
*                                                                     *
* INPUT =                                                             *
*        GIVEN CONTROL IN PROTECT KEY 5.                              *
*        REGISTER 2 POINTS TO THE COPIED DCB.                         *
*        DEBDCBAD POINTS TO THE COPIED DCB.                           *
*        REGISTER 4 POINTS TO THE EOV WORKAREA                        *
*                                                                     *
* OUTPUT =                                                            *
*        THE NEXT MODULE IS GIVEN CONTROL IN PROTECT KEY 5 WITH       *
*        REGISTER 2 POINTING TO THE COPIED DCB,                       *
*        DEBDCBAD POINTING TO THE COPIED DCB,                         *
*        AND REGISTER 4 POINTING TO THE EOV WORKAREA,                 *
*                                                                     *
* EXIT-NORMAL =                                                       *
*        IFG0554A - MOUNT/VERIFY, BRANCH CODE = 16                    *
*           RETURN MODULE - IFG0552B, BRANCH CODE = 0                 *
*        IFG0552F - NSL EXIT, BRANCH CODE = 4                         *
*                                                                     *
* EXIT-ERROR = NONE                                                   *
*                                                                     *
* EXTERNAL REFERENCES = SEE BELOW                                     *
*                                                                     *
*    ROUTINES =                                                       *
*        IFG019RA THROUGH THE IECRES MACRO.                           *
*                                                                     *
*    DATA AREAS =                                                     *
*        EOV WORKAREA.                                                *
*                                                                     *
*    CONTROL BLOCK =                                                  *
*        CVT                                                          *
*        DEB                                                          *
*        JFCB                                                         *
*        UCB                                                          *
*        TCB                                                          *
*        TIOT                                                         *
*                                                                     *
* TABLES =                                                            *
*                                                                     *
* MACROS =                                                            *
*        MODESET                                                      *
*        IECRES LOAD                                                  *
*        IECRES GET                                                   *
*        IECRES LOCJFCB                                               *
*        IECRES WRJFCB                                                *
*        ENQ                                                          *
*                                                                     *
* CHANGE ACTIVITY =                                                   *
*        SEE CHANGES/DELETIONS SECTION JUST AFTER CSECT CARD.         *
*                                                                     *
***********************************************************************
         EJECT
         BALR  RBASE,0                  ESTABLISH BASE
         USING *,RBASE
         USING FORCORE,RCORE
         USING WTG,RWTG                                          Y02080
         USING DEB,RDEB
         USING IHADCB,RDCB
         USING SRT,RUCB
         USING CVT,RET
         USING TIOENTRY,RTIOT
         L     RUCB,DXUCBADR            LOAD CURRENT UCB ADDR    Y02134
*
         TM    DXOPCLSW,HEX40           IS ENTRY FROM NSL ROUTINE
         BO    ETO33000                 YES, BR
*
ETO32700 NI    DXDCBDEB,X'FF'-DCBIFPIO  TURN OFF PERM ERR FLG
         MVC   DXRETMOD,ID2N2B          SET UP RETURN LOAD       Y02134
         MVI   DXRETCOD,K0              SET RETURN OFFSET        Y02134
         LA    RWTG,DXXAREA             WTG TABLE POINTER      @ZA02875
         LA    RWTGC,DXXENTRY           CURRENT WTG ENTRY      @ZA02875
*                                                                Y02134
         IECRES LOAD,MODID=ID2N4A,BRCODE=K16,BRANCH=QUEUED       Y02134
         EJECT
*                                                                Y02134
ETO33000 EQU   *                        ENTRY FROM NSL
         L     RDCB,DXUDCBAD            PT TO CALLER'S DCB       YM3005
         MODESET KEYADDR=DXUKEY,WORKREG=1 ASSUME CALLER'S KEY    YM3005
         OC    DCBOFLGS,DCBOFLGS        PGM CHK IF NOT CALLER'S  YM3005
         LA    R0,ALLBITS-DCBOBUSY      ISOLATE BUSY BIT MASK    YM3005
         IC    R1,DCBOFLGS              GET CALLER'S OFLGS       YM3005
         MODESET EXTKEY=DATAMGT         DATA MANAGEMENT KEY      YM3005
         MVI   DXECB,K32                RESTORE DXECB          @ZA13589
         L     RDCB,DXPDCBAD            PT TO COPIED DCB         YM3005
         OR    R0,R1                    ISOLATE CALLER BUSY BIT  YM3005
         IC    R1,DCBOFLGS              GET COPIED DCBOFLGS      YM3005
         NR    R1,R0                    UPDTE BUSY BIT TO CALLER YM3005
         STC   R1,DCBOFLGS              UPDATE OFLGS FIELD       YM3005
         LR    RES,R8                   RESTORE R5  FOR RES RTN
         MVC   DXXMODNM,IGG2N           RESTORE XCTL NAME      @ZA02873
         TM    UCBDMCT,UCBMOUNT         WAS NSL CHK OK
         BZ    ETO33050                 BR IF OFF              @ZA29302
         NI    JFCBMASK+K5,ALLBITS-K1   RESET L NUMBER GENED   @ZA29302
         B     ETO36000                 BR NSL CK NOT OK       @ZA29302
ETO33050 EQU   *                                               @ZA29302
*                                                                Y02134
         MODESET EXTKEY=SUPR            UCB KEY                  Y02082
         NI    UCBDMCT,ALLBITS-UCBMOUNT SET NSL REJECT OFF     @ZA03195
         MODESET EXTKEY=DATAMGT         DATA MANAGEMENT KEY      Y02082
*                                                                Y02134
         CLC   DXVOLSR2,SCRTCH          CHECK FOR NONSPECIFIC    Y02134
         BNE   ETO35000                 BRANCH IF NOT SCRTCH     Y02134
*                                                                Y02134
         TM    JFCBMASK+K5,K1           WAS L-NUMBER GENERATED @ZA02874
         BZ    ETO33100                 NO, BR                 @ZA02874
         CLC   UCBVOLI,DXCCW7           DID NSL ROUTINE        @ZA02874
*                                       CHANGE L-NUMBER TO     @ZA02874
*                                       SPECIFIC VOLUME SERIAL @ZA02874
         BE    ETO35000                 NO, BR                 @ZA02874
         NI    JFCBMASK+K5,ALLBITS-K1   YES, SET L-NUMBER      @ZA02874
*                                       GENERATED BIT OFF      @ZA02874
ETO33100 EQU   *                                               @ZA02874
*                                                                Y02134
         MVC   DXWORK1(K16),ENQLIST     INIT ENQ LIST            Y02134
         L     RF,DXJSCBAD              LOAD JSCB ADDRESS        Y02134
         L     R0,JSCBTCBP-IEZJSCB(,RF) INITIATOR TCB POINTER    Y02134
         ENQ   (,UCBVOLI),TCB=(0),RET=USE,MF=(E,DXWORK2),        Y02134X
               RELATED=(SYSZVOLS,IFG0194A(VUS02450))             Y02134
*                                                                Y02134
         SR    R0,R0                    VOL VERIFICATION ERROR @ZA03195
         LTR   RF,RF                    VOLUME ALREADY IN USE    Y02134
         BNZ   ETO32700                 BRANCH TO REJECT         Y02134
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
*        UPDATE JFCB WITH NEW VOLUME FROM UCBVOLI                Y02134
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
         LH    RC,DXVOLSEQ              GET VOL SEQ OF VOL N     Y02134
         LA    RD,JFCBVOLS              LOAD POINTER TO VOLUMES  Y02134
         LA    RET,DXJBF                LOAD POINTER TO JFCB   @ZA02873
         MVC   DXWORK1,TIOEJFCB         SAVE JFCB PTR            Y02134
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
*        CHECK FOR VOLUME IN MAIN JFCB                           Y02134
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
         CH    RC,VOL5                  VOL SEQ LESS THAN 5      Y02134
         BNH   ETO34200                 BRANCH, VOL SER IN MAIN  Y02134
         B     ETO34500                 WRITE BACK MAIN JFCB   @ZA25563
ETO33600 EQU   *                                               @ZA25563
         SH    RC,VOL5                  DECREMENT FOR INDEXING   Y02134
         LA    RET,JFCBEXAD             SET UP POINTER FOR EXT @ZA02873
*                                                                Y02134
         IECRES GET,LV=JFCBLGTH,PREFIX=YES,STM=(2,14,WTGPREFX)   Y02134
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
*        CHECK FOR VOLUME IN JFCB EXTENSION.                     Y02134
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
ETO34100 EQU   *                        LOCATE VOLSER            Y02134
*                                                                Y02134
         MVC   DXWORK1,JFCBEXAD-JFCBEXAD(RET) SAVE JFCB PTR      Y02134
         IECRES LOCJFCB,(RET)           LOCATE JFCB EXTENSION    Y02134
         MVC   K0(JFCBLGTH,R1),K0(RET)  MOVE JFCB TO WORK AREA   Y02134
         LR    RET,R1                   LOAD POINTER TO JFCB     Y02134
         LA    RD,K4(RET)               LOAD POINTER TO VOLUMES  Y02134
         CH    RC,VOL15                 VOL SEQ LESS THAN 15     Y02134
         BNH   ETO34200                 BRANCH, VOL SER IN EXT   Y02134
         SH    RC,VOL15                 DECREMENT FOR INDEXING   Y02134
         B     ETO34100                 BRANCH TO CHECK NEXT EXT Y02134
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
*        REFERENCED VOLUME HAS BEEN FOUND.                       Y02134
*        RC CONTAINS THE POSITION OF THE VOLUMES IN THE LIST.    Y02134
*        RD POINTS TO THE LIST OF VOLUMES.                       Y02134
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
ETO34200 EQU   *                        MOVE VOL SER TO JFCB     Y02134
*                                                                Y02134
         BCTR  RC,R0                    DECREMENT FOR INDEXING   Y02134
         MH    RC,VOL6                  CONVERT SEQ NO TO OFFSET Y02134
         AR    RD,RC                    ADD OFFSET TO BASE       Y02134
         MVC   K0(L'UCBVOLI,RD),UCBVOLI MOVE VOLUME N TO JFCB    Y02134
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
*        WRITE THE JFCB BACK.                                    Y02134
*                                                                Y02134
*****************************************************************Y02134
ETO34500 EQU   *                                               @ZA25563
         LA    R1,DXWORK1               POINT TO SAVED JFCB PTR  Y02134
*                                                                Y02134
         IECRES WRJFCB,(R1),(RET)       WRITE JFCB BACK          Y02134
*                                                                Y02134
         LH    RC,DXVOLSEQ              GET VOL SEQUENCE       @ZA25563
         CH    RC,VOL5                  MORE THAN FIVE VOLS?   @ZA25563
         BNH   ETO35000                 CONTINUE               @ZA25563
         CLC   TIOEJFCB,DXWORK1         HAS EXT BEEN WRITTEN?  @ZA25563
         BZ    ETO33600                 GO UPDATE JFCB EXT     @ZA25563
ETO35000 EQU   *                        JFCB WRITTEN BACK        Y02134
*                                                                Y02134
         LA    RET,K4                   SET UP BRANCH OFFSET     Y02134
         LA    R7,ID2N2F                GO TO TAPE EXIT          Y02134
         LA    RWTG,DXXAREA             POINT TO EOV WTG         Y02134
         LA    RWTGC,DXXENTRY           SET R8 FOR RES RTN       Y02134
         IECRES LOAD,MODID=(R7),BRANCH=QUEUED GO TO NEXT MODULE  Y02080
*                                                                Y02134
ETO36000 EQU   *                        NSL CHECK NOT OK         Y02144
         NI    DXATOUTA,X'FF'-DXATNSL   RESET NSL RTN CNTRL FLAG Y02144
         MODESET EXTKEY=SUPR            UCB KEY                @ZA03195
         NI    UCBDMCT,ALLBITS-UCBMOUNT SET NSL REJECT OFF     @ZA03195
         MODESET EXTKEY=DATAMGT         DATA MANAGEMENT KEY    @ZA03195
         SR    R0,R0                    VOL VERIFICATION ERROR @ZA03195
         CLC   DXVOLSR2,SCRTCH          IS THIS SCRTCH REQUEST @ZA03195
         BE    ETO32700                 YES,BRANCH TO NEXT MOD @ZA03195
         MODESET EXTKEY=SUPR            UCB KEY                @ZA03195
         XC    UCBVOLI,UCBVOLI          CLEAR WRONG VOL SER    @ZA03195
         MODESET EXTKEY=DATAMGT         DATA MANAGEMENT KEY    @ZA03195
         B     ETO32700                 BRANCH TO NEXT MODULE    Y02144
*                                                                Y02144
IGG2N    DC    C'IFG0552N'              1ST 6 CHAR OF MOD NAME @ZA02873
*                                                                Y02134
ENQLIST  DC    A(0)                     ENQ LIST                 Y02134
         ENQ   (SYSZVOLS,,E,6,SYSTEM),TCB=,RET=USE,MF=L          Y02134
SYSZVOLS DC    CL8'SYSZVOLS'            ENQ MAJOR NAME           Y02134
SCRTCH   DC    CL6'SCRTCH'              NONSPECIFIC REQUEST      Y02134
VOL5     DC    H'5'                     HALFWORD OF FIVE         Y02134
VOL6     DC    H'6'                     HALFWORD OF SIX          Y02134
VOL15    DC    H'15'                    HALFWORD OF FIFTEEN      Y02134
*                                                                Y02134
XCTLTA2N XCTLTABL ID=(ID2N4A,4A,ID2N2B,2B,ID2N2F,2F),            Y02134X
               SVC=055,BRT=YES,LENGTH=                           Y02134
         IECDSECS CVT,TCB,JSCB,TIOT,UCB,DCB,DEB,MAIN,WTG,PREFX,  Y02134*
               EXPAND=YES                                        Y02080
         IECEQU
         END
