         TITLE 'IFG0232M - TCLOSE TAPE STANDARD TRAILER LABEL FUNCTION'
IFG0232M CSECT
*
***********************************************************************
*                                                                     *
*                                                                     *
*          VS2 RELEASE 02 DELETIONS/CHANGES                           *
*0000295500                                                      YM1456
*0000247000-252000,296000,297000,351500-357000,562000-564000     YM3000
*0000279500                                                      YM5314
*0000                                                            YM5389
*0000                                                            YM7548
*          RELEASE 21.7 DELETIONS/CHANGES                             *
*0000387000-388000,390000                                       SA53865
*          RELEASE 21 DELETIONS/CHANGES                               *
*0000160000-162000                                               M0011
*0000382000-384000,426000-427000                                 M0112
*                                                                     *
* MODULE NAME - IFG0232M                                              *
*                                                                     *
* DESCRIPTIVE NAME - TCLOSE TAPE STANDARD TRAILER LABEL PROCESSING    *
*                                                                     *
* COPYRIGHT - NONE                                                    *
*                                                                     *
* CHANGE ACTIVITY - SEE DELETIONS/CHANGES FOLLOWING THE CSECT CARD.   *
*                                                                     *
* STATUS CHANGE LEVEL 000                                             *
*                                                                     *
* FUNCTION -                                                          *
*    THIS MODULE CONTAINS PART II OF THE 'TCLOSE TAPE STANDARD        *
*    TRAILER LABEL FUNCTION'. REFER TO THE FOLLOWING 'FUNCTION        *
*    PROLOG' FOR DETAILS.                                             *
*                                                                     *
* ENTRY POINTS -                                                      *
*         IFG0232M                                                    *
*                                                                     *
* INPUT -                                                             *
*    ENTERED IN DATA MANAGEMENT KEY                              Y02082
*    REGISTER 14 ('RET') WILL CONTAIN ONE OF THE BRANCH TABLE OFFSETS *
*    LISTED BELOW, THE BRANCH TABLE BEING USED TO DETERMINE AT WHICH  *
*    POINT PROCESSING IS TO BEGIN:                                    *
*       0 - ENTRY FROM MODULE IFG0232G TO FINISH FORMATTING AND       *
*           WRITE TRAILER LABEL 'EOF2' AND TO PROCESS OUTPUT USER     *
*           LABELS, AS REQUIRED;                                      *
*       4 - ENTRY FROM MODULE IGC0002C TO PROCESS DEFERRED            *
*           INPUT USER LABELS, AS REQUIRED.                           *
*    REFER TO THE FOLLOWING 'FUNCTION PROLOG' FOR ADDITIONAL DETAILS. *
*                                                                     *
* OUTPUT -                                                            *
*    EXIT IN DATA MANAGEMENT KEY                                 Y02082
*    REFER TO THE FOLLOWING 'FUNCTION PROLOG'.                        *
*                                                                     *
* EXTERNAL REFERENCES -                                               *
*         IFG019RA - OPEN/CLOSE/EOV/DADSM COMMON ROUTINE TO OBTAIN    *
*                    AND FREE CORE, TO REFRESH THE CALLER'S DCB,      *
*                    TO XCTL, AND TO WAIT.                            *
*                                                                     *
* EXITS, NORMAL -                                                     *
*    REFER TO THE FOLLOWING 'FUNCTION PROLOG'.                        *
*                                                                     *
* EXITS, ERROR -                                                      *
*         IFG0230P - WHEN AN ERROR CONDITION OCCURS IN THIS MODULE.   *
*                    REFER TO THE FOLLOWING 'FUNCTION PROLOG'.        *
*                                                                     *
* TABLES/WORK AREAS -                                                 *
*    REFER TO THE FOLLOWING 'FUNCTION PROLOG'.                        *
*                                                                     *
* ATTRIBUTES -                                                        *
*    REENTRANT, REFRESHABLE, READ-ONLY, ENABLED, PRIVILEGED           *
*                                                                     *
* CHARACTER CODE DEPENDENCY -                                         *
*    CLASS ONE CHARACTER CODE DEPENDENCY - THE EBCDIC CHARACTER CODE  *
*    WAS USED FOR ASSEMBLY.  THE MODULE MUST BE REASSEMBLED IF A      *
*    DIFFERENT CHARACTER SET IS USED FOR EXECUTION.                   *
*                                                                     *
* NOTES -                                                             *
*    REFER TO THE FOLLOWING 'FUNCTION PROLOG'.                        *
*                                                                     *
***********************************************************************
*
*****    TCLOSE INTERNAL ERROR CODES
*
TABD141  EQU   141                      ERROR POSITIONING FOLLOWING
*                                       ..USER LABEL PROCESSING
*
         BALR  RBASE,0                  ESTABLISH ADDRESSABILITY
         USING *,RBASE
         USING FORCORE,RCORE
         USING IHADCB,RDCB
         USING UCB,RUCB
         USING IHADEB,RDEB
         USING TIOT,RTIOT
         USING WTG,RWTG
*
         B     TCT06000(RET)            DETERMINE TYPE OF ENTRY TO MOD
TCT06000 EQU   *
         B     TCT06200                 BR TO FINISH TRLR LABEL 2
         B     TCT07400                 BR TO PROCESS INPUT LABELS
         EJECT
***********************************************************************
*                                                                     *
*                          FUNCTION PROLOG                            *
*                                                                     *
***********************************************************************
*                                                                     *
* FUNCTION NAME -                                                     *
*    'TCLOSE TAPE STANDARD TRAILER LABEL FUNCTION', PART II           *
*                                                                     *
* (STATUS) -                                                          *
*    NOT APPLICABLE                                                   *
*                                                                     *
* FUNCTION -                                                          *
*    1. FINISH FORMATTING AND WRITE END-OF-FILE LABEL 2 (EOF2);       *
*    2. PROCESS OUTPUT OR DEFERRED INPUT USER LABELS AS REQUIRED;     *
*       A. A 152 BYTE USER LABEL WORK AREA IS OBTAINED FOR       Y02082
*          TCLOSE FROM FETCH PROTECTED SUBPOOL 229 IN DATA       Y02082
*          MANAGEMENT KEY. A 96 BYTE USER LABEL WORK AREA IS     Y02082
*          OBTAINED FOR THE USER FROM FETCH PROTECTED SUBPOOL    Y02082
*          229 IN USER KEY.                                      Y02082
*       B. IF OUTPUT USER LABELS ARE TO BE PROCESSED, THE LABEL       *
*          IDENTIFIER IS FORMATTED IN A BUFFER AND CONTROL IS PASSED  *
*          TO THE EXIT FROM IFG019RA THROUGH THE IECRES-UEXIT    Y02082
*          MACRO. A LABEL IS THEN WRITTEN IF THE USER PASSES     Y02082
*          A RETURN CODE OF '8'; THIS CONTINUES UNTIL EITHER THE      *
*          MAXIMUM NUNBER OF LABELS (CURRENTLY 8) HAS BEEN WRITTEN    *
*          OR UNTIL THE USER INDICATES THAT PROCESSING IS TO          *
*          TERMINATE.                                                 *
*       C. IF DEFERRED INPUT USER LABELS ARE TO BE PROCESSED, A       *
*          LABEL IS READ AND CONTROL IS PASSED TO THE USER FROM  Y02082
*          IFG019RA THROUGH THE IECRES-UEXIT MACRO.  ADDITIONAL  Y02082
*          LABELS ARE READ IF THE USER PASSES A RETURN CODE OF '4';   *
*          THIS CONTINUES UNTIL EITHER THE MAXIMUM NUMBER OF LABELS   *
*          HAS BEEN READ OR UNTIL THE USER INDICATES THAT PROCESSING  *
*          IS TO TERMINATE.                                           *
*       D. THE USER LABEL INTERFACE IS AS FOLLOWS-                    *
*          REGISTERS ARE INITIALIZED FOR THE USER BY IFG019RA    Y02082
*          IN THE FOLLOWING MANNER:                              Y02082
*             1    - ADDRESS OF A 4-WORD USER LABEL PARAMETER LIST;   *
*             2-13 - USER'S REGISTERS AT ENTRY TO TCLOSE;             *
*             14   - RETURN ADDRESS                              Y02082
*             15   - USER'S LABEL EXIT ROUTINE ADDRESS.               *
*          PARAMETER LIST CONTENTS-                                   *
*             WORD 1- ADDRESS OF THE USER LABEL BUFFER, OR '0' IF     *
*                     NO LABELS WERE READ (I.E., TAPE MARK WAS READ); *
*             WORD 2- ADDRESS OF THE DCB BEING PROCESSED; THE HIGH    *
*                     ORDER BIT OF THE FIRST BYTE IS ON IF THIS IS    *
*                     EOF AND IT IS OFF IF THIS IS EOV;               *
*             WORD 3- '0' IF NO ERRORS WERE ENCOUNTERED; OTHERWISE,   *
*                     THE ADDRESS OF THE IOB WHICH INCURRED THE ERROR,*
*                     WITH THE HIGH ORDER BIT OF THE FIRST BYTE ON;   *
*             WORD 4- '0';                                            *
*          CONTROL BLOCK INDICATORS-                                  *
*             'DCBOFLGS' - X'02', OFF - THE 'LOCK' BIT IS TURNED OFF  *
*                          WHEN CONTROL IS PASSED TO THE USER AND IS  *
*                          TURNED BACK ON UPON RETURN.                *
*          USER RETURN CODES (PASSED IN REGISTER 15)-                 *
*             0 - INPUT - CONTINUE PROCESSING INPUT USER LABELS;      *
*                 OUTPUT- DISCONTINUE PROCESSING OUTPUT USER LABELS;  *
*             4 - DISCONTINUE PROCESSING INPUT USER LABELS;           *
*             8 - CONTINUE PROCESSING OUTPUT USER LABELS.             *
*                                                                     *
* ENTRY POINTS -                                                      *
*    REFER TO THE PRECEDING MODULE PROLOG.                            *
*                                                                     *
* INPUT -                                                             *
*    1. REGISTER CONTENTS AT ENTRY ARE AS FOLLOWS-                    *
*       'RDCB' - ADDRESS OF THE DCB BEING PROCESSED;                  *
*       'RCORE'- ADDRESS OF THE WORK AREA FOR THAT DCB;               *
*       'RES'  - ADDRESS OF THE OPEN/CLOSE/EOV RESIDENT ROUTINE;      *
*       'RWTG' - ADDRESS OF THE WHERE-TO-GO TABLE;                    *
*       'RPARC'- ADDRESS OF THE CURRENT TCLOSE PARAMETER LIST ENTRY;  *
*       'RWTGC'- ADDRESS OF THE CURRENT WHERE-TO-GO TABLE ENTRY;      *
*       'RTIOT'- ADDRESS OF THE CURRENT TIOT ENTRY FOR THE DCB;       *
*       'RUCB' - ADDRESS OF THE CURRENT UCB FOR THE DCB;              *
*       'RDEB' - ADDRESS OF THE DCB'S DEB;                            *
*                                                                     *
* OUTPUT -                                                            *
*    1. AT EXIT TO MODULE IFG0232S-                                   *
*       REGISTER CONTENTS ARE THE SAME AS AT ENTRY EXCEPT THAT        *
*          REGISTER 'RET' CONTAINS A BRANCH TABLE OFFSET OF '4';      *
*       WORK AREA INDICATORS-                                    M0011*
*          'DXCCW7' - X'FF' - INDICATES THAT DEFERRED USER LABEL M0011*
*                             PROCESSING OCCURRED (REQUIRED BY   M0011*
*                             THE TAPE POSITIONING FUNCTION).    M0011*
*       TAPE POSITION-                                                *
*          OUTPUT- AT THE INTERBLOCK GAP FOLLOWING THE TRAILER LABELS;*
*          INPUT - IF DEFERRED USER LABEL PROCESSING HAS BEEN    M0011*
*                  PERFORMED, AT THE INTERBLOCK GAP FOLLOWING    M0011*
*                  THE LAST LABEL PROCESSED.                     M0011*
*                  IF EOF OCCURRED AND USER LABELS WERE NOT      M0011*
*                  PROCESSED, AT THE INTERBLOCK GAP FOLLOWING    M0011*
*                  THE TAPE MARK FOLLOWING THE TRAILER LABELS.   M0011*
*                  IF EOF DID NOT OCCUR, ANYWHERE IN DATA AREA.  M0011*
*    2. AT EXIT TO PROBLEM DETERMINATION MODULE IFG0230P-             *
*       REGISTER CONTENTS ARE THE SAME AS AT ENTRY EXCEPT THAT        *
*          REGISTER 'R0' CONTAINS ONE OF THE FOLLOWING INTERNAL       *
*          ERROR CODES-                                               *
*          124- ERROR WRITING END-OF-FILE LABEL 2;                    *
*          141- ERROR BACKSPACING FOLLOWING USER TRAILER LABEL        *
*               PROCESSING.                                           *
*       WORK AREA INDICATORS-                                         *
*          'DXCALLID' - CONTAINS THE ID OF THIS MODULE.               *
*                                                                     *
* EXTERNAL REFERENCES -                                               *
*    REFER TO THE PRECEDING MODULE PROLOG.                            *
*                                                                     *
* EXITS, NORMAL -                                                     *
*         IFG0232S - TO DELIMIT AN OUTPUT DATA SET AND TO POSITION    *
*                    ACCORDING TO THE TCLOSE POSITIONING OPTION.      *
*                                                                     *
* EXITS, ERROR -                                                      *
*    REFER TO THE PRECEDING MODULE PROLOG.                            *
*                                                                     *
* TABLES/WORK AREAS -                                                 *
*    THE TCLOSE WORK AREA AND THE WHERE-TO-GO (WTG)                   *
*    TABLE ARE DESCRIBED BY THE DSECTS AT THE END OF THE LISTING.     *
*                                                                     *
* ATTRIBUTES -                                                        *
*    REFER TO THE PRECEDING MODULE PROLOG.                            *
*                                                                     *
* CHARACTER CODE DEPENDENCY -                                         *
*    REFER TO THE PRECEDING MODULE PROLOG.                            *
*                                                                     *
* NOTES -                                                             *
*                                                                     *
***********************************************************************
*
***********************************************************************
*              FINISH TRAILER LABEL 2                                 *
***********************************************************************
*
TCT06200 EQU   *
         SH    RTIOT,DCBTIOT            POINT TO BEGINNING OF TIOT
         MVC   FL2JOBD,TIOCNJOB         PLACE JOB/STEP IDENTIFICATION
         MVC   FL2STEPD,TIOCSTEP        ..FROM THE TIOT INTO THE LABEL
         MVI   FL2JSSP,SLASH            INSERT SLASH BETWEEN JOB AND
*                                       ..STEP NAMES
*
*****          TAPE RECORDING TECHNIQUE
*
         SR    RC,RC                    CLEAR WORK REGISTER AND INSERT
         IC    RC,JFCTRTCH              ..JFCB TAPE RECORDING TECHNIQUE
         SRL   RC,K4                    SHIFT OUT HIGH ORDER CHAR OF
         STC   RC,FL2TRTCH              ..JFCTRTCH AND STORE IN LABEL
         TR    FL2TRTCH(K1),ATRTCH      TRANSLATE RECORDING TECHNIQUE
         CLI   JFCTRTCH,JFCTREV         IS THE RECORDING TECHNIQUE BOTH
*                                       ..TRANSLATION AND EVEN PARITY
         BNE   TCT06400                 ..BR IF NOT- 2ND CHAR IS BLANK
         MVI   FL2TRTCH+K1,CHART        ..ELSE SET 2ND CHAR TO 'T'
*
*****          CARRIAGE CONTROL CHARACTER SET
*
TCT06400 EQU   *
         TM    JFCRECFM,JFCMAC          DOES JFCB SPECIFY MACHINE CTRL-
         BNO   TCT06600                 ..BR IF NOT- CHK FOR 'ASA'
         MVI   FL2CNTRL,CHARM           ..ELSE SET MACHINE CTRL IN LBL
TCT06600 EQU   *
         TM    JFCRECFM,JFCASA          DOES JFCB SPECIFY ASA CONTROL-
         BNO   TCT06800                 ..BR IF NOT- DO NOT SET CONTROL
         MVI   FL2CNTRL,CHARA           ..ELSE SET ASA CONTROL IN LABEL
TCT06800 EQU   *
         TM    SRTESTAB,UCBBSTR         WERE ANSI LABELS SPECIFIED-
         BNO   TCT06900                 ..BR IF NOT TO CHKPT FLG Y02083
*
*****          BUFFER ALIGNMENT
*
         MVC   DXCCW5(K1),JFCBUFOF      GET BUFFER OFFSET LENGTH
         NI    DXCCW5,X'FF'-LASTNTRY    TURN OFF BIT 0
         SR    RC,RC                    CLEAR WORK REGISTER
         IC    RC,DXCCW5                PLACE BUFFER LENGTH INTO REG
         CVD   RC,DXCCW5                ..AND CONVERT IT TO DECIMAL,
         UNPK  FL2BUFOF,DXCCW5          ..THEN UNPACK AND STORE IN LBL
         OI    FL2BUFOF+K1,ZONEOF       RESET SIGN BITS
         MVC   FL1LABI,AEOF1            MOVE IN E-O-F CONSTANT
*
*****          TRANSLATE ANSI DATA BEFORE WRITING LABEL
*
         XLATE DXLBL,K80,TO=A           TRANSLATE LABEL DATA     YM3000
*                                                                Y02083
*****          SET CHECKPOINT D/S INDICATOR IF APPROPRIATE       Y02083
*                                                                Y02083
TCT06900 L     RC,DXDSAB                FETCH DSAB ADDRESS       Y02083
         USING DSAB,RC                  ADDRESS DSAB             Y02083
         TM    DSABFLG4,DSABCKDS        Q - CHECKPOINT D/S       Y02083
         BZ    TCT07000                 IF NOT, BRANCH           Y02083
         DROP  RC                       DSAB ADDRESSABILITY      Y02083
         MVI   FL2DSIND,FL2CKDS         SET CHKPT LABEL FLAG     Y02083
*
*****          WRITE END OF FILE LABEL 2
*
TCT07000 EQU   *
         LA    RD,TABD124               LOAD INTERNAL ERROR CODE
         BAL   RC,TCT12600              WRITE FILE LABEL 2
         BNO   TCT12800                 BR IF I/O ERROR
         OI    DXATOUTA,DXATTRL2        TRL2 LABEL WRITTEN       Y02144
         EJECT
***********************************************************************
*              DETERMINE IF USER LABELS ARE TO BE PROCESSED           *
***********************************************************************
*
TCT07200 EQU   *
         TM    JFCBTSDM,JFCSDS          IS DATA SET SYSOUT OR SYSIN-
         BO    TCT12400                 ..BR IF YES- BYPASS USER LABELS
         TM    JFCBLTYP,JFCBUL          WERE USER LABELS SPECIFIED-
         BNO   TCT12400                 ..BR IF NOT- BYPASS USER LABELS
         LA    RD,EXIT4                 PT TO OUTPUT USER TRLR LBL CODE
         B     TCT07600                 BR TO TEST FOR USER LABELS
TCT07400 EQU   *
         LA    RD,SLPSUP                PT TO DEFERRED USER LABEL
*                                       ..PROCESSING CODE
TCT07600 EQU   *
         L     RC,DCBEXLST              LOAD EXIT LIST ADDR FROM DCB
         LA    RC,K0(,RC)               ZERO EXIT LIST HIGH-ORDER BYTE
         LTR   RC,RC                    IS THERE A DCB EXIT LIST-
         BZ    TCT12400                 ..BR IF NOT
         MODESET KEYADDR=DXUKEY,WORKREG=15  ASSUME USER KEY      YM5314
TCT07800 EQU   *
         CLC   K0(K1,RC),K0(RD)         DO CODE BYTES MATCH-
         BE    TCT08000                 ..BR IF YES- PROCESS USER LBLS
         CLC   K0(K1,RC),K1(RD)         IS 'LAST ENTRY' IND ALSO ON-
         BE    TCT08000                 ..BR IF YES- PROCESS USER LABEL
         TM    K0(RC),LASTNTRY          IS THIS LAST EXIT LIST ENTRY-
         LA    RC,K4(,RC)               POINT TO NEXT ENTRY
         BNO   TCT07800                 ..BR IF NOT- CHK NEXT ENTRY
         B     TCT12400                 ..ELSE BR TO POSITION TAPE
TCT08000 EQU   *
         LR    RD,RDEB                  SAVE THE DEB ADDRESS
*
*****          RDEB WILL NOW BE USED AS A WORK REGISTER
*****          RD WILL NOW BE USED AS THE DEB BASE REGISTER
*
         L     RDEB,K0(,RC)             LOAD EXIT LIST ENTRY
         NC    K1(K3,RC),K1(RC)         TEST FOR A VALID EXIT    YM3000X
                                        LIST ENTRY ADDRESS       YM3000
         MODESET EXTKEY=DATAMGT         RETURN TO DATAMGT KEY    YM1456
         BZ    TCT12400                 BRANCH IF NOT - BYPASS   YM3000X
                                        USER LABEL PROCESSING    YM3000
*
***********************************************************************
*              USER TRAILER LABEL PROCESSING                          *
***********************************************************************
*
*                                                                Y02082
*    OBTAIN USER LABEL WORK AREA FOR TCLOSE                      Y02082
*                                                                Y02082
         IECRES GET,LV=USERLDM,PREFIX=YES,SP=K229,               Y02082*
               STM=(2,14,WTGPREFX),ID=ULWA                       Y02144
         USING ULDMWK,R1                                         Y02082
         STM   RPAR,RC,ULREGSAV         SAVE REGS (INCLUDES DCB EXIT
*                                       ..LIST ENTRY)
         DROP  R1
         LR    RUCB,R1                  POINT TO USER LABEL WORK AREA
         USING ULDMWK,RUCB              BASE FOR TCLOSE UL W.A.  Y02082
*                                                                Y02082
*    OBTAIN USER LABEL WORK AREA FOR USER                        Y02082
*                                                                Y02082
         IECRES GET,LV=USERLU,PREFIX=NO,KEY=USER,SP=K229,        Y02082*
               STM=(2,14,WTGPREFX)                               Y02082
         USING ULUWK,R9                 BASE FOR USER'S UL W.A.  Y02082
         LR    R9,R1                    ADDR OF GOTTEN CORE      Y02082
         MODESET EXTKEY=DATAMGT         RET TO TCLOSE KEY        Y02082
         LA    R1,CHAR0                 INITIALIZE USER LABEL COUNT
         STH   R1,ULCNT                 ..AND SAVE FOR LATER USE
         MVI   ULDMBUFR,BLANK           INIT LABEL BUFFER AREA   Y02082
         MVC   ULDMBUFR+K1(L'ULDMBUFR-K1),ULDMBUFR TO BLANDS     Y02082
*
*****    INITIALIZE CHANNEL PROGRAM FOR USER LABEL PROCESSING
*
         LA    R1,ULDMBUFR              ADDR OF USER LABEL BUFFERY02082
         ST    R1,DXCCW1                ..AND STORE IN CHANNEL PROGRAM
         MVI   DXCCW1,CCWRDTAP          PLACE READ CMD INTO CCW  M0112
         MVI   DXCCW1+K4,K0             TURN OFF ALL CCW FLAGS   M0112
         MVI   DXCCW1+K7,ULPARM-ULBUFR  SET LENGTH TO BFR SIZE   M0112
         TM    ULREG11,XLDEFSL          WAS DEFERRED INPUT LBL PROCESS-
*                                       ..ING SPECIFIED (TEST REG 11)-
         BO    TCT09200                 ..BR IF YES
*
***********************************************************************
*              OUTPUT USER LABEL PROCESSING                           *
***********************************************************************
*
TCT08200 EQU   *
         TM    JFCBLTYP,JFCBAL+JFCBUL   WERE ANSI USER LBLS SPECIFIED-
         BO    TCT08400                 ..BR IF YES- DO NOT COUNT
         LH    R1,ULCNT                 LOAD PREVIOUS COUNT AND
         LA    R1,K1(,R1)               ..INCREMENT BY 1
         STH   R1,ULCNT                 SAVE UPDATED COUNT
         CLI   ULCNT+K1,MAXNOLAB        HAS THE MAXIMUM NUMBER OF
*                                       ..USER LABELS BEEN PROCESSED-
         BH    TCT12200                 ..BR IF YES
TCT08400 EQU   *
         MVC   ULDMBUFR(K3),AUTL        PLACE 'UTL' IN BUFFER    Y02082
         MVC   ULDMBUFR+K3(K1),ULCNT+K1 ESTAB LABEL NO. IN BUFFERY02082
         L     R7,ULDMBUFR              SAVE UTLN IN REG         Y02082
         MODESET KEYADDR=DXUKEY,WORKREG=15 ASSUME USER KEY       Y02082
         ST    R7,ULBUFR                PUT 'UTLN' IN USER BUFFERY02082
*    RETURN FROM SYNCH RTN WILL BE IN DATA MANAGEMENT KEY        Y02082
         BAL   R7,TCT10200              BR TO SYNCH TO USER
         LTR   RF,RF                    SHOULD THE LABEL BE WRITTEN-
         BZ    TCT12200                 ..BRANCH IF NOT
*                                                                Y02082
*    COPY LABEL FROM USER'S BUFFER TO TCLOSE'S BUFFER            Y02082
*                                                                Y02082
         MODESET EXTKEY=ZERO            ASSUME KEY ZERO          Y02082
         L     RF,WTGPREFX              POINT TO PREFIX          Y02082
         STM   R0,RET,IECREGSV-IECPREFX(RF) SAVE REGS THRU LOCK  Y02082
*    GET LOCAL LOCK                                              Y02082
         SETLOCK OBTAIN,TYPE=LOCAL,MODE=UNCOND,                  Y02082*
               RELATED=('PREVENT FREE USER CORE-RELEASE BELOW')  Y02082
         L     RF,WTGPREFX              POINT TO PREFIX          Y02082
         LM    R0,RET,IECREGSV-IECPREFX(RF) RESTORE REGS         Y02082
         MODESET KEYADDR=DXUKEY,WORKREG=15 ASSUME USER KEY       Y02082
*
*    VERIFY USER'S USER LABEL WORK AREA STILL HELD BY USER       Y02082
*
         OC    ULUWK(USERLU),ULUWK      PGM CHK IF NOT           Y02082
*                                                                Y02082
         MODESET EXTKEY=ZERO            ASSUME KEY ZERO          Y02082
         MVC   ULDMBUFR,ULBUFR          COPY LABEL FROM USER     Y02082
         L     RF,WTGPREFX              POINT TO PREFIX          Y02082
         STM   R0,RET,IECREGSV-IECPREFX(RF) SAVE REGS THRU LOCK  Y02082
*    RELEASE LOCAL LOCK                                          Y02082
         SETLOCK RELEASE,TYPE=LOCAL,RELATED=('SEE ABOVE')        Y02082
         L     RF,WTGPREFX              POINT TO PREFIX          Y02082
         LM    R0,RET,IECREGSV-IECPREFX(RF) RESTORE REGS         Y02082
         MODESET EXTKEY=DATAMGT         DATA MANAGEMENT KEY      Y02082
*
*****          INITIALIZE CHANNEL PROGRAM TO WRITE USER LABEL
*
         MVC   ULDMBUFR(K3),AUTL        PUT 'UTL' IN LABEL       Y02082
         TM    JFCBLTYP,JFCBAL+JFCBUL   WERE ANSI USER LBLS SPECIFIED-
         BO    TCT08600                 ..BRANCH IF YES
         MVC   ULDMBUFR+K3(K1),ULCNT+K1 RESTORE LABEL NO.        Y02082
TCT08600 EQU   *
         MVI   DXCCW1,CCWWRTAP          INITLZ CCW FOR WRITE COMMAND
         MVI   DXCCW1+K4,CCWSILI        SET 'SUPPRESS INCORRECT  M0112
*                                       ..LENGTH' INDICATOR
         TM    JFCBLTYP,JFCBAL+JFCBUL   WERE ANSI USER LBLS SPECIFIED-
         BNO   TCT08800                 ..BR IF NOT- BYPASS TRANSLATE
*
         XLATE ULDMBUFR,K80,TO=A        TRANSLATE LABEL DATA     YM3000
*                                                                YM3000
TCT08800 EQU   *
         BAL   RC,TCT12600              BRANCH TO WRITE LABEL
         BNO   TCT09000                 BR IF I/O ERROR
         CLI   ULRETCOD,WRITNEXT        SHOULD ADD'L LBLS BE PROCESSED-
         BE    TCT08200                 ..BR IF YES TO CONTINUE
         B     TCT12200                 ..ELSE PROCESSING IS COMPLETE
*
*****          ERROR WHEN ATTEMPTING TO WRITE LABEL.  SYNCH
*****          TO USER WITH ERROR INDICATIONS, THEN TERMINATE
*****          UL PROCESSING ( THE RETURN CODE IS NOT EXAMINED )
*
TCT09000 EQU   *
         LA    R1,DXIOB                 INSERT POINTER TO STATUS INFO
         MODESET KEYADDR=DXUKEY,WORKREG=15 ASSUME USER KEY       Y02082
         ST    R1,ULPARM+K8             ..IN PARAMETER LIST
         OI    ULPARM+K8,ERROR          FLAG THE ERROR
         BAL   R7,TCT10400              BR TO SYNCH TO USER (BUFFER
*                                       CONTAINS LABEL UNABLE TO WRITE)
* RETURNS IN DATAMGT KEY                                         Y02082
         B     TCT12200                 UL PROCESSING COMPLETED
*
***********************************************************************
*              INPUT USER LABEL PROCESSING                            *
***********************************************************************
*
TCT09200 EQU   *
         MVI   DXCCW7,ALLBITS           IND DEFERRED USER LABEL  M0011
*                                       ..PROCESSING TOOK PLACE  M0011
         OI    DXATALL,DXATFC           FORCE CLOSE ON ERROR     YM7099
TCT09400 EQU   *
         BAL   RC,TCT12600              BR AND LINK TO READ A LABEL
         BNO   TCT11400                 BRANCH IF ERROR         SA53865
         TM    IOBSTAT0,CSWUNITX        TEST FOR UNIT EXCEPTION SA53865
         BO    TCT11600                 ..BR IF YES- TAPE MARK READ
TCT09600 EQU   *
         TM    JFCBLTYP,JFCBAL+JFCBUL   WERE ANSI USER LBLS SPECIFIED-
         BNO   TCT09800                 ..BR IF NOT- BYPASS TRANSLATION
         XLATE ULDMBUFR,K80             TRANSLATE ANSI LABLE     Y02082
TCT09800 EQU   *
         CLC   ULDMBUFR(K3),AUHL        WAS USER HEADER LABEL?   Y02082
         BE    TCT10000                 ..BR IF YES
         CLC   ULDMBUFR(K3),AUTL        WAS USER TRAILER LABEL?  Y02082
         BE    TCT10000                 ..BR IF YES
*
*              NOTE - INPUT TRAILER LABEL PROCESSING LEAVES THE TAPE
*              POSITIONED BETWEEN THE FIRST AND SECOND DATA SET
*              TRAILER LABELS. THE FOLLOWING ALLOWS PROCESSING OF USER
*              LABELS EVEN THOUGH THE TAPE MAY CONTAIN MORE DATA SET
*              LABELS THAN CAN CURRENTLY BE CREATED UNDER O/S.
*
         CLC   ULDMBUFR(K3),EOVM        WAS EOV LABEL READ?      Y02082
         BE    TCT09400                 ..BR IF YES- READ ANOTHER LABEL
         CLC   ULDMBUFR(K3),AEOF1       WAS EOF LABEL READ?      Y02082
         BE    TCT09400                 ..BR IF YES- READ ANOTHER LABEL
         CLC   ULDMBUFR(K3),HDRM        WAS HEADER LABEL READ?   Y02082
         BE    TCT09400                 ..BR IF YES- READ ANOTHER LABEL
         MVI   DXCCW1,CCWBSR            INITLZ CCW FOR 'BSR' COMMAND
         B     TCT12000                 BR- AN 'UNKNOWN' LABEL WAS READ
TCT10000 EQU   *
         MVC   ULCNT+K1(K1),ULDMBUFR+K3 SAVE LABEL NUMBER        Y02082
*                                                                YM7548
*    COPY THE LABEL FROM TCLOSE'S BUFFER TO THE USER'S BUFFER    YM7548
*                                                                YM7548
         L     RF,WTGPREFX              POINT TO PREFIX          YM7548
         STM   R0,RET,IECREGSV-IECPREFX(RF)  SAVE REGS THRU LOCK YM7548
         MODESET EXTKEY=ZERO            ASSUME KEY ZERO          YM7548
*    GET LOCAL LOCK                                              YM7548
         SETLOCK OBTAIN,TYPE=LOCAL,MODE=UNCOND,                  YM7548*
               RELATED=('PREVENT FREE USER CORE-RELEASED BELOW') YM7548
*                                                                YM7548
         LM    R0,RET,IECREGSV-IECPREFX(RF)  RESTORE REGISTERS   YM7548
*    VERIFY CORE HELD IN USER'S KEY AND MEMORY                   YM7548
         MODESET KEYADDR=DXUKEY,WORKREG=15  ASSUME USER KEY      YM7548
         OC    ULUWK(USERLU),ULUWK      PROG CHK IF NOT USER'S   YM7548
*                                                                YM7548
         MODESET EXTKEY=ZERO            ASSUME KEY ZERO          YM7548
*                                                                YM7548
         MVC   ULBUFR,ULDMBUFR          COPY LABEL FOR USER      YM7548
*    RELEASE LOCAL LOCK                                          YM7548
         L     RF,WTGPREFX              POINT TO PREFIX          YM7548
         STM   R0,RET,IECREGSV-IECPREFX(RF)  SAVE REGS THRU LOCK YM7548
*                                                                YM7548
         SETLOCK RELEASE,TYPE=LOCAL,RELATED=('SEE ABOVE')        YM7548
*                                                                YM7548
         LM    R0,RET,IECREGSV-IECPREFX(RF)  RESTORE REGISTERS   YM7548
*                                                                YM7548
         LA    R7,TCT11200              LOAD ADDR FOR RETURN FROM SYNCH
         MODESET KEYADDR=DXUKEY,WORKREG=15 ASSUME USER KEY       Y02082
TCT10200 EQU   *
         XC    ULPARM+K8(K4),ULPARM+K8  ZERO ERROR IND
TCT10400 EQU   *
         LA    R1,ULBUFR                POINT TO LABEL BUFFER
TCT10600 EQU   *
         ST    R1,ULPARM                PTR TO LABEL BUFFER      Y02082
         L     R1,DXUDCBAD              GET USER'S DCB ADDR      Y02082
         ST    R1,ULPARM+K4             PUT IN PARM LIST         Y02082
         XC    ULTOTPTR(K4),ULTOTPTR    ZERO USER TOTALING ENTRY
         MVI   ULDCBPTR,EOFFLG          SET FLAG TO INDICATE EOF M0112
         TM    DEBOPATB-DEB(RD),DEBOPOUT-DEBOPRBK   IS THE DCB OPENED
*                                       ..FOR OUTPUT OR 'RDBACK'-
         BZ    TCT10800                 ..BR IF YES- SKIP USER TOTALING
         TM    DCBOPTCD,DCBOPTT         WAS USER TOTALING SPECIFIED-
         BNO   TCT10800                 ..BR IF NOT- SKIP USER TOTALING
         L     RC,DEBSTRHH-DEB(RD)      LOAD USER TOTALING WK AREA ADDR
         USING TOTSAVWA,RC              ESTABLISH BASE ADDR FOR WK AREA
         MVC   ULTOTPTR(K4),TOTEOVPT    MOVE TOTALING PT TO PARM LIST
         DROP  RC                       DROP USER TOTALING BASE REG
TCT10800 EQU   *
         MODESET EXTKEY=DATAMGT         DCB COPY KEY             Y02082
         NI    DCBOFLGS,X'FF'-DCBOLOCK  IND USER EXIT TAKEN      Y02082
*                                                                Y02082
* COPY THE DCB FROM THE WORK AREA TO USER'S STORAGE              Y02082
*                                                                Y02082
         IECRES INIT,DCBCOPY=FRWKAR,STM=(3,14,WTGPREFX)          Y02082
*                                                                Y02082
         LA    R1,ULPARM                PT TO USER LABEL PARAMETER LIST
         IECRES UEXIT,EXITAD=ULREG11,STM=(2,13,WTGPREFX)         Y02082
         STC   RF,ULRETCOD              SAVE USER'S RETURN CODE  YM5389
         L     RDCB,DXUDCBAD            GET PTR TO USER'S DCB    Y02082
         MODESET KEYADDR=DXUKEY,WORKREG=1 ASSUME USER'S KEY      Y02082
         OI    DCBOFLGS,DCBOLOCK        IND RETURN FR USER EXIT  Y02082
         IC    R1,DCBOFLGS              GET USER'S DCBOFLGS      Y02082
         MODESET EXTKEY=DATAMGT         RESUME DATA MGT KEY      Y02082
         L     RDCB,DXPDCBAD            POINT TO COPIED DCB      Y02082
         LA    R0,DCBOBUSY              SET MASK FOR BUSY BIT    Y02082
         NR    R1,R0                    SELECT THE BUSY BIT      Y02082
         IC    R0,DCBOFLGS              COMBINE WITH DCBOFLGS    Y02082
         OR    R1,R0                    ..FROM COPIED DCB.       Y02082
         STC   R1,DCBOFLGS              UPDATE DCBOFLGS IN COPY  Y02082
         OI    DCBOFLGS,DCBOLOCK        IND RETURN FROM USER EXIT
         BR    R7                       RETURN TO CALLER OF SUBROUTINE
TCT11200 EQU   *
         CH    RF,RDLBLCOD              SHOULD ANOTHER LABEL BE READ-
         BNE   TCT12200                 ..BR IF NOT- ALL DONE
         TM    JFCBLTYP,JFCBAL+JFCBUL   WERE ANSI USER LBLS SPECIFIED-
         BO    TCT09400                 ..BR IF YES- READ ANOTHER LABEL
         CLI   ULCNT+K1,MAXNOLAB        HAS THE MAXIMUM NUMBER OF
*                                       ..LABELS BEEN PROCESSED-
         BNL   TCT12200                 ..BR IF YES- ALL DONE
         B     TCT09400                 ..ELSE BR TO READ ANOTHER LABEL
*
*****          ERROR ENCOUNTERED DURING READ
*
TCT11400 EQU   *
         LA    R1,DXIOB                 INSERT POINTER TO STATUS INFO
         MODESET KEYADDR=DXUKEY,WORKREG=15 ASSUME USER KEY       Y02082
         ST    R1,ULPARM+K8             ..INTO PARAMETER LIST
         OI    ULPARM+K8,ERROR          FLAG THE ERROR
         MODESET EXTKEY=DATAMGT         DATA MANAGEMENT KEY      Y02082
         LH    R1,ULCNT                 LOAD THE CURRENT LABEL NO.,
         LA    R1,K1(,R1)               ..UPDATE IT BY ONE, THEN
         STH   R1,ULCNT                 ..SAVE THE UPDATED NUMBER
         STC   R1,ULDMBUFR+K3           SAVE LABEL NUMBER        Y02082
*                                       ..WHICH COULD NOT BE READ
         LA    R7,TCT11200              LOAD ADDR FOR RETURN FROM SYNCH
         B     TCT10400                 BR TO SYNCH TO USER
*
*              A TAPE MARK HAS BEEN READ. IF USER HAS PREVIOUSLY
*              PROCESSED ANY LABELS,  REPOSITION TAPE, FREEMAIN,
*              AND RETURN TO CALLER. IF USER HAS NOT PREVIOUSLY
*              PROCESSED ANY LABELS, SYNCH TO USER WITH LABEL
*              BUFFER POINTER SET TO ZERO, THEN REPOSITION
*              TAPE, FREEMAIN, AND RETURN TO CALLER.
*
TCT11600 EQU   *
         CLI   ULCNT+K1,CHAR0           HAVE ANY LABELS BEEN PROCESSED-
         BH    TCT11800                 ..BRANCH IF YES
         TM    JFCBLTYP,JFCBAL+JFCBUL   WERE ANSI USER LBLS SPECIFIED-
         BO    TCT11800                 ..BRANCH IF YES
         MODESET KEYADDR=DXUKEY,WORKREG=15 ASSUME USER KEY       Y02082
         XC    ULPARM+K8(K4),ULPARM+K8  ZERO ERROR PARAMETER
         SR    R1,R1                    ZERO BUFFER POINTER
         BAL   R7,TCT10600              BR AND LINK TO SYNCH TO USER
* RETURNS IN DATAMGT KEY                                         Y02082
TCT11800 EQU   *
         MVI   DXCCW1,CCWBSF            INITLZ CCW FOR BSF COMMAND
TCT12000 EQU   *
         LA    RD,TABD141               LOAD INTERNAL ERROR CODE
         MVC   DXCCW1+K4(K4),CHNCOUNT   CHAIN TO 'NOP', SET CT TO 1
         MVI   DXCCW2,CCWNOP            INITLZ SECOND CCW FOR NOP CMD
         MVC   DXCCW2+K4(K4),CHNCOUNT   INITLZ LNG FOR NOP TO 1
         MVI   DXCCW2+K4,CCWSILI        TURN ON 'SILI' INDICATOR ONLY
         BAL   RC,TCT12600              BRANCH TO PERFORM BACKSPACE
         BNO   TCT12800                 BR IF I/O ERROR
*                                                                Y02082
*    FREE USER LABEL WORK AREAS                                  Y02082
*                                                                Y02082
TCT12200 EQU   *
         IECRES FREE,A=(R9),PREFIX=NO,SP=K229,KEY=USER,          Y02082*
               LV=USERLU,STM=(2,14,WTGPREFX)                     Y02082
         MODESET EXTKEY=DATAMGT         DATA MANAGEMENT KEY      Y02082
         LR    R1,RUCB                  POINT TO USER LABEL WORK AREA
         LM    RPAR,RC,ULREGSAV         RESTORE TCLOSE REGISTER 5-12
*
* RELEASE USER LABEL WORK AREA MAIN STORAGE                      Y02080
*
         IECRES FREE,A=(R1),PREFIX=YES,STM=(2,14,WTGPREFX)       Y02082
         USING UCB,RUCB                 RE-ESTABLISH BASE REG FOR UCB
*
         EJECT
***********************************************************************
*        INVOKE IECRES MACRO TO TRANSFER CONTROL                      *
***********************************************************************
*
TCT12400 EQU   *
         MODESET EXTKEY=DATAMGT         TCLOSE WORK AREA KEY     Y02082
         L     RDEB,DCBDEBAD            RESTORE THE DEB ADDRESS
         IECRES LOAD,MODID=MOD2M2S,BRCODE=K0,BRANCH=QUEUED       Y02080
         SPACE 2
***********************************************************************
*        CLOSED SUBROUTINE TO PERFORM I/O OPERATIONS                  *
***********************************************************************
*
*****          ENTRY TO THE SUBROUTINE IS VIA A 'BAL   RC,TCT12600'
*
TCT12600 EQU   *
         EXCP  DXIOB                    INITIATE I/O OPERATION
         IECRES WAIT                    INVOKE IECRES MACRO TO 'WAIT'
         TM    IOBSTAT0,CSWUNITX        TEST FOR TAPE INDICATE  SA53865
         BCR   1,RC                     RETURN IF ON            SA53865
         TM    DXECB,ECBNOERR           TEST FOR SUCCESSFUL COMPLETION-
         BR    RC                       RETURN TO CALLER WITH
*                                       ..CONDITION CODE SET
         SPACE 2
***********************************************************************
*              ERROR PROCESSING                                       *
***********************************************************************
*
TCT12800 EQU   *
         MODESET EXTKEY=DATAMGT         TCLOSE WORK AREA KEY     Y02082
         DMABCOND (RD),ABEND2M          EXIT TO PROB DETERMINATION RTN
         EJECT
***********************************************************************
*              CONSTANTS                                              *
***********************************************************************
*
RDLBLCOD DC    H'4'                     RET CODE IND 'READ ANOTHER LBL'
CHNCOUNT DC    X'60000001'              CMD CHAIN, SILI BIT, LNG EQ 1
EOVM     DC    C'EOV'                   END-OF-VOLUME LABEL IDENTIFIER
AEOF1    DC    C'EOF'                   END-OF-FILE LABEL IDENTIFIER
HDRM     DC    C'HDR'                   HEADER LABEL IDENTIFIER
AUHL     DC    C'UHL'                   USER HEADER LABEL IDENTIFIER
AUTL     DC    C'UTL'                   USER TRAILER LABEL IDENTIFIER
ATRTCH   DC    C' CET'                  TRANSLATE TABLE FOR JFCB TAPE
*                                       ..RECORDING TECHNIQUE
EXIT4    DC    X'04'                    OUTPUT USER LABEL CODE BYTE
         DC    X'84'                    SAME AS ABOVE WITH 'LAST ENTRY'
*                                       ..INDICATOR
SLPSUP   DC    X'0C'                    DEFERRED USER LABEL PROCESSING
*                                       ..CODE
         DC    X'8C'                    SAME AS ABOVE WITH 'LAST ENTRY'
*                                       ..INDICATOR
         SPACE 2
*****************************************************************Y02083
*              EQUATES                                           Y02083
*****************************************************************Y02083
*                                                                Y02083
FL2CKDS  EQU   C'C'                     TAPE CHKPT D/S INDICATOR Y02083
         EJECT
***********************************************************************
*              XCTL TABLE                                             *
***********************************************************************
*
XCTLTB2M XCTLTABL ID=(ABEND2M,0P,MOD2M2S,2S),SVC=023,            Y02080X
               BRT=YES,LENGTH=                                   Y02080
         SPACE 2
         IECDSECS CVT,                                                 C
               TCB,                                                    C
               RB,                                                     C
               TIOT,                                                   C
               DCB,                                                    C
               DEB,                                                    C
               UCB,                                                    C
               DSAB,                                             Y02083C
               MAIN,                                                   C
               USERTOT,                                                C
               UNITTAB,                                                C
               USERLAB,                                                C
               WTG,                                                    C
               PREFX,                                            Y02080*
               PSA,                                              Y02082*
               RRPL,                                             Y02144*
               EXPAND=YES
         IECEQU ,                       INVOKE OPEN/CLOSE/EOV EQUATES
         SPACE 2
         END
