         TITLE 'IFG0200W - CLOSE ACESS METHOD INTERFACE STRING'
IFG0200W CSECT
***********************************************************************
*                                                                     *
*          VS2 RELEASE 03 DELETIONS/CHANGES                           *
*0000442000,466000,552000,800180                               @ZA10539
*                                                                     *
*          VS1 RELEASE 02 DELETION/CHANGES                            *
*0000                                                            XM1037
*                                                                     *
*          RELEASE 21 DELETIONS/CHANGES                               *
*0000482000-486000,502000,720000-722000,742000,746000-750000     M1508
*0000226000-242000,350000-366000,394000,628000-644000,886000-    M0050
*0000980000                                                      M0050
*0000770500-794100                                               A52328
*                                                                     *
* MODULE NAME = IFG0200W                                              *
*                                                                     *
* DESCRIPTIVE NAME = CLOSE ACCESS METHOD INTERFACE STRING             *
*                                                                     *
* COPYRIGHT = NONE                                                    *
*                                                                     *
* STATUS CHANGE LEVEL 000                                             *
*                                                                     *
* FUNCTION -                                                          *
*    THIS MODULE CONTAINS THE FUNCTION(S) OR PART(S) OF FUNCTION(S)-- *
*    CLOSE TAPE ACCESS METHOD EXECUTOR INTERFACE FUNCTION.            *
*    CLOSE UNIT RECORD/TELEPROCESSING FUNCTION.                       *
*    CLOSE DA ACCESS METHOD EXECUTOR INTERFACE FUNCTION.              *
*    REFER TO THE FOLLOWING 'FUNCTION PROLOG(S)' FOR DETAILS.         *
*                                                                     *
* ENTRY POINTS -                                                      *
*         IFG0200W - ENTRY POINT VIA AN XCTL FROM THE FOLLOWING--     *
*             INITIAL ENTRY--                                         *
*                IFG0200V - PROCESS UNIT RECORD/TELEPROCESSING        *
*                           FUNCTION OR TAPE ACCESS METHOD EXECUTOR   *
*                           INTERFACE FUNCTION.                       *
*             SECOND ENTRY--                                          *
*                IFG0200P - PROCESS DA ACCESS METHOD EXECUTOR         *
*                IFG0200V   INTERFACE FUNCTION.                       *
*                                                                     *
* INPUT -                                                             *
*    ENTERED IN DATA MANAGEMENT KEY                              Y02082
*    REFER TO THE FOLLOWING 'FUNCTION PROLOG(S)'.                     *
*                                                                     *
* OUTPUT -                                                            *
*    EXIT IN DATA MANAGEMENT KEY                                 Y02082
*    REFER TO THE FOLLOWING 'FUNCTION PROLOG(S)'.                     *
*                                                                     *
* EXTERNAL REFERENCES -                                               *
*         IFG019RA - OPEN/CLOSE/EOV RESIDENT ROUTINE FOR XCTL AND     *
*                    WAIT.                                            *
*    REFER TO THE FOLLOWING 'FUNCTION PROLOG(S)'.                     *
*                                                                     *
* EXITS, NORMAL -                                                     *
*    EXIT VIA THE RESIDENT ROUTINE XCTL TO THE FOLLOWING--            *
*         IFG0200Y - CONTINUE PROCESSING ACCESS METHOD EXECUTOR       *
*                    INTERFACE FUNCTION.                              *
*    OR AN XCTL MACRO INSTRUCTION TO THE ACCESS METHOD EXECUTOR.      *
*    REFER TO THE FOLLOWING 'FUNCTION PROLOG(S)'.                     *
*                                                                     *
* EXITS, ERROR -                                                      *
*    EXIT IS TO THE PROBLEM DETERMINATION MODULE IFG0200P, IF AN      *
*    ABEND SITUATION OCCURS IN THIS MODULE.  REFER TO THE FOLLOWING   *
*    'FUNCTION PROLOG(S)'.                                            *
*                                                                     *
* TABLES/WORK AREAS -                                                 *
*    REFER TO THE FOLLOWING 'FUNCTION PROLOG(S)'.                     *
*                                                                     *
* ATTRIBUTES -                                                        *
*    REENTRANT, REFRESHABLE, READ-ONLY, ENABLED, PRIVILEGED           *
*                                                                     *
* CHARACTER CODE DEPENDENCY -                                         *
*    CLASS ONE CHARACTER CODE DEPENDENCY - THE EBCDIC CHARACTER CODE  *
*    WAS USED FOR ASSEMBLY.  THE MODULE MUST BE REASSEMBLED IF A      *
*    DIFFERENT CHARACTER SET IS USED FOR EXECUTION.                   *
*                                                                     *
* NOTES -                                                             *
*    REFER TO THE FOLLOWING 'FUNCTION PROLOG(S)'.                     *
*                                                                     *
***********************************************************************
         EJECT
         USING TIOENTRY,RTIOT
         USING FORCORE,RCORE
         USING TCB,RET
         USING IHADCB,RDCB
         USING UCBOB,RUCB
         USING CVT,RD
         USING DEB,RDEB
         USING WTG,RWTG
         USING WTGENTRY,RWTGC
*                                                                     *
         SPACE 1
         BALR  RBASE,0                  ESTABLISH ADDRESSABILITY
         USING *,RBASE
*
         B     CIN04300(RET)            USE BRANCH TABLE
CIN04300 EQU   *
         B     CIN04400                 ENTRY IS FOR UR/TP OR TAPE
         B     CIN05100                 ENTRY IS FROM DA
         B     CIN05410                 ENTRY IS FOR SPOOLED     YM1342
*                                       OR VTAM                  YM1342
         B     CIN05520                 FR AM EXECS VIA PROB DET YM1195
         EJECT
*                                                                     *
*                          FUNCTION PROLOG                            *
*                                                                     *
***********************************************************************
*                                                                     *
* FUNCTION NAME -                                                     *
*    CLOSE UNIT RECORD/TELEPROCESSIN AND TAPE FUNCTION                *
*                                                                     *
* (STATUS) -                                                          *
*    NOT APPLICABLE                                                   *
*                                                                     *
* FUNCTION -                                                          *
*    DETERMINE ACCESS METHOD EXECUTOR USED.                           *
*    INTERFACE WITH THE EXECUTORS IF USER I/O OPERATION IS NOT ON EXCP*
*    LEVEL.                                                           *
*                                                                     *
* ENTRY POINTS -                                                      *
*    ENTERED FROM THE FOLLOWING--                                     *
*    CLOSE INITIALIZATION FUNCTION.                                   *
*                                                                     *
* INPUT -                                                             *
*    A POINTER TO EACH OF THE FOLLOWING--                             *
*       CURRENT PARAMETER LIST ENTRY- RPARC                           *
*       DD ENTRY IN THE TIOT- RTIOT                                   *
*       WTG TABLE- RWTG                                               *
*       CURRENT WTG TABLE ENTRY- RWTGC                                *
*       DCB- RDCB                                                     *
*       WORK AREA- RCORE                                              *
*       RESIDENT ROUTINE- RES                                         *
*       UCB- RUCB                                                     *
*       DEB- RDEB                                                     *
*                                                                     *
* OUTPUT -                                                            *
*    ALL PENDING CHANNEL PROGRAMS ARE HANDLED AND MAIN STORAGE USED   *
*    BY THE IOBS, ICBS AND CHANNEL PROGRAMS ARE RELEASED.             *
*                                                                     *
* EXTERNAL REFERENCES -                                               *
*    REFER TO THE PRECEEDING MODULE PROLOG.                           *
*                                                                     *
* EXITS, NORMAL -                                                     *
*    REFER TO THE PRECEEDING MODULE PROLOG.                           *
*    CLOSE FINAL STRING.                                              *
*                                                                     *
* EXITS, ERROR -                                                      *
*    REFER TO THE PRECEEDING MODULE PROLOG.                           *
*                                                                     *
* TABLES/WORK AREAS -                                                 *
*    THE OPEN, CLOSE, OR EOV WORK AREA AND THE WHERE-TO-GO (WTG)      *
*    TABLE ARE DESCRIBED BY THE DSECTS AT THE END OF THE LISTING.     *
*                                                                     *
* ATTRIBUTES -                                                        *
*    REFER TO THE PRECEEDING MODULE PROLOG.                           *
*                                                                     *
* CHARACTER CODE DEPENDENCY -                                         *
*    REFER TO THE PRECEEDING MODULE PROLOG.                           *
*                                                                     *
* NOTES -                                                             *
*    NOT APPLICABLE                                                   *
*                                                                     *
***********************************************************************
         EJECT
CIN04400 EQU   *
*
***** FOR TSO - XCTL TO IGG0201A
***** FOR TCAM QNAME - XCTL TO IGG02046
***** FOR EXCP & DUMMY DATA SETS - NO EXECUTORS
*
         L     RD,CVTPTR                VECTOR TABLE ADDRESS     M1508
         L     RET,CVTTCBP              POINTER TO TCB           M1508
         L     RET,K4(,RET)             CURRENT TCB ADDRESS      M1508
         TM    TIOELINK,TIOTTERM        IS THIS TSO
         LA    RF,PSSECT                DEFAULT TO IGG0201A
         BO    CIN05450                 BRANCH IF YES          @ZA10539
         TM    TIOELINK,TIOEQNAM        IS THIS TCAM QNAME
         LA    RF,TCPSSECT              POINT TO IGG02046
         BO    CIN05500                 YES, GO LOAD TCAM EXECUTOR
         LA    RF,EXSECTZ               SET NAME TO ZERO
         TM    DCBMACRF,DCBMEXCP        CK FOR EXCP
         BO    CIN05500                 YES,BRANCH
         LTR   RUCB,RUCB                TEST FOR NULL DATA SET   YM1342
         BZ    CIN04500                 BRANCH IF YES
*
***** FOR DATA SETS ON TAPE AND UNIT RECORD DEVICES, XCTL TO
***** IGG0201A EXCEPT WHEN ABEND IS IN CONTROL, IN WHICH CASE    YM7389
***** EXECUTORS ARE REQUIRED ONLY FOR A 2540 DEVICE WITH 'CNTRL' YM7389
*
         TM    UCBTBYT3,UCB3TAPE        IS THIS TAPE
         BZ    CIN04500                 BRANCH IF NO TO UR PROC
         TM    TCBFLG,TCBFA             IS ABEND BIT ON          YM7389
         BO    CIN05500                 YES, BR TO SYNCH DCBS    YM7389
         LA    RF,PSSECT                POINT TO IGG0201A
         B     CIN05450                 BRANCH TO MOVE IN NAME @ZA10539
CIN04500 EQU   *
         TM    DCBDSORG,DCBORGCX        IS IT CX?
         BNZ   CIN04600                 BRANCH IF YES
         TM    DCBDSORG+K1,DCBORGGR+DCBORGTX+DCBORGTQ  GRAPHIC, TX, TQ
         BNZ   CIN04600                 BRANCH IF ANY
         TM    DCBDSORG+K1,DCBDSGTR     IS IT A TCAM TR          S22024
         BNZ   CIN04600                 YES, BRANCH              S22024
         LTR   RUCB,RUCB                TEST FOR NULL DATA SET
         BZ    CIN05500                 BRANCH IF NULL DATA SET
         TM    TCBFLG,TCBFA             IS ABEND BIT ON          YM7389
         BZ    CIN04700                 BRANCH IF NO             YM7389
         TM    DCBDEVT,DCBDVCR0         CHK FOR 2540 RDR         YM7389
         BZ    CIN05500                 NO, BR TO SYNCH DCBS     YM7389
         TM    DCBMACRF,DCBMCTRL        CHK FOR CNTRL            YM7389
         BZ    CIN05500                 NO, BR TO SYNCH DCBS     YM7389
         B     CIN04700                 BRANCH TO SET UP WTG
CIN04600 EQU   *
*
***** FOR COMMON LINE GROUP - XCTL TO IGG0203M
***** FOR GRAPHICS - XCTL TO IGG0203Y
***** FOR TCAM TX - XCTL TO IGG02035
***** FOR TCAM TQ - XCTL TO IGG02030
***** FOR TCAM TR - XCTL TO IGG0203B                             S22024
         TM    DCBDSORG,DCBORGCX        IS IT COMM LINE GROUP
         LA    RF,CXSECT1               POINT TO IGG0203M
         BO    CIN05500                 BRANCH IF COMM LINE GROUP
         TM    DCBDSORG+K1,DCBORGGR     IS IT GRAPHICS ACCESS METHOD
         LA    RF,GRSECT                POINT TO IGG0203Y
         BO    CIN05500                 BR IF GRAPHICS
         TM    DCBDSORG+K1,DCBORGTX     IS IT TCAM TX
         LA    RF,TCTXSECT              POINT TO IGG02035
         BO    CIN05500                 BRANCH IF TCAM TX
         TM    DCBDSORG+K1,DCBORGTQ     IS IT TCAM TQ
         LA    RF,TCTQSECT              POINT TO IGG02030
         BO    CIN05500                 BRANCH IF TCAM TQ
         TM    DCBDSORG+K1,DCBDSGTR     IS IT TCAM TR            S22024
         LA    RF,TCTRSECT              POINT TO IGG0203B        S22024
         BO    CIN05500                 BRANCH IF TCAM TR        S22024
*
***** FOR 1275/1419 DEVICES - XCTL TO IGG0201D
*
CIN04700 EQU   *
         LA    RF,PSSECT                DEFAULT TO IGG0201A
         CLI   DCBDEVT,DCBDEVOR         IS THIS A 1275
         BE    CIN04800                 YES,BRANCH
         CLI   DCBDEVT,MICRMSK          IS DCB FOR 1419
         BNE   CIN05500                 NO/GO MOVE ENTRY TO    @ZA10539
*                                       WTG TABLE              @ZA10539
CIN04800 EQU   *
         LA    RF,PSSECT1               POINT TO IGG0201D
         B     CIN05500                 MOVE IN WTG ENTRY
         EJECT
*                                                                     *
*                          FUNCTION PROLOG                            *
*                                                                     *
***********************************************************************
*                                                                     *
* FUNCTION NAME -                                                     *
*    CLOSE DA ACCESS METHOD EXECUTOR INTERFACE FUNCTION.              *
*                                                                     *
* (STATUS) -                                                          *
*    NOT APPLICABLE                                                   *
*                                                                     *
* FUNCTION -                                                          *
*    DETERMINE ACCESS METHOD EXECUTOR USED.                           *
*    INTERFACE WITH THE EXECUTORS IF USER I/O OPERATION IS NOT ON EXCP*
*    LEVEL.                                                           *
*                                                                     *
* ENTRY POINTS -                                                      *
*    ENTERED FROM THE FOLLOWING--                                     *
*    CLOSE INITIALIZATION FUNCTION.                                   *
*    CLOSE DA READ DSCB FUNCTION.                                     *
*                                                                     *
* INPUT -                                                             *
*    A POINTER TO EACH OF THE FOLLOWING--                             *
*       CURRENT PARAMETER LIST ENTRY- RPARC                           *
*       DD ENTRY IN THE TIOT- RTIOT                                   *
*       WTG TABLE- RWTG                                               *
*       CURRENT WTG TABLE ENTRY- RWTGC                                *
*       DCB- RDCB                                                     *
*       WORK AREA- RCORE                                              *
*       RESIDENT ROUTINE- RES                                         *
*       UCB- RUCB                                                     *
*       DEB- RDEB                                                     *
*                                                                     *
* OUTPUT -                                                            *
*    ALL PENDING CHANNEL PROGRAMS ARE HANDLED AND MAIN STORAGE USED   *
*    BY THE IOBS, ICBS AND CHANNEL PROGRAMS ARE RELEASED.             *
*                                                                     *
* EXTERNAL REFERENCES -                                               *
*    REFER TO THE PRECEEDING MODULE PROLOG.                           *
*                                                                     *
* EXITS, NORMAL -                                                     *
*    REFER TO THE PRECEEDING MODULE PROLOG.                           *
*    CLOSE DA UPDATE DSCB FUNCTION.                                   *
*    CLOSE DA INPUT USER LABELS FUNCTION.                             *
*    CLOSE DA EXIT FUNCTION.                                          *
*    CLOSE FINAL STRING.                                              *
*                                                                     *
* EXITS, ERROR -                                                      *
*    REFER TO THE PRECEEDING MODULE PROLOG.                           *
*                                                                     *
* TABLES/WORK AREAS -                                                 *
*    THE OPEN, CLOSE, OR EOV WORK AREA AND THE WHERE-TO-GO (WTG)      *
*    TABLE ARE DESCRIBED BY THE DSECTS AT THE END OF THE LISTING.     *
*                                                                     *
* ATTRIBUTES -                                                        *
*    REFER TO THE PRECEEDING MODULE PROLOG.                           *
*                                                                     *
* CHARACTER CODE DEPENDENCY -                                         *
*    REFER TO THE PRECEEDING MODULE PROLOG.                           *
*                                                                     *
* NOTES -                                                             *
*    NOT APPLICABLE                                                   *
*                                                                     *
***********************************************************************
         EJECT
CIN05100 EQU   *
*
***** FOR EXCP - NO EXECUTORS
*
         L     RD,CVTPTR                VECTOR TABLE ADDRESS     M1508
         L     RET,CVTTCBP              POINTER TO TCB           M1508
         L     RET,K4(,RET)             CURRENT TCB ADDRESS      M1508
         NI    DEBFLGS1,X'FF'-DEBF1CEV  TURN OFF DEBF1CEV        M0050
         LA    RF,EXSECTZ               SET NAME TO ZERO
         TM    DCBMACRF,DCBMEXCP        CK FOR EXCP
         BO    CIN05500                 YES,BRANCH
*
*
***** FOR ISAM -
*****    BISAM - XCTL TO IGG0202A
*****    QISAM SCAN MODE - XCTL TO IGG02029
*****    QISAM LOAD MODE - XCTL TO IGG0202I FOR FIXED LENGTH RECORDS
*****                      AND IGG02028 FOR VARIABLE LENGTH RECORDS
*****                      EXCEPT WHEN ABEND IS IN CONTROL, IN WHICH
*****                      CASE XCTL TO IGG0202D
         TM    DCBDSORG,DCBORGIS        IS IT INDEXED SEQUENTIAL
         BNO   CIN05300                 BR IF NO
         LA    RF,ISSECT1               '2A'  BISAM
         TM    DCBMACRF,DCBMREAD        IS IT READ
         BO    CIN05500                 BRANCH IF YES
         TM    DCBMACRF+K1,DCBMWRIT     IS IT WRITE              S22024
         BO    CIN05500                 BRANCH IF YES
         TM    DCBMACRF,DCBMGET         CK FOR ISAM SCAN MODE
         LA    RF,ISSECT                '29' QISAM SCAN MODE     M1508
         BO    CIN05500                 BR IF YES
         TM    TCBFLG,TCBFA             IS ABEND IN CONTROL
         LA    RF,ISSECT3               '2D'  INDICATE COMMON ISAM
         BO    CIN05500                 BRANCH IF YES
         TM    DCBRECFM,DCBRECFX        FIXED LENGTH RECORDS
         LA    RF,ISSECT2               '2I' QISAM LOAD - FIXED LENGTH
         BO    CIN05500                 BRANCH IF YES
         LA    RF,ISSECT4               '28' QISAM LOAD - VLR
         B     CIN05500                 BRANCH TO MOVE IN NAME
*
CIN05300 EQU   *
*
***** FOR TCAM TQ - XCTL TO IGG02030                             A52328
***** IF ABEND IN CONTROL - BYPASS THESE EXECUTORS --            A52328
***** FOR PHYSICAL SEQUENTIAL DATA SETS - XCTL TO IGG0201Z       A52328
***** FOR DIRECT ORGANIZATION DATA SETS - XCTL TO IGG0203A       A52328
*
         TM    DCBDSORG+K1,DCBORGTQ     IS IT TCAM TQ            A52328
         LA    RF,TCTQSECT              '30' POINT TO IGG02030   A52328
         BO    CIN05500                 BRANCH IF TCAM TQ        A52328
         TM    DCBDSORG,DCBORGDA        IS IT DIRECT ACCESS     SA61534
         LA    RF,DASECT                '3A'                    SA61534
         BO    CIN05500                 BRANCH IF DIRECT ACCESS SA61534
         LA    RF,EXSECTZ               SET NAME TO ZERO         YM7389
         TM    TCBFLG,TCBFA             IS ABEND BIT ON          YM7389
         BO    CIN05500                 YES, BR TO SYNCH DCBS    YM7389
*
CIN05400 EQU   *
         LA    RF,POSECT                '1Z' POINT TO EXECUTOR FOR DA
         B     CIN05450                 BR TO MOVE IN NAME     @ZA10539
*
*  ENTRY FOR SPOOLED DCB, SPOOLED ACB OR VTAM ACB                YM1342
*
CIN05410 EQU   *                        ENTER FROM BRANCH TABLE
         LA    RF,TRANSLAT              POINT TO TRANSLATE MODULE
         TM    DCBMACRF,DCBMEXCP        IS ACCESS METHOD EXCP
         BO    CIN05500                 YES, CANNOT BE ACB
         TM    DCBDSORG+K1,ACBDORGA     IS THIS AN ACB
         BZ    CIN05500                 NO, MUST BE SPOOLED DCB
         CLI   ACBAMETH-IFGACB(RDCB),ACBVTAM IS THIS VTAM ACB?   YM3011
         LA    RF,EXSECTZ               SET MOD ID TO ZERO       YM1342
         BE    CIN05500                 YES, BRANCH              YM1342
CIN05420 EQU   *                        SPOOLED ACB
         LA    RF,JESEX                 POINT TO JES EXECUTOR
CIN05450 EQU   *                                               @ZA10539
         NI    JFCOPTCD,X'FF'-JFCPCIBT  RESET CHAIN SCHED IND  @ZA10539
         TM    DCBCIND2,DCBCNCHS        CHAIN SCHEDULING USED  @ZA10539
         BNO   CIN05500                 NO CONTINUE            @ZA10539
         OI    JFCOPTCD,JFCPCIBT        TURN CHAIN SCHEDULING  @ZA10539
*                                       INDICATOR ON           @ZA10539
CIN05500 EQU   *
         LR    R1,RF                    SAVE NAME IN REG 1
         IECRES SYNCHDCB                SYNCH ALL DCBS
CIN05520 EQU   *
         L     R5,WTGPREFX              RELOAD REG 5
         USING IECPREFX,R5              PREFIX ADDRESSABILITY    Y02080
         L     RF,IECRRPRM              GET RRPLIST PTR          YM1195
         L     RPAR,IECUPRML            POINTER TO USER PARMLIST Y02080
         DROP  R5                                                Y02080
         LR    RPARC,RPAR               SET PTR TO PARM LIST     XM1037
         LA    RWTGC,WTGENTRY-WTG(,RWTG) SET PTR TO WTG ENTRY    XM1037
         USING RRPLIST,RF                                        YM1195
         TM    RRFLAGS1,RRFAMEXG        FR AM EXECS VIA PROB DET YM1195
         BO    CIN05800                 BRANCH YES, GO TO EXECS  YM1195
CIN05600 EQU   *
         L     RCORE,WTGCORE-K1         GET WA ADDR              YM1293
         LA    RCORE,0(,RCORE)          CLEAR HIGH ORDER BYTE    YM1293
         LTR   RCORE,RCORE              PROCESS THIS ENTRY?      YM1293
         LA    R1,EXSECTZ               SET MOD ID TO ZERO       YM1293
         BZ    CIN05700                 NO, BRANCH               YM1293
         L     RDCB,PLISTDCB(,RPARC)    GET DCB ADDRESS
         TM    DCBOFLGS,DCBOBUSY        BUSY BIT ON
         BZ    CIN05700                 BRANCH IF OFF
         L     RCORE,WTGCORE-1          GET WA ADDR
         L     R1,DXREG1                RELOAD REG 1
* DON'T REFRESH THE USER'S DCB SINCE THERE MAY BE ACTIVE I/O     YM3071
CIN05700 EQU   *
         MVC   WTGIDTTR,0(R1)           MOVE IN NAME
         TM    PLISTOPT(RPARC),LASTNTRY  LAST ENTRY ON LIST
         LA    RWTGC,K8(,RWTGC)         GET NEXT WTG ENTRY
         LA    RPARC,K4(,RPARC)         GET NEXT DCB ENTRY
         BC    12,CIN05600              BRANCH IF NOT
         LA    RF,AMRETMOD              LOAD RETURN MOD NAME
         MVC   WTGIDTTR,0(RF)           MOVE IN WTG TABLE
         LR    RPARC,RPAR               REINITIALIZE PARM LIST
         LA    RWTGC,WTGENTRY-WTG(,RWTG)  REINITIALIZE WTG ENTRY
CIN05800 EQU   *
         CLI   WTGIDTTR,K0              TEST FOR ZERO ENTRY
         BNE   CIN05900                 BRANCH IF ENTRY NOT=0
         LA    RWTGC,K8(,RWTGC)         GET NEXT WTG ENTRY
         LA    RPARC,K4(,RPARC)         GET NEXT DCB ENTRY
         B     CIN05800                 BRANCH TO CHECK NEXT ENTRY
CIN05900 EQU   *
         MVI   WTGMODNM+K1,C'G'         CHANGE ID TO IGG FOR EXECUTORS
         MVC   WTGMODID,WTGIDTTR        MOVE ID TO BLDL ENTRY
         MVC   WTGMODEP+K1(K3),WTGIDTTR+K2 MOVE IN TTR NEXT MOD  Y02080
         MVC   WTGATTRB(K7),MODATTRB
*                                                                YM1195
*  NOTE: IT IS POSSIBLE TO BE AT THIS POINT WITH THE WTGMODNM    YM1195
*        FIELD CONTAINING THE NAME IGG0200G, INDICATING THAT     YM1195
*        THE ONLY NON-ZERO ID LEFT IN A WTG ENTRY WAS '0G' IN    YM1195
*        THE LAST WTG ENTRY. CONTROL WILL BE GIVEN TO IGG0200G   YM1195
*        (AN ALIAS FOR IFG0200Y, NOT AN EXECUTOR) TO SET UP FOR  YM1195
*        PROPER ENTRY TO IFG0202L.                               YM1195
*                                                                YM1195
         L     RF,WTGPREFX              GET ADDR OF EXT PREF     YM1104
         L     RF,IECRRPRM-IECPREFX(RF) GET ADDR RRPLIST         YM1104
         OI    RRFLAGS1,RRFAMEXG        SHOW GOING TO A.M. EXECS Y02144
         DROP  RF                                                YM1342
*                                                                Y02082
         IECRES LOAD,PREFIX=WTGPREFX,BRANCH=DIRECT               Y02080
         EJECT
         SPACE 1
***********************************************************************
*                                                                     *
*                       CONSTANTS                                     *
*                                                                     *
***********************************************************************
MODATTRB DC    X'C379'                  MODULE ATTRIBUTES
         DC    X'000400'                MODULE SIZE
         DC    X'0400'                  TEXT LENGTH
         SPACE 1
EXSECTZ  DC    XL2'0'                   ZERO ENTRY               M0050
         XCTLTABL ID=(PDMOD,0P,,FFFF,AMRETMOD,0G,ISSECT,29,            *
               ISSECT1,2A,ISSECT2,2I,ISSECT3,2D,ISSECT4,28,            *
               DASECT,3A,POSECT,1Z,TCTQSECT,30,PSSECT,1A,              *
               PSSECT1,1D,CXSECT1,3M,GRSECT,3Y,TCPSSECT,46,            *
               TCTRSECT,3B,                                      S22024*
               TCTXSECT,35,JESEX,3K,TRANSLAT,1W),                      *
               SVC=020,BRT=YES,LENGTH=                           Y02080
         IECDSECS CVT,TCB,TIOT,ACB,DEB,UCB,MAIN,WTG,PREFX,       S22024*
               RRPL,EXPAND=YES                                   Y02144
         DCBD  DSORG=(PS,IS,DA,PO,BS,TQ,TR)  INCLUDE 3705        S22024
ACBMJEP  EQU  X'40'                     ACB CREATED BY JEPS           $
         IECEQU  AOS=YES
         END
