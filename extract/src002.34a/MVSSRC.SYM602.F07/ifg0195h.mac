         TITLE 'IFG0195H - OPEN TAPE INPUT SL HDR1 PROCESSING'
         COPY  LCGASMSW
IFG0195H CSECT
***********************************************************************
*                                                                     *
*        VS2 RELEASE 04.0 DELETIONS/CHANGES                           *
*0000                                                          @ZA00003
*0000                                                          @ZA04604
*0000                                                          @ZA04609
*0000554000                                                    @ZA15669
*                                                                     *
*         VS2 RELEASE 037 DELETIONS/CHANGES/ADDITIONS                 *
*                                                                     *
*A(550600,550700,555200)                                       @ZA19601
*                                                                     *
* MODULE NAME = IFG0195H (OS/VS2)                                     *
*                                                                     *
* DESCRIPTIVE NAME = TAPE INPUT HEADER LABER VERIFICATION             *
*                                                                     *
* COPYRIGHT = NONE                                                    *
*                                                                     *
* STATUS = RELEASE 4, LEVEL 0                                         *
*                                                                     *
* FUNCTION =                                                          *
*        1. READ FILE LABEL 1.                                        *
*                                                                     *
*        2. VERIFY THAT THE DSNAME FROM THE LABEL IS THE SAME AS THAT *
*        SPECIFIED IN THE JFCB.                                       *
*                                                                     *
*        3. IF OPENING FOR RDBACK OR DISP = MOD, THE BLOCK COUNT IS   *
*        MOVED FROM THE LABEL TO THE DCB.                             *
*                                                                     *
*        4. WHEN OPENING FOR ANY TYPE OF OUTPUT, THE EXPIRATION DATE  *
*        IS CHECKED TO SEE IF IT HAS PASSED.  IF NOT, THE OPERATOR'S  *
*        DECISION MUST BE OBTAINED AS TO WHETHER THE TAPE MAY BE USED.*
*        A WTOR IS ISSUED VIA MESSAGE MODULE IFG0194J, AND THE REPLY  *
*        IS CHECKED FOR 'U' (USE) OR 'M' (MOUNT - ABEND).             *
*                                                                     *
* NOTES = SEE BELOW                                                   *
*                                                                     *
*    DEPENDENCIES =                                                   *
*            CLASS ONE CHARACTER CODE.  THE EBCDIC CHARACTER CODE     *
*            WAS USED FOR ASSEMBLY.  THE MODULE MUST BE REASSEMBLED   *
*            IF A DIFFERENT CHARACTER SET IS USED FOR EXECUTION.      *
*                                                                     *
*    RESTRICTIONS = NONE                                              *
*                                                                     *
*    REGISTER CONVENTIONS =                                           *
*            R2 POINTS TO DCB                                         *
*            R4 POINTS TO OPEN WORK AREA                              *
*            R5 POINTS TO THE RESIDENT ROUTINE                        *
*            R6 POINTS TO THE WTG TABLE                               *
*            R7 POINTS TO THE CURRENT PARAMETER LIST ENTRY            *
*            R8 POINTS TO THE CURRENT WTG TABLE ENTRY                 *
*            R9 POINTS TO THE DD ENTRY IN THE TIOT                    *
*            R10 POINTS TO THE UCB                                    *
*                                                                     *
*    PATCH LABEL = SEE NEXT TO LAST LABEL BEFORE ORG STATEMENT AT END *
*                  OF LISTING.                                        *
*                                                                     *
* MODULE TYPE = CONTROL (OPEN, CLOSE, EOV DATA MANAGEMENT)            *
*                                                                     *
*    PROCESSOR = ASSEMBLER XF                                         *
*                                                                     *
*    MODULE SIZE = SEE EXTERNAL SYMBOL DICTIONARY OR LOC FIELD ON     *
*                  ORG STATEMENT AT END OF LISTING                    *
*                                                                     *
*    ATTRIBUTES = REENTRANT, REFRESHABLE,READ-ONLY, ENABLED,          *
*                 PRIVILEGED, SUPERVISOR STATE, DATA MANAGEMENT KEY,  *
*                 LINK PACK AREA RESIDENT/PAGEABLE                    *
*                                                                     *
* ENTRY POINT = IFG0195H                                              *
*                                                                     *
*    PURPOSE = SEE FUNCTION                                           *
*                                                                     *
*    LINKAGE =                                                        *
*        FROM IFG0195B:                                               *
*          LA    RET,K4  K8  K12                                      *
*          LA    RF,ID5B5H                                            *
*          IECRES LOAD,MODID=(RF),BRCODE=(RET),BRANCH=QUEUED          *
*                                                                     *
*        FROM IFG0194J:                                               *
*          BRANCH QUEUED USING DXRETMOD AND DXRETCOD SET UP IN        *
*          IFG0195H.                                                  *
*                                                                     *
* INPUT = STANDARD REGISTERS                                          *
*    DXCCW1 - READ TAPE LABEL CCW (IN CASE NOT YET AT HDR1 LABEL)     *
*    DXCCW3 - SET BY IFG0195B TO INDICATE IF RDBACK OR OUTPUT         *
*             DISP=MOD POSITIONG (FOR ERROR RECOVERY DECISION).       *
*                                                                     *
* OUTPUT = STANDARD REGISTERS AND/OR EXPIRATION DATE MESSAGE          *
*                                                                     *
* EXIT-NORMAL = IFG0195K - TAPE FILE LABEL 2 PROCESSING               *
*               IFG0194J - MESSAGE MODULE                             *
*               IFG0195B - TO BACK UP 1 VOLUME LOOKING FOR END OF     *
*                          MULTIVOLUME DATA SET.                      *
*                                                                     *
* EXIT-ERROR = IFG0190P PROBLEM DETERMINATION; RETURN TO IFG0194A     *
*              EXIT WITH ONE OF THE FOLLOWING INTERNAL CODES--        *
*         11 - 613-08 ABEND - I/O ERROR READING LABEL. (RECOVERABLE)  *
*         12 - 613-0C ABEND - INVALID LABEL READ. (RECOVERABLE)       *
*         13 - 713-04 ABEND - EXPIRATION DATE NOT PASSED. (RECOVERABLE)
*         14 - 813-04 ABEND - INCORRECT DATA SET NAME. (RECOVERABLE)  *
*         20 - 913-08 ABEND - ASCII SECURITY BYTE NOT BLANK AND NOT   *
*              INDICATING SECURITY.  (DONE IN IFG0195K)               *
*        153 - A13-0C ABEND - FILE SEQUENCE BEFORE START OF VOLUME    *
*              WHILE POSITIONING TO END OF FILE.                      *
*              (DETECTED BY IFG0195B, ISSUED BY IFG0195H)             *
*        154 - A13-10 ABEND - TM INSTEAD OF HDR1 DURING FSF           *
*              POSITIONING. (DETECTED BY IFG0195B, ISSUED BY IFG0195H)*
*        155 - A13-14 ABEND - READ TM INSTEAD OF HDR1.                *
*                                                                     *
* EXTERNAL REFERENCES = SEE BELOW                                     *
*                                                                     *
*    ROUTINES = IFG019RA                                              *
*                                                                     *
*    DATA AREAS = STANDARD                                            *
*                                                                     *
*    CONTROL BLOCK = JFCB, DCB, UCB                                   *
*                                                                     *
* TABLES = STANDARD                                                   *
*                                                                     *
* MACROS = IECEQU, IECRES, DMABCOND, XLATE, EXCP, XCTLTABL, IECDSECS  *
*                                                                     *
* CHANGE ACTIVITY = SEE DELETIONS/CHANGES ABOVE AT START OF LISTING.  *
*                                                                     *
***********************************************************************
         EJECT
         BALR  RBASE,0                  ESTABLISH ADDRESSABILITY
         USING *,RBASE
*
         USING IHADCB,RDCB              DEFINE BASE TO COPIED DCB
         USING FORCORE,RCORE            ADDRESS OF WORK AREA
*        USING WTG,RWTG                 DEFINE BASE TO WTG TABLE
         USING TIOENTRY,RTIOT           DEFINE BASE TO TIOT DD ENTRY
         USING UCB,RUCB                 DEFINE BASE TO UCB
*
         B     OTA27800(RET)            GO TO PROPER ENTRY
OTA27800 EQU   *
         B     OTA29700                 EXPIRATION MSG RETURN    Y02134
         B     OTA27900                 NORMAL ENTRY
         B     OTA29200                 RET=8 DSN BAD IN HDR1  @ZA00003
         B     OTA29210                 RET=12 FSEQ BEFORE TAPE@ZA00003
*                                       OR TM BEFORE FILE      @ZA00003
*
***********************************************************************
*
*  READ HDR1 LABEL OF DESIRED FILE (NORMALLY STARTED IN IFG0195B)
*
OTA27850 EQU   *
         EXCP  DXIOB
OTA27900 EQU   *                        NORMAL ENTRY FROM BT
         IECRES WAIT
         TM    DXECB,ECBNOERR           I/O ERROR
         BO    OTA28200                 NO, BRANCH
OABD011H EQU   011                      I/O ERR READ TAPE LABEL@ZA04604
         LA    R0,OABD011H              LOAD 613-08 ABEND CODE @ZA04604
*
         TM    IOBSTAT0,CSWUNITX        TEST UNIT EXCEPTION TM @ZA04604
         BZ    OTA28100                 BR IF NO, I/O ERROR    @ZA04604
*
         MVC   DXCCW1,BSFCCW5H          SET UP FOR BSF OVER TM @ZA04604
         EXCP  DXIOB                    START BSF              @ZA04604
         IECRES WAIT                    WAIT FOR I/O COMPLETE  @ZA04604
*
OABD155  EQU   155                      TM INSTEAD OF HDR1 READ@ZA04604
         LA    R0,OABD155               LOAD A13-14 ABEND CODE @ZA04604
*
OTA28100 EQU   *                        GO TO PROB DETERMINATION
         MVC   DXSAVMOD,ID5H5B          SAVE RETURN LOAD         Y02134
         MVI   DXSAVOFF,K0              SAVE RETURN OFFSET       Y02134
*                                                                Y02134
         LA    RET,K24                  LOAD RETRY ENTRY OFFSET  Y02134
         DMABCOND (0),ID5H0P,RETURN=ID5H4A                       Y02134
         SPACE 1
*                   DETERMINE LABEL TYPE - VOL,UVL,HDR,EOV,EOF
         SPACE 1
OTA28200 EQU   *                        DETERMINE LABEL TYPE
         TM    UCBSTAB,UCBBSTR          IS THIS AN ANSI LABEL
         BZ    OTA28300                 NO, CONTINUE
         XLATE DXLBL,K80                YES, TRANSLATE LABEL DATA
OTA28300 EQU   *                        CONT LABEL TYPE DETERMINATION
         CLC   FL1LABI,AVOL             IS THIS A VOLUME LABEL
         BE    OTA27850                 BR IF VOL TO READ ANOTHER LBL
         CLC   FL1LABI,AHDR5H           HDR LABEL
         BE    OTA28400                 YES, GO CHECK TYPE
         CLC   FL1LABI,AEOV5H           EOV LABEL
         BE    OTA28400                 YES, GO CHECK TYPE
         CLC   FL1LABI,AEOF5H           EOF LABEL
         BE    OTA28400                 YES, BRANCH
         CLC   FL1LABI,AUVL5H           IS IT ANSI USER VOL LBL  A43864
         BE    OTA27850                 BR YES, READ NEXT LABEL  A43864
         B     OTA28450                 BR ABEND INVALID LABEL
OTA28400 EQU   *                        CHECK FILE LABEL TYPE
         CLI   FL1NO,CHAR1              IS THIS A FILE LABEL 1
         BE    OTA28500                 YES, BRANCH
OABD012H EQU   012                      INVALID TAPE LABEL READ
OTA28450 LA    R0,OABD012H              LOAD 613-0C ABEND CODE
         B     OTA28100                 GO TO PROBLEM DETERMINATION
*
***********************************************************************
*
*                   PERFORM FILE LABEL 1 VERIFICATION
         SPACE 1
*                   DETERMINE 17 LEAST SIGNIFICANT NON-BLANK
*                   CHARACTERS IN DATA SET NAME IN JFCB
         SPACE 1
OTA28500 EQU   *
         LA    RD,JFCBDSNM              POINT TO DSNAME
         LA    RB,JFCBDSNM+K27          ADDR OF 1ST POSS SIG CHAR
OTA28600 EQU   *                        CHECK FOR BLANK CHAR
         CLI   K16(RB),BLANK            IS THE CHARACTER BLANK
         BNE   OTA28700                 NO,GO TO SET UP STARTING ADDR
         BCT   RB,OTA28600              GO CHECK NEXT CHAR
OTA28700 EQU   *                        SET STARTING ADDR
         LA    RC,K17(RB)               POINT TO FIRST BLANK
         CLR   RB,RD                    IS LEFT PTR WITHIN FIELD
         BNL   OTA28800                 YES, BRANCH
         LR    RB,RD                    SET BASE OF START
         SPACE 1
*                   COMPARE DSNAME IN JFCB TO FILE IDENTIFICATION
         SPACE 1
*   COMPARE THE DATA SET NAME IN THE LABEL AGINST THE NAME IN THE
*   JFCB. CONTINUE IF EQUAL. IF NOT EQUAL AND THE DATA SET IS A
*   GENERATION DATA SET, IT MAY BE A DOS TAPE IN WHICH CASE THE
*   DATA SET NAME MUST BE COMPARED SEPARATELY FROM THE GENERATION
*   AND VERSION NUMBERS.
*
OTA28800 EQU   *
         CLC   FL1ID,0(RB)              COMPARE DSNAME
         BE    OTA29300                 BRANCH, DSNAME EQUAL
         TM    JFCBIND1,JFCGDG          GENERATION DATA SET
         BNO   OTA29200                 NO, BRANCH
         LA    RF,K9                    PREPARE TO BACK OVER GEN
         SR    RC,RF                    BACK UP END POINTER
         CLC   FL1GNO,K2(RC)            COMPARE GEN NUMBER
         BNE   OTA29200                 BRANCH IF NOT EQUAL
         CLC   FL1VNG,K7(RC)            COMPARE VERSION NUMBER
         BNE   OTA29200                 BRANCH IF NOT EQUAL
         SR    RB,RF                    BACK UP COMPARE START
         CLR   RB,RD                    IS LEFT POINTER WITHIN FIELD
         BNL   OTA29000                 YES, BRANCH
         SR    RC,RD                    GET CHAR COUNT
         LA    RF,FL1ID(RC)             PT DSN IN LBL + CHAR CNT S21940
         CLI   0(RF),BLANK              SHOULD BE PAST NAME
         BNE   OTA29200                 BR, NAME LONGER THAN IN JFCB
         BCTR  RC,R0                    SUB 1 FOR EX INSTRUCTION
         EX    RC,OTA28900              COMPARE DSNAMES
         B     OTA29100                 GO TEST COMPARE RESULT
OTA28900 CLC   FL1ID(*-*),JFCBDSNM      CLC INSTR FOR EXEC
OTA29000 CLC   FL1ID,0(RB)              COMPARE DSNAMES
OTA29100 BE    OTA29300                 BR IF DSNAME CORRECT
*
OTA29200 EQU   *                        DSNAME ERROR
         BAL   RC,OTA29280              GO BSR OR FSF TAPE     @ZA00003
OABD014  EQU   014                      DSNAME IH HDR1 NOT= JCL@ZA00003
         LA    R0,OABD014               LOAD 813-04 ABEND CODE @ZA00003
*
***********************************************************************
*
*  IF POSITIONING TO END OF FILE (RDBACK OR OUTPUT DISP=MOD),
*  THEN IT IS POSSIBLE WE STARTED AT TOO GREAT A VOLUME SEQUENCE
*  NUMBER AND CAN RECOVER BY BACKING UP VOLUMES.  BACKING UP IS
*  USED RATHER THAN READING ALL OF THE VOLUMES IN A FORWARD
*  DIRECTION BECAUSE READING COULD TAKE 10 MINUTES PER VOLUME.
*  BACKING UP IS FAST BUT ONLY WORKS FOR SINGLE FILE MULTI-VOLUME
*  DATA SETS AND NOT MULTI-FILE MULTI-VOLUME.
*  (WOULD NEED AN INDICATOR TO PREVENT LOOPING IF WE TRIED TO RECOVER
*  MULIT-VOLUME MULTI-FILE TAPES AND WOULD HAVE TO PROCESS TAPES IN
*  FORWARD ORDER FIRST WITH ONLY 1 VOLUME BACK UP ALLOWED.)
*  NO RECOVERY IS TRIED IF THE USER SPECIFICALLY ASKED FOR THIS VOLUME.
*
OTA29210 EQU   *                        FSEQ OR TM BEFORE FILE @ZA00003
         LH    RD,DXCCW3                GET RDBACK,MOD IND FROM@ZA00003
         LTR   RD,RD                    IFG0195B TAPE POSITIONG@ZA00003
         BZ    OTA28100                 BR IF NOT RDBACK,MOD   @ZA00003
*
         CLC   JFCBFLSQ,FW5H101         IS JFCB FILE SEQ NO = 1@ZA00003
         BNE   OTA28100                 BR IF NO, NO RECOVERY  @ZA00003
*
         LH    RD,JFCBVLSQ              IS IT SPECIFIEC JFCB   @ZA00003
         LTR   RD,RD                    VOL SEQUENCE REQUEST   @ZA00003
         BNZ   OTA28100                 BR IF YES TO ABEND     @ZA00003
*
         LH    RD,DXVOLSEQ              IS WORK VOL SEQ NO.    @ZA00003
         CH    RD,FW5H101               GREATER THAN 1         @ZA00003
         BNH   OTA28100                 BR IF NO TO ABEND      @ZA00003
*
*                                       DECREMENT CURRENT WORK @ZA00003
         BCTR  RD,0                     VOL SEQUECE NUMBER BY 1@ZA00003
         STH   RD,DXVOLSEQ              TO BACK UP 1 VOLUME    @ZA00003
*
*  WRITE MSG 'IEC140I DDNAMEXX,VOLIDX END OF DATA SET NOT ON VOLUME'
*
         MVC   MSGLSTSZ(OTA2980L),OTA29800  MOVE MSG WORK AREA @ZA00003
         MVC   MSGACTN(L'TIOEDDNM),TIOEDDNM  DDNAME TO MSG     @ZA00003
         MVC   MSGVOLSR+K3,UCBVOLI      VOLID TO MESSAGE       @ZA00003
         WTO   MF=(E,MSGLSTSZ)          WRITE MSG TO PROGRAMMER@ZA00003
*
         MVC   DXVOLSR1,UCBVOLI         GET VOLUME SERIAL NO.  @ZA00003
         MODESET EXTKEY=SUPR            UCB KEY                @ZA00003
         MVI   UCBDMCT,K0               SET DATAMGT COUNT TO 0 @ZA00003
         MODESET EXTKEY=DATAMGT         DATA MANAGEMENT KEY    @ZA00003
         MVC   DXRETMOD,ID5H5B          ID OF IFG0195B         @ZA00003
         MVI   DXRETCOD,K0              RETURN BR TABLE ENTRY  @ZA00003
         IECRES LOAD,MODID=ID5H4A,BRCODE=K0,BRANCH=QUEUED      @ZA00003
*
***********************************************************************
*
*   BACKSPACE TO BEGINNING OF HDR1 SO THAT IF A SUCCEEDING
*   STEP ATTEMPTS TO REFERENCE THIS TAPE IT WILL NOT GET
*   A 613-0C ABEND FOR READING AN INVALID TAPE LABEL.
*   IF EOV1/EOF1, FSF AND INCREMENT UCB FILE SEQUENCE COUNTERS.
*
OTA29280 EQU   *                        ENTRY VIA BAL RC,---   @ZA04609
         MVC   DXCCW1,BSRCCW5H          BSR CCW                @ZA04609
         CLI   FL1LABI,C'E'             IS THIS EOV1/EOF1 LABEL@ZA04609
         BNE   OTA29290                 BR IF NO, HDR1         @ZA04609
         MVI   DXCCW1,CCWFSF            CHANGE TO FSF CCW      @ZA04609
         L     RF,UCBFSCT+UCBFSEQ-UCBFSEQ  INCREMENT UCB FILE  @ZA04609
         A     RF,FW5H101               COUNT AND SEQUENCE     @ZA04609
         MODESET EXTKEY=SUPR            UCB KEY                @ZA04609
         ST    RF,UCBFSCT+UCBFSEQ-UCBFSEQ  BY 1 EACH           @ZA04609
         MODESET EXTKEY=DATAMGT         DATA MANAGEMENT KEY    @ZA04609
OTA29290 EQU   *                                               @ZA04609
         EXCP  DXIOB                    BSR                      A39154
         IECRES WAIT                                             A39154
         BR    RC                       RETURN                 @ZA04609
*
***********************************************************************
*
*                    PLACE BLOCK COUNT INTO THE DCB
         SPACE
OTA29300 EQU   *                        BLOCK COUNT INTO DCB
         CLC   FL1LABI,AHDR5H           IS THIS A HEADER LABEL
         BE    OTA29500                 YES,DO NOT MERGE BLOCK COUNT
         TM    DCBMACRF,DCBMEXCP        IS THE PROBLEM PROG USING EXCP
         BZ    OTA29400                 BR IF NO TO MERGE BLOCK COUNT
         TM    DCBMACRF+K1,DCBMDEV      DOES TAPE DEPEND PORTION EXIST
         BZ    OTA29500                 NO,DO NOT MERGE BLOCK COUNT
OTA29400 EQU   *                        MERGE BLOCK COUNT
         PACK  DXCCW2,FL1BLKCT          YES,PACK BLOCK COUNT
         CVB   RB,DXCCW2                CONVERT IT TO BINARY
         ST    RB,DCBBLKCT              STORE IT INTO THE DCB
*
***********************************************************************
*         PLACE TAPE LABEL CREATION DATE IN JFCB
***********************************************************************
*
OTA29500 EQU   *                        MERGE CREATION DATE    @ZA15669
         TM    FL1CREDT,ALLBITS-BLANK   DATE = 0 OR BLANK      @ZA19601
         BZ    OTA29520                 YES, BRANCH            @ZA19601
         PACK  DXCCW12(K8),FL1CREDT+K1(K2) GET YEAR FROM LABEL @ZA15669
         CVB   RB,DXCCW12               CONVERT YEAR TO BINARY @ZA15669
         STC   RB,JFCBCRDT              STORE YEAR IN JFCB     @ZA15669
         PACK  DXCCW12(K8),FL1CREDT+K3(K3)  GET DAY FROM LABEL @ZA15669
         CVB   RB,DXCCW12               CONVERT DAY TO BINARY  @ZA15669
         STCM  RB,B'0011',JFCBCRDT+K1   STORE DAY IN JFCB      @ZA15669
***********************************************************************
*
*  IF OPENING FOR ANY TYPE OUTPUT,RETENTION MUST BE CHECKED
*
OTA29520 EQU *                                                 @ZA19601
         SPACE
         TM    PLISTOPT(RPARC),PLISTIO  THIS DCB INOUT,OUTIN,OR OUTPUT
         BNO   OTA30200                 BR IF NO
         PACK  DXCCW2+K5(K3),FL1EXPDT+K1(K5)  PACK EXPIRATION DATE FROM
         CLC   DXCCW2+K5(K3),NODATE     IS DATE IN LBL BLANK
         BE    OTA30200                 YES,BRANCH
         PACK  DXCCW12(K8),FL1EXPDT+K1(K2)  GET YEAR FROM LABEL
         CVB   RB,DXCCW12               CONVERT YEAR TO BINARY
         STC   RB,JFCBXPDT              STORE YEAR IN JFCB
         PACK  DXCCW12(K8),FL1EXPDT+K3(K3)  GET DAY FROM LABEL
         CVB   RB,DXCCW12               CONVERT DAY TO BINARY
         STH   RB,JFCBXPDT+K1           STORE DAY IN JFCB
         L     RB,CVTPTR                GET ADDR. OF CVT
         CLC   DXCCW2+K5(K3),CVTDATE+K1-CVT(RB)  IS RETENTION EXPIRED
         BNH   OTA30200                 YES, GO CHECK SECURITY
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
*        GO TO ERROR ROUTINE TO WRITE EXPIRATION DATE            Y02134
*        ERROR MESSAGE TO THE OPERATOR, AND WAIT FOR             Y02134
*        HIS REPLY TO USE OR NOT USE THE DATA SET.               Y02134
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
OTA29600 EQU   *                        WRITE EXPIRATION DATE MSG
*                                                                Y02134
         MVC   DXRETMOD,ID5H5H          RETURN ID TO SAVE AREA   Y02134
         MVI   DXRETCOD,K0              SET RETURN INDICATOR     Y02134
*                                                                Y02134
         XC    DXWORK1,DXWORK1          SET REPLY AREA TO BLANKS Y02134
         LA    R1,DXWORK1               POINT TO REPLY AREA      Y02134
         LA    R0,K1                    SET REPLY LENGTH         Y02134
         SLL   R0,K24                   MOVE LENGTH TO HI BYTE   Y02134
         OR    R1,R0                    SET UP WTOR PARM         Y02134
         MVC   DXCCW2(L'FL1ID),FL1ID    MOVE IN DSNAME           Y02134
         LA    R0,DXCCW2                GET DSNAME ADDRESS       Y02134
         MVI   DXCCW2+L'FL1ID,BLANK     DELIMIT 17 CHAR NAME     Y02134
         MVC   DXCCW2+L'FL1ID+K1(L'JFCBDSNM-L'FL1ID-K1),DXCCW2+L'FL1ID
*                                                                Y02134
         IECRES LOAD,MODID=ID5H4J,BRCODE=K20,BRANCH=QUEUED       Y02134
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
*        RETURN HERE FROM EXPIRATION CHECK                       Y02134
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
OTA29700 EQU   *                        EXPIRATION MSG           Y02134
*                                                                Y02134
         IECRES WAIT                    WAIT FOR OPERATOR REPLY  Y02134
*                                                                Y02134
         OI    DXWORK1,BLANK            FOLD REPLY               Y02134
         CLI   DXWORK1,CHARU            CHECK FOR REPLY U        Y02134
         BE    OTA30200                 BRANCH TO USE            Y02134
*                                                                Y02134
         CLI   DXWORK1,CHARM            CHECK IF REJECT          Y02134
         BNE   OTA29600                 BRANCH, INCORRECT REPLY  Y02134
*                                                                Y02134
         BAL   RC,OTA29280              GO BSR OR FSF TAPE     @ZA04609
OABD013  EQU   13                       TAPE DS NOT EXPIRED    @ZA04609
         LA    R0,OABD013               LOAD 713-04 ABEND CODE @ZA04609
         B     OTA28100                 BRANCH TO PROB DET       Y02134
*                                                                Y02134
OTA30200 EQU   *                        GO TO IFG0195K
         IECRES LOAD,MODID=ID5H5K,BRCODE=K0,BRANCH=QUEUED        Y02080
*
***********************************************************************
         EJECT
***********************************************************************
*
*              CONSTANTS
*
OTA29800 WTO   'IEC140I DDNAMEXX,VOLIDX END OF DATA SET NOT ON VOLUME',X
               MF=L,DESC=6,             JOB STATUS             @ZA00003X
               ROUTCDE=11               PROGRAMMER INFORMATION @ZA00003
OTA2980L EQU   *-OTA29800               LENGTH OF WTO          @ZA00003
FW5H101  DC    0F'0',H'1',H'1'          TO INCR UCB COUNT+SEQ  @ZA04609
BSRCCW5H DC    X'2700000020000001'      BSR CCW                @ZA04609
BSFCCW5H DC    X'2F00000020000001'      BSF CCW                @ZA04604
AVOL     DC    C'VOL'                   VOLUME
AHDR5H   DC    C'HDR'                   HEADER
AEOV5H   DC    C'EOV'                   END OF VOLUME
AEOF5H   DC    C'EOF'                   END OF FILE
AUVL5H   DC    C'UVL'                   ANSI USER VOL LBL ID     A43864
NODATE   DC    X'00000F'                NO DATE
*
XCTL5H   XCTLTABL ID=(ID5H4A,4A,ID5H5K,5K,ID5H5H,5H,ID5H0P,0P,   Y02134X
               ID5H5B,5B,ID5H4J,4J),BRT=YES,LENGTH=              Y02080
         EJECT
         IECEQU ,                       DEFINE EQUATES
         IECDSECS CVT,TIOT,UCB,DCB,MAIN,EXPAND=YES
         END
