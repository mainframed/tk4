         TITLE 'IFG0232Z - TCLOSE FINAL PROCESSING'
IFG0232Z CSECT
         ENTRY IFG0202U                                          Y02080
IFG0202U EQU   IFG0232Z                 ALIAS ENTRY POINT        Y02080
*
***********************************************************************
*                                                                     *
*          VS2 RELEASE 03 DELETIONS/CHANGES                           *
*0000                                                          @ZA03180
*          VS1 RELEASE 02 DELETIONS/CHANGES                           *
*0000483790-483798                                               XM1037
*0000427000,484000-485000,575000,586000                          X02989
*                                                                     *
*          VS2 RELEASE 02 DELETIONS/CHANGES                           *
*0000450000-452000                                               Y02080
*0000077000,173000,192000-193000,267000-268000,575000-575620,    YM1272
*0000580000                                                      YM1272
*          RELEASE 22 DELETIONS/CHANGES                               *
*          RELEASE 21 DELETIONS/CHANGES                               *
*0000                                                            A38013
*                                                                M0039
*0000024000,030000,033000-036000,037000,490000-495000            A35953
*D485120-485280,486000                                          SA48558
*D445000                                                         S21940
*                                                                     *
* MODULE NAME - IFG0232Z                                              *
*                                                                     *
* DESCRIPTIVE NAME - TCLOSE FINAL PROCESSING                          *
*                                                                     *
* COPYRIGHT - NONE                                                    *
*                                                                     *
* CHANGE ACTIVITY - SEE DELETIONS/CHANGES FOLLOWING THE CSECT CARD.   *
*                                                                     *
* STATUS CHANGE LEVEL 000                                             *
*                                                                     *
* FUNCTION -                                                          *
*    THIS MODULE CONTAINS THE FOLLOWING FUNCTIONS -                   *
*       1. TCLOSE DIRECT ACCESS OUTPUT FUNCTION, PART II;             *
*       2. TCLOSE FINAL FUNCTION                                      *
*    REFER TO THE FOLLOWING 'FUNCTION PROLOGS' FOR DETAILS.           *
*                                                                     *
* ENTRY POINTS -                                                      *
*         IFG0232Z - REFER TO 'INPUT' FOR A LIST OF CALLING MODULES   *
*                    AND THEIR RESPECTIVE ENTRY CONDITIONS.           *
*                                                                     *
* INPUT -                                                             *
*    ENTERED IN DATA MANAGEMENT KEY                              Y02082
*    REGISTER 14 ('RET') WILL CONTAIN ONE OF THE BRANCH TABLE OFFSETS *
*    LISTED BELOW, THE BRANCH TABLE BEING USED TO DETERMINE AT        *
*    WHICH POINT PROCESSING IS TO BEGIN-                              *
*       0 - ENTRY FROM EITHER IFG0232D OR IFG0232J TO WRITE A FILE    *
*           MARK TO DELIMIT AN OUTPUT DATA SET ON A DIRECT ACCESS     *
*           DEVICE.                                                   *
*       4 - ENTRY FROM ANY OF THE FOLLOWING MODULES TO PERFORM        *
*           TCLOSE FINAL PROCESSING-                                  *
*           IGC0002C                                                  *
*           IFG0232D                                                  *
*           IFG0232J                                                  *
*           IFG0232S                                                  *
*           IFG0232Y (AN ALIAS FOR IFG0202B, THE CLOSE/TCLOSE NSL     *
*                    INTERFACE FUNCTION)                              *
*    REFER TO THE FOLLOWING 'FUNCTION PROLOGS' FOR ADDITIONAL INPUTS. *
*                                                                     *
* OUTPUT -                                                            *
*    EXIT IN DATA MANAGEMENT KEY                                 Y02082
*    REFER TO THE FOLLOWING 'FUNCTION PROLOGS'.                       *
*                                                                     *
* EXTERNAL REFERENCES -                                               *
*         IFG019RA - OPEN/CLOSE/EOV RESIDENT ROUTINE FOR XCTL, WAIT,  *
*                    AND SYNCH. REFER TO THE FOLLOWING 'FUNCTION      *
*                    PROLOGS'.                                        *
*                                                                     *
* EXITS, NORMAL -                                                     *
*    REFER TO THE FOLLOWING 'FUNCTION PROLOGS'.                       *
*                                                                     *
* EXITS, ERROR -                                                      *
*         IFG0230P - WHEN AN ERROR CONDITION OCCURS IN THIS MODULE.   *
*                    REFER TO THE FOLLOWING 'FUNCTION PROLOGS'.       *
*                                                                     *
* TABLES/WORK AREAS -                                                 *
*    REFER TO THE FOLLOWING 'FUNCTION PROLOGS'.                       *
*                                                                     *
* ATTRIBUTES -                                                        *
*    REENTRANT, REFRESHABLE, READ-ONLY, ENABLED, PRIVILEGED           *
*                                                                     *
* CHARACTER CODE DEPENDENCY -                                         *
*    CLASS ONE CHARACTER CODE DEPENDENCY - THE EBCDIC CHARACTER CODE  *
*    WAS USED FOR ASSEMBLY.  THE MODULE MUST BE REASSEMBLED IF A      *
*    DIFFERENT CHARACTER SET IS USED FOR EXECUTION.                   *
*                                                                     *
* NOTES -                                                             *
*    THIS MODULE HAS AN ALIAS NAME OF IFG0202U.                       *
*    REFER TO THE FOLLOWING 'FUNCTION PROLOGS'.                       *
*                                                                     *
***********************************************************************
*
         BALR  RBASE,0                  ESTABLISH ADDRESSABILITY
         USING *,RBASE
         USING FORCORE,RCORE
         USING CVT,RET
         USING IHADCB,RDCB
         USING DEBBASIC,RDEB            ADDRESSABILITY TO DEB    YM1272
         USING TIOT,RTIOT
         USING WTG,RWTG
         USING UCB,RUCB
*
         B     BRTABLE5(RET)            DETERMINE TYPE OF ENTRY TO RTN
BRTABLE5 EQU   *
         B     TCD01700             (0) BR TO FINISH DA PROCESSING
         B     TCM50000             (4) BR TO PERFORM TCLOSE CLEANUP
***********************************************************************
*                                                                     *
*                          FUNCTION PROLOG                            *
*                                                                     *
***********************************************************************
*                                                                     *
* FUNCTION - 'TCLOSE DIRECT ACCESS OUTPUT FUNCTION', PART II          *
*                                                                     *
* (STATUS) -                                                          *
*    NOT APPLICABLE                                                   *
*                                                                     *
* FUNCTION -                                                          *
*    1.  WRITE FILE MARK FOLLOWING USER'S DATA;                       *
*    2.  POSITION THE VOLUME, EITHER BEFORE THE FIRST RECORD          *
*        (REREAD), OR AFTER THE LAST RECORD (LEAVE).                  *
*                                                                     *
* ENTRY POINTS -                                                      *
*    REFER TO THE PRECEDING MODULE PROLOG.                            *
*                                                                     *
* INPUT -                                                             *
*    1. REGISTER CONTENTS AT ENTRY ARE AS FOLLOWS-                    *
*       'RDCB' - ADDRESS OF THE DCB BEING PROCESSED;                  *
*       'RCORE'- ADDRESS OF THE WORK AREA FOR THAT DCB;               *
*       'RES'  - ADDRESS OF THE OPEN/CLOSE/EOV RESIDENT ROUTINE;      *
*       'RWTG' - ADDRESS OF THE WHERE-TO-GO TABLE;                    *
*       'RPARC'- ADDRESS OF THE CURRENT TCLOSE PARAMETER LIST ENTRY;  *
*       'RWTGC'- ADDRESS OF THE CURRENT WHERE-TO-GO TABLE ENTRY;      *
*       'RTIOT'- ADDRESS OF THE CURRENT TIOT ENTRY FOR THE DCB;       *
*       'RUCB' - ADDRESS OF THE CURRENT UCB FOR THE DCB;              *
*       'RDEB' - ADDRESS OF THE DCB'S DEB;                            *
*                                                                     *
* OUTPUT -                                                            *
*    1. AT EXIT TO THE TCLOSE FINAL FUNCTION, OUTPUT IS VIRTUALLY THE *
*       SAME AS AT INPUT TO THIS FUNCTION;                            *
*    2. AT EXIT TO PROBLEM DETERMINATION MODULE IFG0230P-             *
*       REGISTER CONTENTS ARE THE SAME AS AT ENTRY EXCEPT THAT        *
*          REGISTER 'R0' CONTAINS THE INTERNAL ERROR CODE;            *
*       WORK AREA INDICATORS-                                         *
*          'DXCALLID' - CONTAINS THE ID OF THIS MODULE.               *
*                                                                     *
* EXTERNAL REFERENCES -                                               *
*    REFER TO THE PRECEDING MODULE PROLOG.                            *
*                                                                     *
* EXITS, NORMAL -                                                     *
*    1. TO PART II OF THIS MODULE- THE 'TCLOSE FINAL FUNCTION'.       *
*                                                                     *
* EXITS, ERROR -                                                      *
*    EXIT TO IFG0230P WITH ONE OF THE FOLLOWING INTERNAL ABEND CODES: *
*        131 (117/04)- ERROR WRITING FILE MARK;                       *
*        233 (117/34)- M IN DCBFDAD REFERENCES A NONEXISTENT EXTENT   *
*            IN DEB.                                                  *
*                                                                     *
* TABLES/WORK AREAS -                                                 *
*    THE OPEN, CLOSE, OR EOV WORK AREA AND THE WHERE-TO-GO (WTG)      *
*    TABLE ARE DESCRIBED BY THE DSECTS AT THE END OF THE LISTING.     *
*                                                                     *
* ATTRIBUTES -                                                        *
*    REFER TO THE PRECEDING MODULE PROLOG.                            *
*                                                                     *
* CHARACTER CODE DEPENDENCY -                                         *
*    REFER TO THE PRECEDING MODULE PROLOG.                            *
*                                                                     *
* NOTES -                                                             *
*                                                                     *
***********************************************************************
*
TCD01700 EQU   *
         L     RDEB,DCBDEBAD            RESTORE USER'S DEB ADDR
*
*****    WRITE FILE MARK FOLLOWING USER'S DATA RECORDS
*
         SR    RC,RC                    CLEAR WORK REGISTER
         IC    RC,DCBFDAD               PICK UP M (CURRENT EXTENT NUMBE
         CLM   RC,B'0001',DEBNMEXT      ARE THERE M EXTENTS      Y02082
*                                       IN THE DEB               Y02082
         BL    TCD01710                 BRANCH IF YES-CONTINUE   Y02082
         LA    RC,TCABD117              LOAD INTERNAL ABCODE     Y02082
         B     TCD03100                 ABEND                    Y02082
TCABD117 EQU   233                      INVALID DCBFDAD INTERNAL Y02082
*                                       ABEND CODE               Y02082
TCD01710 EQU   *                        SET UP ABEND CODE        Y02082
         SLL   RC,K4                    MULTIPLY BY 16 TO POINT TO EXTE
         LR    RD,RDEB                  SAVE DEB ADDRESS
         LA    RDEB,DEBBASND            POINT TO END OF BASIC    YM1272
*                                       SECTION OF THE DEB       YM1272
         USING DEBDASD,RDEB             ADDRESSABILITY TO DIRECT YM1272
*                                       ACCESS SECTION OF DEB    YM1272
         AR    RDEB,RC                  NOW POINTING TO PROPER EXTENT
         MVC   DXDEBBIN(K12),DEBBINUM   PLACE USER'S EXTENT IN WORK ARE
         DROP  RDEB                                              YM1272
         MVC   DXCCW4+K1(K8),DCBFDAD    MOVE MBBCCHHR TO WORK AREA
         MVI   DXDEBUCB,X'C0'           SET MODIFIER BYTE TO ALLOW WRIT
         L     R1,DCBDVTBL              ADDRESS OF DEVICE TABLE
         LH    RF,K4(R1)                PICK UP NR OF BYTES PER TRACK
         CH    RF,DCBTRBAL              IS FULL TRCK AVAILABLE FOR FM
         BE    TCD02300                 BR IF FULL TRCK AVAILABLE
         TM    DCBRECFM,DCBRECFX+DCBRECST IS THE USER WRITING F STD
         BNO   TCD01800                 NO,DO NOT WRITE F.M. ON THIS
*                                       TRACK
         SR    RC,RC                    INITIALIZE REGISTER
         IC    RC,DEBNMEXT-DEBBASIC(,RD)  PICK UP NBR OF EXTENTS YM1272
         SLL   RC,K4                    MULTIPLY BY 16
         AR    RC,RD                    ADD DEB ADDRESS
         LH    RF,K44(RC)               ADD KEYLENGTH+BLOCKSIZE
         CH    RF,DCBTRBAL              WILL A RECORD FIT ON THIS TRACK
         BH    TCD01800                 NO,DO NOT ATTEMPT WRITE THIS TR
         BAL   RD,TCD02900              YES,GO SET UP CHANNEL PROGRAM
         IC    RF,DXCCW4+K8             ADD 1 TO LAST RECORD NUMBER
         LA    RF,K1(,RF)               WRITTEN BY THE PROBLEM PROGRAM
         STC   RF,DXDSCB+K4             STORE UPDATED RECORD NUMBER
         BAL   RD,TCD03000              GO TO WRITE THE FILE MARK
         OI    DXATDACC,DXATEOF         EOF MARK WRITTEN         Y02144
TCD01800 EQU   *
         STM   RWTGC,RDEB,DXCCW5+K4     SAVE VOLATILE REGS
         LR    RWTGC,RDCB               ESTABLISH DCB BASE
         DROP  RDCB
         USING IHADCB,RWTGC
         LA    RDCB,DXCCW4+K1           ADDR OF MBBCCHHR WORK
*                                       AREA FOR CONVERT RTNS
         L     R1,DCBDEBAD              ESTABLISH DEB BASE
         USING DEBBASIC,R1              ADDRESSABILITY TO DEB    YM1272
         L     RF,CVTPTR
         L     RF,CVTPRLTV-CVT(RF)      MBBCCHHR TO TTR ADDRESS
         BALR  RET,RF                   TTR0 IN REG 0
         AL    R0,CON10000              BUMP IT BY ONE
         ST    R0,DXCCW7+K4             SAVE TT+1,R
         L     RF,CVTPTR
         L     RF,CVTPCNVT-CVT(RF)      TTR TO MBBCCHHR ADDRESS
         BALR  RET,RF                   MBBCCHHR IN DXCCW4+1
         LTR   RF,RF                    IS ANOTHER TRK AVAILABLE
         LA    RF,TCD02500              BRANCH IF NO - DO NOT
         BNZ   TCD02200                 WRITE FILE MARK
         CLI   DXCCW4+K1,K0             IS EXTENT NUMBER 0
*                                       SPLIT ALLOCATION ALLOWED
*                                       ON FIRST EXTENT ONLY
         BE    TCD01900                 YES - GO CHECK IF SPLIT
         SR    RF,RF                    CLEAR WORK REGISTER
         IC    RF,DXCCW4+K1             PICK UP CURRENT EXT NBR
         CLM   RF,B'0001',DEBNMEXT      ARE THERE M EXTENTS      Y02082
*                                       IN THE DEB               Y02082
         BL    TCD01850                 BRANCH IF YES,CONTINUE   Y02082
         LA    RC,TCABD117              BAD FDAD INTERNAL ABCODE Y02082
         LA    RF,TCD03100              ABEND-INVALID FDAD       Y02082
         B     TCD02200                 BRANCH TO RESTORE REGS   Y02082
*                                                                Y02082
TCD01850 EQU   *                        CALCULATE EXTENT ADDRESS Y02082
         SLL   RF,K4                    MULTIPLY BY 16
         AR    RF,R1                    ADD DEB ADDRESS
         MVC   DXDEBUCB+K1(K15),K33(RF)    PUT USER EXTENT IN WORK AREA
         B     TCD02100                 GO WRITE FILE MARK
TCD01900 EQU   *
         TM    DEBOFLGS,DEBOFSPL        ARE THERE SPLIT CYLINDER
         BNO   TCD02100                 BR IF NO SPLIT CYLINDERS
         SR    RC,RC                    START H
         SR    RD,RD                    END H
         SR    RF,RF                    NEW H IN FDAD
         SR    RUCB,RUCB                EXTENT OFFSET
         IC    RUCB,DXCCW4+K1           GET NEW M
         SLL   RUCB,K4                  TIMES 16 FOR INDEXING
         IC    RF,DXCCW4+K7             SECOND H OF NEW HH
         LA    R1,DEBBASND              POINT TO END OF BASIC    YM1272
*                                       SECTION OF THE DEB       YM1272
         USING DEBDASD,R1               ADDRESSABILITY TO DIRECT YM1272
*                                       ACCESS SECTION OF DEB    YM1272
         IC    RC,DEBSTRHH+K1(RUCB)     START H
         IC    RD,DEBENDHH+K1(RUCB)     END H
         DROP  R1                                                YM1272
*
*****    TEST IF CURRENT ADDRESS WITHIN SPLIT
*
         CR    RF,RC                    IS NEW H BELOW START H
         BL    TCD02000                 BRANCH IF YES - NEED TO
*                                       GET NEW MBBCCHHR
         CR    RF,RD                    IS NEW H ABOVE END H
         BNH   TCD02100                 BR IF NO -  H IS GOOD
*
*****    NEW H OUTSIDE DEB EXTENTS - NEED TO GET NEW MBBCCHHR
*
TCD02000 EQU   *
         SR    RD,RC                    NO OF TRACKS -1 IN SPLIT
         L     RUCB,DCBDVTBL            ADDR OF DEVICE TABLE
         USING UNITABLE,RUCB
         IC    RC,UNITSIHH+K1           GET NO. OF TRACKS
         DROP  RUCB
         SR    RC,RD                    SUBTRACT NO TRACKS IN
*                                     SPLIT - 1 FROM NO OF
*                                       TRACKS IN CYLINDER
         BCTR  RC,0                     SUBTRACT ONE MORE - THIS
*                                       IS CORRECT TRACK
*                                       DISPLACEMENT TO ADD
*                                       TO OLD TT+1,R
         SLL   RC,K16                   SHIFT TO ADD
         AL    RC,DXCCW7+K4             ADD TT+1,R
         LR    R0,RC                    PUT IN RE FOR CONVERSION
         L     RF,CVTPTR
         L     RF,CVTPCNVT-CVT(RF)      TTR TO MBBCCHHR ADDR
         BALR  RET,RF                   MBBCCHHR IN DXCCW4+1
         LTR   RF,RF                    NEW TRACK AVAILABLE
         LA    RF,TCD02500
         BNZ   TCD02200                 BR IF NO - NO FILE MARK
TCD02100 EQU   *
         LA    RF,TCD02400              WRITE FILE MARK
TCD02200 EQU   *
         LR    RDCB,RWTGC               RESTORE DCB BASE
         DROP  RWTGC
         USING IHADCB,RDCB
         LM    RWTGC,RDEB,DXCCW5+K4     RESTORE VOLATILE REGS
         USING DEBDASD,RDEB             ADDRESSABILITY TO DIRECT YM1272
*                                       ACCESS SECTION OF DEB    YM1272
         MVI   DXCCW4+K8,K0             ZERO R TO SEARCH ON TO
*                                       WRITE ON NEW TRACK
         BR    RF                       RETURN
TCD02300 EQU   *
         CLC   DXCCW4+K4(K4),DEBENDCC   IS CCHH IN THIS EXTENT
         BH    TCD01800                 NO,GO SEE IF ANOTHER
*                                       EXTENT
TCD02400 EQU   *
         BAL   RD,TCD02900              YES,GO TO SET UP CHANNEL PROG
         MVI   DXDSCB+K4,K1             SET RECORD NUMBER OF FILE MARK
         BAL   RD,TCD03000              GO TO WRITE THE FILE MARK
         OI    DXATDACC,DXATEOF         EOF MARK WRITTEN         Y02144
*
*****    SET CURRENT POSITION POINTER IN THE DCB
*
TCD02500 EQU   *
         USING DEBBASIC,RDEB            ADDRESSABILITY TO DEB    YM1272
         L     RDEB,DCBDEBAD            RESTORE ADDRESS OF USER'S DEB
         TM    PLISTOPT(RPARC),PLISTLV  IS DISPOSITION 'LEAVE'-
         BNM   TCD02600                 ..BR IF YES- DCBFDAD IS CORRECT
         LA    RD,DEBBASND              POINT TO END OF BASIC    YM1272
*                                       SECTION OF THE DEB       YM1272
         USING DEBDASD,RD               ADDRESSABILITY TO DIRECT YM1272
*                                       ACCESS SECTION OF DEB    YM1272
         MVC   DCBFDAD+K1(K6),DEBBINUM  SET BBCCHH TO BEGINNING OF EXTE
         DROP  RD                                                YM1272
         MVI   DCBFDAD,K0               SET M EQUAL TO ZERO
         MVI   DCBFDAD+K7,K0            SET RECORD NUMBER TO ZERO
         L     RC,DCBDVTBL              PICK UP BASE OF DEVICE TABLE
         MVC   DCBTRBAL,K4(RC)          SET NUM OF BYTES REMAING TO MAX
*
*****    UPDATE FULL DISK ADDRESS IN BSAM IOB
*
TCD02600 EQU   *
         L     RD,DCBIOBA               PICK UP ADDRESS OF PREVIOUS IOB
         LA    RD,K0(,RD)               CLEAR HIGH BYTE (FOR COMPARE)
         LR    R0,RD
         SR    RC,RC
         LR    R1,RD                    COPY ICB/IOB ADDRESS
         TM    DCBCIND2,DCBC2CHN        IS PCI USED
         BO    TCD02700                 YES  BRANCH
         LA    R1,K8(RD)                ADJUST IOB FOR NORMAL SCHED.
         LA    RC,K8
TCD02700 EQU   *
         MODESET KEYADDR=DXUKEY,WORKREG=14 ICB/IOB KEY           Y02082
         MVC   K32(K8,R1),DCBFDAD       UPDATE 1ST ICB/IOB
TCD02800 EQU   *
         L     RD,K0(,RD)               ADDRESS OF NEXT IOB IN CHAIN
         LA    RD,K0(,RD)               CLEAR HI BYTE
         LA    R1,K0(RC,RD)             FORM THE IOB OR ICB ADDRESS
         MVC   K32(K8,R1),DCBFDAD       UPDATE IOB/ICB  FDAD
         CR    RD,R0                    IS THIS LAST IOB IN CHAIN
         BNE   TCD02800                 ..BRANCH IF NOT
*                                       ..ELSE DROP THROUGH TO TCLOSE
*                                       ..FINAL FUNCTION
         MODESET EXTKEY=DATAMGT         TCLOSE WORK AREA KEY     Y02082
         TITLE 'IFG0232Z - TCLOSE FINAL FUNCTION'
***********************************************************************
*                                                                     *
*                          FUNCTION PROLOG                            *
*                                                                     *
***********************************************************************
*                                                                     *
* FUNCTION NAME - 'TCLOSE FINAL FUNCTION'                             *
*                                                                     *
* (STATUS) -                                                          *
*    NOT APPLICABLE                                                   *
*                                                                     *
* FUNCTION -                                                          *
*    1. SYNCHRONIZE PROCESSING OF ALL DATA CONTROL BLOCKS TO THE      *
*       BEGINNING OF THIS FUNCTION;                                   *
*    2. ISSUE A 'DELAYED ABEND' UTILIZING THE ABEND CODE PROVIDED     *
*       BY THE PROBLEM DETERMINATION ROUTINE IF THAT WAS REQUESTED    *
*       BY THE USER IN HIS ABEND EXIT ROUTINE;                        *
*       RESET DEBDCBAD TO POINT TO THE CALLER'S DCB.             Y02082
*    3. TERMINATE THE OPTIONAL TRACE FACILITY (GTF) IF IT IS ACTIVE;  *
*    4. RESET VARIOUS DATA SET STATUS INDICATORS IN THE DCB AND DEB   *
*       IN ORDER THAT PROCESSING MAY CONTINUE.                        *
*    5. RELEASE THE WHERE-TO-GO (WTG) TABLE MAIN STORAGE WORK AREA    *
*       AND THE TCLOSE WORK AREA FOR EACH DCB. THIS IS DONE VIA THE   *
*       RESIDENT ROUTINE WHICH ALSO DEQ'S THE TIOT.                   *
*    6. RETURN TO THE USER.                                           *
*                                                                     *
* ENTRY POINTS -                                                      *
*    REFER TO THE PRECEDING 'MODULE PROLOG'.                          *
*                                                                     *
* INPUT -                                                             *
*    1. REGISTER CONTENTS AT ENTRY ARE AS FOLLOWS-                    *
*       'RDCB' - ADDRESS OF THE DCB BEING PROCESSED;                  *
*       'RCORE'- ADDRESS OF THE WORK AREA FOR THAT DCB;               *
*       'RES'  - ADDRESS OF THE OPEN/CLOSE/EOV RESIDENT ROUTINE;      *
*       'RWTG' - ADDRESS OF THE WHERE-TO-GO TABLE;                    *
*       'RPARC'- ADDRESS OF THE CURRENT TCLOSE PARAMETER LIST ENTRY;  *
*       'RWTGC'- ADDRESS OF THE CURRENT WHERE-TO-GO TABLE ENTRY;      *
*       'RTIOT'- ADDRESS OF THE CURRENT TIOT ENTRY FOR THE DCB;       *
*       'RUCB' - ADDRESS OF THE CURRENT UCB FOR THE DCB;              *
*       'RDEB' - ADDRESS OF THE DCB'S DEB;                            *
*    2. WORK AREA CONTENTS ARE AS FOLLOWS-                            *
*       'WTGPATHS+1' X'80' - THE OPTIONAL TRACE FACILTY IS ACTIVE     *
*                            AND SHOULD BE TERMINATED PRIOR TO EXIT;  *
*       'DCBOFLGS' - X'01', OFF - THE COMBINATION OF THESE TWO CONDI- *
*       'JFCBMASK+4' - X'02'    - TIONS INDICATES THAT A DELAYED      *
*                                 ABEND HAS BEEN REQUESTED, IN WHICH  *
*                                 CASE THE FOLLOWING ADDITIONAL       *
*                                 INPUT APPLIES-                      *
*       'DXRETCOD' - CONTAINS THE TCLOSE INTERNAL ERROR CODE;         *
*       'DXABCODE-1' - CONTAINS THE SYSTEM ABEND CODE.                *
*       'DXCCW1'   - X'FF' - INDICATES THAT PROCESSING WAS TERMINATED *
*                            ON THE CORRESPONDING DCB. DO NOT RESET   *
*                            ANY PROCESSING INDICATORS OTHER THAN     *
*                            'DCBOBUSY' IN FIELD 'DCBOFLGS' (INDI-    *
*                            CATES THAT THE DCB IS BEING PROCESSED    *
*                            BY AN I/O SUPPORT FUNCTION).             *
*                                                                     *
* OUTPUT -                                                            *
*    1. AT EXIT TO THE USER-                                          *
*       REGISTER CONTENTS WILL BE THE SAME AS WHEN THE TCLOSE    X02989
*       MACRO WAS INVOKED BY THE USER EXCEPT FOR REGISTER 15.    X02989
*         RETURN CODES IN REGISTER 15:                           X02989
*              0     ALL ENTRIES IN TCLOSE PARAMETER LIST        X02989
*                    HAVE COMPLETED TCLOSE SUCCESSFULLY.         X02989
*                                                                X02989
*              4     ONE OR MORE ENTRIES IN TCLOSE PARAMETER     X02989
*                    LIST HAVE FAILED TO COMPLETE TCLOSE         X02989
*                    SUCCESSFULLY.                               X02989
*       CONTROL BLOCK INDICATORS-                                     *
*          'DCBOFLGS' - X'C5', OFF - THE DCB RDBACK, EOF, OUTPUT, AND *
*                                    'BUSY' INDS WILL BE TURNED OFF;  *
*          'DEBOFLGS' - X'20', OFF - THE DEB 'EOF' IND IS TURNED OFF; *
*          IOB - ALL NECESSARY FLAGS ARE RESET TO CONTINUE PROCESSING.*
*                                                                     *
* EXTERNAL REFERENCES -                                               *
*    REFER TO THE PRECEDING MODULE PROLOG.                            *
*                                                                     *
* EXITS, NORMAL -                                                     *
*    RETURN TO THE USER UPON COMPLETION OF ALL TCLOSE PROCESSING.     *
*                                                                     *
* EXITS, ERROR -                                                      *
*    REFER TO THE PRECEDING MODULE PROLOG.                            *
*                                                                     *
* TABLES/WORK AREAS -                                                 *
*    THE OPEN, CLOSE, OR EOV WORK AREA AND THE WHERE-TO-GO (WTG)      *
*    TABLE ARE DESCRIBED BY THE DSECTS AT THE END OF THE LISTING.     *
*                                                                     *
* ATTRIBUTES -                                                        *
*    REFER TO THE PRECEDING MODULE PROLOG.                            *
*                                                                     *
* CHARACTER CODE DEPENDENCY -                                         *
*    REFER TO THE PRECEDING MODULE PROLOG.                            *
*                                                                     *
* NOTES -                                                             *
*                                                                     *
***********************************************************************
*
TCM50000 EQU   *
         IECRES SYNCHDCB                SYNCHRONIZE DCB PROCESSING     *
                                        ..OF ALL DCBS TO THIS POINT
*
***********************************************************************
*
*  CHECK IF ANY DCB WANTS TO BRANCH OUT OF THIS MODULE RATHER
*  THAN SYNCHDCB.  IF SO, BRANCH THE DCB OUT EXPLICITLY, SINCE
*  THE BRANCH WILL NOT BE DONE BY THE RESIDENT ROUTINE.  THIS
*  CHECK IS NEEDED ONLY AS LONG AS AN IECRES LOAD EXISTS IN THE
*  SAME LAST MODULE WHICH HAS AN IECRES SYNCHDCB.
*
         L     RD,WTGPREFX              GET PTR TO WTG PREFIX    Y02080
         USING IECPREFX,RD                                       Y02080
         L     RD,IECUPRML              GET DCB PARM LIST ADDR   Y02080
         DROP  RD                                                Y02080
         LA    RF,WTGENTRY              ADDR OF FIRST WTG ENTRY
         SR    R0,R0                    CLEAR XCTL OUT DCB INDICATOR
         USING WTGENTRY,RF              DEFINE BASE TO WTG ENTRY
*
TCM50100 EQU   *
         L     RC,WTGCORE-K1            LOAD MAIN WORK AREA ADDR
         LA    RC,0(RC)                 CLEAR HIGH ORDER BYTE
         LTR   RC,RC                    TEST IF DCB HAS WORK AREA
         BZ    TCM50300                 BR IF NO
*
         CLC   WTGMODID,WTGIDTTR        IS WTG ENTRY ID CURRENT ID
         BNE   TCM50200                 BR IF NO, IS XCTL OUT DCB
         LA    RET,K4                   SET RET CODE TO RE-SYNCH ENTRY
         ST    RET,DXREGE-FORCORE(,RC)  STORE RET IN DCB REG SAVE AREA
         B     TCM50300                 BR TO INCR TO NEXT DCB IN LOOP
*
TCM50200 EQU   *
         LR    R0,RD                    SAVE RPARC DCB PARM LIST ADDR
         LR    R1,RF                    SAVE RWTGC WTG ENTRY ADDR
*
TCM50300 EQU   *
         TM    PLISTOPT(RD),LASTNTRY    CHECK FOR LAST DCB ENTRY
         LA    RD,K4(,RD)               ADVANCE TO NEXT DCB ENTRY
         LA    RF,WTGEND-WTGENTRY(,RF)  ADVANCE TO NEXT WTG ENTRY
         BNO   TCM50100                 BR TO PROCESS NEXT DCB
*
TCM50400 EQU   *
         LTR   R0,R0                    DOES A DCB WANT TO XCTL OUT
         BZ    TCM50500                 BR IF NO
*
         LR    RPARC,R0                 LOAD CURRENT RPARC ADDR
         LR    RWTGC,R1                 LOAD CURRENT RWTGC ADDR
         DROP  RF                       DROP OLD BASE TO WTG ENTRY
         USING WTGENTRY,RWTGC           DEFINE BASE TO WTG ENTRY
         L     RDCB,PLISTDCB(,RPARC)    LOAD CURRENT DCB ADDR
         L     RCORE,WTGCORE-K1         LOAD CURRENT WORK AREA ADDR
         LM    RTIOT,RET,DXREG9         LOAD REGS 9-14
         LM    R0,R1,DXREG0             LOAD REGS 0-1
*
         IECRES LOAD,MODID=(RWTGC),PREFIX=WTGPREFX,BRANCH=DIRECT Y02080
         DROP  RWTGC
*
TCM50500 EQU   *
*
***********************************************************************
*
*****    TERMINATE THE TRACE FACILITY IF IT WAS ACTIVE
*
         SR    RD,RD                    SET INTERNAL SW TO IND THAT THE
*                                       ..TRACE FACILITY IS NOT ACTIVE
         TM    WTGPATHS+K1,WTGTRACE     IS THE TRACE FACILITY ACTIVE-
         BNO   TCM52500                 ..BR IF NOT TO RELEASE   S21940
*                                       ..THE RES RTN WORK AREA  S21940
         LA    RD,K1                    SET INTERNAL SW TO IND THAT THE
*                                       ..TRACE FACILITY WAS ACTIVE
         NI    WTGPATHS+K1,X'FF'-WTGTRACE INDICATE THE THE TRACE
*                                       ..FACILITY IS TO BE TERMINATED
*
***********************************************************************
*
*****    LOOP THROUGH THE LIST OF DCB'S AND DETERMINE WHICH
*****    DCB'S ARE HAVING A DEFERRED ABEND.
*
TCM52500 EQU   *
         L     RPAR,WTGPREFX            LOAD PREFIX POINTER      Y02080
         USING IECPREFX,RPAR            PREFIX ADDRESSABILITY    Y02080
         L     RPAR,IECUPRML            LOAD TCLOSE PARM LIST    Y02080
         DROP  RPAR                                              Y02080
         LR    RPARC,RPAR               PT TO 1ST PARM ENTRY
         LA    RWTGC,WTGENTRY           LOAD ADDR OF FIRST WTG ENTRY
         USING WTGENTRY,RWTGC           DEFINE BASE TO WTG ENTRY
         MVI   WTGLNG,K0                SET RETURN CODE TO ZERO  Y02080
TCM52700 EQU    *
         L     RCORE,K4(RWTGC)          LOAD ADDRESS OF TCLOSE WK AREA
         LA    RCORE,K0(,RCORE)         CLEAR HIGH-ORDER BYTE
         LTR   RCORE,RCORE              IS THERE A WORK AREA ADDRESS-
         BNZ   TCM52900                 BR IF YES                X02989
         MVI   WTGLNG,K4                SET RETURN CODE          Y02080
         B     TCM54100                 BR TO CHECK NEXT ENTRY   X02989
TCM52900 EQU   *                                                 X02989
         L     RDCB,PLISTDCB(,RPARC)    LOAD ADDRESS OF DCB
         TM    JFCBMASK+K4,JFCMABND     DELAYED ABEND PENDING    Y02144
         BO    TCM54100                 BR IF YES                Y02144
         TM    DXATOPEN,DXATIGN         IGNORED ABEND PENDING    Y02144
         BO    TCM54100                 BR IF YES                Y02144
         L     RDEB,DCBDEBAD            LOAD DEB ADDR            Y02082
         TM    DCBMACRF,DCBMEXCP        IS ACCESS METHOD EXCP    X02989
         BO    TCM53600                 BR IF YES                X02989
         TM    DCBDSORG+K1,ACBDORGA     IS CONTROL BLOCK AN ACB  X02989
         BNO   TCM53600                 BR IF NO                 X02989
         TM    ACBAMETH-IFGACB(RDCB),ACBVSAM IS ACB FOR VSAM     X02989
         BNO   TCM53600                 BR TO FREE WORK AREA     X02989
         CLI   ACBERFLG-IFGACB(RDCB),K0 IS ERROR FLAG ZERO       X02989
         BE    TCM53800                 BRANCH IF YES            XM1037
         MVI   WTGLNG,K4                SET RETURN CODE          Y02080
         B     TCM53800                 BR TO FREE WORK AREA     X02989
TCM53600 EQU   *                                                 X02989
         TM    DCBOFLGS,DCBOPEN+DCBOLOCK+DCBOBUSY  WAS DCB PROC  X02989
         BO    TCM53700                 BR IF YES                X02989
         MVI   WTGLNG,K4                SET RETURN CODE          Y02080
         B     TCM54100                 BR TO CHECK NEXT ENTRY   X02989
TCM53700 EQU   *                                                 X02989
         NI    DCBOFLGS,X'FF'-DCBOBUSY  TURN OFF 'DCB BUSY' IND  A35953
*                                                                A35953
         L     RUCB,DEBSUCBA            LOAD UCB ADDR          @ZA03180
         LA    RUCB,K0(RUCB)            CLEAR HIGH ORDER BYTE  @ZA03180
         LTR   RUCB,RUCB                DUMMY DATA SET?        @ZA03180
         BZ    TCM53750                 YES, BR-NOT DA DATA SET@ZA03180
         USING UCB,RUCB                                        @ZA03180
         CLI   UCBTBYT3,UCB3DACC        DIRECT ACCESS DATA SET?@ZA03180
         BNE   TCM53750                 NO, BR                 @ZA03180
         DROP  RUCB                                            @ZA03180
         TM    DXCCW,ALLBITS            WAS PROCESSING TERMIN-  SA48558
*                                       ..ATED ON THIS DCB-     SA48558
         BO    TCM53800                 ..BR IF YES- DON'T RESET A35953
*                                       ..PROCESSING INDICATORS  A35953
*                                                                A35953
*****    RESET DCB AND DEB PROCESSING STATUS INDICATORS          A35953
*                                                                A35953
TCM53750 EQU   *                                               @ZA03180
         NI    DCBOFLGS,X'FF'-DCBOWRIT-DCBORDBK-DCBOEOF          A35953
*                                       RESET OUTPUT, READBACK,  A35953
*                                       EOF INDICATORS IN DCB    A35953
         NI    DEBOFLGS,X'FF'-DEBOFEOF  RESET 'VOLUME FULL'      A35953
*                                       ..INDICATOR IN DEB       A35953
*
TCM53800 EQU   *                                                 A35953
         TM    DCBMACRF,DCBMEXCP        TEST IF EXCP             Y02082
         BO    TCM53850                 BRANCH IF YES            Y02082
         TM    DCBDSRG2,ACBDORGA        TEST IF AN ACB           Y02082
         BO    TCM53900                 DO NOT POINT THE DEB     Y02082X
                                        TO THE ACB               Y02082
TCM53850 EQU   *                        POINT DEB TO USER'S DCB  Y02082
         L     RDEB,DCBDEBAD            GET DEB ADDRESS          Y02082
         MVC   DEBDCBAD+K1(L'DEBDCBAD-K1),DXUDCBAD+K1 POINT DEB  Y02082*
                                        TO USER'S DCB            Y02082
         LR    R1,RDEB                  LOAD DEB ADDRESS         YM1272
         LA    RF,DEBBASIC-DEBXTNP      NEGATIVE OFFSET TO DEB   YM1272
*                                       EXTENSION ADDRESS        YM1272
         SR    R1,RF                    POINT R1 TO DEB PREFIX   YM1272
         L     RF,DEBXTNP-DEBXTNP(,R1)  LOAD DEB EXTENSION ADDR  YM1272
         NI    DEBXFLG1-DEBXTN(RF),X'FF'-DEBXCDCB  DEBDCBAD      YM1272
*                                       POINTS TO THE USER'S DCB YM1272
*                                                                Y02082
* COPY THE DCB FROM THE WORK AREA TO USER'S STORAGE              Y02082
*                                                                Y02082
TCM53900 EQU   *                        REFRESH THE USER'S DCB   Y02082
         IECRES INIT,DCBCOPY=FRWKAR,STM=(3,14,WTGPREFX)          Y02082
*                                                                Y02083
* IF A CHECKPOINT DATA SET SECURITY INTERFACE EXISTS, UPDATE     Y02083
* THE USER'S DCB FROM THE PROTECTED DCB.                         Y02083
* IF A SECURITY INTERFACE EXISTS, THERE ARE THREE DCBS: THE      YM3806
* USER'S IN USER'S KEY, THE COPIED DCB IN KEY 5, AND THE CHECK-  YM3806
* POINT DCB IN KEY ZERO.  AT THIS TIME THE TRUE USER'S DCB WILL  YM3806
* BE UPDATED WITH THE TRUE COPIED DCB WHIS IS IN KEY 5.          YM3806
*                                                                Y02083
         L     R1,DXDSAB                FETCH DSAB ADDRESS       Y02083
         USING DSAB,R1                  ADDRESS DSAB             Y02083
         TM    DSABFLG4,DSABCKSI        CHKPT SECURITY INTERFACE Y02083
         DROP  R1                       DSAB ADDRESSABILITY      Y02083
         BZ    TCM53950                 IF NOT, BRANCH           Y02083
         L     RDCB,DXUDCBAD            GET KEY ZERO DCB ADDR    Y02083
         LA    R1,K4                    BACKUP PROT. DCB ADDR BY Y02083
         SR    RDCB,R1                  4 TO GET USER DCB ADDR   Y02083
         MODESET EXTKEY=ZERO            GET INTO KEY ZERO        YM3806
         L     RF,K0(,RDCB)             GET USER'S DCB ADDR      Y02083
         L     RDCB,DXPDCBAD            PNT TO COPIED DCB        YM3806
         AH    RF,DXUDCBPL              PNT PAST USER DCB PREFX  Y02083
         AH    RDCB,DXUDCBPL            PNT PAST PROT. DCB PREFX Y02083
         LH    R1,DXUDCBML              GET DCB MOVE LENGTH      Y02083
         BCTR  R1,R0                    ADJUST FOR MOVE INSTR    Y02083
         L     RET,DXTCBADR             FETCH USER TCB ADDRESS   Y02083
         USING TCB,RET                  ADDRESS TCB              Y02083
         MODESET KEYADDR=TCBPKF,WORKREG=14 USER KEY              Y02083
         DROP  RET                      TCB ADDRESSABILITY       Y02083
         EX    R1,TCMMOVER              UPDATE USER'S DCB        Y02083
         MODESET EXTKEY=DATAMGT         BACK TO D/M KEY          Y02083
TCM53950 EQU   *                        CLEAR WTG TABLE ENTRY    Y02083
         XC    WTGENTRY,WTGENTRY        CLEAR WTG TABLE ENTRY
*
TCM54100 EQU   *
*
***********************************************************************
*
*  CHECK FOR END OF DCB WORK AREA FREEMAIN LOOP
*
         TM    PLISTDCB(RPARC),LASTNTRY IS THIS THE LAST TCLOSE ENTRY-
         LA    RPARC,K4(,RPARC)         PT TO NEXT TCLOSE ENTRY
         LA    RWTGC,WTGEND-WTGENTRY(,RWTGC)   PT TO NEXT WTG ENTRY
         BNO   TCM52700                 ..BR IF NOT TO CHECK NEXT ENTRY
*
***********************************************************************
*
*  SEE IF THERE IS A DCB LEFT THAT HAS A DEFERRED ABEND PENDING.
*
         LR    RPARC,RPAR               ADDR OF FIRST DCB PARM ENTRY
         LA    RWTGC,WTGENTRY-WTG(,RWTG)  ADDR OF FIRST WTG ENTRY
         L     RC,WTGPREFX              GET WTG PREFIX PTR       Y02144
         L     RC,IECRRPRM-IECPREFX(,RC) GET RRPLIST ADDRESS     Y02144
         USING RRPLIST,RC                                        Y02144
*
TCM54120 EQU   *
         L     RCORE,WTGCORE-K1         LOAD MAIN WORK AREA ADDR
         LA    RCORE,0(RCORE)           CLEAR HIGH ORDER BYTE
         LTR   RCORE,RCORE              TEST IF DCB HAS WORK AREA
         BZ    TCM54160                 BR IF NO, ADDR CLEARED   Y02144
         TM    JFCBMASK+4,JFCMABND      DELAYED ABEND DCB?       Y02144
         BO    TCM54200                 YES, GO ISSUE ABEND      Y02144
*                                       NO,MUST BE IGNORED ABEND Y02144
         CLC   RRMLRTRY,RRFWORK         PRIOR IGNORED ABEND DCB  Y02144
         BNE   TCM54160                 YES,GO CHK IF LAST ENTRY Y02144
         STM   RPARC,RWTGC,RRMLRTRY     SAVE CURR DCB ENTRY ADDR Y02144
*                                       AND CURR WTG ENTRY ADDR  Y02144
***********************************************************************
*
*  CHECK FOR END OF LOOP LOOKING FOR A DELAYED/IGNORED ABEND.
*
TCM54160 EQU   *
         TM    PLISTOPT(RPARC),LASTNTRY CHK FOR LAST DCB ENTRY
         LA    RPARC,K4(RPARC)          PT TO NEXT DCB ENTRY
         LA    RWTGC,WTGEND-WTGENTRY(RWTGC) PT TO NEXT WTG ENTRY
         BZ    TCM54120                 BR NO TO CHK FURTHER FOR Y02144
*                                       A DELAYED ABEND DCB      Y02144
         CLC   RRMLRTRY,RRFWORK         IGNORED ABEND PENDING    Y02144
         BE    TCM54500                 NO, BR TO FREE WTG TABLE Y02144
         LM    RPARC,RWTGC,RRMLRTRY     GET CURR DCB ENTRY ADDR  Y02144
*                                       AND CURR WTG ENTRY ADDR  Y02144
         LA    RCORE,TCM54500           GET MAINLINE RETRY ADDR  Y02144
         ST    RCORE,RRMLRTRY           SAVE MAINLINE RETRY ADDR Y02144
         XC    RRFWORK,RRFWORK          CLEAR RRPLIST WORK WORD  Y02144
         L     RCORE,WTGCORE-K1         LOAD MAIN WORK AREA ADDR Y02144
*
***********************************************************************
*
*  DO A DELAYED/IGNORED ABEND ON THIS DCB.  RESTORE REGISTERS FIRST.
*
TCM54200 EQU   *
         L     RDCB,DXUDCBAD            LOAD CURRENT DCB ADDR    YM5924
         LM    RTIOT,RET,DXREG9         RESTORE REGISTERS 9-14
         L     R0,DXREG0                RESTORE REGISTER 0
*
         SR    RF,RF                    LOAD PROBLEM DETERMINATION
         IC    RF,DXRETCOD              RETURN CODE IN REGISTER 15
*
         LH    R1,DXABCODE              LOAD THE ABEND CODE      Y02134
         SRL   R1,K4                    ALIGN RIGHT              Y02134
         ABEND (1),DUMP,,SYSTEM,        SYSTEM ABEND WITH A DUMP Y02080*
               DUMPOPT=SNAPLIST                                  Y02144
*
***********************************************************************
*
*  FREE MAIN STORAGE USED FOR WTG TABLE AND ALL OTHERS           Y02080
*
TCM54500 EQU   *
         SR    R9,R9                    ZERO REGISTER            Y02080
         IC    R9,WTGLNG                SAVE RETURN CODE         Y02080
         IECRES FREE,A=(RWTG),PREFIX=WTG  FREE WTG TABLE         Y02080
*
*****    RETURN TO CALLER
*
         LR    RF,R9                    LOAD RETURN CODE         Y02080
         IECRES EXIT                    RETURN                   Y02080
*                                                                Y02080
         SPACE 2
***********************************************************************
*              SUBROUTINES                                            *
***********************************************************************
*
*****    INITIALIZE CHANNEL PROGRAM TO WRITE A FILE MARK
*
TCD02900 EQU   *
         LA    RF,DXCCW4+K4             PLACE ADDRESS OF CCHHR
         ST    RF,DXCCW1                INTO SEARCH CCW
         LA    RF,DXCCW1                ADDRESS OF CCW1
         MVI   DXCCW1,CCWSCHID          SET UP SEARCH OPERATION CODE
         ST    RF,DXCCW2                ADDRESS OF CCW1 INTO TIC CCW
         MVC   DXCCW1+K4(K5),ACCW1CD    COMMAND CHAIN,5 CHARACTERS,TIC
         LA    RF,DXDSCB                PLACE ADDRESS OF THE FILE MARK
         ST    RF,DXCCW3                INTO THE WRITE DATA CCW
         MVI   DXCCW3,X'1D'             WRITE COUNT , KEY , DATA
         MVC   DXCCW3+K4(K4),ACCW3CC    8 CHARACTERS
         MVC   DXDSCB(K5),DXCCW4+K4     PLACE CCHHR INTO THE KEY
         XC    DXDSCB+K5(K3),DXDSCB+K5  ZERO OUT THE DATA PORTION
         BR    RD                       RETURN TO CALLER
         EJECT
*
*****    COMPLETE CONTROL BLOCKS AND WRITE FILE MARK
*
TCD03000 EQU   *
         MVC   DXDAADDR,DXCCW4+K1       PLACE FULL DISK ADDRESS INTO IO
         MVI   DXDAADDR,K0              SET M (EXTENT NUMBER) TO ZERO
         LA    RC,TABD131               LOAD INTERNAL ERROR CODE
         EXCP  DXIOB                    INITIATE I/O OPERATION
         IECRES WAIT                    INVOKE IECRES MACRO TO 'WAIT'
         TM    DXECB,ECBNOERR           TEST FOR SUCCESSFUL COMPLETION-
         BCR   1,RD                     ..BR IF YES- RETURN TO CALLER
TCD03100 EQU   *                        ERROR EXIT TO P.D.       Y02082
         DMABCOND (RC),ABEND2Z          ..ELSE EXIT TO THE PROBLEM     C
                                        ..DETERMINATION FUNCTION
         EJECT
***********************************************************************
*              CONSTANTS                                              *
***********************************************************************
*
TCMMOVER MVC   K0(K0,RF),K0(RDCB)       OBJECT OF EXECUTE INS    Y02083
CON10000 DC    F'65536'                 FULLWD CONSTANT OF X'00010000'
ACCW3CC  DC    X'20000008'              LENGTH EQ 8, SILI FLAG ON
ACCW1CD  DC    X'6000000508'            LENGTH EQ 5, CMD CHAIN AND SILI
*                                       ..FLAGS, AND 'TIC' OP CODE
SNAPLIST SNAP  SDATA=TRT,MF=L           READ ONLY SNAP LIST FOR  Y02144*
                                        ABEND OPTION DUMPOPT     Y02144
*
         SPACE 2
***********************************************************************
*        TABLE OF MODULE IDS AND ENTRY POINT ADDRESSES                *
***********************************************************************
*
XCTLTB2Z XCTLTABL ID=(ABEND2Z,0P),SVC=023,BRT=YES,LENGTH=        YM1272
         SPACE 2
         IECEQU AOS=YES,IEZDEB=YES                               YM1272
         IECDSECS CVT,                                                 C
               TIOT,                                                   C
               DSAB,                                             Y02083C
               DCB,                                                    C
               ACB,                                              Y02080X
               IEZDEB,                                           YM1272C
               UCB,                                                    C
               MAIN,                                                   C
               UNITTAB,                                                C
               WTG,                                                    C
               PREFX,                                            Y02080*
               TCB,                                                    C
               RRPL,                                             Y02144*
               EXPAND=YES
         SPACE 2
         END
