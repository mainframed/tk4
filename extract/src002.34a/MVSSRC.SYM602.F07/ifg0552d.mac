         TITLE 'IFG0552D EOV WRITE HEADER LABEL 1 - OUTPUT TAPE'
         COPY  LCGASMSW
IFG0552D CSECT
***********************************************************************
*                                                                     *
*                                                                     *
*          VS2 RELEASE 04 DELETIONS                                   *
*0000                                                          @ZA02850
*                                                                     *
*                                                                     *
***********************************************************************
*                                                                     *
* MODULE NAME = IFG0552D (OS/VS2)                                     *
*                                                                     *
* DESCRIPTIVE NAME = OUTPUT TAPE - WRITE HEADER LABEL 1               *
*                                                                     *
* COPYRIGHT = NONE                                                    *
*                                                                     *
* STATUS = RELEASE 2, LEVEL 0                                         *
*                                                                     *
* FUNCTION =                                                          *
*        1) WAIT FOR I/O TO COMPLETE INITIATED IN PREVIOUS MODULE.    *
*        2) IF AN I/O ERROR OCCURRED DURING THE REWRITING OF THE      *
*        VOLUME LABEL, CONTROL IS TRANSFERRED TO IFG0552N TO          *
*        ATTEMPT TO REWRITE THE LABEL AGAIN (BRANCH CODE = 0).        *
*        3) CONSTRUCT HEADER LABEL 1 FROM INFORMATION FOUND IN        *
*        THE JFCB AND UCB.                                            *
*        4) IF ASCII IS SPECIFIED, TRANSLATE THE LABEL FROM           *
*        EBCDIC TO ASCII.                                             *
*        5) WRITE THE HEADER 1 LABEL AND EXIT TO IFG0552F (BRANCH     *
*        CODE = 0) TO CHECK FOR I/O ERRORS.                           *
*                                                                     *
* NOTES = SEE BELOW                                                   *
*                                                                     *
*    DEPENDENCIES =                                                   *
*            CLASS ONE CHARACTER CODE.  THE EBCDIC CHARACTER CODE     *
*            WAS USED FOR ASSEMBLY.  THE MODULE MUST BE REASSEMBLED   *
*            IF A DIFFERENT CHARACTER SET IS USED FOR EXECUTION.      *
*                                                                     *
*    RESTRICTIONS = NONE                                              *
*                                                                     *
*    REGISTER CONVENTIONS =                                           *
*            R2 POINTS TO DCB                                         *
*            R4 POINTS TO OPEN WORK AREA                              *
*            R5 POINTS TO THE RESIDENT ROUTINE                        *
*            R6 POINTS TO THE WTG TABLE                               *
*            R7 POINTS TO THE CURRENT PARAMETER LIST ENTRY            *
*            R8 POINTS TO THE CURRENT WTG TABLE ENTRY                 *
*            R9 POINTS TO THE DD ENTRY IN THE TIOT                    *
*            R10 POINTS TO THE UCB                                    *
*                                                                     *
*    PATCH LABEL = SEE THIRD FROM LAST LABEL BEFORE ORG STATEMENT AT  *
*                  END OF LISTING.                                    *
*                                                                     *
* MODULE TYPE = CONTROL (OPEN, CLOSE, EOV DATA MANAGEMENT)            *
*                                                                     *
*    PROCESSOR = ASSEMBLER XF                                         *
*                                                                     *
*    MODULE SIZE = SEE EXTERNAL SYMBOL DICTIONARY OR LOC FIELD ON     *
*                  ORG STATEMENT AT END OF LISTING                    *
*                                                                     *
*    ATTRIBUTES = REENTRANT, REFRESHABLE,READ-ONLY, ENABLED,          *
*                 PRIVILEGED, SUPERVISOR STATE, DATA MANAGEMENT KEY,  *
*                 LINK PACK AREA RESIDENT/PAGEABLE                    *
*                                                                     *
* ENTRY POINT =                                                       *
*        IFG0552D                                                     *
*                                                                     *
*    PURPOSE = SEE FUNCTION                                           *
*                                                                     *
*    LINKAGE =                                                        *
*        THIS MODULE IS TRANSFERRED CONTROL THROUGH THE IECRES-LOAD   *
*        MACRO INSTRUCTION.                                           *
*                                                                     *
* INPUT =                                                             *
*        GIVEN CONTROL IN PROTECT KEY 5.                              *
*        REGISTER 2 POINTS TO THE COPIED DCB.                         *
*        DEBDCBAD POINTS TO THE COPIED DCB.                           *
*        REGISTER 4 POINTS TO THE EOV WORKAREA                        *
*                                                                     *
* OUTPUT =                                                            *
*        THE NEXT MODULE IS GIVEN CONTROL IN PROTECT KEY 5 WITH       *
*        REGISTER 2 POINTING TO THE COPIED DCB,                       *
*        DEBDCBAD POINTING TO THE COPIED DCB,                         *
*        AND REGISTER 4 POINTING TO THE EOV WORKAREA,                 *
*                                                                     *
* EXIT-NORMAL =                                                       *
*        IFG0552F - WAIT FOR HDR1 REWRITE, BRANCH CODE = 0            *
*        IFG0552N - I/O ERROR, BRANCH CODE = 0                        *
*                                                                     *
* EXIT-ERROR = NONE                                                   *
*                                                                     *
* EXTERNAL REFERENCES = SEE BELOW                                     *
*                                                                     *
*    ROUTINES =                                                       *
*        IFG019RA THROUGH THE IECRES MACRO.                           *
*                                                                     *
*    DATA AREAS =                                                     *
*        EOV WORKAREA.                                                *
*                                                                     *
*    CONTROL BLOCK =                                                  *
*        CVT                                                          *
*        DEB                                                          *
*        JFCB                                                         *
*        TCB                                                          *
*        UCB                                                          *
*                                                                     *
* TABLES =                                                            *
*                                                                     *
* MACROS =                                                            *
*        IECRES LOAD                                                  *
*        WAIT                                                         *
*        EXCP                                                         *
*        XLATE                                                        *
*                                                                     *
* CHANGE ACTIVITY =                                                   *
*        SEE CHANGES/DELETIONS SECTION JUST AFTER CSECT CARD.         *
*                                                                     *
***********************************************************************
         EJECT
*                   MASKS
         SPACE 1
EABD144  EQU   144                      I/O ERROR WRITING LBL  @ZA02850
         SPACE 1
*                   ADDRESSABILITY
         SPACE 1
         BALR  RBASE,0                  ESTABLISH BASE
         USING *,RBASE
         USING FORCORE,RCORE
         USING WTG,RWTG                 BASE OF WTG TABLE        Y02080
         USING DEB,RDEB
         USING IHADCB,RDCB
         USING SRT,RUCB
         SPACE 1
         IECRES WAIT                    WAIT FOR I/O             Y02134
         TM    DXECB,ECBNOERR           TEST FOR ERROR
         BO    ETO18900                 BR IF NO ERROR
         LA    R0,EABD144               637-04 INTERNAL CODE   @ZA02850
         LA    R7,MOD2N2D               SET R7 TO MOD ID
         B     ETO20300                 GO TO NEXT MOD
         EJECT
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
*                   CONSTRUCT HEADER LABEL 1
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
ETO18900 EQU   *                        HEADER 1 PROCESSING      Y02134
*                                                                Y02134
         MVC   FL1LABI(K4),AHDR19G      SET LABEL IDENTIFIER TO HDR
         L     R7,DXCCW4                GET ADR OF DSN SAVED
*                                       BY TRL LBL MODULE
ETO19600 MVC   FL1ID,K0(R7)             17 LEAST SIGNIFICANT NON-BLNK
         SPACE 1
*                   GENERATION NUMBER AND VERSION NUMBER OF GENERATION
         SPACE 1
         MVI   FL1GNO,BLANK             SET GENERATION NUMBER AND VER
         MVC   FL1GNO+K1(K44),FL1GNO    NUMBER OF GENERATION TO BLANKS
         CLI   JFCBELNM+K1,BLANK        GENERATION DATA SET
         BE    ETO19700                 NO,GO SET UP FILE SERIAL NUMBER
         L     R7,DXCCW4+K4             GET ADR SAVED BY TRL MOD
         MVC   FL1GNO,K3(R7)            RA NOW POINTS TO END OF DSN
         MVC   FL1VNG,K8(R7)            LESS GEN NO. ( GNNNNVNN )
         SPACE 1
*                   FILE SERIAL,VOLUME SEQUENCE AND FILE SEQUENCE
         SPACE 1
ETO19700 MVC   FL1FILSR,JFCBVOLS        FILE SER NUM SAME AS 1ST
*                                       VOL OF DATA SET
         LH    R7,DXVOLSEQ              PICK UP VOLUME SEQ NUM   Y02134
         CVD   R7,DXCCW2                CONVERT FROM BI TO PACKED DEC
         UNPK  FL1VOLSQ,DXCCW2          VOLUME SEQ NUMBER TO EBCDIC
         OI    FL1VOLSQ+K3,ZONEOF       REMOVE SIGN BITS
         LH    R7,UCBFSEQ               FILE SEQ NUMBER TO WORK REG
         CVD   R7,DXCCW2                CONVERT FROM BI TO PACKED DEC
         UNPK  FL1FILSQ,DXCCW2          FILE SEQ NUM IN EBCDIC TO LBL
         OI    FL1FILSQ+K3,ZONEOF       REMOVE SIGN BITS
         SPACE 1
*                   CREATION DATE
         SPACE 1
         SR    R7,R7                    CLEAR WORK REGISTER
         IC    R7,JFCBCRDT              YEAR OF CREATION IS 1 BYTE
         CVD   R7,DXCCW2                CONVERT YEAR TO DECIMAL
         MVI   FL1CREDT,BLANK           LABEL TO BE OF FORM  BYYDDD
         UNPK  FL1CREDT+K1(K2),DXCCW2   PLACE YEAR IN EBCDIC INTO LABEL
         OI    FL1CREDT+K2,ZONEOF       REMOVE SIGN BITS
         IC    R7,JFCBCRDT+K1           PICK UP THE TWO CHARACTERS OF
         SLA   R7,K8                    THE DAY OF CREATION
         IC    R7,JFCBCRDT+K2           FROM THE JFCB
         CVD   R7,DXCCW2                CONVERT DAY TO DECIMAL
         UNPK  FL1CREDT+K3(K3),DXCCW2   CREATION DAY IN EBCDIC TO LABEL
         OI    FL1CREDT+K5,ZONEOF       RESET SIGN BITS CREATED BY CONV
         SPACE 1
*                   EXPIRATION DATE
         SPACE 1
         SR    R7,R7                    CLEAR WORK REGISTER
         IC    R7,JFCBXPDT              PICK UP YR OF EXPIRATION IN BI
         CVD   R7,DXCCW2                CONVERT YEAR TO DECIMAL
         MVI   FL1EXPDT,BLANK           LABEL FIELD TO BE OF FORM  BYYD
         UNPK  FL1EXPDT+K1(K2),DXCCW2   PLACE YEAR IN EBCDIC INTO LABEL
         OI    FL1EXPDT+K2,ZONEOF       RESET SIGN BITS CREATED BY CONV
         LH    R7,JFCBXPDT+K1           PICK UP DAY OF EXPIRATION
         CVD   R7,DXCCW2                CONVERT DAY TO DECIMAL
         UNPK  FL1EXPDT+K3(K3),DXCCW2   EXPIRATION DAY IN EBCDIC TO LAB
         OI    FL1EXPDT+K5,ZONEOF       RESET SIGN BITS CREATED BY CONV
         SPACE 1
*                   BLOCK COUNT
         SPACE 1
         MVI   FL1BLKCT,ZONEOF          PLACE EBCDIC ZEROES INTO THE
         MVC   FL1BLKCT+K1(K5),FL1BLKCT  BLK COUNT FIELD OF THE LABEL
         SPACE 1
*                   DATA SET SECURITY
         SPACE 1
         MVI   FL1FSEC,ZONEOF           RESET SECURITY INDICATOR
         TM    JFCBIND2,JFCBSCTY        DOES JFCB SPECIFY SECURITY
         BC    12,ETO19900              NO,DO NOT SET INDICATOR
         MVI   FL1FSEC,CHAR1            YES,SET DATA SET SECURITY IND
         TM    JFCBIND2,JFCBRWPW        DOES JFCB SPEC RWPW OPTION
         BNO   ETO19900                 NO, DO NOT SET INDICATOR
         MVI   FL1FSEC,FL1WRSEC         IND SECTY IS FOR WR ONLY
         SPACE 1
*                   SYSTEM CODE AND RESERVED FIELDS
         SPACE 1
ETO19900 EQU   *                        INSERT SYSTEM CODE
         MVI   FL1RES,BLANK             PAD THE REST OF THE LABEL
         MVC   FL1RES1,FL1RES           WITH BLANKS - 7 CHARACTERS
*    TEST FOR ANSI LABEL SPECIFIED AND CHANGE LABELS IF REQUIRED
         TM    JFCBLTYP,JFCBAL          WERE ANSI LABEL SPEC.
         BZ    ETO20100                 NO, GO WRITE LABEL
         MVC   FL1SYSCD(K5),ANSCODE     MOVE IN ANSI SYS CODE
         TM    JFCBIND2,JFCBSCTY        DOES JFCB IND SECURITY
         BO    ETO20000                 YES, SECURITY SET ALREADY
         MVI   FL1FSEC,BLANK            NO, SET TO BLANK
ETO20000 XLATE DXLBL,K80,TO=A           TRANSLATE LABEL DATA
         B     ETO20200                 GO WRITE LABEL
ETO20100 EQU   *                        INSERT SYSTEM CODE
         MVC   FL1SYSCD,VSSYSC          INSERT SYSTEM CODE       XM1201
*                                       INTO LABEL               XM1201
ETO20200 EQU   *                        SET UP CHANNEL PROGRAM
         SPACE 1
*                   SET UP CHANNEL PROGRAM TO WRITE LABEL
         SPACE 1
*                                  PLACE ADDRESS OF DXLBL AREA IN
         ST    RCORE,DXCCW1             OBTAINED CORE INTO CCW
         MVI   DXCCW1,CCWWRTAP          DITTO FOR WRITE OPERATION CODE
         MVC   DXCCW1+K4(K4),AWRCCW9G   SILI FLAG ON, 80 CHARACTERS
         SPACE 1
*                   WRITE HEADER LABEL 1
         SPACE 1
         EXCP  DXIOB                    WRITE HDR1 NXT MOD CHECKS
*                                       FOR I/O ERROR
         SPACE 1
*                   EXIT
         LA    R7,MOD2F2D               SET R7 TO TTR
ETO20300 SR    RET,RET                  ZERO RETURN REG
         LA    RWTGC,DXXENTRY           SET R8 FOR RES RTN
         LA    RWTG,DXXAREA             POINT TO EOV WTG
         IECRES LOAD,MODID=(R7),BRANCH=QUEUED GO TO NEXT MODULE  Y02080
*
         SPACE
*                   CONSTANTS
         SPACE 1
AWRCCW9G DC    X'20000050'              CCW SILI, 80 CHAR LNGTH
AHDR19G  DC    C'HDR1'                  CHAR FOR HDR1
*
ANSCODE  DC    C'OS360'                 ANSI SYSTEM CODE
VSSYSC   DC    C'IBM OS/VS 370'         VS SYSTEM CODE           XM1201
         SPACE
XCTLTA2D XCTLTABL ID=(MOD2F2D,2F,MOD2N2D,2N),SVC=055,            Y02080X
               BRT=YES,LENGTH=                                   Y02080
         IECDSECS CVT,TCB,DCB,UCB,DEB,MAIN,TIOT,WTG,PREFX,       Y02080*
               EXPAND=YES                                        Y02080
         IECEQU
         END
