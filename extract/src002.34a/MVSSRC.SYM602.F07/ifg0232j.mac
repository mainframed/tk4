         TITLE 'IFG0232J - TCLOSE OUTPUT UTLS PROCESSING MODULE'
IFG0232J CSECT                                                   YM3009
***********************************************************************
*                                                                     *
*                                                                     *
*           VS2 RELEASE 02 DELETIONS/CHANGES                          *
*0000306000,528000,772500,840000-842000                          YM3009
*           VS2 RELEASE 03 DELETIONS/CHANGES                          *
*                                                                     *
* MODULE NAME - IFG0232J                                              *
*                                                                     *
* DESCRIPTIVE NAME - TCLOSE OUTPUT USER TRAILER LABELS PROCESSING     *
*                                                                     *
* COPYRIGHT - NONE                                                    *
*                                                                     *
* CHANGE ACTIVITY - SEE DELETIONS/CHANGES FOLLOWING THE CSECT CARD.   *
*                                                                     *
* STATUS CHANGE LEVEL 000                                             *
*                                                                     *
* FUNCTION -                                                          *
*    THIS MODULE CONTAINS THE TCLOSE OUTPUT USER TRAILER LABELS       *
*    PROCESSING FUNCTION.                                             *
*    REFER TO THE FOLLOWING 'FUNCTION PROLOG(S) FOR DETAILS.          *
*                                                                     *
* ENTRY POINTS -                                                      *
*         IFG0232J                                                    *
*                                                                     *
* INPUT -                                                             *
*    ENTERED IN DATA MANAGEMENT KEY                              Y02082
*    REFER TO THE FOLLOWING 'FUNCTION PROLOG(S)'.                     *
*                                                                     *
* OUTPUT -                                                            *
*    EXIT IN DATA MANAGEMENT KEY                                 Y02082
*    REFER TO THE FOLLOWING 'FUNCTION PROLOG(S)'.                     *
*                                                                     *
* EXTERNAL REFERENCES -                                               *
*         IFG019RA - OPEN/CLOSE/EOV/DADSM COMMON FUNCTION ROUTINE     *
*         TO GET AND FREE CORE, TO BRANCH, AND TO WAIT.               *
*         REFER TO THE FOLLOWING 'FUNCTION PROLOG(S)'.                *
*                                                                     *
* EXITS, NORMAL -                                                     *
*    REFER TO THE FOLLOWING 'FUNCTION PROLOG(S)'.                     *
*                                                                     *
* EXITS, ERROR                                                        *
*    REFER TO THE FOLLOWING 'FUNCTION PROLOG(S)'.                     *
*                                                                     *
* TABLE/WORK AREA -                                                   *
*    REFER TO THE FOLLOWING 'FUNCTION PROLOG(S)'.                     *
*                                                                     *
* ATTRIBUTES -                                                        *
*    REENTRANT, REFRESHABLE, READ-ONLY, ENABLED, PRIVILEGED           *
*                                                                     *
* CHARACTER CODE DEPENDENCY -                                         *
*    CLASS ONE CHARACTER CODE DEPENDENCY - THE EBCDIC CHARACTER CODE  *
*    WAS USED FOR ASSEMBLY.  THE MODULE MUST BE REASSEMBLED IF A      *
*    DIFFERENT CHARACTER SET IS USED FOR EXECTION.                    *
*                                                                     *
* NOTES -                                                             *
*    REFER TO THE FOLLOWING 'FUNCTION PROLOG(S)'.                     *
*                                                                     *
***********************************************************************
          EJECT
***********************************************************************
*                                                                     *
*                          FUNCTION PROLOG                            *
*                                                                     *
***********************************************************************
*                                                                     *
*FUNCTION/OPERATION -
*   THIS MODULE IS USED BY I/O SUPPORT ROUTINES (TCLOSE) TO
*   WRITE UP TO EIGHT USER TRAILER LABELS AS REQUIRED FOR DATA SETS
*   ON DIRECT ACCESS VOLUMES.  IT IS ENTERED ONCE FOR EACH DCB TO
*   BE PROCESSED.
*   A 152 BYTE USER LABEL WORK AREA IS OBTAINED FOR TCLOSE       Y02082
*   FROM FETCH PROTECTED SUBPOOL 229 IN DATA MANAGEMENT KEY.     Y02082
*   A 96 BYTE USER LABEL WORK AREA IS OBTAINED FOR THE USER      Y02082
*   FROM FETCH PROTECTED SUBPOOL 229 IN USER KEY.                Y02082
*   IF A TRACK WAS ALLOCATED FOR USER LABELS, CONTROL IS GIVEN TO THE
*   USER'S LABEL CONSTRUCTION ROUTINE VIA SYNCHRONOUS EXIT (SVC 12).
*   ON RETURN FROM THE USER'S ROUTINE, IF A LABEL WAS CONSTRUCTED, IT
*   IS WRITTEN FOLLOWED BY 2 FILEMARKS FOR HEADERS (UHL(N+1),UTL0) OR
*   ONE FILEMARK FOR TRAILERS (UTL(N+1).  DEPENDING ON THE RETURN CODE
*   PASSED BY THE USER'S ROUTINE, ANOTHER LABEL IS PROCESSED OR CONTROL
*   IS GIVEN BACK TO THE I/O SUPPORT FUNCTION WHICH CALLED THIS MODULE.
*
*ENTRY POINTS -
*   IFG0232J -INITIAL ENTRY TO THIS MODULE IS BY MEANS OF BRANCH FROM
*             IFG0232D.
*
*INPUT -
*   REGISTERS 2-11 - CONTENTS AT TIME OF BRANCH FROM PREVIOUS MODULE
*             12   - EXIT LIST ENTRY FOR EXIT TO BE TAKEN
*                    BITS 3-7 EXIT TYPE CODE
*                         8-31 EXIT ADDRESS, ZEROES IF NO ACTIVE EXIT
*
*   JFCBMASK+5 (IN I/O SUPPORT WORK AREA) BITS 5-6 INDICATE FUNCTION
*              REQUIRING USER LABEL PROCESSING
*
*
*OUTPUT -
*   USER LABELS ARE WRITTEN AS REQUESTED
*   ON FINAL EXIT REGISTERS 2-11 HAVE CONTENTS AS AT ENTRY TO THIS
*   MODULE.
*
*EXTERNAL ROUTINES -
*   USER'S OUTPUT LABEL EXIT ROUTINES - ENTERED FROM IFG019RA    Y02082
*   THROUGH THE IECRES-UEXIT MACRO WITH THE FOLLOWING INPUT.     Y02082
*
*                    REG 0    CONTAINS NO MEANINGFUL INFO
*                        1    POINTER TO 4 WORD PARAMETER LIST WHICH
*                             CONTAINS -
*                               POINTER TO LABEL BUFFER
*                               POINTER TO DCB
*                               FLAG BYTE, PTR TO STATUS INFO (IF
*                                          PERMANENT ERROR) OR ZERO
*                               POINTER TO IMAGE AREA (IF TOTALING WAS
*                               REQUESTED) OR ZERO
*                        2-13 CONTENTS OF USER'S REGS BEFORE I/O SUPP
*                        14   RETURN ADDRESS
*                        15   EXIT ADDRESS
*
*      THE LOCK BIT (BIT 6) IN DCBOFLGS FIELD IS SET TO 1 TO     Y02082
*      PREVENT ANOTHER I/O SUPPORT FUNCTION FROM OPERATING ON THIS
*      DCB WHILE THE USER IS IN CONTROL.  ON RETURN FROM THE USER'S
*      ROUTINE, THIS BIT IS RESET TO 0.
*
*EXITS-NORMAL
*   EXIT IS MADE TO THE USER'S OUTPUT HEADER OR OUTPUT TRAILER
*   ROUTINES AS REQUESTED BY USER (MAXIMUM NUMBER OF LABELS IS EIGHT).
*
*   FINAL EXIT IS VIA THE RESIDENT ROUTINE TO THE TCLOSE FINAL        *
*   MODULE, IFG0232Z TO CONTINUE TCLOSE I/O PR0CESSING.  REGISTER 14  *
*   MUST CONTAIN A ZERO UPON ENTRY INTO THAT MODULE.                  *
*
*
*EXITS-ERROR
*   NO USER LABEL EXTENT EXISTS - THE APPROPRIATE EXIT ROUTINE IS
*   ENTERED WITH THE BUFFER POINTER SET TO ZERO.  THE OTHER INPUT
*   IS AS FOR A NORMAL ENTRY.
*
*   I/O ERROR WRITING A USER LABEL - THE USER'S ROUTINE IS ENTERED
*   AGAIN WITH BIT 0 OF FLAG BYTE SET TO ONE AND THE THREE LOW ORDER
*   BYTES CONTAINING A POINTER TO THE STATUS INFORMATION IN THE IOB.
*   THE OTHER INPUT IS AS FOR A NORMAL ENTRY.
*
*TABLES/WORKAREAS -
*   FORMAT AND CONTENTS OF THE WORK AREAS USED ARE DESCRIBED BY THE
*   DSECTS AT THE END OF THIS LISTING.
*
*ATTRIBUTES -
*   REENTRANT, ENABLED, PRIVILEGED
*
*
         SPACE 1
         BALR  RBASE,0                  ESTABLISH ADDRESSABILTIY
         USING *,RBASE
         USING FORCORE,RCORE
         USING WTG,RWTG                                          Y02080
         USING IHADEB,RDEB
         USING IHADCB,RDCB
         USING UNITABLE,R1
         USING TIOENTRY,RTIOT
         USING ULDMWK,RWTGC             TCLOSE UL WORK AREA BASE Y02082
         USING TCB,RA                                            Y02082
         SPACE 1
         ST    RWTGC,DXCCW11            SAVE CONTENTS OF REG
*                                                                Y02082
*    OBTAIN CORE FOR USER LABEL WORK AREA FOR TCLOSE             Y02082
*                                                                Y02082
         IECRES GET,LV=USERLDM,PREFIX=YES,SP=K229,               Y02082*
               STM=(2,14,WTGPREFX),ID=ULWA                       Y02144
*
* GET CORE FOR WORK AREA, LABEL BUFFER AND PARAMETER LIST        Y02080
*
         STM   RPAR,RC,K0(R1)           SAVE REGS 5-12 FOR CALLER
         LR    RWTGC,R1                 SET BASE FOR ULWK DSECT
         IECRES GET,LV=USERLU,PREFIX=NO,SP=K229,KEY=USER,        Y02082*
               STM=(2,14,WTGPREFX)                               Y02082
         MODESET EXTKEY=DATAMGT         DATA MANAGEMENT KEY      Y02082
         USING ULUWK,R9                 USER'S UL WORK BASE      Y02082
         LR    R9,R1                    ADDR OF GOTTEN CORE      Y02082
         NI    K28(RWTGC),K255-UCBMOUNT TURN OFF LAST ENTRY BIT  Y02080
         MVC   DXCCW12(K8),DXCCW7       SAVE DSCB ADDR FOR OPEN
         XC    DXCCW1(K80),DXCCW1       CLEAR CHAN PROG AREA
*
*        DETERMINE IF A USER LABEL EXTENT HAS BEEN ALLOCATED FOR THIS
*        DATA SET.
*
         CLI   DSCEXTYP,HEX40           IS 1ST EXTENT FOR USER LABELS
         BNE   TCD03500                 NO EXTENT, GO TO USER
*
*        EXTENT EXISTS - PROCESS USER LABELS
*
*
         XC    DXDAADDR(K8),DXDAADDR    ZERO IOBOADAD IN IOS' IOB
         MVC   DXDAADDR+K3(K4),DSCLOWLM  CCHH FROM START OF 1ST EXTENT
*
*
*        PROCESS USER TRAILER LABELS.
*
*        SET UP CHANNEL PROGRAM TO FIND
*        STRUCTURE COUNT AND KEY FIELDS IN WORK AREA.  SET IOBDADAD
*        IN I/O SUPPORT'S IOB.
*
         LA    R1,ULWK8
         ST    R1,DXCCW1                READ COUNT INTO WK1
         LA    R1,ULWK10
         ST    R1,DXCCW2                SEARCH ON KEY IN WK3
         LA    R1,DXCCW1
         ST    R1,DXCCW3                TIC TO CCW1
         OC    DXCCW1(K32),ULCP13       OR CP SKELETON OVER ADDRESSES
         MVC   ULWK10(K4),UTLCONS3      KEY=UTLO FOR SEARCH KEY EQUAL
*
         BAL   RD,TCD04300              EXECUTE CP TO FIND UTL0
         BZ    TCD03300                 ERROR, BR TO SET UP EXIT TO USR
         SPACE
*                      SET UP  CP TO WRITE USER TRAILER
*                      AS EACH LABEL (UTLN) IS WRITTEN, A FILE
*                      MARK (UTL(N+1)) WILL BE WRITTEN FOLLOWING
*
         XC    DXCCW1(K32),DXCCW1       CLEAR CHNL PGM 1 OUT
         BAL   RPARC,TCD03700           GO SET UP CP
         OC    DXCCW1(K32),ULCP23       OR CP SKELETON OVER ADDRESSES
         MVC   DXCCW5(K24),DXCCW1       APPEND CP FOR WRITE CHECKING
         LA    R1,DXCCW5
         ST    R1,DXCCW6                TIC BACK TO CCW5
         MVI   DXCCW6,CCWTIC            RESTORE TIC OP
         MVI   DXCCW7,X'1E'             MAKE OP READ
         OI    DXCCW7+K4,X'10'          SET SKIP BIT ON
         NI    DXCCW7+K4,X'BF'          TURN OFF CC FLAG IN LAST CCW
         MVC   ULWK5(K12),ULWK8         SET UP COUNT, KEY FOR FILE MARK
         SR    R1,R1                    CLEAR RF
         STC   R1,ULWK6+K3              DATA LENGTH=0 FOR FILE MRK
*                                       IF DISP=MOD,UTL0 MAY HAVE
*                                       LENGTH OF 80 BYTES
TCD03200 EQU   *
         IC    R1,ULWK9                 R OF COUNT FOR UTL TO BE WRITTN
         BCTR  R1,K0                    R MINUS 1
         STC   R1,DXDAADDR+K7           R IN IOBDADAD
         LA    R1,K2(K0,R1)             R-1+2=R+1
         STC   R1,ULWK6                 R IN COUNT FIELD OF UTL(N+1)
*
         IC    R1,ULWK10+K3             N FROM LABEL TO BE WRITTEN
         LA    R1,K1(K0,R1)             N+1
         STC   R1,ULWK7+K3              KEY FOR UTL(N+1)
*
         MVI   ULWK9+K3,X'50'           DATA LENGTH = 80 FOR UTLN
*
         CLI   ULWK10+K3,X'F8'          CHECK IF 8 TRAILERS WERE WRITTN
         BNL   TCD03600                 YES, BRANCH TO SET UP RETURN
*
         MVC   ULDMBUFR(K4),ULWK7       PRIME BUFFER WITH UTLN   Y02082
         MODESET EXTKEY=ZERO            ASSUME KEY ZERO          Y02082
         L     RF,WTGPREFX              POINT TO PREFIX          Y02082
         STM   R0,RET,IECREGSV-IECPREFX(RF) SAVE REGS THRU LOCK  Y02082
*    GET LOCAL LOCK                                              Y02082
         SETLOCK OBTAIN,TYPE=LOCAL,MODE=UNCOND,                  Y02082*
               RELATED=('PREVENT FREE USER CORE-RELEASED BELOW') Y02082
*                                                                Y02082
         L     RF,WTGPREFX              POINT TO PREFIX          Y02082
         LM    R0,RET,IECREGSV-IECPREFX(RF)  RESTORE REGISTERS   Y02082
         MODESET KEYADDR=DXUKEY,WORKREG=15 ASSUME USER KEY       Y02082
*    VERIFY USER'S USER LABEL WORK AREA STILL BELONGS TO USER    Y02082
         OC    ULUWK(USERLU),ULUWK      PGM CHK IF NOT USER CORE Y02082
*                                                                Y02082
         MODESET EXTKEY=ZERO            ASSUME KEY ZERO          Y02082
         MVC   ULBUFR(K4),ULDMBUFR      COPY 'UTLN' TO USER      Y02082
         L     RF,WTGPREFX              POINT TO PREFIX          Y02082
         STM   R0,RET,IECREGSV-IECPREFX(RF) SAVE REGS THRU LOCK  Y02082
*    RELEASE LOCAL LOCK                                          Y02082
         SETLOCK RELEASE,TYPE=LOCAL,RELATED=('SEE ABOVE')        Y02082
*                                                                Y02082
         L     RF,WTGPREFX              POINT TO PREFIX          Y02082
         LM    R0,RET,IECREGSV-IECPREFX(RF)  RESTORE REGISTERS   Y02082
         MODESET KEYADDR=DXUKEY,WORKREG=15 ASSUME USER KEY       Y02082
*
*        GO TO USER'S ROUTINE TO CONSTRUCT LABEL.
*
         BAL   RPARC,TCD03800           GO EXIT TO USER'S ROUTINE
* RETURN IN DATAMGT KEY                                          Y02082
         MVC   ULDMBUFR(K4),ULWK7       REINSERT UTLN            Y02082
         LTR   RF,RF                    CHECK FOR RETURN CODE=0
         BZ    TCD03600                 CODE IS ZERO, BRANCH TO RETURN
         BAL   RD,TCD04300              GO WRITE LABEL
*
         BZ    TCD03300                 YES, BR TO TAKE SPECIAL EXIT
         CLI   ULWK1+K3,DCBOEOF         CHECK RET CODE FOR MORE LABELS
         BE    TCD03600                 NO, PREVIOUS LABEL WAS LAST
         CLI   ULWK1+K3,X'08'           CHECK,BR IF RETURN CODE
         BNE   TCD03600                 WAS NOT VALID
*
         MVC   ULWK8(K12),ULWK5         UTL(N+1) BECOMES C,K OF NXT LBL
         B     TCD03200                 BR TO PROCESS NEXT LABEL
TCD03300 EQU   *
*
*        IF AN I/O ERROR OCCURS WRITING USER TRAILER LABELS, A SPECIAL
*        EXIT IS TAKEN TO THE USER'S ROUTINE.  INPUT TO THE USER'S
*        ROUTINE IS THE SAME AS FOR OTHER EXITS WITH ONE EXCEPTION-
*        IN THE THIRD WORD OF THE PARAMETER LIST, BIT 0 IS SET TO ONE
*        AND THE THREE LOW ORDER BYTES CONTAIN A POINTER TO STATUS
*        INFO (PTR TO IOB).
*
         LA    R1,DXIOB                 INSERT POINTER TO STATUS INFO
         MODESET KEYADDR=DXUKEY,WORKREG=15 ASSUME USER KEY       Y02082
         ST    R1,ULPARM+K8             IN PARAMETER LIST
         OI    ULPARM+K8,ERROR          FLAG ERROR
         BAL   RPARC,TCD03900           GO TO SYNCH TO USER
*    RETURN HERE IN DATA MANAGEMENT KEY                          Y02082
*
         B     TCD03600
*
*
*        NO USER LABEL TRACK WAS ALLOCATED.  EXIT TO USER'S ROUTINE
*        WITH POINTER TO LABEL BUFFER ZEROED.
*
TCD03500 EQU   *
         MODESET KEYADDR=DXUKEY,WORKREG=15 ASSUME USER KEY       Y02082
         XC    ULPARM+K8(K4),ULPARM+K8  CLEAR ANY ERROR INDICTNS
         SR    R1,R1                    BUFFER POINTER TO ZERO
         BAL   RPARC,TCD04000           GO SYNCH TO USER         Y02082
*    RETURN HERE IN DATA MANAGEMENT KEY                          Y02082
*
TCD03600 EQU   *
         OI    K31(RWTG),UCBLDPT        SET UL-RETURN INDICATION
*                                       USED BY TCLOSE D. A. STRING
*
*    FREE USER'S USER LABEL WORK AREA                            Y02082
         IECRES FREE,A=(R9),PREFIX=NO,SP=K229,LV=USERLU,         Y02082*
               KEY=USER,STM=(2,14,WTGPREFX)                      Y02082
         MODESET EXTKEY=DATAMGT         DATA MANAGEMENT KEY      Y02082
*        FREE  WORKAREA AND LABEL BUFFER AND XCTL TO I/O SUPPORT
*        MODULE WHICH CALLED THIS MODULE.
         MVC   DXCCW7(K8),DXCCW12       RESTORE DSCB ADDR FOR OPEN
*
         LR    R1,RWTGC                 START OF AREA TO BE FREED
         LM    RPAR,RDEB,K0(R1)         RESTORE CALLING MODULE'S REGS
         IECRES FREE,A=(1),PREFIX=YES,STM=(0,14,WTGPREFX)        Y02080
*
*
*        LOAD RETURN MODULE OF THE TCLOSE D. A. STRING
*
         SR    RET,RET                  SET RETURN CODE
         LA    RF,LFG0232Z              FINAL MODULE
         L     RWTGC,DXCCW11            RESTORE RES ROUTINE REG
         IECRES LOAD,MODID=(RF),BRCODE=(RET),BRANCH=QUEUED       Y02080
*
TCD03700 EQU   *
         LA    R1,DXDAADDR+K3
         ST    R1,DXCCW1                SEARCH ID FROM IOBDADAD+3
         LA    R1,DXCCW1
         ST    R1,DXCCW2                TIC TO CCW1
         LA    R1,ULWK8
         ST    R1,DXCCW3                WRITE C,K FROM ULWK1
         LA    R1,ULWK5
         ST    R1,DXCCW4                WRITE LABEL FROM BUFFER
         BR    RPARC                    UNCONDITIONAL BRANCH
*
         EJECT
*
*        CLOSED SUBROUTINE TO SET UP INPUT, AND ISSUE THE        Y02082
*        IECRES-UEXIT MACRO TO GIVE CONTROL TO THE RESIDENT      Y02082
*        ROUTINE WHICH WILL TAKE THE USER EXIT.                  Y02082
*        REGISTER CONTENTS AT ENTRY TO THE EXIT ARE AS FOLLOWS-  Y02082
*                        R0 - CONTAINS NO MEANINGFUL INFO
*                        R1 - PTR TO PARAMETER LIST
*                    R2-R13 - USER'S CONTENTS BEFORE I/O SUPPORT
*                       R14 - RETURN ADDRESS (SET BY SYNCH)
*                       R15 - EXIT ADDRESS
*
TCD03800 EQU   *
         XC    ULPARM+K8(K4),ULPARM+K8  ZERO ERROR INDICATIONS
TCD03900 EQU   *
         LA    R1,ULBUFR                PTR TO LABEL BUFFER
TCD04000 EQU   *
         ST    R1,ULPARM                PTR TO LABEL BUFFER      Y02082
         L     R1,DXUDCBAD              GET USER'S DCB ADDR      Y02082
         ST    R1,ULPARM+K4             PUT IN PARM LIST         Y02082
         XC    ULPARM+K12(K4),ULPARM+K12  ZERO TOTALING AREA
         TM    DCBDSORG,DCBORGDA        CK FOR DIRECT ORGANIZATION
         BO    TCD04100                 BR IF DIRECT
         TM    DCBOFLGS,DCBOPEN         CK IF DCB ALREADY OPEN
         BZ    TCD04100                 IF YES, CTL FROM OPEN, BYPASS
*                                       TOTALING
         TM    DCBMACRF,DCBMEXCP        CK IF EXCP
         BO    TCD04100                 BR IF EXCP,SKIP TOTALING
         TM    DCBOPTCD,DCBOPTT         TEST,BR IF USER TOTALING
         BZ    TCD04100                 HAS NOT BEEN REQUESTED
*
         L     RDEB,DCBDEBAD            GET ADDRESS OF USER'S DEB
         SR    R1,R1                    CLEAR WORK REG
         IC    R1,DEBNMEXT-IHADEB(RDEB)  GET NUMBER OF EXTENTS
         SLL   R1,K4                    MULTIPLY BY 16
         L     RDEB,K36(R1,RDEB)        GET ADDR TOTALING WORKAREA-
*                                       DEB ADDR+32+4+LENGTH EXTENTS
         USING TOTSAVWA,RDEB            SET BASE - TOTALING AREA
         MVC   ULTOTPTR,TOTEOVPT        CURRENT IMAGE AREA ADDR TO
*                                       USR LBL PARM LIST
         DROP  RDEB                     DROP TOTALING AREA BASE
*
TCD04100 EQU   *
         MODESET EXTKEY=DATAMGT         DCB COPY KEY             Y02082
         NI    DCBOFLGS,ALLBITS-DCBOLOCK  SET LOCK BIT = 0       YM3009
*                                                                Y02082
* COPY THE DCB FROM THE WORK AREA TO USER'S STORAGE              Y02082
*                                                                Y02082
         IECRES INIT,DCBCOPY=FRWKAR,STM=(0,14,WTGPREFX)          Y02082
*                                                                Y02082
         LA    R1,ULPARM                POINT TO PARAMETER LIST
*                                                                YM3009
* SYNCH TO THE USER VIA THE IECRES-UEXIT MACRO.                  YM3009
*                                                                YM3009
         IECRES UEXIT,EXITAD=K28(RWTGC),STM=(2,13,WTGPREFX)      Y02082
         ST    RF,ULWK1                 SAVE USER'S RETURN CODE  YM3009
         L     RDCB,DXUDCBAD            GET PTR TO USER'S DCB    Y02082
         MODESET KEYADDR=DXUKEY,WORKREG=1 ASSUME USER'S KEY      Y02082
         OI    DCBOFLGS,DCBOLOCK        SET LOCK BIT = 1         Y02082
         IC    R1,DCBOFLGS              GET USER'S DCBOFLGS      Y02082
         MODESET EXTKEY=DATAMGT         RESUME DATA MGT KEY      Y02082
         L     RDCB,DXPDCBAD            POINT TO COPIED DCB      Y02082
         LA    R0,DCBOBUSY              SET MASK FOR BUSY BIT    Y02082
         NR    R1,R0                    SELECT THE BUSY BIT      Y02082
         IC    R0,DCBOFLGS              COMBINE WITH DCBOFLGS    Y02082
         OR    R1,R0                    ..FROM COPIED DCB.       Y02082
         STC   R1,DCBOFLGS              UPDATE DCBOFLGS IN COPY  Y02082
         OI    DCBOFLGS,DCBOLOCK        SET LOCK BIT = 1
         MODESET EXTKEY=ZERO            SUPERVISOR KEY           Y02082
         L     RF,WTGPREFX              POINT TO PREFIX          Y02082
         STM   R0,RET,IECREGSV-IECPREFX(RF) SAVE REGS THRU LOCK  Y02082
*    GET LOCAL LOCK                                              Y02082
         SETLOCK OBTAIN,TYPE=LOCAL,MODE=UNCOND,                  Y02082*
               RELATED=('PREVENT FREE USER CORE-RELEASED BELOW') Y02082
*                                                                Y02082
         L     RF,WTGPREFX              POINT TO PREFIX          Y02082
         LM    R0,RET,IECREGSV-IECPREFX(RF) RESTORE REGS         Y02082
         MODESET KEYADDR=DXUKEY,WORKREG=15 ASSUME USER KEY       Y02082
*                                                                Y02082
*    VERIFY USER'S USER LABEL WORK AREA STILL HELD BY USER       Y02082
*                                                                Y02082
         OC    ULUWK(USERLU),ULUWK      PGM CHK IF NOT           Y02082
         MODESET EXTKEY=ZERO            SUPERVISOR KEY           Y02082
         MVC   ULBUFR(K4),ULWK10        REINSERT UTLN/UHLN       Y02082
         MVC   ULDMBUFR,ULBUFR          COPY LABEL FROM USER     Y02082
         L     RF,WTGPREFX              POINT TO PREFIX          Y02082
         STM   R0,RET,IECREGSV-IECPREFX(RF)                      Y02082
*    RELEASE LOCAL LOCK                                          Y02082
         SETLOCK RELEASE,TYPE=LOCAL,RELATED=('SEE ABOVE')        Y02082
*                                                                Y02082
         L     RF,WTGPREFX              POINT TO PREFIX          Y02082
         LM    R0,RET,IECREGSV-IECPREFX(RF)  RESTORE REGISTERS   Y02082
         MODESET EXTKEY=DATAMGT         DATA MANAGEMENT KEY      Y02082
         L     RF,ULWK1                 RELOAD USER'S RETURN CDE YM3009
         BR    RPARC                    UNCONDITIONAL BRANCH
         EJECT                                                   YM3009
*
*        CLOSED SUBROUTINE TO ISSUE I/O OPERATIONS
*
TCD04300 EQU   *
         EXCP  DXIOB                    INITIATE I/O OPERATION
         WAIT  ,ECB=DXECB
         TM    DXECB,ECBNOERR           IS THERE AN UNUSUAL I/O COND
         BCR   15,RD                    RETURN TO CALLER
*
         EJECT                                                   YM3009
*
*        CONSTANTS
*
         DS    0F
UTLCONS3 DC    C'UTL0'                  KEY USED IN SEARCH KEY EQ
ULCP13   CCW   X'12',0,X'40',8          READ COUNT
         CCW   X'29',0,X'40',4          SEARCH KEY EQUAL
         CCW   X'08',0,0,0              TIC
         CCW   X'03',0,0,1              NOP
*
ULCP23   CCW   X'31',0,X'40',5          SEARCH ID EQUAL
         CCW   X'08',0,0,0              TIC TO CCW1
         CCW   X'1D',0,X'40',92         WRITE CKD    UHLN     LABEL
         CCW   X'1D',0,X'40',12         WRITE CKD    UHL(N+1) FILE MARK
         CCW   X'1D',0,X'40',12         WRITE CKD    UTL0     FILE MARK
*
*        TABLE OF MODULE ID'S AND ENTRY POINT ADDRESSES
*
         XCTLTABL ID=(LFG0232Z,2Z),SVC=023,BRT=YES,LENGTH=       Y02080
         IECDSECS CVT,TCB,TIOT,DCB,DEB,UCB,MAIN,USERLAB,USERTOT, Y02080*
               UNITTAB,WTG,PREFX,PSA,RRPL,EXPAND=YES             Y02144
         IECEQU
* ***
         END
