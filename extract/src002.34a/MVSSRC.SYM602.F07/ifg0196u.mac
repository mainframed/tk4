         TITLE ' IFG0196U - OPEN TAPE OUTPUT USER LABEL MODULE'
         COPY  LCGASMSW
IFG0196U CSECT
***********************************************************************
*                                                                     *
* MODULE NAME = IFG0196U (OS/VS2)                                     *
*                                                                     *
* DESCRIPTIVE NAME = TAPE OUTPUT USER LABEL MODULE                    *
*                                                                     *
* COPYRIGHT = NONE                                                    *
*                                                                     *
* STATUS = RELEASE 2, LEVEL 0                                         *
*                                                                     *
* FUNCTION =                                                          *
*        1. CHECK THE DCB EXIT LIST FOR AN ACTIVE OUTPUT LABEL ROUTINE*
*                                                                     *
*        2. IF THERE IS AN ACTIVE OUTPUT LABEL ROUTINE, OBTAIN CORE   *
*        FOR USER LABEL WORK AREA FOR OPEN (152 BYTES) FROM FETCH     *
*        PROTECTED SUBPOOL 229 IN DATA MANAGEMENT KEY AND, OBTAIN     *
*        CORE FOR THE USER'S LABEL WORK AREA (96 BYTES) FROM FETCH    *
*        PROTECTED SUBPOOL 229 IN USER KEY.                           *
*                                                                     *
*        3. GIVE CONTROL TO THE USER'S USER LABEL ROUTINE VIA SYNCH   *
*        FOR CONSTRUCTION OF THE LABELS.                              *
*                                                                     *
*        4.ON RETURN FROM THE USER'S ROUTINE, WRITE THE LABEL AND     *
*        EXAMINE THE RETURN CODE TO SEE IF ANY MORE LABELS ARE TO BE  *
*        PROCESSED. CONTINUE UNTIL ALL LABELS HAVE BEEN PROCESSED.    *
*                                                                     *
*        5. IF THE BUSY BIT WAS NOT TURNED OFF, WRITE A TAPE MARK     *
*        FOLLOWING THE HEADER LABELS.                                 *
*                                                                     *
* NOTES = SEE BELOW                                                   *
*                                                                     *
*    DEPENDENCIES =                                                   *
*            CLASS ONE CHARACTER CODE.  THE EBCDIC CHARACTER CODE     *
*            WAS USED FOR ASSEMBLY.  THE MODULE MUST BE REASSEMBLED   *
*            IF A DIFFERENT CHARACTER SET IS USED FOR EXECUTION.      *
*                                                                     *
*    RESTRICTIONS = NONE                                              *
*                                                                     *
*    REGISTER CONVENTIONS =                                           *
*            R2 POINTS TO DCB                                         *
*            R4 POINTS TO OPEN WORK AREA                              *
*            R5 POINTS TO THE RESIDENT ROUTINE                        *
*            R6 POINTS TO THE WTG TABLE                               *
*            R7 POINTS TO THE CURRENT PARAMETER LIST ENTRY            *
*            R8 POINTS TO THE CURRENT WTG TABLE ENTRY                 *
*            R9 POINTS TO THE DD ENTRY IN THE TIOT                    *
*            R10 POINTS TO THE UCB                                    *
*                                                                     *
*    PATCH LABEL = SEE NEXT TO LAST LABEL BEFORE ORG STATEMENT AT END *
*                  OF LISTING.                                        *
*                                                                     *
* MODULE TYPE = CONTROL (OPEN, CLOSE, EOV DATA MANAGEMENT)            *
*                                                                     *
*    PROCESSOR = ASSEMBLER XF                                         *
*                                                                     *
*    MODULE SIZE = SEE EXTERNAL SYMBOL DICTIONARY OR LOC FIELD ON     *
*                  ORG STATEMENT AT END OF LISTING                    *
*                                                                     *
*    ATTRIBUTES = REENTRANT, REFRESHABLE,READ-ONLY, ENABLED,          *
*                 PRIVILEGED, SUPERVISOR STATE, DATA MANAGEMENT KEY,  *
*                 LINK PACK AREA RESIDENT/PAGEABLE                    *
*                                                                     *
* ENTRY POINT = IFG0196U FROM IFG0196T - TAPE OUTPUT HEADER LABEL     *
*                                        PREPARATION                  *
*                                                                     *
*    PURPOSE = SEE FUNCTION                                           *
*                                                                     *
*    LINKAGE =                                                        *
*        FROM IFG0196T:                                               *
*          IECRES LOAD,MODID=ID6T6U,BRCODE=K0,BRANCH=QUEUED           *
*                                                                     *
* INPUT = TAPE POSITIONED TO WRITE USER LABELS                        *
*                                                                     *
* OUTPUT = USER LABELS WRITTEN ON TAPE                                *
*                                                                     *
* EXIT-NORMAL = IFG0196V - ACCESS METHOD EXECUTOR DETERMINATION       *
*               IFG0194A - VOLUME AND UNIT SELECTION                  *
*                                                                     *
* EXIT-ERROR = IFG0194A   613 - 14 ABEND  IC = 16                     *
*                                                                     *
* EXTERNAL REFERENCES = SEE BELOW                                     *
*                                                                     *
*    ROUTINES = IFG019RA                                              *
*                                                                     *
*    DATA AREAS = NONE                                                *
*                                                                     *
*    CONTROL BLOCK = CVT, DCB, JFCB                                   *
*                                                                     *
* TABLES = STANDARD                                                   *
*                                                                     *
* MACROS = MODESET, IECRES, SETLOCK, XLATE, EXCP, XCTLTABL, IECDSECS, *
*          IECEQU                                                     *
*                                                                     *
* CHANGE ACTIVITY = NEW RELEASE (LEVEL 0)                             *
*                                                                     *
***********************************************************************
         EJECT
*
*
         USING FORCORE,RCORE            WORK AREA ADDRESSABILITY
         USING WTG,RWTG                 WHERE-TO-GO TABLE        Y02080
         USING IHADCB,RDCB              DCB ADDRESSABILITY
         USING SRT,RUCB                 UCB ADDRESSABILITY
         USING TIOENTRY,RTIOT           TIOT ADDRESSABILITY
         USING CVT,RB                   CVT ADDRESSABILITY
*
         BALR  RBASE,0                  SET BASE REG
         USING *,RBASE                  ESTABILISH ADDRESSABILITY
         TM    DCBMACRF,DCBMEXCP        CHECK FOR EXCP
         BC    12,OTA46100              BRANCH IF NO
         TM    DCBMACRF,DCBMFOUN        CHK FOR FOUNDATION EXTENSION
         BC    12,OTA47600              IF NO, NO DCB EXIT
OTA46100 EQU   *                        POSSIBLE DCB EXIT
         L     RF,DCBEXLST              GET EXIT LIST
 MODESET KEYADDR=DXUKEY,WORKREG=12 ASSUME USER KEY               Y02082
         LA    RF,0(0,RF)               CLEAR HI-ORDER BYTE
         LTR   RF,RF                    CHECK FOR ZERO
         BZ    OTA47600                 BRANCH IF NO EXIT LIST
OTA46200 EQU   *                        CHECK DCB EXIT
         CLI   0(RF),X'02'              CHK IF OUTPUT UL SPEC
         BE    OTA46300                 YES, CHECK IF EXIT ACTIVE
         CLI   0(RF),X'82'              CHECK IF OUTPUT USER LBLS SPEC
         BE    OTA46300                 YES, CHECK IF EXIT ACTIVE
         TM    0(RF),LASTNTRY           IS THIS LAST DCB EXIT
         BO    OTA47600                 IF YES, WRITE TAPE MARK
         LA    RF,K4(RF)                INCREMENT PTR TO NEXT DCB EXIT
         B     OTA46200                 BR BACK TO CHK NEXT DCB EXIT
*
OTA46300 EQU   *                        ECHECK IF EXIT BIT ACTIVE
         L     RC,0(0,RF)               LOAD EXIT LIST DATA INTO R12
         LA    RF,0(0,RC)               ZERO HI-BYTE OF EXIT LST PTR
         LTR   RF,RF                    IS THE DCB EXIT ACTIVE
         BZ    OTA47600                 NO, RETURN TO NORMAL PROC
*
         SPACE 2
         MODESET EXTKEY=DATAMGT         DATA MANAGEMENT KEY      Y02082
*                                                                Y02082
*    GET USER LABEL WORK AREA CORE                               Y02082
*                                                                Y02082
         IECRES GET,LV=USERLDM,PREFIX=YES,SP=K229,               Y02082*
               STM=(2,14,WTGPREFX),ID=ULWA                       Y02082
         USING ULDMWK,R1                                         Y02082
         STM   RPAR,RC,ULREGSAV         SAVE REGS                Y02082
*                                                                Y02082
         DROP  R1                                                Y02082
         LR    RC,R1                    POINT TO GOTTEN CORE     Y02082
         USING ULDMWK,RC                                         Y02082
*     OBTAIN USER'S USER LABEL CORE                              Y02082
         IECRES GET,LV=USERLU,PREFIX=NO,SP=K229,KEY=USER,        Y02082*
               STM=(2,14,WTGPREFX)                               Y02082
         USING ULUWK,R9                                          Y02082
         LR    R9,R1                    PT TO GOTTEN CORE        Y02082
         LA    R1,CHAR0                 INITIALIZE COUNT
         MODESET EXTKEY=DATAMGT         DATA MANAGEMENT KEY      YM3845
         STH   R1,ULCNT                 AND SAVE FOR LATER USE
         NI    ULREQ,X'FF'-LASTNTRY     ZERO HI-ORDER BIT OF UL ENTRY
*
*  OUTPUT LABEL PROCESSING
         MODESET KEYADDR=DXUKEY,WORKREG=15 ASSUME USER KEY       Y02082
         MVI   ULBUFR,BLANK             SET LABEL BUFFER AREA
         MVC   ULBUFR+K1(L'ULBUFR-K1),ULBUFR  TO BLANKS
         MODESET EXTKEY=DATAMGT         DATA MANAGEMENT KEY      Y02082
OTA46400 EQU   *                        UL PROCESSING
         TM    JFCBLTYP,JFCBAL+JFCBUL   ANSI USER LABELS SPEC.
         BO    OTA46500                 YES, SKIP LABEL COUNTING
         LH    R1,ULCNT                 PICK UP PREVIOUS COUNT
         LA    R1,K1(K0,R1)             INCREMENT BY 1
         STH   R1,ULCNT                 SAVE UPDATED COUNT
         CLI   ULCNT+K1,MAXNOLAB        TEST,BR IF MAX NBR OF UL
         BH    OTA47300                 ALREADY PROCESSED
OTA46500 EQU   *                        INITIALIZE BUFFER HEADING
         IC    RB,ULCNT+K1              GET LABEL NUMBER         Y02082
         MODESET KEYADDR=DXUKEY,WORKREG=15 ASSUME USER KEY       Y02082
         STC   RB,ULBUFR+K3             PUT LABEL NUMBER         Y02082
         MVC   ULBUFR(K3),UHLCON        SET 'UHL' IN BUFFER
         BAL   RB,OTA48000              GO TO SYNCH TO USER
*
*
         LTR   RF,RF                    TEST,BR IF LABEL NOT
         BZ    OTA47300                 TO BE WRITTEN
*  RESET FIRST 4 CHAR OF LABEL THEN
*  ESTABLISH CHANNEL PROGRAM TO WRITE USER LABEL
*
*
*                                                                Y02082
*     COPY LABEL FROM USERS BUFFER                               Y02082
*                                                                Y02082
         L     RF,WTGPREFX              PT TO PREFIX             Y02082
         STM   R0,RET,IECREGSV-IECPREFX(RF) SAVE REGS THRU LOCK  Y02082
         MODESET EXTKEY=ZERO            SUPERVISOR KEY ZERO      Y02082
         SETLOCK OBTAIN,TYPE=LOCAL,MODE=UNCOND,                  Y02082*
               RELATED=('PREVENT FREE USER CORE-RELEASE BELOW')  Y02082
         LM    R0,RET,IECREGSV-IECPREFX(RF) RESTORE REGS         Y02082
         MODESET KEYADDR=DXUKEY,WORKREG=15 ASSUME USER KEY       Y02082
*     VERIFY USER STILL HOLDS USER LABEL BUFFER                  Y02082
         OC    ULUWK(USERLU),ULUWK      PGM CHECK IF NOT         Y02082
         MODESET EXTKEY=ZERO            ASSUME KEY ZERO          Y02082
         MVC   ULDMBUFR,ULBUFR          COPY LABEL FROM USER     Y02082
         L     RF,WTGPREFX              POINT TO PREFIX          Y02082
         STM   R0,RET,IECREGSV-IECPREFX(RF) SAVE REGS THRU LOCK  Y02082
*     RELEASE LOCAL LOCK                                         Y02082
         SETLOCK RELEASE,TYPE=LOCAL,RELATED=('SEE ABOVE')        Y02082
         LM    R0,RET,IECREGSV-IECPREFX(RF) RESTORE REGS         Y02082
         MODESET EXTKEY=DATAMGT         DATA MANAGEMENT KEY      Y02082
         MVC   ULDMBUFR(K3),UHLCON      GET 'UHL' IN LABEL       Y02082
         TM    JFCBLTYP,JFCBAL+JFCBUL   IF 'AUL' SPECIFIED, BYPASS
         BO    OTA46800                 RESTORING LABEL NUMBER
         MVC   ULDMBUFR+K3(K1),ULCNT+K1 LABEL NO. IN LABEL       Y02082
OTA46800 EQU   *                        POINT TO LABEL BUFFER
         LA    R1,ULDMBUFR              POINT CCW TO KEY 5 BUFF  YM3881
         ST    R1,DXCCW1
         MVI   DXCCW1,CCWWRTAP          WRITE OP-CODE TO CCW
         MVC   DXCCW1+K4(K4),ARDCCWK    80-CHAR LENGTH
         TM    JFCBLTYP,JFCBAL+JFCBUL   ANSI USER LABEL SPEC.
         BNO   OTA46900                 NO, BYPASS TRANSLATE
*   TRANSLATE LABEL BEFORE PROCESSING
         XLATE (R1),K80,TO=A            TRANSLATE USER LABEL
OTA46900 EQU   *                        GO WRITE LABEL
         BAL   RB,OTA48500              GO TO ISSUE EXCP
*  ERROR CHECKING
         BZ    OTA47000                 BR IF I/O ERROR
*
*
         CLI   ULRETCOD,WRITNEXT        TEST,BR IF USER RETURNED A
         BE    OTA46400                 TO CONTINUE UL PROCESSING
*  OTHERWISE, IT IS ASSUMED THE USER RETURNED A CODE = 4, I.E. NO
*  FURTHER UL PROCESSING.
         B     OTA47300                 UNCONDITIONAL BRANCH
*
*  ERROR WHEN ATTEMPTING TO WRITE LABEL.  GO TO USER WITH ERROR
*  INDICATIONS, THEN TERMINATE UL PROCESSING (RETURN CODE NOT
*  EXAMINED).
OTA47000 EQU   *                        I/O ERROR
         LA    R1,DXIOB                 INSERT POINTER TO STATUS INFO
         MODESET KEYADDR=DXUKEY,WORKREG=15 ASSUME USER KEY       Y02082
         ST    R1,ULPARM+K8             IN PARAMETER LIST
         OI    ULPARM+K8,LASTNTRY       FLAG IN ERROR
         BAL   RB,OTA48100              GO TO SYNCH TO USER (BUFFER
*                                       CONTAINS LABEL UNABLE TO WRITE)
*
         NI    DCBOFLGS,X'FF'-DCBOBUSY  PROHIBIT OPEN FROM CONTINUING
*                                       TO PROCESS THIS DCB
*
*  ESTABLISH NAME OF ROUTINE WHICH CALLED THIS ROUTINE, THEN
*  FREEMAIN AND RETURN TO CALLER.
*
*     FREE USER LABEL WORK AREAS                                 Y02082
*                                                                Y02082
OTA47300 EQU   *                        FREE WORK AREA
         IECRES FREE,A=(R9),PREFIX=NO,LV=USERLU,SP=K229,         Y02082*
               KEY=USER,STM=(2,14,WTGPREFX)                      Y02082
         MODESET EXTKEY=DATAMGT         DATA MANAGEMENT KEY      Y02082
         LR    R1,RC                    PTR TO AREA TO BE FREED  Y02082
         LM    RPAR,RC,ULREGSAV         RESTORE REGS 5 - 12      Y02082
*     FREE OPEN'S USER LABEL WORK AREA                           Y02082
         IECRES FREE,A=(1),PREFIX=YES,STM=(2,14,WTGPREFX)        Y02082
*
* CHECK IF FURTHER PROCESSING IS REQUIRED AFTER USER LABEL PROC
         TM    DCBOFLGS,DCBOBUSY        CHK IF BUSY BIT WAS TURNED OFF
         BZ    OTA47800                 IF YES, SKIP TO NEXT DCB
         SPACE 1
*                   WRITE TAPE MARK FOLLOWING HEADER LABELS
         SPACE 1
OTA47600 EQU   *                        WRITE TM AND CONT PROCESSING
         MODESET EXTKEY=DATAMGT         O/C KEY                  Y02082
         MVI   DXCCW1,CCWWTM            SET UP CCW TO WRITE TAPE MARK
         MVI   DXCCW1+K4,X'60'          CHAIN A NOP
         MVI   DXCCW2,CCWNOP            SET CCW FOR NOP
         LA    RC,K16                   INT CODE - I/O ERROR WRITE TM
         BAL   RB,OTA48500              GO TO WRITE TAPE MARK
         BO    OTA47700                 BRANCH IF NO ERROR
         TM    IOBSTAT0,CSWUNITX        IS ERROR UNIT EXCEPTION
         BZ    OTA48600                 NO, GO REJECT VOLUME
OTA47700 EQU   *                        NO ERROR ON WRITING TAPE MARK
         OI    DXATOUTA,DXATHDTM        TM WRITTEN AFTER HDR LBL Y02144
*                                                                YM8526
         CLC   JFCBFLSQ,ONE6U           CHECK FOR FILE ONE       YM8526
         BH    OTA47800                 BRANCH IF GT ONE         YM8526
*                                                                YM5728
*        ISSUE TAPE ATTRIBUTES MESSAGE                           YM5728
         MVC   DXRETMOD,ID6U6V          SET UP RETURN LOAD       YM5728
         MVI   DXRETCOD,K8              SET UP RETURN OFFSET     YM5728
*                                                                YM5728
         IECRES LOAD,MODID=ID6U4J,BRCODE=K48,BRANCH=QUEUED       YM5728
OTA47800 EQU   *                        PREPARE TO GO TO IFG0196V
         LA    RET,K8                   ENTRY FOR EXECUTOR LOAD
         LA    RF,ID6U6V                EXECUTOR EXIT LOAD IDTTR
OTA47900 EQU   *                        BRANCH TO NEXT MODULE
         IECRES LOAD,MODID=(RF),BRCODE=(RET),BRANCH=QUEUED       Y02080
*
OTA48000 EQU   *                        SYNCH TO USER
         XC    ULPARM+K8(K4),ULPARM+K8  ZERO ERROR IND
OTA48100 EQU   *                        CONTINUE SYNCH
         LA    R1,ULBUFR                POINT TO LABEL BUFFER
         ST    R1,ULPARM                PTR TO LABEL BUFFER      Y02082
         L     R1,DXUDCBAD              GET USER'S DCB ADDR      Y02082
         ST    R1,ULPARM+K4             PUT IN USER'S PARM LIST  Y02082
         XC    ULTOTPTR(K4),ULTOTPTR    ZERO TOTALING ENTRY
         MVI   ULDCBPTR,K0              CLEAR FLAG BYTE
*
         MODESET EXTKEY=DATAMGT         DATA MANAGEMENT KEY      Y02082
         NI    DCBOFLGS,X'FF'-DCBOLOCK  SET LOCK BIT TO ZERO     Y02082
*                                                                Y02082
* COPY THE DCB FROM THE WORK AREA TO USER'S STORAGE              Y02082
*                                                                Y02082
         IECRES INIT,DCBCOPY=FRWKAR,STM=(3,14,WTGPREFX)          Y02082
*                                                                Y02082
         LA    R1,ULPARM                POINT TO PARAMETER LIST  Y02082
         IECRES UEXIT,EXITAD=ULREQ,STM=(2,13,WTGPREFX)           Y02082
         L     RDCB,DXUDCBAD            GET PTR TO USER'S DCB    Y02082
         MODESET KEYADDR=DXUKEY,WORKREG=1 ASSUME USER'S KEY      Y02082
         OI    DCBOFLGS,DCBOLOCK        SET LOCK BIT TO ONE      Y02082
         OC    DCBOFLGS,DCBOFLGS        TOUCH USER'S CORE        YM3005
         LA    R0,ALLBITS-DCBOBUSY      SET MASK FOR DCBOBUSY    YM3005
         IC    R1,DCBOFLGS              GET USER'S DCB OFLGS     Y02082
         MODESET EXTKEY=DATAMGT         RESUME DATA MGT KEY      Y02082
         OR    R0,R1                    COMBINE COPIED DCBOFLGS  YM3005
         L     RDCB,DXPDCBAD            WITH USER'S DCBOFLGS     YM3005
         IC    R1,DCBOFLGS              MAKING COPIED BUSY BIT   YM3005
         NR    R1,R0                    SAME AS USER'S BUSY BIT  YM3005
         STC   R1,DCBOFLGS              UPDATE DCBOFLGS IN COPY  Y02082
         OI    DCBOFLGS,DCBOLOCK        SET LOCK BIT TO ONE
         ST    RF,ULWK1                 SAVE RETURN CODE
         BR    RB                       RETURN
*
*                   CLOSED SUBROUTINE TO ISSUE I/O OPERATIONS
         SPACE 1
*                        IT IS ASSUMED THAT ALL CONTROL BLOCKS
*                        HAVE ALREADY BEEN SET UP
         SPACE 1
*                             BAL  RA,PRHR50
*                             RETURN
         SPACE 1
OTA48500 EQU   *                        I/O SUBROUTINE
         EXCP  DXIOB                    ISSUE I/O OPERATION
         IECRES WAIT
         TM    DXECB,ECBNOERR           IS THERE AN UNUSUAL CONDITION
         BR    RB                       NO - RETRN TO CALLER
         SPACE
*  OPEN HAS FOUND AN UNCORRECTABLE I/O ERROR, BUT WILL ASK FOR NEW
*  VOLUME RATHER THAN ABEND WITH '613' UNLESS THIS IS NOT THE FIRST
*  DATA SET ON THE TAPE
         SPACE
OTA48600 EQU   *                        ASK FOR NEW VOLUME
         NI    DCBIFLGS,X'FF'-DCBIFPIO  TURN OFF ERROR FLAGS IN
*                                       DCB TO ISSUE REWIND
*                                       AND/OR UNLOAD COMMAND
*                                                                Y02134
         LR    R0,RC                    LOAD ERROR CORE          Y02134
*                                                                Y02134
         MVC   DXRETMOD,ID6U6N          SET UP RETURN LOAD       Y02134
         MVI   DXRETCOD,K0              SET UP RETURN OFFSET     Y02134
*                                                                Y02134
         IECRES LOAD,MODID=ID6U4A,BRCODE=K16,BRANCH=QUEUED       Y02134
         EJECT
         SPACE 1
*                   CONSTANTS
         SPACE 1
ONE6U    DC    H'1'                     HALFWORD CONSTANT ONE    YM8526
         DS    0F
ARDCCWK  DC    X'20000050'              SILI FLAG, 80 CHARACTERS
UHLCON   DC    C'UHL'                   HEADER LABEL CONSTANT
         SPACE
XCTL6U   XCTLTABL ID=(ID6U4A,4A,ID6U4J,4J,ID6U6V,6V,ID6U6N,6N),  YM5728X
               BRT=YES,LENGTH=                                   Y02080
         IECDSECS CVT,TCB,TIOT,DCB,UCB,MAIN,USERLAB,USERTOT,WTG,RB,    X
               PREFX,PSA,RRPL,EXPAND=YES                         Y02144
         IECEQU
         END
