         TITLE 'IFG0552B  EOV - RE-WRITE VOLUME LABEL'
FFG0552B CSECT
***********************************************************************
*                                                                     *
*                                                                     *
*          VS2 RELEASE 03 DELETIONS                                   *
*0000                                                          @ZA02874
*0000474962,475022                                             @ZA09625
*          VS2 SU32 DELETIONS
*301902-301010                                                 @ZA32147
*                                                              @G32DSMI
*                                                              @ZM04476
*          VS2 RELEASE 02 DELETIONS                                   *
*                                                                     *
*                                                                     *
***********************************************************************
*                                                                     *
* MODULE NAME = IFG0552B (OS/VS2)                                     *
*                                                                     *
* DESCRIPTIVE NAME = RE-WRITE VOLUME LABEL                            *
*                                                                     *
* COPYRIGHT = NONE                                                    *
*                                                                     *
* STATUS = RELEASE 2, LEVEL 0                                         *
*                                                                     *
* FUNCTION =                                                          *
*        1) IF NSL IS SPECIFIED, THE DCB IS COPIED FROM THE           *
*        WORK AREA TO THE CALLER'S STORAGE AND CONTROL IS PASSED TO   *
*        NSLEHDRO ROUTINE.                                            *
*        2) IF NL/BLP SPECIFIED, TRANSFER CONTROL TO IFG0552F WITH A  *
*        BRANCH CODE OF 4.                                            *
*        3) READ HDR1 LABEL AND TRANSLATE IF ASCII. IF UNIT EXCEPTION *
*        OCCURS, A NEW VOLUME IS REQUESTED VIA TRANSFER OF CONTROL    *
*        TO IFG0552N WITH A BRANCH CODE OF 0.                         *
*        4) EXAMINE THE LABEL TO CHECK FOR SECURITY PROTECTION. IF    *
*        DATA SET SECURITY IS INCONSISTENT WITH THAT SPECIFIED IN THE *
*        JFCB, ISSUE AN ASCII DISMOUNT MESSAGE FOR ASCII (TRANSFER    *
*        CONTROL TO IFG0554J, BRANCH CODE = 8), OR A SECURE VOLUME    *
*        MESSAGE FOR SL (TRANSFER CONTROL TO IFG0554J, BRANCH CODE=40)*
*        IN EITHER CASE, THE RETURN MODULE IS IFG0552N WITH A         *
*        RETURN OFFSET OF 0.                                          *
*        5) IF THE DATA SET IS SECURITY PROTECTED, TRANSFER CONTROL   *
*        TO IFG0555T TO REQUEST PASSWORD WITH A BRANCH CODE = 0. THE  *
*        RETURN MODULE IS IFG0552B WITH A RETURN OFFSET OF 20.        *
*        6) RETURN FROM PASSWORD CHECK TO EXAMINE THE EXPIRATION      *
*        DATE. IF THE DATE HAS NOT BEEN PASSED, TRANSFER CONTROL      *
*        TO IFG0554J, BRANCH CODE = 20, TO ISSUE THE EXPIRATION DATE  *
*        MESSAGE. THE RETURN MODULE IS IFG0552B WITH A RETURN OFFSET  *
*        OF 4.                                                        *
*        7) IF THE VOLUME IS REJECTED (REPLY OF M), THEN TRANSFER     *
*        CONTROL TO IFG0552N, BRANCH CODE = 0, TO ASK FOR A NEW VOL.  *
*        8) IF THE VOLUME IS ACCEPTED, PERFORM CHECKPOINT DATA SET    *
*        PROCESSING.                                                  *
*        9) REWINDS THE TAPE, CLEARS THE LABEL AREA, AND REREADS THE  *
*        VOLUME LABEL. IF THE FIRST 4 CHARACTERS OF THE LABEL ARE     *
*        CORRECT (VOL1), THEN THE LABEL IS REWRITTEN; OTHERWISE, THE  *
*        LABEL IS REBUILT (AND TRANSLATED IF ASCII), AND REWRITTEN.   *
*        THE TEST FOR ERRORS IS IN THE NEXT MODULE.                   *
*        10) TRANSFER CONTROL TO IFG0552D, BRANCH CODE = 0.           *
*                                                                     *
* NOTES = SEE BELOW                                                   *
*                                                                     *
*    DEPENDENCIES =                                                   *
*            CLASS ONE CHARACTER CODE.  THE EBCDIC CHARACTER CODE     *
*            WAS USED FOR ASSEMBLY.  THE MODULE MUST BE REASSEMBLED   *
*            IF A DIFFERENT CHARACTER SET IS USED FOR EXECUTION.      *
*                                                                     *
*    RESTRICTIONS = NONE                                              *
*                                                                     *
*    REGISTER CONVENTIONS =                                           *
*            R2 POINTS TO DCB                                         *
*            R4 POINTS TO OPEN WORK AREA                              *
*            R5 POINTS TO THE RESIDENT ROUTINE                        *
*            R6 POINTS TO THE WTG TABLE                               *
*            R7 POINTS TO THE CURRENT PARAMETER LIST ENTRY            *
*            R8 POINTS TO THE CURRENT WTG TABLE ENTRY                 *
*            R9 POINTS TO THE DD ENTRY IN THE TIOT                    *
*            R10 POINTS TO THE UCB                                    *
*                                                                     *
*    PATCH LABEL = SEE THIRD FROM LAST LABEL BEFORE ORG STATEMENT AT  *
*                  END OF LISTING.                                    *
*                                                                     *
* MODULE TYPE = CONTROL (OPEN, CLOSE, EOV DATA MANAGEMENT)            *
*                                                                     *
*    PROCESSOR = ASSEMBLER XF                                         *
*                                                                     *
*    MODULE SIZE = SEE EXTERNAL SYMBOL DICTIONARY OR LOC FIELD ON     *
*                  ORG STATEMENT AT END OF LISTING                    *
*                                                                     *
*    ATTRIBUTES = REENTRANT, REFRESHABLE,READ-ONLY, ENABLED,          *
*                 PRIVILEGED, SUPERVISOR STATE, DATA MANAGEMENT KEY,  *
*                 LINK PACK AREA RESIDENT/PAGEABLE                    *
*                                                                     *
* ENTRY POINT =                                                       *
*        IFG0552B                                                     *
*                                                                     *
*    PURPOSE = SEE FUNCTION                                           *
*                                                                     *
*    LINKAGE =                                                        *
*        THIS MODULE IS TRANSFERRED CONTROL THROUGH THE IECRES-LOAD   *
*        MACRO INSTRUCTION.                                           *
*                                                                     *
* INPUT =                                                             *
*        GIVEN CONTROL IN PROTECT KEY 5.                              *
*        REGISTER 2 POINTS TO THE COPIED DCB.                         *
*        DEBDCBAD POINTS TO THE COPIED DCB.                           *
*        REGISTER 4 POINTS TO THE EOV WORKAREA                        *
*                                                                     *
* OUTPUT =                                                            *
*        THE NEXT MODULE IS GIVEN CONTROL IN PROTECT KEY 5 WITH       *
*        REGISTER 2 POINTING TO THE COPIED DCB,                       *
*        DEBDCBAD POINTING TO THE COPIED DCB,                         *
*        AND REGISTER 4 POINTING TO THE EOV WORKAREA,                 *
*                                                                     *
* EXIT-NORMAL =                                                       *
*        IFG0552D - WAIT VOL LABEL REWRITE, BRANCH CODE = 0           *
*        IFG0554J - ISSUE EXPIR. DATE MSG, BRANCH CODE = 20           *
*                   ISSUE SECURE VOL. MSG, BRANCH CODE = 40           *
*                   ISSUE ASCII DISMOUNT MSG, BRANCH CODE = 8         *
*        IFG0552F - NL/BLP, BRANCH CODE = 4                           *
*        IFG0552N - I/O ERROR, NEW VOL., BRANCH CODE = 0              *
*        IFG0555T - CHECK PSWD FOR SECURITY PROTECT, BRANCH CODE = 0  *
*            RETURN MOD = IFG0552B, BRANCH CODE = 20                  *
*                                                                     *
* EXIT-ERROR =                                                        *
*                                                                     *
* EXTERNAL REFERENCES = SEE BELOW                                     *
*                                                                     *
*    ROUTINES =                                                       *
*        IFG019RA THROUGH THE IECRES MACRO.                           *
*                                                                     *
*    DATA AREAS =                                                     *
*        EOV WORKAREA.                                                *
*                                                                     *
*    CONTROL BLOCK =                                                  *
*        CVT                                                          *
*        DEB                                                          *
*        JFCB                                                         *
*        TCB                                                          *
*        UCB                                                          *
*                                                                     *
* TABLES =                                                            *
*                                                                     *
* MACROS =                                                            *
*        MODESET                                                      *
*        IECRES LOAD                                                  *
*        IECRES WAIT                                                  *
*        IECRES INIT                                                  *
*        EXCP                                                         *
*        WAIT                                                         *
*        XLATE                                                        *
*                                                                     *
* CHANGE ACTIVITY =                                                   *
*        SEE CHANGES/DELETIONS SECTION JUST AFTER CSECT CARD.         *
*                                                                     *
***********************************************************************
         EJECT
*
*        INTERNAL ABEND CODES
*
EABD108  EQU   108                      EXPIRATION DATE NOT PASS YM5718
EABD109  EQU   109                      PASSWORD PROTECTION IN   YM5718
*                                       JFCB NOT MATCH HDR LABEL YM5718
EABD144  EQU   144                      I/O ERROR READING/REWIND YM8520
EABD249  EQU   249                      ASCII ACCESS BYT NONBLNK YM5718
*                   ADDRESSABILITY
         SPACE 1
         BALR  RBASE,0                  ESTABLISH BASE
         USING *,RBASE
         USING FORCORE,RCORE
         USING WTG,RWTG                 BASE OF WTG TABLE        Y02080
         USING DEBBASIC,RDEB                                   @G32DSMI
         USING IHADCB,RDCB
         USING TIOENTRY,RTIOT
         USING SRT,RUCB
         SPACE 1
         B     ETO14400(RET)            BRANCH TO BRANCH TABLE   Y02134
*                                                                Y02134
ETO14400 EQU   *                        BRANCH TABLE             Y02134
*                                                                Y02134
         B     ETO14500                 BRANCH INITIAL ENTRY     Y02134
         B     ETO17000                 BRANCH EXPIRATION DATE   Y02134
         B     ETO17120                 FROM QUERY CHKPT STATUS  Y02083
         B     ETO17200                 FROM REVOKE CHKPT STATUS Y02083
         B     ETO17170                 FROM REQ TO CHKPT MSG    Y02083
         B     ETO16860                 RETURN FROM PASSWORD     Y02082
*                                                                Y02134
         EJECT
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
*  SET UP THE DESIRED DENSITY FOR THE TAPE VOLUME LABEL REWRITE.
*  CONTINUE TO READ HEADER LABELS AT THE CURRENT DENSITY.
*                                                                Y02134
*****************************************************************Y02134
*
ETO14500 EQU   *                        DENSITY                  Y02134
*                                                                Y02134
         L     RDEB,DCBDEBAD            LOAD DEB ADDRESS         Y02134
         STCM  RUCB,B'0111',DEBSUCBB    PUT UNIT ADDR INTO DEB @G32DSMI
*
         TM    DCBMACRF,DCBMEXCP        TEST FOR EXCP          @ZA32147
         BO    ETO14600                 BR, IF EXCP            @ZA32147
         TM    DCBCIND2,DCBC2PUT        EOV CALLED BY CLOSE?   @ZA32147
         BNO   ETO14600                 NO, BYPASS CL/EOV BIT  @ZA32147
         OI    DEBFLGS1,DEBF1CEV        IND TO CLOSE THAT EOV  @ZA32147
*                                       WAS CALLED BY QSAM     @ZA32147
ETO14600 MVC   DEBSDVM,DXDEBMOD         SET MODE & DENSITY     @G32DSMI
         TM    UCBTBYT2,UCB3TAPE        TEST FOR 7 TRK TAPE      Y02134
         BNO   ETO14800                 BRANCH IF NOT 7-TRK      Y02134
*
*              SET UP DEBDVMOD WITH TRTCH AND DENSITY
*
         MVC   DEBSDVM,DCBDEN           DENSITY                @G32DSMI
         OC    DEBSDVM,DCBTRTCH         TAPE RECORDING TECH    @G32DSMI
ETOHEX38 EQU   X'38'                    TAPE RECORD TECH MASK
ETOHEX30 EQU   X'30'                    DEBDVMOD MASK
         TM    DEBSDVM,ETOHEX38         WAS TRTCH SPECIFIED    @G32DSMI
         BNZ   ETO14800                 YES, BRANCH
         OI    DEBSDVM,ETOHEX30         ASSUME XLATE,NO CONVERT@G32DSMI
*                                       ODD PARITY, LOW DENSITY
*
         EJECT
*
ETO14800 EQU   *                        CHECK FOR NSL
         TM    JFCBLTYP,JFCNSL          NSL SPEC
         BNO   ETO15700                 BR IF NSL NOT SPEC       YM3841
ETO15200 EQU   *                        NSL
*                                                                Y02082
* COPY THE DCB FROM THE WORK AREA TO USER'S STORAGE              Y02082
*                                                                Y02082
         IECRES INIT,DCBCOPY=FRWKAR,STM=(3,14,WTGPREFX)          Y02082
*                                                                Y02082
         OI    DXOPCLSW,HEX40           IND ENTRY TO NSLEHDRO
         MODESET EXTKEY=SUPR            ASSUME KEY ZERO          Y02082
         NI    UCBDMCT,ALLBITS-UCBMOUNT CLEAR MOUNT FLAG IN UCB  Y02080
         MODESET EXTKEY=DATAMGT         DATA MANAGEMENT KEY      Y02082
         MVC   DXXMODNM,NSLEHDRO        CHANGE TO NSLEHDRO       Y02134
         LR    R8,RES                   SAVE RES PTR
         LR    RES,RDEB                 PUT DEB IN R5 FOR NSL
         L     RDCB,DXPDCBAD            PASS ADDR COPY DCB       YM5702
         OI    DXATOUTA,DXATNSL         CONTROL TO NSL ROUTINE   Y02144
         L     RF,DXATCOM3              GET RRPLIST PTR          Y02144
         USING RRPLIST,RF                                        Y02144
         OI    RRFLAGS1,RRFNSL          SET NSL GIVEN CONTROL    Y02144
         CLC   DXVOLSR2,SCRTCH          SCRATCH REQUEST?       @ZA02874
         BNE   ETO15225                 NO, BR                 @ZA02874
         MVC   DXCCW7(L'UCBVOLI),UCBVOLI SAVE VOL SER FOR 552N @ZA02874
ETO15225 EQU   *                                               @ZA02874
         SR    RET,RET                  BR ENTRY RETURN FOR 552N YM5315
         MODESET EXTKEY=ZERO            ASSUME KEY ZERO          YM2866
         IECRES LOAD,PREFIX=DXXPREFX,BRANCH=DIRECT XCTL BY NAME  Y02080
*
         EJECT
*                                                                Y02134
ETO15700 EQU   *                        NOT NSL                  Y02134
*                                                                Y02134
*        INITIALIZE UCB FILE SEQUENCE NUMBER/COUNT               YM6559
         MODESET EXTKEY=SUPR            ASSUME KEY ZERO          YM6559
         MVC   SRTEFSEQ(K2),DXCCW7+K6   GET SAVED LOG COUNT      YM6559
         MVC   SRTEFSCT(K2),CONSTONE    SET PHYSICAL TO 1        YM6559
         MODESET EXTKEY=DATAMGT         DATA MANAGEMENT KEY      YM6559
         LA    R7,ID2B2F                SET UP NL/BLP LOAD       Y02134
         LA    RET,K4                   LOAD ENTRY CODE          Y02134
         TM    JFCBLTYP,JFCBAL+JFCSL    TEST FOR SL OR AL        Y02134
         BZ    ETO17400                 BRANCH IF NEITHER        Y02134
*                                                                Y02134
*
*        RE-ESTABLISH 17 LAST SIGNIFICANT NON-BLANK CHARACTERS   YM3880
*        IN DATASET NAME - USED IN SECURITY CHECK AND TO         YM3880
*        CONSTRUCT NEW LABEL                                     YM3880
*
         LA    RET,JFCBDSNM+(L'JFCBDSNM-K17) ADDR OF LAST 17     YM3880
*                                       CHARACTERS               YM3880
ETO15710 EQU   *                        IS 17TH CHAR BLANK       YM3880
         CLI   K16(RET),BLANK           STOP SCAN IF NOT BLANK   YM3880
         BNE   ETO15720                 BRANCH IF NOT BLANK      YM3880
         BCT   RET,ETO15710             REDUCE RET BY ONE        YM3880
ETO15720 EQU   *                        SEE IF DSN LESS THAN 17  YM3880
         LA    RF,JFCBDSNM              DSN ADDRESS              YM3880
         CR    RF,RET                   RET BACKED UP TOO FAR    YM3880
         BL    ETO15730                 BRANCH IF NOT            YM3880
         LR    RET,RF                   SET TO START OF DSN      YM3880
ETO15730 EQU   *                        SAVE ADDR                YM3880
         ST    RET,DXCCW4               SAVE IN DXCCW4           YM3880
*
*        FIND ADDR OF GENERATION & VERSION NUMBER IF GDG         YM3880
*
         CLI   JFCBELNM+K1,BLANK        IS IT A GDG              YM3880
         BE    ETO15760                 BRANCH IF NO             YM3880
         LA    RET,JFCBDSNM+(L'JFCBDSNM-K1-K9) LAST CHAR - 9     YM3880
ETO15740 EQU   *                        FIND LAST NON-BLANK CHAR YM3880
         CLI   K9(RET),BLANK            STOP SCAN IF NOT BLANK   YM3880
         BNE   ETO15750                 BRANCH IF NOT BLANK      YM3880
         BCT   RET,ETO15740             CONTINUE SCAN            YM3880
ETO15750 EQU   *                        SAVE GEN/VER ADDR        YM3880
         ST    RET,DXCCW4+K4            SAVE IN DXCCW4+4         YM3880
ETO15760 EQU   *                        READ THOSE LABELS        YM3880
*
*              SET UP CHANNEL PROGRAM AND READ LABEL
*              IT IS ASSUMED THAT THE TAPE IS CURRENTLY POSITIONED
*              JUST AFTER THE VOL1 LABEL BY IFG0554F             Y02134
*
         MVC   DXCCW1,ACCW              SET UP BASIC CCW
*
ETO15800 EQU   *                        FIND HEADER LABEL 1
         SPACE
         BAL   RWTG,ETO17600            GO READ AGAIN
         LA    RWTG,DXXAREA             RESTORE REG RWTG         YM3841
         SPACE
ETO15900 EQU   *                        CHECK FOR VOLUME LABEL
         TM    JFCBLTYP,JFCBAL          WERE ANSI LABEL SPEC
         BZ    ETO16100                 NO, CONTINUE PROCESSING
         CLC   AHDR1,FL1LABI            EBCDIC HDR1 LABEL
         BNE   ETO16000                 NO, GO TRANSLATE LABEL
         CLI   FL1FSEC,CHAR0            SECURITY PROTECTED
         BNE   ETO16200                 MAYBE, GO CHECK SOME MORE
         MVI   FL1FSEC,BLANK            NO SECURITY PROTECT
         B     ETO16900                 CONTINUE PROCESSING      Y02134
         EJECT
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
*        TRANSLATE LABEL BEFORE PROCESSING                       Y02134
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
ETO16000 XLATE DXLBL,K80                TRANSLATE LABEL
         CLC   VOLLABI(K3),UVOL0G       IS THIS A USER VOL LABEL
         BE    ETO15800                 YES, GO READ AGAIN
ETO16100 EQU   *                        CHECK FOR VOL LABEL
         SPACE
         CLI   VOLLABI,CHARV            IF THIS IS VOLUME LABEL
         BE    ETO15800                 YES, GO READ AGAIN
         SPACE
         CLC   AHDR1,FL1LABI            TEST FOR HDR 1
         BE    ETO16200                 BR YES TO CHK SECURITY
         SPACE
         TM    IOBSTAT0,CSWUNITX        CHK FOR UNIT EXCEPTION
         BZ    ETO17900                 NO, GO ASK FOR NEW VOLUME
         B     ETO17100                 BR TO PROCESS LABEL
*
         SPACE 1
ETO16200 EQU   *                        CHECK FOR SECURITY
*              DETERMINE IF DATA SET SECURITY IS INCONSISTENT WITH    *
*              JFCB. DEMOUNT THE VOLUME IF IT IS. OTHERWISE GO   Y02082
*              TO CHECK PASSWORD IF DATA SET IS PROTECTED.       Y02082
*
         CLI   FL1FSEC,CHAR1            HDR READ INDICATES SECURITY
         BNE   ETO16300                 BRANCH IF NO
         TM    JFCBIND2,JFCBRWPW        JFCB SPECIFY SECURITY IS FOR
*                                       READ AND WRITE
         BM    ETO16400                 YES, BRANCH
         B     ETO18500                 NO, MOUNT A SCRATCH VOLUME
ETO16300 EQU   *                        NO SECURITY IN HEADER
         CLI   FL1FSEC,FL1WRSEC         IS SECURITY FOR WRITE ONLY
         BNE   ETO16800                 NO, BRANCH
         TM    JFCBIND2,JFCBRWPW        JFCB SPECIFY SECURITY IS FOR
*                                       WRITE ONLY
         BNO   ETO18500                 NO, MOUNT A SCRATCH VOLUME
ETO16400 EQU   *                        GET STARTING ADR OF DSN
         LM    RET,RF,DXCCW4            SAVED BY TRL LBL MODULE
ETO16500 CLC   FL1ID,K0(RET)            HDR ID EQ JFCBDSNM
         BE    ETO16850                 YES, CHECK PASSWORD      Y02082
         CLI   JFCBELNM+K1,BLANK        IS THIS A GDG
         BE    ETO18500                 NO, BYPASS DOS TESTS
         CLC   FL1GNO,K3(RF)            IS GEN NO EQ
         BNE   ETO18500                 NO, BR
         CLC   FL1VNG,K8(RF)            IS VER NO EQ
         BNE   ETO18500                 NO, BR
         LA    R0,K9                    FIND LENGTH OF DSN
         SR    RET,R0                   WITHOUT GENERATION NO
         LA    R0,JFCBDSNM              POINT TO START OF DSN
         CLR   RET,R0                   IS DSN STILL MORE THAN 17
         BNL   ETO16600                 BR, YES COMP LAST 17 OF DSN
         LR    RET,R0                   SET RD TO START OF NAME
         SR    RF,RET                   GET NO CHARS IN DSN
         LA    R1,FL1ID(RF)             POINT TO END OF DSN
         CLI   K1(R1),BLANK             IN LABEL
         BNE   ETO18500                 BR, MUST BE DIFF LENGTH
         EX    RF,ETO16700              DSN WITHOUT GDG NO.
         BNE   ETO18500                 GO ASK FOR NEW TAPE
         B     ETO16850                 GO CHECK PASSWORD        Y02082
ETO16600 EX    R0,ETO16500              COMP 17 CHAR W/O GEN NO.
         BNE   ETO18500                 GO GET NEW TAPE
         B     ETO16850                 GO CHECK PASSWORD        Y02082
ETO16700 CLC   FL1ID(K0),K0(RET)        MOVE DSNAME
ETO16800 EQU   *                        READ/WRITE SECURITY
         TM    JFCBLTYP,JFCBAL          WERE ANSI LABEL SPEC.
         BZ    ETO16900                 NO, CHECK EXPIRE DATE    YM3841
         CLI   FL1FSEC,BLANK            UNLIMITED ACCESS TO D.S.
         BNE   ETO18500                 NO, CAN'T USE VOLUME
         B     ETO16900                 CHECK EXPIRATION DATE    YM8520
         EJECT
ETO16850 EQU   *                        GO TO VERIFY PASSWORD    Y02082
         MVC   DXRETMOD(K5),ID2B2B      SET UP RETURN            Y02082
         MVI   DXRETCOD,K20             SET UP RETURN CODE       Y02082
         NI    DEBFLGS1,X'FF'-DEBPWCKD  TURN OFF PASSWORD BIT    Y02082
         IECRES LOAD,MODID=ID2B5T,BRCODE=K0,BRANCH=QUEUED        Y02082
ETO16860 EQU   *                        RETURN FROM PASSWORD     Y02082
         OI    DEBFLGS1,DEBPWCKD        TURN ON PASSWORD BIT     Y02082
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
*        CHECK EXPIRATION DATE                                   Y02134
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
ETO16900 EQU   *                        EXPIRATION DATE CHK      Y02134
*                                                                Y02134
         XC    DXWORK,DXWORK            CLEAR WORK AREA          Y02134
         PACK  DXWORK(K4),FL1EXPDT+K1(K5)  EBCDIC TO DECIMAL     YM5368
         L     R1,CVTPTR                LOAD CVT ADDRESS         Y02080
         USING CVT,R1                   R1 POINTS TO CVT         Y02134
         CLC   CVTDATE,DXWORK           CHECK IF DATE EXPIRED    Y02080
         DROP  R1                                                Y02134
         BNL   ETO17100                 BRANCH IF DATE PASSED    Y02134
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
*        GO TO ERROR ROUTINE TO WRITE EXPIRATION DATE            Y02134
*        ERROR MESSAGE TO THE OPERATOR, AND WAIT FOR             Y02134
*        HIS REPLY TO USE OR NOT USE THE DATA SET.               Y02134
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
ETO16950 EQU   *                        GO TO ERROR ROUTINE
*                                                                Y02134
         MVC   DXRETMOD,ID2B2B          RETURN ID TO SAVE AREA   Y02134
         MVI   DXRETCOD,K4              SET RETURN INDICATOR     Y02134
*                                                                Y02134
         XC    DXWORK1,DXWORK1          SET REPLY AREA TO BLANKS Y02134
         LA    R1,DXWORK1               POINT TO REPLY AREA      Y02134
         LA    R0,K1                    SET REPLY LENGTH         Y02134
         SLL   R0,K24                   MOVE LENGTH TO HI BYTE   Y02134
         OR    R1,R0                    SET UP WTOR PARM         Y02134
         LA    R0,FL1ID                 GET DSNAME ADDRESS       Y02134
         MVI   FL1ID+L'FL1ID,BLANK      DELIMIT 17 CHAR NAME     Y02134
         MVC   FL1ID+L'FL1ID+K1(L'JFCBDSNM-L'FL1ID-K1),FL1ID+L'FL1ID
*                                                                Y02134
         IECRES LOAD,MODID=ID2B4J,BRCODE=K20,BRANCH=QUEUED       Y02134
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
*        RETURN HERE FROM EXPIRATION CHECK                       Y02134
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
ETO17000 EQU   *                        RET FROM EXPIR DATE CHK  Y02134
*                                                                Y02134
         IECRES WAIT                    WAIT FOR OPERATOR REPLY  Y02134
*                                                                Y02134
         OI    DXWORK1,BLANK            FOLD REPLY               Y02134
         CLI   DXWORK1,CHARU            CHECK FOR REPLY U        Y02134
         BE    ETO17100                 BRANCH TO USE            Y02134
*                                                                Y02134
         CLI   DXWORK1,CHARM            CHECK IF REJECT          Y02134
         BNE   ETO16950                 BRANCH, INCORRECT REPLY  Y02134
*                                                                Y02134
         LA    R0,EABD108               ABEND 137-24             YM5718
         B     ETO17900                 BRANCH TO REJECT VOLUME  Y02134
         EJECT
*                                                                Y02083
*****************************************************************Y02083
*                                                                Y02083
*        CHECKPOINT DATA SET PROCESSING                          Y02083
*                                                                Y02083
*****************************************************************Y02083
*                                                                Y02083
ETO17100 EQU   *                        CHKPT/RESTART PROC       Y02083
         CLC   AHDR1,FL1LABI            TEST FOR HDR 1           Y02083
         BNE   ETO17150                 IF NOT, BYPASS WTOR    @ZA09625
         BAL   RWTG,ETO17600            GO READ HDR 2            Y02083
         LA    RWTG,DXXAREA             RESTORE REG RWTG         YM3841
         CLC   AHDR2,FL1LABI            TEST FOR HDR 2           Y02083
         BNE   ETO17150                 IF NOT, BYPASS WTOR    @ZA09625
         CLI   FL2DSIND,FL2CKDS         TEST FOR CHKPT D/S       Y02083
         BNE   ETO17150                 IF NOT, BYPASS WTOR      Y02083
*                                                                Y02083
*        ASK OPERATOR IF VOLUME IS SECURE                        Y02083
*                                                                Y02083
ETO17110 MVC   DXRETMOD,ID2B2B          SET RETURN NAME & EPA    Y02083
         MVI   DXRETCOD,K8              SET BR TABLE RETURN CODE Y02083
         XC    DXWORK1,DXWORK1          CLEAR WTOR REPLY AREA    Y02083
         LA    R1,DXWORK1               POINT TO REPLY AREA      Y02083
         LA    R0,K3                    SET REPLY LENGTH         Y02083
         SLL   R0,K24                   MOVE LENGTH TO HI BYTE   Y02083
         OR    R1,R0                    SET UP WTOR PARM         Y02083
         IECRES LOAD,MODID=ID2B4J,BRCODE=K56,BRANCH=QUEUED       Y02083
ETO17120 IECRES WAIT                    WAIT FOR REPLY           Y02083
         OC    DXWORK1,BLANKS           FOLD REPLY TO UPPER CASE Y02083
         CLC   KNO,DXWORK1              REPLY = 'NO'             Y02083
         BE    ETO17150                 IF 'NO', BRANCH          Y02083
         CLC   KYES,DXWORK1             REPLY = 'YES'            Y02083
         BNE   ETO17110                 IF NOT, TRY AGAIN        Y02083
         L     R1,DXDSAB                FETCH DSAB ADDRESS       Y02083
         TM    DSABFLG4-DSAB(R1),DSABCKDS TEST FOR CHKPT D/S     Y02083
         BO    ETO17180                 IF YES, GO SET SECURE FLGY02083
*                                                                Y02083
*        TELL OPERATOR VOLUME IS NO LONGER SECURE                Y02083
*                                                                Y02083
ETO17130 MVC   DXRETMOD,ID2B2B          SET RETURN NAME & EPA    Y02083
         MVI   DXRETCOD,K12             SET BR TABLE RETURN CODE Y02083
         IECRES LOAD,MODID=ID2B4J,BRCODE=K60,BRANCH=QUEUED       Y02083
*                                                                Y02083
*        IF USER INTENDS TO USE D/S FOR CHECKPOINTING, SEE       Y02083
*        IF VOLUME CAN BE MADE SECURE                            Y02083
*                                                                Y02083
ETO17150 L     R1,DXDSAB                FETCH DSAB ADDRESS       Y02083
         TM    DSABFLG4-DSAB(R1),DSABCKDS TEST FOR CHKPT D/S     Y02083
         BZ    ETO17200                 IF NOT, CONTINUE         Y02083
ETO17160 MVC   DXRETMOD,ID2B2B          SET RETURN NAME & EPA    Y02083
         MVI   DXRETCOD,K16             SET BR TABLE RETURN CODE Y02083
         XC    DXWORK1,DXWORK1          CLEAR WTOR REPLY AREA    Y02083
         LA    R1,DXWORK1               POINT TO REPLY AREA      Y02083
         LA    R0,K3                    SET REPLY LENGTH         Y02083
         SLL   R0,K24                   MOVE LENGTH TO HI BYTE   Y02083
         OR    R1,R0                    SET UP WTOR PARM         Y02083
         IECRES LOAD,MODID=ID2B4J,BRCODE=K52,BRANCH=QUEUED       Y02083
ETO17170 IECRES WAIT                    WAIT FOR REPLY           Y02083
         OC    DXWORK1,BLANKS           FOLD REPLY TO UPPER CASE Y02083
         CLC   KYES,DXWORK1             REPLY = 'YES'            Y02083
         BE    ETO17180                 IF YES, BRANCH           Y02083
         CLC   KNO,DXWORK1              REPLY = 'NO'             Y02083
         BNE   ETO17160                 IF NOT, TRY AGAIN        Y02083
*                                                                Y02083
*        AT THIS POINT IT IS FOUND THAT A SUBSEQUENT VOLUME      Y02083
*        OF A CHECKPOINT D/S CAN NOT BE MADE SECURE, SO ISSUE    Y02083
*        ABEND 937-10                                            Y02083
*                                                                Y02083
         LA    R0,EABD240               LOAD INTERNAL ABEND CODE Y02083
         DMABCOND (R0),ID2B0P           GO ABEND                 Y02083
*                                                                Y02083
*        SET THE VOLUME SECURE INDICATOR IN THE DSAB             Y02083
*                                                                Y02083
ETO17180 L     R1,DXDSAB                FETCH DSAB ADDRESS       Y02083
         MODESET EXTKEY=SCHED           DSAB KEY                 Y02083
         OI    DSABFLG4-DSAB(R1),DSABCKVL SET SECURE VOL IND     Y02083
         MODESET EXTKEY=DATAMGT         BACK TO D/M KEY          Y02083
         B     ETO17200                 CONTINUE                 Y02083
         EJECT
*
***********************************************************************
*
*  REREAD VOLUME LABEL AND REWRITE IT, POSSIBLY IN A DIFFERENT DENSITY
*
***********************************************************************
*
ETO17200 EQU   *                        VOLUME LABEL PROC        Y02083
*
         BAL   RWTG,ETO17500            GO REWIND, CLEAR AND READ
         SPACE
         CLI   VOLLABI,CHARV            CHECK TAPE CONTAINS VOLID
         BE    ETO17300                 BR TO BUILD CHANNEL PROG
         CLI   VOLLABI,ASCIIV           DOES TAPE HAVE ANSI LABEL
         BE    ETO17300                 YES, WRITE LABEL
         SPACE
         MVI   VOLLABI,BLANK            CLEAR
         MVC   VOLLABI+K1(L'DXLBL-K1),VOLLABI LABEL AREA
         SPACE
         MVC   VOLLABI(K4),AVOL         VOL LBL IDENTIFIER
         MVC   VOLSERNO,UCBVOLI         VOLUME SERIAL NUMBER
         MVI   VOLSEC,K0                SECURITY CODE
         TM    JFCBLTYP,JFCBAL          WERE ANSI LABEL SPEC.
         BNO   ETO17300                 NO, GO WRITE LABEL
         MVI   VOLSEC,BLANK             SET ACCESSIBILITY BLANK
         MVI   LABSTAND,CHAR1           SET LABEL STANDARD BYTE
         XLATE DXLBL,K80,TO=A           TO ASCII
         SPACE
ETO17300 EQU   *                        BUILD CCW'S AND RACDEF @G32DSMI
         SPACE
         L     R7,DXJSCBAD              ADDRESS OF JSCB        @G32DSMI
         USING IEZJSCB,R7                                      @G32DSMI
         TM    JSCBSWT1,JSCBPASS        JSCB PASS SPECIFIED    @G32DSMI
         BO    ETO17350                 YES - SKIP RACDEF      @G32DSMI
         DROP  R7                                              @G32DSMI
         L     R7,CVTPTR                ADDRESS OF CVT         @G32DSMI
         USING CVT,R7                                          @G32DSMI
         L     R7,CVTRAC                ADDRESS OF RACF CVT    @G32DSMI
         LTR   R7,R7                    IS RACF ACTIVE         @G32DSMI
         BZ    ETO17350                 NO - SKIP RACDEF       @G32DSMI
         USING RCVT,R7                                         @G32DSMI
         TM    RCVTSTA1,RCVTTAPE        TAPE OPTION SET        @G32DSMI
         BZ    ETO17350                 NO - SKIP RACDEF       @G32DSMI
         L     R7,DXDEBXAD              ADDRESS OF THE DEB EXT @G32DSMI
         USING DEBXTN,R7                                       @G32DSMI
         TM    DEBXFLG1,DEBXDSSI        PREV VOL RACF DEFINED  @G32DSMI
         BZ    ETO17350                 NO - SKIP RACDEF       @G32DSMI
         DROP  R7                                              @G32DSMI
         TM    DXRCFLAG,DXRCOUT         TAPE ALREADY RACDEF-ED @G32DSMI
         BO    ETO17350                 YES - SKIP RACDEF      @G32DSMI
         MVC   DXCCW8(RACDFLEN),RACDFL  MOVE IN LIST FORM      @G32DSMI
         RACDEF ENTITY=DXOLDVOL,VOLSER=UCBVOLI,MF=(E,DXCCW8)   @ZM04449
         LTR   15,15                    RETURN CODE ZERO       @G32DSMI
         BZ    ETO17350                 YES CONTINUE           @G32DSMI
         LA    R0,EABD007               SET FOR 937-40         @G32DSMI
         LA    RWTG,DXXAREA             GET ADDR OF WTG TABLE  @ZM04476
         DMABCOND (R0),ID2B0P           GO ABEND               @G32DSMI
EABD007  EQU   7                                               @G32DSMI
ETO17350 EQU   *                        END RACF CODE          @G32DSMI
         MVI   DXCCW1,CCWREW            REWIND COMMAND
         SPACE
         BAL   R7,ETO17800              GO REWIND AGAIN
         SPACE
         MVI   DXCCW1,CCWWRTAP          1ST CCW IS WRITE VOL1
         SPACE
         EXCP  DXIOB                    WRITE VOL1 NXT MOD CHKS ERR
         SR    RET,RET                  ZERO RETURN REG
*
         LA    R7,MOD2D2B               SET R7 TO TTR
         EJECT
ETO17400 LA    RWTGC,DXXENTRY           SET R8 FOR RES RTN
         LA    RWTG,DXXAREA             POINT TO EOV WTG
         IECRES LOAD,MODID=(R7),BRANCH=QUEUED GO TO NEXT MODULE  Y02080
         EJECT
         SPACE
*                     REFRESH LABEL AREA
         SPACE
ETO17500 EQU   *                        REWIND, CLEAR AREA & READ
         SPACE
         MVC   DXCCW1,ACCW              SET UP TO REWIND
         SPACE
         BAL   R7,ETO17800              GO ISSUE REWIND AND WAIT
         SPACE
ETO17600 EQU   *                        CLEAR AREA & READ
         SPACE
         XC    DXLBL,DXLBL              CLEAR LABEL AREA
         SPACE
ETO17700 EQU   *                        READ
         SPACE
         ST    RCORE,DXCCW1             GET ADDRESS OF READ AREA
         MVI   DXCCW1,CCWRDTAP          AND READ OP CODE
         MVI   DXCCW1,CCWRDTAP          AND READ OP CODE
         SPACE
         BAL   R7,ETO17800              GO ISSUE READ AND WAIT
         SPACE
         BR    RWTG                     RETURN TO MAIN LINE
         EJECT
         SPACE
*                   CLOSED SUBROUTINE TO ISSUE I/O OPERATIONS
         SPACE 1
*                        IT IS ASSUMED THAT ALL CONTROL BLOCKS
*                        HAVE ALREADY BEEN SET UP
         SPACE 1
*                             BAL  R7,PRHR50
*                             RETURN
         SPACE 1
ETO17800 EXCP  DXIOB                    ISSUE I/O OPERATION
*
         WAIT  ,ECB=DXECB               WAIT FOR COMPLETION OF I/O OPER
*
         TM    DXECB,ECBNOERR           IS THERE AN UNUSUAL I/O COND
         BCR   1,R7                     NO - RETURN TO CALLER
         SPACE
         TM    IOBSTAT0,CSWUNITX        CHK FOR UNIT EXCEPTION
         BCR   1,R7                     YES - IGNORE FOR NOW
         SPACE
ETO17900 EQU   *                        I/O ERR GO ASK FOR NEW VOL
         LA    R0,EABD144               613-04 ABEND INDICATOR   YM8520
         SR    RET,RET                  ZERO RETURN REG
         LA    R7,ID2B2N                GO TO REMOUNT            Y02134
         B     ETO17400                 BR TO DO XCTL
         EJECT
ETO18500 EQU   *                        CHECK FOR ASCII LABEL
         TM    JFCBLTYP,JFCBAL          IS THIS ASCII LABEL
         BO    ETO18700                 YES, BR
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
*        GO ISSUE SEC VOL MESSAGE                                Y02134
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
         MVC   DXRETMOD,ID2B2N          SET UP RETURN LOAD       Y02134
         MVI   DXRETCOD,K0              RETURN OFFSET            Y02134
         LA    R0,EABD109               ABEND 937-24             YM5718
*                                                                Y02134
         IECRES LOAD,MODID=ID2B4J,BRCODE=K40,BRANCH=QUEUED       Y02134
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
*        GO ISSUE ASCII DISMOUNT MESSAGE                         Y02134
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
ETO18700 EQU   *                        ASCII DISMOUNT MSG       Y02134
*                                                                Y02134
         MVC   DXRETMOD,ID2B2N          SET UP RETURN LOAD       Y02134
         MVI   DXRETCOD,K0              RETURN OFFSET            Y02134
*                                                                Y02134
         LA    R1,K2                    SET ASCII ERROR CODE     Y02134
         LA    R0,EABD249               ABEND 937-20             YM5718
*                                                                Y02134
         IECRES LOAD,MODID=ID2B4J,BRCODE=K8,BRANCH=QUEUED        Y02134
*                                                                Y02134
         EJECT
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
*                   CONSTANTS
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
ACCW     DC    X'0700000020000050'      BASIC CCW - REWIND COMMAND
AHDR1    DC    C'HDR1'                  CHAR HDR1
AHDR2    DC    C'HDR2'                  CHAR HDR2                Y02083
AVOL     DC    C'VOL1'                  CHAR VOL1
UVOL0G   DC    C'UVL'                   USER VOLUME LABEL
CONSTONE DC    X'0001'                  CONSTANT OF ONE          YM6559
KNO      DC    C'NO '                   WTOR REPLY COMPARAND     Y02083
KYES     DC    C'YES'                   WTOR REPLY COMPARAND     Y02083
BLANKS   DC    CL4' '                   CHAR BLANKS              Y02083
SCRTCH   DC    C'SCRTCH'                SCRATCH REQUEST        @ZA02874
RACDFL   RACDEF TYPE=ADDVOL,CLASS='TAPEVOL',MF=L RACDEF        @G32DSMI
RACDFLEN EQU   *-RACDFL                 LENGTH OF PARM LIST    @G32DSMI
*
         EJECT
*                                                                Y02083
*****************************************************************Y02083
*                                                                Y02083
*        EQUATES                                                 Y02083
*                                                                Y02083
*****************************************************************Y02083
*                                                                Y02083
FL2CKDS  EQU   C'C'                     FL2 CHECKPOINT D/S IND   Y02083
EABD240  EQU   240                      937-10 INTERNAL CODE     Y02083
         EJECT
*
XCTLTA2B XCTLTABL ID=(ID2B4J,4J,ID2B2B,2B,MOD2D2B,2D,ID2B2F,2F,  Y02134X
               ,NSLEHDRO,ID2B2N,2N,ID2B0P,0P,ID2B5T,5T),         Y02082X
               SVC=055,BRT=YES,LENGTH=                           Y02134
         IECDSECS CVT,TCB,DCB,UCB,MAIN,TIOT,WTG,PREFX,IEZDEB,  @G32DSMI*
               DSAB,RACVT,JSCB,RRPL,EXPAND=YES                 @G32DSMI
         IECEQU IEZDEB=YES
         END
