         TITLE 'IFG0552V - EOV TAPE INPUT VOLUME POSITIONING FUNCTION'
         COPY  LCGASMSW
IFG0552V CSECT
***********************************************************************
*          VS2 RELEASE 03 DELETIONS                                   *
*0000                                                          ZA09140*
*          VS2 RELEASE 02 DELETIONS                                   *
*                                                                     *
*                                                                     *
***********************************************************************
*                                                                     *
* MODULE NAME = IFG0552V (OS/VS2)                                     *
*                                                                     *
* DESCRIPTIVE NAME = INPUT TAPE - VOLUME POSITIONING FUNCTION         *
*                                                                     *
* COPYRIGHT = NONE                                                    *
*                                                                     *
* STATUS = RELEASE 2, LEVEL 0                                         *
*                                                                     *
* FUNCTION =                                                          *
*        1) IF THE DCB CONTAINS A TAPE DEVICE DEPENDENT SECTION,      *
*        THE BLOCK COUNT IS ZEROED.                                   *
*        2) THE VOLUME SEQUENCE COUNT IN THE DEB IS INCREMENTED       *
*        (READ FORWARD) OR DECREMENTED (READ BACKWARD) BY ONE.        *
*        3) FOR READ BACKWARD, IF THE NEXT VOLUME IS THE LAST ONE,    *
*        (IE. FIRST PHYSICAL VOLUME OF THE DATA SET), THE FILE        *
*        SEQUENCE NUMBER SAVED IN THE WORK AREA IS SET EQUAL TO       *
*        THE FILE SEQUENCE COUNT.                                     *
*        4) IF ONLY ONE UNIT IS ALLOCATED TO THE DATA SET, GO         *
*        TO (7) BELOW.                                                *
*        5) THE TAPE IS POSITIONED ACCORDING TO OPTIONS SELECTED      *
*        BY FEOV, OPEN, OR DISP PARAMETER IN DD CARD IN THAT          *
*        ORDER OF PRECEDENCE.                                         *
*        6) THIS FUNCTION PERFORMS ALL VOLUME POSITIONING FOR DATA    *
*        SETS AT EOV OR EOF (WHEN CONCATENATION IS TAKING PLACE) ON   *
*        AL, NL, AND SL TAPES; NSL VOLUMES CAN ONLY BE UNLOADED.      *
*        THE FOLLOWING TERMINOLOGY APPLIES:                           *
*             LOGICAL END OF DATA SET-                                *
*             FOR READ FORWARD- THE INTERBLOCK GAP FOLLOWING          *
*             EITHER THE TAPE MARK FOLLOWING THE DATA (NL) OR         *
*             THE TAPE MARK FOLLOWING THE DATA SET TRAILER LABELS     *
*             (AL AND SL).                                            *
*             FOR READBACK- THE INTERBLOCK GAP PRECEDING EITHER       *
*             THE FIRST DATA RECORD (NL) OR HEADER LABEL 1 (AL,SL).   *
*             LOGICAL BEGINNING OF DATA SET-                          *
*             FOR READ FORWARD- SAME AS FOR READBACK ABOVE.           *
*             FOR READBACK- SAME AS READ FORWARD ABOVE.               *
*        6A) POSITION ACCORDING TO ANY FEOV OPTIONS:                  *
*           REWIND- REWIND THE TAPE.                                  *
*           LEAVE- POSITION THE TAPE TO THE LOGICAL END OF            *
*           THE DATA SET.                                             *
*        6B) POSITION ACCORDING TO ANY POSITIONING OPTION SPECIFIED   *
*           IN THE OPEN MACRO:                                        *
*           REREAD- POSITION TAPE TO LOGICAL BEGINNING OF DATA SET    *
*           LEAVE- POSITION TAPE TO LOGICAL END OF DATA SET.          *
*           DISP- REFER TO 6C) BELOW.                                 *
*        6C) IF THERE ARE NONE OF THE ABOVE OPTIONS OR IF THE OPEN    *
*        POSITIONING OPTION IS DISP, POSITIONING TAKES PLACE          *
*        AS FOLLOWS:                                                  *
*           PASSED DATA SET- POSITION TO LOGICAL END OF D.S.          *
*           NOT PASS, PUBLIC VOL- REWIND THE TAPE                     *
*           NOT PASS, PRIVATE VOL- UNLOAD THE TAPE                    *
*        7) INCREMENT THE UCB FILE SEQUENCE COUNT AND NUMBER BY       *
*        ONE EACH WHEN THE TAPE IS POSITIONED TO THE PHYSICAL END OF  *
*        THE DATA SET, AND ZERO THE DATA MANAGEMENT COUNT IN THE UCB. *
*        8) IF THERE ARE CONCATENATED DATA SETS, FORWARD CHAIN TO THE *
*        NEXT DSAB AND UPDATE THE WORKAREA POINTERS REFLECTING THE    *
*        NEW DSAB AND TIOT ENTRY, UPDATE THE DCB,DEB, UCB,AND DSABS   *
*        (DECREMENT OPEN COUNT IN OLD DSAB, INCREMENT IN NEW DSAB) AND*
*        DEB EXTENSION.                                               *
*        9) TRANSFER CONTROL TO THE EOV INITIAL FUNCTION TO CONTINUE  *
*        CONCATENATION (IFG0551F).                                    *
*        10) FOR NON-CONCATENATED DATA SETS, UPDATE THE VOLUME        *
*        SEQUENCE NUMBER IN THE WORK AREA AND TRANSFER CONTROL        *
*        TO IFG0554A TO PERFORM MOUNT/VERIFY FUNCTION FOR NEXT        *
*        VOLUME. SET UP FOR RETURN TO IFG0553F.                       *
*                                                                     *
* NOTES = SEE BELOW                                                   *
*                                                                     *
*    DEPENDENCIES =                                                   *
*            CLASS ONE CHARACTER CODE.  THE EBCDIC CHARACTER CODE     *
*            WAS USED FOR ASSEMBLY.  THE MODULE MUST BE REASSEMBLED   *
*            IF A DIFFERENT CHARACTER SET IS USED FOR EXECUTION.      *
*                                                                     *
*    RESTRICTIONS = NONE                                              *
*                                                                     *
*    REGISTER CONVENTIONS =                                           *
*            R2 POINTS TO DCB                                         *
*            R4 POINTS TO OPEN WORK AREA                              *
*            R5 POINTS TO THE RESIDENT ROUTINE                        *
*            R6 POINTS TO THE WTG TABLE                               *
*            R7 POINTS TO THE CURRENT PARAMETER LIST ENTRY            *
*            R8 POINTS TO THE CURRENT WTG TABLE ENTRY                 *
*            R9 POINTS TO THE DD ENTRY IN THE TIOT                    *
*            R10 POINTS TO THE UCB                                    *
*                                                                     *
*    PATCH LABEL = SEE THIRD FROM LAST LABEL BEFORE ORG STATEMENT AT  *
*                  END OF LISTING.                                    *
*                                                                     *
* MODULE TYPE = CONTROL (OPEN, CLOSE, EOV DATA MANAGEMENT)            *
*                                                                     *
*    PROCESSOR = ASSEMBLER XF                                         *
*                                                                     *
*    MODULE SIZE = SEE EXTERNAL SYMBOL DICTIONARY OR LOC FIELD ON     *
*                  ORG STATEMENT AT END OF LISTING                    *
*                                                                     *
*    ATTRIBUTES = REENTRANT, REFRESHABLE,READ-ONLY, ENABLED,          *
*                 PRIVILEGED, SUPERVISOR STATE, DATA MANAGEMENT KEY,  *
*                 LINK PACK AREA RESIDENT/PAGEABLE                    *
*                                                                     *
* ENTRY POINT =                                                       *
*        IFG0552V                                                     *
*                                                                     *
*    PURPOSE = SEE FUNCTION                                           *
*                                                                     *
*    LINKAGE =                                                        *
*        THIS MODULE IS TRANSFERRED CONTROL THROUGH THE IECRES-LOAD   *
*        MACRO INSTRUCTION.                                           *
*                                                                     *
* INPUT =                                                             *
*        GIVEN CONTROL IN PROTECT KEY 5.                              *
*        REGISTER 2 POINTS TO THE COPIED DCB.                         *
*        DEBDCBAD POINTS TO THE COPIED DCB.                           *
*        REGISTER 4 POINTS TO THE EOV WORKAREA                        *
*                                                                     *
* OUTPUT =                                                            *
*        THE NEXT MODULE IS GIVEN CONTROL IN PROTECT KEY 5 WITH       *
*        REGISTER 2 POINTING TO THE COPIED DCB,                       *
*        DEBDCBAD POINTING TO THE COPIED DCB,                         *
*        AND REGISTER 4 POINTING TO THE EOV WORKAREA,                 *
*                                                                     *
* EXIT-NORMAL =                                                       *
*        IFG0551F - LIKE ATTRIBUTE CONCATENATION                      *
*        IFG0554A - MOUNT/VERIFY FUNCTION                             *
*           RETURN MODULE - IFG0553F                                  *
*                                                                     *
* EXIT-ERROR =                                                        *
*        IFG0550P - PROBLEM DETERMINATION                             *
*             INTERNAL CODE = 151, POSITIONING ERROR FOR LEAVE        *
*             INTERNAL CODE = 152, POSITIONING ERROR FOR REREAD       *
*             INTERNAL CODE = 153, POSITIONING ERROR FOR PASS         *
*             INTERNAL CODE = 155, POSITIONING ERROR FOR REWIND       *
*                                                                     *
* EXTERNAL REFERENCES = SEE BELOW                                     *
*                                                                     *
*    ROUTINES =                                                       *
*        IFG019RA THROUGH THE IECRES MACRO.                           *
*                                                                     *
*    DATA AREAS =                                                     *
*        EOV WORKAREA.                                                *
*                                                                     *
*    CONTROL BLOCK =                                                  *
*        DSAB                                                         *
*        DEB                                                          *
*        JFCB                                                         *
*        TCB                                                          *
*        UCB                                                          *
*        TIOT                                                         *
*                                                                     *
* TABLES =                                                            *
*                                                                     *
* MACROS =                                                            *
*        IECRES LOAD                                                  *
*        DMABCOND                                                     *
*                                                                     *
* CHANGE ACTIVITY =                                                   *
*        SEE CHANGES/DELETIONS SECTION JUST AFTER CSECT CARD.         *
*                                                                     *
***********************************************************************
         EJECT
*
*****    INTERNAL ERROR CODES
*
EABD151  EQU   151                      ERROR POSITIONING FOR LEAVE
EABD152  EQU   152                      ERROR POSITIONING FOR REREAD
EABD153  EQU   153                      ERROR POSITIONING FOR 'PASS'
EABD155  EQU   155                      ERROR REWINDING FOR COBOL
*                                       ..REWIND OR 'DELETE'
*
         BALR  RBASE,0                  ESTABLISH ADDRESSABILITY
         USING *,RBASE
         USING FORCORE,RCORE
         USING IHADCB,RDCB
         USING UCB,RUCB
         USING TIOENTRY,RTIOT
         USING DEB,RDEB
         USING WTG,RWTG
*
         B     ETI08000(RET)            DETERMINE WHERE TO CONT PROCESS
ETI08000 EQU   *                        BRANCH TABLE
         B     ETI08100                 BR IF NORMAL ENTRY (0)
         B     ETI08100                 BR IF RETURN FROM SMF (4)
         EJECT
*
***********************************************************************
*              COMPLETE PROCESSING ON CURRENT VOLUME                  *
***********************************************************************
*
ETI08100 EQU   *                        CURRENT VOLUME PROC
         L     RUCB,DXUCBADR            LOAD UCB ADDRESS         Y02134
         TM    DCBMACRF,DCBMEXCP        IS THE ACCESS METHOD EXCP-
         BZ    ETI08200                 ..BR IF NOT TO ZERO BLK CT
         TM    DCBMACRF+K1,DCBMDEV      IS THERE A TAPE DEPEND SECTION-
         BZ    ETI08300                 ..BR IF NONE- CAN'T ZERO BLK CT
ETI08200 EQU   *                        NOT EXCP
         XC    DCBBLKCT,DCBBLKCT        ZERO BLOCK COUNT IN DCB
ETI08300 EQU   *                        CHECK FOR CONCATENATION
         TM    DCBOFLGS,DCBOCON         IS CONCATENATION IN PROGRESS-
         BO    ETI08600                 ..BR IF YES TO POSITION VOL
         TM    DCBOFLGS,DCBORDBK        TEST FOR READ BACK       Y02134
         BO    ETI08400                 BRANCH IF RDBACK         Y02134
         LH    RC,DXVOLSEQ              LOAD VOLUME SEQUENCE     Y02134
         LA    RC,K1(RC)                INCREMENT BY ONE         Y02134
         STH   RC,DEBTVLSQ              ..STORE IT BACK IN DEB
         B     ETI08500                 BR TO POSITION VOL AT E-O-V
ETI08400 EQU   *                        READ BACK PROCESSING
         LH    RC,DXVOLSEQ              LOAD VOLUME SEQUENCE     Y02134
         BCTR  RC,R0                    DECREMENT BY ONE         Y02134
         STH   RC,DEBTVLSQ              ..STORE IT BACK IN DEB   Y02134
         CLI   DEBTVLSQ+K1,K1           IS THE NEXT VOL THE LAST ONE-
         BNE   ETI08500                 ..BR IF NOT TO POSITION VOL
         MVC   DXCCW7+K4(K2),DXCCW7+K6  ..YES- MOVE FILE SEQ NO. TO
*                                       ..FILE SEQ CT IN WORK AREA
***********************************************************************
*              CURRENT VOLUME POSITIONING                             *
***********************************************************************
*
ETI08500 EQU   *                        POSITION CURRENT VOLUME
         CLI   DXNOUNIT+K1,K1           IS ONE UNIT ALLOCATED TO DS-
         BE    ETI11000                 ..BR IF YES- NO POSITIONING
ETI08600 EQU   *                        MORE THAN ONE UNIT
         TM    UCBFL2,UCBNOTRD          IS THE UNIT 'READY'-
         BO    ETI11000                 ..BR IF NOT
         TM    JFCBLTYP,JFCNSL          WAS NSL SPECIFIED-
         BO    ETI09600                 ..BR IF YES TO AVOID FEOV POS
         MODESET EXTKEY=SUPR            UCB KEY                  Y02082
         NI    UCBWGT,ALLBITS-UCBREW    TURN OFF THE UCB RWD BIT Y02080
         MODESET EXTKEY=DATAMGT         DATA MANAGEMENT KEY      Y02082
         MVI   DXCCW1,CCWFSF            INITLZ CCW FOR FWD SPACE FILE
*
***********************************************************************
*              POSITIONING FOR FEOV                                   *
***********************************************************************
*
         TM    DCBMACRF,DCBMEXCP        TEST FOR EXCP           SA59750
         BO    ETI09600                 BRANCH IF EXCP          SA59750
         TM    DCBCIND2,DCBC2FEO        IS THIS AN FEOV ENTRY-
         BNO   ETI09600                 ..BR IF NOT TO CONTINUE
         L     RD,DXXPREFX              LOAD PREFX ADDRESS       Y02080
         USING IECPREFX,RD              PREFIX ADDRESSABILITY    Y02080
         L     RD,IECUPRML              LOAD PARMLIST ADDRESS    Y02080
         USING USERPRML,RD              PARM LIST ADDRESSABILITY Y02080
         TM    DXDCBLST,PLISTLV         TEST FOR COBOL/FEOV OPTIONS-
         DROP  RD                                                Y02080
         BM    ETI09100                 ..BR IF FEOV 'REWIND'
         BO    ETI08700                 ..BR IF COBOL/FEOV 'LEAVE'
         TM    DEBOPATB,DEBOPLV         IS THERE AN 'OPEN' POS OPTION-
         BZ    ETI10500                 ..BR IF NOT
         BM    ETI09200                 ..BR IF 'REREAD'
*
*****          POSITION FOR LEAVE FOLLOWING FORWARD READ
*
ETI08700 EQU   *                        LEAVE POSITIONING
         LA    RD,EABD151               LOAD INTERNAL ERROR CODE
         BAL   RC,ETI11500              INCREMENT THE FILE SEQUENCE
*                                       ..COUNT AND NUMBER IF REQ'D
         TM    DCBOFLGS,DCBORDBK        TEST FOR 'RDBACK' PROCESSING-
         BO    ETI08900                 ..BR IF YES TO HANDLE 'RDBACK'
         TM    DCBOFLGS,DCBOEOF         WAS A TAPE MARK READ-
         BO    ETI08800                 ..BR IF YES- BYPASS FIRST FSF
         BAL   RC,ETI11700              BR AND LINK TO FSF PAST TM
*                                       ..WHICH FOLLOWS DATA
ETI08800 EQU   *                        TAPE MARK READ
         TM    JFCBLTYP,JFCBAL+JFCSL    WAS AL OR SL SPECIFIED-
         BZ    ETI10400                 ..BR IF NOT
         B     ETI10300                 ..ELSE BR TO PERFORM 'FSF' CMD
*
*****          POSITION FOR LEAVE FOLLOWING 'RDBACK'
*
ETI08900 EQU   *                        LEAVE POSITIONING
         MVI   DXCCW1,CCWBSF            INITLZ CCW FOR 'BSF' COMMAND
         TM    DCBOFLGS,DCBOEOF         WAS A TAPE MARK READ-
         BO    ETI09000                 ..BR IF YES
         BAL   RC,ETI11700              BR AND LINK TO PERFORM 'BSF'
ETI09000 EQU   *                        TAPE MARK READ
         TM    JFCBLTYP,JFCBAL+JFCSL    WAS AL OR SL SPECIFIED-
         BZ    ETI10300                 ..BR IF NOT TO FSF
         BAL   RC,ETI11700              BR AND LINK TO PERFORM 'BSF'
         B     ETI10300                 BR TO PERFORM LAST FSF
*
*****          POSITION FOR COBOL/FEOV 'REWIND'
*
ETI09100 EQU   *                        COBOL/FEOV POSITIONING
*                                                                YM7834
         MODESET EXTKEY=ZERO            UCB KEY                  YM7834
*                                                                YM7834
         XC    UCBFSCT,UCBFSCT          FORCE REWIND             YM7834
         XC    UCBFSEQ,UCBFSEQ          CLEAR FILE COUNTERS      YM7834
*                                                                YM7834
         MODESET EXTKEY=DATAMGT         DM KEY                   YM7834
*                                                                YM7834
         B     ETI11000                 BR- POSITIONING COMPLETE Y02134
*
*****          POSITION FOR 'REREAD' FOLLOWING FORWARD READ
*
ETI09200 EQU   *                        REREAD POSITIONING
         LA    RD,EABD152               REREAD POSITIONING ERROR CODE
         TM    DCBOFLGS,DCBORDBK        IS THE DCB OPENED FOR 'RDBACK'-
         BO    ETI09400                 ..BR IF YES
         MVI   DXCCW1,CCWBSF            INITLZ CCW FOR BSF COMMAND
         TM    DCBOFLGS,DCBOEOF         WAS A TAPE MARK READ-
         BNO   ETI09300                 ..BR IF NOT
         BAL   RC,ETI11700              BR AND LINK TO BSF
ETI09300 EQU   *                        CHECK FOR SL/AL
         TM    JFCBLTYP,JFCBAL+JFCSL    WAS SL OR AL SPECIFIED-
         BZ    ETI10000                 ..BR IF NOT TO PERFORM 1 BSF
         B     ETI09900                 ..ELSE BR TO PERFORM 2 BSF
*
*****          POSITION FOR 'REREAD' FOLLOWING 'RDBACK'
*
ETI09400 EQU   *                        REREAD POSITIONING
         TM    DCBOFLGS,DCBOEOF         WAS A TAPE MARK READ-
         BNO   ETI09500                 ..BR IF NOT-BYPASS 1ST FSF
         BAL   RC,ETI11700              BR AND LINK TO FSF
ETI09500 EQU   *                        INCREMENT UCB FILE SEQ
         BAL   RC,ETI11600              INCREMENT UCB FILE SEQ  SA56485
*                                       COUNT AND NO.           SA56485
         TM    JFCBLTYP,JFCBAL+JFCSL    WAS SL OR AL SPECIFIED-
         BZ    ETI10300                 ..BR IF NOT- DO 1 FSF
         BAL   RC,ETI11700              BR AND LINK TO FSF
         B     ETI10300                 BR TO TO SECOND FSF
         EJECT
***********************************************************************
*              POSITION ACCORDING TO 'OPEN' OPTION 2                  *
***********************************************************************
*
ETI09600 EQU   *                        OPEN 2 POSITIONING
         TM    DEBOPATB,DEBOPLV         TEST FOR 'OPEN' OPTION 2
         BZ    ETI10500                 ..BR IF NONE SPECIFIED
         TM    JFCBLTYP,JFCNSL          WAS NSL SPECIFIED-
         BO    ETI11000                 ..BR IF YES- DON'T POS FOR NSL
         TM    DEBOPATB,DEBOPLV         WAS 'REREAD' SPECIFIED-
         BM    ETI09700                 ..BR IF YES
*
*****          POSITION FOR 'OPEN' OPTION 2 'LEAVE'
*
ETI09650 EQU   *                        OPEN 2 LEAVE
         BAL   RC,ETI11500              BR AND LINK TO INCR SEQ CT/NO.
*                                       ..IF IN READ FORWARD MODE
         TM    JFCBLTYP,JFCBAL+JFCSL    WAS SL OR AL SPECIFIED-
         BM    ETI11000                 ..BR IF YES- POS COMPLETE
         TM    DCBOFLGS,DCBORDBK        IS THE DCB OPENED FOR 'RDBACK'-
         BZ    ETI11000                 ..BR IF NOT- POS COMPLETE
         TM    DCBOFLGS,DCBOEOF         WAS A TAPE MARK READ-
         BZ    ETI11000                 ..BR IF NOT- POS COMPLETE
         B     ETI10300                 BR TO FSF PAST LEADING TAPE MK
*
*****          POSITION FOR 'OPEN' OPTION 2 'REREAD' FOLLOWING FWD READ
*
ETI09700 EQU   *                        OPEN 2 REREAD
         LA    RD,EABD152               LOAD INTERNAL ERROR CODE
         TM    DCBOFLGS,DCBORDBK        IS THE DCB OPENED FOR 'RDBACK'-
         BO    ETI10200                 ..BR IF YES
         LA    RF,K1                    CHECK FOR FIRST FILE     YM7526
         CH    RF,UCBFSCT               CHECK IF ON FIRST        YM7526
         BNL   ETI09100                 BRANCH TO REWIND         YM7526
         MVI   DXCCW1,CCWBSF            INITLZ CCW FOR BACKSPACE FILE
         TM    JFCBLTYP,JFCBAL+JFCSL    WAS AL OR SL SPECIFIED-
         BZ    ETI09900                 ..BR IF NOT TO DO 2 BSF
         BAL   RC,ETI11700              BR AND LINK TO BACKSPACE FILE
ETI09800 EQU   *                        BSF
         BAL   RC,ETI11700              BR AND LINK TO BACKSPACE FILE
ETI09900 EQU   *                        BSF
         BAL   RC,ETI11700              BR AND LINK TO BACKSPACE FILE
ETI10000 EQU   *                        BSF
         BAL   RC,ETI11700              BR AND LINK TO BACKSPACE FILE
ETI10100 EQU   *                        NOP
         MVI   DXCCW1,CCWNOP            INITLZ CCW FOR 'NOP'
         BAL   RC,ETI11700              BR AND LINK TO PERFORM 'NOP'-
*                                       ..(DETECT 'BOT' ON LAST 'BSF')
         TM    IOBSENS1,UCBLDPT         IS TAPE NOW AT LOAD POINT-
         BO    ETI11000                 ..BR IF YES- POS COMPLETE
         B     ETI10300                 ..BR IF NOT TO BYPASS LEAD TM
*
*****          POSITION FOR 'OPEN OPTION 2' 'REREAD' FOLLOWING 'RDBACK'
*
ETI10200 EQU   *                        INCREMENT FILE SEQ CT/NO.
         BAL   RC,ETI11600              BR AND LINK TO INCR SEQ CT/NO.
         MVI   DXCCW1,CCWFSF            INITLZ CCW FOR 'FSF' COMMAND
         TM    JFCBLTYP,JFCBAL+JFCSL    WAS AL OR SL SPECIFIED-
         BZ    ETI10250                 ..BR IF NOT- BYPASS FIRST FSF
         BAL   RC,ETI11700              BR AND LINK TO FWD SPACE FILE
ETI10250 EQU   *                        CHECK TAPE MARK READ
         TM    DCBOFLGS,DCBOEOF         WAS A TAPE MARK READ-
         BNO   ETI10300                 ..BR IF NOT- BYPASS FSF
         BAL   RC,ETI11700              BR AND LINK TO FSF
ETI10300 EQU   *                        FSF
         MVI   DXCCW1,CCWFSF            INITLZ CCW FOR 'FSF' COMMAND
*                                       ..(ENTRY MAY FOLLOW 'BSF' CMD)
         BAL   RC,ETI11700              BR AND LINK TO FWD SPACE FILE
         TM    IOBSENS1,UCBLDPT         WAS THE TAPE AT LOAD POINT-
*                                       ..(THIS CAN OCCUR IF THIS FSF
*                                       ..FOLLOWS THE BSF FOR FEOV NL
*                                       ..'LEAVE' FOLLOWING 'RDBACK')
         BO    ETI11000                 ..BR IF YES- POS COMPLETE
ETI10400 EQU   *                        NOP
         MVI   DXCCW1,CCWNOP            INITLZ CCW FOR 'NOP' CMD
         BAL   RC,ETI11700              BR AND LINK TO PERFORM 'NOP'
*                                       (DETECT ERR ON LAST 'FSF' CMD)
         B     ETI11000                 BR- POSITIONING COMPLETE
*
***********************************************************************
*              POSITION ACCORDING TO DATA SET DISPOSITION-            *
*              NO 'OPEN' OPTION 2 WAS SPECIFIED                       *
***********************************************************************
*
ETI10500 EQU   *                        CHECK FOR RESERVED VOLUME
         TM    SRTEJBNR,UCBMONT         IS DISPOSITION 'PASS'    Y02134
         BNO   ETI09100                 ..BR IF NOT              YM7526
*
*****          POSITION A DATA SET WITH DISPOSITION 'PASS' OR
*****          A DATA SET ON A RESERVED VOLUME
*
ETI10600 EQU   *                        CHECK FOR NSL
         LA    RD,EABD153               LOAD INTERNAL ERROR CODE
         TM    JFCBLTYP,JFCNSL          WAS NSL SPECIFIED-
         BO    ETI11000                 ..BR IF YES- ALL DONE
         TM    DCBMACRF,DCBMEXCP        TEST FOR EXCP           SA59750
         BO    ETI09650                 BRANCH IF EXCP          SA59750
         TM    DCBCIND2,DCBC2FEO        WAS 'FEOV' ISSUED-
         BO    ETI08700                 ..BR IF YES TO FINISH FEOV POS
         B     ETI09650                 ..ELSE BR TO FINISH EOV POS
*
***********************************************************************
*              EXITS                                                  *
***********************************************************************
*
*****          PREPARE TO XCTL TO NEW-VOLUME MOUNTING MODULE
*****          OR TO INITIALIZATION MODULE ( CONCATENATED DATA SETS )
*
ETI11000 EQU   *                        DECREMENT DATA MGT COUNT
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
*        DECREMENT DM COUNT                                      Y02134
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
         MODESET EXTKEY=SUPR            ASSUME KEY ZERO          Y02082
         MVI   UCBDMCT,K0               SET DM COUNT TO ZERO     Y02134
         MODESET EXTKEY=DATAMGT         DATA MANAGEMENT KEY      Y02082
         OI    DXATEOV,DXATNVOL         INDIC CURR UNIT INACTIVE YM6520
         TM    DCBOFLGS,DCBOCON         IS CONCATENATION IN PROGRESS-
         BZ    ETI11400                 ..BR IF NOT TO EXIT
*
*****          COMPLETE CONCATENATION PROCESSING
*
ETI11200 EQU   *                        CONCATENATION PROCESSING
*
*****          INITIALIZE DCB AND DEB FOR CONCATENATED DATA SET
*
*                                                                Y02134
         L     RC,DXDSABAD              GET CURRENT DSAB         Y02134
         USING DSAB,RC                                           Y02134
* UPDATE WORKAREA, DCB, DEB WITH NEW DSAB/TIOT ENTRY AND         Y02134
* INCREMENT DSAB OPEN COUNT                                      Y02134
         L     RC,DSABFCHN              GET NEXT DSAB ADDRESS    Y02134
         ST    RC,DXDSABAD              STORE IN WORKAREA        Y02134
         ST    RC,DXDSAB                CURRENT DSAB             Y02134
         L     RTIOT,DSABTIOT           NEXT TIOT ENTRY          Y02134
         ST    RTIOT,DXTIOTAD           SAVE IN WORKAREA         Y02134
         L     RF,DXTCBADR              GET CURRENT TCB          Y02134
         L     RF,TCBTIO-TCB(RF)        TIOT START ADDRESS       Y02134
         SR    RTIOT,RF                 RTIOT IS NEW TIOT OFFSET Y02134
         STH   RTIOT,DCBTIOT            UPDATE DCBTIOT           Y02134
         L     RTIOT,DXTIOTAD           RESTORE RTIOT            Y02134
         MODESET EXTKEY=SCHED           ASSUME KEY ONE           Y02082
         OI    DSABFLG2,DSABOPEN        SET OPEN BIT IN NEW DSAB Y02134
         MODESET EXTKEY=DATAMGT         DATA MANAGEMENT KEY      Y02082
         DROP  RC                                                Y02134
         L     RC,DXDEBXAD              GET DEB EXTENSION ADD    Y02134
         USING DEBXTN,RC                                         Y02134
         MVC   DEBXDSAB,DXDSABAD        UPDATE DSAB PTR IN DEBX  Y02134
         MVC   DXVOLSEQ(2),ONE          INITIALIZE VOLSEQ       ZA09140
         MVC   DEBTVLSQ(2),DXVOLSEQ     UPDATE THE DEB          ZA09140
         LA    RF,ID2V1F                INIT CONCATENATION       Y02134
         IECRES LOAD,MODID=(RF),BRCODE=K8,BRANCH=QUEUED          YM1277
*                                                                Y02134
ETI11400 EQU   *                        TEST FOR READBACK
*
         LH    R1,DXVOLSEQ              GET VOL SEQ              Y02134
*                                                                Y02134
         TM    DCBOFLGS,DCBORDBK        TEST FOR READ BACK       Y02134
         BO    ETI11450                 BRANCH IF READ BACK      Y02134
*                                                                Y02134
         LA    R1,K1(R1)                INCREMENT BY ONE         Y02134
         B     ETI11460                 BRANCH TO UPDATE VOL SEQ Y02134
*                                                                Y02134
ETI11450 EQU   *                        READ BACK                Y02134
*                                                                Y02134
         BCTR  R1,R0                    DECREMENT BY ONE         Y02134
*                                                                Y02134
ETI11460 EQU   *                        UPDATE VOLUME SEQ        Y02134
*                                                                Y02134
         STH   R1,DXVOLSEQ              UPDATE VOL SEQ           Y02134
*                                                                Y02134
         LA    RF,ID2V4A                SET UP FOR MOUNT/VERIFY  Y02134
         MVC   DXRETMOD,ID2V3F          SET UP RETURN MOD        Y02134
*                                                                Y02134
ETI11470 EQU   *                                                SA47382
*                                                                Y02134
         IECRES LOAD,MODID=(RF),BRCODE=K0,BRANCH=QUEUED          Y02134
*
         EJECT
***********************************************************************
*              SUBROUTINES                                            *
***********************************************************************
*
*****          INCREMENT THE UCB FILE SEQUENCE COUNT AND NUMBER
*
ETI11500 EQU   *                        CHECK FOR READBACK
         TM    DCBOFLGS,DCBORDBK        IS THE DCB OPENED FOR 'RDBACK'-
         BCR   1,RC                     ..BR IF YES TO RET TO CALLER
ETI11600 EQU   *                        NOT READBACK
         L     RF,SRTEFSCT              LD THE UCB FILE SEQ CT AND NO.,
         A     RF,CNST0101              ..INCR EACH NUMBER BY 1, THEN
         MODESET EXTKEY=SUPR            TIOT/UCB KEY             Y02082
         ST    RF,SRTEFSCT              ..STORE THEM BACK IN THE UCB
         MODESET EXTKEY=DATAMGT         DATA MANAGEMENT KEY      Y02082
         BR    RC                       RETURN TO CALLER
         SPACE 2
*
*****          CLOSED SUBROUTINE TO PERFORM I/O OPERATIONS
*****          ENTRY TO THE SUBROUTINE IS VIA A 'BAL  RC,ETI11700'
*
ETI11700 EQU   *                        I/O SUBROUTINE
         OI    DXATALL,DXATFC           FORCE CLOSE ON ERROR     YM7099
         EXCP  DXIOB                    PERFORM I/O OPERATION
         IECRES WAIT                    INVOKE IECRES MACRO TO 'WAIT'
         TM    DXECB,ECBNOERR           WAS THERE A PERMANENT I/O ERR-
         BCR   1,RC                     ..BR IF NOT- RETURN TO CALLER
         TM    IOBSENS1,UCBLDPT         IS THE TAPE AT 'LOAD POINT'-
         BCR   1,RC                     ..BR IF YES- RETURN TO CALLER
*                                       ..ELSE DROP THROUGH TO ERR RTN
         SPACE 2
***********************************************************************
*              ERROR PROCESSING                                       *
***********************************************************************
*
ETI11800 EQU   *                        ISSUE ABEND
         DMABCOND (RD),ABEND2V          EXIT TO PROB DETERMINATION RTN
         EJECT
***********************************************************************
*              CONSTANTS                                              *
***********************************************************************
*
CLCOMBSF CCW   X'2F',0,X'20',1          BACKSPACE FILE CCW
CNST0101 DC    F'65537'                 THE HEXADECIMAL FULLWORD '00010
AEOF13   DC    C'EOF'                   E-O-F LABEL IDENTIFIER
EOVMARK  DC    C'EOV'                   'EOV' TRAILER LABEL IDENTIFIER
ONE      DC    X'0001'
         SPACE 2
***********************************************************************
*              XCTL TABLE                                             *
***********************************************************************
*
XCTLTB2V XCTLTABL ID=(ABEND2V,0P,ID2V1F,1F,ID2V4A,4A,ID2V3F,3F), Y02134X
               SVC=055,BRT=YES,LENGTH=                           Y02080
         SPACE 2
         IECDSECS TIOT,                                                C
               DCB,                                                    C
               DEB,                                                    C
               DEBX,                                             Y02134*
               UCB,                                                    C
               MAIN,                                                   C
               WTG,                                                    C
               PREFX,                                            Y02080*
               DSAB,                                             Y02134*
               TCB,                                              Y02134*
               EXPAND=YES
         IECEQU ,                       INVOKE OPEN/CLOSE/EOV EQUATES
         SPACE 2
         END
