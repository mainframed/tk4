         TITLE 'IFG0193D                      LABEL EDITOR INTERFACE AND
               D DESTROY STANDARD LABEL ROUTINE'                 Y02134
*
IFG0193D CSECT
         ENTRY IGG0190A                 ALIAS ENTRY POINT        Y02134
         ENTRY IGG0550P                 ALIAS ENTRY POINT        Y02134
         ENTRY IFG0553D                 ALIAS ENTRY POINT        Y02134
IGG0190A EQU   IFG0193D                 ALIAS ENTRY POINT        Y02134
IGG0550P EQU   IFG0193D                 ALIAS ENTRY POINT        Y02134
IFG0553D EQU   IFG0193D                 ALIAS ENTRY POINT        Y02134
*                                                                Y02134
***********************************************************************
*                                                                     *
*          VS2 SU32 DELETIONS                                         *
*                                                              @G32DSMI
*                                                                     *
***********************************************************************
*                                                                     *
*        VS2 RELEASE 04 DELETIONS/CHANGES                             *
*0000317920-317988                                             @ZA04223
*                                                                     *
* MODULE NAME = IFG0193D (OS/VS2)                                     *
*                                                                     *
* DESCRIPTIVE NAME = LABEL EDITOR INTERFACE AND DESTROY STANDARD      *
*                    LABEL ROUTINE                                    *
*                                                                     *
* COPYRIGHT = NONE                                                    *
*                                                                     *
* STATUS = RELEASE 2, LEVEL 0                                         *
*                                                                     *
* FUNCTION =                                                          *
*        1. DESTROY STANDARD LABELS FOR DATA SETS REQUESTING NSL OR   *
*        NL.                                                          *
*                                                                     *
*        2. VERIFY THAT IT IS OKAY TO DESTROY A STANDARD LABEL SO AN  *
*        ANSII LABEL MAY BE CREATED AND VICE VERSA.                   *
*                                                                     *
*        3A. CHECK FOR A VOLUME PROTECTED BY RACF. DESTROY THE LABEL  *
*        ONLY IF THE USER HAS ALTER OWNERSHIP OF THE TAPE OR IF THE   *
*        TAPE IS NOT DEFINED TO RACF.                                 *
*                                                                     *
*        3B. CHECK FOR A SECURITY PROTECTED VOLUME.  DO NOT DESTROY   *
*        LABEL IF PROTECTED.                                          *
*                                                                     *
*        4. CHECK EXPIRATION DATE.  GET OPERATOR'S PERMISSION TO      *
*        DESTROY IF EXPIRATION DATE HAS NOT BEEN PASSED.              *
*                                                                     *
*        5. TRANSLATE ANSII TAPE LABELS BEFORE CHECKING AND GET       *
*        OPERATOR'S DECISION TO DESTROY ANSII LABEL OR USE ANOTHER    *
*        VOLUME.                                                      *
*                                                                     *
* NOTES = SEE BELOW                                                   *
*                                                                     *
*    DEPENDENCIES =                                                   *
*            CLASS ONE CHARACTER CODE.  THE EBCDIC CHARACTER CODE     *
*            WAS USED FOR ASSEMBLY.  THE MODULE MUST BE REASSEMBLED   *
*            IF A DIFFERENT CHARACTER SET IS USED FOR EXECUTION.      *
*                                                                     *
*    RESTRICTIONS = NONE                                              *
*                                                                     *
*    REGISTER CONVENTIONS =                                           *
*            R2 POINTS TO DCB                                         *
*            R4 POINTS TO OPEN WORK AREA                              *
*            R5 POINTS TO THE RESIDENT ROUTINE                        *
*            R6 POINTS TO THE WTG TABLE                               *
*            R7 POINTS TO THE CURRENT PARAMETER LIST ENTRY            *
*            R8 POINTS TO THE CURRENT WTG TABLE ENTRY                 *
*            R9 POINTS TO THE DD ENTRY IN THE TIOT                    *
*            R10 POINTS TO THE UCB                                    *
*                                                                     *
*    PATCH LABEL = SEE NEXT TO LAST LABEL BEFORE ORG STATEMENT AT END *
*                  OF LISTING.                                        *
*                                                                     *
* MODULE TYPE = CONTROL (OPEN, CLOSE, EOV DATA MANAGEMENT)            *
*                                                                     *
*    PROCESSOR = ASSEMBLER XF                                         *
*                                                                     *
*    MODULE SIZE = SEE EXTERNAL SYMBOL DICTIONARY OR LOC FIELD ON     *
*                  ORG STATEMENT AT END OF LISTING                    *
*                                                                     *
*    ATTRIBUTES = REENTRANT, REFRESHABLE,READ-ONLY, ENABLED,          *
*                  PRIVILEGED, SUPERVISOR STATE, DATA MANAGEMENT KEY, *
*                  LINK PACK AREA RESIDENT/PAGEABLE                   *
*                                                                     *
* ENTRY POINT = IFG0193D                                              *
*               IGG0190A                                              *
*               IGG0550P                                              *
*               IFG0553D                                              *
*                                                                     *
*    PURPOSE = SEE FUNCTION                                           *
*                                                                     *
*    LINKAGE = XCTL OR IECRES LOAD,BRANCH=DIRECT                      *
*                                                                     *
* INPUT = RWTG (REG 6) POINTS TO WTG TABLE                            *
*                                                                     *
* OUTPUT = STANDARD OPEN/CLOSE/EOV REGISTERS                          *
*          TAPE MARK ON LABEL TO BE DESTROYED                         *
*                                                                     *
* EXIT-NORMAL = IFG0194F R14 = 16 TAPE MOUNT/VERIFY VOLUME            *
*               IFG0193E R14 = 0 CREATE STANDARD LABEL                *
*               IFG0194J R14 = 0 REJECT VOLUME MESSAGE                *
*                            = 20 EXPIRATION DATE MESSAGE             *
*                            = 32 MESSAGE RE DESTROY SL               *
*                            = 40 SECURITY MESSAGE                    *
*                            = 48 TAPE ATTRIBUTE MESSAGE              *
*                                                                     *
* EXIT-ERROR = NONE                                                   *
*                                                                     *
* EXTERNAL REFERENCES = SEE BELOW                                     *
*                                                                     *
*    ROUTINES = IFG0194F,IFG0193E,IFG0194J.                           *
*                                                                     *
*    DATA AREAS = SEE PARAMETERS OF IECDSECS MACRO.                   *
*                                                                     *
*    CONTROL BLOCK = SEE PARAMETERS OF IECDSECS MACRO.                *
*                                                                     *
* TABLES = SEE XCTLTABL MACRO.                                        *
*                                                                     *
* MACROS = MODESET,EXCP,IECRES,XLATE,XCTLTABL,IECDSECS,IECEQU,        *
*          RACHECK,RACDEF.                                            *
*                                                                     *
* CHANGE ACTIVITY = SEE CHANGES/DELETIONS SECTION AFTER CSECT CARD.   *
*                                                                     *
***********************************************************************
         EJECT
         USING FORCORE,RCORE            WORK AREA
         USING WTG,RWTG                 WHERE-TO-GO TABLE        Y02080
         USING WTGENTRY,RWTGC           CURRENT WTG ENTRY PTR    Y02134
         USING USERPRML,RPARC           CURRENT PARM LIST PTR    Y02134
         USING TIOENTRY,RTIOT           TIOT ENTRY
         USING IHADCB,RDCB              DCB
         USING UCBOB,RUCB               UCB
         USING CVT,RB                   CVT
         BALR  RBASE,R0                 SET BASE REGISTER        YM3869
         USING *,RBASE
*                                                                Y02080
         CLC   WTGMODNM(L'IGG),IGG      TEST FOR LABEL EDITOR    Y02134
         BE    DSL00010                 BR IF FROM LABEL EDITOR  Y02134
*                                                                Y02134
         B     DSL00000(RET)            BRANCH ON REG 14 CODE    Y02080
*                                                                Y02080
DSL00000 EQU   *                        BRANCH TABLE             YM3869
*                                                                Y02080
         B     DSL06000                 WTOR DESTROY LABEL RETRN Y02080
         B     DSL00100                 BRANCH, PROCESS IFG0193D Y02134
         B     DSL04000                 EXPIRATION DATE RETURN   Y02134
         B     DSL08000                 SEC VOL MSG RETURN       Y02134
         B     DSL06600                 FROM CP STATUS QUERY MSG Y02083
         B     DSL07000                 FROM REVOKE CHKPT STATUS YM5728
         EJECT
*                                                                Y02134
DSL00010 EQU   *                        LABEL EDITOR RETURN      YM3869
*
         MODESET EXTKEY=DATAMGT         DATA MANAGEMENT KEY      Y02082
*
*****************************************************************Y02134
*                                                                Y02134
*        RESTORE MODULE NAME                                     Y02134
*        IF ENTERED FROM OMODVOLI NAME SHOULD BE IGG0190A        Y02134
*        IF ENTERED FROM EMODVOLI NAME SHOULD BE IGG0550P        Y02134
*        CHANGE IGG0190A - IFG0193D, IGG0550P - IFG0553D         Y02134
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
         MVC   WTGMODNM(K3),IFG         RESTORE IFG NAME         Y02134
         MVC   WTGMODID,ID3D3D          RESTORE 3D ID            Y02134
*                                                                Y02134
         L     RF,WTGPREFX              LOAD POINTER TO PREFIX   Y02134
         L     RPAR,IECUPRML-IECPREFX(,RF) POINTER TO PARM LIST  Y02134
         LR    RPARC,RPAR               POINT CURRENT AT FIRST   Y02134
         LA    RWTGC,WTGENTRY-WTG(,RWTG) POINT CURRENT TO FIRST  Y02134
*                                                                Y02134
DSL00020 EQU   *                        LOOP TO CHECK EACH ENTRY Y02134
*                                                                Y02134
         CLC   WTGIDTTR(K2),ID3D3C      CHECK FOR LABEL EDITOR   Y02134
         BNE   DSL00030                 BRANCH IF NOT TO PROCESS Y02134
*                                                                Y02134
         MVC   WTGIDTTR,ID3D3D          SET THIS MOD ID          Y02134
         L     RCORE,WTGCORE-K1         LOAD WORK AREA POINTER   Y02080
         LA    RET,K4                   OFFSET FOR RE-ENTRY      Y02080
         ST    RET,DXREGE               SAVE RETURN OFFSET       Y02080
*                                                                Y02134
DSL00030 EQU   *                        CHECK FOR LAST ENTRY     Y02134
*                                                                Y02134
         TM    DXDCBLST,LASTNTRY        CHECK FOR MORE DCBS      Y02134
         LA    RPARC,DXDCBLST+L'DXDCBLST GET NEXT ENTRY          Y02134
         LA    RWTGC,WTGENTRY+L'WTGENTRY GET NEXT ENTRY          Y02134
         BNO   DSL00020                 BRANCH IF MORE ENTRIES   Y02134
*                                                                Y02134
         LR    RPARC,RPAR               POINT CURRENT AT FIRST   Y02134
         LA    RWTGC,WTGENTRY-WTG(,RWTG) POINT CURRENT TO FIRST  Y02134
         L     RES,CVTPTR               LOAD POINTER TO CVT      Y02134
         L     RES,CVTDMSR-CVT(,RES)    PTR TO RESIDENT ROUTINE  Y02134
*                                                                Y02134
DSL00040 EQU   *                        LOOP TO CHECK EACH ENTRY Y02134
*                                                                Y02134
         CLC   WTGIDTTR(K2),ID3D3D      CHECK FOR THIS MOD       Y02134
         BE    DSL00100                 BRANCH, PROCESS THIS MOD Y02134
*                                                                Y02134
         TM    DXDCBLST,LASTNTRY        CHECK FOR MORE DCBS      Y02134
         LA    RPARC,DXDCBLST+L'DXDCBLST GET NEXT ENTRY          Y02134
         LA    RWTGC,WTGENTRY+L'WTGENTRY GET NEXT ENTRY          Y02134
         BNO   DSL00040                 BRANCH IF MORE ENTRIES   Y02134
         EJECT
*
DSL00100 EQU   *                        CHECK OMOD/EMOD PROCESS  Y02134
*                                                                Y02080
         L     RCORE,WTGCORE-K1         LOAD WORK AREA POINTER   Y02134
         L     RDCB,DXDCBLST            LOAD DCB POINTER         Y02134
         L     RTIOT,DXDSAB             LOAD DSAB POINTER        Y02134
         L     RTIOT,DSABTIOT-DSAB(,RTIOT) LOAD TIOENTRY POINTER Y02134
         L     RUCB,DXUCBADR            LOAD UCB POINTER         Y02134
*
         TM    UCBDMCT,UCBMOUNT         TEST IF VOL REJECTED   @ZA04223
         BO    DSL08000                 BRANCH IF REJECTED     @ZA04223
*                                                                Y02134
         TM    JFCBMASK+K5,LBLERR2V     TEST FOR LABEL CONFLICT  YM3869
         BO    DSL00200                 BRANCH IF LABEL CONFLICT Y02134
*                                                                Y02134
         B     DSL09050                 BRANCH TO NEXT LOAD      YM8218
*                                                                Y02134
DSL00200 EQU   *                        DESTROY OR CREATE        Y02134
*                                                                Y02134
         TM    JFCBMASK+K5,JFCSLDES     CHECK FOR DESTROY SL     Y02134
         BO    DSL00300                 BRANCH TO DESTROY SL     Y02134
*                                                                Y02134
         TM    JFCBMASK+K5,JFCSLCRE     CHECK FOR CREATE SL      Y02134
         BO    DSL09100                 BRANCH TO CREATE SL      Y02134
*                                                                Y02134
         B     DSL08000                 BRANCH TO REJECT VOLUME  Y02134
*                                                                Y02134
DSL00300 EQU   *                        DESTROY LABEL            Y02134
*                                                                Y02134
         CLC   DSLEOV,WTGMODNM+K3       CHECK FOR EOV            YM3869
         BE    DSL00400                 BRANCH IF EOV            YM3869
*                                                                YM3869
         CLC   JFCBFLSQ,ONE9U           OPENING THE 1ST DATA SET Y02134
         BH    DSL08000                 BRANCH - WRONG VOLUME    Y02134
*                                                                Y02134
DSL00400 EQU   *                        FIRST FILE ON TAPE       YM3869
*                                                                YM3869
         MVC   DXCCW1,ACCWU             MOVE IN REWIND CCW
         EXCP  DXIOB                                             Y02134
*                                                                Y02134
         IECRES WAIT                                             Y02134
*                                                                Y02134
         TM    DXECB,ECBNOERR           TEST FOR I/O ERROR       Y02134
         BNO   DSL08000                 BRANCH IF I/O ERROR      Y02134
*                                                                Y02134
         XC    DXCCW4(L'FL1ID),DXCCW4   CLEAR DSNAME SAVE        YM3869
*                                                                YM3869
         TM    UCBSTAB,UCBBSTR          AL MOUNTED               A48556
         BO    DSL01000                 YES, GO READ LABEL       A48556
         TM    UCBSTAT,UCBDADI          SL MOUNTED               A48556
         BO    DSL01000                 YES, GO READ LABEL       A48556
         B     DSL05000                 CHECK WITH OPERATOR      Y02134
         EJECT
*                                                                Y02134
DSL01000 EQU   *                        CHECK FOR SECURE TAPE    YM3869
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
*        READ VOLUME LABELS                                      Y02134
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
         ST    RCORE,DXCCW1             RESET CCW1
         MVI   DXCCW1,CCWRDTAP          CHANGE TO READ OPERATION CODE
*                                                                Y02134
DSL01100 EQU   *                        LOOP TO READ LABELS      YM3869
*                                                                Y02134
         MVI   DXLBL,BLANK              CLEAR AREA
         MVC   DSCFILSR,DXLBL           OF VOLUME LABEL
*                                                                Y02134
         EXCP  DXIOB                    ISSUE I/O OPERATION      Y02134
*                                                                Y02134
         IECRES WAIT                                             Y02134
*                                                                Y02134
         TM    DXECB,ECBNOERR           TEST FOR ERROR           Y02134
         BO    DSL01200                 BRANCH IF NO ERROR       Y02134
*                                                                Y02134
         TM    IOBSTAT0,CSWUNITX        CK FOR UNIT EXCEPTION    Y02134
         BO    DSL05000                 BRANCH IF TM READ        Y02134
         B     DSL08000                 PROCESS I/O ERROR        Y02134
*                                                                Y02134
DSL01200 EQU   *                        CHECK FOR ANSI LABEL     YM3869
*                                                                Y02134
         TM    UCBSTAB,UCBBSTR          IS ANSI VOLUME MOUNTED   Y02134
         BZ    DSL01300                 NO GO CHECK LABEL TYPE   Y02134
*                                                                Y02134
         XLATE DXLBL,K80                TRANSLATE LABEL DATA     Y02134
*                                                                Y02134
DSL01300 EQU   *                        VOL1,HDR1,HDR2           YM3869
*                                                                Y02134
         CLI   FL1LABI,CHARV            IS THIS VOL LBL
         BE    DSL01800                 READ ANOTHER LABEL     @G32DSMI
         CLI   FL1LABI+K1,CHARV         CK FOR USER VOL LBLS-ANSI
         BE    DSL01100                 BRANCH IF YES
         CLC   AHDR1U,FL1LABI           IS THIS A HEADER LABEL 1
         BE    DSL02000                 YES, GO CHECK SECURITY
         CLC   AHDR2U,FL1LABI           IS THIS A HDR 2 LABEL    Y02083
         BE    DSL06500                 IF HDR2, GO TEST FOR     YM5914
*                                       CHECKPOINT D/S           YM5914
         B     DSL05000                 CHECK WITH OPERATOR      Y02134
         EJECT
*                                                              @G32DSMI
DSL01800 EQU   *                        VOL1 LABEL READ        @G32DSMI
*                                                              @G32DSMI
***************************************************************@G32DSMI
*                                                              @G32DSMI
*        TEST FOR RAC PROTECTION                               @G32DSMI
*                                                              @G32DSMI
***************************************************************@G32DSMI
*                                                              @G32DSMI
         L     RC,DXJSCBAD              LOAD JSCB ADDRESS      @G32DSMI
         USING IEZJSCB,RC               JSCB ADDRESSABILITY    @G32DSMI
         TM    JSCBSWT1,JSCBPASS        TEST IF RAC CHECKING   @G32DSMIX
                                        SHOULD BE BYPASSED     @G32DSMI
         BO    DSL01100                 YES, GO READ NEXT LBL  @G32DSMI
         L     RB,CVTPTR                LOAD CVT ADDRESS       @G32DSMI
         L     RC,CVTRAC                LOAD RAC CVT ADDRESS   @G32DSMI
         LTR   RC,RC                    IS RACF ACTIVE         @G32DSMI
         BZ    DSL01100                 NO, GO READ NEXT LABEL @G32DSMI
         USING RCVT,RC                  RACF CVT BASE REG      @G32DSMI
         TM    RCVTSTA1,RCVTTAPE        TEST FOR TAPE VOLUME   @G32DSMIX
                                        PROTECTION OPTION      @G32DSMI
         BNO   DSL01100                 NO, GO READ NEXT LABEL @G32DSMI
         DROP  RC                                              @G32DSMI
         MVC   DXCCW4(RACKLNTH),RACKLIST  MOVE IN RACHECK      @G32DSMIX
                                        PARAMETER LIST         @G32DSMI
*                                                              @G32DSMI
         RACHECK ENTITY=DXVOLSR1,MF=(E,DXCCW4)   CHECK USER'S  @G32DSMIX
                                        RACF AUTHORIZATION TO  @G32DSMIX
                                        DESTROY THE TAPE LABEL @G32DSMI
*                                                              @G32DSMI
         LA    RC,K8                    TEST IF RETURN CODE 8  @G32DSMI
         CR    RF,RC                    TEST IF DEFINED BUT    @G32DSMIX
                                        NOT AUTHORIZED (RC=8)  @G32DSMI
         BE    DSL08000                 BR IF YES TO REJECT    @G32DSMI
         LTR   RF,RF                    TEST IF DEFINED AND    @G32DSMIX
                                        AUTHORIZED (RC=0)      @G32DSMI
         BNZ   DSL01100                 BRANCH IF UNDEFINED    @G32DSMI
         OI    DXRCFLAG,DXRCDEF         SET RACDEF DELETE BIT  @G32DSMI
         B     DSL01100                 GO READ ANOTHER LABEL  @G32DSMI
         EJECT
*                                                              @G32DSMI
DSL02000 EQU   *                        CHECK HDR1 LABEL       @G32DSMI
*                                                              @G32DSMI
*****************************************************************Y02134
*                                                                Y02134
*        CHECK VOLUME SECURITY                                   Y02134
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
         TM    DXRCFLAG,DXRCDEF         TEST IF VOLUME DEFINED @G32DSMI*
                                        AND AUTHORIZED         @G32DSMI
         BO    DSL03000                 BR YES, SKIP SECURITY  @G32DSMI*
                                        CHECKING               @G32DSMI
         TM    UCBSTAB,UCBBSTR          IS ANSI VOL MOUNTED
         BZ    DSL02100                 NO, TEST SECURITY
         CLI   FL1FSEC,BLANK            IS ACCESSABILITY BLANK
         BE    DSL03000                 YES, UNLIMITED ACCESS    Y02134
         B     DSL08200                 NO, PROTECTED VOL        Y02134
*                                                                Y02134
DSL02100 EQU   *                        CHECK SL SECURITY        YM3869
*                                                                Y02134
         CLI   FL1FSEC,FL1SECTY         CHK FOR DATA SET SECORITY
         BE    DSL08200                 BRANCH IF PASSWORD       Y02134
         CLI   FL1FSEC,FL1WRSEC         CK FOR WRITE SECURITY
         BE    DSL08200                 BR IF WRITE PROTECTED    Y02134
         EJECT
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
*        CHECK EXPIRATION DATE                                   Y02134
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
DSL03000 EQU   *                        CHECK DATE               YM3869
*                                                                Y02134
         MVC   DXCCW4(L'FL1ID),FL1ID    SAVE DSNAME              YM3869
*                                                                YM3869
         XC    DXWORK,DXWORK            CLEAR WORK AREA          Y02134
         PACK  DXWORK(K4),FL1EXPDT+K1(K5)  EBCDIC TO DECIMAL     YM5368
         L     R1,CVTPTR                LOAD CVT ADDRESS         Y02080
         CLC   CVTDATE-CVT(K4,R1),DXWORK  CHECK IF DATE EXPIRED  YM5368
         BNL   DSL04050                 GO TEST HDR2 READ REQ    YM5914
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
*        GO TO ERROR ROUTINE TO WRITE EXPIRATION DATE            Y02134
*        ERROR MESSAGE TO THE OPERATOR, AND WAIT FOR             Y02134
*        HIS REPLY TO USE OR NOT USE THE DATA SET.               Y02134
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
DSL03100 EQU   *                        GO ISSUE MESSAGE         YM3869
*                                                                Y02134
         MVC   DXRETMOD,ID3D3D          RETURN ID TO SAVE AREA   Y02134
         MVI   DXRETCOD,K8              SET RETURN INDICATOR     Y02134
*                                                                Y02134
         XC    DXWORK1,DXWORK1          SET REPLY AREA TO BLANKS Y02134
         LA    R1,DXWORK1               POINT TO REPLY AREA      Y02134
         LA    R0,K1                    SET REPLY LENGTH         Y02134
         SLL   R0,K24                   MOVE LENGTH TO HI BYTE   Y02134
         OR    R1,R0                    SET UP WTOR PARM         Y02134
         LA    R0,FL1ID                 GET DSNAME ADDRESS       Y02134
         MVI   FL1ID+L'FL1ID,BLANK      DELIMIT 17 CHAR NAME     Y02134
         MVC   FL1ID+L'FL1ID+K1(L'JFCBDSNM-L'FL1ID-K1),FL1ID+L'FL1ID
*                                                                Y02134
         IECRES LOAD,MODID=ID3D4J,BRCODE=K20,BRANCH=QUEUED       Y02134
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
*        RETURN HERE FROM EXPIRATION CHECK                       Y02134
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
DSL04000 EQU   *                        WTOR RETURN              YM3869
*                                                                Y02134
         IECRES WAIT                    WAIT FOR OPERATOR REPLY  Y02134
*                                                                Y02134
         OI    DXWORK1,BLANK            FOLD REPLY               Y02134
         CLI   DXWORK1,CHARU            CHECK FOR REPLY U        Y02134
         BE    DSL04050                 GO TEST HDR2 READ REQ    YM5914
*                                                                Y02134
         CLI   DXWORK1,CHARM            CHECK IF REJECT          Y02134
         BNE   DSL03100                 BRANCH, INCORRECT REPLY  Y02134
*                                                                Y02134
         B     DSL08000                 BRANCH TO REJECT VOLUME  Y02134
*                                                                YM5914
*****************************************************************YM5914
*                                                                YM5914
*        CHECK TO SEE IF HEADER 2 MUST BE READ FOR CHKPT TEST    YM5914
*                                                                YM5914
*****************************************************************YM5914
*                                                                YM5914
DSL04050 EQU   *                        TEST FOR HDR2 READ REQ   YM5914
         TM    UCBSTAT,UCBDADI          SL TAPE MOUNTED          YM5914
         BO    DSL01100                 IF SL, GO READ HDR2      YM5914
         B     DSL05000                 IF NOT, GO ISSUE WTOR;   YM5914
*                                       CAN'T BE CHKPT D/S       YM5914
         EJECT
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
*        CHECK WITH OPERATOR BEFORE DESTROYING SL TAPES          Y02134
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
DSL05000 EQU   *                        ISSUE MESSAGE            YM3869
         MVI   DXWORK,K0                CLEAR CHKPT D/S SW       YM5914
DSL05050 EQU   *                        NON-SW-CLEAR ENTRY       YM5914
*                                                                Y02134
         MVC   DXRETMOD,ID3D3D          RETURN ID TO SAVE AREA   Y02134
         MVI   DXRETCOD,K0              SET RETURN INDICATOR     Y02134
*                                                                Y02134
         XC    DXWORK1,DXWORK1          SET REPLY AREA TO BLANKS Y02134
         LA    R1,DXWORK1               POINT TO REPLY AREA      Y02134
         LA    R0,K1                    SET REPLY LENGTH         Y02134
         SLL   R0,K24                   MOVE LENGTH TO HI BYTE   Y02134
         OR    R1,R0                    SET UP WTOR PARM         Y02134
*                                                                YM3869
         MVC   FL1ID,DXCCW4             GET SAVED DSNAME         YM3869
         LA    R0,FL1ID                 GET DSNAME ADDRESS       YM3869
         MVI   FL1ID+L'FL1ID,BLANK      DELIMIT 17 CHAR NAME     YM3869
         MVC   FL1ID+L'FL1ID+K1(L'JFCBDSNM-L'FL1ID-K1),FL1ID+L'FL1ID
*                                       PAD WITH BLANKS          YM3869
*                                                                Y02134
         IECRES LOAD,MODID=ID3D4J,BRCODE=K32,BRANCH=QUEUED       Y02134
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
*        RETURN HERE WITH OPERATOR REPLY                         Y02134
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
DSL06000 EQU   *                        WTOR RETURN              YM3869
*                                                                Y02134
         IECRES WAIT                    WAIT FOR OPERATOR REPLY  Y02134
*                                                                Y02134
         OI    DXWORK1,BLANK            FOLD REPLY               Y02134
         CLI   DXWORK1,CHARU            CHECK FOR REPLY U        Y02134
         BE    DSL06650                 GO CHECK CHKPT STATUS    YM5914
*                                                                Y02134
         CLI   DXWORK1,CHARM            CHECK IF REJECT          Y02134
         BNE   DSL05000                 BRANCH, INCORRECT REPLY  Y02134
*                                                                Y02134
         B     DSL08000                 BRANCH TO REJECT VOLUME  Y02134
         EJECT
*                                                                YM5914
*****************************************************************YM5914
*                                                                YM5914
*        TEST FOR CHECKPOINT STATUS                              YM5914
*                                                                YM5914
*****************************************************************YM5914
*                                                                YM5914
DSL06500 EQU   *                        TEST FOR CHKPT STATUS    YM5914
         CLI   FL2DSIND,FL2CKDS         CHECKPOINT D/S           YM5914
         BNE   DSL05000                 IF NOT, GO ISSUE WTOR    YM5914
*                                                                Y02083
*****************************************************************Y02083
*                                                                Y02083
*        CHECK VOLUME FOR SECURE STATUS                          Y02083
*                                                                Y02083
*****************************************************************Y02083
*                                                                Y02083
DSL06550 EQU   *                        CHECK POINT VOLUME       YM5914
*                                                                Y02083
         MVC   DXRETMOD,ID3D3D          SET RETURN NAME AND EPA  YM3869
         MVI   DXRETCOD,K16             SET BR TABLE RETURN VALUEY02083
*                                                                YM3869
         XC    DXWORK1,DXWORK1          CLEAR WTOR REPLY AREA    Y02083
         LA    R1,DXWORK1               POINT TO REPLY AREA      Y02083
         LA    R0,K3                    SET REPLY LENGTH         Y02083
         SLL   R0,K24                   MOVE LENGTH TO HI BYTE   Y02083
         OR    R1,R0                    SET UP WTOR PARM         Y02083
*                                                                YM3869
         IECRES LOAD,MODID=ID3D4J,BRCODE=K56,BRANCH=QUEUED       Y02083
*                                                                Y02083
*****************************************************************Y02083
*                                                                Y02083
*        RETURN FROM QUERY CHECK POINT VOLUME STATUS             YM3869
*                                                                Y02083
*****************************************************************Y02083
*                                                                Y02083
DSL06600 EQU   *                        WTOR RETURN              YM3869
*                                                                Y02083
         IECRES WAIT                    WAIT FOR REPLY           Y02083
*                                                                YM3869
         OC    DXWORK1,BLANKS           FOLD REPLY TO UPPER CASE Y02083
         CLC   KNO,DXWORK1              REPLY = 'NO'             Y02083
         BE    DSL05000                 IF 'NO', GO ISSUE WTOR   YM5914
         CLC   KYES,DXWORK1             REPLY = 'YES'            Y02083
         BNE   DSL06550                 IF NOT, ASK AGAIN        YM5914
*                                                                YM5914
*****************************************************************YM5914
*                                                                YM5914
*        SECURE CHECKPOINT D/S PROCESSING                        YM5914
*                                                                YM5914
*****************************************************************YM5914
*                                                                YM5914
         CLC   DSLEOV,WTGMODNM+K3       IS THIS EOV              YM5914
         BE    DSL06610                 IF YES, BRANCH           YM5914
         TM    JFCBMASK+K5,JFCSLCRE     CREATING SL              YM5914
         BO    DSL06615                 IF YES, BRANCH           YM5914
DSL06605 EQU   *                        SET CHKPT DESTROY SW     YM5914
         MVI   DXWORK,K1                IF NOT CREATING SL OR    YM5914
*                                       EOV FOR CHKPT D/S, SET   YM5914
*                                       CHKPT DESTROY SW         YM5914
         B     DSL05050                 GO ISSUE NL-WTOR         YM5914
DSL06610 EQU   *                        TEST EOV D/S STATUS      YM5914
         L     RB,DXDSAB                GET DSAB ADDRESS         YM5914
         TM    DSABFLG4-DSAB(RB),DSABCKDS PREVIOUS VOL = CHKPT   YM5914
         BNO   DSL06605                 IF NOT, SET DESTROY SW   YM5914
DSL06615 EQU   *                        SET STILL-CHKPT SW       YM5914
         MVI   DXWORK,K2                INDICATE CHKPT D/S BEING YM5914
*                                       OVERLAID BY ANOTHER      YM5914
*                                       CHKPT D/S                YM5914
         B     DSL05050                 GO ISSUE NL-WTOR         YM5914
         EJECT
*                                                                YM5914
*****************************************************************YM5914
*                                                                YM5914
*        POST NL-REQUEST-WTOR PROCESSING FOR CHECKPOINT D/S      YM5914
*                                                                YM5914
*****************************************************************YM5914
*                                                                YM5914
DSL06650 EQU   *                        TEST OLD STATUS          YM5914
         CLI   DXWORK,K1                WAS IT A SECURE VOL      YM5914
         BL    DSL07000                 IF NOT, GO WRITE TM      YM5914
         BE    DSL06700                 IF WAS BUT NOT NOW, BR - YM5914
*                                       OTHERWISE, INDICATE THIS YM5914
*                                       IS A SECURE CHKPT VOL    YM5914
*                                       VIA THE DSAB             YM5914
         L     RB,DXDSAB                GET DSAB ADDRESS         YM5914
         MODESET EXTKEY=SCHED           ASSUME DSAB KEY          YM5914
         OI    DSABFLG4-DSAB(RB),DSABCKDS+DSABCKVL SET DSAB FLGS YM5914
         MODESET EXTKEY=DATAMGT         RESUME D/M KEY           YM5914
         B     DSL07000                 GO WRITE TM              YM5914
*                                                                Y02083
*****************************************************************Y02083
*                                                                Y02083
*        REVOKE VOLUME CHECKPOINT STATUS                         Y02083
*                                                                Y02083
*****************************************************************Y02083
*                                                                Y02083
DSL06700 MVC   DXRETMOD,ID3D3D          SET RETURN NAME AND EPA  YM3869
         MVI   DXRETCOD,K20             SET BR TABLE RETURN VALUEY02083
*                                                                YM3869
         IECRES LOAD,MODID=ID3D4J,BRCODE=K60,BRANCH=QUEUED       Y02083
         EJECT
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
*        WRITE 1 TAPE MARK TO INSURE NL TAPE                     Y02134
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
DSL07000 EQU   *                        REWIND                   YM3869
*                                                                Y02134
         MVC   DXCCW1,ACCWU             MOVE IN REWIND CCW
         EXCP  DXIOB                                             Y02134
*                                                                Y02134
         IECRES WAIT                                             Y02134
*                                                                Y02134
         TM    DXECB,ECBNOERR           TEST FOR I/O ERROR       Y02134
         BNO   DSL08000                 BRANCH IF I/O ERROR      Y02134
*                                                                Y02134
         MVC   DXCCW1,ACCWU             MOVE IN REWIND CCW       Y02134
         MVC   DXCCW2,ACCWU             WILL NEED 2 CCWS
         MVI   DXCCW1,CCWWTM            1ST CCW IS WRITE TAPE MRK
         MVI   DXCCW1+K4,CCWCMDCH       CONNECT TO NEXT CCW
         MVI   DXCCW2,CCWNOP            GET NOP CODE
*                                                                Y02134
         EXCP  DXIOB                                             Y02134
*                                                                Y02134
         IECRES WAIT                                             Y02134
*                                                                Y02134
         TM    DXECB,ECBNOERR           TEST FOR I/O ERROR       Y02134
         BNO   DSL08000                 BRANCH IF I/O ERROR      Y02134
*                                                                Y02134
         TM    DXRCFLAG,DXRCDEF         TEST IF RACDEF DELETE  @G32DSMIX
                                        MUST BE ISSUED         @G32DSMI
         BNO   DSL07200                 BRANCH IF NOT          @G32DSMI
*                                                              @G32DSMI
         MVC   DXCCW4(RACDLNTH),RACDLIST  MOVE IN RACDEF       @G32DSMIX
                                        DELETE PARAMETER LIST  @G32DSMI
*                                                              @G32DSMI
         RACDEF ENTITY=DXVOLSR1,MF=(E,DXCCW4)   ISSUE RACDEF   @G32DSMIX
                                        DELETE FOR DESTROYED   @G32DSMIX
                                        VOLUME SERIAL          @G32DSMI
*                                                              @G32DSMI
         NI    DXRCFLAG,ALLBITS-DXRCDEF   RESET RACDEF DELETE  @G32DSMIX
                                        SWITCH                 @G32DSMI
*                                                              @G32DSMI
DSL07200 EQU   *                        RESET UCB INDICATORS   @G32DSMI
         MODESET EXTKEY=SUPR            UCB KEY                  Y02082
*                                                                Y02134
         NI    UCBSTAT,ALLBITS-UCBDADI  TURN OFF SL INDICATOR    Y02134
         NI    UCBSTAB,ALLBITS-UCBBSTR  TURN OFF AL INDICATOR    Y02134
         NI    UCBTFL1,ALLBITS-UCBNSLTP TURN OFF NSL INDICATOR   Y02134
         OI    UCBTFL1,UCBNLTP          TURN ON NL INDICATOR     Y02134
*                                                                Y02134
         MODESET EXTKEY=DATAMGT         DATA MANAGEMENT KEY      Y02082
*                                                                Y02134
         TM    JFCBLTYP,JFCNSL          TEST FOR NSL SPECIFIED   Y02134
         BNO   DSL09100                 BRANCH IF NOT NSL        Y02134
*                                                                Y02134
         MODESET EXTKEY=SUPR            UCB KEY                  Y02082
*                                                                Y02134
         NI    UCBTFL1,ALLBITS-UCBNLTP  TURN OFF NL INDICATOR    Y02134
         OI    UCBTFL1,UCBNSLTP         TURN ON NSL INDICATOR    Y02134
*                                                                Y02134
         MODESET EXTKEY=DATAMGT         DATA MANAGEMENT KEY      Y02082
*                                                                Y02134
         B     DSL09100                 BRANCH TO CONTINUE       Y02080
         EJECT
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
*        REJECT VOLUME CURRENTLY MOUNTED                         Y02134
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
DSL08000 EQU   *                        ISSUE MESSAGE            YM3869
*                                                                Y02134
         NI    DCBIFLGS,X'FF'-DCBIFPIO  TURN OFF ERROR FLAGS     Y02134
         NI    JFCBMASK+K5,LBWTRI9T     TURN OFF LBL ERR INDICATORS
*                                                                Y02134
         MVC   DXRETMOD,ID3D4F          SET UP RETURN TO 4F      Y02134
         MVI   DXRETCOD,K4              4F MOUNT/VERIFY OFFSET   Y02134
*                                                                Y02134
         IECRES LOAD,MODID=ID3D4J,BRCODE=K4,BRANCH=QUEUED        Y02134
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
*        ISSUE SECURITY MESSAGE                                  Y02134
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
DSL08200 EQU   *                        ISSUE MESSAGE            YM3869
*                                                                Y02134
         NI    DCBIFLGS,X'FF'-DCBIFPIO  TURN OFF ERROR FLAGS     Y02134
         NI    JFCBMASK+K5,LBWTRI9T     TURN OFF LBL ERR INDICATORS
*                                                                Y02134
         MVC   DXRETMOD,ID3D3D          SET UP RETURN LOAD       Y02134
         MVI   DXRETCOD,K12             REJECT OFFSET            Y02134
*                                                                Y02134
         IECRES LOAD,MODID=ID3D4J,BRCODE=K40,BRANCH=QUEUED       Y02134
         EJECT
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
*        RETURN TO VERIFY VOLUME                                 Y02134
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
DSL09000 EQU   *                        CHECK FOR DISP=MOD       YM8218
*                                                                YM8218
         CLC   DXVOLSEQ,ONE9U           CHECK FOR FIRST VOLUME   YM8218
         BNE   DSL09050                 BRANCH, BYPASS NEW-MOD   YM8218
         TM    JFCBIND2,JFCOLD          DISP=MOD                 YM8218
         BO    DSL09050                 BR IF NOT- CONTINUE      YM8218
*                                                                YM8218
         OI    JFCBIND2,JFCNEW          CHANGE 'MOD' TO 'NEW'    YM8218
         OI    JFCBFLG2,JFCMODNW        INDICATE THAT 'MOD' WAS  YM8218
*                                       CHANGED TO 'NEW'         YM8218
DSL09050 EQU   *                        CONTINUE                 YM8218
*                                                                Y02134
         NI    DCBIFLGS,X'FF'-DCBIFPIO  TURN OFF ERROR FLAGS     Y02134
         NI    JFCBMASK+K5,LBWTRI9T     TURN OFF LBL ERR INDICATORS
*                                                                Y02134
         IECRES LOAD,MODID=ID3D4F,BRCODE=K16,BRANCH=QUEUED       Y02134
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
*        GO TO CREATE LABEL ROUTINE                              Y02134
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
DSL09100 EQU   *                        GO TO CREATE LABEL       YM3869
*                                                                Y02134
         NI    DCBIFLGS,X'FF'-DCBIFPIO  TURN OFF ERROR FLAGS     Y02134
         NI    JFCBMASK+K5,ALLBITS-JFCSLDES RESET DESTROY BIT    Y02134
*                                                                Y02134
         TM    JFCBMASK+K5,JFCSLCRE     CHECK FOR CREATE SL      Y02134
         BNO   DSL09000                 BRANCH, NOT CREATING LBL YM4647
*                                                                Y02134
         IECRES LOAD,MODID=ID3D3E,BRCODE=K0,BRANCH=QUEUED        Y02134
         EJECT
*                                                                Y02083
*****************************************************************Y02083
*                                                                Y02083
*        EQUATES                                                 Y02083
*                                                                Y02083
*****************************************************************Y02083
*                                                                Y02083
FL2CKDS  EQU   C'C'                     FL2 CHKPT D/S INDICATOR  Y02083
         EJECT
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
*        CONSTANTS                                               Y02134
*                                                                Y02134
*****************************************************************Y02134
RACKLIST RACHECK CLASS='TAPEVOL',ATTR=ALTER,MF=L               @G32DSMI
RACKLNTH EQU   *-RACKLIST               LENGTH OF RACHECK LIST @G32DSMI
RACDLIST RACDEF CLASS='TAPEVOL',TYPE=DELETE,MF=L               @G32DSMI
RACDLNTH EQU   *-RACDLIST               LENGTH OF RACDEF LIST  @G32DSMI
*                                                                Y02134
ONE9U    DC    X'0001'                  USED TO TEST JFCB FILE
*                                       SEQUENCE NUMBER
AHDR1U   DC    C'HDR1'                  USED TO TEST IF HEADER
AHDR2U   DC    C'HDR2'                  USED TO TEST FOR HDR 2   Y02083
KNO      DC    C'NO '                   WTOR REPLY COMPARATOR    Y02083
KYES     DC    C'YES'                   WTOR REPLY COMPARATOR    Y02083
BLANKS   DC    C'    '                  FOLDING MASK             Y02083
DSLEOV   DC    C'055'                   EOV MODULE ID            Y02083
ACCWU    DC    X'0700000020000050'      BASIC CCW - REWIND COMMAND
IGG      DC    CL3'IGG'                 IGG NAMING CONVENTION    Y02134
IFG      DC    CL3'IFG'                 IFG NAMING CONVENTION    Y02134
ID3D3C   DC    CL2'3C'                  ID OF LABEL EDITOR       Y02134
         SPACE
XCTL3D   XCTLTABL ID=(ID3D3D,3D,ID3D3E,3E,ID3D4F,4F,ID3D4J,4J,   Y02134X
               ID3D0P,0P),                                       Y02083X
               SVC=019,LENGTH=,BRT=YES                           Y02134
         IECDSECS CVT,TCB,DSAB,TIOT,DCB,UCB,MAIN,PREFX,WTG,      Y02134X
               JSCB,RACVT,                                     @G32DSMIX
               EXPAND=YES                                        Y02134
         IECEQU
         END
