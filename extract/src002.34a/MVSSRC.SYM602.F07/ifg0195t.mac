         TITLE 'IFG0195T                      OPEN/EOV - PASSWORD SECURI
               ITY INITIALIZATION'                               Y02134
IFG0195T CSECT
         ENTRY IFG0555T                 ALIAS ENTRY POINT        Y02134
IFG0555T EQU   *                        ENTRY FROM EOV           Y02134
*                                                                Y02134
***********************************************************************
*                                                                     *
*          VS2 SU32 DELETIONS                                         *
*0000005022-005023,006844                                      @G32DSMI
*                                                                     *
***********************************************************************
*                                                                     *
* MODULE NAME = IFG0195T (OS/VS2)                                     *
*                                                                     *
* DESCRIPTIVE NAME = OPEN/EOV PASSWORD SECURITY INITIALIZATION        *
*                                                                     *
* COPYRIGHT = NONE                                                    *
*                                                                     *
* STATUS = RELEASE 2, LEVEL 0                                         *
*                                                                     *
* FUNCTION =                                                          *
*                                                                     *
*        1. CHECKS FOR PASSWORD PROTECTION BYPASS AUTHORIZATION IN    *
*           THE JSCB AND TO SEE IF THE VOLUME IS RACF PROTECTED.      *
*                                                                     *
*        2. OBTAINS FETCH PROTECTED SECURITY WORK AREA IN DATA        *
*           MANAGEMENT KEY (KEY 5) FOR EACH DCB IN A PARALLEL OPEN.   *
*                                                                     *
*        3. INITIALIZES WORK AREA TO APPEAR SIMILAR TO OPEN/CLOSE/EOV *
*           WORK AREA (THIS IS DONE TO USE THE SAME ROUTINES TO       *
*           PROCESS A SCRATCH/RENAME REQUEST - SEE IFG0195V).         *
*                                                                     *
*        4. SET INDICATOR FOR OPEN OR EOV.                            *
*                                                                     *
*        5. SET UP RETURN TO THIS ROUTINE WHEN SECURITY PROCESSING    *
*           HAS COMPLETED.                                            *
*                                                                     *
*        6. RETURNS TO CALLING ROUTINE IF SECURITY PROCESSING WAS     *
*           SUCCESSFUL. IF UNSUCCESSFUL, RETURNS TO PROBLEM           *
*           DETERMINATION.                                            *
*                                                                     *
* NOTES = THIS ROUTINE IS AN INTERFACE ROUTINE BETWEEN OPEN/EOV AND   *
*         THE PASSWORD PROCESSING ROUTINES. THE FUNCTION IN THIS      *
*         ROUTINE IS SIMILAR TO THE FUNCTION IN THE DADSM INTERFACE   *
*         ROUTINE (SECLOADA).                                         *
*                                                                     *
*    DEPENDENCIES =                                                   *
*                                                                     *
*            CLASS ONE CHARACTER CODE.  THE EBCDIC CHARACTER CODE     *
*            WAS USED FOR ASSEMBLY.  THE MODULE MUST BE REASSEMBLED   *
*            IF A DIFFERENT CHARACTER SET IS USED FOR EXECUTION.      *
*            THIS ROUTINE ADHERES TO OPEN/CLOSE/EOV PROGRAMMING       *
*            CONVENTIONS AND REGISTER USAGE.                          *
*                                                                     *
*    RESTRICTIONS = NONE                                              *
*                                                                     *
*    REGISTER CONVENTIONS = SEE IECEQU MACRO.                         *
*                                                                     *
*    PATCH LABEL = SEE XCTLTABL MACRO.                                *
*                                                                     *
* MODULE TYPE = CONTROL (OPEN, CLOSE, EOV DATA MANAGEMENT)            *
*                                                                     *
*    PROCESSOR = ASSEMBLER XF                                         *
*                                                                     *
*    MODULE SIZE = SEE EXTERNAL SYMBOL DICTIONARY OR LOC FIELD ON     *
*                  ORG STATEMENT AT END OF LISTING                    *
*                                                                     *
*    ATTRIBUTES = REENTRANT, REFRESHABLE,READ-ONLY, ENABLED,          *
*                 PRIVILEGED, SUPERVISOR STATE, DATA MANAGEMENT KEY,  *
*                 LINK PACK AREA RESIDENT/PAGEABLE                    *
*                                                                     *
* ENTRY POINT = IFG0195T FROM OPEN                                    *
*               IFG0555T FROM EOV                                     *
*               IFG0195T FROM PASSWORD PROCESSING ROUTINES.           *
*                                                                     *
*    PURPOSE = OPEN/EOV INTERFACE ROUTINE TO PASSWORD PROCESSING      *
*                                                                     *
*    LINKAGE = AS FOLLOWS -                                           *
*                                                                     *
*        MVC   DXRETMOD,IDRETURN        RETURN MOD NAME               *
*        MVC   DXRETCOD,OFFSET          RETURN BRANCH TABLE OFFSET    *
*        IECRES LOAD,MODID=MOD5T,BRCODE=VALUE,BRANCH=QUEUED           *
*                                                                     *
* INPUT = AS FOLLOWS -                                                *
*                                                                     *
*        FOR RET (REG 14) = 0 OR 12 (INITIAL ENTRY) -                 *
*        RDCB (REG 2) = CONTENTS OF CURRENT PARMLIST ENTRY            *
*                       CONTAINING DCB POINTER                        *
*        RCORE (REG 4) = OPEN/EOV WORK AREA POINTER                   *
*                        ALSO IF RET (REG14)=0 POINTER TO FL1 LABEL   *
*        REG 5 = POINTER TO RESIDENT ROUTINE                          *
*        RWTG (REG 6) = POINTER TO OPEN/EOV WTG TABLE                 *
*        RPARC (REG 7) = POINTER TO CURRENT ENTRY IN PARMLIST         *
*        RWTGC (REG 8) = POINTER TO CURRENT WTG ENTRY                 *
*        RB (REG 11) = POINTER TO F1 DSCB IF RET (REG 14) = 12        *
*        RD (REG 13) = POINTER TO DSNAME IF RET (REG 14) = 12         *
*                                                                     *
*        FOR RET (REG 14) = 4 (RETURNING FROM PASSWORD PROCESSING)    *
*        RD (REG 13) = POINTER TO SECURITY WORK AREA                  *
*                                                                     *
* OUTPUT = AS FOLLOWS -                                               *
*                                                                     *
*        WHEN RETURNING TO CALLER REGS 1 - 13 ARE RESTORED            *
*                                                                     *
*        WHEN GOING TO PASSWORD PROCESSING -                          *
*        RCORE POINTS TO F1 DSCB FOR DA AND ZEROS FOR TAPE            *
*        RWTG POINTS TO SECURITY WTG TABLE                            *
*        RD POINTS TO SECURITY WORK AREA WITH FIELDS INITIALIZED      *
*        FOR RETURN SAVE AREA, DSNAME, DSCB, TCB, DSAB, JSCB, AND     *
*        A CODE SET TO 0 FOR OPEN AND 1 FOR EOV.                      *
*                                                                     *
* EXIT-NORMAL = AS FOLLOWS - TO CALLER                                *
*                                                                     *
*        RESTORE REGS                                                 *
*        IC    RET,DXRETCOD             GET RETURN OFFEST IN REG 14   *
*        IECRES LOAD,MODID=DXRETMOD,BRCODE=(RET),BRANCH=QUEUED        *
*                                                                     *
* EXIT-ERROR = PROBLEM DETERMINATION - CODE PASSED IN MYERRCOD        *
*                                                                     *
* EXTERNAL REFERENCES = SEE BELOW                                     *
*                                                                     *
*    ROUTINES = IFG0195V,IFG0190P.                                    *
*                                                                     *
*    DATA AREAS = OPEN/EOV WORK AREA (SEE IECDSECS MAIN,WTG).         *
*                 SECURITY WORK AREA (SEE IECDSECS SECCORE).          *
*                                                                     *
*    CONTROL BLOCK = CVT,TCB,JSCB,DEB,DCB.                            *
*                                                                     *
* TABLES = SEE XCTLTABL.                                              *
*                                                                     *
* MACROS = IECRES,DMABCOND,XCTLTABL,IECDSECS,IECEQU.                  *
*                                                                     *
* CHANGE ACTIVITY = SEE DELETIONS/CHANGES SECTION AFTER CSECT CARD.   *
*                                                                     *
***********************************************************************
         EJECT
         USING FORCORE,RCORE            DEFINE BASE TO MAIN WORK AREA
         USING WTG,RWTG                 DEFINE BASE TO WTG TABLE
         USING SECCORE,RD               DEFINE BASE TO SECURITY AREA
         USING IHADCB,RDCB              RDCB POINTS TO DCB       Y02134
*
         BALR  RBASE,R0                 ESTABLISH BASE REGISTER
         USING *,RBASE                  DEFINE BASE REGISTER
*
         B     OEP00000(RET)            BR TO INDICATED FUNCTION
*
OEP00000 B     OEP01000                 RET=0 INITIAL ENTRY TAPE Y02134
         B     OEP09000                 RET=4 RETURN ENTRY       Y02134
         B     OEP01000                 RET=8 MULTI-FILE TAPE    YM7897
         B     OEP01000                 RET=C INITIAL ENTRY DA   Y02134
*
***********************************************************************
*
*        INITIAL ENTRY FROM OPEN/EOV.
*
*****************************************************************Y02134
*                                                                Y02134
OEP01000 EQU   *                        INITIAL ENTRY
*
         L     RF,WTGPREFX              LOAD PREFIX POINTER      Y02134
         STM   R0,RET,IECREGSV-IECPREFX(RF) SAVE REGS            YM5950
*                                                                Y02134
         XR    R0,R0                    SET ERROR CODE TO ZERO   Y02134
         L     R1,DXJSCBAD              GET ACTIVE JSCB ADDRESS  Y02134
         TM    JSCBSWT1-IEZJSCB(R1),JSCBPASS  CHECK FOR BYPASS   Y02134
         BO    OEP09100                 AUTHORIZATION, BR IF YES Y02134
         TM    DXRCFLAG,DXRCOUT+DXRCINP TEST IF TAPE ALREADY   @G32DSMIX
                                        HAS BEEN RACHECKED     @G32DSMI
         BNZ   OEP09100                 BYPASS PASSWORD        @G32DSMIX
                                        PROTECTION IF YES      @G32DSMI
         EJECT
*
***********************************************************************
*
*        GETMAIN SECURITY WORK AREA                              Y02134
*
*****************************************************************Y02134
*                                                                Y02134
         L     R8,WTGPREFX              SAVE PREFIX POINTER      Y02134
         IECRES GET,LV=SECSIZE,SP=229,ID=SEWA,                   Y02134X
               A=IECSTART-IECPREFX(R8),PREFIX=YES                Y02134
*
         LM    R2,RET,IECREGSV-IECPREFX+K8(R8) RESTORE REGS      YM5950
         L     R8,WTGPREFX              LOAD RETURN PREFIX       YM1242
         LR    RC,RD                    SAVE REG 13              Y02134
*                                                                Y02134
         LA    RD,0(R1)                 LOAD SECURITY WORK AREA REG
         LA    R0,IECSTART-IECPREFX     LOAD PREFIX OFFSET       Y02134
         SR    R1,R0                    POINT TO PREFIX          Y02134
         ST    R1,MYPREFX               SAVE PREFIX POINTER      Y02134
         ST    R8,MYRETURN              SAVE RETURN SAVE AREA    Y02134
*                                                                Y02134
         MVC   MYWTGWTO,WTOLEN          SET UP MODULE WTO        Y02134
         MVC   MYWTGNM,NAME5T           MODNM TO WTG TABLE       Y02134
         MVC   MYRETMOD,NAME5T          SET UP RETURN LOAD       Y02134
         MVI   MYRETCOD,K4              SET UP RETURN OFFSET     Y02134
*                                                                Y02134
         MVC   MYDSN,JFCBDSNM           SETUP JFCB DATA SET NAME YM7897
*                                                                YM7897
         B     OEP01050(RET)            BRANCH TO BRANCH TABLE   YM7897
OEP01050 EQU   *                        BRANCH TABLE             YM7897
         B     OEP02000                 FINISH WORK AREA INIT    YM7897
         NOP   K0                       INVALID ENTRY            YM7987
         B     OEP01100                 SPECIAL TAPE ENTRY       YM7897
         NOP   K0                       DA PROCESSING            YM7897
*                                                                Y02134
         MVC   MYDSN,0(RC)              MOVE IN DSNAME           Y02134
         MVC   MYDSCB,0(RB)             MOVE IN F1 DSCB          Y02134
*                                                                Y02134
         B     OEP02000                 BRANCH TO INITIALIZE     Y02134
*
OEP01100 EQU   *                        SPECIAL TAPE PROCESSING  YM7897
*                                                                Y02134
*****************************************************************YM7897
*                                                                YM7897
*        GET 17 CHAR DSNAME FROM TAPE LABEL FOR                  YM7897
*        MULTI-FILE PASSWORD PROTECTED TAPE VOLUMES              YM7897
*                                                                YM7897
*****************************************************************YM7897
*                                                                YM7897
         MVI   MYDSN,BLANK              INITIALIZE WITH BLANKS   YM5950
         MVC   MYDSN+K1(L'MYDSN-K1),MYDSN INITIALIZE WITH BLANKS YM5950
         MVC   MYDSN(L'FL1ID),FL1ID     MOVE IN DSNAME           Y02134
         EJECT
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
*        FINISH WORK AREA INITIALIZATION                         Y02134
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
OEP02000 EQU   *                        WORK AREA INITIALIZATION Y02134
*
         MVI   MYCODE1,K0               SET OPEN INDICATOR       YM1242
         CLC   WTGMODNM+K3(L'OEPEOV),OEPEOV TEST FOR EOV         Y02134
         BNE   OEP02100                 BRANCH IF OPEN           Y02134
         MVI   MYCODE1,K1               SET EOV INDICATOR IN WORK AREA
*                                                                Y02134
OEP02100 EQU   *                        CONTINUE                 Y02134
*                                                                Y02134
         MVC   IECRRPRM-IECEXTPR(K4,RD),IECRRPRM-IECPREFX(R8) RR Y02134
*                                                                Y02134
         L     R1,DXTCBADR              LOAD TCB                 Y02134
         ST    R1,MYTCB                 SAVE TCB ADDRESS         Y02134
         L     R1,DXDSAB                LOAD DSAB ADDRESS        Y02134
         ST    R1,MYDSAB                SAVE DSAB ADDRESS        Y02134
         L     R1,DXJSCBAD              LOAD ACTIVE JSCB         Y02134
         ST    R1,MYJSCB                SAVE JSCB POINTER        Y02134
         STCM  RDCB,B'1000',MYDEBSYS    SAVE OPEN ATTRIBUTES     Y02134
         LA    RCORE,MYDSCB             LOAD POINTER TO DSCB     Y02134
         LA    RWTG,MYWTG               LOAD POINTER TO WTG TABL Y02134
*                                                                Y02134
         IECRES LOAD,MODNM=IFG0195V,                             Y02134X
               PREFIX=WTGPREFX,BRANCH=DIRECT                     Y02134
         EJECT
*
***********************************************************************
*
*        EXIT TO CALLER                                          Y02134
*
*****************************************************************Y02134
*
***********************************************************************
*
*  INDICATE TO ANY FOLLOWING EOV THAT THE PASSWORD HAS ALREADY BEEN
*  CHECKED FOR THIS DATA SET.  THE INDICATOR WILL BE PROPOGATED FROM
*  OLD TO NEW DEB.  THE SAME INDICATOR IS SET ON AT THE END OF OPEN
*  IF THE PASSWORD WAS CHECKED BY OPEN.
*
*****************************************************************Y02134
*                                                                Y02134
OEP09000 EQU   *                        RETURN                   Y02134
*                                                                Y02134
         L     R8,MYRETURN              LOAD SAVE AREA           Y02134
         XR    R9,R9                    CLEAR REG FOR IC INST    Y02134
         IC    R9,MYERRCOD              GET ERROR CODE           Y02134
         IECRES FREE,PREFIX=YES,A=(RD)  FREE WORK AREA           Y02134
*                                                                Y02134
         LR    R0,R9                    GET RETURN CODE          Y02134
         LR    RF,R8                    GET SAVED PREFIX PTR     Y02134
         EJECT
*                                                                Y02134
OEP09100 EQU   *                        PASSWORD BYPASS ENTRY    Y02134
*                                                                Y02134
         LM    R1,RET,IECREGSV-IECPREFX+K4(RF) RESTORE REGISTERS YM5950
*                                                                Y02134
         LTR   R0,R0                    TEST FOR ERROR           Y02134
         BNZ   OEP10000                 BRANCH IF ERROR          Y02134
*                                                                Y02134
         CLC   WTGMODNM+K3(L'OEPEOV),OEPEOV TEST FOR EOV         Y02134
         BNE   OEP09200                 BRANCH IF NOT EOV        Y02134
*                                                                Y02134
         L     RF,DCBDEBAD              GET DEB ADDR             YM1486
         OI    DEBFLGS1-DEB(RF),DEBPWCKD INDICATE PASSWORD CHKED YM1486
*
OEP09200 EQU   *                        SYNCH UP AND RETURN      Y02134
*                                                                Y02134
         XR    RET,RET                  CLEAR REG 14             Y02134
         IC    RET,DXRETCOD             GET RETURN OFFSET        Y02134
*
         IECRES LOAD,MODID=DXRETMOD,BRCODE=(14),BRANCH=QUEUED    Y02080
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
*        PROCESS PASSWORD ERROR                                  Y02134
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
OEP10000 EQU   *                        PROBLEM DETERMINATION    Y02134
*                                                                Y02134
         DMABCOND (0),ID5T0P            GO PROCESS ERROR         Y02134
         EJECT
*
***********************************************************************
*
*        CONSTANTS
*
***********************************************************************
*
OEPEOV   DC    C'055'                   EOV SVC CODE             Y02134
WTOLEN   DC    0XL4'00',AL2(12),AL2(0)  MODULE TRACE WTO LENGTH  Y02134
*
         XCTLTABL ID=(ID5T0P,0P,NAME5T,IFG0195T,,IFG0195V),      Y02134X
               BRT=YES,LENGTH=                                   Y02134
         IECDSECS CVT,TCB,JSCB,DEB,DCB,                          Y02134X
               MAIN,SECCORE,WTG,PREFX,EXPAND=YES                 Y02134
         IECEQU ,                       DEFINE EQUATES           Y02134
         END
