         TITLE 'IFG0196Q/OPEN - OUTPUT DATE CHECK AND REWRITE VOL LBL'
IFG0196Q CSECT
***********************************************************************
*                                                                     *
*           VS2 RELEASE 037 CHANGES/DELETIONS                         *
*OOOO494100,491600                                             @ZA17986
*           VS2 RELEASE 030 CHANGES/DELETIONS                         *
*                                                                     *
*0000                                                          @ZA09999
*0000                                                          @ZA13523
*0000                                                          @ZA17605
*0000                                                          @ZA24754
* MODULE NAME = IFG0196Q (OS/VS2)                                     *
*                                                                     *
* DESCRIPTIVE NAME = TAPE OUTPUT DATE CHECK AND REWRITE VOLUME LABEL  *
*                                                                     *
* COPYRIGHT = NONE                                                    *
*                                                                     *
* STATUS = RELEASE 2, LEVEL 0                                         *
*                                                                     *
* FUNCTION =                                                          *
*        1. CHECK THE EXPIRATION DATE.                                *
*                                                                     *
*        2. IF THE EXPIRATION DATE HAS NOT EXPIRED, ISSUE WTOR VIA    *
*        THE MESSAGE MODULE (IFG0190R) TO DETERMINE THE OPERATOR'S    *
*        DECISION AS TO WHETHER TO USE THE VOLUME OR NOT.             *
*                                                                     *
*        3. IF THE EXPIRATION DATE HAS EXPIRED, CHECK WHETHER OR NOT  *
*        SECURITY PROCESSING IS NEEDED.  IF YES, GO TO CHECK SECURITY *
*        VIA THE SECURITY MODULE (IFG0195T).                          *
*                                                                     *
*        4. IF NO SECURTIY PROCESSING IS NEEDED, CHECK FILE SEQUENCE  *
*        NUMBER.  IF IT IS GREATER THAN 1, GO TO HEADER LABEL WRITING *
*        MODULE (IFG0196T).                                           *
*                                                                     *
*        5. IF THE FILE SEQUENCE NUMBER IS NOT GREATER THAN 1, READ   *
*        THE VOLUME LABEL AND INITIATE THE REWRITING OF THE LABEL.    *
*                                                                     *
* NOTES = SEE BELOW                                                   *
*                                                                     *
*    DEPENDENCIES =                                                   *
*            CLASS ONE CHARACTER CODE.  THE EBCDIC CHARACTER CODE     *
*            WAS USED FOR ASSEMBLY.  THE MODULE MUST BE REASSEMBLED   *
*            IF A DIFFERENT CHARACTER SET IS USED FOR EXECUTION.      *
*                                                                     *
*    RESTRICTIONS = NONE                                              *
*                                                                     *
*    REGISTER CONVENTIONS =                                           *
*            R2 POINTS TO DCB                                         *
*            R4 POINTS TO OPEN WORK AREA                              *
*            R5 POINTS TO THE RESIDENT ROUTINE                        *
*            R6 POINTS TO THE WTG TABLE                               *
*            R7 POINTS TO THE CURRENT PARAMETER LIST ENTRY            *
*            R8 POINTS TO THE CURRENT WTG TABLE ENTRY                 *
*            R9 POINTS TO THE DD ENTRY IN THE TIOT                    *
*            R10 POINTS TO THE UCB                                    *
*                                                                     *
*    PATCH LABEL = SEE NEXT TO LAST LABEL BEFORE ORG STATEMENT AT END *
*                  OF LISTING.                                        *
*                                                                     *
* MODULE TYPE = CONTROL (OPEN, CLOSE, EOV DATA MANAGEMENT)            *
*                                                                     *
*    PROCESSOR = ASSEMBLER XF                                         *
*                                                                     *
*    MODULE SIZE = SEE EXTERNAL SYMBOL DICTIONARY OR LOC FIELD ON     *
*                  ORG STATEMENT AT END OF LISTING                    *
*                                                                     *
*    ATTRIBUTES = REENTRANT, REFRESHABLE,READ-ONLY, ENABLED,          *
*                 PRIVILEGED, SUPERVISOR STATE, DATA MANAGEMENT KEY,  *
*                 LINK PACK AREA RESIDENT/PAGEABLE                    *
*                                                                     *
* ENTRY POINT = IFG0196Q FROM IFG0196N - TAPE OUTPUT SECURITY         *
*                                                                     *
*    PURPOSE = SEE FUNCTION                                           *
*                                                                     *
*    LINKAGE =                                                        *
*        FROM IFG0196N:                                               *
*          LA    RF,ID6N6Q                                            *
*          LA    RET,K8    OR LA    RET,K4                            *
*          IECRES LOAD,MODID=(RF),BRCODE=(RET),BRANCH=QUEUED          *
*                                                                     *
*        FROM IFG0195T AND IFG0194J:                                  *
*          BRANCH=QUEUED USIND DXRETMOD AND DXRETCOD                  *
*                                                                     *
* INPUT = TAPE LABELS                                                 *
*                                                                     *
* OUTPUT =                                                            *
*        TAPE LABELS AND WTO DATE EXPIRATION MESSAGE                  *
*                                                                     *
* EXIT-NORMAL = IFG0195T - OPEN/EOV PASSWORD SECURITY INITALIZATION   *
*               IFG0196T - TAPE OUTPUT HEADER LABEL PREPARATION       *
*               IFG0194J - MESSAGE MODULE                             *
*              IFG0194A - VOLUME AND UNIT SELECTION                   *
*                                                                     *
* EXIT-ERROR = IFG0194A - MESSAGE MODULE                              *
*                713 - 04 ABEND                                       *
*                613 - 10 ABEND                                       *
*                                                                     *
* EXTERNAL REFERENCES = SEE BELOW                                     *
*                                                                     *
*    ROUTINES = IFG019RA                                              *
*                                                                     *
*    DATA AREAS = NONE                                                *
*                                                                     *
*    CONTROL BLOCK = COPED DCB AND JFCB                               *
*                                                                     *
* TABLES = WORK AREA                                                  *
*                                                                     *
* MACROS = IECRES, MODESET, XLATE, EXCP, XCTLTABL                     *
*                                                                     *
* CHANGE ACTIVITY = NEW RELEASE (LEVEL 0)                             *
* YM1413,YM1478,YM5712                                                *
*                                                                     *
***********************************************************************
         EJECT
*                                                                     *
*                   ADDRESSABILITY
         SPACE 1
         BALR  RBASE,0                  LOAD BASE REGISTER
         USING *,RBASE                  ESTABLISH ADDRESSABILITY
         USING FORCORE,RCORE            WORK AREA ADDRESSABILITY
         USING WTG,RWTG                 WHERE-TO-GO TABLE        Y02080
         USING IHADCB,RDCB              DCB ADDRESSABILITY
         USING SRT,RUCB                 UCB ADDRESSABILITY
         USING TIOENTRY,RTIOT           TIOT ADDRESSABILITY
         USING CVT,RB                   CVT ADDRESSABILITY
*
         B     OTA40600(RET)            GO TO PROPER ENTRY
OTA40600 EQU   *
         B     OTA41300                 WAIT FOR WTOR REPLY
         B     OTA41830                 REWRITE VOL LABEL        YM7530
         B     OTA41100                 CHECK EXPIRATION DATE    Y02134
         B     OTA41800                 RETURN FROM PASSWORD     YM7530
         B     OTA41810                 RETURN CHK PT QUERY      YM5712
         SPACE
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
*        CHECK EXPIRATION DATE                                   Y02134
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
OTA41100 EQU   *                        CHECK EXPIRATION DATE    Y02134
*                                                                Y02134
         XC    DXWORK,DXWORK            CLEAR WORK AREA          Y02134
         PACK  DXWORK(K4),FL1EXPDT+K1(K5)  EBCDIC TO DECIMAL     YM5368
         L     RB,CVTPTR                LOAD CVT ADDRESS         YM4692
         CLC   CVTDATE,DXWORK           CHECK IF DATE EXPIRED    Y02080
         BNL   OTA41600                 GO TO CHECK SECURITY     Y02134
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
*        GO TO ERROR ROUTINE TO WRITE EXPIRATION DATE            Y02134
*        ERROR MESSAGE TO THE OPERATOR, AND WAIT FOR             Y02134
*        HIS REPLY TO USE OR NOT USE THE DATA SET.               Y02134
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
OTA41200 EQU   *                        WRITE EXPIRATION DATE MSG
*                                                                Y02134
         MVC   DXRETMOD,ID6Q6Q          RETURN ID TO SAVE AREA   Y02134
         MVI   DXRETCOD,K0              SET RETURN INDICATOR     Y02134
*                                                                Y02134
         XC    DXWORK1,DXWORK1          SET REPLY AREA TO BLANKS Y02134
         LA    R1,DXWORK1               POINT TO REPLY AREA      Y02134
         LA    R0,K1                    SET REPLY LENGTH         Y02134
         SLL   R0,K24                   MOVE LENGTH TO HI BYTE   Y02134
         OR    R1,R0                    SET UP WTOR PARM         Y02134
         MVC   DXCCW2(L'FL1ID),FL1ID    SAVE DSNAME              Y02134
         LA    R0,DXCCW2                GET DSNAME ADDRESS       Y02134
         MVI   DXCCW2+L'FL1ID,BLANK     DELIMIT 17 CHAR NAME     Y02134
         MVC   DXCCW2+L'FL1ID+K1(L'JFCBDSNM-L'FL1ID-K1),DXCCW2+L'FL1ID
*                                                                Y02134
         IECRES LOAD,MODID=ID6Q4J,BRCODE=K20,BRANCH=QUEUED       Y02134
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
*        RETURN HERE FROM EXPIRATION CHECK                       Y02134
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
OTA41300 EQU   *                        EXPIRATION CHECK RETURN  Y02134
*                                                                Y02134
         IECRES WAIT                    WAIT FOR OPERATOR REPLY  Y02134
*                                                                Y02134
         MVI   DXECB,K127               SET ERROR OFF          @ZA09999
         OI    DXWORK1,BLANK            FOLD REPLY               Y02134
         CLI   DXWORK1,CHARU            CHECK FOR REPLY U        Y02134
         BE    OTA41600                 BRANCH TO USE            Y02134
*                                                                Y02134
         CLI   DXWORK1,CHARM            CHECK IF REJECT          Y02134
         BNE   OTA41200                 BRANCH, INCORRECT REPLY  Y02134
*                                                                Y02134
         LA    R0,K13                   ABEND 713-04             Y02134
         B     OTA42800                 BRANCH TO REJECT VOLUME  Y02134
         EJECT
*                                                                Y02134
OTA41600 EQU   *                        CHECK SECURITY
*                                                                Y02134
         TM    JFCBMASK+K4,DS1WRSEC     SECURITY LOAD NEEDED
         BNO   OTA41800                 BYPASS IF NOT            YM7530
*                                                                Y02134
         MVC   DXRETMOD,ID6Q6T          SETUP MULTIFILE RETURN   YM7530
         MVI   DXRETCOD,K0              SETUP MULTIFILE OFFSET   YM7530
*                                                                YM7530
         CLC   JFCBFLSQ,CONONE9P        FILE SEQ GREATER THAN 1  Y02134
         BH    OTA41700                 YES, BRANCH              Y02134
*                                                                YM7530
         MVC   DXRETMOD,ID6Q6Q          SET UP RETURN LOAD       YM7530
         MVI   DXRETCOD,K12             SET UP RETURN OFFSET     YM1478
*                                                                Y02134
OTA41700 EQU   *                        GO TO SECURITY IFG0195T  Y02134
*                                                                Y02134
*                                                                Y02134
         IECRES LOAD,MODID=ID6Q5T,BRCODE=K0,BRANCH=QUEUED        Y02134
         EJECT
*
***********************************************************************
*                                                                YM7530
*        BEFORE STANDARD LABEL VOLUME LABEL IS REWRITTEN, READ        *
*        HDR 2 LABEL TO SEE IF THE CHECK POINT DATA TESTING           *
*        PROCESS MUST BE EXECUTED.                                    *
*                                                                YM7530
***********************************************************************
*                                                                YM7530
OTA41800 EQU   *                        CHKPT D/S SECURITY TEST  YM5712
*                                                                YM7530
         NI    JFCBMASK+K4,ALLBITS-DS1WRSEC CLEAR SECURITY IND   YM7530
*                                                                YM7530
*        CHKPT SECURITY ALREADY CHECKED IN 195B FOR MULTI FILE   YM7530
         LA    RET,K0                   NO WAIT ENTRY-196T       YM7530
         CLC   JFCBFLSQ,CONONE9P        FILE SEQ GREATER THAN 1  YM7530
         BH    OTA42400                 WRITE HDR1,HDR2          YM7530
*                                                                YM7530
         TM    JFCBLTYP,JFCSL           STANDARD LABELED TAPE    YM5712
         BNO   OTA41830                 NO, BYPASS CHK PT PROC   YM5712
         L     RC,DXDSAB                FETCH DSAB ADDR          YM5712
         USING DSAB,RC                  ADDRESS DSAB             YM5712
         TM    DSABFLG4,DSABCKVL        IS VOL SECURE            YM5712
         BO    OTA41830                 YES, REWRITE VOL LABEL   YM5712
*****************************************************************YM5712
*        READ HEADER 2 LABEL                                     YM5712
*****************************************************************YM5712
         LA    RB,DXLBL                 LABEL BUFFER ADDR        YM5712
         ST    RB,DXCCW1                BUFFER ADDR TO CCW       YM5712
         MVI   DXCCW1,CCWRDTAP          READ OP CODE TO CCW      YM5712
         BAL   RB,OTA42500              GO READ HDR 2 LABEL      YM5712
*                                                                YM7530
         TM    IOBSTAT0,CSWUNITX        CK FOR UNIT EXCEPTION    YM7530
         BO    OTA41830                 BRANCH IF NO HDR2        YM7530
*                                                                YM7530
         CLI   FL2DSIND,CHARC           CHK PT IND ON IN LABEL   YM5712
         BNE   OTA41830                 NO, REWRITE VOL LABEL    YM5712
*                                                                YM7530
*****************************************************************YM5712
*        ASK OPERATOR ABOUT VOLUME STATUS                        YM5712
*****************************************************************YM5712
OTA41805 EQU   *                        QUERY OPERATOR           YM5712
         MVC   DXRETMOD,ID6Q6Q          SET RETURN NAME & EPA    YM5712
         MVI   DXRETCOD,K16             SET BRANCH TABLE RET VAL YM5712
         XC    DXWORK1,DXWORK1          ZERO WTOR REPLY AREA     YM5712
         LA    R1,DXWORK1               POINT TO REPLY AREA      YM5712
         LA    R0,K3                    SET REPLY LENGTH         YM5712
         SLL   R0,K24                   MOVE LENGTH TO HI BYTE   YM5712
         OR    R1,R0                    SETUP WTOR PARM          YM5712
         IECRES LOAD,MODID=ID6Q4J,BRCODE=K56,BRANCH=QUEUED       YM5712
OTA41810 EQU   *                        WAIT ENTRY               YM5712
         IECRES WAIT                    WAIT FOR REPLY           YM5712
         OC    DXWORK1(K3),BLANKS       FOLD REPLY TO UPPER CASE YM5712
         CLC   KYES,DXWORK1             REPLY = 'YES'            YM5712
         BE    OTA41820                 YES, GO SET DSAB IND     YM5712
         CLC   KNO,DXWORK1              REPLY = 'NO '            YM5712
         BE    OTA41830                 GO REWRITE VOL LABEL   @ZA17986
         B     OTA41805                 NEITHER, ISSUE MSG AGAIN YM5712
*****************************************************************YM5712
*        SET DSAB CHECKPOINT DATA SET INDICATORS                 YM5712
*****************************************************************YM5712
OTA41820 EQU   *                                               @ZA17986
         CLC   DXVOLSR2,SCRATCH         IS SCRATCH MOUNTED?    @ZA17986
         BE    OTA42800                 YES= REJECT TAPE       @ZA17986
*                                       NO? SET DSAB FLAGS     @ZA17986
         MODESET EXTKEY=SCHED           SCHEDULER KEY            YM5712
         OI    DSABFLG4,DSABCKDS+DSABCKVL SET CHKPT DSAB FLAGS   YM5712
         MODESET EXTKEY=DATAMGT         BACK TO D/M KEY          YM5712
         DROP  RC                       DSAB ADDRESSABILITY      YM5712
         EJECT
***********************************************************************
*        REWRITE THE VOLUME LABEL                                     *
***********************************************************************
OTA41830 EQU   *                        REWRITE VOL LABEL        YM5712
         MVC   DXCCW1,ACCW9P            MOVE IN BASIC CCW
         BAL   RB,OTA42500              GO REWIND TAPE
         ST    RCORE,DXCCW1             GET ADDRESS OF LBL AREA
         MVI   DXCCW1,CCWRDTAP          ADD READ OP CODE
         BAL   RB,OTA42500              GO READ THE LABEL
         CLI   VOLLABI,CHARV            CHECK TAPE HAS VOL ID
         BE    OTA42200                 YES GO TO BUILD CHAN PROG
         CLI   VOLLABI,ASCIIV           CHECK TAPE FOR ANSI VOL ID
         BE    OTA42200                 YES GO TO BUILD CHAN PROG
         MVI   VOLLABI,BLANK            CLEAR
         MVC   VOLLABI+1(K79),VOLLABI   LABEL AREA
         MVC   VOLLABI(K4),AVOL19P      VOL LBL IDENTIFIER
         MVC   VOLSERNO,UCBVOLI         VOLUME SERIAL NUMBER
         MVI   VOLSEC,K0                SECURITY CODE
         TM    JFCBLTYP,JFCBAL          ANSI LABEL SPECIFIED
         BZ    OTA42200                 NO GO TO REWIND
         MVI   VOLSEC,BLANK             SECURITY CODE TO BLANK
         MVI   LABSTAND,K1              SET LABEL STANDARD LEVEL
         XLATE DXLBL,K80,TO=A           TRANSLATE LABEL DATA
OTA42200 EQU   *                        BUILD CHANNEL PROGRAM
         MVI   DXCCW1,CCWREW            GET REWIND COMMAND
         BAL   RB,OTA42500              GO ISSUE CHANNEL PROGRAM
         MVI   DXCCW1,CCWWRTAP          SET CCW TO WRITE
*        ISSUE I/O HERE AND WAIT FOR COMPLETION IN NEXT LOAD
         EXCP  DXIOB
*                                                                Y02134
         LA    RET,K4                   WAIT ENTRY
         MODESET EXTKEY=SUPR                                   @ZA17605
         CLC   UCBFSEQ,CONONE9P         FILE SEQ > ONE         @ZA17605
         BH    OTA42400                 YES, BRANCH            @ZA13523
         MVC   UCBFSEQ,CONONE9P         FORCE TO A ONE         @ZA17605
         MVC   UCBFSEQ-K2,CONONE9P      FORCE FSEQ CNT TO ONE  @ZA24754
OTA42400 MODESET EXTKEY=DATAMGT                                @ZA17605
         IECRES LOAD,MODID=ID6Q6T,BRCODE=(14),BRANCH=QUEUED      Y02080
         EJECT
***********************************************************************
*                                                                     *
*                   CLOSED SUBROUTINE TO ISSUE I/O OPERATIONS
*                                                                     *
***********************************************************************
         SPACE 1
*                        IT IS ASSUMED THAT ALL CONTROL BLOCKS
*                        HAVE ALREADY BEEN SET UP
         SPACE 1
*                             BAL  RA,PRHR50
*                             RETURN
         SPACE 1
OTA42500 EQU   *                        ISSUE I/O OPERATION
         EXCP  DXIOB                    ISSUE I/O OPERATION
         IECRES WAIT
         TM    DXECB,ECBNOERR           IS THERE AN UNUSUAL CONDITION
         BCR   1,RB                     NO - RETURN TO CALLER
         TM    IOBSTAT0,CSWUNITX        CK FOR UNIT EXCEPTION
         BCR   1,RB                     YES - IGNORE FOR NOW
         SPACE
         NI    DCBIFLGS,X'FF'-DCBIFPIO  TURN OFF ERROR FLAGS IN
*                                       DCB TO ISSUE REWIND
*                                       AND/OR UNLOAD COMMAND
         LA    R0,K15                   ABEND 613-10             Y02134
*                                                                Y02134
OTA42800 EQU   *                        REJECT VOLUME            Y02134
*                                                                Y02134
         MVC   DXCALLID,ID6Q6Q          SET UP ERROR ID          YM7530
         MVC   DXRETMOD,ID6Q6N          SET UP RETURN LOAD       Y02134
         MVI   DXRETCOD,K0              SET UP RETURN OFFSET     Y02134
*                                                                Y02134
         IECRES LOAD,MODID=ID6Q4A,BRCODE=K16,BRANCH=QUEUED       Y02134
         EJECT
*                   CONSTANTS
         SPACE 1
ACCW9P   DC    X'0700000020000050'      BASIC CCW - REWIND
AVOL19P  DC    C'VOL1'                  VOLUME LABEL ID
CONONE9P DC    X'0001'                  CONSTANT OF ONE
BLANKS   DC    C'   '                   3 BLANK CHARACTERS       YM5712
KYES     DC    C'YES'                   'YES' REPLY MASK         YM5712
KNO      DC    C'NO '                   'NO ' REPLY MASK         YM5712
SCRATCH  DC    C'SCRTCH'                SCRATCH REQUEST ?      @ZA17986
K127     EQU   X'7F'                    CONSTANT OF X'7F'      @ZA09999
CHARC    EQU   C'C'                     CHARACTER 'C'            YM5712
         SPACE
XCTL6Q   XCTLTABL ID=(ID6Q6Q,6Q,ID6Q6N,6N,ID6Q6T,6T,ID6Q5T,5T,   Y02134X
               ID6Q4A,4A,ID6Q4J,4J),BRT=YES,LENGTH=              Y02134
         IECDSECS CVT,TCB,TIOT,DSAB,DCB,UCB,MAIN,WTG,EXPAND=YES  YM5712
         IECEQU
         END
