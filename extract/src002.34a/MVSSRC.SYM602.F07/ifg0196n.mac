         TITLE 'IFG0196N/OPEN TAPE OUTPUT SECURITY PROCESSING'
         COPY  LCGASMSW
IFG0196N CSECT
***********************************************************************
*                                                                     *
* MODULE NAME = IFG0196N (OS/VS2)                                     *
*                                                                     *
* DESCRIPTIVE NAME = TAPE OUTPUT SECURITY PROCESSING                  *
*                                                                     *
* COPYRIGHT = NONE                                                    *
*                                                                     *
* STATUS = RELEASE 2, LEVEL 0                                         *
*                                                                     *
*        VS2 RELEASE 040 DELETIONS/CHANGES                            *
*0000544000-548000                                             @ZA02200
*                                                                     *
* FUNCTION =                                                          *
*   INITIAL ENTRY:                                                    *
*        1. READ HEADER LABEL 1.                                      *
*                                                                     *
*        2. CHECK FOR SECURITY PROTECTION. IF YES, COMPARE THE DATA   *
*        SET NAME IN THE LABEL TO THE DATA SET NAME IN THE JFCB. IF   *
*        THEY ARE NOT EQUAL, THE TAPE IS REJECTED.                    *
*                                                                     *
*        3. FOR AN ASCII TAPE WITH THE SECURITY BYTE NON-BLANK BUT    *
*        NOT INDICATING SECURITY, THE TAPE IS REJECTED.               *
*                                                                     *
*        4. IF PASSWORD REQUIRED, SET PASSWORD REQUIRED SWITCH.       *
*                                                                     *
*        5. TRANSFER CONTROL TO THE NEXT MODULE TO PERFORM EXPIRATION *
*        DATE CHECK.                                                  *
*                                                                     *
*        6. IF NOT THE FIRST DATA SET AND THE OLD DATA SET IS    Y02082
*        NOT BEING REWRITTEN(HDR LABELS DO NOT EXIST) CHECK THAT Y02082
*        THE SECURITY LEVEL OF THE NEW FILE MATCHES THE PREVIOUS Y02082
*        FILE. EXIT TO VERIFY PASSWORD OF THE PREVIOUS FILE IF   Y02082
*        PASSWORD PROTECTED.                                     Y02082
*                                                                     *
*   ENTRY FROM SECURITY:                                              *
*        1. IF PROCESSING THE FIRST DATA SET ON THE TAPE, EXIT TO     *
*        REWRITE THE VOLUME LABEL.                                    *
*                                                                     *
*        2. IF NOT THE FIRST DATA SET, POSITION THE TAPE TO THE       *
*        PROPER FILE AND EXIT TO WRITE HEADER LABELS.                 *
* NOTES = SEE BELOW                                                   *
*                                                                     *
*    DEPENDENCIES =                                                   *
*            CLASS ONE CHARACTER CODE.  THE EBCDIC CHARACTER CODE     *
*            WAS USED FOR ASSEMBLY.  THE MODULE MUST BE REASSEMBLED   *
*            IF A DIFFERENT CHARACTER SET IS USED FOR EXECUTION.      *
*                                                                     *
*    RESTRICTIONS = NONE                                              *
*                                                                     *
*    REGISTER CONVENTIONS =                                           *
*            R2 POINTS TO DCB                                         *
*            R4 POINTS TO OPEN WORK AREA                              *
*            R5 POINTS TO THE RESIDENT ROUTINE                        *
*            R6 POINTS TO THE WTG TABLE                               *
*            R7 POINTS TO THE CURRENT PARAMETER LIST ENTRY            *
*            R8 POINTS TO THE CURRENT WTG TABLE ENTRY                 *
*            R9 POINTS TO THE DD ENTRY IN THE TIOT                    *
*            R10 POINTS TO THE UCB                                    *
*                                                                     *
*    PATCH LABEL = SEE NEXT TO LAST LABEL BEFORE ORG STATEMENT AT END *
*                  OF LISTING.                                        *
*                                                                     *
* MODULE TYPE = CONTROL (OPEN, CLOSE, EOV DATA MANAGEMENT)            *
*                                                                     *
*    PROCESSOR = ASSEMBLER XF                                         *
*                                                                     *
*    MODULE SIZE = SEE EXTERNAL SYMBOL DICTIONARY OR LOC FIELD ON     *
*                  ORG STATEMENT AT END OF LISTING                    *
*                                                                     *
*    ATTRIBUTES = REENTRANT, REFRESHABLE,READ-ONLY, ENABLED,          *
*                 PRIVILEGED, SUPERVISOR STATE, DATA MANAGEMENT KEY,  *
*                 LINK PACK AREA RESIDENT/PAGEABLE                    *
*                                                                     *
*   ENTRY POINT = IFG0196N FROM IFG0194A- VOLUME REMOUNT              *
*                               IFG0196M - DCB TO JFCB MERGE          *
*                               IFG0194J - MESSAGE MODULE             *
*                                                                     *
*    PURPOSE = SEE FUNCTION                                           *
*                                                                     *
*    LINKAGE =                                                        *
*        FROM IFG0194A:                                               *
*          IECRES BRANCH=QUEUED USING DXRETCOD AND DXRETMOD SETUP     *
*          IN IFG0196N AFTER REJECTING VOLUME.                        *
*                                                                     *
*        FROM IFG0196M:                                               *
*          IECRES LOAD,MODID=ID6M6N,BRCODE=0,BRANCH=QUEUED            *
*                                                                     *
*        FROM IFG0194J:                                               *
*          IECRES BRANCH=QUEUED USING DXRETCOD AND DXRETMOD SETUP     *
*          IN IFG0196N.                                               *
*                                                                     *
* INPUT = STANDARD                                                    *
*                                                                     *
* OUTPUT = TAPE POSITIONED                                            *
*                                                                     *
* EXIT-NORMAL = IFG0196Q - REWRITE VOLUME LABEL                       *
*               IFG0196T - WRITE HEADER LABEL                         *
*               IFG0194J - ISSUE MOUNT MESSAGE                        *
*               IFG0194A - VOLUME REJECT                              *
*               IFG0195T - CHECK PASSWORD, IF MULTIPLE FILES     Y02082
*                                                                     *
* EXIT-ERROR =                                                        *
*    EXIT WITH ONE OF THE FOLLOWING INTERNAL CODES--                  *
*       9 - 613 ABEND - I/O ERROR POSITIONING TAPE.                   *
*       11 - 613 ABEND - I/O ERROR READING LABEL.                     *
*       12 - 613 ABEND - INVALID LABEL READ.                          *
*       20 - 913 ABEND - ASCII ACCESSIBILITY ERROR.                   *
*      244 - 913 ABEND - SECURITY LEVEL OF MULTIPLE FILES DIFFER Y02082
*                                                                     *
*                                                                     *
* EXTERNAL REFERENCES = SEE BELOW                                     *
*                                                                     *
*    ROUTINES = IFG019RA                                              *
*                                                                     *
*    DATA AREAS = NONE                                                *
*                                                                     *
*    CONTROL BLOCK = UCB, JFCB, IOB, DCB                              *
*                                                                     *
* TABLES = STANDARD                                                   *
*                                                                     *
* MACROS = XLATE, IECRES, EXCP, XCTLTABL, IECDSECS, IECEQU            *
*                                                                     *
* CHANGE ACTIVITY = NEW RELEASE (LEVEL 0)                             *
*                                                                     *
***********************************************************************
         EJECT
*
         SPACE 1
*                   ADDRESSABILITY
         SPACE 1
         BALR  RBASE,0                  LOAD BASE REGISTER
         USING *,RBASE                  ESTABLISH ADDRESSABILITY
*
         USING FORCORE,RCORE            WORK AREA ADDRESSABILITY
         USING WTG,RWTG                 WHERE-TO-GO TABLE        Y02080
         USING IHADCB,RDCB              DCB ADDRESSABILITY
         USING SRT,RUCB                 UCB ADDRESSABILITY
         USING TIOENTRY,RTIOT           TIOT ADDRESSABILITY
*
         L     RUCB,DXUCBADR            GET UCB ADDRESS
         XC    DXCALLID,DXCALLID        CLEAR ERROR ID           YM7530
*                                                                Y02134
         B     OTA37800(RET)            BRANCH ON OFFSET         Y02134
*                                                                Y02134
OTA37800 EQU   *                        BRANCH TABLE             Y02134
*                                                                Y02134
         B     OTA37850                 BRANCH ON INITIAL ENTRY  Y02134
         B     OTA40400                 REJECT ENTRY             Y02134
         SPACE 2
*
*        POINT REG13 TO LAST 17 CHARACTERS IN JFCB DATA SET NAME Y02134
*                                                                Y02134
OTA37850 EQU   *                        INITIAL ENTRY POINT      Y02134
*                                                                Y02134
         LA    RB,JFCBDSNM              GET ADDR OF START
         LA    RD,JFCBDSNM+K27          GET ADDR 1ST POSS SIG CHR
         SPACE ,
OTA37900 EQU   *                        TEST FOR BLANK CHARACTER
         SPACE ,
         CLI   K16(RD),BLANK            IF CHAR NOT BLANK
         BNE   OTA38000                 BRANCH TO FINISH ROUTINE
         SPACE ,
         BCT   RD,OTA37900              DECR PTR AND BRANCH TO
*                                       TEST NEXT CHAR TO LEFT
OTA38000 EQU   *                        FINAL REGISTER ADJUSTMENT
         SPACE ,
         LA    RC,K17(RD)               POINT TO FIRST BLANK
         CLR   RD,RB                    IF LEFT PTR WITHIN FIELD
         BNL   OTA38100                 BRANCH OUT, ALL THRU
         SPACE ,
         LR    RD,RB                    LEFT PTR CAN'T BE LEFT
*                                       OF START OF FIELD
OTA38100 EQU   *                        R13 POINTS TO 17 SIG CHARS
         EJECT
         MVC   DXCCW1,ACCW
         ST    RCORE,DXCCW1             RESET CCW1
*
         SPACE 1
*                   SET UP CHANNEL PROGRAM AND READ LABEL
         SPACE 1
         MVI   DXCCW1,CCWRDTAP          DITTO FOR READ OPERATION CODE
         SPACE
OTA38300 EQU   *                        READ TAPE
         SPACE
         MVI   DXLBL,BLANK              CLEAR AREA
         MVC   DSCFILSR,DXLBL           OF VOLUME LABEL
OABD011  EQU   11                       INTERNAL ABEND CODE      M0034
         MVI   DXCCW5,OABD011           INT CODE-I/O ERR ON READ M0034
         SPACE
         BAL   RB,OTA40200              GO PERFORM READ OF LABEL
         SPACE
OTA38400 CLI   FL1LABI,CHARV            IS THIS A VOLUME LABEL
         BE    OTA38300                 YES,GO TO READ ANOTHER LABEL
         CLI   FL1LABI,ASCIIV           IS THIS AN ANSI VOL LABEL
         BE    OTA38300                 YES, GO READ ANOTHER LABEL
         CLC   AHDR1,FL1LABI            NO,IS THIS A HEADER LABEL 1
         BE    OTA38600                 YES, GO CHECK SECURITY
         SPACE
         CLI   FL1LABI+K1,UCBBSTR       IS THIS ANSI USER VOL LAB
         BE    OTA38300                 YES, GO READ ANOTHER LABEL
         CLC   ANSHDR1,FL1LABI          ASCII HEADER 1 LABEL READ
         BNE   OTA38500                 NO, GO TEST FOR UNIT EXCEPTION
         XLATE DXLBL,K80                YES, TRANSLATE LABEL DATA
         B     OTA38600                 GO CHECK SECURITY
OTA38500 EQU   *                        TEST FOR UNIT EXCEPTION
         TM    IOBSTAT0,CSWUNITX        CHK FOR UNIT EXCEPTION
OABD012  EQU   12                       INTERNAL ABEND CODE      M0034
         MVI   DXCCW5,OABD012           INT CODE-INVALID LBL     M0034
*                                       READ                     M0034
         BNO   OTA40400                 NO, GO GET A NEW REEL
         CLC   CONONEP,UCBFSCT          IS THIS THE 1ST DS       Y02082
         BL    OTA38510                 NO, GO CHECK VOL SEC     Y02082
         SPACE
*        REWRITE VOL1 LABEL                                      YM7530
         IECRES LOAD,MODID=ID6N6Q,BRCODE=K4,BRANCH=QUEUED        YM7530
         EJECT
*
*        DATA SET SECURITY
*
OTA38510 EQU   *                        CHECK TAPE VOL SECURITY  Y02082
*
* ALL DATA SETS ON A TAPE VOLUME MUST HAVE THE SAME LEVEL OF     Y02082
* SECURITY. FURTHERMORE, THE ADDITION OF ANY DATA SET AFTER THE  Y02082
* FIRST WILL ONLY BE PERMITTED IF THE PRECEEDING DATA SET'S      Y02082
* PASSWORD IS CORRECTLY ISSUED.                                  Y02082
*
* THE FOLLOWING CODE READS THE 'EOF1' LABEL OF THE PRECEEDING    Y02082
* DATA SET. A CHECK IS MADE TO INSURE THAT THE NEW DATA SET AND  Y02082
* THE PRECEEDING DATA SET HAVE IDENTICAL SECURITY LEVELS.        Y02082
* DIFFERENT SECURITY LEVELS WILL RESULT IN AN ABEND. CONTROL IS  Y02082
* PASSED TO THE APPROPRIATE MODULE FOR A PASSWORD CHECK OF THE   Y02082
* PREVIOUS DATA SET.                                             Y02082
*
*
* POSITION TAPE TO PREVIOUS DATA SET EOF1  RECORD BY 3 BACKSPACE Y02082
* FILES AND A FORWARD SPACE FILE. ISSUE SEPARATE EXCP'S FOR EACH Y02082
* CHANNEL COMMAND TO MINIMIZE CHANNEL TIEUP.                     Y02082
*
         MVC   DXCCW1(K8),ACCW          INITIALIZE COUNT AND SIL Y02082
         MVI   DXCCW1,CCWBSF            SET UP BSF               Y02082
         BAL   RB,OTA40200              ISSUE EXCP FOR 1ST BSF   Y02082
         BAL   RB,OTA40200              2ND BSF                  Y02082
         BAL   RB,OTA40200              3RD BSF                  Y02082
         MVI   DXCCW1,CCWFSF            SET UP FSF COMMAND       Y02082
         BAL   RB,OTA40200              ISSUE EXCP               Y02082
*
* READ THE EOF1 RECORD AND RESTORE TAPE POSITION                 Y02082
         MVC   DXCCW1(K8),ACCW          INITIALIZE CCW COUNT     Y02082
         ST    RCORE,DXCCW1             SET UP READ ADDRESS      Y02082
         MVI   DXCCW1,CCWRDTAP          SET UP READ COMMAND      Y02082
         BAL   RB,OTA40200              ISSUE EXCP               Y02082
         MVC   DXCCW1(K8),ACCW          RESTORE COUNT AND ADDR   Y02082
         MVI   DXCCW1,CCWFSF            SET UP FSF               Y02082
         BAL   RB,OTA40200              1ST FSF                  Y02082
         MVI   DXCCW1,CCWNOP            SET UP AND ISSUE NOP     Y02082
         BAL   RB,OTA40200              TO CHECK CONDITIONS      Y02082
         EJECT
*
* VERIFY THAT THIS IS 'EOF1' RECORD AND CHECK SECURITY CODE      Y02082
*
         CLC   AEOF1(K4),FL1LABI        IS THIS  'EOF1'          Y02082
         BE    OTA38520                 YES, CHECK SECURITY CODE Y02082
         TM    UCBSTAB,UCBBSTR          IS THIS AN ANS TAPE      Y02082
         BNO   OTA40400                 NO, REJECT VOLUME        Y02082
         CLC   ANSEOF1(K4),FL1LABI      IS THIS AN ANS 'EOF1'    Y02082
         BNE   OTA40400                 NO, REJECT VOLUME        Y02082
         XLATE DXLBL,K80                TRANSLATE TO EBCDIC      Y02082
         CLI   FL1FSEC,BLANK            IS SECURITY CODE BLANK   Y02082
         BNE   OTA38520                 NO, CONTINUE CHECK       Y02082
         MVI   FL1FSEC,FL1NOSEC         YES, CHANGE TO EBCDIC 0  Y02082
OTA38520 EQU   *                        CHECK FOR SAME SECURITY  Y02082
         IC    RB,JFCBIND2              GET NEW DS SECURITY BITS Y02082
         SLL   RB,K26                   SHIFT OFF HIGH ORDER BIT Y02082
         SRL   RB,K30                   SHIFT OFF LOW ORDER BITS Y02082
         LA    RB,FL1NOSEC(RB)          MAKE IT AN EBCDIC CHAR   Y02082
         CLM   RB,K1,FL1FSEC            DO SECURITY CODES MATCH? Y02082
         BE    OTA38530                 YES CONTINUE CHECK       Y02082
OABD0244 EQU   244                      INTERNAL ABEND CODE      Y02082
         MVI   DXCCW5,OABD0244          SET ABEND CODE FOR REJ   Y02082
         B     OTA40300                 GO ISSUE ABEND MESSAGE   Y02082
OTA38530 EQU   *                        CHECK FOR SECURITY LEVEL Y02082
         CLI   FL1FSEC,FL1NOSEC         IS IT NO PASSWORD?       Y02082
         BE    OTA38550                 YES, AVOID CHECK         YM5755
         MVC   DXRETMOD,ID6N6T          SET UP RETURN MOD        Y02082
         MVI   DXRETCOD,K0               AND RETURN CODE         Y02082
         IECRES LOAD,MODID=ID6N5T,BRCODE=K8,BRANCH=QUEUED        YM7897
OTA38550 IECRES LOAD,MODID=ID6N6T,BRCODE=K0,BRANCH=QUEUED        YM7877
         EJECT
OTA38600 EQU   *                        CHECK SECURITY
         TM    UCBSTAB,UCBBSTR          ASCII VOLUME MOUNTED     M0034
         BZ    OTA38900                 NO, PERFORM SECURITY CHECK
         CLI   FL1FSEC,BLANK            UNLIMITED ACCESSIBILITY
         BE    OTA39600                 YES, GO CHECK FILE       YM7530
         CLI   FL1FSEC,FL1SECTY         CHECK PASSWORD PROTECTION
         BE    OTA38900                 YES, CONTINUE SECURITY CHECK
         CLI   FL1FSEC,FL1WRSEC         WR ONLY PASSWORD PROTECT A53145
         BE    OTA38900                 YES, CONT SECURITY CHK   A53145
         B     OTA40350                 BRANCH TO REJECT         YM7530
*                                                                YM7530
*   CONTINUE PROCESSING                                          M0034
OTA38900 EQU   *                        CONTINUE SECURITY CHECK
         CLI   FL1FSEC,FL1SECTY         TEST FOR SEC PROT VOLUME
         BNE   OTA39000                 BRANCH IF NOT FOR BOTH RD + WR
         BAL   RB,OTA39200              GO CHECK DSNAME
         B     OTA39100                 BRANCH TO IND PASSWORD REQ
OTA39000 EQU   *                        NOT BOTH RD AND WR
         CLI   FL1FSEC,FL1WRSEC         IS SECURITY FOR WRITE ONLY
         BNE   OTA39600                 BRANCH IF NO             YM7530
         BAL   RB,OTA39200              GO CHECK DSNAME
OTA39100 EQU   *                        INDICATE PASSWORD REQ
         OI    JFCBMASK+K4,DS1WRSEC     SECURITY PASSWORD REQ SW YM7530
         TM    JFCBIND2,JFCNEW          IS DISP=NEW?           @ZA02200
         BO    OTA39600                 YES, DO NOT PROPOGATE  @ZA02200
*                                       SECURITY INDICATOR     @ZA02200
         OI    JFCBIND2,JFCBSCTY        PROPOGATE SECURITY INIDICATOR
         B     OTA39600                 BRANCH TO CHECK FILE     YM7530
         EJECT
*                                                                YM7530
*****************************************************************YM7530
*                                                                YM7530
*   COMPARE THE DATA SET NAME IN THE LABEL AGINST THE NAME IN THE
*   JFCB. RETURN IF EQUAL. IF NOT EQUAL AND THE DATA SET IS A
*   GENERATION DATA SET, IT MAY BE A DOS TAPE IN WHICH CASE THE
*   DATA SET NAME MUST BE COMPARED SEPARATELY FROM THE GENERATION
*   AND VERSION NUMBERS.
*                                                                YM7530
*****************************************************************YM7530
*
OTA39200 EQU   *                        CHECK DS NAME
         CLC   FL1ID,0(RD)              COMPARE DSNAME
         BCR   8,RB                     BRANCH, DSNAME EQUAL
         TM    JFCBIND1,JFCGDG          GENERATION DATA SET
         BNO   OTA40300                 NO, BRANCH
         LA    RF,K9                    PREPARE TO BACK OVER GEN
         SR    RC,RF                    BACK UP END POINTER
         CLC   FL1GNO,K2(RC)            COMPARE GEN NUMBER
         BNE   OTA40300                 BRANCH IF NOT EQUAL
         CLC   FL1VNG,K7(RC)            COMPARE VERSION NUMBER
         BNE   OTA40300                 BRANCH IF NOT EQUAL
         SR    RD,RF                    BACK UP COMPARE START
         LA    RET,JFCBDSNM             GET START OF DSNAME
         CLR   RD,RET                   IS LEFT POINTER WITHIN FIELD
         BNL   OTA39400                 YES, BRANCH
         SR    RC,RET                   GET CHAR COUNT
         LA    RF,FL1ID                 POINT TO DSNAME IN LABEL
         AR    RF,RC                    ADD CHAR COUNT
         CLI   K0(RF),BLANK             SHOULD BE PAST END OF NAME
         BNE   OTA40300                 BR, NAME WRONG LENGTH
         BCTR  RC,R0                    SUB 1 FOR EXECUTE INSTRUCTION
         EX    RC,OTA39300              COMPARE DSNAMES
         B     OTA39500                 GO TEST COMPARE RESULT
OTA39300 EQU   *                        INSTRUCTION FOR EX
         CLC   FL1ID(0),JFCBDSNM        COMPARE DS NAME
OTA39400 EQU   *                        LEFT POINTER WITHIN FIELD
         EX    R0,OTA39200              COMPARE DSNAMES
OTA39500 EQU   *                        TEST COMPARE RESULTS
         BNE   OTA40300                 BRANCH, DSNAME ERROR
         BR    RB                       RETURN, DSNAME CORRECT
***
         EJECT
*                                                                YM7530
*****************************************************************YM7530
*                                                                YM7530
*  THIS IS NOT THE FIRST DATA SET ON THIS TAPE SO DON'T REWIND
*  CHANNEL PROGRAM WILL BACK SPACE RECORD OVER THE HEADER,
*  BACK SPACE FILE OVER THE TAPE MARK, FORWARD SPACE OVER THE
*  TAPE MARK TO CLEAR THE BACKWARD STATUS AND A NOP INSTRUCTION TO
*  IGNORE AN UNUSUAL END.
*                                                                YM7530
*****************************************************************YM7530
*                                                                YM7530
OTA39600 EQU   *                        BACKSPACE TO BEG OF LABEL
*                                                                YM7530
         CLC   CONONEP,UCBFSCT          CHECK IF FIRST FILE      YM7530
         BNL   OTA39900                 BRANCH IF FIRST DATA SET YM7530
         SPACE
*              POSITION TAPE IN FRONT OF FIRST HEADER LABEL      YM7530
         SPACE
         MVC   DXCCW1,ACCW              GET BASIC CCW
OABD009  EQU   9                        INTERNAL ABEND CODE      M0034
         MVI   DXCCW5,OABD009           INT CODE-I/O             M0034
*                                       ERR-POSITION             M0034
         MVI   DXCCW1,CCWBSF            SETUP BACK SPACE FILE    YM7530
         BAL   RB,OTA40200              GO BACKSPACE FILE        YM7530
         MVI   DXCCW1,CCWFSF            2ND CCW IS FORWARD SPACE YM7530
         BAL   RB,OTA40200              GO FORWARD SPACE FILE    YM7530
         MVI   DXCCW1,CCWNOP            NOP TO IGNORE UNUSUAL ENDYM7530
         BAL   RB,OTA40200              GO EXECUTE CHAN PROGRAM
*                                                                Y02134
OTA39900 EQU   *                        SET UP VOL LABEL
         IECRES LOAD,MODID=ID6N6Q,BRCODE=K8,BRANCH=QUEUED        YM7530
         EJECT
*
*                   CLOSED SUBROUTINE TO ISSUE I/O OPERATIONS
         SPACE 1
*                        IT IS ASSUMED THAT ALL CONTROL BLOCKS
*                        HAVE ALREADY BEEN SET UP
         SPACE 1
OTA40200 EQU   *                        I/O OPERATION ROUTINE
         LR    R0,RB                    SET REG 0 FOR TRACE TBL  YM5755
         EXCP  DXIOB                    ISSUE I/O OPERATION
         IECRES WAIT
         TM    DXECB,ECBNOERR           IS THERE AN UNUSUAL CONDITIN
         BCR   1,RB                     NO - RETURN TO CALLER
         TM    IOBSTAT0,CSWUNITX        CK FOR UNIT EXCEPTION
         BCR   1,RB                     YES - IGNORE FOR NOW
         SPACE
         NI    DCBIFLGS,X'FF'-DCBIFPIO  TURN OFF ERROR FLAGS IN
*                                       DCB TO ISSUE REWIND
*                                       AND/OR UNLOAD COMMAND
*                                                                Y02134
         B     OTA40400                 GO COMPLETE MESSAGE      Y02134
         EJECT
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
*        SECURITY PROTECTED VOLUME MUST BE REJECTED              Y02134
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
OTA40300 EQU   *                        ISSUE SEC VOL MESSAGE    Y02134
*                                                                Y02134
         MVC   DXRETMOD,ID6N6N          RETURN LOAD              Y02134
         MVI   DXRETCOD,K4              RETURN OFFSET            Y02134
*                                                                Y02134
         IECRES LOAD,MODID=ID6N4J,BRCODE=K40,BRANCH=QUEUED       Y02134
         EJECT
*                                                                YM7530
*****************************************************************YM7530
*                                                                YM7530
*   DATA SET ON ANSI VOLUME INDICATE LIMITED ACCESSIBILITY. THE  YM7530
*   VOLUME CANNOT BE USED.                                       YM7530
*                                                                YM7530
*****************************************************************YM7530
*                                                                YM7530
OTA40350 EQU   *                        CAN'T USE THIS DATA SET  YM7530
*                                                                YM7530
OABD020  EQU   20                       INTERNAL ABEND CODE      YM7530
         MVI   DXCCW5,OABD020           SET UP ERROR CODE        YM7530
*                                                                YM7530
         LA    R1,2                     SET MSG IDENT            YM7530
*                                                                YM7530
         MVC   DXRETMOD,ID6N6N          RETURN LOAD              YM7530
         MVI   DXRETCOD,K4              SET UP RETURN OFFSET     YM7530
*                                                                YM7530
         IECRES LOAD,MODID=ID6N4J,BRCODE=K8,BRANCH=QUEUED        YM7530
         EJECT
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
*        REJECT VOLUME                                           Y02134
*                                                                Y02134
*****************************************************************Y02134
*                                                                Y02134
OTA40400 EQU   *                        WRITE TO OPERATOR-NO REPL
         XR    R0,R0                    CLEAR REGISTER           Y02134
         IC    R0,DXCCW5                GET ERROR CODE           Y02134
*                                                                Y02134
         MVC   DXCALLID,ID6N6N          SETUP ERROR ID           YM7530
         MVC   DXRETMOD,ID6N6N          RETURN MODULE            Y02134
         MVI   DXRETCOD,K0              RETURN OFFSET            Y02134
*                                                                Y02134
         IECRES LOAD,MODID=ID6N4A,BRCODE=K16,BRANCH=QUEUED       Y02134
         EJECT
*                   CONSTANTS                                         *
AEOF1    DC    C'EOF1'                  TEST FOR EOF1 LABEL      Y02082
ANSEOF1  DC    X'454F4631'              ASCII FOR EOF1           Y02082
ANSHDR1  DC    X'48445231'              ASCII CODE FOR 'HDR1'
         SPACE 1
AHDR1    DC    C'HDR1'
ACCW     DC    X'0700000020000050'      BASIC CCW - REWIND COMMAN
AVOL1    DC    C'VOL1'                  VOLUME LABEL ID
CONONEP  DC    X'0001'                  CONSTANT OF ONE
         SPACE
XCTL6N   XCTLTABL ID=(ID6N6N,6N,ID6N6Q,6Q,ID6N6T,6T,ID6N4A,4A,   Y02134X
               ID6N4J,4J,ID6N5T,5T),BRT=YES,LENGTH=              Y02082
         IECDSECS TIOT,DCB,UCB,MAIN,WTG,EXPAND=YES               Y02080
         IECEQU
         END
