         GBLC  &LIB
&LIB     SETC  'LIB1'                   SET VARIABLE TO LIB1
         TITLE 'IGC0N06C  RESCHEDULE I/O PHASE                         *
                       RESUME I/O  CHECKPOINT-SVC 63  OS/360'
IGC0N06C CSECT
*STATUS CHANGE LEVEL 000
***********************************************************************
*                                                                     *
*                                                                     *
*FUNCTION/OPERATION- IGC0N06C PERFORMS THE FOLLOWING FUNCTIONS:       *
*     1.  LOADS THE VSAM MODULE (IDA0I96C) FOR CHECKPOINTING VSAM     *
*         DATA SETS, THEN WRITES THE RESULTANT VSAM SSCR RECORDS      *
*         TO THE CHECKPOINT DATA SET.                                 *
*                                                                     *
*     2.  CALLS IEFSSREQ MACRO FOR EACH SUBSYSTEM DATA SET THEN       *
*         WRITES EACH SUBSYSTEM SSCR TO THE CHECKPOINT DATA SET.      *
*                                                                     *
*     3.  FORMATS AND WRITES THE END RECORD TO THE CHECKPOINT DATA    *
*         SET FOR THE CURRENT CHECKPOINT ENTRY BEING GENERATED.       *
*                                                                     *
*     4.  RESTORES ANY I/O REQUESTS THAT WERE PURGED IN IGC0506C.     *
*                                                                     *
*ENTRY POINTS-IGC0N06C                                                *
*                                                                     *
*INPUT- WORK AREA ADDRESS IN REGISTER 1                               *
*     TASK CONTROL BLOCK ADDRESS IN WORKAREA FIELD 'CKTCBAD'          *
*                                                                     *
*OUTPUT- WORK AREA ADDRESS IN REGISTER 1                              *
*     VSAM AND SUBSYSTEM SSCRS, AND END RECORD WRITTEN TO CHKPT D/S   *
*     TASK CONTROL BLOCK ADDRESS IN WORKAREA FIELD 'CKTCBAD'          *
*                                                                     *
*EXTERNAL ROUTINES- VSAM MODULE (IDA0I96C)                            *
*                                                                     *
*EXITS-NORMAL- TO THE EXIT ROUTINE OF CHECKPOINT                      *
*                                                                     *
*     -ERRORS- TO THE EXIT ROUTINE OF CHECKPOINT                      *
*          ERRORS DETECTED:                                           *
*            RET.CODE  MSG.CODE  MSG.TYPE                             *
*              0C        023      I/O ERROR WRITING CHKPT DATA SET    *
*              08        027      EOV ERROR WRITING CHKPT DATA SET    *
*              0C        207      UNSUCCESSFUL IEFSSREQ (SUBSYS D/S)  *
*              18        215      UNSUCCESSFUL RESTORE                *
*                                                                     *
*CHANGE ACTIVITY                                                      *
*     OS/VS2 RELEASE 037 ADDS/DELETES/CHANGES                         *
*A566978                                                      @ZA13552*
*                                                                     *
*TABLES/WORKAREAS- CHECKPOINT WORK AREA (CHKWA), TCB, DEB, TIOT,      *
*     DSAB, CVT, DCB, JESCT, SSOB, SSIB, SSCR, CHECKPOINT PARM LIST   *
*                                                                     *
*ATTRIBUTES- READ ONLY, REENTERABLE AND PRIVILEGED                    *
*                                                                     *
***********************************************************************
         EJECT
*  REGISTERS USED                                                     *
*                                                                     *
RPM0     EQU   0                        PARAMETER REGISTER            *
RTCB     EQU   4                        ADDRESS OF TCB           Y02076
RBASE    EQU   2                        BASE REGISTER                 *
RDEB     EQU   3                        ADDRESS OF DEB                *
RIOB     EQU   5                        ADDRESS OF IOB                *
RWRK     EQU   6                        ADDRESS OF WORKAREA           *
RSVI     EQU   7                                                      *
RTPG     EQU   8                                                      *
RWK9     EQU   9                                                      *
RWKA     EQU   10                                                     *
RWKB     EQU   11                       WORK REGISTER                 *
RWKC     EQU   12                                                     *
REG13    EQU   13                       SAVE AREA POINTER             *
RET14    EQU   14                       RETURN REGISTER               *
RPM15    EQU   15                       PARAMETER REGISTER            *
*                                                                     *
*        MASKS                                                        *
*                                                                     *
NTPRGD   EQU   X'FF'                    TEST DEBUSRPG FOR NOT PURGED  *
TCAMID1  EQU   X'C0'                    UNIQUE TCAM DEB CODE     M3408
TCAMID2  EQU   X'CF'                    UNIQUE TCAM DEB CODE     M3408
*                                                                     *
***********************************************************************
         EJECT
         BALR  RBASE,0                  ESTABLISH BASE
         USING *,RBASE                                           Y01909
         B     BEGIN               BRANCH AROUND EYE CATCHER     Y01909
         CNOP  0,8                 ALIGN TO A DBL WORD BOUNDARY  Y01909
         DC    C'IGC0N06C'         CSECT NAME                    Y01909
BEGIN    EQU   *                                                 Y01909
         USING TCB,RTCB
         USING DEBBASIC,RDEB                                     Y02076
         USING CHKWA,RWRK
         USING CHKIOWA,RWKB
         USING IHADCB,RWKC                                       Y02076
         LR    RWRK,R1                  SET CHKPT WA BASE REG    Y02076
         LR    RWKB,RWRK                CALC ADDR OF XCTL WA     Y02076
         A     RWKB,CKWAOFST            ADD OFFSET               Y02076
         MVC   LIST(E8),XADDR           NEXT MODULE FOR XCTL     Y02076
         ST    RWKB,CKERAS04            SAVE XCTL WA ADDRESS     Y02076
         L     RWKC,CKDCBAD             GET CHKPT DCB ADDR       Y02076
         LA    R1,RESYNAD               GET ADR OF SYNAD RTN     Y02076
         STCM  R1,ADDRBYTS,DCBSYNAD+E1  ST IN CHKPT DS DCB       Y02076
         LA    REG13,CKREGSAV           INIT REG SAVEAREA ADDR   YM6584
         DROP  RWKC                                              Y02076
         NI    CKFLAG2,VSAMOFF          TURN OFF VSAM LOAD FLAG  Y02076
         L     RTCB,CKTCBAD             GET ADR OF TCB           Y02076
***********************************************************************
*     PREPARE TO WRITE OUT VSAM SSCR RECORDS ON CHECKPOINT DATA SET   *
***********************************************************************
         MVC   CKLNGTH1(E2),BLOCKLNG+E1 SET WRITE RECORD TO 4K @Z30SSDT
         XC    CKSSCR(E4),CKSSCR        CLEAR SSCR ANCHOR FIELD  Y02076
         MVC   VSAMLOAD(E8),VSAMODUL    MOVE VSAM MODULE NAME IN Y02076
         LOAD  EPLOC=VSAMLOAD           LOAD VSAM MODULE         Y02076
         ST    R0,VSAMLOAD+E8           SETUP FOR VSAM MODULE    Y02076
         MVI   VSAMLOAD+E8,E0           CLEAR HIGH ORDER BYTE  @OZ03934
         OI    CKFLAG2,VSAMON           TURN ON VSAM LOAD FLAG   Y02076
         L     RPM15,VSAMLOAD+E8        GET ADDR OF VSAM ROUTINE Y02076
         LR    R1,RWRK                  SET REG 1=W. A. ADDR     Y02076
         BALR  RET14,RPM15              GO TO VSAM ROUTINE       Y02076
         LTR   RPM15,RPM15              TEST FOR ERROR RETURN  @Z30SSDT
         BNZ   RESIOB                   BR IF ERROR FROM VSAM  @Z30SSDT
         OC    CKSSCR(E4),CKSSCR        ANY VSAM SSCRS         @Z30SSDT
         BZ    RESIO500                 NO-PROCESS SS & VAM DATA Y02076
         L     RWKC,CKSSCR              GET ADR OF 1ST SSCR      Y02076
         USING SSCR,RWKC                                         Y02076
RESIOA   ST    RWKC,CMBUF               STORE ADR OF SSCR      @Z30SSDT
         BAL   R7,C13B2A                GO WRITE OUT SSCR        Y02076
         L     RWKC,SSCRFCHN            GET ADR OF NEXT SSCR     Y02076
         LTR   RWKC,RWKC                ANY MORE SSCRS           Y02076
         BNZ   RESIOA                   BRANCH IF YES          @Z30SSDT
***********************************************************************
*   FREEMAIN SSCRS AND DELETE VSAM MODULE                        Y02076
***********************************************************************
RESIOB   ICM   RPM0,ADDRBYTS,BLOCKLNG   SET UP 0 FOR FREEMAIN  @Z30SSDT
         ICM   RPM0,HIORDER,SP253                                Y02076
         L     RWKC,CKSSCR              GET FIRST SSCR BLOCK     Y02076
RESIOC   LTR   RWKC,RWKC                TEST IF THERE IS ONE     Y02076
         BZ    RESIOD                   BRANCH IF ALL DONE     @Z30SSDT
         L     R1,SSCRFCHN              GET ADDR OF NEXT SSCR    Y02076
         ST    R1,CKSSCR                STORE AT TOP OF CHAIN    Y02076
         FREEMAIN  R,LV=(RPM0),A=(RWKC) FREE UP THE BLOCK        Y02076
         L     RWKC,CKSSCR              GET NEXT SSCR BLOCK      Y02076
         B     RESIOC                   GO TO FREE UP NEXT BLOCK Y02076
RESIOD   CLI   CKRETCOD+E1,CKPSBERR     PROB ERROR BEEN SET    @Z30SSDT
         BE    RESIO500                 CONTINUE IF YES        @Z30SSDT
         CLI   CKRETCOD+E1,E0           ANY OTHER ERRORS       @Z30SSDT
         BE    RESIO500                 CONTINUE IF NOT        @Z30SSDT
         B     RESUMEIO                 BR TO RESTORE IF ERROR @Z30SSDT
***********************************************************************
*    OBTAIN CHAIN OF DEBS FROM TCB FOR RESTORING I/O THAT WAS PURGED  *
***********************************************************************
RESUMEIO EQU   *                        PREPARE TO RESTORE I/O   Y02076
         L     RWKB,CKERAS04            GET PTR TO XCTL WA       Y02076
         L     RDEB,TCBDEB              GET ADDR OF FIRST DEB FROM TCB
RESIO010 LTR   RDEB,RDEB                TEST FOR END OF DEB CHAIN
         BZ    RESIO030                 IF DONE GO TO EXIT       Y02076
         LR    RWKC,RDEB                GET ADDR OF DEB BASIC    Y02076
         LA    RWKA,DEBPRFLN            GET DEB PREFIX LENGTH    Y02076
         SR    RWKC,RWKA                CALC. DEB PREFIX ADR     Y02076
         USING DEBPREFX,RWKC                                     Y02076
         CLI   DEBAMTYP,VSAMIDCD        IS IT VSAM               Y02076
         BE    RESIO020                 Y-BYPASS RESTORE         Y02076
         L     RWKA,DEBXTNP             GET DEB EXT. ADR         Y02076
         DROP  RWKC                                              Y02076
         USING DEBXTN,RWKA                                       Y02076
         L     RWKA,DEBXDSAB-DEBXTN(,RWKA)  GET ADR OF DSAB      Y02076
         USING DSAB,RWKA                                         Y02076
         L     RWKA,DSABTIOT-DSAB(,RWKA)  GET ADR OF DD ENTRY    Y02076
         USING TIOENTRY,RWKA                                     Y02076
         TM    TIOELINK,TIOESSDS        IS IT A SS DATA SET      Y02076
         BO    RESIO020                 IF YES - BYPASS RESTORE  Y02076
         DROP  RWKA                                              Y02076
         CLI   DEBEXSCL,DUMMYDEB        IS IT A DUMMY DEB        Y02076
         BNE   RESIO015                 BRANCH IF NO             Y02076
         TM    DEBFLGS1,DEBCINDI        TEST FOR CI BIT SET      Y02076
         BO    RESIO020                 IF ON - BYPASS RESTORE   Y02076
RESIO015 EQU   *                        ANY TCAM DATA SETS OPEN  Y02076
         CLI   DEBNMSUB,TCAMID1         Q - TCAM DEB (TYPE 1)    M3408
         BE    RESIO020                 IF YES, SKIP THIS DEB    M3408
         CLI   DEBNMSUB,TCAMID2         Q - TCAM DEB (TYPE 2)    M3408
         BE    RESIO020                 IF YES, SKIP THIS DEB    M3408
*                                                                     *
         L     RIOB,DEBUSRPG            ADDR OF 1ST IOB IN PURGE CHAIN
         LA    RIOB,0(0,RIOB)           CLEAR HIGH ORDER BITS
         LTR   RIOB,RIOB                TEST IF PURGE ISSUED AT ALL
         BZ    RESIO020                 IF NO PURGE GO GET NEXT DEB
*                                                                     *
*                                                                     *
*  SET UP FOR RESTORE                                                 *
*                                                                     *
*                                                                     *
RESIO017 EQU *                          PREPARE FOR RESTORE      Y02076
         CLC   DEBUSRPG+E1(E3),NOIOPRGD WERE I/O REQUESTS PURGED Y02076
         BE    RESIO020                 GET NEXT DEB IF NO PURGE Y02076
         LA    R1,DEBUSRPG              R1 PTS TO PIRL POINTER   Y02076
         L     RWKC,E0(R1)              GET ADDR OF PIRL OPT BYT Y02076
         MVI   E0(RWKC),RESPOPTB        SET PIRL OPT BYT FOR RES Y02076
         RESTORE DEBUSRPG               RESTORE PURGED I/O       Y02076
         LA    RWKA,RESRETCD            GET BAD RESTORE CODE     Y02076
         CR    RWKA,RPM15               WAS RESTORE UNSUCCESFUL  Y02076
         BNE   RESIO020                 BR IF SUCESS FOR NXT DEB Y02076
         CLI   CKRETCOD+E1,E0           ERRORS DETECTED BEFORE   Y02076
         BNE   RESIO019                 BRANCH IF YES            Y02076
         MVI   CKMSGCOD+E1,ERR215       SET RESTORE ERROR CODE   Y02076
         MVI   CKMSGCOD,E2              SET ERROR MSG SHELL CODE Y02076
         MVI   CKRETCOD+E1,RESTERCD     SET RESTORE ERR(18) CODE Y02076
         B     RESIO030                 BRANCH TO EXIT           Y02076
RESIO019 MVI   CKRETCOD,RESTERCD        REST ERR CODE TO 1ST BYT Y02076
         B     RESIO030                 BRANCH TO EXIT           Y02076
***                                                                 ***
*                                                                     *
*  GET NEXT DEB FROM CHAIN                                            *
*                                                                     *
RESIO020 L     RDEB,DEBDEBAD            GET ADDRESS OF PREVIOUS DEB
         LA    RDEB,0(0,RDEB)           CLEAR HIGH ORDER BITS
         B     RESIO010                 GO GET NEXT DEB
*                                                                     *
***                                                                 ***
*                                                                     *
RESIO030 EQU   *
         TM    CKFLAG2,VSAMON           WAS VSAM MODULE LOADED   Y02076
         BZ    RESIO035                 BRANCH IF NO             Y02076
         DELETE  EPLOC=VSAMLOAD         DELETE VSAM MODULE       Y02076
         NI    CKFLAG2,VSAMOFF          TURN OFF VSAM LOAD FLAG  Y02076
RESIO035 EQU   *                        SET UP FOR EXIT          Y02076
         LR    R1,RWRK                  RELOAD ADDR OF PASSED WA Y02076
         L     RWKB,CKERAS04      GET PTR TO XCTL PARM LIST      YM7306
         LA    RPM15,LIST               GET ADR OF XCTL P.L.     Y02076
         ST    RPM15,LIST+8             STORE IN XCTL LIST       Y02076
         LA    RPM15,LIST+8             TELL XCTL WHERE TO GO
*                                                                     *
         XCTL  EPLOC=(RWKB),SF=(E,(15)) TO CHECKPOINT ROUTINE
*                                                                     *
***********************************************************************
*  PREPARE TO WRITE SUBSYSTEM DATA SET SSCRS TO CHECKPOINT DATA SET   *
***********************************************************************
RESIO500 EQU   *                        ACCESS DEB CHAIN         Y02076
         L     RDEB,TCBDEB              GET ADDR OF 1ST DEB      Y02076
RESIO502 EQU   *                        PROCESS DEB              Y02076
         LTR   RDEB,RDEB                END OF DEB CHAIN         Y02076
         BZ    ENDRECA                  BRANCH IF YES            Y02076
         LR    RWKC,RDEB                GET ADDR OF DEB BASIC    Y02076
         LA    RWKA,DEBPRFLN            GET LENGTH OF DEB PREFIX Y02076
         SR    RWKC,RWKA                CALC. ADDR OF DEB PREFIX Y02076
         USING DEBPREFX,RWKC                                     Y02076
         L     RWKA,DEBXTNP             GET DEB EXT POINTER      Y02076
         USING DEBXTN,RWKA                                       Y02076
         L     RWKA,DEBXDSAB-DEBXTN(,RWKA)  GET ADR OF DSAB      Y02076
         USING DSAB,RWKA                                         Y02076
         L     RWKA,DSABTIOT-DSAB(,RWKA)   GET ADR OF DD ENTRY   Y02076
         USING TIOENTRY,RWKA                                     Y02076
         TM    TIOELINK,TIOESSDS        IS IT A SS DATA SET      Y02076
         BO    RESIO510                 BRANCH IF A SS DATA SET  Y02076
RESIO505 EQU   *                        BEGIN DEB PROCESSING     Y02076
         L     RDEB,DEBDEBAD            GET NEXT DEB ADR         Y02076
         LA    RDEB,0(RDEB)             CLEAR HI ORDER BYTE      Y02076
         B     RESIO502                 BRANCH IF MORE DEBS      Y02076
RESIO510 EQU   *                        CHKPT SUBSYS DATA SETS   Y02076
**********************************************************************
*   INITIALIZE FOR THE IEFSSREQ MACRO TO CHECKPOINT SUBSYSTEM DATA   *
*         SETS                                                   Y02076
***********************************************************************
         LR    RWK9,RDEB                GET ADR DEBBASIC         Y02076
         LA    RWK9,DEBBASND            END OF DEBBASIC SECT     Y02076
         LA    RWK9,E4(RWK9)            ADDR OF SSIB             Y02076
         USING SSIB,RWK9                SSIB ADDRESSABILITY      Y02076
         AH    RWK9,SSIBLEN             ADDR OF SSOB             Y02076
         ST    RWK9,SSREQPRM            SAVE IN PARM WORD        Y02076
         MVI   SSREQPRM,HIORDER2        SET HIORDER BIT-PARM WD  Y02076
         USING SSOB,RWK9                                         Y02076
         MVI   SSOBFUNC+E1,SSOBCKPT     SET CHKPT FUNCTION       Y02076
         ICM   RPM0,ADDRBYTS,BLOCKLNG   SET UP REG 0 FOR GETMAIN Y02076
         ICM   RPM0,HIORDER,SP253       SET SUBPOOL NO.          Y02076
         GETMAIN  R,LV=(RPM0)           GET 4K BUFR FOR IEFSSREQ Y02076
         ST    R1,CMBUF                 BLOCK ADR TO WRITE SSCR  Y02076
         LR    RWKB,R1                  ADR OF SSCR IN RWKB      Y02076
         USING SSCR,RWKB                                         Y02076
         LA    R0,SSCRDATA              GET DATA ADR SUB SYS SSCRVS4685
         ST    R0,SSDABUFR              STORE IT IN SS0B         VS4685
         MVI   SSCRID,SSCRIDCK          SET CHKPT ID IN SSCR     Y02076
         MVI   SSCRSSID,SSCRSS          SET SUBSYS ID IN SSCR    Y02076
         OI    SSCRFLG1,SSCRCKRS        SET CHECKPOINT BIT       Y02076
         XC    SSCRFCHN,SSCRFCHN        CLEAR CHAIN FIELD        Y02076
         XC    SSCRDATL,SSCRDATL        CLEAR DATA LENGTH FIELD  Y02076
         XC    SSCRDCBA,SSCRDCBA        CLEAR DCB ADDR FIELD     Y02076
         LA    R1,SSREQPRM              IEFSSREQ PRM WRD ADR-R1  Y02076
         IEFSSREQ                       GET SUBSYSTEM CHKPT DATA Y02076
         LTR   RPM15,RPM15              TEST RETURN CODE         Y02076
         BNZ   RESIO520                 BR TO SET ERR CODE       Y02076
         OC    SSOBRETN,SSOBRETN        TEST SSOB RETURN CODE    Y02076
         BZ    RESIO525                 BRANCH IF SUCCESSFUL     Y02076
RESIO520 MVI   CKMSGCOD+E1,ERRMSGC3     SET ERR CODE =207        Y02076
         MVI   CKRETCOD+E1,CKIOERR      SET SUBSYSTEM FAILURE  @ZA13552
         MVI   CKMSGCOD,E3              SET INVLD MSG SHELL CODE Y02076
         B     RESUMEIO                 BRANCH TO ERROR EXIT     Y02076
RESIO525 EQU   *                        CALL WRITE ROUTINE       YM5721
         MVC   SSCRDCBA,DEBDCBAD        MOVE DCBADDR IN SSCR     Y02076
         BAL   R7,C13B2A                BR TO WRITE OUT SSCR     Y02076
         L     R1,CMBUF                 GET ADR OF 4K BUFFER     VS4685
         ICM   R0,ADDRBYTS,BLOCKLNG     SET UP R0 FOR FREEMAIN   Y02076
         ICM   R0,HIORDER,SP253         SET SUBPOOL 253          Y02076
         FREEMAIN  R,LV=(R0),A=(R1)     FREEUP THE 4K BLOCK      Y02076
         B     RESIO505                 GO GET NEXT DEB          Y02076
***********************************************************************
*       WRITE ROUTINE FOR THE CHECKPOINT DATA SET                     *
***********************************************************************
C13B2A   EQU   *                        WRITE ROUTINE            Y02076
         L     RA,CMBUF                 INIT START OF BUFFER     Y02076
         TM    CKFLAG1,CKVEQR           IS THIS TASK V=R?        Y02076
CKVEQR   EQU   X'08'                    V=R FLAG IN W.A.         YM7393
         BNO   CKNOVEQR                 NO, GO ON                Y02076
         LRA   RA,E0(,RA)               GET REAL ADDR OF BUFFER  Y02076
CKNOVEQR EQU   *                        ISSUE WRITE              Y02076
         WRITE CKDECB1,SF,,(E10),MF=E                            Y02076
         CHECK CKDECB1                  ISSUE CHECK              Y02076
         TM    CKFLAG1,CKTAPE           CHKPT DATA SET ON TAPE   Y02076
         BZ    CMEOVDA                  BRANCH IF NO             Y02076
         L     RF,CKTSTEOV                                       Y02076
         CLC   E0(E4,RF),CKEOVCMR       DID EOV OCCUR            Y02076
         BNE   CMEOV                    BRANCH IF YES            Y02076
CMTIOTST EQU   *                        DID I/O ERROR OCCUR      Y02076
         CLI   CKRETCOD+E1,CKIOERR      I/O ERROR                Y02076
         BE    CMTIOTSA                 BRANCH TO SET CODE       Y02076
         BR    R7                       RETURN                   Y02076
CMTIOTSA LA    RF,CKERIO3               I/O ERROR MSG CODE       Y02076
         STH   RF,CKMSGCOD              STORE IN WORK AREA       Y02076
         B     RESUMEIO                 BRANCH TO EXIT           Y02076
         EJECT                                                   Y02076
*                                                                Y02076
*        END OF VOLUME TESTING                                   Y02076
*                                                                Y02076
CMEOV    EQU   *                        END OF VOLUME TESTS      Y02076
         TM    CKFLAG2,CKEOV            2ND EOV FOR THIS REQUEST Y02076
         BO    CMEOVER                  YES- GO TO EXIT RTN      Y02076
         OI    CKFLAG2,CKEOV            TURN ON EOV INDICATOR    Y02076
         L     RWKB,CKERAS04            GET PTR TO XCTL WA       Y02076
         USING CHKIOWA,RWKB                                      Y02076
         MVI   LIST+E4,F2               SET TO XCTL TO 206C      Y02076
         B     RESIO030                 XCTL INTERFACE           Y02076
CMEOVDA  EQU   *                        TEST EOV ON DA DEVICE    Y02076
         USING IHADCB,RF                DCB                      Y02076
         L     RF,CKDCBAD               GET ADR. OF CHKPT DCB    Y02076
         L     RF,DCBDEBAD              GET ADR. OF CHKPT DEB    Y02076
         DROP  RF                       DCB                      Y02076
         USING DEBBASIC,RF              DEB                      Y02076
         CLC   DEBNMEXT(E1),CKTSTEOV                             Y02076
*                                       DID EOV OCCUR ON DA DEV. Y02076
         BE    CMTIOTST                 NO- CONTINUE PROCESSING  Y02076
         DROP  RF                       DEB                      Y02076
CMEOVER  EQU   *                        SET EOV ERROR            Y02076
         LA    RF,CKERIO7               INDICATE EOV ERROR       Y02076
         STH   RF,CKMSGCOD              PUT IN W.A.              Y02076
         MVI   CKRETCOD+E1,E8           PUT IN RETURN CODE ERR   Y02076
         B     RESIO030                 XCTL INTERFACE           Y02076
RESYNAD  MVI   CKRETCOD+E1,CKIOERR      RTN TO SET I/O ERROR     Y02076
         BR    RE                       RETURN                   Y02076
***********************************************************************
*     THIS ROUTINE WRITES OUT THE CHECKPOINT END RECORD          Y02076
***********************************************************************
ENDRECA  EQU   *                        WRITE END RECORD         Y02076
         L     RWKB,CKBFOFST            GET ADR  END RECORD BUFF Y02076
         AR    RWKB,RWRK                GET ABS ADR OF BUFFER    Y02076
         XC    E0(E20,RWKB),E0(RWKB)    CLEAR END RECORD BUFFER  Y02076
         MVC   E0(E2,RWKB),ENDRECID     MOVE FFFF INTO ID BYTE   Y02076
         MVI   E4(RWKB),SPACE           BLANK OUT CHECKID        Y02076
         MVC   E5(E15,RWKB),E4(RWKB)       FIELD OF END RECORD   Y02076
         TM    CKFLAG2,CKUSPCHD         DID USER SUPPLY CHECKID  Y02076
         BZ    ENDRECB                  BR IF NO TO MOVE GEN ID  Y02076
         L     RWKC,CKPARMAD            GET ADR OF CHKPT PARMLST Y02076
         USING PARMSECT,RWKC                                     Y02076
         L     RWK9,IDADDR              GET LNGTH & ADR OF ID    Y02076
         LR    RTPG,RWK9                LNGTH & ID ADR IN REG8   Y02076
         SRL   RTPG,E24                 SHIFT OUT ADDR           Y02076
         BCTR  RTPG,E0                  REDUCE LNGTH BY 1 FOR EX Y02076
         LA    RWK9,E0(RWK9)            CLEAR HI ORDER BYTE ADR  Y02076
         EX    RTPG,IDMOVE              EXECUTE MOVE INSTRUCTION Y02076
         B     ENDRECC                  BR TO WRITE              Y02076
ENDRECB  EQU   *                        MOVE IN GEN. CHECKID     Y02076
         LA    RWK9,CKCHEKID            GET ADR - GEN. CHECKID   Y02076
         MVC   E4(E8,RWKB),E0(RWK9)     MOVE CHECKID TO RECORD   Y02076
ENDRECC  EQU   *                        PREPARE FOR WRITE        Y02076
         ST    RWKB,CMBUF               STORE END REC BUFF ADR   Y02076
         BAL   R7,C13B2A                WRITE END OF CKPT RECORD Y02076
         B     RESUMEIO                 GO TO RESTORE PURG'D IO ZA00617
***                                                                 ***
*                                                                     *
XADDR    DC    CL8'IGC0Q06C'            CHECKPOINT EXIT ROUTINE
IDMOVE   MVC   E4(E1,RWKB),E0(RWK9)     MVC INSTR FOR EXECUTE    Y02076
NOIOPRGD DC    XL3'FFFFFF'              'NO I/O REQSTS PRGD' IND Y02076
ENDRECID DC    XL2'FFFF'                ID FOR END OF CHKPT REC  Y02076
*                                                                     *
*                                                                     *
***********************************************************************
*                                                                     *
*        MAINTENANCE SPACE                                       Y01909
*                                                                     *
PATCH    DC    XL((*-IGC0N06C)/20)'00'   FIVE PERCENT SPACE      Y01909
*                                                                     *
***********************************************************************
DEBPRFLN EQU   16                       LNGTH OF DEB PREFIX      Y02076
VSAMIDCD EQU   X'01'                    VSAM ID CODE             Y02076
DUMMYDEB EQU   X'00'                    DUMMY DEB CODE(DEBEXSCL) Y02076
ERRMSGC3 EQU   207                      SS INTERFACE ERR CODE    Y02076
HIORDER2 EQU   X'80'                    HI ORDER BYTE BIT        Y02076
HIORDER  EQU   8                        HI ORDER BYTE MASK       Y02076
ADDRBYTS EQU   7                        ADDRESS BYTES MASK       Y02076
E0       EQU   0                        CONSTANT 0               Y02076
E1       EQU   1                        CONSTANT 1               Y02076
E2       EQU   2                        CONSTANT 2               Y02076
E3       EQU   3                        CONSTANT 3               Y02076
E4       EQU   4                        CONSTANT 4               Y02076
E5       EQU   5                        CONSTANT 5               Y02076
E6       EQU   6                        CONSTANT 6               Y02076
E7       EQU   7                        CONSTANT 7               Y02076
E8       EQU   8                        CONSTANT 8               Y02076
E9       EQU   9                        CONSTANT 9               Y02076
E10      EQU   10                       CONSTANT 10              Y02076
E15      EQU   15                       CONSTANT 15              Y02076
E20      EQU   20                       CONSTANT 20              Y02076
E24      EQU   24                       CONSTANT 24              Y02076
SPACE    EQU   X'40'                    SPACE                    Y02076
ENDRECLN EQU   20                       LNGTH OF CHKPT END REC.  Y02076
RESPOPTB EQU   X'C0'                    PIRL OPT BYT FOR RESTORE Y02076
VSAMOFF  EQU   X'BF'                    TURN OFF VSAM LOAD MASK  Y02076
VSAMON   EQU   X'40'                    TURN ON VSAM LOAD MASK   Y02076
RESTERCD EQU   X'18'                    RESTORE ERR RETURN CODE  Y02076
RESRETCD EQU   4                        BAD RESTORE RETURN CODE  Y02076
ERR215   EQU   215                      ERR CODE FOR BAD RESTORE Y02076
C2       EQU   C'2'                     CHAR 2                   Y02076
F2       EQU   X'F2'                    FOR XCTL TO IGC0206C     Y02076
CKERIO3  EQU   X'0317'                  CHKPT D/S WRITE ERR      Y02076
CKERIO7  EQU   X'031B'                  EOV ERR MSG CODE         Y02076
VSAMODUL DC    C'IDA0I96C'              VSAM MODULE NAME       @Z30SSDT
BLOCKLNG DC    AL3(4096)                SSCR BLOCK LENGTH        Y02076
SP253    DC    AL1(253)                 SUBPOOL NO.              Y02076
SSCR     DSECT                                                   Y02076
SSCRID   DS    C                        SSCR RECORD ID           Y02076
SSCRIDCK EQU   X'01'                    CHECKPOINT ID            Y02076
SSCRSSID DS    C                        SUBSYSTEM ID             Y02076
SSCRSS   EQU   X'00'                    SUBSYSTEM                Y02076
SSCRVSAM EQU   X'01'                    VSAM                     Y02076
SSCRSPIE EQU   X'02'                    SPIE                     Y02076
SSCRHDRL DS    H                        HDR LNGTH(OFSET TO DATA) Y02076
SSCRFCHN DS    F                        CHAIN PTR TO XTRA SSCRS  Y02076
SSCRFLG1 DS    C                        FLAG BYTE                Y02076
SSCRCKRS EQU   X'80'                    CHECKPOINT TIME          Y02076
SSCRDATL DS    H                        DATA LENGTH              Y02076
SSCRDCBA DS    F                        ADDR OF ASSOC. DCB       Y02076
SSCRDATA DS    F                        1ST WORD DATA AREA       Y02076
PARMSECT DSECT                                                   Y02076
PARMFLG  DS    1C                       FLAG                     Y02076
PARMDCB  DS    3C                       DCB ADDR                 Y02076
IDADDR   DS    0F                       CHECKID ADDRESS          Y02076
LNCHKID  DS    1C                       CHECKID LENGTH           Y02076
         DS    3C                       ADDR OF CHECKID          Y02076
         IEFJSSOB   (DA),CONTIG=YES                              Y02076
         IEFJSSIB                                                Y02076
         IEZDEB                                                  Y02076
         IEFTIOT1                                                Y02076
         CVT   DSECT=YES,PREFIX=YES,LIST=YES                     Y02076
         IEFJESCT                                                Y02076
         DCBD  DSORG=(BS,PO),DEVD=(DA,TA)                        Y02076
         IHADSAB                                                 Y02076
         EJECT
TCB      DSECT      TASK CONTROL BLOCK
*
TCBRBP   DS    A                       POINTER TO EXECUTING RB
TCBPIE   DS    A                       POINTER TO PROG. INTERRUPT EL.
TCBDEB   DS    A                       POINTER TO DEB QUEUE
TCBTIO   DS    A                       POINTER TO TIOT
TCBCMP   DS    F                       TASK COMPLETION CODE
TCBTRN   DS    A                       TESTRAN FIELD
TCBMSS   DS    A                       DATA BLOCK QUEUE ELEMENT POINTER
TCBPRTK  DS    BL1                     PROTECTION TAG
TCBFLGS  DS    BL5                     FLAG BYTES
TCBLMP   DS    BL1                     LIMIT PRIORITY
TCBDSP   DS    BL1                     DISPATCHING PRIORITY
TCBLLS   DS    A                       POINTER TO LOAD LIST
TCBJLB   DS    A                       POINTER TO JOBLIB DCB
TCBJSE   DS    A                       LIST OF INACTIVE PROGRAMS ADDR.
TCBGRS   DS    16F                     GENERAL REGISTER SAVE AREA
TCBIDF   DS    0BL1                    TCB IDENTIFIER FIELD
TCBFSA   DS    A                       POINTER TO 1ST PROB.PROG.SAVEAR
TCBTCB   DS    A                       NEXT TCB ON READY QUEUE POINTER
TCBTME   DS    A                       POINTER TO TIMER ELEMENT
*
*
*
*
CHKIOWA  DSECT      CHECKIO'S WORK AREA
*
LIST     DS    3F   AREA WHERE ENTRY POINT NAME IS STORED
         EJECT
         IEEVCHWA VER=3                 USE AOS2 VERSION OF WA
*
**********************************************************************
SSREQPRM EQU   CKERAS05                 CONTAINS SSOB ADDR       Y02076
CMBUF    EQU   CKERAS06                 OUTPUT BUFFER ADDR       Y02076
VSAMLOAD EQU   CKERAS00                 VSAM MODULE NAME         Y02076
CKTSTEOV EQU   CKUNIT                   END OF VOLUME TEST       Y02076
CKEOVCMR EQU   CKUNIT+4                 END OF VOLUME TEST       Y02076
         END   IGC0N06C
