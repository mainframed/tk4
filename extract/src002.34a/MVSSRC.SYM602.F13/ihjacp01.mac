*
         COPY  LCGASMSW
*
         TITLE 'IGC0106C (IHJACP01) CHKPT HOUSEKEEPING RTN - 2ND LOAD'
***********************************************************************
*TITLE:  CHKPT HOUSEKEEPING - SECOND LOAD
*
*STATUS:  CHANGE LEVEL 000
*
*FUNCTION/OPERATION:  IGC0106C WILL DO THE FOLLOWING:
*   1.)  CHECK FOR ENVIRONMENT ERROR CONDITIONS. THESE ERROR CONDITIONS
*   AND THEIR ASSOCIATED ERROR CODES ARE LISTED BELOW IN THE OUTPUT
*   SECTION.
*
*   THE BLKSIZE SPECIFIED IN THE DCB WILL BE SAVED IN THE CKDCBSI
*   FIELD OF THE CHECKPOINT WORK AREA, AND A STANDARD BLOCKSIZE OF
*   TWO K (2048) WILL BE SUBSTITUTED DURING CHECKPOINT AND RESTART
*   PROCESSING.  THE BLOCKSIZE WILL BE RESTORED TO ITS ORIGINAL VALUE
*   BEFORE CHECKPOINT PROCESSING IS COMPLETED.  ADDITIONALLY, THE
*   BLOCKSIZE SPECIFIED IN THE DCB WILL BE INSERTED INTO A SUR RECORD
*   FOR THE PURPOSE OF RESTORING THE ORIGINAL BLKSIZE AT RESTART TIME.
*
*   IF OUTSTANDING ENQUEUE REQUESTS ARE FOUND, A RETURN CODE OF X'10'
*   IS PUT IN CHKWA TO INDICATE A POSSIBLE ERROR AND THEN CONTINUE
*   NORMAL PROCESSING. IF ANY OF THE ABOVE ERRORS ARE FOUND, A RETURN
*   CODE OF X'08' IS PLACED IN CHKWA AND XCTL TO  IGC0Q06C.
*
*   2.) IF THE USER SUPPLIED A CHECKID THIS ROUTINE WILL CHECK FOR
*   VALID LENGTH AND CHARACTERS. IF EITHER ARE INVALID A RETURN CODE
*   OF X'08' IS PLACED IN CHKWA AND XCTL TO IGC0Q06C.
*
*ENTRY POINTS   IGC0106C
*
*INPUT:  ADDRESS OF CHKPT WORK AREA IN REGISTER 1.
*
*OUTPUT:
*    1.) FOR NORMAL PROCESSING THE ADDRESS OF CHKPT WORK AREA IS IN
*        REGISTER 1.
*    2.) A RETURN CODE OF X'10' IS GIVEN WHEN THERE ARE OUTSTANDING
*        ENQUEUES AND A MESSAGE CODE OF X'0100' IS PLACED IN THE
*        CHECKPOINT WORK AREA.  A RETURN CODE OF X'08' IS GIVEN FOR
*        ANY OTHER ERROR CONDITIONS THAT OCCUR
*   WHEN AN ERROR OCCURS ONE OF THE FOLLOWING MESSAGE CODES IS PLACED
*   INTO CHKWA TO BE PRINTED IN THE CONSOLE MESSAGE BY CHKPT EXIT ROU-
*   TINE:
*        DEC VALUE  MNEMONIC               MEANING
*           5       CKERPG5         FOR D.A., KEYLENGTH SPECIFIED NOT
*                                   EQUAL TO ZERO
*           6       CKERPG6        ERROR IN DCBRECFM
*           7       CKERPG7        ERROR IN DCBDSORG
*           8       CKERPG8        ACTIVE TQE
*           9       CKERPG9        ERROR IN RBSTAB
*          10       CKPERG10      GRAPHICS DS ORG ERR CODE       Y02076
*         206       CKPER206     VTAM DATA SET ORG ERR CODE      Y02076
*          14       CKERPG14       OUTSTANDING WTOR
*          15       CKERPG15       INCORRECT CHECKID LENGTH/FORMAT
*          16       CKERPG16       CHKPT D.S. DEVICE TYPE ERROR
*         224       CKPER224      SAM-SI(NON-CI) DS ORG          Y02076
*          19       CKERPG19       CHKPT D.S. NOT OPENED TO WRITE
*          21       CKERPG21       USASI LABEL TYPE NOT ALLOWED   19200
*          29       CKERPG21       ANSI TRANSLATION ON TAPE NOT ALLOWED
*          32       CKPERG32       ISAM DATA SET OPEN DISP =SHR XA01816
*         250       CKPER250       IMAGE LIB DCB                 YM7813
*
*EXTERNAL ROUTINES:  NONE
*
*SVC'S USED:  XCTL
*
*EXITS-NORMAL:  XCTL TO IGC0206C
*
*EXITS-ERROR:  CONTROL WILL TRANSFER TO IGC0Q06C IF ANY OF THE ERROR
*   CONDITIONS THAT ARE LISTED UNDER OUTPUT ARE FOUND.
*
*TABLES/WORKAREAS CREATED:  CHKTABLE-CONTAINS ALL VALID CHARACTERS FOR
*   CHECKID.
*
*TABLES/WORKAREAS REFERENCED  CHKWA, DCB, TCB, TIOT, TQE, RBS, CHKTABLE
*   CVT, UCM, ORE, DEB
*
*ATTRIBUTES:  REENTRANT, SUPERVISOR STATE, TYPE 4 SVC ROUTINE.
*
*RESTRICTIONS:  OS/VS2/2 ONLY
*
*NOTES:  NONE
*
*VS2/3 CHANGES/DELETIONS
*226694,226844                                                 @ZA00622
*222000,222400,222700                                          @ZA03906
*3350000-33570000                                              @ZA07036
*A22688102,22688202                                            @ZA08009
*
***********************************************************************
         EJECT
IGC0106C CSECT
*
         USING CHKWA,RC            ADDRESS CHKPT W.A.
         USING IHADCB,R2           ADDRESS CHKPT DCB.
         USING TCB,R4              ADDRESS TCB.
         USING RBSECT,R5           ADDRESS RBS.
         BALR  RB,0                BASE ADDR
*                                                                     *
***********************************************************************
         USING *,RB                ADDRESS MODULE.
         B     BEGIN               BRANCH AROUND EYE CATCHER
         CNOP  0,8                 ALIGN TO A DOUBLE WORD BOUNDARY
         DC    C'IGC0106C'         CSECT NAME
BEGIN    EQU   *
         LR    RC,R1               SAVE ADDRESS OF CHKPT W.A.
         L     R2,CKDCBAD          GET ADDR OF CHKPT DCB.
         L     R4,CKTCBAD          GET TCB ADDR
         L     R5,TCBRBP           GET ADDR OF CHKPT SVRB
***********************************************************************
*                                                                     *
*        CHECK DCBDEVT FIELD IN DCB FOR DEVICE TYPE                   *
*         AND DCBDSORG FOR DS ORGANIZATION                       M0968
*                                                                     *
***********************************************************************
*                                                                     *
         L     R6,DCBDEBAD         GET DEB ADDRESS              Y02049
         USING DEBBASIC,R6                                      Y02049
         LR    R7,R6               SAVE DEB ADDR FOR CALC       Y02049
         LA    RD,DEBPRFLN         GET LENGTH OF DEB PREFIX     Y02049
         SR    R7,RD               CALCULATE ADDR OF DEB PREFIX Y02049
         USING DEBPREFX,R7                                      Y02049
         L     RD,DEBXTNP          GET POINTER TO DEB EXTENSION Y02049
         USING DEBXTN,RD                                        Y02049
         LA    RA,CKERPG7               LOAD RETURN CODE FOR ERR M0968
         TM    DEBXDSO1,DCBDSGPS        WAS DS OPENED FOR PS    Y02049
         BO    CHKDEVT                  YES-SKIP TEST FOR PO     M0968
         TM    DEBXDSO1,DCBDSGPO        WAS DS OPENED FOR BPAM  Y02049
         BZ    CHRERX                   NO-IS ERROR              M0968
         OI    CKFLAG1,CKBPAM           SET BPAM INDICATOR       M0968
CHKDEVT  EQU   *                                                 M0968
         L     R7,DEBSUCBA         GET UCB ADDRESS              Y02049
         USING UCBOB,R7                                         Y02049
         TM    UCBTBYT3,UCB3DACC   IS DATA SET ON DIRECT ACCESS Y02049
         BO    CHRDACL             YES-GO TO CHECK KEY LENGTH SPECIFIED
         LA    RA,CKERPG16          LOAD RETURN CODE IF ERROR OCCURS.
         TM    UCBTBYT3,UCB3TAPE   IS DATA SET ON MAGNETIC TAPE Y02049
         BZ    CHRERX              NO-GO TO ERROR EXIT ROUTINE. Y02049
         OI    CKFLAG1,CKTAPE      INDICATE DATA SET ON TPAE.
         DROP  R7                                               Y02049
*                                                                     *
***********************************************************************
*        CHECK DCBRECFM FIELD IN DCB FOR UNDEFINED FORMAT AND TRACK   *
*        OVERFLOW OPTION                                              *
*                                                                     *
***********************************************************************
*                                                                     *
CHRTRFM  EQU   *
         LA    RA,CKERPG6          LOAD RETURN CODE IF ERROR OCCURS.
         TM    DCBRECFM,DCBRECU    IS RECFM UNDEFINED
         BM    CHRERX              NO-GO TO ERROR EXIT RTN
         TM    DCBRECFM,DCBRECTO   IS TRACK OVERFLOW SPECIFIED
         BZ    CHRDSORG            NO-SKIP CODE TO SET CKFLAG1
         OI    CKFLAG1,CKTRKOV     SET TRACK OVERFLOW INDICATOR.
*                                                                     *
***********************************************************************
*                                                                     *
*        SAVE USER BLKSIZE AND SET CKPT BLKSIZE TO TWO K,             *
*        DCBMACRF IN DCB FOR OPENED TO WRITE,                         *
*        AND DCBDSORG IN DCB FOR DATA SET ORGANIZATION                *
*                                                                     *
***********************************************************************
*                                                                     *
CHRDSORG EQU   *
         MVC   CKDCBSI(BLKLENF),DCBBLKSI
*                                  SAVE USER BLKSIZE IN CKPT WORK AREA
         MVC   DCBBLKSI(BLKLENF),H4096 SET BLOCKSIZE TO 4K       YM5921
         MVC   CKBUFLN(CKBUFLN-CKBUFL),DCBBLKSI MOVE BLKSIZE FROM DCB
         LA    RA,CKERPG19         LOAD MESSAGE CODE IF ERROR OCCURS
         TM    DEBXMCF2,DCBMRWRT   WAS DATA OPENED TO 'WRITE'   Y02049
         BZ    CHRERX              NO-GO TO ERROR EXIT ROUTINE.
         TM    DEBOPATB,DEBACCS         OUTPUT PROCESSING        M0969
         BNO   CHRERX                   NO-IS ERROR              M0969
         EJECT
*                                                                 19200
****************************************************************  19200
*                                                                 19200
*        THIS CODE CHECKS LABEL TYPE FIELD ON CHK. PT. DD CARD    19200
*        IF   LABEL=AL OR LABEL=AUL THEN THE USER IS IN ERROR     19200
*        USASI  CODE CAN NOT BE USED IN A CHECKPOINT  DATA SET.   19200
*                                                                 19200
****************************************************************  19200
*                                                                 19200
CHRLABEL EQU   *                       CHECK LABEL TYPE           19200
         L     RF,TCBTIO               GET ADDRESS OF TIOT        19200
         AH    RF,DCBTIOT              COMPUTE CK.PT. DD ADDRESS  19200
         LA    RA,CKERPG21             INDICATE JCL LABEL ERROR   19200
*                                      IN CASE OF ERROR           19200
         USING TIOENTRY,RF             TIOT ENTRY ADDRESSABILITY  19200
         TM    TIOESTTA,TIOSLTYP+TIOSLABL
*                                      TEST FOR USASI LABEL TYPE
         DROP  RF                      DROP ADDRESSABILITY        19200
         BO    CHRERX                  USASI LABEL NOT ALLOWED    19200
*                                                                S20038
**************************************************************** S20038
*                                                                S20038
*        THIS CODE CHECKS THE DCB OPTCD FIELD TO SEE THAT THERE  S20038
*        IS NO ATTEMPT TO TAKE A CHECK POINT ON TAPE AFTER       S20038
*        SPECIFYING OPTCD=Q. OPTCD=Q MEANS ANSI TRANSLATION.     S20038
*                                                                S20038
**************************************************************** S20038
*                                                                S20038
CHROPTCD EQU   *                                                 S20038
         TM    CKFLAG1,CKTAPE          IS THIS TAPE ?            S20038
         BNO   CHRTME                  NO. SKIP OPTION CODE TEST S20038
*
*        TEST FOR TAPE IS NECESSARY DO TO FACT THAT THE SAME BIT S20038
*        IS USED TO MEAN TRACK OVERFLOW FOR DIRECT ACCESS.       S20038
*        TRACK OVERFLOW IS ACCEPTABLE FOR CHECKPOINTS.           S20038
*        ANSI DOES NOT APPLY TO DIRECT ACCESS.                   S20038
*
         LA    RA,CKERPG29             IN CASE OF ERROR          S20038
*                                      INDICATE DCB OPTCD ERROR  S20038
         TM    DCBOPTCD,DCBOPTQ        ANSI ENCODING SPECIFIED ? S20038
         BO    CHRERX                  OPTCD=Q NOT ALLOWED       S20038
*                                                                     *
***********************************************************************
*                                                                     *
*        TEST FOR ACTIVE TQE                                          *
*                                                                     *
***********************************************************************
*
CHRTME   EQU   *
         L     RF,CVTPTR           GET ADDRESS OF CVT
         USING CVTSECT,RF
         L     RF,CVTTPC           GET ADDRESS OF PSEUDO CLOCK
         LTR   RF,RF               IS THERE A TIMER
         BZ    CHRBT               BRANCH IF NO--SKIP ACTIVE TQE TEST
         DROP  RF
         NC    TCBTME1(LNADDR),TCBTME1  IS THERE A TQE
         BZ    CHRBT               NO-SKIP CODE TO PROCESS TQE.
         L     RF,TCBTME           GET ADDR OF TQE.
         LA    RA,CKERPG8           LOAD RETURN CODE IF ERROR OCCURS.
         USING TQE,RF               MAPPING OF TQE
         TM    TQEFLGS,TQEOFF       IS TQE ACTIVE
         DROP  RF
         BZ    CHRERX              YES-GO TO ERROR ROUTINE.
*
***********************************************************************
*                                                                     *
*        TEST FOR INVALID RB ON RB CHAIN                              *
*                                                                     *
***********************************************************************
*
CHRBT    EQU   *
         L     R5,RBLINK           GET ADDR NEXT RB
         LA    RA,CKERPG9
         TM    RBSTAB1,RBFTSVRB    IS THIS AN IRB OR AN SIRB
         BM    CHRERX              YES-GO TO ERROR EXIT ROUTINE.
         BZ    CHRLSRB             BRANCH IF ITS A PRB
         TM    RBSTAB,RBFTCKPT     IS THIS EOV SVRB
         BZ    CHRERX              BRANCH IF NO--ERROR
CHRLSRB  EQU   *
         TM    RBSTAB2,RBTCBNXT    IS THIS THE LAST RB
         BZ    CHRBT          NO - GO TO GET ADDR OF NEXT RB.    A43190
*
**********************************************************************
*
*        TEST FOR ACTIVE SPIE
*
**********************************************************************
*
         L     R3,TCBPIE      GET ADDR OF SPIE CONTR AREA(SCA) @ZA03906
         LA    R3,0(R3)       ZERO HIGH BYTE                     A43190
         LTR   R3,R3          IS THERE A SCA                   @ZA03906
         BZ    CHKENV         NO -- GO CHECK   ENVIRONMENT       A43190
         L     R3,0(R3)                 GET ADDR OF PIE        @ZA03906
         TM    0(R3),PIEFLAG  HAS PIE BEEN ENTERED               A43190
         BO    CHRERX         YES -- GO TO ERROR EXIT            A43190
*
CHKENV   EQU   *                                                 A43190
*                                                             *  A39163
***************************************************************  A39163
*                                                             *  A39163
*    TEST FOR GRAPHICS DCB'S, VTAM DCB'S, SAM-SI(NON CI) DCB'S  XA01816
*     AND ISAM DCB'S OPEN FOR SHR                               XA01816
*                                                             *  A39163
***************************************************************  A39163
*                                                             *  A39163
         L     R6,TCBDEB      GET DEB ADDRESS                    A39163
         USING DEBBASIC,R6    ADDRESS DEB                        A39163
CHKGRAPH EQU   *                                                 A39163
         LA    RA,CKPERG10    SET MSG CODE FOR ERR               A39163
         LA    R6,0(R6)       CLEAR HIGH BYTE                    A39163
         LTR   R6,R6          ANY MORE DEBS                      A39163
         BZ    LSTDEB         NO -- EXIT                         A39163
         LR    R7,R6                    LD R7 WITH DEB ADDR      Y02076
         LA    RD,DEBPRFLN              GET DEB PREFIX LENGTH    Y02076
         SR    R7,RD                    CALC ADDR OF DEB PREFIX  Y02076
         USING DEBPREFX,R7                                       Y02076
         L     RD,DEBXTNP             GET ADDR OF DEB EXTENSION  Y02049
         TM    DEBXDSO2,DCBDSGGS      IS THIS A GRAPHICS D S     Y02049
         BO    CHRERX         YES -- IS ERROR                    A39163
         LA    RA,CKPER206              LOAD VTAM DS ERR CODE    Y02076
         CLI   DEBAMTYP,VTAMDSCD        TEST FOR VTAM            Y02076
         BE    CHRERX                   IF VTAM - BR TO ERROR    Y02076
         L     R2,DEBDCBAD              GET ADDR OF DCB          Y02076
***********************************************************************
*        CHECK FOR IMAGE LIB DCB                                 YM7338
***********************************************************************
*
         CLC   DCBDDNAM(K4),IMGDDNAM  IMAGE LIB DCB              YM7338
         BNE   CHKDEV                 NO                         YM7338
         LA    RA,CKPER250            LOAD ERROR RTN CD          YM7813
         B     CHRERX                 GO TO ERROR  EXIT          YM7338
*                                                                     *
CHKDEV   EQU   *                        INSTR MUST FOLLOW        YM7338
         LA    RA,CKPER224              SAM-SI(NON CI) ERR CODE  Y02076
         CLI   DCBRELAD,ACBIDENT        DOES DEB PT TO AN ACB    Y02076
         BNE   CHKISHR                  IF NO - CHK ISAM SHR    ZA00622
         CLI   DEBAMTYP,VSAMDSCD        IS IT VSAM               Y02076
         BE    CHKNXDEB                 BR IF YES                Y02076
         DROP  R7                                                Y02076
         TM    DEBFLGS1,DEBCIBIT        IS CI BIT ON             Y02076
         BZ    CHRERX                   TO ERR IF CI BIT NOT ON  Y02076
*
*     TEST FOR ISAM DATA SETS OPEN FOR DISP=SHR                 XA01816
*
CHKISHR  EQU   *                        ISAM SHR TESTS FOLLOW   ZA00622
         TM    DCBDSRG1,DCBDSGIS        IS THIS AN ISAM DATA ST XA01816
         BZ    CHKNXDEB                 NO, GET THE NEXT DEB    XA01816
         CLI   DEBNMSUB,X'00'           ANY SUBROUTINES LOADED @ZA08009
         BE    CHKNXDEB                 NO GO ON               @ZA08009
         TM    DCBMACF2,DCBMRPUT        IS THIS QISAM LOAD MODE XA01816
         BO    CHKNXDEB                 YES, DISP=SHR NOT POSS  XA01816
         TM    DCBMACF1,DCBMRGET        IS IT QISAM SCAN MODE   XA01816
         BO    CHKISCAN                 YES,CHECK SCAN MODE W/A XA01816
         TM    DCBMACF1,DCBMRRD         IS THIS BISAM READ      XA01816
         BO    CHKBISAM                 YES,CHECK BISAM W/A     XA01816
         TM    DCBMACF2,DCBMRWRT        IS THIS BISAM WRITE     XA01816
         BZ    CHKNXDEB                 NO, GO GET NEXT DEB     XA01816
CHKBISAM EQU   *                                                XA01816
         L     RD,DCBWKPT2              BET BISAM W/A PTR       XA01816
         USING BISAMWA,RD                                       XA01816
         TM    DCWDCBFA,ISAMSHR         IS IT OPEN DISP=SHR     XA01816
         BZ    CHKNXDEB                 NO, GET NEXT DEB        XA01816
         DROP  RD                                               XA01816
CHKISERR EQU   *                                                XA01816
         LA    RA,CKPERG32              ISAM DISP=SHR ERROR     XA01816
         B     CHRERX                   TO TO ERROR EXIT        XA01816
CHKISCAN EQU   *                                                XA01816
         L     RD,DCBWKPT1              GET SCAN MODE W/A PTR   XA01816
         USING SCANWA,RD                                        XA01816
         TM    W1DCBFA,ISAMSHR          IS IT OPEN FOR SHR      XA01816
         BO    CHKISERR                 YES,IT IS SHARED        XA01816
         DROP  RD                                               XA01816
CHKNXDEB EQU   *                                                XA01816
         L     R6,DEBDEBAD    GET NEXT DEB                       A39163
         B     CHKGRAPH       GO CHK NXT DEB                     A39163
LSTDEB   EQU   *                                                 A39163
         EJECT 1                                                   I250
*
*******************************************************************I250
*                                                                  I250
*        AOS/2 ENVIRONMENT TESTING -                               I250
*              OUTSTANDING ENQUEUES                                I250
*              OUTSTANDING WTOR                                    I250
*                                                                  I250
*******************************************************************I250
*                                                                  I250
         LA    RA,CKERENQS         LOAD MESSAGE CODE FOR POSSIBLE ERROR
         NC    TCBFSA(1),TCBFSA    ARE THERE ANY OUTSTANDING ENQUEUES.
         BNZ   CHRPRE              YES, CHK END OF QCB CHAIN     YM0520
CHRNXT   EQU   *
         L     RF,CVTPTR(R0)       GET ADDR OF CVT.
         USING CVTSECT,RF          ADDRESS CVT.
         L     R1,CVTCUCB          GET ADDR OF UCM TABLE
         USING UCMSECT,R1          ADDRESS UCM.
         L     R1,UCMRPYQ          GET ADDR OF FIRST ORE.
         USING OREF,R1             ADDRESS OPERATOR REPLY ELEMENT.
CHRWTORQ EQU   *
         LTR   R1,R1               ARE THERE ANY MORE ORE'S
         BZ    CHRCKT              NO-GO CHECK CHECK ID          Y02083
         LA    RA,CKERPG14         LOAD RETURN CODE IF ERROR OCCURS.
         C     R4,ORETCB           IS THERE AN OUTSTANDING WTOR
         BNE   NEXT                NO TEST NEXT ELEMENT        @ZA07036
         L     RF,CVTPTR           GET THE CVT POINTER         @ZA07036
         L     RF,CVTTCBP-CVT(,RF)  GET TCB POINTER            @ZA07036
         L     RF,K12(RF)           GET ASCB                   @ZA07036
         L     R3,OREWQE            GET THE WQE ADDRESS        @ZA07036
         LA    R3,0(R3)             CLEAR HIGH ORDER BYTE      @ZA07036
         CLC   ASCBASID-ASCB(,RF),161(R3)  COMPARE ASIDS       @ZA07036
         BE    CHRERX               GO TO ERROR ROUTINE        @ZA07036
NEXT     EQU   *
         L     R1,ORELKP           GET ADDR OF NEXT REPLY Q. ELEMENT.
         B     CHRWTORQ            CONTINUE CHECKING ORE CHAIN
         EJECT 1                                                   I250
*                                                                     *
***********************************************************************
*                                                                     *
*        CHECK LENGTH OF CHECKID                                      *
*                                                                     *
***********************************************************************
*                                                                     *
CHRCKT   EQU   *
         L     R3,CKPARMAD          GET ADDR OF PARAMETER LIST.
         USING PARMSECT,R3         MAPPING CHKPT PARAMETER LIST
         TM    PARMFLG,NOID        DID USER SUPPLY CHECKID
         BO    CHRBKPRB            NO-SKIP CODE TO EXAMINE CHECKID.
         TM    LNCHKID,USERID      Q. DID USER SUPPLY CHECKID
         BO    CHRBKPRB            NO-SKIP CODE TO EXAMINE CHECKID.
         OI    CKFLAG2,CKUSPCHD    INDICATE USER SUPPLIED CHECKID.
         LA    RA,CKERPG15          LOAD RETURN CODE IF ERROR OCCURS.
         CLI   LNCHKID,LNGZERO     DOES CHECKID LENGTH = 0
         BE    CHRERX              YES-GO TO ERROR EXIT RTN
         CLI   LNCHKID,CHR16       IS CHECKID TOO LONG
         BH    CHRERX              YES-GO TO ERROR EXIT ROUTINE.
         TM    CKFLAG1,CKBPAM      WAS CHKPT DATA SET OPENED FOR BPAM.
         BZ    CHKCHARS       NO-SKIP CODE TO CHECK BPAM LENGTH  A44785
         CLI   LNCHKID,BPAMLNG     IS CHECKID TOO LONG
         BH    CHRERX              YES-GO TO ERROR EXIT ROUTINE.
*                                                                     *
***********************************************************************
*                                                                     *
*        TEST USER'S CHECKID FOR VALID CHARACTERS                     *
*                                                                     *
***********************************************************************
*
*                                                                     *
CHKCHARS EQU   *                                                 A44785
         XR    R6,R6               SET SWITCH TO INDICATE FIRST CHAR.
         XR    R7,R7               CLEAR REGISTER
         IC    R7,LNCHKID          GET LENGTH OF CHECKID
         L     R9,IDADDR           GET ADDR OF CHECKID
         DROP  R3
CHRTAB   EQU   *
         LA    R8,CHKTABLE         GET ADDR OF TABLE OF VALID CHAR.
         XR    RE,RE
CHRBGN   EQU   *
         CLC   0(1,R8),0(R9)       DOES CHECKID CHAR. MATCH A VALID ONE
         BE    CHROUT              YES-EXAMINE NEXT CHAR.
         LTR   R6,R6               IS THIS THE FIRST CHAR
         BNZ   CHRTST2             NO-GO TO SEE IF END OF TABLE.
         CH    RE,=H'28'           END OF ALPHABETIC PART OF TABLE
         BL    CHRCOMUP       NO-EXAMINE NEXT CHAR IN TABLE      A44785
         TM    CKFLAG1,CKBPAM IS BPAM ORGANIZATION               A44785
         BO    CHRERX         YES-GO TO ERROR EXIT ROUTINE       A44785
         CH    RE,=H'50'                END NATL CHAR TBL        M1355
         BE    CHRERX              YES-GO TO ERROR EXIT ROUTINE.
         B     CHRCOMUP            NO-EXAMINE NEXT CHAR IN TABLE.
CHROUT   EQU   *
         LR    R6,R9               SET SWITCH TO INDICATE NOT-1ST-CHAR.
         LA    R9,ADDONE(R9)       UPDATE POINTER TO NEXT CHAR IN CKID
         BCT   R7,CHRTAB           END OF TESTING-NO-TEST NEXT CHAR
CHRBKPRB EQU   *
*                                                                     *
***********************************************************************
*                                                                     *
*        SET PARAMETER REGISTER AND XCTL TO IGC0206C                  *
*                                                                     *
***********************************************************************
*                                                                     *
         LR    R1,RC               LOAD PARM REG WITH W.A. ADDR.
         MVC   CKJCT(LXCTLNAM),CHRXLIST      SET UP XCTL PARM LIST
CHREXT   EQU   *
CHKCER   EQU   *
         LA    RF,CKJCT
         USING XCTLSECT,RF         MAPPING OF XCTL PARM LIST
         ST    RF,XCPARMPT
         LA    RF,XCPARMPT
         DROP  RF
         XCTL  SF=(E,(15))
*                                                                     *
***********************************************************************
*        ERROR ROUTINE - SETS RETURN CODE AND SAVES MESSAGE CODE IN   *
*        CHKWA; ALTERS XCTL PARM LIST TO TRANSFER CONTROL TO IGC0Q06C *
*                                                                     *
***********************************************************************
*                                                                     *
CHRERX   EQU   *
         LR    R1,RC
         OI    CKFLAG2,CKMSG1      INDICATES MSG-ENVIRONMENT ERROR
         OI    CKFLAG2,CKFREEM     INDICATES ONLY FREEMAIN WA IN CER
         MVI   CKRETCD1,CKPGMERR   SAVE RETURN CODE IN CHKWA
         STH   RA,CKMSGCOD         SAVE MESSAGE CODE IN WA
         LA    RF,CKJCT
         USING XCTLSECT,RF         MAPPING OF XCTL PARM LIST
         MVC   XCTLNAME(LXCTLNAM),CHRXLIST   SET UP XCTL PARMLST TO
         MVI   XCTLNM2,CHARQ                 TRANSFER CONTROL TO CER
         B     CHKCER              GO TO TRANSFER CONTROL
CHRBLANK EQU   *
         BCTR  R7,0                DECREMENT COUNT FOR MOVES.
         EX    R7,CHRCLC1          ARE REST OF CHARS. BLANKS.
         BNE   CHRERX              NO-GO TO ERROR EXIT ROUTINE.
         B     CHRBKPRB            YES-VALID CHECKID,GO ON PROCESSING.
CHRTST2  EQU   *
         CH    RE,=H'38'           END OF TABLE
         BL    CHRCOMUP       NO-EXAMINE NEXT CHAR IN TABLE      A44785
         TM    CKFLAG1,CKBPAM IS BPAM ORGANIZATION               A44785
         BO    CHRBLANK       YES-SEE IF REST OF CHARS ARE BLANK A44785
         CH    RE,=H'50'      END OF NATIONAL CHAR PART OF TABLE M1355
         BE    CHRBLANK            GO TO SEE IF REST OF CHAR.ARE BLANKS
CHRCOMUP EQU   *
         LA    RE,ADDONE(RE)       UPDATE COUNT OF CHAR TRIED
         LA    R8,ADDONE(R8)       UPDATE POINTER TO NEXT CHAR IN TABLE
         B     CHRBGN              GO TO TRY NEXT CHAR IN TABLE.
*
CHRPRE   EQU   *                                                 YM0520
*
*  FOLLOWING IS A TEST FOR DATA SET INTEGRITY (DSI) ONLY ENQS    YM0520
*
         L     R3,CVTPTR                GET CVT ADDR             YM2878
         L     R3,CVTFQCB-CVT(,R3)      GET FIRST QCB            YM2878
         B     CHRQCB01                 BR TO PROC. QCBS         YM2878
         USING MAJ,R3                                            YM2878
CHRQCBMJ L     R3,MAJNMAJ               GET NEXT MAJOR QCB       YM2878
CHRQCB01 LTR   R3,R3                    CHECK IF MAJ EXISTS      YM2878
         BZ    CHRNXT                   NO MORE-CHECK ENVIRON.   YM2878
         CLC   MAJNAME(K8),SYSDSN       SCHED HANLES SYSDSN      YM2878
         BE    CHRQCBMJ                 IGNORE                   YM2878
         CLC   MAJNAME(K8),SYSVSAM      VSAM HANDLES SYSVSAM     YM2878
         BE    CHRQCBMJ                 IGNORE                   YM2878
         LA    R6,MAJFMIN               PRETEND MAJOR IS MINOR   YM2878
         USING MIN,R6                                            YM2878
CHRQCBMN EQU   *                        CHECK MINORS             YM2878
         L     R6,MINNMIN               GET NEXT MINOR QCB       YM2878
         LTR   R6,R6                    ANY MORE MINORS          YM2878
         BZ    CHRQCBMJ                 NO-GET NEXT MAJOR        YM2878
         LA    R7,MINFQEL               PRETEND MIN IS QEL       YM2878
         USING QEL,R7                                            YM2878
CHRQEL   EQU   *                        CHECK QELS               YM2878
         L     R7,QELNQEL               GET NEXT QEL             YM2878
         LTR   R7,R7                    ANY MORE QELS?           YM2878
         BZ    CHRQCBMN                 NO-TRY NXT MINOR QCB     YM2878
         LR    R8,R7                    SAVE QEL-MAY BE LIST     YM2878
         DROP  R7                                                YM2878
         USING QEL,R8                                            YM2878
         B     CHRQEL08                 BR TO CHECK IF LIST      YM2878
CHRQELLS EQU   *
         L     R8,QELLQEL               GET LIST QEL             YM2878
CHRQEL08 EQU   *                        LIST QEL TEST            YM2878
         TM    QELLQEL,QELXLIST         LIST QEL?                YM2878
         BO    CHRQELLS                 GET REAL QEL             YM2878
         L     RF,CVTPTR                GET CVT POINTER          YM2878
         L     RF,CVTTCBP-CVT(,RF)      GET TCB PTR              YM2878
         L     RF,K12(RF)               CURRENT ASCB             YM2878
         CLC   ASCBASID-ASCB(,RF),QELASID   THIS MEMORY?         YM2878
         BNE   CHRQEL                   BR FOR NEXT QEL          YM2878
         CLC   QELTCB+K1(K3),CKTCBAD+K1 THIS TCB?                YM2878
         BNE   CHRQEL                   IF NO-GET NEXT QEL       YM2878
         DROP  R3,R6,R8                                          YM2878
***********************************************************************
*
*        SET RETURN CODE = PROBABLE ERROR
*
***********************************************************************
*
CHRPROBR EQU   *
         MVI   CKRETCD1,CKPSBERR   INDICATE PROBABLE ERROR
         STH   RA,CKMSGCOD         STORE MESSAGE CODE
         B     CHRNXT             GO TO CONTINUE ENVIRONMENT CHECKING
*                                                                     *
***********************************************************************
*                                                                     *
*        FOR CHKPT DATA SET ON DIRECT ACCESS, CHECK KEYLENGTH IN DCB  *
*        EQUAL TO ZERO.                                               *
*                                                                     *
***********************************************************************
*                                                                     *
CHRDACL  EQU   *
         LA    RA,CKERPG5           LOADED RETURN CODE IF ERROR OCCURS.
         CLI   DCBKEYLE,LNGZERO    IS KEYLENGTH ZERO
         BNE   CHRERX              NO-GO TO ERROR EXIT ROUTINE.
         B     CHRTRFM             YES-CONTINUE ENVIRONMENT CHECKING.
CHRCLC1  CLC   0(1,R9),BLANKS      EXECUTED COMPARE FOR PADDED BLANKS.
*
*        TABLE CONTAINING VALID CHARACTERS FOR CHECKID
*
CHKTABLE EQU   *
         DC    CL26'ABCDEFGHIJKLMNOPQRSTUVWXYZ'
         DC    CL3'$#@'
         DC    CL10'0123456789'
         DC    CL12',./''()*&&+-= '     NATIONAL CHARACTERS      M1355
BLANKS   DC    C'       '
*
H4096    DC    H'4096'             4K BLKSIZE FOR CHKPT DATA SET YM5921
CHRXLIST DC    C'IGC0206C'         TRANSFER CONTROL(NORMAL FLOW)
SYSDSN   DC    C'SYSDSN  '              SYSTEM D/S ENQ MAJ NAME  YM0520
SYSVSAM  DC    C'SYSVSAM '              VSAM DS MAJOR NAME       YM0520
ZERO     DC    F'0'                     FULL WORD OF ZERO'S      YM0520
IMGDDNAM DC    C'IMGLIB  '              IMG LIB DCB MASK         YM7338
BLKLENF  EQU   2                   LENGTH OF DCB BLKSIZE FIELD
K1       EQU   1                        CONSTANT 1               YM2878
K3       EQU   3                        CONSTANT 3               YM2878
K4       EQU   4                        CONSTANT 4               YM7338
K8       EQU   8                        CONSTANT 8               YM2878
K12      EQU   12                       CONSTANT 12              YM2878
*                                                                     *
*        MAINTENANCE SPACE                                            *
*                                                                     *
PATCH    DC    XL((*-IGC0106C)/20)'00'   FIVE PERCENT MAINTENANCE SPACE
*                                                                     *
         IHAASCB                                                 YM2878
         IHAQCB                                                  YM2878
         IHAQEL                                                  YM2878
         IEEVCHWA VER=3                 USE AOS2 VERION OF MACRO
         DCBD  DSORG=(BS,PO,IS),DEVD=(DA,TA)                    XA01816
SCANWA   IGGSCAN                                                XA01816
BISAMWA  IGGBISAM                                               XA01816
*                                                                     *
*        MAPPING OF CHECKPOINT PARAMETER LIST                         *
*                                                                     *
PARMSECT DSECT
PARMFLG  DS    1C   FLAG BYTE
NOID     EQU   X'80'     INDICATES NO CHECKID SPECIFIED BY USER AND
*                        GENERATED CHECKID ADDR NOT TO BE RETURNED
CHKID    EQU   X'00'     USER-SPECIFIED CHECKID
PARMDCB  DS    3C        ADDR OF CHKPT DCB
IDADDR   DS    0F
LNCHKID  DS    1C        LENGTH OF USER-SPECIFIED CHECKID
USERID   EQU   X'FF'     INDICATES USER WANTS SYSTEM-GENERATED CHECKID
*                        AND RETURNED AT LOCATION SPECIFIED BY IDADDR
         DS    3C        ADDR OF USER-SUPPLIED CHECKID,OR, CONTAINS
*                        RETURN ADDRESS OF SYSTEM-GENERATED CHKID
*                                                                     *
*        MAPPING OF XCTL PARAMETER LIST                               *
*                                                                     *
XCTLSECT DSECT
XCTLNAME DS    1F        MODULE NAME TO TRANSFER CONTROL
XCTLNM2  DS    1F
XCPARMPT DS    1F        POINTER TO XCTL PARM LIST
UCMSECT  DSECT
         IEECUCM
         IHAORE  DSECT=YES
CVTSECT  DSECT
CVT      CVT                                                     YM2878
         IKJTCB
         IKJRB
         IHATQE
         IEZDEB
CKTIOTD  DSECT
         IEFTIOT1
         EJECT
SRT      DSECT                                                   Y02049
         IEFUCBOB  LIST=YES                                      Y02049
         EJECT
SVRB34   EQU   X'D0'     INDICATES TYPE 3 OR 4 SVRB.
CHR16    EQU   16     MAXIMUM LENGTH FOR A CHECKID
TQENTQ   EQU   X'80'     INDICATES TQE IS NOT ON TIMER QUEUE.
FIXD     EQU   128     DCBRECFM FIELD- INDCIATES FIXED
TRKOVF   EQU   32            DCBRECFM FIELD-INDICATES TRACK OVERFLOW
UNDF     EQU   X'C0'   DCBRECFM FIELD- INDICATES UNDEFINED
FIXUND   EQU   128     DCBRECFM FIELD- INDCIATES FIXED OR UNDEFINED
CLOSEOPT EQU   128       OPTION BYTE FOR CLOSE'S PARM LIST.
USERCKID EQU   X'FF'     INDICATES USER SUPPLIED CHECKID
LNGZERO  EQU   X'00'     LENGTH = ZERO
BPAMLNG  EQU   X'08'     CHECKID LENGTH = 8
CHARQ    EQU   C'Q'      USED TO ALTER PARMLST TO XCTL TO CER
LNADDR   EQU   3         LENGTH OF ADDRESS
ADDONE   EQU   1         INCREMENT QUANTITY
ONECHAR  EQU   1         INDICATES ONE CHARACTER COMPARE
LXCTLNAM EQU   8         LENGTH OF XCTL PARAMETER LIST NAME
CKSPBKSZ EQU   8         INDICATES CHKPT PROVIDED BLKSIZE FOR CHKPT DS
HALFWD   EQU   2         LENGTH OF HALF-WORD
ADDRBYTS EQU   7         MASK OF ADDRESS BITS IN FULLWORD
TAPE     EQU   X'80'      TAPE BIT OF DEVTYPE FIELD              S20038
ALLBITS   EQU  X'FF'     MASK WITH ALL BITS ON
*
*        ADDRESSING BYTES WITHIN DSECTS
CKBUFLN  EQU   CKBUFL+2
TCBTME1  EQU   TCBTME+1
TCBLTC1  EQU   TCBLTC+1
CKRETCD1 EQU   CKRETCOD+1
PIEFLAG  EQU   X'80'                                             A43190
CKMSG1   EQU   32    WTO MESSAGE-NOT TAKEN-ENVIRONMENT ERROR
CKERPG5  EQU   X'05'    FOR D.A.,KEYLENGTH SPECIFIED=0
CKERPG6  EQU   X'06'    ERROR IN DCBRECFM
CKERPG7  EQU   X'07'    ERROR IN DCBDSORG
CKERPG8  EQU   X'08'    ACTIVE TQE
CKERPG9  EQU   X'09'    ERROR IN RBSTAB
CKPERG10 EQU   X'0A'          GRAPHICS DSORG ERR CODE            A39163
CKERPG11 EQU   X'0B'    CURRENT TCB IS NOT JOB STEP TCB
CKERPG12 EQU   X'0C'    TCB HAS DAUGHTER TASKS
CKPER206 EQU   206                      VTAM DSORG ERR CODE      Y02076
CKERENQS EQU   X'0100'             OUTSTANDING ENQS
CKERPG14 EQU   X'0E'    OUTSTANDING WTOR
CKERPG15 EQU   X'0F'    INCORRECT CKID LN/FORMAT
CKERPG16 EQU   X'10'    CHKPT D.S. NOT ON D.A. OR TAPE
CKPER224 EQU   224                      SAM-SI(NON CI) ERR CODE  Y02076
CKPER250 EQU   250                      IMAGE LIB DCB            YM7813
CKERPG19 EQU   X'13'    CHKPT DS NOT OPENED TO WRITE
CKERPG21 EQU   X'15'    MSG CODE FOR JCL LABEL ERROR              19200
CKERPG29 EQU   X'1D'    MSG CODE FOR OPTCD FIELD OF DCB ERROR    S20038
CKPERG32 EQU   X'20'            MSG CODE FOR ISAM (DISP=SHR)    XA01816
ISAMSHR  EQU   X'80'                    ISAM WA BIT FOR SHR     XA01816
VTAMDSCD EQU   X'82'                    VTAM DATA SET DEB CODE   Y02076
VSAMDSCD EQU   X'01'                    VSAM DATA SET DEB CODE   Y02076
ACBIDENT EQU   X'A0'                    ACB ID BYTE              Y02076
DEBCIBIT EQU   X'08'    DEB CI BIT POSITION                      Y02076
DEBPRFLN EQU   16                       LNGTH OF DEB PREFIX      Y02076
         END
