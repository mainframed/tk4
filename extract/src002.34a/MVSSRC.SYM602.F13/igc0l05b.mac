         GBLC  &LIB
&LIB     SETC  'LIB1'                   SET VARIABLE TO LIB1
         TITLE 'IGC0L05B  SYSIN/SYSOUT NON-DA DATA SET PROCESSOR     RE*
               POSITION I/O  RESTART-SVC 52  OS/360'
IGC0L05B CSECT
*                                                                A46670
*
*          RELEASE 21 DELETIONS/CHANGES
*0794229300                                                      M0187
*          RELEASE 20 DELETIONS/CHANGES
*0960171000,174200,198000,201500,539000,541200,542200,574000-    A30921
*0960576000                                                      A30921
*0960262300,395000,504000,514100-514500                          S20038
*          RELEASE 19 DELETIONS/CHANGES
*3252099000,174000,201000                                        M4873
*3252003100-003200,003300-003900,395000,411000,449000,453000,    A28296
*3252483000,489000,504000                                        A28296
*          VS2/RELEASE 3 DELETIONS/CHANGES
*539682                                                        @ZA03651
*
* STATUS CHANGE LEVEL 004
*
***********************************************************************
*
*TITLE - NON-DA SYSIN/SYSOUT DATA SET POSITIONING
*
*STATUS - CHANGE LEVEL 0
*
*FUNCTION/OPERATION - PRIME BUFFERS FOR CARD READER
*
*
*ENTRY POINTS - IGC0L05B - INITIAL ENTRY TO THIS MODULE BY MEANS OF AN
*                          XCTL FROM IGC0K05B OR IGC0M05B
*
*INPUT - REGISTER 8   BEGINNING OF DCB TABLE
*        REGISTER 1   USER BUFFER AREA
*        ALL OPENED DCB (S) WITH THEIR ASSOCIATED CONTROL BLOCKS
*        THE 48 BYTE TABLE ENTRY (IES) THAT IS (ARE) USED IN RESTART
*        RESTART WORKAREA
*
*OUTPUT - REGISTER 8   BEGINNING OF DCB TABLE
*         REGISTER 1   USER BUFFER AREA
*         BUFFERS ARE PRIMED FOR CARD READER
*
*EXTERNAL ROUTINE - NONE
*
*EXITS - NORMAL - FINAL EXIT IS MADE VIA XCTL TO THE NEXT MODULE OF
*                 REPI/O. THIS MAY BE IGC0N05B (IF  DATA SETS EXIST
*                 ON DA) OR IGC0P05B.
*
*
*
*TABLES/WORKAREAS - THE WORKAREA USED IS DESCRIBED BY THE DSECTS AT THE
*                   END OF THE LISTING.
*
***********************************************************************
*                                                                     *
*    THE FOLLOWING IS A DESCRIPTION OF THE 48 BYTE TABLE ENTRY THAT   *
*    IS USED IN THE RESTART PORTION OF CHECKPOINT/RESTART.  EACH      *
*    ENTRY IN THE TABLE (ONE FOR EACH OPEN DATA SET AT CHECKPOINT)    *
*    IS 48 BYTES LONG.                                                *
*                                                                     *
***********************************************************************
*
*        ------------------- 4 BYTES -----------------
*
*      0 +++++++++++++++++++++++++++++++++++++++++++++
*        +DATA SET  +           DCB                  +
*        + ORGN.    +         ADDRESS                +
*        +++++++++++++++++++++++++++++++++++++++++++++ 4
*        +FLAG BYTE +    WORKAREA SEGMENT            +
*        + NO. 1    +         ADDRESS                +
*      8 +++++++++++++++++++++++++++++++++++++++++++++
*        +NUMBER    +         JFCB TTR               +
*        + OF VOLS  +         ADDRESS                +
*        +++++++++++++++++++++++++++++++++++++++++++++ 12
*
*     12 +++++++++++++++++++++++
*        +TAPE LABL + FILE SEQ +        DEVICE = TAPE
*        +INFO BYTE + NUMBER   +
*        +++++++++++++++++++++++
*
*     12 +++++++++++++++++++++++
*        +    OLD   TIOT       +        DA AND UNIT RECORD
*        +      OFFSET         +
*        +++++++++++++++++++++++
*
*                              +++++++++++++++++++++++
*                              +FLAG BYTE +FLAG BYTE +
*                              +  NO. 2   +  NO. 3   +
*     16 +++++++++++++++++++++++++++++++++++++++++++++
*        +FLAG BYTE +FLAG BYTE +                     +
*        + NO. 4    + NO. 5    +   VOLUME ID NO. 1   +
*        +++++++++++++++++++++++                     + 20
*        +                                           +
*        +                                           +
*     24 +++++++++++++++++++++++++++++++++++++++++++++
*        +                                           +
*        +   VOLUME ID NO. 2                         +
*        +                     +++++++++++++++++++++++ 28
*        +                     +                     +
*        +                     +   VOLUME ID NO. 3   +
*     32 +++++++++++++++++++++++                     +
*        +                                           +
*        +                                           +
*        +++++++++++++++++++++++++++++++++++++++++++++ 36
*        +                                           +
*        +   VOLUME ID NO. 4                         +
*     40 +                     +++++++++++++++++++++++
*        +                     +                     +
*        +                     +                     +
*        +++++++++++++++++++++++   VOLUME ID NO. 5   + 44
*        +                                           +
*        +                                           +
*        +++++++++++++++++++++++++++++++++++++++++++++ 48
*
*
*ATTRIBUTES - REENTRANT
*             REUSABLE
*             ENABLED
*             PRIVILEGED
*
***********************************************************************
*
*    REGISTER CONVENTION TO BE USED THRU-OUT REPI/O PHASE
*
***********************************************************************
*
WRKR1    EQU   0                        WORK/PARAMETER REGISTER
WRKR2    EQU   1                        WORK/PARAMETER REGISTER
R1       EQU   1                        REG 1                    YM7873
DCBR     EQU   2                        CURRENT DCB REGISTER
BASER    EQU   3                        BASE RIGISTER
DEBR     EQU   4                        CURRENT DEB REGISTER
UCBR     EQU   5                        CURRENT UCB REGISTER
TIOTR    EQU   6                        NEW TIOT RESISTER
TCBR     EQU   7                        TCB REGISTER
TABLR    EQU   8                        BEGINNING TO DCB TABLE REGISTER
CTABLR   EQU   9                        CURRENT ENTRY IN DCB TABLE
R9       EQU   9                        REG 9                    YM7873
BUFR     EQU   10                       USER BUFFER AREA
RA       EQU   10                       REG 10                   YM7873
DSCNTR   EQU   11                       NUMBER OF DATA SETS
WRKR3    EQU   12                       WORK REGISTER
WRKR4    EQU   13                       WORK REGISTER
WRKR5    EQU   14                       WORK/PARAMETER REGISTER
RE       EQU   14                       REG 14                   YM7873
WRKR6    EQU   15                       WORK/PARAMETER REGISTER
RF       EQU   15                       REG 15                   YM7873
*
***********************************************************************
*
*    OFFSETS TO BE USED IN REPI/O PHASE
*
***********************************************************************
*
IS       EQU   X'80'                    INDEXED SEQUENCIAL
PS       EQU   X'40'                    PHYSICAL SEQUENCIAL
DA       EQU   X'20'                    DIRECT ACCESS
PO       EQU   X'02'                    PARTITIONED ORGANIZATION
INOROUT  EQU   X'40'                    SYSIN OR SYSOUT DATA SET
UNITREC  EQU   X'04'                    UNIT RECORD EQUIPMENT
ERRINNSL EQU   X'80'                    ERROR IN NSL ROUTINE
NSLMTSW  EQU   X'40'                    NSL MOUNT SWITCH
NSLCOMU  EQU   X'20'                    NSL COMMUNICATION BIT
BYPASS   EQU   X'10'                    BYPASS LABEL PROCESSING
AL       EQU   X'08'                    ASCII LABEL              S20038
NSL      EQU   X'04'                    NON-STANDARD LABEL
SL       EQU   X'02'                    STANDARD LABEL
NL       EQU   X'01'                    NO LABEL
GTR5VOL  EQU   X'80'                    GREATER THAN 5 VOLUMES
POCONCAT EQU   X'40'                    P.O. CONCATENATED
DAFLAG   EQU   X'80'                    DA DATA SET FLAG
NONDAFLG EQU   X'40'                    NON-DA DATA SET FLAG
NSLFLAG  EQU   X'10'                    NSL ROUTINE FLAG
TYPERST  EQU   X'80'                    IMMEDIATE RESTART
TABENT   EQU   48                       TABLE ENTRY LENGTH
WRITE    EQU   X'01'                    WRITE ON TAPE
READ     EQU   X'02'                    READ FOR TAPE
NOPOP    EQU   X'03'                    NOP
SENSE    EQU   X'04'                    SENSE
REWIND   EQU   X'07'                    REWIND
READBACK EQU   X'0C'                    READBACK
UNLOAD   EQU   X'0F'                    REWIND AND UNLOAD
WTM      EQU   X'1F'                    WRITE TAPE MARK
BSFOP    EQU   X'2F'                    BACKWARD SPACE FILE
FSFOP    EQU   X'3F'                    FORWARD SPACE FILE
SEARCH   EQU   X'31'                    SEARCH
SRCHCNT  EQU   X'05'                    COUNT FOR SEARCH ID
TIC      EQU   X'08'                    TIC
READDA   EQU   X'06'                    READ FOR DA
LENJFCB  EQU   X'B0'                    LENGTH OF JFCB
LBLLEN   EQU   X'50'                    LENGTH OF LABEL
LBLOFST  EQU   96                       OFFSET FROM RSJFCB TO    S20038
*                                       AREA FOR                 S20038
*                                            LABEL CONSTRUCTION
PERMERR  EQU   X'7F'                    PERMANENT ERROR
ERR24    EQU   24                       ERROR IN READING IN JFCB
ERR50    EQU   50                       ERROR IN WRITING LABELS
ERR54    EQU   54                       ERROR - ASCII LABEL FOR  S20038
*                                       SYSIN                    S20038
*                                            OR SYSOUT TAPE
ONE      EQU   X'01'                    CONSTANT ONE
OUT      EQU   X'40'                    SYSOUT DATA SET (UCBWGT)
CHAIN    EQU   X'40'                    CHAINING FLAG
BLANK    EQU   C' '                     BLANK CHARACTER
SIGNOFF  EQU   X'F0'                    REMOVE SIGN BIT
ZERO     EQU   C'0'                     ZERO CONSTANT
SECIND   EQU   C'1'                     SECURITY INDICATOR
SLIFLAG  EQU   X'20'                    SLI FLAG
HDR2ID   EQU   C'2'                     HDR2 IDENTIFIER
MASK     EQU   X'03'                    MASK USED FOR ZEROING BLKA
TRANS    EQU   C'T'                     TRANSLATE IN TRTCH
GTR32K   EQU   X'F9'                    GREATER THAN 32K INDICATOR
SLASH    EQU   C'/'                     SLASH
MACHCNTL EQU   C'M'                     MACHINE CODE CONTROL
ASACNTL  EQU   C'A'                     ASA CNTRL CHAR
CHSLI    EQU   CHAIN+SLIFLAG            CHAINING AND SLI FLAG
IOERROR  EQU   X'20'                    PERMANENT I/O ERROR
UNITEX   EQU   X'01'                    UNIT EXCEPTION
IN       EQU   X'80'                    SYSIN DATA SET (UCBWGT)
RDR2501  EQU   X'01'                    2501 CARD READER         A46670
RDR2540  EQU   X'04'                    2540 CARD READER         A46670
CARDRDR  EQU   X'05'                    CARD READER
P3211    EQU   X'09'                    3211 PRINTER             YM7873
WALNTH   EQU   X'23A'                   LENGTH OF 3211 ERP WA    YM7873
MOVE     EQU   X'14'                    MOVE OR SUBSTITUTE MODE
NTPRGD   EQU   X'FF'                    NO PURGED
AET      EQU   X'2B'                    EVEN PARITY AND TRANSLATION IN
*                                            TRTCH
AMCODE   EQU   X'02'                    MACHINE CODE
ASA      EQU   X'04'                    ASA CONTROL CHARACTER
QSAMDCB  EQU   X'01'                    QSAM DCB INDICATOR
DAFLG    EQU   X'80'               DIRECT ACCESS FLAG            M4873
IOBSTAT0 EQU   20                       BYTE 12 OF AN IOB        A30921
DEBPRFLN EQU   16                  LENGTH OF DEB PREFIX          Y02049
K1       EQU   1                        CONST 1                  YM7873
K4       EQU   4                        CONST 4                  YM7873
*
***********************************************************************
*
         BALR  BASER,0                  ESTABLISH ADDRESSIBILITY
         USING *,BASER                  ESTABLISH BASE REG
         B     BEGIN               BRANCH AROUND EYE CATCHER     Y01909
         CNOP  0,8                 ALIGN TO A DBL WORD BOUNDARY  Y01909
         DC    C'IGC0L05B'         CSECT NAME                    Y01909
BEGIN    EQU   *                                                 Y01909
*
         USING IHADCB,DCBR
         USING DEBBASIC,DEBR                                     Y02049
         USING UCBOB,UCBR
         USING TABL,CTABLR
         USING RSTWA,BUFR
         USING RSDSECT,WRKR4
         USING CVT,WRKR2
         USING FILELAB,TIOTR
         USING TCB,TCBR
         USING TIOT1,WRKR5
*
         LR    CTABLR,TABLR             INITIALIZE TABL PTR
*
REPIO002 EQU   *
*
         L     WRKR4,TABSEGAD-1         WORKAREA SEGMENT ADDRESS
         LA    WRKR6,REPIO100           LOAD REG WITH BRANCH     A46670
*                                       ADDRESS                  A46670
         TM    TABFLG1,TABDA+TABNULL+TABTCAM SKIP IF DA          Y02076
         BCR   7,WRKR6                  TCAM DS OR NULL          A46670
*
REPIO003 EQU   *                                                 M4873
*
*    ******************************************************************
*
*     QSAM CARD READER BUFFERS ARE PROCESSED BY THIS MODULE
*
*    ******************************************************************
*
*    *** PICK UP NECESSARY ADDRESSES ***
*
         LA    WRKR4,0(WRKR4)           CLEAR HIGH ORDER BYTE
         L     DCBR,TABDCBAD-1          DCB ADDR IN THIS ENTRY
         LA    DCBR,0(DCBR)             CLEAR HIGH ORDER BYTE
         L     DEBR,DCBDEBAD            DEB ADDR ASSOC WITH THIS DCB
         L     UCBR,DEBSUCBA            UCB ADDR ASSOC WITH DEB  Y02049
*
         TM    TABFLG1,TABTAPE          IS IT ON TAPE DEVICE
         BO    REPIO100                 BRANCH TO GET NXT ENTRY  Y02076
*
REPIO050 EQU   *
*
*    *** UNIT RECORD DEVICE PROCESS - CARD READER ONLY ***
*
         CLI   UCBTBYT3,UCB3UREC        TEST UNIT RECORD         A46670
         BCR   7,WRKR6                  SKIP IF NOT              A46670
         CLI   UCBTBYT4,P3211           IS IT A 3211 PRINTER     YM7873
         BNE   REPIO05K                 BRANCH IF NO             YM7873
         STM   R9,RF,RSREGSAV+R9*K4     SAVE REGS                YM7873
         LA    R9,RSREGSAV+R9*K4        GET ADDR REG SAVE AREA   YM7873
         L     RE,UCBXTADR              PRINTER EXTENSION        YM7873
         USING UCBUCS,RE                                         YM7873
         SR    RF,RF                    ZERO RF FOR IC, LTR      YM7873
         IC    RF,UCBERADR              GET USE COUNT            YM7873
         LTR   RF,RF                    IS IT NOW ZERO           YM7873
         LA    RF,K1(,RF)               INCREMENT BY ONE         YM7873
         STC   RF,UCBERADR              STORE UPDATED COUNT      YM7873
         BNZ   REPIO05D                 BRANCH IF COUNT WASN'T 0 YM7873
*                                                                YM7873
*        GET ERP WORKAREA                                        YM7873
         GETMAIN R,SP=255,LV=WALNTH     GET ERP WORK AREA        YM7873
         STCM  R1,B'0111',UCBERADR+K1   STORE WORKAREA ADDR      YM7873
*                                                                YM7873
REPIO05D EQU   *                        EXIT THIS TAB ENTRY      YM7873
         LM    R9,RF,0(R9)              RESTORE REGS           @ZA03651
         BR    WRKR6                    GET OUT                  YM7873
*                                                                YM7873
REPIO05K EQU   *                        CHK FOR CARD READER      YM7873
         CLI   UCBTBYT4,RDR2501         TEST 2501                A46670
         BE    REPIO051                 PROCESS IF 2501          A46670
         CLI   UCBTBYT4,RDR2540         TEST 2540R               A46670
         BCR   7,WRKR6                  SKIP IF NEITHER          A46670
*
REPIO051 EQU   *                        PROCESS 2501 OR 2540R    A46670
*
         TM    DCBCIND2,QSAMDCB         IS IT QSAM DCB
         BCR   8,WRKR6                  NO - GO GET NEXT ENTRY   A30921
*
*    *** PICK UP USER IOB ADDRESS ***
*
         L     WRKR3,DCBIOBA            GET FIRST IOB ADDRESS
         LR    DSCNTR,WRKR3             SAVE FOR WORK REGISTER
*
         XC    DCBEOBAD(8),DCBEOBAD     FORCE END OF BUFFER CONDITION
*
         SR    TIOTR,TIOTR              CLEAR WORK REGISTER
         IC    TIOTR,DCBBUFNO           GET NUMBER OF BUFFERS TO PRIME
*
         LR    WRKR5,DEBR          GET DEB ADDR FOR CALC         Y02049
         LA    WRKR2,DEBPRFLN      GET LENGTH OF DEB PREFIX      Y02049
         SR    WRKR5,WRKR2         POINT TO DEB PREFIX           Y02049
         USING DEBPREFX,WRKR5                                    Y02049
         L     WRKR5,DEBXTNP       GET POINTER TO DEB EXTENSION  Y02049
         USING DEBXTN,WRKR5                                      Y02049
         TM    DEBXMCF1,DCBMRMVG+DCBMRSBG                        Y02049
*                                  IS IT MOVE OR SUBSTITUTE MODE Y02049
         DROP  WRKR5                                             Y02049
         BC    5,REPIO060               BRANCH IF YES TO GET NEXT IOB
*                                            FOR PRIMING
         BCTR  TIOTR,0                  REDUCE BY ONE
         LTR   TIOTR,TIOTR              MORE THAN ONE IOB
         BZ    REPIO080                 BRANCH IF NOT TO FINISH THIS
*                                            ENTRY
REPIO060 EQU   *
*
*    *** PRIMING BUFFERS - WILL BRANCH AND LINK TO EOB ROUTINE ***
*
         L     WRKR3,0(WRKR3)           GET NEXT IOB
*
         LA    WRKR4,RSJFCB             GET REGISTER SAVE AREA
*
REPIO070 EQU   *
*
         STM   WRKR1,WRKR6,0(WRKR4)     SAVE REGISTERS
*
         L     WRKR6,DCBEOB             GET READ EOB ADDRESS
         ST    WRKR3,DCBIOBA            MAKE DCB POINT TO IOB    A30921
         TM    IOBSTAT0(WRKR3),UNITEX   CHECK FOR UNIT EXCEPTION A30921
         BNZ   REPIO075                 YES, DON'T PRIME BUFFERS A30921
         LR    BASER,WRKR3              PUT IOB IN PROPER REG    A30921
         BALR  WRKR5,WRKR6              TO EOB ROUTINE TO PRIME BUFFERS
*
REPIO075 EQU   *                                                 A30921
*
         LM    WRKR1,WRKR6,0(WRKR4)     RESTORE REGISTERS
*
         L     WRKR3,0(WRKR3)           GET NEXT IOB
         BCT   TIOTR,REPIO070           BRANCH IF MORE BUFFERS TO PRIME
*
         ST    DSCNTR,DCBIOBA           RESET DCB TO ORIGINAL IOB
*
REPIO080 EQU   *
*
         NI    DCBIOBA,X'FE'            TURN OFF FIRST IOB BIT IN DCB
         MVI   DEBUSRPG+3,NTPRGD        SET NO PURGED BYTE IN DEB TO
*                                           SKIP RESTORING
         B     REPIO100                 BRANCH TO GET NEXT ENTRY
*
REPIO100 EQU   *
*
         TM    TABFLG1,TABLAST          IS THIS LAST ENTRY
         BO    REPIO160                 BRANCH IF YES TO XCTL NEXT LOAD
*
         LA    CTABLR,TABENT(CTABLR)    INCREMENT TABL PTR
         B     REPIO002                 BRANCH TO PICK UP NEXT ENTRY
*
REPIO160 EQU   *
         LA    WRKR6,RSJFCB             SET UP WORKAREA FOR XCTL YM7577
         MVC   0(8,WRKR6),DALOADNM      GO TO N05B               YM7577
*
         LA    WRKR6,8(0,WRKR6)         SET UP FOR XCTL
         LR    WRKR2,BUFR               POINT REG 1 TO THE RESTART W/A
         LA    WRKR5,RSJFCB             REG14 PTS TO XCTL LIST   YM7582
         LR    WRKR3,BUFR               POINT REG12 TO RSTRT WA  YM5319
*
         XCTL  EPLOC=(WRKR5),SF=(E,(15))
*
***********************************************************************
*
*     CONSTANTS USED IN REPI/O
*
***********************************************************************
*
         DS    0H
AGRTR32K DC    X'8000'                  VALUE IN LABEL WHEN LRECL GTR
*                                            32K
ARECFM   EQU   *-1                      RECFM TRANSLATE TABEL (NO ZERO
         DC    C'VFU'                        VALUE VARIABLE, FIXED,
*                                            UNDEFINED)
ATRTCH   DC    C' CET'                  BLANK, CONVERSION, EVEN PARITY,
*                                            TRANSLATE
ABLKA    DC    C' SBR'                  BLOCK ATTRIBUTE TRANSLATE TABLE
ABFALN   DC    C'0C8C'                  TRANSLATE TABLE FOR BFALN
AHDR1    DC    C'HDR1'                  HEADER 1
DALOADNM DC    CL8'IGC0N05B'            DA LOAD
RSTMODNM DC    CL8'IGC0V05B'            RESTART EXIT
*                                                                     *
*        MAINTENANCE SPACE                                       Y01909
*                                                                     *
PATCH    DC    XL((*-IGC0L05B)/20)'00'   FIVE PERCENT SPACE      Y01909
*                                                                     *
         EJECT
         IEEVRSWA VER=3                 AOS2 VERSION OF WA
         EJECT
RSDSECT  DSECT
*
*    DATA EXTENT BLOCK
*
RSDEB    DS    0CL48
RSDEBTCB DC    X'00000000'              TCB ADDRESS
RSDEBDEB DC    X'00000000'              DEB ADDRESS
*
*    DATA CONTROL BLOCK
*
RSDEBOFL DS    0CL1
RSDEBIRB DC    X'00000000'              IRB ADDRESS
RSDEBSYS DC    X'00000000'              1ST IOB ADDR IN SYS.PURGE CHAIN
RSDEBUSR DC    X'00000000'              1ST IOB ADDR IN USER PRGE CHAIN
RSDEBECB DC    X'00000000'              PARM.LIST ADDR TO PURGE ECB
RSDEBID  DS    0CL1
RSDEBDCB DC    AL4(RSDCBREP)            DCB ADDR
RSDEBAPP DS    CL4                      I/O APPENDAGE VECTOR TABLE
RSDEBMOD DS    0CL1
RSDEBUCB DS    F                        UCB ADDRESS
RSDEBBIN DS    H                        BIN NUMBER
RSDEBSCC DS    H                        CYLINDER START ADDRESS
RSDEBSHH DS    H                        TRACK START ADDRESS
RSDEBECC DS    H                        CYLINDER END ADDRESS
RSDEBEHH DS    H                        TRACK END ADDRESS
RSDEBNTR DS    H                        NO. OF TRACKS IN THIS EXTENT
*
*    EVENT CONTROL BLOCK
*
RSECBAD  DS    CL4                      ECB ADDRESS
RSDCBDEB DC    AL4(RSDEB)               DEB ADDRESS
*
RSDCBREP EQU   RSDEB+8                  POINT TO RELATIVE BEGINNING DCB
*
*    INPUT OUTPUT BLOCK
*
RSIOB    DS    0CL40
RSIOBFG1 DC    X'00'                    I/O FLAGS BYTE 1
RSIOBFG2 DC    X'00'                    I/O FLAGS BYTE 2
RSIOBSN1 DS    CL1                      FIRST SENSE BYTE
RSIOBSN2 DS    CL1                      SECOND SENSE BYTE
RSIOBECB DC    AL4(RSECBAD)             ECB ADDRESS
RSIOBCSW DS    D                        LAST CSW
RSIOBCPA DS    AL4(RSCCW1)              CHANNEL PROGRAM ADDRESS
RSIOBDCB DS    AL4(RSDCBREP)            DCB ADDRESS
RSIOBRCP DS    CL4                      RESTART ADDRESS FOR IOS
RSIOBINC DS    H                        INCREMENT BLOCK COUNT
RSIOBECT DS    H                        NO. OF ERROR ENTRIES
RSIOBDAD DS    D                        SEEK ADDRESS FOR DA
*
*    CHANNEL PROGRAM
*
RSCCWLST DS    0CL24
RSCCW1   DS    D                        CCW1
RSCCW2   DS    D                        CCW2
RSCCW3   DS    D                        CCW3
*
*    BUFFER SEGMENT FOR EXTERNAL BLOCKS
*
RSBUFSEG DS    0CL176
RSJFCB   EQU   RSBUFSEG
*
***********************************************************************
*
*                  JFCB
*
***********************************************************************
*
JFCBDSNM DS    CL44                         DATA SET NAME
JFCBELNM DS    CL8                          ELEMENT NAME OR VERSION
JFCBTSDM DS    CL1                          TASK SCHEDULER - DATA
*                                            MANAGEMENT INTERFACE BYTE
JFCBSYSC DS    CL13                         SYSTEM CODE
JFCBLTYP DS    CL1                          LABEL TYPE AND USER'S-LABEL
*                                            INDICATOR
         DS    CL1                          NOT USED
JFCBFLSQ DS    CL2                          FILE SEQUENCE NUMBER
JFCBVLSQ DS    CL2                          VOLUME SEQUENCE NUMBER
JFCBMASK DS    CL8                          DATA MANAGEMENT MASK
JFCBCRDT DS    CL3                          DATA SET CREATION DATE
JFCBXPDT DS    CL3                          DATA SET EXPIRATION DATE
JFCBIND1 DS    CL1                          INDICATOR BYTE 1
JFCBRLSE EQU   64                            BITS 0 AND 1 - EXTERNAL
*                                            STORAGE RELEASE INDICATOR
JFCBLOCT EQU   16                            BITS 2 AND 3 - DATA SET
*                                            HAS BEEN LOCATED
JFCBNEWV EQU   4                             BITS 4 AND 5 - NEW VOLUME
*                                            ADDED TO DATA SET
JFCBPMEM EQU   1                             BITS 6 AND 7 - DATA SET IS
*                                            A MEMBER OF A PODS OR GDG
JFCBIND2 DS    CL1                          INDICATOR BYTE 2
JFCBSTAT EQU   64                            BITS 0 AND 1 - DATA SET
*                                            STATUS (NEW, OLD, OR MOD)
JFCBSCTY EQU   16                            BITS 2 AND 3 - DATA SET
*                                            SECURITY INDICATOR
JFCBUFNO DS    0AL1
JFCBUFRQ DS    AL1                      BUFFER REQUESTED
JFCBFTEK DS    0BL1
JFCBFALN DS    BL1                      BUFFER ALIGNMENT
JFCBUFL  DS    AL2                      BUFFER LENGTH
JFCEROPT DS    BL1                      ERROR ROUTINE
JFCTRTCH DS    0BL1
JFCKEYLE DS    0AL1
JFCMODE  DS    0BL1
JFCCODE  DS    0BL1
JFCSTACK DS    0BL1
JFCPRTSP DS    BL1                      PRINTER SPACING
JFCDEN   DS    BL1                      TAPE DENSITY
JFCLIMCT DS    AL3                      NO. OF TRKS.TO BE SEARCHED
JFCDSORG DS    BL2                      DATA SET ORGANIZATION
JFCRECFM DS    BL1                      RECORD FORMAT
JFCOPTCD DS    BL1                      OPTION CODES
JFCBLKSI DS    AL2                      BLOCK SIZE
JFCLRECL DS    AL2                      LOGICAL RECORD LENGTH
JFCNCP   DS    AL1                      NO. OF CHANNEL PROGRAM
JFCNTM   DS    AL1                      NO. OF TRKS. FOR MASTER INDEX
JFCRKP   DS    AL2                      RELATIVE POSITION OF KEY
JFCCYLOF DS    AL1                      NO. OF TRKS FOR OVERFLOW
JFCDBUFN DS    AL1                      RESERVED
JFCINTVL DS    AL1                      INTENTIONAL INTERVAL
JFCCPRI  DS    BL1                      PRIORITY
JFCSOWA  DS    AL2                      SIZE OF WORKAREA
JFCBNTCS DS    CL1                          NUMBER OF OVERFLOW TRACKS
JFCBNVOL DS    CL1                          NUMBER OF VOLUME SERIAL
*                                            NUMBERS
JFCBVOLS DS    CL30                         VOLUME SERIAL NUMBERS (THE
*                                            FIRST FIVE)
JFCBEXTL DS    CL1                          LENGTH OF BLOCK OF EXTRA
*                                            VOLUME SERIAL NUMBERS
*                                            (BEYOND FIVE)
JFCBEXAD DS    CL3                          TRACK ADDRESS OF BLOCK OF
*                                            EXTRA VOL SERIAL NUMBERS
JFCBPQTY DS    CL3                          PRIMARY QUANTITY OF D.A.
*                                            STORAGE REQUIRED
JFCBCTRI DS    CL1                          INDICATES WHETHER CYLINDERS
*                                            TRACKS, OR RECORDS ARE
*                                            SPECIFIED IN JFCBPQTY AND
*                                            JFCBSQTY
JFCBSQTY DS    CL3                          SECONDARY QUANTITY OF D.A.
*                                            STORAGE REQUIRED
JFCBIND3 DS    CL1                          INDICATOR BYTE 3
JFCBCNTG EQU   64                            BITS 0 AND 1 - CONTIGUOUS
*                                            STORAGE INDICATOR
JFCBMXIG EQU   16                            BITS 2 AND 3 - MAXIMUM
*                                            AVAILABLE EXTENT INDICATOR
JFCBALXI EQU   4                             BITS 4 AND 5 - ALL EXTENTS
*                                            INDICATOR
JFCBRNDC EQU   1                             BITS 6 AND 7 - ROUND
*                                            CYLINDER INDICATOR
JFCBDQTY DS    CL3                          QUANTITY OF D.A. STORAGE
*                                            REQUIRED FOR A DIRECTORY
JFCBSPNM DS    CL3                          CORE ADDRESS OF THE JFCB
*                                            WITH WHICH CYLINDERS ARE
*                                            SPLIT
JFCBABST DS    CL2                          RELATIVE ADDRESS OF FIRST
*                                            TRACK TO BE ALLOCATED
JFCBSBNM DS    CL3                          CORE ADDRESS OF THE JFCB
*                                            FROM WHICH SPACE IS TO BE
*                                            SUBALLOCATED
JFCBDRLH DS    CL3                          AVERAGE DATA RECORD LENGTH
JFCBVLCT DS    CL1                          VOLUME COUNT
JFCBSPTN DS    CL1                          NUMBER OF TRACKS PER
*                                            CYLINDER TO BE USED BY
*                                            THIS DATA SET WHEN SPLIT
*                                            CYLINDER IS INDICATED
JFCBLGTH EQU   176                          LENGTH OF JFCB
JFCBEND  EQU   *
         ORG   RSBUFSEG+176             ORG BACK TO BUFFER SEGMENT
RSSTATUS DS    0CL8
RSSTAT1  DS    CL1                      STATUS BYTE 1
RSSTAT2  DS    CL1                      STATUS BYTE 2
RSSTAT3  DS    CL1                      STATUS BYTE 3
RSSTAT4  DS    CL1                      STATUS BYTE 4
         DS    F                        RESERVED FOR FUTURE USE
         EJECT
CVT      DSECT
         CVT
         EJECT
*
*
*   TASK CONTROL BLOCK
*
*
         CNOP  0,4
TCB      DSECT
TCBRBP   DS    F                       POINTER TO EXECUTING RB
TCBPIE   DS    F                       POINTER TO PROG. INTERRUPT EL.
TCBDEB   DS    F                       POINTER TO DEB QUEUE
TCBTIO   DS    F                       POINTER TO TIOT
TCBCMP   DS    F                       TASK COMPLETION CODE
TCBTRN   DS    F                       TESTRAN FIELD
TCBMSS   DS    F                       DATA BLOCK QUEUE ELEMENT POINTER
TCBPRTK  DS    CL1                     PROTECTION TAG
TCBFLGS  DS    FL5                     FLAG BYTES
TCBLMP   DS    CL1                     LIMIT PRIORITY
TCBDSP   DS    CL1                     DISPATCHING PRIORITY
TCBLLS   DS    F                       POINTER TO LOAD LIST
TCBJLB   DS    F                       POINTER TO JOBLIB DCB
TCBJSE   DS    F                       LIST OF INACTIVE PROGRAMS ADDR.
TCBGRS   DS    CL64                    GENERAL REGISTER SAVE AREA
TCBIDF   DS    CL1                     TCB IDENTIFIER FIELD
TCBFSA   DS    CL3                     POINTER TO 1ST PROB.PROG.SAVEAR
TCBTCB   DS    F                       NEXT TCB ON READY QUEUE POINTER
TCBTME   DS    F                       POINTER TO TIMER ELEMENT
         EJECT
         DCBD  DSORG=PS
         EJECT
*
*
*   DATA EXTENT BLOCK
*
*
         IEZDEB                                                  Y02049
         EJECT
         IHJDSTAB                                                Y02076
         EJECT
TIOT     DSECT
         IEFTIOT1
         EJECT
SRT      DSECT
         IEFUCBOB
         EJECT
***********************************************************************
*
*        FILE LABELS
*
***********************************************************************
*
*        FILE LABEL 1
*
         CNOP  0,4
FILELAB  DSECT
FL1LABI  DS    CL3                      LABEL IDENTIFIER
FL1NO    DS    CL1                      FILE LABEL NUMBER
FL1ID    DS    CL17                     FILE IDENTIFIER
FL1FILSR DS    CL6                      FILE SERIAL NUMBER
FL1VOLSQ DS    CL4                      VOLUME SEQUENCE NUMBER
FL1FILSQ DS    CL4                      FILE SEQUENCE NUMBER
FL1GNO   DS    CL4                      GENERATION NUMBER
FL1VNG   DS    CL2                      VERSION NUMBER OF GENERATION
FL1CREDT DS    CL6                      CREATION DATE
FL1EXPDT DS    CL6                      EXPIRATION DATE
FL1FSEC  DC    C'0'                     FILE SECURITY INDICATOR
FL1BLKCT DS    CL6                      BLOCK COUNT
FL1SYSCD DS    CL13                     SYSTEM CODE
FL1RES   DS    0CL7                     RESERVED FOR FUTURE USE
         DS    CL1                      RESERVED
FL1RES1  DS    CL6                      RESERVED
*
*        FILE LABEL 2
*
         ORG   FL1ID                    ORG BACK TO FILE LABEL 1
FL2RECFM DS    CL1                      RECORD FORMAT
FL2BLKL  DS    CL5                      BLOCK LENGTH
FL2LRECL DS    CL5                      BLOCKING FACTOR/RECORD LENGTH
FL2DEN   DS    CL1                      DENSITY
FL2FILP  DS    CL1                      FILE POSITION
FL2JSID  DS    0CL17                    JOB/STEP IDENTIFICATION
FL2JOBD  DS    CL8                      JOB IDENTIFICATION
FL2JSSP  DC    C'/'                     SLASH
FL2STEPD DS    CL8                      STEP IDENTIFICATION
FL2TRTCH DS    CL2                      TAPE RECORDING TECHNIQUE
FL2CNTRL DS    CL1                      CARRIAGE CONTROL CHARACTER
FL2RES   DS    CL43                     RESERVED FOR FUTURE USE
FL2BFALN EQU   FL2RES                   BUFFER ALIGNMENT
FL2BLKA  EQU   FL2RES+1                 BLOCK ATTRIBUTE
*
*
*
         END
