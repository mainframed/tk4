*
         COPY  LCGASMSW
*
 TITLE 'IGC0006C (IHJACP00) CHECKPOINT HOUSEKEEPING ROUTINE- 1ST LOAD'
***********************************************************************
*STATUS CHANGE LEVEL 000
*
*FUNCTION/OPERATION:
*   1.) IF CHKPT IS TO BE SUPPRESSED(I.E.,IF RD=NC OR RD=RNC WAS SPEC-
*       IFIED ON JOB OR EXEC CARD), THIS ROUTINE WILL PLACE A RETURN
*       CODE OF X'00' IN REGISTER 15 AND RETURN CONTROL TO USER.
*   2.) IF 'CANCEL' REQUEST WAS SPECIFIED BY CHKPT MACRO
*       A.) A GETMAIN IS ISSUED FOR A WORK AREA IN SUBPOOL 253 OF
*           LSQA. THE WORK AREA IS INITIALIZED, THE ADDRESS OF THE
*           WORK AREA IS PLACED IN REGISTER 1 AND XCTL TO IGC0206C.
*       B.) IF GETMAIN WAS UNSUCCESSFUL A RETURN CODE OF X'08' IS
*           PLACED IN THE SVRB EXTENDED SAVE AREA AND CONTROL IS TRANS-
*           FERED TO CHKPT EXIT ROUTINE, IGC0Q06C.
*   3.) FOR NORMAL PROCESSING OF CHKPT THIS ROUTINE PERFORMS THE
*       FOLLOWING:
*        *  DETERMINES WHETHER THE CHKPT PARAMETER LIST IS VALID. IF
*           IT IS INVALID, THIS ROUTINE PUTS A RETURN CODE OF X'08'
*           IN THE SVRB EXTENDED SAVE AREA AND XCTLS TO IGC0Q06C.
*
*        *  DETERMINES WHETHER THE CHKPT DATA SET IS OPEN. IF IT IS
*           NOT OPEN, THIS ROUTINE WILL OPEN THE DATA SET. IF OPEN WAS
*           UNSUCCESSFUL, EITHER AN I/O OR CHKPT DD ERROR OCCURRED AND
*           A RETURN CODE OF X'0C' IS PLACED IN THE SVRB EXTENDED SAVE
*           AREA AND THIS ROUTINE XCTLS TO IGC0Q06C.
*
*        *  TESTS IF CHKPT=EOV SPECIFIED ON CHKPT DD AND, IF ON,
*           TURNS IT OFF VIA QMNGRIO FUNCTION.
*
*        *  COMPUTES THE SIZE OF THE CHKPT WORK AREA.
*
*        *  GETS CORE FOR THE WORK AREA FROM SUBPOOL 253 IF POSSIBLE.
*           IF THERE IS NOT ENOUGH SPACE, A RETURN CODE OF X'08' IS
*           PLACED IN THE SVRB EXTENDED SAVE AREA AND THIS ROUTINE
*           XCTLS TO IGC0Q06C.
*
*        *  FILLS IN THE FIRST PART OF THE WORK AREA WITH
*           INFORMATION NEEDED BY OTHER CHKPT MODULES.
*   4.) FOR CHECKPOINT DATA SET SECURITY THIS ROUTINE PERFORMS THE
*       FOLLOWING OPERATIONS:
*        *  IF THE DATA SET IS KNOWN TO BE SECURE, THE MODULE TESTS
*           IF DATA SET IS ON A SHARED DEVICE.  IF SO, ERROR CODE
*           214 IS SET.  IF ANOTHER DCB OF NON CHECKPOINT STATUS IS
*           CONCURRENTLY OPEN TO THE CHECKPOINT DATA SET, ERROR CODE
*           209 IS SET.  IF THE DATA SET IS NOT ENQUEUED EXCLUSIVE,
*           ERROR CODE 210 IS SET.
*        *  IF THE DATA SET IS NOT KNOWN TO BE SECURE, THE MODULE SETS
*           ERROR CODE 016 IF IT IS NOT TAPE OR DA.  IF IT IS TAPE
*           AND BLOCK COUNT IS NOT 0, ERROR CODE 208 IS SET.  THE
*           MODULE THEN CLOSES AND REOPENS THE CHECKPOINT DATA SET
*           TO ALLOW OPEN TO GET A SECURE VOLUME.  IF A SECURE
*           VOLUME IS NOT OPENED, ERROR CODE 211 IS SET.
*        *  IF A SECURITY INTERFACE WAS SET UP FOR AN UNAUTHORIZED
*           USER, THIS MODULE UPDATES THE DCB ADDRESS IN THE CHECK-
*           POINT WORK AREA.
*
*ENTRY POINTS: IGC0006C
*
*INPUT:  ADDRESS OF PARAMETER LIST(SHOWN BELOW) IN REGISTER 1.
*   AL2(FLAG)    FLAG EQUALS X'80' IF NO CHECKID WAS SPECIFIED BY THE
*                USER AND THE GENERATED CHECKID ADDRESS IS NOT TO BE
*                RETURNED TO THE USER. FLAG EQUALS X'00' IF CHECKID WAS
*                SPECIFIED.
*   AL3(DCBORCN) ADDRESS OF CHKPT DCB.
*   AL1(LENGTH)  LENGTH OF USER SPECIFIED CHECKID. IF THIS BYTE EQUALS
*                X'FF', THIS INDICATES THAT THE USER WANTS THE SYSTEM
*                TO GENERATE A CHECKID AND RETURN IT AT THE LOCATION
*                SPECIFIED BY IDADDR.
*   AL3(IDADDR)   IF USER SUPPLIES THE CHECKID, THIS FIELD WILL POINT
*                TO THAT CHECKID. IF THE SYSTEM IS TO SUPPLY THE
*                CHECKID, THIS FIELD POINTS TO AN 8 BYTE AREA WHERE
*                THE GENERATED CHECKID WILL BE RETURNED.
*   IF 'CANCEL' WAS SPECIFIED IN THE CHKPT MACRO, REGISTER 1 WILL CON-
*   TAIN ALL ZEROS.
*
*OUTPUT:  A RETURN CODE AND MESSAGE CODE WILL BE PLACED IN THE SVRB
*   EXTENDED SAVE AREA IF AN ERROR EXISTS.
*   1.) IF THE USER REQUESTS THAT CHKPTS BE SUPPRESSED, A RETURN CODE
*       OF X'00' IS GIVEN.
*   2.) IF THIS ROUTINE WAS UNSUCCESSFUL IN OPENING CHKPT DATA SET
*       THEN A RETURN CODE OF X'0C' WILL BE GIVEN.
*   3.) IF A WORK AREA OF THE PROPER SIZE COULD NOT BE GETMAIN'D,
*       OR IF THE PARAMETER LIST CONTAINS AN INVALID ADDRESS,
*       OR IF SECURITY/INTEGRITY ERRORS WERE DETECTED,
*       THEN A RETURN CODE OF X'08' IS GIVEN.
*   4.) IF NORMAL CHKPT PROCESSING IS TO CONTINUE, THE ADDRESS OF THE
*       CHKPT WORK AREA WILL BE PLACED IN REGISTER 1.
*   ONE OF THE FOLLOWING MESSAGE CODES IS PLACED IN THE SVRB EXTENDED
*   SAVE AREA TO BE PRINTED IN THE CONSOLE MESSAGE BY THE CHKPT EXIT
*   ROUTINE IF AN ERROR OCCURS:
*        DEC VALUE  MNEMONIC                 MEANING
*            1      CKERPG1        PARAMETER LIST ERROR
*            2      CKERPG2        OPEN UNSUCCESSFUL
*            3      CKERPG3        GETMAIN UNSUCCESSFUL
*           11      CKERPG11       MULTITASKING (MOTHER TASK)
*           12      CKERPG12       MULTITASKING (DAUGHTER TASK)
*           16      CKERPG16       CHKPT DATA SET NOT ON TAPE OR DA
*           19      CKERPG19       USER OPENED CHKPT D/S FOR INPUT
*          202      CKERP202       LOCAL LOCK NOT OBTAINED
*          208      CKERP208       TAPE BLOCK COUNT NOT 0
*          209      CKERP209       CONCURRENT OPEN TO CHKPT D/S
*          210      CKERP210       CHKPT D/S NOT EXCLUSIVE ENQ'D
*          211      CKERP211       OPEN DID NOT FIND SECURE CHKPT D/S
*          214      CKERP214       CHKPT DATA SET ON SHARED DEVICE
*   IF ANY ERROR OCCURS, THIS ROUTINE XCTL'S TO IGC0Q06C WITH THE
*   FOLLOWING INPUT:
*   REGISTER 1 - CONTAINS ALL ZEROS.
*   REGISTER 5 - ADDRESS SVRB
*   ONE BYTE OF FLAGS IN SVRB EXTENDED SAVE AREA TO DETERMINE WHAT
*   ACTION IS TO BE TAKEN BY CER AND WHAT MESSAGE IS TO BE WRITTEN.
*  ACTION IS TO BE TAKEN BY CER, RETURN CODE AND MESSAGE CODE IN
*  BYTES 0-3, TIOT ADDRESS IN BYTES 33-35 AND DCB ADDRESS IN BYTES
*  37-39 (IF A BAD PARAMETER LIST IS PASSED BYTES 37-39 WILL BE ZERO).
*
*EXTERNAL ROUTINES:  IEAOVLOO,01 - ADDRESSES OF VALIDITY CHECK ROUTINES
*                    QMNGRIO - FOR READING CONTROL BLOCKS FROM SWA
*                    SETLOCK - TO GET LOCAL LOCK
*
*SVCS USED  GETMAIN, FREEMAIN, OPEN, XCTL, SVC3, CLOSE, ENQ, EXCP, WAIT
*
*EXITS-NORMAL:
*   1.) XCTL TO IGC0106C IF NORMAL PROCESSING OF CHKPT CAN CONTINUE.
*   2.) XCTL TO IGC0206C IF 'CANCEL' WAS SPECIFIED.
*   3.) RETURN TO USER IF CHKPT'S ARE TO BE SUPPRESSED.
*
*EXITS-ERROR:
*   1.) XCTL TO IGC0Q06C IF THIS ROUTINE WAS UNABLE TO:
*       A.) GETMAIN FOR THE WORK AREA
*       B.) OPEN THE CHKPT DATA SET.
*       C.) VALIDATE THE ADDRESSES OF THE PARAMETER LIST.
*       D.) VERIFY THAT THE CHECKPOINT DATA SET WAS SECURE.
*
*TABLES/WORKAREAS CREATED:  CHKWA IS THE FIRST PART OF THE CHKPT WORK
*   AREA. THIS ROUTINE FILLS IN THE WORK AREA WITH INFORMATION THAT IS
*   NEEDED BY OTHER CHKPT ROUTINES.
*
*TABLES/WORKAREAS REFERENCED  CVT, TCB, TIOT, SVRB, DCB, DEB, PQE,
*                ASCB, DSAB, UCB, SIOT, JFCB, JESCT, VDSCB, DSL1
*
*ATTRIBUTES:  REENTRANT, SUPERVISOR STATE, TYPE 4 SVC ROUTINE.
*
*RESTRICTIONS ON THE USE OF THIS MODULE:  VS/2-2 ONLY
*
*NOTES:  NONE
* CHANGES FOR VS2 3.0
*A27284900-27289800                                           @ZA11608
*A201503-201530,C250002                                       @ZA25063
*A348943-349183,349303                                        @ZA27494
*C264427,A393143                                              @ZA30032
*ZZZ                                                          @ZA99999
*ZZZ                                                          @ZA99999
*C267829-267929,C275410-275417                                @ZA31342
***********************************************************************
         EJECT
IGC0006C CSECT
         USING CVTMAP,R3           ADDRESS CVT.                  Y02083
         USING TCB,R4              ADDRESS TCB.
         USING TIOTSECT,R2         ADDRESS TIOT.
         USING HKPSECT,R5          ADDRESS CHKPT SVRB
         BALR  RB,0                BASE ADDR
         USING *,RB                ADDRESS MODULE.
BASE1    B     BEGIN               BRANCH AROUND EYE CATCHER     YM7008
         CNOP  0,8                 ALIGN TO A DOUBLE WORD BOUNDARY
         DC    CL8'IGC0006C'       MODULE NAME                 @ZA30032
         DC    CL8'@ZA31342'       LAST CODE SHIPPED           @ZA30032
         DC    CL8'&SYSDATE'       LAST DATE SHIPPED           @ZA30032
BEGIN    L     R2,TCBTIO           GET ADDR OF TIOT.
         L     R3,ADRCVT(R0,R0)    GET ADDR OF CVT               Y02083
*                                                                     *
***********************************************************************
*                                                                     *
*        IF CHECKPOINTS ARE TO BE SUPPRESSED, RETURN TO USER.         *
*                                                                     *
***********************************************************************
*                                                                     *
         TM    TCBTRN,TCBNOCHK     ARE CHKPTS TO BE SUPPRESSED
         BO    CHKSUP              YES-GO RETURN TO USER.
         XC    HKPGML2(LNXSA),HKPGML2 CLEAR SVRB EXTENDED SAVEAREA
         ST    R2,HKPTIO           SAVE TIOT ADDRESS
*                                                                     *
***********************************************************************
*                                                                     *
*        IF CANCEL WAS REQUESTED, BRANCH TO CANCEL ROUTINE TO GETMAIN *
*        A WORK AREA.                                                 *
*                                                                     *
***********************************************************************
*                                                                     *
CHKSM    LTR   R1,R1               WAS CANCEL REQUESTED
         BZ    CHKCANC1            YES-GO TO CANCEL ROUTINE.
*                                                                     *
***********************************************************************
*                                                                     *
*        CHECK PARAMETER LIST ADDRESSES FOR BEING WITHIN P/P CORE AND *
*        DCB ADDRESS FOR BEING ON FULL WORD BOUNDARY.                 *
*                                                                     *
***********************************************************************
*                                                                     *
CHKPARM  LR    R7,RB               SAVE BASE REG                 Y02076
         SETLOCK OBTAIN,TYPE=LOCAL,MODE=UNCOND, GET LOCAL LOCK   Y02076X
               RELATED=(LOCAL,IGC0006C(CHKPARM4))                Y02076
         LR    RB,R7               RESTORE BASE REG              Y02076
         LTR   RD,RD               SETLOCK ERROR                 Y02076
         LA    RA,CKERP202         ANTICIPATE SETLOCK ERROR      Y02076
         BNZ   CHKERX              IF SETLOCK ERROR, BR          Y02076
         LR    RC,R1               RELOCATE PARM ADDR            YM5767
         USING PARMLST,RC          ADDR PARAMETER LIST           YM5767
         LA    R1,K0(RC)           CLEAR HI ORDER BYTE           YM5767
         LA    R2,K7(RC)           END ADR=BEG ADR + 7           YM5767
         L     RF,CVT0VL01         GET ADR OF VALID.RTN01        Y02076
         SR    RA,RA               CLEAR ERROR REG               Y02076
         BALR  RE,RF               PERFORM VALIDITY CHECK        Y02076
         BNZ   CHKPARM2            TO ERROR-IF INVALID ADR       Y02076
         ICM   R1,M7,DCBADDR+K1    GET USER DCB ADDR             YM5767
         LA    R2,K3(R1)           CHECK 1ST DCB WORD            YM5767
         L     RF,CVT0VL01         GET EPA OF CHECK RTN          YM5767
         BALR  RE,RF               GO CHECK DCB ADDR             YM5767
         BNZ   CHKPARM2            TO ERROR EXIT IF INVALID      YM5767
         CLI   FLAG,NOCHKID        CHECKID SPECIFIED-
         BE    CHKPARM3            NO, SKIP NEXT TEST            Y02083
         L     R1,IDADDR           GET ADR OF CHECKID            Y02076
         LA    R1,K0(R1)           CLEAR HI ORDER BYTE           Y02076
         LA    R2,SYSGENID(R1)     ANTICIPATE SYSGENED ID        YM5793
         CLI   IDADDR,X'FF'        DOES USER WANT THE SYSTEM     YM5793
*                                  GENERATED CKID RETURNED       YM5793
         BE    CHKPARM1            BR IF YES (LENGTH=8)          YM5793
         SR    R2,R2               CLEAR WORK REG                YM5793
         ICM   R2,M1,IDADDR        GET USER CKID LENGTH          YM5793
         BZ    CHKPARM3            LET IGC0106C FIND A           YM5793
*                                  ZERO LENGTH ERROR             YM5793
         LA    R2,0(R1,R2)         END ADDRESS = R1 + USER       YM5793
         BCTR  R2,0                CKID LNGTH - 1                YM5793
CHKPARM1 EQU   *                   CALL VL00 CHECK RTN           YM5793
         L     RF,CVT0VL00         ADR OF VALID.RTN 00           Y02076
         BALR  RE,RF               PERFORM VALIDITY CHECK        Y02076
         BZ    CHKPARM3            BR IF NO ERROR                Y02076
CHKPARM2 LA    RA,CKERPG1          SET ERROR REG                 Y02076
CHKPARM3 LR    R1,RC               RESTORE REG 1                 Y02076
         DROP  RC                  PARM LIST ADDRESSABILITY      YM5767
         USING PARMLST,R1          ADDR PARM LIST OFF R1         YM5767
         LR    R7,RB               SAVE BASE REG                 Y02076
CHKPARM4 SETLOCK RELEASE,TYPE=LOCAL,                             Y02076X
               RELATED=(LOCAL,IGC0006C(CHKPARM1))                Y02076
         LR    RB,R7               RESTORE BASE REG              Y02076
         LTR   RA,RA               VALCHK ERROR                  Y02076
         BNZ   CHKERX              IF ERROR, BR ERROR EXIT       Y02076
         LR    RD,R1               SAVE PARM REG                 Y02083
         L     RC,DCBADDR          GET CHKPT DCB ADDRESS         Y02083
         USING IHADCB,RC           ADDRESS CHKPT DCB             Y02083
         DROP  R1                  PARM LIST ADDRESSABILITY      Y02083
         EJECT
***********************************************************************
*                                                                     *
*        COMPUTE SIZE OF WORK AREA NEEDED BY RESTART                  *
*                                                                     *
***********************************************************************
*                                                                     *
         LA    R7,TIOLEN           INIT TIOT-LENGTH-ACCUMULATOR  Y02083
         XR    R6,R6               CLEAR WORK REG                Y02083
         L     R2,HKPTIO           GET TIOT ADDRESS              Y02083
         LA    R9,TIOENTRY         PNT TO 1ST TIOT ENTRY         Y02083
         USING TIOENTRY,R9         ADDRESS DD ENTRY IN TIOT
CHKETI   NC    TIOELNGH(TIOEDDNM-TIOENTRY),TIOELNGH IS THIS THE
*                                  END OF THE TIOT
         BNZ   CHKTIL              NO-GO TO GET ADDR OF NEXT DD ENTRY.
         STH   R7,HKPTIOTL         SAVE LENGTH OF TIOT.
*                                                                     *
***********************************************************************
*                                                                     *
*        LENGTH OF TIOT + LENGTH OF FIXED PART OF RESTART WORK AREA + *
*        LENGTH OF BASIC PART OF REPDCB'S WORK AREA                   *
*                                                                     *
***********************************************************************
*                                                                     *
         AH    R7,WAK              ADD LENGTH OF FIXED WA        Y02076
*                                                                     *
***********************************************************************
*                                                                     *
*        INCREASE WORK AREA SIZE FOR REPDCB ACCORDING TO THE NUMBER   *
*        OF OPEN DATA SETS.                                           *
*                                                                     *
***********************************************************************
*                                                                     *
         USING DEBBASIC,R8
CHKDEBN  XR    R6,R6
         OC    TCBDEB,TCBDEB       CHECK FOR NO DEB CHAIN        Y02083
         BZ    CHKCOMP             IF NOT, GO DO GETMAIN         Y02083
         L     R8,TCBDEB           GET ADDR OF FIRST DEB.
CHKLSDB  NC    DEBDEBB,DEBDEBB     IS THIS THE LAST DEB
         BNZ   CHKNXDB             NO-GET ADDR OF NEXT DEB.
         TM    DCBOFLGS,DCBOFOPN   IS CHKPT D/S OPEN             Y02083
         BO    CHKDCBOP            IF YES, BRANCH                Y02083
         LA    R6,K1(,R6)          BUMP DEB COUNT FOR CHKPT D/S  Y02083
         B     AROUND              BYPASS DEB CHECKING         @ZA25063
CHKDCBOP EQU   *                                               @ZA25063
         ICM   R8,M7,45(RC)        GET THE CHECKPOINT DEB      @ZA25063
         ICM   R8,M7,25(R8)        GET THE PROTECTED DCB       @ZA25063
         DEBCHK (R8),TYPE=VERIFY   VERIFY THE DEB IS IN TABLE  @ZA25063
         LTR   RF,RF               ANY ERRORS FROM DEBCHECK    @ZA25063
         BZ    AROUND              NO SO PRESS ON              @ZA25063
         LA    RA,CKERPG1          SET ERROR CODE , NICE TRY   @ZA25063
         B     CHKERX              ABEND CHECKPOINT            @ZA25063
AROUND   EQU   *                                               @ZA25063
         LTR   R6,R6               Q. WAS THERE ONLY ONE DEB   @ZA25063
         BZ    CHKCOMP             YES-SKIP CODE TO INCREASE W. A. SIZE
         BCTR  R6,0                GET NUMBER OF DEBS MINUS TWO
         MH    R6,REPDCB1          COMPUTE W.A THAT IS
         AR    R7,R6               NEEDED BY REPDCB.
         DROP  R8
*                                                                     *
***********************************************************************
*                                                                     *
*        PUT ADDRESS OF WORK AREA ON DOUBLE-WORD BOUNDARY AND ISSUE   *
*        CONDITIONAL GETMAIN FOR WORK AREA OF SIZE COMPUTED.          *
*                                                                     *
***********************************************************************
*                                                                     *
CHKCOMP  STC   R7,HKPSP            SAVE LOW ORDER BYTE OF ADDR.
         TM    HKPSP,DBLWDBND      IS ADDR ALREADY ON DBL WD BOUNDARY
         BC    ZERO,CHKGOON        YES-GO ON, NO FURTHER WORK NEEDED
         LA    R7,8(0,R7)          INCREASE ADDR BY EIGHT.
         SRA   R7,TRUNC3           TRUNCATE 3 LOW
         SLA   R7,TRUNC3           ORDER BINARY DIGITS
CHKGOON  BAL   R9,CHKCOMG          GO TO ISSUE CONDITIONAL GETMAIN
         B     *+4(RF)             IF RETURN CODE IS 0,
         B     CHKCONT             CONTINUE NORMAL PROCESSING
         B     CHKERC              OTHERWISE-GO TO ERROR EXIT ROUTINE
*                                                                     *
***********************************************************************
*                                                                     *
*        INITIALIZE WORK AREA WITH INFORMATION THAT WILL BE USED BY   *
*        CHECKPOINT MODULES                                           *
*                                                                     *
***********************************************************************
*                                                                     *
         USING CHKWA,R6            ADDRESS W.A.
CHKCONT  XC    CHKWA(ZLN),CHKWA    ZERO OUT W.A.
         MVC   CKSYNAD(WDLNG),DCBSYNAD SAVE DCBSYNAD FIELD IN WA
         MVC   CKTIOTLN(LNG),HKPTIOTL MOVE INFO FROM SVRB SA TO WA
         USING PARMLST,RD          ADDR PARAMETER LIST
         MVC   CKDCB(ADDRLNG),DCBORCN PUT ADDR OF CHKPT DCB INTO
         MVC   CKDCB2(ADDRLNG),DCBORCN DECBS INWA
         LR    R8,R7               SAVE W.A. LENGTH.
         SH    R7,SSCRBUFL         GET SUR/SSCR BUFFER OFFSET    Y02076
         ST    R7,CKBFOFST         SAVE IT                       Y02076
CHKCOCNC ST    R4,CKTCBAD          INITIALIZE CHKWA
         ST    R8,CKWACL           SAVE LENGTH
         ST    R5,CKERAS07         SAVE SVRB PTR                 Y02083
         LA    RF,CHKWAL           GET OFFSET TO CHKDCB W.A.
         ST    RF,CKWAOFST         SAVE IT
         MVC   CKFLAG1(ONE),HKPFLAG MOVE IN FLAGS
         TM    TCBFLGS6,TCBRV      IS THIS TASK V=R?             YM2776
         BNO   CKNOVEQR            NO, DO NOT SET BIT            YM2776
         OI    CKFLAG1,CKVEQR      SET V=R FLAG IN W.A.          YM2776
         DROP  R9                                                  I283
CKNOVEQR L     RF,TCBPQE           GET ADDR OF DUMMY PQE           I283
         LA    RF,L8(RF)           GET TO 1ST PQE PTR            Y02076
         L     RF,K0(RF)           GET ADR OF PQE                Y02076
         USING PQESECT,RF
CHKPQELP L     R9,PQEREGN          GET REGION STRT ADDR          Y02076
         ST    R9,CKPPM            STORE IN CHKPT W.A.           Y02076
         ST    RD,CKPARMAD
         TM    HKPFLAG,CKCANCEL    CANCEL REQUEST                Y02083
         BO    CHKDSS2D            IF YES, SKIP D/S              Y02083
*                                  SECURITY TESTING              Y02083
         EJECT
*****************************************************************Y02083
*                                                                Y02083
*         TEST FOR MULTI-TASKING. IF FOUND, TERMINATE IN ERROR.  Y02083
*                                                                Y02083
*****************************************************************Y02083
*                                                                Y02083
CHKMULTT L     R2,HKPTIO           RESTORE TIOT ADDRESS          Y02083
         LA    RA,CKERPG12         LOAD MSG CODE IF ERROR OCCURS Y02083
         NC    TCBLTC,TCBLTC       ANY DAUGHTER TASKS            Y02083
         BNZ   CHKERX5B            YES, GO TO ERROR ROUTINE      Y02083
         LA    RA,CKERPG11         LOAD MSG CODE IF ERROR OCCURS Y02083
         CLM   R4,ADDRBYTS,TCBJSTCA IS THIS A JOB STEP TCB       Y02083
         BNE   CHKERX5B            IF NOT, GO TO ERROR ROUTINE   Y02083
*****************************************************************Y02083
*                                                                Y02083
*        OPEN CHECKPOINT DATA SET IF USER DID NOT OPEN DATA SET  Y02083
*                                                                Y02083
*****************************************************************Y02083
*                                                                Y02083
CHKOPEN  TM    DCBOFLGS,DCBOFOPN   CHKPT D/S ALREADY OPEN        Y02083
         BO    CHKOPEN2            IF YES, BRANCH                Y02083
         L     R7,TCBJSCB          FETCH JSCB ADDRESS            Y02083
         USING IEZJSCB,R7          ADDRESS JSCB                  Y02083
         L     R7,JSCDSABQ         FETCH DSAB-QDB ADDRESS        Y02083
         USING QDB,R7              ADDRESS DSAB-QDB              Y02083
         L     R7,QDBFELMP         FETCH 1ST DSAB ADDRESS        Y02083
         USING DSAB,R7             ADDRESS DSAB                  Y02083
         LA    RA,CKERPG2          ANTICIPATE MISSING DDNAME     Y02083
CHKOPEN1 LTR   R7,R7               DSAB ADDRESS OR ZERO          Y02083
         BZ    CHKERX5A            IF NO DSAB ADDR, GO EXIT      Y02083
         L     R9,DSABTIOT         FETCH TIOT ENTRY ADDRESS      Y02083
         USING TIOENTRY,R9         ADDRESS TIOT ENTRY            Y02083
         CLC   TIOEDDNM,DCBDDNAM   IS THIS THE CHKPT DD ENTRY    Y02083
         BE    CHKOPEN3            IF YES, BRANCH                Y02083
         L     R7,DSABFCHN         FETCH NEXT DSAB ADDRESS       Y02083
         B     CHKOPEN1            LOOP BACK FOR THIS DSAB       Y02083
CHKOPEN2 L     R7,DCBDEBAD         FETCH CHKPT D/S DEB ADDRESS   YM1237
         LA    RA,DEBBASIC-DEBPREFX ESTABLISH ADDRESSABILITY TO  YM1237
         SR    R7,RA               DEB PREFIX                    YM1237
         USING DEBPREFX,R7         ADDRESS DEB + PREFIX          YM1237
         LA    RA,CKERPG19         ANTICIPATE OPEN FOR INPUT ERR YM7008
         TM    DEBOPATB,OUTPUT     D/S OPEN FOR OUTPUT           YM7008
         BZ    CHKERX5B            IF NOT, TERMINATE IN ERROR    YM7040
         L     R7,DEBXTNP          FETCH DEB EXTENSION ADDRESS   YM1237
         USING DEBXTN,R7           ADDRESS DEB EXTENSION         YM1237
         L     R7,DEBXDSAB         FETCH DSAB ADDRESS            YM1237
         USING DSAB,R7             ADDRESS DSAB                  YM1237
         L     R9,DSABTIOT         FETCH TIOT ENTRY ADDRESS      Y02083
CHKOPEN3 LA    RA,CKERP213         ANTICIPATE SUBSYSTEM D/S ERR  Y02083
         TM    TIOELINK,TIOESSDS   CHKPT D/S A SUBSYSTEM D/S     Y02083
         BO    CHKERX5B            IF YES, TERMINATE IN ERROR    Y02083
         STM   R2,R7,CKJCT+TEMPSAVE-QMIOP SAVE REGS              YM4683
         ICM   R2,M14,TIOEJFCB     FETCH JFCB SWA ADDRESS        Y02083
         DROP  R9                  TIOT ENTRY ADDRESSABILITY     Y02083
         BAL   R5,CHKQMNGR         GO READ JFCB                  Y02083
         USING JFCBDSNM,R7         ESTABLISH JFCB ADDRESSABILITYZ30JSFC
         TM    JFCFLGS1,JFCBCEOV   CHKPT=EOV FOR THIS DATA SET? Z30JSFC
         BZ    CHKOPN3A            NO - CONTINUE                Z30JSFC
         NI    JFCFLGS1,XFF-JFCBCEOV    TURN OFF - INVAL REQUESTZ30JSFC
         BAL   R5,CHKQMNGW         REWRITE JFCB W/O CHKPT=EOV   Z30JSFC
         USING DSAB,R7             RESET DSAB ADDRESSABILITY    Z30JSFC
CHKOPN3A EQU   *                   CONTINUE                     Z30JSFC
         LR    R1,R7               JFCB ADDRESS TO REG 1         Y02083
         LM    R2,R7,CKJCT+TEMPSAVE-QMIOP RESTORE SAVED REGS     YM4683
         USING JFCBDSNM,R1         ADDRESS JFCB                  Y02083
         LA    RA,CKERP212         ANTICIPATE LABEL ERROR        Y02083
         TM    JFCBLTYP,JFCSUL     IS ONLY SL SPECIFIED          Y02083
         BNM   CHKERX5B            IF NOT, TERMINATE IN ERROR    Y02083
         DROP  R1                  JFCB ADDRESSABILITY           Y02083
         TM    DCBOFLGS,DCBOFOPN   IS CHKPT DATA SET OPEN        YM1237
         BNO   CHKOPEN4            IF NOT, GO OPEN CHKPT D/S     YM1237
         TM    DSABFLG4,DSABCKSI   SECURITY INTERFACE EXIST      YM1237
         BO    CHKAFOP             IF YES, BRANCH                YM1237
         OI    CKFLAG1,USERAUTH    SET AUTHORIZED-USER FLAG      YM1237
         B     CHKAFOP             CONTINUE                      YM1237
CHKOPEN4 TESTAUTH FCTN=1,KEY=YES   TEST FOR AUTHORIZED USER      YM1237
         LTR   RF,RF               TEST RETURN CODE              YM1237
         BNZ   CHKOPEN5            IF NOT AUTHORIZED, BRANCH     YM1237
         OI    CKFLAG1,USERAUTH    SET AUTHORIZED-USER FLAG      YM1237
         B     CHKOPEN6            CONTINUE                      YM1237
CHKOPEN5 OI    DSABFLG4,DSABCKSI   TELL OPEN USER NOT AUTHORIZED YM1237
         DROP  R7                  DSAB ADDRESSABILITY           YM1237
CHKOPEN6 ST    RC,CKERAS04         DCB ADDR TO OPEN'S PARM LIST  YM1237
         MVI   CKERAS04,OPENOPT    MOVE OPTION BYTE TO PARM LIST YM1237
         BAL   RE,CHKESTES         ESTABLISH ESTAE ENVIRON       YM5913
         MODESET KEYADDR=TCBPKF,WORKREG=10 ASSUME CALLER KEY     YM5921
         OPEN  MF=(E,CKERAS04)     OPEN CHKPT DATA SET           Y02083
         MODESET EXTKEY=ZERO       RESUME KEY ZERO               YM5921
         BAL   RE,CHKESTCN         CANCEL ESTAE ENVIRON          YM5913
         LA    RA,CKERPG2          ANTICIPATE OPEN ERROR         Y02083
         TM    DCBOFLGS,DCBOFOPN   WAS OPEN SUCCESSFUL           Y02083
         BZ    CHKERX5A            IF NOT, GO ERROR RTN          Y02083
         OI    CKFLAG1,CKOPEN      SET CHKPT OPEN FLAG           Y02083
CHKAFOP  EQU   *                   END OF OPEN PROCESSING        Y02083
         EJECT
**************************************************************** Y02083
*                                                                Y02083
*        CHECKPOINT DATA SET SECURITY TESTING                    Y02083
*                                                                Y02083
**************************************************************** Y02083
*                                                                Y02083
*                                                                Y02083
*        TEST FOR DUMMY DATA SET. IF FOUND, TERMINATE THE        YM7008
*        CHECKPOINT WITH ERROR CODE (016) - INVALID DEVICE       YM7008
*                                                                Y02083
CHKDSSEC L     R7,DCBDEBAD         FETCH CHKPT DS DEB ADDRESS    Y02083
         LA    R8,DEBBASIC-DEBPREFX ESTABLISH ADDRESSABILITY TO  Y02083
         SR    R7,R8                 DEB PREFIX (DEBBASIC-DEB)   Y02083
         USING DEBPREFX,R7         ADDRESS DEB AND PREFIX        Y02083
         ST    R7,CKERAS00         SAVE CHKPT DEB PREFIX ADDRESS Y02083
         LA    RA,CKERPG16         ANTICIPATE DEVICE ERROR       YM7008
         CLC   DEBDEVED(2),ABLEV   DUMMY DATA SET              @ZA30032
         BE    CHKERX5B            IF DUMMY, GET OUT ON ERROR    Y02083
         LA    R9,DEBBASND         GET END ADR OF BASIC SECT     Y02083
         USING DEBDASD,R9          DASD SECT FOLLOWS BASIC       Y02083
         ICM   R8,M7,DEBUCBA       GET CHKPT UCB ADDR            Y02083
         DROP  R9                                                Y02083
         ST    R8,CKERAS03         SAVE CHKPT UCB ADDR           Y02083
*                                                                Y02083
*        CHECK TO SEE IF DATA SET IS ALREADY SECURE.             Y02083
*                                                                Y02083
         L     R8,DEBXTNP          FETCH DEB EXTENSION ADDRESS   Y02083
         ST    R8,CKERAS01         SAVE CHKPT DEB EXTENSION ADDR Y02083
         USING DEBXTN,R8           ADDRESS DEB EXTENSION         Y02083
         L     R8,DEBXDSAB         FETCH DSAB ADDRESS            Y02083
         ST    R8,CKERAS02         SAVE CHKPT DSAB ADDR          Y02083
         USING DSAB,R8             DSAB ADDRESSING               Y02083
         TM    DSABFLG4,DSABCKDS+DSABCKVL KNOWN SECURE CHKPT DS  Y02083
         BNO   CHKDSS01            IF NOT, CONTINUE TEST         YM5921
*                                                                Y02083
*        DETERMINE IF ALREADY-SECURE CHECKPOINT DATA SET HAS TO  Y02083
*        BE TESTED FOR CONCURRENT OPENS. THIS IS NECCESSARY FOR  Y02083
*        DIRECT ACCESS PHYSICAL SEQUENTIAL DATA SETS BECAUSE EOV Y02083
*        PROCESSING MAY HAVE EXTENDED THE DATA SET TO A NEW      Y02083
*        VOLUME WHICH MAY HAVE BEEN PREVIOUSLY OPENED TO BY      Y02083
*        ANOTHER USER DCB.                                       Y02083
*                                                                Y02083
         LA    RE,CHKDSS2D         SET RETURN ADDRESS            YM2860
         L     R7,CKERAS03         FETCH CHKPT UCB ADDRESS       Y02083
         USING UCBOB,R7            ADDRESS UCB                   Y02083
         TM    UCBJBNR,UCBVRDEV    VIO PSEUDO DEVICE             Y02083
         BO    CHKDSS2C            IF YES, CAN ONLY BE 1 VOL     Y02083
         CLI   UCBTBYT3,UCB3TAPE   TAPE DEVICE                   YM7811
         BE    CHKDSS2C            IF YES, CAN ONLY BE 1 OPEN    YM7811
         CLI   DSABORG1,DSABPO     D/S ORG PARTITIONED           Y02083
         BE    CHKDSS2C            IF YES, CAN ONLY BE 1 VOL     Y02083
         BAL   RE,CHKDSNRD         GO READ AND SAVE CHKPT DSN    YM7537
         LA    RE,CHKDSS2B         SET RETURN ADDRESS            YM5767
         B     CHKDSS2C            GO GET PROTECTED DCB ADDR, IF YM5767
*                                  NECESSARY, THEN GO CHECK FOR  YM5767
*                                  CONCURRENT OPEN'S             YM5767
*                                                                Y02083
*        TEST THE DEVICE TYPE ALLOCATED TO THE DATA SET.         Y02083
*                                                                Y02083
CHKDSS01 L     R7,CKERAS03         FETCH UCB ADDRESS             Y02083
         CLI   UCBTBYT3,UCB3DACC   DA DEVICE                     YM7811
         BNE   CHKNOTDA            IF NOT DA, BRANCH             YM7811
*                                                                YM7811
*        PRE-CONCURRENT OPEN TEST - SEE IF ANY DCB (OTHER THAN   YM7811
*        THE CHECKPOINT DCB) IS USING THE CHECKPOINT DSAB.       YM7811
*        THIS MUST BE PERFORMED BEFORE ANY POSSIBLE SECURITY     YM7811
*        INTERFACE IS INVOKED BY THE COMING OPEN SINCE A NON-S/I YM7811
*        DCB AND AN S/I DCB CAN NOT BE CORRECTLY DESCRIBED BY    YM7811
*        ONE DSAB.                                               YM7811
*                                                                YM7811
         LA    R1,DEBBASIC-DEBPREFX GET NEG DEB PREFIX OFFSET    YM7811
         ICM   R5,MF,TCBDEB        GET/TEST 1ST DEB ADDRESS      YM7811
CHKPRECT EQU   *                   TEST LOOP ENTRY               YM7811
         BZ    CHKDSS1A            GET OUT ON ZERO ADDRESS       YM7811
         SR    R5,R1               GET DEB PREFIX ADDRESS        YM7811
         USING DEBPREFX,R5         ADDRESS DEB PREFIX            YM7811
         C     R5,CKERAS00         IS THIS THE CHKPT DEB         YM7811
         BE    CHKGETNT            IF YES, GET NEXT DEB          YM7811
         L     R7,DEBXTNP          GET DEB EXTENSION ADDRESS     YM7811
         USING DEBXTN,R7           ADDRESS DEB EXTENSION         YM7811
         LA    RA,CKERP209         ANTICIPATE CONCURRENT OPEN    YM7811
         CLC   CKERAS02,DEBXDSAB   COMPARE FOR SAME DSAB         YM7811
         BE    CHKERX5B            BRANCH IF THEY ARE THE SAME   YM7811
CHKGETNT EQU   *                   GET NEXT DEB ENTRY            YM7811
         ICM   R5,M7,DEBDEBB       GET/TEST NEXT DEB ADDRESS     YM7811
         B     CHKPRECT            LOOP BACK                     YM7811
         DROP  R5,R7               DEB, AND DEB EXTENSION        YM7811
*                                                                YM7811
*        CONTINUE DEVICE CHECK                                   YM7811
*                                                                YM7811
         USING UCBOB,R7            RE-ESTABLISH UCB ADDRESSING   YM7811
CHKNOTDA EQU   *                   NON-DA ENTRY                  YM7811
         LA    RA,CKERPG16         ANTICIPATE ERRONEOUS DEVICE   Y02083
         CLI   UCBTBYT3,UCB3TAPE   TAPE DEVICE                   YM7811
         BNE   CHKERX5B            IF NOT, GO TO ERROR ROUTINE   YM7811
*                                                                Y02083
*        TEST FOR A VALID TAPE BLOCK COUNT.                      Y02083
*                                                                Y02083
         LA    RA,CKERP208         ANTICIPATE BLOCK COUNT ERROR  Y02083
         OC    DCBBLKCT,DCBBLKCT   TEST TAPE BLOCK COUNT         Y02083
         BNZ   CHKERX5B            IF NOT ZERO, ERRONEOUS DATA   Y02083
*                                  EXISTS ON TAPE; GO TERMINATE  Y02083
*                                  IN ERROR                      Y02083
         EJECT
*        IN ORDER TO CREATE TAPE AND DA LABELS WITH THE          Y02083
*        CHECKPOINT DATA SET INDICATORS, THE FOLLOWING           Y02083
*        CODE WILL:                                              Y02083
*                                                                Y02083
*        1. TURN OFF THE DSABUNAL BIT FOR THE CHECKPOINT D S     YM7324
*        2. SET DEBDSNM TO ZERO TO PREVENT A STOW                YM7324
*        3. CLOSE THE CHECKPOINT DATA SET                        Y02083
*        4. SET THE JFCB STATUS TO "NEW"                         Y02083
*        5. RESTORE THE DSABUNAL BIT                             YM7324
*        6. OPEN THE CHECKPOINT DATA SET                         Y02083
*        7. RESTORE THE JFCB STATUS FIELD                        Y02083
*                                                                Y02083
CHKDSS1A EQU  *                                                  YM7324
         MVC   SIOTSAVE(ONE),DSABFLG2  SAVE DSAB FLAG            YM7324
         NI    DSABFLG2,X'FF'-DSABUNAL TURN OFF UNALLOC FLAG     YM7324
         L     R7,CKERAS00         GET DEB ADDRESS               YM7324
         USING DEBPREFX,R7                                       YM7324
         SR    R5,R5               ZERO REGISTER                 YM7324
         SR    R1,R1               ZERO REGISTER                 YM7324
         IC    R5,DEBNMEXT         GET NUMBER OF EXTENTS         YM7324
         IC    R1,DEBEXSCL         GET EXTENT SCALING FACTOR     YM7324
         SLL   R5,0(R1)            EXT SIZE X EXT NUMBER         YM7324
         LA    R5,DEBBASND(R5)     EXTENTS + DEB + BASE          YM7324
         USING DEBACSMD,R5                                       YM7324
         TM    DCBDSORG,DCBDSGPO   DCB FOR PO DATA SET?          YM7324
         BO    CHKDSS1B            BRANCH IF YES                 YM7324
         LA    R5,DEBDSNM          ADD SAM LENGTH TO DEB SIZE    YM7324
CHKDSS1B EQU   *                                                 YM7324
         CLI   DEBAMLNG,L'DEBDSNM  IS THE ACCESS METHOD DEP      YM7324
*                                  SECTION PRESENT?              YM7324
         BL    CHKDSS1C            BRANCH IF NO                  YM7324
         USING DEBDSNM,R5                                        YM7324
         MVI   DEBDSNM,X00         ZERO OUT MEMBER NAME IN DEB   YM7324
*                                  TO PREVENT STOW BY CLOSE      YM7324
         DROP  R5                                                YM7324
CHKDSS1C EQU   *                                                 YM7324
         ST    RC,CKERAS04         SETUP PARM LIST FOR           Y02083
         MVI   CKERAS04,CLOSEOPT   CLOSE WITH LEAVE              Y02083
         MODESET KEYADDR=DEBPROTG,WORKREG=5 ASSUME D/S KEY       YM5744
         DROP  R7                  DEB ADDRESSABILITY            YM5744
         CLOSE MF=(E,CKERAS04)     CLOSE D/S                     Y02083
         MODESET EXTKEY=ZERO       RETURN TO SUPERVISOR KEY      YM5744
         L     R7,DSABTIOT         FETCH TIOT DD ENTRY ADDR      Y02083
         USING TIOELNGH,R7         ADDRESS TIOT DD ENTRY         Y02083
         ICM   R2,M14,TIOEJFCB     FETCH JFCB SVA                Y02083
         DROP  R7                  TIOT DD ENTRY ADDRESSING      Y02083
         BAL   R5,CHKQMNGR         GO READ JFCB                  Y02083
         USING JFCBDSNM,R7         ADDRESS JFCB                  Y02083
         MVC   JFCBSAVE(ONE),JFCBIND2 SAVE JFCB STATUS           Y02083
         MVI   JFCBIND2,JFCNEW     SET STATUS=NEW                Y02083
         DROP  R7                  JFCB ADDRESSING               Y02083
         BAL   R5,CHKQMNGW         GO WRITE BACK JFCB            Y02083
         MVC   DSABFLG2(ONE),SIOTSAVE RESTORE DSAB FLAG FIELD    YM7324
         OI    DSABFLG4,DSABCKDS   SET DSAB CHKPT D/S FLAG       Y02083
         TM    CKFLAG1,USERAUTH    AUTHORIZED USER               YM1237
         BO    CHKDSS1F            IF YES, BRANCH                YM1237
         OI    DSABFLG4,DSABCKSI   TELL OPEN USER NOT AUTHORIZED YM1237
CHKDSS1F MVI   CKERAS04,OPENOPT    SET OPEN FOR OUTPUT           Y02083
         BAL   RE,CHKESTES         ESTABLISH ESTAE ENVIRON       YM5913
         MODESET KEYADDR=TCBPKF,WORKREG=10 ASSUME CALLER KEY     YM5921
         OPEN  MF=(E,CKERAS04)     OPEN FOR OUTPUT               Y02083
         MODESET EXTKEY=ZERO       RESUME KEY ZERO               YM5921
         BAL   RE,CHKESTCN         CANCEL ESTAE ENVIRON          YM5913
         L     R7,DSABTIOT         FETCH TIOT DD ENTRY ADDRESS   Y02083
         USING TIOELNGH,R7         ADDRESS TIOT DD ENTRY         Y02083
         ICM   R2,M14,TIOEJFCB     FETCH JFCB SVA                Y02083
         DROP  R7                  TIOT DD ENTRY ADDRESSING      Y02083
         BAL   R5,CHKQMNGR         GO READ JFCB                  Y02083
         USING JFCBDSNM,R7         ADDRESS JFCB                  Y02083
         MVC   JFCBIND2(ONE),JFCBSAVE RESTORE JFCB STATUS        Y02083
         DROP  R7                  JFCB ADDRESSING               Y02083
         BAL   R5,CHKQMNGW         GO WRITE BACK JFCB            Y02083
*                                                                Y02083
*        TEST TO SEE IF OPEN FOUND A SECURE DATA SET.            Y02083
*                                                                Y02083
         LA    RA,CKERP211         ANTICIPATE NON-SECURE D/S     Y02083
         TM    DSABFLG4,DSABCKVL   DID OPEN FIND A SECURE D/S    Y02083
         BZ    CHKERX5B            IF NOT, GO TO ERR EXIT        Y02083
         BAL   RE,CHKDSS2C         GO GET PROPER DCB ADDR        YM2860
*                                                                Y02083
*        TEST FOR TAPE DATA SET AND IF TAPE, BYPASS REMAINING    Y02083
*        SECURITY TESTS.                                         Y02083
*                                                                Y02083
         L     R7,CKERAS03         FETCH CHKPT UCB ADDR          Y02083
         USING UCBOB,R7            ADDRESS UCB                   Y02083
         CLI   UCBTBYT3,UCB3TAPE   TAPE DEVICE                   YM7811
         BE    CHKDSS2D            IF YES, SKIP REMAINING TESTS  YM7811
*
*        READ THE CHECKPOINT DATA SET FORMAT 1 DSCB FOR          YM7324
*        DATA SET ORGANIZATION CHECK, AND SUBSEQUENT             YM7324
*        CONCURRENT OPEN TESTING.                                YM7324
*
         TM    UCBJBNR,UCBVRDEV    PSEUDO DEVICE FOR VIO         YM7324
         BO    CHKDSS1G            IF YES, BYPASS READING DSCB   YM7324
         BAL   RE,CHKDSNRD         GO READ AND SAVE CHKPT DSN    YM7537
*                                                                Y02083
*        TEST DATA SET ORGANIZATION                              Y02083
*                                                                Y02083
         LA    RE,XBUF             GET POINTER TO FORMAT 1 DSCB  YM7324
         USING IECSDSF1,RE                                       YM7324
         CLI   DS1DSORG,DCBDSGPO   IS THIS A PO DATA SET?        YM7324
         BE    CHKDSS2A            IF YES, BR                    YM7324
         B     CHKDSS1H            GO CHECK FOR PS               YM7324
*
*        FOR A VIO UCB, THE FORMAT 1 DSCB FOLLOWS THE UCB        YM7324
*        IN CORE.                                                YM7324
*
CHKDSS1G EQU   *                                                 YM7324
         L     R7,CKERAS03         GET UCB POINTER               YM7324
         USING VDSUCB,R7                                         YM7324
         LA    RE,VDSDSCB          GET POINTER TO VIO DSCB       YM7324
         CLI   DS1DSORG,DCBDSGPO   IS THIS A PO DATA SET?        YM7324
         BE    CHKDSS2A            IF YES, BR                    Y02083
         DROP  RE                                                YM7324
CHKDSS1H EQU   *                                                 YM7324
         LA    RA,CKERPG07         ANTICIPATE ERRONEOUS DSORG    Y02083
         CLI   DCBDSORG,DCBDSGPS   D/S ORG PHYSICAL SEQUENTIAL   YM5717
         BNE   CHKDS60             IF NOT, GO RESET CHKPT FLAG   Y02083
*                                                                Y02083
*        ENSURE THAT THE NEW BSAM DATA SET IS EMPTY.             Y02083
*                                                                Y02083
         L     R7,CKERAS00         FETCH CHKPT DEB ADDRESS       Y02083
         USING DEBPREFX,R7         ADDRESS DEB                   Y02083
         CLC   DCBFDAD+K1(K6),DEBBASND+DEBBINUM-DEBDASD COMPARE  Y02083
*                                  FIRST DEB EXTENT ADDRESS TO   Y02083
*                                  THE FIRST DCB WRITE ADDRESS   Y02083
*                                  IF NOT EQUAL, THE SUBJECT     Y02083
*                                  BSAM D/S IS NOT EMPTY         Y02083
         LA    RA,CKERP208         ANTICIPATE D/S ERROR          Y02083
         BNE   CHKDS60             IF NOT EQUAL, GO RESET CP D/S Y02083
         CLI   DCBFDAD+K7,K0       ENSURE RECORD # IS 1          YM5717
         BNE   CHKDS60             IF NOT 1, GO RESET CP D/S     Y02083
         B     CHKDSS2B            BYPASS PO D/S TESTING         Y02083
*                                                                Y02083
*        ENSURE THAT THE NEW PO DATA SET DIRECTORY IS EMPTY.     Y02083
*                                                                Y02083
CHKDSS2A BAL   RE,CHKDEBLD         GO BUILD SKELETON DEB/IOB/CP  Y02083
         L     R7,CKERAS00         FETCH CHKPT DEB ADDRESS       Y02083
         USING DEBPREFX,R7         ADDRESS DEB                   Y02083
         MVC   XDCBFDAD+K1(K6),DEBBASND+DEBBINUM-DEBDASD MOVE IN Y02083
*                                  BB, CC, AND HH OF DCBFDAD     Y02083
         MVI   XDCBFDAD+K7,K1      SET RECORD # TO 1             Y02083
         MVI   XCCW3,RDDATACM      SET CCW READ DATA COMMAND     Y02083
         MVI   XCCW3+K7,K14        SET DATA LENGTH TO 14         Y02083
         MVC   XDEBBINU(L12),DEBBASND+DEBBINUM-DEBDASD MOVE IN   Y02083
*                                  UCB ADDRESS AND 1ST EXTENT    Y02083
         MVC   XIOBFDAD(L8),XDCBFDAD SET IOB SEEK ADDRESS        Y02083
         BAL   RE,CHKEXCP2         GO READ PDS DIRECTORY ENTRY   Y02083
         LA    RA,CKERP208         ANTICIPATE PDS ERRORS         Y02083
         CLC   XBUF(K10),KPDS      IS DIRECTORY EMPTY            YM5717
         BNE   CHKDS60             IF NOT EMPTY, GO RESET CP D/S Y02083
*                                                                Y02083
*        TEST FOR A SHARED DA DEVICE.                            Y02083
*                                                                Y02083
CHKDSS2B L     R7,CKERAS03         FETCH CHKPT UCB ADDRESS       Y02083
         USING UCBOB,R7            ADDRESS UCB                   Y02083
         LA    RA,CKERP214         ANTICIPATE SHARED DA ERROR    Y02083
         TM    UCBTBYT2,UCBRR      TEST DA SHARABLE BIT          Y02083
         BO    CHKDS60             IF ON, GO RESET CP D/S        Y02083
*                                                                Y02083
*        TEST FOR ANOTHER DCB OF NON-CHECKPOINT STATUS BEING     Y02083
*        CONCURRENTLY OPEN TO OUR CHECKPOINT DATA SET.           Y02083
*                                                                Y02083
         BAL   RE,CHKDEBLD         CONSTRUCT DEB/IOB/CP          YM7537
         ICM   R7,MF,TCBDEB        FETCH/TEST 1ST DEB ADDR       Y02083
         USING DEBBASIC,R7                                       YM7324
CHKDS02B BZ    CHKDS42B            TERM TEST IF NO MORE DEB'S    Y02083
         TM    DEBFLGS1,DEBCINDI   IS IT ISAM/VSAM CI          @ZA31342
         BO    CHKDS22B            BYPASS DSCB READ IF YES     @ZA31342
         LA    R9,DEBBASIC-DEBPREFX GET DEB                      Y02083
         SR    R7,R9               PREFIX ADDRESS                Y02083
         USING DEBPREFX,R7                                       YM7324
         CLI   DEBAMTYP,X'01'      IS IT VSAM                    YM5913
         BE    CHKDS22B            BYPASS DSCB READ IF YES       YM5913
         L     R9,DEBXTNP          FETCH DEB EXTENSION ADDR      Y02083
         USING DEBXTN,R9           ADDRESS DEB EXTENSION         Y02083
         L     RA,DEBXDSAB         FETCH DSAB ADDR               Y02083
         USING DSAB,RA             ADDRESS DSAB                  Y02083
         TM    DSABFLG4,DSABCKDS   D/S ALREADY FLAGED AS CHKPT   Y02083
         BO    CHKDS22B            IF YES, BYPASS REMAINING TSTS Y02083
         L     RA,DSABTIOT         FETCH TIOT ENTRY ADDRESS      Y02083
         USING TIOENTRY,RA         ADDRESS TIOT ENTRY            Y02083
         TM    TIOELINK,TIOESSDS   SUBSYSTEM D/S                 Y02083
         BO    CHKDS22B            IF YES, CAN'T BE CHKPT D/S    Y02083
         SR    R8,R8               CLEAR REG FOR TEST            Y02083
         ICM   R8,M7,TIOEFSRT      FETCH/TEST 1ST UCB ADDR       Y02083
         BZ    CHKDS22B            IF ZERO - DUMMY -, BR         Y02083
         USING UCBOB,R8            ADDRESS UCB                   Y02083
         CLI   UCBTBYT3,UCB3DACC   DA DEVICE                     Y02083
         BNE   CHKDS22B            IF NOT DA, SKIP THIS D/S      Y02083
         TM    UCBJBNR,UCBVRDEV    PSEUDO DEVICE FOR VIO         Y02083
         BO    CHKDS12B            IF YES, GO COMPARE UCB ADDR'S Y02083
         L     R8,CKERAS03         GET CHKPT UCB ADDR            YM6545
         TM    UCBJBNR,UCBVRDEV    IS CHKPT DEVICE VIO           YM6545
         BO    CHKDS22B            IF YES, BYPASS REMAINING TEST YM6545
         BAL   RE,CHKEXCP          GO READ FORMAT 1 DSCB         Y02083
         CLC   XDSN(K44),XBUF      MATCHING DSN'S                Y02083
         LA    RA,CKERP209         ANTICIPATE CONCURRENT OPEN    Y02083
         BE    CHKDS60             IF MATCH, IT'S AN ERROR       Y02083
         B     CHKDS22B            GO FOR NEXT D/S               Y02083
CHKDS12B C     R8,CKERAS03         MATCHING UCB ADDRESS'S        Y02083
         LA    RA,CKERP209         ANTICIPATE CONCURRENT OPEN    Y02083
         BE    CHKDS60             IF MATCH, IT'S AN ERROR       Y02083
CHKDS22B ICM   R7,M7,DEBDEBB       FETCH/TEST NEXT DEB ADDR      Y02083
         B     CHKDS02B            LOOP BACK                     Y02083
         DROP  R7,R9,RA            DEB, DEBX, AND DSAB           YM6564
*                                                                Y02083
*        ENSURE CHECKPOINT DATA SET IS ENQUED EXCLUSIVE.         Y02083
*                                                                Y02083
CHKDS42B EQU   *
*        NOTE: SCHEDULER DOES NOT ENQUE ON VIO DATA SETS.        YM6564
         L     R8,CKERAS03         GET CHKPT UCB ADDR            YM6564
         TM    UCBJBNR,UCBVRDEV    IS CHKPT DEVICE VIO           YM6564
         BO    CHKDSS2D            IF YES, BYPASS REMAINING TEST YM6564
         DROP  R8                  UCB                           YM6564
         XC    CKERAS05,CKERAS05   CLEAR ENQ PARM FLAGS          Y02083
         MVI   CKERAS05,X81        SET LAST ENTRY AND TCB FLAGS  Y02083
         MVC   CKERAS08(L8),CHKQNAME RELOCATE ENQ QNAME          Y02083
         LA    R7,XDSN             GET ADDR OF CHKPT DSN         Y02083
*        LR    R8,R7               INIT DSN PNTR TO 1ST CHAR   @ZA11608
         LA    R8,X'2B'(R7)        ADD MAX LENGTH OF DSN       @ZA11608
BACK     EQU   *                                               @ZA11608
         CLI   0(R8),X'40'         IS LAST CHAR A BLANK ?      @ZA11608
         BNE   ADJUST              NO END HAS BEEN FOUND       @ZA11608
         BCT   R8,BACK             NO BRANCH BACK TO TEST      @ZA11608
ADJUST   EQU   *                                               @ZA11608
         LA    R8,1(R8)            ADD ONE TO LENGTH           @ZA11608
*HKDS50  CLI   K0(R8),BLANK        IS CHAR BLANK                 Y02083
*        BE    CHKDS52             IF BLANK, COMPUTE DSN LENGTH  Y02083
*        LA    R8,K1(,R8)          BUMP DSN POINTER TO NEXT CHA  Y02083
*        B     CHKDS50             LOOP BACK                     Y02083
CHKDS52  SR    R8,R7               GET LENGTH OF CHKPT DSN       Y02083
         L     R9,TCBOTC           GET INIT TCB ADDR FOR         Y02083
*                                  DIRECTED ENQ                  Y02083
         L     R5,CKERAS07         SAVE SVRB ADDR ACROSS ENQ     Y02083
         ENQ   (CKERAS08,XDSN,S,(R8),SYSTEM),RET=TEST,TCB=(R9),  YM7305*
               MF=(E,CKERAS05)                                   Y02083
         ST    R5,CKERAS07         RESTORE SAVED SVRB ADDR       Y02083
         LA    RA,CKERP210         ANTICIPATE DISP=SHR ERROR     Y02083
         LTR   RF,RF               SUCCESS SHARED ENQ            YM7305
         BZ    CHKDS60             IF YES, BR - IT'S AN ERROR    YM7305
         CLI   K3(RF),RETCD08      WAS THERE A PREVIOUS ENQ      YM7305
         BNE   CHKDS60             IF NOT, IT'S AN ERROR         YM7305
         TM    K0(RF),SHRFLAG      IS DSN ENQ'ED FOR SHR         YM7305
         BO    CHKDS60             IF YES, IT'S AN ERROR         YM7305
         B     CHKDSS2D            CONTINUE WITH CHECKPOINT      YM2860
*                                                                Y02083
*        TEST FOR A SECURITY INTERFACE. IF ONE EXITS, ESTABLISH  Y02083
*        THE ADDRESS OF THE PROTECTED DCB.                       Y02083
*                                                                Y02083
CHKDSS2C L     R8,CKERAS02         FETCH ADDRESS OF CHKPT DSAB   Y02083
         USING DSAB,R8             ADDRESS DSAB                  Y02083
         NI    CKFLAG1,X'FF'-USERAUTH 0 POSSIBLE AUTH-USER FLAG  YM1237
         SR    R7,R7               CLEAR WORK REG                YM2860
         ICM   R7,M7,DCBDEBA       FETCH NEW DEB ADDRESS         YM2860
         LA    RF,DEBBASIC-DEBPREFX GET LENGTH OF PREFIX         YM2860
         SR    R7,RF               BACK UP TO DEB PREFIX         YM2860
         ST    R7,CKERAS00         STORE DEB PREFIX ADDR         YM2860
         TM    DSABFLG4,DSABCKSI   IS USER AUTHORIZED            Y02083
         BZR   RE                  IF YES, RETURN - NO SI        YM2860
         USING DEBPREFX,R7         ADDRESS DEB                   Y02083
         ICM   RC,M7,DEBDCBB       SHIFT DCB POINTER TO          Y02083
*                                  PROTECTED DCB                 Y02083
         STCM  RC,M7,CKDCB         UPDATE DCB PNTR IN DECB1      YM5767
         STCM  RC,M7,CKDCB2        AND DECB2                     YM5767
         BR    RE                  RETURN WITH PROPER DCB ADDR   YM2860
         EJECT
*        EXCP SUBROUTINE FOR READING THE PDS DIRECTORY ENTRY OF  Y02083
*        THE CHECKPOINT DATA SET AND FOR READING AND WRITING     Y02083
*        THE FORMAT 1 DSCB OF CHECKPOINT AND NON-CHECKPOINT      Y02083
*        DATA SETS DURING THE CONCURRENT OPEN TEST.              Y02083
*                                                                Y02083
*        INPUT:                                                  Y02083
*                                                                Y02083
*              R7 - ADDRESS OF DATA SET'S DEB (PREFIX)           Y02083
*                                                                Y02083
         USING DEBPREFX,R7         ADDRESS DEB PREFIX            Y02083
CHKEXCP  MVC   XIOBFDAD+K1(K7),DEBDSCBA MOVE IN DSCB DISK ADDR   Y02083
         MVI   XIOBFDAD,X00        SET M TO ZERO                 Y02083
CHKEXCP2 LA    R9,DEBBASND         GET END ADR OF BASIC SECT     Y02083
         USING DEBDASD,R9          DASD SECT FOLLOWS BASIC       Y02083
         CLI   DEBAMTYP,AMTYPIS    IS THIS AN ISAM DEB?        @ZA31342
         BNE   CHKEXCP3            NO, GET UCB POINTER         @ZA31342
         LA    R9,ISEXTLEN(,R9)    SKIP OVER ISAM SECT           YM7076
CHKEXCP3 EQU   *                                                 YM7076
         MVC   XDEBUCBA(K3),DEBUCBA UCB ADDR TO WORKING DEB      Y02083
         DROP  R9                                                Y02083
         MVI   XECB,X00            CLEAR ECB                     Y02083
         EXCP  XIOB                                              Y02083
         WAIT  ECB=XECB                                          Y02083
         CLI   XECB,X7F            TEST FOR SUCCESSFUL I/O       Y02083
         BNE   CHKEXCPR            BR IF ERR TO SET CODE         YM5717
         BR    RE                  RETURN TO CALLER              Y02083
CHKEXCPR LA    RA,CKERPG2          SET I/O ERR CODE              YM5717
         B     CHKERX5A            BR TO ERR EXIT                YM5717
*                                                                Y02083
*        SUBROUTINE TO BUILD A SKELETON DEB, IOB, AND CP FOR     Y02083
*        VARIOUS I/O FUNCTIONS.                                  Y02083
*                                                                Y02083
CHKDEBLD XC    XDCB(XBUF-XDCB),XDCB CLEAR WORK SPACE             Y02083
         LA    R5,XIOB             SETUP DCB                     Y02083
         ST    R5,XDCBIOBA           IOB ADDRESS                 Y02083
         LA    R5,XDEB             SETUP DCB                     Y02083
         ST    R5,XDCBDEBA           DEB ADDRESS                 Y02083
         MVI   XDEBNMEX,K1         SET DEB EXTENT COUNT=1        Y02083
         L     R5,CVTXAPG          GET ADDR OF IOS APPENDAGES    Y02083
         ST    R5,XDEBAPPA         STORE IN DEB                  Y02083
         LA    R5,XDCB             SETUP DEB                     Y02083
         ST    R5,XDEBDCBA           DCB ADDRESS AND IOB         Y02083
         ST    R5,XIOBDCBA           DCB ADDRESS                 Y02083
         MVI   XDEBDEBI,X0F        SET DEB ID AND STORAGE KEY    Y02083
         MVI   XDEBEXSC,X04        SET DEB EXTENT SCALE          Y02083
         MVI   XDEBDVMO,XC0        SET DEVICE MODIFIER           Y02083
         LH    R5,MAXTRK           SET MAX TRACK                 Y02083
         STH   R5,XDEBENDC           CAPACITY FOR                Y02083
         STH   R5,XDEBENDH           HANDLING ANY                Y02083
         STH   R5,XDEBNMTR           SIZE DA DEVICE              Y02083
         LA    R5,XECB             SETUP IOB                     Y02083
         ST    R5,XIOBECBA           ECB ADDRESS                 Y02083
         MVI   XIOBFLG1,X02        SET IOB UNRELATED FLAG        Y02083
         LA    R5,XCCW1            SETUP IOB                     Y02083
         ST    R5,XIOBCPA            CHANNEL PROGRAM ADDRESS     Y02083
         MVC   XCCW1(L24),KCCW123  MOVE IN CCW CONSTANTS         Y02083
         LA    R5,XIOBFDAD+K3      SET CCW1'S                    Y02083
         STCM  R5,M7,XCCW1+K1        SEARCH ARG ADDRESS          Y02083
         MVI   XCCW1+K4,CC              SET COMMAND CHAINING     YM5717
         LA    R5,XCCW1            SET CCW2'S                    Y02083
         STCM  R5,M7,XCCW2+K1        TIC ADDRESS                 Y02083
         LA    R5,XBUF             SET CCW3'S                    Y02083
         STCM  R5,M7,XCCW3+K1        READ/WRITE AREA ADDRESS     Y02083
         BR    RE                  RETURN TO CALLER              Y02083
*                                                                YM7537
*        SUBROUTINE TO READ THE CHECKPOINT DATA SET F1 DSCB      YM7537
*        AND SAVE THE DATA SET NAME FOR SUBSEQUENT USE.          YM7537
*                                                                YM7537
CHKDSNRD ST    RE,CKERAS09         SAVE RETURN ADDRESS           YM7537
         BAL   RE,CHKDEBLD         CONSTRUCT DEB/IOB/CP          YM7324
         L     R7,CKERAS00         FETCH CHKPT DEB ADDRESS       YM7324
         USING DEBPREFX,R7         ADDRESS DEB PREFIX            YM7324
         BAL   RE,CHKEXCP          GO READ CHKPT FORMAT 1 DSCB   YM7324
         MVC   XDSN(K44),XBUF      SAVE CHKPT DSN FOR COMPARE    YM7324
         L     RE,CKERAS09         RESTORE RETURN ADDRESS        YM7537
         BR    RE                  RETURN TO MAINLINE            YM7537
         EJECT
*        RESET THE CHECKPOINT DATA SET INDICATOR IN THE FORMAT   Y02083
*        1 DSCB IF A SECURITY ERROR IS ENCOUNTERED AFTER A NEW   Y02083
*        CHECKPOINT DATA SET IS CREATED BY OPEN.                 Y02083
*                                                                Y02083
CHKDS60  LM    R7,R8,CKERAS02      GET DSAB & UCB ADDRESSES      YM7811
         USING DSAB,R7             ADDRESS DSAB                  YM7811
         NI    DSABFLG4,X'FF'-DSABCKVL RESET CHKPT SECURE FLAG   YM7811
         DROP  R7                  DSAB                          UM7811
         USING UCBOB,R8            ADDRESS UCB                   Y02083
         TM    UCBJBNR,UCBVRDEV    VIO PSEUDO DEVICE             Y02083
         BZ    CHKDS62             IF NOT, BR                    Y02083
         NI    VDSDSCB-VDSCB+DS1DSIND-DS1DSNAM(R8),XFF-DS1CHKPT  Y02083
*                                  RESET CHECKPOINT D/S          Y02083
*                                  INDICATOR IN VIO DSCB         Y02083
         B     CHKERX5B            GO TO ERROR EXIT              Y02083
CHKDS62  BAL   RE,CHKDEBLD         GO CONSTRUCT DEB/IOB/CP       Y02083
         L     R7,CKERAS00         FETCH CHKPT DEB ADDR FOR EXCP Y02083
         BAL   RE,CHKEXCP          GO READ CHKPT DSCB            Y02083
         NI    XBUF+DS1DSIND-DS1DSNAM,XFF-DS1CHKPT RESET CHKPT   Y02083
*                                  INDICATOR IN DSCB             Y02083
         MVI   XCCW3,WRTKDATA      SET CCW TO WRITE BACK DSCB    Y02083
         BAL   RE,CHKEXCP          GO REWRITE DSCB               Y02083
         B     CHKERX5B            GO TO ERROR EXIT              Y02083
         EJECT
**************************************************************** Y02083
*                                                                Y02083
*        Q MANAGER I/O ROUTINE                                   Y02083
*                                                                Y02083
*        INPUT:                                                  Y02083
*          R2=SVA (SWA CONTROL BLK ADDRESS)                      Y02083
*          R4=TCB ADDRESS                                        Y02083
*          R5=RETURN ADDRESS                                     Y02083
*                                                                Y02083
*        OUTPUT:                                                 Y02083
*          R7=ADDRESS OF CONTROL BLK READIN AREA                 Y02083
*                                                                Y02083
**************************************************************** Y02083
*                                                                Y02083
CHKQMNGR EQU   *                   READ ENTRY POINT              Y02083
         LA    R7,QMIOREAD         LOAD QMNGR READ CODE          Y02083
         B     CHKQMNG             BR TO COMMON ROUTINE          Y02083
CHKQMNGW EQU   *                   WRITE ENTRY POINT             Y02083
         LA    R7,QMIOWRTE         LOAD QMNGR WRITE CODE         Y02083
CHKQMNG  EQU   *                   START OF COMMON ROUTINE       Y02083
         LA    R1,CKJCT            GET ADDR OF QMNGRIO PARM LIST Y02083
         USING QMIOP,R1            ADDRESS QMNGRIO PARM LIST     Y02083
         ST    R4,QMITCBAD         STORE TCB ADDR IN PL          Y02083
         STCM  R7,M1,QMIFLAGS      SET RD/WR FLAG IN PARM LIST   Y02083
         LA    R9,K176             LOAD RECORD LENGTH OF 176     Y02083
         STH   R9,QMIRECL          STORE RECORD LENGTH IN PL     Y02083
         ST    R2,QMIDSKAD         STORE CONTROL BLK SVA IN PL   Y02083
         LA    R9,QMNGRWA          GET AND STORE QMNGR WA        Y02083
         ST    R9,QMIWRKAD           ADDRESS IN PL               Y02083
         LA    R7,QMNGRBUF         GET AND STORE BUFFER          Y02083
         ST    R7,QMIRECAD           ADDRESS IN PL               Y02083
         ST    RD,CKERAS09         SAVE REG 13                   YM3982
         LA    RD,CKREGSAV         PASS SAVEAREA ADDR TO QMNGRIO YM3982
         QMNGRIO                   READ/WRITE TO SWA             Y02083
         DROP  R1                  PARM LIST ADDRESSING          Y02083
         L     RD,CKERAS09         RESTORE REG 13                YM3982
         BR    R5                  RETURN TO CALLER              Y02083
         EJECT
CHKDSS2D LR    R1,R6                                             Y02083
         L     R5,CKERAS07         RESTORE SVRB ADDR             Y02083
         USING HKPSECT,R5          ADDRESS CHKPT SVRB            YM7324
         MVC   HKPGLIST(LNXCTLST),CHKXLIST
*                                                                     *
***********************************************************************
*                                                                     *
*        IF CANCEL IS REQUESTED, XCTL TO IGC0206C; OTHERWISE, TRANSFER*
*        CONTROL TO NEXT LOAD, IGC0106C.                              *
*                                                                     *
***********************************************************************
         TM    CKFLAG1,CKCANCEL    IS THIS A CANCEL REQUEST
         BO    CHKCANC2            YES, BRANCH
CHKRET   EQU   *
         LA    RF,HKPGLIST
         ST    RF,HKPNTLST
         LA    RF,HKPNTLST
         XCTL  SF=(E,(15))
*                                                                     *
***********************************************************************
*                                                                     *
*        RETURN TO USER WHEN CHECKPOINTS ARE TO BE SUPPRESSED         *
*                                                                     *
***********************************************************************
*                                                                     *
CHKSUP   EQU   *
         LA    RF,CKNORM           LOAD RETURN CODE.
         SVC   3                   ISSUE EXIT SVC.
         EJECT
***********************************************************************
*                                                                     *
*        ERROR EXIT ROUTINES - SETS MESSAGE AND RETURN CODE IN SVRB   *
*        EXTENDED WORK AREA, FREEMAINS INVALID CHKPT W.A. IF NECESSARY*
*        AND XCTL'S TO EXIT ROUTINE, IGC0Q06C.                        *
*                                                                     *
***********************************************************************
*                                                                     *
CHKERX5A MVI   CKRETCOD+K1,CKIOERR SET I/O ERR IN RET CODE       Y02076
         B     CHKERX5             BR TO CONTINUE EXIT           Y02076
CHKERX5B MVI   CKRETCOD+K1,CKPGMERR SET PGM ERR RET CODE         Y02076
CHKERX5  L     R5,CKERAS07         RESTORE SVRB ADDR             Y02083
         STH   RA,CKMSGCOD         SET ERR MSG CODE              Y02076
         LR    R1,R6               RESTORE PTR TO W.A.           Y02076
         B     CHKERX6             GO SET UP XCTL                Y02076
CHKERX   EQU   *
         OI    HKPFLAG,HKPEXIT     INDICATES ACTION TO BE TAKEN IN CER
CHKERCOD EQU   *
         MVI   HKPRETCD,CKPGMERR   PUT RETURN CODE IN PARM. AREA
CHKERMSG EQU   *
         STH   RA,HKPMSGCD         PUT MESSAGE CODE IN PARM AREA
         XR    R1,R1               SET UP PARM REG - NO CHKPT WA
CHKCER   EQU   *
         OI    HKPFLAG,CKMSG1      INDICATES MSG-ENVIRONMENT ERROR
CHKERX6  MVC   HKPGLIST(8),CHKXLIST SET XCTL PARLIST TO GO TO CER.
         MVI   HKPWD2,Q06C
         B     CHKRET              GO TO XCTL TO IGC0Q06C.
CHKERG   EQU   *
CHKERC   EQU   *
         TM HKPFLAG,CKOPEN         DID CHKPT OPEN DS
         BZ    CHKERX              NO-GO TO INDICATE ACTION IN CER
         OI    HKPFLAG,HKPCLOSE    INDICATES ACTION TO BE TAKEN IN CER
         LR    R2,RC               LOAD PARM REG WITH ADDR DCB
         B     CHKERCOD            GO TO STORE ERROR CODES IN CHKWA
         EJECT
***********************************************************************
*                                                                     *
*        TALLY LENGTH OF TIOT AND POINT TO NEXT DD ENTRY IN TIOT      *
*                                                                     *
***********************************************************************
*                                                                     *
CHKTIL   EQU   *
         USING TIOENTRY,R9         ADDRESS DD ENTRY IN TIOT
         IC    R6,TIOELNGH
         AR    R7,R6               UPDATE TIOT-LENGTH ACCUMULATOR
         AR    R9,R6               UPDATE DD ENTRY POINTER.
         B     CHKETI              RETURN TO TIOT-LENGTH-LOOP
         USING DEBBASIC,R8         ADDRESS DEBS.
*                                                                     *
***********************************************************************
*                                                                     *
*        INCREMENT NUMBER OF DEB'S AND POINT TO NEXT DEB              *
*                                                                     *
***********************************************************************
*                                                                     *
CHKNXDB  EQU   *
         LA    R6,1(0,R6)          INCREMENT DEB COUNTER.
         L     R8,DEBDEBAD         GET ADDR OF NEXT DEB.
         B     CHKLSDB             GO TO CHECK FOR END OF DEB CHAIN.
         DROP  R8
*                                                                     *
***********************************************************************
*                                                                     *
*        SET UP PARAMETER LIST AND ISSUE CONDITIONAL GETMAIN FOR      *
*        WORK AREA                                                    *
*                                                                     *
***********************************************************************
*                                                                     *
CHKCOMG  EQU   *
         LA    RE,HKPGLIST         SET UP GETMAIN PARM LIST
         ST    R7,HKPGLIST
         LA    R0,HKPGML
         LA    RA,CKERPG3          LOAD MESSAGE CODE IF ERROR OCCURS.
         LA    R8,HKPGMLAD
         TM    TCBFLGS6,TCBRV      IS THIS TASK V=R            @ZA27494
         BNO   GET253              NO,USE SUBPOOL 253          @ZA27494
         GETMAIN EC,A=(R8),SP=252,MF=(E,(RE)) YES,USE SP252    @ZA27494
         B     CONTIN                                          @ZA27494
GET253   EQU   *                                               @ZA27494
         GETMAIN EC,A=(R8),SP=253,MF=(E,(RE))
CONTIN   EQU   *                                               @ZA27494
         L     R6,HKPGMLAD         GET ADDR OF W.A.
         LA    R6,0(R6)            CLEAR HIGH ORDER BYTE.
         XC    HKPGML2(HKPGML-HKPGML2),HKPGML2
*                                  CLEAR AREA TO STORE ERROR CODES
         LR    R8,R7               SAVE LENGTH OF W.A. FOR CANCEL RTN
         BR    R9                  RETURN TO CALLER
         EJECT
***********************************************************************
*                                                                     *
*        FOR CANCEL REQUEST, ISSUE CONDITIONAL GETMAIN FOR WORK AREA  *
*        NEEDED AND BRANCH TO INITIALIZE WORK AREA                    *
*                                                                     *
***********************************************************************
*                                                                     *
CHKCANC1 EQU   *
         OI    HKPFLAG,CKCANCEL    SET CANCEL INDICATOR
         LA    R7,LENCANCL         GET LENGTH OF W.A.
         BAL   R9,CHKCOMG          GO TO GETMAIN FOR W.A.
         B     *+4(RF)             WAS GETMAIN SUCCESSFUL
         B     CHKCCN              YES-CONTINUE PROCESSING CANCEL REQ.
         B     CHKERC              NO-GO TO ERROR EXIT ROUTINE
         USING CHKWA,R6            ADDRESS W.A.
CHKCCN   EQU   *
         XC    CHKWA(ZLN),CHKWA    ZERO OUT W.A.
         B     CHKCOCNC            GO TO INITIALIZE CANCEL WA
*                                                                     *
***********************************************************************
*                                                                     *
*        ALTER XCTL PARAMETER LIST FOR CANCEL REQUEST TO TRANSFER     *
*        CONTROL TO IGC0206C.                                         *
*                                                                     *
***********************************************************************
*                                                                     *
CHKCANC2 EQU   *
         MVI   HKPWD2,C206C        ALTER XCTL PARM LIST SO THAT
*                                  IGG0206C WILL GET CONTROL NEXT.
         B     CHKRET              GO TO XCTL
         EJECT
**********************************************************************
*       ESTABLISH ESTAE ENVIRONMENT                              YM5913
**********************************************************************
CHKESTES LA    R5,CHKESRTN         GET ESTAE ENTRY PT            YM5913
         LR    R1,R6               GET WORK AREA PTR             YM5913
         A     R1,CKBFOFST         POINT TO PARM BUFFER          YM5913
         MVC   K0(CHKESTLN,R1),CHKESTAE MOVE PARM LIST TO BUFF   YM5913
         STM   R0,RF,CKREGSAV      SAVE RETRY REGS               YM7082
         ESTAE (R5),PARAM=(R6),MF=(E,(R1)) ISSUE ESTAE           YM5913
         BR    RE                  RETURN TO MAINLINE            YM5913
**********************************************************************
*     CANCEL ESTAE ENVIRONMENT                                   YM5913
**********************************************************************
CHKESTCN ESTAE 0                   CANCEL ESTAE                  YM5913
         BR    RE                  RETURN TO MAINLINE            YM5913
**********************************************************************
*     ESTAE ROUTINE TESTS IF AN ABEND 913 - 28 OCCURRED          YM5913
**********************************************************************
CHKESRTN CH    R0,RTNCOD12-CHKESRTN(RF) DID RTM GET AN RTCA      YM7082
         BNE   CHKESTB2-CHKESRTN(RF) BR IF YES                   YM7082
*        IF NOT, DON'T BOTHER TRYING TO AVOID AN ABEND           YM7008
         SR    R0,R0               ZERO RETRY                    YM5913
         SR    RF,RF               TELL RTM TO TERM              YM5913
         BR    RE                  RETURN TO RTM                 YM5913
CHKESTB2 STM   RE,RC,12(RD)        SAVE RTM'S REGS               YM7082
         LR    RB,RF               ESTABLISH CODE BASE           YM7082
         LA    RA,CHKESRTN-BASE1     AS ORIGINALLY               YM7082
         SR    RB,RA                   ASSEMBLED                 YM7082
         USING SDWA,R1             ADDRESS SDWA                  YM7008
         L     R6,SDWAPARM         GET CHKPT WA ADDR             YM7008
         CLI   SDWAERRA,SDWASVCD   WAS ABEND ISSUED              YM7008
         BNE   CHKESTB3            BR IF NOT                     YM7008
         L     R0,SDWAABCC         GET ABEND CODE                YM7008
         SLL   R0,L8               SHIFT OFF HIORDER BYTE        YM7008
         SRL   R0,L8+L12           SHIFT ABEND CODE RIGHT        YM7008
         CH    R0,AB913            IS IT ABEND 913               YM7008
         BNE   CHKESTB3            BR IF NOT                     YM7008
         L     RF,SDWAGR15         GET 913 DASH NO.              YM7008
         CH    RF,DASH28           IS IT 913-28                  YM7008
         BNE   CHKESTB3            BR IF NOT                     YM7008
         MVC   SDWASR00(64),CKREGSAV MOVE IN RETRY REGS          YM7082
         SETRP RC=4,RETADDR=CHKESTB4,FRESDWA=YES,REGS=(14,12),   YM7008C
               RETREGS=YES,DUMP=NO,RECORD=NO GO RETRY            YM7008
CHKESTB3 SETRP RC=0,REGS=(14,12)   CONTINUE WITH TERM            YM7008
*****************************************************************YM7008
*      ESTAE RETRY ENTRY FOR CONVERSION OF 913-28 ABEND          YM7008
*      TO A (007) ERROR CODE FAILURE - USER SPECIFIED            YM7008
*      OTHER THAN BSAM OR BPAM AS THE ACCESS METHOD TYPE         YM7008
*      FOR THE CHECKPOINT DATA SET.                              YM7008
*****************************************************************YM7008
CHKESTB4 EQU   *                   (007) RETRY ENTRY             YM7008
         BAL   RE,CHKESTCN         CANCEL ESTAE ENVIRONMENT      YM7008
         LA    RA,CKERPG07         LOAD ERROR CODE (07)          YM5913
         B     CHKERX5B            BR TO ERR EXIT                YM5913
         EJECT
*                                                                     *
***********************************************************************
*                                                                     *
*        CONSTANTS AND GENERAL EQUATES                                *
*                                                                     *
***********************************************************************
*                                                                     *
RTNCOD12 DC    H'12'               RTCA CODE                     YM5913
AB913    DC    H'2323'             ABEND 913                     YM5913
DASH28   DC    H'40'               DASH 28 CODE                  YM5913
ABLEV    DC    CL2'AV'             DUMMY DEB MODULE            @ZA30032
CHKESTAE ESTAE CHKESTAE,CT,PARAM=CHKESTAE,TERM=YES,              YM5913X
               RECORD=YES,MF=L     ESTAE PARM LIST               YM5913
CHKESTLN EQU   *-CHKESTAE          PARM LIST LNGTH               YM5913
         DS    0F
CHKXLIST DC    C'IGC0106C'         MODULE NAME FOR XCTL PARMLST
CHKQNAME DC    C'SYSDSN  '         QNAME FOR ENQ TEST            Y02083
WAK      DC    H'5316'             FIXED LENGTH OF WA(1220+4096) Y02076
SSCRBUFL DC    H'4096'             SSCR BUFFER LENGTH            Y02076
CHK8     DC    H'8'                CONSTANT USED TO GET NEXT DBLWD ADDR
KCCW123  DC    X'31',XL7'05'       CONSTANT FOR CCW1             Y02083
         DC    X'08',XL7'00'       CONSTANT FOR CCW2             Y02083
         DC    X'0E',XL3'00',X'2000008C' CONSTANT FOR CCW3       Y02083
KPDS     DC    X'000E',8X'FF',4X'00' VALUE FOR AN EMPTY PDS DIR. Y02083
REPDCB1  DC    H'48'               AMOUNT OF EXTRA CORE NEEDED BY
MAXTRK   DC    X'7FFF'             MAX DA TRACK CAPACITY         Y02083
VALIDERR EQU   98                  VALIDITY CHECK ERR CODE       Y02076
SYSGENID EQU   7                   SYSTEM GENERATED CHECKPOINT   YM5793
*                                  ID LENGTH - 1                 YM5793
CURASOFS EQU   12                  OFFSET TO CURRENT ASCB        Y02076
ADRCVT   EQU   16                  ADDR OF CVT PTR               Y02083
*                                                                     *
*        MAINTENANCE SPACE                                            *
*                                                                     *
PATCH    DC    XL((*-IGC0006C)/20)'00' FIVE PERCENT MAINTENANCE SPACE
*                                                                     *
         EJECT
         IHASDWA                                                 YM5913
         IHAPSA                                                  Y02076
         EJECT
         IHAASCB                                                 Y02076
         EJECT
         IHALDA                                                  Y02076
         EJECT
HKPSECT  DSECT
HKPSVRB  DS    24F                 INITIAL SECTION OF SVRB
HKPGML2  DS    0F                  SAVE AREA FOR ERR AND MSG CODES
         DS    C                   RETURN CODE
HKPRETCD DS    C
HKPMSGCD DS    H                   MESSAGE CODE
HKPGML   DS    F                   GETMAIN PARAMETER LIST:
HKPGMLAD DS    2F                  LNG AND ADDR OF GETMAIN'D AREA
HKPGLIST DS    1F                  PARAMETER LIST AREA
HKPWD2   DS    F
HKPNTLST DS    F
HKPTIOTL DS    H                   LENGTH OF TIOT
HKPBUFL  DS    H                   UNUSED
HKPTIO   DS    0F
HKPFLAG  DS    1C                  FLAG BYTE
HKPCLOSE EQU   16                  INDICATES CER MUST CLOSE CHKPT D.S.
HKPEXIT  EQU   1
HKPTIOAD DS    3C                  ADDRESS OF TIOT
HKPWD1   DS    0F
HKPSP    DS    1C                  USED TO CHECK BOUNDARY ALIGNMENT
HKPDCBAD DS    3C                  ADDRESS CHKPT DCB
HKPSAVRN DS    F                   UNUSED
         EJECT
*        CHKPT PARAMETER LIST
PARMLST  DSECT
DCBADDR  DS    0CL4
FLAG     DS    CL1                 FLAG BYTE
NOCHKID  EQU   X'80'               SYSTEM-GENERATED CHECKID NOT
*                                  RETURNED TO USER
UCHKID   EQU   X'00'               USER-SPECIFIED CHECKID
DCBORCN  DS    CL3                 ADDRESS OF CHKPT DCB
IDADDR   DS    0CL4
LNGID    DS    CL1                 LENGTH OF USER SPECIFIED CHKID
RTNCKID  EQU   X'FF'               SYSTEM-GENERATED CHECKID RETURNED
*                                  TO USER
         DS    CL3                 ADDR OF CHECKID
ADDRLNG  EQU   3                   LENGTH OF ADDRESS
         EJECT
         IEEVCHWA   VER=3          USE AOS2 VERSION OF MACRO
JFCBSAVE EQU   CKJCTMB             SAVEAREA FOR JFCB STATUS      Y02083
SIOTSAVE EQU   CKJCTMB+1           SAVEAREA FOR SIOT DISP        Y02083
         ORG   CKJCT                                             Y02083
XDCB     DS    F                   DATA CONTROL BLOCK            Y02083
         DS    X                   FILLER                        Y02083
XDCBFDAD DS    8X                  FULL DISK ADDRESS             Y02083
         DS    15X                 FILLER                        Y02083
XDCBIOBA DS    F                   IOB ADDRESS                   Y02083
         DS    3F                  FILLER                        Y02083
XDCBDEBA DS    F                   DEB ADDRESS                   Y02083
XDEB     DS    0F                  DATA EXTENT BLOCK             Y02083
         DS    4F                  FILLER                        Y02083
XDEBNMEX DS    F                   NUMBER OF EXTENTS             Y02083
         DS    F                   FILLER                        Y02083
XDEBDEBI DS    0F                  DEB ID AND KEY                Y02083
XDEBDCBA DS    F                   DCB ADDRESS                   Y02083
XDEBEXSC DS    0F                  EXTENT SCALE                  Y02083
XDEBAPPA DS    F                   APPENDAGE ADDRESS             Y02083
XDEBDVMO DS    X                   DEVICE MODIFIER               Y02083
XDEBUCBA DS    3X                  UCB ADDRESS                   Y02083
XDEBBINU DS    H                   BIN NUMBER                    Y02083
XDEBSTRC DS    H                   CYLINDER START ADDRESS        Y02083
XDEBSTRH DS    H                   HEAD START ADDRESS            Y02083
XDEBENDC DS    H                   CYLINDER END ADDRESS          Y02083
XDEBENDH DS    H                   HEAD END ADDRESS              Y02083
XDEBNMTR DS    H                   NUMBER OF TRACKS              Y02083
XIOB     DS    0F                  IO REQUEST BLOCK              Y02083
XIOBFLG1 DS    X                   FLAG 1 FIELD                  Y02083
         DS    3X                  FILLER                        Y02083
XIOBECBA DS    F                   ECB ADDRESS                   Y02083
         DS    2F                  FILLER                        Y02083
XIOBCPA  DS    F                   CHANNEL PROGRAM ADDRESS       Y02083
XIOBDCBA DS    F                   DCB ADDRESS                   Y02083
         DS    2F                  FILLER                        Y02083
XIOBFDAD DS    2F                  FULL DISK ADDRESS             Y02083
XECB     DS    F                   EVENT CONTROL BLOCK           Y02083
XCCW1    DS    D                   CCW 1                         Y02083
XCCW2    DS    D                   CCW 2                         Y02083
XCCW3    DS    D                   CCW 3                         Y02083
XBUF     DS    140C                READ/WRITE BUFFER             Y02083
XDSN     DS    44C                 HOLD AREA FOR CHKPT DSN       Y02083
XEND     EQU   *                   END OF WORK SPACE             Y02083
CHRDEC   DS    D
CHRCON   DS    5H
CHRAD    DS    F
CHRXL    DS    CL8                 NAME OF MODULE TO TRANSFER
*                                  CONTROL TO
         EJECT
         IEFQMIOP                                                Y02083
QMNGRWA  DS    88C                 QUEUE MANAGER WORKAREA        Y02083
QMNGRBUF DS    176C                QUEUE MANAGER BUFFER          Y02083
TEMPSAVE DS    6F                  TEMP REG SAVEAREA             YM4683
         EJECT
TIOTSECT DSECT
         IEFTIOT1
         EJECT
         DCBD  DSORG=(BS,PO),DEVD=(DA,TA)
         EJECT
CVTSECT  DSECT
         CVT
         EJECT
         IKJTCB
         EJECT
         IEZDEB
         EJECT
*        MAPPING OF PQE
         IHAPQE
         EJECT
         IEFUCBOB PREFIX=YES,LIST=YES                            Y02083
         EJECT
         IHADSAB                                                 Y02083
         EJECT
         IHAQDB                                                  Y02083
         EJECT
         IEZJSCB                                                 Y02083
         EJECT
         IEFASIOT                                                Y02083
         EJECT
         IEFJFCBN                                                Y02083
         EJECT
         IEFJESCT                                                Y02083
         EJECT
         IDDVDSCB
         EJECT
         IECSDSL1 (1)
         EJECT
OPENOPT  EQU   143                 OPTION BYTE FOR OPEN'S PARM LIST
CLOSEOPT EQU   X'B0'               CLOSE OPTION FLAGS            Y02083
CKVEQR   EQU   8                   V=R FLAG IN W.A.              YM3092
USERAUTH EQU   X'10'               AUTHORIZED-USER FLAG IN W.A.  YM1237
LENCANCL EQU   CHRXL+8-CHKWA       LENGTH OF W.A. NEED
CHKWAL   EQU   DXCCW3+8-CHKWA      LENGTH OF CHKWA.
TAPEDML  EQU   48                  LENGTH OF IOB/CP FOR TAPE TO
*                                  BE USED AT RESTART
DADML    EQU   88                  LENGTH OF IOB/CP FOR D.A.
*                                  TO BE USED AT RESTART
DADEB    EQU   344                 MAXIMUM LENGTH FOR D.A. DEB.
TAPEDEB  EQU   92                  MAXIMUM LENGTH FOR TAPE DEB.
TIOLEN   EQU   28                  LENGTH OF TIOT THAT IS NOT
*                                  PART OF A DD ENTRY
ZLN      EQU   256                 LENGTH OF W.A. TO BE ZEROED OUT.
DADCK    EQU   32                  DCBDEVT FIELD- INDICATES DIRECT
*                                  ACCESS DEVICE
CKSUPRS  EQU   64                  TIOESTTA FIELD-INDICATES CHKPT
*                                  SUPPRESS REQUEST
DD2L     EQU   44                  NO. OF BYTES TIOT IS ALLOWED
*                                  TO INCREASE AT RESTART
LOWBYTE  EQU   3                   ADDRESSING LOW-ORDER BYTE OF A WORD
LNXSA    EQU   48                  LENGTH OF SVRB EXTENDED SAVE AREA
Q06C     EQU   C'Q'                CHARACTER TO ALTER XCTL NAME
DBLWDBND EQU   X'07'               TEST FOR DOUBLE WORD BDRY ADDR
C206C    EQU   C'2'                CHARACTER TO ALTER XCTL NAME
LNXCTLST EQU   8                   LENGTH OF XCTL PARM LIST
ZERO     EQU   8                   CONDITION CODE SETTING FOR TM INSTR
WDBNDTST EQU   X'03'               TEST FOR ADDRESS ON WORD BOUNDARY
LNG      EQU   2                   LENGTH OF HALFWORD
WDLNG    EQU   4                   LENGTH OF WORD
ONE      EQU   1                   LENGTH OF FLAG BYTE
TRUNC3   EQU   3                   USED TO MAKE ADDR MULTIPLE OF 8
CKTIOTLN EQU   CKTIOLN+2
CKDCB    EQU   CKDCBAD+1
CKDCB2   EQU   CKDCBAD2+1
CKMSG1   EQU   32                  WTO MESSAGE = NOT TAKEN
CKERPG1  EQU   X'01'               PARAMETER LIST ERROR
CKERPG2  EQU   X'02'               OPEN UNSUCCESSFUL
CKERPG3  EQU   X'03'               GETMAIN UNSUCCSSSFUL
CKERPG12 EQU   12                  ERROR CODE FOR DAUGHTER TASKS Y02083
CKERPG11 EQU   11                  ERROR CODE FOR SUBTASKING     Y02083
CKERPG16 EQU   16                  ERROR CODE FOR INVALID DEVICE Y02083
CKERPG19 EQU   19                  ERROR CODE FOR OPEN FOR INPUT YM7008
CKERP202 EQU   202                 ERROR CODE FOR SETLOCK ERROR  Y02076
CKERP208 EQU   208                 ERROR CODE FOR BLK CNT ERR    Y02083
CKERP214 EQU   214                 ERROR CODE FOR SHARED DA      Y02083
CKERPG07 EQU   7                   ERROR CODE FOR INVALID DSORG  Y02083
CKERP209 EQU   209                 ERROR CODE FOR MULTIPLE OPENS Y02083
CKERP210 EQU   210                 ERROR CODE FOR DISP=SHR ERR   Y02083
CKERP211 EQU   211                 ERROR CODE FOR NON-SECURE D/S Y02083
CKERP212 EQU   212                 ERROR CODE FOR SUBSYSTEM D/S  Y02083
CKERP213 EQU   213                 ERROR CODE FOR NON SL TAPE    Y02083
ADDRBYTS EQU   X'07'               REG MASK FOR LO-ORDER 3 BYTES Y02083
K0       EQU   0                   CONSTANT OF 0                 Y02083
SIX      EQU   6                   CONSTANT OF 6                 Y02083
K7       EQU   7                   CONSTANT OF 7                 Y02083
TWO      EQU   2                   CONSTANT OF 2                 Y02083
FOUR     EQU   4                   CONSTANT OF 4                 Y02083
L15      EQU   15                  CONSTANT OF 15                Y02083
L8       EQU   8                   CONSTANT OF 8                 Y02083
THREE    EQU   3                   CONSTANT OF 3                 Y02083
L24      EQU   24                  CONSTANT OF 24                Y02083
L6       EQU   6                   CONSTANT OF 6                 Y02083
K16      EQU   16                  CONSTANT OF 16                Y02083
M7       EQU   7                   REG MASK FOR LOW 3 BYTES      Y02083
M14      EQU   14                  REG MASK FOR HI 3 BYTES       Y02083
K176     EQU   176                 LENGTH OF JFCB AND SIOT       Y02083
M1       EQU   1                   REG MASK FOR LOW BYTE         Y02083
KEEP     EQU   8                   SCT/SCTSDISP DISP=KEEP FLAG   Y02083
RDDATACM EQU   X'06'               READ DATA CCW COMMAND         Y02083
CC       EQU   X'40'               COMMAND CHAINING              YM5717
K4       EQU   4                   CONSTANT OF 4                 YM5717
K10      EQU   10                  CONSTANT OF 10                YM5717
K14      EQU   14                  CONSTANT OF 14                Y02083
L12      EQU   12                  CONSTANT OF 12                Y02083
K1       EQU   1                   CONSTANT OF 1                 Y02083
BLANK    EQU   C' '                BLANK CONSTANT                Y02083
X7F      EQU   X'7F'               ECB TEST MASK                 Y02083
XFF      EQU   X'FF'               BIT RESET MASK                Y02083
DS1CHKPT EQU   X'01'               DSCB CHKPT D/S FLAG           Y02083
WRTKDATA EQU   X'0D'               WRITE KEY/DATA CCW COMMAND    Y02083
X0F      EQU   X'0F'               DEB ID AND STORAGE KEY        Y02083
XC0      EQU   X'C0'               DEB DEVICE MODIFIER           Y02083
X02      EQU   X'02'               IOB UNRELATED FLAG            Y02083
K3       EQU   3                   CONSTANT OF 3                 Y02083
K44      EQU   44                  CONSTANT OF 44                Y02083
X00      EQU   X'00'               CONSTANT OF 0                 Y02083
X04      EQU   X'04'               DEB EXTENT SCALE              Y02083
K6       EQU   6                   CONSTANT OF 6                 Y02083
MF       EQU   15                  MASK FOR CHARACTER INSERTION  Y02083
X81      EQU   X'81'               FLAGS FOR ENQ PARM LIST       Y02083
OUTPUT   EQU   X'04'               COMMON DEB OUTPUT FLAG        YM7008
AMTYPIS  EQU   X'80'               ISAM FLAG IN DEBAMTYP         YM7076
ISEXTLEN EQU   16                  LENGTH OF ISAM DEPENDANT      YM7076
*                                  SECTION IN DEB                YM7076
RETCD08  EQU   8                   PREVIOUS ENQ RETURN CODE      YM7305
SHRFLAG  EQU   X'10'               ENQ'ED SHR FLAG               YM7305
         END
