*
  TITLE 'IGC0005B (IHJARS00) SVC 52 -- RESTART HOUSEKEEPING ROUTINE 1'
IGC0005B CSECT
***********************************************************************
*                                                                     *
*ROUTINE NAME    IGC0005B     RESTART HOUSEKEEPING ROUTINE 1          *
*                                                                     *
*STATUS - CHANGE LEVEL 000                                            *
*                                                                     *
*FUNCTION/OPERATION - 1.)  GET A WORK AREA OF THE SAME SIZE AS THE    *
*                          CHKPT WORK AREA.                           *
*                                                                     *
*                     2.)  INITIALIZE THE RESTART WORK AREA WITH      *
*                          INFORMATION PASSED FROM DSDR PROCESSING.   *
*                                                                     *
*                     3.)  IF ANY OF THE FOLLOWING ERRORS ARE         *
*                          ENCOUNTERED, THE INDICATED ERROR MESSAGE   *
*                          CODE IS SET AND CONTROL IS TRANSFERRED     *
*                          TO IGC0V05B.                               *
*                             CODE   ERROR TYPE                       *
*                              216   REGION START ADDR NOT=CURRENT    *
*                                    TOP OF REGION                    *
*                              023   I/O ERROR READING STEP CONTROL   *
*                                    TABLE(SCT) FROM SCHEDULER WORK   *
*                                    AREA(SWA)                        *
*                              203   ILLEGAL CALL TO RESTART          *
*                                    (SCTSYSCK BIT IN STEP CONTROL    *
*                                    TABLE NOT SET)                   *
*                              002   INCORRECT V=R STATUS BETWEEN     *
*                                    CHECKPOINT AND RESTART           *
*                                                                     *
*                     4.)  CONSTRUCT A DCB FOR THE CHKPT DATA SET.    *
*                                                                     *
*                     5.)  OPEN THE CHKPT DATA SET.                   *
*                                                                     *
*ENTRY POINT - IGC0005B                                               *
*                                                                     *
*INPUT - ADDRESS OF THE FOLLOWING PARAMETER LIST WILL BE PASSED IN    *
*        REGISTER 1.                                                  *
*    NAME     LNGTH   DESCRIPTION                                     *
*    REPCIRAD   4     TTR OF 1ST CIR                                  *
*    REPCOUNT   2     NO. OF CHKPTS TAKEN                             *
*    REPCKIDL   2     LENGTH OF CHECKID                               *
*    REPCHKID  16     CHECKID                                         *
*    REPDDNM    8     DDNAME OF CHKPT D/S USED FOR RESTART            *
*    REPPPM     4     LO ORDER ADDR OF P/P AREA                       *
*    REPPPE     4     SIZE OF P/P AREA                                *
*    REPBLKSI   2     CHKPT D/S BLOCK SIZE                            *
*    REPTIOTL   2     LENGTH OF TIOT                                  *
*    REPFLAGS   1     CHECKPOINT FLAGS 1                              *
*                     X'80' - AUTO RESTART                            *
*                     X'40' - TRACK OVERFLOW SPECIFIED                *
*                     X'20' - CHKPT D/S ON TAPE                       *
*                     X'10' - USER AUTH FOR MODESET                   *
*                     X'08' - V=R REQUEST                             *
*                     X'02' - CHECKPOINT ISSUED OPEN                  *
*                     X'01' - CHECKPOINT D/S OPEN FOR BPAM            *
*    REPWACL    3     CHECKPOINT WORK AREA LENGTH                     *
*    REPFLAG2   1     CHECKPOINT FLAGS 2                              *
*                     X'04' - USER SUPPLIED CHECKID                   *
*                                                                     *
*OUTPUT - ADDRESS OF RESTART WORK AREA WILL BE IN REGISTER 1.         *
*                                                                     *
*EXTERNAL ROUTINES - QMNGRIO                                          *
*SVCS USED - GETMAIN                                                  *
*            FREEMAIN                                                 *
*            XCTL                                                     *
*            OPEN                                                     *
*            TESTAUTH                                                 *
*                                                                     *
*EXITS-NORMAL  XCTL TO IGC0205B WITH ADDR OF RSTRT W.A. IN            *
*             REGISTER 1.                                             *
*                                                                     *
*EXITS-ERROR  XCTL TO IGC0V05B WITH ADDR OF RSTRT W.A. IN REG 1,      *
*             AND ERROR CODE IN RSRETCOD                              *
*                                                                     *
*TABLES/WORK AREAS-  RSTWA IS THE FIRST PART OF THE RESTART WORK AREA.*
*                  THIS ROUTINE FILLS IN RSTWA WITH INFORMATION THAT  *
*                  IS USED BY OTHER RESTART ROUTINES.                 *
*                  TABLES - DCB,TIOT,TCB,PSA,ASCB,LDA,PQE,CVT,SCT,    *
*                           DSAB,QDB,JSCB,QMIOP,JESCT,RESTART         *
*                           SVRB+EXTENDED SAVE AREA,RESTART PARM LIST *
*                                                                     *
*ATTRIBUTES - REENTRANT, SUPERVISOR STATE, TYPE 4 SVC ROUTINE.        *
*                                                                     *
*RESTRICTIONS ON USE OF ROUTINE - VS/2-2 ONLY                         *
*                                                                     *
*   CHANGES FOR RELEASE 3.7
*A762100,C447000,C615000                                       @ZA18280
* APAR OZ31477 HAS BEEN FLAGGED AS @ZSUT222                    @ZSUT222
*A176500,176700-177000,196507-196513,387665-387685,562000      @ZSUT222
*C029920,178690,387695,796500,797100                           @ZSUT222
*D387705                                                       @ZSUT222
*NOTES - NONE.                                                        *
***********************************************************************
         EJECT
         BALR  RB,ZERO             SET UP MODULE ADDRESSABILITY
         USING *,RB
         B     BEGIN               BRANCH AROUND EYE CATCHER
         CNOP  0,8                 ALIGN TO A DOUBLE WORD BOUNDARY
         DC    CL8'IGC0005B'       MODULE NAME                 @ZSUT222
         DC    CL8'&SYSDATE'       LAST DATE SHIPPED           @ZSUT222
         DC    CL8'@ZSUT222'       LAST CODE SHIPPED           @ZSUT222
BEGIN    EQU   *
         USING RSTSECT,R5          ADDRESS RESTART WORK AREA.
*                                                                     *
***********************************************************************
*                                                                     *
*        THIS SECTION WILL GET CORE FOR THE RESTART WORK AREA.        *
*                                                                     *
***********************************************************************
*                                                                     *
         LH    R0,MINWALN          PRELOAD MINIMUM WA SIZE       YM7063
         LTR   R1,R1               PARM ANCHOR VALID             YM7063
         BZ    RSGETWA             IF NOT, USE MINIMUM SIZE      YM7063
         ICM   R3,ICMMSK,0(R1)     GET/TEST PARM ADDR            YM7063
         BZ    RSGETWA             IF ZERO, USE MINIMUM SIZE     YM7063
         USING PARMSECT,R3         ADDRESS PARM FIELD            YM6541
         CLM   R0,ADDRBYTS,REPWACL WAS MORE THAN MINIMUM PASSED  YM7063
         BNL   RSGETWA             IF NOT, USE MINIMUM SIZE      YM7063
         ICM   R0,ADDRBYTS,REPWACL USE LENGTH PASSED BY CALLER   YM7063
RSGETWA  ICM   R0,HIBYTE,SP253     INSERT SUBPOOL NUMBER         YM7063
         LR    RD,R0               SAVE THE LENGTH REQUESTED   @ZSUT222
         GETMAIN  R,LV=(R0)        OBTAIN WORK AREA              YM6541
         SR    R8,R8               CLEAR REG 8                 @ZSUT222
         SR    R9,R9               CLEAR REG 9                 @ZSUT222
         LR    RC,R1               BEGIN ADDRESS TO CLEAR      @ZSUT222
         MVCL  RC,R8               CLEAR THE GOTTEN STORAGE    @ZSUT222
         LR    RC,R1               RELOAD ADDR OF THE WORKAREA   YM6541
         USING RSTWA,RC            ADDRESS RESTART WORKAREA      YM6541
         ST    R4,RSTCBAD          SAVE TCB ADDRESS              YM7063
         USING TCB,R4              ADDRESS TCB                   YM7063
         LA    R2,RSTIOT-RSTWA     SETUP TEMPORARY               YM7063
         ST    R2,RSBFOFST           SUB-WORKAREA FOR            YM7063
         AR    R2,RC                   POSSIBLE ERROR PROCESSING YM7063
*                                                                YM7063
*****************************************************************YM7063
*                                                                YM7063
*        CHECK THE SCT (STEP CONTROL BLK) TO ENSURE THAT         YM7063
*        IEFRSTRT WAS INVOKED BY IEFDSDRP.                       YM7063
*                                                                YM7063
*****************************************************************YM7063
*                                                                YM7063
         LA    RD,RSREGSAV         INIT REG SAVEAREA ADDR        YM7063
         L     R1,TCBJSCB          GET JSCB PTR                  YM7063
         USING IEZJSCB,R1          ADDRESS JSCB                  YM7063
         L     R9,JSCSCT           GET SVA ADR OF SCT            YM7063
         DROP  R1                  JSCB                          YM7063
         LA    R1,RSTIOT           GET @ OF QMNGR PARM LIST      YM7063
         USING QMIOP,R1            ADDRESS PARM LIST             YM7063
         ST    R4,QMITCBAD         STORE TCB @ IN PARM LIST      YM7063
         MVI   QMIFLAGS,QMIOREAD   SET READ FLAG IN PARM LIST    YM7063
         LA    R0,K176             SET RECORD LENGTH IN PARM     YM7063
         STH   R0,QMIRECL            LIST TO 176                 YM7063
         SLL   R9,8                     SHIFT TTR LEFT 8         YM7063
         ST    R9,QMIDSKAD         SET SVA FOR SCT IN PARM LIST  YM7063
         LA    R0,QMNGRWA          SET QMNGR WORK AREA ADDRESS   YM7063
         ST    R0,QMIWRKAD           IN PARM LIST                YM7063
         LA    R9,QMNGRBUF         SET RECORD BUFFER ADDRESS     YM7063
         ST    R9,QMIRECAD           IN PARM LIST                YM7063
         QMNGRIO                   READ SCT                      YM7063
         LTR   RF,RF               TEST RETURN CODE              YM7063
         BZ    RSSYSCK             BR IF READ OK                 YM7063
         MVI   RSRETCOD+K1,ERR023  SET SWA READ ERR            @ZSUT222
         B     RSPREXIT            BR TO ERROR EXIT              YM7063
         DROP  R1                  PARM LIST ADDRESSING          YM7063
         USING INSMSCT,R9          ADDRESS SCT                   YM7063
RSSYSCK  TM    SCTSTEND,SCTSYSCK   IS RESTART CALL LEGITIMATE    YM7063
         DROP  R9                  SCT ADDRESSING                YM7063
         BO    RSOKCALL            IF LEGITIMATE CALL, BR        YM7063
         MVI   RSRETCOD+K1,ERR203  SET ERROR CODE                YM7063
         B     RSPREXIT            GO XCTL TO IGC0V05B           YM7063
RSOKCALL EQU   *                   CALL WAS LEGITIMATE           YM7063
*                                                                YM7063
*****************************************************************YM7063
*                                                                YM7063
*        RESTART CALL IS LEGITIMATE; BEGIN PROCESSING -          YM7063
*                                                                YM7063
*****************************************************************YM7063
*                                                                YM7063
         SR    R7,R7               CLEAR WORK REG                YM6541
         ICM   R7,ADDRBYTS,REPWACL GET LENGTH OF RST WA          YM6541
         LH    R8,REPLNGTH         GET LENGTH OF RST PARMS       YM6541
         AR    R7,RC               GET END OF RST WA +1          YM6541
         SR    R7,R8               BACK UP TO GET PARM SAVE ADDR YM6541
         SH    R7,C0008            LEAVE ROOM FOR REPFLAG2       YM6541
*                                  AND RSCKPPML                  YM6541
         SRL   R7,3                ENSURE PARM SAVEAREA IS ON    YM6541
         SLL   R7,3                  A DOUBLE WORD BOUNDARY      YM6541
         LA    R8,1(,R8)           GET PARM MOVE LENGTH          YM6541
         EX    R8,PARMSAV          SAVE RESTART PARMS            YM6541
*PARMSAV MVC   0(1,R7),0(R3)       MOVE PARMS TO END OF RST WA   YM6541
         LR    R3,R7               UPDATE RST PARMS BASE REG     YM6541
         ST    R3,RSTPARM          SAVE ADDR OF RST PARMS        YM6541
*                                                                     *
***********************************************************************
*                                                                     *
*        ISSUE FREEPOOL FOR SUBPOOL 250, 251, AND 252 TO FREE CORE OF *
*        IEFRSTRT (STEP THAT ISSUED SVC 52), AND THE JOB PACK AREA.   *
*                                                                     *
***********************************************************************
*                                                                     *
         FREEMAIN R,SP=252
         FREEMAIN R,SP=251         FREE JOB PACK AREA            A36350
         FREEMAIN R,SP=250                                       YM6541
*                                                                YM6541
*****************************************************************YM6541
*                                                                YM6541
*        BEGIN WORKAREA INITIALIZATION                           YM6541
*                                                                YM6541
*****************************************************************YM6541
*                                                                YM6541
         L     RF,TCBPQE           GET ADDR OF DUMMY PQE         Y02076
         USING PQESECT,RF                                        Y02076
         L     RF,PQEFPQE               GET PQE PTR              Y02076
         L     R9,PQEREGN          GET REGION START ADDR         Y02076
         DROP  RF                                                Y02076
         L     R7,CVTPTR           GET ADR OF CVT                Y02076
         USING CVTMAP,R7                                         Y02076
         L     R8,CVTTCBP          GET ADR OF NEXT TCB           Y02076
         L     R8,CURASOFS(R8)     GET CURRENT ASCB              Y02076
         USING ASCB,R8                                           Y02076
         L     R8,ASCBLDA-ASCB(R8) GET PTR TO LDA                Y02076
         USING LDA,R8                                            Y02076
***********************************************************************
*                                                                     *
*        COMPUTE OFFSETS WITHIN W.A. BY INCREMENTING BY               *
*        LENGTH OF TIOT DURING CHKPT(CHKPT TIOT WILL BE MOVED TO W.A. *
*        DURING REPMAIN WHEN IT READS IN THE SURS).                   *
*                                                                     *
***********************************************************************
*                                                                     *
         LA    R2,RSTIOT           GET ADDR NEXT AVAILABLE BYTE  Y02076
         AH    R2,REPTIOTL         IN THE W.A. (FOR MVT)         Y02076
         SR    R2,RC               COMPUTE OFSET TO REPDCB'S AND Y02076
         ST    R2,RSWAOFST         REPMAIN'S W.A.                Y02076
         ST    R2,RSMWOFST         SAVE OFFSET IN W.A.           Y02076
         AR    R2,RC               GET ADDRESS OF W.A.           Y02076
         USING RSECT,R2            ADDR IGC0005B WORK AREA       Y02076
         L     R8,CURRGNTP         GET CURRENT TOP OF REG        Y02076
         CR    R9,R8               COMPARE REGION ADDRS          Y02076
         BE    BEGINB              BRANCH IF ADDRS OK            Y02076
         MVI   RSRETCOD+K1,ERR216  SET ERROR CODE                Y02076
RSPREXIT EQU   *                   SET UP FOR ERROR EXIT         Y02076
         MVC   RSREGSAV(8),MODID   SAVE NAME OF MODULE THAT    @ZSUT222
*                                  HAS DETECTED THE ERROR.     @ZSUT222
         STM   0,15,RSREGSAV+8     SAVE CURRENT REGISTERS      @ZSUT222
         MVC   RHRXLST(LMODNAME),ERREXIT  MVE NAME OF EXIT MOD   Y02076
         B     RSLDR1              BR TO EXIT                    YM4671
BEGINB   EQU   *                   TEST CALLER'S AUTH            Y02076
         L     R1,TCBJSCB          GET @ OF JSCB                 YM5774
         USING IEZJSCB,R1          ADDRESS JSCB                  YM5774
         L     R6,JSCDSABQ         GET @ OF DSAB QDB             YM5774
         TM    REPFLAGS,USERAUTH   TEST USER'S AUTH-AT CKPT      YM5774
         BO    RSAUTHA             BR IF AUTH FOR MODESET        YM5774
         NI    JSCBOPTS,X'FF'-JSCBAUTH RESET JSCB AUTH BIT       YM7800
         B     RSAUTHB             BRANCH TO CHECK DSABS         YM5958
RSAUTHA  OI    JSCBOPTS,JSCBAUTH   OR IN USER'S AUTH BIT         YM7800
RSAUTHB  EQU   *                   SET UP TO LOOK FOR CHKPT D.S. YM5958
         DROP  R1                  JSCB ADDRESSING               YM5774
         USING QDB,R6              ADDRESS QDB                   Y02083
         L     R6,QDBFELMP         GET @ OF 1ST DSAB             Y02083
         DROP  R6                  QDB ADDRESSING                Y02083
         USING DSAB,R6             ADDRESS DSAB                  Y02083
RSDSABCK EQU   *                   LOOK FOR CHKPT DATA SET       Y02083
         L     R1,DSABTIOT         GET @ OF TIOT DD ENTRY        Y02083
         USING TIOENTRY,R1         ADDRESS TIOT DD ENTRY         Y02083
         CLC   TIOEDDNM,REPDDNM    IS THIS CHKPT D/S DD NAME     Y02083
         BE    RSDSABOK            IF YES, GO SET CHKPT FLAGS    Y02083
         L     R6,DSABFCHN         GET @ OF NEXT DSAB            Y02083
         B     RSDSABCK            GO CHECK THIS DSAB            Y02083
RSDSABOK EQU   *                   CHKPT D/S DDNAME FOUND        Y02083
         OI    DSABFLG4,DSABCKDS+DSABCKVL SET CHKPT STATUS FLAGS Y02083
*                                  SO OPEN WILL BYPASS SECURITY  Y02083
*                                  TESTS WHICH HAVE ALREADY BEEN Y02083
*                                  DONE BY DSDRP PROCESSING      Y02083
         DROP  R1                  TIOT DD ENTRY ADDRESSING      Y02083
         DROP  R6                  DSAB ADDRESSING               Y02083
         ICM   R0,ADDRBYTS,REPPPE+K1    GET LNGTH OF P/P CORE    Y02083
         ICM   R0,HIBYTE,SP250          SET SUBPOOL NO.          Y02083
         GETMAIN R,LV=(R0)              GET P/P CORE             Y02083
*                                                                     *
RSTCONT  EQU   *
*                                                                     *
***********************************************************************
*                                                                     *
*        THIS SECTION WILL INITIALIZE THE RESTART WORK AREA WITH      *
*     INFORMATION THAT WILL BE USED BY SUBSEQUENT LOADS OF RESTART.   *
*                                                                     *
***********************************************************************
*                                                                     *
         LA    RD,RSREGSAV         INITIALIZE SAVE AREA REGISTER.
*        INITIALIZE W.A. WITH INFORMATION PASSED IN PARM FIELD
         MVC   RSPPM(RSPPML-RSPPM),REPPPM    LO ADR P/P CORE     Y02076
         MVC   RSBUFL2(LBLKSZE),REPBLKSI     CHKPT D/S BLK SIZE  Y02076
         MVC   RSTIOTO2(RSTIOTAD-RSTIOTO2),REPTIOTL  TIOT LNGTH  Y02076
         MVC   RSWACL1(RSBUFL-RSWACL1),REPWACL  WORK AREA LNGTH  Y02076
         MVC   RSFLAG1(LFLAGS),REPFLAGS   MOVE FLAGS TO W.A.     Y02076
         MVC   RSTIOTL2(LTIOT),REPTIOTL   FOR MVT-MOVE IN LENGTH Y02076
*                                         OF TIOT SA.
         MVC   RSCIRTTR(RSSVRBN-RSCIRTTR),REPCIRAD               Y02076
*                                  SAVE TTR OF CIR
*                                                                     *
***********************************************************************
*                                                                     *
*        TEST THE RESTARTING JOB FOR V=R (TCBFLGS6)              YM2776
*        IN CONJUNCTION WITH THE CHECKPOINT HEADER               YM2776
*        INFORMATION ON THE CHKPT JOB (RSFLAGS) TO               YM2776
*        DETERMINE IF THE V=R BIT IS EITHER ON OR                YM2776
*        OFF IN BOTH JOBS.  IF MIXED, ERROR MESSAGE CODE 8       YM2776
*        IS GIVEN.                                               YM2776
*                                                                     *
***********************************************************************
*                                                                     *
*
         MVI   RSCKPPML,ZERO       MOVE ZERO BYTE FOR TEST       YM3092
         TM    REPFLAGS,CKVEQR      WAS THE CHECKPOINT JOB V=R?  YM3092
CKVEQR   EQU   8                   V=R BIT IN PARAMETER LIST     YM3092
         BNO   VEQRTEST            NO, DO TEST NOW               YM3092
         OI    RSCKPPML,VEQR       INDICATE V=R IN CHKPT JOB     YM3092
VEQRTEST EQU   *                                                 YM3092
         XC    RSCKPPML(LFLAGS),TCBFLGS6                         YM3092
*                                  HALF ADD TWO BYTES TOGETHER   YM2776
*
*        WHEN THESE TWO BYTES ARE EXCLUSIVE OR'ED TOGETHER       YM2776
*        THE HIGH ORDER BIT WILL BE OFF (0) IF BOTH THE          YM2776
*        CHECKPOINT AND RESTART STEPS ARE EITHER V=R, OR         YM2776
*        V=V.  IF THE STEPS WERE DIFFERENT, THE BIT              YM2776
*        WILL BE ON (1).                                         YM2776
*
         TM    RSCKPPML,VEQR       TEST V=R BIT (BOTH)           YM3540
         BNO   RSVEROK             STATUS IS GOOD, GO ON         YM2776
         MVC   RSREGSAV(8),MODID   SAVE NAME OF MODULE THAT    @ZSUT222
*                                  HAS DETECTED THE ERROR      @ZSUT222
         STM   0,15,RSREGSAV+8     SAVE CURRENT REGISTERS      @ZSUT222
         MVI   RSRETCOD+K1,ERR002  INDICATE V=R TYPE ERROR     @ZSUT222
         MVC   RHRXLST(LMODNAME),ERREXIT  SET V05B XCTL          YM4671
*
*        FREE ONE PAGE IN THE REGION SO THAT THE WTO ROUTINE     YM3092
*        WILL HAVE SPACE TO OPERATE IN.                          YM3092
*
FREEPAGE EQU   *                   FREE 1 PAGE FOR ERROR MSG     Y02083
         ICM   R1,ICMMSK,REPPPM         GET LOW BOUND-PP CORE    Y02076
         FREEMAIN R,LV=4096,SP=250,A=(1)                         YM3092
*
RSLDR1   EQU   *                        INSTR MUST FOLLOW        YM4671
         LR    R1,RC               LOAD PARM REG WITH W A ADDR   YM2776
         B     RSEREXIT            GO XCTL TO V05B               YM2776
*                                                                     *
***********************************************************************
*                                                                     *
*        CONSTRUCT A BSAM DCB. THIS DCB WILL BE USED BY REPMAIN TO    *
*     READ THE CHKPT DATA SET.                                        *
*                                                                     *
***********************************************************************
RSVEROK  EQU   *                                                 YM2776
         MVC   RHRCHKID(RHRXLST-RHRCHKID),REPCHKID               Y02076
*                                  SAVE CHECKID FOR NEXT MODULE
         LA    R6,RSBDCB           GET ADDR OF DCB MOVE-IN AREA.
         USING IHADCB,R6           ADDRESS AREA WHERE DCB WILL BE BUILT
         MVC   IHADCB21(LDCB),RSTDCBD        MOVE PRE-BUILT DCB TO W.A.
         MVC   DCBDDNAM(DDL),REPDDNM  INSERT DDNAME IN DCB.      Y02076
         MVC   DCBBLKSI(LBLKSZE),RSBUFL2     MOVE BLOCKSIZE INTO DCB.
         TM    RSFLAG1,RSTRKOV      CHKPT D.S. SPECIFY TRACK OVERFLOW
         BZ    RSTRESM              NO-SKIP CODE TO SET DCBRECFM FIELD
         OI    DCBRECFM,DCBRECTO    SET TRACK OVERFLOW BIT IN CHKPT DCB
RSTRESM  EQU   *
         ST    R6,RSDCBAD1    INITIALIZE DECBS WITH ADDR OF DCB.
RSTSKP   EQU   *                                                  MFTCR
RSTADD4K EQU   *
*                                                                     *
         ST    R6,RSTOPEN          SET UP OPEN'S PARAMETER LIST.
         MVI   RSTOPEN,INOUT       MOVE OPTION BYTE TO W.A.    @ZA18280
*  SET UP RDJFCB PARAMETERS                                     YA00142
         LA    R1,RSTRDJ                ADDR OF EXIT LIST       YA00142
         ST    R1,DCBEXLST              PLACE IN DCB            YA00142
         LA    R1,INFMJFCB              ADDR OF JFCB AREA       YA00142
         ST    R1,RSTRDJ                PLACE IN EXIT LST ENTRY YA00142
         MVI   RSTRDJ,OPENJOPT          SET RDJFCB AND LIST END YA00142
         RDJFCB MF=(E,RSTOPEN)          READ THE JFCB           YA00142
         TM    RSFLAG1,RSTAPE           IS THE CHKPT ON TAPE    YA00142
         BZ    RSTOPN1                  BR IF NOT TO OPEN       YA00142
*                                                               YA00142
*   DETERMINE THE SEQUENCE NO. OF THE VOLUME WITH THE           YA00142
*   REQUIRED CHKPT RECORDS.                                     YA00142
*                                                                     *
         LH    RF,JFCBVLSQ              LOAD ORIG VOL SEQ NO.   YA00142
         LTR   RF,RF                    WAS A VOL SEQ SPECIFIED YA00142
         BNZ   RSTVLSQ1                 BR IF YES               YA00142
*                                                               YA00142
*   WHEN A DATA SET WAS OPENED WITH A DISP OF 'MOD' AND NO JFCB YA00142
*   VOL SEQ NO., THEN THE REQUIRED VOL SEQ IS THAT OF THE LAST  YA00142
*   VOLUME OF THE CHKPT DATA SET                                YA00142
*                                                               YA00142
         TM    JFCBIND2,JFCOLD          IS DISP = MOD           YA00142
         BO    RSTVLSQ3                 BR IF NOT               YA00142
         MVC   JFCBVLSQ+1(1),JFCBNVOL   SET VOL SEQ TO NO. OF   YA00142
*                                       VOLS IN DATA SET        YA00142
         B     RSTOPN1                  BR TO OPEN CHKPT D.S.   YA00142
*                                                               YA00142
*   WHEN A VOL SEQ NO. WAS ORIGINALLY SPECIFIED OR IF NONE WAS  YA00142
*   SPECIFIED BUT THE DISPOISITION IS 'OLD' OR 'NEW', THEN THE SEQ
*   NO. IS THE SUM OF (JFCBVLSQ-1) AND DEBTVLSQ, WHERE          YA00142
*   (JFCBVLSQ-1) GR OR = 0.                                     YA00142
*                                                               YA00142
RSTVLSQ1 EQU   *                                                YA00142
         BCTR  RF,0                     DECR NON=ZERO VLSQ      YA00142
RSTVLSQ3 EQU   *                                                YA00142
         SR    R1,R1                    ZERO WORK REG           YA00142
         IC    R1,RSSVRB                LOAD DEBTVLSQ SAVED AT  YA00142
*                                       CHKPT TIME IN SVRB PTR  YA00142
         AR    RF,R1                    CALC REQ'D VOL SEQ NO.  YA00142
         STH   RF,JFCBVLSQ              STORE IT IN JFCB        YA00142
*                                                               YA00142
*   OPEN THE CHKPT D.S. USING THE JUST-MODIFIED JFCB.  NOTE THAT
*   THE MODIFIED JFCB IS NOT WRITTEN BACK TO THE JOBQ BY OPEN   YA00142
*                                                               YA00142
RSTOPN1  EQU   *                                                YA00142
**********************************************************************
*        FREE 2K  OF CORE SO OPEN WILL HAVE ENOUGH SPACE         YM7325
***********************************************************************
         L     R1,RSPPM                 ADR P/P CORE             YM7325
         LH    R0,TWOKBLK               2K                       YM7325
         ICM   R0,HIBYTE,SP250          SP250                    YM7325
         FREEMAIN R,LV=(R0),A=(R1)                               YM7325
         MVI   RSSVRB,X'00'             ZERO VOL SEQ NO.        SA62489
         OI    JFCBTSDM,JFCNWRIT        IND DON'T REWRITE JFCB  YA00142
         OPEN  TYPE=J,MF=(E,RSTOPEN)    OPEN CHKPT D.S.         YA00142
         LR    R1,RC               LOAD PARM REG WITH ADDR OF CHKPT WA.
         MVC   RHRXLST(LMODNAME),RHRXLIST    MOVE NAME OF NEXT MODULE
RSEREXIT EQU   *                                                 YM2776
         LA    RF,RHRXLST                    TO XCTL'S PARM LIST IN WA.
         ST    RF,RHRXPTR
         LA    RF,RHRXPTR
**********************************************************************
         XCTL  SF=(E,(15))         TRANSFER CONTROL TO 2ND LOAD OF
PARMSAV  MVC   0(1,R7),0(R3)       SUBJECT OF AN EXEC INS        YM6541
ICMMSK   EQU   X'F'                     INSERT CHAR MASK         Y02076
CURASOFS EQU   12                  OFSET TO CURRENST ASCB        Y02076
K1       EQU   1                   CONSTANT 1                    Y02076
ERR216   EQU   216                 WRONG REGION BEGIN ADR        Y02076
USERAUTH EQU   X'10'               USER AUTH'D FOR MODESET       Y02076
AUTHOFF  EQU   X'EF'               MASK - TURN OFF AUTH BIT      Y02076
RHRXLIST DC    C'IGC0205B'         NEXT LOAD OF HSEKEEPING        MFTCR
ERREXIT  DC    C'IGC0V05B'         ERROR EXIT LOAD OF RESTART    YM2776
MODID    DC    CL8'IGC0005B'       MODULE ID                   @ZSUT222
MODESUB  DC    X'E0FA'             U MODE, SP=250 TO GETMAIN     Y02076
SP253    DC    X'FD'               SUBPOOL 253
SP250    DC    AL1(250)                 SUBPOOL 250              Y02076
TWOKBLK  DC    H'2048'             TWO K BLOCKSIZE
RSTMASK  DC    X'FF'
C0008    DC    H'8'                SUBTRACT CONSTANT             YM6541
MINWALN  DC    H'4096'             MINIMUM RESTART WA SIZE       YM7063
PAGEMASK DC    X'00FFF000'         'AND' MASK FOR PAGE ADDRESS   M2658
         DS    0F                  PUTS DCB ON FULL WORD BOUNDARY
         DS    C                   ALIGNS ADDRESS TO FULL WORD BOUNDARY
RSTDCBD  EQU   *
         DC    AL3(0)    BUFCB
         DC    AL2(0)  BUFL
         DC    BL2'0100000000000000'  DSORG  (BSAM)
         DC    A(1)    IOBAD
         DC    AL1(0)  BFTEK,BFALN
         DC    AL3(1)  EODAD
         DC    BL1'11000000'
         DC    AL3(0)  EXLST
*                       FOUNDATION BLOCK
         DC    CL8'        '   DDNAME
         DC    BL1'00000010'  OFLGS
         DC    BL1'00000000'  IFLG
         DC    BL2'0010010000100100'  MACR                     @ZA18280
*                       BSAM-BPAM-QSAM INTERFACE
         DC    BL1'00000000'
         DC    AL3(1)  CHECK, GERR, PERR
         DC    A(1)    SYNAD
         DC    H'0'    CIND1, CIND2
         DC    AL2(0) BLKSIZE
         DC    F'0'   WCPO, WCPL, OFFSR, OFFSW
         DC    A(1)   IOBA
         DC    AL1(1)    NCP
         DC    AL3(1) EOBR, EOBAD
*                       BSAM-BPAM INTERFACE
         DC    A(1)   EOBW
         DC    H'0'   DIRCT
         DC    AL2(0) LRECL
         DC    A(1)   CNTRL, NOTE, POINT
*                                                                     *
*        MAINTENANCE SPACE                                            *
*                                                                     *
PATCH    EQU   ((*-IGC0005B)/10)   TEN PERCENT MAINTENANCE SPACE
         DC    XL(PATCH)'00'
*                                                                     *
RSECT    DSECT                     IGC0005B'S WORK AREA.
RHRCHKID DS    4F   CHECKID
RHRXLST  DS    2F   NAME OF MODULE TO RECEIVE CONTROL.
RHRXPTR  DS    F    POINTER XCTL PARM LIST.
RSTOPEN  DS    F    OPEN PARAMETER LIST.
RSTRDJ   DS    A(INFMJFCB)              OPENJ & RDJFCB EXLST    YA00142
         IEFJFCBN                       MAP JFCB                YA00142
PARMSECT DSECT
REPLNGTH DS    CL2                      LNGTH OF PARM LIST       Y02076
REPCIRAD DS    CL4                      TTR OF CIR RECORDS       Y02076
REPCOUNT DS    CL2                      NUMBER OF CHKPTS TAKEN   Y02076
REPCKIDL DS    CL2                      LENGTH OF CHECKID        Y02076
REPCHKID DS    CL16                     CHECKID                  Y02076
REPDDNM  DS    CL8                      DDNAME OF CHKPT D/S      Y02076
REPPPM   DS    CL4                      LO ORDER ADR OF P/P AREA Y02076
REPPPE   DS    CL4                      SIZE OF P/P AREA         Y02076
REPBLKSI DS    CL2                      CHKPT D/S BLOCK SIZE     Y02076
REPTIOTL DS    CL2                      LENGTH OF TIOT           Y02076
REPFLAGS DS    CL1                      CHECKPOINT FLAGS 1       Y02076
*              X'80'                    AUTOMATIC RESTART        Y02076
*              X'40'                    TRACK OVERFLOW SPECIFIED Y02076
*              X'20'                    CHKPT   D/S ON TAPE      Y02076
*              X'10'                    USER AUTH'D FOR MODESET  Y02076
*              X'08'                    V=R REQUEST              Y02076
*              X'02'                    CHKPT ISSUED OPEN        Y02076
*              X'01'                    CHKPT D/S OPEN FOR BPAM  Y02076
REPWACL  DS    CL3                      CHKPT W/A LENGTH         Y02076
REPFLAG2 DS    CL1                      CHKPT FLAGS 2            Y02076
*              X'04'                    USER SUPPLIED CHECKID    Y02076
RSCKPPML DS    CL4                      FOR V=R TESTS            Y02076
*                                                                     *
*        MAPPING OF RESTART SVRB AND EXTENDED SAVE AREA USED BY MODULE*
*                                                                     *
RSTSECT  DSECT
RSTSVRB  DS    24F
RSPSD    DS    2F   GETMAIN RETURNS ADDR AND LNGTH HERE, BUT
*                   FIELD IS NEVER REFERENCED
RSTPARM  DS    F    ADDR OF PARM FIELD.
RSTFLAG1 DS    F    FLAGS FIELD.
RSTAD    DS    F    WORK AREA USED TO STORE ADDRESSES
RCOUNT   DS    F
RSTSAVE  DS    F    SAVE AREA FOR CIR TTR AND CHKPT DATA SET DDNAME.
*                                                                     *
*        GETMAIN PARAMETER LIST                                       *
*                                                                     *
RSTGMLIS DS    1F   POINTER TO GETMAIN LIST
RSTGMLS4 DS    1F   POINTER TO GETMAIN RETURN ADDRESS
RSTGMLS8 DS    H    MODE BYTE AND SUBPOOL NUMBER
RSTGMLLW DS    2F    GETMAIN LIST
         DCBD  DSORG=(BS,PO),DEVD=(DA,TA)
         IEFTIOT1
         IKJTCB                                                  YM2776
         IHAPSA                                                  Y02076
         IHAASCB                                                 Y02076
         IHALDA                                                  Y02076
         IHAPQE                                                  Y02076
         CVT   DSECT=YES,PREFIX=YES,LIST=YES                     Y02076
         IEFASCTB                                                Y02083
SCTSTEND EQU   INSMSCT+172         INTRIM SYMBOL DEFINITION      Y02083
         IHADSAB                                                 Y02083
         IHAQDB                                                  Y02083
         IEZJSCB                                                 Y02083
         IEEVRSWA VER=3                 USE AOS2 VERSION OF WA
         IEFQMIOP                                                Y02083
QMNGRWA  DS    88C                                               Y02083
QMNGRBUF DS    176C                                              Y02083
         IEFJESCT                                                Y02083
PRBL     EQU   32        LENGTH OF IEFRSTRT'S PRB
INPUTOPN EQU   128  OPTION BYTE FOR OPEN PARM LIST.
INOUT    EQU   131  OPTION BYTO FOR IN OUT PROCESSING          @ZA18280
RST256   EQU   256
DDL      EQU   8    LENGTH OF DDNAME FIELD IN DCB.
LENL     EQU   32             OFFSET DEBLNGTH FROM BEG OF APPEND. TABLE
DCBDECBL EQU   128  LENGTH OF RESTART'S DCB AND TWO DECB.
DIOBL    EQU   80   LENGTH OF DA IOB.
DICBL    EQU   112  LENGTH OF DA ICB.
TICBL    EQU   48   LENGTH OF TAPE ICB
TIOBL    EQU   48   LENGTH OF TAPE IOB
RSDIRACC EQU   2    INDICATES DIRECT ACCESS DEVICE
KCVTMVT  EQU   X'10'
TIOLEN   EQU   28
VEQR     EQU   128              V=R BIT IN TCBFLGS6 AND RSFLAG   YM2776
ERR002   EQU   2                ERROR CODE FOR V=R             @ZSUT222
ERR203   EQU   203              ILLEGITIMATE SVC RESTART CALL    Y02083
ERR023   EQU   23               SWA READ IN ERROR              @ZSUT222
K176     EQU   176              LENGTH OF SCT                    Y02083
RSL      EQU   256  LENGTH OF RSTWA TO BE ZEROED OUT.
SVRBL    EQU   144  LENGTH OF RESTART SVRB
DSORGBPM EQU   B'01000010'    MASK TO CHANGE BSAM DSORG FIELD TO BPAM.
DCBL     EQU   88   LENGTH OF BSAM/BPAM DCB.
RSTWAL   EQU   RSTIOT-RSTWA   LENGTH OF RESTART W.A.
RSTRKOV  EQU   64   INDICATES CHKPT DCB SPECIFIED TRACK OVERFLOW
TRKOV    EQU   32   TRACK OVERFLOW IN DCBRECFM
RSSPBKSZ EQU   X'08'
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
RA       EQU   10
RB       EQU   11
RC       EQU   12
RD       EQU   13
RE       EQU   14
RF       EQU   15
*
*       DEFINITION OF CONSTANTS, POSITIONS WITHIN OTHER AREAS,
*                     AND LENGTHATTRIBUTES
ZERO     EQU   0         DECIMAL - 0
ONE      EQU   1         DECIMAL - 1
EIGHT    EQU   8                   DECIMAL - 8                   A35696
DEC256   EQU   256       DECIMAL - 256
HIBYTE   EQU   8                   ICM MASK FOR HIGH ORDER BYTE
ADDRBYTS EQU   7                   ICM MASK FOR ADDRESS
*
RSBUFL2  EQU   RSBUFL+2            BUFFER+2
RSTIOTO2 EQU   RSTIOTO+2           TIOTO
RSWACL1  EQU   RSWACL+1            WORKAREA+1
RSTIOTL2 EQU   RSTIOTL+2           TIOTL
IHADCB21 EQU   IHADCB+21           WORKAREA
*
LMODESUB EQU   2                   MODE BYTE AND SUBPOOL
LBLKSZE  EQU   2                   BLOCK SIZE
LTIOT    EQU   2                   TIOT
LFLAGS   EQU   1                   FLAGS
LDCB     EQU   67                  DCB
LMODNAME EQU   8                   MODULE NAME
OPENJOPT EQU   X'87'                    END LIST & OPENJ OPT    YA00142
     END
