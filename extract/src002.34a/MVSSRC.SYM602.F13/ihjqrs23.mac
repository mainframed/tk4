         TITLE 'IHJQRS23 - AOS/2 REPMAIN 4 ROUTINE - IGC0805B'
*        VS2 RELEASE 037 CHANGES                               @ZSUT222
* APAR OZ31481 HAS BEEN FLAGGED AS @ZSUT222                    @ZSUT222
*C056956,057035,061893,062015,063684,064530                    @ZSUT222
*A062749-062754,063748                                         @ZSUT222
* STATUS
*
* FUNCTION-
*
*    REPMAIN 4 RESETS THE SUPERVISOR CONTROL BLOCKS TO REFLECT
*    THE STATUS OF THE RESTARTING TASK AT CHECKPOINT TIME. IT
*    ALSO RELEASES CONTROL OF ALL LPA MODULES FOR RESTART BY
*    USING THE DELETE SYSTEM FUNCTION.
*
* ENTRY POINTS
*         IGC0805B
*    ENTERED FROM REPMAIN 3 TO UPDATE THE RESTART
*    SYSTEM CONTROL BLOCKS TO REFLECT THE STATUS OF THE USER
*    CHECKPOINT TIME.
*
* INPUT-
*    GPR12- CONTAINS THE ADDRESS OF THE RESTART WORK AREA(RSTWA)
*
* OUTPUT
*    GPR 1-CONTAINS THE ADDRESS OF THE RSTWA
*
* EXTERNAL REFERENCES
*    NONE
*
* EXITS, NORMAL
*    REPMAIN 5(IGC0905B) TO FREE THE RESTART CONTROL
*    BLOCKS REPLACED BY THE CHECKPOINTED CONTROL BLOCKS
*    AND TO RESET THE KEYS ON THE ALLOCATED PAGES OF
*    THE REGION
*
* EXITS, ERROR
*    REPMAIN 5(IGC0905B) WITH RSRETCOD SET TO 202              @ZSUT222
*    IF SETLOCK ERROR ENCOUNTERED                                Y02076
*
* TABLES/WORK AREAS
*
*    RSTWA
*
*    RPMWA
*
*    TCB       TCBMSS
*    .         TCBOTC
*    .         TCBLLS                                            Y02076
*    .         TCBRBP
*    .         TCBPKF                                            Y02076
*    .         TCBJPQ
*    .         TCBJLB
*
*    LLE       LLECOUNT                                          Y02076
*    .         LLESYSCT                                          Y02076
*    .         LLECHN                                            Y02076
*    .         LLECDPT                                           Y02076
*
*    CDE       CDUSE                                             Y02076
*    .         CDCHAIN
*    .         CDRRBP                                            Y02076
*    .         CDXLMJP
*    .         CDATTR                                            Y02076
*    .         CDATTR2                                           Y02076
*    .         CDNAME                                            Y02076
*
*    RB        RBLINK
*
*    SCB       SCBCHAIN                                          Y02076
*    .         SCBOWNRA                                          Y02076
*    .         SCBPKEY                                           Y02076
*                                                                Y02076
*    CVT       CVTLINK                                           Y02076
*                                                                Y02076
*    PSA       PSALITA                                           Y02076
*                                                                Y02076
*    XTLST     XTLMSBAA                                          Y02076
*
* ATTRIBUTES-
*    KEY 0, SUPERVISOR STATE, ENABLED
*
* CHARACTER CODE DEPENDENCY
*    NONE
*
* NOTES
*    APPLICABILITY-AOS/2
*    DEPENDENCIES- NONE
*    INTERFACES- DELETE AND SETLOCK                              Y02076
*
         EJECT
IGC0805B CSECT
         BALR  RB,N0               ADDRESSABILITY FOR MODULE
         USING *,RB                BASE REGISTER 11
         USING RSTWA,RC            RESTART WORK AREA
         USING RPMWA1,R9           REPMAIN WORK AREA
         USING TCB,R5              TCB
         USING RBSECT,R4           RB
         B     RM4BEGIN            BRANCH AROUND CONSTANTS
         DC    H'0'                PAD TWO BYTES ZEROS
         DC    CL8'IGC0805B'       MODULE NAME                 @ZSUT222
         DC    CL8'&SYSDATE'       LAST DATE SHIPPED           @ZSUT222
         DC    CL8'@ZSUT222'       LAST CODE SHIPPED           @ZSUT222
RM4BEGIN L     R9,RSMWOFST         OFFSET TO REPMAIN'S W.A.
         AR    R9,RC               TO ESTABLISH BASE FOR W.A.
         L     R5,RSTCBAD          GET TCB ADDR
         LA    R3,RPMMSS3          GET PTRS TO
         LA    R4,TCBMSS           NEW AND OLD TCBMSS CHAINS
         BAL   R6,RPMSWAP          AND SWAP THEM
         L     R5,TCBOTC           GET INIT TCB
         LA    R4,TCBMSS           INIT TCBMSS
         LA    R3,RPMMSS2          SWAP MSS CHAINS
         BAL   R6,RPMSWAP          GO TO SWAP RTN
         L     R5,TCBOTC           GET STC TCB                   Y02076
         LA    R4,TCBMSS           STC TCBMSS
         LA    R3,RPMMSS1          SWAP MSS CHAINS
         BAL   R6,RPMSWAP          GO TO SWAP RTN
         L     R5,RSTCBAD          P/P TCB
         L     R4,TCBRBP           RESTART SVRB
         LA    R4,RBLINK           SVRB'S RB PTR
         LA    R3,RPMRB            ADRS OF RESTORED RB CHAIN
         BAL   R6,RPMSWAP          SWAP RB CHAINS
         DROP  R4                  RB                            Y02076
         SPACE 2
*
* ANY LLE WITH A CDE ON THE LPAQ WILL BE DELETED VIA USE OF THE
* DELETE SYSTEM SERVICE.
*
         L     R8,TCBLLS           ADDRESS OF RESTART LLE CHAIN
R11A1    LTR   R8,R8               ANY LLE'S                     Y02076
         BZ    R11A4               NO'SWAP LLE'S
         USING LLE,R8              LLE
         L     R7,LLECHN           GET ADDRESS OF NEXT LLE
         L     R3,LLECDPT          ADDRESS OF CDE FOR MODULE
         USING CDENTRY,R3          CDE
         TM    CDATTR,CDNIP        CDE IN LPAQ
         BZ    R11A3               NO GET NEXT LLE
         LH    R6,LLECOUNT         GET TOTAL LOAD COUNT          Y02076
         BCT   R6,R11A1A           DECR BY 1, IF COUNT WAS       YM7045
*                                  GREATER THAN 1, GO DELETE     YM7045
         B     R11A3               IF COUNT WAS 1, SKIP DELETE   YM7045
*        THE DELETE COUNT HAS BEEN CORRECTED BY -1               YM7045
R11A1A   LA    RF,RSREGSAV         GET SAVEAREA ADDR             YM7045
         STM   R0,RF,N0(RF)        SAVE REGS                     Y02076
R11A11   SETLOCK OBTAIN,TYPE=LOCAL,MODE=UNCOND,                  Y02076,
               RELATED=(LOCAL,IGC0805B(R11A14))                  Y02076
         LTR   RD,RD               SETLOCK ERROR                 Y02076
         LM    R0,RF,N0(RF)        RESTORE REGS                  Y02076
         BNZ   R13A1               IF ERROR, BR                  Y02076
R11A12   SETLOCK OBTAIN,TYPE=CMS,MODE=UNCOND,                    Y02076,
               RELATED=(CMS,IGC0805B(R11A13))                    Y02076
         LTR   RD,RD               SETLOCK ERROR                 Y02076
         LM    R0,RF,N0(RF)        RESTORE REGS                  Y02076
         BZ    R11A12B             IF NO ERROR, BR               Y02076
         MVI   RSRETCOD+N1,ERR202 STORE SETLOCK ERROR CODE     @ZSUT222
         B     R11A14              GO RELEASE LOCAL LOCK         Y02076
R11A12B  LH    R4,CDUSE            GET CDE USE COUNT             Y02076
         SR    R4,R6               DECR BY TOTAL LLE USE         Y02076
*                                  COUNT - 1                     Y02076
         STH   R4,CDUSE            UPDATE CDE USE COUNT          Y02076
         STM   R0,RF,N0(RF)        SAVE REGS                     Y02076
R11A13   SETLOCK RELEASE,TYPE=CMS,                               Y02076,
               RELATED=(CMS,IGC0805B(R11A12))                    Y02076
R11A14   SETLOCK RELEASE,TYPE=LOCAL,                             Y02076,
               RELATED=(LOCAL,IGC0805B(R11A11))                  Y02076
         LM    R0,RF,N0(RF)        RESTORE REGS                  Y02076
         CLI   RSRETCOD+N1,ERR202 SETLOCK ERROR                @ZSUT222
         BE    RSOUT               IF ERROR, GO XCTL TO 905B     Y02076
R11A3    MVC   RPMN40,CDNAME       INSURE NAME IS AVAILABLE      Y02076
         LA    R4,N1               SET                           Y02076
         STH   R4,LLECOUNT         TOTAL AND                     Y02076
         STH   R4,LLESYSCT         SYSTEM COUNTS TO 1 SO         Y02076
*                                  LLE WILL BE FREED BY DELETE   Y02076
         DELETE EPLOC=RPMN40                                     Y02076
         LR    R8,R7               ADDR OF NEXT LLE              Y02076
         B     R11A1               BRANCH
         DROP  R3,R8               CDE,LLE
*
* SWAP THE LLE,CDE AND STAE POINTERS
*
R11A4    LA    R3,RPMLLE           ADDRESS OF RESTORED LLE
         LA    R4,TCBLLS           ADDRESS OF RESTART LLE CHAIN
         BAL   R6,RPMSWAP          SWAP LLE CHAINS
         LA    R4,TCBJPQ           ADDR OF RESTART CDE CHAIN
         LA    R3,RPMCDE           ADDR OF RESTORED CDE'S
         BAL   R6,RPMSWAP          SWAP CDE CHAINS
         MVC   RSINT+N1(N3),RPMDEB+N1 PASS RESTORED DEB CHAIN
*
*     ROUTINE TO MODIFY POINTERS IN CDE'S AND SCB'S
*     THIS ROUTINE FIRST TESTS THE CDE OVERLAY FLAG AND IF SET,  Y02076
*     THE SEGTAB NOTE LIST AND DCB ADDRESSES ARE UPDATED.        Y02076
*     NEXT THIS ROUTINE RESTORES THE POINTERS TO THE RB IN EACH  Y02076
*     CDE.  BEFORE RESTORATION, EACH CDE CONTAINS A POSITIONAL
*     POINTER IN THE FORM OF AN INTEGER N.  THE ROUTINE
*     FOLLOWS THE CHAIN OF RB'S UNTIL IT FINDS THE 'NTH' RB.  IT THEN
*     PUTS THE ADDRESS OF THIS RB INTO THE CDE.                  Y02076
*     THE RB POINTER IN THE SCB IS RESTORED IN THE SAME WAY
*
         SPACE 1
*
*        RESET RB POINTER IN CDE
*
         ICM   R3,M15,TCBJPQ       GET/TEST 1ST CDE ADDR         YM7347
         BZ    R12A10              IF NO CDE, BR                 YM7347
         USING CDENTRY,R3          CDE
         L     R2,CVTPTR           LOCATE CVT
         USING CVT,R2              CVT
R11B1    TM    CDATTR2,CDOLY       OVERLAY PROGRAM               Y02076
         BZ    R11B3               BR NO                         Y02076
         L     R4,CDXLMJP          GET EXTENT LIST ADDR          Y02076
         USING XTLST,R4            XTLIST                        Y02076
         L     R6,XTLMSBAA         GET SEGTAB ADDR               Y02076
         DROP  R4                  XTLIST                        Y02076
         LA    R4,N16(,R4)         GET NOTE LIST ADDR            Y02076
         ST    R4,N4(,R6)          NOTE LIST ADDR IN SEGTAB      Y02076
         TM    CDRRBP,LINKLIB      DID SEGTAB POINT TO LINKLIB   Y02076
*                                  AT CHKPT TIME                 Y02076
         MVI   CDRRBP,N0           RESET IND IF ON               Y02076
         BO    R11B2               BR YES                        Y02076
         MVC   N1(N3,R6),TCBJLB+N1 SEGTAB DCB PTR TO JOBLIB DCB  Y02076
         B     R11B3               GO AROUND                     Y02076
R11B2    MVC   N1(N3,R6),CVTLINK+N1 SEGTAB DCB PTR TO LINKLIB    Y02076
*                                  DCB                           Y02076
R11B3    SR    R4,R4               CLEAR WORKREG
         IC    R4,CDRRBP+N3        POSITIONAL PTR TO PRB         Y02076
         LTR   R4,R4               CDE HAVE AN RB
         BZ    R11B5               NO,BRANCH                     Y02076
         LA    R6,N1               SET SEARCH COUNT=1
         L     R7,TCBRBP           GET FIRST RB
         USING RBSECT,R7           RB
R11E3    CR    R4,R6               IS THIS RB FOR THIS CDE
         BE    R11E4               YES-BRANCH
         L     R7,RBLINK           GET NEXT RB
         LA    R6,N1(N6)           INCREMENT SEARCH COUNT
         B     R11E3               CHECK THIS PRB
R11E4    ST    R7,CDRRBP           UPDATE CDE RB PTR             Y02076
R11B5    L     R3,CDCHAIN          GET NEXT CDE
         LTR   R3,R3               ANY MORE CDE'S
         BNZ   R11B1               YES-BRANCH
         DROP  R3                  CDE
*
*        RESET RB POINTER IN SCB'S
*
R12A10   L     R3,RSNSTAE          GET ADDRESS OF FIRST SCB      YM7347
         USING SCB,R3              SCB
R12A11   LA    R3,N0(R3)           CLEAR HIGH BYTE
         LTR   R3,R3               ANY SCB'S
         BZ    R12B1               NO-BRANCH
         LA    R6,N1               SET SEARCH COUNT=1
         SR    R4,R4               CLEAR WORK REG
         IC    R4,SCBOWNRA+N2      GET POSITIONAL NUMBER
         L     R7,TCBRBP           GET ADDRESS OF RB
R12A12   CR    R4,R6               SEARCH COUNT =POSITIONAL INDEX
         BE    R12A13              YES-UPDATE SCB RB PTR
         L     R7,RBLINK           GET NEXT RB
         LA    R6,N1(R6)           INCREMENT SEARCH COUNT BY 1
         B     R12A12              LOOP TO CONTINUE SEARCH
R12A13   STCM  R7,N7,SCBOWNRA      PUT RB POINTER IN SCB
         CLI   SCBPKEY,N8          IS KEY SUBJECT TO CHANGE      Y02076
         BL    R12A13A             BR NO                         Y02076
         MVC   SCBPKEY,TCBPKF      ADJUST USER KEY               Y02076
R12A13A  L     R3,SCBCHAIN         GET NEXT SCB ADDRESS
         B     R12A11              LOOP TO PROCESS NEXT SCB
         DROP  R3,R7               SCB,RB
*
* FOR A LLE WITH A CDE IN THE LPAQ THE CDE USE COUNT             Y02076
* WILL BE INCREMENTED BY THE LLE RESPONSIBILITY COUNT.           Y02076
*
R12B1    EQU   *                   UPDATE COUNT
         L     R3,TCBLLS           GET FIRST LLE ON P/P TCB
R12B11   LTR   R3,R3               ARE THERE ANY LLE'S           Y02076
         BZ    RSOUT               NO-BRANCH                     Y02076
         USING LLE,R3              LLE
         L     R7,LLECDPT          LOAD ADDR OF CDE
         USING CDENTRY,R7          CDE
         TM    CDATTR,CDMIN        IS THIS A MINOR CDE
         BZ    R12B11A             NO-BRANCH
         L     R7,CDXLMJP          GET ADDR OF MAJOR CDE
R12B11A  TM    CDATTR,CDNIP        LLECDE IN LPA
         BZ    R12B12              NO-BRANCH
         LH    R6,LLECOUNT         GET LLE TOTAL LOAD COUNT      Y02076
         BCT   R6,R12B11A1         DECR BY 1, IF COUNT WAS       YM7045
*                                  GREATER THAN 1, UPDATE CDE    YM7045
         B     R12B12              OTHERWISE, SKIP CDE UPDATE    YM7045
R12B11A1 EQU   *                   NON-ZERO ENTRY                YM7045
*        THE LOAD COUNT HAS ALREADY BEEN CORRECTED BY -1         YM7045
         LA    RF,RSREGSAV         GET SAVEAREA ADDR             Y02076
         STM   R0,RF,N0(RF)        SAVE REGS                     Y02076
R12B11B  SETLOCK OBTAIN,TYPE=LOCAL,MODE=UNCOND,                  Y02076,
               RELATED=(LOCAL,IGC0805B(R12B11E))                 Y02076
         LTR   RD,RD               SETLOCK ERROR                 Y02076
         LM    R0,RF,N0(RF)        RESTORE REGS                  Y02076
         BNZ   R13A1               IF ERROR, BR                  Y02076
R12B11C  SETLOCK OBTAIN,TYPE=CMS,MODE=UNCOND,                    Y02076,
               RELATED=(CMS,IGC0805B(R12B11D))                   Y02076
         LTR   RD,RD               SETLOCK ERROR                 Y02076
         LM    R0,RF,N0(RF)        RESTORE REGS                  Y02076
         BZ    R12B11CA            IF NO ERROR, BR               Y02076
         MVI   RSRETCOD+N1,ERR202  STORE SETLOCK ERROR CODE    @ZSUT222
         B     R12B11E             GO RELEASE LOCAL LOCK         Y02076
R12B11CA LH    R0,CDUSE            GET CDE USE COUNT             Y02076
         AR    R6,R0               BUMP BY LLE TOTAL COUNT-1     Y02076
         STH   R6,CDUSE            UPDATE CDE USE COUNT          Y02076
         STM   R0,RF,N0(RF)        SAVE REGS                     Y02076
R12B11D  SETLOCK RELEASE,TYPE=CMS,                               Y02076,
               RELATED=(CMS,IGC0805B(R12B11C))                   Y02076
R12B11E  SETLOCK RELEASE,TYPE=LOCAL,                             Y02076,
               RELATED=(LOCAL,IGC0805B(R12B11B))                 Y02076
         LM    R0,RF,N0(RF)        RESTORE REGS                  Y02076
         CLI   RSRETCOD+N1,ERR202  SETLOCK ERROR               @ZSUT222
         BE    RSOUT               IF ERROR, GO XCTL TO 905B     Y02076
R12B12   L     R3,LLECHN           GET NEXT LLE
         B     R12B11              LOOP PROCESS LLE
         DROP  R3,R7               LLE,CDE
RSOUT    MVC   RPMN40,RPMN40A      SET FIELD FOR XCTL TO NEXT LOAD
         MVC   RSREGSAV(8),MODID   SAVE NAME OF MODULE THAT    @ZSUT222
*                                  HAS DETECTED THE ERROR      @ZSUT222
         STM   0,15,RSREGSAV+8     SAVE THE CURRENT REGISTERS  @ZSUT222
         LR    R1,RC               SAVE ADDR OF RESTART W.A.
         LA    RF,RPMN40           GET ADDR OF NEXT LOAD
         ST    RF,RP40XCTL         STORE ADDR IN W.A.
         LA    RF,RP40XCTL         GET ADDR OF RP40XCTL IN W.A.
         XCTL  SF=(E,(15))         XCTL TO NEXT LOAD
*
RPMSWAP  DS    0H                  SWAP ROUTINE
         XC    N1(N3,R3),N1(R4)    SWAP
         XC    N1(N3,R4),N1(R3)       TWO
         XC    N1(N3,R3),N1(R4)         VALUES
         BR    R6                  RETURN
*                                                                Y02076
R13A1    DS    0H                  SETLOCK ERROR ROUTINE         Y02076
         MVI   RSRETCOD+N1,ERR202  STORE SETLOCK ERROR CODE    @ZSUT222
         B     RSOUT               GO XCTL TO 905B               Y02076
         EJECT
*        DEFINED CONSTANTS
*
RPMN40A  DC    CL8'IGC0905B'       NEXT LOAD OF REPMAIN
MODID    DC    CL8'IGC0805B'       MODULE ID                   @ZSUT222
PATCH    EQU   *                   END OF MODULE
RM4LN    EQU   PATCH-IGC0805B      LENGTH OF CSECT
RM4PALN  EQU   RM4LN/10            LENGTH OF PATCH AREA
RM4PATCH DS    XL(RM4PALN)         10 PERCENT ALLOWANCE FOR PATCH AREA
*
*        EQUATES FOR GENERAL DISPLACEMENTS
*
N0       EQU   0                   0
N1       EQU   1                   1
N2       EQU   2                   2
N3       EQU   3                   3
N4       EQU   4                   4
N6       EQU   6                   6
N7       EQU   7                   7
N8       EQU   8                   8                             Y02076
N16      EQU   16                  16
LINKLIB  EQU   B'10000000'         LINKLIB IND FOR CDE           Y02076
*
*        GENERAL REGISTER EQUATES
*
R0       EQU   0                   REG 0
R1       EQU   1                   REG 1
R2       EQU   2                   REG 2
R3       EQU   3                   REG 3
R4       EQU   4                   REG 4
R5       EQU   5                   REG 5
R6       EQU   6                   REG 6
R7       EQU   7                   REG 7
R8       EQU   8                   REG 8
R9       EQU   9                   REG 9
RA       EQU   10                  REG 10
RB       EQU   11                  REG 11
RC       EQU   12                  REG 12
RD       EQU   13                  REG 13
RE       EQU   14                  REG 14
RF       EQU   15                  REG 15
*                                                                Y02076
*        EQUATED CONSTANTS                                       Y02076
*                                                                Y02076
ERR202   EQU   202                 SETLOCK ERROR CODE          @ZSUT222
M15      EQU   15                  ICM MASK FOR ALL BYTES        YM7347
         EJECT
*
*        DSECT FOR REPMAIN WORK AREA
*
RPMWA1   DSECT
RPMERAS1 DS    F                   TEMP SAVE AREA                Y02076
RMNEOV   DS    F                   TO TEST IF EOV OCCURRED
RSBFSTAD DS    F                   ADR OF NEXT BYTE IN BUFFER
RSBFLWA  DS    F                   NO. OF REMAINING BYTES IN BUFFER
RPMMSS1  DS    F                   ADDR OF NEW L-SHAPED PROG MSS CHAIN
RPMMSS2  DS    F                   ADDRESS OF NEW INIT MSS CHAIN
RPMMSS3  DS    F                   ADDRESS OF NEW P/P MSS CHAIN
RPMRB    DS    F                   ADDRESS OF RESTORED RB CHAIN
RPMLLE   DS    F                   ADDRESS OF RESTORED LLE CHAIN
RPMCDE   DS    F                   ADDRESS OF RESTORED CDE CHAIN
RPMDEB   DS    F                   ADDRESS OF RESTORED DEB CHAIN
RPMFOE   DS    F                   ADDRESS OF RESTORED FOE CHAIN
RSCORE   DS    F                   ADDR OF CORE AQUIRED BY LAST GETMAIN
RSW001   DS    F                   POINTER TO NEXT BYTE AVAILABLE CORE
RSW002   DS    F                   POINTER TO REPMAIN SYNAD ROUTINE
RPLNT    DS    F                   LENGTH OF THE BLOCK
RMADEOV  DS    F                   SAVE AREA FOR INFORMATION NEEDED
         DS    0D                  ALIGNMENT
CKSAVAR  DS    16F                 GENERAL SAVE AREA
RPMN40   DS    D                   MODULE NAME FOR XCTL
RP40XCTL DC    F'0'                POINTER TO XCTL PARAMETER LIST
RSSWIT1  DS    X'00'               BIG BLOCK INDICATOR
         TITLE 'IHJQRS23 - RESTART WORK AREA'
         IEEVRSWA
         TITLE 'IHJQRS23 - TASK CONTROL BLOCK'
         IKJTCB
         TITLE 'IHJQRS23 - STAE CONTROL BLOCK'
         IHASCB
         TITLE 'IHJQRS23 - LOAD LIST ELEMENT'
         IHALLE
         TITLE 'IHJQRS23 - CONTENTS DIRECTORY ENTRY'
         IHACDE
         TITLE 'IHJQRS23 - REQUEST BLOCK - SVRB,IRB,PRB'
         IKJRB
         TITLE 'IHJQRS23 - CONTROL VECTOR TABLE'
         CVT   DSECT=YES,PREFIX=NO
         TITLE 'IHJQRS23 - PREFERENTIAL STORAGE AREA'
         IHAPSA
         TITLE 'IHJQRS23 - EXTENT LIST'
         IHAXTLST
         END
