*
         COPY  LCGASMSW
  TITLE 'IGC0205B (IHJARS01) RESTART HOUSEKEEPING ROUTINE-2ND LOAD'
*
**********************************************************************
*STATUS - CHANGE LEVEL 000
*
*TITLE 'IGC0205B (IHJARS01) RESTART HOUSEKEEPING ROUTINE-2ND LOAD'
*
*FUNCTION/OPERATION  IGC0205B WILL DO THE FOLLOWING               MFTCR
*                   1.)  MOVE THE IOB, ICBS, AND CHANNEL PROGRAMS
*                      ASSOCIATED WITH THE CHKPT DATA SET TO THE
*                      RESTART WORK AREA AND UPDATE ALL POINTERS TO
*                      THE IOB, ICBS, AND CHANNEL PROGRAMS.
*                   2.)  FOR PCP, MOVE THE DEB ASSOCIATED WITH THE
*                      CHKPT DATA SET TO THE RESTART WORK AREA.
*                   3.)  ISSUE A POINT MACRO SO THAT THE NEXT
*                      READ MACRO WILL READ IN THE FIRST CIR.
*                   4.)  CALCULAGE OFFSET TO BUFFER AND LENGTH OF
*                      REPDCB'S WORK AREA AND SAVE THESE VALUES IN
*                      THE RESTART WORK AREA.
*ENTRY POINTS - FROM IGC0005B(IHJARS00)
*
*INPUT - ADDRESS OF THE RESTART WORK AREA IN REGISTER 1.
*
*OUTPUT- ADDRESS OF THE RESTART WORK AREA IN REGISTER 1.
*
*EXTERNAL ROUTINES AND SVC ROUTINES - BSAM POINT
*                                     XCTL
*
*EXITS-NORMAL  XCTL TO REPMAIN, IGC0505B
*
*EXITS-ERROR- NONE.
*
*WORK AREAS - RSTWA IS THE MAPPING OF THE RESTART WORK AREA.
*             TAPESECT IS THE MAPPING OF AN IOB, TWO ICBS AND THEIR
*           RESPECIVE CHANNEL PROGRAMS USED FOR READING IN THE CHKPT
*           DATA SET FROM TAPE USING CHAINED SCHEDULING
*             DASECT IS THE MAPPING OF AN IOB, TWO ICBS AND THEIR
*           RESPECTIVE CHANNEL PROGRAMS USED FOR READING IN THE CHKPT
*           DATA SET FROM D.A. USING CHAINED SCHEDULING.
*
*TABLES REFERENCED - TCB, DCB, DEB, IOB, ICB.
*ATTRIBUTES - REENTRANT, SUPERVISOR STATE, TYPE 4 SVC ROUTINE.
*
*NOTES - NONE.
*
* RELEASE 037 CHANGES ADDS DELETIONS
*A585340-585390                                                @ZA17064
**********************************************************************
*
IGC0205B CSECT
         BALR  RB,ZERO             SET UP MODULE ADDRESSABILITY
         USING *,RB
         B     BEGIN               BRANCH AROUND EYE CATCHER
         CNOP  0,8                 ALIGN TO A DOUBLE WORD BOUNDARY
         DC    C'IGC0205B'         CSECT NAME
         DC    C'JAN 1977'         LAST UPDATE DATE
         DC    C'@ZA17064'         LAST APAR
BEGIN    EQU   *
         USING IHADCB,R6           ADDRESS CHKPT DCB.
         USING R2SECT,RA           ADDR IGC0205BS W.A.            MFTCR
         USING RSTWA,RC            ADDRESS RESTART WORK AREA.
         LR    RC,R1               GET ADDR OF RESTART WORK AREA.
         L     R4,RSTCBAD          GET TCB ADDR.
         LA    R6,RSBDCB           GET ADDRESS OF RESTART DCB.
*
*        TO PREVENT CLOSE FROM ABENDING IF ERRORS OCCUR DURING   YM7073
*        RESTART, THE CHECKPOINT DATA SET DEB WILL BE REMOVED    YM7073
*        FROM THE DEB TABLE AND THE TCB.                         YM7073
*
*        LA    R1,0(R6)           LOAD ADDRESS INTO PARM REG     YM7073
*        DEBCHK (R1),TYPE=DELETE  REMOVE DEB FROM DEB TABLE      YM7073
*        SR    R1,R1              ZERO OUT REGISTER              YM7073
*        ST    R1,TCBDEB(R4)      ZERO OUT TCBDEB FIELD          YM7073
         LR    RA,RC
         A     RA,RSMWOFST         GET ADDRESS OF IGC0205BS W.A.  MFTCR
         LA    R1,RSIOBCP          GET ADDR OF RELOCATE AREA     Y02076
         L     R2,DCBIOBA          GET ADDR OF IOB.
         TM    RSFLAG1,RSTAPE      IS DATA SET ON A D.A. DEVICE
         BZ    RSTDA               YES-GO TO MOVE D.A. IOB, ICBS
*                                  AND CPS TO W.A.
*                                                                     *
***********************************************************************
*                                                                     *
*        THIS SECTION WILL MOVE THE TAPE IOB,ICBS, AND CHANNEL        *
*     PROGRAMS CONSTRUCTED BY OPEN FOR THE RESTART DATA SET TO THE    *
*     RESTART WORK AREA.                                              *
*                                                                     *
***********************************************************************
*                                                                     *
         USING TAPESECT,R1         ADDR AREA WHERE IOB, ICBS AND CPS
*                                  WILL BE MOVED TO.
         MVC   TIOB(TIOBL),ZERO(R2)  MOVE IOB TO W.A.
         LA    R2,TIOB             GET ADDRESS OF IOB.
         ST    R2,DCBIOBA          UPDATE IOB ADDR FIELD IN DCB.
         ST    R2,RSTAD            UPDATE POINTER TO NEXT IOB.
         MVC   TNIOB1(LIOB),RSTAD1
         LA    R2,TIOBECB          GET ECB ADDR.
         ST    R2,TECBPT           UPDATE POINTER TO ECB.
         LA    R2,TREAD            GET ADDR OF CHANNEL PROGRAM.
         ST    R2,TCPSTART         UPDATE POINTER TO CHANNEL PROGRAM.
RSTDRT   EQU   *
*                                                                     *
***********************************************************************
*                                                                     *
*        CALCULATE OFFSETS WITHIN RESTART WORKAREA FOR BUFFER AND     *
*        REPDCB'S WORKAREA                                            *
*                                                                     *
***********************************************************************
*                                                                     *
RSTSKP2  EQU   *
         L     R1,RSWACL           GET LENGTH OF WA              Y02076
         SH    R1,SSCRBUFL         LENGTH OF SSCR/SUR BUFFER     Y02076
         ST    R1,RSBFOFST         SAVE OFFSET IN W.A.
RSTREPL  EQU   *
         L     R2,RSWACL           COMPUTE LENGTH OF REPDCB'S W.A.
         S     R2,RSWAOFST
         ST    R2,RSDCBL           SAVE LENGTH IN W.A.
*                                                                     *
***********************************************************************
*                                                                     *
*        FOR CHKPT D.S. ON D/A, ISSUE POINT MACRO TO POSITION D.S. TO *
*        FIRST CIR IN CHKPT ENTRY SO THAT REPMAIN CAN RESTORE P/P     *
*        CORE (TTR OF CIR PASSED TO RESTART FROM IEFDSDRP WHO ISSUED  *
*        A CORRESPONDING NOTE MACRO)                                  *
*                                                                     *
***********************************************************************
*                                                                     *
         TM    RSFLAG1,RSTAPE      IS DATA SET ON TAPE
         BO    RSTTAPE             YES-GO TO POSITION TAPE TO 1ST CIR
         LA    R0,RSCIRTTR         LOAD PARM REG WITH POINTER TO TTRN.
         LA    R1,RSBDCB           LOAD PARM REG. WITH DCB ADDR.
         POINT (R1),(R0)           INSURE THAT THE NEXT RECORD TO
*                                  BE READ WILL BE THE FIRST CIR.
*                                                                     *
*   THE FOLLOWING CODE HAS BEEN ADDED TO ENHANCE SYSTEM INTEGRITY
*   BY PERFORMING LENGTH-CHECKING ON CIR*S AND SUR'S AS THEY ARE
*   READ IN BY REPMAIN.
*
*   IF THE CHECKPOINT BEING READ IS INTACT AND VALID, ALL RECORDS WILL
*   BE OF THE EXACT LENGTH COMPUTED BY REPMAIN AND PLACED IN THE
*   DECB BEFORE EACH READ.  THUS DETECTION OF A WRONG LENGTH RECORD
*   ALLOWS US TO PREVENT RESTORING OF SYSTEM CONTROL BLOCKS FROM
*   INVALID RECORDS.
*
*   WRONG LENGTH RECORD DETECTION IS ACCOMPLISHED BY:
*
*          (1)   TURNING OFF THE SILI BIT IN THE READ DATA CCW BUILT
*                BY BSAM.  BSAM BUILT THE CCW WITH THE SILI ON
*                BECAUSE IT USES THIS METHOD TO AVOIID LENGTH
*                CHECKING ON FORMAT U RECORDS.
*
*          (2)   TESTING, IN ALL REPMAIN SYNAD ROUTINES, TO SEE IF
*                THE I/0 ERROR THAT GAVE SYNAD CONTROL WAS A WRONG
*                LENGTH RECORD.  IF SO, A UNIQUE ERROR CODE IS SET.
*
*   THE FOLLOWING CODE LOCATES THE SILI BIT AND TURNS IT OFF.  THIS
*   CODE IS DEPENDANT UPON THE FORMAT OF BSAM CHANNEL PROGRAMS.
*
RSTSKP3  EQU   *                                                YA00153
         LA    R6,RSBDCB                GENERATE DCB POINTER    YA00153
         USING IHADCB,R6                ADDRESS CHKPT DCB       YA00153
         L     R1,DCBIOBA               POINT TO IOB            YA00153
         USING DASECT,R1                ADDRESS IT              YA00153
         L     R1,DCPAD                 GET CP START ADDRESS    YA00153
         DROP  R1                                               YA00153
         CLI   0(R1),TAPECC             TEST FOR TAPE CHAN PROG YA00153
         BNE   RSTSILI2                 NO-TRY NEXT TYPE        YA00153
         NI    TAPESILI(R1),X'FF'-SILIBIT  TURN OFF SILI BIT    YA00153
         B     RSTSKP4                                          YA00153
RSTSILI2 EQU   *                                                YA00153
         CLI   0(R1),RPSCC              TEST FOR DISK -- RPS    YA00153
         BNE   RSTSILI3                 NO- ASSUME DISP CP      YA00153
         NI    RPSSILI(R1),X'FF'-SILIBIT  TURN OFF SILI BIT     YA00153
         B     RSTSKP4                                          YA00153
RSTSILI3 EQU   *                                                YA00153
         NI    DISKSILI(R1),X'FF'-SILIBIT  TURN OFF DISK SILI   YA00153
***********************************************************************
*                                                                     *
*        XCTL TO REPMAIN TO RESTORE P/P CORE                          *
*                                                                     *
***********************************************************************
*                                                                     *
RSTSKP4  EQU   *                                                YA00153
         LR    R1,RC               GET ADDR OF RSTWA
         MVC   RHRXL(LMODNAME),RHRXRPM
*                                  MOVE NAME OF NEXT MODULE TO RECEIVE
         LA    RF,RHRXL            CONTROL TO XCTL PARM LIST IN W.A.
         ST    RF,RHRXL8
         LA    RF,RHRXL8
         XCTL  SF=(E,(RF))         TRANSFER CONTROL TO REPMAIN.
         USING DASECT,R1           ADDRESS AREA WHERE IOB, ICBS AND
*                                  CPS WILL BE MOVED TO.
*                                                                     *
***********************************************************************
*                                                                     *
*        THIS SECTION WILL MOVE THE DIRECT ACCESS IOB, ICBS, AND      *
*     CHANNEL PROGRAMS CONSTRUCTED BY OPEN FOR THE RESTART DATA SET   *
*     TO THE RESTART WORK AREA.                                       *
*                                                                     *
***********************************************************************
*                                                                     *
RSTDA    EQU   *
         LA    RE,IOBL             GET NON-RPS IOB LENGTH        M5907
         CLI   DCPS-DIOB(R2),RPSOPCD Q - RPS CHANNEL PROGRAM     M5907
         BNE   RSTDA0              IF NOT, BR                    M5907
         LA    RE,RPSIOBL          GET RPS IOB LENGTH            M5907
RSTDA0   EQU   *                                                 M5907
         BCTR  RE,0                ADJUST LNGTH FOR MOVE INS     M5907
         EX    RE,RSTMVIOB         MOVE IOB/CP TO WORKAREA       M5907
         LA    R2,DIOB             GET ADDR OF IOB.
         ST    R2,DCBIOBA          UPDATE JOB ADDR FIELD IN DCB.
         ST    R2,RSTAD            UPDATE POINTER TO NEXT IOB.
         MVC   DNIOB1(LIOB),RSTAD1
         LA    R2,DECB             GET ECB ADDR.
         ST    R2,DECBAD           UPDATE POINTER TO ECB.
         LA    R2,DCPS             GET ADDR OF CHANNEL PROGRAM.
         ST    R2,RSTAD
         MVC   DCPAD1(LCPPOINT),RSTAD1  UPDATE POINTER TO CP.
         CLI   DCPS,RPSOPCD        Q - RPS CHANNEL PROGRAM       M5907
         BE    RSTDA2              IF YES, BR                    M5907
         MVC   DCPT1(LCPPOINT),RSTAD1   UPDATE POINTER TO CP IN TIC CCW
         LA    R2,DSEEK3                GET ADDR OF SEEK ADDR
         ST    R2,RSTAD            UPDATE POINTER TO SEEK ADDR
         MVC   DCPS1(LCCW),RSTAD1       IN SEARCH CCW.
         LA    R2,DCPRCNT          GET ADDR OF READ COUNT CCW.
         ST    R2,RSTAD            UPDATE POINTER TO READ COUNT
         MVC   DCPTI1(LCCW),RSTAD1      CCW IN TIC CCW.
RSTDA1   EQU   *
         LA    R2,DCBFDAD3         GET ADDR OF FDAD ADDR IN DCB.
         ST    R2,RSTAD            UPDATE POINTER TO FDAD IN READ
         MVC   DCPRCNT1(LCCW),RSTAD1    COUNT CCW.
         B     RSTDRT                   GO BACK.
*                                                                M5907
* RPS CHANNEL PROGRAMS HAVE TWO ADDITIONAL CCW'S, A COUNT FIELD  M5907
* READ-IN AREA, AND AN EXTENSION TO HANDLE SECTOR THETA'S        M5907
*                                                                M5907
RSTDA2   EQU   *                                                 M5907
         LA    R2,RPSCPS           GET ADDR OF SEARCH ID         M5907
         ST    R2,RSTAD              EQ CCW AND UPDATE           M5907
         MVC   RPST+1(LCCW),RSTAD1     TIC CCW                   M5907
         LA    R2,DSEEK3           GET ADDR OF SEARCH ARG        M5907
         ST    R2,RSTAD              AND UPDATE SEARCH           M5907
         MVC   RPSCPS+1(LCCW),RSTAD1   ID EQ CCW                 M5907
         LA    R2,RSECTOR1         GET ADDR OF THETA-1           M5907
         ST    R2,RSTAD              AND UPDATE                  M5907
         MVC   RPSCPSS+1(LCCW),RSTAD1  SET SECTOR CCW            M5907
         LA    R2,RPSRCNT          GET ADDR OF CCW5              M5907
         ST    R2,RSTAD              AND UPDATE                  M5907
         MVC   RPSTI+1(LCCW),RSTAD1    TIC CCW                   M5907
         LA    R2,RPSCOUNT         GET ADDR OF COUNT FIELD       M5907
         ST    R2,RSTAD              READ-IN AREA AND UPDATE     M5907
         MVC   RPSRCNT+1(LCCW),RSTAD1  READ COUNT CCW            M5907
         LA    R2,RSECTOR2         GET ADDR OF THETA-2           M5907
         ST    R2,RSTAD              AND UPDATE                  M5907
         MVC   RPSRS+1(LCCW),RSTAD1    READ SECTOR CCW           M5907
         B     RSTDRT              GO HANDLE DEB NEXT            M5907
*                                                                     *
***********************************************************************
*                                                                     *
*        SUBROUTINE USED TO MOVE REMAINDER  OF DEB TO RESTART WORKAREA*
*                                                                     *
***********************************************************************
*                                                                     *
RSTMOV2  EQU   *
         MVC   ZERO(DEC256,R8),ZERO(R9)
         AH    R8,=H'256'
         SH    R7,=H'256'
         AH    R9,=H'256'
         BR    RE                  RETURN TO CALLER
*                                                                     *
***********************************************************************
*                                                                     *
*        SUBROUTINE TO POSITION TAPE CHKPT D.S. TO FIRST CIR BY       *
*        READING EACH RCD ON D.S. CHECKING FIRST FOR CORRECT CHKPT    *
*        ENTRY(COMPARING ON CHECKIDS,PASSED BY IEFDSDRP) AND, WITHIN  *
*        CORRECT ENTRY SEARCH FOR LAST DSDS(SEARCHING ON CODE INDICA- *
*        TING END OF DSDRS)                                           *
*                                                                     *
***********************************************************************
*                                                                     *
RSTTAPE  EQU   *
         LA    RD,RSREGSAV         ADDR REG SAVEAREA FOR READ MACRO
         LA    R7,RSBDCB
         ST    R7,RSDCBAD1
         LR    R7,RC               GET ADDR OF IOB IN W.A.
         A     R7,RSWACL
         SH    R7,=H'48'
         ST    R7,RSTAD
         MVC   RSIOBP11(LIOBADDR),RSTAD1  MOVE IOB ADDR TO DECB IN W.A.
         LA    R5,LREC                    LENGTH OF RECORD
         STH   R5,RSLNGTH1         SAVE LENGTH OF RECORD IN DECB
         LA    R5,RSDECB1          GET ADDR OF DECB FOR READ
         L     R7,RSBFOFST         BUFFER ADDR
         SH    R7,=H'200'
         AR    R7,RC
         ST    R7,RSTAD
         MVC   RSAREA11(LRIAADDR),RSTAD1  MOVE READ-IN AREA ADR TO DECB
*        POSITION TAPE TO CORRECT CHKPT ENTRY
RSTRGN   EQU   *
         BAL   R2,RSTREAD          GO TO READ RECORD FROM CHKPT D.S.
         CLC   CHRPOS(LCHR,R7),RSTCHKID   IS THIS THE CORRECT CHR
         BNE   RSTRGN              NO-GO TO READ NEXT RECORD
         LA    R6,RSBDCB           GET RESTART DCB ADDRESS     @ZA17064
         LA    R1,0(R6)            LOAD ADDRESS IN REG 1       @ZA17064
         DEBCHK (R1),TYPE=DELETE   DELETE THE DEB              @ZA17064
         SR    R1,R1               ZERO REG 1                  @ZA17064
         ST    R1,TCBDEB(4)        ZERO TCB DEB FIELD          @ZA17064
*                                                              @ZA17064
*        TAPE POSITIONED TO FIRST DSDR
RSTCOMP  EQU   *
         XR    R9,R9               INITIALIZE SWITCH FOR NO.RCD.IN BUFR
RSTTPK   EQU   *
         LR    R8,R7               INITIALIZE REG WITH BUFFER ADDR
RSTTPK1  EQU   *
         BAL   R2,RSTREAD          READ ANOTHER RECORD
RSTTP1   EQU   *
         CLI   DSDRPOS(R8),LASTDSDR     END OF DSDR'S
         BE    RSTSKP3             YES
         LA    R4,L23DSDR               LENGTH OF TYPE 2,3 DSDR'S
         CLI   DSDRPOS(R8),HEXZERO      TYPE 1 DSDR
         BNE   RSTTP2              NO
         LA    R4,L1DSDR(R4)       GET LENGTH OF TYPE 1 DSDR
RSTTP2   EQU   *
         AR    R8,R4               INCREMENT BUFFER POINTER TO NEXT RCD
RSTTP3   EQU   *
         LTR   R9,R9               SWITCH=0(MUST CHECK 2ND RCD IN BUFFR
         BNE   RSTSW3              NO-CHECKED SECOND
         LA    R9,ONE              CHECK FOR SECOND RCD
         B     RSTTP1              GO TO CHECK IF END OF DSDR'S
RSTSW3   EQU   *
         CLI   ONE(R8),LASTDSDR    END-OF-DSDR'S
         BE    RSTSKP3             YES-RETURN TO XCTL TO REPMAIN
         B     RSTCOMP             READ NEXT RECORD
RSTREAD  EQU   *
         READ  (R5),SF,,,,,,MF=E
         CHECK (R5)
         BR    R2                  RETURN TO CALLER
RSTMVC1  MVC   ZERO(ONE,R8),ZERO(R9)
RSTMVIOB MVC   DIOB(ONE),ZERO(R2)  MOVE IOB/CP TO WORKAREA       M5907
RHRXRPM  DC    C'IGC0505B'         MODULE NAME FOR REPMAIN
SSCRBUFL DC    H'4096'             MAX LENGTH OF SSCR RECORD     Y02076
*                                                                     *
*        MAINTENANCE SPACE                                            *
*                                                                     *
PATCH    DC    XL((*-IGC0205B)/20)'00'   FIVE PERCENT MAINTENANCE SPACE
*                                                                     *
TAPESECT DSECT
*        IOB
TIOB     DS    0D
TNIOB    DS    F         POINTER TO NEXT IOB.
TIOBECB  DS    F    ECB
TFLAGS   DS    F    FLAGS
TECBPT   DS    F    ADDR OF ECB
TIOBCSW  DS    2F   LOW ORDER BYTES OF LAST CSW
TCPSTART DS    F    ADDR OF CHANNEL PROGRAM
TDCBPT   DS    F    ADDR OF DCB
TEND     DS    2F
TREAD    DS    2F        READ CCW
DASECT   DSECT
*        IOB
DIOB     DS    0D
DNIOB    DS    F         POINTER TO NEXT IOB.
DECB     DS    F    ECB
DSENSE   DS    F    FLAGS
DECBAD   DS    F    ADDR OF ECB
DIOBCSW  DS    2F   LOW ORDER BYTES OF CSW
DCPAD    DS    F         ADDR OF CHANNEL PROGRAM.
DDCB     DS    F    ADDR OF DCB
DEND     DS    2F
DSEEK    DS    2F
*                                                                M5907
* NON-RPS CHANNEL PROGRAM                                        M5907
*                                                                M5907
DCPS     DS    2F   SEARCH CCW                                   M5907
DCPT     DS    2F   TIC CCW                                      M5907
DCPTI    DS    2F   TIC CCW                                      M5907
DCPRCNT  DS    2F   READ COUNT CCW                               M5907
DCPRDAT  DS    2F   READ DATA CCW                                M5907
*                                                                M5907
* RPS CHANNEL PROGRAM AND THETA EXTENSION                        M5907
*                                                                M5907
         ORG   DCPS                                              M5907
RPSCPSS  DS    2F   SET SECTOR CCW                               M5907
RPSCPS   DS    2F   SEARCH CCW                                   M5907
RPST     DS    2F   TIC CCW                                      M5907
RPSTI    DS    2F   TIC CCW                                      M5907
RPSRCNT  DS    2F   READ COUNT CCW                               M5907
RPSRDAT  DS    2F   READ DATA CCW                                M5907
RPSRS    DS    2F   READ SECTOR CCW                              M5907
RPSCOUNT DS    2F   COUNT READ-IN AREA                           M5907
RSECTOR1 DS    XL1  SECTOR THETA-1                               M5907
RSECTOR2 DS    XL1  SECTOR THETA-2                               M5907
         DS    XL6  RESERVED                                     M5907
R2SECT   DSECT                     IGC0205BS W.A.                 MFTCR
RSTCHKID DS    CL16      CHECKID
RSTAD    DS    F
RHRXL    DS    3F   XCTL PARM LIST
         DCBD  DSORG=(BS,PO),DEVD=(DA,TA)
         IEEVRSWA   VER=3
RST256   EQU   256
DEBAPPEN EQU   29   OFFSET TO APP.VEC.TBL. POINTER IN DEB.
DDL      EQU   8    LENGTH OF DDNAME FIELD IN DCB.
LENL     EQU   32             OFFSET DEBLNGTH FROM BEG OF APPEND. TABLE
IOBL     EQU   88                  NON-RPS IOB/CP LENGTH         M5907
RPSIOBL  EQU   120                 RPS IOB/CP LENGTH             M5907
RPSOPCD  EQU   X'23'               RPS OP CODE (SET SECTOR)      M5907
DCBDECBL EQU   128  LENGTH OF RESTART'S DCB AND TWO DECB.
DIOBL    EQU   88        LENGTH OF D.A  IOB AND CP
TIOBL    EQU   48        LENGTH OF TAPE IOB/CP.
RSDIRACC EQU   2    INDICATES DIRECT ACCESS DEVICE
KCVTMVT  EQU   X'10'
TIOLEN   EQU   28
RSL      EQU   256  LENGTH OF RSTWA TO BE ZEROED OUT.
SVRBL    EQU   144  LENGTH OF RESTART SVRB
DCBL     EQU   88   LENGTH OF BSAM/BPAM DCB.
RSTWAL   EQU   RSTIOT-RSTWA   LENGTH OF RESTART W.A.
RSTRKOV  EQU   64                 INDICATES TRACK OVERFLOW
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
RA       EQU   10
RB       EQU   11
RC       EQU   12
RD       EQU   13
RE       EQU   14
RF       EQU   15
*
*        DECIMAL CONSTANTS, POSITIONS, LENGTHATTRIBUTES
*
ZERO     EQU   0              DECIMAL - 0
ONE      EQU   1              DECIMAL - 1
DEC256   EQU   256            DECIMAL - 256
TCBDEB   EQU   8                  OFFSET IN TCB TO TCBDEB        YM7073
*
TNIOB1   EQU   TNIOB+1        POSITIONS IN RELATION TO PREVIOUSLY
RSTAD1   EQU   RSTAD+1        DEFINED AREAS
DCBDEBA1 EQU   DCBDEBAD+1     *
RHRXL8   EQU   RHRXL+8        *
DNIOB1   EQU   DNIOB+1        *
DCPAD1   EQU   DCPAD+1        *
DCPT1    EQU   DCPT+1         *
DCPS1    EQU   DCPS+1         *
DCPTI1   EQU   DCPTI+1        *
DCBFDAD3 EQU   DCBFDAD+3      *
DCPRCNT1 EQU   DCPRCNT+1      *
RSIOBP11 EQU   RSIOBPT1+1     *
RSAREA11 EQU   RSAREA1+1      READ-IN AREA ADDRESS
DSEEK3   EQU   DSEEK+3        SEEK ADDRESS
*
LIOB     EQU   3    LENGTH OF IOB
LDCB     EQU   3              DCB
LPTRAVT  EQU   3              POINTER TO APP. VEC. TBL.
LMODNAME EQU   8              MODULE NAME
LREC     EQU   400            RECORD
LRIAADDR EQU   3              READ-IN AREA ADDRESS
LIOBADDR EQU   3              IOB ADDRESS
LCHR     EQU   16             CHR
L23DSDR  EQU   178            TYPE 2,3 DSDR
LCPPOINT EQU   3              POINTER TO CP
LCCW     EQU   3              CCW
L1DSDR   EQU   17   PLUS 178  TYPE 1 DSDR                        YM5302
*
CHRPOS   EQU   4              CHR
DSDRPOS  EQU   1              DSDR
*
*      - MASKS -
*
HEXZERO  EQU   X'00'          HEXADECIMAL ZERO
LASTDSDR EQU   X'10'          END OF DSDRS
TAPECC   EQU   X'02'                    1ST CC IN TAPE CH PGM   YA00153
TAPESILI EQU   X'04'                    TAPE READ CCW+4         YA00153
SILIBIT  EQU   X'20'                    SILI BIT IN CCW FLAGS   YA00153
RPSCC    EQU   X'23'                    1ST CC IN RPS CH PGM    YA00153
RPSSILI  EQU   X'2C'                    RPS READ CCW+4          YA00153
DISKSILI EQU   X'24'                    DISK READ CCW+4         YA00153
 END
