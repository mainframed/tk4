         TITLE  'IGC0V05B (IHJARS60)  RESTART EXIT ROUTINE'
IGC0V05B CSECT
*
**********************************************************************
*ROUTINE NAME - IGC0V05B           RESTART EXIT ROUTINE
*STATUS CHANGE LEVEL 003
*
*FUNCTION/OPERATION - 1.)  ISSUE FREEMAIN FOR RESTART WORK AREA
*
*                     2.)  PUT RETURN CODE IN REGISTER 15 AND RETURN
*                        CONTROL TO THE CALLER.
*
*                     3.)  IF AN ERROR HAS OCCURRED, THIS ROUTINE WILL
*                        ISSUE AN ABEND MACRO.
*
*                     4.) IF SECURITY INTERFACE EXISTS UPDATE USERS DCB
*ENTRY POINT - IGC0V05B
*
*INPUT  - ADDRESS OF THE RESTART WORK AREA WILL BE IN REGISTER 1.
*
*OUTPUT - RETURN CODE IN REGISTER 15.
*         A MESSAGE TO CONSOLE
*          1.) IF RESTART WAS SUCCESSFUL -
*              IHJ008I SSSSSSSS RESTARTED
*              WHERE SSSSSSSS=JOBNAME
*          2.) IF RESTART WAS UNSUCCESSFUL -
*              IHJ007I RESTART NOT SUCCESSFUL FOR SSSSSSSS (XXX,YYY)
*              WHERE XXX = RETURN CODE, YYY = UNIT ADDRESS, AND
*              SSSSSSSS = JOBNAME.
*
*EXTERNAL ROUTINES - WTO, FREEMAIN, CLOSE.
*
*EXITS-NORMAL - RETURN TO CALLER WITH RETURN CODE (X'04') IN REG. 15.
*
*EXITS-ERRORS - ABEND IF AN ERROR OCCURED.
*
*TABLES/WORK AREAS - RSTWA, TIOT.
*
*ATTRIBUTES - REENTRANT, SUPERVISOR STATE, TYPE 4 SVC ROUTINE.
*
*RESTRICTIONS OF THE USE OF ROUTINE - AOS/2 ONLY.
*
*CHANGE ACTIVITY
*
*1387380200-381100,544000                                        A43260
*1053206000-207000,296000,758300,758500,759000,774000,777400,    A37817
*1053                                                            M6115
*1053778000                                                      A37817
*1053752400                                                      A29744
*1053                                                            A30491
*1053768000                                                      A33991
*1053378000-384000,752300                                        A32683
*  APAR 25627 IS TEMPORARILY FIXED IN REL'S 17 & 18 IN MODULE    A25627
* IHJARS60.  IT IS PERMANENTLY FIXED IN REL 19 BY SAN JOSE       A25627
* (UNDER APAR 28676) AND KINGSTON(UNDER APAR 26118).  CHECK THE  A25627
* FOLLOWING DATA MANAGEMENT AND SUPERVISOR MODULES RESPECTIVELY. A25627
* IGC0G05B,IGC0H05B,IGC0T05B, AND IGC0605B,IGC0805B.             A25627
*1053779800-780000                                               M6254
*
*      VS2 RELEASE 037 ADDS/DELETETIONS/CHANGES
*A409200                                                       @ZA13512
*C409200,A759800                                               @ZA15683
*A409200-409280,409440                                         @ZA27549
*D409200-409440,759800                                         @ZA27748
*NOTES - NONE
*
**********************************************************************
         BALR  RB,ZERO             SET UP
         USING *,RB                MODULE ADDRESSABILITY
         B     BEGIN               BRANCH AROUND EYE CATCHER
         CNOP  0,8                 ALIGN TO A DOUBLE WORD BOUNDARY
         DC    C'IGC0V05B'         CSECT NAME
         DC    C'@ZA27748'         LAST CODE SHIPPED           @ZA27748
         DC    C'&SYSDATE'         DATE LAST CODE SHIPPED      @ZA27748
BEGIN    EQU   *
         USING RSTWA,RC            WORK AREA ADDRESSABILITY
         USING TCB,R4              ADDRESS TCB.
         LR    RD,R5                    SAVE UCB ADDRESS         A37817
         LR    RC,R1                   SET UP                    M6115
         USING RBSECT,R5           ADDRESS RESTART SVRB.
         L     R4,RSTCBAD          GET ADDR OF TCB.
         L     R5,TCBRBP           GET ADDRESS OF RESTART SVRB.
         L     RA,RSCKDCB          GET ADDRESS OF RESTART DCB.
         MVC   RBEXSAVE(LFLAG),RSFLAG1       SAVE RESTART FLAGS.
*
         LH    R8,RSRETCOD              GET ERROR RETURN CODE   YA00343
*                                       FOR USE IN MSG ROUTINE  YA00343
*
*                                                                     *
***********************************************************************
*                                                                     *
*        IF ANY ERRORS HAVE OCCURRED RESTARTING JOB, GO TO ABEND JOB  *
*                                                                     *
***********************************************************************
*                                                                     *
         LR    R3,RC               FETCH ADDR OF RESTART WA      A32683
         A     R3,RSBFOFST         CALC ADDR OF MESSAGE BUFFER   A32683
         LTR   R8,R8               HAVE ANY ERRORS OCCURED?     YA00343
         BNZ   RERABEND            YES-ABEND JOB.
         L     R6,RSWACL           SAVE LENGTH OF W.A. TO BE FREED
*                                                                     *
***********************************************************************
*                                                                     *
*        CHKPT SUPPLIED BLOCKSIZE FOR CHKPT DCB, RESTORE USER         *
*        BLKSIZE                                                      *
*                                                                     *
***********************************************************************
*                                                                     *
RERCOM2  EQU   *
         USING IHADCB,RA
         MVC   DCBBLKSI(LBLKSZE),RSDCBSI    RESTORE USER BLKSIZE
*                                                                     *
***********************************************************************
*                                                                     *
*        BRANCH TO SUBROUTINE TO ISSUE CONSOLE MESSAGE AND            *
*        FREEMAIN (SUBPOOL 253) RESTART WORKAREA                      *
*                                                                     *
***********************************************************************
*                                                                     *
RERCOM3  EQU   *
         MVC   RBEXSAV8(N8),RSNSTAE SAVE USER SCB & PIE CHAINS   Y02076
         LR    R0,R3               FETCH ADDR OF MSG BFR         A32683
         SR    R0,RC               SUBTRACT ADDR OF RS WA        A32683
         N     R0,RERMASK          MAKE A DOUBLEWORD MULTIPLE    A43260
         LR    R9,R0               SAVE LENGTH FOR LATER         A43260
         SR    R6,R0               SUBTRACT FROM TOTAL WA LEN    A32683
         O     R0,SP253            SET SUBPOOL 253 IN LENGTH     A32683
         LR    R1,RC               FETCH ADDR OF CORE TO FREE    A32683
         FREEMAIN R,LV=(0),A=(1)   FREE FIRST PART OF RS WA      A32683
*
         BAL   R2,RERWTO           BR TO WRITE CONSOLE MESSAGE   A32683
*
         LR    R1,R9               GET LENGTH FREED              A32683
         AR    R1,RC               ADD START ADDRESS OF RS WA    A32683
         LR    R0,R6               FETCH LEN OF RS WA TO FREE    A32683
         O     R0,SP253            SET SUBPOOL 253 IN LENGTH     A32683
         FREEMAIN R,LV=(0),A=(1)   FREE WORK AREA CORE.
*                                                                     *
***********************************************************************
*                                                                     *
*        IF CHKPT OPEN'D CHKPT D.S. RESTART WILL CLOSE IT             *
*                                                                     *
***********************************************************************
*                                                                     *
RERCOM5  EQU   *
         L     RF,DCBDEBAD         FETCH DEB ADDRESS             YM5921
         LA    RE,DEBBASIC-DEB     GET PREFIX LENGTH             YM5921
         SR    RF,RE               POINT TO DEB PREFIX           YM5921
         USING DEB,RF              ADDRESS DEB PREFIX            YM5921
         L     RE,DEBXTNP          FETCH DEB EXT ADDRESS         YM5921
         USING DEBXTN,RE           ADDRESS DEB EXTENSION         YM5921
         L     RE,DEBXDSAB         FETCH DSAB ADDRESS            YM5921
         USING DSAB,RE             ADDRESS DSAB                  YM5921
         LR    RD,RA               INIT CLOSE DCB ADDR           YM5921
*                                                                     *
***********************************************************************
*                                                                     *
*        TEST FOR SECURITY INTERFACE. IF ONE EXISTS, UPDATE THE       *
*        USER'S DCB BEFORE RETURNING TO HIM.                          *
*                                                                     *
***********************************************************************
*                                                                     *
         TM    DSABFLG4,DSABCKSI   DOES S/I EXIST                YM5921
         BZ    RERCOM6             IF NOT, BRANCH                YM5921
         LA    RC,FOUR             GET ADDR OF USER DCB ADDR VIA YM5921
         SR    RD,RC               PROTECTED DCB ADDR -4         YM5921
         L     RD,0(,RD)           FETCH USER'S DCB ADDR         YM5921
         MODESET KEYADDR=TCBPKF,WORKREG=7 ASSUME CALLER KEY      YM5932
         MVC   DCBRELAD-IHADCB(LUPDATE1,RD),DCBRELAD UPDATE USER YM5921
         MVC   DCBDEBAD-IHADCB(L'DCBDEBAD,RD),DCBDEBAD DCB       YM5921
         MODESET EXTKEY=ZERO       RESUME KEY ZERO               YM5932
RERCOM6  EQU   *                   NON-S/I ENTRY                 YM5921
         TM    RBEXSAVE,RSOPEN     DID CHKPT OPEN DATA SET
         BZ    RERSKOP             IF NOT, GO EXIT               YM5921
         ST    RD,RBEXSAV4         SET UP CLOSE'S PARM LIST      YM5921
         TM    DEBOFLGS,DBMOD      IS THIS A MOD DATA SET        YM5744
         BO    RERNOTMD            BRANCH IF NOT MOD
         MVI   RBEXSAV4,RERLEAVE   FOR A MOD D/S, CLOSE OPTION   YM5921
*                                  IS LEAVE
         B     RERCLOSE            GO CLOSE DATA SET
RERNOTMD EQU   *
         MVI   RBEXSAV4,RERRWIND   CLOSE OPTION IS REWIND        YM5921
RERCLOSE EQU   *
         MODESET KEYADDR=TCBPKF,WORKREG=7 ASSUME CALLER KEY      YM5932
         CLOSE MF=(E,RBEXSAV4)     CLOSE CHKPT DATA SET.
         MODESET EXTKEY=ZERO       RESUME KEY ZERO               YM5744
*                                                                     *
***********************************************************************
*                                                                     *
*        RETURN TO USER VIA SVC 3 WITH RETURN CODE = 4 IN REG15       *
*        INDICATING SUCCESSFUL RESTART                                *
*                                                                     *
***********************************************************************
*                                                                     *
RERSKOP  EQU   *
         LA    RF,RESTART          LOAD RESTART RETURN CODE IN REG 15.
         MVC   TCBNSTAE(FOUR),RBEXSAV8 RESTORE USER SCB CHAIN    Y02076
         MVC   TCBPIE(FOUR),RBEXSAV8+FOUR RESTORE USER PIE CHAIN Y02076
         SVC   3    RETURN CONTROL TO USER VIA EXIT SVC.
*                                                                YM5921
         DROP  RE,RF               DSAB & DEB                    YM5921
*                                                                     *
***********************************************************************
*                                                                     *
*        BRANCH TO SUBROUTINE TO ISSUE CONSOLE MESSAGE AND            *
*        ISSUE SYSTEM ABEND 13F FOR UNSUCCESSFUL RESTART              *
*                                                                     *
***********************************************************************
*                                                                     *
RERABEND EQU   *
         BAL   R2,RERWTO           GO TO WRITE MESSAGE TO CONSOLE
         L     R1,ABENDCOD         LOAD PARM REG WITH ABEND CODE.
         ABEND (1)                 ABEND JOB.
*                                                                     *
***********************************************************************
*                                                                     *
*        SUBROUTINE TO ISSUE CONSOLE MESSAGE                          *
*                                                                     *
***********************************************************************
*                                                                     *
RERWTO   EQU   *
         L     R7,TCBTIO           GET ADDR OF TIOT
         LTR   R8,R8               ANY ERRORS OCCURED?          YA00343
         BNZ   RERMSG1             YES-GO WRITE UNSUCCESSFUL MESSAGE
*                                                                     *
***********************************************************************
*        CONSTRUCT MESSAGE FOR SUCCESSFUL RESTART                     *
***********************************************************************
*                                                                     *
         USING MSG2SECT,R3
         MVC   MC2PLIST(LPLIST2),MSG2  MOVE IN MSG2 SHELL
         MVC   MSG2JOB(LJOBNM),ZERO(R7)  MOVE JOBNAME TO MSG2
         LA    R1,MC2PLIST
         B     RERWT               GO TO WRITE MESSAGE
*                                                                     *
***********************************************************************
*        CONSTRUCT MESSAGE FOR UNSUCCESSFUL RESTART                   *
***********************************************************************
*                                                                     *
RERMSG1  EQU   *
         USING MSG1SECT,R3
         MVC   MC1PLIST(LPLIST1),MSG1  MOVE IN MSG1 SHELL
         MVC   MSG1JOB(LJOBNM),ZERO(R7)  MOVE JOBNAME TO MSG1 FROM TIOT
         CH    R8,K28                   IS RETURN CODE 28.       A37817
         BE    GETUCB                   YES-GO SET UNIT ADDR.    A37817
         CH    R8,K40                   IS RETURN CODE 40.       A37817
         BE    GETUCB                   YES-GO SET UNIT ADDR.    A37817
         CH    R8,K60                   IS RETURN CODE 60.       A37817
         BE    GETUCB                   YES-GO SET UNIT ADDR.    A37817
         CH    R8,K64                   IS RETURN CODE 64.       A37817
         BE    GETUCB                   YES-GO SET UNIT ADDR.    A37817
         CH    R8,K218                  IS RETURN CODE 218       YM7337
         BE    GETUCB                   YES-GO SET UNIT ADDR.    YM7337
         CH    R8,K251                  IS RETURN CODE 251       YM7337
         BE    GETUCB                   YES-GO SET UCB ADDR.     YM7337
*
CONTMSG  EQU   *                                                 A37817
         CVD   R8,MSGWK            CONVERT ERROR CODE TO
         UNPK  MSGWK(THREE),MSGUNPK(TWO)    PRINTABLE CHARACTER
         OI    MSGZONE,EBCDICMK    CONVERT 2ND BYTE TO EBCDIC
         MVC   MSG1ERCD(THREE),MSGWK  MOVE ERROR CODE TO MSG1    YM5301
         LA    R1,MC1PLIST         GET ADDR OF PARM LIST
RERWT    EQU   *
         WTO   ,MF=(E,(1))
         BR    R2                  RETURN TO CALLER
GETUCB   EQU   *                        SET UP UNIT ADDR  IN     A37817
         MVI   MSG1ERCD+3,C','           ERROR MESSAGE.          YM5955
         MVC   MSG1ERCD+4(3),13(RD)     GET UNIT ADDR FROM UCB.  YM5955
         MVI   MSG1ERCD+7,C')'                                   YM5959
         B     CONTMSG                  FINISH BUILDING ERR MSG. A37817
MSG1     WTO   'IHJ007I RESTART NOT SUCCESSFUL FOR          (   )    ',*
               MF=L,ROUTCDE=(2,11),DESC=(6)                      YM5955
* MSG1 TEXT = IHJ007I RESTART NOT SUCCESSFUL FOR JOBNAME (XX,UUU)
         SPACE
MSG2     WTO   'IHJ008I          RESTARTED',MF=L,ROUTCDE=(2,11),       *
               DESC=(6)
* MSG2 TEXT = IHJ008I JOBNAME RESTARTED
LPLIST1  EQU   61                       ERR MSG LENGTH           YM5955
LPLIST2  EQU   34
ZERO     EQU   0
LJOBNM   EQU   8
EBCDICMK EQU   X'F0'
THREE    EQU   3
TWO      EQU   2
         DS    0F
ABENDCOD DC    X'8013F000'         ABEND CODE FOR UNSUCCESSFUL   A33991
*                                       RESTART                  A33991
SP253    DC    X'FD000000'         SUBPOOL 253 FOR RSTWA FREEMAIN
RERMASK  DC    X'00FFFFF8'         MASK FOR LENGTH OF FREEMAIN   A32683
MAXMSGLN DC    H'60'                    MAXIMUM MESSAGE LENGTH   A37817
RER256   DC    H'256'
K28      DC    H'28'                                             A37817
K40      DC    H'40'                                             A37817
K60      DC    H'60'                                             A37817
K64      DC    H'64'                                             A37817
K218     DC    H'218'                   218 ERROR CODE CONSTANT  YM7337
K251     DC    H'251'                   251 ERROR CODE CONSTANT  YM7337
*                                                                     *
*        MAINTENANCE SPACE                                            *
*                                                                     *
PATCH    DC    XL((*-IGC0V05B)/20)'00'   FIVE PERCENT MAINTENANCE SPACE
*                                                                     *
MSG1SECT DSECT
         DS    24F
CK1WORD  DS    F
MC1PLIST DS    0F
MSG1LN   DS    CL2                 LENGTH OF MESSAGE
MSG1FLGS DS    CL2                 MCS FLAGS
MSG1BGIN DS    CL35                     MESSAGE TEXT--NOT        A37817
*                                       RESTARTED                A37817
MSG1JOB  DS    CL8                 JOBNAME
         DS    CL2                 OPEN PAREN FOR ERROR CODE
MSG1ERCD DS    CL7                      ERROR CODE               YM5955
         DS    CL1                 CLOSED PAREN FOR ERROR CODE
MSG1CDS  DS    CL4                 MCS DESCRIPTOR & ROUTCODES
UNPKAREA DS    0D
MSGWK    DS    CL1                 STORE UNPKD ERROR MSG CODE
MSGCD    DS    CL1                 2 BYTE MSG CODE
MSGZONE  DS    CL4                 ZONE FIELD OF MSG CODE
MSGUNPK  DS    CL2                 MSG CODE TO BE CONVERTED
MSG2SECT DSECT
MC2PLIST DS    0F
MSG2LN   DS    CL2                 MESSAGE LENGTH
MSG2FLGS DS    CL2                 MCS PLAGS
MSG2BGIN DS    CL8                 MESSAGE ID
MSG2JOB  DS    CL8                 JOBNAME
         DS    CL10                MESSAGE TEXT--RESTARTED
MSG2CDS  DS    CL4                 MCS DESCRIPTOR & ROUTE CODES
         SPACE
         IEEVRSWA VER=3                 USE AOS2 VERSION OF WA
         DCBD  DSORG=(BS,PO),DEVD=(DA,TA)
LUPDATE1 EQU   DCBTRBAL-IHADCB+L'DCBTRBAL LENGTH OF 1ST DCB UPDT Y02083
         IKJTCB
         IKJRB
         IEZDEB                                                  Y02083
         IHADSAB                                                 Y02083
DBOFLAGS EQU   8
DBMOD    EQU   X'40'
RERLEAVE EQU   X'B0'
RERRWIND EQU   X'90'
RSSPBKSZ EQU   X'08'
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
RA       EQU   10
RB       EQU   11
RC       EQU   12
RD       EQU   13
RE       EQU   14
RF       EQU   15
*
*        DECIMAL CONSTANTS, POSITIONS, LENGTHCODES
*
ONE      EQU   1                   DECIMAL - 1
FOUR     EQU   4                   DECIMAL - 4                   Y02083
N8       EQU   8                   DECIMAL - 8                   Y02076
DEC256   EQU   256                 DECIMAL - 256
*
CONDCODE EQU   8                   CONDITION CODE
SEQPOS   EQU   37                  DEB-VOL-SEQ POSITION
*
RBEXSAV4 EQU   RBEXSAVE+4          DCB CLOSE PARM
RBEXSAV8 EQU   RBEXSAVE+8          TEMP SAVE FOR SCB/PIE CHAINS  Y02076
*
LMSG1    EQU   8         LENGTH OF MSG1-INITIAL
LMSG1A   EQU   35                  MSG1-ALL
LMSG2    EQU   10                  MSG2
LMSG3    EQU   5                   MSG - ' (  )'
LFLAG    EQU   1                   RESTART FLAG
LRETCOD  EQU   2                   RET. CODE
LBLKSZE  EQU   2                   BLOCK SIZE
LTIOT    EQU   4                   TIOT
LJOBNAME EQU   8                   JOBNAME
LERRCODE EQU   2                   ERROR CODE
*
*        HEXADECIMAL VALUES ( CODES,MASKS)   AND CHARACTERS
*
HEXONE   EQU   X'01'     HEXADECIMAL - 1
ORMK     EQU   X'F0'               MASK
MSGCODE  EQU   C'8'                MESSAGE CODE
VOLSEQNO EQU   37        OFFSET TO VOL SEQ NO IN DEB
FIRSTVOL EQU   X'01'     VOL SEQ NO IN CHKPT DEB SET TO INDICATE 1STVOL
*
         END
