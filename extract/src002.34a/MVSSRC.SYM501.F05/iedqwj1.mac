         TITLE 'IEDQWJ1 TRM PROMPTER MODULE 2'
IEDQWJ1  CSECT
* CHANGE ACTIVITY AS FOLLOWS:                                    S22024
*A500000,502000,529000
*C138000,458000,465000,500500-501000
*D460000
*A600016,600312,647050                                          SA41595
*A152000,155600,202000,569000,633000                            SA41606
*C195000,197000,423000,425000                                   SA41606
*C185000,201000                                                 SA41615
*A001330,003100-003800,135711-135717,135811-135817               S22024
*A425700-425960,435010-435420                                    S22024
*A539200-539600,552500,565030-565750,634700,646200-646600        S22024
*A647063                                                         S22024
*C005000,142000-144000,152000,157000-157600,158200,160000        S22024
*C162000-162500,174100,174300,175000-176000,177100-180100        S22024
*C182500,186000,190100,193000-194000,196000                      S22024
*C198600-199200,203000-203500,207000,209000,218000,232000        S22024
*C238000,240000-241000,248000,252000-253000,261000,263000        S22024
*C272000-273000,277000-278000,279500,281000,283000,285000        S22024
*C287000-288000,301000,302000,307000,313000-317000,322000        S22024
*C329000,331000,340000,342000,354000,356000,364000-365000        S22024
*C367000-367500,369000,371000,405000,407000,413000,419000        S22024
*C430000,432000,435600,435660,435700-435720,435600,435660        S22024
*C435700-435720,435980,435988,437000,439000,441000-442000        S22024
*C444500,446000,449000,455000,,466000-467000,472000-473000       S22024
*C475000,481000-483100,485000-486000                             S22024
*C492000,494000,497000,500100,500300-501300,502400,507000-508000 S22024
*C512000,515000,517000,519000,524000,526000-528700               S22024
*C529500-533100,535000-536000,537500,546000-547000               S22024
*C551100-551500,553000-561000,567000,569360,569810               S22024
*C583000,585000,587000,590000-591000,596000-599000               S22024
*C647840,647890                                                  S22024
*D218000,230000                                                  S22024
*D529000,542000,544000,548000,562000,600000                      S22024
*D595000                                                       @OX12556
**************************************************************** S99528
*                                                              * S99528
* TITLE = TRM PROMPTER MODULE 2                                * S22024
*                                                              * S22024
* MODULE NAME = IEDQWJ1                                        * S22024
*                                                              * S22024
* DESCRIPTIVE NAME = TRM PROMPTER MODULE 2                     * S22024
*                                                              * S22024
* COPYRIGHT = NONE                                             * S22024
*                                                              * S22024
* STATUS --                                                    * S99528
*    CHANGE LEVEL 2                                            * S22024
*                                                              * S99528
* FUNCTIONS --                                                 * S99528
*    THE PURPOSE OF THOS MODULE IS TO PROMPT THE CE FOR THE    * S99528
*    TEST AND OPTIONS FIELDS OF THE TRM                        * S99528
*                                                              * S99528
*    THE CE IS FIRST PROMPTED FOR THE TEST ID. THE CE IS THEN  * S99528
*    ASKED IF HE WANTS TO SELECT ROUTINES IN THIS TEST. IF HE  * S99528
*    ANSWERS YES HE IS PROMPTED FOR ROUTINE NUMBERS. IF HE     * S99528
*    ANSWERS NO HE IS ASKED IF HE WANTS TO RUN OTHER TESTS ON  * S99528
*    THESE SAME TEST DEVICES.                                  * S99528
*                                                              * S99528
*    ALL TEST , ROUTINES, AND ADDITIONAL TEST ENTRIES ARE      * S99528
*    CHECKED FO SYNTAX.                                        * S99528
*                                                              * S99528
*    AFTER PROMPTING FOR THE TEST FIELD THE CE IS THEN PROMPTED* S99528
*    FOR THE OPTION FIELD. FIRST THE CE IS TOLD THE DEFAULT    * S99528
*    OPTIONS, THEN THE VALID OPTIONS. ALL OPTIONS ARE CHECKED  * S99528
*    FOR VALIDITY.                                             * S99528
*    IF THE AP OPTION IS ENTERED A FLAG IS SET TO CALL IEDQWJ2 * S99528
*    TO PROMPT FOR THE AP LOCATION.                            * S99528
*                                                              * S99528
*    IF AT ANY TIME DURING TEST AND OPTION FIELDS PROMPTING THE* S99528
*    CE MAKES AN ERROR HE IS GIVEN A CHANCE TO CANCEL          * S99528
*    PROMPTING AND REJECT THE TRM.                             * S99528
*                                                              * S99528
* ENTRY POINTS --                                              * S99528
*                                                              * S99528
*    IEDQWJ1 - CALLED BY TRM PROMPTER MODULE 1 (IEDQWJ)        * S99528
*    TO PROMPT TEST AND OPTIONS FIELDS.                        * S99528
*                                                              * S99528
* INPUT --                                                     * S99528
*    REGISTERS 02,13,14,15 CONTAIN THE FOLLOWING VALUES:       * S99528
*                                                              * S99528
*         02 - OLTCB POINTER                                   * S99528
*         13 - CALLING MODULE'S SAVE AREA ADDRESS              * S99528
*         14 - RETURN ADDRESS                                  * S99528
*         15 - ENTRY POINT ADDRESS                             * S99528
*                                                              * S99528
* OUTPUT --                                                    * S99528
*    NONE                                                      * S99528
*                                                              * S99528
* EXITS,NORMAL --                                              * S99528
*    RETURN TO CALLER                                          * S99528
*                                                              * S99528
* EXITS, ERROR --                                              * S99528
*    NONE                                                      * S99528
*                                                              * S99528
* EXTERNAL REFERENCES --                                       * S99528
*    LINK TO IEDQWO TO PRINT DPRINT LINE                       * S99528
*                                                              * S99528
* TABLES/WORK AREAS --                                         * S99528
*    OLTCB                                                     * S99528
* ATTRIBUTES --                                                * S99528
*    ENABLED, PROBLEM PROGRAM MODE, TRANSIENT.                 * S99528
*                                                              * S99528
* NOTES --                                                     * S99528
*    NONE                                                      * S99528
*                                                              * S99528
**************************************************************** S99528
         EJECT
*
*              INITIALIZATION
*
*
R0       EQU   0                  REG 0                          S99528
R1       EQU   1                  REG 1                          S99528
R2       EQU   2                  REG 2                          S99528
R3       EQU   3                  REG 3                          S99528
R4       EQU   4                  REG 4                          S99528
R5       EQU   5                  REG 5                          S99528
R6       EQU   6                  REG 6                          S99528
R7       EQU   7                  REG 7                          S99528
R8       EQU   8                  REG 8                          S99528
R9       EQU   9                  REG 9                          S99528
R10      EQU   10                 REG 10                         S99528
R11      EQU   11                 REG 11                         S99528
R12      EQU   12                 REG 12                         S99528
R13      EQU   13                 REG 13                         S99528
R14      EQU   14                 REG 14                         S99528
R15      EQU   15                 REG 15                         S99528
PARMREG0 EQU   0                  PARAMETER REG 0                S99528
PARMREG1 EQU   1                  PARAMETER REG 1                S99528
XFF      EQU   X'FF'              MASK BYTE                      S99528
D7       EQU   7                  DISP OF 7                      S99528
D29      EQU   29                 DISP OF 29                     S99528
KT       EQU   C'T'               CHAR T                         S99528
X40      EQU   X'40'              MASK BYTE                      S99528
D0       EQU   0                  DISP OF 0
D1       EQU   1                  DISP OF 1
D2       EQU   2                  DISP OF 2
D3       EQU   3                  DISP OF 3
D4       EQU   4                  DISP OF 4
D8       EQU   8                  DISP OF 8
D12      EQU   12                 DISP OF 12
D16      EQU   16                 DISP OF 16
D17      EQU   17                 DISP OF 17
D20      EQU   20                 DISP OF 20
D26      EQU   26                 DISP OF 26                     S22024
D24      EQU   24                 DISP OF 24
GO       EQU   C'G'               USE TRM AS IS                  S22024
CAN      EQU   C'C'               CANCEL TRM                     S22024
REENT    EQU   C'R'               REENTER TRM                    S22024
TRMLEN   EQU   72                 LENGTH OF TRM BUFFER           S22024
NUMER    EQU   X'F0'              NUMERIC ENTRY                  S22024
YES      EQU   X'E8'              POSITIVE RESPONSE              S22024
NO       EQU   X'D5'              NEGATIVE RESPONSE              S22024
RTNSFLG  EQU   X'80'              END OF TEST FIELD
RTNDFLG  EQU   X'40'              INCLUSIVE ROUTINES
RTNCFLG  EQU   X'20'              SEPARATE ROUTINE ENTRY
RTNNFLG  EQU   X'10'              SECOND PASS FLAG
RTNIFLG  EQU   X'08'              INCLUSIVE RTN LOOP FLAG
SLASH    EQU   X'61'              SLASH
COMMA    EQU   X'6B'              COMMA
DASH     EQU   X'60'              DASH
D40      EQU   40                 DISP OF 40                     S22024
F0       EQU   X'F0'              EBCDIC 0
F9       EQU   X'F9'              EBCDIC 9
XE9      EQU   X'E9'              CHAR Z
XEA      EQU   X'EA'              CHAR COMPARE BYTE              S22024
XC0      EQU   X'C0'              MASK COMPARE
D5       EQU   5                  DISP OF 5                      S22024
X0F      EQU   X'0F'              MASK FIELD                     S22024
LEV0     EQU   X'F0'              EP OPTION LEVEL 0              S22024
LEV3     EQU   X'F3'              EP OPTION LEVEL 3              S22024
INCLUS   EQU   X'80'              INCLUSIVE ROUTINES
BLANK    EQU   X'40'              A BLANK CHAR
         EJECT
         USING IEDQWJ1,R15
         SAVE  (14,12)
IEDQWJ1  IEDHJN IEDJ1,HJN
         LR    R6,R15                   LOAD BASE
         DROP  R15
         USING IEDQWJ1,R6
         USING TOTOLTCB,R2
         LA    R3,TOTSAVE4        SAVE AREA ADDR                 S22024
         ST    R3,D8(R13)         CALLER'S SAVE                  S22024
         ST    R13,D4(R3)         SAVE CALLER'S                  S22024
         LR    R13,R3                   SET R13 TO SAVE AREA
         LA    R3,TOTTRMBF        POINT TO TRM BUFFER            S99528
         ST    R3,TRMSAVE              SAVE
         MVC   TRMWRK(TRMLEN),D0(R3)                             S22024
         TM    TOTFLG03,TOTTSTER  TEST ERROR                    SA41606
         BO    FLD4               YES                           SA41606
         TM    TOTFLG03,TOTOPTER  OPTION ERROR                   S99528
         BO    OPTROUT                 YES BRANCH
         TM    TOTFLG03,TOTAPOER  AP ERROR                       S99528
         BO    OPTROUT            YES - BRANCH
FLD4     EQU   *                                                SA41606
         LA    R4,TRMWRK               SET UP POINTER
         LA    R3,D2              SET UP COUNTER                 S22024
FLDHUNT  EQU   *
         CLI   D0(R4),SLASH       END OF FIELD                   S22024
         BE    FLDFIND                 YES BRANCH
         LA    R4,D1(R4)          NO - BUMP POINTER              S22024
         B     FLDHUNT                 GO LOOK AGAIN
FLDFIND  EQU   *                                                 S22024
         LA    R4,D1(R4)          BUMP PAST SLASH                S22024
         BCT   R3,FLDHUNT         FIND END OF TEST DEVICE FIELD  S99528
         MVI   TOTCROLT,X40       BLANK TEST ID                  S99528
         MVC   TOTCROLT+D1(D7),TOTCROLT FIELD                    S99528
         MVI   TOTOLTTB,X40       BLANK TSST SECTION             S99528
         MVC   TOTOLTTB+D1(D29),TOTOLTTB SUFFIX TABLE            S99528
*
*              R4 POINTS TO TEST FIELD
*
TEST     EQU   *
*
*              R4 POINTS TO TEST FIELD
*
         IEDQMSG MSGID=075,FUNCT=REP,LINK=YES,INBUF=CEIN,INCNT=8 S99528
*        ENTER TEST TO BE RUN                                    S99528
         SPACE
         LA    R5,CEIN            POINT TO INPUT BUFFER
         TM    D0(R5),NUMER       NUMERIC START                  S22024
         BO    TEST01             YES - NO PREFIX GIVEN
         MVC   TOTCROLT(D1),D0(R5) SET PREFIX
         LA    R5,D1(R5)          POINT PASS PREFIX              S22024
         B     TEST02             BYPASS DEFAULT MOVE OF 'T'
TEST01   EQU   *
         MVI   TOTCROLT,KT        SET SECTION PREFIX             S99528
TEST02   EQU   *
          MVC   WORK1(5),0(R5)
         LA    R8,TOTOLTTB        POINT TO SUFFIX TABLE          S99528
         LA    R9,TOTCROLT+D1     POINT TO DEVICE TYPE FIELD     S99528
         NC    WORK1(D4),NUMWRK   CLEAR UNWANTED FLAGS           S22024
         CLC   WORK1(D4),NUMWRK   NUMERIC CHAR                   S22024
         BNE   TESTERR                 NO BRANCH TO ERRROR ROUT
         MVC   D0(D4,R4),D0(R5)   MOVE DEVICE TYPE TO TRM        S22024
         MVC   D0(D4,R9),D0(R5)   MOVE DEVICE TYPE TO TABLE      S22024
         LA    R4,D4(R4)          POINT TO NEXT PLACE            S22024
         LA    R5,D4(R5)          POINT TO SUFFIX PLACE          S22024
         LA    R10,D0             SET COUNTER                    S22024
         LA    R1,D3              SET COMPARAND                  S22024
         LA    R7,D0              SET COMPARAND                  S22024
         LR    R3,R5              POINT TO SUFFIX
TESTA    EQU   *
         CLI   D0(R3),BLANK       END OF ENTRY                   S22024
         BNE   TESTB                   NO BRANCH
         CR    R7,R10                   ANY ALPHA CHARACTERS?
         BE    TESTD              NO ERROR                      SA41615
         LA    R15,TRMWRKE-D2     END OF TRM BUFFER              S22024
         SR    R15,R10                 SUBTRACT LENGTH OF NEW ENTRY
         CR    R15,R4                  IS THERE ROOM FOR NEW ENTRY?
         BNH   TOOLONG                   NO
         EX    R10,MOVER          MOVE SUFFIX CHARS TO TRM       S22024
         EX    R10,MOVETBL        MOVE SUFFIX CHARS TO TABLE
         LA    R8,D3(R8)          POINT TO NEXT SLOT IN TABLE    S22024
         ST    R8,TDSAVE          SAVE NEXT AVAIL. SUFX SLOT
         LA    R4,D0(R10,R4)      INCREMENT POINTER              S22024
         B     TEST3              GET NEXT ENTRY                 S99528
TESTB    EQU   *                                                 S22024
         MVC   WORK1(D1),D0(R3)   GET NEXT CHAR                  S22024
         CLI   WORK1,XC0          CHAR LESS THAN 'A'             S22024
         BNH   TESTD              NO - ERROR                    SA41606
         CLI   WORK1,XEA          CHAR GRT THAN 'Z'              S22024
         BNL   TESTD              NO - ERROR                    SA41606
TESTC    EQU   *
         LA    R10,D1(R10)        BUMP COUNTER                   S22024
         LA    R3,D1(R3)          BUMP POINTER                   S22024
         CR    R10,R1                   GT 3
         BH    TESTD              NO ERROR                      SA41615
         B     TESTA              NO                             S99528
         SPACE
TESTD    EQU   *                                                SA41606
         S     R4,K4              RESET TRM POINTER             SA41606
         B     TESTERR            REPORT ERROR                  SA41606
         SPACE
MOVER    MVC   D0(D0,R4),D0(R5)   SUFFIX CHARS TO TRM            S22024
MOVETBL  MVC   D0(D0,R8),D0(R5)   SUFFIX CHARS TO TABLE          S22024
TEST3    EQU   *
         IEDQMSG MSGID=074,FUNCT=REP,INBUF=CEIN,INCNT=8 SEL RTN? S9952,
         SPACE
         CLI   CEIN,YES           YES                            S22024
         BE    TEST1                   YES BRANCH
         CLI   CEIN,NO            NO                             S22024
         BE    TEST2                   YES BRANCH
         LA    R9,TEST3
         B     INVAL              INVALID RESPONSE               S99528
TEST1    EQU   *
         IEDQMSG MSGID=073,FUNCT=REP,INBUF=CEIN,INCNT=8 ENT RTN #S99528
         SPACE
         LR    R3,R4                   SAVE POINTER
         LA    R1,CEIN            SET CEIN POINTER
         LA    R7,D3              SET COMPARAND                  S22024
         LA    R10,CEIN                 SET CEIN POINTER
         LA    R8,CEIN                 SET POINTER FOR MOVE
TEST6    EQU   *
         B     RTNCHK             SET ROUTINE MASK
TEST7    EQU   *
         SPACE
         MVI   D0(R4),COMMA       MOVE IN DELIMITER
         LA    R4,D1(R4)          INCREMENT TRM POINTER
         LA    R15,TRMWRKE-D2     END OF TRM BUFFER              S22024
         L     R5,KCNT            GET ENTRY LENGTH
         SR    R15,R5                  SUBTRACT LENGTH OF NEW ENTRY
         CR    R15,R4                  WILL NEW ENTRY FIT IN BUFFER?
         BNH   TOOLONG                   NO
         BCTR  R5,R0              DECREMENT COUNT FOR EXECUTE    S22024
         EX    R5,RMOVE                MOVE RTN TO TRMWRK
         LA    R5,D1(R5)          BUMP COUNTER                   S22024
         LA    R4,D0(R5,R4)       BUMP TRM POINTER               S22024
         B     TEST8              CLOSE TEST FIELD
RMOVE    MVC   D0(D0,R4),D0(R8)   RTN NUMBER TO TRM BUFFER       S22024
TEST5    OI    TRMFLG,FULL             TURN ON END FLAG
         B     TEST7              PUT ENTRY IN TRM BUFFER        S99528
TEST8    NI    TRMFLG,EMPTY            TURN OFF FLG
         MVI   D0(R4),SLASH       CLOSE FIELD                    S22024
         LA    R4,D1(R4)          BUMP PASS SLASH                S22024
         B     OPTION              BUILD OPTION FIELD            S99528
TEST2    EQU   *
         IEDQMSG MSGID=072,FUNCT=REP,INBUF=CEIN,INCNT=8  MORE    S99528
*                                                          SECT? S99528
         SPACE
         CLI   CEIN,YES           YES                            S22024
         BE    MOTEST                  YES BRANCH
         CLI   CEIN,NO            NO                             S22024
         BE    TEST8                   YES BRANCH
         LA    R9,TEST2
         B     INVAL               INVALID RESPONSE              S99528
MOTEST   EQU   *
         IEDQMSG MSGID=071,FUNCT=REP,INBUF=CEIN,INCNT=8 ALPHA SFXS99528
         SPACE
         LR    R8,R4              SAVE POINTER
         MVI   D0(R4),COMMA       INSERT A COMMA                 S22024
         LA    R4,D1(R4)          BUMP PASS COMMA                S22024
         LA    R10,CEIN                 SET CEIN POINTER
         LR    R3,R10                   SET UP REG FOR MOVE INST
MOTEST1  EQU   *
         LA    R5,D0              SET UP COUNTER                 S22024
         LA    R7,D7              COMPARAND                      S22024
MOTEST2  EQU   *
         CLI   D0(R10),BLANK      END OF ENTRY                   S22024
         BE    TEST8A                  YE BRANCH
         CLI   D0(R10),COMMA      ANOTHER ENTRY                  S22024
         BE    TEST8B                  YES BRANCH
         CLI   D0(R10),XC0        ALPHA                          S22024
         BL    TESTERR2                NO BRANCH
         CLI   D0(R10),XE9        CHAR Z                         S22024
         BH    TESTERR2            ERROR
         LA    R5,D1(R5)          BUMP COUNTER                   S22024
         LA    R10,D1(R10)        BUMP POINTER                   S22024
         CR    R7,R5                   MORE THAN 3?
         BE    TESTERR2                YES BRANCH
         B     MOTEST2            CHECK NEXT CHAR                S99528
TEST8A   EQU   *
         LTR   R5,R5                   AN× ENRIES?
         BZ    TEST80                  NO BRANCH
         CL    R5,K3              SUFFIX > 3                     S22024
         BH    TESTERR0           YES - ERROR                    S22024
         LA    R15,TRMWRKE-2           GET END OF TRM BUFFER
         SR    R15,R5                  SUBTRACT LENGTH OF NEW ENTRY
         CR    R15,R4                  WILL NEW ENTRY FIT IN BUFFER?
         BNH   TOOLONG                   NO
         BCTR  R5,R0              DECREMENT COUNT FOR EX         S99528
         EX    R5,MOVET                MOVE TO TRM
         L     R15,TDSAVE         POINT TO NEXT SUFX SLOT
         EX    R5,MOVTD           MOVE LAST SUFX TO TABLE
         LA    R5,D1(R5)          RESTORE COUNT                  S22024
         LA    R4,D0(R5,R4)       BUMP POINTER                   S22024
         B     TEST8              GET NEXT ENTRY                 S99528
TEST8B   EQU   *
         LTR   R5,R5                   AN× CHAR?
         BZ    TESTERR2                NO BRANCH
         CL    R5,K3              SUFFIX > 3                     S22024
         BH    TESTERR0           YES - ERROR                    S22024
         LA    R15,TRMWRKE-D2     END OF TRM BUFFER              S22024
         SR    R15,R5                  SUBTRACT LENGTH OF NEW ENTRY
         CR    R15,R4                  WILL IT FIT IN BUFFER?
         BNH   TOOLONG                   NO
         BCTR  R5,R0              DECREMENT COUNT FOR EX         S99528
         EX    R5,MOVET           MOVE DATE
         L     R15,TDSAVE         POINT TO NEXT SUFX SLOT
         EX    R5,MOVTD           MOVE SUFX TO TABLE
         LA    R5,D1(R5)          RESTORE COUNT                  S22024
         LA    R4,D0(R5,R4)       BUMP TRM POINTER               S22024
         MVI   D0(R4),COMMA       SET DELIMITER                  S22024
         LA    R4,D1(R4)          BUMP POINTER                   S22024
         LA    R10,D1(R10)        BUMP POINTER                   S22024
         LR    R3,R10                   SET UP MOVE REG
         LA    R15,D3(R15)        POINT TO NEXT SUFX SLOT        S99528
         ST    R15,TDSAVE         SAVE IT
         LA    R14,TOTOLTTE       END OF TABLE                   S99528
         CR    R14,R15            END OF TABLE ?
         BE    TEST80             YES
         B     MOTEST1            GO EXAMINE NEXT ENTRY
MOVET    MVC   D0(D0,R4),D0(R3)   GENERAL MOVE                   S99528
MOVTD    MVC   D0(D0,R15),D0(R3)  MOVE SUFFIX TO TABLE
TEST80   EQU   *
         BCTR  R4,R0              MOVE POINTER BACK TO COMMA     S22024
         B     TEST8                   GO CLOSE FIELD
         SPACE 2
TESTERR0 DS    0H                 BOUNDARY ALLIGNMENT            S22024
         LA    R15,TOTOLTTB+D3    RESET SUFFIX POINTER           S22024
         ST    R15,TDSAVE         SAVE IT                        S22024
         MVI   TOTOLTTB+D3,BLANK  BLANK REMAINDER OF TEST        S22024
         MVC   TOTOLTTB+D4(D26),TOTOLTTB+D3  SUFFIX TABLE        S22024
         B     TESTERR2           REPORT ERROR                   S22024
         SPACE 2
TESTERR  EQU   *
         IEDQMSG MSGID=070,FUNCT=CEC INVAL TEST NAME             S99528
TESTERRA EQU   *
         BAL   R9,CONTINUE        DO YOU WISH TO CONTINUE        S99528
         CLI   CEIN,YES           YES                            S22024
         BE    TEST                    YES BRANCH
         CLI   CEIN,NO            NO                             S22024
         BE    GETOUT                  YES BRANCH
         LA    R9,TESTERRA        GET ADDRESS FOR RETURN AFTER BRANCH
         B     INVAL              GO ISSUE INVALID RESPONSE MSG
TESTERR1 EQU   *
         IEDQMSG MSGID=069,FUNCT=CEC INVALID RTN NUMBER          S99528
         SPACE
TESERR1A EQU   *
         BAL   R9,CONTINUE        DO YOU WISH TO CONTINUE        S99528
         CLI   CEIN,YES           YES                            S22024
         BE    TESERR1B                YES BRANCH
         CLI   CEIN,NO            NO                             S22024
         BE    GETOUT             ERROR EXIT TO IEDQWE
         LA    R9,TESERR1A
         B     INVAL              INVALID RESPONSE               S99528
TESERR1B LR    R4,R3                   RESET POINTER
         B     TEST3              ASK QUESTION AGAIN             S99528
TESTERR2 EQU   *
         IEDQMSG MSGID=068,FUNCT=CEC INVALID TEST ENTRY          S99528
         SPACE
TESERR2A EQU   *
         BAL   R9,CONTINUE        CONTINUE?                      S99528
         CLI   CEIN,YES           YES                            S22024
         BE    TESERR2B                YES BRANCH
         CLI   CEIN,NO            NO                             S22024
         BE    GETOUT                  YES BRANCH
         LA    R9,TESERR2A
         B     INVAL              INVALID RESPONSE               S99528
TESERR2B LR    R4,R8                   RESTORE POINTER
         B     MOTEST             BRANCH
OPTROUT  EQU   *
         L     R3,TRMSAVE
         MVC   TRMWRK(TRMLEN),D0(R3) TRM TO WORK AREA            S22024
         LA    R3,D3              SET COUNTER                    S22024
         LA    R4,TRMWRK               SET POINTER
OPHUNT   EQU   *                                                 S22024
         CLI   D0(R4),SLASH       END OF FIELD                   S22024
         BE    OPTFIND                 YES BRANCH
         LA    R4,D1(R4)          NO - BUMP POINTER              S22024
         B     OPHUNT             FIND END OF FIELD
OPTFIND  EQU   *                                                 S22024
         LA    R4,D1(R4)          POINT PASS SLASH               S22024
         BCT   R3,OPHUNT          LOOP UNTIL END FOUND           S99528
*
*              R4 HAS ADDR OF OPTION FIELD
*
OPTION   EQU   *
*
*              R4 HAS ADDRESS OF OPTION FIELD
*
         NI    TRMFLG,X'00'            CLEAR FLAGS
         IEDQMSG MSGID=067,FUNCT=CEC DEFAULT OPTIONS             S99528
         SPACE
         IEDQMSG MSGID=066,FUNCT=CEC DEFAULT OPTIONS             S99528
         SPACE
EXT10    EQU   *
         IEDQMSG MSGID=065,FUNCT=CEC VALID OPTIONS               S99528
         SPACE
         IEDQMSG MSGID=064,FUNCT=CEC VALID OPTIONS               S99528
OPTENTER EQU   *   ENTER NEXT OPTION
         SPACE
         IEDQMSG MSGID=063,FUNCT=REP ENTER ONE OPTION            S99528
         CLC   CEIN(D4),NONE      NO MORE ENTRIES                S99528
         BE    OPTEND                  YES BRANCH
         OI    TRMFLG,FULL             FLAG THAT OPTION WAS ENTERED
         LA    R1,CEIN                 SET R1 TO INPUT AREA
OPTCHK1  EQU   *                                                 S99528
         CLC   D0(D2,R1),TL       TEST LOOP                      S99528
         BE    TLOPT                   YES BRANCH
         CLC   D0(D3,R1),NTL      NO TEST LOOP                   S99528
         BE    DEFAULT3           YES - BRANCH                   S99528
         CLC   D0(D2,R1),ELL      ERROR LOOP                     S99528
         BE    ELOPT                   YES BRANCH
         CLC   D0(D3,R1),NEL      NO ERROR LOOP                  S99528
         BE    NELOPTT            YES - BRANCH                   S22024
         CLC   D0(D2,R1),CP       CONTROL PRINT                  S99528
         BE    CPOPTT             YES - BRANCH                   S22024
         CLC   D0(D3,R1),NCP      NO CONTROL PRINT               S99528
         BE    NCPOPT                  YES BRANCH
         CLC   D0(D2,R1),APP      ALTERNATE PRINTER              S99528
         BE    APOPT                   YES BRANCH
         CLC   D0(D3,R1),NAP      NO ALTERNATE PRINTER           S99528
         BE    NAPOPTT            YES - BRANCH                   S22024
         CLC   D0(D2,R1),EP       ERROR PRINT                    S99528
         BE    EPOPT                   YES BRANCH
         CLC   D0(D3,R1),NEP      NO ERROR PRINT                 S99528
         BE    NEPOPT                  YES BRANCH
         CLC   D0(D2,R1),BK       BREAK OPTION                   S22024
         BE    BKOPT              YES BRANCH                     S22024
         CLC   D0(D3,R1),NBK      NO BREAK OPTION                S22024
         BE    NBKOPT             YES BRANCH                     S22024
         CLC   D0(D2,R1),TR       TRACE FUNCTION                 S99528
         BE    TROPT              YES
         CLC   D0(D3,R1),NTR      NO TRACE                       S99528
         BE    NTROPT             YES
         CLC   D0(D2,R1),CM       CONCURRENT MODE                S99528
         BE    CMOPTT             YES - BRANCH                   S22024
         CLC   D0(D3,R1),NCM      NON CONCURRENT MODE            S99528
         BE    NCMOPT                  YES BRANCH
         CLC   D0(D3,R1),NMI      NO MANUAL INTERVENTION         S99528
         BE    NMIOPT             YES - BRANCH                  SA41606
         CLC   D0(D2,R1),MI       MANUAL INTERVENTION            S99528
         BE    MIOPT              YES - BRANCH                  SA41606
         CLC   D0(D4,R1),EXT      EXT OPTION                     S99528
         BE    EXTOPT             YES
         CLC   D0(D2,R1),NP       NP OPTION                      S22024
         BE    NPOPT              YES                            S22024
         CLC   D0(D3,R1),TTP      TEST PRIMARY PATH              S22024
         BE    TTPOPT             YES                            S22024
         CLC   D0(D3,R1),TAP      TEST ALTERNATE PATH            S22024
         BE    TAPOPT             YES                            S22024
         IEDQMSG MSGID=062,FUNCT=CEC INVALID OPTION ENTRY        S99528
         SPACE
OPTERR1  EQU   *
         BAL   R9,CONTINUE        CONTINUE?                      S99528
         CLI   CEIN,YES           YES                            S22024
         BE    OPTENTER                YES BRANCH
         CLI   CEIN,NO            NO                             S22024
         BE    GETOUT                  YES BRANCH
         LA    R9,OPTERR1
         B     INVAL              INVAL ENTRY                    S99528
         SPACE
TTPOPT   DS    0H                 BOUNDARY ALLIGNMENT            S22024
         NI    $TOTFLG1,XFF-$TERMSEC TEST PRIMARY PATH           S22024
         LA    R5,D2              SET EXECUTE COUNT              S22024
         B     TLOPT5             MOVE OPTION TO TRM BUFFER      S22024
         SPACE
TAPOPT   DS    0H                 BOUNDARY ALLIGNMENT            S22024
         OI    $TOTFLG1,$TERMSEC  TEST ALTERNATE PATH            S22024
         LA    R5,D2              SET EXECUTE COUNT              S22024
         B     TLOPT5             MOVE OPTION TO TRM BUFFER      S22024
         SPACE 2
NPOPT    EQU   *                                                 S22024
         OI    TOTFLG03,TOTIMNCP  SHOW NCP OPTION                S22024
         OI    $ERROPT,$NERRPRT   SHOW NEP OPTION                S22024
         LA    R5,D1              SET EXECUTE COUNT              S22024
         B     TLOPT5             MOVE OPTION TO TRM BUFFER      S22024
         SPACE
BKOPT    DS    0H                 BOUNDARY ALLIGNMENT            S22024
         NI    TOTFLG09,XFF-TOTNOBRK CLEAR NO BREAK FLAG         S22024
         LA    R5,R1              SET EXECUTE COUNT              S22024
         B     TLOPT5             MOVE OPTION TO TRM BUFFER      S22024
         SPACE
NBKOPT   DS    0H                 BOUNDARY ALLIGNMENT
         OI    TOTFLG09,TOTNOBRK  SET NO BREAK FLAG
         OI    TOTFLG06,TOTNCMFG  SHOW NON CONCURRENT MODE       S22024
         LA    R5,D2              SET EXECUTE COUNT
         B     TLOPT5             MOVE OPTION TO TRM BUFFER      S22024
         SPACE 2
EXTOPT   EQU   *
         LA    R12,CEIN+D4        POINT TO DATA FIELD            S22024
         SR    R5,R5              CLEAR DATA COUNTER
EXTLOOP  EQU   *
         CLI   D0(R12),SLASH      END OF FIELD                   S22024
         BE    EXTEND             YES
         LA    R12,D1(R12)        POINT TO NEXT CHAR             S22024
         LA    R5,D1(R5)          DATA COUNTER                   S22024
         B     EXTLOOP            CHECK NEXT BYTE
*
EXTEND   EQU   *   END OF EXT DATA FIELD HAS BEEN FOUND
         LTR   R5,R5              WAS ANY DATA ENTERED?
         BZ    OPTERR1            NO - ERROR
         STC   R5,TOTEXT          MAKE COUNT FIRST CHAR OF BUFFER
         BCTR  R5,R0              DECREMENT COUNT FOR MOVE       S22024
         EX    R5,EXTMOVE         MOVE DATA TO SCT
         LA    R5,D4(R5)          INPUT COUNT MINUS 1            S22024
         B     TLOPT5             MOVE EXT DATA TO TRM BFR       S99528
         SPACE
TLOPT    EQU   *
         NI    TRMFLG,XFF-EL      CLEAR ERROR LOOP FLAG          S22024
         LA    R5,0                    SET UP COUNTER
         OI    $ERROPT,$LOOPTST   TEST LOOP FLAG                 S22024
TLOPT3A  EQU   *
         LA    R10,D4             COMPARAND                      S22024
         LA    R1,D2(R1)          POINT PAST 'TL'                S22024
         LR    R3,R1                   SAVE POINTER
TLOPT3   EQU   *
         CLI   D0(R1),BLANK       END OF ENTRY                   S22024
         BE    TLOPT2                  YES BRANCH
         LA    R5,D1(R5)          BUMP COUNTER                   S22024
         CR    R5,R10                   MORE THAN 4 NUMBERS?
         BH    TLELERR                 YES BRANCH
         LA    R1,D1(R1)          BUMP POINTER                   S22024
         B     TLOPT3             FIND END OF ENTRY              S99528
TLOPT2   LTR   R5,R5                   ANY NUMBERS GIVEN?
         BZ    TLOPT4                  NO BRANCH
         XC    WORK1,WORK1             CLEAR WORK AREAS
         XC    WORK2,WORK2
         LA    R7,D8              SET MAX                        S22024
         LA    R8,WORK2                SET REG FOR MOVE
         SR    R7,R5
         AR    R8,R7              BUMP SOURCE BUFFER POINTER     A41586
         BCTR  R5,R0              DECREMENT COUNT FOR EX         S99528
         EX    R5,MOVEIT               MOVE NUMBER TO WORK AREA
         EX    R5,ANDIT                CLEAR UNWANTED BITS
         EX    R5,COMPIT               ARE ALL ENTRIES NUMERIC?
         BNE   TLELERR                 NO BRANCH
         EX    R5,MOVETL          MOVE LOOP COUNT TO WORK AREA   A41586
         PACK  WORK1+D5(D3),WORK2+D4(D4) PACK IT                 S22024
         OI    WORK1+D7,X0F       INSURE POSITIVE                S22024
         SR    R10,R10
         CVB   R10,WORK1                CONVERT TO BINARY
         TM    TRMFLG,EL               ERROR LOOP
         BO    TLOPT9                  YES BRANCH
         STH   R10,TOTTLCNT       TEST LOOP COUNT                S22024
         LA    R5,D2(R5)          SET EXECUTE COUNT              S22024
TLOPT5   EQU   *
         LA    R15,TRMWRKE-D2     BUFFER END ADDRESS             S22024
         SR    R15,R5                  SUBTRACT LENGTH OF NEW ENTRY
         OI    TRMFLG,INOPTFLD         SET OPTION FIELD FLAG
         CR    R15,R4                  WILL IT FIT IN BUFFER?
         BNH   TOOLONG                   NO
         EX    R5,OPTMOVE              MOVE TO TRM WRK
         LA    R4,D1(R5,R4)       BUMP POINTER                   S22024
         MVI   D0(R4),COMMA       INSERT COMMA                   S22024
         LA    R4,D1(R4)          POINT PASS COMMA               S22024
         MVI   ELOPT1,D0          TURN OFF EL SWITCH             S22024
         B     OPTENTER           ENTER NEXT OPTION              S99528
TLOPT9   EQU   *                                                 S22024
         STH   R10,$ERRLPCT       LOOP ON ERROR COUNT            S22024
         LA    R5,D2(R5)          SET EXECUTE COUNT              S22024
         B     TLOPT5             GO EXAMINE NEXT ENTRY
TLOPT4   EQU   *
         SR    R10,R10                   CLEAR COUNT REG FOR DEFAULT
         TM    TRMFLG,EL               ERROR LOOP?
         BO    TLOPT4A                 YES BRANCH
         STH   R10,TOTTLCNT       TEST LOOP COUNT                S22024
TLOPT4B  EQU   *
         LA    R5,D1              SET EXECUTE COUNT              S22024
         B     TLOPT5             MOVE TL COUNT                  S99528
TLOPT4A  EQU   *
         STH   R10,$ERRLPCT       LOOP ON ERROR COUNT            S22024
         B     TLOPT4B            SET TL COUNT                   S99528
TLELERR  EQU   *
         IEDQMSG MSGID=061,FUNCT=CEC ERROR IN TL OPT             S99528
         SPACE
         CLI   ELOPT1,XFF         EL SWITCH ON                   S22024
         BNE   TLELERR1            NO
         NI    TRMFLG,XFF-EL      TURN OFF INDICATOR             S22024
         MVI   ELOPT1,D0          TURN OFF ERROR LOOP SWITCH     S22024
         NI    $ERROPT,XFF-$LOOPERR TURN OFF LOOP ON ERROR FLAG  S22024
         B     OPTENTER           ENTER NEXT OPTION              S99528
TLELERR1 EQU   *                  RESET TEST                     A41586
         NI    $ERROPT,XFF-$LOOPTST TURN OFF TST ENTRY OPT FLAG  S22024
         B     OPTENTER           REQUEST NEXT OPTION            A41586
OPTEND   EQU   *
         TM    TRMFLG,FULL             OPTION ENTERED?
         BO    OPTEND2                 YES BRANCH
OPTEND1  EQU   *
         MVI   D0(R4),SLASH       END FIELD                      S22024
         LA    R5,D1(R4)          SET EXECUTE COUNT              S22024
         LA    R7,TRMWRKE              POINT R7 TO END OF TRMWRK
         SR    R7,R5                   GET NUMBER OF POSITIONS
*                                      FOR BLANKS
         MVI   D0(R5),BLANK       BLANK CHAR                     S22024
         BCTR  R7,R0              DECREMENT COUNT OOR EX         S99528
         EX    R7,BLNKMOVE             MOVE BLANKS INTO REST OF TRM
         IEDQMSG OTBUF=TRMWRKA,OTCNT=80,FUNCT=CEC PRT TRM        S22024
         L     R3,TRMSAVE              GET ADDR OF OLTCB BUFFER
         MVC   D0(TRMLEN,R3),TRMWRK MOVE TRM TO OLTCB            S22024
         B     APROUT                  YES BRANCH
BLNKMOVE MVC   D1(D0,R5),D0(R5)   BLANK OUT REST OF TRM          S22024
OPTEND2  EQU   *
         BCTR  R4,R0              BACK UP ONE CHAR               S99528
         CLI   D0(R4),COMMA       LAST ENTRY A COMMA
         BE    OPTEND1                 YES BRANCH
         LA    R4,D1(R4)          RESET POINTER                  S22024
         B     OPTEND1            CLOSE FIELD
APROUT   EQU   *
         L     R13,D4(R13)        CALLER'S SAVE AREA             S22024
         L     R14,D12(R13)       RESTORE REG 14                 S22024
         XCTL  (2,12),EP=IEDQWJ2  EXIT TO IEDQWJ2                S22024
MOVETL   MVC   D0(D0,R8),D0(R3)   LOOP COUNT TO BUFFER           S22024
OPTMOVE  MVC   D0(D0,R4),CEIN     OPTION TO BUFFER               S22024
MOVEIT   MVC   REGSAVE(D0),CEIN+D2 LOOP COUNT TO WORK AREA       S22024
ANDIT    NC    REGSAVE(D0),NUMWRK CLEAR UNWANTED BITS            S22024
COMPIT   CLC   REGSAVE(D0),NUMWRK NUMERIC ENTRY                  S22024
ELOPT    EQU   *
         LA    R5,D0              EXECUTE COUNT MINUS 1          S22024
         OI    $ERROPT,$LOOPERR   LOOP ON ERROR OPTION           S22024
         OI    TRMFLG,EL
         MVI   ELOPT1,XFF         TURN ON EL SWITCH              S22024
         B     TLOPT3A            SET EL COUNT                   S99528
ELOPT1   DS    CL1                 ERROR LOOP SWITCH
         CNOP  2,4
NCPOPT   EQU   *
         OI    $ERROPT,$NOCNTRL   SET NO CTL PRINT FLAG          S22024
         LA    R5,D2              EXECUTE COUNT                  S22024
         B     TLOPT5             OPTION TO TRM                  S22024
NTLOPT   EQU   *
         LA    R5,D2              EXECUTE COUNT                  S22024
         B     TLOPT5             MOVE EL TO TRM BUFFER          S99528
CPOPT    EQU   *
         LA    R5,D1              EXECUTE COUNT                  S22024
         NI    $ERROPT,XFF-$NOCNTRL TURN OFF NO CTL PRINT FLAG   S22024
         B     TLOPT5             OPTION TO TRM                  S22024
NCMOPT   EQU   *
         OI    TOTFLG06,TOTNCMFG  NON CONCURRENT MODE            S22024
         B     NTLOPT                  GO CHECK NEXT OPTION
NEPOPT   EQU   *                                                 S22024
         OI    $ERROPT,$NERRPRT   SET NO ERROR PRINT FLAG        S22024
         LA    R5,D2              EXECUTE COUNT                  S22024
         B     TLOPT5             OPTION TO TRM                  S22024
         SPACE
EPOPT    EQU   *
         NI    $ERROPT,XFF-$NERRPRT TURN OFF ERR PRINT FLAG      S22024
         LA    R14,CEIN+D2        POINT PASS EP                  S22024
         CLI   D0(R14),BLANK      END OF OPTION                  S22024
         BE    EPOPT4             YES                            S22024
         SPACE
         CLI   D0(R14),LEV0       LEVEL LESS THAN 0              S22024
         BL    EPERR              YES - ERROR                    S22024
         CLI   D0(R14),LEV3       LEVEL GREATER THAN 3           S22024
         BH    EPERR              YES - ERROR                    S22024
         SPACE
         MVC   WORK1(D1),D0(R1)   LEVEL VALUE TO WORK AREA       S22024
EPOPT2   EQU   *                                                 S22024
         NI    WORK1,X0F          CLEAR UNWANTED BITS            S22024
         MVC   TOTFLG08(D1),WORK1 SET EP LEVEL FOR DPRINT        S22024
         LA    R5,D2              SET EXECUTE COUNT              S22024
         B     TLOPT5             MOVE ENTRY TO TRM              S22024
         SPACE
EPOPT4   EQU   *                                                 S22024
         MVI   WORK1,LEV3         SET DEFAULT LEVEL              S22024
         B     EPOPT2             SET VALUE IN OLTCB             S22024
         SPACE
EPERR    EQU   *                                                 S22024
         IEDQMSG MSGID=047,FUNCT=CEC INVALID EP LEVEL            S99528
         B     OPTENTER           ENTER NEXT OPTION              S99528
         SPACE
NELOPTT  EQU   *                                                 S22024
         NI    $ERROPT,XFF-$LOOPERR TURN OFF EL FLAG             S22024
         LA    R5,D2              SET EXECUTE COUNT              S22024
         B     TLOPT5             MOVE OPTION TO TRM BUFFER      S22024
         SPACE
CPOPTT   EQU   *                                                 S22024
         NI    TOTFLG03,XFF-TOTIMNCP TURN OFF NCP INTERMED FLAG  S22024
         LA    R5,D1              SET EXECUTE COUNT              S22024
         B     TLOPT5             MOVE OPTION TO TRM BUFFER      S22024
         SPACE
NAPOPTT  EQU   *                                                 S22024
         NI    $ERROPT,XFF-$ALTPRNT TURN OFF AP FLAG             S22024
         LA    R5,D2              SET EXECUTE COUNT              S22024
         B     TLOPT5             MOVE OPTION TO TRM BUFFER      S22024
         SPACE
CMOPTT   EQU   *                                                 S22024
         TM    TOTTDTBL,TOTTDLIN  LINE TEST                      S22024
         BZ    CMOPT2             NO                             S22024
         TM    TOTTDTBL,TOTTDORN  TD ON 3705                     S22024
         BO    CMOPT2             YES                            S22024
         NI    TOTFLG06,XFF-TOTNCMFG TURN OFF NCM FLAG           S22024
CMOPT2   EQU   *                                                 S22024
         LA    R5,D1              SET EXECUTE COUNT              S22024
         B     TLOPT5             MOVE OPTION TO TRM BUFFER      S22024
APOPT    EQU   *
         OI    $ERROPT,$ALTPRNT   SET AP FLAG                    S22024
         LA    R5,1
         B     TLOPT5             MOVE ENTRY TO TRM BFR          S99528
MIOPT    EQU   *                                                SA41606
         OI    $OLTFLGS,$MANINTV  SET MI FLAG                   SA41606
         LA    R5,D1              SET EXECUTE BYTE COUNT        SA41606
         B     TLOPT5             MOVE ENTRY TO TRM BUFFER      SA41606
         SPACE
NMIOPT   EQU   *                                                SA41606
         NI    $OLTFLGS,XFF-$MANINTV RESET MI FLAG              SA41606
         LA    R5,D2              SET EXECUTE BYTE COUNT        SA41606
         B     TLOPT5             MOVE ENTRY TO TRM BUFFER      SA41606
         SPACE 2
TROPT    EQU   *
          OI    $OLTFLGS,$TRACE    SET TRACE FLAG
         LA    R5,D1              EXECUTE COUNT                  S22024
         B     TLOPT5             GO DO MOVE
         SPACE 2
NTROPT   EQU   *
         NI    $OLTFLGS,X'FF'-$TRACE TURN OFF TRACE FLAG
         LA    R5,D2              EXECUTE COUNT                  S22024
         B     TLOPT5             GO DO MOVE
         SPACE 2
DEFAULT2 EQU   *
         LA    R5,D1              SET EXECUTE BYTE COUNT
         B     TLOPT5             GO DO MOVE
         SPACE
DEFAULT3 EQU   *
         LA    R5,D2              SET EXECUTE BYTE COUNT
         B     TLOPT5             GO DO MOVE
         EJECT
TOOLONG  EQU   *
         IEDQMSG MSGID=060,FUNCT=CEC TRM BFR TO SMALL FOR LAST ENS99528
         SPACE
         TM    TRMFLG,INOPTFLD         WAS ENTRY FOR OPTION FIELD?
         BZ    GETOUT             NO                             S22024
TOOLONG1 EQU   *
         IEDQMSG MSGID=059,FUNCT=CEC RENT,CAN OR GO AS IS        S99528
         SPACE
         SPACE
         IEDQMSG MSGID=058,FUNCT=REP GIVE RESPONSE FROM 3 CHOICE S99528
         SPACE
         CLI   CEIN,GO            USE AS IS                      S22024
         BE    OPTEND                    YES
         CLI   CEIN,CAN           CANCEL REQUEST                 S22024
         BE    GETOUT                    YES
         CLI   CEIN,REENT         REENTER                        S22024
         BNE   TOOLONG1                  NO: INVALID ENTRY
         TM    TRMFLG,INOPTFLD    ENTRY FOR OPTION FIELD         S22024
         BZ    TOOLONG3           NO                             S22024
         B     OPTENTER           YES - CLOSE OPTION FIELD       S22024
TOOLONG3 DS    0H                 BOUNDAY ALLIGNMENT             S22024
TOOLONG2 EQU   *
         L     R13,D4(R13)        GET CALLER'S SAVE              S22024
         L     R14,D12(R13)       RESTORE REG 14                 S22024
         XCTL  (2,12),EP=IEDQWJ   RETURN TO IEDQWJ
         EJECT
GETOUT   EQU   *
         OI    TOTFLG06,TOTOTERM  SECTION TERMINATE FLAG         S22024
         L     R13,D4(R13)        CALLER'S SAVE                  S22024
         L     R14,D12(R13)       RESTORE REG 14                 S22024
         XCTL  (2,12),EP=IEDQWE   CANCEL TRM                     S22024
         EJECT
RTNCHK   EQU   *                  SET ROUTINE MASKS IN SCT
*
         XC    RTNFLG(D1),RTNFLG  CLEAR FLAG BYTE               SA41595
         XC    KCNT(D4),KCNT      CLEAR COUNT SAVE AREA
         OI    $EXECFLG,$RTNSLCT  SHOW THAT ROUTINES SELECTED
         STM   R7,R12,SAVAREA     SAVE REGISTERS
RTN2     EQU   *
         TM    RTNFLG,RTNSFLG     END OF TEST FIELD ?
         BO    RTNEND             YES
         XC    RTNFLG,RTNFLG      CLEAR FLAG BYTE
         LR    R7,R1              POINT TO FIRST DIGIT
RTN3     EQU   *
         LA    R8,D0              INIT. DIGIT COUNTER
RTN4     EQU   *
         L     R12,KCNT           GET PRESENT COUNT
         LA    R12,D1(R12)        INCREMENT BY 1
         ST    R12,KCNT           SAVE COUNT
         CLI   D0(R7),BLANK       END OF FIELD ?
         BE    RTNSLH             YES
         CLI   D0(R7),DASH        INCLUSIVE TEST ?
         BE    RTNDSH             YES
         CLI   D0(R7),COMMA       COMMA DELIMITER ?
         BE    RTNCMA             YES
         CLI   D0(R7),F0          DIGIT LESS THAN 0 ?
         BL    TESTERR1           YES - ERROR
         CLI   D0(R7),F9          DIGIT GREATER THAN 9 ?
         BH    TESTERR1           YES - ERROR
         LA    R5,D1(R5)          INCREMENT CHAR COUNTER
         LA    R7,D1(R7)          POINT TO NEXT CHAR
         LA    R8,D1(R8)          INCREMENT CHAR COUNTER
         B     RTN4               CHECK NEXT CHAR
RTNSLH   EQU   *
         OI    RTNFLG,RTNSFLG     SHOW END OF TEST FIELD
         B     RTN6               EXIT
RTNDSH   EQU   *
         TM    RTNFLG,RTNDFLG     INCLUSIVE ROUTINES ?
         BO    RTN6               YES, GO SEE IF DIGITS ARE SPECIFIED
         OI    RTNFLG,RTNDFLG     SHOW INCLUSIVE ROUTINES
         B     RTN6               EXIT
RTNCMA   EQU   *
         OI    RTNFLG,RTNCFLG     SHOW SEPARATE ENTRIES
RTN6     EQU   *
         LTR   R8,R8              ANY DIGITS ?
         BZ    TESTERR1           NO - ERROR
         CL    R8,K3              NUMBER OF DIGITS > 3          SA41595
         BH    TESTERR1           YES - ERROR                   SA41595
         CL    R8,K3              NUMBER OF DIGITS = 3           S22024
         BNE   RTN8               NO - BYPASS MAXIMUM CHECK      S22024
         SR    R7,R8              POINT TO FIRST DIGIT           S22024
         CLC   D0(D3,R7),RTNMAX   RTN NUMBER > MAXIMUM           S22024
         BH    TESTERR1           YES - RTN # TOO LARGE          S22024
         AR    R7,R8              RESET POINTER                  S22024
RTN8     DS    0H                 BOUNDARY ALLIGNMENT            S22024
         BCTR  R8,R0              DECREMENT COUNT FOR EX         S22024
         EX    R8,PACK            PACK THE NUMBER                S22024
         CVB   R9,DEC             CONVERT THE NUMBER TO BINARY   S22024
         TM    RTNFLG,RTNNFLG     INCLUSIVE SECOND TEST  ?
         BO    RTN12              YES
         ST    R9,DIG1            SAVE NUMBER
         TM    RTNFLG,RTNDFLG     INCLUSIVE ROUTINES ?
         BZ    RTN15              NO
         OI    RTNFLG,RTNNFLG     YES - SET SECOND PASS FLAG
         LA    R1,D1(R7)          POINT PASS DELIMITER
         LR    R7,R1              SAVE POINTER
         B     RTN3               GO FIND UPPER LIMIT
RTN12    EQU   *
         ST    R9,DIG2            SAVE UPPER LIMIT
         CLC   DIG1(D4),K256      NUMBER GREATER THAN MAX. ?
         BH    TESTERR1           YES - ERROR
         CLC   DIG2,K256          NUMBER GREATER THAN MAX. ?
         BH    TESTERR1           YES -ERROR
         CLC   DIG1(D4),DIG2      1ST NUMBER GRT/EQ TO 2ND NUMBER ?
         BNL   TESTERR1           YES - ERROR
RTN15    EQU   *                  SET ROUTINE MASK BITS
         L     R9,DIG1            GET ROUTINE NUMBER
         CL    R9,K0              RTN NUMBER EQU ZERO
         BE    TESTERR1           YES - ERROR
         CLC   DIG1(D4),K16       SET MASK BETWEEN 1 AND 16
         BH    RTN24              NO - CHECK NEXT FIELDS
*        SET MASK FOR ROUTINES 1-16
         LA    R12,$RT0108        POINT TO RTN MASK FIELD
         LA    R11,TESET          POINT TO SET MASK TABLE
RTN17    EQU   *
         S     R9,K8              DECREMENT BY 8
         BNP   RTN19              IF RESULT NEG. , EXIT
         LA    R12,D1(R12)        POINT TO SECOND BYTE
         B     RTN19A             GO SET MASK
RTN19    EQU   *
         L     R9,DIG1            GET RTN NUMBER
RTN19A   EQU   *
         BCTR  R9,R0              DECREMENT FOR MOVE
RTN20    EQU   *
         AR    R9,R11             POINT TO MASK SETTING
         OC    D0(D1,R12),D0(R9)  SET MASK
RTN21    EQU   *
         TM    RTNFLG,RTNIFLG     INCLUSIVE LOOP ?
         BO    RTN22            YES
         LA    R7,D1(R7)          POINT PASS DELIMITER
         LR    R1,R7              RESET TRM PTR
RTN22    EQU   *
         TM    RTNFLG,RTNDFLG     INCLUSIVE ROUTINES
         BZ    RTN2               NO CONTINUE
         OI    RTNFLG,RTNIFLG     SET INCLUSIVE LOOP FLAG
         CLC   DIG1(D4),DIG2      END OF INCLUSIVE SET ?
         BE    RTN2               YES CONTINUE
         L     R9,DIG1            GET FIRST NUMBER
         LA    R9,D1(R9)          INCREMENT BY 1
         ST    R9,DIG1            REPLACE NUMBER
         B     RTN15              EVALUATE THIS ROUTINE NUMBER
RTN24    EQU   *                  SET MASK FOR ROUTINES 17-256
         L     R9,DIG1            GET FIRST NUMBER
         LA    R12,$R025032       POINT TO SECOND MASK FIELD
         LA    R8,D24             INIT COMPARAND
         LA    R11,TESET          POINT TO SET MASK TABLE
RTN28    EQU   *
         CR    R9,R8              IS FLAG IN PRIOR BYTE ?
         BNH   RTN32              YES - EXIT
         LA    R8,D8(R8)          INCREMENT COMPARAND
         LA    R12,D1(R12)        POINT TO NEXT MASK BYTE
         B     RTN28              CHECK NEXT BYTE
RTN32    EQU   *                  MASK FIELD FOUNT
         BCTR  R12,R0             ADJUST TO CORRECT BYTE
         LA    R9,D8(R9)          ADJUST RTN NUMBER
         SR    R9,R8              ADJUST INDEX INTO MASK TABLE
         BCTR  R9,R0              DECREMENT FOR INDEX INTO MASK TABLE
         B     RTN20              GO SET MASK
RTNEND   EQU   *
         L     R12,KCNT           GET RUNNING COUNT
         BCTR  R12,R0             DECREMENT IT
         ST    R12,KCNT           SAVE ACTUAL CHAR COUNT
         LM    R7,R12,SAVAREA     RESTORE REG.
         B     TEST5              GO CHECK OPTION FIELD
         EJECT
CONTINUE EQU   *
         IEDQMSG MSGID=057,FUNCT=REP,INBUF=CEIN,INCNT=8 CONTINUE S99528
         BR    R9                 RETURN
         SPACE
         SPACE
         SPACE
         SPACE
INVAL    EQU   *
         IEDQMSG MSGID=079,FUNCT=CEC INVALID RESPONSE            S99528
         SPACE
         BR    R9                 RETURN                         S99528
         EJECT
         EJECT
CEOUT    DS    CL72               OUTPUT BUFFER                  S99528
PATCH    DC    CL200' '           IEDQWJ1 PATCH AREA             S99528
ECB      DS    F                  ECB                            S99528
REGSAVE  DS    9F                 REGISTER SAVE AREA             S99528
SAVAREA  DS    6F                 REGISTER SAVE AREA
         DS    0D                 BOUNDARY ALLIGNMENT
WORK1    DC    XL8'0'             WORK AREA                      S99528
WORK2    DC    XL8'0'             WORK AREA                      S99528
TRMSAVE  DS    F                  TRM BUFFER ADDR SAVE             9528
K4       DC    F'4'               VALUE OF 4                    SA41606
KCNT     DC    F'0'               SAVE AREA
TRMWRKA  DC    C'IED344I '        TRM                            S22024
TRMWRK   DS    CL72               TRM WORK BUFFER                S99528
TRMWRKE  DS    0C                 END OF BUFFER                  S99528
TRMFLG   DC    X'00'              TRM CONTROL FLAG               S99528
FULL     EQU   X'80'              END OF TEST FIELD FLAG          99528
EMPTY    EQU   X'7F'              AND FULL BIT OFF               S99528
AP       EQU   X'10'              AP OPTION SELECTED             S99528
INOPTFLD EQU   X'08'              OPTION FIELD FLAG              S99528
EL       EQU   X'40'              ERROR LOOP FLAG                S99528
KF0      DC    X'F0F0F0F0'        NUMERIC AND FIELD              S99528
NONE     DC    C'NONE'            NO MORE OPTION ENTRIES         S99528
EXT      DC    C'EXT='            EXTERNAL DATA                  S99528
TL       DC    C'TL'              TEST LOOP                      S99528
ELL      DC    C'EL'              ERROR LOOP                     S99528
CP       DC    C'CP'              CONTROL PRINT                  S99528
APP      DC    C'AP'              ALT PRINTER                    S99528
EP       DC    C'EP'              ERROR PRINT                    S99528
TR       DC    C'TR'              TRACE OPTION                   S99528
CM       DC    C'CM'              CONCURRENT MODE                S99528
MI       DC    C'MI'              MANUAL INTERVENTION            S99528
NTL      DC    C'NTL'             NO TEST LOOP                   S99528
NEL      DC    C'NEL'             NO ERROR LOOP
NCP      DC    C'NCP'             NO CONTOOL PRINT               S99528
NAP      DC    C'NAP'             NO ALTERNATE PRINTER           S99528
NEP      DC    C'NEP'             NO ERROR PRINT                 S99528
NTR      DC    C'NTR'             NO TRACE                       S99528
NCM      DC    C'NCM'             NON CONCURRENT MODE            S99528
NMI      DC    C'NMI'             NO MANUAL INTERVENTION         S99528
NP       DC    C'NP'              NO PRINT                       S22024
TTP      DC    C'TPP'             TEST PRIMARY PATH              S22024
TAP      DC    C'TAP'             TEST ALTERNATE PATH            S22024
BK       DC    C'BK'              BREAK OPTION                   S22024
NBK      DC    C'NBK'             NO BREAK OPTION                S22024
RTNMAX   DC    C'255'             MAXIMUM VALID RTN NUMBER       S22024
DEC      DC    D'0'               WORK AREA
DIG1     DC    F'0'               WORK AREA
DIG2     DC    F'0'               WORK AREA
K0       DC    F'0'               VALUE OF ZERO
K1       DC    F'1'               VALUE OF 1
K3       DC    F'3'               VALUE OF 3
K8       DC    F'8'               VALUE OF 8
K16      DC    F'16'              VALUE OF 16
K248     DC    F'248'             VALUE OF 248
K256     DC    F'256'             VALUE OF 256
TDSAVE   DS    F                  TD SUFX TABLE POINTER
RTNFLG   DC    X'00'              ROUTINE CONTROL FLAG
TESET    DC    X'8040201008040201' SET RTN MASK TABLE
NUMWRK   DC    X'F0F0F0F0'        NUMERIC MASK FIELD             S22024
EXTMOVE  MVC   TOTEXT+1(1),CEIN+4 MOVE EXT DATA
PACK     PACK  DEC(D8),D0(D0,R1)  CONVERT FROM ZONE TO DECIMAL
         EJECT
         TQCBD
         EJECT
         DCBD  DSORG=TX
         EJECT
         TSCBD
         EJECT
         TPRFD
         EJECT
CVT      DSECT
         CVT
         EJECT
UCB      DSECT
         IEFUCBOB
         EJECT
         TAVTD
         EJECT
         TDEBD
         EJECT
         OLTCB
CEIN     EQU   TOTINBUF           INPUT BUFFER                  S22024
*
*
*
         END
