         TITLE 'IEDQWJ TRM PROMPTER MODULE 1'
IEDQWJ   CSECT
*A000000-999999                                                @Y16X5U0
* CHANGE ACTIVITY AS FOLLOWS:
*A453200                                                        SA41597
*A209300-209600                                                 SA41606
*A291000,303000,339000                                          SA41608
*A135070-135990,136640-136650,136650,136674-136681,357080-361720 S22024
*A374860-375400,379200-379800,443100-443600,458195,458305        S22024
*A458372-458378,459430-459450,462147-462203,463757-463806        S22024
*A516700,521500,523500,524600-525200                             S22024
*C004200,146000-147000,151000,157000,169000,192000,200000        S22024
*C200000-203200,231000,239800,241000-249000,255000-256000,265000 S22024
*C269000,275000-276200,279000-280000,284000,287000,294000,       S22022
*C296000,298000,302000,305000,311000-311800,313200,315000-316000 S22024
*C321000-322000,324000,327000,331012,331690,331700,333600,       S22024
*C334000-335000,338000,340000-340500,347000,348000,350000 369800 S22024
*C373000-373600,378700,383000-384000,388000,394000,396000,424000 S22024
*C424032,424040,424056,429100-429108,429140,429456,429472        S22024
*C429572-429972,430072-430672,437000-437200,43800,441000-442000  S22024
*C445000-445500,451600-456400,458240-458310-458320               S22024
*C458440-458449,459430-459450,460670-460730,460740-460800        S22024
*C460810-460820,479000-479500,526720-526850                      S22024
*D257000,291000,292500,462280                                    S22024
*A135090-135110,381070-381840,523700                             S05331
*D331224-333800                                                  S05331
*A245000,249600,254000                                         @YA12402
*A360120,360170,374920,375130,445500,458373,458442,460740      @Y17XAUU
*A462161,516720,516780                                         @Y17XAUU
*C360778,360820,361370                                         @Y17XAUU
* A021000,111300,114800,287700,543900,547400,549500,812700     @G36XRUV
* C180600,544600                                               @G36XRUV
* D546700,548100,550200-553000                                 @G36XRUV
*
         USING IEDQWJ,R15                                      @YA12402
*
IEDQWJ   IEDHJN STARJ,HJN
*
         SPACE 2
**************************************************************** S99528
*                                                              *
* MODULE NAME = IEDQWJ (TCAM,TOTE)                             @Y17XAUU
*                                                              *
* DESCRIPTIVE NAME = TRM PROMPTER MODULE 1                     *
*                                                              *
* COPYRIGHT = NONE                                             *
*                                                              *
* STATUS --                                                    * S99528
*                                                              * S99528
*    VERSION 10.0                                              @Y17XAUU
*                                                              * S99528
* FUNCTIONS --                                                 * S99528
*    THE PURPOSE OF THIS MODULE IS TO ANALYZE THE OLTCB FLAG   * S99528
*    BYTES TO DETERMINE WHY THE PROMPTER WAS CALLED.           * S99528
*                                                              * S99528
*    IF THE IEDQWJ MODULE IS CALLED AS THE RESULT OF A PROMPT  * S99528
*    REQUEST, THE OPERATOR AT THE CONTROL TERMINAL WILL BE     * S99528
*    PROMPTED FOR THE 'TEST DEVICE' FIELD OF THE TRM. WHEN     * S99528
*    A VALID 'TEST DEVICE' FIELD IS ENTERED, CONTROL IS        * S99528
*    TRANSFERRED TO IEDQWJ1.                                   * S99528
*                                                              * S99528
*    IF ENTRY TO IEDQWJ IS THE RESULT OF ERRONEOUS INFORMATION * S99528
*    IN THE TRM, THE OPERATOR AT THE CONTROL TERMINAL IS GIVEN * S99528
*    THE OPTION OF RE-ENTERING THE TRM OR BEING PROMPTED. IF   * S99528
*    PROMPTING IS SELECTED, THIS MODULE WILL CHECK TO SEE IF   * S99528
*    THE ERROR WAS IN THE 'TEST DEVICE' FIELD OR THE AP TERM   * S99528
*    OPTION OF THE TRM. IF THE ERROR WAS IN NEITHER OF THESE   * S99528
*    FIELDS, CONTROL IS TRANSFERRED TO IEDQWJ1. OTHERWISE,     * S99528
*    THIS MODULE PROMPTS FOR THE 'TEST DEVICE' FIELD AND THEN  * S99528
*    TRANSFERS CONTROL TO IEDQWJ1 TO PROMPT FOR 'TESTS' AND    * S99528
*    'OPTIONS' FIELDS                                          * S99528
*                                                              * S99528
*    WHEN ENTRY IS FROM A PROMPT REQUEST, THE CE IS PROMPTED   * S99528
*    FOR EACH OF THE TRM FIELDS EXCEPT THE CONTROL TERMINAL.   * S99528
*    WHEN ENTRY IS THE RESULT OF AN ERROR IN TRYING TO         * S99528
*    DETERMINE IF AP TERM IS SPECIFIED, THEN THE CE IS PROMPTED* S99528
*    FOR ONLY THE OPTION FIELD.                                * S99528
*                                                              * S99528
*    ALL VALIDITY CHECKING PERFORMED ON A NORMAL TRM IS        * S99528
*    PERFORMED BY THIS MODULE.                                 * S99528
*                                                              * S99528
*    THIS MODULE ONLY PROMPTS FOR THE TEST DEVICE FIELD AND    * S99528
*    THEN TRANSFERS CONTROL TO IEDQWJ1 TO PROMPT FOR TESTS AND * S99528
*    OPTIONS FIELD.                                            * S99528
*                                                              @Y17XAUU
* PURPOSE -- SEE FUNCTION                                      @Y17XAUU
*                                                              @Y17XAUU
* LINKAGE -- XCTL'ED TO FROM IEDQWB1                           @Y17XAUU
*                                                              @Y17XAUU
* MODULE TYPE -- PROCEDURE                                     @Y17XAUU
*                                                              @Y17XAUU
* MODULE SIZE -- MAXIMUM 4K                                    @Y17XAUU
*                                                              @Y17XAUU
* PROCESSOR -- ASSEMBLE XF                                     @Y17XAUU
*                                                              @Y17XAUU
* MACROS -- AQCTL,IEDHJN,IEDQMSG,WAIT,WTO,XCTL                 @Y17XAUU
*                                                              * S99528
* ENTRY POINTS --                                              * S99528
*    IEDQWJ                                                    @Y17XAUU
*                                                              * S99528
* INPUT --                                                     * S99528
*    REGISTERS 02,13,14,15 CONTAIN THE FOLLOWING VALUES        * S99528
*                                                              * S99528
*    02 - OLTCB BASE POINTER                                   * S99528
*    13 - SAVE AREA ADDRESS                                    * S99528
*    14 - RETURN ADDRESS                                       * S99528
*    15 - ENTRY POINT ADDRESS                                  * S99528
*                                                              * S99528
* OUTPUT --                                                    * S99528
*    SAME AS INPUT                                             @Y17XAUU
*                                                              * S99528
* EXITS,NORMAL --                                              * S99528
*    XCTL TO IEDQWJ1 TO CONTINUE BUILDING TRM.                 * S99528
*                                                              * S99528
* EXITS,ERROR --                                               * S99528
*    XCTL TO IEDQWE TO TERMINATE TRM                           * S99528
*                                                              * S99528
* EXTERNAL REFERENCES --                                       * S99528
*    IEDQWK - MESSAGE MODULE                                   * S99528
*    AVTUI - TCAM BINARY SEARCH MODULE                         * S99528
*                                                              * S99528
* ATTRIBUTES --                                                * S99528
*    ENABLED, PROBLEM ROGRAM MODE, TRANSIENT                   * S99528
*                                                              * S99528
* NOTES -- SEE BELOW                                           @Y17XAUU
*                                                              @Y17XAUU
*  DEPENDENCIES --EBCDIC CODE CHARACTER SET                    @Y17XAUU
*                                                              @Y17XAUU
*  RESTRICTIONS -- NONE                                        @Y17XAUU
*                                                              @Y17XAUU
*  REGISTER CONVENTIONS -- SEE REGISTER ASSIGNMENTS            @Y17XAUU
*                                                              @Y17XAUU
*  PATCH LABEL -- PATCH                                        @Y17XAUU
*                                                              * S99528
**************************************************************** S99528
         EJECT
*
*              INITIALIZATION
*
*
R0       EQU   0                  REG 0                          S99528
R1       EQU   1                  REG 1                          S99528
R2       EQU   2                  REG 2                          S99528
R3       EQU   3                  REG 3                          S99528
R4       EQU   4                  REG 4                          S99528
R5       EQU   5                  REG 5                          S99528
R6       EQU   6                  REG 6                          S99528
R7       EQU   7                  REG 7                          S99528
R8       EQU   8                  REG 8
R9       EQU   9                  REG 9                          S99528
R10      EQU   10                 REG 10                         S99528
R11      EQU   11                 REG 11                         S99528
R12      EQU   12                 REG 12                         S99528
R13      EQU   13                 REG 13                         S99528
R14      EQU   14                 REG 14                         S99528
R15      EQU   15                 REG 15                         S99528
ADRMSK   EQU   7                  ADDRESS MASK                 @YM08136
COMMA    EQU   X'6B'              COMMA                          S22024
SWITCHB  EQU   X'96'              SWITCHED BI-SYNC               S05331
SWITCHS  EQU   X'90'              SWITCHED START/STOP            S05331
ACVT     EQU   16                 CVT POINTER                    S22024
A1060    EQU   X'52'              1060 UCB TYPE                  S22024
PASS1    EQU   X'01'              TEST DEVICE FIRST PASS CHECK   S22024
ADUCBMSK EQU   3                  MASK FOR UCB ADDRESS         @G36XRUV
YES      EQU   X'E8'              POSITIVE RESPONSE
NO       EQU   X'D5'              NEGATIVE RESPONSE
LGBA     EQU   X'80'              LGB MAP                        S22024
SLASH    EQU   X'61'              SLASH                          S22024
X0F      EQU   X'0F'              MASK FIELD                     S22024
XF0      EQU   X'F0'              MASK FIELD                   @G36XRUV
X09      EQU   X'09'              USED FOR 3270 LOCAL CHECK    @G36XRUV
OPEN     EQU   X'10'              DCB OPEN                       S22024
XE0      EQU   X'E0'              MASK SETTING                   S22024
TCU3705  EQU   X'05'              3705 CHECK                     S22024
LINE     EQU   X'01'              LINE TEST                      S22024
RNTERM   EQU   X'08'              RN TERMINAL                    S22024
RNLINE   EQU   X'04'              RN LINE                        S22024
RNCHAN   EQU   X'02'              RN SUBCHAN                     S22024
*                                                                S22024
NUMER    EQU   X'F0'              NUMERIC ENTRY                  S22024
TRMLEN   EQU   72                 TRM BUFFER LENGTH              S22024
RQINIT   EQU   X'0C'              START OF LIST                  S22024
RQEND    EQU   X'80'              END OF LIST                    S22024
D0       EQU   0                  DISPLACEMENT OF 0
D1       EQU   1                  DISPLACEMENT OF 1
D2       EQU   2                  DISPLACEMENT OF 2
D3       EQU   3                  DISPLACEMENT OF 3
D4       EQU   4                  DISPLACEMENT OF 4
D5       EQU   5                  DISPLACEMENT OF 5
D6       EQU   6                  DISPLACEMENT OF 6
D7       EQU   7                  DISPLACEMENT OF 7
D8       EQU   8                  DISPLACEMENT OF 8
D9       EQU   9                  DISPLACEMENT OF 9
D10      EQU   10                 DISPLACEMENT OF 10
D11      EQU   11                 DISPLACEMENT OF 11
D12      EQU   12                 DISPLACEMENT OF 12
D13      EQU   13                 DISPLACEMENT OF 13
D14      EQU   14                 DISPLACEMENT OF 14
D15      EQU   15                 DISPLACEMENT OF 15
D16      EQU   16                 DISPLACEMENT OF 16
D17      EQU   17                 DISPLACEMENT OF 17
D18      EQU   18                 DISPLACEMENT OF 18
D19      EQU   19                 DISPLACEMENT OF 19
D20      EQU   20                 DISPLACEMENT OF 20
D25      EQU   25                 DISP OF 25                     S22024
D72      EQU   72                 DISP OF 72                     S22024
DCBNIDLE EQU   X'02'              3705 ACTIVE                    S22024
*
         SPACE
INACT    EQU   X'48'              INACTIVATE VERB CODE           S22024
INACTRC  EQU   X'08'              TERM ALREADY INACTIVE RC       S22024
ZERO     EQU   0                  VALUE OF 0                     S99528
ONE      EQU   1                  VALUE OF 1                     S99528
EIGTH    EQU   8                  VALUE OF 8                     S99528
TWELVE   EQU   12                 VALUE OF 12                    S99528
TRMSOH   EQU   X'04'              BI-SYNC DEVICE                 S99528
TCAMDEB  EQU   X'44'              DEB BELONGS TO TCAM FLAG       S22024
TRM9S    EQU   X'08'              START-STOP DEVICE              S99528
PARMREG0 EQU   0                  PARAMETER REG 0                S99528
PARMREG1 EQU   1                  PARAMETER REG 1                S99528
X80      EQU   X'80'              MASK FIELD                     S99528
X40      EQU   X'40'              MASK FIELD                     S99528
D24      EQU   24                 DISP OF 24                     S99528
D32      EQU   32                 DISP OF 32                     S99528
D40      EQU   40                 DISP OF 40                     S99528
TWO      EQU   2                  DISP OF TWO
FOUR     EQU   4                  DISP OF FOUR
ACTRTCD  EQU   X'08'              INV. LIST INIT ACTIVE
TERMACTV EQU   X'46'              OP CONTROL ACTIVATE VERB CODE
XFF      EQU   X'FF'              MASK FIELD
TDLIM    EQU   10                 MAXIMUM TEST DEVICE ENTRIES
BLANK    EQU   X'40'              BLANK SPACE
         SPACE 2
         SAVE  (14,12)            SAVE CALLER'S REG              S99528
         LR    R6,R15                   LOAD BASE
         DROP  R15
         USING IEDQWJ,R6
         USING TOTOLTCB,R2
         LA    R3,TOTSAVE4        SAVE AREA ADDR                 S99528
         ST    R3,D8(R13)         STORE IN CALLER'S SAVE         S22024
         ST    R13,D4(R3)         SAVE CALLER'S                  S22024
         LR    R13,R3                   SET R13 TO SAVE AREA
         L     R10,ACVT(R0)       CVT ADDR                       S22024
         USING CVT,R10
         L     R3,AVTCVTPT(R10)        PICK UP TCAM DISP ADDR
         L     R4,CVTILK2              PICK UP 2 BYTE UCB POINTER
         ST    R4,UCBSAVE              SAVE IT
         DROP  R10
         L     R10,D0(R3)         AVT ADDR                       S22024
         USING IEDQAVTD,R10
         L     R3,AVTRNMPT             PICK UP ADDR OF TERMNAME TABLE
         ST    R3,TERNMTAB             SAVE IT
         USING IEDQTNTD,R3
         IC    R4,TNTENLEN             PICK UP ENTRY LENGTH
         STC   R4,TERNMLEN             SAVE IT
         DROP  R3
         MVC   PAREMLST(D1),AVTBSX SET UP FOR BINARY SEARCH      S99528
         L     R3,AVTMSGS-D1      GET VCON TABLE ADDR            S99528
         L     R3,D8(R3)          ADDR OF A1 ROUTINE             S99528
         ST    R3,AVTUISV
         LA    R3,AVTOPCOB             PICK UP OP CONTROL QCB ADDR
         DROP  R10
         ST    R3,OCQCB                SAVE IT
         SR    R4,R4
         ICM   R4,ADUCBMSK,TOTCTUCB GET CT UCB ADDR            @G36XRUV
         USING UCB,R4
         CLI   UCBTBYT1,A1060     1060 EQU CT                    S22024
         DROP  R4
*
*
*
         BNE   PMT090             NO                             S99528
         IEDQMSG MSGID=091,FUNCT=CEC,LINK=YES NO PROMPTING ON 106099528
         B     GETOUT               & ABORT
PMT090   EQU   *
         TM    TOTFLG03,TOTTDFER  ENTRY FROM TD ERROR            S99528
         BO    TDROUT                  YES BRANCH
         TM    TOTFLG03,TOTTSTER  ENTRY FROM TEST ERRROR         S99528
         BO    TROUT                   YES BRANCH
         TM    TOTFLG03,TOTOPTER  ENTRY FROM OPTION ERROR        S99528
         BO    OPTROUT                 YES BRANCH
         TM    TOTFLG03,TOTAPOER  AP OPTION ERROR                S99528
         BO    OPTROUT            YES - BRANCH
*
*
*              CALL FROM TRM PROMPT REQUEST
*
*
START    EQU   *
         IEDQMSG MSGID=090,FUNCT=CEC TRM PROMPTER RUNNING        S99528
TDSTART  EQU   *
         NI    TOTFLG06,XFF-TOTNCMFG CLEAR NCM FLAG              S99528
         LA    R3,TOTTRMBF        POINT TO TRM BUFFER            S99528
         ST    R3,TRMSAVE
         MVC   TRMWRK,D0(R3)      TRM TO WORK AREA               S22024
         LA    R4,TRMWRK               SET POINTER
*
*              FIND TEST DEVICE FIELD
*
FLDFIND  CLI   0(R4),C'/'              END OF CT FIELD?
         BE    TDFLD                   YES BRANCH
         LA    R4,D1(R4)          BUMP POINTER                   S22024
         B     FLDFIND                 GO LOOK AGAIN
TDFLD    EQU   *                                                 S22024
         LA    R4,D1(R4)          POINT PASS SLASH               S22024
         NI    TDFLG,XFF-SYM      CLEAR SYMBOLIC ENTRY FLAG      S22024
         NI    TOTFLG10,XFF-TOTTERMS CLEAR TERMINAL TD FLAG      S22024
         B     TESTDEV            GO EXAMINE TEST DEVICE FIELD   S99528
TDROUT   EQU   *
         IEDQMSG MSGID=089,FUNCT=CEC ERROR IN TEST DEVICE FIELD  S99528
         BAL   R9,PRMCONT         GO SEE IF CE WANTS PROMPTING   S99528
*                                   CONTINUED                    S99528
         XC    TOTTDTBL(D40),TOTTDTBL CLEAR DEVICE TABLE         S99528
         NI    TOTFLG03,XFF-TOTOPTER CLEAR OPTION ERR FLAG      SA41606
         NI    TOTFLG03,XFF-TOTAPOER CLEAR AP OPTION ERR FLAG   SA41606
         B     TDSTART            CONTINUE PROCESSING            S99528
TROUT    EQU   *
         IEDQMSG MSGID=088,FUNCT=CEC ERROR IN TEST FIELD
         BAL   R9,PRMCONT         CONTINUE PROMPTING ?           S99528
         B     TDDONE1            NO - EXIT                      S99528
OPTROUT  EQU   *
         IEDQMSG MSGID=087,FUNCT=CEC ERROR IN OPTION FIELD       S99528
         BAL   R9,PRMCONT         CONTINUE PROMPTING ?           S99528
         B     TDDONE1            EXIT TO QWJ1
TESTDEV  EQU   *
*
*        R4 POINTS TO TEST DEVICE FIELD IN TRM WORK
*
         LA    R7,ONE             INCREMENT VALUE                S22024
         A     R7,NUMBER
         ST    R7,NUMBER
         LA    R7,TDLIM           SET COMPARAND LIMIT
         C     R7,NUMBER               IS NUMBER GT 9?
         BNL   TD1                     NO BRANCH
         B     TDERR                   YES GO TO ERROR ROUTINE
TD1      EQU   *
         NI    TDFLG,XFF-RNTERM-RNLINE CLEAR INTERMEDIATE FLAGS  S22024
*        ENTER NEXT TEST DEVICE                                  S99528
         IEDQMSG MSGID=085,FUNCT=REP,INBUF=CEIN,INCNT=8          S99528
         CLI   CEIN,BLANK         ANY REPLY                      S22024
         BE    TDERR1               NO
         BAL   R9,TYPECHK         DETERMINE TYPE OF TEST         S22024
         TM    TDFLG,RNTERM+RNLINE+SYM SYMBOLIC ENTRY            S22024
         BM    TDSYM              YES                            S22024
         CLI   CEIN,NUMER               IS FIRST CHAR A ZERO?  @YA12402
         BNE   DIGIT3                   NO                     @YA12402
         CLI   CEIN+D3,BLANK            IS 4TH CHAR BLANK?     @YA12402
         BE    DIGIT3                   YES                    @YA12402
         MVC   WORK1(D3),CEIN+D1        MOVE CUU TO WORK AREA  @YA12402
         CLI   CEIN+D4,BLANK            END OF ENTRY?          @YA12402
         BNE   TDERR1                   NO - ERROR             @YA12402
         B     FINDUCB                  GO FIND UCB ADDR       @YA12402
         SPACE 2                                               @YA12402
DIGIT3   EQU   *                                               @YA12402
         MVC   WORK1(D3),CEIN     MOVE CUU TO WORK AREA          S22024
         CLI   CEIN+D3,BLANK      END OF ENTRY ?
         BNE   TDERR1             NO - ERROR
FINDUCB  EQU   *                                               @YA12402
         BAL   R9,UCBLOOK              GO CHECK UCB ENTRY
         B     CUUOK                   BRANCH AROUND ERROR RETURN
         B     TDERR1                  CUU CANNOT BE FOUND
CUUOK    EQU   *
         BAL   R9,PLUG                 GO PLUG LINE ADDR TABLE
         CLI   CEIN,NUMER               IS FIRST CHAR A ZERO?  @YA12402
         BNE   MOVE3                    NO                     @YA12402
         CLI   CEIN+D3,BLANK            IS 4TH CHAR BLANK?     @YA12402
         BE    MOVE3                    YES                    @YA12402
         MVC   D0(D4,R4),CEIN           GET CUU                @YA12402
         LA    R4,D4(R4)                BUMP TRM PTR           @YA12402
         B     ASKAGN1                  GO ASK AGAIN           @YA12402
         SPACE 2                                               @YA12402
MOVE3    EQU   *                                               @YA12402
         MVC   D0(D3,R4),WORK1    MOVE CUU TO TRM WORK AREA      S22024
         LA    R4,D3(R4)          BUMP TRM POINTER               S22024
ASKAGN1  EQU   *
         IEDQMSG MSGID=084,FUNCT=REP,INBUF=CEIN,INCNT=8 MORE TD  S99528
         SPACE 2
*
ASKAGN2  EQU   *
*
         SPACE
         CLI   CEIN,YES           YES - MORE ENTRIES             S22024
         BE    YESMORE            YES                            S99528
         CLI   CEIN,NO            NO                             S22024
         BE    NOMORE             NO                             S99528
         IEDQMSG MSGID=083,FUNCT=REP,INBUF=CEIN,INCNT=8 MORE TD  S99528
         SPACE
         B     ASKAGN2            CHECK RESPONSE
YESMORE  EQU   *                                                 S22024
         MVI   D0(R4),COMMA       INSERT COMMA                   S22024
         LA    R4,D1(R4)          BUMP TRM POINTER               S22024
         B     TESTDEV            GET NEXT DEVICE                S99528
NOMORE   EQU   *
         MVI   D0(R4),SLASH       CLOSE FIELD                    S22024
         LA    R4,D1(R4)          BUMP TRM POINTER               S22024
         B     TDDONE             EXIT TO IEDQWJ1
TDERR    EQU   *
         IEDQMSG MSGID=082,FUNCT=CEC ALREADY HAVE 10 TEST DEV    S22024
         BCTR  R4,D0              POINT TO PLACE TO CLOSE FLIED  S99528
         MVI   D0(R4),SLASH       CLOSE FIELD                    S22024
         LA    R4,1(R4)                POINT BACK TO NEXT FIELD
         B     TDDONE             EXIT TO IEDQWJ1                S99528
TDERR1   EQU   *
         CLI   NUMBER+D3,ONE      ERROR ON FIRST PASS           SA41608
         BNE   TDERR2A            NO                            SA41608
         NI    TDFLG,XFF-SYM-LINE CLEAR CONTROL FLAGS            S22024
TDERR2A  EQU   *                                                SA41608
         IEDQMSG MSGID=081,FUNCT=CEC INVALID TEST DEVICE ENTRY   S99528
TDERR3   EQU   *                                                 S22024
         SPACE
         IEDQMSG MSGID=057,FUNCT=REP,INBUF=CEIN,INCNT=8 CONTIN P S22024
         CLI   CEIN,YES           YES                            S22024
         BE    TD1                     YES BRANCH
         CLI   CEIN,NO            NO                             S22024
         BE    GETOUT                  YES BRANCH
         IEDQMSG MSGID=079,FUNCT=CEC INVALID RESPONSE            S99528
         SPACE
         B     TDERR1             CONTINUE?                      S22024
GETOUT1  DS    0H                                              @G36XRUV
         IEDQMSG MSGID=094,FUNCT=CEC                           @G36XRUV
GETOUT   EQU   *
         OI    TOTFLG06,TOTOTERM  SET TERMINATE FLAG             S22024
         L     R13,4(R13)              PICK UP CALLERS SAVE
         LM    R14,R1,12(R13)          RESTORE REGS
         XCTL  (2,12),EP=IEDQWE
TDSYM    EQU   *
         LA    R10,CEIN
         LA    R5,D8              SET LIMIT                      S22024
         LA    R3,D0              COUNTER                        S22024
TDSYM3   EQU   *
         CLI   D0(R10),BLANK      END OF NAME                    S22024
         BE    TDSYM1                  YES BRANCH
         LA    R10,D1(R10)        BUMP POINTER                   S22024
         LA    R3,D1(R3)          BUMP COUNTER                   S22024
         CR    R3,R5                   HAVE I LOOKED AT 8 POSITIONS
         BE    TDSYM1                  YES BRANCH
         B     TDSYM3                  NO LOOK AGAIN
TDSYM1   EQU   *
         MVI   WORK1,BLANK        PAD WORK AREA                  S22024
         MVC   WORK1+1(D7),WORK1  WITH BLANKS                    S22024
         STC   R3,LENGTH               SET UP BINARY SEARCH
         LA    R14,TRMWRK+D72     SET LIMIT                      S22024
         LR    R15,R4             CURRENT TRM SETTING            S22024
         AR    R15,R3             NEW SETTING                    S22024
         CR    R15,R14            ENTRY TOO LONG                 S22024
         BNL   TOOLONG            YES - ERROR                    S22024
         BCTR  R3,R0              DECREMENT FOR MOVE             S22024
         EX    R3,MOVE                 MOVE NAME TO WORK AREA
         B     TDSYM4             VERIFY NAME                    S99528
MOVE     MVC   WORK1(D0),CEIN     GET SYMBOLIC NAME              S22024
TDSYM4   BAL   R9,NAMESRCH             GO FIND NAME IN TCAM
         B     TDSYM5                  BRANCH AROUND ERROR RETURN
         B     TDERR1             ERROR                          S99528
TDSYM5   EQU   *
         EX    R3,MOVE1                MOVE NAME TO TRMWRK
         LA    R4,D1(R3,R4)       BUMP TRM POINTER               S22024
         BAL   R9,UCBFIND         GO GET UCB
         BAL   R9,PLUG            PUT LINE ADDRESS IN TABLE
         B     ASKAGN1              NO                           S22024
MOVE1    MVC   D0(D0,R4),CEIN     MOVE ENTRY TO TRM              S22024
TDDONE   EQU   *
         L     R4,TRMSAVE              GET ADDR OF TRM BUFFER
         MVC   D0(TRMLEN,R4),TRMWRK MOVE MESSAGE BACK TO BUFFER  S22024
TDDONE1  EQU   *
         BAL   R11,LINECHK        VERIFY THAT TEST LINES OPENED  S99528
         BAL   R3,TERMSTAT        CHECK INIT STATUS OF TEST DEVICES
*                                                               SA41608
         L     R13,D4(R13)        GET CALLER'S SAVE AREA ADDR
*        LINE OR TERMINAL. LOCATION 'CEIN' POINTS TO THE       * S22024
         LM    R14,R1,D12(R13)    RESTORE REG                    S22024
         XCTL  (2,12),EP=IEDQWJ1
         EJECT
TOOLONG  DS    0H                 BOUNDARY ALLIGNMENT            S22024
         IEDQMSG MSGID=060,FUNCT=CEC ENTRY TOO LONG              S22024
         SPACE
         B     GETOUT             TERMINATE PROMPT               S22024
         EJECT
PRMCONT  EQU   *
         ST    R9,REGSAVE              SAVE R9
PRMCON1  EQU   *                                                 S99528
         IEDQMSG MSGID=076,FUNCT=REP,INBUF=CEIN,INCNT=8 PROMPTED S22024
         CLI   CEIN,YES           YES                            S22024
         BE    PRMCONT1                YES BRANCH
         CLI   CEIN,NO            NO                             S22024
         BE    GETOUT                  YES BRANCH
         IEDQMSG MSGID=079,FUNCT=CEC INVALID RESPONSE            S99528
         SPACE
         B     PRMCON1            CONTINUE PROMPTING?            S99528
         SPACE
PRMCONT1 EQU   *
         L     R9,REGSAVE              RESTORE R9
         BR    R9                      RETURN
         EJECT
**************************************************************** S22024
*                                                              * S22024
*        ROUTINE TO DETERMINE THE TYPE OF TEST TO BE RUN --    * S22024
*        TEST DEVICE ENTRY.                                    * S22024
*                                                              * S22024
**************************************************************** S22024
TYPECHK  STM   R7,R11,SAVE        SAVE CALLER'S REG              S22024
         LA    R7,D0              ZERO TTE ADDR                  S22024
         LA    R10,CEIN           INPUT BUFFER                   S22024
         TM    D0(R10),NUMER      LINE TEST                      S22024
         BO    TYPELINE           YES                            S22024
*                                                              * S22024
*        FIND LENGTH OF TEST DEVICE ENTRY                      * S22024
*                                                              * S22024
         MVC   WORK2(D9),D0(R10)  MOVE ENTRY TO WORK AREA        S22024
         LA    R7,D9              LOOP COUNT                     S22024
         LA    R8,D0              COUNTER                        S22024
         LA    R9,WORK2           WORK AREA                      S22024
TYPELP   EQU   *                                                 S22024
         CLI   D0(R9),BLANK       END OF FIELD                   S22024
         BE    TYPEFD             YES                            S22024
         LA    R9,D1(R9)          NEXT CHAR                      S22024
         LA    R8,D1(R8)          INCREMENT COUNTER              S22024
         BCT   R7,TYPELP          NEXT TEST                      S22024
         B     TDERR1             ERROR - NAME TOO LONG          S22024
         SPACE
TYPEFD   EQU   *                                                 S22024
         LTR   R8,R8              TD ENTRY BLANK                 S22024
         BZ    TDERR              YES - ERROR                    S22024
         STC   R8,LENGTH          LENGTH OF ENTRY TO PLIST       S22024
         BCTR  R8,R0              LENGTH MINUS 1                 S22024
         MVC   WORK1(D8),XBLANK   BLANK NAME FIELD               S22024
         EX    R8,TDEVICE         NAME TO PLIST                  S22024
         SPACE
         BAL   R9,NAMESRCH        VERIFY SYMBOLIC NAME           S22024
         B     TYPRN              NORMAL RETURN                  S22024
         B     TDERR1             ERROR RETURN                   S22024
         SPACE
TYPRN    EQU   *
         L     R7,TNTENTRY        TTE ADDR                       S22024
         USING IEDNTRM,R7                                        S22024
         LA    R9,TRMPRFSZ        GET TTE PREFIX SIZE          @Y17XAUU
         SLR   R7,R9              POINT TO TTE PREFIX          @Y17XAUU
         TM    TRMSTATE,TRMPROC   VERIFY NOT PROCESS ENTRY     @Y17XAUU
         BNZ   TYPETERM           BR, NOT 3705 RESOURCE        @Y17XAUU
         TM    TRMSTATE,TRMPREF   3705 RESOURCE                @Y17XAUU
         BZ    TYPETERM           NO                             S22024
         CLI   TRMTYPE,TRMPUNT    PU ENTRY                     @Y17XAUU
         BE    TDERR1             YES - ERROR                  @Y17XAUU
         CLI   TRMTYPE,TRMLNCP    NCP ENTRY                    @Y17XAUU
         BE    TDERR1             YES - ERROR                  @Y17XAUU
         CLI   TRMTYPE,TRMSSCP    SSCP ENTRY                   @Y17XAUU
         BE    TDERR1             YES - ERROR                  @Y17XAUU
         CLI   TRMTYPE,TRMSDLC    SNA LINE ENTRY               @Y17XAUU
         BE    TYPELINE           YES - PROCESS                @Y17XAUU
         CLI   TRMTYPE,TRMNSDLC   PRE SNA LINE ENTRY           @Y17XAUU
         BE    TYPELINE           YES - PROCESS                @Y17XAUU
         TM    TRMSTATE,XE0       RN TERMINAL                    S22024
         BZ    TYPETERM           YES                            S22024
         B     TDERR1             ERROR EXIT                     S22024
         SPACE
TYPETERM EQU   *                                                 S22024
         TM    TDFLG,LINE         LINE FLAG SET                  S22024
         BO    TDERR1             ERROR - MIXED TD               S22024
         OI    TDFLG,SYM          SHOW TERMINAL TEST             S22024
         TM    TRMSTATE,TRMPREF   3705 MODE                    @Y17XAUU
         BO    TERM0              YES - BYPASS CHECK             S22024
         TM    TRMSTATE,XE0       TERMINAL ENTRY                 S22024
         BNZ   TDERR1             NO - ERROR
TERM0    DS    0H                 BOUNDARY ALLIGNMENT            S22024
         TM    TRMSTATE,TRMPREF   3705 MODE                    @Y17XAUU
         BZ    TERM4              BRANCH                         S22024
         OI    TDFLG,RNTERM       YES - SET FLAG                 S22024
TERM4    EQU   *                                                 S22024
         LM    R7,R11,SAVE        RESTORE REG                    S22024
         BR    R9                 RETURN                         S22024
         SPACE
TYPELINE EQU   *                                                 S22024
         TM    TDFLG,SYM          TERMINAL FLAG SET              S22024
         BO    TDERR1             ERROR MIXED TEST DEV           S22024
         OI    TDFLG,LINE         SHOW LINE TEST                 S22024
         LTR   R7,R7              TTE ADDR PRESENT               S22024
         BZ    TERM4              NO - EXIT                      S22024
         TM    TRMSTATE,TRMPREF   3705 MODE                    @Y17XAUU
         BZ    TERM4              NO                             S22024
         OI    TDFLG,RNLINE       YES - SET FLAG                 S22024
TERM8    EQU   *                                                 S22024
         B     TERM4              RETURN                         S22024
         DROP  R7                                                S22024
TDEVICE  MVC   WORK1(D0),WORK2    GET DEVICE ENTRY               S22024
         EJECT
*
*
*        ROUTINE TO FIND UCB ADDR AFTER ENTRY IN TERMINAL TABLE HAS
*             BEEN FOUND
*
*
UCBFIND  EQU   *
         L     R8,TNTENTRY             R8 NOW HAS ADDR OF TERM TAB ENT
         USING IEDQTRM,R8
         L R8,TRMDESTQ-D1         QCB ADDR                       S22024
         DROP  R8
         USING IEDQQCB,R8
         SR    R7,R7                   GET RELATIVE
         IC    R7,QCBRELLN          RELATIVE LINE NUMBER
         SLL   R7,D2              MULT BY 4                      S22024
         L     R8,QCBDCBAD-D1     DCB ADDR                       S22024
         DROP  R8
         USING IHADCB,R8
         TM    DCBDSORG,LGBA      LGB VS. DCB                    S22024
         BZ    UCB0X              NO                             S22024
         L     R1,TNTENTRY        GET TERMINAL TTE ADDRESS     @Y17XAUU
         BAL   R14,NCPTTE         GET NCP TTE ADDRESS          @Y17XAUU
*                                                              @Y17XAUU
*        ON RETURN R1 CONTAINS ADDRESS OF NCP TTE PREFIX       @Y17XAUU
*                                                              @Y17XAUU
         USING IEDNTRM,R1         SET TTE ADDRESSABILITY       @Y17XAUU
         TM    TRMBYTE2,TRMRSACT  3705 ACTIVE                  @Y17XAUU
         BZ    UCB0X1             NO                           @Y17XAUU
         TM    TRMBYTE2,TRMINPG   ACT/DEACT IN PROGRESS        @Y17XAUU
         BO    UCB0X1             YES - ERROR                  @Y17XAUU
         ST    R1,NCPSAVE         SAVE NCP TTE ADDRESS         @Y17XAUU
         L     R1,TRMDESTQ-D1     GET QCB ADDRESS              @Y17XAUU
         DROP  R1                                              @Y17XAUU
         USING IEDQQCB,R1         SET QCB ADDRESSABILITY       @Y17XAUU
         L     R8,QCBDCBAD-D1     GET 3705 DCB ADDRESS         @Y17XAUU
         DROP  R1                                              @Y17XAUU
         ST    R8,DCBSAVE         SAVE 3705 DCB ADDRESS        @Y17XAUU
         LA    R7,D4              SET RLN EQU 1                  S22024
         B     UCB0X              CHECK FOR DCB OPEN           @Y17XAUU
UCB0X1   EQU   *                                               @Y17XAUU
         IC    R7,LENGTH          LENGTH OF INVALID ENTRY        S22024
         SR    R4,R7              RESET TRM POINTER              S22024
         NI    TDFLG,XFF-RNTERM-RNLINE CLEAR INTERMEDIATE FLAGS  S22024
         IEDQMSG MSGID=010,FUNCT=CEC MSG                         S22024
         B     TDERR2A            ERROR EXIT                     S22024
UCB0X    EQU   *                                                 S22024
         TM    DCBOFLGS,OPEN      DCB OPEN                       S22024
         BZ    NOOPEN                  NO BRANCH
         L     R8,DCBDEBAD        GET DEB ADDRESS
         DROP  R8
         USING DEBNMSUB,R8
         SR    R15,R15
         IC    R15,DEBNMEXT       GET NUMBER OF EXTENTS
         SLL   R15,D2             MULT BY 4
         CR    R15,R7             IS DESIRED LINE OPENED?
         BL    NOOPEN               NO
         L     R8,DEBUCBAD-D4(R7) UCB ADDR                       S22024
         USING UCB,R8
         MVC   WORK1(D3),UCBNAME  UCB ID IN EBCDIC               S22024
         MVC   WORK2(D2),UCBCHA   UCB ID IN HEX                  S22024
         CLI   UCBTBYT3,UCB3COMM  COMMUNICATION DEVICE           S05331
         BNE   UCB08              NO - BYPASS SWT CHECK          S05331
         TM    UCBTBYT2,SWITCHS   SWITCHED START/STOP            S05331
         BNZ   UCB04              YES                            S05331
         MVN   WORKAREA(D1),UCBTBYT1 IS TERMINAL                 S05331
         MVZ   WORKAREA(D1),UCBTBYT4 SWITCHED                    S05331
         XI    WORKAREA,SWITCHB      BI-SYNC                     S05331
         BNZ   UCB08              NO                             S05331
UCB04    DS    0H                 BOUNDARY ALLIGNMENT            S05331
         OI    TOTFLG04,TOTTTSWT  SET SWITCHED TD FLAG           S05331
         OI    TOTFLG06,TOTNCMFG  SET NCM OPTION                 S05331
UCB08    DS    0H                 BOUNDARY ALLIGNMENT            S05331
         DROP  R8
         MVI   WORK2+D2,D0        CLEAR UNUSED BYTE              S22024
         NI    WORK2,X0F          CLEAR UNWANTED BITS            S22024
         BR    R9                      RETURN
NOOPEN   EQU   *
         SR    R4,R3                   REMOVE CURRENT
         BCTR  R4,R0              DEVICE FROM                    S22024
         EX    R3,CLEAR                  WORK AREA
         IEDQMSG MSGID=038,FUNCT=CEC TEST LINE NOT OPENED        S99528
         B     TDERR3             ERROR                          S22024
*
CLEAR    XC    D0(D0,R4),D0(R4)   CLEAR WORK AREA                S22024
*
         EJECT
*              ROUTINE TO SEARCH TERMNAME TABLE. R3 HAS ADDR OF
*              START OF TABLE. WORK1 HAS NAME IN IT. R2 HAS LENGTH
*
NAMESRCH EQU   *
         LA    R1,PAREMLST              SET R1
         L     R15,AVTUISV
         LH    R0,TWO(R1)         LOAD CHAR STRING LEN           S99528
         L     R1,FOUR(R1)        LOAD ADDR OF CHARS             S99528
         BAL   R14,FOUR(R15)      BRANCH TO SCAN ROUTINE         S99528
         LTR   R15,R15
         BZ    D4(R9)             NAME NOT FOUND                 S22024
         SPACE
         STM   R7,R9,TRMSAV1      SAVE REG 7-9
         L     R8,TERNMTAB        SET START OF TABLE
         USING IEDQTNTD,R8
         LA    R7,TNTFIRST        POINT TO FIRST ENTRY IN TABLE
         SR    R9,R9              CLEAR REG 9
         IC    R9,TNTENLEN        GET MAX. LENGTH OF NAME FIELD
         LA    R9,D3(R9)          GET LENGTH OF 1 ENTRY          S22024
         DROP  R8
         BCTR  R15,R0             DECREMENT ENTRY REL. POSITION  S22024
         MR    R8,R15             GETDISP. TO ENTRY DESIRED
         AR    R7,R9              GET DESIRED TNT ADDRESS
         ST    R7,TRMTNT          SAVE IT
         LA    R1,D1(R15)         RESTORE ENTRY REL. POSITION    S22024
         LM    R7,R9,TRMSAV1      RESTORE REG 7-9
         SPACE
         SPACE
         L     R15,TERNMTAB
         BALR  R14,R15            GET TERMINAL ENTRY ADDR        S99528
         ST    R1,TNTENTRY
         USING IEDQTRM,R1                                        S22024
         TM    TRMDSORG,TRMLGB    LGB RATHER THAN A TTE          S22024
         BO    D4(R9)             YES - ERROR - NOT A TERMINAL   S22024
         L     R1,TRMDESTQ-D1     DEST. QCB ADDR                 S22024
         DROP  R1                                                S22024
         USING IEDQQCB,R1                                        S22024
         LA    R1,D0(R1)            CLEAR HI ORDER BYTE          S22024
         TM    QCBFLAG,QCBTSSES   TSO TERMINAL                  SA61759
         BO    TRMTSO             YES - ERROR EXIT               S22024
         BR    R9                 RETURN                         S99528
         SPACE
TRMTSO   DS    0H                 BOUNDARY ALLIGNMENT            S22024
         IEDQMSG MSGID=011,FUNCT=CEC TSO TEST DEVICE             S22024
         B     GETOUT             TERMINATE TRM                  S22024
         EJECT
LINECHK  EQU   *
*
         LA    R4,TOTTDTBL        TEST DEVICE TABLE
         LA    R7,TOTTDEND        END OF TABLE
ALLTEST  EQU   *
         CR    R4,R7              ALL ENTRIES PROCESSED
         BE    D0(R11)            YES - EXIT                     S22024
         TM    D0(R4),TOTTDLIN+TOTTDORN RN LINE TEST             S22024
         BO    D0(R11)            YES - BYPASS CHECK             S22024
         TM    D0(R4),TOTTDLIN    270X LINE TEST                 S22024
         BZ    D0(R11)            NO - RETURN                    S22024
         L     R8,ZERO(R4)        GET ADDR FROM TABLE
         LA    R8,ZERO(R8)        CLEAR HI ORDER BYTE
         STH   R8,KUCB            SAVE UCB ADDR
         LTR   R8,R8              IS THERE AN ADDRESS
         BZ    D0(R11)            NO - RETURN                    S22024
         LA    R9,CVTPTR          POINTER TO CVT
         L     R9,ZERO(R9)        CVT ADDR
         L     R9,ZERO(R9)        TCB ADDR
         L     R9,FOUR(R9)        CURRENT TCB ADDR
         L     R9,TWELVE(R9)      TIOT ADDR
         USING TIOT,R9
         LA    R9,TIOENTRY        FIRST ENTRY
         DROP  R9
         USING TIOENTRY,R9
GETLENGT EQU   *
         SR    R3,R3              CLEAR REG 3
         IC    R3,TIOELNGH        DD ENTRY LENGTH
         LTR   R3,R3              ANY LENGTH ?
         BZ    NOTFOUND           NO - ERROR
         LA    R5,POOLSTAR-TIOESTTB DEV ENTRY LENGTH
         DROP  R9
         LR    R10,R3             SAVE LENGTH
         SR    R3,R5              DD LENGTH - 4
         AR    R3,R9              DEVICE
         USING TIOESTTB,R3
         CLC   TIOEFSRT+ONE(TWO),KUCB UCB ENTRY FOUND ?
         DROP  R3
         BE    FOUND              ENTRY FOUND
         AR    R9,R10             POINT TO NEXT ENTRY
         B     GETLENGT           CONTINUE LOOKING
FOUND    EQU   *
         LA    R4,FOUR(R4)        POINT TO NEXT LINE
         B     ALLTEST            GO SEE IF ALL ENTRIES HAVE BEENS99528
*                                   PROCESSED                    S99528
NOTFOUND EQU   *
         IEDQMSG MSGID=052,FUNCT=CEC
*
         IEDQMSG MSGID=081,FUNCT=CEC ERROR IN TEST DEV FIELD     S22024
*
PRMEND   EQU   *                                                 S22024
         IEDQMSG MSGID=057,FUNCT=REP,INBUF=CEIN,INCNT=8 CONTIN P S22024
         CLI   CEIN,YES           YES                            S22024
         BE    PRMEND1            YES - BRANCH                   S22024
         CLI   CEIN,NO            NO                             S22024
         BE    GETOUT             YES - BRANCH                   S22024
         IEDQMSG MSGID=079,FUNCT=CEC INVALID RESPONSE            S22024
         B     PRMEND             ASK AGAIN                      S22024
PRMEND1  EQU   *                                                 S22024
         XC    TOTTDTBL(D40),TOTTDTBL  CLEAR TEST DEV TABLE      S22024
         B     TDSTART            BEGIN PROMPTING                S22024
         EJECT
*                                                                S22024
*              ROUTINE TO LOOK UP UCB ENTRY
*
*
UCBLOOK  EQU   *
         L     R8,UCBSAVE               PICK UP ADDR OF UCB PTRS
         USING UCB,R7
UCBLOOK1 EQU   *                                                 S22024
         SR    R7,R7              CLEAR                        @G36XRUV
         ICM   R7,ADUCBMSK,D0(R8) LOAD UCB ADDR                @G36XRUV
         ST    R7,WORK4           SAVE UCB ADDR
         LA    R8,D2(R8)          POINT TO NEXT ENTRY            S22024
         BZ    UCBLOOK1                 PADDING IGNORE
         CLM   R7,ADUCBMSK,ENDTABLE Q-END OF TABLE REACHED     @G36XRUV
         BE    UCBEND             Y-GO SETUP ERROR RETURN      @G36XRUV
         CLC   WORK1(D3),UCBNAME  MATCH                          S22024
         BNE   UCBLOOK1                 NO GO LOOK AGAIN
***********************************************************************
*        THE FOLLOWING CODE IS MVS UNIQUE. IT CHECKS FOR 3270  @G36XRUV
*        LOCAL AND 3705 USING THE UCB. IF THE DEVICE IS EITHER,@G36XRUV
*        IT IS AN ERROR. NOTE: THE MEANINGS OF THE UCB BITS MAY@G36XRUV
*        CHANGE IN THE FUTURE NECESSITATING CHANGES TO THIS    @G36XRUV
*        CODE.                                                 @G36XRUV
***********************************************************************
         MVC   WORK2(D1),UCBTBYT4       MOVE TYPE TO WORK AREA @G36XRUV
         TM    UCBDVCLS,UCB3DISP        Q-GRAPHICS DEVICE      @G36XRUV
         BNO   NCPCHK                   N-CHECK FOR NCP        @G36XRUV
         TM    WORK2,XF0                Q-ANY HI BITS ON       @G36XRUV
         BNZ   NCPCHK                   N-NOT 3270             @G36XRUV
         CLI   WORK2,X09                Q-IN 3270 RANGE        @G36XRUV
         BL    NCPCHK                   N-CHECK FOR NCP        @G36XRUV
         B     GETOUT1                  3270 LOCAL - ERROR     @G36XRUV
NCPCHK   DS    0H                                              @G36XRUV
         NI    WORK2,X0F                CLEAR HI BITS          @G36XRUV
         CLI   WORK2,TCU3705            3705?                  @G36XRUV
         BNER  R9                       RETURN                 @G36XRUV
         B     GETOUT1                  3705 - ERROR           @G36XRUV
         EJECT
*              ROUTINE TO SEARCH TERMNAME TABLE. R3 HAS ADDR OF
*              START OF TABLE. WORK1 HAS NAME IN IT. R2 HAS LENGTH
*
UCBEND1  EQU   *                                                 S22024
         BR    R9                       GOOD RETURN
UCBEND   EQU   *                                                 S22024
         B     D4(R9)             ERROR RETURN                   S22024
         EJECT
************************************************************** @Y17XAUU
*        ROUTINE TO FINE NCP TTE ADDRESS. INPUT: REG 1 =     * @Y17XAUU
*        TERMINAL TTE ADDRESS. OUTPUT: = REG 1 = NCP TTE PREFIX@Y17XAUU
*        ADDRESS. RETURN TO CALLER VIA REG 14.               * @Y17XAUU
************************************************************** @Y17XAUU
NCPTTE   EQU   *                                               @Y17XAUU
         ST    R14,TRMSAVE2       SAVE RETURN ADDRESS          @Y17XAUU
         LA    R8,TRMPRFSZ        GET TTE PREFIX SIZE          @Y17XAUU
         SLR   R1,R8              POINT TO TTE PREFIX          @Y17XAUU
         USING IEDNTRM,R1         SET TTE ADDRESSABILITY       @Y17XAUU
NCPLOOP  EQU   *                                               @Y17XAUU
         LH    R1,TRMCOHRT        GET COHORT POINTER           @Y17XAUU
         LTR   R1,R1              ENTRY IN HIERARCHY           @YM07415
         BZ    UCB0X1             NO - ERROR RETURN            @YM07415
         L     R15,TERNMTAB       TTCIN TO TTE CONVERSION      @Y17XAUU
*                                 ROUTINE ADDRESS              @Y17XAUU
         BALR  R14,R15            FIND TTE ADDRESS             @Y17XAUU
         SLR   R1,R8              POINT TO TTE PREFIX          @Y17XAUU
         CLI   TRMTYPE,TRMLNCP    NCP ENTRY                    @Y17XAUU
         BNE   NCPLOOP            NO - CHECK NEXT ENTRY        @Y17XAUU
         L     R14,TRMSAVE2       RESTORE RETURN ADDRESS       @Y17XAUU
         BR    R14                RETURN TO CALLER             @Y17XAUU
         DROP  R1                                              @Y17XAUU
         EJECT
*        SET UP TEST DEVICE TABLE
PLUG     EQU   *
         LA    R7,TOTTDTBL        POINT TO TEST DEVICE TABLE     S99528
         LA    R8,TOTTDTBL+D40    POINT TO END OF TABLE          S99528
PLUG001  EQU   *                                                 S99528
         CR    R7,R8              TABLE FILLED                   S99528
         BE    TDERR              YES - TOO MANY ENTRIES         S99528
         L     R12,D0(R7)         GET NEXT ENTRY                 S99528
         LTR   R12,R12            VACANT SPACE IN TABLE          S99528
         BZ    PLUG002            YES                            S99528
         LA    R7,D4(R7)          NO - POINT TO NEXT SLOT        S99528
         B     PLUG001            GET NEXT ENTRY                 S99528
PLUG002  EQU   *                                                 S22024
         TM    TDFLG,SYM+RNTERM   TERMINAL TEST                  S22024
         BZ    PLUG008            NO                             S22024
         MVI   TRMTNT,TOTTDTRM    YES - SHOW TERMINAL TEST       S22024
         TM    TDFLG,RNTERM       TERMINAL ON RN                 S22024
         BZ    PLUG003            NO                             S22024
         OI    TRMTNT,TOTTDORN    YES - SHOW RN TERMINAL         S22024
PLUG003  EQU   *                                                 S22024
         MVC   D0(D4,R7),TRMTNT   TNT ADDR TO TD TABLE           S22024
         OI    TOTFLG10,TOTTERMS  SHOW TERMINAL TEST             S22024
         B     PLUGEND            EXIT                           S22024
PLUG008  EQU   *                  LINE ENTRY                     S22024
         LA    R14,TOTTDTBL       POINT TO TD TABLE              S22024
         TM    TDFLG,RNLINE       RN LINE TEST                   S22024
         BO    PLUG018            YES                            S22024
         MVI   WORK4,TOTTDLIN     SHOW LINE TEST                 S22024
         CR    R14,R7             FIRST ENTRY                    S22024
         BE    PLUG010            YES - BYPASS MIXED CHECK       S22024
         LR    R14,R7             GET CURRENT SETTINT            S22024
         S     R14,F4             LAST ENTRY ADDR                S22024
         CLI   D0(R14),TOTTDLIN   CUU TEST                       S22024
         BNE   TDERR1             NO - ERROR                     S22024
PLUG010  DS    0H                 BOUNDARY ALLIGNMENT            S22024
         MVC   D0(D4,R7),WORK4    UCB ADDR TO TD TABLE           S22024
         OI    TOTFLG06,TOTNCMFG  SET NON-CONCURRENT MODE FLAG   S22024
         B     PLUGEND            EXIT                           S22024
PLUG018  EQU   *                  RN SUB LINE                    S22024
         MVI   TRMTNT,TOTTDLIN    SHOW LINE TEST                 S22024
         OI    TRMTNT,TOTTDORN    SHOW RN LINE                   S22024
         CR    R14,R7             FIRST ENTRY                    S22024
         BE    PLUG024            YES - BYPASS MIXED CHECK       S22024
         LR    R14,R7             GET CURRENT SETTING            S22024
         S     R14,F4             LAST ENTRY ADDR                S22024
         TM    D0(R14),TOTTDORN   RN LINE TEST                   S22024
         BZ    TDERR1             NO - ERROR                     S22024
PLUG024  DS    0H                 BOUNDARY ALLIGNMENT            S22024
         MVC   D0(D4,R7),TRMTNT   TNT ADDR TO TD TABLE           S22024
PLUGEND  EQU   *                                                 S22024
         BR    R9                 RETURN                         S22024
         EJECT
*
*        ROUTINE TO DETERMINE INITIAL STATUS OF
*        TERMINAL TEST DEVICES
*
TERMSTAT EQU   *
         TM    TOTFLG10,TOTTERMS  TERMINAL TEST ?
         BZ    LINESTAT           NO - LINE TEST
         LA    R12,TOTTDTBL       POINT TO TEST DEVICE TABLE
         LA    R11,TOTTDEND       POINT TO END OF TABLE
STAT2    EQU   *
         CR    R12,R11            END OF TABLE ?
         BE    ZERO(R3)           YES - RETURN
         L     R7,D0(R12)         GET TNT ADDR
         LA    R7,D0(R7)          CLEAR HI ORDER BYTE
         LTR   R7,R7              LAST ENTRY ?
         BZ    ZERO(R3)           YES - RETURN
         ST    R7,TDTNT           SAVE TD TNT ADDRESS          @YM08136
         MVI   RQLADR,BLANK
         MVC   RQLADR+D1(D7),RQLADR
         SR    R10,R10            CLEAR REG
         IC    R10,TOTTTBEL       GET MAX NAME LENGTH
         LR    R9,R10             SAVE LENGTH
         BCTR  R9,R0              DECREMENT FOR EX
         EX    R9,RQMOVE          MOVE NAME TO REQ. ELEMENT
         AR    R7,R10             POINT TO TTE ADDR
         MVC   TTEADDR+D1(D3),D0(R7) ALLIGN ADDR
         L     R8,TTEADDR         GET TTE ADDR
         LA    R1,TRMPRFSZ        GET TTE PREFIX SIZE          @Y17XAUU
         SLR   R8,R1              POINT TO TTE PREFIX          @Y17XAUU
         USING IEDNTRM,R8                                      @Y17XAUU
         CLI   TRMTYPE,TRMLUNT    LU ENTRY                     @Y17XAUU
         BE    STAT16             YES - BYPASS INIT STATUS     @Y17XAUU
         L     R8,TRMDESTQ-D1     GET QCB ADDR
         DROP  R8
         USING IEDQQCB,R8
         MVC   RQRLN(D1),QCBRELLN SAVE RLN IN OP CONTROL PLIST
         TM    QCBSTAT,QCBTRMHO   TERMINAL INITIALLY HELD ?
         BZ    STAT6              NO
         OI    D0(R12),TOTTDTHD   SHOW INIT HELD
STAT6    EQU   *
         L     R9,TOTAVTPT        GET AVT ADDR
         TM    TOTFLG04,TOTTTSWT  TEST DEVICE SWITCHED ?
         BO    STAT12             YES - TERMINAL ACTIVE          S22024
*
*        IF THE TEST ENTRY IS AN NCP PRE SNA RESOURCE AND      @YM08136
*        EQUAL TO THE CONTROL TERMINAL OR ALTERNATE PRINTER    @YM08136
*        BYPASS ISSUING AN INACTIVATE FOR THE RESOURCE.        @YM08136
*
         SLR   R1,R1              CLEAR REGISTER               @YM08136
         ICM   R1,ADRMSK,TOTCRTNT+D1 GET CT TNT ADDRESS        @YM08136
         BZ    CHKAP              CHECK AP IF CT NOT TERMINAL  @YM08136
         CL    R1,TDTNT           CT EQU TD                    @YM08136
         BE    CHKTYPE            YES - CHECK FOR PRE SNA TERM @YM08136
CHKAP    EQU   *                                               @YM08136
         SLR   R1,R1              CLEAR REG                    @YM08136
         ICM   R1,ADRMSK,TOTARTNT+D1 GET AP TNT ADDRESS        @YM08136
         BZ    CHKEND             GO - INACTIVATE RESOURCE     @YM08136
         CL    R1,TDTNT           AP EQU TD                    @YM08136
         BNE   CHKEND             NO - GO INACTIVATE RESOURCE  @YM08136
CHKTYPE  EQU   *                                               @YM08136
         L     R1,TTEADDR         GET TTE ADDRESS              @YM08136
         LA    R15,TRMPRFSZ       GET TTE PREFIX SIZE          @YM08136
         SLR   R1,R15             POINT TO TTE PREFIX          @YM08136
         USING IEDNTRM,R1         SET TTE ADDRESSABILITY       @YM08136
         CLI   TRMTYPE,TRMPSNA    NCP PRE SNA TERMINAL         @YM08136
         BE    STAT12             YES - PROCESS NEXT TD ENTRY  @YM08136
CHKEND   EQU   *                                               @YM08136
         LA    R1,TOTOTECB        GET ECB ADDR
         ST    R1,RQECBA          PUT IN REQ ELEMENT
         L     R5,TOTAVTPT        GET AVT ADDR
         USING IEDQAVTD,R5
         LA    R14,AVTOPCOB       GET OP CONTROL QCB ADDR
         DROP  R5
         ST    R14,RQQCBA-D1      PUT IN QCB ADDR
         MVI   RQVBCD,TERMACTV    SET ACTIVATE VERB CODE
         MVI   RQVBCD,INACT       INACTIVATE VERB CODE           S22024
         XC    RQLINK(D3),RQLINK  CLEAR LINK FIELD
         XC    TOTOTECB(D4),TOTOTECB CLEAR ECB
         MVC   TOTPLIST(RQND-RQELE),RQELE ELEMENT TO OLTCB       S22024
         LA    R1,TOTPLIST        ADDR OF ELEMENT                S22024
         ST    R1,TRMSLPL         PUT IN PLIST                   S22024
         ST    R1,TRMSLPL+D4      PUT IN PLIST                   S22024
         MVI   TRMSLPL,RQINIT     SET INITIAL ELEMENT FLAG       S22024
         MVI   TRMSLPL+D4,RQEND   SET LAST ELEMENT FLAG          S22024
         LA    R1,TRMSLPL         ADDR OF PLIST                  S22024
*
         AQCTL                    ISSUE ACTIVATE COMMAND
         WAIT  ECB=TOTOTECB
*
         MVC   RQRTCD(D1),TOTORTCD RETURN CODE                   S22024
         CLI   RQRTCD,INACTRC     INIT INACTIVE                  S22024
         BNE   STAT12             NO - ACTIVE                    S22024
STAT10   EQU   *
         OI    D0(R12),TOTTDIAC   SHOW THAT LIST INACTIVE
STAT12   EQU   *                  FIND LCB ADDR
         L     R9,QCBDCBAD-D1     GET DCB ADDR
         USING IHADCB,R9
         TM    DCBDSORG,LGBA      LGB VS. DCB                    S22024
         BZ    STAT13             NO                             S22024
         DROP  R9                                                S22024
         L     R9,DCBSAVE         GET 3705 DCB ADDRESS         @Y17XAUU
         USING IHADCB,R9                                         S22024
STAT13   EQU   *                                                 S22024
         L     R7,DCBDEBAD        GET DEB ADDR
         USING DEBNMSUB,R7
         SR    R15,R15            CLEAR REG
         IC    R15,DEBNMEXT       GET DEB NUMBER OF EXTENTS
         DROP  R7
         SR    R7,R7              CLEAR REG
         IC    R7,QCBRELLN        GET RLN                        S22024
         TM    D0(R12),TOTTDORN   TD ON 3705                     S22024
         BZ    STAT14             NO                             S22024
         L     R5,NCPSAVE         GET NCP TTE ADDRESS          @Y17XAUU
         USING IEDNTRM,R5         SET TTE ADDRESSABILITY       @Y17XAUU
         TM    TRMBYTE2,TRMRSACT  NCP ACTIVE                   @Y17XAUU
         BZ    STAT16             NO - EXIT                    @Y17XAUU
         OI    D0(R12),TOTTDLST   YES - SHOW LINE STOPPED        S22024
         DROP  R5                                                S22024
         B     STAT16             EXIT                           S22024
STAT14   EQU   *                                                 S22024
         LR    R5,R7              COPY RLN                       S22024
         BCTR  R5,R0              DECREMENT BY 1
         SR    R15,R5             GET COUNT OF LCB'S TO SCAN
         SR    R14,R14            CLEAR REG
         IC    R14,DCBEIOBX       GET LCB SIZE
         LR    R5,R7              GET RLN
         MR    R4,R14             GET STARTING LCB OFFSET
         AL    R5,DCBIOBAD        POINT TO IOB OF LCB
         DROP R9
         S     R5,K32             ADJUST TO START OF LCB
         USING IEDQLCB,R5
         TM    LCBSTAT1,LCBFREEN+LCBRECVN+LCBSENDN  LINE STOPPED ?
         BNZ   STAT16             NO
         OI    D0(R12),TOTTDLST   YES - SHOW LINE STOPPED
         DROP  R5
*
STAT16   EQU   *
         LA    R12,D4(R12)        POINT TO NEXT ENTRY
         B     STAT2              PROCESS NEXT ENTRY
         DROP  R8
         EJECT
LINESTAT EQU   *   SET STATUS BITS FOR LINE ENTRIES
         LA    R12,TOTTDTBL       POINT TO TEST DEVICE TABLE
         LA    R11,TOTTDEND       POINT TO END OF TABLE
STAT20   EQU   *
         CR    R12,R11            END OF TABLE
         BE    ZERO(R3)           YES - RETURN
         L     R7,D0(R12)         GET UCB ADDR
         LA    R7,D0(R7)          CLEAR HI ORDER BYTE
         LTR   R7,R7              LAST ENTRY
         BZ    ZERO(R3)           YES - RETURN
         TM    D0(R12),TOTTDORN   TD ON 3705                     S22024
         BO    STAT40             YES                            S22024
         SPACE
         USING UCBOB,R7
         MVC   WORK1(D3),UCBNAME  GET UCB NAME
         DROP  R7
         BAL   R9,LCBFIND         GET LCB ADDR
         B     STAT24             LCB ADDR FOUND
         B     STAT28             BYPASS LCB CHECK
STAT24   EQU   *
         L     R8,WORK1           GET LCB ADDR
         LA    R8,D0(R8)          CLEAR HI ORDER BYTE
         USING IEDQLCB,R8
         TM    LCBSTAT1,LCBFREEN+LCBRECVN+LCBSENDN LINE STOPPED
         BNZ   STAT28             NO
         OI    D0(R12),TOTTDLST   YES - SHOW LINE STOPPED
         DROP R8
STAT28   EQU   *
         LA    R12,D4(R12)        POINT TO NEXT ENTRY
         B     STAT20             CHECK NEXT ENTRY
         SPACE 2
STAT40   EQU   *                                                 S22024
         SR    R10,R10            CLEAR REG                      S22024
         IC    R10,TOTTTBEL       MAX NAME LENGTH                S22024
         AR    R7,R10             POINT TO TTE ADDR              S22024
         MVC   TTEADDR+D1(D3),D0(R7) ALLIGN ADDR                 S22024
         L     R8,TTEADDR         TTE ADDR                       S22024
         USING IEDNTRM,R8         SET TTE ADDRESSABILITY       @Y17XAUU
         LA    R1,TRMPRFSZ        GET TTE PREFIX SIZE          @Y17XAUU
         SLR   R8,R1              POINT TO TTE PREFIX          @Y17XAUU
         TM    TRMLINK,TRMLACTV   LINE ACTIVE                  @Y17XAUU
         BO    STAT28             YES - EXIT                   @Y17XAUU
         OI    D0(R12),TOTTDLST   YES - SHOW LINE STOPPED        S22024
         DROP  R8                                                S22024
         B     STAT28             CHECK NEXT ENTRY               S22024
         EJECT
*                                                                S22024
*        FIND LCB ADDR FOR LINE ENTRY IN TEST DEVICE TABLE.
*        THE UCB NAME IS PASSED TO THIS ROUTINE.
*        THE LCB ADDRESS IS RETURN TO THE CALLER
*
LCBFIND  EQU   *
         STM   R1,R12,REGSAVE     SAVE CALLER'S REG
NOUCBAD  EQU   *
         L     R1,D16(R0)         GET CVT ADDR
         L     R1,AVTCVTPT(R1)    GET TCAM DISP ADDR
         L     R1,D0(R1)          GET AVT ADDR
         USING IEDQAVTD,R1
         L     R1,AVTTCB          GET TCB ADDR
         DROP  R1
         L     R1,D8(R1)          GET DEB CHAIN ADDR
AGAIN    EQU   *
         SR    R3,R3              CLEAR REG
         IC    R3,D16(R1)         PICK UP NUMBER OF EXTENTS
         LA    R5,D32(R1)         GET UCB ADDR CHAIN
         L     R4,D24(R1)         DCB ADDR                       S99528
         USING IHADCB,R4                                         S22024
         TM    DCBDSORG,LGBA      LGB VS. DCB                    S22024
         BZ    HERE0              NO                             S22024
         L     R4,DCBSAVE         GET 3705 DCB ADDRESS         @Y17XAUU
HERE0    EQU   *                                                 S22024
         L     R12,D4(R1)         GET ADDR OF NEXT DEB
         TM    DCBDSORG+D1,TCAMDEB TCAM DEB
         BZ    HERE1              NO - BRANCH
         MVC   LCBLEN(D1),DCBEIOBX GET LCB SIZE
         LA    R8,D1              SET UP COUNTER
HERE     EQU   *
         L     R9,D0(R5)          PICK UP UCB ADDR
         CLC   WORK1(D3),D13(R9)  CUU MATCH
         BE    THERE              YES - BRANCH
         LA    R8,D1(R8)          BUMP COUNTER
         LA    R5,D4(R5)          BUMP UCB ADDR POINTER
         BCT   R3,HERE            GO LOOK AGAIN
HERE1    EQU   *
         LA    R12,D0(R12)        CLEAR HI ORDER BYTE
         LTR   R12,R12            ANY MORE DEBS
         BZ    BADRET             NO - BRANCH
         LR    R1,R12             SET R1 TO NEXT DEB
         B     AGAIN              GO LOOK AT NEXT DEB
BADRET   EQU   *
         LM    R1,R12,REGSAVE     RESTORE REG
         B     D4(R9)             RETURN
THERE    EQU   *
         USING UCB,R9                                            S22024
         MVC   RNWORK(D1),UCBTBYT4 TCU TYPE BYTE                 S22024
         NI    RNWORK,X0F         CLEAR UNWANTED BITS            S22024
         CLI   RNWORK,TCU3705     3705 TCU                       S22024
         BNE   THERE2             NO                             S22024
         LA    R8,D1              SET RLN EQU 1                  S22024
         DROP  R9                                                S22024
THERE2   EQU   *                                                 S22024
         STC   R8,RLN             SAVE RLN
         STC   R8,MULT+D3         SET UP THE MULTIPLIER
         L     R8,DCBIOBAD        PICK UP LCB IOB ADDR
         SR    R10,R10            CLEAR REG
         SR    R11,R11            CLEAR REG
         IC    R11,LCBLEN         SET R3 TO SIZE OF LCB
         M     R10,MULT           GET DISPLACEMENT
         LA    R8,D0(R11,R8)      POINT TO CORRECT LCB
         S     R8,K32             LCB ADDR
         ST    R8,WORK1           SAVE LCB ADDR
         LM    R1,R12,REGSAVE     RESTORE REG
         BR    R9                 RETURN
         EJECT
         DROP  R4
CEOUT2   DC    C'XXXXXXXX NOT OPENED' LINE NOT OPENED MSG        S99528
CEOUT2E  DS    0C                 END OF MSG                     S99528
UCBSAVE  DS    A                  UCB ADDR SAVE AREA             S99528
MULT     DC    XL4'0'             WORK AREA                      S99528
TNTENTRY DS    A                  ADDR OF TERNNAME TABE          S99528
TRMTNT   DS    A                  TNT ADDR                       S99528
TRMSAV1  DS    3F                 REG SAVE AREA                  S99528
TDTNT    DS    A                  SAVE AREA FOR TD TNT ADDRESS @YM08136
TERNMTAB DS    A                  TERMINAL TABLE ADDR            S99528
AVTUISV  DS    A                  BINARY SEARCH RTN ADDR
F4       DC    F'4'               VALUE OF 4                     S22024
         DS    0D                 BOUNDARY ALLIGNMENT
NUMBER   DC    X'0000000000000000' COUNTER                       S99528
         DS    0F                 ALLIGNMENT
WORK4    DS    F                  WORK AREA
WORK1    DC    XL8'0'             TERMINAL NAME
WORK2    DC    XL12'0'            WORK AREA                      S22024
SAVE     DS    5F                 SAVE AREA                      S22024
TRMSAVE  DS    F                  SAVE AREA
TRMWRK   DS    CL72               TRM WORK BUFFER
TRMWRKE  DS    0C                 END OF TRM BUFFER
ANSWER   DS    CL4                INPUT BUFFER
         DS    0F                 ALLIGNMENT
PAREMLST DS    0CL8               BINARY SRCH P-LIST
         DS    CL1                BINARY OFFSET
         DC    X'00'              FLAG BYTE
         DC    X'00'              FLAG BYTE
LENGTH   DS    CL1                LENGTH OF TERMINAL NAME
         DC    A(WORK1)           ADDR OF TERMINAL NAME
         DS    0F                 ALLIGNMENT
RE       DS    0CL8               OP CONTROL P-LIST
OCQCB    DS    F                  QCB ADDR
         DC    X'DC'              FLAG
         DC    XL3'0'             LINK FIELD
ENDTABLE DC    XL2'FFFF'          END OF UCB TABLE             @G36XRUV
VERBCODE DC    X'41'              COMMAND
RELEN    DC    X'18'              LENGTH OF ELEMENT
RLN      DS    CL1                RELATIVE LINE NUMBER
RETCODE  DC    X'00'              RETURN CODE
         DC    A(ECB)             ECB ADDR
ECB      DC    2F'0'              ECB
REPAREM  DC    X'0C'              FIRST ELEMENT
         DC    AL3(RE)            ADDR OF ELEMENT
         DC    X'80'              LAST ELEMENT
         DC    AL3(RE)            ADDR OF LAST ELEMENT
REGSAVE  DS    12F                REG SAVE AREA
         SPACE
***********************************************************************
*                                                                     *
*        PARAMETER LIST FOR OPERATOR CONTROL COMMANDS                 *
*                                                                     *
TRMSLPL  DS    0F                 PARAMETER LIST FOR ACT. TERMINAL
         DC    X'0C'              FIRST ELEMENT
         DC    AL3(RQELE)         ADDR OF FIRST ELEMENT
         DC    X'80'              LAST ELEMENT
         DC    AL3(RQELE)         ADDR OF LAST ELEMENT
***********************************************************************
         SPACE
RQELE    DS    0F                 OP CONTROL REQUEST ELEMENT
RQKEY    DC    X'00'              KEY
RQQCBA   DC    X'000000'          QCB ADDR
RQPRI    DC    X'DC'              PRIORITY
RQLINK   DC    X'000000'          LINK FIELD
RQVBCD   DC    X'00'              VERB CODE
RQELNG   DC    X'18'              LENGTH CONSTANT
RQRLN    DC    X'00'              RELATIVE LINE NUMBER
RQRTCD   DC    X'00'              RETURN CODE
RQECBA   DC    AL4(0)             ECB ADDR
RQLADR   DC    8X'40'             LINE ADDR OR TERMINAL NAME
RQND     DS    0C                 END OF ELEMENT                 S22024
*
         SPACE
NCPSAVE  DS    F                  SAVE AREA                    @Y17XAUU
DCBSAVE  DS    F                  SAVE AREA                    @Y17XAUU
TTEADDR  DC    F'0'               WORK AREA TO ALLIGN TTE ADDR
TRMSAVE2 DS    F                  REGISTER SAVE AREA           @Y17XAUU
K32      DC    F'32'              VALUE OF 30
RQMOVE   MVC   RQLADR(D0),D0(R7) MOVE NAME TO REQ ELEMENT
LCBLEN   DS    CL1                LCB LENGTH                     S99528
TERNMLEN DS    CL1                LENGTH OF TERMINAL NAME
TDFLG    DC    X'00'              CONTOOL BYTE                   S99528
TRMFLG   DC    X'00'              CONTROL BYTE
TDFLGG   DC    X'00'              CONTROL BYTE
RNWORK   DS    C                  WORK AREA                      S22024
SYM      EQU   X'40'              TERMINAL TEST FLAG
KUCB     DC    H'0'               TEST UCB SAVE AREA
XBLANK   DC    8XL1'40'           BLANK WORK AREA                S22024
WORKAREA DC    XL1'0'             WORK AREA                      S05331
PATCH    DS    CL200              IEDQWJ PATCH AREA
         EJECT
         TLGBD                                                   S22024
         EJECT
         TLCBD
         EJECT
TIOT     DSECT
         IEFTIOT1
         EJECT
         SPACE 4
         TQCBD
         EJECT
         DCBD  DSORG=TX
         EJECT
         TTRMD
         EJECT
         TSCBD
         EJECT
         TPRFD
         EJECT
CVT      DSECT
         CVT
         EJECT
UCB      DSECT
         IEFUCBOB
         EJECT
         TAVTD
         EJECT
         TDEBD
         EJECT
         TTNTD
         EJECT
RESPL    RESPL                                                 @Y17XAUU
         EJECT
         OLTCB                                                   S22024
CEIN     EQU   TOTINBUF           INPUT BUFFER                   S22024
         END
