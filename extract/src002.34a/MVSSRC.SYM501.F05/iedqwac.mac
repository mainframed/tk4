         TITLE 'IEDQWV  GRAB - $LETGO SERVICE MODULE'
IEDQWV   CSECT
*A000000-999999                                                @Y16X5U0
* CHANGE ACTIVITY AS FOLLOWS:                                    S22024
*A216000                                                         A41584
*A276000                                                         A41584
*C444000                                                         A41584
* A004400,A010000-016000,A136000,A228540-228560,C232000,C244000  S22024
* A395000-398700,C672400,A676410-676430,C836000                  S22024
*C675800                                                       @XA06343
*A228780,228800,269120,396500,397300,652000,696000,696370      @Y17XAUU
*A752000,755700,821700,836000                                  @Y17XAUU
*C269720,395100,397200,398600,676410,752100-752200,752315      @Y17XAUU
*C752321,752333,752350                                         @Y17XAUU
* A014000,138000,198000,199000,445000,623000,624000,631000,    @G36XRUV
* A644000,673000                                               @G36XRUV
* C199000,200000,446000,632000,645000                          @G36XRUV
* D622000                                                      @G36XRUV
*   TOTE ALIAS                                                 @OY13283
*
***********************************************************************
*                                                                     *
* DESCRIPTIVE NAME:  GRAB AND $LETGO SERVICE MODULE              S22024
*                                                                S22024
* MODULE NAME: IEDQWV (TCAM,TOTE)                              @Y17XAUU
*                                                                S22024
* STATUS: VERSION 10.0                                         @Y17XAUU
*                                                                     *
* FUNCTIONS:                                                          *
*                                                                     *
*       GRAB -                                                        *
*       THE PURPOSE OF THIS ROUTINE IS TO ASSIGN A SECONDARY          *
*       DEVICE TO THE UNIT TEST.                                      *
*                                                                     *
*       THE GRAB MACRO REQUESTS THAT A SECONDARY DEVICE MEETING       *
*       SPECIFIED REQUIREMENTS BE ASSIGNED TO THE UNIT TEST.          *
*                                                              @Y17XAUU
*       IF THE GRAB REQUEST IS FOR A SNA LU COUNTERFEIT        @Y17XAUU
*       CONTROL BLOCKS MUST BE SET UP FOR THE TEST DEVICE.     @Y17XAUU
*       IEDQWB2 IS CALLED TO PROCESS THE COUNTERFEITING OF     @Y17XAUU
*       TTE, QCB, AND LGB.                                     @Y17XAUU
*                                                                     *
*       THE DEVICE ENTRY LIST BUILT FROM THE TEST REQUEST MESSAGE     *
*       IS SEARCHED TO FIND A DEVICE MEETING THE SPECIFIED REQUIRE-   *
*       MENTS.  IF A PROPER DEVICE IS FOUND, IT IS ASSIGNED AS        *
*       A TEST DEVICE TO THE UNIT TEST.                               *
*                                                                     *
*       AT THE COMPLETION OF THIS ROUTINE, ONE OF THE FOLLOWING       *
*       RETURN CODES IS PLACED IN REGISTER 15:                        *
*                                                                     *
*       X'00' - NORMAL COMPLETION OF THE GRAB FUNCTION.               *
*       X'04' - UNSUPPORTED MACRO                              @Y17XAUU
*       X'08' - NO (MORE) DEVICES IN THE ENTRY LIST MEET              *
*       THE SPECIFIED REQUIREMENTS.                                   *
*                                                                     *
*       X'0C' - DEVICE ENTRY LIST CONTAINS ONLY ONE DEVICE.           *
*                                                                     *
*       $LETGO -                                                      *
*       THE PURPOSE OF THIS ROUTINE IS TO REMOVE A PREVIOUSLY         *
*       GRABED DEVICE FROM THE DEVICE TABLE                           *
*                                                                     *
* ENTRY POINT:                                                        *
*                                                                     *
*       IEDQWV - CALLED WHEN A GRAB OR $LETGO MACRO IS ISSUED         *
*       BY THE UNIT TEST                                              *
*                                                                     *
* LINKAGE: BALR R14,R15 FROM IEDQWA                            @Y17XAUU
*                                                              @Y17XAUU
* MODULE TYPE: PROCEDURE                                       @Y17XAUU
*                                                              @Y17XAUU
* MODULE SIZE: 2K MAXIMUM                                      @Y17XAUU
*                                                              @Y17XAUU
* PROCESSOR: ASSEMBLER XF                                      @Y17XAUU
*                                                              @Y17XAUU
* MACROS:AQCTL,SAVE,CLOSE,DELETE,LOAD,DEQ,ENQ,IEDHJN,OPEN      @Y17XAUU
*                                                              @Y17XAUU
* INPUT:        REGISTERS 01,02,13,14,15 CONTAIN THE FOLLOWING VALUES:*
*                                                                     *
*                         01-GRAB PARAMETER LIST ADDRESS              *
*                         02-OLTCB POINTER                            *
*                         13-SAVE AREA ADDRESS                        *
*                         14-RETURN ADDRESS                           *
*                         15-ENTRY POINT ADDRESS                      *
*                                                                     *
*                                                                     *
* OUTPUT:       REGISTERS 15 CONTAIN THE FOLLOWING VALUES:            *
*                                                                     *
*                         15-RETURN CODE:- GRAB                       *
*                            X'00' - NORMAL COMPLETION                *
*                            X'04' - UNSUPPORTED MACRO         @Y17XAUU
*                            X'08' - NO (MORE)DEVICES IN THE ENTRY    *
*                                    LIST MEET THE SPECIFIED          *
*                                    REQUIREMENTS                     *
*                            X'0C' - DEVICE ENTRY LIST CONTAINS ONLY  *
*                                    ONE DEVICE                       *
*                                                                     *
*                         15-RETURN CODE:- $LETGO                     *
*                            X'00' NORMAL COMPLETION                  *
*                            X'04' - UNSUPPORTED MACRO         @Y17XAUU
*                            X'0C' DEVICE NOT PREVIOUSLY GRABED       *
* EXTERNAL ROUTINE: IEDQTNT - FIND TTE                         @Y17XAUU
*                    IEDQA1 - FIND TTCIN                       @Y17XAUU
*                    IEDIAP04 - FIND NETWORK ADDRESS           @Y17XAUU
*                    IEDQWB2 - COUNTERFEIT SNA LU TEST DEVICE  @Y17XAUU
*                                                                     *
* EXIT POINTS:    BR AFTER 'GRB070'   TO IEDQWA                       *
*                                                                     *
* TABLES/WORK AREAS:  OLTCB                                           *
*                                                                     *
* ATTRIBUTES:                                                         *
*                                                                     *
*       ENABLED, PROBLEM PROGRAM MODE, RESIDENT, REENTRANT     @Y17XAUU
*                                                                     *
* COPYRIGHT='NONE'                                               S22024
*                                                              @Y17XAUU
* NOTE: SEE BELOW                                              @Y17XAUU
*                                                              @Y17XAUU
*  DEPENDENCIES: EBCDIC CHARACTER CODE SET                     @Y17XAUU
*                                                              @Y17XAUU
*  RESTRICTIONS: NONE                                          @Y17XAUU
*                                                              @Y17XAUU
*  REGISTER CONVENTIONS: SEE REGISTERS ASSIGNMENT              @Y17XAUU
*                                                              @Y17XAUU
*  PATCH LABEL: PATCH                                          @Y17XAUU
*                                                                     *
***********************************************************************
*
*        EQUATES
*
R0       EQU   0                       REG 0                     S99528
R1       EQU   1                       REG 1                     S99528
R2       EQU   2                       REG 2                     S99528
R3       EQU   3                       REG 3                     S99528
R4       EQU   4                       REG 4                     S99528
R5       EQU   5                       REG 5                     S99528
R6       EQU   6                       REG 6                     S99528
R7       EQU   7                       REG 7                     S99528
R8       EQU   8                       REG 8                     S99528
R9       EQU   9                       REG 9                     S99528
R10      EQU   10                      REG 10                    S99528
R11      EQU   11                      REG 11                    S99528
R12      EQU   12                      REG 12                    S99528
R13      EQU   13                      REG 13                    S99528
R14      EQU   14                      REG 14                    S99528
R15      EQU   15                      REG 15                    S99528
PURGE    EQU   X'10'              SVC EQUATE FOR PURGE           A41584
PARMREG0 EQU   R0                      MSG PARM                  S99528
PARMREG1 EQU   R1                      MSG PARM                  S99528
ZERO     EQU   0                       ZERO DISPLACEMENT         S99528
THREE    EQU   3                       THREE                     S99528
HALF     EQU   3                       HALF WORD               @G36XRUV
FOUR     EQU   4                       FOUR                      S99528
FIVE     EQU   5                       FIVE                      S99528
EIGHT    EQU   8                       EIGHT                     S99528
D0       EQU   0                  DISPLACEMENT OF 0            @Y17XAUU
D2       EQU   2                       TWO                       S22024
D3       EQU   3                  DISPLACEMENT OF 3            @Y17XAUU
TEN      EQU   10                      TEN                       S99528
D4       EQU   4                       FOUR                      S22024
D32      EQU   32                      THIRTY TWO                S22024
D34      EQU   34                      DISPLACEMENT 34           S99528
SYMCLASS EQU   X'44'                   CLASS SUPORTED            S22024
SAMSAMSA EQU   X'55'                   SAME CHANNEL SAME CONTROL S22024
*                                      UNIT SAME DEVICE          S22024
NXTDEV   EQU   X'01'                   NEXT DEV IN LIST          S22024
ADRMSK   EQU   7                       ADDRESS MASK            @Y17XAUU
RETDAF   EQU   X'10'              DAF RETURNED RET CODE        @Y17XAUU
DISREG2  EQU   28                 DISPLACEMENT TO REG 2        @Y17XAUU
ERRCD08  EQU   8                  ERROR RETURN CODE            @Y17XAUU
ERRCD12  EQU   12                 ERROR RETURN CODE            @Y17XAUU
D1       EQU   1                  DISPLACEMENT OF 1            @Y17XAUU
D5       EQU   5                       DISPLACEMENT OF 5       @Y17XAUU
D8       EQU   8                  DISPLACEMENT OF 8            @Y17XAUU
X25      EQU   X'25'                   DISPLACEMENT OF X'25'   @Y17XAUU
XFF      EQU   X'FF'              MASK FIELD                   @Y17XAUU
ADRMSK7  EQU   7                  ADDRESS MASK                 @Y17XAUU
         EJECT
IEDQWAC  EQU   *                                                 S99528
IEDQW21  EQU   *                                               @Y17XAUU
         ENTRY IEDQW21,IEDQWAC                                   S99528
         USING IEDQWV,R15              ADDRESSIBILITY            S99528
IEDQWV   IEDHJN IEDQWVV,HJN            NAME AND HJN              S99528
         DROP  R15                                               S99528
         USING IEDQWV,R4
         USING TOTOLTCB,R2             TOTE CONTROL BLOCK        S22024
         SAVE  (14,12)                 SAVE REGS                 S99528
         LR    R4,R15             SET BASE REG.
         LA    R15,TOTSAVE1            ADDRESS OF OWN SAVE AREA  S22024
         ST    R15,8(R13)         PUT ADDR. IN CALLER'S SAVE.
         ST    R13,4(R15)         SAVE ADDR. OF CALLER'S SAVE AREA.
         LR    R13,R15            SET R13 TO POINT TO OWN SAVE AREA.
         SPACE
         CLI   1(R1),X'02'        CORRECT MACRO LEVEL.
         BE    GRB000             YES.
*
         LA    R15,FOUR                INCORRECT LEVEL MACRO     S99528
         B     GRB070                  EXIT                      S99528
         SPACE
GRB000   EQU   *                                                 S99528
         MVC   TOTSERV(END-START),START SET UP WORK AREA       @Y17XAUU
         MVC   BLDLLIST(BLDLEND-BLDLLIST),QLDLLIST COPY        @Y17XAUU
*                                      BLDL PLIST              @Y17XAUU
         LA    R15,JFCBAA              GET JFCB ADDRESS        @Y17XAUU
         STCM  R15,ADRMSK,JFCBADDR+D1  SAVE IN EXIT PLIST      @Y17XAUU
         LA    R15,ADDOFUCB            USC ADDRESS             @Y17XAUU
         STCM  R15,ADRMSK,TOT102+D1    SAVE IN PLIST           @Y17XAUU
         LA    R15,TOT102LN            GET ADDRESS OF LEN FIELD@Y17XAUU
         STCM  R15,ADRMSK,TOT102A+D5   ADDRESS OF LENGTH FIELD @Y17XAUU
         LA    R15,JFCBADDR            GET ADDRESS OF JFCB     @Y17XAUU
         STCM  R15,ADRMSK,EXCPDCB+X25  PUT IT IN DCB EXLST     @Y17XAUU
         LA    R15,QNAME               GET QNAME ADDRESS       @Y17XAUU
         ST    R15,DEQLIST+D4          PUT IN DEQ PLIST        @Y17XAUU
         LA    R15,RNAME               GET RNAME ADDRESS       @Y17XAUU
         ST    R15,DEQLIST+D8          PUT IN DEQ PLIST        @Y17XAUU
         MVC   CDSLINE,NAMEFLD         INIT NAME FIELD         @Y17XAUU
         EJECT
         ST    R1,GRABPARM             SAVE PARM POINTER         S99528
         CLC   2(2,R1),GRABREQ         GRAB REQUEST?           @Y17XAUU
         BNE   LET002                  NO ASSUME ENTRY FOR LETGO S99528
         LA    R11,8(R1)               GET FLAG BYTE             S99528
         SR    R10,R10                 CLEAR                   @G36XRUV
         ICM   R10,HALF,TOTPRUCB       GET PRIMARY UCB ADDR    @G36XRUV
         SR    R9,R9                   CLEAR                   @G36XRUV
         ICM   R9,HALF,TOTSCUCB        GET SECONDARY UCB ADDR  @G36XRUV
         LA    R9,0(R9)                CLEAR HIGH ORDER BYTE     S99528
         LTR   R9,R9                   IS PURGE REQUIRED         S99528
         BZ    GRB0005                 NO BYPASS PURGE           S99528
         LA    R1,TOTSCDCB             SECENDARY DEC             S99528
         ST    R1,OPENAREA             STORE IN P LIST           S99528
         MVI   OPENAREA,X'B0'          OPTION CLOSE              S99528
         LA    R1,OPENAREA             ADDRESS OF P LIST         S99528
         CLOSE MF=(E,(1))              CLOSE SECONDARY DCB       S99528
GRB0005  EQU   *                                                 A41584
         LA    R1,TOTTDTBL             FIRST ENTRY POINTER       S99528
         L     R1,4(R1)                GET SECOND ENTRY          S99528
         LTR   R1,R1                   AT LEAST TWO ENTRIES      S99528
         BP    GRB001                  YES BRANCH                S99528
         LA    R15,ERRCD12             SET RETURN CODE         @Y17XAUU
          B     GRB070                 GET OUT                   S99528
GRB001   L     R8,TOTGRABP             GET GRAB POINTER          S99528
GRB002   LA    R8,4(R8)                BUMP GRAB POINTER TO NEXT S99528
         L     R9,0(R8)                GET TNT ADDRESS           S99528
         ST    R9,TOTSRTNT             SAVE IT                   S99528
         LA    R5,TOTTDEND             GET ADDRESS OF END OF TABLS99528
         CR    R5,R8                   GRAB POINTER PAST END OF  S99528
         BE    EIGRET                  YES BRANCH                S22024
         L     R7,0(R8)                GET UCB ADDRESS           S99528
         LA    R7,0(R7)                CLEAR HI BYTE             S99528
         LTR   R7,R7                   IS THIS A UCB ADDRESS?    S99528
         BZ    EIGRET                  NO BRANCH                 S22024
         ST    R8,TOTGRABP             SAVE GRAB POINTER TO NXT  S22024
         OI    0(R8),TOTTDGRB          TURN ON GRAB FLAG         S22024
         CR    R7,R10                  IS GRABED DEVICE PRIMARY  S99528
         BNE   GRB008                  NO BRANCH                 S99528
EIGRET   EQU   *                                                 S22024
         LA    R15,8                   DEVICE NOT FOUND CODE     S99528
         B     GRB070                  RETURN                    S99528
MOVECDS  MVC   CDSLINE,0(R7)           MOVE NAME FOR LOAD        S22024
         EJECT
QCBFIND  EQU   *                       FIND SECONDARY QCB,RESOU@Y17XAUU
*                                      CE ID,AND THE LCB         S22024
         LR    R9,R7                   GET TNT ADDRESS           S22024
         ST    R9,TRMTNT               SAVE TNT ADDRESS        @Y17XAUU
         SR    R3,R3                   CLEAR REG                 S22024
         IC    R3,TOTTTBEL             GET NAME LENGTH           S22024
         AR    R9,R3                   ADDRESS PTR               S22024
         MVC   TNTENTRY+D1(D3),D0(R9)  ALLINE ADDRESS          @Y17XAUU
         BCTR  R3,0                    DECREMENT FOR EX          S22024
         EX    R3,MOVECDS              MOVE NAME TO CDS          S22024
         L     R9,TNTENTRY             TTE ADDRESS             @Y17XAUU
         USING IEDQTRM,R9              TTE                       S22024
         L     R6,TOTAVTPT             GET AVT ADDRESS         @Y17XAUU
         USING IEDQAVTD,R6             SET AVT ADDRESSABILITY  @Y17XAUU
         L     R15,AVTRNMPT            TERM NAME TABLE ADDR    @Y17XAUU
         ST    R15,TERNMTAB            SAVE IT                 @Y17XAUU
         L     R15,AVTMSGS-D1          VCON TABLE ADDRESS      @Y17XAUU
         L     R15,D8(R15)             ADDRESS OF A1 ROUTINE   @Y17XAUU
         LA    R0,1(R3)                GET NAME LENGTH         @Y17XAUU
         LA    R1,CDSLINE              GET ADDRESS OF NAME     @Y17XAUU
         BAL   R14,FOUR(R15)           FIND SEC TD TTCIN       @Y17XAUU
*                                                              @Y17XAUU
*        IEDQA1 RETURNS TEST DEVICE TTCIN IN REG 15            @Y17XAUU
*                                                              @Y17XAUU
         STH   R15,TOTSCOFF            SAVE SC TD TTCIN        @Y17XAUU
         LR    R1,R15                  SET UP TO FIND NETWORK  @Y17XAUU
*                                      ADDRESS                 @Y17XAUU
         L     R15,AVTSAVTP            GET SAVT ADDRESS        @Y17XAUU
         L     R15,SAVTTNTX-IEDNSVTD(R15) GET ADDRESS OF TTCIN @Y17XAUU
*                                      TO NETWORK ADDR CONV RTN@Y17XAUU
         BALR  R14,R15                 FIND NETWORK ADDRESS    @Y17XAUU
*                                                              @Y17XAUU
*        IEDIAP04 RETURNS NETWORK ADDRESS IN REG 15            @Y17XAUU
*                                                              @Y17XAUU
         STH   R15,TOTSRSID            SAVE NETWORK ADDRESS    @YM07278
         DROP  R6                                              @Y17XAUU
         EJECT
         L     R9,TRMDESTQ-1           QCB ADDRESS               S22024
         ST    R9,TRMQCB               SAVE QCB ADDRESS        @Y17XAUU
         DROP  R9                      TTE                       S22024
         USING IEDQQCB,R9              QCB                       S22024
         L     R9,QCBLGBAD-1           LGB ADDRESS               S22024
         DROP  R9                      QCB                       S22024
         USING IEDNLGB,R9              LGB                       S22024
         MVC   NAMESAVE(D8),LGBNAME    SAVE GROUP NAME         @Y17XAUU
         DROP  R9                      LGB                       S22024
         L     R1,TNTENTRY             GET TTE ADDRESS         @Y17XAUU
         BAL   R14,NCPTTE              GET NCP TTE ADDRESS     @Y17XAUU
*                                                              @Y17XAUU
*        ON RETURN R1 CONTAINS ADDRESS OF NCP TTE PREFIX       @Y17XAUU
*                                                              @Y17XAUU
         USING IEDNTRM,R1              SET TTE ADDRESSABILITY  @Y17XAUU
         L     R1,TRMDESTQ-D1          GET QCB ADDRESS         @Y17XAUU
         DROP  R1                                              @Y17XAUU
         USING IEDQQCB,R1              SET QCB ADDRESSABILITY  @Y17XAUU
         L     R9,QCBDCBAD-D1          GET 3705 DCB ADDRESS    @Y17XAUU
         USING IHADCB,R9               DCB                       S22024
         L     R3,DCBIOBAD             IOB ADDRESS               S22024
         SR    R6,R6                   CLEAR REG                 S22024
         IC    R6,DCBEIOBX             LCB SIZE                  S22024
         AR    R3,R6                   LCB OFFSET FROM DCB       S22024
         LA    R6,D32                  ADD ADJUSTMENT TO START   S22024
*                                      OF LCB                    S22024
         DROP  R9                                              @Y17XAUU
         SR    R3,R6                   STARTING LCB ADDRESS      S22024
         STCM  R3,ADRMSK,TOTSCLCB      SAVE RN LCB             @Y17XAUU
         BAL   R9,DUMMYTNT             CHECK FOR COUNTERFEITING@Y17XAUU
         LTR   R15,R15                 GOOD RETURN             @Y17XAUU
         BNZ   MOVEIN1                 ERROR EXIT              @Y17XAUU
         L     R5,GRABPARM             GET PARM ADDRESS          S22024
         USING GRABMAP,R5              GRAB PARM                 S22024
         CLI   $GRABCLS,SYMCLASS       CLASS 44                  S22024
         BNE   EIGRET                  NO BRANCH                 S22024
         CLI   $GRABFLG,NXTDEV         REQ FOR NEXT DEV IN LIST  S22024
         BE    LOADNAME                YES BRANCH                S22024
         CLI   $GRABFLG,SAMSAMSA       REQ FOR CH,CU,DV          S22024
         BNE   EIGRET                  NO BRANCH                 S22024
         CLC   TOTSCLCB,TOTPRLCB       LCB FOR PRI AND SEC SAME  S22024
         BNE     GRB001                NO BRANCH                 S22024
         B     LOADNAME                GO LOAD CDS               S22024
         EJECT
GRB008   EQU   *                                                 S22024
         TM    0(R8),TOTTDORN          TEST DEVICE ON RN         S22024
         BO    QCBFIND                 YES BRANCH              @Y17XAUU
         TM    0(R8),TOTTDTRM          TERMINAL TEST ON 0X       S22024
         BO    UCBFIND                 YES BRANCH                S99528
*        CHECK FOR VALID DEVICE
TNTRET   EQU   *                                                 S99528
         SPACE
         TM    0(R11),CLOMIT           WAS CLASS OMITTED         S99528
         BO    GRB010             BRANCH IF YES.
         SPACE
         CLC   18(1,R7),1(R11)         DOES CLASS COMPARE        S99528
         BNE   GRB002             BRANCH IF NO - GET NEXT ENTRY.
         SPACE
GRB010   TM    0(R11),TYOMIT      WAS TYPE OMITTED ?
         BO    GRB015             BRANCH IF YES.
         SPACE
         MVC   CLASS(1),19(R7)         UCB TYPE                  S99528
         NI    CLASS,X'0F'             CLEAR HI NIBBLE           S99528
         CLC   CLASS,CLAS02            CLASS 2702                S99528
         BNE   CLASS1                  NO BR                     S99528
         MVI   CLASS,X'02'             CLASSS TO 2702            S99528
         B     COMPCLAS                GO COMPARE CLASS          S99528
CLASS1   CLC   CLASS,CLAS01            CLASS 2701                S99528
         BNE   CLA7770                 NO BR                     S99528
         MVI   CLASS,X'01'             CLASS TO 2701             S99528
         B     COMPCLAS                GO COMPARE CLASS          S99528
CLA7770  CLC   CLASS,CLASS77           CLASS 7770                S99528
         BNE   COMPCLAS                NO BR                     S99528
         MVI   CLASS,X'04'             CLASS TO 04               S99528
COMPCLAS CLC   CLASS,2(R11)            DOES TYPE COMPARE         S99528
         BNE   GRB002             BRANCH IF NO - GET NEXT ENTRY.
         SPACE
GRB015   TM    0(R11),DCH+CH      IS ANY CHANNEL OK ?
         BZ    GRB025             BRANCH IF YES.
         SPACE
         CLC   4(1,R7),4(R10)     SAME CHANNEL AS PRIMARY ?
         BE    GRB020             BRANCH IF YES.
         SPACE
         TM    0(R11),DCH         WAS DIFFERENT CHNL REQUESTED ?
         BO    GRB025             BRANCH IF YES.
         B     GRB002             GET NEXT ENTRY.
         EJECT
         SPACE
GRB020   TM    0(R11),CH          WAS SAME CHNL REQUESTED ?
         BZ    GRB002             BRANCH IF NO - GET NEXT ENTRY.
         SPACE
GRB025   TM    0(R11),DCU+CU      IS ANY CONTROL UNIT OK ?
         BZ    GRB035             BRANCH IF YES.
         SPACE
         L     R5,0(R7)           GET UCB INFO.
         N     R5,GRB080          CLEAR ALL BUT CNTL UNIT BITS.
         L     R6,0(R10)          GET PRIM DEV UCB INFO.
         N     R6,GRB080          CLEAR ALL BUT CNTL UNIT BITS.
         CR    R5,R6              IS IT SAME CNTL UNIT ?
         BE    GRB030             BRANCH IF YES.
         SPACE
         TM    0(R11),DCU         WAS DIFFERENT CNTL UNIT REQUESTED.
         BO    GRB035             BRANCH IF YES.
         B     GRB002             GET NEXT ENTRY.
         SPACE
GRB030   TM    0(R11),CU          WAS SAME CNTL UNIT REQUESTED ?
         BZ    GRB002             BRANCH IF NO - GET NEXT ENTRY.
         SPACE
GRB035   TM    0(R11),DDV+DV      IS ANY DEVICE OK ?
         BZ    GRB045             BRANCH IF YES.
         SPACE
         L     R5,0(R7)           GET UCB INFO.
         N     R5,GRB081          CLEAR ALL BUT DEVICE BITS.
         L     R6,0(R10)          GET PRIM DEV UCB INFO.
         N     R6,GRB081          CLEAR ALL BUT DEVICE BITS.
         CR    R5,R6              IS IT THE SAME DEVICE ?
         BE    GRB040             BRANCH IF YES.
         SPACE
         TM    0(R11),DDV         WAS DIFFERENT DEVICE REQUESTED ?
         BO    GRB045             BRANCH IF YES.
         B     GRB002             GET NEXT ENTRY.
         SPACE
GRB040   TM    0(R11),DV          WAS SAME DEVICE REQUESTED ?
         BZ    GRB002             BRANCH IF NO - GET NEXT ENTRY.
         SPACE
GRB045   L     R11,TOTAVTPT            GET AVT ADDRESS         @Y17XAUU
         USING IEDQAVTD,R11            AVT ADDRESSIBILITY        S99528
         L     R11,AVTTCB              GGT TCB  ADDRESS          S99528
         DROP  R11                                               S99528
         L     R11,8(R11)              GET FIRST DEB POINTER     S99528
         USING DEBNMSUB,R11            CLEAR R10                 S99528
         SR    R10,R10                 GET NUMBER OF EXTENTS     S99528
NXTDEBNT IC    R10,DEBNMEXT            FROM DEB                  S99528
         LR    R12,R10                 SAVE                      S99528
         L     R5,DEBUCBAD             GET UCB ADDRESS           S99528
UCBCOMP  LA    R5,ZERO(R5)             CLEAR HI BYTE             S99528
         CR    R5,R7                   SAME UCB                  S99528
         BE    RLNCOMPT                YES BRANCH                S99528
         BCTR  R10,0                   DECREMENT EXTENT NUMBER   S99528
         LTR   R10,R10                 ZERO YET?                 S99528
         BZ    NEXTDEB                 YES BRANCH                S99528
         L     R5,DEBUCBAD+4           GET NEXT UCB ADDRESS      S99528
         B     UCBCOMP                 BRANCH                    S99528
         EJECT
NEXTDEB  L     R11,4(R11)              GET ADDRESS OF NEXT DEB   S99528
         LA    R11,ZERO(R11)           CLEAR HI BYTE             S99528
         LTR   R11,R11                 ADDRESS HERE              S99528
         BZ    NOTFOUND                NO BR                     S99528
         B     NXTDEBNT                BRANCH                    S99528
RLNCOMPT SR    R12,R10                 COMPUTE RLN               S99528
         STC   R12,TOTSCRLN            STORE RLN IN TOTSCENT     S99528
         STH   R5,TOTSCUCB             STORE UCB IN TOTSCENT     S99528
         STH   R5,ADDOFUCB             STORE UCB ADDRESS         S99528
         L     R10,DEBDCBAD            GET DCB ADDRESS           S99528
         USING IHADCB,R10                                        S99528
         L     R7,DCBIOBAD             GET DUMMY IOB ADDRESS     S99528
         DROP  R10                                               S99528
         LA    R6,32                   GET IOB DISPLACEMENT      S99528
         SR    R7,R6                   POINT TO DUMMY LCB        S99528
         MH    R12,LCBLENGT            FIND POINTER TO           S99528
         AR    R7,R12                  RIGHT LCB                 S99528
         TM    TOTTDTBL,TOTTDTRM       IS ENTRY TNT              S99528
         BO    NAMEBULD                YES BRANCH                S99528
         ST    R7,TOTSCQCB             SAVE LCB ADDRESS          S22024
         USING UCBOB,R5                                          S99528
         MVC   CDSLINE+FIVE(THREE),UCBNAME  NAME TO LOAD         S99528
         DROP  R5                                                S99528
         EJECT
*        CDS FOR GRABED DEVICE
LOADNAME EQU   *                                                 S99528
         MVC   BLDLNAM(D8),CDSLINE COPY SYMBOLIC NAME          @Y17XAUU
         BLDL  0,BLDLLIST         FIND CDS                     @Y17XAUU
         LTR   R15,R15            RECORD FOUND                 @Y17XAUU
         BZ    GC040              YES - GO LOAD IT             @Y17XAUU
         TM    TOTFLG10,TOTTERMS  TERMINAL ENTRY               @Y17XAUU
         BZ    GCLINK             NO - LINE                    @Y17XAUU
         SLR   R9,R9              CLEAR REG                    @Y17XAUU
         SLR   R8,R8              CLEAR REG                    @Y17XAUU
         IC    R9,TOTTTBEL        NAME LENGTH                  @Y17XAUU
         ICM   R8,ADRMSK7,TOTTDTBL+D1 TNT ADDRESS              @Y17XAUU
         AR    R8,R9              ADDRESS OF TTE ADDRESS       @Y17XAUU
         ICM   R8,ADRMSK7,D0(R8)  TTE ADDRESS                  @Y17XAUU
         LA    R9,TRMPRFSZ        TTE PREFIX SIZE              @Y17XAUU
         SLR   R8,R9              TTE PREFIX ADDRESS           @Y17XAUU
         USING IEDNTRM,R8         TTE PREFIX BASE              @Y17XAUU
         CLI   TRMTYPE,TRMLUNT    LU ENTRY                     @Y17XAUU
         BNE   CDSPRES            NO                           @Y17XAUU
         LH    R9,TRMCHCIN        DCT INDEX                    @Y17XAUU
         BCTR  R9,R0              ADJUST INDEX                 @Y17XAUU
         L     R6,TOTAVTPT        GET AVT ADDRESS              @Y17XAUU
         USING IEDQAVTD,R6        SET AVT ADDRESSABILITY       @Y17XAUU
         MH    R9,AVTDCTLN        OFFSET TO DCT ENTRY          @Y17XAUU
         A     R9,AVTCSTCS        DCT ENTRY ADDRESS            @Y17XAUU
         DROP  R6                                              @Y17XAUU
         USING IEDDCT,R9          SET DCT ADDRESSABILITY       @Y17XAUU
         TM    DCTBYTE1,DCT3270   SNA 3270 LU                  @Y17XAUU
         BO    CDSPRES            YES                          @Y17XAUU
         DROP  R9                                              @Y17XAUU
*        BUILD CDS RECORD FOR 3767 OR 3770                     @Y17XAUU
         L     R1,GRABPARM        GET GRAB PARAMETER           @Y17XAUU
         USING GRABMAP,R1         SET ADDRESSABILITY           @Y17XAUU
         SLR   R3,R3              CLEAR REG                    @Y17XAUU
         IC    R3,$GRABCNT        GET BUFFER SIZE              @Y17XAUU
         L     R5,$GRABCDS        GET BUFFER ADDRESS           @Y17XAUU
         LA    R15,0              SET GOOD RETURN CODE         @Y17XAUU
         BCTR  R3,0               ADJUST BYTES COUNT           @Y17XAUU
         EX    R3,SNACDS          MOVE CDS TO BUFFER           @Y17XAUU
         DROP  R1                                              @Y17XAUU
         USING IEDTCDSD,R5        SET CDS ADDRESSABILTITY      @Y17XAUU
         MVC   TCDSNAM(D8),CDSLINE  SET SYMBOLIC NAME          @Y17XAUU
         SLR   R1,R1              CLEAR REG                    @Y17XAUU
         ICM   R1,3,TOTSCUCB      GET UCB ADDRESS              @Y17XAUU
         USING UCB,R1             SET UCB ADDRESSABILITY       @Y17XAUU
         MVC   TCDSUCB+D2(D2),UCBCHA  SET UCB BINARY CUU ADDR  @Y17XAUU
         DROP  R1                                              @Y17XAUU
         B     NODELETE           EXIT                         @Y17XAUU
         SPACE
GCLINK   DS    0H                 BOUNDARY ALLIGNMENT          @Y17XAUU
*        CONSTRUCT 3705 LINE CDS                               @Y17XAUU
         L     R1,GRABPARM        POINT TO GRAB PLIST          @Y17XAUU
         USING GRABMAP,R1                                      @Y17XAUU
         L     R5,$GRABCDS        BUFFER ADDRESS               @Y17XAUU
         SLR   R3,R3              CLEAR REG                    @Y17XAUU
         IC    R3,$GRABCNT        GET BUFFER SIZE              @Y17XAUU
         LA    R15,0              SET GOOD RETURN CODE         @Y17XAUU
         LA    R8,SNALCDS         GET FIXED LINE CDS FORMAT    @Y17XAUU
         BCTR  R3,0               ADJUST BYTE COUNT            @Y17XAUU
         EX    R3,SNACDS          MOVE CDS TO BUFFER           @Y17XAUU
         MVC   TCDSNAM(D8),CDSLINE  SET SYMBOLIC NAME          @Y17XAUU
         DROP  R1                                              @Y17XAUU
         SLR   R1,R1              CLEAR REG                    @Y17XAUU
         ICM   R1,3,TOTSCUCB      GET UCB ADDRESS              @Y17XAUU
         USING UCB,R1             SET UCB ADDRESSABILITY       @Y17XAUU
         MVC   TCDSUCB+D2(D2),UCBCHA SET UCB BINARY CUU ADDR   @Y17XAUU
         DROP  R1                                              @Y17XAUU
         MVC   TCDSDAF(D2),TOTSRSID SET NETWORK ADDR IN CDS    @Y17XAUU
         B     NODELETE           EXIT                         @Y17XAUU
         SPACE
CDSPRES  DS    0H                 BOUNDARY ALLIGNMENT          @Y17XAUU
         LA    R15,RETDAF         SET RETURN CODE FOR CDS NOT  @Y17XAUU
*                                 FOUND AND NETWORK ADDR RETURN@Y17XAUU
         SLR   R0,R0              CLEAR REG                    @Y17XAUU
         ICM   R0,3,TOTSRSID      SET UP NETWORK ADDRESS       @Y17XAUU
         B     NODELETE           EXIT
         SPACE
         DROP  R8                                              @Y17XAUU
GC040    DS    0H                 BOUNDARY ALLIGNMENT          @Y17XAUU
         LOAD  EPLOC=CDSLINE           BRING MEMBER IN CORE      S99528
         LR    R6,R0                   EP OF CDS                 S99528
         L     R5,GRABPARM             GET PARM POINTER          S99528
         L     R7,FOUR(R5)             GET CDSMOVE ADDRESS       S99528
         L     R5,EIGHT(R5)            POINT TO FLAGS            S99528
         N     R5,CDSNO                CLEAR ALL BUT NOS BYTES   S99528
         LTR   R7,R7                   CDS ADDRESS GIVEN         S99528
         BZ    DDMOVE                  NO BR                     S99528
         LTR   R5,R5                    NUMBER GIVEN           @XA06343
         BP    CDSMV                   YES BR                    S99528
         LA    R5,TEN                  ASSUME DEFULT             S99528
CDSMV    EX    R5,CDSMOVE              GO MOVE CDS               S99528
DDMOVE   EQU   *                       FIND DD ENTRY             S99528
         DELETE EPLOC=CDSLINE          REMOVE CDS                S22024
NODELETE DS    0H                                              @Y17XAUU
         ICM   R9,ADRMSK,TOTSCLCB      GET LCB ADDRESS         @Y17XAUU
         LTR   R9,R9                   IS THTER ONE              S22024
         BNZ   GOOD00                  YES BRANCH                S22024
         SR    R9,R9                   CLEAR                   @G36XRUV
         ICM   R9,HALF,TOTSCUCB        GET UCB ADDRESS         @G36XRUV
         USING UCB,R9                                            S99528
         TM    UCBTBYT3,UCB3DISP       GRAGHIC                   S99528
         BO    GRAPHIC                 YES BRANCH                S99528
         NI    EXCPDCB+50,X'DF'        NO DEV DEPENDENT          S99528
         DROP  R9                                                S99528
GRAPHIC  LA    R9,CVTPTR               GET CVT ADDRESS           S99528
         L     R9,0(R9)                CVT ADDRESS               S99528
         L     R9,0(R9)                TCBPOINTERS ADDRESS       S99528
         L     R9,4(R9)                CURRENT TCB ADDRESS       S99528
         L     R9,12(R9)               TIOT POINTER              S99528
         USING TIOT,R9                                           S99528
         LA    R9,TIOENTRY             POINT TO FIRST ENTRY      S99528
         DROP  R9                                                S99528
         USING TIOENTRY,R9                                       S99528
         EJECT
GETLENGT SR    R3,R3                   CLEAR REG                 S99528
         IC    R3,TIOELNGH             DD ENTRY LENGTH           S99528
         LTR   R3,R3                   ANY LENGTH                S99528
         BZ    NOTFOUND                NO BRANCH                 S99528
         LA    R5,POOLSTAR-TIOESTTB    GET LENGTH OF DEVICE ENTRYS99528
         DROP  R9                                                S99528
         LR    R6,R3                   SAVE LENGTH               S99528
         SR    R3,R5                   DD LENGTH -4              S99528
         AR    R3,R9                   DEVICE                    S99528
         USING TIOESTTB,R3                                       S99528
         CLC   TIOEFSRT+1(2),TOTSCUCB  COMPARE UCB ADDRESSES FOR S99528
         DROP  R3                                                S99528
         BE    UCBFOUND                BRANCH WHEN FOUND         S99528
         AR    R9,R6                   BUMP TO NEXT DD ENTRY     S99528
         B     GETLENGT                BRANCH                    S99528
NOTFOUND EQU   *                                                 S99528
          LA    R15,EIGHT              RETURN CODE               S99528
         B     GRB070                  RETURN                    S99528
GOOD00   EQU   *                       ZERO                      S22024
         SR    R15,R15                 RETURN                    S22024
         B     GRB070                  CODE                      S22024
UCBFOUND EQU   *                                                 S99528
         USING TIOENTRY,R9             TIOT ENTRY BASE
COMPAREN EQU   *                                                 S99528
         CLI   TIOEDDNM,X'40'          IS NAME FIELD BLANK?      S99528
         BNE   MOVEIN                  NO, GO MOVE DDNAME        S99528
         SR    R9,R6                   SET ENTRY TO PRECEDING ONES99528
         B     COMPAREN                GO LOOK FOR NAME          S99528
MOVEIN   EQU   *                                                 S99528
         MVC   EXCPDCB+40(8),TIOEDDNM  MOVE DDNMME TO DCB        S99528
         DROP  R9
         LA    R6,EXCPDCB         GET DCB ADDRESS              @Y17XAUU
         RDJFCB (R6)                                           @Y17XAUU
         MVI   JFCBAA+53,X'00'         CLEAR                     S99528
         MVC   JFCBAA+54(12),JFCBAA+53 UNUSED                    S99528
         MVI   JFCBAA+67,X'00'         SECTION                   S99528
         MVC   JFCBAA+68(12),JFCBAA+67 OF                        S99528
         MVI   JFCBAA+88,X'00'         THE                       S99528
         MVC   JFCBAA+89(29),JFCBAA+88 JFCB                      S99528
         MVI   JFCBAA+148,X'00'        FOR                       S99528
         MVC   JFCBAA+149(25),JFCBAA+148 TOTE                    S99528
         MVI   JFCBAA+52,X'08'         PREVENT                   S99528
         MVI   JFCBAA+66,X'02'         REVERSE MERGE             S99528
         MVC   TOTSCDCB(72),EXCPDCB    MOVE DCB TO OLTCB         S99528
         LA    R1,TOTSCDCB             DCB ADDRESS               S99528
         ST    R1,OPENAREA             STORE IN P LIST           S99528
         MVI   OPENAREA,X'8F'          OPTION OPEN               S99528
         LA    R1,OPENAREA             ADDRESS OF P LIST         S99528
         OPEN  MF=(E,(1)),TYPE=J       SECONAARY DCB             S99528
         EJECT
         TM    TOTSCDCB+48,X'10'       SUCCESSFUL OPEN?          S99528
         BNO   MOVEIN1                 BRANCH IF ERROR ON OPEN   S99528
         L     R3,TOTSCDEB             GET ADDRESS OF DEB        S99528
         LA    R3,D34(R3)              GET ADDRESS OF UCB IN DEB S99528
         ST    R3,TOT102A              STORE ADDRESS INTO SVC    S99528
*                                        102 PARAMETER LIST      S99528
         LA    R1,TOT102               LOAD ADDRESS OF PARM LIST S99528
*                                        INTO REG 1              S99528
         AQCTL                         ISSUE DATA MOVE SVC       S99528
         B     GRB070                  GOOD EXIT                 S99528
MOVEIN1  EQU   *                                                 S99528
         LA    R15,EIGHT               SET BAD RETURN CODE       S99528
         SPACE
GRB070   EQU   *                       EXIT                      S99528
         STC   R15,TOTRTCOD            RETURN CODE TO OLTCB      S99528
         TM    $OLTFLGS,$TRACE         TRACE OPTION              S99528
         BO    M2                      YES BR                    S99528
         TM    $RETMASK,X'FF'          RETURN OPTION             S99528
         BZ    RETURN                  NO BR                     S99528
         CLI   TOTRTCOD,0              RETURN CODE ZERO          S99528
         BNE   M2                      NO BR                     S99528
RETURN   L     R13,4(R13)              CALLERS SAVE ADDRESS      S99528
         L     R14,12(R13)        RESTORE R14.
         LM    R0,R12,20(R13)     RESTORE R0-R12.
         BR    R14                RETURN TO CALLER.
M2       EQU   *                       GO TO IEDQWM2             S99528
         L     R13,4(R13)              RESTORE R13               S99528
         L     R14,12(R13)             RESTORE R14               S99528
         USING RESPL,R15               SET RESPL ADDRESSABILITY@Y17XAUU
         L     R15,TOTRESPL            GET RESPL ADDRESS       @Y17XAUU
         L     R15,RESPLM2             GET TRACE ROUTINE ADDR  @Y17XAUU
         LM    R2,R12,DISREG2(R13)     RESTORE REGISTERS       @Y17XAUU
         BR    R15                     EXIT TO IEDQWM2         @Y17XAUU
         DROP  R15                                             @Y17XAUU
         EJECT
NAMEBULD EQU   *                                                 S99528
         MVI   CDSLINE,C' '            BLANK OUT                 S99528
         MVC   CDSLINE+1(7),CDSLINE    EIGHT CHARACTER NAME      S99528
         L     R12,TRMTNT              GET SAVED TNT ADDRESS   @Y17XAUU
         L     R9,CDSLINE              GET ADDRESS OF NAME FIELD S99528
         SR    R3,R3                   CLEAR REG                 S99528
         IC    R3,TOTTTBEL             GET NAME LRNGTH           S99528
         BCTR  R3,0                    DECREMENT COUNT FOR EX    S99528
         EX    R3,NAMEMOVE             GO MOVE NAME              S99528
         B     LOADNAME                GO LOAD NAME            @Y17XAUU
         SPACE 2
NAMEMOVE MVC   0(0,R9),0(R12)          MOVE NAME FROM TERMINAL TAS99528
         EJECT
***   THIS CODE FINDS UCB ADDRESS IF GRAB IS FOR A TERMINAL      S99528
UCBFIND  EQU   *                                                 S99528
         ST    R7,TRMTNT               SAVE TNT ADDRESS        @Y17XAUU
         SR    R6,R6                   CLEAR REG                 S99528
         IC    R6,TOTTTBEL             GET TERMINAL NAME LENGTH  S99528
         AR    R5,R6                   GET TNT ADDRESS           S99528
         MVC   TNTENTRY+D1(D3),D0(R6)  ALLINE ADDRESS          @Y17XAUU
         L     R7,TNTENTRY             GET TTE ADDRESS         @Y17XAUU
         USING IEDQTRM,R7                                        S99528
         L     R7,TRMDESTQ-1           GET QCB ADDRESS           S99528
         DROP  R7                                                S99528
         USING IEDQQCB,R7                                        S99528
         SR    R3,R3                   CLEAR REG                 S99528
         IC    R3,QCBRELLN             GET RLN                   S99528
         SLL   R3,2                    MULT BY 4                 S99528
         L     R7,QCBDCBAD-1           GET DCB ADDRESS           S99528
         DROP  R7                                                S99528
         USING IHADCB,R7                                         S99528
         L     R7,DCBDEBAD             GET DBE ADDRESS           S99528
         DROP  R7                                                S99528
         USING DEBNMSUB,R7                                       S99528
         SR    R15,R15                 CLEAR REG                 S99528
         IC    R15,DEBNMEXT            GET NUMBER OF EXTENTS     S99528
         SLL   R15,2                   MULT BY 4                 S99528
         L     R7,DEBUCBAD-4(R3)       GET UCB ADDRESS           S99528
         B     TNTRET                  BRANCH                    S99528
         EJECT
************************************************************** @Y17XAUU
*        ROUTINE TO FIND NCP TTE ADDRESS. INPUT: REG 1 =     * @Y17XAUU
*        TERMINAL TTE ADDRESS. OUTPUT: REG 1 = NCP TTE PRFIX * @Y17XAUU
*        ADDRESS. RETURN TO CALLER VIA REG 14.                 @Y17XAUU
************************************************************** @Y17XAUU
NCPTTE   EQU   *                                               @Y17XAUU
         ST    R14,SAVERET             SAVE RETURN ADDRESS     @Y17XAUU
         LA    R8,TRMPRFSZ             GET TTE PREFIX SIZE     @Y17XAUU
         SLR   R1,R8                   POINT TO TTE PREFIX     @Y17XAUU
         USING IEDNTRM,R1              SET TTE ADDRESSABILITY  @Y17XAUU
NCPLOOP  EQU   *                                               @Y17XAUU
         LH    R1,TRMCOHRT             GET COHORT POINTER      @Y17XAUU
         L     R15,TOTAVTPT            GET AVT ADDRESS         @Y17XAUU
         USING IEDQAVTD,R15            SET AVT ADDRESSABILITY  @Y17XAUU
         L     R15,AVTRNMPT            TTCIN TO TTE CONVERSION @Y17XAUU
*                                      ROUTINE ADDRESS         @Y17XAUU
         DROP  R15                                             @Y17XAUU
         BALR  R14,R15                 FIND TTE ADDRESS        @Y17XAUU
         SLR   R1,R8                   POINT TO TTE PREFIX     @Y17XAUU
         CLI   TRMTYPE,TRMLNCP         NCP ENTRY               @Y17XAUU
         BNE   NCPLOOP                 NO - CHECK NEXT ENTRY   @Y17XAUU
         L     R14,SAVERET             RESTORE RETURN ADDRESS  @Y17XAUU
         BR    R14                     RETURN TO CALLER        @Y17XAUU
         DROP  R1                                              @Y17XAUU
         EJECT
***************************************************************@Y17XAUU
*        ROUTINE TO GO TO IEDQWB2 TO CALCULATE THE AMOUNT OF   @Y17XAUU
*        OF CORE NEEDED TO BUILD DUMMY ENTRIES FOR A SPECIFIED @Y17XAUU
*        REMOTE TERMINAL. CONTROL IS PASS TO IEDQWB TO ATTAIN  @Y17XAUU
*        THE ACTUAL CORE.  CONTROL IS THEN RETURNED TO IEDQWB2 @Y17XAUU
*        TO COPY THE ENTRIES.                                  @Y17XAUU
***************************************************************@Y17XAUU
DUMMYTNT EQU   *                                               @Y17XAUU
         STM   R7,R11,TRMSAVE2    SAVE REGISTERS               @Y17XAUU
         SLR   R15,R15            CLEAR RETURN CODE REG        @Y17XAUU
         L     R8,TNTENTRY        GET TTE ADDRESS              @Y17XAUU
         LA    R7,TRMPRFSZ        GET TTE PREFIX SIZE          @Y17XAUU
         SLR   R8,R7              POINT TO TTE PREFIX          @Y17XAUU
         USING IEDNTRM,R8         SET TTE ADDRESSABILITY       @Y17XAUU
         CLI   TRMTYPE,TRMLUNT    LU ENTRY                     @Y17XAUU
         BNE   DUMOUT             NO - RETURN TO CALLER        @Y17XAUU
         MVI   TRMRQFLG,SCREQ     SHOW SEC TD REQUEST          @Y17XAUU
         LA    R12,KTRM           PASS PARAMETERS              @Y17XAUU
         XC    TRMFLG01(D1),TRMFLG01 CLEAR CONTROL FLAG        @Y17XAUU
*                                                              @Y17XAUU
         LINK  EP=IEDQWB2         CALCULATE CORE               @Y17XAUU
         LTR   R15,R15            REQUEST SUCCESSFUL           @Y17XAUU
         BNZ   DUMOUT             NO - ERROR                   @Y17XAUU
*                                                              @Y17XAUU
         CLI   TRMRQFLG,ALRDY     COUNTERFEITING COMPLETE      @Y17XAUU
         BNE   DUM08              NO CONTINUE PROCESSING       @Y17XAUU
         NI    TOTSCFLG,TOTSCTAS  RESET ASSIGNED FLAG          @Y17XAUU
         B     DUMOUT             RETURN TO CALLER             @Y17XAUU
DUM08    EQU   *                                               @Y17XAUU
         L     R10,TOTRESPL       ADDR OF TOTE RES. CTL BLK    @Y17XAUU
         USING RESPL,R10                                       @Y17XAUU
         XC    TOTOTECB(D4),TOTOTECB CLEAR ECB                 @Y17XAUU
         POST  RESTECB1           POST REQUEST TO IEDQWB       @Y17XAUU
         WAIT  ECB=TOTOTECB       WAIT FOR REQUEST TO BE HONORE@Y17XAUU
         DROP  R10                                             @Y17XAUU
*                                                              @Y17XAUU
         CLI   TOTWKSPC,XFF       REQUEST SUCCESSFUL           @Y17XAUU
         BE    DUMOUT             NO - ERROR                   @Y17XAUU
*                                                              @Y17XAUU
         OI    TRMFLG01,TRMQWB3   SHOW SECOND PASS             @Y17XAUU
         LA    R12,KTRM           PASS PARAMETERS              @Y17XAUU
         LINK  EP=IEDQWB2         COPY ENTRIES                 @Y17XAUU
*                                                              @Y17XAUU
         OI    TOTFLG04,X'04' .TOTSCCTF ********************** @YM08057
DUMOUT   EQU   *                                               @Y17XAUU
         LM    R7,R11,TRMSAVE2    RESTORE REGISTERS            @Y17XAUU
         BR    R9                 RETURN                       @Y17XAUU
         EJECT
*
*        $LETGO ENTRY
*
LET002   EQU   *
         L     R5,4(R1)                GET UNIT                  S99528
         L     R5,0(R5)                ADDRESS FROM CDS          S99528
         STH   R5,UCBNAMES             SAVE UNIT NAME            S99528
         L     R9,CVTPTR               GET CVT ADDRESS           S99528
         USING CVT,R9                                            S99528
         L     R11,CVTILK2             GET ADDR OF UCB ADDR TABLES99528
*                                      UCB LOOK UP TABLE         S99528
         DROP  R9                                                S99528
DISP083  EQU   *                                                 S99528
         LH    R10,0(R11)              GET FIRST UCB ADDRESS     S99528
         LTR   R10,R10                 TEST ENTRY                S99528
         BZ    DISP085                 ZERO ENTRY,GET NEXT       S99528
         BP    GOODUCB                 NO HI ORDER BITS ON     @G36XRUV
         N     R10,CLRHALF             CLEAR HIGH ORDER        @G36XRUV
         CLM   R10,HALF,TABEND         END OF TABLE ?          @G36XRUV
         BE    ERROR                   YES - END OF TABLE      @G36XRUV
         USING UCB,R10                                           S99528
GOODUCB  DS    0H                                              @G36XRUV
         CLC   UCBCHA(2),UCBNAMES      CORRECT ENTRY?            S99528
         BE    FOUND                   YES BRANCH                S99528
DISP085  EQU   *                                                 S99528
         LA    R11,2(R11)              GET NEXT ENTRY            S99528
         B     DISP083                 BR -LOOP                  S99528
FOUND    L     R9,TOTTDEND             GET END OF TABLE          S99528
         L     R11,TOTTDTBL            GET START OF DEVICE TABLE S99528
LOOP     DS    0H                                              @G36XRUV
         SR    R12,R12                 CLEAR                   @G36XRUV
         ICM   R12,HALF,2(R11)         GET UCB ADDRESS         @G36XRUV
         CR    R12,R10                 UCB ADDRESSESTHESAME      S99528
         BE    FOUND2                  YES BRANCH                S99528
         LA    R11,4(R11)              POINT TO NEXT ENTRY       S99528
         CR    R9,R11                  ENDOFLIST                 S99528
         BE    ERROR                   YESBRANCH                 S99528
         B     LOOP                    BRANCH                    S99528
FOUND2   EQU   *                                                 S99528
         TM    0(R11),TOTTDGRB         BEEN PREVIOUSLY GRABED    S99528
         BO    LET008                  YES BRANCH                S99528
         LA    R15,X'0C'               SET RETURN CODE           S99528
         B     GRB070                  RETURN                    S99528
         EJECT
LET008   EQU   *                                                 S99528
         SR    R9,R9                   CLEAR                   @G36XRUV
         ICM   R9,HALF,TOTSCUCB        SECONDARY UCB ADDRESS   @G36XRUV
         USING UCB,R9                  UCB                       S99528
         MVC   RNAME(THREE),UCBNAME    NAME TO DEQ               S99528
         DROP  R9                      UCB                       S99528
         DEQ   ,MF=(E,DEQLIST)         DEQ UCB NAME              S99528
         LA    R1,TOTSCDCB             GET SECONDARY DCB         S99528
         ST    R1,OPENAREA             STORE IN P LIST           S99528
         MVI   OPENAREA,X'B0'          OPTION CLOSE              S99528
         LA    R1,OPENAREA             ADDRESS OF P LIST         S99528
         CLOSE MF=(E,(1))              CLOSE SECONDARY DCB       S99528
*
LET010   EQU   *
         SR    R15,R15            SET RETURN CODE TO 0
         LR    R8,R11                  SAVE LET GO ENTRY ADDR    S99528
         LA    R6,TOTTDEND             GET END OF TABLE ADDRESS  S99528
         LA    R11,4(R11)              SET UP TO                 S99528
         SR    R6,R11                  DETERMINE                 S99528
         BCTR  R6,ZERO                 DECREMENT FOR MOVE        S99528
         EX    R6,MOVE                 GO MOVE                   S99528
          XC    TOTLETGO(4),TOTLETGO   CLEAR LAST ENTRY          S99528
         L     R7,TOTGRABP             GET GRAB POINTER          S99528
         LA    R9,FOUR                 BACK UP POINTER           S99528
         SR    R7,R9                   BY FOUR                   S99528
         ST    R7,TOTGRABP             SAVE NEW ADDRESS          S99528
         B     GRB070             EXIT TO RETURN
ERROR    LA    R15,X'0C'               SET RETURN CODE           S99528
         B     GRB070                  BRANCH                    S99528
         EJECT
         DS    0F
CLRHALF  DS    0F                      CLEAR HALF WORD         @G36XRUV
         DC    X'0000FFFF'                                     @G36XRUV
TABEND   DC    X'FFFF'                 END OF UCB TAB COMPARAND@G36XRUV
QLDLLIST DS    0F                 BLDL P-LIST                  @Y17XAUU
         DC    XL2'0001'          NUMBER OF LIST ENTRIES       @Y17XAUU
         DC    XL2'003A'          LENGTH OF EACH ENTRY         @Y17XAUU
         DC    8XL1'40'           MODULE ID                    @Y17XAUU
         DC    XL16'0'            ADDITIONAL DIRECTORY INFO    @Y17XAUU
QORENEED DC    XL3'0'             MAIN STORAGE NEEDED          @Y17XAUU
         DC    XL31'0'            ADDITIONAL DIRECTORY INFO    @Y17XAUU
WORKA    DC    X'000F'            USED FOR CONVERSION          @Y17XAUU
GRB080   DC    X'00F00000'        MASK FOR ISOLATING CNTL UNIT BITS.
GRB081   DC    X'000F0000'        MASK FOR ISOLATING DEVICE BITS.
MOVE     MVC   0(0,R8),0(R11)          MOVE REST OF TABLE UP     S99528
SNACDS   MVC   ZERO(ZERO,R5),ZERO(R8) CDS TO OLT               @Y17XAUU
SNATCDS  DC    XL4'0',XL2'0',XL4'4420180A',XL2'0',CL8' ',XL4'0'
*                                       BASIC 3767/3770 CDS    @Y17XAUU
SNALCDS  DC    XL4'0',XL2'0',XL4'4420240A',XL2'0',CL8' ',X'10',XL3'0',X*
               L4'0',X'E0',XL2'0',XL5'0'
*                                 BASIC 3705 LINE CDS          @Y17XAUU
PATCH    DS    CL50               MAINTENANCE AREA.
         SPACE
DCH      EQU   X'80'              DIFFERENT CHANNEL.
CH       EQU   X'40'              SAME CHANNEL.
DCU      EQU   X'20'              DIFFERENT CONTROL UNIT.
CU       EQU   X'10'              SAME CONTROL UNIT.
DDV      EQU   X'08'              DIFFERENT DEVICE.
DV       EQU   X'04'              SAME DEVICE.
CLOMIT   EQU   X'02'              CLASS OMITTED.
TYOMIT   EQU   X'01'              TYPE OMITTED.
         EJECT
START    DS    0F                 ***** START OF MODIFIABLE    @Y17XAUU
* DATA AREA -- DO NOT INSERT RECORD BETWEEN ***                @Y17XAUU
         DC    X'07'                   EXIT ADDR LIST          @Y17XAUU
         DC    AL3(0)                  JFCB ADDRESS            @Y17XAUU
         DC    X'80'                   END                       S99528
         DC    AL3(0)                  FILLER                    S99528
*                                                                S99528
**************************************************************** S99528
*        PARAMETER LIST FOR DATA MOVE                          * S99528
**************************************************************** S99528
         DS    0F                      ALIGNMENT               @Y17XAUU
         DC    X'08'                   FLAG FOR DATA MOVE        S99528
         DC    AL3(0)                  DATA ADDRESS            @Y17XAUU
         DC    X'00'                   FLAGS                   @Y17XAUU
         DC    AL3(0)                  TARGET ADDRESS            S99528
         DC    X'80'                   LAST WORD FLAG            S99528
         DC    AL3(0)                  ADDRESS OF LENGTH FIELD @Y17XAUU
         DC    H'2'                    LENGTH OF UCB ADDRESS MO@Y17XAUU
*                                                                S99528
         DC    H'0'                    ADDRESS OF UCB          @Y17XAUU
         DCB   DDNAME=DD274E,DSORG=PS,EXLST=*,MACRF=(E),       @Y17XAUUC
               SIOA=AO,CENDA=AP,XENDA=AP                         S99528
         SPACE
         DS    0F                                              @Y17XAUU
         DC    X'A0'                   FLAG BYTE               @Y17XAUU
         DC    XL3'0'             DEB ADDR
         DC    F'0'                    PURGE USE                 S99528
         DS    0F                 ADDR OF IOB FOR PURGE        @Y17XAUU
         DC    X'01'                   NUMBER OF BEDS          @Y17XAUU
         DC    XL3'0'             IOB ADDR
         DEQ   (QQAME,RRAME,8,STEP),RET=HAVE,MF=L DEQ PLIST    @Y17XAUU
QQAME    DC    CL8'SYSZTOTE'           QNAME ADDRESS           @Y17XAUU
RRAME    DC    CL8' '                  RNAME ADDRESS           @Y17XAUU
         DC    CL8'G0000000'           BUILD CDS NAME          @Y17XAUU
         DC    AL4(0)                  PARM SAVE AREA          @Y17XAUU
END      DS    0C                 *** END OF MODIFIABLE DATA   @Y17XAUU
***                               AREA TO BE MOVE***           @Y17XAUU
         EJECT
LCBLENGT DC    X'0090'                 LCB LENGTH                S99528
CDSMOVE  MVC   ZERO(ZERO,R7),ZERO(R6)  CDS TO OLT                S99528
CDSNO    DC    X'000000FF'             MASK # CDS BYTES          S99528
CLASS    DC    X'00'                   CLASS BYTE                S99528
CLAS02   DC    X'01'                   2702                      S99528
CLAS01   DC    X'02'                   2701                      S99528
CLASS77  DC    X'00'                   7770                      S99528
NAMEFLD  DC    CL8'G0000000'           INIT NAME FIELD         @Y17XAUU
GRABREQ  DC    CL2'21'                 GRAB FUNCTION REQ       @Y17XAUU
         EJECT
         OLTCB                         TOTE CONTROL BLOCK        S22024
JFCBADDR DC    X'07'                   EXIT ADDR LIST          @Y17XAUU
         DC    AL3(0)                  JFCB ADDRESS            @Y17XAUU
         DC    X'80'                   END                     @Y17XAUU
         DC    AL3(0)                  FILLER                  @Y17XAUU
*                                                              @Y17XAUU
****************************************************           @Y17XAUU
*        PARAMETER LIST FOR DATA MOVE               *          @Y17XAUU
****************************************************           @Y17XAUU
TOT102   DS    0F                      ALIGNMENT               @Y17XAUU
         DC    X'08'                   FLAG FOR DATA MOVE      @Y17XAUU
         DC    AL3(0)                  DATA ADDRESS            @Y17XAUU
TOT102A  DC    X'00'                   FLAGS                   @Y17XAUU
         DC    AL3(0)                  TARGET ADDRESS          @Y17XAUU
         DC    X'80'                   LAST WORD FLAG          @Y17XAUU
         DC    AL3(0)                  ADDRESS OF LENGTH FIELD @Y17XAUU
TOT102LN DC    H'2'                    LEN OF UCB ADDRESS      @Y17XAUU
*                                                              @Y17XAUU
ADDOFUCB DC    H'0'                    ADDRESS OF UCB          @Y17XAUU
EXCPDCB  DCB   DDNAME=DD274E,DSORG=PS,EXLST=JFCBADDR,MACRF=(E),        C
               SIOA=AO,CENDA=AP,XENDA=AP                       @Y17XAUU
QVPRGLST DS    0F                                              @Y17XAUU
QVPURFLG DC    X'A0'                   FLAG BYTE               @Y17XAUU
         DC    XL3'0'             DEB ADDR                     @Y17XAUU
         DC    F'0'                    PURGE USE               @Y17XAUU
QVPRGIOB DS    0F                 ADDR OF IOB FOR PURGE        @Y17XAUU
QVPDEBNO DC    X'01'                   NUMBER OF BEDS          @Y17XAUU
         DC    XL3'0'             IOB ADDR                     @Y17XAUU
DEQLIST  DEQ   (QNAME,RNAME,8,STEP),RET=HAVE,MF=L DEQ PLIST    @Y17XAUU
QNAME    DC    CL8'SYSZTOTE'           QNAME ADDRESS           @Y17XAUU
RNAME    DC    CL8' '                  RNAME ADDRESS           @Y17XAUU
CDSLINE  DC    CL8'G0000000'           BUILD CDS NAME          @Y17XAUU
GRABPARM DC    AL4(0)                  PARM SAVE AREA          @Y17XAUU
OPENAREA DS    F                  OPEN CLOSE PARM              @Y17XAUU
TNTPOINT DS    F                  TNT ADDRESS                  @Y17XAUU
JFCBAA   DS    44F                JFCB                         @Y17XAUU
SAVERET  DS    F                  SAVE AREA                    @Y17XAUU
NAMESAVE DS    CL8                SECONDARY TD NAME            @Y17XAUU
BLDLLIST DS    0F                 BLDL P-LIST                  @Y17XAUU
         DC    XL2'0001'          NUMBER OF LIST ENTRIES       @Y17XAUU
         DC    XL2'003A'          LENGTH OF EACH ENTRY         @Y17XAUU
BLDLNAM  DC    8XL1'40'           MODULE ID                    @Y17XAUU
         DC    XL16'0'            ADDITIONAL DIRECTORY INFO    @Y17XAUU
CORENEED DC    XL3'0'             MAIN STORAGE NEEDED          @Y17XAUU
         DC    XL31'0'            ADDITIONAL DIRECTORY INFO    @Y17XAUU
BLDLEND  DS    0C                 END OF BLDL PLIST            @Y17XAUU
KTRM     DS    0F                 ***DO NOT INSERT BETWEEN *** @Y17XAUU
TERNMTAB DS    A                  TERMNAME TABLE ADDRESS       @Y17XAUU
TNTENTRY DS    A                  TERMNAME ENTRY ADDRESS       @Y17XAUU
TRMQCB   DS    A                  CT/AP QCB ADDRESS            @Y17XAUU
TRMTNT   DS    A                  CT/AP TNT ADDRESS            @Y17XAUU
TRMSAVE1 DS    3F                 REGISTERS SAVE AREA          @Y17XAUU
TRMSAVE2 DS    5F                 REGISTERS SAVE AREA          @Y17XAUU
TRMSAV1  DS    3F                 REGISTERS SAVE AREA          @Y17XAUU
REGSAVE  DS    F                  WORK AREA                    @Y17XAUU
F32      DC    F'32'              VALUE OF 32                  @Y17XAUU
KPQCB    DS    A                  TRUE P-QCB ADDRESS           @Y17XAUU
KMQCB    DS    A                  DUMMY M-QCB ADDRESS          @Y17XAUU
CONCTTE  DS    A                  TRUE CONC TERM TTE ADDRESS   @Y17XAUU
KDRQ     DS    A                  TRUE DRQ ADDRESS             @Y17XAUU
KPQCBSZ  DS    H                  P-QCB SIZE                   @Y17XAUU
KMQCBSZ  DS    H                  M-QCB SIZE                   @Y17XAUU
KDRQSZ   DS    H                  DRQ SIZE                     @Y17XAUU
TTETSZ   DS    CL1                CONC TERM TTE SIZE           @Y17XAUU
TTECSZ   DS    CL1                CONC TTE SIZE                @Y17XAUU
TRMRQFLG DS    CL1                DUMMY TNT REQUEST FLAG       @Y17XAUU
CTREQ    EQU   X'80'              CT REQUEST                   @Y17XAUU
APREQ    EQU   X'40'              AP REQUEST                   @Y17XAUU
PRREQ    EQU   X'20'              PRIMARY TD REQUEST           @Y17XAUU
SCREQ    EQU   X'10'              SECONDARY TD REQUEST         @Y17XAUU
ALRDY    EQU   X'00'              COUNTERFEIT STORAGE ALREADY  @Y17XAUU
*                                 EXIST FOR THIS REQUEST       @Y17XAUU
TRMFLG01 DS    CL1                CONTROL FLAG                 @Y17XAUU
CONC     EQU   X'80'              TERMINAL ATTACHED TO CONC    @Y17XAUU
TRMSPAS  EQU   X'40'              PROCESS CONC TTE             @Y17XAUU
TRMQWB3  EQU   X'20'              SECOND PASS FUNCTION         @Y17XAUU
TRMRNLU  EQU   X'10'              SNA LU ENTRY                 @Y17XAUU
TRMDELY  EQU   X'08'              QCB IN TIME DELAY QUEUE      @Y17XAUU
TRM3705  EQU   X'04'              3705 MODE                    @Y17XAUU
TRMCTEAP EQU   X'02'              CT EQU AP                    @Y17XAUU
WRKFLG   DS    CL1                WORK AREA                    @Y17XAUU
KTRMEND  DS    0C                 END OF KTRM DSECT            @Y17XAUU
************************************************************** @Y17XAUU
UCBNAMES DS    H                  UCB ADDRESS                  @Y17XAUU
         EJECT
GRABMAP  DSECT                                                   S22024
         DS    XL1                     CONTROL PROMRAM FLAGS     S22024
         DS    XL1                     MACRO LEVEL               S22024
         DS    XL2                     MACRO ID                  S22024
$GRABCDS DS    A                       CDS INFO AREA             S22024
$GRABFLG DS    XL1                     FLAGS                     S22024
$GRABCLS DS    XL1                     CLASS                     S22024
$GRABTYP DS    XL1                     TYPE                      S22024
$GRABCNT DS    XL1                     NUMBER OF CDS BYTES       S22024
         TLGBD                         LGB                       S22024
         EJECT
         TDEBD
DCBDSECT DCBD  DSORG=TX                DCB                       S22024
TIOT     DSECT                                                   S22024
         IEFTIOT1                      TIOT                      S99528
         TAVTD                                                   S99528
         TTRMD                                                   S99528
         TQCBD                                                   S99528
         TLCBD                                                   S99528
CVT      DSECT                                                   S99528
         CVT                                                     S99528
         EJECT
RESPL    RESPL                                                 @Y17XAUU
         EJECT
UCB      DSECT                                                   S99528
         IEFUCBOB                                                S99528
         EJECT
         TDCTD                                                 @Y17XAUU
         EJECT
         TTCDSD                                                @Y17XAUU
         END
