QNQ      TITLE 'IEDQNQ-CHECKPOINT NOTIFICATION AND DISPOSITION MOD'
IEDQNQ   CSECT
         SPACE 3
*C240300,247000,481100,483100,508000,720000,722000,724000        Y02027
*C726000,738000,768000,770000,772000,774000,776000.778000        Y02027
*C780000,782000,784000,786000,788000-790000,795000               Y02027
*D199000,238400,238800,239200,239600,240600-241500,481200-482300 Y02027
*D524000,728000,730000,732000,734000,736000,740000,742000,774400 Y02027
*D774800,775200,775600,778300,778600,778900,779200,779500        Y02027
*D792000-794100                                                  Y02027
*A301000,303000                                                  Y02027
*A508200-509200,587500,720900-721800,723600,724500-725400,727200 Y02027
*A728100-729900,730800-731700,732600-733500,734400-735300        Y02027
*A736200-737100,738900-739800,740700-741600,769000,771000,773000 Y02027
*A775000,777000,779000,781000,783000,785000,787000,796420-794680 Y02027
*A822600-823200                                                  Y02027
*A198000,238000,774000,794000                                    A42413
*C208000,240300,778300                                           A42413
*A216000,304000,794000                                          SA52951
*C204000-210000,422000-440000,480000-484000,694000,697000-698000SA52951
*A506700                                                        SA58451
*D356000                                                       @SA70674
*A218000,774000                                                @XA05298
*D778000                                                       @XA05298
*D428000                                                       @SA71082
*A420000                                                       @SA74853
*D421000                                                       @OY13275
         SPACE 3
***********************************************************************
*                                                                     *
*TITLE:'IEDQNQ'-CHECKPOINT NOTIFICATION AND DISPOSITION ROUTINE       *
*                                                                     *
*  MODULE NAME = IEDQNQ                                               *
*                                                                     *
*  DESCRIPTIVE NAME = CHECKPOINT NOTIFICATION & DISPOSITION           *
*                                                                     *
*  COPYRIGHT = ' NONE '                                               *
*                                                                     *
*  STATUS:  CHANGE LEVEL 6                                            *
*                                                                     *
*FUNCTION:THIS MODULE GET CONTROL AFTER A DISK WRITE OPERATION        *
*   CONPLETES OR AFTER A CHECKPOINT COULD NOT BE SATISFIED.IT         *
*   REMOVES THE REQUEST ELEMNT(S) FROM THE QCB CHAIN AND QPOSTS       *
*   THE ELEMENT(IF FROM AN NH MACRO) OR POSTS AN ECB(IF FROM          *
*   OPERATOR CONTROL OR AN APPLICATION PROGRAM).THE REQUEST ELEMENT   *
*   IS NOT REMOVED IF THE REQUEST HAS NOT BEEN COMPLETELY SATISFIED   *
*   (CKREQ, OPERATOR CONTROL INCIDENT, AND ENVIRONMENT CHECKPOINTS    *
*   MAY REQUIRE MULTIPLE SEGMENTS), OR IF A DISK ERROR OCCURRED ON    *
*   WRITE. IF THE LAST SEGMENT OF AN ENVIRONMENT CHECKPOINT WAS       *
*   JUST WRITTEN WITH AN INCIDENT OVERFLOW CONDITION(INDICATED IN     *
*   THE ENVIRONMENT REQUEST ELEMENT),SEVERAL INCIDENT REQUEST         *
*   ELEMENTS MAY BE REMOVED FORM THE QCB CHAIN; THE INCIDENT OVER-    *
*   FLOW BIT IN EACH INCIDENT REQUEST ELEMENT(BIT 0 OF KEY) AND IN    *
*   THE ENVIRONMENT REQUEST ELEMENT IS TURNED OFF. IF THE LAST        *
*   SEGMENT OF A CHECKPOINT WAS JUST WRITTEN,A FREEMAIN IS ISSUED     *
*   FOR THE RECORD. IF THE LAST SEGMENT OF AN ENVIRONMENT CHECK-      *
*   POINT WAS JUST WRITTEN A BIT IS TURNED ON IN EACH PCB(BIT 2 OF    *
*   PCBOFLG) TO INDICATE TO THE APPLICATION PROGRAM(S) THAT THE       *
*   CHECKPOINT WAS TAKEN,AND AN ELEMENT IS QPOSTED TO THE READY       *
*   QUEUE. IF THE REQUEST WAS FROM MCPCLOSE,THE ELEMENT IS THE        *
*   CLOSEDOWN COMPLETION QCB;OTHERWISE,IT IS THE ENVIRONMENT          *
*   REQUEST ELEMENT(TO BE PUT ON THE TIME DELAY QCB).                 *
*                                                                     *
*ENTRY POINT:                                                         *
*                                                                     *
*        IEDQNQ                                                       *
*                                                                     *
*INPUT:REGISTERS 2,9,12,14,15 CONTAIN THE FOLLOWING VALUES:           *
*                                                                     *
*   2-ADDRESS OF CHECKPOINT WORK AREA                                 *
*   9-ADDRESS OF AVT                                                  *
*   12-BASE REG FOR CHECKPOINT EXECUTOR-IEDQNF                        *
*   14-RETURN ADDRESS-IN IEDQNF                                       *
*   15-ENTRY POINT OF THIS MODULE                                     *
*                                                                     *
*OUTPUT:AN ELEMENT MAY BE QPOSTED TO THE READY QUEUE. SEE FUNCTION    *
*   FOR THE SITUATIONS.                                               *
*                                                                     *
*   A BIT IS SET FOR EACH APPLICATION PROGRAM                         *
*                                                                     *
*EXTERNAL ROUTINES:                                                   *
*                                                                     *
*        AQCTL SVC ROUTINE-QPOSTS ELEMENTS TO READY QUEUE             *
*        OS POST - CROSS MEMORY POST OF APPLICATION PROGRAM ECB       *
*        SVC 5 (FREEMAIN)                                             *
*        SVC 35 (WTO)                                                 *
*                                                                     *
*EXITS-NORMAL:IF A REQUEST WAS NOT COMPLETLY SATISFIED THE OFFSET     *
*   OF THE MODULE TO BUILD A RECORD FOR CKREQ,ICHNG,OR ENVIRONMENT    *
*   REQUESTS IS RETURNED IN R15,AND THIS MODULE BRANCHES TO THE       *
*   ADDRESS IN R14. OTHERWISE, IT BRANCHES TO 4 PLUS THE ADDRESS IN   X
*   R14.                                                              *
*                                                                     *
*EXITS-ERROR:THIS MODULE ISSUES AN ERROR MESSAGE VIA WTO. IF THE      *
*   RECORD WITH THE DISK ERROR IS CKREQ OR ENVIRONMENT,THE RECORD     *
*   IS FLAGGED IN THE CHECKPOINT WORK AREA. IT THEN BRANCHES TO THE   *
*   ADDRESS IN R14 WITH THE OFFSET OF THE DISK I/O ROUTINE,SO THAT    *
*   THE SAME RECORD CAN BE WRITTEN AT ANOTHER LOCATION.               *
*                                                                     *
*TABLES/WORK AREAS:                                                   *
*                                                                     *
*   AVT(AVTCKLE,AVTCKPTB,AVTCLOSB,AVTDELYB,AVTHA3,AVTOPCOB,           *
*       AVTPROPT)                                                     *
*   CHECKPOINT WORK AREA                                              *
*                                                                     *
*ATTRIBUTES:REENTERABLE                                               *
*                                                                     *
*NOTES:THE OPERATION OF THIS MODULE DEPENDS UPON AN INTERNAL REPRE-   *
*   SENTATION OF THE EXTERNAL CHARACTER SET WHICH IS EQUIVALENT TO    *
*   THE ONE USED AT ASSEMBLY TIME. THE CODING HAS BEEN ARRANGED SO    *
*   THAT REDEFINITION OF 'CHARACTER' CONSTANTS,BY REASSEMBLY, WILL    *
*   RESULT IN A CORRECT MODULE FOR THE NEW DEFINITIONS.               *
*                                                                     *
***********************************************************************
         EJECT
         SPACE 3
*REGISTER ASSIGNMENTS
         SPACE
R0       EQU   0
R1       EQU   1
R2       EQU   2                        ADDRESS OF CKPT WORK AREA
R3       EQU   3                        ADDRESS OF REQUEST ELEMENT
R4       EQU   4                        ADDRESS OF DISK RECORD
R5       EQU   5
R6       EQU   6                        ADDRESS OF PREVIOUS REQ ELM
R7       EQU   7
R8       EQU   8
RAVT     EQU   9                        ADDRESS OF AVT
R10      EQU   10                       LENGTH FOR FREEMAIN
R11      EQU   11                       BRANCH REG
R12      EQU   12                       BASE FOR CKPT EXECUTOR
R13      EQU   13                       BRANCH REG
R14      EQU   14                       RETURN ADDR-IN CKPT EXECUTR
R15      EQU   15                       ENTRY POINT FOR THIS MODULE
         SPACE 3
         USING *,R15                    BASE REG
IEDQNQ   IEDHJN QNQ00                                            S99240
         USING IEDQAVTD,RAVT            BASE FOR AVT
         USING IEDQCKPD,R2              BASE FOR CKPT WORK AREA
         USING IEDQCRED,R3              BASE FOR REQUEST ELEMENT
         USING IEDQCDRD,R4              BASE FOR DISK RECORD    SA52951
         L     R3,AVTCKPTB              GET QCB ELEMENT CHAIN
         CLI   CREKEY,CREREMVE          THIS CHECKPOINT REMOVAL@XA05298
*                                         ELEMENT              @XA05298
         BNE   QNQ02                    NO, BRANCH             @XA05298
         BAL   R6,QNQ78                 REMOVE THIS ELEMENT    @XA05298
         L     R3,AVTCKPTB              GET NEXT ELEMENT       @XA05298
QNQ02    EQU   *                                               @XA05298
         TM    CKPLREB,CKPNDRB          CHECK FOR UNSATISFIED REQ
         BZ    QNQ60                    BRANCH IF NOT UNSATISFIED
         SPACE 3
*A CHECKPOINT REQUEST COULD NOT BE SATISFIED.REMOVE REQUEST ELEMENT
*FROM CKPT QCB CHAIN AND NOTIFY REQUESTOR.
         SPACE
         L     R6,CKPLREB               LAST REQ EL.-DISK RCD BUILT
         NC    CKPLREB+1(3),CKPLREB+1   CHECK FOR PREVIOUS ELEMENT
         BNZ   QNQ05                    BRANCH NOT ZERO
         LA    R6,AVTCKPTB-4            GET DUMMY PREVIOUS ELEMENT
QNQ05    EQU   *
         BAL   R13,GETLOCK              SET MODE AND GET LOCK    Y02027
         L     R3,4(0,R6)               NEXT REQ EL.IN CHAIN-RCD       X
                                        NOT BUILD FOR IT
         MVC   5(3,R6),5(R3)            REMOVE REQ EL.FROM CHAIN
         BAL   R13,RELLOCK              SET MODE AND RELEASE LOCK×02027
         TM    CREKEY,B'01010000'       CHECK FOR ENVIRONMENT REQ
         BNO   QNQ80                    BRANCH IF NOT FOR ENV
         SPACE 3
*THIS IS AN ENVIRONMENT REQUEST TO DISPOSE OF.
         SPACE
QNQ07    EQU   *
         TM    AVTCKELF,AVTCINCN        CHECK FOR INCIDENT OVERFLOW
         BZ    QNQ30                    BRANCH IF NO INC OVERFLOW
         L     R3,AVTCKPTB              GET ELEMENT CHAIN FROM QCB
         LA    R6,AVTCKPTB-4            DUMMY PREVIOUS ELEMENT
         NI    AVTCKELF,X'FF'-AVTCINCN  TURN OFF INCIDENT OVERFLOW
         SPACE 3
*THIS ENVIRONMENT CKPT IS A RESULT OF AN INCIDENT OVERFLOW. ALL
*INCIDENT REQUEST ELEMENTS WHICH HAVE AN OVERFLOW INDICATOR ON(HIGH
*ORDER BIT OF KEY FIELD) MUST BE REMOVED AND THE REQUESTORS
*NOTIFIED.
         SPACE 3
*BEGINNING OF LOOP THROUGH QCB CHAIN
         SPACE
QNQ10    EQU   *
         CLC   5(3,R6),AVTDELAD+1       CHECK FOR END OF ELMNT CHAI
         BE    QNQ30                    BRANCH IF END
         SPACE
         TM    CREKEY,CREINCOF          CHECK INCIDENT OVERFLOW BIT
         BZ    QNQ20                    BRANCH IF NOT ON
         SPACE
         NI    CREKEY,X'FF'-CREINCOF    TURN OFF INCIDENT OVERFLOW
         BAL   R13,GETLOCK              SET MODE AND GET LOCK    Y02027
         MVC   5(3,R6),5(R3)            MOVE LINK FIELD TO PREVIOUS
         BAL   R13,RELLOCK              SET MODE AND RELEASE LOCKY02027
         BAL   R13,QNQ90                DETERMINE REQUESTOR,POST
         L     R3,AVTECD4(,R6)          GET NEXT ELE IN CHAIN   SA52951
         B     QNQ10                    CHECK FOR END OF CHAIN  SA52951
         SPACE
QNQ20    EQU   *
         LR    R6,R3                    SAVE PREVIOUS REQUEST ELEMT
         L     R3,4(0,R3)               GET NEXT ELEMENT IN CHAIN
         B     QNQ10                    GO CHECK FOR END OF CHAINS99240
         SPACE
*END OF LOOP THROUGH QCB CHAIN
         SPACE 3
*THERE IS NO INCIDENT OVERFLOW
         SPACE
QNQ30    EQU   *
         TM    AVTCKELF,AVTCMCPN        CHECK FOR MCPCLOSE REQUEST
         BZ    QNQ35                    BRANCH IF NOT MCPCLOSE REQ
         SPACE
         LA    R1,AVTCLOSB              ADDR OF CLOSEDOWN COMPL QCB
         MVI   4(R1),PRICLSDN           SET LOW PRIOITY
         LR    R0,R1                    QCB IS POSTED TO ITSELF
         NI    AVTCKELF,X'FF'-AVTCMCPN  TURN OFF REQUEST BITS
         B     QNQ40                    QPOST QCB TO READY QUEUE
QNQ35    EQU   *
         MVC   AVTCKTIM,AVTCKELV        RESET CKPT TIME INTERVAL
         LA    R1,AVTCKELE              ADDRESS OF CKPT REQ ELEMENT
         LA    R0,AVTDELYB              ADDRESS TIME DELAY QCB
QNQ40    EQU   *
         BAL   R11,QNQ85                QPOST
         SPACE 3
*TURN ON CHECKPOINT BIT IN EACH PCB.
         SPACE
QNQ45    EQU   *
         USING IEDQPCB,R10              BASE FOR PCB
         LA    R10,AVTPCBPT-X'18'       DUMMY LAST PCB IN CHAIN
         SPACE 3
*BEGINNING OF LOOP THROUGH ALL PCBS
         SPACE
QNQ46    EQU   *
         NC    PCBLINK(3),PCBLINK       CHECK FOR END OF CHAIN
         BZ    QNQ47                    BRANCH IF END
         L     R10,PCBLINK-1            GET NEXT IN CHAIN
         OI    PCBOFLG,PCBCKPTN         TURN ON CKPT BIT
         B     QNQ46                    BRANCH TO LOOP
         SPACE
*END OF LOOP THROUGH PCBS
         SPACE 3
QNQ47    EQU   *
         LH    R10,CKPBPERR             LENGTH FOR ENV.DISK RCD
         SPACE 3
*ISSUE FREEMAIN FOR CURRENT EXCP RECORD
         SPACE
QNQ50    EQU   *
         TM    CKPLREB,CKPNDRB          IS THIS UNSATISFIED REQUEST
         BO    QNQ55                    BRANCH IF UNSATISFIED
         SPACE
         LA    R11,CKPEXCP              ADDRESS OF RCD TO FREE
         FREEMAIN E,LV=(R10),A=(R11),MF=(E,CKPGETML)
         SPACE
         XC    CKPEXCP,CKPEXCP          CLEAR CURRENT EXCP SLOT
QNQ55    EQU   *
         NI    CKPLREB,AVTEFF-CKPNDRB   TURN OFF UNSATISFIED BITSA52951
         B     AVTECD4(R14)             RETURN TO CKPT EXECUTOR SA52951
         SPACE 3
*THE DISK RECORD IN THE CURRENT EXCP SLOT HAS BEEN WRITTEN. DISPOSE
*OF IT. DETERMINE IF REQUEST IS COMPLETED.
         SPACE
QNQ60    EQU   *
         L     R4,CKPEXCP               ADDR OF DISK RCD JUST WRITN
         MVI   CKPECB,0                 CLEAR ECB COMPLETION CODE
         TM    CKPIOECB,X'7F'           CHECK FOR DISK ERROR
         BNO   QNQ82                    BRANCH IF DISK ERROR
         CLI   CDRKEY,CDRTOTLC          CHECK FOR TOTAL(NOT LASTSEG
         BNE   QNQ65                    BRANCH NOT TOTAL-LAST SEG
QNQ63    EQU   *
         LA    R15,CKPENV               OFFSET FOR MODULE TO BUILD     X
                                        NEXT SEGMENT OF ENV CKPT
         BR    R14                      RETURN TO EXECUTOR
         SPACE
QNQ65    EQU   *
         CLI   CDRKEY,CDRTOTAL          CHECK FOR ENV-LAST SEGMENT
         BNE   QNQ67                    BRANCH IF NOT ENV RCD
         LA    R6,AVTCKPTB-AVTECD4      DUMMY PREVIOUS ELEMENT  SA52951
         LA    R13,AVTCKELE             ADDR OF CKPT ELEMENT    SA52951
         ST    R13,CKPTRMAD             SAVE ADDR FOR COMPARE   SA52951
QNQ66    EQU   *                                                SA52951
         CLC   OFFST5(L3,R6),CKPTRMAD+ONE IS THIS THE CKPT ELE  SA52951
         BE    QNQ6605                  IF SO,REMOVE FROM CHAIN SA52951
         SPACE 1
         L     R6,AVTECD4(,R6)          NEXT ELEMENT IN CHAIN   SA52951
         B     QNQ66                    TEST FOR CKPT ELEMENT   SA52951
         SPACE 1
QNQ6605  EQU   *                                                SA52951
         BAL   R13,GETLOCK              SET MODE AND GET LOCK    Y02027
         MVC   OFFST5(L3,R6),AVTCKELE+OFFST5 REMOVE CKPT ELE    SA52951
         CLC   CKPLREB+ONE(L3),CKPTRMAD+ONE WAS LAST DISK RCD   SA52951
*                                       BUILT FOR CKPT ELEMENT  SA52951
         BNE   QNQ6609                  IF NOT, BYPASS CLEAR    SA52951
         XC    CKPLREB,CKPLREB          CLEAR ELEMENT POINTER   SA52951
         SPACE 1
QNQ6609  EQU   *                                                SA52951
         BAL   R13,RELLOCK              SET MODE AND RELEASE LOCKY02027
         B     QNQ07                    DISPOSE OF ENVIRON. REQ. S99240
         SPACE
QNQ67    EQU   *
         TM    CDRCKFLG,CDRCKRNC        CHECK FOR REQ NOT COMPLETED
         BZ    QNQ75                    BRANCH NOT CKREQ CONTINUED
         CLI   CDRKEY,CDRCKREQ          CHECK FOR CKREQ RECORD
         BNE   QNQ75                    BRANCH IF NOT CKREQ
         LA    R15,CKPCKREQ             OFFSET FOR CKREQ RTN
         BR    R14                      RETURN TO EXECUTOR
         SPACE
QNQ75    EQU   *
         LA    R6,QNQ80                 LOAD BR REG            @XA05298
         CLI   CDRKEY,CDROPC            WAS OPERATOR CONTROL     S99240
*                                        INCIDENT RECORD WRITTEN S99240
         BNE   QNQ78                    NO, REMOVE REQ ELEMENT   S99240
         LH    R10,CKPINCLN             FREEMAIN LNGTH FOR INCIDSA58451
         TM    CDRTTRLI+2,2             IS THIS THE LAST SEGMENT S99240
         BZ    QNQ50                    NO, FREE THIS RECORD AND S99240
*                                        RETURN TO WRITE NEXT    S99240
QNQ78    EQU   *                                                 S99240
         BAL   R13,GETLOCK              SET MODE AND GET LOCK    Y02027
         CLC   AVTCKPTB+1(3),CKPLREB+1  CHK PREVIOUS ELEMENT     Y02027
         BNE   QNQ79                    BR IF NOT EQUAL TO FIRST Y02027
         XC    CKPLREB,CKPLREB          CLEAR ADDR OF ELEMENT    Y02027
QNQ79    EQU   *                                                 Y02027
         MVC   AVTCKPTB+1(3),5(R3)      DELINK ELEMENT           Y02027
         BAL   R13,RELLOCK              SET MODE AND RELEASE LOCKY02027
         BR    R6                       RETURN                 @XA05298
QNQ80    EQU   *
         BAL   R13,QNQ90                DETERMINE REQUESTOR,POST
         B     QNQ50                    FREEMAIN
         SPACE 3
*THERE WAS A DISK ERROR IN THE LAST RECORD WRITTEN
         SPACE
QNQ82    EQU   *
         CLI   CDRKEY,CDRCKREQ          CHECK RECORD TYPE
         BL    QNQ84                    INCIDENT
         BH    QNQ83                    ENV
         SPACE 3
*CKREQ RECORD
         SPACE
         L     R8,CKPCTTRB              ADDRESS OF CKREQ TTR RABLE
         SPACE 3
*BEGINNING OF LOOP THROUGH CKREQ-TTR TABLE
         SPACE
QNQ825   EQU   *
         TM    CKPCTFLG(R8),CKPCTDER    CHECK FOR DISK ERROR
         BO    QNQ827                   BRANCH IF ERROR
         CLC   CDRCKOFF,CKPCTOFF(R8)    COMPARE OFFSETS
         BE    QNQ829                   BRANCH IF EQUAL
QNQ827   EQU   *
         LA    R8,CKPCTTRL(0,R8)        MOVE TO NEXT ENTRU
         B     QNQ825                   LOOP TO NEXT ENTRY
         SPACE
*END OF LOOP THROUGH CKREQ-TTR TABLE
         SPACE 3
QNQ829   EQU   *
         OI    CKPCTFLG(R8),CKPCTDER    TURN ON DISK ERROR FLAG
         B     QNQ84                    WRITE ERROR MESSAGE
         SPACE 3
*ENVIRONMENT SEGMENT
         SPACE
QNQ83    EQU   *
         CLC   CKPRW+6(2),CKPCRLEN      CHECK FOR CONTROL RECORD
         BNE   QNQ835                   BRANCH IF NOT CONTROL RCD  0217
         WTO   'IED087I CHECKPOINT DISK ERROR-CONTROL RECORD',     0217X
               ROUTCDE=11,DESC=4
         B     QNQ845                   BRANCH TO EXIT CODE        0217
QNQ835   EQU   *                                                   0217
         SR    R7,R7                    CLEAR FOR IC             Y02027
         IC    R7,CKPTTRCT              CORRENT ENV TTR
         MH    R7,AVTHA3                MULTIPLY BY 3
         LA    R7,CKPTTRT1-3(R7)        ADDRESS OF TTR             0217
         XC    0(3,R7),0(R7)            CLEAR TTR
         SPACE 3
*INCIDENT RECORD
         SPACE
QNQ84    EQU   *
         WTO   'IED084I CHECKPOINT DISK ERROR-RECOVERED',          0217X
               ROUTCDE=11,DESC=4
QNQ845   EQU   *
         LA    R15,CKPDIOR              OFFSET FOR DISK I/O ROUTINE
         BR    R14                      RETURN TO EXICUTOR
         SPACE 3
*SUBROUTINE TO SET UP REGISTERS FOR DISPATCHER TO QPOST ELEMENT TO
*THE READY QUENE. REGISTER 11 IS THE BRANCH REG.
         SPACE
QNQ85    EQU   *
         ST    R0,CKPTRMAD              ADDR OF QCB
         MVC   1(3,R1),CKPTRMAD+1       PUT QCB ADDR IN REQ ELEMENT
QNQ86    EQU   *                                                   1016
         XC    5(3,R1),5(R1)             CLEAR LINK FIELD
         LR    R0,R1                    DUPLICATE ADDRESS
         STM   R0,R1,CKPSAVE2           PUT INTO PARM LIST
         MVI   CKPSAVE2,X'0C'           PUT IN 1ST BYTE
QNQ87    EQU   *
         LA    R1,CKPSAVE2              ADDRESS OF PARM LIST
         LR    R5,R15                   SAVE REG
         AQCTL                          POST ELEMENT TO READY Q
         LR    R15,R5                   RESTORE REG
         SPACE
         BR    R11                      RETURN
         SPACE
*END OF SUBROUTINE
         SPACE 3
*SUBROUTINE TO DETERMINE CKPT REQUESTOR AND POST IT COMPLETE
*REGISTER 13 IS BRANCH REG
QNQ90    EQU   *
         SPACE
         LH    R10,CKPINCLN             FREEMAIN LENGTH FOR INCIDNT
         LR    R1,R3                    ADDR OF ELEMENT FOR READY Q1016
         CLI   CREKEY,CREOPC            CHECK FOR OPERATOR CONTROL
         BNE   QNQ92                    BRANCH IF NOT OP CONTROL
         SPACE
         LA    R0,AVTOPCOB              ADDR OPC QCB               1013
         BAL   R11,QNQ85                POST ELEMENT TO READY QUEUE1016
         BR    R13                      RETURN                     1016
         SPACE
QNQ92    EQU   *
         CLI   CREKEY,CRELCB            CHECK FOR LCB FROM MH
         BNE   QNQ94                    BRANCH IF NOT LCB
         SPACE
         USING IEDQLCB,R3
         LA    R11,LCBERB-IEDQLCB       OFFSET FOR ERB INTO LCB    1016
         SR    R3,R11                   ADDR OF LCB BEGINNING      1016
         LR    R1,R3                    COPY LCB ADDR FOR POST     1021
         L     R11,LCBSCBA-1            ADDRESS OF SCB
         USING IEDQSCB,R11
         NI    SCBSTATE,X'FF'-SCBCKPT   TURN OFF CHECKPT BIT
         BAL   R11,QNQ86                POST ELEMENT TO READY QUEUE1016
         BR    R13                      RETURN FROM SUBROUTINE
         SPACE
QNQ94    EQU   *
         LR    R11,R3                   ADDRESS OF CRE - IT IS   Y02027
*                                       LOCATED IN AIB           Y02027
         SH    R11,AVTHA16              BACK UP TO AIB           Y02027
         USING IEDQAIB,R11              ADDRESSABILITY OF AIB    Y02027
         MVC   AIBCMPTC(4),AIBCKECB     STORE ECB IN LIST        Y02027
         L     R1,AIBPCBAD              GET PCB ADDRESS          Y02027
         L     R1,PCBPEBAD-IEDQPCB(R1)  GET PEB ADDRESS          Y02027
         LTR   R1,R1                    TEST IF AP IS ACTIVE     Y02027
         BZ    BYPASS                   BRANCH IF NO TO AVOID    Y02027
*                                       XMPOST                   Y02027
         L     R1,PEBASCB-IEDQPEB(R1)   GET ASCB ADDRESS         Y02027
         LTR   R1,R1                    TEST IF FLAG HAS         Y02027
*                                       INVALIDATED ASCB ADDRESS Y02027
         BM    BYPASS                   BRANCH IF NEGATIVE - FLAGY02027
*                                       ON - AVOID XMPOST        Y02027
         ST    R1,AIBCMPTC+FOUR         STORE IN LIST            Y02027
         L     R1,CVTPTR                ADDRESS OF CVT           Y02027
         LA    R1,CVTBRET(R1)           GET ERROR ADDRESS -BR 14 Y02027
         ST    R1,AIBCMPTC+EIGHT        STORE IN LIST            Y02027
         LA    R1,AIBCMPTC              GET ADDR OF LIST         Y02027
         LR    R5,R15                   SAVE REG                 Y02027
         SR    R0,R0                    ZERO COMPLETION CODE     Y02027
         POST  ,(0),MF=(E,(1))                                   Y02027
         LR    R15,R5                   RESTORE REGISTER         Y02027
BYPASS   EQU   *                                                 Y02027
         CLI   CDRKEY,CDRCKREQ          CHECK FOR CKREQ
         BCR   7,R13                    BRANCH IF NOT CKREQ
         LH    R10,CKPCKRLN             LENGTH OF CKREQ RCD
         BR    R13                      RETURN FROM SUBROUTINE
         SPACE 3
GETLOCK  EQU   *                                                 Y02027
         LR    R5,R15                   SAVE REG                 Y02027
         LR    R7,R14                   SAVE REG                 Y02027
         MODESET MODE=SUP               SET TO SUPERVISOR STATE  Y02027
         MODESET EXTKEY=SUPR            SET TO KEY ZERO          Y02027
SETATSK  EQU   *                                                 Y02027
         SETLOCK OBTAIN,TYPE=LOCAL,MODE=UNCOND,REGS=USE,               C
               RELATED=ATTACHED-TASKS-IGG019RO/IGG019RB(SETATSK) Y02027
         LR    R15,R5                   RESTORE REG              Y02027
         LR    R14,R7                   RESTORE REG              Y02027
         BR    R13                      RETURN                   Y02027
         SPACE 3                                                 Y02027
RELLOCK  EQU   *                                                 Y02027
         LR    R7,R14                   SAVE REG                 Y02027
         LR    R5,R15                   SAVE REG                 Y02027
         SETLOCK RELEASE,TYPE=LOCAL,REGS=USE,                          C
               RELATED=ATTACHED-TASKS-IEDQNQ(SETATSK)            Y02027
         MODESET EXTKEY=TCAM            SET TO TCAM KEY          Y02027
         LR    R15,R5                   RESTORE BASE REG         Y02027
         MODESET MODE=PROB              SET TO PROBLEM STATE     Y02027
         LR    R14,R7                   RESTORE REG              Y02027
         LR    R15,R5                   RESTORE REG              Y02027
         BR    R13                      RETURN                   Y02027
* EQUATED SYMBOLS                                               SA52951
         SPACE 1
ONE      EQU   1                        CONSTANT ONE            SA52951
L3       EQU   3                        LENGTH OF THREE         SA52951
OFFST5   EQU   5                        OFFSET OF FIVE          SA52951
FOUR     EQU   4                        DISPLACEMENT             Y02027
EIGHT    EQU   8                        DISPLACEMENT             Y02027
CVTPTR   EQU   16                       PTR TO CVT               Y02027
CVTBRET  EQU   X'52'                    OFFSET IN CVT TO BR 14   Y02027
         EJECT                                                  SA52951
         IHAPSA                         DSECT FOR SETLOCK        Y02027
         SPACE 3
         SPACE 3
         TCKPD 3330                     MERLIN VERSION      0512 S21101
         TAVTD
         TDISPD
         TPCBD
         TLCBD
         TSCBD
         TPRIOR
         TAIBD EXT=(CKREQ)                                       Y02027
         TPEBD                                                   Y02027
         END
