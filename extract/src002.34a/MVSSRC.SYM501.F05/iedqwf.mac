         TITLE ' IEDQWF - TEST CONTROL MODULE 11'
IEDQWF   CSECT                                                   S22024
*CHANGE ACTIVITY AS FOLLOWS:
*C470000-475000                                                 SA41596
*A470500-474500,560600-568400                                    S05331
*C555000                                                         S05331
*************************************************************** S22024*
*                                                               S22024*
* TITLE:  'IEDQWF - TEST CONTROL MODULE FOR TCAM/TOTE'          S22024*
*                                                                     *
* MODULE NAME:  IEDQWF                                                *
*                                                                     *
* DESCRIPTIVE NAME:  TEST CONTROL MODULE FOR TCAM/TOTE          S22024*
*                                                               S22024*
* COPYRIGHT:  'NONE'                                            S22024*
*                                                               S22024*
* STATUS:  CHANGE LEVEL 5                                       S05331*
*                                                                     *
* FUNCTION:                                                           *
*                                                                     *
*       TO FREE THE CORE REQUIRED BY IEDQWE DURING OLT EXECUTION      *
*       AND TO PASS CONTROL TO THE OLT ROOT MODULE AND RECEIVE        *
*       CONTROL BACK FROM IT.                                         *
*                                                                     *
*       ENTRY POINT:                                                  *
*                                                                     *
*       IEDQWF - CALLED BY OLT TEST CONTROL MODULE I - (IEDQWE).      *
*                                                                     *
* INPUT:        REGISTERS 02,13,14,15 CONTAIN THE FOLLOWING VALUES:   *
*                                                                     *
*                         02-OLTCB POINTER                            *
*                         13-SAVE AREA ADDRESS                        *
*                         14-RETURN ADDRESS                           *
*                         15-ENTRY POINT ADDRESS                      *
*                                                                     *
*                                                                     *
* EXTERNAL ROUTINES:                                                  *
*                                                                     *
*       DEVICE TESTS (OLT'S)                                          *
*                                                                     *
* EXIT POINT:  XCTL TO IEDQWE1
*                                                                     *
* TABLES/WORK AREA:  OLTCB                                            *
*                                                                     *
* ATTRIBUTES:                                                         *
*                                                                     *
*       ENABLED, PROBLEM PROGRAM MODE, TRANSIENT.                     *
***********************************************************************
         EJECT
*
*        REGISTER EQUATES
*
R0       EQU   0                       REGISTER 0                S99528
R1       EQU   1                       REGISTER 1                S99528
R2       EQU   2                       REGISTER 2                S99528
R3       EQU   3                       REGISTER 3                S99528
R4       EQU   4                       REGISTER 4                S99528
R5       EQU   5                       REGISTER 5                S99528
R6       EQU   6                       REGISTER 6                S99528
R7       EQU   7                       REGISTER 7                S99528
R8       EQU   8                       REGISTER 8                S99528
R9       EQU   9                       REGISTER 9                S99528
R10      EQU   10                      REGISTER 10               S99528
R11      EQU   11                      REGISTER 11               S99528
R12      EQU   12                      REGISTER 12               S99528
R13      EQU   13                      REGISTER 13               S99528
R14      EQU   14                      REGISTER 14               S99528
R15      EQU   15                      REGISTER 15               S99528
TWELVE   EQU   12                      DISPLACEMENT              S99528
EIGHT    EQU   8                       DISPLACEMENT              S99528
FOUR     EQU   4                       DISPLACEMENT              S99528
D16      EQU   X'10'              CONSTANT DISPLACEMENT          S22024
         EJECT
IEDQWF   CSECT
*C470000-475000                                                 SA41596
         USING IEDQWF,R15
IEDQWF   IEDHJN IEDQWFF,HJN            NAME AND HJN              S99528
         DROP  R15
         USING TOTOLTCB,R2             OLTCB ADDRESSIBILITY      S99528
         USING IEDQWF,R4
         STM   R14,R12,TWELVE(R13)     SAVE CALLERS REGS         S99528
         LR    R4,R15             SET UP BASE
         LA    R12,TOTSAVE4            GET MY SAVE ADDRESS       S99528
         ST    R12,EIGHT(R13)          ADD MY SAVE               S99528
         ST    R13,FOUR(R12)           TO SAVE CHAIN
         LR    R13,R12
*
         ST    R2,TOTOLTPL             SET UP SCT POINTER        S99528
*
         L     R5,TOTSTAE         GET ADDRESS OF STAE EXIT       S22024
*                                   ROUTINE                      S22024
         STAE  (5),XCTL=YES,PURGE=NONE,ASYNCH=YES                S22024
         L     R3,TOTTCBAD        GET ACTIVE TCB ADDRESS         S22024
         CLC   D16(FOUR,R3),ZERO  IS COMPLETION CODE IN TCB ZERO?S22024
         BE    LOADOLT            YES, GO LOAD OLT
         B     EXIT               NO, ABEND, EXIT TO IEDQWE1     S22024
*        LOAD OLT SCHEDULAR                                      S99528
*
LOADOLT  EQU   *                                                 S22024
         LOAD  EPLOC=TOTCROLT          LOAD OLT                  S99528
*
*        BRANCH TO OLT SCHEDULER
*
         LR    R15,R0                  SET UP ENTRY ADDRESS      S99528
         ST    R15,TOTOLTEN       SAVE OLT EP                   SA41596
         USING PSPREF,R15         SET UP BASE FOR SECT. PREFACE  S05331
         LH    R14,RTNDISP        GET DISPLACEMENT TO ROUTINE    S05331
         AR    R14,R15            GET ADDRESS OF ROUTINE         S05331
         USING SPREF,R14          SET UP BASE FOR ROUTINE PREFIX S05331
         TM    #FLAG,#MSGIDRQ     IS MESSAGE ID PROVIDED?        S05331
         BZ    CONTINUE           NO, BRANCH                     S05331
         MVC   $MSGIDAC,MSGID     MOVE OLT ID FROM SECTION       S05331
*                                   PREFACE TO SCT               S05331
CONTINUE EQU   *                                                 S05331
         L     R15,TWELVE(R15)    OLT ENTRY ADDRESS             SA41596
         LA    R1,TOTOLTPL             SET UP PARM LIST POINTER  S99528
         BALR  R14,R15                 BRANCH TO OLT             S99528
*
*        DELETE OLT SCHEDULER
*
         DELETE EPLOC=TOTCROLT         DELETE OLT
EXIT     EQU   *                                                 S22024
         STAE  0                  CANCEL PRECEDING STAE REQUEST  S22024
         LR    R1,R15             SAVE RETURN CODE
         L     R13,FOUR(R13)           GET CALLERS SAVE ADDR     S99528
         L     R14,TWELVE(R13)         GET RETURN POINT IN
         XCTL  (2,12),EP=IEDQWE1  RECALL TEST CONTROL MODULE
PATCH    DC    CL100' '           PATCH AREA                     S05331
ZERO     DC    F'0'               CONSTANT VALUE 0               S22024
         SPACE 3
PSPREF   DSECT                    SECTION PREFACE                S05331
SECTID   DS    CL8                SECTION ID                     S05331
LEVEL    DS    CL2                LEVEL                          S05331
RTNDISP  DS    AL2                DISPLACEMENT TO RTN #          S05331
INST     DS    A                  POINTER TO FIRST EXECUTABLE    S05331
*                                   INSTRUCTION                  S05331
MSGID    DS    CL2                OLT COMPONENT ID               S05331
         SPACE 3
SPREF    DSECT                    ROUTINE PREFIX                 S05331
RTN#     DS    CL1                ROUTINE NUMBER                 S05331
         DS    CL15               SECTION PREFACE FLAGS/DATA     S05331
#FLAG    DS    CL1                FLAGS                          S05331
#MSGIDRQ EQU   X'10'              MESSAGE ID PROVIDED            S05331
         EJECT
         OLTCB
         END
