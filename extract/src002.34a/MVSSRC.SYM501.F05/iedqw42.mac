QW2      TITLE 'IEDQW42 - MORECORE/FREECORE SERVICE MODULE'
IEDQW42  CSECT
*A000000-999999                                                @Y16X5U0
* CHANGE ACTIVITY AS FOLLOWS:                                    S22024
*A007000,021000-021020,388000-389000,493000-494000,500700        S22024
*A568000,696040,722200-722700                                    S22024
*C021090,021390,414000,747000                                    S22024
*D369000                                                         S22024
*   TOTE ALIAS                                                 @OY13283
*A389000,462000,500700,558000,564000,568000,660000,747000      @Y17XAUU
*C696400-696440,708000-721400                                  @Y17XAUU
         SPACE 2
IEDQW43  EQU   *
         ENTRY IEDQW43
* ***************************************************************
* *                                                             *
* MODULE NAME = IEDQW42 (TCAM,TOTE)                            @Y17XAUU
*                                                              * S22024
* DESCRIPTIVE NAME = TOTE MORECORE/FREECORE MODULE             * S22024
*                                                              * S22024
* COPYRIGHT = NONE                                             * S22024
*                                                              * S22024
* STATUS:                                                      *
*                                                              *
*    VERSION 10.0                                              @Y17XAUU
*                                                              *
* FUNCTION:                                                    *
*                                                              *
*    THIS MODULE IS CALLED IF A MORECORE OR FREECORE MACRO IS  *
*    ISSUED BY AN OLT PROGRAM.                                 *
*                                                              *
*    THE MACRO LEVEL FIELD OF THE PARAMETER FIELD IS CHECKED   *
*    TO VERIFY THAT THE MACRO LEVEL IS 2. IF THE MACRO LEVEL   *
*    IS INCORRECT,REGISTERS ARE RESTORED AND CONTROL IS PASSED *
*    TO IEDQWM2 TO PRINT THE ERROR MESSAGE. THE RETURN CODE    * S22024
*    IN REGISTER 15 IS SET TO X'04'.                           *
*                                                              *
*    MORECORE REQUEST: THE PURPOSE OF THIS ROUTINE IS TO       *
*    OBTAIN ADDITIONAL CORE FOR THE UNIT TEST VIA A GETMAIN    *
*    MACRO. IF THE AMOUNT OF CORE REQUESTED IS NOT AVAILABLE,  *
*    REGISTERS ARE RESTORED AND A 'BAD' RETURN CODE IS         *
*    RETURNED TO THE UNIT TEST. IF THE CORE REQUESTED IS       *
*    AVAILABLE THE ENTRY POINT IS PLACED IN REGISTER 1 AND     *
*    THE AMOUNT OF CORE ASSIGNED IS PLACED IN REGISTER 0.      *
*                                                              *
*    AT THE COMPLETION OF THIS ROUTINE ONE OF THE FOLLOWING    *
*    RETURN CODES IS PLACED IN REGISTER 15:                    *
*                                                              *
*         X'00'-NORMAL COMPLETION                              *
*         X'04'-FUNCTION NOT AVAILABLE                         *
*         X'08'-CORE REQUEST NOT AVAILABLE                     *
*                                                              *
*    FREECORE REQUEST: THIS ROUTINE FREES THE CORE OBTAINED    *
*    BY A MORECORE MACRO. A FREEMAIN MACRO IS EXECUTED. AN     *
*    ERROR EXIT IS TAKEN IF THE ADDRESS OF CORE TO BE RELEASED *
*    DOES NOT CORRESPOND TO ANY RETURNED ON A MORECORE REQUEST.*
*                                                              *
*    AT THE COMPLETION OF THIS ROUTINE, ONE OF THE FOLLOWING   *
*    RETURN CODES IS PLACED IN REGISTER 15:                    *
*                                                              *
*         X'00'-NORMAL COMPLETION                              *
*         X'04'-FUNCTION NOT AVAILABLE                         *
*         X'08'-ADDRESS OF AREA TO BE RELEASED DOESN'T         *
*               CORRESPOND TO ANY RETURN ON A MORECORE         *
*               REQUEST.                                       *
*                                                              *
* ENTRY POINT:                                                 *
*                                                              *
*         IEDQW42                                              *
*                                                              *
* PURPOSE: SEE FUNCTION                                        @Y17XAUU
*                                                              @Y17XAUU
* MODULE TYPE: PROCEDURE                                       @Y17XAUU
*                                                              @Y17XAUU
* MODULE SIZE: 2K MAXIMUM                                      @Y17XAUU
*                                                              @Y17XAUU
* PROCESSOR: ASSEMBLER XF                                      @Y17XAUU
*                                                              @Y17XAUU
* LINKAGE: BALR R14,R15 FROM IEDQWA                            @Y17XAUU
*                                                              @Y17XAUU
* MACROS: IEDHJN,SAVE,GETMAIN,FREEMAIN                         @Y17XAUU
*                                                              @Y17XAUU
* INPUT:                                                       *
*                                                              *
*    REGISTERS 1,2,3,13,14,15 CONTAIN THE FOLLOWING VALUES:    *
*                                                              *
*     1 - MORECORE/FREECORE PARAMETER LIST ADDRESS             *
*     2 - SCT ADDRESS                                          *
*     3 - OLT SECTION PREFACE ADDRESS                          *
*    13 - CALLER'S SAVE AREA ADDRESS                           *
*    14 - RETURN ADDRESS                                       *
*    15 - ENTRY POINT ADDRESS FOR THIS MODULE                  *
*                                                              *
* OUTPUT:                                                      *
*                                                              *
*    REGISTER 15 CONTAINS ONE OF THE RETURN CODES  LISTED ABOVE*
*                                                              *
* EXTERNAL REFERENCES:                                         *
*                                                              *
*         IEDQWM2                                              *
*                                                              *
* EXITS NORMAL:                                                *
*                                                              *
*         CONTROLL IS RETURNED TO CALLING ROUTINE              *
*                                                              *
* EXITS ERROR:                                                 *
*                                                              *
*         XCTL TO IEDQWM2 TO REPORT ERROR                      *
*                                                              *
* TABLES/WORK AREAS:                                           *
*                                                              *
*    SCT                                                       *
*    SECTION PREFACE                                           *
*                                                              *
* ATTRIBUTES:                                                  *
*                                                              *
*    ENABLED,PROBLEM PROGRAM MODE,RESIDENT,REENTANT            @Y17XAUU
*                                                              *
* NOTES: SEE BELOW                                             @Y17XAUU
*                                                              *
*  DEPENDENCIES: EBCDIC CHARACTER CODE                         @Y17XAUU
*                                                              @Y17XAUU
*  RESTRICTIONS: NONE                                          @Y17XAUU
*                                                              @Y17XAUU
*  REGISTERS CONVENTION: SEE REGISTERS ASSIGNMENT              @Y17XAUU
*                                                              @Y17XAUU
*  PATCH LABEL: PATCH                                          @Y17XAUU
* *                                                             *
* ***************************************************************
         EJECT
*
*        E Q U A T E S
*
R0       EQU   0                  REG 0
R1       EQU   1                  REG 1
R2       EQU   2                  REG 2
R3       EQU   3                  REG 3
R4       EQU   4                  REG 4
R5       EQU   5                  REG 5
R6       EQU   6                  REG 6
R7       EQU   7                  REG 7
R8       EQU   8                  REG 8
R9       EQU   9                  REG 9
R10      EQU   10                 REG 10
R11      EQU   11                 REG 11
R12      EQU   12                 REG 12
R13      EQU   13                 REG 13
R14      EQU   14                 REG 14
R15      EQU   15                 REG 15
         SPACE
D0       EQU   0                  DISP OF 0
D1       EQU   1                  DISP OF 1
D2       EQU   2                  DISP OF 2
D3       EQU   3                  DISP OF 3
D4       EQU   4                  DISP OF 4
D5       EQU   5                  DISP OF 5
D6       EQU   6                  DISP OF 6
D7       EQU   7                  DISP OF 7
D8       EQU   8                  DISP OF 8
D9       EQU   9                  DISP OF 9
D10      EQU   10                 DISP OF 10
D11      EQU   11                 DISP OF 11
D12      EQU   12                 DISP OF 12
D13      EQU   13                 DISP OF 13
D14      EQU   14                 DISP OF 14
D15      EQU   15                 DISP OF 15
D16      EQU   16                 DISP OF 16
D20      EQU   20                 DISP OF 20
D24      EQU   24                 DISP OF 24
D28      EQU   28                 DISP OF 28
D32      EQU   32                 DISP OF 32
         SPACE 2
MMACLEV  EQU   X'02'              MACRO LEVEL OF THIS MODULE
MBLEV    EQU   X'04'              INCORRECT MACRO LEVEL
MOREC    EQU   X'F2'              MORECORE INDICATOR
MORNO    EQU   X'08'              MORE CORE NOT AVAILABLE
MORYES   EQU   X'00'              MORE CORE AVAILABLE
MORTERM  EQU   X'FF'              TERMINATE FLAG
LOA      EQU   X'80'              MORECORE REQUEST               S22024
NOCOR    EQU   X'08'              CORE NOT AVAILABLE             S22024
DISREG2  EQU   28                 DISPLACEMENT TO REG 2        @Y17XAUU
MALLBITS EQU   X'FF'              MASK FIELD                   @Y17XAUU
         EJECT
         USING IEDQW42,R15
IEDQW42  IEDHJN STAR42,HJN
         DROP  R15
         SPACE 4
         SAVE  (14,12)            SAVE CALLER'S REGISTERS
         SPACE
         USING IEDQW42,R4
         USING TOTOLTCB,R2                                       S22024
         SPACE
         LR    R4,R15             SET BASE REGISTER
         LA    R15,TOTSAVE1       GET ADDR OF OWN SAVE AREA
         ST    R15,D8(R13)        PUT ADDR IN CALLER'S SAVE AREA
         ST    R13,D4(R15)        SAVE ADDR OF CALLER'S SAVE AREA
         LR    R13,R15            SET REG 13 TO MY SAVE AREA
         SPACE 2
         USING PLIST,R1
         SPACE
         CLI   PLEV,MMACLEV       CORRECT MACRO LEVEL
         BE    MOR010             YES
*
         LA    R15,MBLEV          SET INCORRECT MACRO LEVEL RETURN CODE
         STC   R15,TOTRTCOD       SAVE RETURN CODE
         L     R13,D4(R13)        RESTORE CALLER'S SAVE AREA PTR
         L     R14,D12(R13)       RESTORE REG 14
*
         USING RESPL,R5           SET RESPL ADDRESSABILITY     @Y17XAUU
         L     R5,TOTRESPL        GET RESPL ADDRESS            @Y17XAUU
         L     R15,RESPLM2        GET TRACE ROUTINE ADDRESS    @Y17XAUU
         LM    R2,R12,DISREG2(R13) RESTORE REGISTERS           @Y17XAUU
         BR    R15                EXIT TO TRACE ROUTINE        @Y17XAUU
         DROP  R5                                              @Y17XAUU
         SPACE 2
*
MOR010   EQU   *
*
         MVC   TOTSERV(END-START),START SET UP WORK AREA       @Y17XAUU
         CLI   PLID+D1,MOREC      MORECORE REQUEST
         BNE   FRE010             NO - ASSUME FREECORE REQUEST
         OI    MORFLG1,LOA        SHOW LOAD REQUEST            @Y17XAUU
         SPACE
         L     R5,PCORE2          GET MIN. CORE SIZE REQUESTED
         ST    R5,MCLIM           PUT IN P-LIST
         SLL   R5,D8              ADJUST SIZE                    S22024
         ST    R5,TOTWKSPC        SAVE SIZE                      S22024
         L     R5,PCORE1          GET MAX. CORE SIZE REQUESTED
         ST    R5,MCLIM+D4        PUT IN P-LIST
*
         CL    R5,MCLIM           MAX < MIN ?
         BNL   MOR015             NO
         LA    R15,MORNO          SET ERROR RETURN CODE
         B     MOREND             EXIT
*
*
MOR015   EQU   *
         BAL   R3,COREUPD         UPDATE AVAILABLE CORE          S22024
         SPACE 2
         LA    R7,MCADR           ADDRESS OF GOTTEN CORE       @Y17XAUU
         SR    R10,R10            SET SUBPOOL TO 0             @Y17XAUU
         LA    R6,MCLIM           MIN/MAX CORE REQUIRED        @Y17XAUU
         GETMAIN VC,A=(R7),LA=(R6),SP=(R10),MF=(E,BLIST)       @Y17XAUU
         SPACE
         LTR   R15,R15            REQUEST SATISFIED ?
         BZ    MOR020             YES
         SPACE
*
*        ASSUME CORE NOT AVAILABLE
*
         LA    R15,MORNO          SET CORE NOT AVAILABLE RET CODE
         B     MOREND             BRANCH TO EXIT
         SPACE 2
MOR020   EQU   *
         LA    R15,MORYES         SET CORE AVAILABLE RETURN CODE
         L     R1,MCADR           ADDR OF CORE GOTTEN
         L     R0,MCCNT           CORE SIZE GOTTEN
         ST    R0,TOTWKSPC        SAVE CORE SIZE
         B     MOREND             BRANCH TO EXIT
         EJECT
*
*        FREECORE ENTRY
*
FRE010   EQU   *
         NI    MORFLG1,MALLBITS-LOA RESET GETMAIN REQ FLAG     @Y17XAUU
         L     R5,PCORE1          POINT TO PTR TO CORE TO BE FREED
         L     R5,D0(R5)          GET ADDR OF CORE TO BE FREED
         L     R6,TOTWKSPC        SIZE OF AREA TO FREE         @Y17XAUU
         STM   R5,R6,MCADR        SET UP PLIST                 @Y17XAUU
         BAL   R3,COREUPD         UPDATE AVAILABLE CORE          S22024
         SPACE
         LA    R5,MCADR           PLIST ADDRESS                @Y17XAUU
         FREEMAIN V,A=(R5),MF=(E,BLIST) FREE THE CORE          @Y17XAUU
         SPACE
         LTR   R15,R15            REQUESTED SUCCESSFUL ?
         BZ    MOR020             YES
*
*        ASSUME NO PREVIOUS MORECORE REQUEST
*
         LA    R15,MORNO          SET NO CORE TO FREE RET CODE
         B     MOREND             BRANCH TO EXIT
*
*
*
*
*
         SPACE 4
MOREND   EQU   *
         STC   R15,TOTRTCOD       SAVE RETURN CODE
*
         TM    $OLTFLGS,$TRACE    TRACE FUNCTION SET ?
         BO    MOREND1            YES
         LTR   R15,R15            RETURN CODE OF ZERO ?
         BZ    MOREND2            YES - RETURN NORMALLY
         TM    $RETMASK,MORTERM   ANY TERMINATE FLAGS SET
         BZ    MOREND2            NO - RETURN NORMALLY
*
MOREND1  EQU   *    REQUEST TRACE FUNCTION FROM IEDQWM2
*
         L     R13,D4(R13)        RESTORE CALLER'S SAVE AREA ADDR
         L     R14,D12(R13)       RESTORE REGISTER 14
*
         USING RESPL,R5           SET RESPL ADDRESSABILITY     @Y17XAUU
         L     R5,TOTRESPL        GET RESPL ADDRESS            @Y17XAUU
         L     R15,RESPLM2        GET TRACE ROUTINE ADDRESS    @Y17XAUU
         LM    R2,R12,DISREG2(R13) RESTORE REGISTERS           @Y17XAUU
         BR    R15                EXIT TO TRACE ROUTINE        @Y17XAUU
         DROP  R5                                              @Y17XAUU
*
         SPACE
*
MOREND2  EQU   *    RETURN TO OLT NORMALLY
*
         L     R13,D4(R13)        RESTORE CALLER'S SAVE AREA ADDR
         L     R14,D12(R13)       RESTORE RETURN ADDR
         LM    R2,R12,D28(R13)    RESTORE REGS
*
         BR    R14                     RETURN
*
         EJECT
**************************************************************** S22024
*        ROUTINE TO UPDATE TOTE'S AVAILABLE CORE               * S22024
**************************************************************** S22024
*                                                                S22024
COREUPD  EQU   *                                                 S22024
*                                                                S22024
         STM   R8,R12,CORSAVE1    SAVE REG                       S22024
         ST    R6,CORSAVE2        SAVE REG                       S22024
         TM    MORFLG1,LOA        MORECORE REQUEST             @Y17XAUU
         BO    COR20              YES                          @Y17XAUU
*                                 ASSUME DELETE REQUEST          S22024
         L     R6,TOTWKSPC        GET CORE SIZE                  S22024
         SRL   R6,D8              CLEAR HIGH ORDER BYTE          S22024
         L     R11,TOTUSCOR       CURRENT CORE IN USE            S22024
         SR    R11,R6             UPDATE IT                      S22024
         ST    R11,TOTUSCOR       REPLACE IT                     S22024
         L     R9,TOTMMSPC        GET CURRENT SPACE AVAILABLE    S22024
         AR    R9,R6              UPDATE AVAILABLE CORE          S22024
         ST    R9,TOTMMSPC        SAVE NEW VALUE                 S22024
         AR    R11,R9             GET TOTAL OF CORE IN USE AND   S22024
*                                 CORE AVAILABLE                 S22024
         S     R11,K8192          SUBTRACT DEFAULT VALUE OF 8K   S22024
         LTR   R11,R11            HAS ANY CORE BEEN OBTAINED     S22024
*                                 FROM RESPL                     S22024
         SR    R8,R8              CLEAR REG                      S22024
         D     R8,K8192           GET 8K MULTIPLES               S22024
         LTR   R9,R9              LESS THAN 1K                   S22024
         BZ    COR16              YES - RETURN                   S22024
*                                                                S22024
         ST    R8,TOTMMSPC        SET NEW BYTE VALUE             S22024
         L     R8,TOTRESPL        GET ADDR OF RESPL P-LIST       S22024
         USING RESPL,R8                                          S22024
         LH    R12,RESBKTOT       GET CURRENT 1K BLOCKS  COUNT   S22024
         AR    R9,R12             UPDATE BLOCK COUNT             S22024
         STH   R9,RESBKTOT        REPLACE BLOCK COUNT            S22024
         DROP  R8                                                S22024
COR16    EQU   *                                                 S22024
         L     R6,CORSAVE2        RESTORE REG                    S22024
         LM    R8,R12,CORSAVE1    RESTORE REG                    S22024
         BR    R3                 RETURN                         S22024
*                                                                S22024
COR20    EQU   *                  LOAD REQUEST                   S22024
*                                                                S22024
         L     R9,TOTWKSPC        SIZE OF MODULE                 S22024
         SRL   R9,D8              CLEAR HIGH ORDER BYTE          S22024
         LR    R11,R9             AMOUNT OF CORE TO BE TAKEN     S22024
COR24    EQU   *                                                 S22024
         CL    R9,TOTMMSPC        CORE AVAIL IN SUB-TOTAL        S22024
         BNH   COR32              YES                            S22024
         L     R12,TOTRESPL       GET ADDR OF RES. P-LIST        S22024
         USING RESPL,R12                                         S22024
         LH    R8,RESBKTOT        GET CURRENT 1K BLOCK COUNT     S22024
COR28    EQU   *                                                 S22024
         LTR   R8,R8              COUNT ZERO ?                   S22024
         BZ    CORERR             YES - NOT ENOUGH CORE AVAIL.   S22024
         BCTR  R8,R0              DECREMENT COUNT                S22024
         STH   R8,RESBKTOT        UPDATE AVAIL. CORE             S22024
         L     R10,TOTMMSPC       GET SUB-TOTAL COUNT            S22024
         A     R10,K1024          ADD 1K TO SUB TOTAL            S22024
         ST    R10,TOTMMSPC       UPDATE SUB-TOTAL               S22024
         B     COR24              MAKE ANOTHER  CHECK            S22024
         DROP  R12                                               S22024
*                                                                S22024
COR32    EQU   *                  ENOUGH CORE - UPDATE SUB-TOTAL S22024
*                                                                S22024
         L     R12,TOTMMSPC       GET SUB TOTAL                  S22024
         A     R11,TOTUSCOR       UPDATE CORE IN USE BY THIS OLT S22024
         ST    R11,TOTUSCOR       REPLACE IT                     S22024
         SR    R12,R9             SUBTRACT MODULE SIZE           S22024
         ST    R12,TOTMMSPC       REPLACE SUB TOTAL              S22024
         B     COR16              RETURN                         S22024
*                                                                S22024
CORERR   EQU   *   CORE NOT AVAILABLE                            S22024
*                                                                S22024
         LA    R15,NOCOR          SET NOT ENOUGH CORE RET. CODE  S22024
         B     MOREND             EXIT                           S22024
         EJECT
*
*        C O N S T A N T
*
START    DS    0F *************** START OF DATA TO BE MOVED    @Y17XAUU
QCADR    DC    F'0'               ADDR OF CORE GOTTEN          @Y17XAUU
QCCNT    DC    F'0'               SIZE OF CORE GOTTEN          @Y17XAUU
         SPACE
QCLIM    DC    F'0'               MIN CORE REQUIRED            @Y17XAUU
         DC    F'0'               MAX CORE REQUIRED
QORSAVE1 DC    5F'0'              SAVE AREA                    @Y17XAUU
QORSAVE2 DC    F'0'               SAVE AREA                    @Y17XAUU
QORFLG1  DC    X'00'              CONTROL FLAG                 @Y17XAUU
END      DS    0C *************** END OF DATA TO BE MOVED      @Y17XAUU
         SPACE
         DS    0F                 BOUNDARY ALLIGNMENT
PATCH    DC    100XL1'40'         PATCH AREA
K1024    DC    F'1024'            VALUE OF 1K                    S22024
K8192    DC    F'8192'            VALUE OF 8K                    S22024
         EJECT
RESPL    RESPL                                                   S22024
         EJECT
PLIST    DSECT
PFLG     DS    CL1                CONTROL PROGRAM FLAGS
PLEV     DS    AL1                MACRO LEVEL
PLID     DS    CL2                MACRO ID
PCORE1   DS    A                  CORE PTR / MAXIMUM CORE SIZE REQ.
PCORE2   DS    A                  MINIMUM CORE SIZE REQUESTED
         SPACE 4
         OLTCB                                                   S22024
BLIST    DC    3F'0'              GETMAIN/FREEMAIN PLIST       @Y17XAUU
MCADR    DC    F'0'               ADDR OF CORE GOTTEN          @Y17XAUU
MCCNT    DC    F'0'               SIZE OF CORE GOTTEN          @Y17XAUU
MCLIM    DC    F'0'               MINIMUM CORE REQUIRED        @Y17XAUU
         DC    F'0'               MAX CORE REQUIRED            @Y17XAUU
CORSAVE1 DC    5F'0'              SAVE AREA                    @Y17XAUU
CORSAVE2 DC    F'0'               SAVE AREA                    @Y17XAUU
MORFLG1  DC    X'00'              CONTROL FLAG                 @Y17XAUU
         END
