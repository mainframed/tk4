         TITLE 'IEDQWE - TOTE TEST CONTROL MODULE'
IEDQWE   CSECT
*A000000-999999                                                @Y16X5U0
*  TOTE ALIAS                                                  @OY13283
*** THIS MODULE IS RESEQUENCED AS OF TCAM 10.0                 @Y17XAUU
* CHANGE ACTIVITY AS FOLLOWS:                                   SA41597
* C011000-011500,C018500,A018700,C035800-035890,                SA41597
* C039300,A039400,A482100-482400                                SA41597
* A074500-075500,A453960-453990,A474935-474938                  SA41597
* A073700-074300,C086000,C097800,C104000,A116700-116750,         S22024
* A118130-118160,C120000,C134400,C137000,C149000,C153600,C154600 S22024
* C157000,A161000-163000,C192000,C194400,C195100,A195860-195920  S22024
* C197200,C202000,C211000,C215000,C219000-219500,C227000         S22024
* C235100,A237700-238000,C243340,C243700,A283660-283720,C296000  S22024
* C299550,C300000-301000,C303000,C308000,A327000-335000          S22024
* C339000,C345000,C348000,A352000-412660,C503000,C506000-508000  S22024
* D584000-589381,A589650,C589765,C589790,C589810,A589963-589966  S22024
*A073800,076200,344100-344800,351200-351800,382300-409600        S05331
*C139000,155200,285000,290400-291000,295200                    @YA12242
*D144000                                                       @YA12242
*A062500,412120,412440,412660,589580,589755                    @Y17XAUU
*C505000                                                       @Y17XAUU
*D351200-351600,382900-409300                                  @Y17XAUU
* A283500,378000,378400,850000                                 @G36XRUV
* C284400,378400                                               @G36XRUV
***********************************************************************
*                                                              @Y17XAUU
* DESCRIPTIVE NAME: 'OLT TEST CONTROL MODULE'                   SA41597
*                                                              @Y17XAUU
* MODULE NAME:  IEDQWE (TCAM,TOTE)                             @Y17XAUU
*                                                                     *
* STATUS:  VERSION 10.0                                        @Y17XAUU
*                                                                     *
* ENTRY POINT:                                                        *
*                                                                     *
*       IEDQWE CALLED BY THE TOTE DISPATCHER MODULE IEDQWD            *
*       IEDQWE1 ON RETURN FROM IEDQWF.                                *
*                                                                     *
* FUNCTIONS:                                                          *
*       THE PURPOSE OF THIS MODULE IS TO SCHEDULE THE OLT'S REQUESTED *
*       IN THE TRM AND TO CLEAN UP AFTER THE EXECUTION OF EACH.       *
*                                                                     *
*       IF THE SECTION-TERMINATE FLAG IS SET, REQUIRED CLEANUP IS     *
*       DONE AND IMMEDIATE RETURN IS MADE TO THE TOTE RESIDENT        *
*        MODULE, UNLESS ANY TEST DEVICE IS ON A CONCENTRATOR    SA41597
*        THEN EXIT IS TO IEDQWEC.                               SA41597
*                                                                     *
*       REGISTER POINTERS ARE INITIALIZED FOR SECTION, UCB AND DEVICE *
*       TABLES.  THE SIZE OF THE OLT ROOT MODULE IS OBTAINED BY       *
*       BLDL MACRO.  IF BLDL WAS UNSUCCESSFUL, A MESSAGE IS           *
*       SENT TO THE CONTROL TERMINAL AND A CHECK MADE FOR ANOTHER     *
*       MODULE REQUESTED.  IF SUCCESSFUL, MODULE SIZE IS CHECKED      *
*       AGAINST AVAILABLE CORE.  IF TOTAL CORE IS INSUFFICIENT, A     *
*       MESSAGE IS SENT TO CONTROL TERMINAL AND A CHECK MADE          *
*       FOR ANOTHER MODULE REQUESTED.  IF ALLOCATED CORE IS           *
*       INSUFFICIENT, IEDQWA IS POSTED FOR THE ASSIGNMENT OF          *
*       ADDITIONAL CORE.                                              *
*                                                                     *
*       IF CORE IS SUFFICIENT, A SECTION START MESSAGE IS SENT TO     *
*       CONTROL TERMINAL AND THE MODULE XCTL'S TO IEDQWF.             *
*       UPON RETURN FROM IEDQWF A TERMINATE MESSAGE IS SENT           *
*       TO CONTROL TERMINAL AND A CHECK IS MADE FOR ANOTHER MODULE    *
*       REQUESTED.                                                    *
*                                                                     *
*       WHEN ANOTHER OLT MODULE IS REQUESTED IT WILL BE EXECUTED      *
*       AS DESCRIBED ABOVE.  IF A NEW MODULE IS NOT REQUIRED, A CHECK *
*       IS MADE FOR ANOTHER DEVICE REQUIRED.  IF A DEVICE IS REQUIRED,*
*       TABLES AND POINTERS ARE UPDATED AND THE OLT MODULE IS         *
*       EXECUTED AGAIN.                                               *
*                                                                     *
*       WHEN AN OLT IS FINISHED WITH ALL REQUESTED DEVICES, A CHECK   *
*       IS MADE FOR TEST LOOP REQUESTED.  IF YES, THE LOOP COUNT IS   *
*       DECREMENTED AND THE OLT MODULE IS RE-EXECUTED UNTIL THE       *
*       COUNT IS EXHAUSTED.                                           *
*                                                                     *
*       WHEN THE TEST REQUEST IS SATISFIED ANY STILL LOADED MODULES   *
*       ARE DELETED.  IF THE TEST DEVICES ARE TERMINALS A START       *
*       TERMINAL REQUEST IS POSTED TO OPERATOR CONTROL FOR EACH.      *
*       IF THEY ARE TCU'S A START LINE REQUEST IS POSTED FOR EACH.    *
*       FINALLY RETURN IS MADE TO IEDQWA.                             *
*                                                              @Y17XAUU
* PURPOSE: SEE FUNCTION                                        @Y17XAUU
*                                                              @Y17XAUU
* MODULE TYPE: PROCEDURE                                       @Y17XAUU
*                                                              @Y17XAUU
* PROCESSOR: ASSEMBLER XF                                      @Y17XAUU
*                                                              @Y17XAUU
* MODULE SIZE: 4K                                              @Y17XAUU
*                                                              @Y17XAUU
* LINKAGE: XCTL'ED FROM IEDQWD AND IEDQWF                      @Y17XAUU
*                                                              @Y17XAUU
* MACROS: AQCTL                                                @Y17XAUU
*         BLDL                                                 @Y17XAUU
*         CLOSE                                                @Y17XAUU
*         DELETE                                               @Y17XAUU
*         IEDHJN                                               @Y17XAUU
*         IEDQMSG                                              @Y17XAUU
*         LINK                                                 @Y17XAUU
*         LOAD                                                 @Y17XAUU
*         OPEN                                                 @Y17XAUU
*         POST                                                 @Y17XAUU
*         SAVE                                                 @Y17XAUU
*         WAIT                                                 @Y17XAUU
*         XCTL                                                 @Y17XAUU
*                                                                     *
* INPUT:        REGISTER 02,13,14,15 CONTAIN THE FOLLOWING VALUES:    *
*                                                                     *
*                         02-OLTCB POINTER                            *
*                         13-SAVE AREA ADDRESS                        *
*                         14-RETURN ADDRESS                           *
*                         15-ENTRY POINT ADDRESS                      *
*                                                                     *
*                                                                     *
* COPYRIGHT: 'NONE'                                             SA41597
*                                                                     *
*                                                                     *
* EXTERNAL ROUTINES:                                                  *
*                                                                     *
*       IEDQWK - TOTE MESSAGE MODULE                                  *
*       IGC00059                                                      *
*       IEDQCA - TCAM STARTLINE                                       *
*       IEDQCA - TCAM STOP LINE                                       *
*                                                                     *
* EXIT POINTS:  XCTL AFTER 'TCNTL06'  TO IEDQWF                       *
*                 BR AFTER 'RETURN4'  TO IEDQWA                       *
*                                                                     *
* TABLES/WORK AREAS:                                                  *
*                                                                     *
*       OLTCB, UCB                                                    *
*                                                                     *
* ATTRIBUTES:                                                         *
*                                                                     *
*       ENABLED, PROBLEM PROGRAM MODE, TRANSIENT.                     *
*                                                              @Y17XAUU
* NOTES: SEE BELOW                                             @Y17XAUU
*                                                              @Y17XAUU
*  DEPENDENCIES: EBCDIC CHARACTER CODE                         @Y17XAUU
*                                                              @Y17XAUU
*  RESTRICTION: NONE                                           @Y17XAUU
*                                                              @Y17XAUU
*  REGISTER CONVENTIONS: SEE REGISTERS ASSIGNMENT              @Y17XAUU
*                                                              @Y17XAUU
*  PATCH LABEL: PATCH                                          @Y17XAUU
*                                                              @Y17XAUU
***********************************************************************
         EJECT
*
*        REGISTER EQUATES
*
R0       EQU   0                       REG 0                     S99528
PARMREG0 EQU   0                       PARAMETER REGISTER      @XM05678
PARMREG1 EQU   1                       PARAMETER REGISTER      @XM05678
R1       EQU   1                       REG 1                     S99528
R2       EQU   2                       REG 2                     S99528
R3       EQU   3                       REG 3                     S99528
R4       EQU   4                       REG 4                     S99528
R5       EQU   5                       REG 5                     S99528
R6       EQU   6                       REG 6                     S99528
R7       EQU   7                       REG 7                     S99528
R8       EQU   8                       REG 8                     S99528
R9       EQU   9                       REG 9                     S99528
R10      EQU   10                      REG 10                    S99528
R11      EQU   11                      REG 11                    S99528
R12      EQU   12                      REG 12                    S99528
R13      EQU   13                      REG 13                    S99528
R14      EQU   14                      REG 14                    S99528
R15      EQU   15                      REG 15                    S99528
DISREG2  EQU   16                      DISPLACEMENT TO REG2    @Y17XAUU
D0       EQU   0                       DISPLACEMENT OF 0       @Y17XAUU
D3       EQU   3                       DISPLACEMENT OF 3       @Y17XAUU
JFCBTSDM EQU   X'08'                   DISP                      S99528
JFCBLTYP EQU   X'02'                   TYPE                      S99528
ZERO     EQU   0                       ZERO                      S99528
ONE      EQU   1                       ONE                       S99528
TWO      EQU   2                       TWO                       S99528
THREE    EQU   3                       THREE                     S99528
FOUR     EQU   4                       FOUR                      S99528
FIVE     EQU   5                       FIVE                      S99528
SIX      EQU   6                       SIX                       S99528
SEVEN    EQU   7                       SEVEN                     S99528
EIGHT    EQU   8                       EIGHT                     S99528
NINE     EQU   9                       NINE                      S99528
TENN     EQU   10                      TEN                       S99528
ONEONE   EQU   11                      ELEVEN                    S99528
D34      EQU   34                      DISPLACEMENT 34           S99528
DEVTBEND EQU   X'00'                   TEST DEVICE TABLE END CODES99528
DEC58    EQU   58                      FIFTY EIGHT               S99528
SECTBEND EQU   X'40'                   SECTION TABLE END CODE    S99528
STRTCODE EQU   X'40'                   START CODE                S99528
STOPCODE EQU   X'41'                   START LINE                S99528
LINESTRT EQU   X'40'                   STOP LINE                 S99528
LINESTOP EQU   X'41'                   VERB CODE                 S99528
TERMSTRT EQU   X'45'                   VERB CODE START TERM      S99528
TERMSTOP EQU   X'47'                   TERMINAL CODE STOP        S99528
HOLDTERM EQU   X'88'                   HOLD TERMINAL VERB CODE   S99528
ACTIVETM EQU   X'46'                   ACTIVATE TERM VERB CODE   S99528
REALTERM EQU   X'87'                   RELEASE TERM VERB CODE    S99528
INACTERM EQU   X'48'                   INACTIVATE VERB CODE      S99528
BLANK    EQU   X'40'                   INCLUSIVE DLIM            S99528
D1       EQU   1                       ONE                       S22024
D2       EQU   2                       TWO                       S22024
D8       EQU   8                       EIGHT                     S22024
D4       EQU   4                       FOUR                      S22024
D12      EQU   12                      CONSTANT DISPLACEMENT     S05331
D32      EQU   32                      LCB ADJUSTMENT            S22024
D62      EQU   62                      DISP 62                   S22024
D64      EQU   64                      DISP 64                   S22024
TOTEIND  EQU   X'00'                   CLEAR TOTE INDICATOR     SA41597
*                                      IN THE LCB               SA41597
XFF      EQU   X'FF'                   USED TO CLEAR BITS        S05331
ADRMSK   EQU   7                             ADDRESS MASK      @Y17XAUU
BCDSLEN  EQU   20                      CDS LEN TO MOVE TO OLTCB@Y17XAUU
*
         EJECT
         SAVE  (14,12)                 SAVE REGS                 S99528
         USING TOTOLTCB,R2             TOTE CONTROL BLOCK        S22024
*
*        SATISFY LINKAGE CONVENTIONS
*
         LR    R4,R15                  ESTABLISH BASE REG        S99528
         USING IEDQWE,R4               IEDQWE                    S99528
         ST    R13,TOTSAVE4+FOUR       SAVE CALLERS SAVE ADDR    S99528
         LR    R12,R13                 SAVE IT HERE TOO          S99528
         LA    R13,TOTSAVE4            MY SAVE AREA ADDRESS      S99528
         ST    R13,EIGHT(R12)          MY SAVE TO HIS            S99528
IEDQWE   IEDHJN IEDQWEE,HJN            NAME AND HJN              S99528
         TM    TOTFLG06,TOTOTERM+TOTCANCL  CANCELOR TERMINATE    S99528
         BNZ   TCNTL07                 YES BRANCH                S99528
         LA    R11,TOTOLTTB            SECTION ID TABLE ADDRESS  S99528
         ST    R11,TOTSAVE5+D4         SAVE ADDRESS              S22024
         LA    R11,TOTTDTBL            RESTORE FIRST DEV ENTRY   S99528
         ST    R11,TOTGRABP            GRAB POINTER              S99528
         ST    R11,TOTSAVE5            CURRENT DEVICE            S22024
TCNTL004 EQU   *
*
*        INITIALIZE TABLES & POINTERS
*
*
TCNTL01  EQU   *
***
***
***     FIND UCB FOR FIRST TEST DEVICE
**AFTER UCB FOUND BAL TO FIND DDNAME AND OPEN
         L     R8,TOTTDTBL             GET TNT OR UCB            S99528
         ST    R8,SAVEENT              SAVE STATUS               S22024
         TM    SAVEENT,TOTTDORN        TEST DEVICE ON RN         S22024
         BZ    NORNTST                 NO BRANCH                 S22024
         BAL   R12,QCBFIND             GO FIND QCB AND LCB       S22024
         BAL   R9,DUMMYTNT             CHECK FOR COUNTERFEITING@Y17XAUU
         B     PRIENT                  GET FIRST SECTION ID      S22024
NORNTST  LA    R8,0(R8)                CLEAR HI BYTE             S22024
NEXTDEV  EQU   *                                                 S22024
         TM    SAVEENT,TOTTDLIN        LINES TESTED              S22024
         BO    STORE                   YES BRANCH                S99528
         BAL   R12,UCBFIND             FIND UCB FOR TERMINALS    S99528
STORE    STH   R8,ADDOFUCB             SAVE UCB ADDRESS          S99528
         BAL   R12,DDMOVE              GO FIND DDNAME IN TIOT    S99528
**                                     TO MOVE TO DCB AND OPEN   S99528
**       SET UP FIRST PRIMARY DEVICE ENTRY IN TOTPRENT           S99528
FSTPRI   MVC   TOTPRUCB,ADDOFUCB       MOVE UCB ADD TO TOTPRENT  S99528
         TM    SAVEENT,TOTTDLIN        LINES TESTED              S22024
         BZ    PRIENT                  NO BRANCH                 S99528
         B     FSTSECID                SET UP FIRST SECTION ID   S99528
PRIENT   EQU   *                                                 S99528
         L     R8,0(R11)               GET REAL TNT POINTER      S99528
         ST    R8,TOTTNTPR             SAVE IN TOTPRENT          S99528
         TM    SAVEENT,TOTTDORN        TEST DEVICE ON RN         S22024
         BO    FSTSECID                YES BRANCH                S22024
***    BAL TO ROUTINE TO FIND PLN AND LCB ADDRESS   GIVEN TNT ADDÙ99528
         BAL   R12,RLNLCB              LCB AND RLN INTO TOTPRENT S99528
FSTSECID EQU   *                                                 S99528
         MVC   TOTCRALP(3),TOTOLTTB    GET FIRST OLT ID          S99528
         LA    R6,TOTOLTTB             UPDATE OLT ID             S99528
          ST    R6,TOTOLTPT             TABLE POINTER            S99528
         TM    TOTFLG03,TOTTSINC       INCLUSIVE ID ENTRY        S99528
         BZ    TCNT01A                 NO BRANCH                 S99528
         MVI   TOTCRFLG,TOTFTTIM       TURN ON FST PASS FLAG     S99528
TCNT01A  EQU   *
         NI    $ERROPT,X'FF'-$TESTERR-$ASTERIK RESET ERR FLGS    S22024
*        GET SIZE OF OLT ROOT MODULE
*             & UPDATE CORE AVAILABLE TO SUB-MODULES
*
         XC    TCNTBLDL(62),TCNTBLDL   CLEAR PARM LIST FOR BLDL
         MVI   TCNTBECT,ONE            SET ENTRY COUNT TO ONE
         MVI   TCNTBELN,DEC58          SET ENTRY LENGTH TO 58
         MVC   TCNTMDNM(8),TOTCROLT    MOVE IN MODULE NAME
         BLDL  0,TCNTBLDL              ISSUE BLDL
         LTR   R15,R15                WAS IT SUCCESSFUL?
         BZ    TCNTL04                  YES
TCNTL02  EQU   *
******
******
******   IED330I   OLT MODULE XXXXXX NOT LOADABLE                S22024
******
         MVC   NOTLOAD+ONEONE(EIGHT),TOTCROLT  OLT NAME          S99528
         IEDQMSG OTBUF=NOTLOADD,OTCNT=40,PREG0=R0,PREG1=R1,      S22024C
               FUNCT=(CEC,PRT),RET=TCNT490                     @YA12242
****  BRANCH TO AREA TO FIND THE NEXT OLT ID
TCNTL04  EQU   *
         L     R7,TCNTMDSZ        GET OLT SCHEDULER SIZE
         SRL   R7,8                 MODULE SIZE
         L     R8,TOTMMSPC             TOTAL CORE AVAIL TO OLT   S22024
         SR    R8,R7                  IS THERE ENOUGH CORE?
         BNM   TCNT043                  YES
         BAL   R12,TCNTL30              NO: GO GET MORE ASSIGNED
         B     TCNTL04            ENOUGH CORE WAS AVAILABLE, CONTINUE
************
************
*********   IED331I  NOT ENOUGH CORE FOR SECTION XXX             S22024
*********
         MVC   NOCORE+29(8),TOTCROLT   OLT ID                    S99528
         IEDQMSG OTBUF=NOCOREE,OTCNT=45,PREG0=R0,PREG1=R1,       S22024C
               FUNCT=(CEC,PRT),RET=TCNT490                     @YA12242
TCNT043  EQU   *
         ST    R8,TOTMMSPC             UPDATE CORE AVAILABLE     S22024
         A     R7,TOTUSCOR        ADD SIZE TO USED CORE          Y06330
         ST    R7,TOTUSCOR             SAVE SIZE OF ROOT MODULE  S22024
*                                        IN CORE IN USE FIELD    S22024
         AR    R8,R7                ADD SIZE OF DELETED OLT
         LR    R11,R8             MOVE IT TO WORK REG
         SRL   R11,2              GET AVAIL CORE IN WORDS
         STH   R11,$OLTSIZE            STORE IN SCT              S22024
         TM    SAVEENT,TOTTDTRM+TOTTDORN IS ENTRY TNT            S22024
         BNZ   NAMEBULD                YES BRANCH                S22024
         SR    R5,R5                   CLEAR                   @G36XRUV
         ICM   R5,3,TOTPRUCB           GET UCB ADDRESS         @G36XRUV
         USING UCBOB,R5                                          S99528
         MVC   CDSLINE+5(3),UCBNAME    GET LINE ADDRESS          S99528
         DROP  R5                                                S99528
LOADNAME EQU   *                                                 S99528
         LOAD  EPLOC=CDSLINE           BRING MEMBER IN CORE      S99528
         LR    R6,R0                   EP OF CDS                 S99528
         MVC   $PDEVADR(20),0(R6)      CDS TO SCT                S99528
         B     DELETE                  GO TO DELETE CDS          S99528
NAMEBULD EQU   *                                                 S99528
         MVI   CDSLINE,C' '            BLANK OUT                 S99528
         MVC   CDSLINE+1(7),CDSLINE    EIGHT CHARACTER NAME      S99528
         L     R12,TOTTNTPR            GET SAVED TNT ADDRESS     S99528
         LA    R9,CDSLINE              GET ADDRESS OF NAME FIELD S99528
         SR    R3,R3                   CLEAR REG                 S99528
         IC    R3,TOTTTBEL             GET NAME LRNGTH           S99528
         BCTR  R3,0                    DECREMENT COUNT FOR EX    S99528
         EX    R3,NAMEMOV              GO MOVE NAME              S99528
         L     R8,TNTENTRY        GET TTE ADDRESS              @Y17XAUU
         LA    R1,TRMPRFSZ        GET TTE PREFIX SIZE          @Y17XAUU
         SLR   R8,R1              POINT TO TTE PREFIX          @Y17XAUU
         USING IEDNTRM,R8         SET TTE ADDRESSABILITY       @Y17XAUU
         TM    TRMSTATE,TRMPREF   3705 RESOURCE                @Y17XAUU
         BZ    LOADNAME           NO - LOAD CDS                @Y17XAUU
         CLI   TRMTYPE,TRMNSDLC   PRE SNA LINK                 @XM05808
         BE    BLDLCDS            YES - CHECK FOR CDS IN DS    @XM05808
         CLI   TRMTYPE,TRMSDLC    SNA LINK                     @Y17XAUU
         BE    BLDLCDS            YES -CHECK FOR CDS IN DS     @Y17XAUU
         CLI   TRMTYPE,TRMLUNT    LU ENTRY                     @Y17XAUU
         BNE   LOADNAME           NO - LOAD CDS                @Y17XAUU
BLDLCDS  DS    0H                 BOUNDARY ALLIGNMENT          @Y17XAUU
         MVC   READNAME(D8),CDSLINE COPY NAME TO BLDL PLIST    @Y17XAUU
         BLDL  0,BLDLLIST         FIND CDS ENTRY               @Y17XAUU
         LTR   R15,R15            ENTRY FOUND                  @Y17XAUU
         BZ    LOADNAME           YES - LOAD CDS               @Y17XAUU
         DROP  R8                                              @Y17XAUU
*        CONSTRUCT CDS FOR 3767/3770 OR FOR A 3705 LINE        @Y17XAUU
         LA    R6,$PDEVADR        POINT TO PLACE TO            @Y17XAUU
*                                 BUILD CDS RECORD             @Y17XAUU
         USING IEDTCDSD,R6        SET CDS REC ADDRESSABILITY   @Y17XAUU
         XC    $PDEVADR(BCDSLEN),$PDEVADR CLEAR CDS AREA       @Y17XAUU
         MVI   TCDSCLS,TCDSCL37   SET CLASS FIELD              @Y17XAUU
         MVI   TCDSTYP,TCDSTY37   SET TYPE FIELD               @Y17XAUU
         MVI   TCDSFLG2,TCDSSEC   SET FLAG FIELD               @Y17XAUU
         MVC   TCDSNAM(D8),CDSLINE COPY SYMBOLIC NAME          @Y17XAUU
         MVI   TCDSCNT,TCDSSNA    SET CDS LENGTH 3767/3770     @Y17XAUU
         L     R8,NCPDCB          GET 3705 DCB ADDRESS         @XM05808
         USING IHADCB,R8          SET DCB ADDRESSABILITY       @XM05808
         L     R8,DCBDEBAD        GET DEB ADDRESS              @XM05808
         DROP  R8                                              @XM05808
         USING DEBNMSUB,R8        SET DEB ADDRESSABILITY       @XM05808
         SLR   R15,R15            CLEAR REGISTER               @XM05808
         IC    R15,DEBNMEXT       GET NUMBER OF EXTENTS        @XM05808
         SLL   R15,4              MULT BY FOUR                 @XM05808
         LA    R3,4               SET OFFSET TO UCB ADDRESS    @XM05808
         L     R8,DEBUCBAD-4(R3)  GET UCB ADDRESS              @XM05808
         DROP  R8                                              @XM05808
         USING UCB,R8             SET UCB ADDRESSABILITY       @Y17XAUU
         MVC   TCDSUCB+D2(D2),UCBCHA  SET 3705 UCB CUU         @Y17XAUU
         DROP  R8                                              @Y17XAUU
         L     R8,TNTENTRY        GET TTE ADDRESS              @Y17XAUU
         LA    R1,TRMPRFSZ        GET TTE PREFIX SIZE          @Y17XAUU
         SLR   R8,R1              POINT TO TTE PREFIX          @Y17XAUU
         USING IEDNTRM,R8         SET TTE ADDRESSABILITY       @Y17XAUU
         CLI   TRMTYPE,TRMLUNT    LU ENTRY                     @Y17XAUU
         BE    NODELETE           CONTINUE PROCESSING          @Y17XAUU
         MVI   TCDSCNT,TCDSLINK   SET LINE CDS BYTE COUNT      @Y17XAUU
         B     NODELETE           CONTINUE PROCESSING          @Y17XAUU
         DROP  R6                                              @Y17XAUU
         DROP  R8                                              @YM07769
NAMEMOV  MVC   0(0,R9),0(R12)          MOVE NAME FROM TERMINAL   S99528
DELETE   DELETE EPLOC=CDSLINE          DELETE CDS                S99528
*        SET BIT IN SCT TO INDICATE IF THE CONTROL TERMINAL      S22024
*        AND TEST TERMINAL ARE ON THE SAME COMM LINE             S22024
NODELETE DS    0H                 BOUNDARY ALLIGNMENT          @Y17XAUU
         NI    $TOTFLG1,XFF-$CTEQTT    RESET CT ON TEST LINE   @Y17XAUU
*                                      FLAG                    @Y17XAUU
         TM    TOTCTFLG,TOTCTORN       CT ON A RN                S22024
         BZ    CTNOTRN                 NO BRANCH                 S22024
         TM    SAVEENT,TOTTDORN        TEST DEV ON RN            S22024
         BZ    STRTMSG                 NO BRANCH                 S22024
         CLC   NAMESAVE(D8),TOTCTRNL   GROUP NAMES THE SAME      S22024
         BNE   STRTMSG                 NO BRANCH                 S22024
         CLC   TOTCTRLN(D1),TOTPRRLN   RLNS THE SAME             S22024
         BNE   STRTMSG                 NO BRANCH                 S22024
         L     R1,TNTENTRY             GET TTE ADDRESS         @Y17XAUU
         LA    R14,TRMPRFSZ            GET TTE PREFIX SIZE     @Y17XAUU
         SLR   R1,R14                  POINT TO TTE PREFIX     @Y17XAUU
         USING IEDNTRM,R1              SET ADDRESSABILITY      @Y17XAUU
         CLI   TRMTYPE,TRMLUNT         LU ENTRY                @Y17XAUU
         BNE   CTTTSAME                NO                      @Y17XAUU
         DROP  R1                                              @Y17XAUU
         CLC   TOTCRTNT+D1(D3),TOTTNTPR+D1 LU CT EQU TD        @Y17XAUU
         BNE   STRTMSG                 NO - BYPASS SETTING CT  @Y17XAUU
*                                      EQU TD FLAG             @Y17XAUU
         NI    $OLTFLGS,XFF-$TRACE     INSURE TRACE OPT OFF    @XM05673
CTTTSAME EQU   *                                                 S22024
         OI    $TOTFLG1,$CTEQTT        CT AND TEST DEV SAME LINE S22024
         B     STRTMSG                 BRANCH                    S22024
CTNOTRN  EQU   *                                                 S22024
         TM    SAVEENT,TOTTDORN        TEST DEV ON RN            S22024
         BO    STRTMSG                 YES BRANCH                S22024
         CLC   TOTCTUCB(D2),TOTPRUCB   UCBS THE SEME             S22024
         BE    CTTTSAME                YES BRANCH                S22024
STRTMSG  EQU   *                                                 S22024
         NI    TOTFLG01,XFF-TOTAPTST   RESET AP ON TEST LINE   @Y17XAUU
*                                      FLAG                    @Y17XAUU
         TM    TOTFLG09,TOTPRECT       CT EQU TD               @Y17XAUU
         BO    STRTMSG1                YES - BYPASS CHECK      @Y17XAUU
         TM    TOTAPFLG,TOTAPORN       AP ON 3705              @Y17XAUU
         BZ    STRTMSG1                NO - SEND MESSAGE       @Y17XAUU
         TM    SAVEENT,TOTTDORN        TEST DEVICE ON 3705     @Y17XAUU
         BZ    STRTMSG1                NO - SEND MESSAGE       @Y17XAUU
         CLC   NAMESAVE(D8),TOTAPRNL   SAME GROUP              @Y17XAUU
         BNE   STRTMSG1                NO - SEND MESSAGE       @Y17XAUU
         CLC   TOTAPRLN(D1),TOTPRRLN   AP ON TEST DEVICE LINE  @Y17XAUU
         BNE   STRTMSG1                NO - SEND MESSAGE       @Y17XAUU
         USING IEDNTRM,R1              SET ADDRESSABILITY      @Y17XAUU
         CLI   TRMTYPE,TRMLUNT         LU ENTRY                @Y17XAUU
         BNE   APTTSAME                NO                      @Y17XAUU
         DROP  R1                                              @Y17XAUU
         CLC   TOTARTNT+D1(D3),TOTTNTPR+D1 LU AP = TD          @Y17XAUU
         BNE   STRTMSG1                NO - BYPASS SETTING AP  @Y17XAUU
*                                      EQU TD FLAG             @Y17XAUU
         NI    $OLTFLGS,XFF-$TRACE     INSURE TRACE OPT OFF    @XM05673
APTTSAME DS    0H                      BOUNDARY ALLIGNMENT     @Y17XAUU
         OI    TOTFLG01,TOTAPTST       SHOW AP ON TEST LINE    @Y17XAUU
STRTMSG1 EQU   *                                               @Y17XAUU
*
*        SEND SECTION START MESSAGE
*
         TM    $ERROPT,$NOCNTRL        NO CONTROL PRINT          S22024
         BO    TCNTL06              YES: SKIP MESSAGE
********
********
********   IED333I  S XXXX  UNIT  XXXX                           S22024
********
         XC    UNIT,UNIT               CLEAR NAME                S99528
         MVC   TERMIAT,STARTID         START ID TO MSG           S22024
         MVI   TERMIATE+1,S            START                     S99528
         MVC   TERMIATE+3(8),TOTCROLT  SEC ID                    S99528
         TM    SAVEENT,TOTTDLIN        LINES TESTED              S22024
         BZ    NAMEFIND                NO BRANCH                 S99528
         TM    SAVEENT,TOTTDORN        LINE ON RN                S22024
         BO    NAMEFIND                YES BRANCH                S22024
         L     R1,TOTPRUCB             GET UCB                   S99528
         SRL   R1,16                   ADDRESS                   S99528
         USING UCB,R1                                            S99528
         MVC   UNIT+5(3),UCBNAME       MOVE NAME                 S99528
         DROP R1                                                 S99528
MSGCOMP  MVC   TERMIATE+18(8),UNIT     FILL IN UNIT              S99528
         OI    TOTFLG05,TOTCECOM       TURN ON CECOM FLAG        S99528
         NI    TOTFLG05,X'FF'-TOTREPLY TURN OFF REPLY FLAG       S99528
         TM    TOTFLG09,TOTAPTRM       AP EQUAL CONTROL TERMINAL S99528
         BO    STARTMSG                GO TO PRINT START MSG     S99528
         OI    TOTFLG05,TOTPRINT       TURN ON PRINT FLAG        S99528
STARTMSG EQU   *                                                 S99528
         IEDQMSG OTBUF=TERMIAT,OTCNT=34,FUNCT=(CEC,PRT),         S22024C
               PREG0=R0,PREG1=R1,RET=TCNTL06                     S99528
NAMEFIND L     R1,TOTTNTPR             GET NAME ADDRESS          S99528
         SR    R15,R15                 CLEAR REG                 S99528
         IC    R15,TOTTTBEL            NAME LENGTH               S99528
         BCTR  R15,0                   DECREMENT COUNT           S99528
         EX    R15,NAMEMOVE            GO MOVE NAME              S99528
         B     MSGCOMP                 BRANCH                    S99528
TCNTL06  EQU   *
*
         OI    TOTFLG02,TOTCHKSZ  INDICATE SIZE CHECK OF
*                                   OLT IS REQUIRED
         OI    TOTFLG06,TOTOTACT       SET OLT ACTIVE FLG        S22024
         L     R13,4(R13)         GET CALLER'S SAVE
         L     R14,12(R13)        GET RETURN ADDRESS
         XCTL  (2,12),EP=IEDQWF   GO TO TEST CONTROL SUB-MODULE
         EJECT
IEDQWE1  EQU   *
         ENTRY IEDQWE1
         DROP  R4
         USING IEDQWE1,R15
         STM   R14,R12,12(R13)    SAVE CALLER'S REGISTERS
         L     R5,TOTFLG01             CONTAINS TOTCLNST       @G36XRUV
CSLOOP1  DS    0H                                              @G36XRUV
         LR    R6,R5                   COPY                    @G36XRUV
         O     R6,SETCLNST             SET CLEANUP START IN REG@G36XRUV
         CS    R5,R6,TOTFLG01          SET TOTCLNST            @G36XRUV
         BNZ   CSLOOP1                 BR IF WORD ALTERED      @G36XRUV
         NI    TOTFLG06,X'FF'-TOTOTACT CLEAR OLT ACTIVE FLAG     S22024
         L     R4,=A(IEDQWE)      GET BASE ADDRESS
         DROP  R15
         USING IEDQWE,R4
         LA    R12,TOTSAVE4            GET MY SAVE ADDRESS       S22024
         ST    R12,8(R13)         SAVE IT IN CALLER'S SAVE
         ST    R13,4(R12)         SAVE POINTER TO CALLER'S SAVE
         LR    R13,R12
         L     R11,TOTSAVE5            CURRENT PRIMARY DEVICE PTRS22024
         L     R6,ZERO(R11)            SAVE ENTRY                S22024
         ST    R6,SAVEENT              TO WORK WITH              S22024
         L     R6,TOTSAVE5+D4          CURREST SECTION ID        S22024
*
*
*        SEND SECTION TERMINATE MESSAGE
*
         TM    $ERROPT,$NOCNTRL        NO CONTROL PRINT FLG SET  S22024
         BO    TCNTL07              YES: SKIP MESSAGE
*
         TM    $ERROPT,$ASTERIK        ASTERIK PRINT NEEDED      S99528
         BZ    TMOVE                   NO BRANCH                 S99528
         MVI   TERMIATE,ASTRICK        ASTERICK TO MSG           S99528
TMOVE    MVI   TERMIATE+1,TEE          MOVE T TO MSG             S99528
         MVC   TERMIAT,TERMID          TERMINATE ID TO MSG       S22024
         MVC   TERMIATE+3(8),TOTCROLT  SEC ID                    S99528
         TM    SAVEENT,TOTTDLIN        LINES TESTED              S22024
         BZ    NAMEFIN                 NO BRANCH                 S99528
         TM    SAVEENT,TOTTDORN        LINE ON RN                S22024
         BO    NAMEFIN                 YES BRANCH                S22024
         L     R1,TOTPRUCB             GET UCB                   S99528
         SRL   R1,16                   ADDRESS                   S99528
         USING UCB,R1                                            S99528
         MVC   UNIT+5(3),UCBNAME       MOVE NAME                 S99528
         DROP R1                                                 S99528
MSGCOM   MVC   TERMIATE+18(8),UNIT     FILL IN UNIT              S99528
         OI    TOTFLG05,TOTCECOM       TURN ON CECOM FLAG        S99528
         NI    TOTFLG05,X'FF'-TOTREPLY TURN OFF REPLY FLAG       S99528
         TM    TOTFLG09,TOTAPTRM       AP EQUAL CONTROL TERMINAL S99528
         BO    STOPMSG                 GO TO PRINT STOP MSG      S99528
         OI    TOTFLG05,TOTPRINT       TURN ON PRINT FLAG        S99528
****
****   IED334I  (*) T  XXX   UNIT   XXX                          S22024
****
STOPMSG  EQU   *                                                 S99528
         IEDQMSG OTBUF=TERMIAT,OTCNT=34,FUNCT=(CEC,PRT),         S22024C
               PREG0=R0,PREG1=R1,RET=TCNTL07                     S99528
NAMEFIN  L     R1,TOTTNTPR             GET NAME ADDRESS          S99528
         SR    R15,R15                 CLEAR REG                 S99528
         IC    R15,TOTTTBEL            NAME LENGTH               S99528
         BCTR  R15,0                   DECREMENT COUNT           S99528
         EX    R15,NAMEMOVE            GO MOVE NAME              S99528
         B     MSGCOM                  BRANCH                    S99528
*
TCNTL07  EQU   *
         MVI   TERMIATE,BLANK          CLEAR ASTRIC              S99528
*
*        DELETE ANY OLT SUBMODULES THAT ARE STILL LOADED
*
         TM    TOTFLG06,TOTABEND  OLT ABEND                      S22024
         BO    TCNT490            YES - BYPASS QCB CHECK         S22024
         LA    R8,TOTPLNKQ        GET ADDR OF PLINK QCB
TCNTL13  EQU   *
         L     R9,TOTPLBKW              GET LAST PLINK Q ELEM  @YA12242
         LA    R9,0(R9)           CLEAR HIGH ORDER BYTE
         CR    R8,R9            Q  IS ELEMENT THE QCB
         BE    TCNT487              YES  END OF QUEUE
         USING PLNKPREF,R9
         LH    R10,PLNKMPTR       GET DISPLACEMENT TO
*                                   PLINK PARAMETERS
         AR    R10,R9             GET ADDR OF QUEUE POINTERS
         USING PLNKQPTR,R10
         L     R10,PLNKQBKD             GET NEXT ELEM          @YA12242
         ST    R10,TOTPLBKW             SAVE NEXT POINTER      @YA12242
         DELETE EPLOC=PLNKNAME    DELETE MODULE
         DROP  R10
         LR    R9,R10             GET NEXT QUEUE ELEMENT
         DROP  R9
         B     TCNTL13            GO TRY NEXT ELEMENT
TCNT487  EQU   *
         ST    R8,TOTPLFWD              UPDATE QCB             @YA12242
TCNT490  EQU   *
         TM    TOTFLG06,TOTOTERM+TOTCANCL CANCEL OR TERMINATE    S22024
         BNZ   TCNTL11A                    YES
         TM    TOTFLG03,TOTTSINC       INCLUSIVE ID ENTRY        S99528
         BO    INCHTST                 YES BRANCH                S99528
         CLI   3(R6),SECTBEND          ANY MORE SECTIONS         S99528
         BE    TCNT56               NO
         LA    R8,TOTOLTTE        GET END OF OLT TABLE
         LA    R6,3(R6)           GET NEXT TABLE ENTRY
         CR    R8,R6           Q  IS IT END OF TABLE
         BE    TCNT56               YES
         MVC   TOTCRALP(3),0(R6)  UPDATE CURRENT OLT I.D.
         ST    R6,TOTSAVE5+D4          SAVE SECTION ID           S22024
         LA    R1,TOTTDTBL             START OF DEVICE TABLE     S99528
         LA    R7,R10                  SET LOOP COUNT            S99528
TEN      NI    0(R1),X'FF'-TOTTDGRB    CLEAR GRAB FLAG           S99528
         LA    R1,4(R1)                BUMP TO NEXT DEVICE       S99528
         BCT   R7,TEN                  RESET ALL GRABED FLAGS    S99528
         B     TCNT01A                 GO EXECUTE NEXT OLT       S99528
TCNT56   EQU   *
         TM    TOTPRDCB+X'30',X'10'    PRIMARY DCB OPEN          S99528
         BZ    RESTPTR                 NO BRANCH                 S99528
         LA    R1,TOTPRDCB             TEST DCB                  S99528
         ST    R1,TOTWKSPC             STORE IN P LIST           S99528
         MVI   TOTWKSPC,X'B0'          OPTION CLOSE              S99528
         LA    R1,TOTWKSPC             ADDRESS OF P LIST         S99528
         CLOSE MF=(E,(1))              CLOSE TEST DCB            S99528
RESTPTR  LA    R6,TOTOLTTB             RESTORE SECT POINTER      S22024
         ST    R6,TOTSAVE5+D4          FIRST SECTION             S22024
*
         TM    TOTFLG06,TOTCANCL+TOTOTERM  CANCEL                S99528
         BO    TCNTL11A                YES BRANCH                S99528
         TM    $EXECFLG,$LSTPDEV       IS FINAL FLAG SET         S22024
         BO    TCNTL09              YES: SKIP REMAINING DEVICES
NXTDEV   LA    R11,4(R11)              UPDATE CURRENT            S99528
*                                         DEVICE POINTER
         CLI   0(R11),DEVTBEND         ANY MORE DEVICES          S99528
         BE    TCNTL09                NO
         TM    0(R11),TOTTDGRB         DEV BEEN GRABED           S99528
         BO    NXTDEV                  YES DO NOT ASSIGN         S99528
         LA    R8,TOTTDEND        GET TABLE END ADDRESS
         CR    R8,R11                  ARE THERE ANY MORE ENTRIESS99528
         BE    TCNTL09              NO
         ST    R11,TOTSAVE5            UPDATE DEV ADDR POINTER   S22024
***********************************************************************
***********************************************************************
**                                                                   **
**       UPDATE PRIMARY DEVICE ENTRY AND MOVE                        **
**            PRIMARY INFORMATION TO SCT                             **
**                                                                   **
***********************************************************************
***********************************************************************
         L     R8,0(R11)               GET NEXT ENTRY            S99528
         ST    R8,SAVEENT              SAVE NEXT ENTRY BEING     S22024
         ST    R8,TOTTNTPR             SAVE TNT POINTER          S22024
*                                      PROCESSED                 S22024
         TM    0(R11),TOTTDORN         TEST DEVICE ON RN         S22024
         BZ    NEXTDEV                 NO BRANCH                 S22024
         BAL   R12,QCBFIND             GO FIND QCB               S22024
         B     FSTSECID                GET FIRST SECTION ID      S22024
*
*        CHECK FOR TEST LOOP
*
TCNTL09  EQU   *
         TM    $ERROPT,$LOOPTST        TEST LOOP REQUEST         S22024
         BZ    TCNTL11A             NO
         LH    R11,TOTTLCNT            YES DECREMENT COUNT       S22024
         BCT   R11,TCNTL11             EX OLT AGAIN              S99528
         B     TCNTL11A                GO END OLT                S99528
TCNTL11  EQU   *
         STH   R11,TOTTLCNT            STORE NEW LOOP COUNT      S22024
         B     IEDQWEE                 GO TO RE RUN TEST         S99528
TCNTL11A EQU   *
*
*        SEND ON-LINE TESTING ENDED MESSAGE
*
         TM    TOTFLG01,TOTIPLCU       IS FORCED CLEANUP IN      S05331
*                                        PROGRESS?               S05331
         BO    EXITE2                  YES, BYPASS WAIT FOR LAST Y06330
*                                        MESSAGE RESPONSE        S05331
NOCLEAN  EQU   *                                                 S05331
         TM    TOTFLG05,TOTNDMSG       END OF MESSAGE REQUEST    S22024
         BZ    TCNTL11C             NO
         TM    TOTFLG06,TOTCANCL       CANCELL FLAG SET          S22024
         BZ    TCNTL11B                NO BRANCH                 S99528
**********
**********
**********   ON-LINE TESTING CANCELLED
*******
         IEDQMSG   MSGID=20,PREG0=R0,PREG1=R1,FUNCT=CEC,               X
               RET=TCNTL11C
TCNTL11B EQU   *
*******
*******
*******   ON-LINE TESTING ENDED
*******
         IEDQMSG   MSGID=16,PREG0=R0,PREG1=R1,FUNCT=CEC
TCNTL11C EQU   *
EXITE2   EQU   *                                                 S05331
         L     R13,4(R13)              GET CALLERS SAVE          S22024
         L     R14,D12(R13)            GET RETURN ADDRESS        S05331
         XCTL  (2,12),EP=IEDQWE2       GO TO CLEANUP MODULE      S22024
         EJECT
QCBFIND  EQU   *                       FIND PRIMARY QCB AND      S22024
*                                      THE RN LCB ADDRESS        S22024
         LR    R9,R8                   GET TNT ADDRESS           S22024
         ST    R9,TRMTNT               SAVE TNT ADDRESS        @Y17XAUU
         ST    R9,TOTTNTPR             SAVE TNT ADDRESS        @YM08563
         SR    R3,R3                   CLEAR REG                 S22024
         IC    R3,TOTTTBEL             GET NAME LENGTH           S22024
         AR    R9,R3                   TTE ADDRESS               S22024
         BCTR  R3,R0                   DECREMENT NAME LEN      @Y17XAUU
         EX    R3,MOVENAME             MOVE NAME TO P-LIST     @Y17XAUU
         L     R6,TOTAVTPT             GET AVT ADDRESS         @Y17XAUU
         USING IEDQAVTD,R6             SET AVT ADDRESSABILITY  @Y17XAUU
         L     R15,AVTRNMPT            GET TERM NAME TAB ADDR  @Y17XAUU
         ST    R15,TERNMTAB            SAME IT                 @Y17XAUU
         L     R15,AVTMSGS-D1          VCON TABLE ADDRESS      @Y17XAUU
         L     R15,D8(R15)             ADDRESS OF A1 ROUTINE   @Y17XAUU
         LA    R0,1(R3)                GET NAME LENGTH         @Y17XAUU
         LA    R1,PRINAME              GET ADDRESS OF NAME     @Y17XAUU
         BAL   R14,FOUR(R15)           FIND PRI TD TTCIN       @Y17XAUU
*                                                              @Y17XAUU
*        IEDQA1 RETURNS TEST DEVICE TTCIN IN REG 15            @Y17XAUU
*                                                              @Y17XAUU
         STH   R15,TOTPROFF            SAVE PR TD TTCIN        @Y17XAUU
         LR    R1,R15                  SET UP TO FIND NETWORK  @Y17XAUU
*                                      ADDRESS                 @Y17XAUU
         L     R15,AVTSAVTP            GET SAVT ADDRESS        @Y17XAUU
         L     R15,SAVTTNTX-IEDNSVTD(R15) GET ADDRESS OF TTCIN @Y17XAUU
*                                      TO NETWORK ADDR CONV RTN@Y17XAUU
         BALR  R14,R15                 FIND NETWORK ADDRESS    @Y17XAUU
*                                                              @Y17XAUU
*        IEDIAP04 RETURNS NETWORK ADDRESS IN REG 1             @Y17XAUU
*                                                              @Y17XAUU
         STH   R15,TOTPRSID            SAVE NETWORK ADDRESS    @YM07278
         DROP  R6                                              @Y17XAUU
         MVC   TNTENTRY+D1(D3),D0(R9)  ALLINE ADDRESS          @Y17XAUU
         L     R9,TNTENTRY             TTE ADDRESS             @Y17XAUU
         USING IEDQTRM,R9              TTE                       S22024
         L     R9,TRMDESTQ-1           QCB ADDRESS               S22024
         ST    R9,TRMQCB               SAVE QCB ADDRESS        @Y17XAUU
         DROP  R9                      TTE                       S22024
         USING IEDQQCB,R9              QCB                       S22024
         MVC   TOTPRRLN(D1),QCBRELLN   SAVE PRI RLN              S22024
         L     R9,QCBLGBAD-1           LGB ADDRESS               S22024
         DROP  R9                      QCB                       S22024
         USING IEDNLGB,R9              LGB                       S22024
         MVC   NAMESAVE(D8),LGBNAME    SAVE GROUP NAME           S22024
         DROP  R9                      LGB                       S22024
         L     R1,TNTENTRY             GET TTE ADDRESS         @Y17XAUU
         BAL   R14,NCPTTE              GET NCP TTE ADDRESS     @Y17XAUU
*                                                              @Y17XAUU
*        ON RETURN R1 CONTAINS ADDRESS OF NCP TTE PREFIX       @Y17XAUU
*                                                              @Y17XAUU
         USING IEDNTRM,R1              SET TTE ADDRESSABILITY  @Y17XAUU
         L     R1,TRMDESTQ-D1          GET QCB ADDRESS         @Y17XAUU
         DROP  R1                                              @Y17XAUU
         USING IEDQQCB,R1              SET QCB ADDRESSABILITY  @Y17XAUU
         L     R9,QCBDCBAD-D1          GET 3705 DCB ADDRESS    @Y17XAUU
         ST    R9,NCPDCB               SAVE 3705 DCB ADDRESS   @XM05808
         DROP  R1                                              @Y17XAUU
         USING IHADCB,R9               DCB                       S22024
         L     R3,DCBIOBAD             IOB ADDRESS               S22024
         SR    R6,R6                   CLEAR REG                 S22024
         IC    R6,DCBEIOBX             LCB SIZE                  S22024
         AR    R3,R6                   LCB OFFSET FROM DCB       S22024
         LA    R6,D32                  ADDTESTMENT TO START      S22024
*                                      OF LCB                    S22024
         SR    R3,R6                   STARTING LCB ADDRESS      S22024
         STCM  R3,ADRMSK,TOTPRLCB      SAVE RN LCB               S22024
         DROP  R9                      DCB                       S22024
         BR    R12                     RETURN                    S22024
         EJECT
************************************************************** @Y17XAUU
*        ROUTINE TO FIND NCP TTE ADDRESS. INPUT: REG 1 =     * @Y17XAUU
*        TERMINAL TTE ADDRESS. OUTPUT: REG 1 = NCP TTE       * @Y17XAUU
*        ADDRESS. RETURN TO CALLER VIA REG 14.                 @Y17XAUU
************************************************************** @Y17XAUU
NCPTTE   EQU   *                                               @Y17XAUU
         STM   R14,R12,TRMSAVE3        SAVE CALLER'S REGS      @Y17XAUU
         LA    R8,TRMPRFSZ             GET TTE PREFIX SIZE     @Y17XAUU
         SLR   R1,R8                   POINT TO TTE PREFIX     @Y17XAUU
         USING IEDNTRM,R1              SET TTE ADDRESSABILITY  @Y17XAUU
NCPLOOP  EQU   *                                               @Y17XAUU
         LH    R1,TRMCOHRT             GET COHORT POINTER      @Y17XAUU
         L     R15,TOTAVTPT            GET AVT ADDRESS         @Y17XAUU
         USING IEDQAVTD,R15            SET AVT ADDRESSABILITY  @Y17XAUU
         L     R15,AVTRNMPT            TTCIN TO TTE CONVERSION @Y17XAUU
*                                      ROUTINE ADDRESS         @Y17XAUU
         DROP  R15                                             @Y17XAUU
         BALR  R14,R15                 FIND TTE ADDRESS        @Y17XAUU
         SLR   R1,R8                   POINT TO TTE PREFIX     @Y17XAUU
         CLI   TRMTYPE,TRMLNCP         NCP ENTRY               @Y17XAUU
         BNE   NCPLOOP                 NO - CHECK NEXT ENTRY   @Y17XAUU
         LM    R14,R0,TRMSAVE3         RESTORE REG 14-0        @Y17XAUU
         LM    R2,R12,TRMSAVE3+DISREG2 RESTORE REG 2-12        @Y17XAUU
         BR    R14                     RETURN TO CALLER        @Y17XAUU
         DROP  R1                                              @Y17XAUU
         EJECT
****************************************************************@Y17XAU
*        ROUTINE TO GO TO IEDQWB2 TO CALCULATE THE AMOUNT OF   *@Y17XAU
*        OF CORE NEEDED TO BUILD DUMMY ENTRIES FOR A SPECIFIED *@Y17XAU
*        REMOTE TERMINAL. CONTROL IS PASS TO IEDQWB TO ATTAIN  *@Y17XAU
*        THE ACTUAL CORE.  CONTROL IS THEN RETURNED TO IEDQWB2 *@Y17XAU
*        TO COPY THE ENTRIES.                                  *@Y17XAU
****************************************************************@Y17XAU
DUMMYTNT EQU   *                                               @Y17XAUU
         STM   R7,R11,TRMSAVE2    SAVE REGISTERS               @Y17XAUU
         L     R8,TNTENTRY        GET TTE ADDRESS              @Y17XAUU
         LA    R7,TRMPRFSZ        GET TTE PREFIX SIZE          @Y17XAUU
         SLR   R8,R7              POINT TO TTE PREFIX          @Y17XAUU
         USING IEDNTRM,R8         SET TTE ADDRESSABILITY       @Y17XAUU
         CLI   TRMTYPE,TRMLUNT    LU ENTRY                     @Y17XAUU
         BNE   DUMOUT             RETURN TO CALLER             @Y17XAUU
         DROP  R8                                              @Y17XAUU
         MVI   TRMRQFLG,PRREQ     SHOW PRI TD REQUEST          @Y17XAUU
         LA    R12,KTRM           PASS PARAMETERS              @Y17XAUU
         XC    TRMFLG01(D1),TRMFLG01 CLEAR CONTROL FLAG        @Y17XAUU
*                                                              @Y17XAUU
         LINK  EP=IEDQWB2         CALCULATE CORE               @Y17XAUU
         CL    R15,ERRCD8         SNA LU REJECT                @XM05678
         BE    DUMFAILU           YES - WRITE ERROR MESSAGE    @XM05678
         LTR   R15,R15            REQUEST SUCCESSFUL           @Y17XAUU
         BNZ   DUMFAIL            NO - ERROR                   @Y17XAUU
         CLI   TRMRQFLG,ALRDY     COUNTERFEITING COMPLETE      @Y17XAUU
         BE    DUMOUT             YES - RETURN TO CALLER       @Y17XAUU
*                                                              @Y17XAUU
         L     R10,TOTRESPL       ADDR OF TOTE RES. CTL BLK    @Y17XAUU
         USING RESPL,R10                                       @Y17XAUU
         XC    TOTOTECB(D4),TOTOTECB CLEAR ECB                 @Y17XAUU
         POST  RESTECB1           POST REQUEST TO IEDQWB       @Y17XAUU
         WAIT  ECB=TOTOTECB       WAIT FOR REQUEST TO BE HONORED@Y17XAU
*                                                              @Y17XAUU
         CLI   TOTWKSPC,XFF       REQUEST SUCCESSFUL           @Y17XAUU
         BE    DUMFAIL            NO - ERROR                   @Y17XAUU
*                                                              @Y17XAUU
         OI    TRMFLG01,TRMQWB3   SHOW SECOND PASS             @Y17XAUU
         LA    R12,KTRM           PASS PARAMETERS              @Y17XAUU
         LINK  EP=IEDQWB2         COPY ENTRIES                 @Y17XAUU
         LTR   R15,R15            REQUEST SUCCESSFUL           @Y17XAUU
         BNZ   DUMFAIL            NO - ERROR                   @Y17XAUU
         OI    TOTFLG04,X'08' .TOTPRCTF *********************  @YM08057
*                                                              @Y17XAUU
DUMOUT   EQU   *                                               @Y17XAUU
         LM    R7,R11,TRMSAVE2    RESTORE REGISTERS            @Y17XAUU
         BR    R9                 RETURN                       @Y17XAUU
         SPACE
DUMFAILU EQU   *                                               @XM05678
         IEDQMSG MSGID=093,FUNCT=CEC ERROR,LU NOT IN SSCP LU SESSION
*                                 ONLY                         @XM05678
         OI    TOTFLG06,TOTOTERM  SET TERMINATE FLAG           @XM05678
         B     TCNTL11B           EXIT TO CLEAN UP             @XM05678
         SPACE 2                                               @Y17XAUU
DUMFAIL  EQU   *                                               @Y17XAUU
         WTO   'IED215I OLT REQUEST REJECTED, UNABLE TO ALLOCATE RESOUR*
               CES FOR CT/AP/TD',ROUTCDE=10,DESC=7             @Y17XAUU
         OI    TOTFLG06,TOTOTERM  SET TERMINATE FLAG           @Y17XAUU
         B     TCNTL11B           EXIT TO CLEANUP              @Y17XAUU
         EJECT                                                 @Y17XAUU
TCNTL30  EQU   *                                               @Y17XAUU
         LR    R14,R7             DIVIDE CORE                  @Y17XAUU
         SR    R15,R15              REQUIRED BY                @Y17XAUU
         SRDL  R14,10               X'400'                     @Y17XAUU
         LTR   R15,R15            IS THERE A REMAINDER?        @Y17XAUU
         BZ    TCNTL32              NO                         @Y17XAUU
         LA    R14,1(R14)           YES: ROUND UP TO EVEN MULT @Y17XAUU
TCNTL32  EQU   *                                               @Y17XAUU
         LR    R7,R14             SAVE BLOCK REQUIREMENT       @Y17XAUU
         LA    R8,EIGHT           GET MINIMUM BLOCK COUNT ALREADYS22024
*                                   ASSIGNED                   @Y17XAUU
         SR    R7,R8              SUBTRACT AMOUNT ALREADY      @Y17XAUU
*                                   ASSIGNED FROM REQUIRED     @Y17XAUU
*                                   AMOUNT                     @Y17XAUU
         L     R10,TOTRESPL       GET ADDRESS OF RESIDENT      @Y17XAUU
*                                   PARAMETER LIST             @Y17XAUU
         USING RESPL,R10                                       @Y17XAUU
         CH    R7,RESBKTOT        DOES TOTE HAVE ENOUGH CORE?  @Y17XAUU
         BH    FOUR(R12)          NO: SEND NOT ENOUGH CORE     @Y17XAUU
*                                   MESSAGE                    @Y17XAUU
         LA    R8,THREE           GET TRANSIENT AREA REQUIREMENT@Y17XAU
         AR    R14,R8             GET TOTAL NUMBER OF BLOCKS   @Y17XAUU
*                                   REQUIRED                   @Y17XAUU
         STH   R14,TOTBKRQD       PUT NEW CORE BLOCK REQUIREMENT@Y17XAU
*                                   INTO OLTCB                 @Y17XAUU
*                                   IN OLTCB                   @Y17XAUU
         OI    TOTFLG10,TOTOLTWT+TOTOLTRS  SET RESTART FLAGE   @Y17XAUU
*                                 SET OLT WAITING FOR CORE FLAG@Y17XAUU
         POST  RESTECB1           REQUEST OLT CORE             @Y17XAUU
         XC    TOTOTECB(D4),TOTOTECB   CLEAR ECB               @Y17XAUU
         WAIT  1,ECB=TOTOTECB          WAIT FOR CORE ASSIGNMENT@Y17XAUU
         L     R8,TOTMMSPC             CORE AVAILABLE TO OLT   @Y17XAUU
         DROP  R10                                             @Y17XAUU
         BR    R12                     RETURN TO CK CORE AVAIL @Y17XAUU
         EJECT                                                 @Y17XAUU
*****************************************************************S99528
*****************************************************************S99528
*                                                              @Y17XAUU
*        INCLUSIVE OLT ID ROUTINE                              @Y17XAUU
*                                                              @Y17XAUU
*****************************************************************S99528
*****************************************************************S99528
INCHTST  EQU   *                       INCLUSIVE ROUTINE       @Y17XAUU
         CLI   TOTSECBL,BLANK          SECOND CHAR BLANK       @Y17XAUU
         BE    TWOBLNK                 YES BRANCH     A        @Y17XAUU
         CLI   TOTONECU,BLANK          FIRST CHAR BLANK        @Y17XAUU
         BE    ONEBLNK                 YES BRANCH    AA        @Y17XAUU
**   ENTRY LOOKS LIKE  AAA-AAZ                                 @Y17XAUU
         TM    TOTCRFLG,TOTFTTIM       FIRST TIME THRU         @Y17XAUU
         BZ    NOTFST                  NO BRANCH               @Y17XAUU
         LA    R3,ALPHTABL             GET ALPHBET ADDRESS     @Y17XAUU
FINDTID  CLC   0(1,R3),TOTONECU        FIND TEST ID            @Y17XAUU
         LA    R3,1(R3)                BUMP ALPHBET ADDRESS    @Y17XAUU
         BE    SAVEONEA                GO SAVE BUMPED ADDRESS  @Y17XAUU
         B     FINDTID                 KEEP SEARCHING          @Y17XAUU
SAVEONEA LR    R1,R4                   GET BASE ADDRESS        @Y17XAUU
         SR    R3,R1                   COMPUTE OFFSET          @Y17XAUU
         ST    R3,TOTSAADR             SAVE OFFSET             @Y17XAUU
         L     R5,TOTSAADR             GET SAVED ADDRESS       @Y17XAUU
         AR    R5,R4                   COMPUTE ADDRESS         @Y17XAUU
MOVEID   MVC   TOTONELT(1),0(R5)       MOVE TO CURRENT OLT ID  @Y17XAUU
         MVI   TOTCRFLG,TOTCLRFG       CLEAR FIRST TIME FLAG   @Y17XAUU
         CLC   0(1,R5),TOTONEND        RUN ALL TEST ASK FOR    @Y17XAUU
         BH    TCNT56                  YES BRANCH              @Y17XAUU
         B     TCNT01A                 NO BRANCH               @Y17XAUU
NOTFST   L     R3,TOTSAADR             GET SAVED ADDRESS       @Y17XAUU
         LA    R3,1(R3)                BUMP ADDRESS            @Y17XAUU
         AR    R3,R4                   RESTORE ADDRESS         @Y17XAUU
        B      SAVEONEA                BRANCH                  @Y17XAUU
ONEBLNK  TM    TOTCRFLG,TOTFTTIM       FIRST TIME THRU         @Y17XAUU
         BZ    NOTFST1                 NO BRANCH               @Y17XAUU
         LA    R3,ALPHTABL             GET ALPHBET ADDRESS     @Y17XAUU
FINDID1  CLC   0(1,R3),TOTSECBL        FIND TEST ID            @Y17XAUU
         LA    R3,1(R3)                BUMP ALPHBET ADDRESS    @Y17XAUU
         BE    SAVEONEB                GO SAVE BUMPED ADDRESS  @Y17XAUU
         B     FINDID1                 KEEP SEARCHING          @Y17XAUU
SAVEONEB LR    R1,R4                   GET BASE ADDRESS        @Y17XAUU
         SR    R3,R1                   COMPUTE OFFSET          @Y17XAUU
         ST    R3,TOTSAADR             SAVE OFFSET             @Y17XAUU
         L     R5,TOTSAADR             GET SAVED ADDRESS       @Y17XAUU
         AR    R5,R4                   COMPUTE ADDRESS         @Y17XAUU
MOVEID1  MVC   TOTCRTOO(1),0(R5)       MOVE TO CURRENT OLT ID  @Y17XAUU
         MVI   TOTCRFLG,TOTCLRFG       CLEAR FIRST TIME FLAG   @Y17XAUU
         CLC   0(1,R5),TOTSECBK        RUN ALL TEST ASK FOR    @Y17XAUU
         BH    TCNT56                  YES BRANCH              @Y17XAUU
         B     TCNT01A                 NO  BRANCH              @Y17XAUU
NOTFST1  L     R3,TOTSAADR             GET SAVED ADDRESS       @Y17XAUU
         LA    R3,1(R3)                BUMP ADDRESS            @Y17XAUU
         AR    R3,R4                   RESTORE ADDRESS         @Y17XAUU
         B     SAVEONEB                BRANCH                  @Y17XAUU
TWOBLNK  TM    TOTCRFLG,TOTFTTIM       FIRST TIME THRU         @Y17XAUU
         BZ    NOTFST2                 NO BRANCH               @Y17XAUU
         LA    R3,ALPHTABL             GET ALPHBET ADDRESS     @Y17XAUU
FINDID2  CLC   0(1,R3),TOTOLTTB        FIND TEST ID            @Y17XAUU
         LA    R3,1(R3)                BUMP ALPHBET ADDRESS    @Y17XAUU
         BE    SAVEONEC                GO SAVE BUMPED ADDRESS  @Y17XAUU
         B     FINDID2                 KEEP SEARCHING          @Y17XAUU
SAVEONEC LR    R1,R4                   GET BASE ADDRESS        @Y17XAUU
         SR    R3,R1                   COMPUTE OFFSET          @Y17XAUU
         ST    R3,TOTSAADR             SAVE OFFSET             @Y17XAUU
         L     R5,TOTSAADR             GET SAVED ADDRESS       @Y17XAUU
         AR    R5,R4                   COMPUTE ADDRESS         @Y17XAUU
MOVEID2  MVC   TOTCRALP(1),0(R5)       MOVE TO CURRENT OLT ID  @Y17XAUU
         MVI   TOTCRFLG,TOTCLRFG       CLEAR FIRST TIME FLAG   @Y17XAUU
         CLC   0(1,R5),TOTCRSEC        RUN ALL TEST ASK FOR    @Y17XAUU
         BH    TCNT56                  YES BRANCH              @Y17XAUU
         B     TCNT01A                 NO BRANCH               @Y17XAUU
NOTFST2  L     R3,TOTSAADR             GET SAVED ADDRESS       @Y17XAUU
         LA    R3,1(R3)                BUMP ADDRESS            @Y17XAUU
         AR    R3,R4                   RESTORE ADDRESS         @Y17XAUU
         B     SAVEONEC                BRANCH                  @Y17XAUU
         EJECT                                                 @Y17XAUU
         IEDQMSG LINK=YES,PREG0=R0                             @Y17XAUU
         EJECT                                                 @Y17XAUU
***********************************************************************
***********************************************************************
*                                                              @Y17XAUU
*       FIND UCB ROUTINE FOR TNT ENTRIES                       @Y17XAUU
*                                                              @Y17XAUU
***********************************************************************
***********************************************************************
UCBFIND  EQU   *                                               @Y17XAUU
         ST    R5,UCB5SAVE             SAVE REG                @Y17XAUU
         ST    R3,UCB3SAVE             SAVE REG                @Y17XAUU
         SR    R5,R5                   CLEZA REG               @Y17XAUU
         IC    R5,TOTTTBEL             GET TERMINAL NAME LENGTH@Y17XAUU
         AR    R5,R8                   GET TTE ADDRESS         @Y17XAUU
         MVC   TNTENTRY+D1(D3),D0(R5)  ALLINE ADDRESS          @Y17XAUU
         L     R8,TNTENTRY             GET TTE ADDR            @Y17XAUU
         USING IEDQTRM,R8                                      @Y17XAUU
         L     R8,TRMDESTQ-1           GET QCB ADDR            @Y17XAUU
         DROP  R8                                              @Y17XAUU
         USING IEDQQCB,R8                                      @Y17XAUU
         SR    R3,R3                   CLEAR REG               @Y17XAUU
         IC    R3,QCBRELLN             GET RLN                 @Y17XAUU
         SLL   R3,2                    MULT BY 4               @Y17XAUU
         L     R8,QCBDCBAD-1           GET DCB ADDR            @Y17XAUU
         DROP  R8                                              @Y17XAUU
         USING IHADCB,R8                                       @Y17XAUU
         L     R8,DCBDEBAD             GET DEB ADDR            @Y17XAUU
         DROP  R8                                              @Y17XAUU
         USING DEBNMSUB,R8                                     @Y17XAUU
         SR    R15,R15                 CLEAR REG               @Y17XAUU
         IC    R15,DEBNMEXT            GET # OF EXTENTS        @Y17XAUU
         SLL   R15,2                   MULT BY 4               @Y17XAUU
         L     R8,DEBUCBAD-4(R3)       GET UCB ADDR            @Y17XAUU
         L     R5,UCB5SAVE             RETURN REG              @Y17XAUU
         L     R3,UCB3SAVE             RESTORE REG             @Y17XAUU
         B     0(R12)                  BRANCH RETURN           @Y17XAUU
         EJECT                                                 @Y17XAUU
DDMOVE   EQU   *                       OPEN PRI DCBS           @Y17XAUU
GRAPHIC  LA    R9,CVTPTR               GET POINTER TO CVT      @Y17XAUU
         L     R9,0(R9)                CVT ADDRESS             @Y17XAUU
         L     R9,0(R9)                TCBPOINTERS ADDRESS     @Y17XAUU
         L     R9,4(R9)                CURRENT TCB ADDRESS     @Y17XAUU
         L     R9,12(R9)               TIOT ADDRESS            @Y17XAUU
         USING TIOT,R9                                         @Y17XAUU
         LA    R9,TIOENTRY             POINT TO FIRST ENTRY    @Y17XAUU
         DROP  R9                                              @Y17XAUU
         USING TIOENTRY,R9                                     @Y17XAUU
GETLENGT SR    R3,R3                   CLEAR REG               @Y17XAUU
         IC    R3,TIOELNGH             DD ENTRY LENGTH         @Y17XAUU
         LTR   R3,R3                   ANY LENGTH              @Y17XAUU
         BZ    NOTFOUND                NO BRANCH               @Y17XAUU
         LA    R5,POOLSTAR-TIOESTTB    GET LENGTH OF DEVICE ENTRYS99528
         DROP  R9                                              @Y17XAUU
         LR    R6,R3                   SAVE LENGTH             @Y17XAUU
         SR    R3,R5                   DD LENGTH -4            @Y17XAUU
         AR    R3,R9                   DEVICE                  @Y17XAUU
         USING TIOESTTB,R3                                     @Y17XAUU
         CLC   TIOEFSRT+1(2),ADDOFUCB  COMPARE UCB ADDRESSES FOR
         DROP  R3                                              @Y17XAUU
         BE    UCBFOUND                BR WHEN FOUND           @Y17XAUU
         AR    R9,R6                   BUMP TO NEXT DD ENTRY   @Y17XAUU
         B     GETLENGT                BRANCH                  @Y17XAUU
NOTFOUND EQU   *                                               @Y17XAUU
         B     NOTFOUND                BRANCH                  @Y17XAUU
UCBFOUND EQU   *                                               @Y17XAUU
         USING TIOENTRY,R9             TIOT ENTRY BASE         @Y17XAUU
COMPAREN EQU   *                                               @Y17XAUU
         CLI   TIOEDDNM,X'40'          IS NAME FIELD BLANK?    @Y17XAUU
         BNE   MOVEIN                  NO, GO MOVE DDNAME      @Y17XAUU
         SR    R9,R6                   SET ENTRY TO PRECEDING ONES99528
         B     COMPAREN                GO LOOK FOR NAME        @Y17XAUU
MOVEIN   EQU   *                                               @Y17XAUU
         MVC   EXCPDCB+40(8),TIOEDDNM  MOVE DDNMME TO DCB      @Y17XAUU
         DROP  R9                                              @Y17XAUU
         RDJFCB EXCPDCB                                        @Y17XAUU
         MVI   JFCBAA+53,X'00'         THIS                    @Y17XAUU
         MVC   JFCBAA+54(12),JFCBAA+53 SECTION                 @Y17XAUU
         MVI   JFCBAA+67,X'00'         OF CODE                 @Y17XAUU
         MVC   JFCBAA+68(12),JFCBAA+67 CLEARS                  @Y17XAUU
         MVI   JFCBAA+88,X'00'         UNUSED                  @Y17XAUU
         MVC   JFCBAA+89(29),JFCBAA+88 PARTS                   @Y17XAUU
         MVI   JFCBAA+148,X'00'        OF THE                  @Y17XAUU
         MVC   JFCBAA+149(25),JFCBAA+148  JFCB                 @Y17XAUU
         MVI   JFCBAA+52,X'08'         PREVENT REVERSE         @Y17XAUU
         MVI   JFCBAA+66,X'02'         MERGE AT CLOSE          @Y17XAUU
         MVC   TOTPRDCB(72),EXCPDCB    MOVE DCB TO OLTCB       @Y17XAUU
         LA    R1,TOTPRDCB             TEST DCB                @Y17XAUU
         ST    R1,TOTWKSPC             PUT IN P LIST           @Y17XAUU
         MVI   TOTWKSPC,X'8F'          OPTION OPEN             @Y17XAUU
         LA    R1,TOTWKSPC             ADDRESS OF P LIST       @Y17XAUU
         OPEN  MF=(E,(1)),TYPE=J       OPEN PRIMARY DCB        @Y17XAUU
         TM    TOTPRDCB+48,X'10'       SUCCESSFUL OPEN?        @Y17XAUU
         BNO   MOVEIN1                 BRANCH IF ERROR IN OPEN @Y17XAUU
         L     R3,TOTPRDEB             GET ADDRESS OF DEB      @Y17XAUU
         LA    R3,D34(R3)              GET ADDRESS OF UCB IN DEB
         ST    R3,TOT102A              SAVE UCB ADDRESS IN SVC @Y17XAUU
*                                        102 PARM LIST FOR DATA@Y17XAUU
*                                        MOVE                  @Y17XAUU
         LA    R1,TOT102               LOAD ADDRESS OF PARM LIST
*                                        INTO REG 1            @Y17XAUU
         AQCTL                         ISSUE DATA MOVE SVC     @Y17XAUU
         B     0(R12)                  RETURN TO CALLER        @Y17XAUU
MOVEIN1  EQU   *                                               @Y17XAUU
         IEDQMSG MSGID=003,FUNCT=CEC,PREG0=R0,PREG1=R1 ERROR MSG
         OI    TOTFLG06,TOTCANCL       SET CANCEL BIT IN OLTCB @Y17XAUU
         B     TCNTL11A                GO CANCEL TRM           @Y17XAUU
**                                                             @Y17XAUU
RLNLCB   EQU   *                                               @Y17XAUU
*    IOBBASE ADDRESS PLUS IOBDCB INDEX TIMES RLN MINUS THE     @Y17XAUU
*        IOB DISPLACEMENT IN THE LCB EQUALS THE LCB ADDRESS    @Y17XAUU
         L     R8,TNTENTRY             GET TTE ADDRESS         @Y17XAUU
         USING IEDQTRM,R8              TTE BASE                @Y17XAUU
         L     R8,TRMDESTQ-1           GET QCB ADDRESS         @Y17XAUU
         DROP  R8                                              @Y17XAUU
         USING IEDQQCB,R8              QCB BASE                @Y17XAUU
         SR    R7,R7                   CLEAR REG               @Y17XAUU
         IC    R7,QCBRELLN             GET RLN                 @Y17XAUU
         L     R8,QCBDCBAD-1           GET DCB ADDRESS         @Y17XAUU
         DROP  R8                                              @Y17XAUU
         USING IHADCB,R8               DCB BASE                @Y17XAUU
         SR    R6,R6                   CLEAR REG               @Y17XAUU
         IC    R6,DCBEIOBX             GET INDEX               @Y17XAUU
         LR    R9,R7                   SAVE RLN                @Y17XAUU
         LA    R5,LCBFLAG1-IEDQLCB     GET IOB DISPLACEMENT    @Y17XAUU
         L     R8,DCBIOBAD             GET IOBBASE ADDRESS     @Y17XAUU
         DROP  R8                                              @Y17XAUU
         MR    R6,R7                   RLN TIMES INDEX         @Y17XAUU
         AR    R6,R8                   RESULTS PLUS BASE       @Y17XAUU
         SR    R6,R5                   GET LCB ADDRESS         @Y17XAUU
         ST    R7,TOTPRRLN             PUT RLN AND LCB         @Y17XAUU
         STC   R9,TOTPRRLN             IN TOTPRENT             @Y17XAUU
         B     0(R12)                  RETURN                  @Y17XAUU
         EJECT                                                 @Y17XAUU
         DS    0F                                              @Y17XAUU
CDSLINE  DC    CL8'G0000000'           CDS NAME                @Y17XAUU
DEVADBUF DS    0F                                              @Y17XAUU
         DC    4CL1'0'                 DEVICE ADDRESS BUFFER   @Y17XAUU
NCPDCB   DS    F                       3705 DCB ADDR SAVE AREA @XM05808
SETCLNST DS    0F                      SETS TOTCLNST W/ C&S    @G36XRUV
         DC    AL1(TOTCLNST)                                   @G36XRUV
         DC    AL3(0)                                          @G36XRUV
****************************************************************
*        PARAMETER LIST FOR DATA MOVE SVC                      *
****************************************************************
*                                                              @Y17XAUU
TOT102   DS    0F                      ALIGNMENT               @Y17XAUU
         DC    X'08'                   FLAG FOR DATA MOVE      @Y17XAUU
         DC    AL3(ADDOFUCB)           DATA ADDRESS            @Y17XAUU
TOT102A  DC    X'00'                   FLAGS                   @Y17XAUU
         DC    AL3(0)                  TARGET ADDRESS          @Y17XAUU
         DC    X'80'                   LAST WORD FLAG          @Y17XAUU
         DC    AL3(TOT102LN)           ADDRESS OF LENGTH FIELD @Y17XAUU
TOT102LN DC    H'2'                    LENGTH OF UCB ADDRESS MOVES99528
ADDOFUCB DC    AL3(0)                  UCB ADDRESS             @Y17XAUU
JFCBAA  DC     CL176'0'                JFCB                    @Y17XAUU
         DS    0F                      EXIT LIST               @Y17XAUU
JFCBADDR DC    X'07'                   KEY FOR JFCB            @Y17XAUU
         DC    AL3(JFCBAA)             ADDRESS OF THE JFCB     @Y17XAUU
         DC    X'80'                   END OF LIST             @Y17XAUU
         DC    AL3(0)                  ZEROS                   @Y17XAUU
MOVENAME MVC   PRINAME(0),0(R8)     MOVE NAME TO PLIST         @Y17XAUU
PRINAME  DC    CL8' '                  PRIMARY TEST DEVICE NAME@Y17XAUU
         DS    0F                      BOUNDARY ALLIGNMENT     @Y17XAUU
TTEADDR  DC    A(0)                    TTE ADDRESS             @Y17XAUU
EXCPDCB  DCB   DDNAME=DD274E,DSORG=PS,EXLST=JFCBADDR,MACRF=(E),        C
               SIOA=AO,CENDA=AP,XENDA=AP   GRAPHIC APPENDAGES  @Y17XAUU
EXCP     OPEN  (EXCPDCB,),MF=L                                 @Y17XAUU
SAVEENT  DC    A(0)                    STATUS SAVE AREA        @Y17XAUU
***                                                            @Y17XAUU
***      MESSAGES                                              @Y17XAUU
***                                                            @Y17XAUU
UNIT     DC    CL8'        '           NNIT NAME               @Y17XAUU
UCB5SAVE DC    A(0)                    REG 5 SAVE              @Y17XAUU
UCB3SAVE DC    A(0)                    REG 3 SEVE              @Y17XAUU
TRMSAVE3 DS    15F                     SAVE AREA               @Y17XAUU
ERRCD8   DC    F'8'                    ERROR RETURN CODE       @XM05678
NAMEMOVE MVC   UNIT(ZERO),ZERO(R1) MOVE NAME                   @Y17XAUU
TERMIAT  DC    CL8'        '           START TERM MSG ID       @Y17XAUU
TERMIATE DC    CL26'            UNIT          '  START TERM MSG@Y17XAUU
NOCOREE  DC    CL8'IED331I '           MESSAGE ID              @Y17XAUU
NOCORE   DC    CL37'NOT ENOUGH CORE FOR SECTION          ' MSG @Y17XAUU
NOTLOADD DC    CL8'IED330I '           MESSAGE ID              @Y17XAUU
NOTLOAD  DC    CL32'OLT MODULE          NOT LOADABLE'    MSG   @Y17XAUU
ASTRICK  EQU   X'5C'                   ASTERICK                @Y17XAUU
TEE      EQU   X'E3'                   T                       @Y17XAUU
S        EQU   X'E2'                   S                       @Y17XAUU
INITSTAT DC    X'00'                   INITAL STATUS BYTE      @Y17XAUU
APSTIME  DC    X'00'                   AP STIMER FLAG          @Y17XAUU
APSTIMER EQU   X'80'                   STIMER FOR AP           @Y17XAUU
ALPHTABL DC    CL26'ABCDEFGHIJKLMNOPQRSTUVWXYZ'     TABLE      @Y17XAUU
ENDALTAB DC    X'EA'                   PAST ALPHBET            @Y17XAUU
STARTID  DC    CL8'IED333I '           START ID                @Y17XAUU
TERMID   DC    CL8'IED334I '           TERM ID                 @Y17XAUU
         DS    0D                      DOUBLE WORD ALLINE      @Y17XAUU
NAMESAVE DC    CL8' '                  TEST DEV GROUP NAME     @Y17XAUU
PATCH    DC    CL100' '                PATCH AREA              @Y17XAUU
BLDLLIST DS    0F                 BLDL P-LIST                  @Y17XAUU
         DC    XL2'0001'          NUMBER OF LIST ENTRIES       @Y17XAUU
         DC    XL2'003A'          LENGTH OF EACH ENTRY         @Y17XAUU
READNAME DC    8XL1'40'           MODULE ID                    @Y17XAUU
         DC    XL16'0'            ADDITIONAL DIRECTORY INFO    @Y17XAUU
BLDLNAM  DC    XL3'0'             MAIN STORAGE NEEDED          @Y17XAUU
         DC    XL31'0'            ADDITIONAL DIRECTORY INFO    @Y17XAUU
WORKA    DC    X'000F'            USED FOR CONVERSION          @Y17XAUU
         EJECT                                                 @Y17XAUU
KTRM     DS    0F                 *** DO NOT INSERT BETWEEN *'S@Y17XAUU
TERNMTAB DS    A                  TERMNAME TABLE ADDRESS       @Y17XAUU
TNTENTRY DS    A                  TERMNAME ENTRY ADDRESS       @Y17XAUU
TRMQCB   DS    A                  CT/AP QCB ADDRESS            @Y17XAUU
TRMTNT   DS    A                  CT/AP TNT ADDRESS            @Y17XAUU
TRMSAVE1 DS    3F                 REGISTERS SAVE AREA          @Y17XAUU
TRMSAVE2 DS    5F                 REGISTERS SAVE AREA          @Y17XAUU
TRMSAV1  DS    3F                 REGISTERS SAVE AREA          @Y17XAUU
REGSAVE  DS    F                  WORK AREA                    @Y17XAUU
F32      DC    F'32'              VALUE OF 32                  @Y17XAUU
KPQCB    DS    A                  TRUE P-QCB ADDRESS           @Y17XAUU
KMQCB    DS    A                  DUMMY M-QCB ADDRESS          @Y17XAUU
CONCTTE  DS    A                  TRUE CONC TERM TTE ADDRESS   @Y17XAUU
KDRQ     DS    A                  TRUE DRQ ADDRESS             @Y17XAUU
KPQCBSZ  DS    H                  P-QCB SIZE                   @Y17XAUU
KMQCBSZ  DS    H                  M-QCB SIZE                   @Y17XAUU
KDRQSZ   DS    H                  DRQ SIZE                     @Y17XAUU
TTETSZ   DS    CL1                CONC TERM TTE SIZE           @Y17XAUU
TTECSZ   DS    CL1                CONC TTE SIZE                @Y17XAUU
TRMRQFLG DS    CL1                DUMMY TNT REQUEST FLAG       @Y17XAUU
CTREQ    EQU   X'80'              CT REQUEST                   @Y17XAUU
APREQ    EQU   X'40'              AP REQUEST                   @Y17XAUU
PRREQ    EQU   X'20'              PRIMARY TD REQUEST           @Y17XAUU
SCREQ    EQU   X'10'              SECONDARY TD REQUEST         @Y17XAUU
ALRDY    EQU   X'00'              COUNTERFEIT STORAGE ALREADY  @Y17XAUU
*                                 EXIST FOR THIS REQUEST       @Y17XAUU
TRMFLG01 DS    CL1                CONTROL FLAG                 @Y17XAUU
CONC     EQU   X'80'              TERMINAL ATTACHED TO CONC    @Y17XAUU
TRMSPAS  EQU   X'40'              PROCESS CONC TTE             @Y17XAUU
TRMQWB3  EQU   X'20'              SECOND PASS FUNCTION         @Y17XAUU
TRMRNLU  EQU   X'10'              SNA LU ENTRY                 @Y17XAUU
TRMDELY  EQU   X'08'              QCB IN TIME DELAY QUEUE      @Y17XAUU
TRM3705  EQU   X'04'              3705 MODE                    @Y17XAUU
TRMCTEAP EQU   X'02'              CT EQU AP                    @Y17XAUU
WRKFLG   DS    CL1                WORK AREA                    @Y17XAUU
KTRMEND  DS    0C                 END OF KTRM DSECT            @Y17XAUU
************************************************************** @Y17XAUU
         EJECT                                                 @Y17XAUU
         TLCBD                                                 @Y17XAUU
         EJECT                                                 @Y17XAUU
UCB      DSECT LIST=YES                                        @Y17XAUU
         IEFUCBOB                                              @Y17XAUU
         EJECT                                                 @Y17XAUU
RESPL    RESPL EQUATES=YES                                     @Y17XAUU
         EJECT                                                 @Y17XAUU
         OLTCB                         OLTCB CONTROL BLOCK     @Y17XAUU
         EJECT                                                 @Y17XAUU
         TLGBD                         LGB                     @Y17XAUU
TCNTBLDL EQU   TOTWKSPC                                        @Y17XAUU
TCNTBECT EQU   TCNTBLDL+1                                      @Y17XAUU
TCNTBELN EQU   TCNTBLDL+3                                      @Y17XAUU
TCNTMDNM EQU   TCNTBLDL+4                                      @Y17XAUU
TCNTMDSZ EQU   TCNTBLDL+28                                     @Y17XAUU
TEMPSAVE EQU   TOTWKSPC+8              WORK AREA               @Y17XAUU
         EJECT                                                 @Y17XAUU
         TDEBD                                                 @Y17XAUU
         EJECT                                                 @Y17XAUU
PLNKPREF DSECT                                                 @Y17XAUU
PLNKNAME DS    CL8                     OLT NAME                @Y17XAUU
         ORG   PLNKPREF+10             TO POINTER              @Y17XAUU
PLNKMPTR DS    CL2                     POINTER                 @Y17XAUU
         SPACE 5                                               @Y17XAUU
PLNKQPTR DSECT                                                 @Y17XAUU
PLNKQFWD DS    F                       FORWARD POINTER         @Y17XAUU
PLNKQBKD DS    F                       BACK POINTER            @Y17XAUU
         EJECT                                                 @Y17XAUU
DCBDSECT DCBD  DSORG=TX                                        @Y17XAUU
          TQCBD                                                @Y17XAUU
CVT      DSECT                                                 @Y17XAUU
         CVT                                                   @Y17XAUU
TIOT     DSECT                                                 @Y17XAUU
         IEFTIOT1                                              @Y17XAUU
         EJECT                                                 @Y17XAUU
         TTRMD                                                 @Y17XAUU
         EJECT                                                 @Y17XAUU
         TAVTD                                                 @Y17XAUU
         EJECT                                                 @Y17XAUU
         TTCDSD                                                @Y17XAUU
         END
