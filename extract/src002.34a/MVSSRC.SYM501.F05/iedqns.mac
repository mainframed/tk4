QNS      TITLE 'IEDQNS-INCIDENT OVERFLOW ROUTINE'
IEDQNS   CSECT
         SPACE 3
* CHANGE ACTIVITY AS FOLLOWS
*A444000,684000,852000,894000,912000                             A42413
*C492000,691000,865000                                           A42413
*C048000,522000,570000,672000,822000                            OX02188
*A447000,594000,684000,852000,882000                            OX02188
* A711900                                                      @ZA02087
*A448000,558000,618000                                         @OZ17605
*C840000                                                       @OZ17605
*C886000                                                       @OY20868
*D840000                                                       @OZ32708
         SPACE 3
***********************************************************************
*                                                                     *
*TITLE:'IEDQNS'-CHECKPOINT INCIDENT OVERFLOW ROUTINE                  *
*                                                                     *
*MODULE NAME=IEDQNS
*
*DESCRIPTIVE NAME=INCIDENT OVERFLOW ROUTINE
*
*COPYRIGHT='NONE'
*
*  STATUS:  CHANGE LEVEL 6                                            *
*                                                                     *
*FUNCTION:THIS MODULE GAINS CONTROL WHEN ALL THE INCIDENT DISK        *
*   RECORDS HAVE BEEN USED. IT REMOVES THE ENVIRONMENT CHECKPOINT     *
*   REQUEST ELEMENT FROM ITS QUEUE(IT IS IN EITHER THE TIME DELAY     *
*   QUEUE OR THE CHECKPOINT QCB CHAIN.). IT LOCATES THE LAST          *
*   REQUEST ELEMENT FOR WHICH A DISK RECORD WAS BUILT,AND INSERTS     *
*   THE ENVIRONMENT REQUEST ELEMENT INTO THE NEXT POSITION IN THE     *
*   QCB CHAIN. THE ENVIRONMENT CHECKPOINT WILL BE THE NEXT RECORD     *
*   BUILT AND THE INCIDENT DISK RECORDS CAN BE OVERLAID.              *
*                                                                     *
*ENTRY POINT:                                                         *
*                                                                     *
*        IEDQNS                                                       *
*                                                                     *
*INPUT:REGISTERS 2,9,12,14,15 CONTAIN THE FOLLOWING VALUES:           *
*                                                                     *
*   2-ADDRESS OF CHECKPOINT WORK AREA                                 *
*   9-ADDRESS OF AVT                                                  *
*   12-BASE REGISTER FOR CHECKPOINT EXECUTOR-IEDQNF                   *
*   14-RETURN POINT-IN IEDQNF                                         *
*   15-ENTRY POINT OF THIS MODULE                                     *
*                                                                     *
*OUTPUT:THE ABOVE REGISTERS REMAIN UNCHANGED.                         *
*                                                                     *
*   THE ENVIRONMENT REQUEST ELEMENT IS INSERTED IN THE QCB CHAIN.     *
*                                                                     *
*EXTERNAL ROUTINES:                                                   *
*                                                                     *
*        AQCTL SVC-TPOSTS ELEMENT TO READY QUEUE FOR IEDQHG           *
*EXITS-NORMAL:THIS MODULE BRANCHES TO 4 PLUS THE ADDRESS IN R14. NO   *
*   MORE LOAD MODULES ARE NEEDED FOR THIS MODULE.                     *
*                                                                     *
*EXITS-ERROR:NONE.                                                    *
*                                                                     *
*TABLES/WORK AREAS:                                                   *
*                                                                     *
*   AVT(AVTCKELE,AVTCPRMB,AVTCKPTB                                    *
*   CHECKPOINT WORK AREA                                              *
*                                                                     *
*ATTRIBUTES:REENTERABLE                                               *
*                                                                     *
*NOTES:THE OPERATION OF THIS MODULE DEPENDS UPON AN INTERNAL REPRE-   *
*   SENTATION OF THE EXTERNAL CHARACTER SET WHICH IS EQUIVALENT TO    *
*   THE ONE USED AT ASSEMBLY TIME. THE CODING HAS BEEN ARRANGED SO    *
*   THAT REDEFINITION OF 'CHARACTER' CONSTANTS,BY REASSEMBLY, WILL    *
*   RESULT IN A CORRECT MODULE FOR THE NEW DEFINITIONS.               *
*                                                                     *
         SPACE 3
*REGISTER ASSIGNMENTS
         SPACE
R0       EQU   0
R1       EQU   1
R2       EQU   2                        ADDRESS OF CKPT WORK AREA
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
RAVT     EQU   9                        ADDRESS OF AVT
R10      EQU   10
R11      EQU   11
R12      EQU   12                       BASE REG FOR CHECKPT EXECTR
R13      EQU   13
R14      EQU   14                       RETURN REG
R15      EQU   15                       BASE FOR THIS MODULE
ONE      EQU   1                        CONSTANT OF ONE         OX02188
ADDR     EQU   7                        CONSTANT               @OZ17605
         SPACE 3
         USING *,R15
         USING IEDQAVTD,RAVT            BASE FOR AVT
         USING IEDQCKPD,R2              BASE FOR CKPT WORK AREA
         SPACE
IEDQNS   IEDHJN  QNS00                  NAME AND HJN             Y02027
         LA    R1,AVTCKELE              ADDR OF ENV REQUEST ELEMENT
         ST    R1,CKPTRMAD
         TM    AVTCKELF,AVTCPIPN        CHECK FOR CKPT IN PROGRESS
         BO    QNS60                    RETURN IF IN PROGRESS   OX02188
         TM    AVTCKELF,AVTCRTLN        CHECK REQUEST BIT IN ENVRNM    X
                                        CKPT REQUEST ELEMENT
         BNZ   QNS30                    BRANCH IF ALREADY REQUESTED
         SPACE 3
*REMOVE ENVIRONMENT CKPT REQUEST ELEMENT FROM TIME DELAY QUEUE
         SPACE
DELAY    EQU   *                                               @OZ17605
         LA    R6,AVTCPRMB              ADDR TIME DELAY REMOVAL QCB1020
         ST    R6,AVTCKRMV              PUT QCB ADDR INTO ELEMENT  1020
         MVI   AVTCKRMV,CREREMVE        SET UP KEY FIELD FOR IEDQNF1020
         LA    R6,AVTCKRMV              ADDRESS OF CKPT REMOVAL ELM
         MVI   AVTECD4(R6),PRITIME+ONE  SET HIGHER PRIORITY     OX02188
*                                        THAN TIME ELEMENT      OX02188
         XC    5(3,R6),5(R6)            CLEAR LINK FIELD
         LR    R7,R6
         STM   R6,R7,CKPGETML           PARM LIST FOR SVC
         MVI   CKPGETML,X'0C'           SET FLAG
         MVI   CKPGETML+AVTECD4,AVTE80  SET END OF LIST         OX02188
         LR    R5,R15                   SAVE BASE REG
         LA    R1,CKPGETML              ADDRESS OF LIST
         AQCTL                          POST ELEMENT TO READY QUEUE
         LR    R15,R5                   RESTORE BASE REG
         TM    AVTCKELF,AVTCRTLN        CHECK REQUEST BIT IN   @OZ17605
*                                        ENV CKPT REQ ELEMENT  @OZ17605
         BNZ   QNS30                    BR IF ALREADY REQUESTED@OZ17605
         SPACE 3
*GET THE LAST REQUEST ELEMENT FOR WHICH A DISK RECORD WAS BUILT.
*PUT THE ENVIRONMENT REQUEST ELEMENT IN THE NEXT POSITION IN THE
*QCB CHAIN.
         SPACE
         BAL   R3,GETLOCK               SET MODE AND LOCK        Y02027
QNS10    EQU   *
         L     R3,CKPLREB               GET LAST REQUEST ELEM BUILT
         LTR   R3,R3                    CHECK FOR NO DISK RCDS BLT
         BNZ   QNS15                    BRANCH IF RCDS BUILT    OX02188
         SPACE
         LA    R3,AVTCKPTB-4            ADDRESS OF CKPT QCB
QNS15    EQU   *                                                OX02188
         MVC   AVTCKELE+5(3),5(R3)      MOVE LINK FIELD TO ENVIRON     X
                                        REQUEST ELEMENT
         MVC   5(3,R3),CKPTRMAD+1       PUT ENV REQ EL INTO QCB CHN
         LR    R6,R14                   SAVE REG                 Y02027
         LR    R5,R15                   SAVE REG                 Y02027
         SETLOCK RELEASE,TYPE=LOCAL,REGS=USE,                          C
               RELATED=ATTACHED-TASKS-IEDQNS(SETATSK)            Y02027
         SPACE 2                                               @ZA02087
         LR    R15,R5                   RESTORE BASE REG       @ZA02087
         SPACE 2                                               @ZA02087
         MODESET EXTKEY=TCAM            SET TO TCAM KEY          Y02027
         MODESET MODE=PROB              SET TO PROBLEM STATE     Y02027
         LR    R14,R6                   RESTORE REG              Y02027
         LR    R15,R5                   RESTORE REG              Y02027
         SPACE 3
*RETURN TO CHECKPOINT EXECUTOR. NO MORE LOAD MODULES NEEDED
         SPACE
         OI    AVTCKELF,AVTCINCN+AVTCRTLN  TURN ON REQ BITS FOR OVRF
         B     4(R14)                   RETURN
         SPACE 3
*AN ENVIRONMENT CKPT IS ALREADY REQUESTED. REMOVE ELEMENT FROM QCB
*ELEMENT CHAIN.
         SPACE
QNS30    EQU   *
         BAL   R3,GETLOCK               SET MODE AND LOCK        Y02027
         L     R3,AVTCKPTB              GET QCB ELEMENT CHAIN
         SPACE 3
*BEGINNING OF LOOP THROUGH QCB ELEMENT CHAIN
         SPACE
QNS40    EQU   *
         CLC   5(3,R3),CKPTRMAD+1       IS THIS ELEMENT CHAINED TO     X
                                        ENV REQUEST ELEMENT
         BE    QNS45                    BRANCH IF CHAINED TO ENVOX02188
         SPACE
         L     R3,4(0,R3)               GET NEXT ELEMENT IN CHAIN
         CLM   R3,ADDR,AVTDELAD+ONE     IS THIS DUMMY LAST     @OZ17605
         BNE   QNS40                    NO, CHECK NEXT ELEMENT @OZ17605
         LR    R6,R14                   SAVE REG               @OZ17605
         LR    R5,R15                   SAVE REG               @OZ17605
         SETLOCK  RELEASE,TYPE=LOCAL,REGS=USE,                         X
               RELATED=ATTACHED-TASKS-IEDQNS(SETATSK)          @OZ17605
         LR    R15,R5                   RESTORE BASE REG       @OZ17605
         MODESET   EXTKEY=TCAM          SET TO TCAM KEY        @OZ17605
         MODESET   MODE=PROB            SET TO PROBLEM STATE   @OZ17605
         LR    R14,R6                   RESTORE REG            @OZ17605
         LR    R15,R5                   RESTORE REG            @OZ17605
         B     DELAY                    REMOVE CHPT REQUEST    @OZ17605
*                                        ELEM FROM TIME DELAY  @OZ17605
         SPACE
*END OF LOOP THROUGH QCB ELEMENT CHAIN
QNS45    EQU   *                                                OX02188
         MVC   5(3,R3),AVTCKELE+5       MOVE LINK FIELD OF ENV REQ     X
                                        ELEMENT TO PREVIOUS ELEMENT
         B     QNS10                    PUT ENV REQ EL BACK IN CHAN
         SPACE 3
QNS60    EQU   *                                                OX02188
         OI    AVTCKELF,AVTCINCN        TURN ON OVERFLOW REQUESTOX02188
         B     AVTECD12(R14)             AND RETURN            @OY20868
GETLOCK  EQU   *                                               @OY20868
         LR    R6,R14                   SAVE REG                 Y02027
         LR    R5,R15                   SAVE REG                 Y02027
         MODESET MODE=SUP               SET TO SUPERVISOR STATE  Y02027
         MODESET EXTKEY=SUPR            SET TO KEY ZERO          Y02027
SETATSK  EQU   *                                                 Y02027
         SETLOCK OBTAIN,TYPE=LOCAL,MODE=UNCOND,REGS=USE,               C
               RELATED=ATTACHED-TASKS-IGG019RO/IGG019RB(SETATSK) Y02027
         LR    R14,R6                   RESTORE REG              Y02027
         LR    R15,R5                   RESTORE REG              Y02027
         BR    R3                       RETURN                   Y02027
         EJECT
         SPACE 3
         TAVTD
         TCKPD 3330                     MERLIN VERSION             0421
         TPRIOR
         IHAPSA                         DSECT FOR SETLOCK        Y02027
         END
