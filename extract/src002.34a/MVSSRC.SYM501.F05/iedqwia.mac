         TITLE 'IEDQWIA - CONFIGURATOR ADD   SCHEDULER'
IEDQWIA  CSECT
*CHANGE ACTIVITY AS FOLLOWS:
*A168000,224000,414000,438000,548000,574000,592000               A41585
*C238000,244000,744000                                           A41585
*D486000-512000,518000                                           A41585
*A002300,003230-003290,003320-003380,003410-003470,003500-003590 S22024
*A003620,004250,007430-007490,007520-007550,046300,225200-225900 S22024
*A247000,289700-290140,297600-298440,412100,418400,437300-437800 S22024
*A456400-457100,623200,641350-641550,647450-647650               S22024
*C003200,004000,005700,240000,278100,285400-285800,324900        S22024
*C327600,348480,349480,354480,412000,437200,461200,462800,546000 S22024
*C623000,630150,630250,630400,630850,630950,631200,631300,641250 S22024
*D002860-002920,012500,438800,567000,651000-653000               S22024
*C465200                                                         S05331
*A545300-545600,550090-550900,647550                             S05331
*C648660,648570                                                  Y06330
*A297980-298000,298210,402200-402600                            SA64779
*C298000                                                        SA64779
*A225900,326700,327600,347480,348480                           @YA12402
*C650500                                                       @OY20528
**************************************************************** S99528
*                                                              * S99528
* TITLE:                                                       * S22024
*                                                              * S22024
*    'IEDQWIA - TOTE CONFIGURATOR ADD SCHEDULER'               * S22024
*                                                              * S22024
* MODULE NAME:                                                 * S22024
*                                                              * S22024
*    IEDQWIA                                                   * S22024
*                                                              * S22024
* DESCRIPTIVE NAME:                                            * S22024
*                                                              * S22024
*    TOTE CONFIGURATOR ADD SCHEDULER                           * S22024
*                                                              * S22024
* COPYRIGHT:                                                   * S22024
*                                                              * S22024
*    'NONE'                                                    * S22024
*                                                              * S99528
* STATUS:                                                      * S99528
*                                                              * S99528
*    CHANGE LEVEL 5                                            * S05331
*                                                              * S99528
* FUNCTION:                                                    * S99528
*                                                              * S22024
*    THIS MODULE HANDLES THE ADDITION OF CONFIGURATION DATA TO * S99528
*    THE LOCAL AND REMOTE CONFIGURATION DATA SETS.             * S99528
*                                                              * S99528
*    FOR ADD REQUESTS, THE MODULE PROMPTS FOR THE LINE ADDRESS * S99528
*    OR SYMBOLIC TERMINAL NAME.  FOR A LINE ADDRESS, IEDQWI5U  * S99528
*    IS LOADED TO DETERMINE IF THERE IS A VALID UCB.  VALID    * S99528
*    UCB'S ARE CHECKED FOR A DEVICE CLASS OF 10 OR 40. IEDQWIA * S99528
*    THEN CHECKS TO DETERMINE IF THE LINE HAS ALREADY BEEN     * S99528
*    CONFIGURED.  IF NOT, IEDQWI7 IS LOADED TO DETERMINE THE   * S99528
*    ADAPTER TYPE AND ITS FEATURES.  THIS INFORMATION IS PASSED* S99528
*    TO A UNIT CONFIGURATOR, WHICH PROMPTS FOR ADDITIONAL DATA * S99528
*    NEEDED AND BUILDS A CDS ENTRY IN THE OLTCB.  IEDQWIA THEN * S99528
*    WRITES THE CDS ENTRY IN LOCAL PDS AND UPDATES THE PDS     * S99528
*    DIRECTORY.  THIS MODULE THEN PROMPTS TO DETERMINE IF THERE* S99528
*    ARE MORE ADDITIONS.  IF NOT, CONTROL IS TRANSFERRED       * S22024
*    TO THE TOTE CONFIGURATOR SCHEDULER (IEDQWI).              * S99528
*                                                              * S99528
*    FOR TERMINAL ADDITIONS, IEDQWI8 IS LOADED TO DETERMINE IF * S99528
*    THE SYMBOLIC NAME IS A VALID TCAM TERMINAL. IF THE NAME   * S99528
*    IS VALID, THE REMOTE DATA SET IS CHECKED TO SEE IF THE    * S99528
*    TERMINAL HAS ALREADY BEEN CONFIGURED.  IF NOT, IEDQWI9 IS * S99528
*    LOADED TO DETERMINE THE TERMINAL AND LINE FEATURES. THIS  * S99528
*    INFORMATION IS THEN PASSED TO A UNIT CONFIGURATOR, WHICH  * S99528
*    PROMPTS FOR ADDITIONAL DATA NEEDED AND BUILDS A CDS ENTRY * S99528
*    IN THE OLTCB.  IEDQWIA THEN WRITES THE CDS ENTRY IN THE   * S99528
*    REMOTE PDS AND UPDATES THE DIRECTORY.                     * S99528
*                                                              * S99528
*    FOR CHANGE REQUESTS, THIS MODULE RECEIVES CONTROL FOR THE * S99528
*    DELETE SCHEDULER (IEDQWID).  IT THEN FOLLOWS THE PROCEDURE* S99528
*    DESCRIBED ABOVE FOR VALID LINE ADDRESSES AND TERMINALS.   * S99528
*    WHEN THERE ARE NO MORE CHANGES, CONTROL IS TRASFERRED TO  * S99528
*    IEDQWI.  OTHERWISE CONTROL IS TRANSFERRED TO IEDQWID.     * S99528
*                                                              * S22024
*    IF AN ADD OR CHANGE FUNCTION IS REQUESTED FOR A CDS       * S22024
*    EXTENSION, THE SYMBOLICALLY NAMED CDS IS PLACED IN THE    * S22024
*    LOCAL CDS LIBRARY.                                        * S22024
*                                                              * S22024
* ENTRY POINTS:                                                * S99528
*                                                              * S99528
*         IEDQWIA - RECEIVES CONTROL FROM IEDQWI VIA A 'XCTL'  * S99528
*                   WHEN AN 'ADD' FUNCTION IS REQUESTED;       * S99528
*                                                              * S99528
*                 - RECEIVES CONTROL FROM IEDQWID VIA A 'XCTL' * S99528
*                   WHEN A 'CHANGE' FUNCTION IS REQUESTED.     * S99528
*                                                              * S99528
* INPUT:                                                       * S99528
*                                                              * S99528
*    THE FOLLOWING REGISTERS ARE USED AS INPUT:                * S99528
*                                                              * S99528
*    02 - OLTCB POINTER;                                       * S99528
*    13 - SAVE AREA ADDRESS;                                   * S99528
*    15 - ENTRY POINT ADDRESS/RETURN CODE.                     * S99528
*                                                              * S99528
*    THE FOLLOWING OLTCB FIELD ARE USED AS INPUT:              * S99528
*                                                              * S99528
*    CDSWORK - 256 BYTE AREA TO CREATE CNFG RECORDS FOR ADD;   * S99528
*            - 256 BYTE AREA CONTAINING CDS RECORD FOR CHANGE; * S99528
*    CDSINPUT-  80 BYTE AREA TO PASS INFO BETWEEN MODULES;     * S99528
*    CDSBLDL -  40 BYTE AREA USED AS BLDL LIST FOR PDS;        * S99528
*    CDSCESD -  56 BYTE AREA CONTAINING CDS CESD RECORD;       * S99528
*    CDSCNTL -  20 BYTE AREA CONTAINING CDS CNTL RECORD;       * S99528
*    CDSFNFLG- FUNCTION FLAG: X'02' ADD;                       * S99528
*                             X'16' CHANGE TERMINAL;           * S99528
*                             X'26' CHANGE LINE;               * S99528
*    CDSRETFL- RETURN CODE: X'00' VALID                        * S99528
*                           X'01' INVALID                      * S99528
*    TOTINBUF- 80 BYTE AREA FOR INPUT MESSAGES;                * S99528
*    TOTOTBUF- 80 BYTE AREA FOR OUTPUT MESSAGES;               * S99528
*    TOTSAVES- SAVE AREA FOR USE BY THIS MODULE.               * S99528
*                                                              * S99528
* OUTPUT:                                                      * S99528
*                                                              * S99528
*    THE FOLLOWING REGISTERS ARE USED FOR OUTPUT:              * S99528
*                                                              * S99528
*    02 - OLTCB POINTER;                                       * S99528
*    05 - UCB ADDRESS;                                         * S99528
*    13 - SAVE AREA ADDRESS;                                   * S99528
*    15 - ENTRY POINT ADDRESS.                                 * S99528
*                                                              * S99528
*    THE FOLLOWING OLTCB FIELDS ARE USED FOR OUTPUT:           * S99528
*                                                              * S99528
*    CDSWORK - 256 BYTE AREA TO CREATE CDS RECORD FOR ADD;     * S99528
*            - 256 BYTE AREA CONTAINING CDS RECORD FOR CHANGE; * S99528
*    CDSINPUT-  80 BYTE AREA CONTAINING LINE/TERMINAL DATA;    * S99528
*    CDSRETFL- RETURN CODE: X'00' VALID                        * S99528
*                           X'01' INVALID                      * S99528
* EXTERNAL ROUTINES:                                           * S99528
*                                                              * S99528
*         IEDQWID - CHANGE/DELETE SCHEDULER                    * S99528
*         IEDQWI5U- TCU/ADAPTER TYPE MODULE                    * S99528
*         IEDQWI7 - LINE TYPE MODULE                           * S99528
*         IEDQWI8 - TERMINAL VERIFICATION MODULE               * S99528
*         IEDQWI9 - TERMINAL FEATURES MODULE                   * S99528
*         IEDQWK  - MESSAGE MODULE                             * S99528
*                                                              * S99528
* EXITS, NORMAL:                                               * S99528
*         IEDQWI1 - XCTL AFTER 'EXIT' WHEN ADD/CHANGE COMPLETE * S99528
*         IEDQWID - XCTL AFTER 'CHAGMORE' FOR MORE CHANGES     * S99528
*         IEDQWI5U- BALR AFTER 'ADAPTYPE' FOR LINE ADD         * S99528
*         IEDQWI7 - BALR AFTER 'LINETYPE' FOR LINE ADD/CHNG    * S99528
*         IEDQWI8 - BALR AFTER 'VALIDTER' FOR TERMINAL ADD     * S99528
*         IEDQWI9 - BALR AFTER 'TERMTYPE' FOR TERMINAL ADD/CHNG* S99528
*         UNIT CONFG - BALR AFTER 'GETMOD42' FOR TERMINAL      * S99528
*                    - BALR AFTER 'GETMOD40' FOR LINES         * S99528
*                                                              * S99528
* EXITS, ERROR                                                 * S99528
*                                                              * S99528
*         IEDQWI1 - XCTL AFTER 'EXIT' FOR CONFG TERMINATION    * S99528
*         ERROR RECOVERY - RETURN AFTER 'BADCDS' FOR SYNDAD RET* S99528
*                                                              *
* TABLES/WORK AREAS:                                           *
*                                                              *
*    CDSWORK - WORK AREA USED TO PASS INFO BETWEEN MODULES AND *
*              CREATE CDS RECORDS.                             *
*                                                              *
* CHARACTER CODE DEPENDENCY:                                   *
*                                                              *
*    NONE.                                                     *
*                                                              *
* ATTRIBUTES:                                                  * S99528
*                                                              * S99528
*    ENABLED, PROBLEM PROGRAM MODE, TRANSIENT                  * S99528
*                                                              * S99528
* NOTES:                                                       *
*                                                              *
*    NONE                                                      *
*                                                              *
**************************************************************** S99528
         EJECT
**************************************************************** S99528
*                                                              * S99528
*         R E G I S T E R   E Q U A T E S                      * S99528
*                                                              * S99528
**************************************************************** S99528
         SPACE
PARMREG0 EQU   0                       PARAMETER REGISTER        S99528
PARMREG1 EQU   1                       PARAMETER REGISTER        S99528
R1       EQU   1                       WORK REGISTER           @ZM47904
R2       EQU   2                       OLTCB POINTER             S99528
CDSINREG EQU   4                       CDS WORK AREA POINTER     S99528
UCBREG   EQU   5                       UCB POINTER               S99528
WORKREG5 EQU   5                       WORK REGISTER             S99528
WORKREG6 EQU   6                       WORK REGISTER             S99528
BASREG   EQU   8                       IEDQWIA BASE REGISTER     S99528
RETURN   EQU   9                       RETURN ADDRESS            S99528
SAVEAREA EQU   13                      SAVE AREA POINTER         S99528
R14      EQU   14                      RETURN ADDRESS            S99528
RTRM     EQU   14                      TTE BASE                @ZM47904
RETADDR  EQU   14                      REG 14                    S99528
SAVEADDR EQU   15                      SAVE AREA POINTER         S99528
RETCODE  EQU   15                      RETURN CODE               S99528
ENTRYPT  EQU   15                      ENTRY PT ADDRESS          S99528
         EJECT
**************************************************************** S99528
*                                                              * S99528
*                 O F F S E T   E Q U A T E S                  * S99528
*                                                              * S99528
**************************************************************** S99528
         SPACE
CURUCB   EQU   0                       OFFSET TO CURRENT UCB     S99528
NEXTLOC  EQU   1                       OFFSET TO NEXT BYTE       S99528
NXTUCB   EQU   2                       OFFSET TO NEXT UCB        S99528
LEN      EQU   2                       LENGTH OF UNIT ADDR FLD   S99528
CUUADR   EQU   2                       OFFSET TO UNIT ADDRESS    S99528
UNITLEN  EQU   3                       LENGTH OF UNIT ADDR       S99528
LISTLEN  EQU   4                       LEN OF MEMBER NAME        S99528
CUUPTR   EQU   4                       PTR TO UNIT ADDR IN UCB   S99528
PREVSAVE EQU   4                       OFFSET TO PREV SAVE       S99528
FRSTINST EQU   12                      OFFSET TO FIRST EXEC INST S99528
UCBLNADR EQU   13                      OFFSET TO LINE ADR IN UCB S99528
UCBDEVCL EQU   18                      OFFSET TO DEVICE CLASS    S99528
UCBADTYP EQU   19                      OFFSET TO ADAPTER TYPE    S99528
OPENFLAG EQU   48                      OFFSET TO DCB OPEN FLAGS  S99528
INPUTLEN EQU   78                      LEN OF CONFIG DATA (CDS)  S99528
CVT      EQU   16                      PTR TO CVT                S99528
UCBPTR   EQU   X'28'                   OFFSET TO UCB             S99528
OWN      EQU   4                       OFFSET TO NEXT SAVE       S99528
UAPTR    EQU   5                       PTR TO UNIT ADDR IN OLTCB S99528
CDS40DCB EQU   7                       LOCAL DATA SET DCB PTR    S99528
CDS42DCB EQU   7                       REMOTE DATA SET DCB PTR   S99528
CALLERS  EQU   8                       OFFSET TO PREV SAVE       S99528
NAMELEN  EQU   8                       LENGTH OF TERMINAL NAME   S99528
HIGHORDR EQU   8                       SHIFT CNT FOR HIGH ORDER  S99528
MODSIZE  EQU   10                      MODULE SIZE               S99528
AVAILABL EQU   12                      CORE AVAILABLE FOR MODULE S99528
CDSLEN   EQU   255                     LEN OF CDS MEMBER         S99528
RNCLASS  EQU   X'44'                   3705 RESOURCE CLASS     @ZM47904
**************************************************************** S99528
*                                                              * S99528
*            E Q U A T E S   F O R   C O M P A R S I O N S     * S99528
*                                                              * S99528
**************************************************************** S99528
         SPACE
GOODOPEN EQU   X'10'                   TEST FOR GOODOPEN         S99528
CHANGE   EQU   X'06'                   TEST FOR CHANGE FUNCTN    S99528
CHANLINE EQU   X'26'                   TEST FOR CHANGE LINE      S99528
GRAPHCLS EQU   X'10'                   GRAPHICS DEVICE           S99528
TCUCLASS EQU   X'40'                   TRANSMISSION CONTROL      S99528
TERMCLAS EQU   X'42'                   TERMINALS                 S99528
NO       EQU   X'D5'                   NO RESPONSE               S99528
YES      EQU   X'E8'                   YES RESPONSE              S99528
NUMENTRY EQU   X'F0'                   NUMERIC ENTRY (LINE)      S99528
ZERO     EQU   X'00'                   VALID RETURN CODE         S99528
EXITFLG  EQU   X'01'                   INVALID RETURN            S99528
         SPACE
BYTE3    EQU   3                       3 BYTE MOVE COUNT         S99528
LEFTCLR  EQU   X'0F'                   MASK TO CLEAR LEFT BITS   S99528
DATALEN  EQU   X'2D'                   PDS DIR USER DATA LEN     S99528
USERTTRC EQU   X'2D'                   USER TTRC                 S99528
CDSDPMSK EQU   X'40'                   DEVICE PROTECT BIT        S99528
CDSSTDBS EQU   X'80'                   RFT CONFIG FLAG           S99528
LASTTERM EQU   X'01'              LAST TERMINAL ON LINE FLAG     A41585
L8       EQU   8                       LENGTH CONSTANT           S22024
CONCSELF EQU   X'E0'                   CONCENTRATOR ENTRY NAMED  S22024
SPECIAL  EQU   X'F0'                   SPECIAL CONFIG RUN  -     S22024
*                                        ADD/CHANGE TO LOCAL CDS S22024
XFF      EQU   X'FF'               HEXADECIMAL CONSTANT          S22024
BLANK    EQU   X'40'                    BLANK CHARACTER        @YA12402
D1       EQU   1                        CONSTANT 1             @YA12402
D3       EQU   3                        CONSTANT 3             @YA12402
D4       EQU   4                        CONSTANT 4             @YA12402
         EJECT
**************************************************************** S99528
*                                                              * S99528
*            I N I T I A L I Z A T I O N                       * S99528
*                                                              * S99528
**************************************************************** S99528
         SPACE
         SAVE  (14,12)                 SAVE CALLER'S REGISTERS   S22024
         LR    BASREG,ENTRYPT          SET UP BASE REG           S99528
         USING IEDQWIA,BASREG                                    S99528
         USING TOTOLTCB,R2                                       S99528
IEDQWIA  IEDHJN IEDQWIAA,HJN           SET DATE AND MODULE NAME  S22024
         LA    SAVEADDR,TOTSAVE5       ADDR OF NEXT SAVE         S99528
         ST    SAVEADDR,CALLERS(SAVEAREA)   STORE IN PREV SAVE   S99528
         ST    SAVEAREA,OWN(SAVEADDR)  STORE PREV IN NEXT SAVE   S99528
         LR    SAVEAREA,SAVEADDR       STORE SAVE AREA ADDR      S99528
         TM    CDSFNFLG,CHANGE         CHANGE FUNCTION SPECIFIED S99528
         BO    PROCHANG                BRANCH TO CHANGE PROCEDUR S99528
         EJECT
**************************************************************** S99528
*                                                              * S99528
*            A D D   S C H E D U L E R   L O G I C             * S99528
*                                                              * S99528
**************************************************************** S99528
         SPACE
PROCADD  EQU   *                       CLEAR CDS AREA            S99528
         MVI   CDSREC,ZERO             ZERO OUT FIRST LOCATION   S99528
         MVC   CDSREC+NEXTLOC(CDSLEN),CDSREC ZERO OUT REST       S99528
         MVC   CDSINPUT(INPUTLEN),CDSREC     ZERO OUT WORK AREA  S99528
*                            ENTER A LINE ADDR.OR TERMINAL NAME  S99528
         IEDQMSG MSGID=103,FUNCT=REP,INBUF=CEMSG,INCNT=8,        S99528X
               LINK=YES                                          S99528
         TM    TOTINBUF,NUMENTRY       IS ENTRY A LINE ADDRESS?  S22024
         BO    ADDLINE                 YES  GO ADD LINE PROCED   S99528
*                                      NO   CHECK FOR VALID TERM S99528
         EJECT
**************************************************************** S99528
*                                                              * S99528
*        T E R M I N A L   V A L I D I T Y   C H E C K         * S99528
*                                                              * S99528
**************************************************************** S99528
         SPACE
VALIDTER EQU   *                       CHECK TCAM TABL FOR NAME  S99528
         MVC   CDSSYMBL(NAMELEN),TOTINBUF  MOVE NAME TO WORKAREA S22024
         MVC   CDSCESNM(NAMELEN),TOTINBUF  MOVE ESDID TO CESD    S22024
*                                           RECORD               S22024
         LOAD  EP=IEDQWI8              LOAD VERIFICATION MODULE  S99528
         LR    ENTRYPT,PARMREG0        GET ENTRYPT ADDR          S99528
         BALR  RETURN,ENTRYPT          GO SEARCH TCAM TABLE      S99528
         DELETE EP=IEDQWI8             DELETE MODULE             S99528
         CLI   CDSRETFL,ZERO           WAS SYMBOLIC NAME VALID   S99528
         BE    GOODNAME                YES  GO CHECK IN CDS PDS  S99528
         CLI   CDSRETFL,SPECIAL    IS THIS A SPECIAL SYMBOLIC TO S22024
*                                  LOCAL CDS ADD/CHANGE          S22024
         BE    GOODNAM1            YES                           S22024
         CLI   CDSRETFL,CONCSELF       WAS CONCENTRATOR CONFIG   S22024
*                                        REQUESTED?              S22024
         BE    CHKFNFLG                YES, INVALID REQUEST - NO S22024
*                                        CONFIG ALLOWED          S22024
BADNAME  EQU   *                       INVALID TERMINAL NAME     S99528
         SPACE
         IEDQMSG MSGID=132,FUNCT=CEC,RET=CHKFNFLG                S99528
         EJECT
**************************************************************** S99528
*                                                              * S99528
*   C H E C K   F O R   T E R M I N A L   N A M E   I N   P D S* S99528
*                                                              * S99528
**************************************************************** S99528
GOODNAM1 DS    0H                  ALIGN FOR INSTRUCTION         S22024
         OPEN  (CDS40,(INPUT))     OPEN LOCAL CDS DATA SET       S22024
         LA    CDS40DCB,CDS40      GET DCB ADDR                  S22024
         TM    OPENFLAG(CDS40DCB),GOODOPEN TEST FOR GOOD OPEN    S22024
         BZ    OPENERR             ERROR ON OPEN BRANCH          S22024
*                                                                S22024
         TM    CDSFNFLG,CHANGE         CHANGE FUNCTION SPECIFIED Y06330
         BO    SPEXT              YES, BRANCH                   SA64779
         FIND  CDS40,CDSSYMBL,D    SEARCH PDS FOR NAME           S22024
         LTR   RETCODE,RETCODE     IS NAME IN PDS ALREADY        S22024
         BZ    DUPCDS              YES  DO NOT ADD               S22024
*                                  CONTINUE TO ADD               S22024
SPEXT    EQU   *                                                SA64779
         CLI   CDSCFLGS,SPECIAL   IS THIS A NCP CDS RUN?         S22024
         BE    GOODNAM3           YES, BRANCH                    S22024
         MVC   MODNAME(L8),RNCONFIG MOVE IN SPECIAL UNIT CONFIG  S22024
GOODNAM2 EQU   *                                                 S22024
         CLOSE CDS40               CLOSE DATA SET JUST OPENED    S22024
*                                   NAME FOR BLDL                S22024
         B     BLDL40              SEE IF U.C. MODULE EXISTS     S22024
GOODNAM3 EQU   *                                                 S22024
         MVC   MODNAME(L8),NCPCONFG  MOVE NCP CDS MODULE NAME    S22024
*                                      INTO BLDL LIST            S22024
         MVC   CDSTERM(L'NCPCDSNM),NCPCDSNM LET UNIT CONFIG KNOW S22024
*                                           THIS IS AN NCPCDS RUNS22024
         MVI   CDSCFLGS,ZERO      CLEAR CDS FLAGS                S22024
         B     GOODNAM2           BRANCH                         S22024
         SPACE
GOODNAME EQU   *                       IS DEVICE ALREADY CNFGED  S99528
         OPEN  (CDS42,(INPUT))         OPEN REMOTE DATA SET      S99528
         LA    CDS42DCB,CDS42          GET DCB ADDR              S99528
         TM    OPENFLAG(CDS42DCB),GOODOPEN  TEST FOR GOOD OPEN   S99528
         BZ    OPENERR                 BRANCH IF OPEN ERROR      S99528
         SPACE
         FIND  CDS42,CDSSYMBL,D        SEARCH PDS FOR NAME       S99528
         LTR   RETCODE,RETCODE         IS NAME ALREADY IN PDS    S99528
         BZ    DUPCDS                  YES  DO NOT ADD           S99528
         CLOSE CDS42                   CLOSE REMOTE DATA SET     S99528
         B     TERMPREP                BRANCH TERMINAL PREPROCES S99528
         SPACE
DUPCDS   BXH   RETCODE,WORKREG5,TERMPREP BR ON HI TO TERMINAL    S99528
*                                          PREPROCESSOR          S99528
*                                      ALREADY CONFIG, REQ CHANG S99528
         IEDQMSG MSGID=146,FUNCT=CEC,RET=CHKFNFLG                S99528
         EJECT
**************************************************************** S99528
*                                                              * S99528
*          L I N E   V A L I D I T Y   C H E C K               * S99528
*                                                              * S99528
**************************************************************** S99528
         SPACE
ADDLINE  EQU   *                       CHECK IF VALID UCB EXISTS S99528
UCBLOOK  L     WORKREG5,CVT(PARMREG0)  PICK UP CVT POINTER       S99528
         L     WORKREG6,UCBPTR(WORKREG5) PTR TO 2 BYTE UCB TBLE  S99528
UCBLOOK1 LH    UCBREG,CURUCB(WORKREG6) LOAD UCB ADDRESS          S99528
         LA    WORKREG6,NXTUCB(WORKREG6) POINT TO NEXT UCB ADDR  S99528
         LTR   UCBREG,UCBREG           IS THERE A UCB ADDRESS?   S22024
         BZ    UCBLOOK1                PADDING  IGNORE ENTRY     S99528
         BC    CUUPTR,UCBEND           END OF LIST               S99528
         CLI   TOTINBUF+D3,BLANK        IS 4TH CHAR BLANK?     @YA12402
         BE    DIGIT3                   YES                    @YA12402
         CLI   TOTINBUF+D4,BLANK        MORE THAN FOUR CHAR    @YA12402
         BNE   UCBEND                   YES INVALID LINE ADDR  @YA12402
         CLC   TOTINBUF+D1(UNITLEN),UCBLNADR(UCBREG)           @YA12402
*                                       DOES CUU MATCH?        @YA12402
         B     UCBMATCH                 GO TAKE APPRO. BRANCH  @YA12402
DIGIT3   EQU   *                                               @YA12402
         CLC   TOTINBUF(UNITLEN),UCBLNADR(UCBREG)  DOES CUU MATCHS22024
UCBMATCH EQU   *                                               @YA12402
         BNE   UCBLOOK1                NO MATCH  CHK NEXT UCB    S99528
         CLI   UCBDEVCL(UCBREG),TCUCLASS  IS CLASS EQUAL 40      S99528
         BE    GOODUCB                 VALID DEVICE CLASS        S99528
         CLI   UCBDEVCL(UCBREG),GRAPHCLS  IS CLASS EQUAL 10      S99528
         BE    GOODUCB                 VALID DEVICE CLASS        S99528
*                                      UNSUPPORTED DEVICE TYPE   S99528
         IEDQMSG MSGID=143,FUNCT=CEC,RET=CHKFNFLG                S99528
UCBEND   EQU   *                       INVALID LINE ADDRESS      S99528
         IEDQMSG MSGID=141,FUNCT=CEC,RET=CHKFNFLG                S99528
         EJECT
**************************************************************** S99528
*                                                              * S99528
*  C H E C K   F O R   L I N E   A D D R E S S   I N   P D S   * S99528
*                                                              * S99528
**************************************************************** S99528
         SPACE
GOODUCB  EQU   *                       IS LINE ALREADY CUNFIGED  S99528
         MVC   CDSUNIT+CUUADR(LEN),CUUPTR(UCBREG) MOVE UA TO CDS S99528
         NI    CDSUNIT+CUUADR,LEFTCLR  CLEAR FLAG BITS           S99528
         MVC   CDSCESNM(NAMELEN),MODPREFX   PICK UP PREFIX       S99528
         CLI   TOTINBUF+D3,BLANK        IS 4TH CHAR BLANK?     @YA12402
         BE    MOVE3                    YES                    @YA12402
         MVC   CDSCESNM+UAPTR(UNITLEN),TOTINBUF+D1 ADJUST      @YA12402
*                                       FOR LEADING ZERO       @YA12402
         B     MOVEDON                  FINISHED MOVE          @YA12402
MOVE3    EQU   *                                               @YA12402
         MVC   CDSCESNM+UAPTR(UNITLEN),TOTINBUF  INSERT LINE ADDRS22024
MOVEDON  EQU   *                                               @YA12402
         MVC   CDSMEMBR(NAMELEN),CDSCESNM  MOVE NAME TO LIST     S22024
         OPEN  (CDS40,(INPUT))         OPEN LOCAL DATA SET       S99528
         LA    CDS40DCB,CDS40          GET DCB ADDR              S99528
         TM    OPENFLAG(CDS40DCB),GOODOPEN  TEST FOR GOOD OPEN   S99528
         BZ    OPENERR                 BRANCH IF OPEN ERROR      S99528
         FIND  CDS40,CDSMEMBR,D        SEARCH PDS FOR LINE       S22024
         LTR   RETCODE,RETCODE         IS LINE ALREADY IN PDS    S99528
         BZ    DUPCDS40                YES  DO NOT ADD LINE      S99528
         CLOSE CDS40                   CLOSE LOCAL DATA SET      S99528
         B     LINEPREP                GO TO LINE PREPROCESSOR   S99528
DUPCDS40 BXH   RETCODE,WORKREG5,LINEPREP  BR ON HIGH TO LINE     S99528
*                                           PREPROCESSOR         S99528
*                                      ALREADY CONFIG  REQ CHANG S99528
         IEDQMSG MSGID=146,FUNCT=CEC,RET=CHKFNFLG                S99528
         EJECT
**************************************************************** S99528
*                                                              * S99528
*        C H A N G E   S C H E D U L E R   L O G I C           * S99528
*                                                              * S99528
**************************************************************** S99528
         SPACE
PROCHANG EQU   *                       CHECK IF LINE OR TERMINAL S99528
         TM    CDSFNFLG,CHANLINE       IS ENTRY A LINE ADDR      S99528
         BO    ADAPTYP                 YES                       S99528
TERMTYP  EQU   *                                                 S99528
         MVC   CDSCESNM(NAMELEN),CDSSYMBL   MOVE NAME TO CESD    S99528
         B     TERMPREP                BRANCH TO TERM PREPROCESS S99528
ADAPTYP  EQU   *                                                 S99528
         MVI   CDSCLASS,TCUCLASS       MOVE CLASS TO CDS         S99528
         B     LINETYPE                GO DETERMINE LINE TYPE    S99528
         EJECT
**************************************************************** S99528
*                                                              * S99528
*       A D D   T E R M I N A L   P R E P R O C E S S O R      * S99528
*                                                              * S99528
**************************************************************** S99528
         SPACE
TERMPREP EQU   *                                                 S99528
         CLI   CDSRETFL,SPECIAL        IS THIS A SPECIAL SYMBOLICY06300
*                                        TO LOCAL CDS?           Y06330
         BE    GOODNAM1                YES, GO OPEN LOCAL CDS    Y06330
TERMTYPE EQU   *                                                 S99528
         LOAD  EP=IEDQWI9              LOAD MODULE TO GET CHARTC S99528
         LR    ENTRYPT,PARMREG0        GET ENTRYPT ADDRESS       S99528
         BALR  RETURN,ENTRYPT          BRANCH TO MODULE          S99528
         DELETE EP=IEDQWI9             DELETE MODULE             S99528
         CLI   CDSRETFL,ZERO           CHECK FOR VALID RETURN    S99528
         BNE   CLEANUP                 INVALID RETURN CLEANUP    S99528
CONFIG42 EQU   *                       LOAD UNIT CONFIGURATOR    S99528
         MVC   MODNAME,UNTPREFX        MOVE 'U....UCA' TO BLDL   S22024
*                                        LIST                    S22024
         TM    CDSFNFLG,CDSSTDBS       IS STD RFT FLAG ON        S99528
         BO    GET2700                 YES, GET T2700 UNIT CNFG  S99528
         MVC   MODNAME+NEXTLOC(LISTLEN),CDSTERM  MOVE TERM TYPE  S99528
BLDL42   BLDL  PARMREG0,BLDLLIST       GET ADDR OF BLDL LIST     S99528
         LTR   RETCODE,RETCODE         CHECK IF BLDL OK          S99528
         BNZ   CNFG42                  INVALID RETURN GO ANALYZE S99528
         LA    RETURN,GETMOD42         SET UP RETURN ADDRESS     S99528
         B     CHKSIZE                 CHECK IF MODULE FITS CORE S99528
GET2700  MVC   MODNAME+NEXTLOC(LISTLEN),STDRFTCF MOVE IN T2700   S99528
         NI    CDSFNFLG,XFF-CDSSTDBS TURN OFF STD BI-SYNC FLAG   S22024
         B     BLDL42                  GO GET T2700 UNIT CNFG    S99528
CNFG42   CH    RETCODE,NOTFOUND        NO  CONFIG MODULE FOUND   S99528
         BNE   IOERROR                 BECAUSE OF IO ERROR       S99528
NOMODULE EQU   *                       BECAUSE NO MODULE EXISTS  S99528
         IEDQMSG MSGID=147,FUNCT=CEC,RET=CONTPROC                S99528
         EJECT                                                   S99528
IOERROR  EQU   *                       IO ERROR                  S99528
         IEDQMSG MSGID=148,FUNCT=CEC,RET=CONTPROC                S99528
         EJECT
**************************************************************** S99528
*                                                              * S99528
*      C H E C K   C O R E   N E E D E D   F O R   M O D U L E * S99528
*                                                              * S99528
**************************************************************** S99528
         SPACE
         DS    0F                                                S99528
CHKSIZE  EQU   *                       WILL MODULE FIT IN CORE   S99528
         MVC   WORKAREA(BYTE3),CORENEED PLACE SIZE IN WORKAREA   S99528
         L     AVAILABL,TOTMMSPC       GET NUMBER OF BYTES OF    S22024
*                                        UNUSED CORE FROM OLTCB  S22024
         L     WORKREG6,CONFSIZE       GET MAX SIZE OF IEDQWIA   S22024
         SR    AVAILABL,WORKREG6       GET NUMBER OF BYTES       S22024
*                                        AVAILABLE FOR UNIT      S22024
*                                        CONFIGURATOR MODULES    S22024
         L     MODSIZE,WORKAREA        SAVE SIZE OF MODULE       S22024
         SRL   MODSIZE,HIGHORDR        REMOVE UNWANTED BITS      S99528
         SR    AVAILABL,MODSIZE        WILL MODULE FIT           S99528
         BNM   CORELEFT                YES  GO STORE CORELEFT    S99528
*                                      UNABLE TO LOAD MODULE     S99528
         IEDQMSG MSGID=149,FUNCT=CEC,RET=CONTPROC                S99528
         SPACE
CORELEFT EQU   *                                                 S99528
         ST    AVAILABL,TOTMMSPC       STORE UNUSED CORE         S99528
         BR    RETURN                  RETURN TO CALLER          S99528
         EJECT
**************************************************************** S99528
*                                                              * S99528
*     L O A D   T E R M I N A L   C O N F I G   M O D U L E    * S99528
*                                                              * S99528
**************************************************************** S99528
         SPACE
GETMOD42 EQU   *                                                 S99528
         LA    CDSINREG,CDSWORK        POINT TO CDS WORK AREA    S99528
         LOAD  DE=MODNAME              GET UNIT CONFIG MODULE    S99528
         LR    ENTRYPT,PARMREG0        GET ADDR OF MODULE        S99528
         ST    PARMREG0,TOTOLTEN       SAVE ENTRY POINT ADDR     S22024
         L     ENTRYPT,FRSTINST(ENTRYPT)  GET 1ST INST ADDR      S99528
         BALR  RETADDR,ENTRYPT         BRANCH TO UNIT CONFIG     S99528
         DELETE DE=MODNAME             DELETE UNIT CONFIG MODULE S99528
         SPACE 2
         L     RTRM,TERTABLE           GET TTE ADDRESS         @ZM47904
         USING IEDQTRM,RTRM            SET TTE ADDRESSABILITY  @ZM47904
         TM    TRMSTATE,TRMPREF        3705 RESOURCE           @ZM47904
         BZ    CONFIG30                NO, CONTINUE            @ZM47904
         MVI   CDSCLASS,RNCLASS        SET UP CLASS FIELD      @ZM47904
         DROP  RTRM                                            @ZM47904
CONFIG30 EQU   *                                               @ZM47904
         MVC   WORKAREA(BYTE3),CORENEED  PLACE SIZE OF MODULE    S22024
*                                          IN WORKAREA           S22024
         L     MODSIZE,WORKAREA        SAVE SIZE OF MODULE       S22024
         SRL   MODSIZE,HIGHORDR        REMOVE UNWANTED BITS      S22024
         L     AVAILABL,TOTMMSPC       GET NUMBER OF UNUSED BYTESS22024
         AR    AVAILABL,MODSIZE        ADD DELETED MODULE SIZE   S22024
         L     WORKREG6,CONFSIZE       GET MAX SIZE OF IEDQWIA   S22024
         AR    AVAILABL,WORKREG6       RESTORE INITIAL CORE SIZE S22024
         ST    AVAILABL,TOTMMSPC       UPDATE UNUSED BYTE COUNT  S22024
         CLI   CDSRETFL,ZERO           VALID RETURN              S99528
         BE    ADDCDS                  YES  WAS CHANGE SPECIFIED S99528
         SPACE
CNFIGERR EQU   *                       UNIT CNFG ERROR           S99528
         IEDQMSG MSGID=125,FUNCT=REP                             S99528
         CLI   TOTINBUF,YES            WAS ANSWER YES            S22024
         BE    CHKFNFLG                YES  CHK FUNCTION FLAG    S99528
         CLI   TOTINBUF,NO             WAS ANSWER NO?            S22024
         BE    CLEANUP                 NO ANSWER RECEIVED        S99528
*                                      INVALID RESPONSE          S99528
         IEDQMSG MSGID=079,FUNCT=CEC,RET=CNFIGERR                S05331
         EJECT
**************************************************************** S99528
*                                                              * S99528
*     A D D   T E R M I N A L   C O N F I G   T O   P D S      * S99528
*                                                              * S99528
**************************************************************** S99528
         SPACE
ADDCDS   EQU   *                       ADD CDS ENTRY             S99528
         OPEN  (CDS42,(OUTPUT))        OPEN REMOTE DATA SET      S99528
         LA    CDS42DCB,CDS42          GET ADDR OF REMOTE DCB    S99528
         TM    OPENFLAG(CDS42DCB),GOODOPEN  TEST FOR GOOD OPEN   S99528
         BZ    OPENERR                 BRANCH FOR OPEN ERROR     S99528
         MVC   CDSCESNM(NAMELEN),CDSMEMBR   MOVE NAME TO CESD    S99528
         WRITE CDSDECB,SF,CDS42,CDSCESD,56,MF=E  WRITE CESD REC  S99528
         CHECK CDSDECB                 WAIT FOR IO COMPLETION    S99528
         NOTE  CDS42                   GET REL POSITN OF CESD    S99528
         ST    PARMREG1,CDSTTRC        PLACE POSITN IN BLDL TTRC S99528
         MVI   CDSTTRC4,DATALEN        STORE USER DATA LENGTH    S99528
         WRITE CDSDECB,SF,,CDSCNTL,20,MF=E  WRITE CNTL REC       S99528
         CHECK CDSDECB                 WAIT FOR IO COMPLETION    S99528
         TM    CDSFNFLG,CDSDPMSK       IS DEVICE PROTECT BIT ON  S99528
         BZ    WRTCDS42                NO, WRITE CDS REC AS IS   S99528
         OI    CDSSIGNL,CDSDPMSK       YES, TURN ON DEV PROTECT  S99528
WRTCDS42 EQU   *
         WRITE CDSDECB,SF,,CDSREC,256,MF=E  WRITE CDS REC        S99528
         CHECK CDSDECB                 WAIT FOR IO COMPLETION    S99528
         NOTE  CDS42                   GET REL POSITN OF CDS REC S99528
         ST    PARMREG1,CDSTTRD        PLACE POSITN IN BLDL TTRD S99528
         MVI   CDSTTRD4,ZERO           ZERO OUT 4TH POSTN TTRD   S99528
         STOW  CDS42,CDSBLDL,R         UPDATE DIRECTORY          S99528
         LTR   RETCODE,RETCODE         WAS UPDATE SUCCESSFUL     S99528
         BNZ   STOWADD                 STOW ERROR ENCOUNTERED    S99528
         B     STOWREP                 SUCCESSFUL UPDATE
         EJECT
**************************************************************** S99528
*                                                              * S99528
*                    L I N E   P R E P R O C E S S O R         * S99528
*                                                              * S99528
**************************************************************** S99528
LINEPREP EQU   *                                                 S99528
         MVI   CDSCLASS,TCUCLASS       MOVE CLASS TO CDS CREATIO S99528
ADAPTYPE EQU   *                                                 S99528
         LOAD  EP=IEDQWI5U             GET ADAPTER TYPE;TCU TYPE S99528
         LR    ENTRYPT,PARMREG0        GET MODULE ADDRESS        S99528
         BALR  RETURN,ENTRYPT          BRANCH TO MODULE          S99528
         DELETE EP=IEDQWI5U            DELETE MODULE             S99528
         CLI   CDSRETFL,ZERO           VALID RETURN?             S99528
         BNE   CHKFNFLG                NO                        S99528
LINETYPE EQU   *                       GET LINE TYPE             S99528
         LOAD  EP=IEDQWI7              LOAD MODULE               S99528
         LR    ENTRYPT,PARMREG0        GET MODULE ADDRESS        S99528
         BALR  RETURN,ENTRYPT          BRANCH TO MODULE          S99528
         DELETE EP=IEDQWI7             DELETE MODULE             S99528
         CLI   CDSRETFL,ZERO           VALID RETURN?             S99528
         BNE   CLEANUP                 NO; GO CLEANUP            S99528
CONFIG40 EQU   *                       GET TCU CHARACTERISTICS   S99528
         MVC   MODNAME(NAMELEN),UNTPREFX    MOVE PREFX AND BLNKS S99528
         MVC   MODNAME+NEXTLOC(LISTLEN),CDSTCUTY MOVE CNFG ID TO S99528
         CLC   MODNAME,RNCONFIG        IS CONFIG FOR 3705        S05331
         BE    RN3704                  YES BRANCH                S05331
BLDL40   BLDL  PARMREG0,BLDLLIST   DOES UNIT CONFIG EXIST        S22024
         LTR   RETCODE,RETCODE         WAS BLDL OKAY?            S99528
         BNZ   CNFG40                  GO ANALYZE RETURN CODE    S99528
         LA    RETURN,GETMOD40         SET UP RETURN ADDRESS     S99528
         B     CHKSIZE                 SEE IF MODULE WILL FIT    S99528
RN3704   EQU   *                                                 S05331
         IEDQMSG MSGID=110,FUNCT=REP   IS CONFIG FOR 3704        S05331
         CLI   TOTINBUF,YES            WAS ANSWER YES            S05331
         BE    RN3704MV                YES BRANCH                S05331
         CLI   TOTINBUF,NO             ANSWER NO                 S05331
         BE    BLDL40                  BRANCH                    S05331
         IEDQMSG MSGID=079,FUNCT=CEC,RET=RN3704   INVALID REP    S05331
RN3704MV EQU   *                       CONTROL UNIT IS 3704      S05331
         MVC   MODNAME,RNCON04         NEW CONFIG NAME           S05331
         B     BLDL40                  BRANCH                    S05331
CNFG40   CH    RETCODE,NOTFOUND        WAS UNIT CONFIG FOUND     S99528
         BNE   IOERROR                      NO; GO PRINT I/O ERR S99528
         B     NOMODULE                NO MODULE EXIST           S99528
GETMOD40 EQU   *                                                 S99528
         LA    CDSINREG,CDSWORK        POINT TO CDS WORK AREA    S99528
         LOAD  DE=MODNAME              GET UNIT CONFIG MODULE    S99528
         LR    ENTRYPT,PARMREG0        PICK UP ADDR OF UNIT CONF S99528
         ST    PARMREG0,TOTOLTEN       SAVE ENTRY POINT ADDR     S22024
         L     ENTRYPT,FRSTINST(ENTRYPT)    GET 1ST INST ADDR    S99528
         BALR  RETADDR,ENTRYPT         BRANCH TO UNIT CONFIG     S99528
         DELETE DE=MODNAME             DELETE UNIT CONFIG MODULE S99528
         MVC   WORKAREA(BYTE3),CORENEED  PLACE SIZE OF MODULE    S22024
*                                          IN WORKAREA           S22024
         L     MODSIZE,WORKAREA        SAVE SIZE OF MODULE       S22024
         SRL   MODSIZE,HIGHORDR        REMOVE UNWANTED BITS      S22024
         L     AVAILABL,TOTMMSPC       GET NUMBER OF UNUSED BYTESS22024
         AR    AVAILABL,MODSIZE        ADD DELETED MODULE SIZE   S22024
         L     WORKREG6,CONFSIZE       GET MAX SIZE OF IEDQWIA   S22024
         AR    AVAILABL,WORKREG6       RESTORE INITIAL CORE SIZE S22024
         ST    AVAILABL,TOTMMSPC       UPDATE UNUSED BYTE COUNT  S22024
         CLI   CDSRETFL,ZERO           VALID RETURN?             S99528
         BNE   CNFIGERR                NO; GO PRINT CONFIG ERROR S99528
         EJECT
**************************************************************** S99528
*                                                              * S99528
*        A D D   L I N E   C O N F I G   T O   P D S           * S99528
*                                                              * S99528
**************************************************************** S99528
ADDCDSCU EQU   *                       ADD CDS ENTRY             S99528
         OPEN  (CDS40,(OUTPUT))        OPEN LOCAL DATA SET       S99528
         LA    CDS40DCB,CDS40          WAS OPEN SUCCESSFUL?      S99528
         TM    OPENFLAG(CDS40DCB),GOODOPEN   YES IF MATCH        S99528
         BZ    OPENERR                 OPEN ERROR                S99528
         WRITE CDSDECB,SF,CDS40,CDSCESD,56,MF=E  WRITE CESD RECO S99528
         CHECK CDSDECB                 WAIT FOR I/O COMPLETION   S99528
         ST    PARMREG1,CDSTTRC        PLACE POSTION IN BLDL LIS S99528
         MVI   CDSTTRC4,USERTTRC       SET UP # TTR'S AND DATA   S99528
         WRITE CDSDECB,SF,,CDSCNTL,20,MF=E  WRITE CNTL RECORD    S99528
         CHECK CDSDECB                   WAIT FOR I/O COMPLETION S99528
         TM    CDSFNFLG,CDSDPMSK       IS DEVICE PROTECT BIT ON  S99528
         BZ    WRTCDS40                NO, WRITE CDS REC AS IS   S99528
         OI    CDSSIGNL,CDSDPMSK       YES, TURN ON DEV PROTECT  S99528
WRTCDS40 EQU   *
         WRITE CDSDECB,SF,,CDSREC,256,MF=E  WRITE CDS RECORD     S99528
         CHECK CDSDECB                 WAIT FOR I/O COMPLETION   S99528
         NOTE  CDS40                   GET RELATIVE POSITION     S99528
         ST    PARMREG1,CDSTTRD        PLACE POSITION IN BLDL    S99528
         MVI   CDSTTRD4,ZERO           ZERO OUT 4TH POSITION     S99528
         STOW  CDS40,CDSBLDL,R         UPDATE DIRECTORY          S99528
         LTR   RETCODE,RETCODE         WAS UPDATE SUCCESSFUL?    S99528
         BNZ   STOWADD                 STOW ERROR ENCOUNTERED    S99528
         B     STOWREP                 BR TO REPLACE             S99528
         EJECT
**************************************************************** S99528
*                                                              * S99528
*        I / O   E R R O R   A N A L Y S I S   L O G I C       * S99528
*                                                              * S99528
**************************************************************** S99528
OPENERR  EQU   *                       OPEN ERROR PROCEDURE      S99528
         IEDQMSG MSGID=129,FUNCT=CEC,RET=CONTPROC                S99528
BADCDS   EQU   *                                                 S99528
         LA    SAVEAREA,TOTSAVE5       RESTORE SAVE PTR          S99528
         IEDQMSG MSGID=128,FUNCT=CEC                             S99528
         B     CLEANUP                 CLEANUP AND GET OUT       S99528
STOWADD  EQU   *                       STOW ADD ERROR PROCEDURE  S99528
         CH    RETCODE,NOSPACE         CHECK RETURN CODE - NO    S22024
*                                        SPACE                   S22024
         BL    CDSADDED                CDS ADDED                 S99528
         BH    STOW1                   I/O ERROR                 S99528
*                                      NO SPACE                  S99528
         IEDQMSG MSGID=136,FUNCT=CEC,RET=CLEANUP                 S99528
STOWREP  EQU   *                                                 S99528
         IEDQMSG  MSGID=115,FUNCT=CEC,RET=CDSADDED               S99528
CDSADDED EQU   *                       CDS ENTRY ADDED           S99528
         IEDQMSG MSGID=116,FUNCT=CEC,RET=CHKFNFLG                S99528
STOW1    EQU   *                                                 S99528
         IEDQMSG MSGID=135,FUNCT=CEC,RET=CLEANUP                 S99528
STOWDEL  EQU   *                       STOW DELETE ERROR PROCEDU S99528
         IEDQMSG MSGID=134,FUNCT=CEC,RET=CONTPROC                S99528
         EJECT
**************************************************************** S99528
*                                                              * S99528
*        TO CONTINUE OR NOT TO CONTINUE; THAT IS THE QUESTION  * S99528
*                                                              * S99528
**************************************************************** S99528
CONTPROC EQU   *                       DO YOU WISH TO CONTINUE   S99528
         IEDQMSG MSGID=125,FUNCT=REP                             S99528
         CLI   TOTINBUF,YES            CHECK FOR YES RESPONSE    S22024
         BE    CHKFNFLG                YES RESPONSE              S99528
         CLI   TOTINBUF,NO             CHECK FOR NO RESPONSE     S22024
         BE    CLEANUP                 NO RESPONSE               S99528
CONTPRC1 EQU   *                       INVALID RESPONSE          S99528
         IEDQMSG MSGID=079,FUNCT=CEC,RET=CONTPROC                S22024
CHKFNFLG EQU   *                       CHECK FUNCTION FLAG       S99528
         MVI   CDSRETFL,ZERO           RESET RETURN FLAG         S99528
         CLOSE CDS40                   CLOSE LOCAL DATA SET      S99528
         CLOSE CDS42                   CLOSE REMOTE DATA SET     S99528
         TM    CDSFNFLG,CHANGE         WHAT FUNCTION SPECIFIED   S99528
         BO    CHAGMORE                SEE IF MORE CHANGES       S99528
ADDMORE  EQU   *                       ARE THERE MORE ADDITIONS  S99528
         IEDQMSG MSGID=120,FUNCT=REP                             S99528
         CLI   TOTINBUF,NO             WAS ANSWER NO?            S22024
         BE    CLEANUPA                NO, GO TO CLEANUP         S22024
         CLI   TOTINBUF,YES            WAS ANSWER YES?           S22024
         BE    PROCADD                 YES; GO TO ADD PROCEDURE  S99528
         B     CONTPRC1                INVALID RESPONSE          S99528
CHAGMORE EQU   *                       ARE THERE MORE CHANGES    S99528
         IEDQMSG MSGID=121,FUNCT=REP                             S99528
         CLI   TOTINBUF,NO             NO ANSWER?                S22024
         BE    CLEANUPA                NO, GO TO CLEANUP         S22024
         CLI   TOTINBUF,YES            YES ANSWER                S22024
         BNE   CONTPRC1                INVALID RESPONSE          S99528
         L     SAVEAREA,PREVSAVE(SAVEAREA) ADDR.OF CALLERS SAVE S99528
         LM    R14,R2,12(SAVEAREA)     RESTORE REGS 14-02        S99528
         XCTL  (2,12),EP=IEDQWID                                 S99528
CLEANUP  EQU   *                       TURN ON CLEAN UP FLAG     S99528
         MVI   CDSRETFL,EXITFLG                                  S99528
CLEANUPA EQU   *                                                 S22024
         CLOSE CDS42                   CLOSE REMOTE DATA SET     S99528
         CLOSE CDS40                   CLOSE LOCAL DATA SET      S99528
EXIT     EQU   *                                                 S99528
         L     SAVEAREA,PREVSAVE(SAVEAREA)  RESTORE CALLERS      S99528
         LM    R14,R2,12(SAVEAREA)     RESTORE REGS 14-02        S99528
         XCTL  (2,12),EP=IEDQWI1       TRANSFER CNTL TO IEDQWI1  S99528
         EJECT
**************************************************************** S99528
*                                                              * S99528
*        BLDLLIST; CONSTANTS; PATCH AREA; ETC                  * S99528
*                                                              * S99528
**************************************************************** S99528
         SPACE 1                                                 S99528
         DS    0F                                                S99528
BLDLLIST DC    XL2'0001'               NUMBER OF LIST ENTRIES    S99528
         DC    XL2'003A'                    LENGTH OF EACH ENTRY S99528
MODNAME  DC    XL8'0'                       MODULE ID            S99528
         DC    XL16'0'                      ADDITIONAL DIRECTORY S99528
CORENEED DC    XL3'0'                       MAIN STORAGE NEEDED  S99528
         DC    XL31'0'                      ADDITIONAL DIRECTORY S99528
CONFSIZE DC    F'2560'                 MAXIMUM SIZE OF IEDQWIA INS22024
*                                        BYTES                   S22024
NOSPACE  DC    X'000C'                 RETURN CODE FROM STOW     S22024
*                                        IF NO SPACE IN DIRECTORYS22024
BLANKS   DC    CL8'        '                BLANKS               S99528
MODPREFX DC    CL8'G0000000'                LOCAL CDS PREFIX     S99528
UNTPREFX DC    CL8'U    UCA'           UNIT CONFIG PREFIX,SUFFIX S99528
STDRFTCF DC    CL4'2700'               STD RFT UNIT CONFIG       S99528
NOTFOUND DC    X'0004'                      NO MODULE TEST MASK  S99528
         DS    0F                                                S99528
WORKAREA DS    CL10                         WORK AREA            S99528
         DS    0F                                                S99528
PATCH    DS    CL100                        MAINTENANCE AREA     S99528
RNCONFIG DC    C'U3705UCA'         NAME OF SPECIAL MODULE FOR    S22024
RNCON04  DC    C'U3704UCA'             NAME FOR 3704 CONFIG      S05331
*                                  ADD SYMBOLIC NAME TO LOCAL CDSS22024
NCPCONFG DC    C'U3700UCA'        NAME OF CONFIG MODULE FOR NCP  S22024
*                                   CDS                          S22024
NCPCDSNM DC    C'NCP '            NAME TO INDICATE THIS IS AN    S22024
*                                   NCPCDS CONFIG RUN            S22024
         EJECT
***********************************************************************
*                                                                     *
*        L I S T   F O R M   O F   W R I T E ;  D C B ' S             *
*                                                                     *
***********************************************************************
         SPACE
         WRITE CDSDECB,SF,MF=L
         SPACE
CDS40    DCB   DDNAME=OLTLIB,DSORG=PO,MACRF=(R,W),                     X
               RECFM=U,                                          Y06330*
               SYNAD=BADCDS
CDS42    DCB   DDNAME=CDSLIB,DSORG=PO,MACRF=(R,W),                     X
               RECFM=U,                                          Y06330*
               SYNAD=BADCDS
         EJECT
         TTRMD                                                 @ZM47904
         EJECT
         OLTCB CONFG=YES
TERTABLE DS    F                                               @OY20528
         END
         END
