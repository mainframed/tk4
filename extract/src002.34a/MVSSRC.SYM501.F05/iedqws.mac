         TITLE 'IEDQWS-WAITIO SERVICE ROUTINE'
IEDQWS   CSECT
*A000000-999999                                                @Y16X5U0
* CHANGE ACTIVITY AS FOLLOWS:                                    S22024
* THIS MODULE HAS BEEN RESEQUENCED AS OF TCAM 10.0             @Y17XAUU
* A004500,479700,483300,671400,675900,943200                   @G36XRUV
* C480600,484200                                               @G36XRUV
*
***********************************************************************
* DESCRIPTIVE NAME: WAITIO SERVICE ROUTINE                       S22024
*                                                                S22024
* MODULE NAME: IEDQWS  ALIAS IEDQW36 (TCAM,TOTE)               @Y17XAUU
*                                                                S22024
* STATUS:  VERSION 10.0                                        @Y17XAUU
*                                                                     *
* FUNCTIONS:                                                          *
*                                                                     *
*       THE PURPOSE OF THIS ROUTINE IS TO CAUSE THE ON-LINE TEST      *
*       ROUTINE TO WAIT UNTIL THE INITIATED I/O EVENT HAS BEEN        *
*       COMPLETED, AT WHICH TIME THE FINAL CSW AND SENSE DATA         *
*       (IF UNIT CHECK) ARE STORED IN THE TECB.                       *
*                                                                     *
*        IF POLL IS REQUESTED A BRANCH TO IEDQWK IS ISSUED.  * @Y17XAUQ
*        IEDQWK SENDS THE MESSAGE ' MACRO FUNCTION NOT AVAIL'* @Y17XAUQ
*       TO THE CONTROL TERMINAL AND A X'04' RETURN CODE IS RETURNED   *
*       TO THE OLT.                                                   *
*                                                                     *
*       IF POLL IS NOT REQUESTED A WAIT TIME INTERVAL IS SET.  THE    *
*       MAIN ROUTINE THEN DETERMINES HOW MANY EVENTS MUST BE          *
*       COMPLETED BEFORE CONTINUING.  IF THE WAIT TIMES OUT, CONTROL  *
*       WILL BE RETURNED TO THE CALLING ROUTINE WITH AN ERROR         *
*       INDICATOR.                                                    *
*                                                                     *
*       AFTER THE WAIT TIME HAS ELAPSED, CONTROL IS PASSED TO THE     *
*       TIMER COMPLETION ROUTINE, WHICH DETERMINES IF THE             *
*       ECB HAS BEEN POSTED COMPLETE.  IF THE ECB HAS BEEN            *
*       POSTED COMPLETE, CONTROL IS RETURNED TO THE CALLING PRO-      *
*       GRAM.                                                         *
*                                                                     *
*        WAITIO OPERATION IF THE TEST DEVICE IS CONNECTED THRU   S22024
*        A 3705 RUNNING IN NATIVE MODE:                          S22024
*        1. WAIT AFTER A WRIGHT OPERATION-                       S22024
*              THE WRITE IS ASSUMED TO HAVE GONE CORRECTLY AND   S22024
*        CONTROL IS RETURNED TO THE OLTT.                        S22024
*        2. WAIT AFTER A READ OPERATION-                         S22024
*              ABUFFER WILL BE POSTED TO TOTE FROM TCAM WITH     S22024
*              THE EXPECTED DATA. AT THE TIME IEDQWS GETS THE    S22024
*              BUFFER IT WILL HAVE A TCAM BUFFER PREFIX AND THE  S22024
*              BTU WILL BE IN THE LCB. THE BTU AND THE DATA IN   S22024
*              THE BUFFER WILL BE MOVED INTO THE AREA POINTED    S22024
*              TO BY THE OLTT,S CCW STRUNG AS IF AN EXCP HAD     S22024
*              BEEN DONE.  THE ONLY THING POSTED IN THE TECB     S22024
*              WILL BE A COMPUTED RESIDULE COUNT WHICH WILL BE   S22024
*              THE DIFFERENCE BETWEEN THE TOTAL DATA EXPECTED    S22024
*              AND THE TOTAL DATA RECEIVED. THE BUFFER IS THEN   S22024
*              RETURNED TO TCAM AND CONTROL RETURNED TO THE      S22024
*              OLTT.                                             S22024
*                                                                S22024
*       AT THE COMPLETION OF THIS ROUTINE, ONE OF THE FOLLOWING       *
*       RETURN CODES IS PLACED IN REGISTER 15:                        *
*                                                                     *
*       X'00' - NORMAL COMPLETION OF WAITIO FUNCTION                  *
*                                                                     *
*       X'08' - THE WAIT TIMED OUT.                                   *
*                                                                     *
* PROCESSOR: ASSEMBLE XF                                       @Y17XAUU
*                                                              @Y17XAUU
* MODULE TYPE: PROCEDURE                                       @Y17XAUU
*                                                              @Y17XAUU
* LINKAGE: BALR REG14,REG15 FROM IEDQWA                        @Y17XAUU
*                                                              @Y17XAUU
* MODULE SIZE: SEE ASSEMBLER LISTING                           @Y17XAUU
*                                                              @Y17XAUU
* ENTRY POINT:                                                        *
*                                                                     *
*       IEDQWS - CALLED WHEN A WAITIO MACRO IS ISSUED BY AN           *
*       ON-LINE TEST ROUTINE.                                         *
*                                                                     *
* INPUT:        REGISTERS 01,02,13,14,15 CONTAIN THE FOLLOWING VALUES:*
*                                                                     *
*                         01-PARAMETER LIST ADDRESS                   *
*                         02-OLTCB POINTER                            *
*                         13-SAVE AREA ADDRESS                        *
*                         14-RETURN ADDRESS REGISTER                  *
*                         15-ENTRY POINT ADDRESS                      *
*                                                                     *
*                                                                     *
* OUTPUT:       REGISTERS 15 CONTAIN THE FOLLOWING VALUES:            *
*                                                                     *
*                         15-RETURN CODE:                             *
*                            X'00' NORMAL COMPLETION                  *
*                            X'08' TIMEOUT                            *
*                                                                     *
*                                                                     *
* EXTERNAL ROUTINE:                                                   *
*                                                                     *
*       IEDQWK - TO OUTPUT 'MACRO FUNCTION NOT AVAILABLE'             *
*       MESSAGE                                                       *
*                                                                     *
* EXIT POINTS:    BR AFTER 'NOPECB4'  TO IEDQWA                       *
*                                                                     *
* TABLES/WORK AREAS:                                                  *
*                                                                     *
*       OLTCB, IOBLOCKS, TECB, UCB                                    *
*                                                                     *
* ATTRIBUTES:                                                         *
*                                                                     *
*        ENABLED, PROBLEM PROGRAM MODE, RESIDENT, RE-ENTRANT*  @Y17XAUQ
*
* COPYRIGHT='NONE'                                               S22024
*                                                                S22024
* NOTES: SEE BELOW                                             @Y17XAUU
*                                                              @Y17XAUU
*  DEPENDENCIES: EBCDIC CHARACTER CODE                         @Y17XAUU
*                                                              @Y17XAUU
*  REGISTERS CONVENTION: SEE REGISTERS ASSIGNMENT              @Y17XAUU
*                                                              @Y17XAUU
*  PATCH LABEL: PATCH                                          @Y17XAUU
*                                                              @Y17XAUU
*  RESTRICTIONS: NONE                                          @Y17XAUU
*                                                              @Y17XAUU
***********************************************************************
         SPACE 4
         EJECT
R0       EQU   0                       REG 0                     S99528
R1       EQU   1                       REG 1                     S99528
R2       EQU   2                       REG 2                     S99528
R3       EQU   3                       REG 3                     S99528
R4       EQU   4                       REG 4                     S99528
R5       EQU   5                       REG 5                     S99528
R6       EQU   6                       REG 6                     S99528
R7       EQU   7                       REG 7                     S99528
R8       EQU   8                       REG 8                     S99528
R9       EQU   9                       REG 9                     S99528
R10      EQU   10                      REG 10                    S99528
R11      EQU   11                      REG 11                    S99528
R12      EQU   12                      REG 12                    S99528
R13      EQU   13                      REG 13                    S99528
R14      EQU   14                      REG 14                    S99528
R15      EQU   15                      REG 15                    S99528
PARMREG0 EQU   R0                      MSG PARM0                 S99528
PARMREG1 EQU   R1                      MSG PARM1                 S99528
ZEROO    EQU   0                       ZERO                      S99528
ONE      EQU   1                       ONE                       S99528
FOUR     EQU   4                       DISPLACEMENT              S99528
TWO      EQU   2                       DISPLACEMENT              S99528
TWELVE   EQU   12                      DISPLACEMENT              S99528
X3F      EQU   X'3F'                   CLEAR FLAGS               S99528
XFF      EQU   X'FF'                   CLEAR FLAGS               S22024
CCM      EQU   X'30'                   CONDITION CODE MASK       S99528
CC3      EQU   X'30'                   CONDITION CODE MASK       S99528
ZEROZ    EQU   X'00'                   HEX ZERO                  S99528
XF0      EQU   X'F0'                   LOCAL ANR FLAG            S22024
START    EQU   X'0C'                   SVC 102 P LIST START      S22024
ANR      EQU   X'1E'                   ANR TEST                  S22024
X8       EQU   X'08'                   TIME OUT                  S22024
X80      EQU   X'80'                   SECOND ENTRY              S22024
D0       EQU   0                       ZERO                      S22024
D1       EQU   1                       ONE                       S22024
D2       EQU   2                       TWO                       S22024
D3       EQU   3                       THREE                     S22024
D4       EQU   4                       FOUR                      S22024
D5       EQU   5                       FIVE                      S22024
D8       EQU   8                       EIGHT                     S22024
D10      EQU   10                      TEN                       S22024
D14      EQU   14                      FOURTEEN                  S22024
TIC      EQU   X'08'                   COMMAND CODE TIC          S22024
DC       EQU   X'80'                   DATA CHAIN                S22024
READ     EQU   X'02'                   READ CCW                  S22024
BUFFEND  EQU   X'00'                   DUMMY END CHECK           S22024
DISREG2  EQU   28                      DISPLACEMENT TO REG 2   @Y17XAUU
         EJECT
         USING IEDQWS,R15              FOR BR AROUND HJN         S99528
IEDQWS   IEDHJN IEDQWSS,HJN            NAME AND HJN              S99528
         DROP  R15
         USING IEDQWS,R4
         USING TOTOLTCB,R2             TOTE CONTROL BLOCK        S22024
         USING TTECB,R5
         USING IOBLOCKS,R7
*
* S T A N D A R D  L I N K A G E * * *
*
         STM   R14,R12,12(R13)    SAVE CALLER'S REG
         LR    R4,R15             ESTABLISH BASE REG
         ST    R13,TOTSAVE1+D4         SAVE CALLER SAVE ADDR     S22024
         LR    R12,R13            SAVE IT HERE TOO
         LA    R13,TOTSAVE1            MY SAVE ADDRESS           S22024
         ST    R13,8(R12)         SAVE ADDR OF MY SAVE IN HIS SAVE
*
*
         NI    TOTFLG07,X'FF'-TOTPRIEX CLEAR                     S99528
         NI    TOTFLG07,X'FF'-TOTSECEX WAITIO FLASG              S99528
         ST    R1,@PARM1               SAVE PARM ADDRESS         S99528
         LA    R3,12(R1)               GET FLAGS                 S99528
         TM    0(R3),X'1F'             REQUEST FOR UNSUPPORED FUNCTION?
         BC    5,NOPECB4               YES BRANCH                S99528
         L     R5,4(R1)                TECB ADDR                 S99528
         LTR   R5,R5                   VALIV                     S99528
         BZ    PRIMARIE                NO BR                     S99528
         USING WAITIO,R1               PARM LIST                 S99528
         CLC   WIOCDS,ZEROADDR         CDS ADDRESS PRESENT       S99528
         BE    PRIMARIE                NO BRANCH                 S99528
         L     R6,WIOCDS               GET CDS ADDRESS           S99528
         TM    TOTTDTBL,TOTTDORN       TEST DEV ON RN            S22024
         BO    SYMNAME                 YES BRANCH                S22024
         TM    TOTTDTBL,TOTTDTRM       TEST DEVICE TERMINAL      S99528
         BZ    LINETEST                NO BRANCH                 S99528
SYMNAME  EQU   *                                                 S22024
         SR    R8,R8                   CLEAR REG                 S99528
         IC    R8,TOTTTBEL             GET LENGTH OF NAME        S99528
         BCTR    R8,ZEROO              DECREMENT FOR COMPARE     S99528
         L     R7,TOTTNTPR             GET TNT ADDR PRIMARYE     S99528
         EX    R8,COMPARE              COMPARE NAMES             S99528
         BE    PRIMARIE                IF SAME BRANCH            S99528
         L     R7,TOTSRTNT             GET TNT ADDR SECONDARY    S99528
         EX    R8,COMPARE              COMPARE NAMES        Y    S99528
         BE    SECONDAY                IF SAME BRANCH            S99528
         B     ERRCON                  DEVICE NOT PRI OR SEC     S99528
LINETEST LH    R7,TOTPRUCB             GET PRIMARY UCB ADDRESS   S99528
         MVC   UCBAREA,FOUR(R7)        GET UNIT NAME             S99528
         NI    UCBAREA,X3F             CLEAR HI NIBBLE           S99528
         CLC   UCBAREA,TWO(R6)         PRIMARY WAIT              S99528
         BE    PRIMARIE                YES RRANCH                S99528
         LH    R7,TOTSCUCB             SECONDARY UCB ADDR        S99528
         MVC   UCBAREA,FOUR(R7)        GET NNIT ADDRESSDR        S99528
         NI    UCBAREA,X3F             CLEAR HI NIBBLE           S99528
         CLC   UCBAREA,TWO(R6)         SECONDARY WAIT            S99528
         BE    SECONDAY                YES BRANCH                S99528
         B     ERRCON                  DEVICE NOT PRI OR SEC     S99528
         DROP  R1                      WAITIO                    S99528
SECONDAY LA    R7,TOTSCIBK             SECONDARY ADDRESSIBILITY  S99528
         OI    TOTFLG07,TOTSECEX       WAIT FOR SECONDARY        S99528
**                                     BLOCKS
         B     UCBADDR                 BRANCH
ERRCON   MVI   TOTRTCOD,X'0C'          SET RETURN CODE
         B     NOPECB4                 BRANCH
PRIMARIE EQU   *
         LA    R7,TOTPRIBK             GET ADDRESS OF PRIMARY CONTROL
         OI    TOTFLG07,TOTPRIEX       WAIT FOR PRIMARY          S99528
UCBADDR  EQU   *
         LTR   R5,R5                   TECB ADDRESS ZERO         S22024
         BZ    STALL                   IF SO JUST WAIT           S22024
         TM    $TOTFLG1,$TDATBLK       TEST DEVICE ON RN         S22024
         BO    RNTEST                  YES RR                    S22024
WAITIOE  EQU   *                       WAIT ENTRY 270X           S22024
         USING IOBLOCKS,R7
         L     R10,OLTUCBA             GET UCB ADDRESS
         SRL   R10,16                  UCB ADDRESS               S99528
         LTR   R10,R10                 IS THERE ONE
         BZ    ERRCON                  NO BRANCH
         LTR   R5,R5                   TECB ADDRESS              S99528
         BP    PURTST                  YES BRANCH                S99528
STALL    OI    TOTFLG05,TOTTMOUT       FOURCE TIMEOUT            S22024
         XC    ECBOLT,ECBOLT           CLEAR ECB                 S99528
PURTST   TM    0(R3),X'20'             PURGE REQUEST             S99528
         BO    PURGE                   YES BRANCH
**       IF POLL REQUEST BRANCH TO POLL ROUTINE
RNPOLL   EQU   *                       ENTRY FOR 3705 POLL REQ   S22024
         TM    0(R3),X'40'             POLL REQUESTED?
         BO    POLLREQ                 YES BRANCH                S99528
**       ASSUME WAIT=YES
         SPACE 4
*
* S E T  I N T E R V A L  T I M E R  F O R  W A I T
*
NOPOLL   L     R9,0(R3)           GET TIME INTERVAL
         LA    R8,0               CLEAR WORK REG
         SLL   R9,8                    CLEAR HI BYTE             S99528
         SRL   R9,16                             BYTES TO ZEROS
         D     R8,SIXTY           CONVERT TIME TO MINUTES AND SECONDS
         CVD   R8,SECON           CONVERT THE SECONDS TO DECIMAL
         CVD   R9,MINUTE          CONVERT THE MINUTES TO DECIMAL
         UNPK  SPACE(3),MINUTE+6(2)    UNPACK MINUTES
         OI    SPACE+2,X'F0'      GET RID OF SIGN
         MVC   TOTWAITI+2(2),SPACE+1   MOVE TO WAIT PARM
         UNPK  SPACE(3),SECON+6(2)     UNPACK SECONDS
         OI    SPACE+2,X'F0'           CLEAR SIGN
         MVC   TOTWAITI+4(2),SPACE+1   MOVE TO WAIT PARM
         LH    R8,ZERO                 GET HEX ZERO
         STH   R8,TOTWAITI             PAD THE
         STH   R8,TOTWAITI+6           OUTER FOUR BYTES
         ST    R2,TOLTCBA              SAVE OLTCB ADDRESS
         ST    R7,TOLTCBB              SAVE ADDR FOR POST      @Y17XAUQ
         MVC   TIMEAREA(TIMEITLG),TIMEIT  MOVE EXIT CODE TO    @Y17XAUQ
*                                         OLTCB(NON RE ENTRANT)@Y17XAUQ
*
         STIMER REAL,TIMEAREA,DINTVL=TOTWAITI ISSUE TIMER MACRO@Y17XAUQ
*                                                              @Y17XAUQ
         LA    R6,ECBOLT               GET ADDRESS OF ECB
         ST    R6,TOTWECBA             STORE IN P-LIST
         LTR   R5,R5                   TECB ADDRESS ZERO         S22024
         BZ    WAIT                    YES BRANCH                S22024
         B     PRIM                    BRANCH
         SPACE 2
*                                * * *
TTOUT    MVI   TOTRTCOD,X8             SET TIMEOUT TR CODE       S22024
         LTR   R5,R5                   TECB ADDRESS ZERO         S22024
         BZ    STALLOUT                YES BRANCH                S22024
         TM    $TOTFLG1,$TDATBLK       TEST DEVICE ON RN         S22024
         BO    RNTMOUT                 YES BRANCH                S22024
         CLI   IOBSIOCD,CC3            CC EQUAL 3                S99528
         BE    CC3BR                   YES BRANCH                S99528
         B     TOUTREST                BRANCH                    S22024
RNTMOUT  NI    TOTFLG05,X'FF'-TOTTMOUT CLEAR TIMEOUT FLAG        S22024
STALLOUT EQU   *                                                 S22024
         XC    ECBOLT,ECBOLT           CLEAR ECB                 S22024
         B     OUT                     RETURN                    S22024
*
*
* W A I T  F O R  P R I M A R Y  O N L Y  E N T R A N C E
*
PRIM     NI    IOBSIOCD,CCM            CLEAR ALL BUT CC          S99528
         CLI   IOBSIOCD,CC3            CC EQUAL 3                S99528
         BE    TIMEND                  YES BRANCH                S99528
         TM    $TOTFLG1,$TDATBLK       TEST DEVICE ON RN         S22024
         BO    QCHECK                  YES BRANCH                S22024
WAIT     EQU   *                                                 S22024
         WAIT  1,ECB=ECBOLT            EXIO COMPLETE             S99528
TIMEND  TTIMER CANCEL
         TM    TOTFLG05,TOTTMOUT       DID WAIT TIME OUT         S22024
         BO    TTOUT              YES - EXIT
         TM    $TOTFLG1,$TDATBLK       TEST DEVICE ON RN         S22024
         BO    RNREAD                  YES BRANCH                S22024
CC3BR    MVI   TOTRTCOD,ZEROZ          SET NORMALE RT CODE       S22024
TOUTREST EQU   *                                                 S22024
         L     R10,OLTUCBA             GET UCB ADDRESS
         SRL   R10,16                  UCB ADDRESS               S99528
         BAL   R14,PRESULT             EXIT TO REPORT RESULTS
NOPECB4  EQU   *
         TM    TOTFLG07,TOTPRIEX       PRIMARY DEVICE            S99528
         BZ    SECTEST                 NO BRANCH                 S99528
         LA    R1,ONE                  PRIMARY PARM              S99528
         BAL   R12,TOTUCB00            TEST UNALLOCATE           S99528
         B     OUT                     RETURN                    S99528
SECTEST  TM    TOTFLG07,TOTSECEX       SECONDARY DEVICE          S99528
         BZ    OUT                     NO RRANCH                 S99528
         LA    R1,TWO                  SECONDARY RARP            S99528
         BAL   R12,TOTUCB00            TEST UNALLOCATE           S99528
OUT      EQU   *                       UNALLOCATE RETURN         S99528
         NI    TOTFLG05,XFF-TOTTMOUT   CLEAR TIMEOUT FLG         S22024
         L     R13,4(R13)         GET CALLER'S SAVE ADR
         L     R14,12(R13)        GET RETURN ADDRESS
         SR    R15,R15
         IC    R15,TOTRTCOD            GET RETURN CODE           S22024
         TM    $OLTFLGS,$TRACE         TRACN FUNCTION            S99528
         BO    M2                      YES BRANCH                S99528
         TM    $RETMASK,X'FF'          RETURN FUNCTION           S99528
         BZ    RETURN                  NO BRANCH                 S99528
         CLI   TOTRTCOD,0              RETURN CODE ZERO          S99528
         BNE   M2                      NO BRANCH                 S99528
RETURN   EQU   *                                                 S99528
         LM    R0,R12,20(R13)     RESTORE REMAINING REGS
         BR    R14                RETURN TO CALLER'S PROG
*
* F U N C T I O N  N O T  S U P P O R T E D
*
M2       EQU   *                                                 S99528
         USING RESPL,R5           ESTAB ADDRESSABILITY         @Y17XAUQ
         L     R5,TOTRESPL        GET ADDR OF RESPL            @Y17XAUQ
         L     R15,RESPLM2        GET ADDR OF IEDQWM2          @Y17XAUQ
         LM    R2,R12,DISREG2(R13) RESTORE CALLER'S REG        @Y17XAUN
         BR    R15                GO TO IEDQWM2                @Y17XAUQ
         DROP  R5                 FREE REG                     @Y17XAUQ
QCHECK   EQU   *                                                 S22024
         L     R3,OLTBUFA              GET POINTER TO BUFFER     S22024
         LTR   R3,R3                   IS THERE ONE              S22024
         BZ    WAIT                    NO BRANCH                 S22024
         CLI   D0(R3),BUFFEND          IS THIS THE DUMMY END     S22024
         BE    WAIT                    YES BRANCH                S22024
         B     TIMEND                  GO PROSSES BUFFER         S22024
*                                                                     *
**   THIS ROUTINE GETS THE RESULT OF THE I/O OPERATION AND PASSES    **
**   THE RESULTS TO THE OLT                                          **
*                                                                     *
PRESULT  EQU   *
         NI    IOBFLG4,X'FF'-IOBCSWNV  TURN OFF NOT VALID FLAG   S99528
         USING TTECB,R5                TECB ADDRESSABILITY       S99528
         SR    R8,R8                   CLEAR REG                 S99528
         IC    R8,TECBFDCT             GET NUMBER OF EVENT BLOCKSS99528
         SR    R3,R3                   CLEAR REG                 S99528
         IC    R3,TECBEVOC             NUMBER OF EVENTS OCCURRED S99528
         CR    R3,R8                   COMPARE NUMBER USED WITH  S99528
*                                      NUMBER OF BLOCKS          S99528
         BL    NXTENT                  IF ANY LEFT GO FIND IT    S99528
NONELEFT LA    R3,1(R3)                ADD ONE TO EVENTS OCCURREDS99528
         STC   R3,TECBEVOC             STORE IN TECB             S99528
CLEARECB XC    ECBOLT,ECBOLT           CLEAR ECB                 S99528
         TM    TOTFLG05,TOTTMOUT       TIME OUT OCCURE           S22024
         BO    RESLTND                 YES BRANCH                S22024
         TM    IOBFLG4,IOBCSWNV        NOT VALID SET             S99528
         BO    PRESULT                 YES BRANCH                S99528
RESLTND  EQU   *                                                 S22024
         B     0(R14)                  RETURN                    S99528
***      FIND NEXT AVAILABLE EVENT ENTRY
NXTENT   EQU   *                       NEXT EVENT ENTRY          S99528
         SR    R11,R11                 CLEAR REG                 S99528
         IC    R11,TECBFDLN            GET LENGTH OF FIELD       S99528
         MR    R10,R3                  LENGTH OF ALL EVENT BLOCKS 99528
         LA    R11,8(R11)              ADD LENGTH OF FIXED AREA   99528
         LR    R10,R5                  GET TECB ADDRESS           99528
         AR    R10,R11                 POINT TO FIRST FREE EVENT  99528
*                                      BLOCK                      99528
         TM    $TOTFLG1,$TDATBLK       TEST DEVICE ON RN         S22024
         BO    RESAVE                  GO TO SAVE RESIDULE COUNT S22024
         IC    R6,IOBSIOCD             GET CC                    S99528
         SLL   R6,26                   CLEAR ALL                 S99528
         SRL   R6,30                   OUT BUT C C               S99528
         STC   R6,IOBSIOCD             RETURN TO IOB             S99528
         MVC   0(1,R10),IOBSIOCD       MOVE CONDITION CODE FROM  S99528
*                                      IOB                       S99528
         OI    0(R10),X'F0'            CONVERT TO EBCDIC         S99528
         L     R1,OLTUCBA              GET UCB ADDRESS           S99528
         SRL   R1,16                   UCB ADDRESS               S99528
         USING UCB,R1                  UCB ADDRESSIBILITY        S99528
         MVC   2(2,R10),UCBCHA         MOVE IN I/O ADDRESS       S99528
         CLI   IOBSIOCD,X'00'          CONDITION CODE EQ 0       S99528
         BE    ZEROCC                  YES BRANCH                S99528
         CLI   IOBSIOCD,X'01'          CONDITION CODE EQ 1       S99528
         BE    ONECC                   YES BRANCH                S99528
***                                    ASSUME CONDITION CODE 3   S99528
NOUSEDEV LA    R3,1(R3)                BUMP NUMBER OF BLOCKS USEDS99528
CONTSAVE STC   R3,TECBEVOC             SAVE EVENTS USED COUNT    S99528
         B     UNITCKTS                GO CHECK FOR UNIT CK      S99528
RESAVE   STH   R9,D10(R10)             POST REDISULE COUNT       S22024
         B     NOUSEDEV                GO BUMP EVENT BLOXKS USED S22024
ONECC    EQU   *                                                 S99528
         TM    IOBFLG4,IOBCSWV         SECOND CSW VALID          S99528
         BZ    MOVECSW                 NO BRANCH                 S99528
         NI    IOBFLG4,X'FF'-IOBCSWV   TURN OFF FLAG             S99528
         OI    IOBFLG4,IOBCSWNV        TURN ON NOT VALID         S99528
         MVC   5(7,R10),IOBCSWS        CSW TO TECB               S99528
         B     NOUSEDEV                BRANCH                    S99528
MOVECSW  MVC   5(7,R10),IOBCSW1        MOVE CSW TO EVENT BLK     S99528
         B     NOUSEDEV                BUMP NUMBER EVENTS USED   S99528
ZEROCC   EQU   *                                                 S99528
         TM    OLTFLAG2,OLTLV3IO       WAS EXIO LEVEL 3          S99528
         BO    LEVEL3                  YES BRANCH                S99528
         OI    0(R10),X'FF'            SET CC TO FF              S99528
         B     ONECC                   GO MOVE CSW               S99528
LEVEL3   EQU   *                                                 S99528
         LA    R3,1(R3)                BUMP NUMBER EVENTS USED   S99528
         CR    R3,R8                   ANY BLOCKS LEFT           S99528
         BE    NONELEFT                NO BRANCH                 S99528
         IC    R11,TECBFDLN            GET FIELD LENGTH          S99528
         AR    R10,R11                 POINT TO NEXT FIELD ENTRY S99528
         OI    0(R10),X'FF'            SET CC TO FF CC OF 0      S99528
         USING UCB,R1                  UCB                       S99528
         MVC   2(2,R10),UCBCHA         MOVE IN I/O ADDRESS       S99528
         MVC   5(7,R10),IOBCSW1        MOVE CSW TO EVENT BLOCK   S99528
         B     CONTSAVE                GO SAVE COUNT             S99528
UNITCKTS EQU   *                                                 S99528
         TM    IOBCSW+4,X'02'          UNIT CK IN CSW            S99528
         BZ    CLEARECB                NO BRANCH                 S99528
         IC    R11,TECBFDLN            GET EVENT LENGTH          S99528
         MR    R10,R8                  LENGTH OF ALL EVENT BLOCKSS99528
         LA    R11,8(R11)              ADD LENGTH OF FIXED AREA  S99528
         LR    R10,R5                  GET TECB ADDRESS          S99528
         AR    R10,R11                 POINT TO FIRST SENSE BLOCKS99528
***                                    SENSE FIND                S99528
         IC    R3,TECBSNOC             NUMBER OF USED SENSE BLOCKS99528
         IC    R11,TECBSNCT            NUMBER OF SENSE BLOCKS    S99528
         CR    R3,R11                  ALL USED                  S99528
         BNL   BUMPSNOC                YES BRANCH                S99528
         LH    R9,TECBSNLN             SIZE OF SENSE FIELD       S99528
         MR    R8,R3                   SENSE SIZE TIMES NUMBER   S99528
*                                      USED                      S99528
         AR    R9,R10                  ADD ADDR OF FIRST SENSE   S99528
         LH    R11,TECBSNLN            SIZE OF SENSE FIELD       S99528
         MVC   1(2,R9),UCBCHA          MOVE IN I/O ADDRESS       S99528
         L     R12,SENSELN             SENSE LENGTH COMPUTE      S99528
         SR    R11,R12                 FIND LENGTH OF SENSE TO MVS99528
         EX    R11,SENSEMOV            GO MOVE SENSE             S99528
BUMPSNOC LA    R3,1(R3)                BUMP NUMBER OF USED SENSE S99528
*                                      BLOCKS                    S99528
         B     CLEARECB                BRANCH                    S99528
SENSEMOV MVC   3(0,R9),UCBSNS          MOVE UCB SENSE            S22024
***                                                              S99528
***                                    PURGE ROUTINE             S99528
***                                                              S99528
PURGE    EQU   *                       PURGE ROUTINE             S99528
         XC    QVPRGLST(PLISTLEN),QVPRGLST  CLEAR PARM LIST    @Y17XAUQ
         MVI   QVPVRFLG,PRGFLAG             MOVE IN PURGE FLAG @Y17XAUQ
         MVI   QVPDEBNO,PLSTDEB#            MOVE IN DEB NUMBER @Y17XAUQ
         MVC   QVPRGDEB+1(3),OLTDEBA+1 MOVE DEB ADDR TO P-LIST   S99528
         LA    R1,IOB                  GET IOB ADDRESS           S99528
         ST    R1,QVPRGIOB             STORE IN P-LIST           S99528
         LA    R1,QVPRGLST             GET P-LIST ADDRESS        S99528
         SVC   PURGEE                  SVC FOR PURGE             S99528
         MVI   TOTRTCOD,X'00'          SET RETURN CODE           S99528
         B     NOPECB4                 RETURN                    S99528
***                                                              S99528
***                                    POLL ROUTINE              S99528
***                                                              S99528
POLLREQ  EQU   *                       POLL REQUEST ENTRY        S99528
         TM    ECBOLT,X'80'            IS EVENT COMPLETE         S99528
         BO    EIGHTCD                 NO BRANCH                 S99528
         TM    $TOTFLG1,$TDATBLK       TEST DEVICE ON RN         S22024
         BO    RNREAD                  YES BRANCH                S22024
         BAL   R14,PRESULT             GO REPORT RESULTS         S99528
         MVI   TOTRTCOD,X'00'          SET RETURN CODE           S99528
         B     NOPECB4                 RETURN                    S99528
EIGHTCD  MVI   TOTRTCOD,X'08'          SET RETURN CODE           S99528
         B     NOPECB4                 RETURN                    S99528
**************************************************************** S99528
*                                                              * S99528
*        THIS BLOCK OF CODE IS USED TO TURN OFF THE OLTEP      * S99528
*        FLAG IN THE UCB.  THIS FLAG 'UCBNALOC' NORMALLY MEANS * S99528
*        THAT THE DEVICE IS NOT TO BE ALLOCATED.  IT IS SET BY * S99528
*        BY TOTE OR OLTEP TO ALLOW THE DEVICE TO REMAIN NOT    * S99528
*        ALLOCATED WHILE TESTING IS IN PROGRESS.               * S99528
*                                                              * S99528
*        THIS SETTING DISABLES  THE 'GRAPHIC TRAP CODE' TO     * S99528
*        ALLOW IOS TO HANDLE THE IBM 3270 IN A MANNER ACCEPT-  * S99528
*        ABLE TO THE ON-LINE TESTS.                            * S99528
*                                                              * S99528
*        THIS CODE WILL TURN THE BIT OFF IF AND ONLY IF        * S99528
*        IT IS A GRAPHIC DEVICE.'DEVICE CLASS = X'10'.         * S99528
*                                                              * S99528
*        THE FOLLOWING INPUTS ARE ASSUMED:                     * S99528
*                                                              * S99528
*              R2 = ADDRESS OF OLTCB                           * S99528
*              R4 = BASE REGISTER                              * S99528
*              R1 = 1 IF PRIMARY   UCB IS TO BE ALTERED        * S99528
*                 = 2 IF SECONDARY UCB IS TO BE ALTERED        * S99528
*                                                              * S99528
**************************************************************** S99528
TOTUCB00 DS    0H                  ENTRY POINT                   S99528
         STM   R5,R9,TOTUCBAA      SAVE REGISTERS                S99528
         SR    R5,R5               CLEAR REGISTER                S99528
         LA    R5,TUCBONE(R5)      SET UP FOR COMPARE            S99528
         CR    R5,R1               SHOULD PRIMARY UCB BE USED ?  S99528
         BE    TOTUCB01            YES  BRANCH                   S99528
         LA    R5,TUCBONE(R5)      BUMP REG TO TWO               S99528
         CR    R5,R1               SHOULD SECONDARY UCB BE USED  S99528
         BE    TOTUCB02            YES  BRANCH                   S99528
TOTUCB01 DS    0H                                                S99528
         SLR   R5,R5              CLEAR                        @G36XRUV
         ICM   R5,3,TOTPRUCB       SET UP TO ALTER PRIMARY UCB @G36XRUV
         B     TOTUCB03                PRIMARY UCB               S99528
*                                                                S99528
TOTUCB02 DS    0H                                                S99528
         SLR   R5,R5              CLEAR                        @G36XRUV
         ICM   R5,3,TOTSCUCB      SET UP TO ALTER SECONDARY UCB@G36XRUV
*                                                                S99528
TOTUCB03 DS    0H                                                S99528
         LA    R1,TOTUCBAB        GET DATA ADDRESS             @Y17XAUQ
         ST    R1,TOT102A         PUT IN PARAMTER LIST         @Y17XAUQ
         MVI   TOT102A,TOTDATMV   INDICATE THIS IS A DATA MOVE @Y17XAUQ
         LA    R1,TOTDTMVL        GET LENGTH OF MOVE           @Y17XAUQ
         STH   R1,TOTUCBAC        PUT LENGTH IN HOLD AREA      @Y17XAUQ
         LA    R1,TOTUCBAC        GET ADDRESS OF HOLD AREA     @Y17XAUQ
         ST    R1,TOT102E         PUT IT IN PARAMTER LIST      @Y17XAUQ
         MVI   TOT102E,TOTLTWFG   MOVE IN LAST WORD INDICATION @Y17XAUQ
         MVI   TOT102C,TOT2WFLG   MOVE IN 2ND WORD FLAG        @Y17XAUQ
         MVC   TOTUCBAB(LEN1),ZEROO(R5) UCB TO WORK AREA         S99528
         CLI   TOTUCBAB+TUCBCLOS,TUCBCLAS IS IT A GRAPHIC CLASS  S99528
         BNE   TOTUCB04                   NO  RETURN             S99528
*                                         YES TURN OFF UCBNALOC  S99528
*                                         IN UCB IN WORK AREA    S99528
         NI    TOTUCBAB+TUCBNALO,TUCBNALC AND OFF UCBNALOC BIT   S99528
         STH   R5,TOT102D+TUCBONE         STORE UCB ADDRESS IN @Y17XAUQ
*                                         TARGET ADDRESS         S99528
*                                                                S99528
         LA    R1,TOT102           LOAD R1 TO POINT TO PARAM LISTS99528
*                                                                S99528
         AQCTL                     ISSUE DATA MOVE SVC           S99528
TOTUCB04 DS    0H                                                S99528
         LM    R5,R9,TOTUCBAA          RESTORE REGS              S99528
         BR    R12                     RETURN                    S99528
         EJECT
RNTEST   EQU   *                       WAIT FOR DEVICE ON RN     S22024
         L     R10,OLTCCWA             GET FIRST CCW ADDRESS     S22024
         USING CCW,R10                 CCW                       S22024
         CLI   COMMCODE,READ           WAIT FOR READ             S22024
         BE    RNPOLL                  YES BRANCH                S22024
         DROP  R10                     CCW                       S22024
         MVI   TOTRTCOD,ZEROZ          CLEAR RETURN CODE         S22024
         B     OUT                     GO RETURN                 S22024
         EJECT
RNREAD   EQU   *                       WAIT AFTER RN READ        S22024
*                                                               *S22024
*        THE FUNCTION OF THIS ROUTINE IS TO MOVE THE DATA FROM  *S22024
*        TCAM BUFFERS AND UNITS TO THE AREA POINTED TO BY THE   *S22024
*        OLT CCW AND FREE THE BUFFERS                           *S22024
*                                                               *S22024
         SR    R1,R1                   CLEAR R1 FOR  UNIT COUNT  S22024
         L     R3,OLTBUFA              GET BUFFER ADDRESS        S22024
         LTR   R3,R3                   IS THERE A BUFFER ADDRESS Y06330
         BZ    OUT                     NO, EXIT                  Y06330
         USING CCW,R10                 CCW DESECT                S22024
         L     R8,COMMADDR-1           GET COMMAND ADDRESS       S22024
         LH    R6,BYTECNT              GET BYTE COUNT IN CCW     S22024
         SR    R9,R9                                           @Y17XAUQ
         LA    R9,OLLTTHLG+OLLTRHLG    GET LENGTH OF TRNAS NDR @Y17XAUQ
*                                      PLUS REQ/REST HEADER    @Y17XAUQ
         SR    R6,R9             SUBTRACT TH+RH LENGTH CCW CNT @Y17XAUQ
         AR    R8,R9              ADD TH+RH LENGTH TO DATA PTR @Y17XAUQ
         L     R9,TOTAVTPT             GET AVT ADDRESS           S22024
         USING IEDQAVTD,R9             AVT                       S22024
         LA    R1,AVTHDRSZ       GET HEADER BUFFER PREFIX(AVT) @Y17XAUU
         STC   R1,PRFXSIZE        STORE IT IN SAVE AREA        @Y17XAUQ
         LH    R9,AVTKEYLE             GET UNIT SIZE             S22024
         DROP  R9                                              @YM08168
         STH   R9,UNITSIZE             SAVE UNIT SIZE            S22024
         L     R9,TOTAVTPT        RE LOAD AVT POINTER          @Y17XAUQ
         USING IEDQAVTD,R9        AVT                          @YM08168
         L     R9,AVTSAVTP        GET ADDR OF SAVT             @Y17XAUQ
         DROP  R9                 FREE REG                     @Y17XAUQ
         USING IEDNSVTD,R9        ESTAB ADDRABILITY TO SAVT    @Y17XAUQ
         L     R15,SAVTTNTX       GET ADDR OF TTCIN-NA ROUTINE @Y17XAUQ
         ST    R15,TOTTNTX        SAVE ROUTINE ADDRESS         @Y17XAUU
         DROP  R9                      AVT                       S22024
         USING IEDQPRF,R3                                      @Y17XAUQ
         LH    R1,PRFSCAN         OFFSET TO DATA               @YM08168
         SLR   R14,R14            CLEAR REG                    @YM08168
         IC    R14,PRFXSIZE       GET SIZE OF PREFIX           @YM08168
         SLR   R1,R14             GET SIZE OF PAD              @YM08168
         STC   R1,PADSAV          SAVE PAD SIZE                @YM08168
         L     R9,COMMADDR-1     GET POINTER TO OLLT DATA AREA @Y17XAUQ
*                                 BUILT TH AND RH IN OLLT AREA @Y17XAUQ
         USING OLLTFID1,R9        ESTABLISH ADDRESSABILITY     @Y17XAUQ
         SR    R1,R1              CLEAR REGISTER               @Y17XAUQ
         LH    R1,PRFDEST         SET UP TTCIN OF DESTINATION  @Y17XAUQ
         BALR  R14,R15            GO GET NET ADDR              @Y17XAUQ
         STCM  R15,3,OLLTDAF      STORE NA OF DESTIN IN DAF    @Y17XAUN
         LH    R1,PRFSRCE         GET SOURCE TTCIN             @Y17XAUQ
         L     R15,TOTTNTX        RESTORE CONV RTN ADDRESS     @Y17XAUU
         BALR  R14,R15            FIND NETWORK ADDRESS         @Y17XAUQ
         STCM  R15,3,OLLTOAF      SET ORIGIN NETWORK ADDRESS   @Y17XAUQ
*
         LH    R11,PRFSIZE        GET SIZE OF DATA IN BUFFER   @Y17XAUQ
         LA    R1,AVTHDRSZ        GET SIZE OF HEADER PREFIX    @Y17XAUN
         SR    R11,R1           DECREMENT SIZE BY PREFIX LGNT  @Y17XAUQ
         LA    R1,OLLTRHLG        GET LENGTH OF RH             @Y17XAUQ
         AR    R11,R1             ADD IT TO LENGTH REQD        @Y17XAUQ
*
         SLR   R1,R1              CLEAR REG                    @YM08168
         IC    R1,PADSAV          GET PAD SIZE                 @YM08168
         SLR   R11,R1             DECREMENT BUFFER SIZE BY PAD
*                                 SIZE                         @YM08168
         STCM  R11,3,OLLTDCF      PUT LENGTH OF RH + RU IN TH  @Y17XAUN
*
         MVI   OLLTFID1,OLLTIND1  SET TH FLAG 1                @YM08088
         MVI   OLLTTHF2,OLLTIND2  SET TH FLAG 2                @YM08088
         LA    R1,PRF1LEN         GET LENGTH OF NEG BUFFER PRFX@Y17XAUQ
         SR    R3,R1              SUBTRACT IT FROM BUFFER POINT@Y17XAUQ
         DROP  R3                 FREE REG                     @Y17XAUQ
         USING IEDPF1,R3          ESTAB ADDRABILITY TO NEG PRFX@Y17XAUQ
         MVC   OLLTRHF1(OLLTRHLG),PRF1RH MOVE RH FROM NEG PRFX @Y17XAUQ
*                                 INTP OLLT BUFFER AREA        @Y17XAUQ
         MVC   OLLTSEQN,PRF1SQID MOVE SEQ NUMBER INTO OLLT AREA@Y17XAUQ
         AR    R3,R1              RETURN R3 TO BEGINNING OF    @Y17XAUQ
*                                 BUFFER                       @Y17XAUQ
         DROP  R3                 FREE REG                     @Y17XAUQ
         USING IEDQPRF,R3         ESTAB ADDRABILITY TO BUFFER  @Y17XAUQ
         LH    R9,UNITSIZE        GET SIZE OF UNIT             @Y17XAUQ
         LA    R1,AVTHDRSZ        GET SIZE OF HEADER PREFIX    @Y17XAUN
         SR    R9,R1              REG NOW HAS # OF BYTES IN    @Y17XAUQ
*                                 THIS UNIT                    @Y17XAUQ
         LA    R11,PRFSUNIT       THIS REG NOW POINTS TO FIRST @Y17XAUN
         AH    R11,PRFSCAN        DATA BYTE                    @Y17XAUN
*                                                              @Y17XAUQ
         SR    R1,R1              CLEAR REG FOR UNIT COUNT     @Y17XAUQ
UNITCKSZ EQU   *                       UNIT SIZE CHECK           S22024
         LTR   R9,R9                   IS UNIT SIZE ZERO         S22024
         BZ    GETANIT                 YES BRANCH GET ANOTHER    S22024
*                                      UNIT                      S22024
         CR    R9,R6                   IS UNITSIZE GREATER THAN  S22024
*                                      OR EQUAL TO BYTE COUNT    S22024
         BL    UNITSZSM                BRANCH IF UNITSIZE SMALL  S22024
         LTR   R6,R6                   ANY LEFT                  S22024
         BZ    ZEROLFT                 NO BRANCH                 S22024
         BCTR  R6,0                    DECREMENT FOR EX          S22024
         EX    R6,COUNTBYS             GO MOVE NUMBER OF BYTES   S22024
*                                      IN COUNT                  S22024
         LA    R6,D1(R6)               BUMP COUNT BACK           S22024
ZEROLFT  EQU   *                                                 S22024
         SR    R9,R6                   GET NUMBER BYTES FLFT IN  S22024
*                                      UNIT                      S22024
         TM    FLAGSS,DC               DATA CHAINNING ON         S22024
         BZ    ALLMOVE                 NO BRANCH                 S22024
         LA    R10,D8(R10)             GET NEXT CCW              S22024
         CLI   COMMCODE,TIC            NEW OP CODE A TIC         S22024
         BNE   NOTIC                   NO BRANCH                 S22024
         L     R10,COMMADDR-1          GET CCW WHERE TIC POINTS  S22024
NOTIC    EQU   *                       CONTINUE                  S22024
         L     R8,COMMADDR-1           GET NEW TARGET ADDRESS    S22024
         LH    R6,BYTECNT              GET NEW BYTE COUNT        S22024
         B     UNITCKSZ                GO FILL NEXT CCW AREA     S22024
UNITSZSM EQU   *                       UNIT SIZE SMALLER THAN    S22024
*                                      BYTE COUNT IN CCW         S22024
         BCTR  R9,0                    DECREMENT FOR EX          S22024
         EX    R9,COUNTBYS             MOVE UNIT SIZE LENGTH     S22024
         LA    R9,D1(R9)               BUMP BACK COUNT           S22024
         SR    R6,R9                   CCW COUNT MINUS UNITSIZE  S22024
         AR    R8,R9                   ADD INIT SIZE TO CCW ADDR S22024
*                                      THIS ROUTINE FINDS NEXT   S22024
*                                      UNIT IF THERE IS ONE      S22024
GETANIT  EQU   *                                                 S22024
         LA    R1,D1(R1)               NUMBER OF UNITS           S22024
         CLC   PRFTIC(D4),TICTWO       LAST UNIT                 S22024
         BE    ALLMOVE                 YES BRANCH                S22024
         LH    R9,UNITSIZE             GET UNIT SIZE             S22024
         L     R3,PRFTIC               GET NEXT UNIT ADDRESS     S22024
         LA    R11,PRFSUNIT            GET DATA ADDRESS          S22024
         B     UNITCKSZ                GO XCECK UNITS FOR MOVE   S22024
ALLMOVE  EQU   *                       ENTRY AFTER RN READ       S22024
         L     R3,OLTBUFA              GET BUFFER ADDRESS        S22024
         USING IEDQPRF,R3              BUFFER FREFIX             S22024
         LH    R9,PRFSIZE         GET BUFFER DATA SIZE         @Y17XAUQ
         LA    R8,OLLTRHLG        GET LENGTH OF OLLT/OLTT RH   @Y17XAUQ
         AR    R9,R8              BUMP DATA SIZE BY RH         @Y17XAUQ
         LA    R8,OLLTTHLG        GET LENGTH OF OLLT/OLTT TH   @Y17XAUQ
         AR    R9,R8              BUMP DATA+RH SIZE BY TH      @Y17XAUQ
         LA    R8,AVTHDRSZ        GET SIZE OF HEADER PREFIX    @Y17XAUN
         SR    R9,R8              SUBTRACT IT FROM DATA SIZE   @Y17XAUQ
         LR    R8,R9              PUT SIZE IN REG              @Y17XAUQ
         SR    R9,R9              CLEAR SIZE REG               @Y17XAUU
         IC    R9,PADSAV          GET PAD SIZE                 @Y17XAUU
         SR    R8,R9              ADJUST DATA SIZE             @Y17XAUU
         SPACE 3
************************************************************** @Y17XAUQ
*        REGISTER 8 NOW CONTAINS                             * @Y17XAUQ
*              DATA SIZE + L'RH + L'TH - L'PREFIX            * @Y17XAUQ
*        THIS IS THE SAME AMOUNT THAT A READ CCW WOULD HAVE  * @Y17XAUQ
*        SEEN.                                               * @Y17XAUQ
************************************************************** @Y17XAUQ
         DROP  R9                      BTU                       S22024
         LTR   R1,R1                   NUMBER OF NNITS ZERO      S22024
         BNZ   FORCENOT                NO BRANCH                 S22024
         LA    R1,D1(R1)               FORCE UNITS TO ONE        S22024
FORCENOT EQU   *                                                 S22024
         STC   R1,PRFNBUNT             SET NUMBER OF UNITS IN    S22024
*                                      THIS BUFFER               S22024
         L     R10,OLTCCWA             GET CCW ADDRESS           S22024
         LH    R9,BYTECNT              GET BYTE COUNT OF FIRST   S22024
*                                      CCW                       S22024
DATACHTS TM    FLAGSS,DC               DATA CHAINNING            S22024
         BZ    RESCOUNT                NO BRANCH                 S22024
         LA    R10,D8(R10)             GET NEXT CCW              S22024
         CLI   COMMCODE,TIC            THIS CCW A TIC            S22024
         BNE   NOTICCM                 NO BRANCH                 S22024
         L     R10,COMMADDR-1          GET NEXT CCW              S22024
NOTICCM  AH    R9,BYTECNT              ADD BYTE COUNTS           S22024
         B     DATACHTS                GO GET NEXT CCW           S22024
RESCOUNT EQU   *                       COMPUTE RESIDULE COUNT    S22024
         SR    R9,R8                   TOTAL BYTE COUNT MINUS    S22024
*                                      DATA RECIEVED EQUALS      S22024
*                                      REDIDULE COUNT            S22024
         BP    RESIDCT                 IF POSTTIVE POET TECT     S22024
         SR    R9,R9                   DONT ALLOW NEGITIVE R CT  S22024
RESIDCT  EQU   *                                                 S22024
         BAL   R14,PRESULT             POST TECB                 S22024
*                                      DEQUE BUFFER FROM CHAIN   S22024
         MODESET MODE=SUP              SET TO SUPERVISOR MODE  @G36XRUV
         MODESET EXTKEY=SUPR           SET TO KEY 0            @G36XRUV
SETTSTBF DS    0H                                              @G36XRUV
         SETLOCK OBTAIN,TYPE=LOCAL,MODE=UNCOND,REGS=USE,       @G36XRUVC
               RELATED=ATTACHED-TASKS-IEDQWA1(SETTSTBF)        @G36XRUV
         L     R3,OLTBUFA              GET BUFFER ADDRESS        S22024
         L     R9,PRFLINK-D1           GET LINK FIELD AND PUT    S22024
         LA    R9,D0(R9)               CLEAR HIGH BYTE           S22024
         ST    R9,OLTBUFA              IN FIRST BUFFER ADDRESS   S22024
*                                      POINTER                   S22024
         SETLOCK RELEASE,TYPE=LOCAL,REGS=USE,                  @G36XRUVC
               RELATED=ATTACHED-TASKS-IEDQWS(SETTSTBF)         @G36XRUV
         MODESET EXTKEY=TCAM           SET TO TCAM'S KEY       @G36XRUV
         MODESET MODE=PROB             SET TO PROBLEM MODE     @G36XRUV
*        POST BUFFER TO BUFFER RETURN                            S22024
         L     R12,TOTAVTPT            GET AVT ADDRESS           S22024
         USING IEDQAVTD,R12            AVT                       S22024
         LA    R5,AVTBFRTB             BFR RETURN QCB ADDR       S22024
         ST    R5,PRFKEY               QCB ADDR TO BUFFER        S22024
         MVI   PRFPRI,PRIBFRTB         PRIORITY                  S22024
         DROP  R12                     AVT                       S22024
         ST    R3,TPOSTPAR+D4          BUFFER ADDR TO P LIST     S22024
         MVC   TPOSTPAR+D1(D3),TPOSTPAR+D5  SET UP               S22024
         MVI   TPOSTPAR+D4,X80         SCV 102                   S22024
         MVI   TPOSTPAR,START          PARAMETER LIST            S22024
         LA    R1,TPOSTPAR             PUT ADDR OF P LIST IN REG S22024
         AQCTL                         ISSUE SVC 102             S22024
         EJECT
************************************************************** @Y17XAUQ
*                                                            * @Y17XAUQ
*        NOW A CHECK WILL BE MADE TO SEE IF THE              * @Y17XAUQ
*        TCAM QUE WAS HELD DUE TO TESTING ON THE SAME        * @Y17XAUQ
*        LINE AS THE CONTROL TERMINAL OR ALTERNATE           * @Y17XAUQ
*        IF SO AND THIS WAIT10 BRINGS THE LINE OUT OF        * @Y17XAUQ
*        TEST SESSION THE QUEUE WILL BE RELEASED.            * @Y17XAUQ
*                                                            * @Y17XAUQ
************************************************************** @Y17XAUQ
*                                                            * @Y17XAUQ
         TM    TOTFLG02,TOTTSST+TOTTTSPE ARE TEST SESSION      @Y17XAUQ
*                                        STARTED AND TEST      @Y17XAUQ
*                                        SESSION PENDING END   @Y17XAUQ
*                                        BOTH ON               @Y17XAUQ
         BNO   GOOUT              NO NOTHING MORE TO DO-RETURN @Y17XAUQ
*                                 YES LETS RELEASE QUE IF      @Y17XAUQ
*                                 TEST WITN DISCONNECT         @Y17XAUQ
         L     R10,OLTCCWA        GET ADDRESS OF CCW           @Y17XAUQ
         USING CCW,R10            ESTAB ADDRESSABILITY         @Y17XAUQ
         CLI   COMMCODE,READ      IS COMMAND CODE A READ       @Y17XAUQ
         BNE   GOOUT              NO RETURN                    @Y17XAUQ
         L     R10,COMMADDR-1     YES NOW CHECK FOR DISCONNECT @Y17XAUQ
         USING OLLTFID1,R10       ESTAB ADDRESSABILITY         @Y17XAUQ
         CLI   OLLTPRFX,CD1NETS   IS THIS A VALID NET SERVICES @Y17XAUQ
         BNE   GOOUT              NO RETURN                    @Y17XAUQ
         CLI   OLLTPRFX+1,CD1MAINT IS IT NETWORK SERVICES      @Y17XAUQ
         BNE   GOOUT              NO RETURN                    @Y17XAUQ
         CLI   OLLTREQ,CD1RTTD    IS THIS TEST RECORD DATA     @Y17XAUN
         BNE   GOOUT              NO RETURN                    @Y17XAUQ
*                                 YES TO ALL ABOVE             @Y17XAUQ
         CLI   OLLTCMOD,CD0TD     IS COMMAND TEST WITH         @Y17XAUN
*                                 DISCONNECT                   @Y17XAUQ
         BNE   GOOUT              NO RETURN                    @Y17XAUQ
*                                 YES NOW SEE IF CMD EXECUTED  @Y17XAUQ
*                                 OK                           @Y17XAUQ
         CLI   OLLTSYSR,OLLTOKSF  PHASE 3 OK SO FAR            @Y17XAUQ
         BNE   GOOUT              NO RETURN                    @Y17XAUQ
         NI    TOTFLG02,X'FF'-TOTTSST  TURN OFF TEST SESSION   @Y17XAUQ
         NI    TOTFLG02,X'FF'-TOTTTSPE AND TEST.SESS.PEND.END  @Y17XAUQ
*                                 NOW LETS TURN OFF HELD BITS  @Y17XAUQ
*                                 IF REQD                      @Y17XAUQ
         TM    $TOTFLG1,$CTEQTT   IS CT ON SAME LINE AS TEST DV@Y17XAUQ
         BZ    GOOUT0             YES GO RELEASE               @Y17XAUQ
GOOUT1   TM    TOTFLG01,TOTAPTST  IS AP ON SAME LINE AS TEST DV@Y17XAUQ
         BZ    GOOUT              NO RETURN                    @Y17XAUQ
*                                 YES RELEASE                  @Y17XAUQ
         DS    0H                 ESTAB BRANCH POINT           @Y17XAUQ
         L     R6,TOTAPTNT        ADDR OF DUMMY TNT FOR ALT PRT@Y17XAUQ
         SR    R12,R12            CLEAR REG                    @Y17XAUQ
         ICM   R12,1,TOTTTBEL     GET TNT NAME LENGTH          @Y17XAUQ
         AR    R6,R12             BUMP POINTER TO ADDR         @Y17XAUQ
         ICM   R6,7,ZEROO(R6)     LOAD ADDR OF TTE             @Y17XAUQ
         USING IEDQTRM,R6         ESTAB ADDRESSABILITY         @Y17XAUQ
         NI    TRMSTATE,TRMHELDF  TURN OFF HOLD BIT            @Y17XAUQ
         L     R6,TRMDESTQ-ONE    GET QCB ADDRESS              @Y17XAUQ
*
         DROP  R6                 FREE REG                     @Y17XAUQ
         USING IEDQQCB,R6         ESTAB ADDRESSABILITY         @Y17XAUQ
         NI    QCBSTAT,X'FF'-QCBTRMHO TURN OFF HELD BIT ON QCB @Y17XAUQ
         DROP  R6                                              @Y17XAUQ
*                                                              @Y17XAUQ
         B     GOOUT              RETURN                       @Y17XAUQ
*                                                              @Y17XAUQ
         EJECT
GOOUT0   DS    0H                 ESTAB BRANCH POINT           @Y17XAUQ
         L     R6,TOTCTTNT        ADDR OF DUMMY TNT FOR CT     @Y17XAUQ
         SR    R12,R12            CLEAR REG                    @Y17XAUQ
         ICM   R12,1,TOTTTBEL     GET TNTNAME LENGTH           @Y17XAUQ
         AR    R6,R12             BUMP POINTER TO ADDR         @Y17XAUQ
         ICM   R6,7,ZEROO(R6)     ESTAB ADDRESSABILITY         @Y17XAUQ
         USING IEDQTRM,R6         ESTAB ADDRESSABILITY         @Y17XAUQ
         NI    TRMSTATE,TRMHELDF  TURN OFF HOLD BIT            @Y17XAUQ
         L     R6,TRMDESTQ-ONE    GET QCB ADDRESS              @Y17XAUQ
*                                                              @Y17XAUQ
         B     GOOUT1             SEE IF AP NEEDS TO BE        @Y17XAUQ
*                                 RELEASED                     @Y17XAUQ
GOOUT    DS    0H                 ESTABLISH BRANCH POINE       @Y17XAUQ
*
         EJECT
         MVI   TOTRTCOD,ZEROZ          SET RETURN CODE           S22024
         B     OUT                     GET OUT                   S22024
COUNTBYS MVC   0(0,R8),0(R11)          MOVE TO WHERE CCW POINTS  S22024
         EJECT
*
* T I M E R  C O M P L E T I O N  R O U T I N E S
*
TIMEIT   EQU   *
         USING TIMEIT,R4
         STM   R14,R12,12(R13)    SAVE CALLER'S REGISTERS
         LR    R4,R15                  ADDRESSIBILITY BASE LOAD  S99528
         L     R2,TOLTCBA         LOAD ADDR OF OLTCB DSECT
         L     R7,TOLTCBB         SET UP POST REGISTER         @Y17XAUQ
         ST    R13,TOTSAVE2+D4         SAVE CALLERS SAVE ADDR    S22024
         LR    R12,R13                 GET CALLERS SAVE ADDRESS  S99528
         LA    R13,TOTSAVE2            GET MY SAVE ADDR          S22024
         ST    R13,8(R12)              SAVE IN CALLERS           S99528
         OI    TOTFLG05,TOTTMOUT       SET TIME OUT FLAG         S22024
         TM    TOTFLG07,TOTSECEX       TEST DEVICE SECONDARY     S22024
         BO    NOSET1                  YES BR                    S99528
         POST  ECBOLT,4                POST WAIT ECB
         POST  TOTOTECB,4              POST TOTE'S ECB
*
NOSET1   TM    TOTFLG07,TOTSECEX       TEST DEVICE SECONDARY     S22024
         BZ    NOSET2             NO - EXIT
         POST  TOTSCECB,4              POST SECONDARY ECB        S22024
NOSET2   NI    TOTFLG07,X'FF'-TOTPRIEX TRRN OFF PRIMARY FLAG     S22024
         NI    TOTFLG07,X'FF'-TOTSECEX TURN OFF SECONDFARY FLAG  S22024
         LR    R13,R12                 RESTROE R13               S99528
         LM    R14,R12,12(R13)    RETORE CALLER'S REG
         BR    R14                     RETURN TO THE SYSTEM      S99528
TIMEEND  DS    0H                 ESTABLISH ADDR FOR CALCULATI @Y17XAUQ
TIMEITLG EQU   TIMEEND-TIMEIT     LENGTH OF EXIT               @Y17XAUQ
*                                                              @Y17XAUQ
*                                                              @Y17XAUQ
         EJECT
* C O N S T A N T S****************************************************
         DS    0F
COMPARE  CLC   ZEROO(ZEROO,R7),TWELVE(R6)  TNT CDS COMPARE       S99528
TICTWO   DC    X'08000002'             TIC TO TWO EQUALS LAST    S22024
*                                      UNIT IN A BUFFER          S22024
EIGHT    DC    F'8'                    EIGHT                     S99528
SIXTY    DC    F'60'                   SIXTY                     S99528
ZERO     DC    X'F0F0'                 HEX 0                     S99528
         DS    0F
SENSELN  DC    X'00000004'             SENSE LENGH COMPUTE       S99528
PATCH    DC    CL50' '                 PATCH AREA                Y06330
**************************************************************** S99528
*                                                              * S99528
*   SAVE AREAS / CONSTANTS / EQUATED VALUES                    * S99528
*                                                              * S99528
**************************************************************** S99528
WAITIO   DSECT                         WAIT PARM LIST            S99528
WTIOMAP  DS    F                       TRANS MANAGEMENT WORD     S99528
WIOTECB  DS    F                       TECB ADDRESS              S99528
WIOCDS   DS    F                       CDS ADDRESSS              S99528
WIOFLAG  DS    XL1                     FLAGS                     S99528
*                                      BIT 0 EQUALS YES          S99528
*                                      BIT 1 EQUALS POLL         S99528
*                                      BIT 2 EQUALS PURGE        S99528
WIOTIME  DS    XL2                     TIME IN SECONDS           S99528
         OLTCB                         TOTE CONTROL BLOCK        S22024
*************************************************************  @Y17XAUQ
*                                                           *  @Y17XAUQ
*        SAVE AREAS IN OLTCB TO MAKE THIS MODULE REENTRANT  *  @Y17XAUQ
*                                                           *  @Y17XAUQ
************************************************************** @Y17XAUQ
*                                                              @Y17XAUQ
TOTUCBAA DS    5F                 REGISTER SAVE AREA           @Y17XAUQ
TOTUCBAB DS    24C                UCB COPY AREA COMMON SECTION @Y17XAUQ
TOTUCBAC DS    H                  HALF WORD LENGTH FOR SVC 102 @Y17XAUQ
TUCBCLAS EQU   X'10'              GRAPHIC DEVICE CLASS IN UCE  @Y17XAUQ
TUCBCLOS EQU   18                 OFFSET TO DEVICE CLASS       @Y17XAUQ
TUCBNALC EQU   X'FF'-X'04'        AND FIELD TO TURN OFF        @Y17XAUQ
*                                 UCBNALOC                     @Y17XAUQ
TUCBNALO EQU   1                  OFFSET TO UCBNALOC FLAG      @Y17XAUQ
TUCBZERO EQU   0                  USED AS EQU TO START OF FIELD@Y17XAUQ
TUCBONE  EQU   1                  CONSTANT USED TO BUMP REGS   @Y17XAUQ
LEN1     EQU   24                 LENGTH FOR UCB MOVE          @Y17XAUQ
*                                                              @Y17XAUQ
************************************************************** @Y17XAUQ
*                                                            * @Y17XAUQ
*        THE FOLLOWING AREA WILL BE USED TO HOLD THE STIMER  * @Y17XAUQ
*        EXIT ROUTINE SINCE IT IS DIFFICULT TO HAVE A TIMER  * @Y17XAUQ
*        EXIT IN RE_ENTRANT CODE                             * @Y17XAUQ
*                                                            * @Y17XAUQ
************************************************************** @Y17XAUQ
TIMEAREA DS    100C               SPACE TO HOLD EXIT ROUTINE   @Y17XAUQ
TOLTCBA  DS    F                  SAVE AREA FOR OLTCB ADDR     @Y17XAUQ
TOLTCBB  DS    F                  SAVE AREA FOR REG FOR POST   @Y17XAUQ
         EJECT
*************************************************************  @Y17XAUQ
*                                                           *  @Y17XAUQ
*        PARAMETER LIST FOR SVC 102 DATA MOVE               *  @Y17XAUQ
*                                                           *  @Y17XAUQ
*************************************************************  @Y17XAUQ
PRFXSIZE DS    XL1                HEADER BUFFER PREFIX SIZE    @Y17XAUQ
PADSAV   DS    XL1                PAD SIZE SAVE AREA           @YM08168
*                                                              @Y17XAUQ
TOT102   DS    0F                 ALIGN ON FULL WORD           @Y17XAUQ
TOT102A  DS    XL1                FLAG FOR DATA MOVE           @Y17XAUQ
TOT102B  DS    AL3                DATA ADDRESS                 @Y17XAUQ
TOT102C  DS    XL1                2ND WORD ADDRESS             @Y17XAUQ
TOT102D  DS    AL3                TARGET ADDRESS (DYNAMIC)     @Y17XAUQ
TOT102E  DS    XL1                LAST WORD ADDRESS            @Y17XAUQ
TOT102F  DS    AL3                ADDRESS CONTAINING LENGTH    @Y17XAUQ
*                                                              @Y17XAUQ
TOTDTMVL EQU   24                 DATA MOVE LENGTH             @Y17XANQ
TOTDATMV EQU   X'08'              FLAG FOR DATA MOVE           @Y17XANQ
TOT2WFLG EQU   0                  2ND WORD FLAGS               @Y17XANQ
TOTLTWFG EQU   X'80'              LAST WORD FLAG               @Y17XANQ
*                                                              @Y17XAUQ
*                                                              @Y17XAUQ
ZEROADDR DS    A                  ZERO ADDRESS                 @Y17XANQ
UCBAREA  DS    H                  UCB NAME                     @Y17XANQ
UNITSIZE DS    H                  UNIT SIZE IN BUFFERS         @Y17XANQ
BFRTADDR DS    A                  BUFFER RETURN QCB            @Y17XANQ
TPOSTPAR DS    F                  TPOST PARAMETER              @Y17XANQ
         DS    XL1                LIST FOR                     @Y17XANQ
         DS    XL7                BUFFER RETURN                @Y17XANQ
BDQCB    DS    F                  ADDRESS OF BUFFER DISP QCB   @Y17XANQ
WORKA    DS    F                  WORK AREA                    @Y17XANQ
         DS    0F                                              @Y17XANQ
MINUTE   DS    D                  WAIT MINUTES                 @Y17XANQ
SECON    DS    D                  WAIT SECONDS                 @Y17XANQ
@PARM1   DS    F                  PARAMETER ADDRESS            @Y17XANQ
*                                                              @Y17XAUQ
EICT     DS    CL1                TIME                         @Y17XANQ
SPACE    DS    CL3                TIME                         @Y17XAUQ
*                                                              @Y17XAUQ
*                                                              @Y17XAUQ
*                                                              @Y17XAUQ
*
*
PURGEE   EQU   X'10'              PURGE SVC                    @Y17XAUQ
QVPRGLST DS    0F                 PURGE P-LIST                 @Y17XAUQ
QVPRGDEB EQU   QVPRGLST           PURGE P-LIST                 @Y17XAUQ
QVPVRFLG DS    XL1                FLAG BYTE                    @Y17XAUQ
         DS    XL3                DEB ADDR                     @Y17XAUQ
         DS    F                  TCB ADDR                     @Y17XAUQ
QVPRGIOB DS    0F                 ADDR OF IOB                  @Y17XAUQ
QVPDEBNO DS    XL1                NUMBER OF DEBS               @Y17XAUQ
         DS    XL3                IOB ADDR                     @Y17XAUQ
PRGFLAG  EQU   X'A0'              PURGE FLAG                   @Y17XAUQ
PLISTEND EQU   *                  END OF PARM LIST             @Y17XAUQ
PLISTLEN EQU   PLISTEND-QVPRGLST  LENGTH OF PARM LIST          @Y17XAUQ
PLSTDEB# EQU   1                  DEB NUMBER                   @Y17XAUQ
TOTTNTX  DS    A                  SAVE AREA                    @Y17XAUU
*********      TEST EVENT CONTROL BLOCK    **********          @Y17XAUQ
TTECB    DSECT                                                 @Y17XAUQ
TECBFDCT DS    CL1                     NUMBER OF EVENT FIELDS  @Y17XAUQ
TECBFDLN DS    CL1                     LENGTH OF EVENT FIELDS  @Y17XAUQ
TECBSNLN DS    CL2                     LENGTH OF SENSE FIELDS  @Y17XAUQ
FLAGS    DS    CL1                     FLAGS                   @Y17XAUQ
TECBSNCT DS    CL1                     NUMBER OF SENSE FIELDS
TECBSNOC DS    CL1                     NUMBER SENSES OCCURRED
TECBEVOC DS    CL1                     NUMBER EVENTS OCCURRED
UCB      DSECT
         IEFUCBOB
CCW      DSECT                                                   S22024
COMMCODE DS    C                       COMMAND AODE              S22024
COMMADDR DS    AL3                     ADDRESS                   S22024
FLAGSS   DS    XL2                     FLAGS                     S22024
BYTECNT  DS    H                       BYTE COUNT                S22024
         DS    20F                     PATCH AREA                S22024
         TPRIOR                        EQUATES                   S22024
         TAVTD                         AVT                       S22024
         TSCBD                         SCB                       S22024
         EJECT                                                 @Y17XAUQ
         EJECT                                                 @Y17XAUQ
         TCD0D                                                 @Y17XAUQ
         EJECT                                                 @Y17XAUQ
         TCD1D                                                 @Y17XAUQ
         EJECT                                                 @Y17XAUQ
         DSECT                                                 @Y17XAUQ
OLLTFID1 DS    0C                 ON LINE TEST FID1 DESECT     @Y17XAUQ
OLLTTHF1 DS    XL1                TRANS HEADER FLAG 1          @Y17XAUQ
OLLTIND1 EQU   X'1C'              OLLT FLAG 1 SETTING          @YM08088
OLLTTHF2 DS    XL1                TRANS HEADER FLAG 2          @Y17XAUQ
OLLTIND2 EQU   X'00'              OLLT FLAG 2 SETTING          @YM08088
OLLTDAF  DS    XL2                DAF OF NCP PHYS SERVICES     @Y17XAUQ
OLLTOAF  DS    XL2                OAF OF TCAM'S SSCP           @Y17XAUQ
OLLTSEQN DS    XL2                SEQ NUMBER                   @Y17XAUQ
OLLTDCF  DS    XL2                DATA COUNT FIELD             @Y17XAUQ
OLLTTHLG EQU   10                 OLLT TH LENGTH               @Y17XAUQ
*
*
OLLTRHF1 DS    XL1                REQ HEADER FLAG 1            @Y17XAUQ
OLLTRHF2 DS    XL1                REQ HEADER FLAG 2            @Y17XAUQ
OLLTRHF3 DS    XL1                REQ HEADER FLAG 3            @Y17XAUQ
OLLTRHLG EQU   3                  OLLT RH LENGTH               @Y17XAUQ
*
*
OLLTPRFX DS    XL2                PREFIX(NET SVCS & MAINT SVCX)@Y17XAUQ
OLLTREQ  DS    XL1                REQ TYPE EXEC TSTRCD STATIST@Y17XAUQ
OLLTTDEV DS    XL2                DAF OF TEST DEVICE           @Y17XAUQ
OLLTCMD  DS    XL1                COMMAND                      @Y17XAUQ
OLLTCMOD DS    XL1                COMMAND MODIFIER             @Y17XAUQ
OLLTSYSR DS    XL1                SYSTEM RESPONSE              @Y17XAUQ
OLLTERSP DS    XL2                EXTENDED RESPONSE            @Y17XAUQ
OLLTOKSF EQU   X'62'              SYS RESPONSE OF OK SO FAR    @Y17XAUQ
*                                 PHASE 3
         EJECT
         TPRFD                         PREFIX                    S22024
         EJECT                                                 @G36XRUV
         IHAPSA                                                @G36XRUV
         EJECT                                                 @Y17XAUQ
RESPL    RESPL                    TOTES RESIDENT PARM LIST     @Y17XAUQ
         EJECT                                                 @Y17XAUQ
         TTRMD                                                 @Y17XAUQ
         EJECT                                                 @Y17XAUQ
         TQCBD                                                 @Y17XAUQ
*                                                              @Y17XAUQ
*                                                              @Y17XAUQ
*                                                              @Y17XAUQ
IOBLOCKS IOBLOCKS
         END
