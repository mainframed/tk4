         TITLE 'IEDQWAJ  $CUTEST SERVICE MODULE OF TCAM/TOTE'
IEDQWAJ  CSECT
*A000000-999999                                                @Y16X5U0
*CHANGE ACTIVITY AS FOLLOWS:
*C001000,003000,080000,104000,305000,377000,584000               S21903
*A001300-001600,003500,005200-005800,211430-211460,220700-220800 S21903
*A226170,305500,536800                                           S21903
*D218000,389000-395000                                           S21903
*C136000-137000                                                  S05331
*D138000                                                         S05331
*C496000                                                        SA64769
*A117500,760000,145000,170100,598000,774000                    @Y17XAUU
*C146000,598600,619090-619900,649200,649400-649800,674000      @Y17XAUU
* A257000,630000,752000                                        @G36XRUV
* C257000                                                      @G36XRUV
**************************************************************** S99528
*                                                              * S99528
* MODULE NAME: IEDQWAJ (TCAM,TOTE)                             @Y17XAUU
*                                                              * S99528
* DESCRIPTIVE NAME: TOTE $CUTEST SERVICE MODULE                * S21903
*                                                              * S21903
* COPYRIGHT:  'NONE'                                           * S21903
*                                                              * S21903
* STATUS:                                                      * S99528
*    VERSION 10.0                                              @Y17XAUU
*                                                              * S99528
* FUNCTIONS:                                                   * S99528
*    THIS MODULE VERIFIES THAT ALL REQUESTED CHANNEL ADDRESSES * S99528
*    ARE EITHER OFFLINE OR, IF THEY ARE ALLOCATED TO TCAM, THIS* S99528
*    MODULE WILL ISSUE THE 'STOPLINE' OPERATOR CONTROL COMMAND * S99528
*    TO HAVE THEM ASSIGNED TO TOTE.                            * S99528
*                                                              * S99528
*     IF THE ADDRESS IS OFFLINE OR ONLINE BUT NOT ALLOCATED IT * S99528
*    WILL BE MARKED FOR ON LINE TEST ONLY BY TURNING ON THE    * S99528
*    UCBNALOC BIT.                                             * S99528
*                                                              * S99528
*    BEFORE MARKING THE DEVICE ALLOCATED TO TCAM AN 'ENQ' MACRO* S99528
*    IS ISSUED TO INSURE THAT ANOTHER OLT DOES NOT HAVE CONTROL* S99528
*    OF THIS PARTICULAR DEVICE.                                * S99528
*                                                              * S99528
*    THE STATUS OF THESE UNITS  IS STORED IN A SAVE AREA SO    * S99528
*    THAT WHEN THE TEST OF THE CONTROL UNIT IS COMPLETE THESE  * S99528
*    UNIT ADDRESSES MAY BE RESTORED TO THE SAME CONDITION THEY * S99528
*    WERE IN WHEN THEY WERE OBTAINED.                          * S99528
*                                                              * S99528
*    A SPECIFIC BIT IS CHECKED AT ENTRY TO ALLOW THIS MODULE   * S99528
*    TO BE ENTERED TO RESTORE THE CHANNEL UNITS TO THE STATE   * S99528
*    THEY WERE IN WHEN OBTAINED.                               * S99528
*                                                              * S99528
* MODULE TYPE: PROCEDURE                                       @Y17XAUU
*                                                              @Y17XAUU
* PROCESSOR: ASSEMBLE XF                                       @Y17XAUU
*                                                              @Y17XAUU
* MODULE SIZE: 2K MAXIMUM                                      @Y17XAUU
*                                                              @Y17XAUU
* ENTRY POINTS:                                                * S99528
*                                                              * S99528
*    IEDQWAJ - CALLED WHEN A $CUTEST MACRO IS ISSUED BY THE    * S99528
*    UNIT TEST.                                                * S99528
*                                                              * S99528
* INPUT:                                                       * S99528
*                                                              * S99528
*    REGISTER 01 - $CUTEST PARAMETER LIST ADDRESS              * S99528
*    REGISTER 02 - OLTCB POINTER                               * S99528
*    REGISTER 13 - SAVE AREA ADDRESS                           * S99528
*    REGISTER 14 - RETURN ADDRESS                              * S99528
*    REGISTER 15 - ENTRY POINT ADDRESS                         * S99528
*                                                              * S99528
* OUTPUT:                                                      * S99528
*                                                              * S99528
*    REGISTER 15 CONTAINS THE MACRO RETURN CODE                * S99528
*                                                              * S99528
*           X'00' SUCCESSFUL COMPLETION                        * S99528
*           X'08' ONE OR MORE DEVICES DO NOT MEET REQUIREMENTS * S99528
*           X'0C' PARAMETER ERROR                              * S99528
*                                                              * S99528
*                                                              * S99528
* EXTERNAL ROUTINES:                                           * S99528
*                                                              * S99528
*    NONE                                                      * S99528
*                                                              * S99528
* EXIT POINTS (NORMAL) :                                       * S99528
*                                                              * S99528
*    BR AFTER 'CU007A' TO RETURN TO CALLER                     * S22024
*    XCTL TO IEDQWM2 AFTER 'CU0007B'                           * S22024
*                                                              * S99528
* EXIT POINTS (ERROR) :                                        * S99528
*    NONE                                                      * S99528
*                                                              * S99528
* TABLES/WORK AREAS:                                           * S99528
*                                                              * S99528
*    OLTCB, UNIT TEST, GETMAIN AREA                            * S99528
*                                                              * S99528
* ATTRIBUTES:                                                  * S99528
*                                                              * S99528
*    ENABLED, PROBLEM PROGRAM MODE, RESIDENT,                  @Y17XAU2
*    RE-ENTRANT                                                @Y17XAU2
*                                                              * S99528
* NOTES: SEE BELOW                                             @Y17XAUU
*                                                              @Y17XAUU
*  REGISTERS CONVENTION: SEE REGISTER ASSIGNMENT               @Y17XAUU
*                                                              @Y17XAUU
*  PATCH LABEL: PATCH                                          @Y17XAUU
*                                                              @Y17XAUU
*  RESTRICTIONS: NONE                                          @Y17XAUU
*                                                              @Y17XAUU
*  DEPENDENCIES: EBCDIC CHARACTER CODE                         @Y17XAUU
*                                                              * S99528
**************************************************************** S99528
         EJECT
**************************************************************** S99528
*        EQUATES TO ALLOW SYMBOLIC NAMING OF REGISTERS         * S99528
**************************************************************** S99528
R0       EQU   0                   REGISTER 0                    S22024
R1       EQU   1                   $CUTEST PARAMETER LIST ADDR   S99528
R2       EQU   2                   OLTCB POINTER                 S99528
R3       EQU   3                   WORK REGISTER                 S99528
R4       EQU   4                   BASE REGISTER                 S99528
R5       EQU   5                   WORK REGISTER                 S99528
R6       EQU   6                   WORK REGISTER                 S99528
R7       EQU   7                   WORK REGISTER                 S99528
R8       EQU   8                   WORK REGISTER                 S99528
R9       EQU   9                   WORK REGISTER                 S99528
R10      EQU   10                  WORK REGISTER                 S99528
R11      EQU   11                  POINTER TO PROPER SAVE AREA   S99528
R12      EQU   12                  WORK REGISTER                 S99528
R13      EQU   13                  SAVE AREA ADDRESS             S99528
R14      EQU   14                  RETURN ADDRESS                S99528
R15      EQU   15                  ENTRY POINT ADDRESS           S99528
PARMREG0 EQU   0                   PARAMETER REGS FOR IEDQMSG    S99528
PARMREG1 EQU   1                   PARAMETER REGS FOR IEDQMSG    S99528
ADRMSK7  EQU   7                   ADDRESS MASK BYTE           @Y17XAUU
DISREG2  EQU   28                  DISPLACEMENT TO REG 2       @Y17XAUU
         EJECT                                                   S99528
         USING IEDQWAJ,R4                                        S99528
         USING TOTOLTCB,R2                                       S21903
         SAVE  (14,12)                                           S99528
         LR    R4,R15              LOAD BASE REGISTER            S99528
IEDQWAJ  IEDHJN CU000,HJN                                        S99528
         MVC   TBEG1(TEND1-TBEG1),QOTUCBAB                     @Y17XAUU
         MVC   TBEG2(TEND2-TBEG2),QUENQLST                     @Y17XAUU
         LA    R5,QNAME            ADDRESS OF MAJOR NAME       @Y17XAUU
         LA    R6,RNAME            ADDRESS OF MINOR NAME       @Y17XAUU
         STM   R5,R6,CUENQLST+FSTWORD SET ADDRESSES IN PLIST   @Y17XAUU
         LA    R5,TOTUCBAB         DATA ADDRESS                @Y17XAUU
         STCM  R5,ADRMSK7,TOT102+SNDBYTE PUT IN PLIST          @Y17XAUU
         LA    R5,TOTUCBAC         ADDRESS CONTAINING LENGTH   @Y17XAUU
         STCM  R5,ADRMSK7,TOT102A+FSTWORD PUT IN PLIST         @Y17XAUU
         LA    R12,TOTSAVE1        PICK UP SAVE AREA ADDRESS     S99528
         ST    R12,SNDWORD(R13)    SAVE IT IN CALLERS SAVE AREA  S99528
         ST    R13,TOTSAVE1+FSTWORD SAVE CALLERS SAVE REG        S99528
         LR    R13,R12             SET UP SAVE AREA REG          S99528
*                                                                S99528
         TM    TOTCUFLG,TOTCUCUP   ARE WE BEING ENTERED TO CLEAN S99528
*                                  UP AFTER PREVIOUS $CUTEST     S99528
         BO    CUP000              YES  PROCEED WITH CLEANUP     S99528
         SR    R15,R15             CLEAR RETURN CODE REGISTER    S99528
         CLI   SNDBYTE(R1),LEVEL2  LEVEL 2 MACRO ID?             S22024
         BE    CU001               YES                           S99528
*                                  NO ISSUE ERROR MESSAGE        S99528
         LA    R15,RETCODE4       SET RETURN CODE TO 4           S05331
         B     CU007              BRANCH TO EXIT ROUTINE         S05331
CU001    DS    0H                                                S99528
         LR    R6,R1               SAVE PARAMETER POINTER        S99528
         TM    TOTCUFLG,TOTCUDON   HAS A $CUTEST ALREADY         S99528
*                                  BEEN ISSUED ?                 S99528
         BO    CU002C              YES BRANCH AROUND GETMAIN     S99528
*                                  AND STORING THE GETMAIN ADDR  S99528
*                                      GETMAIN TO GET SAVE       S99528
         LA    R5,CUGETSAV         ADDRESS OF RETURNED SPACE   @Y17XAUU
         GETMAIN EC,LV=256,A=(R5),MF=(E,PLIST) GET CORE        @Y17XAUU
         LTR   R15,R15             WAS GETMAIN SATISFIED?        S99528
         BZ    CU002               YES CONTINUE                  S99528
*                                  NO SET BAD RETURN CODE & EXIT S99528
CU001A   DS    0H
         SR    R15,R15             CLEAR RETURN CODE REGISTER    S99528
         LA    R15,RETCODE8        LOAD RETURN CODE              S99528
CU007    DS    0H                                                S99528
         STC   R15,TOTRTCOD        PUT RET CODE IN OLTCB         S99528
         TM    $OLTFLGS,$TRACE     IS TRACE REQUESTED            S99528
         BO    CU007B              YES                           S99528
         TM    TOTRTCOD,HEXFF      IS RETURN CODE NON ZERO       S99528
         BZ    CU007A              NO DO NOT TEST RETURN CODE    S99528
*                                  OPTION                        S99528
         TM    $RETCODE,HEXFF      ANY BITS SET                  S99528
         BNZ   CU007B              YES BRANCH                    S99528
CU007A   DS    0H                                                S99528
*                                                                S99528
         L     R13,FSTWORD(R13)    GET CALLERS SAVE AREA ADDRESS S22024
         L     R14,TRDWORD(R13)    RESTORE R14                   S99528
         LM    R0,R12,FIFWORD(R13) RESTORE CALLERS REGISTERS     S99528
         BR    R14                 RETURN                        S99528
CU007B   DS    0H                                                S99528
         L     R13,FSTWORD(R13)    GET CALLERS SAVE ADDRESS      S99528
         L     R14,TRDWORD(R13)    GET HIS RETURN ADDRESS        S99528
         USING RESPL,R5                                        @Y17XAU2
         L     R5,TOTRESPL                                     @Y17XAU2
         L     R15,RESPLM2                                     @Y17XAU2
         LM    R2,R12,DISREG2(R13) RESTORE REGISTERS           @Y17XAUU
         BR    R15                                             @Y17XAU2
         DROP  R5                                              @Y17XAU2
         EJECT
*                                                                S99528
CU002    DS    0H                                                S99528
         L     R5,CUGETSAV         GET START OF AREA             S99528
         LA    R5,START(R5)        CLEAR HIGH ORDER BYTE         S99528
         ST    R5,TOTCUSAV         STORE ADDRESS IN OLTCB        S99528
         XC    START(LEN1,R5),START(R5)  ZERO OUT SAVE AREA      S99528
*                                                                S99528
CU002A   DS    0H                                                S99528
         L     R5,FSTWORD(R6)      GET SAVED POINTER TO EXTENT   S99528
         MVC   TOTCU#AD(LEN2),FSTWORD(R5) MOVE # OF CONTIGUOUS   S99528
*                                         ADDRESSES INTO OLTCB   S99528
         MVC   TOTCURS1(LEN2),SNDWORD(R6) MOVE 'CODE' PARAMETER  S99528
*                                         OF CUTEST INTO OLTCB   S99528
         L     R5,START(R5)        GET CU START ADDR IN REG 5    S99528
         STH   R5,TOTCUCUU         STORE STARTING CUU ADDR IN    S99528
*                                  OLTCB                         S99528
         MVC   TOTCURS2(LEN2),TOTCUCUU SAVE CHANNEL NUMBER       S99528
         XC    TOTCUCUU(LEN2),TOTCUCUU CLEAR CHANNEL NUMBER      S99528
*                                       FROM WORK AREA           S99528
         CLI   TOTCURS1,CUCODE0    IS CODE 0 REQUESTED           S99528
         BE    CU002B              YES CONTINUE PROCESSING       S99528
         CLI   TOTCURS1,CUCODE1    IS CODE 1 REQUESTED           S99528
         BE    CU002B              YES CONTINUE PROCESSING       S99528
         SR    R15,R15             CLEAR RETURN CODE REGISTER    S99528
         LA    R15,RETCODEC        INVALID PARAMETER LIST        S99528
         B     CU007               RETURN                        S99528
*                                                                S99528
CU002C   DS    0H                                                S99528
         L     R5,FSTWORD(R6)      GET SAVED POINTER TO EXTENT   S99528
         CLC   TRDBYTE(LEN2,R5),TOTCURS2 SAME CHANNEL NUMBER     S99528
*                                        PREVIOUS $CUTESTS?      S99528
         BE    CU002A              YES CONTINUE                  S99528
         SR    R15,R15             NO SET UP TO RETURN           S99528
         LA    R15,RETCODEC        LOAD BAD RETURN CODE          S99528
         B     CU007               RETURN                        S99528
*                                                                S99528
CU002B   DS    0H                                                S99528
         OI    TOTCUFLG,TOTCUDON   TURN ON $CUTEST ALREADY       S99528
*                                  DONE FLAG IN OLTCB            S99528
         SR    R12,R12             CLEAR REGISTER                S99528
         IC    R12,TOTCU#AD        PUT # OF CONTIGUOUS ADDRESSES S99528
*                                  IN REGISTER 12                S99528
         BCT   R12,CU003           DECREMENT REG 12 TO GET       S99528
CU003    DS    0H                  PROPER AMOUNT FOR PROCESSING  S99528
*                                                                S99528
         SR    R11,R11             CLEAR REGISTER                S99528
         LH    R11,TOTCUCUU        PUT STARTING CUU IN R11       S99528
         L     R5,TOTCUSAV         GET ADDRESS OF SAVE AREA      S99528
         AR    R11,R5              CALCULATE OFFSET INTO SAVAREA S99528
*                                                                S99528
         AR    R11,R12             GET LAST OF CONTIG. ADDRESSES S99528
         LA    R12,CURESTOR(R12)   INCREMENT R12 AFTER           S99528
*                                  CALCULATIONS                  S99528
*                                                                S99528
CU006    DS    0H                                                S99528
         BCT   R12,CU006A          DECREMENT R12 AGAIN           S99528
CU006A   DS    0H                                                S99528
         SR    R10,R10             CLEAR R10                     S99528
         IC    R10,CUCUUSED        SET UP TO TEST IF USED        S99528
         EX    R10,CUEXTM          TEST BIT                      S99528
         BO    CU004               ALREADY USED IN A $CUTEST     S99528
*                                  BYPASS THIS ADDRESS           S99528
*                                                                S99528
         B     CU009               FIND OUT IF OFFLINE           S99528
         EJECT                                                   S99528
**************************************************************** S99528
*                                                              * S99528
*        ROUTINE TO FIND IF UCB IS OFFLINE                     * S99528
*                                                              * S99528
**************************************************************** S99528
CU009    DS    0H                                                S99528
         L     R8,FORWORD(R0)      PICK UP CVT ADDR              S99528
         USING CVT,R8                                            S99528
         L     R9,AVTCVTPT(R8)     PICK UP DISPATCHER ADDRESS    S99528
         L     R7,CVTILK2          PICK UP 2 BYTE UCB TABLE      S99528
         ST    R7,CUUCBSAV         SAVE UCB POINTER              S99528
         DROP  R8                                                S99528
*                                                                S99528
         L     R8,CUUCBSAV         PICK UP ADDR OF UCB POINTERS  S99528
         USING UCB,R7                                            S99528
CUUCB01  LH    R7,START(R8)        LOAD UCB ADDR                 S99528
         LA    R8,LEN3(R8)         POINT TO NEXT ENTRY (+2)      S99528
         LTR   R7,R7               IS R7 EQUAL TO ZERO           S99528
         BZ    CUUCB01             PADDING- IGNORE IT            S99528
         BP    GOODUCB             POSITIVE THEN GOOD UCB      @G36XRUV
         N     R7,CLEARHI          CLEAR HIGH ORDER BYTES      @G36XRUV
         CLM   R7,THREE,TABLEEND   Q-IS THIS END OF TABLE      @G36XRUV
         BE    CU010               Y-BRANCH                    @G36XRUV
GOODUCB  DS    0H                                              @G36XRUV
         ST    R7,CUUCBADR         SAVE UCB ADDRESS              S99528
         SR    R10,R10             CLEAR WORK REGISTER           S99528
         LH    R10,TOTCUCUU        LOAD FIRST CUU INTO REGISTER  S99528
*                                  CUU = CHAN ADDR IN HEX        S99528
         AR    R10,R12             ADD TO FIND CUU WE ARE USING  S99528
         STH   R10,CUCUU           SAVE THIS UCB NAME (HEX)      S99528
         MVC   CUCUU(LEN2),TOTCURS2 MOVE CHANNEL NUMBER INTO     S99528
*                                   COMPARE FIELD                S99528
         CLC   CUCUU(LEN3),UCBCHA     IS THERE A MATCH ?         S99528
         BNE   CUUCB01                NO: GO LOOK AGAIN          S99528
         CLI   UCBTBYT3,CUCOMMUN   IS UCB DEVICE TYPE COMMUNICAT S99528
         BE    CUUCB02             YES OK TO CONTINUE            S99528
         CLI   UCBTBYT3,CUGRAPH    IS UCB DEVICE TYPE GRAPHICS   S99528
         BE    CUUCB02             YES OK TO CONTINUE            S99528
         CLI   UCBTBYT3,CUDUMMY    IS UCBTYPE DUMMY              S21903
         BE    CUUCB02             YES  OK TO CONTINUE           S21903
         SR    R15,R15             SET UP TO RETURN              S99528
         LA    R15,RETCODEC        LOAD BAD RETURN CODE          S99528
         B     CU007               RETURN                        S99528
*                                                                S99528
CUUCB02  DS    0H                                                S99528
*                                                              * S99528
*        WE HAVE FOUND THE UCB - NOW IS IT OFFLINE ?           * S99528
*                                                              * S99528
         TM    SRTESTAT,SRTEALOC+SRTESYSR+SRTEDADI IS THIS UCB   S99528
*                                                  ALLOCATED OR  S99528
*                                                  PRIMARY OR    S99528
*                                                  SECONDARY     S99528
*                                                  CONSOLE       S99528
         BZ    CU010                               NO - NONE     S99528
*                                                  OF THESE      S99528
         MVC   CUUCBNAM(LEN4),UCBNAME  SAVE EBCDIC UCB ADDRESS   S99528
         B     CU011               YES FIND IF ALLOCATED TO TCAM S99528
CU010    DS    0H                                                S99528
         TM    TOTCUFLG,TOTCUCUP   ARE WE CLEANING UP?           S99528
         BO    CU010A              YES                           S99528
         TM    UCBOB+TUCBNALO,TUCBNALC IS UNALLOCATE BIT ON?     S21903
         BO    CU001A                  GO SET BAD RETURN CODE    S21903
         SR    R10,R10             CLEAR REG 10                  S99528
         IC    R10,CUCUUSED        SET UP TO MARK IT USED        S99528
*                                  IN A $CUTEST MACRO            S99528
         EX    R10,CUEXOI          MARK IT USED IN $CUTEST       S99528
         IC    R10,CUCUOFFL        SET UP TO MARK OFF LINE       S99528
         EX    R10,CUEXOI          MARK IT OFF LINE              S99528
         LTR   R7,R7               DID WE FIND UCB OR END OF LISTS99528
         BC    FSTWORD,CU004       END OF LIST DONT SET BIT      S99528
         DROP  R7                      DROP UCB BASE             S21903
*                                                              * S99528
*        TURN ON UCBNALOC BIT IN THE UCB                       * S99528
*                                                              * S99528
         MVC   TOTUCBAB(LEN6),START(R7) MOVE UCB TO WORK AREA    S99528
         OI    TOTUCBAB+TUCBNALO,TUCBNALC TURN ON UCBNALOC BIT   S99528
         STH   R7,TOT102A+TUCBONE  STORE UCB ADDRESS IN TARGET   S99528
*                                  ADDRESS OF SVC 102            S99528
         LA    R1,TOT102           LOAD R1 TO POINT TO DATA      S99528
*                                  MOVE PARAMETER LIST           S99528
         AQCTL                                                   S99528
         B     CU004               CONTINUE $CUTEST              S99528
*                                                                S99528
*                                                              * S99528
*       TURN OFF UCBNALOC BIT IN THE UCB                       * S99528
*                                                              * S99528
CU010A   DS    0H                                                S99528
         LTR   R7,R7               DID WE FIND UCB OR END OF LISTS99528
         BC    FSTWORD,CUP008      END OF LIST DONT SET BIT      S99528
         MVC   TOTUCBAB(LEN6),START(R7) MOVE UCB TO WORK AREA    S99528
         NI    TOTUCBAB+TUCBNALO,TUCBNALF TURN OFF UCBNALOC BIT  S99528
         STH   R7,TOT102A+TUCBONE  STORE UCB ADDRESS IN TARGET   S99528
*                                  ADDRESS OF SVC 102            S99528
         LA    R1,TOT102           LOAD R1 TO POINT TO DATA      S99528
*                                  MOVE PARAMETER LIST           S99528
         AQCTL                                                   S99528
         B     CUP008              CONTINUE TO CLEANUP           S99528
         EJECT
**************************************************************** S99528
*                                                              * S99528
*        FOLLOWING CODE FINDS OUT IF UCB IS TCAM'S. UCB ADDR   * S99528
*        SHOULD BE IN 'CUUCBADR'.                              * S99528
*                                                                S99528
**************************************************************** S99528
CU011    DS    0H                  FIND DD ENTRY                 S99528
         LA    R9,CVTPTR           CVT POINTER ADDRESS           S99528
         L     R9,START(R9)        CVT ADDRESS                   S99528
         L     R9,START(R9)        TCBPOINTERS ADDRESS           S99528
         L     R9,FSTWORD(R9)      CURRENT TCB ADDRESS           S99528
         L     R9,TRDWORD(R9)      TIOT ADDRESS                  S99528
*                                                                S99528
         USING TIOT,R9                                           S99528
         LA    R9,TIOENTRY         POINT TO FIRST ENTRY          S99528
         DROP  R9                                                S99528
*                                                                S99528
         USING TIOENTRY,R9                                       S99528
CU008    SR    R8,R8               CLEAR REG                     S99528
         IC    R8,TIOELNGH         DD ENTRY LENGTH               S99528
         LTR   R8,R8               ANY LENGTH                    S99528
         BZ    CU001A              DOES NOT BELONG TO TCAM       S99528
*                                  SET BAD RETURN DOCE - RETURN  S22024
         LA    R5,POOLSTAR-TIOESTTB  GET LGTH OF DEVICE ENTRY    S99528
         DROP  R9                                                S99528
         LR    R6,R8               SAVE LENGTH                   S99528
         SR    R8,R5               DD LENGTH MINUS FOUR          S99528
         AR    R8,R9               DEVICE                        S99528
         USING TIOESTTB,R8                                       S99528
         CLC   TIOEFSRT+SNDBYTE(LEN3),CUUCBADR+TRDBYTE  COMPARE  S99528
*                                                       UCB ADDR S99528
         DROP  R8                                                S99528
         BE    CU012               UCB FOUND IT IS TCAM'S        S99528
*                                                                S99528
         AR    R9,R6               BUMP POINTER TO NEXT DD ENTRY S99528
         B     CU008               LOOP TO SEE IF NEXT IS TCAM'S S99528
*                                                                S99528
*                                                                S99528
CU012    DS    0H                                                S99528
         MVC   RNAME(LEN4),CUUCBNAM MOVE IN EBCDIC NAME          S99528
         TM    TOTCUFLG,TOTCUCUP   ARE WE CLEANING UP            S99528
         BO    CU012B              YES                           S99528
*                                    BEFORE MARKING IT ALLOCATED S99528
*                                    TO TCAM ENQ IT TO INSURE    S99528
*                                    SOME OTHER OLT DOESN'T      S99528
*                                    HAVE IT                     S99528
         ENQ   ,MF=(E,CUENQLST)      ENQ ON CHANNEL ARRDESS      S99528
         LTR   R15,R15               IS THE DEVICE AVAILABLE?    S99528
         BZ    CU012A                YES - CONTINUE              S99528
         CLI   FORBYTE(R15),RETCODE8 HAVE WE ALREADY REQUESTED   S99528
*                                    IT                          S99528
         BE    CU004               YES CONTINUE WITH $CUTEST BUT S99528
*                                  DONT SAVE STATUS SINCE        S99528
*                                  IEDQWC HAS ALREADY SAVED IT   S99528
*                                                                S99528
         B     CU001A                MARK BAD RETURN CODE AND    S99528
*                                    RETURN                      S99528
CU012B   DS    0H                                                S99528
         DEQ   ,MF=(E,CUENQLST)    DEQUEUE RESOURCE ENQ'ED       S99528
*                                                                S99528
         SR    R10,R10             CLEAR WORK REGISTER           S99528
         IC    R10,CUCUSTRT        TEST IF LINE STARTED WHEN GOT S99528
         EX    R10,CUEXTM          TEST LINE STARTED BIT         S99528
         BO    CU012C              LINE WAS STARTED GO START     S99528
         B     CUP008              NO CONTINUE RESTORING         S99528
*                                                                S99528
CU012C   DS    0H                                                S99528
         BAL   R3,STARTLIN         GO TO START LINE ROUTINE      S99528
*                                                                S99528
         B     CUP008              ALL DONE THIS CUU GET NEXT    S99528
*                                                                S99528
         EJECT                                                   S99528
*                                                                S99528
CU012A   DS    0H                                                S99528
         SR    R10,R10             CLEAR REGISTER                S99528
         IC    R10,CUCUUSED        SET UP TO MARK IT USED        S99528
*                                  IN A $CUTEST MACRO            S99528
         EX    R10,CUEXOI          MARK IT USED IN $CUTEST       S99528
         IC    R10,CUCUTCAM        GET ALLOCATED BIT             S99528
         EX    R10,CUEXOI          MARK IT ALLOCATED TO TCAM     S99528
         BAL   R3,STOPLINE         GO ISSUE STOP LINE            S99528
*                                                                S99528
*                                                                S99528
         CLI   TOTORTCD,RETCODE0   WAS REQUEST SUCCESSFUL?       S22024
         BE    CUSTLN08            YES SET PROPER BITS           S99528
*                                                                S99528
         CLI   TOTORTCD,RETCODE4   DID REQUEST FAIL?             S22024
         BE    CUSTLN02            YES PRINT AND EXIT            S99528
*                                                                S99528
         CLI   TOTORTCD,RETCODE8   WAS REQUEST ALREADY DONE?     S22024
         BE    CUSTLN09            YES SET PROPER BITS           S99528
         B     CUSTLN02                ACT AS IF OP-CONTROL      S21903
*                                       REQUEST FAILED           S21903
*                                                                S99528
*                                                                S99528
CUSTLN02 DS    0H                                                S99528
         B     CU001A              RETURN WITH RETCODE OF 08     S99528
*                                                                S99528
CUSTLN08 DS    0H                                                S99528
         IC    R10,CUCUSTRT        SET UP TO MARK LINE STARTED   S99528
         EX    R10,CUEXOI          MARK LINE ALREADY SYARTED     S99528
CUSTLN09 DS    0H                                                S99528
CU004    DS    0H                                                S99528
         BCT   R11,CU005           DECREMENT TO NEXT ADDRESS     S99528
CU005    DS    0H                                                S99528
         LA    R12,CURESTOR(R12)   INCREMENT R12 TO              S99528
*                                  ADJUST FOR CALCULATIONS       S99528
         BCT   R12,CU006           CONTINUE STUDYING STATUS      S99528
*                                                                S99528
         SR    R15,R15             SET GOOD RETURN CODE AND      S99528
         B     CU007               RETURN                        S99528
         EJECT
**************************************************************** S99528
*                                                              * S99528
*        START LINE  /  STOP LINE  ROUTINE                     * S99528
*                                                              * S99528
**************************************************************** S99528
STARTLIN EQU   *                   START LINE ENTRY              S99528
         MVI   TOTOVBCD,LINESTRT   MOVE IN START LINE VERB CODE  S22024
         B     CUSTLN00            CONTINUE PROCESSING           S99528
STOPLINE EQU   *                   STOP LINE ENTRY               S99528
         MVI   TOTOVBCD,LINESTOP   MOVE IN STOP LINE VERB CODE   S22024
*                                                                S99528
CUSTLN00 STM   R2,R12,CUREGSAV     SAVE REGS ON ENTRY TO         S99528
*                                  START / STOP LINE ROUTINE     S99528
         MVI   TOTOLADR,BLANK      MOVE BLANK INTO NAME FIELD    S22024
         MVC   TOTOLADR+SNDBYTE(D7),TOTOLADR  BLANK OUT FIELD    S22024
         MVC   TOTOLADR(LEN4),CUUCBNAM  MOVE EBCDIC NAME INTO    S22024
*                                     OP CONTROLS PARAMETER LIST S99528
         L     R6,FORWORD(R0)      LOAD ADDRESS OF CUT INTO R1   S99528
         L     R6,AVTCVTPT(R6)     GET TCAM DISPATCHER ADDRESS   S99528
         L     R6,START(R6)        GET AVT ADDRESS               S99528
         USING IEDQAVTD,R6                                       S99528
         LA    R10,AVTOPCOB        GET POINTER TO OP CNTRL QCB   S99528
         ST    R10,TOTOKEY         PUT IN PARAMETER LIST         S22024
         MVI   TOTOKEY,OPCKEY      SET KEY TO ZERO               S22024
         L     R6,AVTTCB           GET TCAM TCB ADDRESS          S99528
         DROP  R6                                                S99528
         L     R6,SNDWORD(R6)      GET DEB CHAIN ADDRESS         S99528
CUSTLN01 DS    0H                                                S99528
         LA    R6,START(R6)        CLEAR HIGH ORDER BYTE         S99528
         LTR   R6,R6               IS THERE ANOTHER DEB ?        S99528
         BZ    CUSTLN11            NO INDICATE CANNOT STOP LINE  S99528
         SR    R10,R10             CLEAR LOOP COUNTER            S99528
*                                                                S99528
         IC    R10,FORWORD(R6)     INITLZ LOOP COUNTER WITN      S99528
*                                  NUMBER OF EXTENTS             S99528
         LA    R5,ETHWORD(R6)      GET UCB CHAIN ADDRESS         S99528
         L     R11,FSTWORD(R6)     GET NEXT DEB ADDRESS          S99528
         L     R9,SIXWORD(R6)      GET DCB ADDRESS               S99528
         USING IHADCB,R9                                         S99528
         TM    DCBDSORG+SNDBYTE,TCAMDEB IS THIS A TCAM DEB?      S99528
         BZ    CUSTLN03                 NO: GET NEXT DEB         S99528
         LA    R7,CURLNINT         INITIALIZE REL. LINE NUMBER   S99528
*                                                                S99528
CUSTLN04 DS    0H                                                S99528
         L     R3,START(R5)        GET UCB ADDRESS               S99528
         LA    R3,START(R3)        FOR COMPARE INSTRUCTION       S99528
         L     R8,CUUCBADR         GET ADDRESS SAVED IN UCB      S99528
*                                  SEARCH ROUTINE                S99528
         LA    R8,START(R8)        CLEAR HIGH ORDER BYTE         S99528
         CR    R3,R8               CORRECT UCB FOUND             S99528
         BE    CUSTLN06                YES, BRANCH               S21903
         LA    R5,FSTWORD(R5)      NO  BUMP TO NEXT UCB ADDRESS  S99528
         LA    R7,LEN2(R7)         BUMP RELATIVE LINE NUMBER    SA64769
         BCT   R10,CUSTLN04        LOOP TILL END OF UCB CHAIN    S99528
*                                                                S99528
*                                                                S99528
*                                                                S99528
CUSTLN03 DS    0H                                                S99528
         LR    R6,R11              POINT R6 TO NEXT DEB          S99528
         B     CUSTLN01            GET NEXT UCB CHAIN            S99528
*                                                                S99528
*                                                                S99528
CUSTLN06 DS    0H                                                S99528
         SR    R10,R10             CLEAR REG 10                  S99528
         IC    R10,DCBEXLST        GET LCB LENGTH                S99528
         LR    R11,R7              GET RELATIVE LINE NUMBER      S99528
         MR    R10,R10             MULTIPLY RLN BY LCB LENGTH    S99528
         L     R10,DCBIOBAD        GET DUMMY IOB ADDRESS         S99528
         LA    R10,START(R10,R11)  GET CORRECT IOB ADDRESS       S99528
         LA    R11,LCBFLAG1-IEDQLCB  GET OFFSET TO LCB START     S99528
         SR    R10,R11             SUBTRACT FROM IOB POINTER     S99528
         USING IEDQLCB,R10                                       S99528
         CLI   TOTOVBCD,LINESTRT   IS START LINE REQUESTED?      S22024
         BNE   CUSTLN07            NO                            S99528
         CLI   LCBCPA,TOTELINE     IS LINE ASSIGNED TO TOTE?     S99528
         BNE   CUSTLN10            NO  DO NOT START LINE         S99528
         MVI   LCBCPA,NOTTOTE      CLEAR LINE ASSIGNED TO TOTE   S99528
*                                                                S99528
         DROP  R9                                                S99528
         DROP  R10                                               S99528
*                                                                S99528
CUSTLN07 DS    0H                                                S99528
         STC   R7,TOTORLN          STORE RELATIVE LINE NUMBER IN S22024
*                                  REQUEST ELEMENT               S99528
         LA    R1,TOTOTECB         GET TOTE ECB                  S99528
         ST    R1,TOTOECBA         PUT IN PARAMETER LIST         S22024
         MVI   TOTOPRI,OPCPRI      SET OPERATOR CONTROL PRIORITY S22024
         MVI   TOTORTCD,OPCRTCD    CLEAR RETURN CODE FIELD       S22024
         MVI   TOTOELNG,OPCELNG    MOVE ELEMENT LENGTH INTO LIST S22024
         XC    TOTOLINK(LEN4),TOTOLINK  CLEAR REQUEST ELEMENT    S22024
*                                         LINK FIELD             S22024
         LA    R1,TOTPLIST         GET ADDRESS OF OP CONTROL     S22024
*                                    PARAMETER LIST              S22024
         ST    R1,ELEM1            PUT PARM LIST ADDR IN 1ST WORDS22024
*                                    OF POST LIST                S22024
         ST    R1,ELEM2            PUT ADDR OF PARM LIST IN 2ND  S22024
*                                    WORD OF POST LIST           S22024
         MVI   ELEM1,START102      SVC 102 START FLAG            S22024
         MVI   ELEM2,LAST102       LAST WORD FLAG BYTE           S22024
         LA    R1,SLPL             POINT R1 TO SVC 102 POST LIST S22024
         XC    TOTOTECB(LEN5),TOTOTECB CLEAR TOTE ECB            S99528
         AQCTL                     POST ELEMENT TO OPERATOR CNTL S99528
*                                                                S99528
         WAIT  ECB=TOTOTECB                                      S99528
*                                                                S99528
CUSTLN10 DS    0H                                                S99528
         LM    R2,R12,CUREGSAV     RESTORE REGS ON EXIT FROM     S99528
*                                  START / STOP LINE ROUTINE     S99528
         BR    R3                  RETURN TO CALLER              S99528
CUSTLN11 DS    0H                                                S99528
         SR    R15,R15                 CLEAR REGISTER            S99528
         LA    R15,RETCODE4            LOAD RETURN CODE          S99528
         B     CUSTLN10                RETURN                    S99528
*                                                                S99528
         EJECT                                                   S99528
**************************************************************** S99528
*                                                              * S99528
*        WHEN CLEANING UP AFTER AN OLT TOTE WILL ENTER HERE    * S99528
*        TO RESTORE $CUTEST'ED DEVICES TO THE CONDITION THEY   * S99528
*        WERE IN WHEN THEY WERE OBTAINED.                      * S99528
*                                                              * S99528
**************************************************************** S99528
*                                                                S99528
CUP000   DS    0H                                                S99528
         NI    TOTCUFLG,HEXFF-TOTCUDON  TURN OFF CUTEST HAS BEEN S99528
*                                       DONE INDICATOR           S99528
         L     R5,TOTCUSAV         GET SAVED GETMAIN ADDRESS     S99528
         SR    R12,R12             CLEAR REGISTER                S99528
         LA    R12,LEN7            INSERT # OF CLEANUP ADDRS     S99528
         LR    R11,R5              PUT START ADDR IN WORK REG    S99528
         AR    R11,R12             ADD OFFSET TO END OF SAVE AREAS99528
*                                                                S99528
CUP001   DS    0H                                                S99528
         SR    R10,R10             CLEAR REGISTER                S99528
         IC    R10,CUCUUSED        SET UP TO TEST IF USED        S99528
         EX    R10,CUEXTM          TEST BIT                      S99528
         BZ    CUP008              NOT USED BYPASS THIS ADDR     S99528
*                                                                S99528
         XC    TOTCU#AD(LEN2),TOTCU#AD CLEAR # OF ADDRS FOR      S99528
*                                      CLEAN UP CALCULATIONS     S99528
         XC    TOTCUCUU(LEN3),TOTCUCUU CLEAR STARTING ADR OFFSET S99528
*                                      FOR CLEANUP CALCULATIONS  S99528
         B     CU009               GO CALCULATE UCB ADDRESS      S99528
*                                  AND CONTINUE RESTORING THIS   S99528
*                                  ADDRESS                       S99528
*                                                                S99528
CUP008   DS    0H                                                S99528
         BCT   R11,CUP009          DECREMENT TO NEXT BYTE SAVED  S99528
CUP009   DS    0H                                                S99528
         BCT   R12,CUP001          CONTINUE RESTORING            S99528
         L     R5,TOTCUSAV         PUT ADDR OF SAVE AREA IN REG  S99528
         ST    R5,CUGETSAV         STORE IN MACRO WORD           S99528
         XC    TOTCUFLG(LEN9),TOTCUFLG  CLEAR CUTEST DATA FROM   S99528
*                                       OLTCB                    S99528
*                                                                S99528
         LA    R5,CUGETSAV         ADDRESS CONTAINING GETMAIN  @Y17XAUU
*                                  AREA ADDRESS                @Y17XAUU
         FREEMAIN E,LV=256,A=(R5),MF=(E,PLIST) FREE SAVE AREA  @Y17XAUU
*                                                                S99528
         SR    R15,R15             CLEAR RETURN CODE REGISTER    S99528
         B     CU007A              ALL DONE RETURN               S99528
*                                                                S99528
         EJECT
**************************************************************** S99528
*                                                              * S99528
*        CONSTANTS USED AS MASKS IN EXAMINING STATUS FIELD     * S99528
*                                                              * S99528
**************************************************************** S99528
*                                                                S99528
CUCUUSED DC    XL1'80'      IF ON: SUB-CHANNEL USED IN CUTEST    S99528
CUCUOFFL DC    XL1'40'      IF ON: OFFLINE TO OS                 S99528
CUCUTCAM DC    XL1'20'      IF ON: LINE ALLOCATED TO TCAM        S99528
CUCUSTRT DC    XL1'10'      IF ON: LINE WAS STARTED              S99528
CUCUNHLD DC    XL1'08'      IF ON: QUEUE WAS NOT HELD            S99528
CUCUPOLL DC    XL1'04'      IF ON: SPECIFIC POLL WAS ACTIVE      S99528
         DC    XL1'02'      RESERVED                             S99528
         DC    XL1'01'      RESERVED                             S99528
         DS    0F                                              @Y17XAUU
*                                                                S99528
QOTUCBAB DS    24C                 UCB COPY AREA COMMON SECTION@Y17XAUU
QOTUCBAC DC    H'24'               HALFWORD LENGTH FOR SUC 102 @Y17XAU2
QUCUU    DS    1H                  SAVE AREA FOR UCB NAME (HEX)@Y17XAUU
QUUCBADR DC    F'0'                ADDRESS OF UCB              @Y17XAUU
QUUCBNAM DS    3C                  NAME OF UCB IN EBCDIC       @Y17XAUU
QUUCBWRK DS    F                   UCB LOOK UP WORK AREA       @Y17XAUU
QUUCBSAV DS    1F                  POINTER TO UCB AREA         @Y17XAUU
QUREGSAV DS    11F                 REG SAVE AREA DURING START/ @Y17XAUU
*                                  STOP LINE CALCULATIONS      @Y17XAU2
QUGETSAV DS    1F                  ADDRESS OF GET MAIN AREA    @Y17XAUU
PATCH    DC    100C' '             PATCH AREA                   SA64769
CLEARHI  DC    X'0000FFFF'         USED TO CLEAR HI-ORDER BYTES@G36XRUV
TABLEEND DC    X'FFFF'             UCB TABLE END INDICATOR     @G36XRUV
         EJECT
**************************************************************** S99528
*                                                              * S99528
*        THESE INSTRUCTIONS ARE USED BY EXECUTE COMMANDS       * S99528
*                                                              * S99528
**************************************************************** S99528
*                                                                S99528
CUEXTM   TM    START(R11),X'00'    TEST PROPER BIT               S99528
*                                                                S99528
CUEXOI   OI    START(R11),X'00'    SET PROPER BIT                S99528
*                                                                S99528
CUEXNI   NI    START(R11),X'00'    CLEAR PROPER BIT              S99528
*                                                                S99528
         EJECT
*************** START OF FIXED AREA ***************************@Y17XAUU
*                                                              * S99528
*        MACRO  LISTS  FOR  MULTIPLE  USE                      * S99528
*                                                              * S99528
**************************************************************** S99528
QUENQLST ENQ (QQNAME,QRNAME,E,8,STEP),RET=USE,MF=L             @Y17XAUU
*                                  ESTABLISH FULLWORD BNDRY    @Y17XAU2
QQNAME   DC    CL8'SYSZTOTE'       NAME FOR ENQ MACRO          @Y17XAUU
QRNAME   DC    8CL1' '             RESOURCE TO BE ENQ'ED       @Y17XAU2
         SPACE 2
**************************************************************** S99528
*                                                              * S99528
*        PARAMETER LIST AREA FOR SVC 102 DATA MOVE             * S99528
*                                                              * S99528
**************************************************************** S99528
*                                                                S99528
QOT102   DS    0F                  ALIGN ON FULL WORD            S99528
         DC    X'08'               FLAG FOR DATA MOVE            S99528
         DC    AL3(0)              DATA ADDRESS                @Y17XAUU
         DC    X'00'               2ND WORD FLAGS                S99528
QOT102A  DC    AL3(0)              TARGET ADDRESS (DYNAMIC)      S99528
         DC    X'80'               LAST WORD FLAG                S99528
         DC    AL3(0)              ADDRESS CONTAINING LENGTH   @Y17XAUU
**************** END OF FIXED AREA *************************** @Y17XAUU
         EJECT
**************************************************************** S99528
*                                                              * S99528
*        EQUATED OFFSETS AND/OR LENGTHS                        * S99528
*                                                              * S99528
**************************************************************** S99528
START    EQU   0                   OFFSET FOR START OF FIELD     S99528
FSTBYTE  EQU   0                   OFFSET TO 1 ST BYTE OF FIELD  S99528
SNDBYTE  EQU   1                   OFFSET TO 2 ND BYTE OF FIELD  S99528
TRDBYTE  EQU   2                   OFFSET TO 3 RD BYTE OF FIELD  S99528
FORBYTE  EQU   3                   OFFSET TO 4 TH BYTE OF FIELD  S99528
ZROWORD  EQU   0                   OFFSET TO 1 ST WORD OF FIELD  S99528
FSTWORD  EQU   4                   OFFSET TO 2 ND WORD OF FIELD  S99528
SNDWORD  EQU   8                   OFFSET TO 3 RD WORD OF FIELD  S99528
TRDWORD  EQU   12                  OFFSET TO 4 TH WORD OF FIELD  S99528
FORWORD  EQU   16                  OFFSET TO 5 TH WORD OF FIELD  S99528
FIFWORD  EQU   20                  OFFSET TO 6 TH WORD OF FIELD  S99528
SIXWORD  EQU   24                  OFFSET TO 7 TH WORD OF FIELD  S99528
SVNWORD  EQU   28                  OFFSET TO 8 TH WORD OF FIELD  S99528
ETHWORD  EQU   32                  OFFSET TO 9 TH WORD OF FIELD  S99528
*                                                                S99528
TCAMDEB  EQU   X'40'               INDICATES DEB IS TCAMS        S99528
CUCOMMUN EQU   X'40'               UCB DEVICE CLASS = COMMUNICAT S99528
CUGRAPH  EQU   X'10'               UCB DEVICE CLASS = GRAPHICS   S99528
CUDUMMY  EQU   X'00'               UCB DEVICE CLASS = DUMMY      S21903
*                                                                S99528
LEN1     EQU   256                 LENGTH OF SAVE AREA           S99528
LEN2     EQU   1                   LENGTH OF # OF CONTIG ADDRS   S99528
LEN3     EQU   2                   LENGTH FOR COMPARE            S99528
LEN4     EQU   3                   LENGTH FOR MOVE               S99528
LEN5     EQU   4                   LENGTH FOR CLEAR              S99528
LEN6     EQU   24                  LENGTH FOR UCB MOVE           S99528
LEN7     EQU   X'FF'               # OF ADDRS TO CLEANUP         S99528
LEN8     EQU   8                   LENGTH TO CLEAR OLTCB OF      S99528
*                                  $CUTEST INFORMATION           S99528
LEN9     EQU   12                  LENGTH OF CUTEST CONTROL AREA S99528
*                                  IN OLTCB                      S99528
*                                                                S99528
RETCODE0 EQU   0                   RETURN CODE OF 0              S99528
RETCODE4 EQU   4                   RETURN CODE OF 4              S99528
RETCODE8 EQU   8                   RETURN CODE OF 8              S99528
RETCODEC EQU   12                  RETURN CODE OF X'0C'          S99528
*                                                                S99528
CURLNINT EQU   1                   INITIAL RELATIVE LINE NO      S99528
TOTELINE EQU   X'F1'               LINE IS ASSIGNED TO TOTE      S99528
NOTTOTE  EQU   X'00'               CLEAR LINE ASSIGNED TO TOTE   S99528
*                                                                S99528
CUCODE0  EQU   0            CODE 0 IN $CUTEST PARAMETER LIST     S99528
CUCODE1  EQU   1            CODE 1 IN $CUTEST PARAMETER LIST     S99528
CUCODE2  EQU   2            CODE 2 IN $CUTEST PARAMETER LIST     S99528
*                                                                S99528
CURESTOR EQU   1                   RESTORE VALUE USED IN OFFSET  S99528
*                                  CALCULATIONS                  S99528
*                                                                S99528
TUCBNALC EQU   X'04'               OR FIELD TO TURN ON UCBNALOC  S99528
TUCBNALF EQU   X'FB'               AND FIELD TO TURN OFF         S99528
*                                  UCBNALOC BIT IN UCB           S99528
TUCBNALO EQU   1                   OFFSET TO UCBNALOC FLAG       S99528
TUCBONE  EQU   1                   CONSTANT USED TO BUMP REGS    S99528
HEXFF    EQU   X'FF'               CONSTANT FOR CALCULATIONS     S99528
LEVEL2   EQU   X'02'               LEVEL 2 MACRO ID              S22024
OPCKEY   EQU   X'00'               OP CONTROL PARM LIST KEY      S22024
OPCPRI   EQU   X'DC'               OP CONTROL PRIORITY           S22024
OPCRTCD  EQU   X'00'               CLEAR OP CONTROL RET CODE FLD S22024
OPCELNG  EQU   X'18'               LENGTH OF OP CONTROL ELEMENT  S22024
LINESTRT EQU   X'40'               VERB CODE FOR START LINE      S22024
LINESTOP EQU   X'41'               VERB CODE FOR STOP LINE       S22024
TERMSTRT EQU   X'45'               VERB CODE FOR START TERMINAL  S22024
TERMSTOP EQU   X'47'               VERB CODE FOR STOP TERMINAL   S22024
START102 EQU   X'0C'               SVC 102 START FLAG            S22024
LAST102  EQU   X'80'               SVC 102 LAST WORD FLAG BYTE   S22024
D7       EQU   7                   CONSTANT DISPLACEMENT         S22024
BLANK    EQU   X'40'               BLANK CHARACTER               S22024
THREE    EQU   3                   CONSTANT THREE              @G36XRUV
         EJECT
UCB      DSECT                                                   S99528
         IEFUCBOB                                                S99528
         EJECT
*                                                                S99528
CVT      DSECT                                                   S99528
         CVT                                                     S99528
         EJECT
*                                                                S99528
         TAVTD                                                   S99528
         EJECT
*                                                                S99528
DCBDSECT DCBD  DSORG=TX                                          S99528
         EJECT
*                                                                S99528
         TLCBD                                                   S99528
         EJECT
*                                                                S99528
TIOT     DSECT                                                   S99528
         IEFTIOT1                                                S99528
         EJECT
*                                                                S99528
         OLTCB                                                   S21903
PLIST    DC    4F'0'               GETMAIN/FREEMAIN PLIST      @Y17XAUU
TBEG1    DS    0H                                              @Y17XAU2
TOTUCBAB DS    24C                 UCB COPY AREA COMMON SECTION@Y17XAU2
TOTUCBAC DC    H'24'               HALFWORD LENGTH FOR SUC 102 @Y17XAU2
CUCUU    DS    1H                  SAVE AREA FOR UCB NAME (HEX)@Y17XAU2
CUUCBADR DC    F'0'         ADDRESS OF UCB                     @Y17XAU2
CUUCBNAM DS    3C                  NAME OF UCB IN EBCDIC       @Y17XAU2
CUUCBWRK DS    1F                  WORK AREA FOR UCB LOOKUP    @Y17XAU2
CUUCBSAV DS    1F                  POINTER TO UCB AREA         @Y17XAU2
CUREGSAV DS    11F                 REG SAVE AREA DURING START/ @Y17XAU2
*                                  STOP LINE CALCULATIONS      @Y17XAU2
CUGETSAV DS    1F                  ADDRESS OF GET MAIN AREA    @Y17XAU2
TEND1    DS    0H                                              @Y17XAU2
TBEG2    DS    0H                                              @Y17XAU2
CUENQLST ENQ   (QNAME,RNAME,E,8,STEP),RET=USE,MF=L             @Y17XAU2
QNAME    DC    CL8'SYSZTOTE'       NAME FOR ENQ MACRO          @Y17XAU2
RNAME    DC    8CL1' '             RESOURCE TO BE ENQ'ED       @Y17XAU2
TOT102   DS    0F                  ALIGN ON FULLWORD           @Y17XAU2
         DC    X'08'               FLAG FOR DATA MOVE          @Y17XAU2
         DC    AL3(TOTUCBAB)       DATA ADDRESS                @Y17XAU2
         DC    X'00'               2ND WORD FLAGS              @Y17XAU2
TOT102A  DC    AL3(0)              TARGET ADDRESS (DYNAMIC)    @Y17XAU2
         DC    X'80'               LAST WORD FLAG              @Y17XAU2
         DC    AL3(TOTUCBAC)       ADDRESS CONTAINING LENGTH   @Y17XAU2
TEND2    DS    0H                                              @Y17XAU2
SLPL     DS    0F                 BOUNDARY ALLIGNMENT          @Y17XAUU
ELEM1    DS    F                  POINTER TO FIRST ELEMENT     @Y17XAUU
ELEM2    DS    F                  POINTER TO LAST ELEMENT      @Y17XAUU
         EJECT                                                 @Y17XAU2
RESPL    RESPL                                                 @Y17XAU2
         END
