         TITLE 'IEDQWM2 - TRACE - RETURN CODE MODULE'
IEDQWM2  CSECT
*A000000-999999                                                @Y16X5U0
* CHANGE ACTIVITY AS FOLLOWS:                                    S22024
*A402000,662000                                                 SA41596
*A340300,311200,517200,782000                                  @Y17XAUU
*C694000-732500                                                @Y17XAUU
         SPACE 4
****************************************************************
*                                                              *
* MODULE NAME: IEDQWM2 (TCAM,TOTE)                             @Y17XAUU
*                                                              *
* DESCRIPTIVE NAME = TRACE - RETURN CODE MODULE                * S22024
*                                                              * S22024
* COPYRIGHT = NONE                                             * S22024
*                                                              * S22024
*    IEDQWM2                                                   *
*                                                              *
* STATUS:                                                      *
*                                                              *
*    VERSION 10.0                                              @Y17XAUU
*                                                              *
* FUNCTION:                                                    *
*                                                              *
*    THIS MODULE PROVIDES THE CE WITH A LIMITED TRACE FACILITY *
*    FOR OLT EXECUTION. A RETURN CODE FUNCTION IS PROVIDED TO  *
*    EVALUATE SERVICE MODULE RETURN CODES.                     *
*                                                              *
*    IF THE RETURN CODE OPTION IS SET, IEDQWM2 EXAMINES THE    *
*    RETURN CODE MASK TO DETERMINE WHETHER THE OLT SECTION     *
*    SHOULD BE TERMINATED. IF THE MASK INDICATES TERMINATION,  *
*    THE ADDRESS OF THE CLEANUP ROUTINE FOR THE OLT IS PLACED  *
*    IN REGISTER 14 (IF THE CLEAN-UP PARAMETER IS CODED IN THE *
*    IN THE INLZE MACRO FOR THE OLT). IF NO CLEANUP ADDRESS IS *
*    SPECIFIED, THE DTERM ADDRESS FOR THE OLT SECTION IS       *
*    PLACED IN REGISTER 14. IF THE MASK DOES NOT INDICATE      *
*    TERMINATION, RETURN WILL BE MADE TO THE OLT NORMALLY.     *
*                                                              *
*    THE RETURN CODE OPTION WILL ALSO PRINT A TRACE LINE AT    *
*    THE ALTERNATE PRINTER SITE. INFORMATION GIVEN IN THE      *
*    TRACE INCLUDES THE ROUTINE NUMBER, THE MACRO ID OF THE    *
*    MACRO JUST EXECUTED, THE LOCATION TO WHICH CONTROL        *
*    SHOULD NORMALLY RETURN TO IN THE OLT, AND THE RETURN      *
*    CODE. AN '*' IS PRINTED IN THE FIRST PRINT POSITION. THE  *
*    FOLLOWING IS AN EXAMPLE OF THE TRACE LINE FOR THIS        *
*    FUNCTION.                                                 *
*                                                              *
*    * RTN01, ID 37, AT 000234A4, RC 08                        *
*                                                              *
*    IN THE ABOVE EXAMPLE, ROUTINE 1 IS CURRENTLY EXECUTING.   *
*    THE CECOM MACRO WAS ISSUED. THE CECOM SERVICE MODULE SET  *
*    A RETURN CODE OF X'08'. NORMALLY CONTROL WOULD BE         *
*    RETURNED TO THE OLT AT CORE LOCATION X'000234A4'.         *
*                                                              *
*    IF THE TRACE OPTION IS SPECIFIED A TRACE LINE IS PRINTED  *
*    AT THE ALTERNATE PRINTER SITE. THIS TRACE IS THE SAME     *
*    FOR THE RETURN CODE FUNCTION EXCEPT THE '*' IS NOT        *
*    PRINTED.                                                  *
*                                                              *
*    IF BOTH OPTIONS ARE SET, ONLY THE ROUTINE FUNCTION IS     *
*    PERFORMED.                                                *
*                                                              *
*    IF NEITHER OPTION IS SET, A CHECK IS MADE FOR INCORRECT   *
*    MACRO LEVEL OR UNSUPPORTED MACRO FUNCTION. IF EITHER OF   *
*    THESE IS FOUND THE CONTROL TERMINAL IS NOTIFIED OF THE    *
*    ERROR. CONTROL IS RETURNED TO THE OLT NORMALLY. IF        *
*    NEITHER IS FOUND CONTROL IS RETURNED TO THE OLT NORMALLY. *
*                                                              *
* ENTRY POINTS:                                                *
*                                                              *
*         IEDQWM2 - CALLED BY ANY SERVICE MODULE TO EVALUATE   *
*         THE RETURN CODES AND/OR PROVIDE A TRACE OF OLT       *
*         EXECUTION.                                           *
*                                                              *
* INPUT:                                                       *
*                                                              *
*    REGISTERS 1,2,13,14,15 CONTAIN THE FOLLOWING VALUES:      *
*                                                              *
*    01-MACRO PARAMETER LIST ADDRESS                           *
*    02-SCT ADDRESS (OLTCB BASE)                               *
*    13-SAVE AREA ADDRESS                                      *
*    14-RETURN ADDRESS                                         *
*    15-ENTRY POINT ADDRESS                                    *
*                                                              *
*    THE RETCODE FIELD IN THE OLTCB CONTAINS THE RETURN CODE   *
*    FROM THE SERVICE MODULE LAST EXECUTED.                    *
*                                                              *
*    THE $RETMASK FIELD IN THE SCT CONTAINS THE MASK BIT       *
*    SETTING OF THE ROUTINE CURRENTLY EXECUTING.               *
*                                                              *
* OUTPUT:                                                      *
*                                                              *
*    PASS TRACE LINE TO IEDQWO TO BE PRINTED                   * S22024
*                                                              *
* EXTERNAL ROUTINES:                                           *
*                                                              *
*    IEDQWO - ACCESS MANAGER TO PRINT MESSAGE                  * S22024
*                                                              *
* EXITS NORMAL:                                                *
*                                                              *
*         RETURNS CONTROL TO CALLING ROUTINE  IF UNSUPPORTED   *
*         MACRO LEVEL                                          *
*                                                              @Y17XAUU
* MODULE TYPE: PROCEDURE                                       @Y17XAUU
*                                                              @Y17XAUU
* MODULE SIZE: 2K MAXIMUM                                      @Y17XAUU
*                                                              @Y17XAUU
* PROCESSOR: ASSEMBLER XF                                      @Y17XAUU
*                                                              @Y17XAUU
* MACRO: IEDHJN,SAVE                                           @Y17XAUU
*                                                              @Y17XAUU
*                                                              *
* ATTRIBUTES:                                                  *
*                                                              *
*         ENABLED, PROBLEM PROGRAM MODE, TRANSIENT             *
*                                                              *
* NOTES: SEE BELOW                                             @Y17XAUU
*                                                              @Y17XAUU
*  DEPENDENCIES: EBCDIC CHARACTER CODE                         @Y17XAUU
*                                                              @Y17XAUU
*  REGISTERS CONVENTIONS: SEE REGISTER ASSIGNMENT              @Y17XAUU
*                                                              @Y17XAUU
*  PATCH LABEL: PATCH                                          @Y17XAUU
*                                                              @Y17XAUU
*  RESTRICTIONS: NONE                                          @Y17XAUU
*                                                              *
****************************************************************
         EJECT
*                                                              *
*        E Q U A T E S                                         *
*                                                              *
R0       EQU   0                  REG 0
R1       EQU   1                  REG 1
R2       EQU   2                  REG 2
R3       EQU   3                  REG 3
R4       EQU   4                  REG 4
R5       EQU   5                  REG 5
R6       EQU   6                  REG 6
R7       EQU   7                  REG 7
R8       EQU   8                  REG 8
R9       EQU   9                  REG 9
R10      EQU   10                 REG 10
R11      EQU   11                 REG 11
R12      EQU   12                 REG 12
R13      EQU   13                 REG 13
R14      EQU   14                 REG 14
R15      EQU   15                 REG 15
         SPACE
D0       EQU   0                  DISPLACEMENT OF 0
D1       EQU   1                  DISPLACEMENT OF 1
D2       EQU   2                  DISPLACEMENT OF 2
D3       EQU   3                  DISPLACEMENT OF 3
D4       EQU   4                  DISPLACEMENT OF 4
D5       EQU   5                  DISPLACEMENT OF 5
D6       EQU   6                  DISPLACEMENT OF 6
D7       EQU   7                  DISPLACEMENT OF 7
D8       EQU   8                  DISPLACEMENT OF 8
D9       EQU   9                  DISPLACEMENT OF 9
D10      EQU   10                 DISPLACEMENT OF 10
D11      EQU   11                 DISPLACEMENT OF 11
D12      EQU   12                 DISPLACEMENT OF 12
D13      EQU   13                 DISPLACEMENT OF 13
D14      EQU   14                 DISPLACEMENT OF 14
D15      EQU   15                 DISPLACEMENT OF 15
D16      EQU   16                 DISPLACEMENT OF 16
D17      EQU   17                 DISPLACEMENT OF 17
D18      EQU   18                 DISPLACEMENT OF 18
D19      EQU   19                 DISPLACEMENT OF 19
D20      EQU   20                 DISPLACEMENT OF 20
D24      EQU   24                 DISPLACEMENT OF 24
D28      EQU   28                 DISPLACEMENT OF 28
D32      EQU   32                 DISPLACEMENT OF 32
*
ZERO     EQU   X'F0'              VALUE OF 0
ONE      EQU   X'F1'              VALUE OF 1
TWO      EQU   X'F2'              VALUE OF 2
NOASK    EQU   X'40'              BLANK
TRASK    EQU   C'*'               ERROR INDICATOR
BADLEV   EQU   X'04'              INCORRECT MACRO LEVEL OR FUNCTION NOT
*                                 AVAILABLE RETURN CODE
ABORT    EQU   X'10'              ABORT ON RETURN CODE 4
RTNALL   EQU   X'FF'              ABORT OLT SECTION ON ANY BAD RET CODE
THIRTY   EQU   X'1E'              VALUE OF 30
K80      EQU   X'80'              INITIAL BIT TESTER
         EJECT
         USING IEDQWM2,R15
*
IEDQWM2  IEDHJN STARM2,HJN
*
         DROP  R15
         SPACE 2
         SAVE  (14,12)            SAVE CALLER'S REGISTERS
*
         USING IEDQWM2,R4
         USING TOTOLTCB,R2
*
         LR    R4,R15             SET BASE REGISTER
         LA    R15,TOTSAVE1       GET ADDR OF OWN SAVE AREA
         ST    R15,D8(R13)        PUT ADDR IN CALLER'S SAVE AREA
         ST    R13,D4(R15)        SAVE ADDR OF CALLER'S SAVE AREA
         LR    R13,R15            SET REG 13 TO MY SAVE AREA
         LA    R8,TOTSERV         WORK AREA ADDRESS            @Y17XAUU
         LR    R10,R8             SAVE ADDRESS                 @Y17XAUU
         LA    R9,L'TOTSERV       GET LENGTH OF WORK AREA      @Y17XAUU
         SLR   R11,R11            INIT COUNTER                 @Y17XAUU
         MVCL  R8,R10             CLEAR WORK AREA              @Y17XAUU
         MVC   TR0MSG(TRNBE-START),START  MVE DATE TO WRK AREA @Y17XAUU
         ST    R13,KSAVE3         SAVE REG 13                  @Y17XAUU
         SPACE 2
*
         MVI   TOTOTBUF,NOASK     BLANK OUTPUT
         MVC   TOTOTBUF+D1(K80),TOTOTBUF  BUFFER
*
*
         CLI   TOTRTCOD,BADLEV    INCORRECT MACRO LEVEL OR FUNCTION NOT
*                                 SUPPORTED RETURN CODE ?
         BNE   TR008              NO - EVALUATE RETURN CODE FUNCTION
         SPACE
         LA    R15,BADLEV         SET RETURN CODE FOR OLT
         BAL   R5,TRRFIND         FIND RETURN ADDR
*
TR004    EQU   *
*
         TM    TOTFLG09,TOTMACFT  UNSUPPORTED MACRO FTN ?
         BZ    TR006              NO - ASSUME UNSUPPORTED MACRO
         NI    TOTFLG09,RTNALL-TOTMACFT TURN OFF FLAG
         MVC   TOTOTBUF(TRNBEX-TRNB),TRNB MOVE MSG TO OLTCB BUF@Y17XAUU
         LA    R5,TRNBEX-TRNB     GET MSG COUNT                @Y17XAUU
         STC   R5,TOTOTCNT        SAVE IT
         NI    TOTFLG05,RTNALL-TOTREPLY TURN OFF REPLY FLAG
         NI    TOTFLG05,RTNALL-TOTPRINT TURN OFF PRINT FLAG
         OI    TOTFLG05,TOTCECOM  SET CECOM FLAG
         B     TR007              GO LINK TO ACCESS MANAGER MODULE
TR006    EQU   *   UNSUPPORTED MACRO LEVEL
         MVC   TOTOTBUF(TRNAE-TRNA),TRNA MOVE MSG TO OLTCB BFR
         LA    R5,TRNAE-TRNA      GET MSG
         STC   R5,TOTOTCNT        SET MSG COUNT
         NI    TOTFLG05,RTNALL-TOTREPLY   TURN OFF REPLY FLAG
         NI    TOTFLG05,RTNALL-TOTPRINT   TURN OFF PRINT FLAG
         OI    TOTFLG05,TOTCECOM  SET CECOM FLAG
TR007    EQU   *
         L     R13,KSAVE3         RESTORE SAVE AREA POINTER
         L     R15,TOTRESPL       GET RESPL ADDRESS            @Y17XAUU
         USING RESPL,R15          SET RESPL ADDRESSABILITY     @Y17XAUU
         L     R15,RESPLWO        GET ACCESS ROUTINE ADDRESS   @Y17XAUU
         BALR  R14,R15            CALL IEDQWO                  @Y17XAUU
         DROP  R15                                             @Y17XAUU
         TM    $OLTFLGS,$TRACE    TRACE SET
         BZ    TR070              NO - RETURN
*
         SPACE
TR008    EQU   *   EVALUATE RETURN CODE FUNCTION
*
         TM    $RETMASK,RTNALL    IS ROUTINE FUNCTION REQUESTED
         BZ    TR040              NO - CHECK FOR TRACE OPTION
*
TR015    EQU   *
*
         LA    R13,TOTRTCOD       GET ADDR OF RETURN CODE
         LA    R14,TRETCD1        GET OUTPUT ADDR
         LA    R15,D1             GET BYTE COUNT
*
         BAL   R10,CONVERT        CONVERT TO EBCDIC
         SPACE 2
*
TR020    EQU   *    PUT MACRO ID AND ROUTINE NUMBER INTO BUFFER
*
         L     R15,TOTPARML       GET ADDRESS OF OLT PLIST     @Y17XAUU
         USING PLIST,R15          SET ADDRESSABILITY           @Y17XAUU
         MVC   TRMID(D2),PLID     MOVE ID TO MESSAGE           @Y17XAUU
         DROP  R15                                             @Y17XAUU
*
         SPACE
*
         USING SPREF1,R3          SECTION PREFACE DSECT PART 1
         USING SPREF2,R9          SECTION PREFACE DSECT PART 2
         L     R3,TOTOLTEN        SECTION PREFACE BASE          SA41596
         SR    R9,R9              ZERO REG 9
         LH    R9,RTNDISP         GET DISPLACEMENT TO ROUTINE NUMBER
         AR    R9,R3              ESTABLISH BASE REG FOR PART 2
*
         LA    R13,RTN#           GET ADDRESS OF ROUTINE NUMBER
         LA    R14,TRRTN          GET ADDRESS OF OUTPUT AREA
         LA    R15,D1             GET NUMBER OF HEX BYTES TO CONVERT
         BAL   R10,CONVERT        CONVERT ROUTINE NUMBER TO EBCDIC
*
         DROP  R3
         DROP  R9
*
         SPACE
*
*        FIND NORMAL RETURN ADDRESS FOR OLT AND CONVERT TO EBCDIC
*
         LA    R13,TOTSMGRT       GET OLT RETURN ADDR
         LA    R14,TRETAD         POINT TO OUTPUT BUFFER
         LA    R15,D4             SET INPUT COUNT
         BAL   R10,CONVERT        CONVERT ADDRESS TO EBCDIC
         SPACE 2
*
         MVI   TRERR,TRASK        PUT ERROR INDICATOR IN BUFFER
*
         SPACE
*
TR040    EQU   *                  TRACE ENTRY
*
         TM    $RETMASK,RTNALL    IS RETURN CODE FUNCTION SET ?
         BNZ   TR050              YES - CONTINUE PROCESSING
         TM    TRFLAG,RTNALL      HAS TRACE PROCESSING BEEN DONE ?
         BO    TR045              YES - SET UP TO EXIT
         MVI   TRFLAG,RTNALL      SET TRACE BYPASS FLAG
         B     TR015              PROCESS TRACE REQUEST
         SPACE
*
TR045    EQU   *
*
         MVI   TRERR,NOASK        BLANK ERROR INDICATOR
*
         SPACE 2
*
TR050    EQU   *   DETERMINE WHERE TO RETURN TO CALLER
         BAL   R5,TRRFIND         FIND RETURN ADDR
TR060    EQU   *
*
         SPACE
         MVC   TOTOTBUF(TR0END-TR0MSG),TR0MSG MOVE MSG TO OLTCB BFR
         LA    R5,TR0END-TR0MSG   GET MSG
         STC   R5,TOTOTCNT        SET MSG COUNT
         NI    TOTFLG05,RTNALL-TOTCECOM TURN OFF CECOM
         NI    TOTFLG05,RTNALL-TOTREPLY  TURN OFF REPLY
         L     R13,KSAVE3         RESTORE MY SAVE AREA ADDR
         OI    TOTFLG05,TOTPRINT  SET PRINT FLAG
         SPACE
         USING RESPL,R5           SET RESPL ADDRESSABILITY     @Y17XAUU
         L     R5,TOTRESPL        GET RESPL ADDRESS            @Y17XAUU
         L     15,RESPLWO         GET ADDRESS OF ACCESS MANAGER@Y17XAUU
         BALR  R14,R15            PRINT MESSAGE                @Y17XAUU
         DROP  R5                                              @Y17XAUU
         SPACE 2
TR070    EQU   *
         SR    R15,R15            CLEAR REG 15
         IC    R15,TOTRTCOD       RESTORE RETURN CODE
         L     R13,KSAVE3         RESTORE MY SAVE AREA PTR
         L     R13,D4(R13)        GET CALLER'S SAVE AREA PTR
         L     R14,D12(R13)       RESTORE RETURN ADDR
         L     R0,TOTWKSPC        RESTORE REG 0 (MORECORE)
         LM    R2,R12,D28(R13)    RESTORE REGS
         BR    R14                RETURN
         EJECT
*        H E X  T O  E B C D I C  C O N V E R T                       *
*                                                                     *
*              CALL SEQ - R13 = INPUT ADDRESS                         *
*                         R14 = OUTPUT ADDRESS                        *
*                         R15 = INPUT BYTE COUNT                      *
*                                                                     *
*                         BAL R10,CONVERT                             *
*                                                                     *
CONVERT  EQU   *
*
         STM   R5,R6,KSAVE2       SAVE WORK REGISTERS
         SPACE
         AR    R14,R15            ADD COUNT TO OUTPUT ADDRESS
         AR    R14,R15            ADD IT AGAIN
         AR    R13,R15            ADD COUNT TO INPUT ADDRESS
         SR    R5,R5              CLEAR REG 5
         SR    R6,R6              CLEAR REG 6
         SPACE
*
CONV1    EQU   *
*
         S     R14,TWO#           GET ADDRESS OF LAST BYTE OF OUTPUT
         BCTR  R13,R0             GET ADDR OF LAST BYTE OF INPUT
         IC    R6,D0(R13)         GET HEX BYTE
         LR    R5,R6              PUT IT IN REG 5
         SRL   R6,4               GET BITS 0-3 IN REG 6
         N     R5,FIF#            GET BITS 4-7 IN REG 5
         IC    R6,KEBCD(R6)       CONVERT BITS 0-3 TO EBCDIC CHAR
         IC    R5,KEBCD(R5)       CONVERT BITS 4-7 TO EBCDIC CHAR
         STC   R6,D0(R14)         PUT 1ST CHAR IN OUTPUT
         STC   R5,D1(R14)         PUT 2ND CHAR IN OUTPUT
         BCT   R15,CONV1          GET NEXT HEX BYTE
*
         LM    R5,R6,KSAVE2       RESTORE WORK REGISTERS
         L     R13,KSAVE3         RESTORE SAVE REG
         BR    R10                RETURN TO CALLER
         EJECT
         SPACE 2
*
*        ROUTINE TO DETERMINE WHETHER OR NOT
*        TO TERMINATE THE OLT.  THIS ROUTINE
*        PLACES THE CORRECT RETURN ADDRESS IN
*        REGISTER 14.
*
*        LINKAGE  BAL R5,TRRFIND
*
TRRFIND  EQU   *
         STM   R5,R8,KSAVE2       SAVE WORK REGISTERS
         L     R13,KSAVE3         RESTORE MY SAVE AREA ADDR
         TM    $RETMASK,RTNALL    IS RETURN CODE FTN SET ?
         BZ    TRR0K              NO - RETURN NORMALLY
         LA    R7,D8              SET LOOP COUNT
         LA    R5,KTERM           GET ADDR OF RETURN CODE TABLE
         LA    R6,K80             SET BIT TO TEST INITIAL
TRR0B    EQU   *
         CLC   D0(D1,R5),TOTRTCOD RETURN CODE MATCH ?
         BE    TRR0H              YES - CHECK BIT
         LA    R5,D1(R5)          POINT TO NEXT CODE
         SRL   R6,1               SET TO TESTEST NEXT BIT
         BCT   R7,TRR0B           TEST NEXT RETURN CODE
         B     TRR0K              NO MATCH - RETURN NORMALLY
TRR0H    EQU   *
         EX    R6,EXRTCOD         TERMINATE OLT ?
         BZ    TRR0K              NO - RETURN NORMALLY
*
*        RETURN TO OLT CLEANUP/DTERM
*
         NI    $OLTFLGS,RTNALL-$RETCODE SET RETURN CODE TO 0 (DTERM)
         OI    $OLTFLGS,$CLEANUP  SET CLEANUP FLAG TO 1 (CLEANUP)
         MVI   TRERR,TRASK        SHOW TERMINATE ON BAD RC
*
         L     R14,TOTOLTEN       GET OLT ENTRY POINT
         L     R14,D12(R14)       ADDR OF FIRST EX INSTR        SA41596
         LA    R14,THIRTY(R14)    POINT TO DESIRED ENTRY POINT
         ST    R14,TOTSMGRT       SET UP OLT RETURN ADDR
         SR    R15,R15            CLEAR REG 15
         IC    R15,TOTRTCOD       GET RETURN CODE
*
         B     TRREND             RETURN TO CALLER
*
TRR0K    EQU   *   RETURN NORMALLY
*
TRREND   EQU   *
         SR    R15,R15            CLEAR REG 15
         IC    R15,TOTRTCOD       GET RETURN CODE
         LM    R5,R8,KSAVE2       RESTORE CALLER'S REGISTERS
         BR    R5                 RETURN
         EJECT
*
*        C O N S T A N T S  A N D  F L A G S
*
START    DS    0H                 START OF DATA AREA           @Y17XAUU
TR0MSGX  DS    0C                 TRACE LINE BUFFER            @Y17XAUU
TRERRX   DC    C' '               ERROR INDICATOR              @Y17XAUU
         DC    C' '               BLANK SPACE
         DC    C'RTN'             ROUTINE
TRRTNX   DS    2C                 ROUTINE NUMBER               @Y17XAUU
         DC    C','               DELIMITER
         DC    C' ID '            IDENTITY
TRMIDX   DS    2C                 MACRO ID                     @Y17XAUU
         DC    C','               DELIMITER
         DC    C' AT '            LOCATION OF
TRETADX  DS    8C                 RETURN ADDRESS IN OLT        @Y17XAUU
         DC    C','               DELIMITER
         DC    C' RC '            RETURN CODE
TRETCD1X DS    C                  FIRST DIGIT OF RETURN CODE   @Y17XAUU
TRETCD2X DS    C                  SECOND DIGIT OF RETURN CODE  @Y17XAUU
TR0ENDX  DS    0C                 END OF TRACE BUFFER          @Y17XAUU
TRNAX    DS    0C                 START OF MSG A               @Y17XAUU
         DC    C'IED337I '        MSG ID                         S22024
         DC    C'MACRO NOT SUPPORTED' ERROR MESSAGE
TRNAEX   DS    0C                 END OF MSG A                 @Y17XAUU
TRNBX    DS    0C                 START OF MSG B               @Y17XAUU
         DC    C'IED336I '        MSG ID                         S22024
         DC    C'MACRO FUNCTION NOT SUPPORTED'   ERROR MESSAGE
TRNBE    DS    0C                 END OF MSG B
FOUR     DC    F'4'               VALUE OF FOUR
EIGHT    DC    F'8'               VALUE OF EIGHT
TWO#     DC    F'2'               VALUE OF 2
FIF#     DC    F'15'              VALUE OF 15
KTERM    DC    X'8040201008040201' RETURN CODE TABLE
KEBCD    DC    C'0123456789ABCDEF' HEX TO EBCDIC CONVERSION TABLE
EXRTCOD  TM    $RETMASK,D0        IS TERMINATE BIT SET ?
         SPACE
         DS    0F                 BOUNDARY ALLIGNMENT
PATCH    DC    100XL1'40'         PATCH AREA
         SPACE 4
PLIST    DSECT
         DS    CL1                CONTROL PROGRAM FLAGS
         DS    AL1                MACRO LEVEL
PLID     DS    CL2                MACRO ID
         SPACE 2
SPREF1   DSECT
         DS    CL8                OLT ROOT MODULE ID
         DS    CL2                LEVEL
RTNDISP  DS    AL2                DISPLACEMENT TO ROUTINE NUMBER
         SPACE 2
SPREF2   DSECT
RTN#     DS    XL1                CURRENT ROUTINE NUMBER
         EJECT
         OLTCB
KSAVE1   DS    F                  SAVE AREA                    @Y17XAUU
KSAVE2   DS    4F                 SUBROUTINES SAVE AREA        @Y17XAUU
KSAVE3   DS    F                  SAVE AREA                    @Y17XAUU
TR0MSG   DS    0C                 TRACE LINE BUFFER            @Y17XAUU
TRERR    DC    C' '               ERROR INDICATOR
         DC    C' '               BLANK SPACE
         DC    C'RTN'             ROUTINE
TRRTN    DS    2C                 ROUTINE NUMBER
         DC    C','               DELIMITER
         DC    C' ID '            IDENTITY
TRMID    DS    2C                 MACRO ID
         DC    C','               DELIMITER
         DC    C' AT '            LOCATION OF
TRETAD   DS    8C                 RETURN ADDRESS IN OLT
         DC    C','               DELIMITER
         DC    C' RC '            RETURN CODE
TRETCD1  DS    C                  FIRST DIGIT OF RETURN CODE
TRETCD2  DS    C                  SECOND DIGIT OF RETURN CODE
TR0END   DS    0C                 END OF TRACE BUFFER
TRNA     DS    0C                 START OF MSG A
         DC    C'IED337I '        MSG ID                         S22024
         DC    C'MACRO NOT SUPPORTED' ERROR MESSAGE
TRNAE    DS    0C                 END OF MSG A
TRNB     DS    0C                 START OF MSG B
         DC    C'IED336I '        MSG ID                         S22024
         DC    C'MACRO FUNCTION NOT SUPPORTED'   ERROR MESSAGE
TRNBEX   DS    0C                 END OF MSG B                 @Y17XAUU
TRFLAG   DC    XL1'0'             FLAG BYTE                    @Y17XAUU
         EJECT
RESPL    RESPL                                                 @Y17XAUU
         END
