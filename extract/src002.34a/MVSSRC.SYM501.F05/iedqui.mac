UI01     TITLE '''IEDQUI'' - USER INTERFACE ROUTINE'
IEDQUI   CSECT
*A895000                                                         A49201
***********************************************************************
*                                                                     *
*STATUS -- CHANGE LEVEL 0                                             *
*                                                                     *
*FUNCTION -- USER INTERFACE IS THE COMMON ROUTINE THROUGH WHICH ALL   *
*   MESSAGE HANDLER MACRO EXPANSIONS LINK TO FIRST-LEVEL MESSAGE      *
*   HANDLER ROUTINES.  IT SAVES THE USER'S REGISTERS,  INITIALIZES    *
*   REGISTER CONTENTS TO COMMONLY NEEDED VALUES,  ACCESSES THE        *
*   ADDRESS OF THE ROUTINE TO BE LINKED TO AND EXITS TO IT.  USER     *
*   INTERFACE IS ALSO USED BY CERTAIN FIRST-LEVEL MESSAGE HANDLER     *
*   ROUTINES TO PROVIDE INITIALIZATION AND LINKAGE TO LOWER LEVEL     *
*   MESSAGE HANDLER ROUTINES. IT ALSO USED AS THE RETURN INTER-  S22025
*   FACE WHEN USING THE PLUS FOUR ENTRY. IT RESTORES THE USER'S  S22025
*   REGISTERS 2 THROUGH 12 AND 14 FROM THE SAVE AREA POINTED TO  S22025
*   BY REGISTER 13 AND EXITS ON REGISTER 14. THIS MODULE PROVIDES     *
*   INDEPENDENT REGISTER PROTECTION BETWEEN MESSAGE HANDLER LEVELS.   *
*                                                                     *
*   REGISTERS 2 THROUGH 12 AND 14 ARE SAVED IN THE SAVE AREA          *
*   POINTED TO BY REGISTER 13.                                        *
*                                                                     *
*   THE ADDRESS OF THE ADDRESS VECTOR TABLE (AVT) IS ACCESSED FROM    *
*   THE COMMUNICATION VECTOR TABLE (CVT).  THE ADDRESS OF THE         *
*   CURRENT BUFFER IS ACCESSED FROM THE AVT (AVTADBUF). THE ADDRESS   *
*   OF THE LCB IS ACCESSED FROM THE BUFFER PREFIX (PRFLCB). THE       *
*   ADDRESS OF THE CURRENT SCB IS ACCESSED FROM THE LCB (LCBSCBA).    *
*                                                                     *
*   REGISTER 0 IS SET TO A VALUE OF ONE TO INDICATE INITIAL (NON-     *
*   MULTIPLE BUFFER HEADER) ENTRY.                                    *
*                                                                     *
*   THE ADDRESS OF THE MESSAGE HANDLER VCON TABLE IS ACCESSED FROM    *
*   THE AVT (AVTMSGS).  THE INDEX TO THE ADDRESS OF THE ROUTINE TO    *
*   BE ENTERED IS ACCESSED FROM THE FIRST BYTE OF THE PARAMETER       *
*   LIST,  INDEX-BYTE FLAGS ARE CLEARED,  AND THE INDEX IS ADDED TO   *
*   THE MESSAGE HANDLER VCON TABLE ADDRESS.  THE ADDRESS OF THE       *
*   ROUTINE TO BE ENTERED IS ACCESSED FROM THE RESULTING LOCATION     *
*   INTO REGISTER 12.  USER INTERFACE THEN EXITS ON REGISTER 12,      *
*   WHICH BECOMES THE BASE FOR THE ROUTINE BEING ENTERED.             *
*                                                                     *
*ENTRY POINTS --                                                      *
*                                                                     *
*       PLUS 0 ENTRY:                                            S22025
*                                                                     *
*       'IEDQUI' TO SAVE REGISTERS,  INITIALIZE REGISTERS AND EXIT    *
*   TO THE ROUTINE SELECTED.  CALLING SEQUENCE FROM THE MESSAGE       *
*   HANDLER MACRO EXPANSION OR FROM THE FIRST-LEVEL MESSAGE           *
*   HANDLER ROUTINE IS:                                               *
*                                                                     *
*        BAL   1,*+(PARAMETER LIST LENGTH) SET ADDR OF PARAM LIST     *
*                                                                     *
*        ********************************                             *
*        * VCON  * PARAM *               *                            *
*        * TABLE * LIST  *               *   PARAMETER LIST           *
*        * INDEX * LNGTH *               *   (GENERATED BY MH         *
*        *****************  PARAMETER    *   MACRO OR BUILT           *
*        *                               *   BY MH ROUTINE)           *
*        *               INFORMATION     *                            *
*        *                               *                            *
*        *                                                            *
*        *                                                            *
*                                                                     *
*        L     15,IEDUI                 GET USER INTERFACE ADDRESS    *
*        BALR  14,15                    EXIT TO USER INTERFACE        *
*                                                                     *
*       PLUS 4 ENTRY:                                            S22025
*                                                                     *
*       'IEDQUI+4' TO RESTORE THE CALLER'S REGISTERS AND RETURN  S22025
*   TO THE CALLER. CALLING SEQUENCE IS:                          S22025
*                                                                S22025
*       L     R12,AVTUI                 GET USER INTERFACE ROUT  S22025
*       B     FOUR(R12)                 EXIT TO RETURN TO CALLER S22025
*                                                                S22025
*INPUT --                                                             *
*   PLUS 0 ENTRY:                                                S22025
*                                                                S22025
*   REGISTER 1 - THE ADDRESS OF A PARAMETER LIST WHOSE FIRST BYTE     *
*   CONTAINS THE MH VCON TABLE INDEX TO THE ROUTINE TO BE ENTERED.    *
*                                                                     *
*   REGISTER 13 - THE ADDRESS OF THE CALLER'S REGISTER SAVE AREA.     *
*                                                                     *
*                                                                S22025
*   PLUS 4 ENTRY:                                                S22025
*                                                                S22025
*   REGISTER 13 -- THE ADDRESS OF THE CALLING ROUTINES SAVE AREA.S22025
*                                                                S22025
*OUTPUT --                                                            *
*                                                                S22025
*   PLUS 0 ENTRY:                                                S22025
*                                                                S22025
*   REGISTER 0 - ENTRY CODE,  VALUE OF ONE.                           *
*                                                                     *
*   REGISTER 1 - PARAMETER LIST ADDRESS.                              *
*                                                                     *
*   REGISTER 3 - THE ADDRESS OF THE SCB.                              *
*                                                                     *
*   REGISTER 4 - THE ADDRESS OF THE LCB.                              *
*                                                                     *
*   REGISTER 6 - THE ADDRESS OF THE CURRENT BUFFER.                   *
*                                                                     *
*   REGISTER 9 - THE ADDRESS OF THE AVT.                              *
*                                                                     *
*   REGISTER 12 - THE ENTRY POINT ADDRESS AND BASE REGISTER FOR       *
*   THE ROUTINE TO BE ENTERED.                                        *
*                                                                     *
*   REGISTER 13 - THE ADDRESS OF THE CALLING ROUTINE'S SAVE AREA.     *
*                                                                     *
*   REGISTER 14 - RETURN ADDRESS TO THE CALLING ROUTINE.              *
*                                                                     *
*   PLUS 4 ENTRY:                                                S22025
*                                                                S22025
*   REGISTERS 2 THROUGH 12 AND 14 -- RESTORED TO VALUES AT ENTRY S22025
*   FROM MESSAGE HANDLER OR FROM FIRST-LEVEL MH ROUTINES.        S22025
*EXTERNAL REFERENCES --                                               *
*   AVT - ADDRESS VECTOR TABLE.                                       *
*                                                                     *
*   CVT - COMMUNICATION VECTOR TABLE.                                 *
*                                                                     *
*   BUFFER TO BE PROCESSED.                                           *
*                                                                     *
*   LCB - LINE CONTROL BLOCK.                                         *
*                                                                     *
*   SCB - STATION CONTROL BLOCK FOR THE CURRENT STATION.              *
*                                                                     *
*   MESSAGE HANDLER VCON TABLE (AVTMSGS).                             *
*                                                                     *
*EXITS, NORMAL --                                                S22025
*   PLUS 0 ENTRY: EXIT IS MADE TO THE ROUTINE SPECIFIED.         S22025
*   REGISTER 0 CONTAINS AN ENTRY CODE OF ONE.  REGISTERS 1, 3, 4,     *
*   6 AND 9 ARE INITIALIZED TO COMMONLY NEEDED VALUES.  REGISTER 12   *
*   CONTAINS THE ADDRESS OF THE ROUTINE BEING ENTERED.  THE SAVE      *
*   AREA POINTED TO BY REGISTER 13 CONTAINS THE CALLER'S REGISTERS    *
*   2 THROUGH 12 AND 14.                                              *
*                                                                     *
*   PLUS 4 ENTRY: REGISTERS 2 THROUGH 12 AND 14 CONTAIN THE      S22025
*   VALUES RESTORED FROM THE SAVE AREA POINTED TO BY THE         S22025
*   BACKWARD POINTER LOCATED AT FOUR PASSED THE ADDRESS IN R13.  S22025
*                                                                     *
*EXITS,  ERROR  -- N/A.                                               *
*                                                                     *
*TABLES/WORK AREAS -- N/A.                                            *
*                                                                     *
*ATTRIBUTES -- REENTRANT,  REFRESHABLE,  ENABLED,  RESIDENT,          *
*   PROBLEM PROGRAM MODE.                                             *
*                                                                     *
*NOTES -- THE OPERATION OF THIS MODULE DOES NOT DEPEND UPON AN        *
*   INTERNAL REPRESENTATION OF THE EXTERNAL CHARACTER SET.            *
*                                                                     *
***********************************************************************
         EJECT
         SPACE
********* REGISTER EQUATES *********
         SPACE
R0       EQU   0                        WORK REGISTER
         SPACE
R1       EQU   1                        PARAMETER LIST ADDRESS
         SPACE
R2       EQU   2                        WORK REGISTER
         SPACE
RSCB     EQU   3                        CURRENT SCB ADDRESS
         SPACE
RLCB     EQU   4                        LCB ADDRESS
         SPACE
RSCAN    EQU   5                        SCAN POINTER ADDRESS
         SPACE
RPREFIX  EQU   6                        BUFFER ADDRESS
         SPACE
RAVT     EQU   9                        AVT ADDRESS
         SPACE
R12      EQU   12                       BASE & BRANCH ADDRESS
         SPACE
R13      EQU   13                       SAVE AREA ADDRESS
         SPACE
R14      EQU   14                       MH RETURN ADDRESS
         SPACE
R15      EQU   15                       LINK ADDRESS
         SPACE
********* OTHER EQUATES *********
         SPACE
ANDLOW   EQU   X'FC'                    MASK TO CLEAR INDEX FLAGS
         SPACE
ONE      EQU   1                        NON-MBH ENTRY CODE
         SPACE
CVTOFF   EQU   16                       ADDRESS OF THE CVT
         SPACE
         EJECT
         USING IEDQUI01,R15
         SPACE
IEDQUI01 EQU   *                                                 S22025
         B     ENTER                    BRANCH TO SAVE REGISTER  S22025
IEDQUI02 EQU   *                        +4 ENTRY FOR RETURN      S22025
         L     R13,4(R13)               ADDR OF PREV SAVE AREA   S22025
         L     R14,12(R13)              LOAD RETRUN ADDRESS      S22025
         LM    R2,R12,28(R13)           REST PREV REG CONTENTS   S22025
         MVI   12(R13),X'FF'            SET BYTE                 S22025
         BR    R14                      RETURN TO CALLER         S22025
IEDQUI   IEDHJN ,                       MODULE ID                S22025
ENTER    EQU   *                                                 S22025
         STM   R14,R12,12(R13)          STORE CALLERS REGISTERS  S22025
         L     RAVT,CVTOFF              GET ADDRESS OF CVT       S22025
         L     RAVT,AVTCVTPT(,RAVT)     FIND POINTER TO AVT      S22025
         L     RAVT,AVTEZERO(,RAVT)     GET ADDRESS OF AVT       S22025
         USING IEDQAVTD,RAVT                                     S22025
         LR    R12,R13                  LOAD CALLERS ADDR IN REG S22025
         LA    R13,72(R13)              LOAD MY SAVE AREA ADDR   S22025
         ST    R12,4(R13)               CONTENTS OF REG 13 STOREDS22025
         ST    R13,8(R12)               ST SAVE ADDR             S22025
         SPACE
         SPACE
         SPACE
         L     RPREFIX,AVTADBUF         INITIALIZE BUFFER ADDR
         USING IEDQPRF,RPREFIX
         SPACE
         L     RLCB,PRFLCB-1            INITIALIZE LCB ADDR
         USING IEDQLCB,RLCB
         SPACE
         L     RSCB,LCBSCBA-1           GET ADDRESS OF SCB
         SPACE
ROUTADDR EQU   *
         L     R12,AVTMSGS-1            GET ADDR OF VCON TABLE
         IC    R15,AVTEZERO(,R1)        GET INDEX TO ROUTINE ADDR
         LA    R0,ANDLOW                SET UP MASK
*** NON-ZERO VALUE IN REGISTER 0 INDICATES ENTRY FROM IEDQUI ***
         NR    R15,R0                   CLEAR INDEX FLAGS
         L     R12,AVTEZERO(R12,R15)    GET ROUTINE ADDR
         SR    R15,R15                  CLEAR REG 15             A49201
         BR    R12                      EXIT TO ROUTINE
         EJECT
********* DSECTS *********
         SPACE 2
         TAVTD
         EJECT
         TPRFD
         EJECT
         TLCBD
         END
