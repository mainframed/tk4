         TITLE 'IEDQWY GETCONFIG SERVICE MODULE'
IEDQWY   CSECT
*A000000-999999                                                @Y16X5U0
* CHANGE ACTIVITY AS FOLLOWS:                                    S22024
*A003000                                                         S22024
*C006000-009000,015000-237000,246000-309000,312000-972000        S22024
*D012472-014950                                                  S22024
*A423000,450000,609000,744000,921000,930000,96000              @Y17XAUU
*C450000,744000                                                @Y17XAUU
* A018000                                                      @G36XRUV
* C392000                                                      @G36XRUV
*THIS IS A DUMMY APAR TO CORRECT FICHE FLAGGING PROBLEM        @OZ19773
*
IEDQW16  EQU   *
         ENTRY IEDQW16
**************************************************************** S22024
*                                                              * S22024
* MODULE NAME = IEDQWY (TCAM,TOTE)                             @Y17XAUU
*                                                              * S22024
* DESCRIPTION NAME = TOTE GETCONFIG MODULE                     * S22024
*                                                              * S22024
* COPYRIGHT = NONE                                             * S22024
*                                                              * S22024
* STATUS:                                                      *
*                                                              *
*    VERSION 10.0                                              @Y17XAUU
*                                                              *
* FUNCTION:                                                    *
*                                                              *
*    THIS MODULE LOADS THE CDS RECORD FOR THE TEST DEVICE      * S22024
*    FROM ONE OF THE CDS LIBRARIES. THE CDS RECORD IS THEN     * S22024
*    PASSED TO THE OLT.                                        * S22024
*                                                              *
*    AT COMPLETION OF THIS MODULE ONE OF THE FOLLOWING RETURN  *
*    CODES IS PLACED IN REGISTER 15:                           *
*                                                              *
*        X'00' - NORMAL COMPLETION                             *
*        X'08' - BUFFER SIZE TOO SMALL                         *
*        X'0C' - CDS RECORD NOT FOUND                          * S22024
*        X'10' - CDS RECORD NOT FOUND, NETWORK ADDRESS         @Y17XAUU
*                RETURNED IN REGISTER 0                        @Y17XAUU
*                                                              *
* ENTRY POINT:                                                 *
*                                                              *
*         IEDQWY-CALLED WHEN A GETCONFG MACRO IS ISSUED BY     *
*         A UNIT TEST.                                         *
*                                                              *
* PURPOSE: SEE FUNCTION                                        @Y17XAUU
*                                                              @Y17XAUU
* PROCESSOR: ASSEMBLER XF                                      @Y17XAUU
*                                                              @Y17XAUU
* MODULE TYPE: PROCEDURE                                       @Y17XAUU
*                                                              @Y17XAUU
* MODULE SIZE: 2K MAXIMUM                                      @Y17XAUU
*                                                              @Y17XAUU
* LINKAGE: BALR R14,R15 FROM IEDQWA                            @Y17XAUU
*                                                              @Y17XAUU
* INPUT:                                                       *
*                                                              *
*    REGISTERS   02, 14,15 CONTAIN THE FOLLOWING VALUES:       *
*                                                              *
*    01 - PARAMETER LIST ADDRESS                               * S22024
*    02 - OLTCB POINTER                                        *
*    13 - CALLER'S SAVE AREA ADDRESS                           * S22024
*    14 - RETURN ADDRESS                                       *
*    15 - ENTRY POINT ADDRESS                                  *
*                                                              *
* OUTPUT:                                                      *
*                                                              *
*    REGISTERS 01,15 CONTAIN THE FOLLOWING VALUES:             *
*                                                              *
*    01 - PARAMETER LIST ADDRESS                               *
*    15 - RETURN CODE:                                         *
*         X'00' - NORMAL COMPLETION                            *
*         X'08' - BUFFER SIZE TOO SMALL                        *
*         X'0C' - CDS NOT FOUND                                * S22024
*                                                              *
* EXTERNAL ROUTINE:                                            *
*                                                              *
*         NONE                                                 *
*                                                              *
* EXITS NORMAL:                                                *
*                                                              *
*         BR AFTER GC085 TO IEDQWA(CALLER)                     *
*        XCTL TO IEDQWM2 IF TRACE IS REQUIRED                  * S22024
*                                                              *
* EXITS ERROR:                                                 *
*                                                              *
*    XCTL TO IEDQWM2 IF INCORRECT MACRO LEVEL                  * S22024
*                                                              *
* TABLES/WORK AREAS:                                           *
*                                                              *
*    OLTCB                                                     *
*    PROGRAM WORK AREA                                         *
*    UNIT TEST PARAMETER LIST                                  *
*                                                              *
* ATTRIBUTES:                                                  *
*                                                              *
*    ENABLED,PROBLEM PROGRAM MODE,RESIDENT,REENTRANT           @Y17XAUU
*                                                              *
* NOTES: SEE BELOW                                             @Y17XAUU
*                                                              *
*  RESTRICTIONS: NONE                                          @Y17XAUU
*                                                              @Y17XAUU
*  REGISTERS CONVENTION: SEE REGISTERS ASSIGNMENT              @Y17XAUU
*                                                              @Y17XAUU
*  DEPENDENCIES: EBCDIC CHARACTER CODE SET                     @Y17XAUU
*                                                              @Y17XAUU
*  PATCH LABEL: PATCH                                          @Y17XAUU
*                                                              @Y17XAUU
**************************************************************** S22024
         EJECT
*
PRIFLG   EQU   X'80'              PRIMARY REQ FLAG
FOUR     EQU   4                  A VALUE OF 4
TWO      EQU   2                  VALUE OF 2
R0       EQU   0                  REG 0
R1       EQU   1                  REG 1
R2       EQU   2                  REG 2
R3       EQU   3                  REG 3
R4       EQU   4                  REG 4
R5       EQU   5                  REG 5
R6       EQU   6                  REG 6
R7       EQU   7                  REG 7
R8       EQU   8                  REG 8
R9       EQU   9                  REG 9
R10      EQU   10                 REG 10
R11      EQU   11                 REG 11
R12      EQU   12                 REG 12
R13      EQU   13                 REG 13
R14      EQU   14                 REG 14
R15      EQU   15                 REG 15
         SPACE
D0       EQU   0                  DISP OF 0
D1       EQU   1                  DISP OF 1
D2       EQU   2                  DISP OF 2
D3       EQU   3                  DISP OF 3
D4       EQU   4                  DISP OF 4
D5       EQU   5                  DISP OF 5
D6       EQU   6                  DISP OF 6
D7       EQU   7                  DISP OF 7
D8       EQU   8                  DISP OF 8
D9       EQU   9                  DISP OF 9
D10      EQU   10                 DISP OF 10
D11      EQU   11                 DISP OF 11
D12      EQU   12                 DISP OF 12
D13      EQU   13                 DISP OF 13
D14      EQU   14                 DISP OF 14
D15      EQU   15                 DISP OF 15
D16      EQU   16                 DISP OF 16
D20      EQU   20                 DISP OF 20
D24      EQU   24                 DISP OF 24
D28      EQU   28                 DISP OF 28
D32      EQU   32                 DISP OF 32
D240     EQU   240                     DISP OF 240               S22024
XF0      EQU   X'F0'                   USED IN HEX TO EBCDIC     S22024
X3F      EQU   X'3F'              USED TO CLEAR FLAG
DISREG2  EQU   28                 DISPLACEMENT TO REG 2        @Y17XAUU
ADRMSK7  EQU   7                  ADDRESS MASK BYTE            @Y17XAUU
RETDAF   EQU   X'10'              RETURN CODE - DAF RETURNED   @Y17XAUU
         EJECT
         USING IEDQWY,R15
         SAVE  (14,12)
         SPACE
IEDQWY   IEDHJN IEDY,HJN
         LR    R6,R15
         DROP  R15
         USING IEDQWY,R6
         USING TOTOLTCB,R2
         LA    R12,TOTSAVE1       GET SAVE AREA ADDR
         ST    R12,8(13)               SAVE IT IN CALLERS SAVE AREA
         ST    R13,D4(R12)        SAVE CALLER'S SAVE REG
         LR    R13,R12                 SET UP SAVE AREA REG
         LR    R11,R1             SET P-LIST BASE
         USING PLIST,R11
         LR    R9,R1              SAVE P-LIST ADDR
         MVC   TOTSERV(END-START),START MOVE MODIFIABLE DATA   @Y17XAUU
*                                 TO WORK AREA                 @Y17XAUU
         SPACE
         CLI   1(R1),X'02'        CORRECT MACRO LEVEL?
         BE    GC000              YES
*
         LA    R15,4              SET FUNCTION NOT AVAILABLE RET CODE
         STC   R15,TOTRTCOD       SAVE IT
         L     R13,4(R13)         GET CALLER'S SAVE AREA ADDR
         L     R14,12(R13)        RESTORE REG 14
         USING RESPL,R5           SET RESPL ADDREASSABILITY    @Y17XAUU
         L     R5,TOTRESPL        GET RESPL ADDRESS            @Y17XAUU
         L     R15,RESPLM2        GET TRACE ROUTINE ADDRESS    @Y17XAUU
         LM    R2,R12,DISREG2(R13) RESTORE CALLER'S REG        @Y17XAUU
         BR    R15                EXIT TO IEDQWM2              @Y17XAUU
         DROP  5                                               @Y17XAUU
         SPACE
*
GC000    EQU   *
         L     R5,GETSEC               GET ADDR OF BASIC CDS INFOS22024
         LTR   R5,R5                   IS THTER ONE              S22024
         BNZ   CDSADRNZ                YES BRANCH                S22024
         SR    R4,R4              CLEAR REG 4
         ICM   R4,3,TOTPRUCB      GET PRIMARY UCB ADDR         @G36XRUV
         LA    R10,4(R4)          GET ADDR OF DEVICE ADDR
*
GC00B    EQU   *
         SR    R3,R3
         IC    R3,12(R1)          GET BUFFER SIZE
         SPACE
         TM    TOTFLG10,TOTTERMS  SYMBOLIC TERMINAL              S22024
         BO    GC010              YES                            S22024
         SPACE
         TM    TOTTDTBL,TOTTDLIN+TOTTDORN RN LINE                S22024
         BO    GC010              YES                            S22024
         SPACE
         SPACE
         L     R5,8(R10)          GET CUU EBCDIC REPRESENTATION
         ST    R5,READNAME+4      BUILD LOAD NAME
         MVI   READNAME,C'G'      PUT IN PREFIX
         MVI   READNAME+1,X'F0'
         MVC   READNAME+2(3),READNAME+1
         B     GC016              EXIT TO BUILD LIST
         SPACE
GC010    EQU   *
         MVC   READNAME(8),$PDEVADR+12  GET TERMINAL SYMBOLIC NAME
         MVC   NETWRKA(D2),TOTPRSID SAVE PRI TD NETWORK ADDR   @Y17XAUU
         MVC   SAVEUCB(D2),TOTPRUCB SAVE UCB ADDRESS           @Y17XAUU
         SPACE
         SPACE
GC016    EQU   *
         BLDL  0,BLDLLIST         BUILD DIRECTORY ENTRY POINT LIST
         SPACE
         LTR   R15,R15            WAS BUILD SUCCESSFUL?
         BZ    GC030              YES
         SPACE
         CH    R15,RETCODE4       CDS RECORD FOUND?            @Y17XAUU
         BE    GC020              NO                           @Y17XAUU
*
         LA    R15,8              ASSUME IO ERROR OCCURRED
         B     GC085              RETURN
         SPACE
GC020    EQU   *
         TM    TOTTDTBL,TOTTDORN  3705 ENTRY                   @Y17XAUU
         BZ    GC025              NO                           @Y17XAUU
         TM    TOTFLG10,TOTTERMS  TERMINAL ENTRY               @Y17XAUU
         BZ    GCLINK             NO - LINE                    @Y17XAUU
         SLR   R9,R9              CLEAR REG                    @Y17XAUU
         SLR   R8,R8              CLEAR REG                    @Y17XAUU
         IC    R9,TOTTTBEL        NAME LENGTH                  @Y17XAUU
         ICM   R8,ADRMSK7,TOTTDTBL+D1 TNT ADDRESS              @Y17XAUU
         AR    R8,R9              ADDRESS OF TTE ADDRESS       @Y17XAUU
         ICM   R8,ADRMSK7,D0(R8)  TTE ADDRESS                  @Y17XAUU
         LA    R9,TRMPRFSZ        TTE PREFIX SIZE              @Y17XAUU
         SLR   R8,R9              TTE PREFIX ADDRESS           @Y17XAUU
         USING IEDNTRM,R8         TTE PREFIX BASE              @Y17XAUU
         CLI   TRMTYPE,TRMLUNT    LU ENTRY                     @Y17XAUU
         BNE   CDSPRES            NO                           @Y17XAUU
         LH    R9,TRMCHCIN        DCT INDEX                    @Y17XAUU
         BCTR  R9,R0              ADJUST INDEX                 @Y17XAUU
         L     R4,TOTAVTPT        GET AVT ADDRESS              @Y17XAUU
         USING IEDQAVTD,R4        SET AVT ADDRESSABILITY       @Y17XAUU
         MH    R9,AVTDCTLN        OFFSET TO DCT ENTRY          @Y17XAUU
         A     R9,AVTCSTCS        DCT ENTRY ADDRESS            @Y17XAUU
         USING IEDDCT,R9          SET DCT ADDRESSABILITY       @Y17XAUU
         TM    DCTBYTE1,DCT3270   SNA 3270 LU                  @Y17XAUU
         BO    CDSPRES            YES                          @Y17XAUU
*        BUILD CDS RECORD FOR 3767 OR 3770                     @Y17XAUU
         SLR   R3,R3              CLEAR REG                    @Y17XAUU
         IC    R3,GETSZE          GET BUFFER SIZE              @Y17XAUU
         L     R5,GETBUF          GET BUFFER ADDRESS           @Y17XAUU
         LA    R7,TCDSSNA         GET CDS BYTE COUNT           @Y17XAUU
         LA    R15,0              SET GOOD RETURN CODE         @Y17XAUU
         LA    R8,SNATCDS         GET FIXED LU CDS FORMAT      @Y17XAUU
         CR    R3,R7              BUFFER LARGE ENOUGH          @Y17XAUU
         BNL   GCSNAMV            YES                          @XM05677
         LA    R15,8              BUFFER FULL,SOME DATA MISS   @Y17XAUU
GCSNAMV  DS    0H                 BOUNDARY ALLIGNMENT          @Y17XAUU
         BCTR  R3,0               ADJUST BYTES COUNT           @Y17XAUU
         EX    R3,MOVECDS         MOVE CDS TO BUFFER           @Y17XAUU
         USING IEDTCDSD,R5        SET CDS ADDRESSABILTITY      @Y17XAUU
         MVC   TCDSNAM(D8),READNAME SET SYMBOLIC NAME          @Y17XAUU
         SLR   R1,R1              CLEAR REG                    @Y17XAUU
         ICM   R1,3,SAVEUCB       GET UCB ADDRESS              @Y17XAUU
         USING UCB,R1             SET UCB ADDRESSABILITY       @Y17XAUU
         MVC   TCDSUCB+D2(D2),UCBCHA  SET UCB BINARY CUU ADDR  @Y17XAUU
         DROP  R1                                              @Y17XAUU
         B     GC085              EXIT                         @Y17XAUU
         SPACE
GCLINK   DS    0H                 BOUNDARY ALLIGNMENT          @Y17XAUU
         TM    TOTTDTBL,TOTTDLIN  3705 LINE TEST               @Y17XAUU
         BZ    GC025              NO                           @Y17XAUU
*        CONSTRUCT 3705 LINE CDS                               @Y17XAUU
         SLR   R3,R3              CLEAR REG                    @Y17XAUU
         IC    R3,GETSZE          GET BUFFER SIZE              @Y17XAUU
         L     R5,GETBUF          GET BUFFER ADDRESS           @Y17XAUU
         LA    R7,TCDSLINK        GET CDS BYTE COUNT           @Y17XAUU
         LA    R15,0              SET GOOD RETURN CODE         @Y17XAUU
         LA    R8,SNALCDS         GET FIXED LINE CDS FORMAT    @Y17XAUU
         CR    R3,R7              BUFFER LARGE ENOUGH          @Y17XAUU
         BNL   GCLNKMV            YES                          @XM05677
         LA    R15,8              BUFFER FULL,SOME DATA MISS   @Y17XAUU
GCLNKMV  DS    0H                 BOUNDARY ALLIGNMENT          @Y17XAUU
         BCTR  R3,0               ADJUST BYTE COUNT            @Y17XAUU
         EX    R3,MOVECDS         MOVE CDS TO BUFFER           @Y17XAUU
         MVC   TCDSNAM(D8),READNAME SET SYMBOLIC NAME          @Y17XAUU
         SLR   R1,R1              CLEAR REG                    @Y17XAUU
         ICM   R1,3,SAVEUCB       GET UCB ADDRESS              @Y17XAUU
         USING UCB,R1             SET UCB ADDRESSABILITY       @Y17XAUU
         MVC   TCDSUCB+D2(D2),UCBCHA SET UCB BINARY CUU ADDR   @Y17XAUU
         DROP  R1                                              @Y17XAUU
         MVC   TCDSDAF(D2),NETWRKA SET NETWORK ADDR IN CDS     @Y17XAUU
         B     GC085              EXIT                         @Y17XAUU
         SPACE
CDSPRES  DS    0H                 BOUNDARY ALLIGNMENT          @Y17XAUU
         LA    R15,RETDAF         SET RETURN CODE FOR CDS NOT  @Y17XAUU
*                                 FOUND AND NETWORK ADDR RETURN@Y17XAUU
         SLR   R0,R0              CLEAR REG                    @Y17XAUU
         ICM   R0,3,NETWRKA       SET UP NETWORK ADDRESS       @Y17XAUU
         B     GC085              EXIT
         SPACE
GC025    EQU   *
         DROP  R8                                              @Y17XAUU
         LA    R15,12             MODULE NOT FOUND
         B     GC085              RETURN
         SPACE
GC030    EQU   *
         LOAD  DE=READNAME        LOAD CDS RECORD
         SPACE
         L     R5,GETBUF          GET BUFFER ADDRESS
         SLR   R3,R3              CLEAR REG                    @Y17XAUU
         IC    R3,GETSZE          GET BUFFER SIZE              @Y17XAUU
         LR    R8,R0              GET ENTRY POINT ADDRESS
         SR    R7,R7              CLEAR REG 7
         IC    R7,8(R8)           GET CDS RECORD COUNT
*
         CR    R3,R7              BUFFER LARGE ENOUGH?
         BNH   GC040              YES                            S22024
*
         LA    R15,8              BUFFER FULL, SOME DATA MISSING
         B     GC050                   BR AROUND GOOD RTN CODE
         SPACE
GC040    EQU   *
         LA    R15,0              GOOD RETURN CODE
*
GC050    EQU   *
         BCTR  R3,0               DECREMENT COUNT FOR MOVE
         EX    R3,MOVECDS              MOVE CDS TO OLT BUFF
         DELETE DE=READNAME       DELETE CDS RECORD
         B     GC085              RETURN
*
MOVECDS  MVC   D0(D0,R5),D0(R8)   CDS TO OLT                     S22024
*
         SPACE 2
GC085    EQU   *
         ST    R0,TOTWKSPC        SAVE NETWORK ADDRESS IF      @Y17XAUU
*                                 RETURN CODE IS X'10'         @Y17XAUU
         L     R13,4(R13)         RESTORE CALLER'S SAVE REG
         L     R14,12(R13)        GET RETURN ADDR
         STC   R15,TOTRTCOD       SAVE RETURN CODE
         TM    $OLTFLGS,$TRACE    TRACE FUNCTION REQUESTED ?
         BO    GC095              YES
         CLI   TOTRTCOD,0         RETURN CODE OF 0 ?
         BE    GC090              YES - RETURN
         CLI   $RETMASK,0         ANY TERMINATE FLAGS SET ?
         BNE   GC095              YES
GC090    EQU   *
         LM    R2,R12,DISREG2(R13) RESTORE REGISTERS           @Y17XAUU
         BR    R14                RETURN
         SPACE
GC095    EQU   *
         USING RESPL,R5           SET RESPL ADDRESSABILITY     @Y17XAUU
         L     R5,TOTRESPL        GET RESPL ADDRESS            @Y17XAUU
         L     15,RESPLM2         GET TRACE ROUTINE ADDRESS    @Y17XAUU
         LM    R2,R12,DISREG2(R13) RESTORE CALLER'S REG        @Y17XAUU
         BR    R15                EXIT TO IEDQWM2              @Y17XAUU
         DROP  R5                                              @Y17XAUU
         SPACE 4
CDSADRNZ EQU   *                       CDS ADDRESS NOT ZERO      S22024
         USING CDSADR,R5               CDS ADDRESS INFO          S22024
         TM    CDSFLG,SNPRES+CASNPRES  SYSMBOLIC NAME IN EBCDIC  S22024
         BNZ   SYMNMPRS           YES
         MVO   WORKA(D2),CUUADDR+D2(D2)  CONVERT TO EBCDIC       S22024
         UNPK  READNAME+D4(D4),WORKA(D2)   FOR BLDL              S22024
         TR    READNAME+D5(D3),TRTAB-D240  RESULT IS EBCDIC NAME S22024
         MVC   READNAME(D4),PREFIX     TACK ON PREFIX            S22024
         B     GC0166                  GET BUFFER SIZE           S22024
SYMNMPRS EQU   *                       SYMBOLIC NAME REQUEST     S22024
         MVC   READNAME(D8),CONFGNM    NAME TO BLDL LIST         S22024
         MVC   NETWRKA(D2),TOTSRSID    SAVE SEC TD NETWORK ADDR@Y17XAUU
         MVC   SAVEUCB(D2),TOTSCUCB    SAVE SEC TD UCB ADDRESS @Y17XAUU
         B     GC0166                  GET BUFFER SIZE           S22024
GC0166   EQU   *                       GET BUFFER SIZE REQUESTED S22024
         SR    R3,R3                   CLEAR REG                 S22024
         IC    R3,D12(R1)              LENGTH OF DATA REQUESTED  S22024
         B     GC016                   GO ISSUE BLDL             S22024
         DROP  R5                                                S22024
         EJECT
         SPACE 2
         SPACE 2
START    DS    0F                 *** START OF DATA TO BE MOVE @Y17XAUU
         DS    0F                      BLDL P-LIST
         DC    XL2'0001'          NUMBER OF LIST ENTRIES
         DC    XL2'003A'          LENGTH OF EACH ENTRY
         DC    8XL1'40'           MODULE ID
         DC    XL16'0'            ADDITIONAL DIRECTORY INFO
         DC    XL3'0'             MAIN STORAGE NEEDED FOR MODULE
         DC    XL31'0'            ADDITIONAL DIRECTORY INFO
         DC    X'000F'            USED FOR CONVERT             @Y17XAUU
END      DS    0F                 *** END OF DATA TO BE MOVED  @Y17XAUU
TRTAB    DC    C'0123456789ABCDEF'     TRANSLATE TABLE           S22024
PREFIX   DC    C'G'                    PREFIX FOR                S22024
         DC    X'F0F0F0F0'             UCB NAME                  S22024
         SPACE 4
RETCODE4 DC    H'04'              RETURN CODE FROM BLDL IF       S22024
SNATCDS  DC    XL4'0',XL2'0',XL4'4420180A',XL2'0',CL8' ',XL4'0'
*                                 BASIC 3767/3767 CDS          @Y17XAUU
SNALCDS  DC    XL4'0',XL2'0',XL4'4420240A',XL2'0',CL8' ',X'10',XL3'0',X*
               L4'0',X'E0',XL2'0',XL5'0'
*                                 BASIC 3705 LINE CDS          @Y17XAUU
         EJECT
PLIST    DSECT
GETFLG   DS    AL1                CONTROL FLAGS
GETMLV   DS    AL1                MACRO LEVEL
GETMID   DS    CL2                MACRO ID
GETSEC   DS    A                  ADDR OF BASIC CDS INFO
GETBUF   DS    A                  CDS BUFFER ADDR
GETSZE   DS    AL1                SIZE OF CDS BUFFER
CDSADR   DSECT                         CDS ADDRESS INFO          S22024
CUUADDR  DS    A                       UNIT ADDRESS              S22024
         DS    CL5                     UNUSED                    S22024
CDSFLG   DS    AL1                     FLAG                      S22024
SNPRES   EQU   X'08'                   SYSBOLIC NAME PRESENT     S22024
CASNPRES EQU   X'02'                   CUSTOMER ASSIGN NAME      S22024
         DS    AL2                     UNUSED                    S22024
CONFGNM  DS    CL8                     SYMBOLIC NAME             S22024
         EJECT
         TAVTD                                                 @Y17XAUU
         EJECT
         TDCTD                                                 @Y17XAUU
         EJECT
         TTRMD                                                 @Y17XAUU
         EJECT
         DCBD  DSORG=PS
         EJECT
RESPL    RESPL                                                 @Y17XAUU
         EJECT
         OLTCB                                                   S22024
BLDLLIST DS    0F                 BLDL P-LIST                  @Y17XAUU
         DC    XL2'0001'          NUMBER OF LIST ENTRIES       @Y17XAUU
         DC    XL2'003A'          LENGTH OF EACH ENTRY         @Y17XAUU
READNAME DC    8XL1'40'           MODULE ID                    @Y17XAUU
         DC    XL16'0'            ADDITIONAL DIRECTORY INFO    @Y17XAUU
CORENEED DC    XL3'0'             MAIN STORAGE NEEDED          @Y17XAUU
         DC    XL31'0'            ADDITIONAL DIRECTORY INFO    @Y17XAUU
WORKA    DC    X'000F'            USED FOR CONVERSION          @Y17XAUU
SAVEUCB  DS    H                  SAVE AREA                    @Y17XAUU
NETWRKA  DS    H                  SAVE AREA                    @Y17XAUU
         EJECT
         TTCDSD                                                @Y17XAUU
         EJECT
UCB      DSECT                                                 @Y17XAUU
         IEFUCBOB LIST=YES                                     @Y17XAUU
         END
