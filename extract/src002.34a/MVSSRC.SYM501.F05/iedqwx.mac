QWX      TITLE 'IEDQWX - TOTE CONVERT SERVICE MODULE'
IEDQWX   CSECT
*A000000-999999                                                @Y17X5U0
* CHANGE ACTIVITY AS FOLLOWS:                                    S22024
IEDQW41  EQU   *
         ENTRY IEDQW41
*C280000,336000                                                  A41583
*A265300,298000,839000,861100,863000                           @Y17XAUU
**************************************************************** S99528
*                                                              * S99528
* MODULE NAME = IEDQWX (TCAM,TOTE)                             @Y17XAUU
*                                                              * S22024
* DESCRIPTIVE NAME = TOTE CONVERT MODULE                       * S22024
*                                                              * S22024
* COPYRIGHT = NONE                                             * S22024
*                                                              * S22024
*    STATUS --                                                 * S99528
*                                                              * S99528
*    VERSION 10.0                                              @Y17XAUU
*                                                              * S99528
* FUNCTION --                                                  * S99528
*                                                              * S99528
*    THIS ROUTINE CONVERTS DATA FROM HEXADECIMAL TO EBCDIC OR  * S99528
*    EBCDIC TO HEXADECIMAL AS SPECIFIED BY THE MACRO PARAMETER * S99528
*    LIST.                                                     * S99528
*                                                              * S99528
*    IF A NON-EBCDIC CHARACTER IS REQUESTED TO BE CONVERTED    * S99528
*    TO HEX, AN ERROR RETURN IS PROVIDED AND THE ADDRESS OF    * S99528
*    THE NON-EBCDIC CHARACTER IS PLACED IN REGISTER 1.         * S99528
*                                                              * S99528
*    AT THE COMPLETION OF THIS ROUTINE, ONE OF THE FOLLOWING   * S99528
*    RETURN CODES IS PLACED IN REGISTER 15:                    * S99528
*                                                              * S99528
*         X'00' - NORMAL COMPLETION OF THE CONVERT FUNCTION    * S99528
*         X'08' - A NON-EBCDIC CHARACTER WAS REQUESTED TO BE   * S99528
*                 CONVERTED TO HEX.                            * S99528
*                                                              * S99528
* PURPOSE -- SEE FUNCTION                                      @Y17XAUU
*                                                              @Y17XAUU
* ENTRY POINT --                                               * S99528
*                                                              * S99528
*    IEDQWX - CALLED WHEN A CONVERT MACRO IS ISSUED BY         * S99528
*             TOTE OR THE UNIT TEST.                           * S99528
*                                                              * S99528
* INPUT --                                                     * S99528
*                                                              * S99528
*    REGISTER 01,02,13,14,15 CONTAIN THE FOLLOWING VALUES:     * S99528
*                                                              * S99528
*         01 - CONVERT PARAMETER LIST ADDRESS                  * S99528
*         02 - OLTCB ADDRESS                                   * S99528
*         13 - SAVE AREA ADDRESS                               * S99528
*         14 - RETURN ADDRESS                                  * S99528
*         15 - ENTRY POINT ADDRESS                             * S99528
*                                                              * S99528
* OUPUT --                                                     * S99528
*                                                              * S99528
*    REGISTERS 01,15 CONTAIN THE FOLLOWING VALUES:             * S99528
*                                                              * S99528
*         01 - ADDRESS OF NON-EBCDIC CHARACTER                 * S99528
*         15 - RETURN CODE:                                    * S99528
*              X'00' - NORMAL COMPLETION                       * S99528
*              X'08' - REQUESTED CONVERSION OF NON-EBCDIC      * S99528
*                      CHARACTER                               * S99528
*                                                              @Y17XAUU
* MODULE TYPE -- PROCEDURE                                     @Y17XAUU
*                                                              @Y17XAUU
* PROCESSOR -- ASSEMBLER XF                                    @Y17XAUU
*                                                              @Y17XAUU
* MODULE SIZE -- 2K MAXIMUM                                    @Y17XAUU
*                                                              @Y17XAUU
* MACROS -- IEDHJN,SAVE                                        @Y17XAUU
*                                                              @Y17XAUU
* LINKAGE -- BALR 14,15 FROM IEDQWA                            @Y17XAUU
*                                                              * S99528
* EXTERNAL REFERENCES --                                       * S99528
*                                                              * S99528
*    BR   TO IEDQWM2 IF FACILITY NOT AVAILABLE                 @Y17XAUU
*                                                              * S99528
* EXITS,NORMAL --                                              * S99528
*                                                              * S99528
*    BR AFTER CNV018 TO IEDQWA                                 * S99528
*                                                              * S99528
* EXITS,ERROR --                                               * S99528
*                                                              * S99528
*    BR   TO IEDQWM2 IF FACILITY NOT AVAILABLE                 * S99528
*    BR   TO IEDQWM2 AFTER CNV035                              * S99528
*                                                              * S99528
* TABLES/WORK AREAS --                                         * S99528
*                                                              * S99528
*    NONE                                                      * S99528
*                                                              * S99528
* ATTRIBUTES --                                                * S99528
*                                                              * S99528
*    ENABLED, PROBLEM PROGRAM MODE, RESIDENT, REENTRANT        @Y17XAUU
*                                                              * S99528
* NOTES -- SEE BELOW                                           @Y17XAUU
*                                                              @Y17XAUU
*  DEPENDENCIES -- EBCDIC CHARACTER CODE                       @Y17XAUU
*                                                              @Y17XAUU
*  REGISTERS CONVENTION -- SEE REGISTERS ASSIGNMENT            @Y17XAUU
*                                                              @Y17XAUU
*  RESTRICTIONS -- NONE                                        @Y17XAUU
*                                                              @Y17XAUU
*  PATCH LABEL -- PATCH                                        @Y17XAUU
*                                                              * S99528
*    NONE                                                      * S99528
*                                                              * S99528
**************************************************************** S99528
         EJECT
         SPACE 2
*
*        EQUATES
*
R0       EQU   0                  REGISTER 0                     S99528
R1       EQU   1                  PARAMETER LIST ADDRESS         S99528
R2       EQU   2                  OLTCB ADDRESS                  S99528
R3       EQU   3                  REGISTER 3                     S99528
R4       EQU   4                  BASE REGISTER                  S99528
R5       EQU   5                  REGISTER 5                     S99528
R6       EQU   6                  REGISTER 6                     S99528
R7       EQU   7                  REGISTER 7                     S99528
R8       EQU   8                  REGISTER 8                     S99528
R9       EQU   9                  REGISTER 9                     S99528
R10      EQU   10                 REGISTER 10                    S99528
R11      EQU   11                 REGISTER 11                    S99528
R12      EQU   12                 REGISTER 12                    S99528
R13      EQU   13                 SAVE AREA ADDRESS              S99528
R14      EQU   14                 RETURN ADDRESS                 S99528
R15      EQU   15                 ENTRY POINT ADDRESS            S99528
DISREG2  EQU   28                 DISPLACEMENT TO REG 2 IN SAVE@Y17XAUU
         USING IEDQWX,R15
         SPACE
IEDQWX   IEDHJN STARX,HJN
         SPACE
         DROP  R15
         USING IEDQWX,R4
         USING TOTOLTCB,R2        OLTCB BASE                     S22024
         SPACE
         SAVE  (14,12)            SAVE CALLER'S REGISTERS
         LR    R4,R15             LOAD BASE REG.
         LA    R15,TOTSAVE1       GET ADDR OF MY SAVE AREA
         ST    R15,8(R13)         PUT IN CALLER'S SAVE AREA
         ST    R13,4(R15)         PUT CALLER'S IN MY SAVE AREA
         LR    R13,R15            SET REG 13 TO MY SAVE AREA
         SR    R15,R15            CLEAR RETURN CODE REG.
         SPACE
         CLI   1(R1),X'02'        LEVEL 2 MACRO ID?
         BE    CNV000             YES
         SPACE
         SPACE
         LA    R15,4              SET FACILITY NOT AVAILABLE RET CODE
         STC   R15,TOTRTCOD       SAVE IT
         L     R14,12(R13)        RESTORE RETURN ADDRESS
         USING RESPL,R5           SET RESPL ADDRESSABILITY     @Y17XAUU
         L     R5,TOTRESPL        GET RESPL ADDRESS            @Y17XAUU
         L     R15,RESPLM2        GET ADDRESS OF TRACE ROUTINE @Y17XAUU
         LM    R2,R12,DISREG2(R13) RESTORE CALLER'S REGISTERS  @Y17XAUU
         BR    R15                GO TO IEDQWM2                @Y17XAUU
         DROP  R5                                              @Y17XAUU
         SPACE
         SPACE
CNV000   EQU   *
         SPACE
         L     R5,4(R1)           GET INPUT ADDR.
         L     R6,8(R1)           GET OUTPUT ADDR.
         L     R7,12(R1)          GET FLAGS AND COUNT
         SLL   R7,1               CLEAR
         SRL   R7,17                    FLAG BIT
         SPACE
         LTR   R7,R7              IS A COUNT GIVEN
         BNZ   CNV001             YES
         LA    R15,0              SET RETURN CODE
         B     CNV018             RETURN
CNV001   EQU   *
         SPACE
         TM    12(R1),X'80'       HEX TO EBCDIC?
         BO    CNV014             YES
         SPACE 2
***********************************************************************
*        EBCDIC TO HEX CONVERT                                        *
***********************************************************************
         SPACE
         SR    R8,R8              CLEAR R8.
         SPACE
CNV002   SR    R10,R10            CLEAR 2ND BYTE SWITCH.
         SR    R9,R9              CLEAR R9.
         SPACE
CNV004   IC    R9,0(R5)           GET EBCDIC CHAR.
         LA    R11,16             SET INDEX FOR SEARCH.
         SPACE
CNV006   IC    R8,CNVEBC-1(R11)   GET CHAR FROM TABLE.
         CLR   R8,R9              IS THIS CHAR THE HEX EQUIVALENT ?
         BE    CNV008             BRANCH IF YES.
         BCT   R11,CNV006         TEST IF NEXT CHAR MATCHES.
         SPACE
         LR    R15,R5             BAD CHAR - GET ADDR.
         L     R13,4(R13)         RESTORE SAVE AREA ADDR
         L     R14,12(R13)        RESTORE REG 14
         LR    R1,R15             PUT ADDR. OF FAIL CHAR IN R1.
         LA    R15,8              SET RETURN CODE = 08.
         STC   R15,TOTRTCOD       SAVE RETURN CODE
         TM    $OLTFLGS,$TRACE    TRACE REQUESTED ?
         BO    CNV035             YES
         CLI   TOTRTCOD,0         RETURN CODE 0 ?
         BE    CNV006D            YES
         CLI   $RETMASK,0         ANY TERMINATE FLAGS SET ?
         BNE   CNV035             YES
CNV006D  EQU   *
         LM    R2,R12,28(R13)     RESTORE REG
         BR    R14                RETURN
         SPACE
CNV008   LA    R5,1(R5)           GET ADDR. OF NEXT CHAR.
         IC    R9,CNVHEX-1(R11)   GET EQUIV. HEX CHAR.
         SPACE
CNV010   LTR   R10,R10            IS THIS THE 2ND BYTE ?
         BNZ   CNV012             BRANCH IF YES.
         SPACE
         LR    R12,R9             PUT CHAR IN R12.
         SR    R9,R9              CLEAR R9.
         LA    R10,1              SET 2ND BYTE FLAG.
         SLL   R12,4              CLEAR BITS 4-7.
         BCT   R7,CNV004          GET BITS 4-7.
         SPACE
         LA    R7,1               SET R7 FOR NEXT BCT.
         SPACE
CNV012   OR    R12,R9             COMBINE 2 HEX DIGITS INTO 1 BYTE.
         STC   R12,0(R6)          STORE BYTE IN OUTPUT FIELD.
         LA    R6,1(R6)           INCREMENT OUTPUT ADDR.
         BCT   R7,CNV002          GET NEXT EBCDIC CHAR.
         B     CNV018             CONVERT COMPLETE - RETURN.
         SPACE 2
***********************************************************************
*        HEX TO EBCDIC CONVERT                                        *
***********************************************************************
         SPACE
CNV014   AR    R6,R7              ADD COUNT TO OUTPUT ADDR.
         AR    R6,R7              ADD COUNT AGAIN.
         AR    R5,R7              ADD COUNT TO INPUT ADDR.
         SR    R11,R11            CLEAR R11.
         SPACE
CNV016   S     R6,CON2            GET ADDR OF LAST BYTE OF OUTP@Y17XAUU
         BCTR  R5,0               GET ADDR OF LAST BYTE OF INPUT.
         IC    R11,0(R5)          GET HEX BYTE.
         LR    R9,R11             PUT IN R9.
         SRL   R11,4              GET BITS 0-3 IN R11.
         N     R9,CON15           GET BITS 4-7 IN R9.          @Y17XAUU
         IC    R11,CNVEBC(R11)    CONVERT BITS 0-3 TO EBCDIC CHAR.
         IC    R9,CNVEBC(R9)      CONVERT BITS 4-7 TO EBCDIC CHAR.
         STC   R11,0(R6)          STORE 1ST EBCDIC CHAR IN OUTPUT.
         STC   R9,1(R6)           STORE 2ND EBCDIC CHAR IN OUTPUT.
         BCT   R7,CNV016          GET NEXT HEX BYTE.
         SPACE 2
CNV018   EQU   *
         L     R13,4(R13)         RESTORE CALLER'S SAVE AREA ADDR
         L     R14,12(R13)        RESTORE RETURN ADDR
         SPACE
CNV025   EQU   *
         STC   R15,TOTRTCOD       SAVE RETURN CODE
         TM    $OLTFLGS,$TRACE    TRACE FTN REQUESTED ?
         BO    CNV035             YES
         CLI   TOTRTCOD,0         RETURN CODE OF 0
         BE    CNV030             YES
         CLI   $RETMASK,0         ANY TERMINATE FLAGS SET ?
         BNE   CNV035             YES
CNV030   EQU   *
         LM    R0,R12,20(R13)     RESTORE CALLER'S REGISTERS
         BR    R14                RETURN
         SPACE
CNV035   EQU   *
         USING RESPL,R5           SET RESPL ADDRESSABILITY     @Y17XAUU
         L     R5,TOTRESPL        GET RESPL ADDRESS            @Y17XAUU
         L     R15,RESPLM2        GET TRACE ROUTINE ADDRESS    @Y17XAUU
         LM    R2,R12,DISREG2(R13) RESTORE CALLER'S REGISTERS  @Y17XAUU
         BR    R15                GO TO IEDQWM2                @Y17XAUU
         DROP  R5                                              @Y17XAUU
         SPACE 2
CNVHEX   DC    X'000102030405060708090A0B0C0D0E0F'  TRANSLATE    S99528
*                                                   TABLE FOR    S99528
*                                                   EBCDIC TO HEXS99528
CNVEBC   DC    C'0123456789ABCDEF'  TRANSLATE TABLE FOR HEX TO   S99528
*                                     EBCDIC                     S99528
         SPACE
         DS    0F                 BOUNDARY ALLIGNMENT            S99528
CON2     DC    F'2'               CONSTANT VALUE OF 2          @Y17XAUU
CON15    DC    F'15'              CONSTANT VALUE OF 15         @Y17XAUU
PATCH    DC    10F'0'             PATCH AREA                   @Y17XAUU
         EJECT
         OLTCB                                                   S22024
         EJECT
RESPL    RESPL                                                 @Y17XAUU
         END
