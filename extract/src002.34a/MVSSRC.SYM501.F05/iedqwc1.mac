         TITLE 'IEDQWC1 TRM ANALYSIS MODULE 2'
IEDQWC1  CSECT
*A000000-999999                                                @Y16X5U0
* CHANGE ACTIVITY AS FOLLOWS:
*D129000,556400-556600                                           S21903
*A552500                                                        SA41597
*A158000                                                        SA41614
*C000000,999999                                                  S22024
*C264000,357600-373600,397600,403200,408800-412000             @YA12402
*D272800-273600,278400-280000,283200,319200-350400,405600      @YA12402
*D416000                                                       @YA12402
*A297600,356000,400800,401600,424000                           @YA12402
*C802400-806400                                                @OY12987
*D812900-813300                                                @OY12987
*D502000                                                       @OY12987
*A191200,466400,472000,500000,532800,622400,688800,801600      @Y17XAUU
*A803200,832800                                                @Y17XAUU
*C812900,813600,824000                                         @Y17XAUU
* A006274,139200,424330,424570,424450,880000                   @G36XRUV
* C424360                                                      @G36XRUV
* D424600-424750,424420,424480                                 @G36XRUV
*
****************************************************************
*                                                              *
* MODULE NAME = IEDQWC1 (TCAM,TOTE)                            @Y17XAUU
*                                                              *
* DESCRIPTIVE NAME = TRM ANALYSIS MODULE 2                     *
*                                                              *
* COPYRIGHT = NONE                                             *
*                                                              *
* STATUS --                                                    *
*                                                              *
*    VERSION 10.0                                              @Y17XAUU
*                                                              *
*                                                              *
* FUNCTIONS --                                                 *
*                                                              *
*    THE PURPOSE OF THIS MODULE IS TO ANALYZE THE TEST DEVICE  *
*    FIELD OF THE TEST REQUEST MESSAGE (TRM).                  *
*                                                              *
*    CONTROL IS PASSED FROM IEDQWC WITH TRM IN THE OLTCB.      *
*    THE TEST DEVICE FIELD IS CHECKED FOR VALID SYMBOLIC       *
*    NAMES OR LINE ADDRESSES (CUU).                            *
*                                                              *
*    IF THE ENTRY IS VALID THE UCB OR TNT OF THE ENTRY IS      *
*    SAVED IN THE OLTCB. IF ANY ENTRY IS INVALID, THE TRM      *
*    PROMPTER (IEDQWJ) IS CALLED.                              *
*                                                              *
* PURPOSE -- SEE FUNCTION                                      @Y17XAUU
*                                                              *
* ENTRY POINTS --                                              *
*                                                              *
*    IEDQWC1                                                   @Y17XAUU
*                                                              *
* LINKAGE -- CALLED BY IEDQWB1 AND IEDQWH TO PROCESS           @Y17XAUU
*             TEST DEVICE FIELD OF TRM.                        @Y17XAUU
*                                                              @Y17XAUU
* MODULE SIZE -- MAXIMUM 4K                                    @Y17XAUU
*                                                              @Y17XAUU
* MODULE TYPE -- PROCEDURE                                     @Y17XAUU
*                                                              @Y17XAUU
* PROCESSOR -- ASSEMBLER XF                                    @Y17XAUU
*                                                              @Y17XAUU
* MACROS -- AQCTL,IEDHJN,IEDQMSG,LINK,SAVE,WAIT,WTO,XCTL       @Y17XAUU
*                                                              *
* INPUT --                                                     *
*                                                              *
*    REGISTERS 02,13,14,15 CONTAINS THE FOLLOWING VALUES:      *
*                                                              *
*    02 - OLTCB POINTER                                        *
*    13 - SAVE AREA ADDRESS                                    *
*    14 - RETURN ADDRESS                                       *
*    15 - ENTRY POINT ADDRESS                                  *
*                                                              *
*                                                              *
* OUTPUT --                                                    *
*                                                              *
*    SAME AS INPUT                                             @Y17XAUU
*                                                              *
*                                                              *
* EXTERNAL REFERENCES --                                       *
*                                                              *
*    IEDQWK - MESSAGE MODULE                                   *
*    IEDQCA - OPERATOR CONTROL STOPLINE ROUTINE                *
*    AVTUI  - TCAM BINARY SEARCH ROUTINE                       *
*                                                              *
*                                                              *
* EXITS,NORMAL --                                              *
*                                                              *
*    XCTL TO IEDQWC2                                           *
*                                                              *
*                                                              *
* EXITS,ERROR --                                               *
*                                                              *
*    XCTL TO IEDQWJ                                            *
*                                                              *
*                                                              *
* TABLES/WORK AREAS --                                         *
*                                                              *
*    CVT, AVT, TERMNAME TABLE, TERMINAL TABLE, OLTCB,          *
*    LCB, QCB, DCB, SCB, TCB                                   *
*                                                              *
*                                                              *
* ATTRIBUTES --                                                *
*                                                              *
*    ENABLED, PROBLEM PROGRAM MODE, TRANSIENT                  *
*                                                              *
*                                                              *
* NOTES -- SEE BELOW                                           @Y17XAUU
*                                                              *
*  DEPENDENCIES -- EBCDIC CODE CHARACTER SET                   @Y17XAUU
*                                                              @Y17XAUU
*  RESTRICTIONS -- NONE                                        @Y17XAUU
*                                                              @Y17XAUU
*  REGISTER CONVENTIONS -- SEE REGISTERS ASSIGNMENT            @Y17XAUU
*                                                              @Y17XAUU
*  PATCH LABEL -- PATCH                                        @Y17XAUU
*                                                              *
****************************************************************
         EJECT
*
*        EQUATES
*
R0       EQU   0                  REG 0
R1       EQU   1                  REG 1
R2       EQU   2                  REG 2
R3       EQU   3                  REG 3
R4       EQU   4                  REG 4
R5       EQU   5                  REG 5
R6       EQU   6                  REG 6
R7       EQU   7                  REG 7
R8       EQU   8                  REG 8
R9       EQU   9                  REG 9
R10      EQU   10                 REG 10
R11      EQU   11                 REG 11
R12      EQU   12                 REG 12
R13      EQU   13                 REG 13
R14      EQU   14                 REG 14
R15      EQU   15                 REG 15
         SPACE
INACT    EQU   X'48'              INACTIVATE VERB CODE           S22024
INACTRC  EQU   X'08'              TERM ALREADY INACTIVE RC       S22024
D19      EQU   25                 DISP OF 25                     S22024
DCBNIDLE EQU   X'02'              3705 ACTIVE                    S22024
         SPACE
         SPACE
SPACE    EQU   X'40'              A BLANK CHAR
TWO      EQU   2                  DISP OF TWO
FOUR     EQU   4                  DISP OF FOUR
PARMREG0 EQU   0                  PARMETER REG 0
PARMREG1 EQU   1                  PARMETER REG 1
ZERO     EQU   0                  VALUE OF ZERO
ONE      EQU   1                  VALUE OF ONE
EIGHT    EQU   8                  VALUE OF EIGHT
TWELVE   EQU   12                 VALUE OF TWELVE
SLASH    EQU   X'61'              A SLASH CHAR
COMMA    EQU   X'6B'              A COMMA CHAR
SWITCHS  EQU   X'90'              SWITCHED START/STOP
SWITCHB  EQU   X'96'              SWITCHED BI-SYNC
MORE     EQU   X'80'              MORE ENTRIES FLAG
NCMOFF   EQU   X'F7'              TURN OFF
END      EQU   X'7F'              NO MORE ENTRIES FLAG
SYM      EQU   X'40'              SYMBOLIC TEST DEVICE FLAG
NOSYM    EQU   X'BF'              TURN OFF SYMBOLIC FLAG
INCLUS   EQU   X'20'              INCLUSIVE TD FLAG
NOINC    EQU   X'DF'              TURN OFF INCLUSIVE FLAG
ADDONE   EQU   X'10'              ADD ONE FLAG
ADDOFF   EQU   X'EF'              TURN OFF ADD ONE FLAG
ALLOFF   EQU   X'00'              MASK OFF ALL BIT
NCM      EQU   X'08'              NON-CONCURRENT MORE FLAG
ADRMSK   EQU   7                  ADDRESS MASK                 @YM08136
LGBA     EQU   X'80'              LGB MAP                        S22024
RQINIT   EQU   X'0C'              START OF LIST                  S22024
RQEND    EQU   X'80'              END OF LIST                    S22024
D0       EQU   0                  DISP OF 0
D1       EQU   1                  DISP OF 1
D2       EQU   2                  DISP OF 2
D3       EQU   3                  DISP OF 3
D4       EQU   4                  DISP OF 4
D5       EQU   5                  DISP OF 5
D6       EQU   6                  DISP OF 6
D7       EQU   7                  DISP OF 7
D8       EQU   8                  DISP OF 8
D9       EQU   9                  DISP OF 9
D10      EQU   10                 DISP OF 10
D11      EQU   11                 DISP OF 11
D12      EQU   12                 DISP OF 12
D16      EQU   16                 DISP OF 16
D20      EQU   20                 DISP OF 20
D24      EQU   24                 DISP OF 24
D28      EQU   28                 DISP OF 28
D32      EQU   32                 DISP OF 32
ACTRTCD  EQU   X'08'              INV. LIST INIT ACTIVE
TERMACTV EQU   X'46'              OP CONTROL ACTIVATE VERB CODE
D40      EQU   40                 DISP OF 40
ACVT     EQU   16                 ADDR OF CVT ADDR
TRMLEN   EQU   72                 TRM BUFFER SIZE
FPAS     EQU   0                  1ST PASS FLAG
NUMER    EQU   X'F0'              NUMERIC FLAG
KF       EQU   X'C6'              CHAR F
F9       EQU   X'F9'              CHAR 9
F0       EQU   X'F0'              CHAR 0
XC0      EQU   X'C0'              COMPARAND
NEG      EQU   4                  NEGATIVE TEST
DASH#    EQU   X'60'              HYPHEN CHAR
D13      EQU   13                 DISP OF 13
X0F      EQU   X'0F'              MASK BYTE
XF0      EQU   X'F0'              MASK BYTE                    @G36XRUV
X09      EQU   X'09'              USED TO TEST FOR 3270 LOCAL  @G36XRUV
DCBOPEN  EQU   X'10'              DCB OPEN FLAG
TCAMDEB  EQU   X'44'              DEB BELONGS TO TCAM FLAG       S22024
TERM     EQU   X'80'              TERMINAL TEST DEV
XFF      EQU   X'FF'              MASK ALL BITS                  S22024
XE0      EQU   X'E0'              MASK SETTING                   S22024
X80      EQU   X'80'              MASK SETTING                   S22024
TCU3705  EQU   X'05'              3705 CHECK                     S22024
LINE     EQU   X'01'              LINE TEST                      S22024
RNTERM   EQU   X'08'              RN TERMINAL                    S22024
RNLINE   EQU   X'04'              RN LINE                        S22024
RNCHAN   EQU   X'02'              RN SUBCHAN                     S22024
*                                                                S22024
         USING IEDQWC1,R15                                       S22024
IEDQWC1  IEDHJN STARC1,HJN
         DROP  R15
         SPACE 2
         SAVE  (14,12)            SAVE CALLER'S REG
         SPACE
         LR    R6,R15                   LOAD BASE
         USING IEDQWC1,R6
         USING TOTOLTCB,R2
         LA    R3,TOTSAVE4        GET MY SAVE AREA ADDR
         ST    R3,D8(R13)         STORE IN CALLER'S SAVE AREA
         ST    R13,D4(R3)         SAVE CALLER'S
         LR    R13,R3                   SET R13 TO SAVE AREA
         XC    TOTTDTBL(D40),TOTTDTBL CLEAR TEST DEV TABLE
         L     R8,ACVT(R0)        CVT ADDR
         USING CVT,R8
         L     R9,AVTCVTPT(R8)            PICK UP DISPATCHER ADDR
         L     R7,CVTILK2              PICK UP 2 BYTE UCB TABLE
         ST    R7,UCBSAVE
         DROP  R8
         L     R8,D0(R9)          AVT ADDR
         USING IEDQAVTD,R8
         L     R7,AVTRNMPT             PICK UP ADDR OF TERMNAME TABLE
         ST    R7,TERNMTAB             SAVE IT
         USING IEDQTNTD,R7
         IC    R9,TNTENLEN             PICK UP LENGTH OF ENTRY NAME
         STC   R9,TERNMLEN             SAVE IT
         DROP  R7
         MVC   PAREMLST(D1),AVTBSX SET UP FOR BIN. SRCH
         L     R9,AVTMSGS-D1      VCON TABLE ADDR
         L     R9,D8(R9)          ADDR OF A1 ROUTINE
         ST    R9,AVTUISV
         LA    R9,AVTOPCOB             PICK UP ADDR OF OP CON QCB
         ST    R9,OCQCB                SAVE IT
         DROP R8
         SR    R5,R5              CLEAR REG 5
         LA    R5,TRMLEN          GET TRM BFR LENGTH
         BCTR  R5,R0              LENGTH MINUS ONE
         LA    R3,TOTTRMBF        POINT TO TRM BUFFER
         EX    R5,MOVE            MOVE TRM TO WORK AREA
         LA    R10,TRMWRK               FIND TEST DEVICE FIELD
         TM    TOTFLG04,TOTNUMDV  NUMERIC TRM ENTRY             SA41614
         BZ    FLDFIND            NO                            SA41614
         LA    R9,D9              SET LIMIT                     SA41614
         OI    TOTFLG09,TOTPRECT  SET CT EQU AP FLAG            SA41614
TRMRTNH  EQU   *                                                SA41614
         CLI   D0(R10),SLASH      CHAR A '/'                    SA41614
         BE    TRMRTNH6           YES                           SA41614
         LA    R10,D1(R10)        CHECK NEXT CHAR               SA41614
         BCT   R9,TRMRTNH         CHECK NEXT CHAR               SA41614
*        ERROR EXIT - NO ENDING SLASH                           SA41614
         WTO   'IED127I OLT REQUEST REJECTED, CONTROL TERMINAL UNIDENTI*
               FIED',ROUTCDE=10,DESC=7                          SA41614
         TM    TOTFLG05,TOTSRCON  TRM ENTERED FROM SYSCON      @Y17XAUU
         BO    GETOUT             YES                          @Y17XAUU
         IEDQMSG MSGID=077,FUNCT=SRC  SEND MSG TO SOURCE       @Y17XAUU
         B     GETOUT             EXIT TO IEDQWE                SA41614
TRMRTNH6 EQU   *                                                SA41614
         LA    R10,TRMWRK         ADDR OF TRM WORK AREA         SA41614
FLDFIND  EQU   *
         CLI   D0(R10),SLASH      END OF CT FIELD
         BE    TDFLD01                 YES BRANCH
         LA    R10,D1(R10)        NO BUMP POINTER
         B     FLDFIND            CHECK AGAIN
TDFLD01  EQU   *
         NI    TDFLG,XFF-RNTERM-RNLINE CLEAR INTERMEDIATE FLAG   S22024
         BAL   R9,TYPECHK         DETERMINE TYPE OF TEST         S22024
TDFLD    EQU   *
         LA    R10,D1(R10)        BUMP PASS DELIMITER
         TM    TDFLG,RNCHAN       RN SUBCHAN TEST                S22024
         BO    NUMDEV             YES                            S22024
         TM    TDFLG,RNTERM+RNLINE+SYM SYMBOLIC ENTRY            S22024
         BM    TDFLD1             YES                            S22024
         B     NUMDEV             NO - ASSUME LINE ENTRY         S22024
TDFLD1   EQU   *
         MVC   WORK1(D9),D0(R10)  FIND LEN OF TEST DEV ENTRY
         LA    R7,D9              LOOP COUNT
         LA    R4,D0              COUNTER
         LA    R3,WORK1
TDCHK    CLI   D0(R3),SLASH       END OF FIELD ?
         BE    TDOK                    YES BRANCH
         CLI   D0(R3),COMMA       END OF NAME ?
         BE    TDOK1                   YES BRANCH
         LA    R3,D1(R3)          BUMP POINTER
         LA    R4,D1(R4)          INCREMENT COUNTER
         BCT   R7,TDCHK                GO LOOK AGAIN
         B     TDERR                   ERROR, NAME TOO LONG
MOVE     MVC   TRMWRK(D0),D0(R3)  MOVE TRM TO WORK AREA
TDERR    EQU   *
         OI    TOTFLG03,TOTTDFER  SHOW ERROR IN TEST DEVICE FIELD
         L     R13,D4(R13)        CALLER'S SAVE AREA ADDR
         LM    R14,R1,D12(R13)    RESTORE CALLER'S REG
         XCTL  (2,12),EP=IEDQWJ
TDOK1    EQU   *
         OI    TDFLG,MORE              TURN ON MORE ENTRIES FLAG
         LA    R7,D1              INITIALIZE COUNTER
         A     R7,NUMBER
         ST    R7,NUMBER               ADD 1 TO NUMBER OF DEVICES
         B     TDOK5              CONTINUE PROCESSING
TDOK     EQU   *
         NI    TDFLG,END               TURN OFF MORE ENTRIES FLAG
TDOK5    EQU   *                       TURN ON SYMBOLIC FLAG
         LA    R7,D10             SET COMPARAND                  S22024
         C     R7,NUMBER               MORE THAN NINE DEVICES?
         BNL   TDOK2                   NO BRANCH
         B     TDTLON             TOOO MANY TD ENTRIES           S22024
TDOK2    EQU   *
         STC   R4,LENGTH
         LA    R9,D8              MAX NAME LENGTH
         CR    R9,R4                   ALREADY HAVE 8 CHARS?
         BE    TDOK22                  YES BRANCH
         SR    R9,R4                   GET NUMBER OF POSITIONS TO FILL
         LA    R1,WORK1                GET WORK AREA ADDRESS
         BCTR  R9,R0              DECREMENT FOR MOVE
         AR    R1,R4                   POINT PAST NAME
         EX    R9,BLMOVE          BLANK NAME WORK AREA
TDOK22   EQU   *
         ST    R10,REGSAVE              SAVE R2
         LR    R10,R4
         BAL   R9,NAMESRCH        VERIFY TEST TERMINAL NAME
         B     TDOK3                   BRANCH AROUND ERROR RETURN
         B     TDERR                   NAME NOT IN TABLE
BLMOVE   MVC   D0(D0,R1),BLANK    BLANK NAME WORK AREA
TDOK3    EQU   *
         L     R10,REGSAVE              RESTORE R2
TDOK3A   EQU   *
         TM    TDFLG,RNTERM+RNLINE+SYM NEED UCB                  S22024
         BZ    TDOK6              NO - BRANCH                    S22024
         BAL   R9,UCBFIND         FIND CUU FOR DEVICE
         USING UCB,R8
         CLI   UCBTBYT3,UCB3COMM       IS TERMINAL COMMUNICATION DEV?
         BNE   TRM1094                   NO
         TM    UCBTBYT2,SWITCHS   SWITCHED/START STOP
         BNZ   TRM1092                   YES
         MVN   WORKAREA(1),UCBTBYT1    IS TERMINAL
         MVZ   WORKAREA(D1),UCBTBYT4 SWITCHED TD
         XI    WORKAREA,SWITCHB   BI-SYNC
         BNZ   TRM1094                     NO
TRM1092  EQU   *
         OI    TOTFLG04,TOTTTSWT  SET SWITCHED TEST DEVICE FLAG
         OI    TOTFLG06,TOTNCMFG  SET NCM FLAG
TRM1094  EQU   *
         DROP  R8
TDOK6    EQU   *
         BAL   R9,PLUG                 GO PLUG LINE NO. IN OLTCB
TDOK7    EQU   *                                               @YA12402
         TM    TDFLG,INCLUS             INCLUSIVE?             @YA12402
         BO    TDOK9                    YES                    @YA12402
         TM    TDFLG,MORE               ANY MORE TO DO         @YA12402
         BO    TDOK4                   YES BRANCH
TRM1099  EQU   *
         BAL   R11,LINECHK        VERIFY THAT LINE IS OPENED
         BAL   R3,TERMSTAT        CHECK INIT STATUS OF TEST DEVICES
         L     R13,D4(R13)        CALLER'S SAVE AREA
         LM    R14,R1,D12(R13)    CALLER'S REG
         XCTL  (2,12),EP=IEDQWC2
TDOK4    EQU   *
         TM    TDFLG,RNTERM+RNLINE+SYM SYMBOLICE                 S22024
         BM    TDOK8              YES - EXIT                     S22024
         B     TDFLD01            PROCESS NEXT ENTRY             S22024
TDOK8    EQU   *
         LA    R10,D0(R4,R10)     POINT TO NEXT ENTRY
         B     TDFLD01            PROCESS NEXT ENTRY             S22024
TDOK9    EQU   *
*
*              ROUTINE TO CHANGE CUU TO NEXT ONE IN ORDER
*
         SR    R3,R3
         IC    R3,WORK1+D2        LAST DIGIT
         LA    R4,KF              HEX 15
         CR    R3,R4                   FIND IF 0-9 OR A-F
         BL    BUMP1                   A-E
         BE    BUMP2                   F?
         LA    R4,F9              HEX 9
         CR    R3,R4                   9?
         BE    BUMP3                   YES
BUMP1    EQU   *
         LA    R3,D1(R3)          ADD 1 TO DIGIT
BUMP1A   EQU   *
         STC   R3,WORK1+D2        CHANGE CUU
         CLC   WORK3,WORK1
         BE    TURNOFF                 LAST DEVICE INCLUSIVE
         B     TURNOFF1           CONTINUE PROCESSING
TURNOFF  NI    TDFLG,NOINC             TURN OFF INCLUSIVE FLG
TURNOFF1 EQU   *
         BAL   R9,UCBLOOK               GO FIND UCB ADDR       @YA12402
         LA    R7,D1              SET INCREMENT
         A     R7,NUMBER               ADD ONE TO NUMBER
         ST    R7,NUMBER               STORE IT
         LA    R7,D10             SET COMPARAND                  S22024
         C     R7,NUMBER               GT THAN 9 DEVICES?
         BNL   TDOK6                   NO BRANCH
         B     TDTLON             TOO MANY TD ENTRIES            S22024
BUMP2    EQU   *
         IC    R3,WORK1+D1        PICK UP SECOND DIGIT
         CR    R3,R4
         BL    BUMP2A                  A-E?
         BE    TDERR                   F TOO LARGE A NUMBER
         LA    R4,F9              SET COMPARAND
         CR    R3,R4                   9?
         BE    BUMP2C                  YES
BUMP2A   EQU   *
         LA    R3,D1(R3)          ADD TO SECOND DIGIT
         STC   R3,WORK1+D1        SAVE
         LA    R3,F0              CHANGE LAST DIGIT TO F0
         B     BUMP1A             MAKE NEXT CHECK
BUMP2C   EQU   *
         LA    R3,XC0             SET COMPARAND
         B     BUMP2A             MAKE NEXT CHECK
BUMP3    EQU   *
         LA    R3,XC0             SET COMPARAND
         B     BUMP1              GO INCREMENT NUMBER
         EJECT
*
*
*              ROUTINE TO HANDLE TEST DEVICE ENTERED AS CUU
*
*
NUMDEV   EQU   *
         CLI   D0(R10),NUMER            IS FIRST CHAR ZERO     @YA12402
         BNE   DIGIT3                   NO                     @YA12402
         TM    D3(R10),XC0              IS 4TH CHAR ALPHANUM   @YA12402
         BNO   DIGIT3                   NO                     @YA12402
         SPACE 1                                               @YA12402
         LA    R10,D1(R10)              ADJUST FOR LEADING ZERO@YA12402
         SPACE 1                                               @YA12402
DIGIT3   EQU   *                                               @YA12402
         MVC   WORK1(D3),D0(R10)  MOVE CUU TO WORK AREA
         BAL   R9,UCBLOOK               GO FIND UCB ADDR       @YA12402
UCBEND1  EQU   *                                                 S22024
         CLI   D3(R10),SLASH      END OF FIELD
         BE    UCBEND3                 YES BRANCH
         CLI   D3(R10),COMMA      ANOTHER ENTRY
         BE    UCBEND4                 YES BRANCH
         CLI   D3(R10),DASH#      INCLUSIVE DEVICE
         BE    UCBEND5                 YES BRANCH
         B     TDERR                   NO DELIMITER
UCBEND3  EQU   *
         NI    TDFLG,END               TURN OFF MORE ENTRIES FLAG
         LA    R7,D9              SET COMPARAND                  S22024
         C     R7,NUMBER               MORE THAN 9 DEVICES?
         BNL   TDOK6              NO - CONTINUE PROCESSING
UCBEND4  EQU   *
         OI    TDFLG,MORE              TURN ON MORE FLAG
         LA    R10,D3(R10)        BUMP PASS CUU
         LA    R7,D1              ADD ONE TO COUNTER
         A     R7,NUMBER
         ST    R7,NUMBER
         LA    R7,D10             SET COMPARAND                  S22024
         C     R7,NUMBER
         BL    TDTLON             ERROR - TOO MANY TD ENTRIES    S22024
         B     TDOK3A             NO BRANCH                      S22024
UCBEND5  EQU   *
         LR    R4,R10
         LA    R10,D4(R10)        POINT PAST DASH
         LA    R3,D5              SET LOOP COUNT
         LA    R7,D0              INIT COUNTER
DASH     CLI   D0(R10),COMMA      END OF ENTRY ?
         BE    DASH1A                   YES                    @YA12402
         CLI   D0(R10),SLASH      END OF FIELD ?
         BE    DASH2                   YES BRANCH
         LA    R10,D1(R10)        BUMP POINTER
         LA    R7,D1(R7)          INCREMENT COUNTER
         BCT   R3,DASH                  CHECK CHAR AGAIN       @YA12402
         B     TDERR                   ERROR TOO MANY DIGITS AFTER DAS
DASH1A   EQU   *                                               @YA12402
         OI    TDFLG,MORE               MORE TO DO             @YA12402
DASH1    EQU   *
         C     R7,F4                    4 DIGITS?              @YA12402
         BE    DASH3                   YES BRANCH
         C     R7,K3              3 DIGITS
         BE    DASH10                  YES BRANCH
         B     TDERR                   WRONG NUMBER OF DIGITS
DASH3    EQU   *
         LA    R4,D1(R4)                ADJUST FOR LEADING ZERO@YA12402
DASH10   EQU   *
         OI    TDFLG,INCLUS            TURN ON INCLUSIVE FLAG
         MVC   WORK3(D3),D4(R4)   MOVE END CUU TO SAVE
DASH11   EQU   *
         LA    R7,D1              ADD ONE TO NUMBER
         A     R7,NUMBER
         ST    R7,NUMBER
         LA    R7,D10             SET COMPARAND                  S22024
         C     R7,NUMBER               MORE THAN 9 DEVICES?
         BNL   TDOK6              NO
         B     TDERR                   TOO MANY DECVICES
DASH2    EQU   *
         NI    TDFLG,ALLOFF            TURN OFF ALL THE FLAGS
         B     DASH1              CHECK NEXT NUMBER
         EJECT
*                                                              @YA12402
*                                                              @YA12402
*    ROUTINE TO LOOK UP UCB ENTRY,                             @YA12402
*     GIVEN THE CUU                                            @YA12402
*                                                              @YA12402
*                                                              @YA12402
UCBLOOK  EQU   *                                               @YA12402
         L     R8,UCBSAVE               GET ADDR OF UCB PTRS   @YA12402
         USING UCB,R7                                          @YA12402
UCBLOOK1 EQU   *                                               @YA12402
         SLR   R7,R7                    CLEAR                  @G36XRUV
         ICM   R7,3,D0(R8)              LOAD UCB ADDR          @G36XRUV
         LA    R8,D2(R8)                POINT TO NEXT ENTRY    @YA12402
         BZ    UCBLOOK1                 PADDING IGNORE         @YA12402
         CLM   R7,3,ENDTABLE            AT END OF UCB TABLE    @G36XRUV
         BE    TDERR                    Y-ERROR                @G36XRUV
         ST    R7,WORK4                 SAVE UCB ADDR          @YA12402
         CLC   WORK1(D3),UCBNAME        MATCH?                 @YA12402
         BNE   UCBLOOK1                 NO GO LOOK AGAIN       @YA12402
***********************************************************************
*        THE FOLLOWING CODE IS MVS UNIQUE. IT CHECKS FOR 3270  @G36XRUV
*        LOCAL AND 3705 USING THE UCB. IF THE DEVICE IS EITHER,@G36XRUV
*        IT IS AN ERROR. NOTE: THE MEANINGS OF THE UCB BITS MAY@G36XRUV
*        CHANGE IN THE FUTURE NECESSITATING CHANGES TO THIS    @G36XRUV
*        CODE.                                                 @G36XRUV
***********************************************************************
         MVC   WORK2(D1),UCBTBYT4       MOVE TYPE TO WORK AREA @G36XRUV
         TM    UCBDVCLS,UCB3DISP        Q-GRAPHICS DEVICE      @G36XRUV
         BNO   NCPCHK                   N-CHECK FOR NCP        @G36XRUV
         TM    WORK2,XF0                Q-ANY HI BITS ON       @G36XRUV
         BNZ   NCPCHK                   N-NOT 3270             @G36XRUV
         CLI   WORK2,X09                Q-IN 3270 RANGE        @G36XRUV
         BL    NCPCHK                   N-CHECK FOR NCP        @G36XRUV
         B     GETOUT1                  3270 LOCAL - ERROR     @G36XRUV
NCPCHK   DS    0H                                              @G36XRUV
         NI    WORK2,X0F                CLEAR HI BITS          @G36XRUV
         CLI   WORK2,TCU3705            3705?                  @G36XRUV
         BNER  R9                       RETURN                 @G36XRUV
         B     GETOUT1                  3705 - ERROR           @G36XRUV
         EJECT
*              ROUTINE TO SEARCH TERMNAME TABLE. R3 HAS ADDR OF
*              START OF TABLE. WORK1 HAS NAME IN IT. R2 HAS LENGTH
*
NAMESRCH EQU   *
         LA    R1,PAREMLST              SET R1
         L     R15,AVTUISV
         LH    R0,TWO(R1)         LOAD CHAR STRING LEN
         L     R1,FOUR(R1)        LOAD ADDR OF CHARS
         BAL   R14,FOUR(R15)      BRANCH TO SCAN ROUTINE
         LTR   R15,R15
         BZ    D4(R9)             NAME NOT FOUND
         SPACE
         STM   R7,R9,TRMSAV1      SAVE REG 7-9
         L     R8,TERNMTAB        SET START OF TABLE
         USING IEDQTNTD,R8
         LA    R7,TNTFIRST        POINT TO FIRST ENTRY OF TABLE
         SR    R9,R9              CLEAR REG 9
         IC    R9,TNTENLEN        GET MAX. LENGTH OF NAME FIELD
         LA    R9,D3(R9)          LENGTH OF 1 ENTRY
         DROP  R8
         BCTR  R15,R0             DECREMENT REL POSITION COUNT
         MR    R8,R15             GET DISPLACEMENT TO ENTRY DESIRED
         AR    R7,R9              GET DESIRED TNT ADDRESS
         ST    R7,TRMTNT          SAVE IT
         LA    R1,D1(R15)         RESTORE REL POSITION COUNT
         LM    R7,R9,TRMSAV1      RESTORE REG
         L     R15,TERNMTAB
         BALR  R14,R15            GET TTE ADDR
         ST    R1,TNTENTRY
         USING IEDQTRM,R1                                        S22024
         TM    TRMDSORG,TRMLGB    LGB RATHER THAN A TTE          S22024
         BO    D4(R9)             YES - ERROR - NOT A TERMINAL   S22024
         L     R1,TRMDESTQ-D1     DEST. QCB ADDR                 S22024
         DROP  R1                                                S22024
         USING IEDQQCB,R1                                        S22024
         LA    R1,D0(R1)          CLEAR HI ORDER BYTE            S22024
         TM    QCBFLAG,QCBTSSES   TSO TERMINAL                  SA61759
         BO    TRMTSO             YES - ERROR EXIT               S22024
         DROP R1                                                 S22024
         BR    R9                 RETURN TO CALLER
         SPACE
TRMTSO   DS    0H                 BOUNDARY ALLIGNMENT            S22024
         IEDQMSG MSGID=011,FUNCT=CEC TSO TEST DEVICE             S22024
         B     GETOUT             TERMINATE TRM                  S22024
         EJECT
*
*
*        ROUTINE TO FIND UCB ADDR AFTER ENTRY IN TERMINAL TABLE HAS
*        BEEN FOUND
*
*
UCBFIND  EQU   *
         L     R8,TNTENTRY
         USING IEDQTRM,R8
         L     R8,TRMDESTQ-D1     GET QCB ADDR
         DROP  R8
         USING IEDQQCB,R8
         SR    R7,R7                   GET RELATIVE
         IC    R7,QCBRELLN          RELATIVE LINE NUMBER
         SLL   R7,2                    MULT BY 4
         L     R8,QCBDCBAD-1      GET DCB ADDRESS
         DROP  R8
         USING IHADCB,R8
         TM    DCBDSORG,LGBA      LGB VS. DCB                    S22024
         BZ    UCB0X              NO                             S22024
         DROP  R8                                                S22024
         L     R1,TNTENTRY        GET TERMINAL TTE ADDRESS     @Y17XAUU
         BAL   R14,NCPTTE         GET NCP TTE ADDRESS          @Y17XAUU
*                                                              @Y17XAUU
*        ON RETURN R1 CONTAINS ADDRESS OF NCP TTE PREFIX       @Y17XAUU
*                                                              @Y17XAUU
         USING IEDNTRM,R1         ESTABLISH ADDRESSABILITY     @Y17XAUU
         LA    R7,D4              SHOW RLN EQU 1                 S22024
         TM    TRMBYTE2,TRMRSACT  3705 ACTIVE                  @Y17XAUU
         BO    UCB0X1             YES - CONTINUE               @Y17XAUU
UCBNACT  EQU   *                                               @Y17XAUU
         IEDQMSG MSGID=010,FUNCT=CEC MSG                         S22024
         B     TDERR              ERROR EXIT                     S22024
UCB0X1   EQU   *                                               @Y17XAUU
         L     R1,TRMDESTQ-D1     GET 3705 QCB ADDRESS         @Y17XAUU
         DROP  R1                                              @Y17XAUU
         USING IEDQQCB,R1         ESTABLISH ADDRESSABILITY     @Y17XAUU
         L     R8,QCBDCBAD-D1     GET 3705 DCB ADDRESS         @Y17XAUU
         DROP  R1                                              @Y17XAUU
UCB0X    EQU   *                                                 S22024
         USING IHADCB,R8                                         S22024
         TM    DCBOFLGS,DCBOPEN   DCB OPEN
         BZ    NOOPEN                  NO BRANCH
         L     R8,DCBDEBAD        GET DEB ADDRESS
         DROP  R8
         USING DEBNMSUB,R8
         SR    R15,R15
         IC    R15,DEBNMEXT       GET NUMBER OF EXTENTS
         SLL   R15,2                MULTIPLY BY 4
         CR    R15,R7             IS DESIRED LINE OPENED?
         BL    NOOPEN               NO
         L     R8,DEBUCBAD-D4(R7) UCB ADDR
         USING UCB,R8
         MVC   WORK1(D3),UCBNAME  MOVE UCB ID IN EBCDIC
         MVC   WORK2(D2),UCBCHA   MOVE UCB ID IN HEX
         DROP  R8
         MVI   WORK2+D2,D0        CLEAR HI ORDER BYTE
         NI    WORK2,X0F          MASK OFF UNWANTED BITS
         BR    R9                      RETURN
NOOPEN   EQU   *
         MVC   CEOUT2(D8),WORK1   PUT TERMINAL NAME IN MSG
         SPACE
*
*        TEST LINE NOT OPEN
*
         SPACE
         IEDQMSG OTBUF=CEOUT2A,OTCNT=CEOUT2E-CEOUT2A,FUNCT=CEC,LINK=YES
         SPACE
         B     TDERR              GO TO PROMPTER
         EJECT
************************************************************** @Y17XAUU
*        ROUTINE TO FIND NCP TTE ADDRESS. INPUT: REG1 =      * @Y17XAUU
*        TERMINAL TTE ADDRESS. OUTPUT: REG 1 = NCP TTE PREFIX* @Y17XAUU
*        ADDRESS. RETURN TO CALLER VIA REG 14.               * @Y17XAUU
************************************************************** @Y17XAUU
NCPTTE   EQU   *                                               @Y17XAUU
         ST    R14,TRMSAVE2       SAVE RETURN ADDRESS          @Y17XAUU
         LA    R8,TRMPRFSZ        GET TTE PREFIX SIZE          @Y17XAUU
         SLR   R1,R8              POINT TO TTE PREFIX          @Y17XAUU
         USING IEDNTRM,R1         ESTABLISH ADDRESSABILITY     @Y17XAUU
NCPLOOP  EQU   *                                               @Y17XAUU
         LH    R1,TRMCOHRT        GET COHORT POINTER           @Y17XAUU
         LTR   R1,R1              ENTRY IN HIERARCHY           @YM07415
         BZ    UCBNACT            NO - ERROR RETURN            @YM07415
         L     R15,TERNMTAB       TTCIN TO TTE CONVERSION      @Y17XAUU
*                                 ROUTINE ADDRESS              @Y17XAUU
         BALR  14,R15             FIND TTE ADDRESS             @Y17XAUU
         SLR   R1,R8              POINT TO TTE PREFIX          @Y17XAUU
         CLI   TRMTYPE,TRMLNCP    NCP ENTRY                    @Y17XAUU
         BNE   NCPLOOP            NO - CHECK NEXT ENTRY        @Y17XAUU
         L     R14,TRMSAVE2       RESTORE RETURN ADDRESS       @Y17XAUU
         BR    R14                RETURN TO CALLER             @Y17XAUU
         DROP  R1                                              @Y17XAUU
         EJECT
GETOUT1  DS    0H                                              @G36XRUV
         IEDQMSG MSGID=094,FUNCT=CEC                           @G36XRUV
GETOUT   EQU   *
         OI    TOTFLG06,TOTOTERM  SET TERMINATE FLAG
         L     R13,D4(R13)        CALLER'S SAVE AREA
         LM    R14,R1,D12(R13)    CALLER'S REG
         XCTL  (2,12),EP=IEDQWE
NOOPEN2  EQU   *
         L     R9,REGSAVE              RESTORE RETURN REG
         BR    R9                      RETURN
         SPACE 4
**************************************************************** S22024
**************************************************************** S22024
*        ERROR EXIT - MORE THAN 10 TEST DEVICES                * S22024
TDTLON   EQU   *                                                 S22024
         IEDQMSG MSGID=000,FUNCT=CEC MORE THAN 10 TEST DEVICES   S22024
         B     TDERR              EXIT TO PROMPTER               S22024
         EJECT
*
*        FIND LCB ADDR FOR LINE ENTRY IN TEST DEVICE TABLE.
*        THE UCB NAME IS PASSED TO THIS ROUTINE.
*        THE LCB ADDRESS IS RETURN TO THE CALLER.
*
LCBFIND  EQU   *
         STM   R1,R12,REGSAVE     SAVE CALLER'S REG
NOUCBAD  EQU   *
         L     R1,ACVT(R0)        CVT ADDR
         L     R1,AVTCVTPT(R1)         GET TCAM DISP ADDR
         L     R1,D0(R1)          AVT ADDR
         USING IEDQAVTD,R1
         L     R1,AVTTCB               GET TCB ADDR
         DROP  R1
         L     R1,D8(R1)          DEB CHAIN ADDR
AGAIN    DS    0H                 ALLIGN ON HALFWORD BOUNDARY    S22024
         SR    R3,R3              CLEAR REG                      S22024
         IC    R3,D16(R1)         NUMBER OF EXTENTS
         LA    R5,D32(R1)         UCB CHAIN ADDR
         L     R4,D24(R1)         DCB ADDR
         USING IHADCB,R4                                         S22024
         TM    DCBDSORG,LGBA      LGB VS. DCB                    S22024
         BZ    HERE0              NO                             S22024
         DROP  R4                                                S22024
         L     R1,TNTENTRY        GET TERMINAL TTE ADDRESS     @Y17XAUU
         BAL   R14,NCPTTE         FIND NCP TTE ADDRESS         @Y17XAUU
         USING IEDNTRM,R1         ESTABLISH ADDRESSABILITY     @Y17XAUU
         L     R7,TRMDESTQ-D1     GET QCB ADDRESS              @Y17XAUU
         DROP  R1                                              @Y17XAUU
         USING IEDQQCB,R7         ESTABLISH ADDRESSABILITY     @Y17XAUU
         L     R4,QCBDCBAD-D1     GET NCP DCB ADDRESS          @Y17XAUU
         DROP  R7                                              @Y17XAUU
         USING IHADCB,R4                                         S22024
HERE0    EQU   *                                                 S22024
         L     R12,D4(R1)         NEXT DEB ADDR
         TM    DCBDSORG+D1,TCAMDEB TCAM DEB
         BZ    HERE1                   NO BRANCH
         MVC   LCBLEN(D1),DCBEIOBX GET LCB SIZE
         LA    R8,D1              COUNTER
HERE     EQU   *
         L     R9,D0(R5)          UCB ADDR
         CLC   WORK1(D3),D13(R9)  CUU MATCH
         BE    THERE                   YES BRANCH
         LA    R8,D1(R8)          BUMP COUNTER
         LA    R5,D4(R5)          BUMP UCB ADDR PTR
         BCT   R3,HERE                 GO LOOK AGAIN
HERE1    EQU   *
         LA    R12,D0(R12)        CLR HI ORDER BYTE
         LTR   R12,R12                 ANY MORE DEBS?
         BZ    BADRET                  NO BRANCH
         LR    R1,R12                  SET R1 TO NEXT DEB
         B     AGAIN                   GO LOOK AT NEXT DEB
BADRET   EQU   *
         LM    R1,R12,REGSAVE     RESTORE REG
         B     D4(R9)             RETURN
THERE    EQU   *
         USING UCB,R9                                            S22024
         MVC   RNWORK(D1),UCBTBYT4 TCU TYPE BYTE                 S22024
         NI    RNWORK,X0F         CLEAR UNWANTED BITS            S22024
         CLI   RNWORK,TCU3705     3705 TCU                       S22024
         BNE   THERE2             NO                             S22024
         LA    R8,D1              SET RLN EQU 1                  S22024
         DROP  R9                                                S22024
THERE2   EQU   *                                                 S22024
         STC   R8,RLN                  STORE THE REL LIN NO.
         STC   R8,MULT+D3         SET UP MULTIPLIER
         L     R8,DCBIOBAD            PICK UP LCB IOB ADDR
         SR    R10,R10
         SR    R11,R11
         IC    R11,LCBLEN               SET R3 TO SIZE OF LCB
         M     R10,MULT                 MULTIPLY
         LA    R8,D0(R11,R8)      IOB OF DESIRED LCB
         S     R8,K32             ADJUST TO START OF LCB
         ST    R8,WORK1           SAVE UCB ADDR
         LM    R1,R12,REGSAVE     RESTORE REG
         BR    R9                      RETURN
         DROP  R4
         EJECT
*
*        ROUTINE TO DETERMINE INITIAL STATUS OF
*        TERMINAL TEST DEVICES
*
TERMSTAT EQU   *
         TM    TOTFLG10,TOTTERMS  TERMINAL TEST ?
         BZ    LINESTAT           NO - LINE TEST
         LA    R12,TOTTDTBL       POINT TO TEST DEVICE TABLE
         LA    R11,TOTTDEND       POINT TO END OF TABLE
STAT2    EQU   *
         CR    R12,R11            END OF TABLE ?
         BE    ZERO(R3)           YES - RETURN
         L     R7,D0(R12)         GET TNT ADDR
         LA    R7,D0(R7)          CLEAR HI ORDER BYTE
         LTR   R7,R7              LAST ENTRY ?
         BZ    ZERO(R3)           YES - RETURN
         ST    R7,TDTNT           SAVE TD TNT ADDRESS          @YM08136
         MVI   RQLADR,SPACE       BLANK NAME FIELD
         MVC   RQLADR+D1(D7),RQLADR  IN REQUEST ELEMENT
         SR    R10,R10            CLEAR REG
         IC    R10,TOTTTBEL       GET MAX NAME LENG9H
         LR    R9,R10             SAVE LENGTH
         BCTR  R9,R0              DECREMENT FOR EX
         EX    R9,RQMOVE          MOVE NAME TO REQ ELEMENT
         AR    R7,R10             POINT TO TTE ADDR
         MVC   TTEADDR+D1(D3),D0(R7) ALLIGN ADDR
         L     R8,TTEADDR         GET TTE ADDR
         LA    R1,TRMPRFSZ        GET TTE PREFIX SIZE          @Y17XAUU
         SLR   R8,R1              POINT TO TTE PREFIX          @Y17XAUU
         USING IEDNTRM,R8                                      @Y17XAUU
         CLI   TRMTYPE,TRMLUNT    LU ENTRY                     @Y17XAUU
         BE    STAT16             YES - BYPASS INIT STATUS     @Y17XAUU
         L     R8,TRMDESTQ-D1     GET QCB ADDR
         DROP  R8
         USING IEDQQCB,R8
         MVC   RQRLN(D1),QCBRELLN SAVE RLN IN OP CONTROL PLIST
         TM    QCBSTAT,QCBTRMHO   TERMINAL INITIALLY HELD ?
         BZ    STAT6              NO
         OI    D0(R12),TOTTDTHD   SHOW INIT HELD
STAT6    EQU   *
         L     R9,TOTAVTPT        GET AVT ADDR
         TM    TOTFLG04,TOTTTSWT  TEST DEVICE SWITCHED ?
         BO    STAT12             YES - TERMINAL ACTIVE          S22024
*
*        IF THE TEST ENTRY IS AN NCP PRE SNA RESOURCE AND      @YM08136
*        EQUAL TO THE CONTROL TERMINAL OR ALTERNATE PRINTER    @YM08136
*        BYPASS ISSUING AN INACTIVATE FOR THE RESOURCE.        @YM08136
*
         SLR   R1,R1              CLEAR REGISTER               @YM08136
         ICM   R1,ADRMSK,TOTCRTNT+D1 GET CT TNT ADDRESS        @YM08136
         BZ    CHKAP              CHECK AP IF CT NOT TERMINAL  @YM08136
         CL    R1,TDTNT           CT EQU TD                    @YM08136
         BE    CHKTYPE            YES - CHECK FOR PRE SNA TERM @YM08136
CHKAP    EQU   *                                               @YM08136
         SLR   R1,R1              CLEAR REG                    @YM08136
         ICM   R1,ADRMSK,TOTARTNT+D1 GET AP TNT ADDRESS        @YM08136
         BZ    CHKEND             GO - INACTIVATE RESOURCE     @YM08136
         CL    R1,TDTNT           AP EQU TD                    @YM08136
         BNE   CHKEND             NO - GO INACTIVATE RESOURCE  @YM08136
CHKTYPE  EQU   *                                               @YM08136
         L     R1,TTEADDR         GET TTE ADDRESS              @YM08136
         LA    R15,TRMPRFSZ       GET TTE PREFIX SIZE          @YM08136
         SLR   R1,R15             POINT TO TTE PREFIX          @YM08136
         USING IEDNTRM,R1         SET TTE ADDRESSABILITY       @YM08136
         CLI   TRMTYPE,TRMPSNA    NCP PRE SNA TERMINAL         @YM08136
         BE    STAT12             YES - PROCESS NEXT TD ENTRY  @YM08136
CHKEND   EQU   *                                               @YM08136
         LA    R1,TOTOTECB        GET ECB ADDR
         ST    R1,RQECBA          PUT IN REQ ELEMENT
         L     R5,TOTAVTPT        GET AVT ADDR
         USING IEDQAVTD,R5
         LA    R14,AVTOPCOB       GET OP CONTROL QCB ADDR
         DROP  R5
         ST    R14,RQQCBA-D1      PUT IN QCB ADDR
         MVI   RQVBCD,INACT       INACTIVATE VERB CODE           S22024
         XC    RQLINK(D3),RQLINK  CLEAR LINK FIELD
         XC    TOTOTECB(D4),TOTOTECB CLEAR ECB
         MVC   TOTPLIST(RQND-RQELE),RQELE ELEMENT TO OLTCB       S22024
         LA    R1,TOTPLIST        ADDR OF ELEMENT                S22024
         ST    R1,TRMSLPL         PUT IN PLIST                   S22024
         ST    R1,TRMSLPL+D4      PUT IN PLIST                   S22024
         MVI   TRMSLPL,RQINIT     SET INITIAL ELEMENT FLAG       S22024
         MVI   TRMSLPL+D4,RQEND   SET LAST ELEMENT FLAG          S22024
         LA    R1,TRMSLPL         ADDR OF PLIST                  S22024
*
         AQCTL                    ISSUE ACTIVATE COMMAND
         WAIT  ECB=TOTOTECB
*
         MVC   RQRTCD(D1),TOTORTCD RETURN CODE                   S22024
         CLI   RQRTCD,INACTRC     INIT INACTIVE                  S22024
         BNE   STAT12             NO - ACTIVE                    S22024
STAT10   EQU   *
         OI    D0(R12),TOTTDIAC   SHOW THAT LIST INACTIVE
STAT12   EQU   *                  FIND LCB ADDR
         L     R9,QCBDCBAD-D1     GET DCB ADDR
         USING IHADCB,R9
         TM    DCBDSORG,LGBA      LGB VS. DCB                    S22024
         BZ    STAT13             NO                             S22024
         DROP  R9                                                S22024
         L     R1,TNTENTRY        GET TERMINAL TTE ADDRESS     @Y17XAUU
         BAL   R14,NCPTTE         FIND NCP TTE ADDRESS         @Y17XAUU
         USING IEDNTRM,R1         ESTABLISH ADDRESSABILITY     @Y17XAUU
         L     R9,TRMDESTQ-D1     GET QCB ADDRESS              @Y17XAUU
         USING IEDQQCB,R9         ESTABLISH ADDRESSABILITY     @Y17XAUU
         L     R9,QCBDCBAD-D1     GET 3705 DCB ADDRESS         @Y17XAUU
         DROP  R1                                              @Y17XAUU
         DROP  R9                                              @Y17XAUU
         USING IHADCB,R9                                         S22024
STAT13   EQU   *                                                 S22024
         L     R7,DCBDEBAD        GET DEB ADDR
         USING DEBNMSUB,R7
         SR    R15,R15            CLEAR REG
         IC    R15,DEBNMEXT       GET DEB NUMBER OF EXTENTS
         DROP  R7
         SR    R7,R7              CLEAR REG
         IC    R7,QCBRELLN        GET RLN                        S22024
         TM    D0(R12),TOTTDORN   TD ON 3705                     S22024
         BZ    STAT14             NO                             S22024
         L     R5,TTEADDR         GET TTE ADDR                   S22024
         LA    R1,TRMPRFSZ        GET TTE PREFIX SIZE          @Y17XAUU
         SLR   R5,R1              POINT TO TTE PREFIX          @Y17XAUU
         USING IEDNTRM,R5         ESTABLISH ADDRESSABILITY     @Y17XAUU
LINELOOP EQU   *                                               @Y17XAUU
         LH    R1,TRMCOHRT        GET COHORT POINTER           @Y17XAUU
         L     R15,TERNMTAB       TTCIN TO TTE CONVERSION      @Y17XAUU
*                                 ROUTINE ADDRESS              @Y17XAUU
         BALR  R14,R15            FIND TTE ADDRESS             @Y17XAUU
         LR    R5,R1              GET TTE ADDRESS              @Y17XAUU
         LA    R1,TRMPRFSZ        GET TTE PREFIX SIZE          @Y17XAUU
         SLR   R5,R1              POINT TO TTE PREFIX          @Y17XAUU
         CLI   TRMTYPE,TRMNSDLC   PRE SNA LINE ENTRY           @Y17XAUU
         BE    LINEFND            YES                          @Y17XAUU
         CLI   TRMTYPE,TRMSDLC    SNA LINE ENTRY               @Y17XAUU
         BNE   LINELOOP           CHECK NEXT ENTRY             @Y17XAUU
LINEFND  EQU   *                                               @Y17XAUU
         TM    TRMBYTE2,TRMRSACT  LINE STOPPED                 @Y17XAUU
         BO    STAT16             NO                           @Y17XAUU
         OI    D0(R12),TOTTDLST   YES - SHOW LINE STOPPED        S22024
         DROP  R5                                                S22024
         B     STAT16             EXIT                           S22024
STAT14   EQU   *                                                 S22024
         LR    R5,R7              COPY RLN                       S22024
         BCTR  R5,R0              DECREMENT BY 1
         SR    R15,R5             GET COUNT OF LCB'S TO SCAN
         SR    R14,R14            CLEAR REG
         IC    R14,DCBEIOBX       GET LCB SIZE
         LR    R5,R7              GET RLN
         MR    R4,R14             GET STARTING LCB OFFSET
         AL    R5,DCBIOBAD        POINT TO IOB OF LCB
         DROP R9
         S     R5,K32             ADJUST TO START OF LCB
         USING IEDQLCB,R5
         TM    LCBSTAT1,LCBFREEN+LCBRECVN+LCBSENDN  LINE STOPPED ?
         BNZ   STAT16             NO
         OI    D0(R12),TOTTDLST   YES - SHOW LINE STOPPED
         DROP  R5
*
STAT16   EQU   *
         LA    R12,D4(R12)        POINT TO NEXT ENTRY
         B     STAT2              PROCESS NEXT ENTRY
         DROP  R8
         EJECT
*
*        SET INITIAL STATUS BITS FOR LINE TEST DEVICE
*
LINESTAT EQU   *
         LA    R12,TOTTDTBL       POINT TO TEST DEVICE TABLE
         LA    R11,TOTTDEND       POINT TO END OF TABLE
STAT20   EQU   *
         CR    R12,R11            END OF TABLE
         BE    ZERO(R3)           YES - RETURN
         L     R7,D0(R12)         GET UCB ADDR
         LA    R7,D0(R7)          CLEAR HI ORDER BYTE
         LTR   R7,R7              LAST ENTRY
         BZ    ZERO(R3)           YES - RETURN
         TM    D0(R12),TOTTDORN   TD ON 3705                     S22024
         BO    STAT40             YES                            S22024
         SPACE
         USING UCBOB,R7
         MVC   WORK1(D3),UCBNAME  GET UCB NAME
         DROP  R7
         BAL   R9,LCBFIND         GET LCB ADDR
         B     STAT24             LCB FOUND
         B     STAT28             BYPASS LCB CHECK
STAT24   EQU   *
         L     R8,WORK1           GET LCB ADDR
         LA    R8,D0(R8)          CLEAR HI ORDER BYTE
         USING IEDQLCB,R8
         TM    LCBSTAT1,LCBFREEN+LCBRECVN+LCBSENDN LINE STOPPED
         BNZ   STAT28             NO
         OI    D0(R12),TOTTDLST   YES - SHOW LINE STOPPED
         DROP  R8
STAT28   EQU   *
         LA    R12,D4(R12)        POINT TO NEXT ENTRY
         B     STAT20             CHECK NEXT ENTRY               S22024
         SPACE 2
STAT40   EQU   *                                                 S22024
         SR    R10,R10            CLEAR REG                      S22024
         IC    R10,TOTTTBEL       MAX NAME LENGTH                S22024
         AR    R7,R10             POINT TO TTE ADDR              S22024
         MVC   TTEADDR+D1(D3),D0(R7) ALLIGN ADDR                 S22024
         L     R8,TTEADDR         TTE ADDR                       S22024
         LA    R1,TRMPRFSZ        GET TTE PREFIX SIZE          @Y17XAUU
         SLR   R8,R1              POINT TO TTE PREFIX          @Y17XAUU
         USING IEDNTRM,R8         ESTABLISH ADDRESSABILITY     @Y17XAUU
         TM    TRMLINK,TRMLACTV   LINE STOPPED                 @Y17XAUU
         BO    STAT28             NO - EXIT                      S22024
         OI    D0(R12),TOTTDLST   YES - SHOW LINE STOPPED        S22024
         DROP  R8                                                S22024
         B     STAT28             CHECK NEXT ENTRY               S22024
         EJECT
PLUG     EQU   *                                                 S22024
*        SET UP TEST DEVICE TABLE
         LA    R7,TOTTDTBL        POINT TO TEST DEVICE TABLE
         LA    R8,TOTTDTBL+D40    END OF TD TABLE
PLUG001  EQU   *
         CR    R7,R8              TABLE FILLED?
         BE    TDTLON             ERROR - MORE THAN TEN ENTRIES  S22024
         L     R12,D0(R7)         GET NEXT ENTRY
         LTR   R12,R12            VACANT SPACE IN TABLE?
         BZ    PLUG002            YES
         LA    R7,D4(R7)          NO - POINT TO NEXT SLOT
         B     PLUG001            CHECK NEXT ENTRY
PLUG002  EQU   *                                                 S22024
         TM    TDFLG,SYM+RNTERM   TERMINAL TEST                  S22024
         BZ    PLUG008            NO                             S22024
         MVI   TRMTNT,TOTTDTRM    YES - SHOW TERMINAL TEST       S22024
         TM    TDFLG,RNTERM       TERMINAL ON RN                 S22024
         BZ    PLUG003            NO                             S22024
         OI    TRMTNT,TOTTDORN    YES - SHOW RN TERMINAL         S22024
PLUG003  EQU   *                                                 S22024
         MVC   D0(D4,R7),TRMTNT   TNT ADDR TO TD TABLE           S22024
         OI    TOTFLG10,TOTTERMS  SHOW TERMINAL TEST             S22024
         B     PLUGEND            EXIT                           S22024
PLUG008  EQU   *                  LINE ENTRY                     S22024
         LA    R14,TOTTDTBL       POINT TO TD TABLE              S22024
         TM    TDFLG,RNLINE       RN LINE TEST                   S22024
         BO    PLUG018            YES                            S22024
         MVI   WORK4,TOTTDLIN     SHOW LINE TEST                 S22024
         MVC   D0(D4,R7),WORK4    UCB ADDR TO TD TABLE           S22024
         CR    R14,R7             FIRST ENTRY                    S22024
         BE    PLUG010            YES - BYPASS MIXED CHECK       S22024
         LR    R14,R7             GET CURRENT SETTING            S22024
         S     R14,F4             LAST ENTRY ADDR                S22024
         CLI   D0(R14),TOTTDLIN   CUU TEST                       S22024
         BNE   TDERR              NO - ERROR                     S22024
PLUG010  DS    0H                 BOUNDARY ALLIGNMENT            S22024
         OI    TOTFLG06,TOTNCMFG  SET NON-CONCURRENT MODE FLAG   S22024
         B     PLUGEND            EXIT                           S22024
PLUG018  EQU   *                  RN SUB LINE                    S22024
         MVI   TRMTNT,TOTTDLIN    SHOW LINE TEST                 S22024
         OI    TRMTNT,TOTTDORN    SHOW RN LINE                   S22024
         CR    R14,R7             FIRST ENTRY                    S22024
         BE    PLUG024            YES - BYPASS MIXED CHECK       S22024
         LR    R14,R7             GET CURRENT SETTING            S22024
         S     R14,F4             LAST ENTRY ADDR                S22024
         TM    D0(R14),TOTTDORN   RN LINE TEST                   S22024
         BZ    TDERR              NO - ERROR                     S22024
PLUG024  DS    0H                 BOUNDARY ALLIGNMENT            S22024
         MVC   D0(D4,R7),TRMTNT   TNT ADDR TO TD TABLE           S22024
PLUGEND  EQU   *                                                 S22024
         BR    R9                 RETURN                         S22024
         EJECT
*
LINECHK  EQU   *
*
         LA    R4,TOTTDTBL        TEST DEVICE TABLE
         LA    R7,TOTTDEND        END OF TABLE
ALLTEST  EQU   *
         CR    R4,R7              ALL ENTRIES PROCESSED
         BE    ZERO(R11)          YES - EXIT                     S22024
         TM    D0(R4),TOTTDLIN+TOTTDORN RN LINE TEST             S22024
         BO    D0(R11)            YES - BYPASS CHECK             S22024
         TM    D0(R4),TOTTDLIN    270X LINE TEST                 S22024
         BZ    D0(R11)            NO - RETURN                    S22024
         L     R8,ZERO(R4)        GET ADDR FROM TABLE            S22024
         LA    R8,ZERO(R8)        CLEAR HI ORDER BYTE
         STH   R8,KUCB            SAVE UCB ADDR
         LTR   R8,R8              IS THERE AN ADDRESS
         BZ    ZERO(R11)          NO - RETURN
         LA    R9,CVTPTR          POINTER TO CVT
         L     R9,ZERO(R9)        CVT ADDR
         L     R9,ZERO(R9)        TCB ADDR
         L     R9,FOUR(R9)        CURRENT TCB ADDR
         L     R9,TWELVE(R9)      TIOT ADDR
         USING TIOT,R9
         LA    R9,TIOENTRY        FIRST ENTRY
         DROP  R9
         USING TIOENTRY,R9
GETLENGT EQU   *
         SR    R3,R3              CLEAR REG 3
         IC    R3,TIOELNGH        DD ENTRY LENGTH
         LTR   R3,R3              ANY LENGTH ?
         BZ    NOTFOUND           NO - ERROR
         LA    R5,POOLSTAR-TIOESTTB DEV ENTRY LENGTH
         DROP  R9
         LR    R10,R3             SAVE LENGTH
         SR    R3,R5              DD LENGTH - 4
         AR    R3,R9              DEVICE
         USING TIOESTTB,R3
         CLC   TIOEFSRT+ONE(TWO),KUCB ENTRY FOUND ?
         DROP  R3
         BE    FOUND              ENTRY FOUND
         AR    R9,R10             POINT TO NEXT ENTRY
         B     GETLENGT           CONTINUE LOOKING
FOUND    EQU   *
         LA    R4,FOUR(R4)        POINT TO NEXT LINE
         B     ALLTEST            CHECK NEXT ENTRY
NOTFOUND EQU   *
*
*        DD CARD OMITTED
*
         IEDQMSG MSGID=052,FUNCT=CEC
         B     TDERR              GO TO PROMPTER                 S22024
         EJECT
**************************************************************** S22024
*                                                              * S22024
*        ROUTINE TO DETERMINE THE TYPE OF TEST TO BE RUN --    * S22024
*        LINE OR TERMINAL. REG 10 PLUS ONE POINTS TO TD FIELD. * S22024
*                                                              * S22024
**************************************************************** S22024
TYPECHK  EQU   *                                                 S22024
         STM   R7,R11,SAVE        SAVE CALLER'S REG              S22024
         LA    R7,D0              ZERO TTE ADDR                  S22024
         TM    D1(R10),NUMER      LINE TEST                      S22024
         BO    TYPELINE           YES                            S22024
*                                                                S22024
*        FIND LENGTH OF TEST DEVICE ENTRY                        S22024
*                                                                S22024
         MVC   WORK2(D9),D1(R10)  MOVE ENTRY TO WORK AREA        S22024
         LA    R7,D9              LOOP COUNT                     S22024
         LA    R8,D0              COUNTER                        S22024
         LA    R9,WORK2           WORK AREA                      S22024
TYPELP   EQU   *                                                 S22024
         CLI   D0(R9),SLASH       END OF FIELD                   S22024
         BE    TYPEFD             YES                            S22024
         CLI   D0(R9),COMMA       END OF ENTRY                   S22024
         BE    TYPEFD             YES                            S22024
         LA    R9,D1(R9)          NEXT CHAR                      S22024
         LA    R8,D1(R8)          INCREMENT COUNTER              S22024
         BCT   R7,TYPELP          NEXT TEST                      S22024
         B     TDERR              ERROR - NAME TOO LONG          S22024
         SPACE
TYPEFD   EQU   *                                                 S22024
         LTR   R8,R8              TD FIELD EMPTY                 S22024
         BZ    TDERR              YES - ERROR                    S22024
         STC   R8,LENGTH          LENGTH OF ENTRY TO PLIST       S22024
*        AN INVALID ENTRY HAS BEEN MADE IN TEST DEVICE FIELD   * S22024
         BCTR  R8,R0              LENGTH MINUS 1                 S22024
         MVC   WORK1(D8),BLANK    BLANK NAME FIELD               S22024
         EX    R8,TDEVICE         NAME TO PLIST                  S22024
         SPACE
         BAL   R9,NAMESRCH        VERIFY SYMBOLIC NAME           S22024
         B     TYPRN              NORMAL RETURN                  S22024
         B     TDERR              ERROR RETURN                   S22024
         SPACE
TYPRN    EQU   *
         L     R7,TNTENTRY        TTE ADDR                       S22024
         USING IEDNTRM,R7                                        S22024
         LA    R9,TRMPRFSZ        GET TTE PREFIX SIZE          @Y17XAUU
         SLR   R7,R9              POINT TO TTE PREFIX          @Y17XAUU
         TM    TRMSTATE,TRMPREF   3705 MODE                    @Y17XAUU
         BZ    TYPETERM           NO                             S22024
         CLI   TRMTYPE,TRMPUNT    PU ENTRY                     @Y17XAUU
         BE    TDERR              YES - ERROR                  @Y17XAUU
         CLI   TRMTYPE,TRMLNCP    NCP ENTRY                    @Y17XAUU
         BE    TDERR              YES - ERROR                  @Y17XAUU
         CLI   TRMTYPE,TRMSSCP    SSCP ENTRY                   @Y17XAUU
         BE    TDERR              YES - ERROR                  @Y17ZAUU
         CLI   TRMTYPE,TRMSDLC    SNA LINE ENTRY               @Y17XAUU
         BE    TYPELINE           YES - PROCESS                @Y17XAUU
         CLI   TRMTYPE,TRMNSDLC   PRE SNA LINE ENTRY           @Y17XAUU
         BE    TYPELINE           YES - PROCESS                @Y17XAUU
         TM    TRMSTATE,XE0       RN TERMINAL                    S22024
         BZ    TYPETERM           YES                            S22024
         B     TDERR              ERROR EXIT                     S22024
         SPACE
TYPETERM EQU   *                                                 S22024
         OI    TDFLG,SYM          SHOW TERMINAL TEST             S22024
         TM    TDFLG,LINE         LINE FLAG SET                  S22024
         BO    TDERR              YES - ERROR                    S22024
         TM    TRMSTATE,TRMPREF   3705 MODE                    @Y17XAUU
         BZ    TERM4              BRANCH                         S22024
         OI    TDFLG,RNTERM       YES - SET FLAG                 S22024
TERM4    EQU   *                                                 S22024
         LM    R7,R11,SAVE        RESTORE REG                    S22024
         BR    R9                 RETURN                         S22024
         SPACE
TYPELINE EQU   *                                                 S22024
         OI    TDFLG,LINE         SHOW LINE TEST                 S22024
         TM    TDFLG,SYM          TERMINAL FLAG SET              S22024
         BO    TDERR              YES - ERROR                    S22024
         LTR   R7,R7              TTE ADDR PRESENT               S22024
         BZ    TERM4              NO - EXIT                    @Y17XAUU
         TM    TRMSTATE,TRMPREF   3705 MODE                    @Y17XAUU
         BZ    TERM8              NO
         OI    TDFLG,RNLINE       YES - SET FLAG                 S22024
TERM8    EQU   *                                                 S22024
         B     TERM4              RETURN                         S22024
         DROP  R7                                                S22024
TDEVICE  MVC   WORK1(D0),WORK2    GET DEVICE ENTRY               S22024
         EJECT
UCBSAVE  DS    A                  UCB SAVE AREA                  S22024
SAVE     DS    5F                 SAVE AREA                      S22024
TRMTNT   DS    A                  TNT SAVE AREA
TRMSAV1  DS    3F                 REG SAVE AREA
TRMSAVE2 DS    F                  REG SAVE AREA                @Y17XAUU
TERNMTAB DS    A                  TERMNAME TABLE ADDR
TNTENTRY DS    A                  TTE ADDR FOR TEST TERMINAL
WORK4    DS    F                  WORK AREA
AVTUISV  DS    A                  ADDRESS OF BINARY SEARCH ROUTINE
WORKAREA DS    F                  WORKAREA
TDTNT    DS    A                  SAVE AREA FOR TD TNT ADDRESS @YM08136
KUCB     DS    H                  TEST UCB ADDR SAVE AREA
         DS    0D                 BOUNDARY ALLINGMENT
NUMBER   DC    X'0000000000000000' COUNT SAVE AREA
NUMB     EQU   NUMBER+D3          LAST DIGIT OF COUNT
WORK1    DS    3F                 WORK AREA
TRMWRK   DS    CL72               TRM WORK AREA
LCBLEN   DS    CL1                LENGTH OF LCB
TDFLG    DC    X'00'              PROGRAM FLAG BYTE
WORK3    DS    CL3                WORK AREA
NO       DS    CL1                WORK AREA
TERNMLEN DS    CL1                MAXIMUM LENGTH OF TD NAME
WORK2    DS    2D                 WORK AREA                      S22024
         SPACE
PAREMLST DS    0CL8               BINARY SEARCH P-LIST
         DS    CL1                OFFSET TO BIN. SEARCH ADDR
         DC    X'00'              FLAG BYTE
         DC    X'00'              FLAG BYTE
LENGTH   DS    CL1                LENGTH OF NAME
         DC    A(WORK1)           ADDRESS OF NAME
         SPACE
         DS    0F                 BOUNDARY ALLIGNMENT
RE       DS    0CL8               OP CONTROL P-LIST
OCQCB    DS    F                  ADDR OF OF CONTROL QCB
         DC    X'DC'              FLAG BYTE
         DC    XL3'00'            LINK FIELD
VERBCODE DS    CL1                OP CONTROL COMMAND
RELEN    DC    X'18'              P-LIST LENGTH
RLN      DS    CL1                REL. LINE NUMBER
RETCODE  DC    X'00'              RETURN CODE
         DC    A(ECB)             ECB ADDR
ADDR     DC    CL8' '             BLANK AREA
         SPACE
ECB      DS    2F                 ECB
         SPACE
REPAREM  DC    X'0C'              FLAG BYTE
         DC    AL3(RE)            FIRST ELEMENT
         DC    X'80'              FLAG BYTE
         DC    AL3(RE)            LAST ELEMENT
CEOUT2A  DC    C'IED332I '        MSG ID                         S22024
         SPACE
CEOUT2   DC    C'XXXXXXXX NOT OPENED' MSG - TEST LINE NOT OPENED
CEOUT2E  DS    0C                 END OF MSG
REGSAVE  DS    12F                REG SAVE AREA
K1       DC    F'1'               VALUE OF 1
F4       DC    F'4'               VALUE OF 4                     S22024
K3       DC    F'3'               VALUE OF 3
LCBST1AD DS    A                  LCB STATUS BYTE SAVE
MULT     DC    8XL1'0'            WORK AREA
TRMSLPL  DS    0F                 PARAMETER LIST FOR ACT. TERMINAL
         DC    X'0C'              FLAG BYTE
         DC    AL3(RQELE)         FIRST REQ ELEMENT
         DC    X'80'              FLAG BYTE
         DC    AL3(RQELE)         LAST REQ ELEMENT
         SPACE
BLANK    DC    8X'40'             BLANK AREA
ENDTABLE DC    X'FFFF'            END OF UCB TABLE             @G36XRUV
RNWORK   DS    C                  WORK AREA                      S22024
         SPACE
***********************************************************************
*                                                                     *
*        PARAMETER LIST FOR OPERATOR CONTROL COMMANDS                 *
*                                                                     *
***********************************************************************
         SPACE
RQELE    DS    0F                 OP CONTROL P-LIST
RQKEY    DC    X'00'              KEY
RQQCBA   DC    X'000000'          QCB ADDR
RQPRI    DC    X'DC'              PRIORITY
RQLINK   DC    X'000000'          LINK FIELD
RQVBCD   DC    X'00'              VERB CODE
RQELNG   DC    X'18'              LENGTH CONSTANT
RQRLN    DC    X'00'              RELATIVE LINE NUMBER
RQRTCD   DC    X'00'              RETURN CODE
RQECBA   DC    AL4(0)             ECB ADDR
RQLADR   DC    8X'40'             LINE ADDR OR TERMINAL NAME
RQND     DS    0C                 END OF ELEMENT                 S22024
*
         SPACE
TTEADDR  DC    F'0'               WORK AREA TO ALLIGN TTE ADDR
K32      DC    F'32'              VALUE OF 30
RQMOVE   MVC   RQLADR(D1),D0(R7)  MOVE NAME TO REQUEST ELEMENT
PATCH    DC    CL200' '           IEDQWC1 PATCH AREA
         EJECT
         TLGBD                                                   S22024
         EJECT
TIOT     DSECT
         IEFTIOT1
         EJECT
         EJECT
         TQCBD
         EJECT
         DCBD  DSORG=TX
         EJECT
         EJECT
         TPRFD
         EJECT
         TLCBD
         EJECT
CVT      DSECT
         CVT
         EJECT
UCB      DSECT
         IEFUCBOB
         EJECT
         TAVTD
         EJECT
         TDEBD
         EJECT
         TTNTD
         EJECT
         TTRMD
         EJECT
         OLTCB
         EJECT
RESPL    RESPL                                                 @Y17XAUU
         END
