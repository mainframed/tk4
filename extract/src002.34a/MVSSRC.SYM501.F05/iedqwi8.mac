QWI8     TITLE 'IEDQWI8 CONFIGURATOR SUB-MODULE 4'
IEDQWI8  CSECT
*A000000-999999                                                @Y16X5U0
*CHANGE ACTIVITY AS FOLLOWS:
*A570300-572400                                                 SA64782
*C571200                                                       @XA06337
*C808800                                                       @OY20528
**************************************************************** S22024
*                                                              * S22024
*                                                              * S22024
* MODULE NAME --                                               * S22024
*                                                              * S22024
*    IEDQWI8 (TCAM,TOTE)                                       @Y17XAUU
*                                                              * S22024
* DESCRIPTIVE NAME --                                          * S22024
*                                                              * S22024
*    TOTE CONFIGURATOR SUB-MODULE                              * S22024
*                                                              * S22024
* COPYRIGHT --                                                 * S22024
*                                                              * S22024
*    'NONE'                                                    * S22024
*                                                              * S99528
* STATUS --                                                    * S99528
*                                                              * S99528
*    VERSION 10.0                                              @Y17XAUU
*                                                              * S99528
* FUNCTION --                                                  * S99528
*                                                              * S99528
*    THE PURPOSE OF THIS MODULE IS TO VERIFY THAT A            @Y17XAUU
*    TERMINAL NAME IS IN THE TERMINAL NAME TABLE.              @Y17XAUU
*                                                              * S99528
*    THE MODULE ALSO OBTAINS THE POLLING AND ADDRESSING        * S99528
*    CHARACTERS FROM THE TERMINAL TABLE.                       * S99528
*                                                              * S99528
* ENTRY POINTS --                                              * S99528
*                                                              * S99528
*    - RECEIVES CONTROL FROM IEDQWIA VIA A 'LOAD' WHEN         * S99528
*      AN 'ADD' FUNCTION IS REQUESTED.                         * S99528
*    - RECEIVES CONTROL FROM IEDQWIA VIA A 'LOAD' WHEN         * S99528
*      A 'CHANGE' OR 'DELETE' FUNCTION IS REQUESTED            * S99528
*                                                              * S99528
* PURPOSE -- SEE FUNCTION                                      @Y17XAUU
*                                                              @Y17XAUU
* MODULE TYPE -- PROCEDURE                                     @Y17XAUU
*                                                              @Y17XAUU
* MODULE SIZE - REFER TO ASSEMBLER LISTING                     @Y17XAUU
*                                                              @Y17XAUU
* PROCESSOR -- ASSEMBLER XF                                    @Y17XAUU
*                                                              @Y17XAUU
* LINKAGE -- LINK TO FROM IEDQWIA, IEDQWID, AND IEDQWIE        @Y17XAUU
*                                                              * S99528
* MACRO -- SAVE, IEDHJN, IEDQMSG                               @Y17XAUU
*                                                              @Y17XAUU
* INPUT --                                                     * S99528
*                                                              * S99528
*    REGISTERS 02,03,05,11,13,14,15 CONTAIN THE FOLLOWING      * S99528
*    VALUES:                                                   * S99528
*                                                              * S99528
*         02 - OLTCB ADDRESS                                   * S99528
*         03 - BASE REGISTER                                   * S99528
*         05 - UCB ADDRESS                                     * S99528
*         11 - CALLING ROUTINE WORK AREA                       * S99528
*         13 - SAVE AREA ADDRESS                               * S99528
*         14 - RETURN ADDRESS                                  * S99528
*         15 - ENTRY POINT ADDRESS                             * S99528
*                                                              * S99528
* OUTPUT --                                                    * S99528
*                                                              * S99528
*    REGISTERS 01,14,15 CONTAIN THE FOLLOWING VALUES:          * S99528
*                                                              * S99528
*         01 - PARAMETER LIST ADDRESS                          * S99528
*         14 - RETURN FROM AVTUI                               * S99528
*         15 - AVTUI ADDRESS                                   * S99528
*                                                              * S99528
* EXTERNAL REFERENCES --                                       * S99528
*                                                              * S99528
*    AVTUI - TCAM BINARY SEARCH ROUTINE                        * S99528
*    IEDIAP04 - TTCIN TO NETWORK ADDRESS CONVERSION RTN        @Y17XAUU
*                                                              * S99528
* EXITS,NORMAL --                                              * S99528
*                                                              * S99528
*    BR AFTER 'PLRET' TO CALLING MODULE                        * S99528
*                                                              * S99528
* EXITS,ERROR --                                               * S99528
*                                                              * S99528
*    BR AFTER ERROR1' TO CALLING MODULE                        * S99528
*                                                              * S99528
* TABLES/WORK AREAS --                                         * S99528
*                                                              * S99528
*    TERMNAME TABLE, TERMINAL TABLE, UCB, CDS WORK AREA        * S99528
*                                                              * S99528
* ATTRIBUTES --                                                * S99528
*                                                              * S99528
*    ENABLED, PROBLEM PROGRAM MODE, TRANSIENT                  * S99528
*                                                              * S99528
* NOTES -- SEE BELOW                                           @Y17XAUU
*                                                              @Y17XAUU
*  RESTRICTIONS-- NONE                                         @Y17XAUU
*                                                              @Y17XAUU
*  DEPENDENCIES -- EBCDIC CHARACTER CODE                       @Y17XAUU
*                                                              @Y17XAUU
*  REGISTERS CONVENTIONS -- SEE REGISTERS ASSIGNMENT           @Y17XAUU
*                                                              @Y17XAUU
*  PATCH LABEL -- PATCH                                        @Y17XAUU
*                                                              * S99528
*                                                              * S99528
****************************************************************
         EJECT
**************************************************************** S99528
*                                                              *
*              EQUATES                                         *
*                                                              *
**************************************************************** S99528
R0       EQU   0                       REGISTER 0                S22024
R1       EQU   1                       REGISTER 1                S22024
WORKREG1 EQU   1                       WORK REGISTER 1           S22024
WORKREG4 EQU   4                       WORK REGISTER 4           S22024
WORKREG5 EQU   5                       WORK REGISTER 5           S22024
WORKREG6 EQU   6                       WORK REGISTER 6           S22024
WORKREG7 EQU   7                       WORK REGISTER 7           S22024
WORKREG8 EQU   8                       WORK REGISTER 8           S22024
RTRM     EQU   8                       TTE PTR                 @Y17XAUU
RETURN   EQU   9                       RETURN REGISTER           S22024
R12      EQU   12                      REGISTER 12               S22024
R13      EQU   13                      SAVE AREA ADDRESS         S22024
R14      EQU   14                      RETURN ADDRESS            S22024
R15      EQU   15                      ENTRY POINT ADDRESS       S22024
R2       EQU   2                       OLTCB POINTER             S99528
PARMREG0 EQU   0                       PARAMETER REG 0           S99528
PARMREG1 EQU   1                       PARAMETER REG 1           S99528
BASEREG  EQU   3                       BASE REGISTER             S99528
ENTRYPT  EQU   15                      ENTRY POINT ADDR          S99528
SAVEAREA EQU   13                      POINTER TO SAVE AREA      S99528
SAVEADDR EQU   15                      SAVE AREA ADDR            S99528
RETCODE  EQU   15                      RETURN CODE               S99528
CALLERS  EQU   8                       OFFSET TO CALLERS SAVE    S99528
OWN      EQU   4                       OFFSET TO OWN SAVE        S99528
NAMLEN   EQU   8                       LENGTH OF SYM NAME        S99528
BRTBLADR EQU   8                       BRANCH TABLE ADDR         S99528
CHANGE   EQU   X'06'                   CHANGE FLAG               S99528
BADNAME  EQU   X'04'                   BAD SYM NAME TEST         S99528
TWO      EQU   2                       OFFSET FOR CHAR LENGTH    S22025
ONE      EQU   1                       OFFSET                  @Y17XAUU
FOUR     EQU   4                       OFFSET FOR BRANCH         S22025
EIGHT    EQU   8                       OFFSET FOR A1 ADDR        S22025
D0       EQU   0                        DISPLACEMENT 0           S99528
D11      EQU   11                       DISPLACEMENT 11          S99528
CONCTERM EQU   X'80'                    FLAG SETTING INDICATING  S99528
*                                        CONCENTRATOR TERMINAL   S99528
DO       EQU   0                       CONSTANT DISPLACEMENT 0   S22024
D1       EQU   1                       DISPLACEMENT 1            S22024
D2       EQU   2                       CONSTANT TWO              S22024
D8       EQU   8                       DISPLACEMENT 8            S22024
L1       EQU   1                       CONSTANT LENGTH FOR MOVE  S22024
CONCSELF EQU   X'E0'                   CONCENTRATOR ENTRY NAMED  S22024
SPECIAL  EQU   X'F0'                   SPECIAL CONFIG RUN  -     S22024
*                                        ADD/CHANGE TO LOCAL CDS S22024
         EJECT
**************************************************************** S99528
*                                                              *
*        INITIALIZATION                                        *
*                                                              *
**************************************************************** S99528
         SAVE  (14,12)                 SAVE CALLER'S REGISTERS   S22024
         LR    BASEREG,ENTRYPT              LOAD BASE REG.       S99528
         USING IEDQWI8,BASEREG         ESTAB PGM BASE            S99528
         USING TOTOLTCB,R2             ESTAB OLTCB BASE          S99528
IEDQWI8  IEDHJN IEDQWI88,HJN           SET DATE AND MODULE NAME  S22024
         LA    SAVEADDR,QWI8SAVE       LOAD ADDR. OF OWN SAVE AR S99528
         ST    SAVEADDR,CALLERS(SAVEAREA)   STORE ADDR.IN CALLER S99528
         ST    SAVEAREA,OWN(SAVEADDR)       STORE CALLERS IN OWN S99528
         LR    SAVEAREA,SAVEADDR       GET SAVE ADDR             S99528
**************************************************************** S99528
*                                                              * S99528
* ROUTINE TO PICK UP TERMNAME TABLE FROM TCAM
*                                                              * S99528
**************************************************************** S99528
         L     WORKREG6,TOTAVTPT       GET ADDRESS OF AVT        S22024
         USING IEDQAVTD,WORKREG6
         L     WORKREG7,AVTMSGS-1      GET VCON TABLE ADDR       S22025
         L     WORKREG7,EIGHT(,WORKREG7) LOAD ADDR OF A1 ROUTINE S22025
         ST    7,AVTUISV               SAVE IT
         MVC   PAREM(1),AVTBSX      GET AVT BSX
         ST    WORKREG6,AVTADDR         SAVE
         L     WORKREG7,AVTCSTCS   PICK UP DCT ADDR
         ST    WORKREG7,DCTADDR         SAVE
         L     WORKREG7,AVTRNMPT   PICK UP TERMNAME TABLE ADDR
         ST    WORKREG7,TNTABLE         SAVE
         DROP  WORKREG6
**************************************************************** S99528
*                                                              * S99528
*   D E T E R M I N E   I F   S Y M B O L I C   N A M E   V A L  S99528
*                                                              * S99528
**************************************************************** S99528
         SPACE
NAMESRCH EQU   *                       FIND LENGTH OF TERMINAL N S99528
         SR    WORKREG4,WORKREG4       CLEAR WORK REG            S99528
         LA    WORKREG6,CEMSG          PLACE ADDR ON NAME IN WRK S99528
BLNKTEST CLI   0(WORKREG6),X'40'       END OF NAME?              S99528
         BE    BLNKFIND                BR EQUAL WITH LEN IN R4   S99528
         LA    WORKREG4,1(WORKREG4)  INCREMENT POINTERS
         LA    WORKREG6,1(WORKREG6)
         LA    WORKREG8,8              SET UP COMPARAND
         CR    WORKREG8,WORKREG4       GT 8?
         BE    BLNKFIND                YES                       S99528
         B     BLNKTEST                BRANCH TO BLANK TEST      S99528
BLNKFIND STC   4,LENGTH                SET UP                    S99528
TERMSRCH EQU   *
         LA    WORKREG1,CEMSG          SET UP TO PAD WITH BLANKS S99528
         AR    WORKREG1,WORKREG4       POINT TO END OF NAME
         LA    R15,8
         CR    WORKREG4,R15            ALREADY HAVE 8 CHARS?
         BE    TERSCH1                 YES BRANCH
         SR    R15,WORKREG4            GET NUMBER OF BLANKS TO MOVE
         EX    R15,BLMOVE              MOVE IN BLANKS            S99528
TERSCH1  EQU   *
         LA    WORKREG7,CEMSG          GET MESSAGE ADDR          S99528
         ST    WORKREG7,MSGADDR        STORE ADDR                S99528
         LA    WORKREG1,PAREM
         L     R15,AVTUISV             SEARCH
         LH    R0,TWO(,R1)             LOAD CHAR STRING LEN      S22025
         L     R1,FOUR(,R1)            LOAD ADDR OF CHARS        S22025
         BAL   R14,FOUR(R15)           BRANCH TO SCAN ROUTINE    S22025
         LTR   R15,R15                 NAME FOUND
         BZ    ERROR1                  NO GO TO ERROR ROUT
TERM1    LR    WORKREG1,R15            PUT OFFSET IN R1
         LR    WORKREG8,R15            SAVE TERMNAME TABLE INDEX
         L     R15,TNTABLE             PICK UP TERMNAME TAB ADDR
         BALR  R14,R15                 GO GET ENTRY
         ST    WORKREG1,TERTABLE  SAVE TERMINAL TABLE ADDR
         STH   WORKREG8,WORK2     SAVE TERMINAL TABLE INDEX
         LA    R14,TRMPRFSZ            GET TTE PREFIX SIZE     @Y17XAUU
         SLR   WORKREG1,R14            POINT TO TTE PREFIX     @Y17XAUU
         USING IEDNTRM,WORKREG1        SET TTE ADDRESSABILITY  @Y17XAUU
         TM    TRMDSORG,TRMLGB         IS THIS A LGB?           SA64782
         BO    ERROR1B                 YES, ERROR               SA64782
         TM    TRMSTATE,TRMPROC+TRMLIST IS THIS LOG            @XA06337
*                                                 LIST, OR      SA64782
*                                                 PROCESS ENTRY?SA64782
         BNZ   ERROR1B                 YES, ERROR               SA64782
         CLI   TRMTYPE,TRMLNCP         LOCAL NCP ENTRY         @Y17XAUU
         BE    ERROR1B                 YES - ERROR             @Y17XAUU
         CLI   TRMTYPE,TRMPUNT         PU ENTRY                @Y17XAUU
         BE    ERROR1B                 YES - ERROR             @Y17XAUU
         DROP  WORKREG1                DROP TRM BASE            SA64782
         B     TERMFIND                GO FIND TERMINAL          S99528
ERROR1   EQU   *                       HANDLE ERROR              S99528
         MVC   CDSMEMBR(NAMLEN),CEMSG MOVE NAME TO CDSBLDL LIST  S22024
         IEDQMSG MSGID=009,FUNCT=REP,INBUF=CEMSG,INCNT=3,              *
               LINK=YES                                          S22024
         CLC   CEMSG(L1),NCPANS   IS THIS A NCP CDS?             S22024
         BE    ERROR1C            YES, SET SPECIAL FLAGS         S22024
         CLC   CEMSG(L1),EXTANS   IS THIS A 3705 EXTENSION?      S22024
         BE    ERROR1A             YES                           S22024
         CLC   CEMSG(L1),NEGANS    NEGATIVE RESPONSE?            S22024
         BE    ERROR1B             YES                           S22024
         B     ERROR1              BAD RESPONSE ASK QUEST AGAIN  S22024
ERROR1B  DS    0H                  ALIGN FO INSTRUCTION          S22024
         MVI   CDSRETFL,BADNAME        SET ERROR RETURN CODE     S99528
         B     PLRET                   BR TO RETURN PROCEDURE    S99528
ERROR1A  DS    0H                  ALIGN FO INSTRUCTION          S22024
         MVI   CDSRETFL,SPECIAL    MOVE IN SPECIAL FLAG          S22024
         B     PLRET               BRANCH TO RETURN PROCEDURE    S22024
ERROR1C  EQU   *                                                 S22024
         MVI   CDSRETFL,SPECIAL   TURN ON SPECIAL CONFIG         S22024
         MVI   CDSCFLGS,SPECIAL   INDICATE THIS IS NCP CDS       S22024
         B     PLRET              GO TO RETURN ROUTINE           S22024
         EJECT
**************************************************************** S99528
*                                                              * S99528
*    M O V E   N A M E   T O   C D S B L D L   L I S T           S99528
*                                                              * S99528
**************************************************************** S99528
TERMFIND MVC   CDSMEMBR(NAMLEN),CEMSG  MOVE NAME TO CDSBLDL LIST S99528
         DS    0H                      ESTAB HALFWORD BOUND      S99528
         L     RETCODE,FOUND           SET RET CODE TO FOUND     S99528
         L     RTRM,TERTABLE           GET TERMINAL TABLE ADDR @YM07417
         SPACE 3
**************************************************************** S99528
*                                                              * S99528
*    P I C K   U P   D E V I C E   D E P E N D E N T   F L D   A S99528
*                                                              * S99528
**************************************************************** S99528
TERM2    EQU   *
         USING IEDNTRM,RTRM            SET UP ADDRESSIBILITY   @Y17XAUU
*                                        TERMINAL ENTRY          S22024
         LA    WORKREG4,TRMPRFSZ       GET TTE PREFIX SIZE     @Y17XAUU
         SLR   RTRM,WORKREG4           POINT TO TTE PREFIX     @Y17XAUU
         TM    TRMSTATE,TRMPREF        3705 MODE?              @Y17XAUU
         BZ    DIAL                    BYPASS RSID GET           S22024
         LR    WORKREG4,WORKREG1       SAVE TTE ADDRESS        @Y17XAUU
         LH    WORKREG1,WORK2          GET TTCIN               @Y17XAUU
         L     R15,TOTAVTPT            GET AVT ADDRESS         @Y17XAUU
         USING IEDQAVTD,R15            SET AVT ADDRESSABILITY  @Y17XAUU
         L     R15,AVTSAVTP            GET SAVT ADDRESS        @Y17XAUU
         DROP  R15                                             @Y17XAUU
         LA    R15,D0(R15)             CLEAR FLAG BYTE         @Y17XAUU
         USING IEDNSVTD,R15            SET SAVT ADDRESSABILITY @Y17XAUU
         L     R15,SAVTTNTX            GET ADDRESS OF IEDIAP04 @Y17XAUU
         BALR  R14,R15                 CALL CONVERSION ROUTINE @Y17XAUU
         STH   R15,CDSREC              MOVE NETWORK ADDRESS    @YM07278
*                                      INTO CDS                @Y17XAUU
DIAL     EQU   *
         L     R14,TOTAVTPT            GET TCAM AVT ADDR       @Y17XAUU
         USING IEDQAVTD,R14            EST. ADDRESSABILITY     @Y17XAUU
         L     R15,AVTDDFT             GET QTL ADDRESS         @Y17XAUU
         DROP  R14                     RELEASE                 @Y17XAUU
         TM    TRMDEVFL,TRMDDIG        ANY DIAL DIGITS         @Y17XAUU
         BZ    IDCHK                   NO BRANCH
         LA    R0,TRMDDIG              SET THE MASK FOR QTL    @Y17XAUU
         LA    R1,TRMPRFSZ             PREFIX LENGTH           @Y17XAUU
         ALR   R1,RTRM                 POINT TO POSTIVE        @Y17XAUU
         BALR  R14,R15                 GO GET ADDR OF DIAL D   @Y17XAUU
         IC    WORKREG4,AVTEZERO(R1)   GET THE LENGTH          @Y17XAUU
         LA    R14,CDS#DIAL
         EX    WORKREG4,GENMOVE        MOVE DIAL DIGITS TO CDS @Y17XAUU
IDCHK    EQU   *
         TM    TRMDEVFL,TRMADDC        ADDRESSING CHARS?       @Y17XAUU
         BZ    INVITE                  NO BRANCH
         LA    R0,TRMADDC              SET THE MASK FOR QTL    @Y17XAUU
         LA    R1,TRMPRFSZ             PREFIX LENGTH           @Y17XAUU
         ALR   R1,RTRM                 POINT TO POSTIVE        @Y17XAUU
         L     R14,TOTAVTPT             GET TCAM AVT ADDRESS   @VM07417
         USING IEDQAVTD,R14             EST. ADDRESSABILITY    @YM07417
         L     R15,AVTDDFT              GET QTL ADDRESS        @YM07417
         DROP  R14                      RELEASE                @YM07417
         BALR  R14,R15                 GET ADDRESS OF DIAL DIGT@Y17XAUU
         IC    WORKREG4,AVTEZERO(R1)   GET THE LENGTH          @Y17XAUU
         B     IDCHK1                  CHECK ID                  S99528
GENMOVE  MVC   D0(D0,R14),AVTEZERO(R1)       MOVE CHARS TO CDS @Y17XAUU
BLMOVE   MVC   D0(D0,WORKREG1),BLANKS  MOVE BLANKS INTO FIELD    S22024
IDCHK1   EQU   *
         LA    R14,CDS#ADDR
         EX    WORKREG4,GENMOVE        MOVE COMP CHARS TO DCHB
INVITE   EQU   *
         CLI   FLAG,CONCTERM            IS THIS THE CONCENTRATOR S99528
*                                         ENTRY?                 S99528
         BE    PLRET                    YES, GO RETURN TO CALLER S99528
         TM    TRMDEVFL+ONE,TRMCONC    IS THE TERMINAL ATTACHED@Y17XAUU
*                                         TO A CONCENTRATOR?     S99528
         BZ    PLRET                    NO, RETURN TO CALLER     S99528
         L     WORKREG8,TRMDESTQ-ONE    GET ADDRESS OF DEST QCB  S99528
         USING IEDQQCB,WORKREG8                                  S99528
         TM    QCBDSFLG,QCBDRQQ        IS QCB A DATA READY QUEUE?S22024
         BZ    INVITE1                 NO, TERMINAL ATTACHED TO  S22024
*                                        CONCENTRATOR - CONTINUE S22024
         IEDQMSG MSGID=105,FUNCT=CEC   ISSUE CONCENTRATOR MSG    S22024
         MVI   CDSRETFL,CONCSELF       SET RETURN CODE TO CONC-  S22024
*                                        ENTRATOR REQUEST        S22024
         B     PLRET                   GO RETURN TO CALLER       S22024
INVITE1  EQU   *                                                 S22024
         LA    WORKREG5,QCBDSFLG        GET ADDRESS OF QCB       S99528
         AH    WORKREG5,QCBEXTO         ADD OFFSET TO QCB        S99528
*                                         EXTENSION              S99528
         DROP  WORKREG8                                          S99528
         L     WORKREG1,D0(WORKREG5)    GET ADDRESS OF CONCEN-   S99528
*                                         TRATOR TERMINAL ENTRY  S99528
         MVI   FLAG,CONCTERM            SET FLAG INDICATING      S99528
*                                         THIS IS CONCENTRATOR   S99528
*                                         ENTRY
         B     TERM2                    GO CHECK ADDRESSING CHARSS99528
PLRET    EQU   *                       RETURN PROCEDURE          S99528
         L     R13,4(R13)              GET ADDR. OF CALLERS SAVE S99528
         L     R14,12(R13)             RESTORE R14               S99528
         LM    R0,R12,20(13)           RESTORE CALLES REGISTERS  S99528
         BR    RETURN                  RETURN TO CALLING MODULE  S99528
         EJECT
         DS    0F
TNTABLE  DS    F                       TERM NAME TAB ADDR        S99528
INVADDR  DS    F                       INIVIT LIST ADDR          S99528
DCTADDR  DS    F                       DCT ADDRESS               S99528
AVTADDR  DS    F                       AVT ADDR                  S99528
SCTADD   DS    F                       SCT ADDRESS               S99528
WORK1    DS    F                       WORK AREA                 S99528
BLANKS   DC    CL8'        '           BLAKS FOR TEST            S99528
FOUND    DC    X'00'                   RETURN CODE TEST          S99528
PATCH    DC    CL50' '                 MAINTENANCE AREA          S99528
         DS    0F                                                S99528
QWI8SAVE DS    18F                     SAVE AREA                 S99528
PAREM    DS    0CL8                    PARAMETER LIST            S99528
         DS    CL1                     WORK AREA                 S99528
         DC    X'00'                   ZERO CONSTANT             S99528
         DC    X'00'                   ZERO CONSTANT             S99528
LENGTH   DS    CL1                     LENGTH                    S99528
MSGADDR  DS    F                       MESSAGE ADDRESS           S99528
FLAG     DC    XL1'00'                  FLAG BYTE                S99528
AVTUISV  DS    F                       ADDRRESS OF BINARY SEARCH
POSANS   DC    C'Y'                POSITIVE RESPONSE             S22024
NEGANS   DC    C'N'                NEGATIVE RESPONSE             S22024
NCPANS   DC    C'C'               NCP CDS REPLY                  S22024
EXTANS   DC    C'E'               3705 EXTENSION REPLY           S22024
         EJECT
         TQCBD                                                   S99528
         EJECT
*                                        ROUTINE
         OLTCB CONFG=YES               OLTCB DSECT               S99528
CEMSG    EQU   TOTINBUF                EQU TO INPUT MESSAGE      S99528
TERTABLE DS    F                        SAVE TTE ADDRESS       @OY20528
WORK2    EQU   CDSCODE                 PTR TO WORK AREA          S99528
         EJECT
         TTRMD                                                   S22024
         EJECT
         TAVTD                                                   S22024
         EJECT
CVT      DSECT                                                   S22024
         CVT                                                     S22024
         END
