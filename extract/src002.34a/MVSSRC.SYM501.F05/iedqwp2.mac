QWP2     TITLE 'IEDQWP2 - TOTE DPRINT SERVICE MODULE (3 OF 3)'
IEDQWP2  CSECT
*A000000-999999                                                @Y17XAUU
* CHANGE ACTIVITY AS FOLLOWS:                                    S22024
*A166000                                                        SA41596
*A133000,698900,821000,905000                                  @Y17XAUU
**************************************************************** S99528
*                                                              * S99528
* MODULE NAME = IEDQWP2 (TCAM,TOTE)                            @Y17XAUU
*                                                              * S22024
* DESCRIPTIVE NAME = TOTE DPRINT MODULE 3                      * S22024
*                                                              * S22024
* COPYRIGHT = NONE                                             * S22024
*                                                              * S22024
* STATUS --                                                    * S99528
*                                                              * S99528
*    VERSION 10.0                                              @Y17XAUU
*                                                              * S99528
* FUNCTION --                                                  * S99528
*                                                              * S99528
*    THIS ROUTINE CONTINUES SERVICING OF THE DPRINT MACRO.     * S99528
*                                                              * S99528
*    THE CALLING ROUTINE'S DPRINT PARAMETER LIST IS EXAMINED.  * S99528
*    IF SENSE, DATA OR COMMENT LINES ARE REQUIRED THE LINES    * S99528
*    ARE FORMATTED ONE AT A TIME AND IEDQWO (ACCESS MANAGER)   * S99528
*    IS CALLED TO ROUTE THE FORMATTED LINE TO THE PROPER       * S99528
*    OUTPUT DEVICE.                                            * S99528
*                                                              * S99528
*    CONTROL IS THEN RETURNED TO THE CALLING PROGRAM.          * S99528
*                                                              * S99528
*    AT THE COMPLETION FO THIS ROUTINE, REGISTER 15 WILL       * S99528
*    CONTAIN X'00' RETURN CODE.                                * S99528
*                                                              * S99528
* ENTRY POINT --                                               * S99528
*                                                              * S99528
*    IEDQWP2 - CALLED BY IEDQWP1                               * S99528
*                                                              * S99528
* PROCESSOR -- ASSEMBLER XF                                    @Y17XAUU
*                                                              @Y17XAUU
* MODULE TYPE -- PROCEDURE                                     @Y17XAUU
*                                                              @Y17XAUU
* MODULE SIZE -- 2K MAXIMUM                                    @Y17XAUU
*                                                              @Y17XAUU
* PURPOSE -- SEE FUNCTION                                      @Y17XAUU
*                                                              @Y17XAUU
* LINKAGE -- BALR R14,R15 FROM IEDQWA                          @Y17XAUU
*                                                              @Y17XAUU
* MACROS -- IEDHJN,SAVE                                        @Y17XAUU
*                                                              @Y17XAUU
* INPUT --                                                     * S99528
*                                                              * S99528
*    REGISTER 01,02,03,13,14,15 CONTAIN THE FOLLOWING VALUES:  * S99528
*                                                              * S99528
*         01 - DPRINT PARAMETER LIST ADDRESS                   * S99528
*         02 - OLTCB ADDRESS                                   * S99528
*         03 - SECTION PREFACE ADDRESS                         * S99528
*         13 - SAVE AREA ADDRESS                               * S99528
*         14 - RETURN ADDRESS                                  * S99528
*         15 - ENTRY POINT ADDRESS                             * S99528
*                                                              * S99528
* OUTPUT --                                                    * S99528
*                                                              * S99528
*    REGISTER 15 CONTAINS THE FOLLOWING RETURN CODE:           * S99528
*                                                              * S99528
*         X'00' - NORMAL COMPLETION                            * S99528
*                                                              * S99528
* EXTERNAL REFERENCES --                                       * S99528
*                                                              * S99528
*    IEDQWO - ACCESS MANAGER - ROUTE THE OUTPUT                * S99528
*             MESSAGE LINE.                                    * S99528
*                                                              * S99528
* EXITS,NORMAL --                                              * S99528
*                                                              * S99528
*    BR AFTER 'DPR500' TO IEDQWA                               * S99528
*                                                              * S99528
* EXITS,ERROR --                                               * S99528
*                                                              * S99528
*    NONE                                                      * S99528
*                                                              * S99528
* TABLES/WORK AREAS --                                         * S99528
*                                                              * S99528
*    SCT, OLTCB, OLT SECTION PREFACE, RESPL                    @Y17XAUU
*                                                              * S99528
* ATTRIBUTES --                                                * S99528
*                                                              * S99528
*    ENABLED, PROBLEM PROGRAM MODE, RESIDENT, REENTRANT        @Y17XAUU
*                                                              * S99528
* NOTES -- SEE BELOW                                           @Y17XAUU
*                                                              @Y17XAUU
*  DEPENDENCIES -- EBCDIC CHARACTER CODE                       @Y17XAUU
*                                                              @Y17XAUU
*  RESTRICTIONS -- NONE                                        @Y17XAUU
*                                                              @Y17XAUU
*  REGISTERS CONVENTION -- SEE REGISTERS ASSIGNMENT            @Y17XAUU
*                                                              @Y17XAUU
*  PATCH LABEL -- PATCH                                        @Y17XAUU
*                                                              * S99528
**************************************************************** S99528
         EJECT
MULTDROP EQU   X'10'              MULTI-DROP TERMINAL            S99528
R0       EQU   0                  REGISTER 0                     S99528
R1       EQU   1                  PARAMETER LIST ADDRESS         S99528
R2       EQU   2                  OLTCB ADDRESS                  S99528
R3       EQU   3                  SECTION PREFACE ADDRESS        S99528
R4       EQU   4                  BASE REGISTER                  S99528
R5       EQU   5                  REGISTER 5                     S99528
R6       EQU   6                  REGISTER 6                     S99528
R7       EQU   7                  REGISTER 7                     S99528
R8       EQU   8                  REGISTER 8                     S99528
R9       EQU   9                  REGISTER 9                     S99528
R10      EQU   10                 REGISTER 10                    S99528
R11      EQU   11                 REGISTER 11                    S99528
R12      EQU   12                 REGISTER 12                    S99528
R13      EQU   13                 SAVE AREA ADDRESS              S99528
R14      EQU   14                 RETURN ADDRESS                 S99528
R15      EQU   15                 ENTRY POINT ADDRESS
D0       EQU   0                  DISP OF 0                      S22024
D1       EQU   1                  DISP OF 1                      S22024
D3       EQU   3                  DISP OF 3                      S22024
D4       EQU   4                  DISP OF 4                      S22024
D6       EQU   6                  DISP OF 6                      S22024
D9       EQU   9                  DISP OF 9                      S22024
D28      EQU   28                 DISP OF 28                     S22024
XFF      EQU   X'FF'              MASK ALL BYTE                  S22024
DISREG2  EQU   28                 DISPLACEMENT TO REG 2        @Y17XAUU
         SPACE 2
         USING IEDQWP2,R15
*
IEDQWP2  IEDHJN STARP2,HJN
*
         DROP  R15
         USING IEDQWP2,R4
         USING TOTOLTCB,R2        OLTCB BASE                     S22024
         USING SPREF1,R3          SECT. PREFACE DSECT PART 1
         SPACE
         SAVE  (14,12)
         LR    R4,R15             SET BASE REG.
         LA    R15,TOTSAVE1       MY SAVE AREA                   S22024
         ST    R15,8(R13)         PUT ADDR IN CALLERS SAVE
         ST    R13,4(R15)         SAVE ADDR OF CALLERS SAVE AREA.
         LR    R13,R15
         SPACE
         L     R12,4(R1)          GET ADDR OF DPRINT CONTROL WORD.
         SPACE
         LA    R10,EPINDX         EP MASK CHECK FIELD ADDR       S22024
         SR    R14,R14            CLEAR INDEX REG                S22024
         IC    R14,TOTFLG08       GET LEVEL CNTL FLAG (BITS 4-7) S22024
         SLL   R14,D28            CLEAR                          S22024
         SRL   R14,D28            UNWANTED BITS                  S22024
         AR    R10,R14            POINT TO EP LEV CTL FLAG       S22024
         MVC   EPSAVE(D1),D0(R10) SAVE IT                        S22024
         L     R3,TOTOLTEN        SECTION PREFACE BASE          SA41596
         SPACE
         SPACE 2
***********************************************************************
*        EXPECTED SENSE                                               *
***********************************************************************
         TM    EPSAVE,RESULTS     PRINT RESULTS                  S22024
         BZ    DPR300             BRANCH IF NO                   S22024
         SPACE
DPR170   TM    2(R12),X'02'       PRINT XPTD SENSE ?
         BZ    DPR185             BRANCH IF NO.
         SPACE
         MVC   PB+D1(D4),DPXPTD   PUT 'XPTD' IN PRINT BUFFER     S22024
         MVC   PB+D6(D3),DPSNS    PUT 'SNS' IN PRINT BUFFER      S22024
         SPACE
         SR    R7,R7              CLEAR R7.
         IC    R7,14(R12)         GET NO. OF XPTD SENSE BYTES.
         CH    R7,MAXSNS          COUNT GREATER THAN MAX ?
         BNH   DPR175             BRANCH IF NO.
         LH    R7,MAXSNS          SET COUNT = MAX.
DPR175   EQU   *
         SPACE
         L     R13,40(R1)         GET XPTD SENSE ADDR.
         LA    R14,WRK            GET OUTPUT ADDR FOR CONVERT.
         LR    R15,R7             GET COUNT.
         BAL   R10,DPRCNV         CONVERT XPTD SENSE TO EBCDIC.
         SPACE
         LA    R8,PB+12           GET PRINT BUFF ADDR FOR 1ST BYTE.
         LA    R5,WRK             GET ADDR OF 1ST SNS BYTE.
DPR180   MVC   0(2,R8),0(R5)      MOVE SENSE 1 BYTE AT A TIME.
         LA    R8,3(R8)           GET NEXT PRINT BUFFER ADDR.
         LA    R5,2(R5)           GET NEXT SENSE BYTE ADDR.
         BCT   R7,DPR180          MOVE NEXT BYTE.
         BAL   R10,DPRPNT         PRINT EXPECTED SENSE.
         SPACE 2
***********************************************************************
*        RECEIVED SENSE                                               *
***********************************************************************
         SPACE
DPR185   TM    2(R12),X'01'       PRINT RECEIVED SENSE.
         BZ    DPR205             BRANCH IF NO.
         SPACE
         TM    1(R12),X'02'       SENSE ERROR.
         BZ    DPR190             BRANCH IF NO.
         MVI   PB,C'*'            PREFIX RCVD SENSE WITH '*'.
         SPACE
DPR190   DS    0H                 BOUNDARY ALLIGNMENT            S22024
         MVC   PB+D1(D4),DPRCVD   PUT 'RCVD' IN PRINT BUFFER     S22024
         MVC   PB+D6(D3),DPSNS    PUT 'SNS' IN PRINT BUFFER      S22024
         SR    R7,R7              CLEAR R7.
         IC    R7,15(R12)         GET NO. OF RCVD SENSE BYTES
         CH    R7,MAXSNS          IS COUNT GREATER THAN ALLOWED ?
         BNH   DPR195             BRANCH IF NO.
         LH    R7,MAXSNS          SET COUNT = MAX.
         SPACE
DPR195   EQU   *
         SPACE
         L     R13,44(R1)         GET RCVD SENSE ADDR.
         LA    R14,WRK            GET OUTPUT ADDR FOR CONVERT.
         LR    R15,R7             GET COUNT.
         BAL   R10,DPRCNV         CONVERT RCVD SENSE TO EBCDIC.
         SPACE
         LA    R8,PB+12           GET PRINT BUFF ADDR FOR 1ST BYTE.
         LA    R5,WRK             GET ADDR OF 1ST SNS BYTE.
DPR200   MVC   0(2,R8),0(R5)      MOVE SENSE 1 BYTE AT A TIME.
         LA    R8,3(R8)           GET NEXT PRINT BUFFER ADDR.
         LA    R5,2(R5)           GET NEXT SENSE BYTE ADDR.
         BCT   R7,DPR200          MOVE NEXT BYTE.
         BAL   R10,DPRPNT         PRINT RECEIVED SENSE.
         SPACE 2
***********************************************************************
*        EXPECTED DATA                                                *
***********************************************************************
         SPACE
DPR205   TM    3(R12),X'80'       PRINT EXPECTED DATA ?
         BZ    DPR250             BRANCH IF NO.
         SPACE
         MVC   PB+D1(D9),DPXPTDAT PUT 'XPTD DATA' IN PRINT BUF   S22024
         SR    R7,R7
         IC    R7,17(R12)         GET FIRST HALF OF XPTD DATA COUNT.
         SLL   R7,8               ADJUST.
         IC    R7,18(R12)         GET SECOND HALD OF COUNT.
         LTR   R7,R7              IS COUNT = 0 ?
         BZ    DPR235             BRANCH IF YES.
         SPACE
         L     R8,48(R1)          GET XPTD DATA ADDR.
         SPACE
         CH    R7,LIMIT           MORE THAN 1 LINE OF PRINT ?
         BNH   DPR230             BRANCH IF NO.
               SPACE
DPR215   LR    R13,R8             GET ADDR OF XPTD DATA.
         LA    R14,PB+11          GET OUTPUT ADDR FOR CONVERT.
         LH    R15,LIMIT          GET COUNT.
         BAL   R10,DPRCNV         CONVERT XPTD DATA TO EBCDIC.
         BAL   R10,DPRPNT         PRINT XPTD DATA.
         SPACE
         AH    R8,LIMIT           GET NEW DATA ADDR.
         SH    R7,LIMIT           GET NO. BYTES REMAINING.
         CH    R7,LIMIT           MORE THAN 1 LINE OF PRINT LEFT ?
         BH    DPR215             BRANCH IF YES.
         SPACE
         LTR   R7,R7              REMAINING COUNT = 0 ?
         BZ    DPR230             BRANCH IF YES.
         SPACE
DPR230   LR    R13,R8             GET ADDR OF XPTD DATA.
         LA    R14,PB+11          GET OUTPUT ADDR FOR CONVERT.
         LR    R15,R7             GET COUNT.
         BAL   R10,DPRCNV         CONVERT XPTD DATA TO EBCDIC.
DPR235   BAL   R10,DPRPNT         PRINT XPTD DATA.
         SPACE  2
***********************************************************************
*        RECEIVED DATA                                                *
***********************************************************************
         SPACE
DPR250   TM    3(R12),X'40'       PRINT RECEIVED DATA ?
         BZ    DPR270             BRANCH IF NO.
         SPACE
         MVC   PB+D1(D9),DPRCVDAT PUT 'RCVD DATA' IN PRINT BUF   S22024
         TM    3(R12),X'08'       RCVD DATA ERROR ?
         BZ    DPR252             BRANCH IF NO.
         MVI   PB,C'*'            PREFIX RCVD DATA WITH '*'.
         SPACE
DPR252   EQU   *
         SPACE
         SR    R7,R7
         IC    R7,19(R12)         GET FIRST HALF OF RCVD DATA COUNT.
         SLL   R7,8               ADJUST.
         IC    R7,20(R12)         GET SECOND HALF OF COUNT.
         LTR   R7,R7              IS COUNT = 0 ?
         BZ    DPR265             BRANCH IF YES.
         SPACE
         L     R8,52(R1)          GET RCVD DATA ADDR.
         SPACE
         CH    R7,LIMIT           MORE THAN 1 LINE OF PRINT ?
         BNH   DPR260             BRANCH IF NO.
         SPACE
DPR255   LR    R13,R8             GET ADDR OF RCVD DATA.
         LA    R14,PB+11          GET OUTPUT ADDR FOR CONVERT.
         LH    R15,LIMIT          GET COUNT.
         BAL   R10,DPRCNV         CONVERT RCVD DATA TO EBCDIC.
         BAL   R10,DPRPNT         PRINT RCVD DATA.
         SPACE
         AH    R8,LIMIT           GET NEW DATA ADDR.
         SH    R7,LIMIT           GET NO. BYTES REMAINING.
         CH    R7,LIMIT           MORE THAN 1 LINE OF PRINT LEFT ?
         BH    DPR255             BRANCH IF YES.
         SPACE
         LTR   R7,R7              REMAINING COUNT = 0 ?
         BZ    DPR260             BRANCH IF YES.
         SPACE
DPR260   LR    R13,R8             GET ADDR OF RCVD DATA.
         LA    R14,PB+11          GET OUTPUT ADDR FOR CONVERT.
         LR    R15,R7             GET COUNT.
         BAL   R10,DPRCNV         CONVERT RCVD DATA TO EBCDIC.
DPR265   BAL   R10,DPRPNT         PRINT RCVD DATA.
         SPACE 2
***********************************************************************
*        WRITTEN DATA                                                 *
***********************************************************************
         SPACE
DPR270   TM    3(R12),X'20'       PRINT WRITTEN DATA ?
         BZ    DPR300             BRANCH IF NO.
         SPACE
         MVC   PB+D1(D9),DPWRTN   PUT WRTN DATA' IN PRINT BUF    S22024
         SR    R7,R7
         IC    R7,21(R12)         GET FIRST HALF OF WRTN DATA COUNT.
         SLL   R7,8               ADJUST.
         IC    R7,22(R12)         GET SECOND HALF OF COUNT.
         LTR   R7,R7              IS COUNT = 0 ?
         BZ    DPR285             BRANCH IF YES.
         SPACE
         L     R8,56(R1)          GET WRTN DATA ADDR.
         SPACE
         CH    R7,LIMIT           MORE THAN 1 LINE OF PRINT ?
         BNH   DPR280             BRANCH IF NO.
         SPACE
DPR275   LR    R13,R8             GET ADDR OF WRTN DATA.
         LA    R14,PB+11          GET OUTPUT ADDR FOR CONVERT.
         LH    R15,LIMIT          GET COUNT.
         BAL   R10,DPRCNV         CONVERT WRTN DATA TO EBCDIC.
         BAL   R10,DPRPNT         PRINT WRTN DATA.
         SPACE
         AH    R8,LIMIT           GET NEW DATA ADDR.
         SH    R7,LIMIT           GET NO. BYTES REMAINING.
         CH    R7,LIMIT           MORE THAN 1 LINE OF PRINT LEFT ?
         BH    DPR275             BRANCH IF YES.
         SPACE
         LTR   R7,R7              REMAINING COUNT = 0 ?
         BZ    DPR280             BRANCH IF YES.
         SPACE
DPR280   LR    R13,R8             GET ADDR OF WRTN DATA.
         LA    R14,PB+11          GET OUTPUT ADDR FOR CONVERT.
         LR    R15,R7             GET COUNT.
         BAL   R10,DPRCNV         CONVERT WRTN DATA TO EBCDIC.
DPR285   BAL   R10,DPRPNT         PRINT WRTN DATA.
         SPACE 2
***********************************************************************
*        MSGE (COMMENTS)                                              *
***********************************************************************
         SPACE
DPR300   EQU   *                                                 S22024
         TM    EPSAVE,MSG         PRINT MSG                      S22024
         BZ    DPR500             BRANCH IF NO - RETURN          S22024
         SPACE
         SR    R7,R7              CLEAR R7.
         IC    R7,4(R12)          GET NO. OF MSGE LINES.
         LTR   R7,R7              NO. LINES = 0 ?
         BZ    DPR500             BRANCH IF YES.
         SPACE
         L     R8,8(R1)           GET ADDR OF MSGE ADDRESSES.
         SR    R9,R9              CLEAR R9.
         IC    R9,5(R12)          GET DISPLACEMENT TO MSGE FIELDS.
         LA    R5,0(R9,R12)       GET ADDR OF MSGE FIELDS.
         SPACE
DPR305   SR    R9,R9              CLEAR R9.
         IC    R9,0(R5)           GET COUNT.
         LTR   R9,R9              IS COUNT = 0 ?
         BZ    DPR310             BRANCH IF YES
         LH    R6,MAXCNT          GET MAX NO. BYTES ALLOWED.
         CR    R9,R6              COUNT GREATER THAN MAX ?
         BNH   DPR307             BRANCH IF NO.
         LR    R9,R6              SET COUNT = MAX.
DPR307   L     R11,0(R8)          GET ADDR OF MSGE LINE.
         SPACE
         SPACE
         BCTR  R9,0               REDUCE COUNT FOR MVC INSTR.
         EX    R9,DPR600          EXECUTE MVC TO PLACE MSGE IN BUFFER.
         BAL   R10,DPRPNT         PRINT MSGE LINE.
         SPACE
DPR310   LA    R8,4(R8)           GET NEXT MSGE ADDR.
         LA    R5,1(R5)           GET NEXT MSG COUNT FIELD ADDR.
         BCT   R7,DPR305          PRINT NEXT LINE.
         SPACE 2
         SPACE
DPR500   EQU   *
         SPACE
         SPACE 2
***********************************************************************
*        RETURN TO CALLER                                             *
***********************************************************************
         SPACE
         TM    $OLTFLGS,$TRACE    TRACE OPTION SET ?
         BO    DPR530             YES
         SPACE
         L     R13,TOTSAVE1+4     CALLER'S SAVE AREA             S22024
         LM    R14,R12,12(R13)    RESTORE CALLER'S REGS.
         SR    R15,R15            CLEAR RETURN CODE REG (R15).
         BR    R14                RETURN TO CALLER.
*
*        REQUEST IEDQWM2 TO PROVIDE TRACE FUNCTION
*
DPR530   EQU   *
         SR    R15,R15            CLEAR RETURN CODE REG
         STC   R15,TOTRTCOD       SAVE RETURN CODE
         L     R13,4(R13)         RESTORE CALLER'S SAVE AREA ADDR
         L     R14,12(R13)        RESTORE REG 14
*
         USING RESPL,R5           SET RESPL ADDRESSABILITY     @Y17XAUU
         L     R5,TOTRESPL        GET RESPL ADDRESS            @Y17XAUU
         L     R15,RESPLM2        GET TRACE ROUTINE ADDRESS    @Y17XAUU
         LM    R2,R12,DISREG2(R13) RESTORE REGISTERS           @Y17XAUU
         BR    R15                EXIT TO TRACE ROUTINE        @Y17XAUU
         DROP  R5                                              @Y17XAUU
         EJECT
***********************************************************************
*        DPRCNV - HEX TO EBCDIC CONVERT                               *
*                                                                     *
*              CALL SEQ -  R13 = INPUT ADDRESS                        *
*                          R14 = OUTPUT ADDRESS                       *
*                          R15 = INPUT BYTE COUNT                     *
*                                                                     *
*                          BAL R10,DPRCNV                             *
***********************************************************************
         SPACE
DPRCNV   ST    R5,WRK+120         SAVE WORK REG R5.
         ST    R6,WRK+124         SAVE WORK REG R6.
         SPACE
         LTR   R15,R15            COUNT = 0 ?
         BZ    DPRCNV8            BRANCH IF YES.
         SPACE
         AR    R14,R15         ADD COUNT TO OUTPUT ADDR.
         AR    R14,R15            ADD COUNT AGAIN.
         AR    R13,R15            ADD COUNT TO INPUT ADDR.
         SR    R5,R5              CLEAR R5.
         SR    R6,R6              CLEAR R6.
         SPACE
DPRCNV4  DS    0H                 BOUNDARY ALLIGNMENT            S22024
         S     R14,F2             GET ADDR OF LAST BYTE OF OUTPUT
*                                                                S22024
         BCTR  R13,0              GET ADDR OF LAST BYTE OF INPUT.
         IC    R6,0(R13)          GET HEX BYTE.
         LR    R5,R6              PUT IN R5.
         SRL   R6,4               GET BITS 0-3 IN R6.
         N     R5,F15             GET BITS 4-7 IN R5             S22024
         IC    R6,DPREBC(R6)      CONVERT BITS 0-3 TO EBCDIC CHAR.
         IC    R5,DPREBC(R5)      CONVERT BITS 4-7 TO EBCDIC CHAR.
         STC   R6,0(R14)          STORE 1ST EBCDIC CHAR IN OUTPUT.
         STC   R5,1(R14)          STORE 2ND EBCDIC CHAR IN OUTPUT.
         BCT   R15,DPRCNV4        GET NEXT HEX BYTE.
DPRCNV8  L     R5,WRK+120         RESTORE WORK REG R5.
         L     R6,WRK+124         RESTORE WORK REG R6.
         SPACE
         BR    R10                RETURN TO CALLER.
         SPACE
DPREBC   DC    C'0123456789ABCDEF' HEX TO EBCDIC CONVERT TABLE.
         EJECT
***********************************************************************
*        PRINT LINE AND FILL PRINT BUFFER WITH BLANKS                 *
**********************************************************************
         SPACE
DPRPNT   EQU   *
         SPACE
         LA    R14,L'PB           GET LENGTH OF PRINT BUFFER.
         LA    R15,PB+79          GET ADDR OF LAST CHAR.
         SPACE
DPRPNT5  EQU   *
         CLI   0(R15),C' '        IS THIS CHAR A BLANK ?
         BNE   DPRPNT10           BRANCH IF NO.
         BCT   R14,DPRPNT8        SUBTRACT 1 FROM COUNT.
         B     DPRPNT10           BUFFER IS ALL BLANKS - SET CNT = 0.
         SPACE
DPRPNT8  EQU   *
         BCT   R15,DPRPNT5        CHECK NEXT CHAR.
DPRPNT10 EQU   *
         STC   R14,TOTOTCNT       STORE MSG COUNT                S22024
         NI    TOTFLG05,XFF-TOTCECOM-TOTREPLY-TOTEXIOF           S22024
         OI    TOTFLG05,TOTPRINT  SET DPRINT FLAG                S22024
         LA    R13,TOTSAVE1       MY SAVE AREA                   S22024
         USING RESPL,R5           SET RESPL ADDRESSABILITY     @Y17XAUU
         L     R5,TOTRESPL        GET RESPL ADDRESS            @Y17XAUU
         L     R15,RESPLWO        GET I/O ROUTINE ADDRESS      @Y17XAUU
         BALR  R14,R15            CALL I/O ROUTINE             @Y17XAUU
         DROP  R5                                              @Y17XAUU
DPRCLR   MVI   PB,C' '            PUT BLANK IN PRINT BUFFER.
         MVC   PB+1(L'PB-1),PB    FILL BUFFER WITH BLANKS.
         BR    R10                RETURN.
         EJECT
LIMIT    DC    H'34'              MAX NO. OF DATA BYTES PER LINE.
MAXCNT   DC    H'80'              MAX NO. BYTES IN DESC AND MSGE LINE.
MAXSNS   DC    H'20'              MAX NO. OF SENSE BYTES.
         DS    0F
MASK1    DC    X'00007FFF'        HALF WORD MASK FOR DATA PRINT.
DPR600   MVC   PB(0),0(R11)       MOVE MSGE TO PRINT BUFFER.
PATCH    DS    CL60               MAINTENANCE AREA.
         SPACE
EPINDX   DS    0CL4               EP MASK TABLE                  S22024
EP0      DC    X'80'              PRINT HEADER LINE              S22024
EP1      DC    X'E0'              PRINT HEADER/DESC/MSG          S22024
EP2      DC    X'90'              PRINT HEADER/RESULTS           S22024
EP3      DC    X'F0'              PRINT ALL                      S22024
*                                                                S22024
HDR      EQU   X'80'              HEADER LINE                    S22024
DESC     EQU   X'40'              DESCRIPTION LINES              S22024
MSG      EQU   X'20'              MSG LINES                      S22024
RESULTS  EQU   X'10'              RESULTS                        S22024
F2       DC    F'2'               VALUE OF 2                     S22024
F15      DC    F'15'              VALUE OF 15                    S22024
DPXPTD   DC    C'XPTD'            CHAR CONSTANT                  S22024
DPRCVD   DC    C'RCVD'            CHAR CONSTANT                  S22024
DPSNS    DC    C'SNS'             CHAR CONSTANT                  S22024
DPXPTDAT DC    C'XPTD DATA'       CHAR CONSTANT                  S22024
DPRCVDAT DC    C'RCVD DATA'       CHAR CONSTANT                  S22024
DPWRTN   DC    C'WRTN DATA'       CHAR CONSTANT                  S22024
         EJECT
***********************************************************************
*        SECTION PREFACE DUMMY SECTION                                *
***********************************************************************
         SPACE
SPREF1   DSECT                    SECTION PREFACE PART 1         S99528
SECTID   DS    CL8                OLT ID                         S99528
LEVEL    DS    CL2                LEVEL                          S99528
RTNDISP  DS    AL2                DISP TO ROUTINE NUMBER         S99528
         SPACE 2
SPREF2   DSECT                    SECTION PREFACE PART 2         S99528
RTN#     DS    XL1                ROUTINE NUMBER                 S99528
         DS    XL2                FLAGS                          S99528
RM       DS    XL2                ROUTINE MASK                   S99528
         DS    XL6                PLINK MANAGEMENT               S99528
REFNUM   DS    XL1                REFERENCE NUMBER               S99528
         EJECT
RESPL    RESPL                                                 @Y17XAUU
         EJECT
         OLTCB                                                   S22024
EPSAVE   DS    C                  EP LEVEL CONTROL FIELD       @Y17XAUU
         SPACE
WRK      EQU   TOTWKSPC           WORK SPACE                     S22024
PB       EQU   TOTOTBUF           OUTPUT BUFFER                  S22024
         END
