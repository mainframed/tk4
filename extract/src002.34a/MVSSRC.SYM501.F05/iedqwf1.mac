         TITLE 'IEDQWF1 - TOTE MVM DEALLOCATION ROUTINE'
IEDQWF1  CSECT
         SPACE 2
* CHANGE ACTIVITY AS FOLLOWS:
*A000000-999999                                                  Y06335
* A004500,288000,291600,575100,675900,719100,733500,829800     @G36XRUV
* C013500,288900,576000,628200,734400                          @G36XRUV
* D290700,292500,735300                                        @G36XRUV
*
         SPACE 2
***************************************************************
*                                                             *
* MODULE NAME = IEDQWF1                                       *
*                                                             *
* DESCRIPTIVE NAME = TOTE MVM DEALLOCATION ROUTINE            *
*                                                             *
* COPYRIGHT = NONE                                            *
*                                                             *
* STATUS = CHANGE LEVEL 10                                    *@G36XRUV
*                                                             *
* FUNCTION = THE PURPOSE OF THIS MODULE IS TWOFOLD:           *
*                                                             *
*            A. TO TAKE DEVICES AWAY FROM TCAM AND CONDITION  *
*               THEM FOR TEST RUNNING UNDER OLTEP.            *
*                                                             *
*            B. TO RETURN DEVICES BACK TO TCAM IN THE SAME    *
*               INITIAL CONDITION THAT THEY WERE IN BEFORE    *
*                                                             *
*               FUNCTION ONE IS INITIATED WHEN A TRM IN THE   *
*               FOLLOWING FORMAT IS ENTERED:                  *
*                                                             *
*               SYSCON/TD/TOTE//                              *
*                                                             *
*               FUNCTION TWO IS INITIATED WHEN THE OPERATOR   *
*               RESPONDS TO A WTOR TELLING TOTE TO RETURN ALL *
*               SUBCHANNELS BACK TO TCAM                      *
*                                                             *
*               IN THE FORMAT ABOVE, 'TD' REPRESENTS THE      *
*               SUBCHANNEL ADDRESS(ES) TO BE TAKEN AWAY       *
*               FROM TCAM.                                    *
*                                                             *
*            IN ORDER TO ACCOMPLISH THE FIRST FUNCTION, TOTE  *
*            WILL EVALUATE THE TEST FIELD OF THE TRM          *
*            SCANNING ON THE KEYWORD 'TOTE'.  TOTE              *
*            WILL EVALUATE ALL ENTRIES IN THE TEST DEVICE     *
*            FIELD CHECKING TO VERIFY THAT ALL THE SUBCHANNEL *
*            ADDRESSES ARE ALLOCATED TO TCAM VIA A TIOT       *
*            SEARCH. IF ALL DEVICES DO NOT BELONG TO TCAM     *
*            THE REQUEST FOR DEALLOCATION WILL BE REJECTED.   *
*                                                             *
*            IF ALL DEVICES BELONG TO TCAM, THEN TOTE WILL    *
*                                                             *
*            A. REQUEST AUTHORIZATION FROM THE SYSTEM OPERATOR*
*               TO TAKE EXCLUSIVE USE OF THE SPECIFIED SUB-   *
*               CHANNELS. IF THE REQUEST IS REJECTED THE TRM  *
*               IS REJECTED.                                  *
*                                                             *
*            B. SAVE THE INITIAL STATUS OF ALL SUB-CHANNELS   *
*               SPECIFIED.                                    *
*                                                             *
*            C. TURN OFF THE BIT (UCBNALOC) IN EACH UCB WHICH *
*               SAYS THAT THE SUB-CHANNEL IS ALLOCATED TO     *
*               TCAM (SRTESTAT,SRTEALOC).                     *
*                                                             *
*            D. TURN ON THE OFFLINE BIT IN THE UCB FOR EACH   *
*               SUB-CHANNEL (SRTESTAT,SRTEONLI).              *
*                                                             *
*            E. ISSUE A STOPLINE TO ALL DEVICES (EXCEPT FOR   *
*               A 370X, IN WHICH A DEACTIVATE IS ISSUED).     *
*                                                             *
*            NOTE: SYSCON MUST ALWAYS BE SPECIFIED AS THE     *
*                  CONTROL TERMINAL.                          *
*                                                             *
*            A WTOR IS ISSUED AND OLTEP CAN BE INVOKED. A     *
*            RESPONSE OF 'TOTE' TO THE WTOR WILL INVOKE       *
*            FUNCTION TWO.
*                                                             *
*                                                             *
*            WHEN THE REQUEST IS FOR REALLOCATION             *
*            OF TCAM DEVICES TOTE WILL RETURN ALL DEVICES TO  *
*            THEIR ORIGINAL STATUS AND FREE UP ALL WORK AREAS *
*                                                             *
* ENTRY POINTS = IEDQWF1                                      *
*                                                             *
* INPUT =                                                     *
*                                                             *
*         REGISTERS 02, 13, 14, 15 CONTAIN THE FOLLOWING      *
*         VALUES:                                             *
*                                                             *
*         02 - OLTCB POINTER                                  *
*                                                             *
*         13 - SAVE AREA ADDRESS                              *
*                                                             *
*         14 - RETURN ADDRESS                                 *
*                                                             *
*         15 - ENTRY POINT ADDRESS                            *
*                                                             *
* OUTPUT =                                                    *
*                                                             *
*         NONE                                                *
*                                                             *
* EXTERNAL REFERENCES =                                       *
*                                                             *
*         NONE                                                *
*                                                             *
* EXITS,NORMAL                                                *
*                                                             *
*                                                             *
* EXITS,ERROR                                                 *
*                                                             *
*                                                             *
*                                                             *
* TABLES/WORK AREAS =                                         *
*                                                             *
*         OLTCB, AVT, TNT, TTE, QCB, PARAMETER LIST           *
*                                                             *
* ATTRIBUTES =                                                *
*                                                             *
*         ENABLED, PROBLEM PROGRAM MODE, TRANSIENT            *
*                                                             *
* NOTES =                                                     *
*                                                             *
*         NONE                                                *
*                                                             *
***************************************************************
         EJECT
***************************************************************
*                                                             *
*        E Q U A T E S                                        *
*                                                             *
***************************************************************
         SPACE
R0       EQU   0                  REG 0
R1       EQU   1                  REG 1
R2       EQU   2                  REG 2
R3       EQU   3                  REG 3
R4       EQU   4                  REG 4
R5       EQU   5                  REG 5
R6       EQU   6                  REG 6
R7       EQU   7                  REG 7
R8       EQU   8                  REG 8
R9       EQU   9                  REG 9
R10      EQU   10                 REG 10
R11      EQU   11                 REG 11
R12      EQU   12                 REG 12
R13      EQU   13                 REG 13
R14      EQU   14                 REG 14
R15      EQU   15                 REG 15
BASE     EQU   12                 BASE REGISTER
PARMREG0 EQU   R0                 MSG REG
PARMREG1 EQU   R1                 MSG REG
         SPACE
D0       EQU   0                  DISP OF 0
D1       EQU   1                  DISP OF 1
D2       EQU   2                  DISP OF 2
D3       EQU   3                  DISP OF 3
D4       EQU   4                  DISP OF 4
D5       EQU   5                  DISP OF 5
D6       EQU   6                  DISP OF 6
D7       EQU   7                  DISP OF 7
D8       EQU   8                  DISP OF 8
D9       EQU   9                  DISP OF 9
D10      EQU   10                 DISP OF 10
D11      EQU   11                 DISP OF 11
D12      EQU   12                 DISP OF 12
D13      EQU   13                 DISP OF 13
D14      EQU   14                 DISP OF 14
D15      EQU   15                 DISP OF 15
D16      EQU   16                 DISP OF 16
D17      EQU   17                 DISP OF 17
D18      EQU   18                 DISP OF 18
D19      EQU   19                 DISP OF 19
D20      EQU   20                 DISP OF 20
D24      EQU   24                 DISP OF 24
D28      EQU   28                 DISP OF 28
D32      EQU   32                 DISP OF 32
D36      EQU   36                 DISP OF 36
D40      EQU   40                 DISP OF 40
D43      EQU   X'43'              DISP OF 43
D44      EQU   44                 DISP OF 44
D48      EQU   48                 DISP OF 48
D53      EQU   53                 DISP OF 53
D72      EQU   72                 DISP OF 72
         SPACE
XDC      EQU   X'DC'              PRIORITY
X18      EQU   X'18'              LENGTH
X0C      EQU   X'0C'              START FLAG
X80      EQU   X'80'              END FLAG
X00      EQU   X'00'              ZERO RT CODE FROM OPCONTROL
X08      EQU   X'08'              EIGHT RT CODE OPCONTROL
X44      EQU   X'44'              TCAM DEB ID
STOP     EQU   X'41'              STOP LINE VERB
START    EQU   X'40'              START LINE VERB
ACTIV    EQU   X'53'              ACTIVATE 370X VERB
DEACT    EQU   X'54'              DEACTIVATE 370X VERB
IPL      EQU   X'52'              IPL 370X VERB
BLK      EQU   X'40'              BLANK
ALREADY  EQU   X'08'              RETURN CODE FROM ENQ
XFF      EQU   X'FF'              MASK SETTING
YES      EQU   X'E8'              Y OR YES
NALLOC   EQU   X'F7'              MASK
OFFLIN   EQU   X'7F'              MASK
XF0      EQU   X'F0'              MASK SETTING
ONALLOC  EQU   X'88'              UCB ONLINE AND ALLOC FLAGS
KA       EQU   X'C1'              CHAR A
ACTVBCD  EQU   X'46'              ACTIVATE COMMAND
ACTRC    EQU   X'08'              INVITATION LIST ACTIVE RT CD
INACT    EQU   X'48'              INACTIVATE RETURN CODE
INACTRC  EQU   X'08'              ALREADY INACT RT CD
MORE     EQU   X'80'              MASK BYTE
END      EQU   X'7F'              MASK BYTE
INCLUS   EQU   X'20'              MASK BYTE
ADDONE   EQU   X'10'              MASK BYTE
ADDOFF   EQU   X'EF'              MASK BYTE
ALLOFF   EQU   X'00'              MASK BYTE
NOINC    EQU   X'DF'              MASK FIELD
BLANK    EQU   X'40'              BLANK CHAR
TDMAX    EQU   256                MAX TD
KF       EQU   X'C6'              CHAR F
F9       EQU   X'F9'              CHAR 9
F0       EQU   X'F0'              CHAR 0
XC0      EQU   X'C0'              MASK SETTING
ZERO     EQU   0                  0 VALUE
FOUR     EQU   4                  FOUR VALUE
TWELVE   EQU   12                 12 VALUE
TRMAX    EQU   72                 TRM BUFFER LENGTH
ACVT     EQU   16                 POINTER TO CVT ADDRESS
SLASH    EQU   X'61'              SLASH CHAR
COMMA    EQU   X'6B'              COMMAS CHAR
DASH     EQU   X'60'              DASH CHAR
RQINIT   EQU   X'0C'              START OF LIST
RQEND    EQU   X'80'              END OF LIST
NEG      EQU   4                  NEGATIVE TEST
X0F      EQU   X'0F'              MASK SETTING
TCU3705  EQU   X'05'              3705 UCB
ENQLOOP  EQU   10                 ENQ RETRY COUNT
         EJECT
         USING IEDQWF1,R15
IEDQWF1  IEDHJN STARF1,HJN
         SPACE
         SAVE  (14,12)            SAVE CALLER'S REGISTERS
         SPACE
         DROP  R15
         LR    BASE,R15           SET BASE
         USING IEDQWF1,BASE
         USING TOTOLTCB,R2
         LA    R3,TOTSAVE4        M4 SAVE AREA
         ST    R3,D8(R13)         SAVE IN CALLER'S SAVE AREA
         ST    R13,D4(R3)         SAVE MY SAVE AREA ADDRESS
         LR    R13,R3             SET UP SAVE AREA REG
         SPACE 2
         BAL   R11,OLTACT         SEND ON LINE TEST ACT MSG
START1   BAL   R11,BLDTBL         BUILD UCB TABLE
         BAL   R11,ALLOC          VERIFY UCB'S ALLOC TO TCAM
         BAL   R11,AUTHNCM        REQUEST NCM MODE
         BAL   R11,INITSTAT       SAVE INITIAL STATUS
         BAL   R11,CONDIT         CONDITION UCB'S
         B     POST               WAIT UNTIL OLTEP IS FINISHED
         EJECT
****************************************************************
*                                                              *
*        SEND ON LINE TESTING ACTIVE MESSAGE                   *
*                                                              *
****************************************************************
OLTACT   DS    0H                 BOUNDARY ALLIGNMENT            Y06335
         IEDQMSG MSGID=002,FUNCT=CEC
****************************************************************
         MVC   TRMCTID(D2),TOTOLTID CONTROL TERMINAL ID
         ST    R2,TMID+D16        PUT OLTCB ADDR IN MSG        @YA12402
         TCONV FROM=TMID+D16,TO=TMID+D8,COUNT=D4,TYPE=HE
         MVC   TMID+16(4),TMID+21  MOVE OLTCB ADDRESS          @YA12402
****************************************************************
         SPACE
         IEDQMSG FUNCT=CEC,OTBUF=TMID,OTCNT=TMIDE-TMID
         SPACE
         BR    R11                RETURN
#EXIT    L     R15,$TABLE          TO BE REMOVED BEFORE SHIP
         MVC   TRMCTID(D2),TOTOLTID
         L     R15,28(R15)         TO BE REMOVED BEFORE SHIP
         BR    R15                 TO BE REMOVED BEFORE SHIP
         EJECT
***************************************************************
*                                                             *
*        ROUTINE TO BUILD A TABLE FOR ALL SUBCHANNEL ADDRESSES*
*        ENTERED IN THE TEST DEVICE FIELD. A MAXIMUM OF 256   *
*        SUBCHANNEL ADDRESSES MAY BE ENTERED. IF MORE THAN    *
*        256 SUBCHANNELS ARE SPECIFIED, AN ERROR MSG IS       *
*        WRITTEN AND THE TRM IS REJECTED. A 256 4-BYTE TABLE  *
*        IS ALLOCATED. THE ENTRIES IN THE TABLE HAVE THE      *
*        FOLLOWING FORMAT:                                    *
*                                                             *
*              BYTE 0   - RESERVED FOR INITIAL STATUS         *
*                                                             *
*              BYTE 1   - RESERVED                            *
*                                                             *
*              BYTE 2-3 - UCB ADDRESS                         *
*                                                             *
*        LINKAGE -  BAL  R11,BLDTBL                           *
*                                                             *
*                                                             *
***************************************************************
         SPACE
BLDTBL   DS    0H                 BOUNDARY ALLIGNMENT
         SPACE
         STM   R3,R10,SAVE1       SAVE CALLER'S REG
         MVI   TDFLG,D0           CLEAR CONTROL FLAG
         LA    R3,TOTTRMBF        POINT TO TRM BUFFER
         LA    R3,D7(R3)          POINT TO FIRST CUU
         L     R4,ACVT(R0)        CVT ADDR
         USING CVT,R4
         L     R4,CVTILK2         PICK UP 2 BYTE UCB TABLE ADDR
         ST    R4,UCBSAVE         SAVE IT
         DROP  R4
         LA    R5,TABLE-D4        ADDR OF TD TABLE-4
         LA    R10,TDMAX          GET MAX NO OF TEST DEVS
BLD04    DS    0H                 BOUNDARY ALLIGNMENT
         LA    R6,D0              INITIALIZE ENTRY COUNTER
BLD05    EQU   *                  LOOP ENTRY
         CLI   D0(R3),F0                IS FIRST CHAR A ZERO?  @YA12402
         BNE   DIGIT3                   NO                     @YA12402
         TM    D3(R3),XC0               IS 4TH CHAR ALPHANUM?  @YA12402
         BNO   DIGIT3                   NO                     @YA12402
         LA    R3,D1(R3)                ADJUST FOR LEADING ZERO@YA12402
DIGIT3   EQU   *                                               @YA12402
         MVC   WORK4+D1(D4),D0(R3) GET LOWER CUU (EBCDIC)
         BAL   R14,CONVERT        CONVERT IT TO HEX
BLD08    DS    0H                 BOUNDARY ALLIGNMENT
         L     R8,UCBSAVE         ADDR OF UCB PTRS
         USING UCB,R7
BLD0C    DS    0H                 BOUNDARY ALLIGNMENT
         SLR   R7,R7              CLEAR                        @G36XRUV
         ICM   R7,3,D0(R8)        UCB ADDR
         LA    R8,D2(R8)          POINT TO NEXT ENTRY
         BZ    BLD0C              PADDING IGNORE
         CLM   R7,3,ENDTABLE      Q-END OF TABLE               @G36XRUV
         BE    ERROR1             Y- ERROR                     @G36XRUV
         CLC   WORK1(D2),UCBCHA   UCB FOUND
         BNE   BLD0C              NO - CHECK NEXT ENTRY
         CLI   UCBTBYT3,UCB3COMM  CLASS EQ COMMUNICATION DEVICE
         BE    BLD14              YES - CONTINUE
         CLI   UCBTBYT3,UCB3DISP  CLASS EQ DISPLAY
         BE    BLD14              YES - CONTINUE
         SPACE
         IEDQMSG MSGID=007,FUNCT=CEC CLASS NOT SUPT
         B     RETURN             TERMINATE
         SPACE
BLD14    DS    0H                 BOUNDARY ALLIGNMENT
         TM    SRTESTAT,SRTEALOC  ALLOCATED TO TCAM
         BZ    NOTFOUND           NO - TERMINATE
         TM    SRTESTAT,SRTEONLI  UCB ONLINE (LINE OPENED)     @Y17XAUU
         BO    BLD16              YES - CONTINUE               @Y17XAUU
*        INVALID TEST DEVICE ENTRY - TEST LINE NOT OPENED      @Y17XAUU
         IEDQMSG MSGID=038,FUNCT=CEC WRITE ERROR MESSAGE       @Y17XAUU
         B     RETURN1            TERMINATE TRM PROCESSING     @Y17XAUU
BLD16    DS    0H                 BOUNDARY ALLIGNMENT          @Y17XAUU
         LA    R5,D4(R5)          NEXT ENTRY SLOT
         LA    R6,D1(R6)          INCREMENT ENTRY COUNTER
         CR    R6,R10             MORE THAN 256 ENTRIES
         BH    ERROR3             YES - ERROR
         STH   R7,D2(R5)          ENTRY TO TABLE
         MVC   WORK2(D1),UCBTBYT4 TCU TYPE TO WORK AREA
         NI    WORK2,X0F          CLEAR UNWANTED BITS
         CLI   WORK2,TCU3705      3705 TCU
         BNE   BLD18              NO
         OI    D0(R5),RNCHAN      YES - SHOW 3705 ENTRY
BLD18    DS    0H                 BOUNDARY ALLIGNMENT
         TM    TDFLG,INCLUS       INCLUSIVE TEST
         BO    BLD34              YES - CONTINUE
         CLI   D3(R3),SLASH       END OF FIELD
         BE    BLDRET             YES - EXIT
         CLI   D3(R3),COMMA       ANOTHER ENTRY
         BE    BLD20              YES - CONTINUE
         CLI   D3(R3),DASH        INCLUSIVE SUBCHAN
         BE    BLD24              YES
         B     ERROR1             NO - INVALID DELIMITER
         SPACE
BLD20    DS    0H                 BOUNDARY ALLIGNMENT
         NI    TDFLG,XFF-INCLUS   CLEAR INCLUSIVE FLAG
         LA    R3,D4(R3)          POINT TO NEXT CUU
         B     BLD05              CONTINUE
         SPACE
BLD24    DS    0H                 BOUNDARY ALLIGNMENT
         LR    R1,R3              SAVE CUU POINTER
         LA    R3,D4(R3)          POINT PASS DASH
         MVC   BIN1(D2),WORK1     SAVE LOWER BOUND
         CLI   D0(R3),F0                IS FIRST CHAR ZERO?    @YA12402
         BNE   MOVE3                    NO                     @YA12402
         TM    D3(R3),XC0               IS 4TH CHAR ALPHANUM?  @YA12402
         BNE   MOVE3                    NO                     @YA12402
         LA    R3,D1(R3)                ADJUST FOR LEADING ZERO@YA12402
MOVE3    EQU   *                                               @YA12402
         MVC   WORK4+D1(D4),D0(R3) GET UPPER CUU
         BAL   R14,CONVERT        CONVERT IT TO HEX
         MVC   BIN3(D2),WORK1     SAVE IT
BLD30    DS    0H                 BOUNDARY ALLIGNMENT
         CLC   D0(D3,R1),D0(R3)   VALID LOWER/UPPER LIMITS
         BNL   ERROR1             NO - ERROR
         OI    TDFLG,INCLUS       SHOW INCLUSIVE CUU
BLD34    DS    0H                 BOUNDARY ALLIGNMENT
         LH    R7,BIN1            GET CURRENT CUU VALUE
         LA    R7,D1(R7)          INCREMENT TO NEXT CUU
         CH    R7,BIN3            PROCESSING COMPLETE
         BH    BLD40              YES - EXIT
         STH   R7,BIN1            SAVE IT
         STH   R7,WORK1           SET COMPARAND
         B     BLD08              VERIFY CUU ENTRY
         SPACE
BLD40    DS    0H                 BOUNDARY ALLIGNMENT
         CLI   D3(R1),SLASH       END OF FIELD                 @YA12402
         BE    BLDRET             YES - EXIT
         LA    R3,D4(R1)          POINT TO SECOND CUU
         CLI   D3(R1),COMMA       DELIMITER                    @YA12402
         BE    BLD20              YES
         B     ERROR1             NO - ERROR
         SPACE
BLDRET   DS    0H                 BOUNDARY ALLIGNMENT
         LM    R3,R10,SAVE1       RESTORE REGS
         BR    R11                RETURN
         EJECT
***************************************************************
*                                                             *
*        ROUTINE TO CONVERT FROM EBCDIC TO HEX.               *
*        INPUT EQU THREE BYTES IN 'WORK4+1'                   *
*        OUTPUT EQU TWO BYTES IN 'WORK1'                      *
*        RETURN EQU 'BR  R14   '                              *
*                                                             *
***************************************************************
CONVERT  DS    0H                 BOUNDARY ALLIGNMENT
         STM   R3,R8,SAVE2        SAVE CALLER'S REGS
         LA    R3,WORK4+D1        ADDR OF INPUT
         LA    R4,D3              SET LOOP COUNT
CONV08   DS    0H                 BOUNDARY ALLIGNMENT
         CLI   D0(R3),KA          LESS THAN 'A'
         BL    ERROR1             YES - ERROR
         CLI   D0(R3),KF          NOT GREATER THAN'F'
         BNH   CONV10             YES - VALID
         CLI   D0(R3),F0          LESS THAN '0'
         BL    ERROR1             YES - ERROR
         CLI   D0(R3),F9          GREATER THAN '9'
         BH    ERROR1             YES - ERROR
CONV10   DS    0H                 BOUNDARY ALLIGNMENT
         LA    R3,D1(R3)          POINT TO NEXT CHAR
         BCT   R4,CONV08          CHECK IT
         CLI   D0(R3),SLASH       END OF TD FIELD
         BE    CONV18             YES
         CLI   D0(R3),DASH        INCLUSIVE DELIMITER
         BE    CONV18             YES
         CLI   D0(R3),COMMA       MORE ENTRIES
         BNE   ERROR1             NO - ERROR
CONV18   DS    0H                 BOUNDARY ALLIGNMENT
         LA    R3,WORK4+D1        ADDR OF INPUT
         LA    R4,D3              SET LOOP COUNT
         LA    R7,XLATE           ADDR OF XLATE TABLE
CONV20   DS    0H                 BOUNDARY ALLIGNMENT
         TM    D0(R3),XF0         NUMERIC
         BNO   CONV24             NO
         NI    D0(R3),X0F         CLEAR ZONE
         B     CONV28             CONTINUE
CONV24   DS    0H                 BOUNDARY ALLIGNMENT
         NI    D0(R3),X0F         CLEAR ALPHA ZONE
         SR    R6,R6              CLEAR REG
         IC    R6,D0(R3)          GET 'ALPHA CHAR'             @YA12402
         AR    R6,R7              POINT TO HEX VALUE
         MVC   D0(D1,R3),D0(R6)   REPLACE WITH HEX VALUE
CONV28   DS    0H                 BOUNDARY ALLIGNMENT
         LA    R3,D1(R3)          POINT TO NEXT CHAR
         BCT   R4,CONV20          PROCESS IT
         MVI   WORK4,D0           PREPARE TO PACK
         MVI   WORK4+D4,D0        PREPARE TO PACK
         PACK  PACK1(D3),WORK4(D5) PACK IT
         MVC   WORK1(D2),PACK1    PASS IT BACK
         LM    R3,R8,SAVE2        RESTORE REGS
         BR    R14                RETURN
         SPACE
ERROR1   DS    0H                 BOUNDARY ALLIGNMENT
         WTO   'IED269I ERROR IN TEST DEVICE FIELD',ROUTCDE=10,DESC=7
         OI    TOTFLG06,TOTTERMS  SET TERMINATE FLAG
         B     RETURN             EXIT
         DROP  R7                 UCB
         SPACE 2
ERROR3   DS    0H                 BOUNDARY ALLIGNMENT
         WTO   'IED273I ERROR - MORE THAN 256 TEST DEVICES ENTERED',ROU*
               TCDE=10,DESC=7
         B     RETURN             EXIT
         EJECT
***************************************************************
*                                                             *
*                                                             *
*        ROUTINE TO VERIFY THAT SUBCHANNELS ARE ALLOCATED TO  *
*        TCAM VIA TIOT SEARCH. THE 256 WORD TEST DEVICE       *
*        TABLE IS SEARCHED. IF ALL ARE NOT ALLOCATED TO TCAM  *
*        THE TRM IS REJECTED                                  *
*                                                             *
***************************************************************
ALLOC    DS    0H                 BOUNDARY ALLIGNMENT
         LA    R4,TABLE           TEST DEVICE TABLE
         LA    R7,TABLEND         END OF TABLE
ALLTEST  DS    0H                 BOUNDARY ALLIGNMENT            Y06335
         CR    R4,R7              ALL ENTRIES PROCESSED
         BE    ZERO(R11)          YES - EXIT
         L     R8,ZERO(R4)        GET ADDR FROM TABLE
         LA    R8,ZERO(R8)        CLEAR HI ORDER BYTE
         STH   R8,KUCB            SAVE UCB ADDRESS
         LTR   R8,R8              IS THERE AN ADDRESS
         BZ    ZERO(R11)          NO - RETURN
         LA    R9,CVTPTR          POINTER TO CVT
         L     R9,ZERO(R9)        CVT ADDR
         L     R9,ZERO(R9)        TCB ADDR
         L     R9,FOUR(R9)        CURRENT TCB ADDR
         L     R9,TWELVE(R9)      TIOT ADDR
         USING TIOT,R9
         LA    R9,TIOENTRY        FIRST ENTRY
         DROP  R9
         USING TIOENTRY,R9
GETLENGT DS    0H                 BOUNDARY ALLIGNMENT            Y06335
         SR    R3,R3              CLEAR REG 3
         IC    R3,TIOELNGH        DD ENTRY LENGTH
         LTR   R3,R3              ANY LENGTH ?
         BZ    NOTFOUND           NO - ERROR
         LA    R5,POOLSTAR-TIOESTTB DEV ENTRY LENGTH
         DROP  R9
         LR    R10,R3             SAVE LENGTH
         SR    R3,R5              DD LENGTH - 4
         AR    R3,R9              DEVICE
         USING TIOESTTB,R3
         CLC   TIOEFSRT+D1(D2),KUCB  ENTRY FOUND
         DROP  R3
         BE    FOUND              ENTRY FOUND
         AR    R9,R10             POINT TO NEXT ENTRY
         B     GETLENGT           CONTINUE LOOKING
FOUND    DS    0H                 BOUNDARY ALLIGNMENY            Y06335
         LA    R4,FOUR(R4)        POINT TO NEXT LINE
         B     ALLTEST            CHECK NEXT ENTRY
NOTFOUND DS    0H                 BOUNDARY ALLIGNMENT            Y06335
         WTO   'IED274I ERROR - TEST DEVICE NOT ALLOCATED TO TCAM',ROUT*
               CDE=10,DESC=7
         B     RETURN             GO TO PROMPTER
         EJECT
***************************************************************
*                                                             *
*        ROUTINE TO ASK SYSTEM OPERATOR FOR EXCLUSIVE USE     *
*        OF SUBCHANNELS ENTERED IN TEST DEVICE FIELD.         *
*                                                             *
***************************************************************
AUTHNCM  DS    0H                 BOUNDARY ALLIGNMENT
         STM   R3,R10,SAVE1       SAVE CALLER'S REGS
         LA    R3,TOTTRMBF+D7     START OF TEST DEVICE FIELD
         LA    R4,D0              INITIALIZE CHAR COUNTER
AUTH04   DS    0H                 BOUNDARY ALLIGNMENT
         CLI   D0(R3),SLASH       END OF FIELD
         BE    AUTH08             YES
         LA    R4,D1(R4)          INCREMENT CHAR COUNTER
         LA    R3,D1(R3)          POINT TO NEXT CHAR
         B     AUTH04             CHECK IT
         SPACE
AUTH08   DS    0H                 BOUNDARY ALLIGNMENT
         LA    R3,TOTTRMBF+D7     RESET TD FIELD POINTER
         BCTR  R4,R0              DECREMENT COUNT FOR EX
         EX    R4,CUUMOVE         NAME TO MESSAGE
         WTO   'IED224D CAN OLT HAVE EXCLUSIVE USE OF CONTROL UNIT ON A*
               DDRESS(ES) ',ROUTCDE=10,DESC=7
         SPACE
         XC    ECB(D4),ECB        CLEAR ECB
         SPACE
WTORNCM  WTOR  '                                                     ',*
               ANSWER,3,ECB,ROUTCDE=(8,10),DESC=2
NCMMSG2  EQU   WTORNCM+D16        PLACE FOR CUU'S
         STIMER REAL,NOANS,DINTVL=TIMOUT SET TIMER FOR TIMEOUT
         WAIT  ECB=ECB            WAIT FOR REPLY
         TTIMER CANCEL            CANCEL TIMER
         OI    ANSWER,BLANK       XLATE TO CAPS
         CLI   ANSWER,YES         ANSWER YES
         BNE   AUTH0C             NO - REJECT TRM
         LM    R3,R10,SAVE1       RESTORE CALLER'S REGS
         BR    R11                RETURN
         SPACE
CUUMOVE  MVC   NCMMSG2(D0),D0(R3) CUU'S TO MSG
         SPACE
AUTH0C   DS    0H                 BOUNDARY ALLIGNMENT
         IEDQMSG MSGID=051,FUNCT=CEC ALLOW ONLY CONCURRENT MODE
         B     RETURN             RETURN
         SPACE
         EJECT
NOANS    DS    0H                 BOUNDARY ALLIGNMENT
         STM   R14,R12,D12(R13)   SAVE CALLER'S REGS
         LR    R4,R15             MY BASE
         USING NOANS,R4
         LA    R12,TOTSAVE1       MY SAVE AREA
         ST    R13,D4(R12)        SAVE HIS SAVE ADDR
         ST    R12,D8(R13)        SAVE MY SAVE ADDR
         LR    R13,R12            SET SAVE SAVE POINTER
         POST  ECB                POST WTOR ECB
         L     R13,D4(R13)        CALLER'S SAVE
         LM    R14,R12,D12(R13)   RESTORE CALLER'S REGS
         SR    R15,R15            RETURN CODE REG
         BR    R14                RETURN TO CALLER
         DROP  R4
         EJECT
***************************************************************
*     A. ROUTINE TO TAKE SUBCHAN ADDRESSES ENTERED IN THE     *
*        TEST DEVICE FIELD AWAY FROM TCAM.                    *
*                                                             *
*        1. IF THE CUU IS FOR A 370X A DEACTIVATE IS ISSUED.  *
*                                                             *
*        2. IF THE CUU IS FOR A 270X OR 32XX A STOPLINE IS    *
*           ISSUED.                                           *
*                                                             *
*        NOTE- IF THE RETURN CODE IS OTHER THAN X'00' OR X'08'*
*        FOR ANY DEACTIVATE OR STOPLINE THE TRM IS REJECTED.  *
*                                                             *
*     B. EXAMINE THE RETURN CODE FROM THE STOPLINE OR DEACT-  *
*                                                             *
*        IVATE TO SAVE THE INITIAL STATUS OF THE DEVICE.      *
*     C. ENQ FOR EXCLUSIVE CONTROL FOR ALL TEST DEVICES.      *
*                                                             *
*        NOTE- IF ANY ENQ FAILS THE TRM IS REJECTED.          *
*                                                             *
*     D. IF ENTERED FOR CLEANUP  RETURNS DEVICES TO TCAM      *
*                                                             *
*        1. IF THE TEST DEVICE IS A 370X ISSUE AN IPL.        *
*                                                             *
*        2. IF THE INITIAL STATUS WAS STOPPED CLEAR THE       *
*           LCBCPA FIELD.                                     *
*                                                             *
*        3. IF THE INITIAL STATUS WAS STARTED ISSUE           *
*           EITHER ACTIVATE(370X) OR STARTLINE                *
*                                                             *
*        4. DEQ ALL TEST DEVICES                              *
*                                                             *
*                                                             *
*                                                             *
*                                                             *
*                                                             *
*                                                             *
***************************************************************
INITSTAT EQU   *                  ROUTINE ENTRY
         STM   R3,R10,SAVE1       SAVE WORK REGS
         TM    CTLFLG,CLEANUP     ENTRY FOR CLEANUP
         BO    INIT05             YES BRANCH
         MVI   TOTOPRI,XDC        PRIORITY OF ELEMENT
         MVI   TOTOELNG,X18       LENGTH OF ELEMENT
         LA    R3,TOTPLIST        GET OP CONTROL P LIST
         ST    R3,SLPL            ADDRESS AND STORE IN
         ST    R3,SLPL+D4         ELEMENT TWICE
         MVI   SLPL,X0C           START FLAG
         MVI   SLPL+D4,X80        END FLAG
         MVI   TOTOLADR,BLK       BLANK OUT
         MVC   TOTOLADR+D1(D7),TOTOLADR  NAME
         LA    R3,TOTOTECB        GET ECB ADDRESS
         ST    R3,TOTOECBA        PUT IN P LIST
         L     R3,TOTAVTPT        GET AVT ADDRESS
         USING IEDQAVTD,R3        AVT
         LA    R3,AVTOPCOB        GET OP CONTROL QCB ADDRESS
         DROP  R3                 AVT
         ST    R3,TOTOKEY         PUT IN P LIST
         SPACE
INIT05   EQU   *                  CONTINUE
         LA    R3,TABLE           TABLE ADDRESS
         LA    R4,TABLEND         END OF TABLE ADDRESS
         SPACE
INIT10   EQU   *                  REENTRY FOR LOOP
         CR    R3,R4              ALL ENTRIES PROCSEEED
         BE    INIT90             YES BRANCH
         SLR   R5,R5              CLEAR                        @G36XRUV
         ICM   R5,3,D2(R3)        GET ENTRY FROM TABLE
*                                 UCB ADDRESSES
         LTR   R5,R5              IS ENTRY FILLED IN
         BZ    INIT90             NO BRANCH
         USING UCBOB,R5           UCB
         OI    CTLFLG,RLNSRCH     INDICATE RLN SEARCH
         BAL   R14,INIT45         GO FIND RLN
         STC   R7,TOTORLN         RLN TO P LIST
         NI    CTLFLG,XFF-RLNSRCH TURN OFF FLAG
         MVC   RNAME(D3),UCBNAME  NAME TO ENQ OR DEQ
         MVC   TOTOLADR(D3),UCBNAME  NAME FOR OPCONTROL
         TM    CTLFLG,CLEANUP     ENTERED FOR CLEANUP
         BO    INIT15             YES BRANCH
         ENQ   ,MF=(E,ENQLIST)    REQUEST EXCLUSIVE USE
         LTR   R15,R15            OK
         BZ    INIT15             YES BRANCH
         CLI   D3(R15),ALREADY    ALREADY FROM THIS TASK
         BE    INIT15             YES BRANCH
*        SEND ERROR MSG AND CANCEL TRM
         XC    D0(D4,R3),D0(R3)   CLEAR FAILLING ENTRY FORM TABLE
         OI    CTLFLG,CLEANUP     TURN ON CLEANUP FLAG
         IEDQMSG MSGID=044,FUNCT=CEC,RET=INIT05
         SPACE
INIT15   EQU   *                  ENQ OK
         TM    D0(R3),RNCHAN      TEST DEVICE ON 370X
         BO    INIT30             YES BRANCH
         TM    CTLFLG,CLEANUP     CLEANUP
         BO    INIT40             YES BRANCH
         MVI   TOTOVBCD,STOP      VERB TO STOPLINE
         SPACE
INIT20   EQU   *                  SET FOR OP CONTROL
         XC    TOTOLINK,TOTOLINK  CLEAR LINK FIELD
         XC    TOTOTECB,TOTOTECB  CLEAR ECB
         LA    R1,SLPL            ADDRESS OF P LIST
         AQCTL                    POST ELEMENT
         WAIT  ECB=TOTOTECB       WAIT FOR COMPLEATION
         CLI   TOTOVBCD,IPL       WAS LAST OPERATION IPL
         BE    INIT70             YES BRANCH
         CLI   TOTORTCD,X00       WAS REQUEST PERFORMED
         BE    INIT75             YES BRANCH
         CLI   TOTORTCD,X08       WAS REQUEST NO-OPED
         BE    INIT80             YES BRANCH
         TM    CTLFLG,CLEANUP     CLEANUP
         BZ    INIT85             NO BRANCH
         MVC   INIT90+D43(D3),UCBNAME MOVE NAME TO MSG
         WTO   'IED154I TOTE CANNOT RETURN DEVICE          TO ORIGINAL *
               STATUS',ROUTCDE=10,DESC=7
         SPACE
INIT23   EQU   *                  DEQ
         DEQ   ,MF=(E,DEQLIST)    DEQ ALL DEVICES
         SPACE
         SPACE
INIT25   EQU   *                  BUMP TABLE
         LA    R3,D4(R3)          ADDRESS POINTER
         B     INIT10             AND CONTINUE THRU TABLE
         SPACE
INIT30   EQU   *                  370X PROCESSING
         TM    CTLFLG,CLEANUP     CLEANUP
         BO    INIT45             YES BRANCH                   @G36XRUV
         MVI   TOTOVBCD,DEACT     VERB TO DEACTIVE 370X
         B     INIT20             GO DEACT 370X
         SPACE
INIT35   EQU   *                  370X CLEANUP
         MVI   TOTOVBCD,IPL       IPL 370X VERB
         B     INIT20             GO IPL
         SPACE
         SPACE
INIT40   EQU   *                  NOT 370X CLEANUP
         TM    D0(R3),INITSTOP    TEST DEVICE INITIALLY STOPPED
         BO    INIT45             YES BRANCH
         MVI   TOTOVBCD,START     VERB TO START LINE
         B     INIT20             GO START LINE
         SPACE
         SPACE
INIT45   EQU   *                  RETURN INITIALLY STOPPED
*                                 DEVICE BY CLEARING LCBCPA
         L     R6,TOTAVTPT        GET AVT ADDRESS
         USING IEDQAVTD,R6        AVT
         L     R6,AVTTCB          GET TCAM TCB ADDRESS
         DROP  R6                 AVT
         USING IEDQTCB,R6         TCB
         L     R6,TCBDEB          GET BED CHAIN ADDRESS
         DROP  R6                 TCB
         SPACE
INIT50   EQU   *                  DEB SEARCH
         SR    R7,R7              CLEAR REG
         USING DEBNMSUB,R6        DEB
         IC    R7,DEBNMEXT        INITIALIZE TO NUMBER OF
*                                 EXTENTS
         LA    R8,DEBUCBAD        UCB CHAIN ADDRESS
         L     R9,DEBDEBAD        GET NEXT DEB ADDRESS
         L     R1,DEBDCBAD        GET DCB ADDRESS
         USING IHADCB,R1          DCB
         TM    DCBDSORG+D1,X44    TCAM DEB
         BZ    INIT60             NO GET NEXT DEB
         LA    R15,D1             INIT RLN COUNTER
         SPACE
INIT55   EQU   *                  UCB COMPARE
         DROP  R6                 DEB
         L     R6,D0(R8)          GET UCB ADDRESS
         LA    R6,D0(R6)          CLEAR HI BYTE
         CR    R6,R5              CORRECT UCB FOUND
         BE    INIT65             YES BRANCH
         LA    R8,D4(R8)          GET NEXT UCB ADDRESS
         LA    R15,D1(R15)        BUMP RLN
         BCT   R7,INIT55          LOOP TILL END OF UCB CHAIN
         SPACE
INIT60   EQU   *                  GET NEXT DEB.
         LR    R6,R9              IN CHAIN
         B     INIT50             AND CONTINUE SEARCH
         SPACE
INIT65   EQU   *                  NOW FIND LCB
         TM    CTLFLG,RLNSRCH     RLN SEARCH
         BO    D0(R14)            YES RETURN
         TM    0(R3),RNCHAN       Q-3705                       @ZM46824
         BO    INIT92             Y-GO TURN OFF TRMSTOTE       @ZM46824
         SR    R6,R6              CLEAR REG
         IC    R6,DCBEXLST        GET LCB LENGTH
         LR    R7,R15             GET RLN
         MR    R6,R6              RLN TIMES LCB LENGTH
         L     R6,DCBIOBAD        GET DUMMY UOB ADDRESS
         LA    R6,D0(R6,R7)       GET IOB ADDRESS
         LA    R7,LCBFLAG1-IEDQLCB GET OFFSET OT LCB START
         SR    R6,R7              SUBSTRACT FROM IOB ADDRESS
         USING IEDQLCB,R6         LCB
         DROP  R1                 DCB
         MVI   LCBCPA,X00         CLEAR TOTE INDICATOR
         DROP  R6                 LCB
         B     INIT23             GO DEQ RESOURCE
         SPACE
INIT70   EQU   *                  AFTER IPL
         TM    D0(R3),INITSTOP    370X INITIALLY DEACTIVE
         BO    INIT23             YES BRANCH GO DEQ
         MVI   TOTOVBCD,ACTIV     VERB TP ACTIVATE
         B     INIT20             GO ACTIVATE
         SPACE
INIT75   EQU   *                  ZERO RETURN FROM OPCONTROL
         TM    CTLFLG,CLEANUP     CLEANUP
         BO    INIT23             YES GO DEQ
         B     INIT25             NO GO CONTINUE THRU TABLE
         SPACE
INIT80   EQU   *                  8 RETURN FROM OP CONRROL
         TM    CTLFLG,CLEANUP     CLEANUP
         BO    INIT23             YES GO DEQ
         OI    D0(R3),INITSTOP    INDICATE DEVICE INITIALLY
*                                 STOPPED
         B     INIT25             GO CONTINUE THRU TABLE
         SPACE
INIT85   EQU   *                  CANNOT STOP DEVICE
*                                 CANCELL TRM
         MVC   DEVSTOP(D3),UCBNAME NAME TO MESSAGE
         IEDQMSG FUNCT=CEC,OTBUF=STOPFAIL,OTCNT=55,PREG0=R0,PREG1=R1
         XC    D4(D4,R3),D4(R3)   CLEAR FAILING ENTRY FROM TABLE
         OI    CTLFLG,CLEANUP     SET CLEAN UP FLAG
         OI    CTLFLG,INITCLUP    SET ERROR FLAG
         B     INIT05             GO CLEAN UP
         SPACE
INIT90   EQU   *                  EXIT
         TM    CTLFLG,INITCLUP    ERROR RETURN
         BO    RETURN             YES - TERMINATE
         LM    R3,R10,SAVE1       RESTORE REGS
         B     D0(R11)            RETURN
         SPACE 1                                               @G36XRUV
INIT92   DS    0H                 TURN OFF TRMSTOTE FOR NCP    @G36XRUV
         LA    R6,0(R1)           COPY DCB ADDRESS             @G36XRUV
         L     R9,TOTAVTPT        GET AVT ADDR                 @G36XRUV
         USING IEDQAVTD,R9                                     @G36XRUV
         L     R9,AVTRNMPT        GET TNT ADDR                 @G36XRUV
         DROP  R9                                              @G36XRUV
         USING IEDQTNTD,R9                                     @G36XRUV
         LA    R5,TNTFIRST        GET FIRST ENTRY IN TABLE     @G36XRUV
         SLR   R7,R7              CLEAR                        @G36XRUV
         IC    R7,TNTENLEN        LENGTH OF NAME FIELD         @G36XRUV
         DROP  R9                                              @G36XRUV
         USING IEDTNTA,R5         BASE FOR ADDR FIELD          @G36XRUV
TNTLOOP  DS    0H                                              @G36XRUV
         AR    R5,R7              BUMP PAST NAME FIELD         @G36XRUV
         ICM   R8,7,TNTTRMAD      GET TTE ADDR                 @G36XRUV
         LA    R9,TRMPRFSZ        PREFIX SIZE                  @G36XRUV
         SLR   R8,R9              POINT TO TTE PREFIX          @G36XRUV
         USING IEDNTRM,R8                                      @G36XRUV
         CLI   TRMTYPE,TRMLNCP    Q-IS THIS A LOCAL NCP ENTRY  @G36XRUV
         BNE   NXTTNT             N-GET NEXT TNT ENTRY         @G36XRUV
         L     R9,TRMDESTQ-1      GET QCB ADDR                 @G36XRUV
         USING IEDQQCB,R9                                      @G36XRUV
         CLM   R6,7,QCBDCBAD      Q-IS QCB FOR TEST DEVICE DCB @G36XRUV
         DROP  R9                                              @G36XRUV
         BE    DCBMATCH           Y-CORRECT TTE FOUND          @G36XRUV
NXTTNT   DS    0H                                              @G36XRUV
         LA    R5,L'TNTTRMAD(R5)  GET NEXT TNT ENTRY           @G36XRUV
         B     TNTLOOP                                         @G36XRUV
DCBMATCH DS    0H                                              @G36XRUV
         NI    TRMBYTE1,X'FF'-TRMSTOTE RESET TOTE BIT          @G36XRUV
         B     INIT35             GO IPL                       @G36XRUV
         DROP  R8                                              @G36XRUV
         DROP  R5                                              @G36XRUV
         EJECT
***************************************************************
*                                                             *
*        ROUTINE TO TURN OFF THE ALLOCATED AND TURN ON THE    *
*        OFFLINE BITS IN THE UCB FOR EVERY ENTRY IN THE TEST  *
*        DEVICE TABLE.                                        *
*                                                             *
***************************************************************
CONDIT   DS    0H                 BOUNDARY ALLIGNMENT
         USING UCB,R8
         STM   R3,R10,SAVE1       SAVE CALLER'S REGS
         LA    R3,TABLE           START OF TD TABLE
         LA    R5,TABLEND         END OF TABLE
DEALLOC  DS    0H                 BOUNDARY ALLIGNMENT
         CR    R3,R5              END OF TABLE
         BE    DEAL04             YES
         SLR   R8,R8              CLEAR                        @G36XRUV
         ICM   R8,3,D2(R3)        UCB ADDR FOR ENTRY           @G36XRUV
         BZ    DEAL04             YES
         MVC   UCBWORK(D4),D0(R8) MOVE UCB TO WORK AREA
         TM    CTLFLG,CLEANUP     CLEANUP
         BO    DEAL10             YES - RESET UCB'S
         NI    UCBWORK+D3,NALLOC  TURN OFF ALLOCATION FLAG
         NI    UCBWORK+D3,OFFLIN  TURN ON OFFLINE FLAG
         ST    R8,TOT102A         TARGET ADDRESS TO PLIST
         LA    R1,TOT102          POINT TO PLIST
         AQCTL MOVE UCB BACK
         LA    R3,D4(R3)          POINT TO NEXT ENTRY
         B     DEALLOC            CONTINUE THRU TABLE
DEAL04   DS    0H                 BOUNDARY ALLIGNMENT
         LM    R3,R10,SAVE1       RESTORE CALLER'S REGS
         BR    R11                RETURN
         SPACE
DEAL10   DS    0H                 BOUNDARY ALLIGNMENT
         TM    SRTESTAT,SRTEALOC  UCB ALLOCATED
         BZ    DEAL18             NO - CONTINUE
DEAL14   DS    0H                 BOUNDARY ALLIGNMENT
         DEQ   (QNAMEE,RNAMEE,2,SYSTEM),RET=HAVE DEQ
*                                 ALL UCB RESOURCES
         B     POST04             ISSUE WTOR
*
DEAL18   DS    0H                 BOUNDARY ALLIGNMENT
         TM    UCBFL5,UCBNALOC    UCB ALLOCATED
         BO    DEAL14             YES - - ISSUE DEQ
*
         DROP  R8
         OI    UCBWORK+D3,ONALLOC RESET UCB
         ST    R8,TOT102A         TARGET ADDRESS TO PLIST
         LA    R1,TOT102          POINT TO PLIST
         AQCTL                    MOVE UCB BACK
         LA    R3,D4(R3)          POINT TO NEXT ENTRY
         B     DEALLOC            CONTINUE THRU TABLE
         EJECT
***************************************************************
*                                                             *
*        ROUTINE TO WAIT UNTIL SUBCHANNEL(S) ARE RETURNED     *
*        TO TOTE. ALL SUBCHANNELS ARE THEN REALLOCATED TO TCAM*
*        AND RETURNED TO THEIR INITIAL STATUS.                *
*                                                             *
***************************************************************
POST     DS    0H                 BOUNDARY ALLIGNMENT
POST04   DS    0H                 BOUNDARY ALLIGNMENT
         XC    ECB(D4),ECB        CLEAR ECB
         WTO   'IED275D REPLY ''TOTE'' TO RETURN TO TCAM SUBCHANNELS ',*
               ROUTCDE=10,DESC=7
         MVC   POSTMSG4(D53),NCMMSG2 GET CUU'S
WTORPOS8 WTOR  '                                                     ',*
               INBUF,4,ECB,ROUTCDE=(8,10),DESC=2
POSTMSG4 EQU   WTORPOS8+D16       PLACE FOR CUU'S
         SPACE
         WAIT  ECB=ECB            WAIT FOR REPLY
         SPACE
         OC    INBUF(D4),CAP      INSURE CAPITALIZATION
         CLC   INBUF(D4),TOTE     REALLOCATION REQUEST
         BNE   POST04             NO
*
*        RETURN SUBCHANNELS TO TCAM
*
         OI    CTLFLG,CLEANUP     SHOW CLEANUP
         LA    R4,ENQLOOP         SET ENQ RETRY COUNT
POST0C   DS    0H                 BOUNDARY ALLIGNMENT
         ENQ   (QNAMEE,RNAMEE,E,2,SYSTEM),RET=HAVE ENQ
*                                 ALL UCB RESOURCES
         LTR   R15,R15            ENQ SUCCESSFUL
         BZ    POST08             YES
*
         STIMER WAIT,DINTVL=TIME  WAIT 5 SECONDS
*
         BCT   R4,POST0C          RETRY ENQ
         B     POST04             ISSUE WTOR AGAIN
POST08   DS    0H                 BOUNDARY ALLIGNMENT
         BAL   R11,CONDIT         RESET UCB'S
         BAL   R11,INITSTAT       RETURN LINES TO TCAM
RETURN   DS    0H                 BOUNDARY ALLIGNMENT
         DEQ   (QNAMEE,RNAMEE,2,SYSTEM),RET=HAVE DEQ
*                                 ALL UCB RESOURCES
RETURN1  DS    0H                 BOUNDARY ALLIGNMENT          @Y17XAUU
         OI    TOTFLG06,TOTTERMS  SET TERMINATE FLAG
         SPACE
         IEDQMSG MSGID=016,FUNCT=CEC,LINK=YES OLT ENDED
         SPACE
         L     R13,D4(R13)        CALLER'S SAVE ADDR
         LM    R14,R12,D12(R13)   RESTORE CALLER'S REGS
         SR    R15,R15            CLEAR RETURN CODE REG
         BR    R14                RETURN TO MOTHER
         EJECT
STOPFAIL DC    CL47'IED221I TRM CANCELLED - STOP FAILED FOR DEVICE '
*        MSG IED221I IS SELF-EXPLANATORY                         Y06335
DEVSTOP  DC    CL8'        '      NAME FOR MESSAGE
QNAME    DC    CL8'SYSZTOTE'       MAJOR ELEMENT                 Y06335
RNAME    DC    8CL1' '            MINOR ELEMENT
DEQLIST  DEQ   (QNAME,RNAME,8,STEP),RET=HAVE,MF=L
ENQLIST  ENQ   (QNAME,RNAME,E,8,STEP),RET=USE,MF=L
CTLFLG   DC    XL1'0'             CONTROL FLAG
RNCHAN   EQU   X'80'              3705 MODE
INITSTOP EQU   X'40'              LINE ALREADY STOPPED OR DEACT
CLEANUP  EQU   X'20'              CLEAN UP ENTRY
RLNSRCH  EQU   X'10'              RLN SEARCH FLAG
INITCLUP EQU   X'01'              ERROR OCCURRED ON STOP
*
*
INBUF    DC    XL4'0'             INPUT BUFFER
TOTE     DC    CL4'TOTE'          CHAR CONSTANT
CAP      DC    X'40404040'        BLANK CHARS
ENDTABLE DC    X'FFFF'            END OF UCB TABLE             @G36XRUV
*
*
*
*        PARAMETER LIST FOR SVC 102                   *
         DS    0F                 F-WORD ALLIGNMENT
SLPL     DC    X'0C'              START
         DC    AL3(0)             ADDRESS OF FIRST
         DC    X'80'              FLAG END
         DC    AL3(0)             ADDRESS OF LAST
*        SVC 102 MOVE DATA PARAMETER LIST
         DS    0F                 BOUNDARY ALLIGNMENT
TOT102   DC    X'08'              FLAG FOR DATA MOVE
         DC    AL3(UCBWORK)       DATA ADDRESS
TOT102A  DC    X'00'              RESERVED
         DC    AL3(0)             TARGET ADDR
         DC    X'80'              LAST WORD FLAG
         DC    AL3(LENGADDR)      LENGTH
UCBWORK  DC    AL4(0)             WORK AREA
LENGADDR DC    H'04'              LENGTH OF DATA TO MOVE
PACK1    DC    D'0'               PACK WORK AREA
BIN1     DC    F'0'               LOWER BOUND INCLUS CUU
BIN3     DC    F'0'               UPPER BOUND INCLUS CUU
TABLE    DC    256F'0'            TEST DEVICE TABLE
TABLEND  DS    0C                 END OF TABLE
UCBSAVE  DS    A                  UCB SAVE AREA
KUCB     DS    H                  TEST UCB ADDR SAVE AREA
         DS    0D                 BOUNDARY ALLINGMENT
TDFLG    DC    X'00'              PROGRAM FLAG BYTE
WORK1    DS    3F                 WORK AREA
WORK2    DS    2D                 WORK AREA
         SPACE
         DS    0F                 BOUNDARY ALLIGNMENT
ECB      DS    2F                 ECB
         SPACE
F4       DC    F'4'               VALUE OF 4
MULT     DC    8XL1'0'            WORK AREA
TTEADDR  DC    F'0'               WORK AREA TO ALLIGN TTE ADDR
PATCH    DC    CL50' '            IEDQWF1 PATCH AREA
SAVE1    DC    8F'0'              SUBRTN SAVE AREA
SAVE2    DC    6F'0'              REGS. SAVE AREA
ANSWER   DS    CL3                INPUT BUFFER
TIMOUT   DC    X'F0F0F0F3F0F0F0F0' TIMER FOR TIMEOUT
TIME     DC    X'F0F0F0F0F0F5F0F0' TIMER FOR ENQ
QNAMEE   DC    C'SYSIEFSD'        MAJOR NAME
RNAMEE   DC    C'Q4'              MINOR NAME
         DS    0F
WORK4    DS    XL5                WORK AREA
XLATE    DC    X'000A0B0C0D0E0F'  XLATE TABLE
*        MSG IED335I IS SELF-EXPLANATORY                         Y06335
TMID     DC    C'IED335I                   ***CONTROL TERMINAL ID IS' *
         DC    X'40'              BLANK CHAR
TRMCTID  DC    CL5'XX***'         MESSAGE TO PRINT CT ID
TMIDE    DS    0C                 END OF MESSAGE
         EJECT
         TLGBD
         EJECT
TIOT     DSECT
         IEFTIOT1
         EJECT
         TQCBD
         EJECT
         TTCBD
         EJECT
         DCBD  DSORG=TX
         EJECT
         TPRFD
         EJECT
         TLCBD
         EJECT
CVT      DSECT
         CVT   LIST =YES
         EJECT
UCB      DSECT
         IEFUCBOB LIST=YES
         EJECT
         TAVTD
         EJECT
         TDEBD
         EJECT
         TTNTD
         EJECT
         TTRMD
         EJECT
         RESPL
         EJECT
         OLTCB
         SPACE 2
         END
