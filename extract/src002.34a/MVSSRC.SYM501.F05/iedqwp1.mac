QWP1     TITLE 'IEDQWP1 - TOTE DPRINT SERVICE MODULE (2 OF 3)'
IEDQWP1  CSECT
*A000000-999999                                                @Y16X5U0
* CHANGE ACTIVITY AS FOLLOWS:                                    S22024
*A166000                                                        SA41596
*A118700,674000,801000,874000                                  @Y17XAUU
***********************************************************************
*                                                                     *
* MODULE NAME = IEDQWP1 (TCAM,TOTE)                            @Y17XAUU
*                                                              * S22024
* DESCRIPTIVE NAME = TOTE DPRINT MODULE 2                      * S22024
*                                                              * S22024
* COPYRIGHT = NONE                                             * S22024
*                                                              * S22024
* STATUS:                                                      * S99528
*                                                              * S99528
*    VERSION 10.0                                              @Y17XAUU
*                                                              * S99528
* FUNCTION:                                                    * S99528
*                                                              * S99528
*    THIS ROUTINE CONTINUES THE SERVICING OF THE DPRINT MACRO. * S99528
*                                                              * S99528
*    THE CALLING ROUTINE'S DPRINT PARAMETER LIST IS EXAMINED.  * S99528
*    IF EXPECTED AND/OR RECIEVED CSW LINES ARE REQUIRED, THE   * S99528
*    LINES ARE FORMATTED ONE AT A TIME AND IEDQWO (ACCESS      * S99528
*    MANAGER) IS CALLED TO ROUTE THE FORMATTED LINE TO THE     * S99528
*    PROPER OUTPUT DEVICE.                                     * S99528
*                                                              * S99528
*    CONTROL IS TRANSFERRED TO IEDQWP2(DPRINT SERVICE MODULE 2)* S99528
*    TO CONTINUE PROCESSING THE DPRINT MACRO.                  * S99528
*                                                              * S99528
* ENTRY POINT:                                                 * S99528
*                                                              * S99528
*         IEDQWP1 - CALLED BY IEDQWP                           * S99528
*                                                              * S99528
* PURPOSE: SEE FUNCTION                                        @Y17XAUU
*                                                              @Y17XAUU
* PROCESSOR: ASSEMBLER XF                                      @Y17XAUU
*                                                              @Y17XAUU
* MODULE TYPE: PROCEDURE                                       @Y17XAUU
*                                                              @Y17XAUU
* MODULE SIZE: 2K MAXIMUM                                      @Y17XAUU
*                                                              @Y17XAUU
* LINKAGE: BALR R14,R15 FROM IEDQWA                            @Y17XAUU
*                                                              @Y17XAUU
* MACROS: IEDHJN,SAVE                                          @Y17XAUU
*                                                              @Y17XAUU
* INPUT:                                                       * S99528
*                                                              * S99528
*    REGISTERS 01,02,03,13,14,15 CONTAIN THE FOLLOWING VALUES: * S99528
*                                                              * S99528
*    01 - DPRINT PARAMETER LIST ADDRESS                        * S99528
*    02 - OLTCB POINTER                                        * S99528
*    03 - SECTION PREFACE ADDRESS                              * S99528
*    13 - SAVE AREA ADDRESS                                    * S99528
*    14 - RETURN ADDRESS                                       * S99528
*    15 - ENTRY POINT ADDRESS                                  * S99528
*                                                              * S99528
* OUTPUT:                                                      * S99528
*                                                              * S99528
*    REGISTER 15 CONTAINS THE FOLLOWING VALUE.                 * S99528
*                                                              * S99528
*    15 - RETURN CODE:                                         * S99528
*           X'00' -NORMAL COMPLETION                           * S99528
*                                                              * S99528
* EXTERNAL REFERENCES:                                         * S99528
*                                                              * S99528
*         IEDQWO-ACCESS MANAGER-TO ROUTE THE OUTPUT MESSAGE    * S99528
*         LINE.                                                * S99528
*                                                              * S99528
* EXITS NORMAL:                                                * S99528
*                                                              * S99528
*         BR   TO IEDQWP2 AFTER 'DPR170'                       @Y17XAUU
*                                                              * S99528
* EXITS ERROR:                                                 * S99528
*                                                              * S99528
*         NONE                                                 * S99528
*                                                              * S99528
* TABLES/WORK AREAS:                                           * S99528
*                                                              * S99528
*    OLTCB                                                     * S99528
*                                                              * S99528
* ATTRIBUTES:                                                  * S99528
*                                                              * S99528
*    ENABLED,PROBLEM PROGRAM MODE,RESIDENT,REENTRANT           @Y17XAUU
*                                                              * S99528
* NOTES: SEE BELOW:                                            @Y17XAUU
*                                                              @Y17XAUU
*  DEPENDENCIES: EBCDIC CHARACTER CODE                         @Y17XAUU
*                                                              @Y17XAUU
*  RESTRICTIONS: NONE                                          @Y17XAUU
*                                                              @Y17XAUU
*  REGISTERS CONVENTION: SEE REGISTERS ASSIGNMENT              @Y17XAUU
*                                                              @Y17XAUU
*  PATCH LABEL: PATCH                                          @Y17XAUU
*                                                              * S99528
***********************************************************************
**************************************************************** S99528
*                                                              * S99528
*                           EQUATES                            * S99528
*                                                              * S99528
**************************************************************** S99528
R0       EQU   0                       GENERAL PURPOSE  REGS     S99528
R1       EQU   1                       GENERAL PURPOSE  REGS     S99528
R2       EQU   2                       GENERAL PURPOSE  REGS     S99528
R3       EQU   3                       GENERAL PURPOSE  REGS     S99528
R4       EQU   4                       GENERAL PURPOSE  REGS     S99528
R5       EQU   5                       GENERAL PURPOSE  REGS     S99528
R6       EQU   6                       GENERAL PURPOSE  REGS     S99528
R7       EQU   7                       GENERAL PURPOSE  REGS     S99528
R8       EQU   8                       GENERAL PURPOSE  REGS     S99528
R9       EQU   9                       GENERAL PURPOSE  REGS     S99528
R10      EQU   10                      GENERAL PURPOSE  REGS     S99528
R11      EQU   11                      GENERAL PURPOSE  REGS     S99528
R12      EQU   12                      GENERAL PURPOSE  REGS     S99528
R13      EQU   13                      GENERAL PURPOSE  REGS     S99528
R14      EQU   14                      GENERAL PURPOSE  REGS     S99528
R15      EQU   15                      GENERAL PURPOSE  REGS     S99528
D0       EQU   0                  DISP OF 0                      S22024
D1       EQU   1                  DISP OF 1                      S22024
D4       EQU   4                  DISP OF 4                      S22024
D6       EQU   6                  DISP OF 6                      S22024
D11      EQU   11                 DISP OF 11                     S22024
D19      EQU   19                 DISP OF 19                     S22024
D32      EQU   32                 DISP OF 32                     S22024
D37      EQU   37                 DISP OF 37                     S22024
D28      EQU   28                 DISP OF 28                     S22024
XFF      EQU   X'FF'              MASK ALL                       S22024
DISREG2  EQU   28                 DISPLACEMENT TO REG 2        @Y17XAUU
         SPACE
         USING IEDQWP1,R15
*
IEDQWP1  IEDHJN STARP1,HJN
*
         SPACE 2
         DROP  R15
         USING IEDQWP1,R4
         USING TOTOLTCB,R2        OLTCB BASE                     S22024
         USING SPREF1,R3
         SPACE
         SAVE  (14,12)
         LR    R4,R15             SET BASE REG.
         LA    R15,TOTSAVE1       MY SAVE AREA                   S22024
         ST    R15,8(R13)         PUT ADDR IN CALLERS SAVE AREA.
         ST    R13,4(R15)         SAVE ADDR OF CALLERS SAVE AREA.
         LR    R13,R15            SET REG 13 TO MY SAVE
         SPACE
         L     R12,4(R1)          GET ADDR OF DPRINT CONTROL WORD.
         SPACE
         LA    R10,EPINDX         EP MASK CHECK FIELD ADDR       S22024
         SR    R14,R14            CLEAR INDEX REG                S22024
         IC    R14,TOTFLG08       GET LEVEL CNTL FLAG (BITS 4-7) S22024
         SLL   R14,D28            CLEAR                          S22024
         SRL   R14,D28            UNWANTED BITS                  S22024
         AR    R10,R14            POINT TO EP LEVEL CTL FLAG     S22024
         MVC   EPSAVE(D1),D0(R10) SAVE IT                        S22024
         SPACE
         TM    EPSAVE,RESULTS     PRINT RESULTS                  S22024
         BZ    DPR170             BRANCH IF NO - EXIT TO QWP2    S22024
         L     R3,TOTOLTEN        SECTION PREFACE BASE          SA41596
         SPACE
         SPACE 2
***********************************************************************
*        EXPECTED  CSW                                                *
***********************************************************************
         SPACE
DPR085   TM    2(R12),X'20'       PRINT XPTD CSW1 ?
         BO    DPR090             BRANCH IF YES.
         SPACE
         TM    2(R12),X'08'       PRINT XPTD CSW2 ?
         BO    DPR100             BRANCH IF YES.
         SPACE
         B     DPR115             GO PROCESS RECEIVED  CSW.
         SPACE
DPR090   TM    2(R12),X'08'       PRINT XPTD CSW2 ?
         BO    DPR105             BRANCH IF YES.
         SPACE
* PRINT CSW1 ONLY.
         SPACE
         MVC   PB+D1(D4),DPXPTD   PUT 'XPTD' IN PRINT BUFFER     S22024
         MVC   PB+D6(D4),DPCSW1   PUT 'CSW1' IN PRINT BUFFER     S22024
         LA    R7,13(R12)         GET ADDR OF XPTD CSW1 MASK.
         L     R13,24(R1)         GET ADDR OF XPTD CSW1.
         SPACE
DPR095   LA    R14,WRK            GET OUTPUT ADDR FOR CONVERT
         LA    R15,8              GET COUNT.
         BAL   R10,DPRCNV         CONVERT XPTD CSW1 TO EBCDIC.
         SPACE
         MVC   PB+D11(D19),DPXX   SET UP FOR UNUSED              S22024
         LA    R6,WRK             GET EBCDIC CSW ADDR.
         SPACE
         TM    0(R7),X'80'        KEY PROVIDED ?
         BO    DPR095A            BRANCH IF YES.
         SPACE
         TM    0(R7),X'40'        CMD ADR PROVIDED ?
         BO    DPR096             BRANCH IF YES.
         SPACE
         TM    0(R7),X'20'        STATUS PROVIDED ?
         BO    DPR097             BRANCH IF YES.
         SPACE
         TM    0(R7),X'10'        RESIDUAL COUNT PROVIDED ?
         BO    DPR098             BRANCH IF YES.
         B     DPR099             PRINT.
         SPACE
DPR095A  MVC   PB+11(2),0(R6)     PUT KEY IN PRINT BUFFER.
         LA    R6,2(R6)           POINT TO CMD ADDR.
         SPACE
DPR096   TM    0(R7),X'40'        CMD ADDR PROVIDED ?
         BZ    DPR099             BRANCH IF NO.
         SPACE
         MVC   PB+14(6),0(R6)     PUT CMD ADDR IN PRINT BUFFER.
         LA    R6,6(R6)           POINT TO STATUS.
         SPACE
DPR097   TM    0(R7),X'20'        STATUS PROVIDED ?
         BZ    DPR099             BRANCH IF NO.
         SPACE
         MVC   PB+21(4),0(R6)     PUT STATUS IN PRINT BUFFER.
         LA    R6,4(R6)           POINT TO RESIDUAL COUNT.
         SPACE
DPR098   TM    0(R7),X'10'        RESIDUAL COUNT PROVIDED ?
         BZ    DPR099             BRANCH IF NO.
         SPACE
         MVC   PB+26(4),0(R6)     PUT RESIDUAL COUNT IN PRINT BUFFER.
         SPACE
DPR099   BAL   R10,DPRPNT         PRINT XPTD CSW .
         B     DPR115             GO PROCESS RECEIVED CSW.
         SPACE
* PRINT CSW2 ONLY.
         SPACE
DPR100   DS    0H                 BOUNDARY ALLIGNMENT            S22024
         MVC   PB+D1(D4),DPXPTD   PUT 'XPTD' IN PRINT BUFFER     S22024
         MVC   PB+D6(D4),DPCSW2   PUT 'CSW2' IN PRINT BUFFER     S22024
         IC    R7,13(R12)         GET XPTD CSW2 MASK.
         SLL   R7,4               POSITION IN UPPER HALF BYTE.
         STC   R7,WRK+50          SAVE CSW2 MASK.
         LA    R7,WRK+50          POINT TO CSW2 MASK.
         L     R13,32(R1)         GET ADDR OF XPTD CSW2.
         B     DPR095             GO PROCESS XPTD CSW2.
         SPACE
* PRINT CSW1 AND CSW2.
         SPACE
DPR105   DS    0H                 BOUNDARY ALLIGNEMENT           S22024
         MVC   PB+D1(D4),DPXPTD   PUT 'XPTD' IN PRINT BUFFER     S22024
         MVC   PB+D6(D4),DPCSW1   PUT 'CSW1' IN PRINT BUFFER     S22024
         MVC   PB+D32(D4),DPCSW2  PUT 'CSW2' IN PRINT BUFFER     S22024
         L     R13,24(R1)         GET ADDR OF XPTD CSW1.
         LA    R14,WRK            GET OUTPUT ADDR FOR CONVERT.
         LA    R15,8              GET COUNT.
         BAL   R10,DPRCNV         CONVERT XPTD CSW1 TO EBCDIC.
         SPACE
         MVC   PB+D11(D19),DPXX   SET UP UNUSED FIELDS           S22024
         MVC   PB+D37(D19),DPXX   SET UP UNUSED FIELDS           S22024
         LA    R6,WRK             GET EBCDIC CSW ADR.
         SPACE
         TM    0(R7),X'80'        KEY PROVIDED ?
         BO    DPR106             BRANCH IF YES.
         SPACE
         TM    0(R7),X'40'        CMD ADDR PROVIDED ?
         BO    DPR107             BRANCH IF YES.
         SPACE
         TM    0(R7),X'20'        STATUS PROVIDED ?
         BO    DPR108             BRANCH IF YES.
         SPACE
         TM    0(R7),X'10'        CMD ADDR PROVIDED ?
         BO    DPR109             BRANCH IF YES.
         B     DPR109A            BRANCH.
DPR106   MVC   PB+11(2),0(R6)     PUT KEY IN PRINT BUFFER.
         LA    R6,2(R6)           POINT TO CMD ADDR.
         SPACE
DPR107   TM    0(R7),X'40'        CMD ADDR PROVIDED ?
         BZ    DPR109A            BRANCH IF NO.
         SPACE
         MVC   PB+14(6),0(R6)     PUT CMD ADDR IN PRINT BUFFER.
         LA    R6,6(R6)           POINT TO STATUS.
         SPACE
DPR108   TM    0(R7),X'20'        STATUS PROVIDED ?
         BZ    DPR109A            BRANCH IF NO.
         SPACE
         MVC   PB+21(4),0(R6)     PUT STATUS IN PRINT BUFFER.
         LA    R6,4(R6)           POINT TO RESIDUAL COUNT.
         SPACE
DPR109   TM    0(R7),X'10'        RESIDUAL COUNT PROVIDED ?
         BZ    DPR109A            BRANCH IF NO.
         SPACE
         MVC   PB+26(4),0(R6)     PUT RESIDUAL COUNT IN PRINT BUFFER.
         SPACE
         SPACE
DPR109A  EQU   *
         L     R13,32(R1)         GET ADDR OF XPTD CSW2.
         LA    R14,WRK            GET OUTPUT ADDR FOR CONVERT.
         LA    R15,8              GET COUNT.
         BAL   R10,DPRCNV         CONVERT XPTD CSW2 TO EBCDIC.
         SPACE
         LA    R7,13(R12)         GET ADDR OF XPTD CSW2 MASK.
         MVC   PB+D37(D19),DPXX   SET UP UNUSED                  S22024
         LA    R6,WRK             GET EBCDIC CSW ADDR.
         SPACE
         TM    0(R7),X'08'        KEY PROVIDED ?
         BO    DPR110             BRANCH IF YES.
         SPACE
         TM    0(R7),X'04'        CMD ADDR PROVIDED ?
         BO    DPR111             BRANCH IF YES.
         SPACE
         TM    0(R7),X'02'        STATUS PROVIDED ?
         BO    DPR112             BRANCH IF YES.
         SPACE
         TM    0(R7),X'01'        RESIDUAL COUNT PROVIDED ?
         BO    DPR113             BRANCH IF YES.
         B     DPR113A            BRANCH.
         SPACE
DPR110   MVC   PB+11(2),0(R6)     PUT KEY IN PRINT BUFFER.
         LA    R6,2(R6)           POINT TO CMD ADDR.
         SPACE
DPR111   TM    0(R7),X'04'        CMD ADDR PROVIDED ?
         BZ    DPR113A            BRANCH IF NO.
         SPACE
         MVC   PB+14(6),0(R6)     PUT CMD ADDR IN PRINT BUFFER.
         LA    R6,6(R6)           POINT TO STATUS.
         SPACE
DPR112   TM    0(R7),X'02'        STATUS PROVIDED ?
         BZ    DPR113A            BRANCH IF NO.
         SPACE
         MVC   PB+21(4),0(R6)     PUT STATUS IN PRINT BUFFER.
         LA    R6,4(R6)           POINT TO RESIDUAL COUNT.
         SPACE
DPR113   TM    0(R7),X'01'        RESIDUAL COUNT PROVIDED ?
         BZ    DPR113A            BRANCH IF NO.
         SPACE
         MVC   PB+26(4),0(R6)     PUT RESIDUAL COUNT IN PRINT BUFFER.
         SPACE
DPR113A  BAL   R10,DPRPNT         PRINT XPTD CSW1 AND CSW2.
         SPACE 2
***********************************************************************
*        RECEIVED  CSW                                                *
***********************************************************************
         SPACE
DPR115   TM    2(R12),X'10'       PRINT RECEIVED CSW1 ?
         BO    DPR120             BRANCH IF YES.
         TM    2(R12),X'04'       PRINT RECEIVED CSW2 ?
         BO    DPR140             BRANCH IF YES.
         B     DPR170             GO PROCESS SENSE.
         SPACE
DPR120   TM    2(R12),X'04'       PRINT RCVD CSW2 ?
         BO    DPR145             BRANCH IF YES.
         SPACE
* PRINT RCVD CSW1 ONLY.
         SPACE
         MVC   PB+D1(D4),DPRCVD   PUT 'RCVD' IN PRINT BUFFER     S22024
         MVC   PB+D6(D4),DPCSW1   PUT 'CSW1' IN PRINT BUFFER     S22024
         SPACE
DPR122   EQU   *
         SPACE
         L     R13,28(R1)         GET ADDR OF RCVD CSW1.
         LA    R14,WRK            GET OUTPUT ADDR FOR CONVERT.
         LA    R15,8              GET COUNT.
         BAL   R10,DPRCNV         CONVERT RCVD CSW TO EBCDIC.
         SPACE
         MVC   PB+11(2),WRK       PUT KEY IN PRINT BUFFER.
         MVC   PB+14(6),WRK+2     PUT CMD ADDR IN PRINT BUFFER.
         MVC   PB+21(4),WRK+8     PUT STATUS   IN PRINT BUFFER.
         MVC   PB+26(4),WRK+12    PUT RES CNT  IN PRINT BUFFER.
         SPACE
         TM    1(R12),X'20'       RCVD CSW1 ERROR ?
         BZ    DPR130             BRANCH IF NO.
DPR125   MVI   PB,C'*'            PREFIX RCVD CSW WITH '*'.
         B     DPR135             GO PRINT.
         SPACE
DPR130   TM    1(R12),X'10'       RCVD CSW2 ERROR ?
         BO    DPR125             BRANCH IF YES.
         SPACE
DPR135   BAL   R10,DPRPNT         PRINT RCVD CSW.
         B     DPR170             GO PROCESS SENSE.
         SPACE
* PRINT RCVD CSW2 ONLY.
         SPACE
DPR140   DS    0H                 BOUNDARY ALLIGNMENT            S22024
         MVC   PB+D1(D4),DPXPTD   PUT 'XPTD' IN PRINT BUFFER     S22024
         MVC   PB+D6(D4),DPCSW2   PUT 'CSW2' IN PRINT BUFFER     S22024
         L     R13,36(R1)         GET ADDR OF RCVD CSW2
         B     DPR122             GO PROCESS RCVD CSW2.
         SPACE
* PRINT RCVD CSW1 AND CSW2.
         SPACE
DPR145   TM    1(R12),X'20'       CSW1 ERROR ?
         BZ    DPR155             BRANCH IF NO.
DPR150   MVI   PB,C'*'            PREFIX RCVD CSW WITH '*'.
         B     DPR160             GO GET CSW'S.
         SPACE
DPR155   TM    1(R12),X'10'       CSW2 ERROR ?
         BO    DPR150             BRANCH IF YES.
         SPACE
DPR160   DS    0H                 BOUNDARY ALLIGNMENT            S22024
         MVC   PB+D1(D4),DPRCVD   PUT 'RCVD' IN PRINT BUFFER     S22024
         MVC   PB+D6(D4),DPCSW1   PUT 'CSW1' IN PRINT BUFFER     S22024
         MVC   PB+D32(D4),DPCSW2  PUT 'CSW2' IN PRINT BUFFER     S22024
         L     R13,28(R1)         GET ADDR OF RCVD CSW1
         LA    R14,WRK            GET OUTPUTADDR FOR CONVERT.
         LA    R15,8              GET COUNT.
         BAL   R10,DPRCNV         CONVERT RCVD CSW1 TO EBCDIC.
         SPACE
         MVC   PB+11(2),WRK+0     PUT KEY IN PRINT BUFFER.
         MVC   PB+14(6),WRK+2     PUT CMD ADDR IN PRINT BUFFER.
         MVC   PB+21(4),WRK+8     PUT STATUS   IN PRINT BUFFER.
         MVC   PB+26(4),WRK+12    PUT RES. CNT IN PRINT BUFFER.
         SPACE
         L     R13,36(R1)         GET ADDR OF RCVD CSW2
         LA    R14,WRK            GET OUTPUT ADDR FOR CONVERT.
         LA    R15,8              GET COUNT.
         BAL   R10,DPRCNV         CONVERT  RCVD CSW2 TO EBCDIC.
         SPACE
         MVC   PB+37(2),WRK+0     PUT KEY IN PRINT BUFFER.
         MVC   PB+40(6),WRK+2     PUT CMD ADDR IN PRINT BUFFER.
         MVC   PB+47(4),WRK+8     PUT STATUS   IN PRINT BUFFER.
         MVC   PB+52(4),WRK+12    PUT RES. CNT IN PRINT BUFFER.
         SPACE
         BAL   R10,DPRPNT         PRINT RCVD CSW1 AND CSW2.
         SPACE 2
***********************************************************************
*        EXIT TO IEDQWP2                                              *
***********************************************************************
         SPACE
DPR170   EQU   *
         SPACE
         L     R13,TOTSAVE1+D4    CALLER'S SAVE AREA             S22024
         L     R14,12(R13)        RESTORE RETURN ADDR.
         USING RESPL,R5           SET RESPL ADDRESSABILITY     @Y17XAUU
         L     R5,TOTRESPL        GET RESPL ADDRESS            @Y17XAUU
         L     R15,RESPLP2        GET IEDQWP2 ADDRESS          @Y17XAUU
         LM    R2,R12,DISREG2(R13) RESTORE REGISTERS           @Y17XAUU
         BR    R15                EXIT TO IEDQWP2              @Y17XAUU
         DROP  R5                                              @Y17XAUU
         EJECT
***********************************************************************
*        DPRCNV - HEX TO EBCDIC CONVERT                               *
*                                                                     *
*              CALL SEQ -  R13 = INPUT ADDRESS                        *
*                          R14 = OUTPUT ADDRESS                       *
*                          R15 = INPUT BYTE COUNT                     *
*                                                                     *
*                          BAL R10,DPRCNV                             *
***********************************************************************
         SPACE
DPRCNV   ST    R5,WRK+120         SAVE WORK REG R5.
         ST    R6,WRK+124         SAVE WORK REG R6.
         SPACE
         LTR   R15,R15            COUNT = 0 ?
         BZ    DPRCNV8            BRANCH IF YES.
         SPACE
         AR    R14,R15         ADD COUNT TO OUTPUT ADDR.
         AR    R14,R15            ADD COUNT AGAIN.
         AR    R13,R15            ADD COUNT TO INPUT ADDR.
         SR    R5,R5              CLEAR R5.
         SR    R6,R6              CLEAR R6.
         SPACE
DPRCNV4  DS    0H                 BOUNDARY ALLIGNMENT            S22024
         S     R14,F2             GET ADDR OF LAST BYTE OF OUTPUT
*                                                                S22024
         N     R5,F15             GET BITS 4-7 IN R5             S22024
         BCTR  R13,0              GET ADDR OF LAST BYTE OF INPUT.
         IC    R6,0(R13)          GET HEX BYTE.
         LR    R5,R6              PUT IN R5.
         SRL   R6,4               GET BITS 0-3 IN R6.
         N     R5,F15             GET BITS 4-7 IN R5             S22024
         IC    R6,DPREBC(R6)      CONVERT BITS 0-3 TO EBCDIC CHAR.
         IC    R5,DPREBC(R5)      CONVERT BITS 4-7 TO EBCDIC CHAR.
         STC   R6,0(R14)          STORE 1ST EBCDIC CHAR IN OUTPUT.
         STC   R5,1(R14)          STORE 2ND EBCDIC CHAR IN OUTPUT.
         BCT   R15,DPRCNV4        GET NEXT HEX BYTE.
DPRCNV8  L     R5,WRK+120         RESTORE WORK REG R5.
         L     R6,WRK+124         RESTORE WORK REG R6.
         SPACE
         BR    R10                RETURN TO CALLER.
         SPACE
DPREBC   DC    C'0123456789ABCDEF' HEX TO EBCDIC CONVERT TABLE.
         EJECT
***********************************************************************
*        PRINT LINE AND FILL PRINT BUFFER WITH BLANKS                 *
**********************************************************************
         SPACE
DPRPNT   EQU   *
         SPACE
         LA    R14,L'PB           GET LENGTH OF PRINT BUFFER.
         LA    R15,PB+79          GET ADDR OF LAST CHAR.
         SPACE
DPRPNT5  EQU   *
         CLI   0(R15),C' '        IS THIS CHAR A BLANK ?
         BNE   DPRPNT10           BRANCH IF NO.
         BCT   R14,DPRPNT8        SUBTRACT 1 FROM COUNT.
         B     DPRPNT10           BUFFER IS ALL BLANKS - SET CNT = 0.
         SPACE
DPRPNT8  EQU   *
         BCT   R15,DPRPNT5        CHECK NEXT CHAR.
DPRPNT10 EQU   *
         STC   R14,TOTOTCNT       MSG COUNT                      S22024
         NI    TOTFLG05,XFF-TOTCECOM-TOTREPLY-TOTEXIOF           S22024
         OI    TOTFLG05,TOTPRINT  SET DPRINT FLAG                S22024
         LA    R13,TOTSAVE1       MY SAVE AREA                   S22024
         USING RESPL,R5           SET RESPL ADDRESSABILITY     @Y17XAUU
         L     R5,TOTRESPL        GET RESPL ADDRESS            @Y17XAUU
         L     R15,RESPLWO        GET I/O ROUTINE ADDRESS      @Y17XAUU
         BALR  R14,R15            CALL I/O ROUTINE             @Y17XAUU
         DROP  R5                                              @Y17XAUU
DPRCLR   MVI   PB,C' '            PUT BLANK IN PRINT BUFFER.
         MVC   PB+1(L'PB-1),PB    FILL BUFFER WITH BLANKS.
         BR    R10                RETURN.
         EJECT
PATCH    DS    CL60               MAINTENANCE AREA.
         SPACE
EPINDX   DS    0CL4               EP MASK TABLE                  S22024
EP0      DC    X'80'              PRINT HEADER                   S22024
EP1      DC    X'E0'              PRINT HEADER/DESC/MSG          S22024
EP2      DC    X'90'              PRINT HEADER/RESULTS           S22024
EP3      DC    X'F0'              PRINT ALL                      S22024
*                                                                S22024
HDR      EQU   X'80'              HEADER LINE                    S22024
DESC     EQU   X'40'              DESCRIPTION LINES              S22024
MSG      EQU   X'20'              MSG LINES                      S22024
RESULTS  EQU   X'10'              RESULTS                        S22024
F2       DC    F'2'               VALUE OF 2                     S22024
F15      DC    F'15'              VALUE OF 15                    S22024
DPXPTD   DC    C'XPTD'            CHAR CONSTANT                  S22024
DPCSW1   DC    C'CSW1'            CHAR CONSTANT                  S22024
DPCSW2   DC    C'CSW2'            CHAR CONSTANT                  S22024
DPRCVD   DC    C'RCVD'            CHAR CONSTANT                  S22024
DPXX     DC    C'XX XXXXXX XXXX XXXX' CHAR CONSTANT              S22024
         EJECT
***********************************************************************
*        SECTION PREFACE DUMMY SECTION                                *
***********************************************************************
         SPACE
SPREF1   DSECT
SECID    DS    CL8                     OLT ID                    S99528
LEVEL    DS    CL2                     LEVEL                     S99528
RTNDISP  DS    AL2                     DISP TO RTN #             S99528
SPREF2   DSECT
RTN#     DS    XL1                     RTN#                      S99528
         DS    XL8                     FLAGS                     S99528
RM       DS    XL2                     ROUTINE MASK              S99528
         DS    XL6                     PLINK MANAGEMENT          S99528
REFNUM   DS    XL1                     REFNUM                    S99528
         EJECT
RESPL    RESPL                                                 @Y17XAUU
         EJECT
         OLTCB                                                   S22024
EPSAVE   DS    C                  EP LEVEL CONTROL FIELD       @Y17XAUU
         SPACE 2
WRK      EQU   TOTWKSPC           WORK SPACE                     S22024
PB       EQU   TOTOTBUF           OUTPUT BUFFER                  S22024
         END
