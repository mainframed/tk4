         TITLE 'IEDQWJ2 TRM PROMPTER MODULE 3'
IEDQWJ2  CSECT
*A000000-999999                                                @Y16X5U0
* CHANGE ACTIVITY AS FOLLOWS:
*C353630                                                         S21903
*D532830-532900                                                 SA51078
*C532340-532410                                                 SA51078
*D417500,575300-575600                                           S21903
*A404000,533320                                                 SA41597
*C532690                                                        SA41597
*D534860-535140                                                 SA41597
*A211700,525550                                                 SA41607
*A250500                                                        SA41610
*C531430                                                        SA41611
*C000000-999999                                                  S22024
*A108300-108600,287190-287910,300000-300300,494140-494820        S05331
*A813700-814000,838850-839550                                    S05331
*C287100,696600,786600,806400                                    S05331
*D269100                                                         S05331
*A157100-157200,571600,571665-572200,572600-573000              SA61759
*A838300                                                         Y06330
*D839700                                                         Y06330
*C646600                                                       @SA70654
*A171900,429300,433800,435000,435300,441900,554400,636300      @Y17XAUU
*A638100,739800,742500,823800,838950,848700,909900,938700      @Y17XAUU
* A020000,454400                                               @G36XRUV
* C211200,455200                                               @G36XRUV
*THIS IS A DUMMY APAR TO CORRECT FICHE FLAGGING PROBLEM        @OZ19773
*
         USING IEDQWJ2,R15
*
IEDQWJ2  IEDHJN START,HJN
*
**************************************************************** S99528
*                                                              * S99528
* MODULE NAME = IEDQWJ2 (TCAM,TOTE)                            @Y17XAUU
*                                                              *
* DESCRIPTIVE NAME = TRM PROMPTER MODULE 3                     *
*                                                              *
* COPYRIGHT = NONE                                             *
*                                                              *
* STATUS --                                                    * S99528
*                                                              * S99528
*    VERSION 10.0                                              @Y17XAUU
*                                                              * S99528
* FUNCTION --                                                  * S99528
*    THE PURPOSE OF THIS MODULE IF TO PROMPT FOR THE ALTERNATE * S99528
*    PRINTER TERMINAL.                                         * S99528
*                                                              * S99528
*    THE CE IS PROMPTED FOR THE AP LOCATION. AP LOCATION       * S99528
*    CAN BE SYSOUT, SYSCON, OR ANY TCAM TERMINAL WITH A        * S99528
*    PRINTER. IF THE ENTRY IS NOT SYSCON OR SYSOUT IT IS       * S99528
*    VERIFIED TO BE A VALID TCAM TERMINAL.                     * S99528
*                                                              * S99528
*    IF NON-CONCURRENT MODE WAS SELECTED, THE SYSTEM OPERATOR  * S99528
*     IS ASKED FOR EXCLUSIVE USE OF THE NECESSARY LINES.       * S99528
*                                                              * S99528
* ENTRY POINT --                                               * S99528
*                                                              * S99528
*    IEDQWJ2                                                   @Y17XAUU
*                                                              @Y17XAUU
* PURPOSE -- SEE FUNCTION                                      @Y17XAUU
*                                                              @Y17XAUU
* LINKAGE -- XCTL'ED TO FROM IEDQWJ1                           @Y17XAUU
*                                                              @Y17XAUU
* MODULE TYPE -- PROCEDURE                                     @Y17XAUU
*                                                              @Y17XAUU
* MODULE SIZE -- 4K MAXIMUM                                    @Y17XAUU
*                                                              @Y17XAUU
* PROCESSOR -- ASSEMBLER XF                                    @Y17XAUU
*                                                              @Y17XAUU
* MACROS -- IEDHJN,AQCTL,LINK,BLDL,LOAD,XCTL,IEDQMSG,WTOR,     @Y17XAUU
*           STIMER,TTIMER,WAIT                                 @Y17XAUU
*                                                              * S99528
* INPUT --                                                     * S99528
*                                                              * S99528
*    REGISTERS 02,13,14,15 CONTAIN THE FOLLOWING VALUES:       * S99528
*                                                              * S99528
*         02 - OLTCB POINTER                                   * S99528
*         13 - SAVE AREA ADDRESS                               * S99528
*         14 - RETURN ADDRESS                                  * S99528
*         15 - ENTRY POINT ADDRESS                             * S99528
*                                                              * S99528
* OUTPUT --                                                    * S99528
*                                                              * S99528
*    NONE                                                      * S99528
*                                                              * S99528
* EXTERNAL REFERENCES --                                       * S99528
*                                                              * S99528
*    AVTUI - TCAM BINARY SEARCH                                * S99528
*    IEDQWK - MESSAGE MODULE                                   * S99528
*                                                              * S99528
* EXITS,NORMAL --                                              * S99528
*                                                              * S99528
*    IEDQWD - CONTINUE TRM PROCESSING                          * S99528
*                                                              * S99528
* EXITS,ERROR --                                               * S99528
*                                                              * S99528
*    IEDQWE - TERMINATE TRM REQUEST                            * S99528
*                                                              * S99528
* TABLES/WORK AREAS --                                         * S99528
*                                                              * S99528
*    OLTCB, CVT, UCB, AVT, TERNAME TABLE.                      * S99528
*                                                              * S99528
* ATTRIBUTES --                                                * S99528
*                                                              * S99528
*    ENABLED, PROBLEM PROGRAM MODE, TRANSIENT                  * S99528
*                                                              * S99528
* NOTES -- SEE BELOW                                           @Y17XAUU
*                                                              * S99528
*  DEPENDENCIES -- EBCDIC CODE CHARACTER SET                   @Y17XAUU
*                                                              @Y17XAUU
*  RESTRICTIONS -- NONE                                        @Y17XAUU
*                                                              @Y17XAUU
*  REGISTER CONVENTIONS -- SEE REGISTER ASSIGNMENT             @Y17XAUU
*                                                              @Y17XAUU
*  PATCH LABEL -- PATCH                                        @Y17XAUU
*                                                              * S99528
**************************************************************** S99528
         EJECT
**************************************************************** S99528
*                                                              * S99528
*        EQUATES                                               * S99528
*                                                              * S99528
**************************************************************** S99528
RNLINE   EQU   X'80'              RN LINE CHECK                  S22024
         SPACE
R0       EQU   0                  REGISTER 0                     S99528
R1       EQU   1                  REGISTER 1                     S99528
R2       EQU   2                  OLTCB ADDRESS                  S99528
R3       EQU   3                  REGISTER 3                     S99528
R4       EQU   4                  REGISTER 4                     S99528
R5       EQU   5                  REGISTER 5                     S99528
R6       EQU   6                  BASE REGISTER                  S99528
R7       EQU   7                  REGISTER 7                     S99528
R8       EQU   8                  REGISTER 8                     S99528
R9       EQU   9                  REGISTER 9                     S99528
R10      EQU   10                 REGISTER 10                    S99528
R11      EQU   11                 REGISTER 11                    S99528
R12      EQU   12                 REGISTER 12                    S99528
R13      EQU   13                 SAVE AREA ADDRESS              S99528
R14      EQU   14                 RETURN ADDRESS                 S99528
R15      EQU   15                 ENTRY POINT ADDRESS            S99528
PARMREG0 EQU   0                  MSG PARAMETER REGISTER 0       S99528
PARMREG1 EQU   1                  MSG PARAMETER REGISTER 1       S99528
DIS      EQU   8                  DISP OF 8                      S99528
*                                                                S99528
DEQFLG   EQU   X'01'              DEQ REQUEST FLAG               S05331
ENQFLG   EQU   X'83'              ENQ REQUEST FLAG               S05331
LGBA     EQU   X'80'              LGB MAP                        S22024
TYPE1CA  EQU   X'15'              UCB ID 3705 TYPE 1 CA          S22024
TYPE2CA  EQU   X'25'              UCB ID OF 3705 TYPE 2 CA       S22024
HOLD     EQU   X'88'              HOLD VERB CODE
HELDN    EQU   X'04'              HOLD MODE                     SA41607
X0F      EQU   X'0F'              POSITIVE MASK                  S22024
SUCES    EQU   X'FF'              REQUEST SUCCESSFUL             S22024
ACVT     EQU   16                 POINTER TO CUT ADDR            S22024
TRMLEN   EQU   72                 LENGTH OF TRM BUFFER           S22024
SLASH    EQU   X'61'              SLASH                          S22024
COMMA    EQU   X'6B'              COMMA                          S22024
TDMAX    EQU   11                 MAX+1 TEST DEVICE COUNT        S22024
RNTERM   EQU   X'E0'              TERMINAL ATTACHED TO RN (OFF)  S22024
YES      EQU   X'E8'              POSITIVE RESPONSE              S22024
NO       EQU   X'D5'              NEGATIVE RESPONSE              S22024
RESDP    EQU   X'20'              DEVICE PROTECTED               S22024
CAP      EQU   X'40'              CAPITALIZATION BIT             S22024
OPEN     EQU   X'10'              DCB OPEN FLAG                  S22024
LAST     EQU   X'80'              LAST ENTRY                     S22024
INITEST  EQU   128                INIT BIT TO TEST               S22024
XFF      EQU   X'FF'              MASK SETTING
X14      EQU   X'14'              DISP OF 20
X40      EQU   X'40'              A BLANK CHAR
RQINIT   EQU   X'0C'              START OF LIST                  S22024
RQEND    EQU   X'80'              END OF LIST                    S22024
D17      EQU   17                 DISP OF 17
XE0      EQU   X'E0'              QCB MASK SETTING
TWO      EQU   2                  DISP OF TWO
FOUR     EQU   4                  DISP OF FOUR
ACTVBCD  EQU   X'46'              OP CTL ACTIVATE CMMD
ACTRTCD  EQU   X'08'              INVITE LIST ALREADY ACTIVE RT CODE
BLANK    EQU   X'40'              A BLANK CHAR.
D0       EQU   0                  DISP OF 0
D1       EQU   1                  DISP OF 1
D2       EQU   2                  DISP OF 2
D3       EQU   3                  DISP OF 3
D4       EQU   4                  DISP OF 4
D5       EQU   5                  DISP OF 5
D6       EQU   6                  DISP OF 6
D7       EQU   7                  DISP OF 7
D8       EQU   8                  DISP OF 8
D9       EQU   9                  DISP OF 9
D10      EQU   10                 DISP OF 10
D11      EQU   11                 DISP OF 11
D12      EQU   12                 DISP OF 12
D13      EQU   13                 DISP OF 13
D14      EQU   14                 DISP OF 14
D15      EQU   15                  DISP OF 15
D16      EQU   16                 DISP OF 16
D19      EQU   25                 DISP OF 25                     S22024
DCBNIDLE EQU   X'02'              3705 ACTIVE                    S22024
D20      EQU   20                 DISP OF 20
D22      EQU   22                 DISP OF 22
D24      EQU   24                 DISP OF 24
D28      EQU   28                 DISP OF 28
D32      EQU   32                 DISP OF 32
D40      EQU   40                 DISP OF 40                     S22024
X60      EQU   X'60'              MASK FIELD                    SA61759
TSONLY   EQU   X'40'              TSO ONLY SYSTEM CHECK         SA61759
         EJECT
         SPACE 2
**************************************************************** S99528
*                                                              * S99528
*        PERFORM INITIALIZATION                                * S99528
*                                                              * S99528
**************************************************************** S99528
         SPACE 1
         SAVE  (14,12)            SAVE CALLER'S REGISTERS        S99528
         LR    R6,R15                   LOAD BASE
         DROP  R15
         USING IEDQWJ2,R6
         USING TOTOLTCB,R2                                       S22024
         LA    R3,TOTSAVE4        PICK UP SAVE AREA ADDRESS      S99528
         ST    R3,D8(R13)         STORE IN CALLER'S SAVE         S22024
         ST    R13,D4(R3)         SAVE CALLER'S                  S22024
         LR    R13,R3                   SET R13 TO SAVE AREA
         L     R10,TOTAVTPT       GET AVT ADDRESS              @Y17XAUU
         USING IEDQAVTD,R10
         L     R3,AVTRNMPT             PICK UP ADDR OF TERMNAME TABLE
         ST    R3,TERNMTAB             SAVE IT
         MVC   PAREMLST(D1),AVTBSX SET UP FOR BINARY SEARCH      S22024
         L     R3,AVTMSGS-D1      VCON TABLE ADDR                S22024
         L     R3,D8(R3)          ADDR OF A1 ROUTINE             S22024
         ST    R3,AVTUISV         SAVE ADDRESS OF A1             S99528
         DROP  R10                                             @Y17XAUU
         LA    R3,TOTTRMBF        POINT TO TRM BUFFER            S99528
         ST    R3,TRMSAVE         SAVE ADDRESS OF TRM BUFFER     S99528
         MVC   TRMWRK(TRMLEN),D0(R3) TRM TO WORK AREA            S22024
         OI    TOTFLG09,TOTPRECT  SET AP = CT FLAG              SA41610
         LA    R3,D3              SET COUNTER                    S22024
         LA    R4,TRMWRK               SET POINTER
FLDHUNT  EQU   *
         CLI   D0(R4),SLASH       END OF FIELD                   S22024
         BE    FLDFIND                 YES BRANCH
         LA    R4,D1(R4)          BUMP POINTER                   S22024
         B     FLDHUNT                 NO GO LOOK AGAIN
FLDFIND  EQU   *
         LA    R4,D1(R4)          BUMP PAST SLASH                S22024
         BCT   R3,FLDHUNT         GO EXAMINE NEXT CHARACTER      S99528
*
*        R4 ADDRESS OF OPTION FIELD
*
APHUNT   EQU   *
         TM    $ERROPT,$ALTPRNT   AP OPTION SET                  S22024
         BZ    EXIT               NO EXIT                        S22024
         CLC   D0(D2,R4),AP       AP OPTION                      S22024
         BE    APFIND                  YES BRANCH
         CLI   D0(R4),SLASH       END OF FIELD                   S22024
         BE    EXIT                      YES
         LA    R4,D1(R4)          NO - BUMP POINTER              S22024
         B     APHUNT                  GO LOOK AGAIN
APFIND   EQU   *
         LR    R3,R4                   SAVE POINTER
         MVC   SAVE(D20),D2(R4)   MOVE REST OF TRM TO SAVE       S22024
APFIND1  EQU   *
         OC    TOTAPUCB(D2),TOTAPUCB INVALID AP                  S22024
         BZ    PMT204                      NO
         SR    R8,R8              CLEAR REGISTER 8               S99528
         ICM   R8,3,TOTAPUCB      GET UCB ADDRESS FOR AP       @G36XRUV
         USING UCB,R8
         MVI   RNAME,X40          BLANK NAME                     S99528
         MVC   RNAME+D1(D7),RNAME FIELD                          S99528
         MVC   RNAME(D3),UCBNAME  MOVE RESOURCE NAME TO PLIST    S22024
         DROP  R8
         DEQ   ,MF=(E,DEQLIST)    ISSUE DEQ
PMT204   EQU   *
         NI    TOTAPFLG,XFF-TOTAPORN CLR AP ON 3705 FLAG         S22024
         IEDQMSG MSGID=056,FUNCT=CEC,LINK=YES  ALT PRINTER LOCATION
         SPACE
         IEDQMSG MSGID=055,FUNCT=REP,INBUF=CEIN,INCNT=10  VALID AP
APFIND2  EQU   *                                                 S22024
         CLC   CEIN(D6),SYSOUTA   SYSOUT                         S22024
         BE    SYSOUT                  YES BRANCH
         CLC   CEIN(D6),SYSCONA   SYSTEM CONSOLE                 S22024
         BE    SYSCON             YES, BRANCH                    S99528
*
*        FIND END OF NAME
*
         LA    R5,9                    SET UP COMPARAND
         LA    R4,CEIN                 SET UP POINTER
         LA    R10,0                    SET UP COUNTER
ENDHUNT  EQU   *
         CLI   D0(R4),BLANK       END OF ENTRY                   S22024
         BE    ENDFIND                 YES BRANCH
         LA    R10,D1(R10)        NO - BUMP POINTER              S22024
         CR    R5,R10                   GT 8?
         BE    ERR1                    YES BRANCH
         LA    R4,D1(R4)          BUMP POINTER                   S22024
         B     ENDHUNT            FIND END OF AP NAME            S99528
ENDFIND  EQU   *
         LTR   R10,R10            WAS A NAME ENTERED?
         BZ    ERR2                 NO
         STC   R10,LENGTH               SET UP BINARY SEARCH
         BCTR  R10,R0             DECREMENT FOR MOVE             S22024
         MVI   WORK1,X40          BLANK NAME                     S99528
         MVC   WORK1+D1(D7),WORK1 FIELD                          S99528
         EX    R10,NAMOVE         GO MOVE NAME INTO WORK AREA    S99528
         MVC   TOTAPNAM(D8),WORK1 MOVE AP NAME TO OLTCB          S22024
         BAL   R9,NAMESRCH        VERIFY AP NAME                 S99528
         B     NAMEOK                  BRANCH AROUND ERROR RETURN
         B      ERR2              ERROR EXIT                     S99528
NAMOVE   MVC   WORK1(D0),CEIN     AP NAME TO WORK AREA           S22024
NAMEOK   EQU   *
         BAL   R9,DEVCHK          VERIFY AP TERMINAL             S22024
         MVC   TOTARTNT(D4),TRMTNT SAVE REAL AP TNT ADDR
         BAL   R9,UCBFIND              GET ALTERNATE PRINTER UCB ADR
         STH   R8,TOTAPUCB        SAVE IN OLTCB
         BAL   R9,DUMMYTNT        REQUEST DUMMY ENTRIES          S22024
         BAL   R9,LCBSCAN         FIND AP LCB ADDR
         ST    R11,TOTAPLCB-1     PUT LCB ADDR IN OLTCB
         MVC   TOTAPRLN(D1),TRMRLN AP RLN                        S22024
         USING UCB,R8
         MVI   RNAME,X40          BLANK NAME                     S99528
         MVC   RNAME+D1(D7),RNAME FIELD                          S99528
         MVC   RNAME(3),UCBNAME        PUT RESOURCE NAME IN PARM LIST
         ENQ   ,MF=(E,ENQLIST)         REQUEST USE OF RESOURCE
         LTR   R15,R15                 IS RESOURCE ASSIGNED?
         BZ    PMT2404                   YES
         CLI   D3(R15),D8         RESOURCE ALREADY ASSIGNED      S22024
         BE    PMT2404                   YES
PMT2400  EQU   *
         IEDQMSG MSGID=054,FUNCT=CEC  TERMINAL IN USE MESSAGE    S99528
         BAL   R7,CONT                 CONTINUE PROMPTING?
         B     PMT2704                   YES
PMT2404  EQU   *
*
*        ENQ AP SYMBOLIC NAME
*
         MVC   RNAME(D8),CEIN     AP NAME TO ENQUEUE LIST        S99528
         ENQ   ,MF=(E,ENQLIST)    REQUEST USE OF RESULT
*
         LTR   R15,R15            REQUEST ENQ'ED ?
         BZ    PMT2404            YES
         CLI   D3(R15),D8         ALREADY ENQ'ED ?
         BE    PMT2080            YES                            S05331
*        DEQ AP UCB NAME                                         S05331
         MVI   ENQLIST+D2,DEQFLG  SET DEQ FLAG                   S05331
         MVI   RNAME,X40          BLANK NAME                     S05331
         MVC   RNAME+D1(D7),RNAME FIELD                          S05331
         DEQ   ,MF=(E,ENQLIST)    DEQ AP UCB NAME                S05331
         MVI   ENQLIST+D2,ENQFLG  RESTORE ENQ FLAG               S05331
         B     PMT2400            ERROR EXIT                     S05331
         DROP  R8                                                S05331
PMT2080  DS    0H                 BOUNDARY ALLIGNMENT            S05331
         BAL   R9,RNLINET         CHECK FOR RN TERM FOR L ENQ    S22024
*
         OI    TOTFLG09,TOTPRECT  ASSUME AP EQU CT               S22024
         SR    R5,R5              CLEAR REG 5
         IC    R5,LENGTH          GET LENGTH OF AP NAME
         BCTR  R5,R0              DECREMENT FOR COMPARE
         EX    R5,CTEQUAP         CT = AP
         BE    PMT2086                   YES
         NI    TOTFLG09,XFF-TOTPRECT CORRECT OPTION              S22024
         OI    TOTFLG09,TOTAPTRM  AP EQU TERMINAL FLAG           S22024
         BAL   R9,DEVPRT          CHECK FOR DEVICE PROTECT       S22024
PMT2086  EQU   *
         EX    R10,MOVE                 MOVE TERM TO TRM
DMOVE    EQU   *
         LA    R10,D1(R10)        RESTORE COUNT                  S22024
         LA    R3,2(R10,R3)             POINT TO END OF TERM NAME
         MVC   D0(D20,R3),SAVE    RESTORE TRM                    S22024
         L     R3,TRMSAVE             L PICK UP BUFFER IN OLTCB
         MVC   D0(TRMLEN,R3),TRMWRK TRM TO OLTCB                 S22024
EXIT     EQU   *
         TM    TOTFLG06,TOTNCMFG  NCM REQUESTED                  S22024
         BZ    PMT259                    NO
         BAL   R9,RNCHK           DETERMINE IF TD ON 3705        S22024
         LA    R7,NCMMSG2         GET START OF DEVICE FLD OF MSG S22024
         LA    R9,TDMAX                                          S22024
         LA    R8,TOTTDTBL        GET START OF TEST DEVICE TABLE
         TM    TOTTDTBL,TOTTDTRM  TERMINAL TEST
         BO    TRMTNCM            YES - BRANCH
         MVC   WORK3(D4),D0(R8)   UCB ADDR                       S22024
         MVI   WORK3,D0           CLEAR FLAG BYTE
         B     PMT2506A           CHECK NEXT ENTRY               S99528
PMT2506  EQU   *
         MVC   WORK3(D4),D0(R8)   SAVE ENTRY
         CLC   D0(D4,R8),K0       ANY MORE ENTRIES               S22024
         BE    PMT2508                   NO
         MVI   D0(R7),COMMA       MOVE COMMA TO MSG              S22024
         LA    R7,D1(R7)          POINT PASS COMMA               S22024
         TM    TOTFLG10,TOTTERMS  TERMINAL TEST ?
         BO    TRMTNCM            YES - BRANCH
PMT2506A EQU   *
         BCT   R9,PMT2507              END OF TABLE?
         B     PMT2508                   YES
PMT2507  EQU   *
         L     R5,WORK3           GET UCB ADDR
         MVC   D0(D3,R7),D13(R5)  MOVE NEXT LINE ADDR TO MSG     S22024
         USING UCB,R5                                            S22024
         TM    TOTFLG10,TOTTERMS   IS THIS A TERMINAL TEST       S22024
         BO    TRM25072            YES BYPASS THIS MIX TEST      S22024
*                                  NO TEST FOR MIXED 270X / 3705 S22024
         CLI   UCBTBYT4,TYPE1CA    3704/5 TYPE 1 CA              S22024
         BE    TRM25071            YES SET PROPER BIT            S22024
         CLI   UCBTBYT4,TYPE2CA    3704/5 TYPE 2 CA              S22024
         BE    TRM25071            YES SET PROPER BIT            S22024
         OI    TOTFLG02,TOTDEV0X   CHANNEL TEST ON 2701,2,3      S22024
         B     TRM25072            CONTINUE CHECKING TCU'S       S22024
TRM25071 DS    0H                  ALIGN ON HALFWORD             S22024
         OI    TOTFLG02,TOTDEVRN   CHANNEL TEST ON 3704,5        S22024
         DROP  R5                                                S22024
TRM25072 DS    0H                 ALIGN ON HALFWORD              S22024
         LA    R8,D4(R8)          BUMP TO NEXT TEST DEVICE       S22024
         LA    R7,D3(R7)          BUMP TO NEXT ENTRY OF MSG      S22024
         B     PMT2506            GET NEXT ENTRY
PMT2508  EQU   *
         TM    TOTFLG02,TOTDEV0X+TOTDEVRN ARE BOTH TCU BITS ON   S22024
         BO    TRM25042                    YES PRINT ERROR       S22024
         TM    TOTFLG02,TOTDEVRN   IS TEST FOR 3704/3705         S22024
         BO    TRM25043            YES ASK FOR PERMISSION        S22024
*                                  NO  ASK TO USE 270X LINES     S22024
         WTO   'IED224D CAN OLT HAVE EXCLUSIVE USE OF CONTROL UNIT ON A*
               DDRESS(ES) ',ROUTCDE=10,DESC=7                    S22024
TRM25041 DS    0H                  ALIGN ON HALFWORD             S22024
         XC    ECB(D4),ECB        CLEAR ECB FOR WTOR             S22024
WTORNCM  WTOR  '                                                     ',*
               ANSWER,3,ECB,ROUTCDE=(8,10),DESC=2                S22024
NCMMSG2  EQU   WTORNCM+D16        LINE TO MESSAGE                S22024
         STIMER REAL,NOANS,DINTVL=TIMOUT    SET TIMER FOR TIMEOUT
         WAIT  ECB=ECB                 WAIT FOR REPLY
         TTIMER CANCEL                 CANCEL TIMER
         OI    ANSWER,BLANK       XLATE TO CAPS                  S22024
         CLI   ANSWER,YES         YES                            S22024
         BE    PMT259                    YES
         IEDQMSG MSGID=051,FUNCT=CEC    ALLOW ONLY CONCURRENT MODE
         SPACE
         B     GETOUT             GO EXIT TO IEDQWE FOR TERMINATION
TRM25042 DS    0H                  ALIGN ON HALFWORD             S22024
         IEDQMSG MSGID=001,FUNCT=CEC   INVALID MIX OF TCU TYPES  S22024
         B     GETOUT              CLEAN UP AND RETURN           S22024
TRM25043 DS    0H                  ALIGN ON HALFWORD
         WTO   'IED235D CAN OLT HAVE EXCLUSIVE USE OF 3705 ON ADDRESS(E*
               S) ',ROUTCDE=10,DESC=7                            S22024
         B     TRM25041            PRINT REST OF MESSAGE         S22024
PMT259   EQU   *
         L     R13,D4(R13)        CALLER'S SAVE                  S22024
         TM    TOTFLG02,TOTCONMD  TD ON CONC                    SA41597
         BZ    PMT260             NO                            SA41597
         LM    R14,R1,D12(R13)    CALLER'S REG                  SA41597
         XCTL  (2,12),EP=IEDQWEC  YES - EXIT TO IEDQWC          SA41597
         SPACE
PMT260   EQU   *                                                SA41597
         L     R14,D12(R13)       RESTORE REG 14                 S22024
         XCTL  (2,12),EP=IEDQWD   XFER CONTROL TO DISPATCHER     S22024
SYSOUT   EQU   *
         MVC   D2(D6,R3),SYSOUTA  SYSOUT TO TRM                  S22024
         NI    TOTFLG09,XFF-TOTPRECT CLEAR PRINTER = CT FLAG     S22024
         OI    TOTFLG09,TOTAPOUT  SET PRINTER = SYSOUT FLAG      S22024
         LA    R10,5              PUT 5 INTO REGISTER 10
         B     DMOVE              MOVE TRM TO OLTCB              S99528
SYSCON   EQU   *
         MVC   D2(D6,R3),SYSCONA  SYSCON TO TRM                  S22024
         OI    TOTFLG09,TOTAPCON  ALT PRINTER EQU SYSCON       @YM08408
         OI    TOTFLG09,TOTPRECT  ASSUME PRINTER EQU CT          S22024
         TM    TOTFLG09,TOTCTCON  CT EQU SYSCON                  S22024
         BO    SYSCON2            YES, BRANCH                    S99528
         NI    TOTFLG09,XFF-TOTPRECT CLEAR PRT EQU CT FLAG       S22024
SYSCON2  EQU   *
         LA    R10,D5             EXECUTE COUNT                  S22024
         B     DMOVE              MOVE TRM TO OLTCB
MOVE     MVC   D2(D0,R3),CEIN     MOVE AP NAME                   S22024
ERR2     EQU   *
         IEDQMSG MSGID=046,FUNCT=CEC  INVALID AP                 S99528
         BAL   R7,CONT                 GO SEE IF TO CONTINUE
         B     APFIND1                 YES GO ASK AGAIN
ERR1     EQU   ERR2               INVALID AP                     S22024
PMT2704  EQU   *
         IEDQMSG MSGID=048,FUNCT=REP,INBUF=CEIN,INCNT=10 ENTER OPTION
         CLC   CEIN(D3),NAP       NAP OPTION                     S22024
         BE    NEWNAP                  YES BRANCH
         B     APFIND2            GO FIND AP OPTION
NEWNAP   EQU   *
         MVC   D0(D3,R3),NAP      MOVE NAP TO TRM                S22024
         MVC   D3(D20,R3),SAVE    RESTORE TRM                    S22024
         MVC   TOTTRMBF(TRMLEN),TRMWRK TRM TO OLTCB              S22024
         B     EXIT               CONTINUE PROCESSING            S99528
CONT     EQU   *
         IEDQMSG MSGID=057,FUNCT=REP,INBUF=CEIN,INCNT=3  CONTINUE?
         SPACE
         CLI   CEIN,YES           YES                            S22024
         BCR   8,R7               YES
         CLI   CEIN,NO            NO                             S22024
         BE    GETOUT                  YES LEAVE
         IEDQMSG MSGID=079,FUNCT=CEC  INVALID RESPONSE           S99528
         SPACE
         B     CONT               GO SEE IF CE WANTS TO CONTINUE?
         EJECT
**************************************************************** S22024
*        ROUTINE TO FIND THE SYMBOLIC NAME OF THE RN LINE      * S22024
*        FOR THE TERMINAL ATTACHED TO THE RN.                  * S22024
**************************************************************** S22024
RNLINET  EQU   *                                                 S22024
         STM   R7,R11,TRMSAVE2    SAVE CALLER'S REG              S22024
         L     R11,TNTENTRY       TTE ADDR                       S22024
         USING IEDNTRM,R11                                       S22024
         LA    R7,TRMPRFSZ        GET TTE PREFIX SIZE          @Y17XAUU
         SLR   R11,R7             POINT TO TTE PREFIX          @Y17XAUU
         TM    TRMSTATE,TRMPREF   3705 RESOURCE                @Y17XAUU
         BZ    RNLINEND           NO                             S22024
         TM    TRMSTATE,XE0       RN TERMINAL                    S22024
         BNZ   RNLINEND           NO                             S22024
         LH    R7,TRMTTCIN        GET TERMINAL TTCIN           @Y17XAUU
         STH   R7,TOTAROFF        AP TTCIN                     @Y17XAUU
         OI    TOTAPFLG,TOTAPORN  AP ON 3705                     S22024
         L     R4,TOTAVTPT        GET AVT ADDRESS              @Y17XAUU
         USING IEDQAVTD,R4        SET AVT ADDRESSABILITY       @Y17XAUU
         L     R4,AVTSAVTP        GET SAVT ADDRESS             @Y17XAUU
         DROP  R4                                              @Y17XAUU
         USING IEDNSVTD,R4        SET SAVT ADDRESSABILITY      @Y17XAUU
         L     R15,SAVTTNTX       TTCIN TO NETWORK ADDR RTN    @Y17XAUU
         DROP  R4                                              @Y17XAUU
         LH    R1,TRMTTCIN        GET TERMINAL TTCIN           @Y17XAUU
         BALR  R14,R15            CALL CONVERSION ROUTINE      @Y17XAUU
         STH   R15,TOTARSID       SAVE NETWORK ADDRESS         @YM07278
         L     R10,TRMQCB         GET QCB ADDR                   S22024
         USING IEDQQCB,R10                                       S22024
         L     R10,QCBLGBAD-D1    LGB ADDR                       S22024
         LA    R10,D0(R10)        CLEAR HIGH ORDER BYTE          S22024
         DROP  R10                                               S22024
         USING IEDNLGB,R10                                       S22024
         MVC   TOTAPRNL(D8),LGBNAME SAVE LINE GROUP NAME         S22024
         DROP  R10                                             @Y17XAUU
CHKTTE   DS    0H                 FIND LINE TNT ENTRY          @Y17XAUU
         LH    R1,TRMCOHRT        GET TTCIN OF NEXT ENTRY      @Y17XAUU
         LR    R9,R1              SAVE TTCIN                   @Y17XAUU
         L     R15,TERNMTAB       TTCIN TO TTE CONVERSION      @Y17XAUU
*                                 ROUTINE ADDRESS              @Y17XAUU
         BALR  R14,R15            FIND TTE ADDRESS             @Y17XAUU
         LR    R11,R1             GET TTE ADDRESS              @Y17XAUU
         LA    R1,TRMPRFSZ        GET TTE PREFIX SIZE          @Y17XAUU
         SLR   R11,R1             POINT TO TTE PREFIX          @Y17XAUU
         CLI   TRMTYPE,TRMNSDLC   PRE SNA LINE ENTRY           @Y17XAUU
         BE    LINETTE            YES                          @Y17XAUU
         CLI   TRMTYPE,TRMSDLC    SNA LINE ENTRY               @Y17XAUU
         BNE   CHKTTE             NO - CHECK NEXT ENTRY        @Y17XAUU
LINETTE  DS    0H                 FIND LINE SYMBOLIC NAME      @Y17XAUU
         BCTR  R9,R0              DECREMENT FOR MULTIPLY       @Y17XAUU
         L     R1,TERNMTAB        SET TNT BASE                 @Y17XAUU
         USING IEDQTNTD,R1        SET TNT ADDRESSABILITY       @Y17XAUU
         SLR   R8,R8              CLEAR REG                    @Y17XAUU
         IC    R8,TNTENLEN        LENGTH OF NAME               @Y17XAUU
         LA    R8,D3(R8)          TOTAL LEN OF A TNT ENTRY     @Y17XAUU
         MR    R8,R8              OFFSET TO LINE TNT ENTRY     @Y17XAUU
         LA    R10,TNTFIRST(R9)   LINE TNT ADDRESS             @Y17XAUU
         DROP  R1                                              @Y17XAUU
         DROP  R11                                             @Y17XAUU
         SPACE
*                                                                S22024
*        ENQUEUE THE RN LINE SHARED                              S22024
*                                                                S22024
RN0C     EQU   *
         SR    R9,R9              CLEAR REG                      S22024
         IC    R9,TOTTTBEL        MAX NAME LENGTH                S22024
         BCTR  R9,R0              DECREMENT FOR EXECUTE          S22024
         EX    R9,RNENQ           MOVE RN LINE NAME TO ENQ LIST  S22024
         SPACE
         ENQ   ,MF=(E,ENQLIST)    ENQ RN LINE                    S22024
         LTR   R15,R15            WAS IT ASSIGNED                S05331
         BZ    RNLINEND           YES                            S05331
         CLI   D3(R15),D8         WAS IT PREVIOUSLY ASSIGNED     S05331
         BE    RNLINEND           YES                            S05331
*        DEQ AP TERMINAL NAME AND UCB NAME                       S05331
         SR    R8,R8              CLEAR                        @G36XRUV
         ICM   R8,3,TOTAPUCB      AP UCB ADDR                  @G36XRUV
         LA    R7,TOTAPNAM        ADDR OF AP NAME                S05331
         USING UCB,R8                                            S05331
         MVI   ENQLIST+D2,DEQFLG  SET DEQ FLAG                   S05331
         MVC   RNAME(D8),D0(R7)   GET TERMINAL NAME              S05331
         DEQ   ,MF=(E,ENQLIST)    DEQ IT                         S05331
         MVI   RNAME,X40          BLANK NAME                     S05331
         MVC   RNAME+D1(D7),RNAME FIELD                          S05331
         MVC   RNAME(D3),UCBNAME  GET UCB NAME                   S05331
         DEQ   ,MF=(E,ENQLIST)    DEQ IT                         S05331
         MVI   ENQLIST+D2,ENQFLG  RESORE ENQ FLAG                S05331
         B     PMT2400            ERROR EXIT                     S05331
         DROP  R8                                                S05331
         SPACE
RNLINEND EQU   *                                                 S22024
         LM    R7,R11,TRMSAVE2    RESTORE REG                    S22024
         BR    R9                 RETURN                         S22024
RNENQ    MVC   RNAME(D0),D0(R10)  MOVE RN LINE NAME TO ENQ LIST
         EJECT
GETOUT   EQU   *
         OI    TOTFLG06,TOTOTERM  TERMINATE FLAG                 S22024
         L     R13,D4(R13)        CALLER'S SAVE                  S22024
         L     R14,D12(R13)       RETURN ADDR                    S22024
         XCTL  (2,12),EP=IEDQWE        GO TO IEDQWE
         EJECT
NOANS    EQU   *
         STM   R14,R12,D12(R13)   SAVE CALLER'S REG              S22024
         LR    R4,R15                  GET MY BASE
         USING NOANS,R4
         LA    R12,TOTSAVE1       MY SAVE AREA                   S22024
         ST    R13,D4(R12)        SAVE HIS SAVE ADDR             S22024
         ST    R12,D8(R13)        SAVE MY SAVE ADDR              S22024
         LR    R13,R12                 SET SAVE POINTER
         POST  ECB                     POST WTOR ECB
         L     R13,D4(R13)        CALLER'S SAVE                  S22024
         LM    R14,R12,D12(R13)   RESTORE REGISTERS              S22024
         SR    R15,R15            CLEAR RETURN CODE REG          S22024
         BR    R14                     RETURN TO CALMLER
         DROP  R4
         SPACE 2
**************************************************************** S22024
*        ROUTINE TO DETERMINE IF TD IS ON A 3705. IF SO ,      * S22024
*        THE NCM OPTION IS TURNOFF.                            * S22024
**************************************************************** S22024
RNCHK    EQU   *                                                 S22024
         STM   R7,R9,TRMSAVE1     SAVE REGISTERS                 S22024
         LA    R7,TOTTDTBL        POINT TO TEST DEVICE TABLE     S22024
         LA    R8,TOTTDEND        END OF TABLE                   S22024
RNCHK2   EQU   *                                                 S22024
         CR    R7,R8              END OF TABLE                   S22024
         BE    RNCHKE             YES - EXIT (NO 3705 TD)        S22024
         L     R9,D0(R7)          GET TD ENTRY                   S22024
         LTR   R9,R9              ANY MORE ENTRIES               S22024
         BZ    RNCHKE             NO - EXIT (NO 3705 TD)         S22024
         SPACE
         TM    D0(R7),TOTTDORN    TD ON 3705                     S22024
         BO    RNCHKNCM           YES                            S22024
         LA    R7,D4(R7)          POINT TO NEXT ENTRY            S22024
         B     RNCHK2             CHECK IT                       S22024
         SPACE
RNCHKE   EQU   *                                                 S22024
         LM    R7,R9,TRMSAVE1     RESTORE REGISTERS
         BR    R9                 RETURN                         S22024
         SPACE
RNCHKNCM EQU   *                                                 S22024
         NI    TOTFLG06,XFF-TOTNCMFG SET CM                      S22024
         IEDQMSG MSGID=008,FUNCT=CEC NCM REJECTED                S22024
         B     PMT259             EXIT                           S22024
         EJECT
*              ROUTINE TO SEARCH TERMNAME TABLE. R3 HAS ADDR OF
*              START OF TABLE. WORK1 HAS NAME IN IT. R2 HAS LENGTH
*
NAMESRCH EQU   *
         LA    R1,PAREMLST              SET R1
         L     R15,AVTUISV        GET ADDRESS OF A1
         LH    R0,TWO(R1)         LOAD CHAR STRING LEN
         L     R1,FOUR(R1)        LOAD ADDR OF CHARS
         BAL   R14,FOUR(R15)      BRANCH TO SCAN ROUTINE
         LTR   R15,R15
         BZ    D4(R9)             NAME NOT FOUND                 S22024
         STH   R15,TRMTTCIN       SAVE TTCIN                   @Y17XAUU
         SPACE
         STM   R7,R9,TRMSAV1      SAVE REG 7-9
         L     R8,TERNMTAB        GET START OF TABLE
         USING IEDQTNTD,R8
         LA    R7,TNTFIRST        POINT TO FIRST ENTRY IN TABLE
         SR    R9,R9              CLEAR REG 9
         IC    R9,TNTENLEN        GET MAX. LENGTH OF NAME FIELD
         LA    R9,D3(R9)          LENGTH OF ONE ENTRY            S22024
         DROP  R8
         BCTR  R15,R0             DECREMENT ENTRY REL POSITION   S22024
         MR    R8,R15             GET DISPLACEMENT TO ENTRY DESIRED
         AR    R7,R9              GET DESIRED TNT ADDRESS
         ST    R7,TRMTNT          SAVE IT
         LA    R1,D1(R15)         RESTORE ENTRY REL POSITION     S22024
         STH   R15,TOTAROFF       SAVE AP REAL TTCIN           @Y17XAUU
         LM    R7,R9,TRMSAV1      RESTORE REG 7-9
         SPACE
         L     R15,TERNMTAB       GET ADDRESS OF TERMNAME TABLE
         BALR  R14,R15            GO SEARCH TABLE
         ST    R1,TNTENTRY        SAVE TERMINAL ENTRY ADDRESS
         USING IEDQTRM,R1                                       SA61759
         TM    TRMDSORG,TRMLGB    LGB RATHER THAN A TTE          S22024
         BO    D4(R9)             YES - ERROR - NOT A TERMINAL   S22024
         L     R14,TOTAVTPT       GET AVT ADDRESS               SA61759
         USING IEDQAVTD,R14                                     SA61759
         MVC   WRKFLG(D1),AVTBIT1 GET ENVIRONMENTAL FLAG        SA61759
         NI    WRKFLG,X60         CLEAR UNWANTED BITS           SA61759
         CLI   WRKFLG,TSONLY      TSO ONLY SYSTEM               SA61759
         BE    TRMTSO             YES - TERMINATE TRM           SA61759
         DROP  R14                                              SA61759
         L     R1,TRMDESTQ-D1     DEST. QCB ADDR                SA61759
         DROP  R1                                               SA61759
         USING IEDQQCB,R1                                       SA61759
         TM    QCBFLAG,QCBTSSES   TSO TERMINAL                  SA61759
         BO    TRMTSO             YES - TERMINATE TRM           SA61759
         TM    QCBDSFLG,QCBREUS+QCBNREUS+QCBCORE  ARE THERE     SA61759
*                                 ANY QUEUES USED               SA61759
         BZ    TRMTSO             NO - TERMINATE                SA61759
         DROP  R1                                               SA61759
         BR    R9                 RETURN TO CALLER
TRMTSO   DS    0H                 BOUNDARY ALLIGNMENT           SA61759
         IEDQMSG MSGID=011,FUNCT=CEC                            SA61759
         B     GETOUT             EXIT TO CLEANUP               SA61759
         EJECT
TRMTNCM  EQU   *   R8 POINTS TO TEST ENTRY
         OI    TOTFLG10,TOTTERMS  INSURE TER. FLAG SET           S22024
         STM   R7,R12,TRMSAVE1    SAVE WORK REG
         L     R9,TRMNCMCT        GET  ENTRY COUNTER
         LA    R9,D1(R9)          INCREMENT BY ONE
         CL    R9,TRMTEN          END OF TABLE ?
         BH    TRMNCM1            YES
         ST    R9,TRMNCMCT       SAVE COUNTER
         L     R12,D0(R8)         GET TNT ADDR
         LA    R12,D0(R12)         CLEAR HIGH ORDER BYTE
         LTR   R12,R12            LAST ENTRY ?
         BZ    TRMNCM1            YES - BRANCH
         SR    R10,R10            CLEAR REG
         IC    R10,TOTTTBEL       GET NAME LENGTH
         AR    R12,R10            GET ADDR OF TTE ADDR
         MVC   TTEADDR+D1(D3),D0(R12)   ALLIGN ADDRESS
         L     R11,TTEADDR        GET TTE ADDR
         USING IEDQTRM,R11
         L     R11,TRMDESTQ-D1    GET QCB ADDR
         DROP  R11
         USING IEDQQCB,R11
         SR    R7,R7              CLEAR REG
         IC    R7,QCBRELLN        GET RLN
         SLL   R7,2               MULT BY 4
         L     R11,QCBDCBAD-D1    GET DCB ADDR
         DROP  R11
         USING IHADCB,R11
         L     R11,DCBDEBAD       GET DEB ADDR
         DROP  R11
         USING DEBNMSUB,R11
         SR    R15,R15            CLEAR REG
         IC    R15,DEBNMEXT       GET # OF EXTENTS
         SLL   R15,D2             MULT BY 4
         L     R11,DEBUCBAD-D4(R7)  GET UCB ADDR
         ST    R11,WORK3          SAVE IT
         LM    R7,R12,TRMSAVE1    RESTORE CALLER'S REG
         B     PMT2507            RETURN
TRMNCM1  EQU   *
         BCTR  R7,R0              DECREMENT R7 BY 1
         MVI   D0(R7),BLANK       BLANK OUT FIRST BYTE
         LM    R7,R12,TRMSAVE1    RESTORE CALLER'S REG
         B     PMT2508            RETURN
         DROP  R11                                               S22024
         EJECT
**************************************************************** S22024
*                                                              * S22024
*        ROUTINE TO VERIFY THAT THE SYMBOLIC NAME ENTERED      * S22024
*        AS THE ALTERNATE PRINTER IS A DEVICE AS OPPOSED TO    * S22024
*        A SUB CHANNEL HUNG OFF THE RN.                        * S22024
*                                                              * S22024
**************************************************************** S22024
DEVCHK   EQU   *                                                 S22024
         STM   R7,R11,TRMSAVE2    SAVE REGISTERS                 S22024
         L     R11,TNTENTRY       TTE ADDR                       S22024
         USING IEDNTRM,R11                                       S22024
         LA    R7,TRMPRFSZ        GET TTE PREFIX SIZE          @Y17XAUU
         SLR   R11,R7             POINT TO TTE PREFIX          @Y17XAUU
         TM    TRMSTATE,TRMPROC   VERIFY NOT PROCESS ENTRY     @Y17XAUU
         BO    DEVCHK8            PROCESS ENTRY - EXIT         @Y17XAUU
         TM    TRMSTATE,TRMPREF   3705 RESOURCE                @Y17XAUU
         BZ    DEVCHKE            NO - RETURN                    S22024
         CLI   TRMTYPE,TRMPUNT    PU ENTRY                     @Y17XAUU
         BE    DEVCHK8            YES - ERROR                  @Y17XAUU
         CLI   TRMTYPE,TRMLNCP    NCP ENTRY                    @Y17XAUU
         BE    DEVCHK8            YES - ERROR                  @Y17XAUU
         CLI   TRMTYPE,TRMSSCP    SSCP ENTRY                   @Y17XAUU
         BE    DEVCHK8            YES - ERROR                  @Y17XAUU
         TM    TRMSTATE,RNTERM    TERMINAL ATTACHED TO RN        S22024
         BZ    DEVCHKE            YES                            S22024
*                                                              * S22024
*        AN INVALID ENTRY HAS BEEN MADE IN THE AP FIELD        * S22024
*                                                              * S22024
DEVCHK8  DS    0H                 BOUNDARY ALLIGNMENT            S22024
         LM    R7,R11,TRMSAVE2    CALLER'S REGISTERS             S22024
         B     ERR2               NOTIFY OF ERROR                S22024
         SPACE
DEVCHKE  EQU   *                                                 S22024
         TM    TRMSTATE,TRMPREF   3705 RESOURCE                @Y17XAUU
         BO    DEVCHKED           YES - BYPASS CHECK             S22024
         TM    TRMSTATE,XE0       TERMINAL ENTRY                 S22024
         BZ    DEVCHKED                 YES, VALID TERMINAL    @SA70654
         MVC   WRKFLG(D1),TRMSTATE      GET FLAG BYTE          @SA70654
         NI    WRKFLG,XE0               CLEAR UNWANTED FLAGS   @SA70654
         CLI   WRKFLG,RNLINE            270X DIAL TERMINAL     @SA70654
         BZ    DEVCHK8                  NO, ERROR              @SA70654
DEVCHKED DS    0H                 BOUNDARY ALLIGNMENT            S22024
         LM    R7,R11,TRMSAVE2    CALLER'S REGISTERS             S22024
         BR    R9                 RETURN TO CALLER               S22024
         DROP  R11
         EJECT
**************************************************************** S22024
*                                                              * S22024
*        ROUTINE TO LOAD CDS RECORD FOR ALTERNATE PRINTER TO   * S22024
*        DETERMINE IF THE DEVICE PROTECT FEATURE IS SET. IF    * S22024
*        EITHER THE RECORD IS NOT FOUND OR THE OPTION IS SET   * S22024
*        THE SYSTEM OPERATOR WILL BE REQUESTED TO GIVE         * S22024
*        AUTHORIZATION FOR USE OF THE TERMINAL.                * S22024
*                                                              * S22024
**************************************************************** S22024
DEVPRT   EQU   *                                                 S22024
         STM   R4,R5,WORK4        SAVE REGISTERS                 S22024
         ST    R7,WORK4+D8        SAVE REG                       S22024
         SPACE
         TM    TOTFLG09,TOTPRECT  AP EQU CT                      S22024
         BO    D0(R9)             YES - BYPASS CHECK             S22024
         TM    TOTFLG09,TOTAPCON+TOTAPOUT AP EQU SYSTEM DEVICE   S22024
         BNZ   D0(R9)             YES - BYPASS CHECK             S22024
         SPACE
         MVC   READNAME(D8),TOTAPNAM AP NAME TO BLDL PLIST       S22024
*                                                              * S22024
*        GET AND EVALUATE CDS RECORD.                          * S22024
*                                                              * S22024
         BLDL  0,BLDLLIST         BUILD DIR. ENTRY POINT LIST    S22024
         SPACE
         LTR   R15,R15            BLDL SUCCESSFULLY              S22024
         BZ    CDSLOAD            YES                            S22024
         CH    R15,H4             RECORD FOUND                   S22024
         BE    CDSDVP             NO                             S22024
*                                                              * S22024
*        ASSUME I/O ERROR                                      * S22024
*                                                              * S22024
         IEDQMSG MSGID=041,FUNCT=CEC                             S22024
         SPACE
CDSERR   EQU   *                                                 S22024
         LM    R4,R5,WORK4        CALLER'S REG                   S22024
         L     R7,WORK4+D8        CALLER'S REG                   S22024
         B     ERR2               REPORT ERROR                   S22024
         SPACE
CDSLOAD  EQU   *                                                 S22024
         LOAD  DE=READNAME        LOAD THE ENTRY                 S22024
         SPACE
         LR    R8,R0              ENTRY POINT ADDR               S22024
         MVC   DVTEST,D10(R8)     SAVE FLAG BYTE                 S22024
         DELETE DE=READNAME       DELETE THE ENTRY               S22024
         SPACE
         TM    DVTEST,RESDP       DEVICE PROTECTED               S22024
         BZ    CDSEND             NO - RETURN                    S22024
CDSDVP   EQU   *                                                 S22024
         MVC   DEVNAM(D8),READNAME AP NAME TO MSG                S22024
         MVC   MSG+D16(D8),READNAME AP NAME TO MSG               S22024
         SPACE
         WTO   'IED213D CAN ON LINE TEST USE PROTECTED DEVICE',        *
               ROUTCDE=10,DESC=7
         SPACE
         XC    ECB(D4),ECB        CLEAR ECB                      S22024
         SPACE
MSG      WTOR  '         (YES OR NO)',ANSWER,3,                        *
               ECB,ROUTCDE=(8,10),DESC=2
         SPACE
         STIMER REAL,NOANS,DINTVL=TIMOUT TIMER FOR TIMEOUT       S22024
         SPACE
         WAIT  ECB=ECB            WAIT FOR REPLY                 S22024
         SPACE
         TTIMER CANCEL            CANCEL TIMER                   S22024
         SPACE
         OI    ANSWER,CAP         XLATE TO CAP                   S22024
         CLI   ANSWER,YES         YES RESPONSE                   S22024
         BE    CDSEND             YES - RETURN                   S22024
         SPACE
         MVC   REJMSG+D8(REJECTE-REJECT),REJECT MSG TO PLIST     S22024
REJMSG   WTO   '                                                       *
                               ',ROUTCDE=10,DESC=7               S22024
         SPACE
         B     GETOUT             TERMINATE TRM                  S22024
         SPACE
CDSEND   EQU   *
         LM    R4,R5,WORK4        RESTORE REG                    S22024
         L     R7,WORK4+8         RESTORE REG                    S22024
         BR    R9                 RETURN                         S22024
         EJECT
*
*
*        ROUTINE TO FIND UCB ADDR AFTER ENTRY IN TERMINAL TABLE HAS
*        BEEN FOUND
*
*
UCBFIND  EQU   *
         L     R8,TNTENTRY        GET ADDRESS OF TERMINAL ENTRY  S99528
         USING IEDQTRM,R8
         L     R8,TRMDESTQ-D1     QCB ADDR                       S22024
         DROP  R8
         USING IEDQQCB,R8
         SR    R7,R7                   GET RELATIVE
         IC    R7,QCBRELLN          RELATIVE LINE NUMBER
         STC   R7,TRMRLN          SAVE AP RLN
         SLL   R7,D2              MULT BY FOUR                   S22024
         L     R8,QCBDCBAD-D1     DCB ADDR                       S22024
         DROP  R8
         USING IHADCB,R8
         TM    DCBDSORG,LGBA      LGB VS. DCB                    S22024
         BZ    UCB0X              NO                             S22024
         DROP  R8                                                S22024
         L     R1,TNTENTRY        GET TERMINAL TTE ADDRESS     @Y17XAUU
         BAL   R14,NCPTTE         GET NCP TTE ADDRESS          @Y17XAUU
*                                                              @Y17XAUU
*        ON RETURN R1 CONTAINS ADDRESS OF NCP TTE PREFIX       @Y17XAUU
*                                                              @Y17XAUU
         USING IEDNTRM,R1         SET TTE ADDRESSABILITY       @Y17XAUU
         TM    TRMBYTE2,TRMRSACT  3705 ACTIVE                  @Y17XAUU
         BO    UCBNACT            YES- ERROR                   @Y17XAUU
         TM    TRMBYTE2,TRMINPG   ACT/DEACT IN PROGRESS        @Y17XAUU
         BZ    UCBNACT            NO - ERROR                   @Y17XAUU
         L     R1,TRMDESTQ-D1     GET QCB ADDRESS              @Y17XAUU
         DROP  R1                                              @Y17XAUU
         USING IEDQQCB,R1         SET QCB ADDRESSABILITY       @Y17XAUU
         L     R8,QCBDCBAD-D1     GET 3705 DCB ADDRESS         @Y17XAUU
         DROP  R1                                              @Y17XAUU
         LA    R7,D4              RLN EQU 1                      S22024
         B     UCB0X              CHECK DCB FOR OPEN           @Y17XAUU
UCBNACT  EQU   *                                               @Y17XAUU
         IEDQMSG MSGID=010,FUNCT=CEC MSG                         S22024
         OI    TOTFLG09,TOTPRECT  CT EQU AP                      S22024
         B     ERR1               ERROR EXIT                     S22024
UCB0X    EQU   *                                                 S22024
         USING IHADCB,R8                                         S22024
         TM    DCBOFLGS,OPEN      DCB OPENED                     S22024
         BZ    NOOPEN                  NO BRANCH
         L     R8,DCBDEBAD        GET DEB ADDRESS
         DROP  R8
         USING DEBNMSUB,R8
         SR    R15,R15            CLEAR REGISTER 15
         IC    R15,DEBNMEXT       GET NUMBER OF EXTENTS
         SLL   R15,D2             MULT BY FOUR                   S22024
         CR    R15,R7             IS DESIRED LINE OPENED?
         BL    NOOPEN               NO
         L     R8,DEBUCBAD-D4(R7) UCB ADDR                       S22024
         USING UCB,R8
         MVC   WORK1(3),UCBNAME        MOVE UCB ID IN EBCDIC
         MVC   WORK2(2),UCBCHA         MOVE UCB ID IN HEX
         DROP  R8
         MVI   WORK2+D2,D0        CLEAR UNUSED BYTE              S22024
         NI    WORK2,X0F          INSURE POSITIVE                S22024
         BR    R9                      RETURN
NOOPEN   EQU   *
         SPACE
         IEDQMSG MSGID=004,FUNCT=CEC AP LINE NOT OPENED
         B     ERR2                    REPORT INVALID TERMINAL NAME
         EJECT
**************************************************************** S22024
*        ROUTINE TO GO TO IEDQWB2 TO CALCULATE THE AMOUNT OF   * S22024
*        OF CORE NEEDED TO BUILD DUMMY ENTRIES FOR A SPECIFIED * S22024
*        REMOTE TERMINAL. CONTROL IS PASS TO IEDQWB TO ATTAIN  * S22024
*        THE ACTUAL CORE.  CONTROL IS THEN RETURNED TO IEDQWB2 * S22024
*        TO COPY THE ENTRIES.                                  * S22024
**************************************************************** S22024
DUMMYTNT EQU   *                                                 S22024
         STM   R7,R11,TRMSAVE2    SAVE REGISTERS                 S22024
         MVI   TRMRQFLG,APREQ     SHOW AP REQUEST                S22024
         LA    R12,KTRM           PASS PARAMETERS                S22024
         XC    TRMFLG01(D1),TRMFLG01 CLEAR CONTROL FLAG          S22024
*                                                                S22024
         LINK  EP=IEDQWB2         CALCULATE CORE                 S22024
         CL    R15,ERRCD8         SNA LU FAILURE               @XM05678
         BE    DUMFAILU           YES                          @XM05678
         LTR   R15,R15            REQUEST SUCCESSFUL             S22024
         BNZ   DUMFAIL            NO - ERROR                     S22024
         TM    TRMFLG01,TRM3705   3705 MODE                      S22024
         BO    DUMOUT             YES - EXIT                     S22024
         TM    TRMFLG01,TRMCTEAP  CT EQU AP                      S22024
         BO    DUMOUT2            YES, BRANCH                    S05331
*                                                                S22024
         L     R10,TOTRESPL       ADDR OF TOTE RES. CTL BLK      S22024
         USING RESPL,R10                                         S22024
         XC    TOTOTECB(D4),TOTOTECB CLEAR ECB                   S22024
         POST  RESTECB1           POST REQUEST TO IEDQWB         S22024
         WAIT  ECB=TOTOTECB       WAIT FOR REQUEST TO BE HONORED S22024
*                                                                S22024
         CLI   TOTWKSPC,XFF       REQUEST SUCCESSFUL             S22024
         BE    DUMFAIL            NO - ERROR                     S22024
*                                                                S22024
         OI    TRMFLG01,TRMQWB3   SHOW SECOND PASS               S22024
         LA    R12,KTRM           PASS PARAMETERS                S22024
         LINK  EP=IEDQWB2         COPY ENTRIES                   S22024
         LTR   R15,R15            REQUEST SUCCESSFUL             S22024
         BNZ   DUMFAIL            NO - ERROR                     S22024
*                                                                S22024
DUMOUT   EQU   *                                                 S22024
         LM    R7,R11,TRMSAVE2    RESTORE REGISTERS              S22024
         BR    R9                 RETURN                         S22024
         SPACE
DUMFAILU EQU   *                                               @XM05678
         IEDQMSG MSGID=093,FUNCT=CEC                           @XM05678
         B     DUMEND             TERMINATE TRM                @XM05678
         SPACE 2
DUMFAIL  EQU   *
         WTO   'IED215I OLT REQUEST REJECTED, UNABLE TO ALLOCATE RESOUR*
               CES FOR CT/AP',ROUTCDE=10,DESC=7
         SPACE
DUMEND   EQU   *
         NI    TRMFLG01,XFF-TRMCTEAP CLEAR CT EQU AP FLAG        S22024
         L     R13,D4(R13)        RESTORE CALLERS SAVE AREA ADDRESS
         LM    R14,R12,12(R13)    RESTORE CALLER'S REG
         SR    R15,R15            CLEAR REG
         BR    R14                RETURN
DUMOUT2  EQU   *                                                 S05331
         NI    $ERROPT,XFF-$ALTPRNT  SHOW NO AP                  S05331
         OI    TOTFLG09,TOTPRECT  SET FLAG INDICATING CT EQU AP  S05331
         LM    R7,R11,TRMSAVE2    RESTORE CALLER'S REGISTERS     S05331
         B     EXIT               BYPASS AP PROCESSING           S05331
         EJECT
LCBSCAN  EQU   *
         STM   R10,R12,TRMSAVE1   SAVE REG 10-12
         STM   R5,R9,TRMSAVE2     SAVE REG 5-9
         L     R5,TRMQCB          GET CT QCB ADDRESS
         USING IEDQQCB,R5
         LA    R5,D0(R5)          CLR HI ORDER BYTE              S22024
         L     R9,QCBDCBAD-D1     DCB ADDR                       S22024
         USING IHADCB,R9
         TM    DCBDSORG,LGBA      LGB VS DCB                     S22024
         BZ    LCB0X              NO                             S22024
         DROP  R9                                                S22024
         L     R1,TNTENTRY        GET TERMINAL TTE ADDRESS     @Y17XAUU
         BAL   R14,NCPTTE         FIND NCP TTE ADDRESS         @Y17XAUU
         USING IEDNTRM,R1         SET TTE ADDRESSABILITY       @Y17XAUU
         L     R9,TRMDESTQ-D1     CB ADDRESS                   @Y17XAUU
         DROP  R1                                              @Y17XAUU
         USING IEDQQCB,R9         SET QCB ADDRESSABILITY       @Y17XAUU
         L     R9,QCBDCBAD-D1     GET 3705 DCB ADDRESS         @Y17XAUU
         DROP  R9                                              @Y17XAUU
         USING IHADCB,R9                                         S22024
LCB0X    DS    0H                 BOUNDARY ALLIGNMENT            S22024
         L     R7,DCBDEBAD        GET DEB ADDR
         USING DEBNMSUB,R7
         SR    R8,R8              CLEAR REG 8
         IC    R8,DEBNMEXT        GET DEB NUMBER OF EXTENTS
         DROP  R7
         SR    R7,R7              CLEAR REG 7
         IC    R7,QCBRELLN        GET QCB'S STARTING RLN
         TM    TRMFLG01,TRM3705   3705 MODE                      S22024
         BZ    LCB04              NO                             S22024
         LA    R7,D1              SET RLN EQU 1                  S22024
LCB04    DS    0H                 BOUNDARY ALLIGNMENT            S22024
         DROP  R5
         LR    R5,R7              COPY RLN FOR NEXT TEST
         BCTR  R5,R0              RLN MINUS 1                    S22024
         SR    R8,R5              GET COUNT OF LCB'S TO SCAN
         SR    R12,R12            CLEAR REG 12
         IC    R12,DCBEIOBX       GET LCB SIZE
         LR    R11,R7             SAVE QCB'S STARTING RLN
         MR    R10,R12            GET STARTING LCB OFFSET
         AL    R11,DCBIOBAD       POINT TO IOB OF LCB
         S     R11,K32            ADJUST TO START OF LCB         Y06330
         DROP  R9
         TM    TRMFLG01,TRM3705   3705 MODE?                     S05331
         BZ    MODE0X             NO, BRANCH                     S05331
         L     R8,TNTENTRY        GET TERMINAL ENTRY ADDRESS     S05331
         LA    R1,TRMPRFSZ        GET TTE PREFIX SIZE          @Y17XAUU
         SLR   R8,R1              POINT TO TTE PREFIX          @Y17XAUU
         USING IEDNTRM,R8         SET TTE ADDRESSABILITY       @Y17XAUU
LINELOOP EQU   *                                               @Y17XAUU
         LH    R1,TRMCOHRT        GET COHORT POINTER           @Y17XAUU
         L     15,TERNMTAB        TTCIN TO TTE CONVERSION      @Y17XAUU
*                                 ROUTINE ADDRESS              @Y17XAUU
         BALR  R14,R15            FIND TTE ADDRESS             @Y17XAUU
         LR    R8,R1              GET TTE ADDRESS              @Y17XAUU
         LA    R1,TRMPRFSZ        GET TTE PREFIX SIZE          @Y17XAUU
         SLR   R8,R1              POINT TO TTE PREFIX          @Y17XAUU
         CLI   TRMTYPE,TRMNSDLC   PRE SNA LINE ENTRY           @Y17XAUU
         BE    LINEFND            YES                          @Y17XAUU
         CLI   TRMTYPE,TRMSDLC    SNA LINE ENTRY               @Y17XAUU
         BNE   LINELOOP           NO - CHECK NEXT ENTRY        @Y17XAUU
LINEFND  EQU   *                                               @Y17XAUU
         TM    TRMLINK,TRMLACTV   LINE STOPPED                 @Y17XAUU
         BZ    LCBSSC             NO, BRANCH                   @Y17XAUU
         OI    TOTAPDFL,TOTAPLST  SHOW LINE INTIALLY STOPPED     S05331
         B     LCBSSC             BRANCH                         S05331
MODE0X   EQU   *                                                 S05331
         USING IEDQLCB,R11
         TM    LCBSTAT1,LCBFREEN+LCBRECVN+LCBSENDN LINE STOPPED
         BNZ   LCBSSC             NO
         OI    TOTAPDFL,TOTAPLST  SHOW CT INIT STOPPED
LCBSSC   EQU   *
         LM    R5,R9,TRMSAVE2     RESTORE REG 5-9
         L     R10,TRMSAVE1       RESTORE REG 10
         L     R12,TRMSAVE1+D8    RESTORE REG 12
         BR    R9                 RETURN TO CALLER
         DROP  R11
         DROP  R8                                              @Y17XAUU
         EJECT
************************************************************** @Y17XAUU
*        ROUTINE TO FIND NCP TTE ADDRESS. INPUT: REG 1 =     * @Y17XAUU
*        TERMINAL TTE ADDRESS. OUTPUT: REG 1 = NCP TTE       * @Y17XAUU
*        ADDRESS. RETURN TO CALLER VIA REG 14.               * @Y17XAUU
************************************************************** @Y17XAUU
NCPTTE   EQU   *                                               @Y17XAUU
         ST    R14,TRMSAVE2       SAVE RETURN ADDRESS          @Y17XAUU
         LA    R8,TRMPRFSZ        GET TTE PREFIX SIZE          @Y17XAUU
         SLR   R1,R8              POINT TO TTE PREFIX          @Y17XAUU
         USING IEDNTRM,R1         ESTABLISH TTE ADDRESSABILITY @Y17XAUU
NCPLOOP  EQU   *                                               @Y17XAUU
         LH    R1,TRMCOHRT        GET COHORT POINTER           @Y17XAUU
         LTR   R1,R1              ENTRY IN HIERARCHY           @YM07415
         BZ    UCBNACT            NO - ERROR RETURN            @YM07415
         L     R15,TERNMTAB       TTCIN TO TTE CONVERSION      @Y17XAUU
*                                 ROUTINE ADDRESS              @Y17XAUU
         BALR  R14,R15            FIND TTE ADDRESS             @Y17XAUU
         SLR   R1,R8              POINT TO TTE PREFIX          @Y17XAUU
         CLI   TRMTYPE,TRMLNCP    NCP ENTRY                    @Y17XAUU
         BNE   NCPLOOP            NO - CHECK NEXT ENTRY        @Y17XAUU
         L     R14,TRMSAVE2       RESTORE RETURN ADDRESS       @Y17XAUU
         BR    R14                RETURN TO CALLER             @Y17XAUU
         DROP  R1                                              @Y17XAUU
         EJECT
TRMRLN   DS    C                  RLN FOR AP
         EJECT
CEOUT2   DC    C'XXXXXXXX NOT OPENED'  MSG - LINE NOT OPENED     S99528
CEOUT2E  DS    0C                 END OF MESSAGE
TRMWRK   DS     CL92              TRM WORK BUFFER                S99528
PATCH    DC    100XL1'40'         PATCH AREA                     S22024
WORK4    DS    4F                 SAVE AREA
F8       DC    F'8'               VALUE OF 8                     S22024
ERRCD8   DC    F'8'               ERROR RETURN CODE            @XM05678
TRMNCMCT DC    F'0'               COUNTER SAVE AREA
TRMTEN   DC    F'10'              VALUE OF TEN
TTEADDR  DC    F'0'               WORK AREA
WORK3    DC    F'0'               WORK AREA
NOUCB    DC    H'0'               COMPARE OPERAND
H4       DC    H'4'               VALUE OF 4                     S22024
K32      DC    F'32'              VALUE OF 32                    S22024
WORK1    DC    XL8'0'             PLACE FOR AP NAME              S99528
WORK2    DC    XL8'0'             PLACE FOR AP NAME              S99528
SAVE     DS    CL20               WORK AREA                      S99528
UCBSAVE  DS    A                  ADDRESS OF AP UCB              S99528
TRMSAVE  DS    F                  ADDRESS OF TRM BUFFER          S99528
TERNMLEN DS    CL1                LENGTH OF AP NAME              S99528
AVTUISV  DS    A                  ADDRESS OF BINARY SEARCH RTN   S99528
PAREMLST DS    0CL8               BINARY SEARCH P-LIST           S99528
         DS    CL1                OFFSET TO BINARY SEARCH        S99528
         DC    X'00'              FLAG BYTE                      S99528
         DC    X'00'              FLAG BYTE                      S99528
LENGTH   DS    CL1                LENGTH OF SYMBOLIC NAME        S99528
         DC    A(WORK1)           ADDRESS OF NAME                S99528
K0       DC    F'0'               VALUE OF 0                     S22024
         DS    0D                 ALIGNMENT                      S99528
TIMOUT   DC    X'F0F0F0F3F0F0F0F0'  TIMER WTOR                   S99528
ECB      DC    F'0'               ECB                            S99528
ANSWER   DC    CL3' '             INPUT BUFFER                   S99528
**************************************************************** S22024
*        THE FOLLOWING MESSAGE IS ISSUED WHEN OPERATOR WILL    * S22024
*        NOT ALLOW OLT TO USE PROTECTED DEVICE                 * S22024
**************************************************************** S22024
REJECT   DC    C'IED345I '                                       S22024
         DC    C'TRM REJECTED - OLT CAN NOT USE PROTECTED DEVICE' MSG
SPACE    DC    X'40'              SPACE                          S22024
DEVNAM   DC    8XL1'40'           DEVICE NAME                    S22024
REJECTE  DS    0C                 END OF MESSAGE                 S22024
         SPACE
BLDLLIST DS    0F                 BLDL P-LIST                    S22024
         DC    XL2'0001'          NUMBER OF LIST ENTRIES         S22024
         DC    XL2'003A'          LENGTH OF EACH ENTRY           S22024
READNAME DC    8XL1'40'           TERMINAL NAME                  S22024
         DC    XL16'0'            ADDITIONAL DIRECTORY INFO      S22024
CORNEED  DC    XL3'0'             CORE NEEDED FOR ENTRY          S22024
         DC    XL31'0'            ADDITIONAL DIRECTORY INFO      S22024
         SPACE
ENQLIST  ENQ   (QNAME,RNAME,S,8,STEP),RET=USE,MF=L ENQUEUE PL    S99528
QNAME    DC    CL8'SYSZTOTE'      MAJOR QCB SUBTASK NAME       @Y17XAUU
RNAME    DC    8CL1' '            RESOURCE NAME                  S99528
DEQLIST  DEQ   (QNAME,RNAME,8,STEP),RET=HAVE,MF=L  DEQUEUE P-LISTS99528
TRMTTCIN DS    H                  TTCIN SAVE AREA              @Y17XAUU
         SPACE
CTEQUAP  CLC   TOTCTNAM(D1),TOTAPNAM CT = AP ?
         SPACE 2
**************************************************************** S22024
KTRM     DS    0F                 PARAMETER LIST PASSED TO QWB2  S22024
TERNMTAB DS    A                  TERMNAME TABLE ADDRESS         S22024
TNTENTRY DS    A                  TERMNAME ENTRY ADDRESS         S22024
TRMQCB   DS    A                  CT/AP QCB ADDRESS              S22024
TRMTNT   DS    A                  CT/AP TNT ADDRESS              S22024
TRMSAVE1 DS    3F                 REGISTERS SAVE AREA            S22024
TRMSAVE2 DS    5F                 REGISTERS SAVE AREA            S22024
TRMSAV1  DS    3F                 REGISTERS SAVE AREA            S22024
REGSAVE  DS    F                  WORK AREA                      S22024
F32      DC    F'32'              VALUE OF 32                    S22024
KPQCB    DS    A                  TRUE P-QCB ADDRESS           @YM08027
KMQCB    DS    A                  DUMMY M-QCB ADDRESS          @YM08027
CONCTTE  DS    A                  TRUE CONC TERM TTE ADDRESS   @YM08027
KDRQ     DS    A                  TRUE DRQ ADDRESS             @YM08027
KPQCBSZ  DS    H                  P-QCB SIZE                   @YM08027
KMQCBSZ  DS    H                  M-QCB SIZE                   @YM08027
KDRQSZ   DS    H                  DRQ SIZE                     @YM08027
TTETSZ   DS    CL1                CONC TERM TTE SIZE           @YM08027
TTECSZ   DS    CL1                CONC TTE SIZE                @YM08027
TRMRQFLG DS    CL1                DUMMY TNT REQUEST FLAG         S22024
CTREQ    EQU   X'80'              CT REQUEST                   @Y17XAUU
APREQ    EQU   X'40'              AP REQUEST                   @Y17XAUU
PRREQ    EQU   X'20'              PRIMARY TD REQUEST           @Y17XAUU
SCREQ    EQU   X'10'              SECONDARY TD REQUEST         @Y17XAUU
TRMFLG01 DS    CL1                CONTROL FLAG                   S22024
CONC     EQU   X'80'              TERMINAL ATTACHED TO CONC      S22024
TRMSPAS  EQU   X'40'              PROCESS CONC TTE               S22024
TRMQWB3  EQU   X'20'              SECOND PASS FUNCTION           S22024
TRMDELY  EQU   X'08'              QCB IN TIME DELAY QUEUE        S22024
TRM3705  EQU   X'04'              3705 MODE                      S22024
TRMCTEAP EQU   X'02'              CT EQU AP                      S22024
WRKFLG   DS    CL1                WORK AREA                      S22024
**************************************************************** S22024
         SPACE 2
DVTEST   DS    CL1                FLAG SAVE AREA (CDS+10)        S22024
AP       DC    C'AP'              ALTERNATE PRINTER OPT          S22024
SYSOUTA  DC    C'SYSOUT'          SYSTEM OUTPUT DEVICE           S22024
SYSCONA  DC    C'SYSCON'          SYSTEM CONSOLE                 S22024
NAP      DC    C'NAP'             NAP OPTION                     S22024
         EJECT
         TLGBD                                                   S22024
         EJECT
         TDRQD
         TQCBED
         EJECT
         EJECT
RESPL    RESPL EQUATES=YES
         EJECT
         TQCBD
         EJECT
DCBDSECT DCBD  DSORG=TX
         EJECT
         TSCBD
         EJECT
         TPRFD
         EJECT
         TLCBD
         EJECT
CVT      DSECT
         CVT
         EJECT
UCB      DSECT
         IEFUCBOB
         EJECT
         TAVTD
         EJECT
         TDEBD
         EJECT
         TTRMD
         EJECT
         TTNTD
         EJECT
         OLTCB                                                   S22024
CEIN     EQU   TOTINBUF           INPUT BUFFER                   S22024
         EJECT
         END
