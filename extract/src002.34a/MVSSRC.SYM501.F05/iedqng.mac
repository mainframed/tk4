QNG      TITLE 'IEDQNG  -BUILD INCIDENT RECORD FOR CHECKPT MACRO'
IEDQNG   CSECT
         SPACE 3
***********************************************************************
*                                                                     *
*TITLE: 'IEDQNG'-INCIDENT CHECKPOINT FOR AN MH MACRO-CHECKPT          *
*                                                                     *
*STATUS:CHANGE LEVEL 0                                                *
*                                                                     *
*FUNCTION:THIS MODULE BUILDS AN INCIDENT DISK RECORD WHEN THE         *
*   REQUEST ELEMENT ON THE CHECKPOIN QCB IS AN LCB FROM A CHECKPT     *
*   MACRO. IT DETERMINES THE TERMINAL CURRENTLY ASSOCIATED WITH THE   *
*   LCB(IF THE CHECKPT MACRO IS IN THE IN MH,IT IS THE SOURCE TERMINAL*
*   IF IT IS IN THE OUT MH, IT IS THE DESTINATION TERMINAL.) IF       *
*   LCBTTCIN IS ZERO THE TERMINAL ENTRY CANNOT BE IDENTIFIED SO AN    *
*   INCIDENT CHECKPOINT                                               *
*  RECORD IS NOT BUILT. A BIT IS TURNED ON IN CKPLREB AND THE OFFSET *
*   OF THE NOTIFICATION ROUTINE IS RETURNED IN REGISTER 15.           *
*                                                                     *
*ENTRY POINT:                                                         *
*                                                                     *
*        IEDQNG                                                       *
*                                                                     *
*INPUT:REGISTERS 2,3,9,12,14,15 CONTAIN THE FOLLOWING:                *
*                                                                     *
*   2-ADDRESS OF THE CHECKPOINT WORK AREA                             *
*   3-ADDRESS OF REQUEST ELEMENT THIS MODULE IS TO PROCESS            *
*   9-ADDRESS OF AVT                                                  *
*   12-BASE REGISTER FOR CHECKPOINT EXECUTOR-IEDQNF                   *
*   14-RETURN ADDRESS-IN IEDQNF                                       *
*   15-ENTRY POINT FOR THIS MODULE                                    *
*                                                                     *
*   THE LCB(REQUEST ELEMENT) POINTED TO BY R3.                        *
*                                                                     *
*OUTPUT:THE ABOVE REGISTERS 2-14 REMAIN THE SAME(R15 MAY HAVE A       *
*   RETURN CODE).                                                     *
*                                                                     *
*   AN INCIDENT DISK RECORD IS BUILT IN A GETMAIN AREA.(ITS ADDRESS   *
*   IS IN CKPLDRB.)                                                   *
*                                                                     *
*EXTERNAL ROUTINES:                                                   *
*        IEDQTNT-DETERMINES TERMINAL TABLE ADDRESS                    *
*                                                                     *
*EXITS-NORMAL:THIS MODULE RETURNS TO THE ADDRESS IN R14. R15 HAS AN   *
*   OFFSET FOR THE CHECKPOINT QUENE MANAGER(72) OR THE INCIDENT       *
*   OVERFLOW ROUTINE(96) OR THE NOTIFICATION ROUTINE(80).             *
*                                                                     *
*EXITS ERROR:THIS MODULE RETURNS TO THE ADDRESS IN R14. R15 HAS AN    *
*   OFFSET FOR THE NO CORE ROUTINE(88).                               *
*                                                                     *
*TABLES/WORK AREAS:                                                   *
*                                                                     *
*   AVT(AVTCLRHI,AVTRNMPT,AVTOPTPT)                                   *
*   CHECKPOINT WORK AREA                                              *
*   TERMNAME TABLE                                                    *
*   TERMINAL TABLE                                                    *
*   OPTION TABLE                                                      *
*   OPTION LENGTH TABLE                                               *
*                                                                     *
*ATTRIBUTES:REENTERABLE                                               *
*                                                                     *
*NOTES:THE OPERATION OF THIS MODULE DEPENDS UPON AN INTERNAL REPRE-   *
*   SENTATION OF THE EXTERNAL CHARACTER SET WHICH IS EQUIVALENT TO    *
*   THE ONE USED AT ASSEMBLY TIME. THE CODING HAS BEEN ARRANGED SO    *
*   THAT REDEFINITION OF 'CHARACTER' CONSTANTS,BY REASSEMBLY,WILL     *
*   RESULT IN A CORRECT MODULE FOR THE NEW DEFINITIONS.               *
*                                                                     *
***********************************************************************
         SPACE 3
*REGISTER ASSIGNMENTS
         SPACE
R0       EQU   0
R1       EQU   1                        PARM REGISTER-OR WORK REG
R2       EQU   2                        ADDRESS OF CKPT WORK AREA
R3       EQU   3                        ADDRESS OF REQUEST ELEMENT
R4       EQU   4                        ADDRESS OF DISK RECORD
R5       EQU   5                        ADDRESS OF OPTION ENTRY
R6       EQU   6                        INDEX REG
R7       EQU   7                        OFFSET TO TERMNAME TABLE
RTERM    EQU   8                        ADDRESS OF TERM. TABLE ENTRY
RAVT     EQU   9                        ADDRESS OF AVT
R10      EQU   10                       LENGTH FOR MOVE(OPTN ENTRY)
R11      EQU   11                       COUNT OF TERMNAME ENTRIES
RBASE    EQU   12                       BASE REG
R13      EQU   13                       ADDRESS OF OPTION FIELDS
R14      EQU   14                       RETURN REG
R15      EQU   15                       ENTRY POINT OR RETURN CODE
         SPACE 3
*STANDARD LINKAGE
         SPACE
         USING *,R15                    TEMP BASE
         B     QNG00
         DC    CL8'IEDQNG'
         DC    CL6'041671'
QNG00    EQU   *
         USING IEDQAVTD,RAVT            BASE FOR AVT
         USING IEDQCKPD,R2              BASE FOR CKPT WORK AREA
         USING LCBERB,R3                BASE FOR ERB-REQUEST ELEMNT1016
         SPACE 3
         LH    R7,LCBTTCIN              GET TERMNAME TABLE OFFSET
         N     R7,AVTCLRHI
         BZ    QNG50                    BRANCH IF NO TRM OFFSET    0720
         LR    R4,R14                   SAVE REG
         LR    R5,R15                   SAVE REG
         DROP  R15
         USING IEDQNG,R5                TEMPORARY BASE
         L     R15,AVTRNMPT             GET BEGINNING OF TERM TABL
         LR    R1,R7                    SET PARM FOR SUBRTN
         BALR  R14,R15                  GET ADDRESS OF TERM ENTRY
         LR    R14,R4                   RESTORE REG
         SPACE 3
         USING IEDQTRM,RTERM            BASE FOR TERM ENTRY
         LR    RTERM,R1
         SR    R0,R0                    CLEAR
         IC    R0,CKPINCNO              GET COUNT OF INCIDENTS LEFT
         LTR   R0,R0                    CHECK COUNT
         BZ    QNG70                    BRANCH IF NONE
         BCTR  R0,0                     SUBTRACH FROM COUNT
         STC   R0,CKPINCNO              RESTORE
         SPACE 3
         LH    R6,CKPINCLN              LENGTH OF INCIDENT RECORD
         GETMAIN EC,LV=(R6),A=CKPLDRB,MF=(E,CKPGETML)
         SPACE 3
         LTR   R15,R15                  CHECK RETURN CODE
         LR    R14,R4                   RESTORE REG
         LR    R15,R5                   RESTORE REG
         DROP  R5
         USING IEDQNG,R15               RESTORE BASE
         BNZ   QNG60                    BRANCH IF NO CORE ALLOCATED
         L     R4,CKPLDRB               GET ADDRESS OF DISK RECORD
         USING IEDQCDRD,R4              BASE FOR CKPT DISK RECORD
         SPACE 3
*BUILD INCIDENT RECORD FOR LCB
         SPACE
         MVI   CDRKEY,CDRLCB            SET KEY FIELD
         STH   R7,CDROFFS               PUT IN OFFSET
         MVC   CDRSTAT,TRMSTATE         MOVE TERM STATUS TO RCD    0720
         MVC   CDRSEQIN(4),TRMINSEQ     MOVE SEQ NUMBERS TO RCD    1008
         TM    TRMSTATE,TRMOPTFN        CHECK FOR OPTION FIELDS
         BZ    QNG48                    BRANCH IF NOT USED
         SPACE 3
*MOVE OPTION FIELDS TO DISK RECORD (USES REGISTERS 1,4,5,6,7,8,10,
*11,13)
         SPACE
*SET UP FOR LOOP
         SPACE
         SR    R11,R11
         IC    R11,TRMOPNO              COMPARAND
         L     R13,AVTOPTPT              ADDRESS OF OPTION TABLE
         L     R7,4(R13)                ADDRESS OF OPT LENGTH TABLE
         LH    R1,TRMOPTBL              OFFST TO OPT FOR THIS ENTRY
         N     R1,AVTCLRHI              CLEAR HIGH ORDER HALF WORD
         AR    R13,R1                   ADD OFFSET TO ADDRESS
         SR    R1,R1                    CLEAR
         SR    R10,R10                  CLEAR-LENGTH FOR MOVE
         LA    R6,TRMOPT                GET BEGINNING OF OFFSETS
         SPACE 3
*BEGINNING OF LOOP THROUGH OPTION TABLE OFFSETS FOR THIS TERM
         SPACE
QNG40    EQU   *
         LR    R5,R13                   INITIALIZE
         IC    R1,0(R6)                 OPTION OFFSET
         CLI   0(R6),X'FF'              CHECK FOR UNUSED ENTRY
         BE    QNG45                    BRANCH IF UNUSED
         AR    R5,R1                    ADDRESS OF OPTION ENTRY
         IC    R10,0(R7)                GET LENGTH OF THIS OP ENTRY
         CLI   1(R7),X'D0'              CHECK OPTION TYPE          1111
         BH    QNG43                    BRANCH IF NOT ADDRESS TYPE 1111
         CLI   1(R7),X'80'              CHECK OPTION TYPE          1111
         BH    QNG45                    BRANCH IF ADDRESS TYPE     1111
QNG43    EQU   *                                                   1111
         EX    R10,QNGMOVE              MOVE OPTIONS TO DISK RECORD
         LA    R4,1(R4,R10)             ADD LENGTH MOVED TO BASE OF    X
                                        DISK RECORD
QNG45    EQU   *
         LA    R7,CKPOPLTL(0,R7)        GET OPT LNGTH TABLE ENTRY
         LA    R6,1(0,R6)
         BCT   R11,QNG40                SUB FROM COUNT OF ENTRIES
         SPACE
*END OF LOOP THROUGH OPTION TABLE OFFSETS FOR THIS TERM ENTRY
         SPACE 3
QNG48    EQU   *                                                   0720
         LA    R15,CKPIOQM              OFFSET FOR ENQUEUE RTN
         BR    R14                      RETURN TO EXECUTOR
         SPACE 3
*THE TERMINAL ENTRY CANNOT BE IDENTIFIED.NO CKPT IS TAKEN.         0720
         SPACE
QNG50    EQU   *
         OI    CKPLREB,CKPNDRB          INDICATE NO DISK RCB BUILT
         LA    R15,CKPNOTIF             OFFSET FOR NOTIFICATION RTN
         BR    R14                      RETURN TO EXECUTOR
         SPACE 3
*NO GETMAIN AREA ALLOCATED. SET UP FOR NO GETMAIN RTN
         SPACE
QNG60    EQU   *
         LA    R15,CKPNOCOR             OFFSET FOR RTN
         BR    R14                      RETURN TO EXECUTOR
         SPACE 3
*THERE ARE NO MORE INCIDENT RECORDS AVAILABLE-GO TO INCIDENT
*OVERFLOW RTN TO SET UP FOR TOTAL.
         SPACE
QNG70    EQU   *
         LA    R15,CKPNOINC             OFFSET FOR RTN
         BR    R14                      RETURN TO EXECUTOR
         SPACE
QNGMOVE  MVC   CDROPTN(0),0(R5)         MOVE TO RECORD FROM OPT TBL
         PRINT NOGEN
         TAVTD
         TCKPD 3330
         TLCBD
         TTRMD
         END
