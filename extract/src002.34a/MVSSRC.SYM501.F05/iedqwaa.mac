         TITLE 'IEDQWAA  $DDATE SERVICE MODULE OF TCAM/TOTE'
IEDQWAA  CSECT
*A000000-999999                                                @Y16X5U0
*CHANGE ACTIVITY AS FOLLOWS:
*A000000-999999                                                  S22024
*C120000,198000                                                @Y17XAU2
*D201000,636000,930000-934000                                  @Y17XAU2
*A306000,774000                                                @Y17XAUU
*A630000,990000                                                @Y17XAU2
***********************************************************************
*                                                                     *
* MODULE NAME = IEDQWAA (TCAM,TOTE)                            @Y17XAUU
*                                                                     *
* DESCRIPTIVE NAME = $DDATE SERVICE MODULE                            *
*                                                                     *
* COPYRIGHT = NONE                                                    *
*                                                                     *
* STATUS = VERSION 10.0                                        @Y17XAUU
*                                                                     *
* FUNCTION = THIS MODULE SUPPORTS THE $DDATE MACRO. THE DATE IS       *
*            RETRIEVED FROM THE SYSTEM AND PASSED TO THE OLT IN THE   *
*            FORMAT MM/DD/YY.                                         *
*                                                                     *
* NOTES = SEE BELOW                                                   *
*                                                                     *
*    DEPENDENCIES = NONE                                              *
*                                                                     *
*    RESTRICTIONS = NONE                                              *
*                                                                     *
*    REGISTER CONVENTIONS = R2 POINTS TO ONLINE TEST CONTROL BLOCK    *
*                           R4 IS BASE REGISTER                       *
*                           OTHERS USED AS NEEDED                     *
*                                                                     *
*    PATCH LABEL = PATCH A DC STATEMENT                               *
*                                                                     *
* MODULE TYPE = PROCEDURE                                      @Y17XAUU
*                                                                     *
*    PROCESSOR = ASSEMBLE XF                                   @Y17XAUU
*                                                                     *
*    ATTRIBUTES = ENABLED, PROBLEM PROGRAM MODE,                      *
*                 RESIDENT, RE-ENTRANT                         @Y17XAU2
*                                                                     *
* ENTRY POINT = IEDQWAA                                               *
*                                                                     *
*    PURPOSE = SEE FUNCTION                                           *
*                                                                     *
* INPUT      = R2 IS ADDRESS OF OLTCB                          @Y17XAUU
*              R1 IS $DDATE PLIST ADDRESS                      @Y17XAUU
*              R13 IS SAVE AREA ADDRESS                               *
*              R14 LOADED WITH RETURN ADDRESS                         *
*              R15 LOADED WITH ENTRY POINT ADDRESS                    *
*                                                                     *
* LINKAGE = BALR REG15,REG14 FROM IEDQWA WITH REG 1 POINTING   @Y17XAUU
*             TO $DDATE PARAMETER LIST.                        @Y17XAUU
*                                                                     *
* OUTPUT = DATE IN R0 = MM/D                                          *
*                  R1 = D/YY                                          *
*                                                                     *
* EXIT-NORMAL = REGISTER 15 RETURN CODES RETURNED TO CALLER           *
*               00 - SUCCESSFUL OPERATION                             *
*               04 - DATE NOT AVAILABLE                               *
*                                                                     *
* EXIT-ERROR = NONE                                                   *
*                                                                     *
* EXTERNAL REFERENCES = SEE BELOW                                     *
*                                                                     *
*    ROUTINES = THE SYSTEM 'TIME' MACRO IS ISSUED                     *
*                                                                     *
*    DATA AREAS = EXTERNAL TO THIS MODULE IN OLTCB SERVICE     @Y17XAU2
*                 MODULE WORK AREA                             @Y17XAU2
*                                                                     *
*    CONTROL BLOCK = ONLINE TEST CONTROL BLOCK (OLTCB)                *
*                                                                     *
* TABLES = LEAPTABL - LEAP YEAR TABLE                                 *
*          NONLPTBL - NON-LEAP YEAR TABLE                             *
*                                                                     *
* MACROS = TIME                                                       *
*                                                                     *
***********************************************************************
         EJECT
         USING IEDQWAA,BASEREG
         USING TOTOLTCB,R2
         SAVE  (14,12)             SAVE CALLERS REGISTERS
         LR    BASEREG,R15         LOAD BASE REGISTER
*
IEDQWAA  IEDHJN DATE0100,HJN
*
         LA    R12,TOTSAVE1        PICK UP SAVE AREA ADDRESS   @Y17XAUU
         ST    R12,EIGHT(R13)      SAVE IT IN CALLERS SAVE AREA@Y17XAUU
         ST    R13,TOTSAVE1+FOUR   SAVE CALLERS SAVE AREA ADDR @Y17XAUU
         LR    R13,R12             SET MY SAVE AREA ADDRESS    @Y17XAUU
         XC    TOTSERV(TEND-TOTOBEND),TOTSERV CLEAR WORK AREA  @Y17XAUU
         MVC   DATE(EIGHT),DATE1   SET DATE FORMAT IN WORK AREA@Y17XAUU
         SPACE 4
         TIME
         SPACE
         ST    R1,SAVE             SAVE DATE FROM MACRO
         MVO   WORK+SIX(TWO),SAVE+ONE(ONE) GET YEAR
         OI    WORK+SEVEN,HEX0F    CREATE A + SIGN
         UNPK  DATE+SIX(TWO),WORK+SIX(TWO) UNPACK YEAR (PRINTABLE)
         CVB   YEAR,WORK           GET YEAR IN BINARY
         SR    REMAIN,REMAIN       CLEAR REG FOR DIVIDE
         LR    QUOTIENT,YEAR       GET YEAR IN DIVIDE REGISTER
         LA    DIVISOR,FOUR        DIVIDE BY 4 TO CHK FOR LEAP
         DR    DIVIDEND,DIVISOR    DIVIDE YEAR
         LA    TABLBASE,NOT        ASSUME NOT-LEAP TABL ADR-2
         LTR   REMAIN,REMAIN       LEAP YEAR?
         BNZ   DATE0600            NO, ASSUMPTION CORRECT
         LA    TABLBASE,LEAPTABL-TWO  YES, USE LEAP-YEAR TABLE
*                                  ADDRESS MINUS 2
DATE0600 EQU   *
         MVC   WORK+SIX(TWO),SAVE+TWO  GET DAY
         CVB   DAYS,WORK           CONVERT TO BINARY
         LA    INDEX,TBLENGTH      SET INDEX REG TO TABLE LNGTH
DATE0700 EQU   *
         LH    ENTRY,ZERO(INDEX,TABLBASE) GET ENTRY FROM TABLE
         CR    DAYS,ENTRY          IS THIS THE CORRECT ENTRY?
         BNH   DATE0900            NO, DECR INDEX & KEEP SEARCHING
         SR    DAYS,ENTRY          YES, GET DAY IN THIS MONTH
         CVD   DAYS,WORK           CONVERT  DAY TO PACKED DEC
         UNPK  DATE+THREE(TWO),WORK+SIX(TWO) UNPACK DAY
         OI    DATE+FOUR,HEXF0     MAKE DAY PRINTABLE
         SPACE
         SRL   INDEX,ONE           GET MONTH FROM INDEX REG
         CVD   MONTH,WORK          CONVERT MONTH TO PACKED DEC
         UNPK  DATE(TWO),WORK+SIX(TWO) UNPACK MONTH
         OI    DATE+ONE,HEXF0      MAKE IT PRINTABLE
         L     R0,DATE             PUT DATE - MM/D IN R0
         L     R1,DATE+FOUR        PUT DATE - D/YY IN R1
         ST    R0,TOTWKSPC         SAVE R0 IN CASE BRANCH TO 'M2' DONE
         SR    R15,R15             SET ZERO RETURN CODE IN R15
         XC    TOTRTCOD,TOTRTCOD   CLEAR RETURN CODE IN OLTCB
         TM    $OLTFLGS,$TRACE     IS A MACRO TRACE REQUESTED?
         BO    DATE0800            YES CALL TRACE MODULE
         L     R13,FOUR(R13)       RESTORE CALLERS SAVE REGISTER
         L     R14,TWELVE(R13)     RESTORE REGISTER 14
         MVC   TWENTY(EIGHT,R13),DATE  PUT DATE IN 'SAVER' REGISTERS
         LM    R0,R12,TWENTY(R13)  RESTORE CALLERS REGISTERS
         BR    R14                 RETURN TO CALLER
*
DATE0800 DS    0H
         L     R13,FOUR(R13)       RESTORE CALLERS SAVE REGISTER
         L     R14,TWELVE(R13)     RESTORE REGISTER 14
         MVC   TWENTY(EIGHT,R13),DATE  PUT DATE IN 'SAVER' REGISTERS
         USING RESPL,R5            SETUP ADDRESIBILITY OF RESPL@Y17XAU2
         L     R5,TOTRESPL         FIND START OF RESPL         @Y17XAU2
         L     R15,RESPLM2         GET ENTRY TO IEDQWM2        @Y17XAU2
         LM    R2,R12,DISREG2(R13) RESTORE REGISTERS           @Y17XAUU
         BR    R15                 GO TO IEDQWM2               @Y17XAU2
         DROP  R5                                              @Y17XAU2
         SPACE 2
DATE0900 EQU   *
         LA    R8,TWO              DECREMENT VALUE
         SR    INDEX,R8            POINT TO NEXT ENTRY DOWN
         B     DATE0700            GO CHECK OUT THIS ENTRY
         EJECT
R0       EQU   0                   REGISTER 0
R1       EQU   1                   REGISTER 1
R2       EQU   2                   REGISTER 2
R3       EQU   3                   REGISTER 3
R4       EQU   4                   REGISTER 4
R5       EQU   5                   REGISTER 5
R6       EQU   6                   REGISTER 6
R7       EQU   7                   REGISTER 7
R8       EQU   8                   REGISTER 8
R9       EQU   9                   REGISTER 9
R10      EQU   10                  REGISTER 10
R11      EQU   11                  REGISTER 11
R12      EQU   12                  REGISTER 12
R13      EQU   13                  REGISTER 13
R14      EQU   14                  REGISTER 14
R15      EQU   15                  REGISTER 15
DISREG2  EQU   28                  REG 2 DISPLACEMENT IS SAVE  @Y17XAUU
REMAIN   EQU   10                  REGISTER 10
QUOTIENT EQU   11                  REGISTER CONTAINING QUOTIENT
DAYS     EQU   10                  REGISTER 10
DIVIDEND EQU   10                  REGISTER 10
YEAR     EQU   5                   REGISTER 5
TABLBASE EQU   5                   REGISTER 5
DIVISOR  EQU   6                   REGISTER 6
MONTH    EQU   6                   REGISTER 6
INDEX    EQU   6                   REGISTER 6
ENTRY    EQU   7                   REGISTER 7
BASEREG  EQU   4                   BASE REGISTER
ZERO     EQU   0                   ZERO
ONE      EQU   1                   ONE
TWO      EQU   2                   TWO
THREE    EQU   3                   THREE
FOUR     EQU   4                   FOUR
SIX      EQU   6                   SIX
SEVEN    EQU   7                   SEVEN
EIGHT    EQU   8                   EIGHT
TWELVE   EQU   12                  TWELVE
D16      EQU   16                  CONSTANT DECIMAL 16
TWENTY   EQU   20                  TWENTY
HEXF0    EQU   X'F0'               TO MAKE LAST CHAR PRINTABLE
HEX0F    EQU   X'0F'               SET + SIGN BIT
         EJECT                                                 @Y17XAU2
*                                  CALLER
LEAPTABL DC    AL2(0,31,60,91,121,152,182,213,244,274,305,335)  LEAP
*                                  YEAR TABLE
NOT      EQU   *-2                 ADDR OF NOT LEAP TBL-2 (BASE)
LENGTH   EQU   *                   ADDR AT BEG OF TABLE
NONLPTBL DC    AL2(0,31,59,90,120,151,181,212,243,273,304,334)  NON-
*                                  LEAP YEAR TABLE
TBLENGTH EQU   *-LENGTH            LENGTH OF TABLE
DATE1    DC    C'00/00/00'         FORMAT OF DATE              @Y17XAUU
PATCH    DC    100X'00'            MAINTENANCE AREA
         EJECT
RESPL    RESPL                                                 @Y17XAU2
         EJECT                                                 @Y17XAU2
         OLTCB
TBEG     DS    0H                                              @Y17XAU2
WORK     DS    D                   WORK AREA                   @Y17XAU2
SAVE     DS    F                   SAVE AREA FOR UNPACKING DATE@Y17XAU2
DATE     DS    CL8                 FORMAT OF DATE RETURNED TO  @Y17XAU2
*                                  CALLER                      @Y17XAU2
TEND     DS    0H                                              @Y17XAU2
         EJECT                                                 @Y17XAU2
         END
