         TITLE 'IEDQWB1 TRM ANALYSIS BUFFER ANALYZER'
IEDQWB1  CSECT
*A000000-999999                                                @Y16X5U0
* CHANGE ACTIVITY AS FOLLOWS:                                    S22024
*A211000,249500-249800,538070-539750,730570,731820              SA41592
*A680600                                                        SA59008
*A246000,520000                                                 SA41604
*A249400,280000,681910                                          SA41614
*A004500,010200-011600,231000,281070,624000-625600,637000        S22024
*A681247,681526-681528,681707,730570,730810,731820-731900        S22024
*C016000,280100-280140,280160-280180,280200-280240               S22024
*C681495-681499,681501-681503,681505-681507,681585,681720        S22024
*C681812,696000-697600,730630,730840,731680                      S22024
*D280350-280400,474000-486000,681395,681645-681655,730120-730300 S22024
*C280260-280280,280300-280340,644000,646000,681420,681425        S22024
*C686200                                                         S05331
*A681291-681292,681387-681389                                    S05331
*A442000                                                       @XA06325
*D358000-370000,378000,440000                                  @XA06325
*C280220-280280,396000-416000,444000                           @XA06325
*C681470-681495                                                @YA07679
*A731740                                                       @YA07679
*C681478                                                       @YA10430
*A249800,338000,340000,489000,496000,624400,687200,730480      @Y17XAUU
*A731760                                                       @Y17XAUU
*D280800-280900,731740                                         @Y17XAUU
*D312000-336000,350000-352000                                  @OY20346
         SPACE
****************************************************************
*                                                              *
* MODULE NAME = IEDQWB1 (TCAM,TOTE)                            @Y17XAUU
*                                                              * S22024
* DESCRIPTIVE NAME = TRM ANALYSIS BUFFER ANALYZER              * S22024
*                                                              * S22024
* COPYRIGHT = NONE                                             * S22024
*                                                              * S22024
* STATUS --                                                    *
*                                                              *
*    VERSION 10.0                                              @Y17XAUU
*                                                              *
* FUNCTIONS --                                                 *
*                                                              *
*    THE PURPOSE OF THIS MODULE IS TO OBTAIN THE TRM FROM      *
*    THE TCAM BUFFER AND RETURN THE BUFFER BACK TO TCAM.       *
*                                                              *
*    IF THE TRM WAS ENTERED FROM THE SYSTEM CONSOLE, THE       *
*    BUFFER IS POSTED TO TCAM.                                 *
*                                                              *
*    IF THE TRM WAS ENTERED FROM A REMOTE TERMINAL, THE        *
*    LCB OR BUFFER IS POSTED TO TCAM.                          @Y17XAUU
*                                                              @Y17XAUU
* PURPOSE -- SEE FUNCTION                                      @Y17XAUU
*                                                              @Y17XAUU
* LINKAGE -- ATTACHED BY IEDQWB OR XCTL'ED TO BY IEDQWC.       @Y17XAUU
*                                                              @Y17XAUU
* MACROS -- IEDHJN                                             @Y17XAUU
*           XCTL                                               @Y17XAUU
*           SAVE                                               @Y17XAUU
*           AQCTL                                              @Y17XAUU
*           WTO                                                @Y17XAUU
*           LINK                                               @Y17XAUU
*           IEDQMSG                                            @Y17XAUU
*           TCONV                                              @Y17XAUU
*           BLDL                                               @Y17XAUU
*           LOAD                                               @Y17XAUU
*           DELETE                                             @Y17XAUU
*           WTOR                                               @Y17XAUU
*           STIMER                                             @Y17XAUU
*           WAIT                                               @Y17XAUU
*           TTIMER                                             @Y17XAUU
*           POST                                               @Y17XAUU
*           ENQ                                                @Y17XAUU
*                                                              *
* ENTRY POINTS                                                 *
*                                                              *
*    IEDQWB1                                                   @Y17XAUU
*                                                              *
* INPUT --                                                     *
*                                                              *
*    REGISTERS 02,13,14,15 CONTAIN THE FOLLOWING VALUES:       *
*                                                              *
*    02 - OLTCB POINTER                                        *
*    13 - SAVE AREA ADDRESS                                    *
*    14 - RETURN ADDRESS                                       *
*    15 - ENTRY POINT ADDRESS                                  *
*                                                              *
* OUTPUT --                                                    *
*                                                              *
*    REGISTERS 0 AND 1 CONTAIN PARAMETERS FOR IEDQWC           *
*                                                              *
* EXTERNAL REFERENCES --                                       *
*                                                              *
*    NONE                                                      *
*                                                              *
* EXITS,NORMAL                                                 *
*                                                              *
*    XCTL TO IEDQWH (NUMERICAL ANALYSIS MODULE) IF TRM WAS     *
*    ENTERED FROM A NUMERIC-ONLY TERMINAL.                     *
*                                                              *
*    XCTL TO IEDQWC (TRM ANALYSIS MODULE I) NORMALLY.          *
*                                                              *
* EXITS,ERROR --                                               *
*                                                              *
*    NONE                                                      *
*                                                              *
* TABLES/WORK AREAS --                                         *
*                                                              *
*    CVT, AVT, OLTCB, LCB, SCB                                 *
*                                                              *
* ATTRIBUTES --                                                *
*                                                              *
*    ENABLED, PROBLEM PROGRAM MODE, TRANSIENT                  *
*                                                              *
* NOTES -- SEE BELOW                                           @Y17XAUU
*                                                              @Y17XAUU
*  DEPENDENCIES -- EBCDIC CHARACTER CODE                       @Y17XAUU
*                                                              @Y17XAUU
*  RESTRICTION -- NONE                                         @Y17XAUU
*                                                              @Y17XAUU
*  REGISTER CONVENTIONS - SEE REGISTER ASSIGNMENT              @Y17XAUU
*                                                              @Y17XAUU
*  PATCH LABEL -- PATCH                                        @Y17XAUU
*                                                              *
****************************************************************
         EJECT
*                                                              *
*        E Q U A T E S                                         *
*                                                              *
R0       EQU   0                  REG 0
R1       EQU   1                  REG 1
R2       EQU   2                  REG 2
R3       EQU   3                  REG 3
R4       EQU   4                  REG 4
R5       EQU   5                  REG 5
R6       EQU   6                  REG 6
R7       EQU   7                  REG 7
R8       EQU   8                  REG 8
R9       EQU   9                  REG 9
R10      EQU   10                 REG 10
R11      EQU   11                 REG 11
R12      EQU   12                 REG 12
R13      EQU   13                 REG 13
R14      EQU   14                 REG 14
R15      EQU   15                 REG 15
         SPACE
D0       EQU   0                  DISP OF 0
D1       EQU   1                  DISP OF 1
D2       EQU   2                  DISP OF 2
D3       EQU   3                  DISP OF 3
D4       EQU   4                  DISP OF 4
D5       EQU   5                  DISP OF 5
D6       EQU   6                  DISP OF 6
D7       EQU   7                  DISP OF 7
D8       EQU   8                  DISP OF 8
D9       EQU   9                  DISP OF 9
D10      EQU   10                 DISP OF 10
D11      EQU   11                 DISP OF 11
D12      EQU   12                 DISP OF 12
D13      EQU   13                 DISP OF 13
D14      EQU   14                 DISP OF 14
D15      EQU   15                 DISP OF 15
D16      EQU   16                 DISP OF 16
D17      EQU   17                 DISP OF 17
D18      EQU   18                 DISP OF 18
D19      EQU   19                 DISP OF 19
D20      EQU   20                 DISP OF 20
D24      EQU   24                 DISP OF 24
D28      EQU   28                 DISP OF 28
D32      EQU   32                 DISP OF 32
D36      EQU   36                 DISP OF 36
D40      EQU   40                 DISP OF 40
D80      EQU   80                 DISP OF 80
         SPACE
XF0      EQU   X'F0'              LCB MASK FOR ANR LOCAL
ANR      EQU   X'1E'              REQ TERMINAL EQU ANR LOCAL
LGBA     EQU   X'80'              LGB MAP                        S22024
BTURSP   EQU   X'18'              OLTT BTU                       S22024
MULBUF   EQU   X'14'              MULTIPLE BUFFER PREFIX         S22024
TRMCONS  EQU   X'0C'              REQ TERMINAL EQU SYSCON
TRMSCCT  EQU   X'10'              REQ TERMINAL EQU SEC. SYSCON
TRMPRI   EQU   X'E7'              LCB PRIORITY FLAG
XBLANK   EQU   X'61'              A SLASH CHAR
ECBCODE  EQU   0                  ECB CODE
TRMAX    EQU   72                 MAXIMUM TRM LENGTH
NUMEN    EQU   X'80'              NUMERIC TRM ENTRY
TPOSTE   EQU   X'80'              END OF LIST FLAG
TPOSTA   EQU   X'0C'              FIRST ELEMENT FLAG            SA41604
POSTLCB  EQU   X'0C'              POST LCB FLAG
CAP      EQU   X'40'              FACTOR FOR CAPITALIZATION
YES      EQU   X'E8'              CHAR 'Y'
PARMREG0 EQU   0                  PARAMETER REG 0
PARMREG1 EQU   1                  PARAMETER REG 1
RESDP    EQU   X'20'              DEVICE PROTECTED
CDSLEN   EQU   255                CDS BUFFER LENGTH
D64      EQU   64                 DISP /F 64
XFF      EQU   X'FF'              MASK SETTING                  SA41614
TRMTERM  EQU   X'10'              PRFKEY SETTING FROM OPERATOR  SA41592
*                                   CONTROL TERMINAL            SA41592
LUTYPE   EQU   X'1C'              SOURCE IS AN LU              @Y17XAUU
         EJECT
         USING IEDQWB1,R15
IEDQWB1  IEDHJN STARB1,HJN
         DROP  R15
         SPACE
         SAVE  (14,12)            SAVE CALLER'S REGISTERS
         SPACE
         LR    R6,R15             SET BASE REG
         USING IEDQWB1,R6
         USING TOTOLTCB,R2
         SPACE
         LA    R3,TOTSAVE4        GET MY SAVE AREA ADDR
         ST    R3,D8(R13)         STORE IN CALLER'S SAVE AREA
         ST    R13,D4(R3)         SAVE CALLER'S SAVE ADDR
         ST    R13,TOTRESSV       SAVE MOTHER TASK SAVE AREA ADDR
         LR    R13,R3             SET REG 13 TO SAVE AREA
         SPACE
         TM    TOTFLG04,TOTNUMDV  NUMERIC ENTRY                 SA41614
         BO    NUMERIC            YES                           SA41614
         SPACE
**************************************************************** S22024
*                                                              * S22024
*        SECOND PASS CHECK. EXIT IS TO 'DEVPRT'.               * S22024
*                                                              * S22024
**************************************************************** S22024
         SPACE
         TM    TOTFLG01,TOTPRMPT+TOTCONFG PROMPT OR CONFIG REQ @XA06325
         BNZ   DEVPRT                   BRANCH IF YES          @XA06325
         TM    TOTFLG02,TOTOLTRQ  ON-LINE TEST REQUEST           S22024
         BO    DEVPRT             YES- CHECK FOR DEVICE PROTECT  S22024
*                                 ELSE ASSUME ENTRY IS FROM QWB  S22024
         SPACE
***********************************************************************
*                                                                     *
*        INITIALIZATION OF THE OLTCB                                  *
*                                                                     *
***********************************************************************
         SPACE 1
         LA    R5,TOTINBUF        GET TOTE INPUT BUFFER ADDRESS
         ST    R5,TOTINADR-D1       AND STORE IN WTOR PARM LIST
         MVI   TOTINCNT,D80       SET TOTE INPUT COUNT TO 80
         MVI   $DRIVER,$TOTE           SET TOTE ID               S22024
         LA    R5,TOTOTECB        GET TOTE SUBTASK ECB ADDRESS
         ST    R5,TOTINECB          AND STORE IN WTOR PARM LIST
         ST    R5,TOTECBPT        SAVE ECB ADDRESS
         MVI   TOTECBPT,D80       SET HI ORDER FLAG
         OI    TOTFLG06,TOTNPERR  SET NO PERMANENT ERROR FLAG
         OI    TOTFLG08,D3        SET DRPINT DEFAULT LEVEL
         LA    R5,TOTPASS         GET ADDRESS OF PASSON DATA BUFFER
         ST    R5,$PASS           PUT IT IN OLT SCT
         XC    D0(D64,R5),D0(R5)  CLEAR PASSON DATA BUFFER
         LA    R5,TOTEXT          GET ADDRESS OF EXTERNAL DATA BUFFER
         ST    R5,$EXT            PUT IT IN OLT SCT
         ST    R2,TOTOLTPL        STORE OLTCB ADDRESS
         LA    R5,TOTPLNKQ        GET ADDRESS OF PLINK MODULE QUEUE
         ST    R5,TOTPLFWD        PUT IT IN FORWARD POINTER
         ST    R5,TOTPLBKW        PUT IT IN BACKWARD POINTER
         SPACE 2
         SPACE 2
****************************************************************
*                                                              *
*        ROUTINE TO GET AND FIND SHADE OF TRM                  *
*                                                              *
****************************************************************
         SPACE
         L     R12,TOTAVTPT       AVT ADDR
         USING IEDQAVTD,R12
         MVI   TOTOTECB,ECBCODE   CLEAR ECB CODE
         L     R11,TOTOLTMQ       GET ADDRESS OF TCAM BUFFER
         SPACE
         LR    R5,R11             SAVE ADDR OF HEADER BUFFER
         USING IEDQPRF,R5
         ST    R5,WORK5           SAVE HEADER ADDRESS
RES0715  EQU   *
         MVC   TOTSRLCB(D4),PRFLCB-D1 SAVE SOURCE LCB ADDRESS  @Y17XAUU
         MVC   TOTSROFF(D2),PRFSRCE SAVE INDEX TO TERMNAME TAB @Y17XAUU
         MVC   TRMSRCE(D2),PRFSRCE SAVE INDEX TO TERMNAME TABLE
         CLI   PRFKEY,TRMCONS     REQ TERMINAL EQU SYSCON      @Y17XAUU
         BE    RES0717            YES                          @Y17XAUU
         SPACE
RES0717  EQU   *
         MVC   TRMKEY(D1),PRFKEY  SAVE REQUEST TERMINAL FLAG
         LH    R1,AVTKEYLE        GET UNIT LENGTH
         SR    R15,R15            INITIALIZE REG
         LR    R10,R5             SAVE BUFFER ADDR
         LA    R3,TOTTRMBF        POINT TO TRM BUFFER
         LA    R14,PRFSUNIT       GET START OF 1ST UNIT THIS BUFFER
         SH    R1,PRFSCAN         SUBTRACT RESERVE CHAR
         AH    R14,PRFSCAN        BUMP R14 PAST RESERVE CHAR
         IC    R15,PRFNBUNT       GET NUMBER OF UNITS
         SPACE
RES080   EQU   *
         LH    R9,PRFSIZE         GET DATA SIZE FROM BUFFER    @XA06325
         SH    R9,PRFSCAN         GET LENGTH OF TRM DATA       @XA06325
         LA    R8,TRMAX           SET MAXIMUM TRM BUFFER SIZE  @XA06325
         CR    R8,R9              WAS MORE THAN MAX ENTERED?   @XA06325
         BNL   RES082A            NO, BRANCH                   @XA06325
         LR    R9,R8              TRUNCATE TRM TO MAX SIZE     @XA06325
RES082A  EQU   *                                               @XA06325
         CR    R1,R9              IS ALL TRM DATA IN THIS UNIT @XA06325
         BL    RES082             NO, BRANCH                   @XA06325
         BCTR  R9,R0              DECREMENT COUNT FOR EXECUTE  @XA06325
         EX    R9,TRMMOVE         MOVE DATA                    @XA06325
         B     RES089             EXIT                         @XA06325
         SPACE
RES082   EQU   *
         BCTR  R1,R0              DECREMENT COUNT FOR EXECUTE
         EX    R1,TRMMOVE         MOVE BUFFER UNIT
         LA    R3,D1(R1,R3)       POINT TO NEW BUFFER LOCATION
         BCT   R15,RES088         ANY MORE UNITS IN THIS BUFFER
         B     RES089             NO - EXIT
         SPACE
RES088   EQU   *
         L     R14,PRFTIC         GET ADDRESS OF NEXT UNIT
         LR    R5,R14             SAVE IT
         SR    R9,R1              DETERMINE NUMBER OF DATA     @XA06325
*                                   BYTES LEFT TO BE MOVED     @XA06325
         LH    R1,AVTKEYLE        GET UNIT LENGTH              @XA06325
         CR    R1,R9              IS REMAINDER OF TRM IN THIS  @XA06325
*                                   UNIT?                      @XA06325
         BL    RES082             NO, MOVE ENTIRE UNIT         @XA06325
         LR    R1,R9              YES, MOVE DATA THAT REMAINS  @XA06325
         B     RES082A            BRANCH TO MOVE DATA          @XA06325
         SPACE
RES089   EQU   *
         CLC   TOTTRMBF(D2),NUMER NUMERIC ENTRY
         BNE   RES089A            NO
         OI    NUMFLG,NUMEN       SHOW NUMERIC ENTRY
         DROP  R5
         SPACE
RES089A  EQU   *
         L     R10,WORK5          GET HEADER ADDR
         USING IEDQPRF,R10
         SPACE
*                                                              *
*        PREPARE TO POST BUFFER TO TCAM                        *
*                                                              *
         CLI   TRMKEY,BTURSP      OLTT BTU RESPONSE              S22024
         BE    RES089B1           YES - ERROR BUFFER             S22024
         CLI   TRMKEY,TRMTERM     IS REQUEST FROM A SECONDARY   SA41592
*                                   OPERATOR CONTROL TERMINAL?  SA41592
         BE    RES089B1           YES, GO FREE BUFFER           SA41592
         CLI   TRMKEY,LUTYPE      SOURCE IS AN LU              @Y17XAUU
         BE    RES089B1           YES - POST BUFFER ONLY       @Y17XAUU
         CLI   TRMKEY,MULBUF      MULTIPLE BUFFERS               S22024
         BE    RES089B1           YES - POST BUFFER ONLY         S22024
         SPACE
         CLI   TRMKEY,TRMCONS     TRM ENTERED FROM SYSCON      @Y17XAUU
         BNE   RES089D            NO
         OI    TOTFLG05,TOTSRCON  SET TRM ENTERED FROM SYSTEM  @Y17XAUU
*                                 CONSOLE FLAG                 @Y17XAUU
         SPACE
*                                                              *
*        TRM ENTERED FROM SYSCON                               *
*                                                              *
RES089B  EQU   *
RES089B1 EQU   *                                                SA41592
         LA    R7,AVTBFRTB        GET ADDR OF BUFFER RETURN QCB
         ST    R7,PRFKEY          PUT IT IN BUFFER BEING RETURNED
         MVI   PRFPRI,PRIBFRTB    PUT IN PRIORITY
         ST    R10,TPOSTPAR+D4    SET UP POST
         MVC   TPOSTPAR+D1(D3),TPOSTPAR+D5 ELEMENT
         MVI   TPOSTPAR+D4,TPOSTE SHOW LAST BUFFER
         MVI   TPOSTPAR,TPOSTA    SET FLAG                      SA41604
         LA    R1,TPOSTPAR        POINT TO PARAMETER LIST
*
         AQCTL
         B     RESEXIT            EXIT
         SPACE 2
RES089D  EQU   *
*                                                              *
*        TRM ENTERED FROM REMOTE TERMINAL                      *
*                                                              *
         L     R7,PRFLCB-D1       GET REQ LINE LCB ADDR
         ST    R7,REQLCB          SAVE IT
         USING IEDQLCB,R7
         MVC   LCBLSPCI(D3),TOTOLTMQ+D1 PUT IN BUFFER ADDR
         MVI   LCBPRI,TRMPRI      SET PRIORITY
         L     R8,LCBSCBA-D1      GET SCB ADDR
         LA    R8,D0(R8)          CLEAR HIGH ORDER BYTE
         USING IEDQSCB,R8
         LA    R4,AVTBFRTB        GET ADDR OF BFR RETURN QCB
         ST    R4,WORK2           SAVE IN WORK AREA
         MVC   SCBDESTQ(D3),WORK2+D1 PUT IT IN SCB
         L     R4,SCBMACR-D1      GET ADDR OF BFR HDR ENTRY
         LA    R4,D0(R4)          CLEAR HI ORDER BYTE
         LA    R3,AVTINOUT        INEND/OUTEND PLIST ADDR
         ST    R3,DPARM           SAVE IT IN WORK AREA
         MVC   SCBMACR(D3),DPARM+D1  PUT IN SCB
         DROP  R8
         L     R8,AVTMSGS-D1      GET VCON TABLE ADDR
         LA    R8,D0(R8)          CLEAR HI ORDER BYTE
         L     R8,D0(R8)          GET BD QCB ADDRESS
         ST    R8,WORK2           SAVE IT
         CLI   LCBRSKEY,ANR       REQ TERM = ANR LOCAL
         BNE   RES089I            NO
         MVI   LCBTSTSW,XF0       SET LCB FLAG FOR ANR LOCAL
         SPACE
RES089I  EQU   *
         MVC   LCBQCBA(D3),WORK2+D1 PUT QCB ADDR FOR BD IN LCB
         ST    R7,TPOSTPAR        PUT LCB ADDR IN PLIST
         ST    R7,TPOSTPAR+D4     PUT LCB ADDR IN PLIST
         MVI   TPOSTPAR,POSTLCB   SET FLAG
         MVI   TPOSTPAR+D4,TPOSTE SHOW LAST ELEMENT
         LA    R1,TPOSTPAR        GET ADDRESS OF ELEMENT
         DROP  R7
*
         AQCTL                    TPOST BUFFER TO BD
         SPACE
         L     R3,REQLCB          GET REQ LINE LCB ADDR
         USING IEDQLCB,R3
         L     R9,LCBDCBPT        GET DCB ADDR
         USING IHADCB,R9
         TM    DCBDSORG,LGBA      LGB VS. DCB                    S22024
         BZ    RES090             NO                             S22024
         LH    R1,TOTSROFF        GET SOURCE TTCIN             @Y17XAUU
         L     R15,AVTRNMPT       TTCIN TO TTE CONVERSION      @Y17XAUU
*                                 ROUTINE ADDRESS              @Y17XAUU
         BALR  R14,R15            GET SOURCE TTE ADDRESS       @Y17XAUU
         BAL   R14,NCPTTE         GET NCP TTE ADDRESS          @Y17XAUU
*                                                              @Y17XAUU
*        ON RETURN R1 CONTAINS ADDRESS OF NCP TTE PREFIX       @Y17XAUU
*                                                              @Y17XAUU
         USING IEDNTRM,R1         SET TTE ADDRESSABILITY       @Y17XAUU
         L     R1,TRMDESTQ-D1     GET QCB ADDRESS              @Y17XAUU
         DROP  R1                                              @Y17XAUU
         USING IEDQQCB,R1                                      @Y17XAUU
         L     R9,QCBDCBAD-D1     GET 3705 DCB ADDRESS         @Y17XAUU
         DROP  R1                                              @Y17XAUU
RES090   EQU   *                                                 S22024
         SR    R15,R15            CLEAR REG
         IC    R15,LCBUCBX        GET RLN
         DROP  R3
         LA    R14,D1(R15)        ADJUST RLN
         L     R8,DCBDEBAD        PICK UP DEB ADDR
         DROP  R9                                                S22024
         SLL   R15,D2             SET DISPLACEMENT
         L     R8,D32(R15,R8)     PICK UP UCB ADDR FOR RQ LINE
         LA    R8,D0(R8)          CLEAR HI ORDER BYTE
         STH   R8,TOTCTUCB        SAVE RQ UCB ADDR               S22024
         STC   R14,TOTCTRLN       SAVE RLN                       S22024
         EJECT
RESEXIT  EQU   *
         LA    R4,TOTTRMBF        SET TO XLATE TO UPPER CASE
         LA    R7,D9              SET LOOP COUNT
RESTRAN  EQU   *
         OC    D0(D8,R4),ORFLD    XLATE 8 BYTES
         LA    R4,D8(R4)          POINT TO NEXT FIELD
         BCT   R7,RESTRAN         XLATE NEXT FIELD
         SPACE 4
*                                                              *
*                                                              *
*        SET UP PARAMETERS TO PASS TO IEDQWC                   *
*                                                              *
*                                                              *
         XC    PLIST(D4),PLIST    CLEAR PARAMETER FIELD
         MVC   PLIST(D1),NUMFLG   GET FLAG BYTE
         MVC   PLIST+D2(D2),TRMSRCE GET INDEX TO TERMNAME TABLE
         L     R1,PLIST
         L     R13,D4(R13)        RESTORE CALLER'S SAVE
         LM    R14,R0,D12(R13)    RESTORE REG                   SA59008
         XCTL  (2,12),EP=IEDQWC   GO TO IEDQWC
         EJECT
****************************************************************
*        ROUTINE TO LOAD CDS RECORDS FOR CONTROL TERMINAL AND  *
*        ALTERNATE PRINTER TO DETERMINE IF THE DEVICE PROTECT  *
*        FEATURE IS SET. IF THE OPTION IS SET THE SYSTEM       *
*        OPERATOR WILL BE REQUESTED TO GIVE AUTHORIZATION      *
*        FOR USE OF TERMINALS.                                 *
****************************************************************
DEVPRT   EQU   *
         TM    $ERROPT,$ALTPRNT   AP OPTION SET
         BZ    DEVCT              NO - CHECK CT
         TM    TOTFLG09,TOTAPCON+TOTAPOUT AP EQU SYSTEM DEVICE
         BNZ   DEVCT              YES - CHECK CT
         SPACE
         MVC   READNAME(D8),TOTAPNAM GET AP SYMBOLIC NAME
         BAL   R5,CDSCHK          LOAD CDS
         SPACE
DEVCT    EQU   *
         MVI   READNAME,CAP       BLANK OUT NAME FIELD           S05331
         MVC   READNAME+D1(D7),READNAME  IN BLDL LIST            S05331
         TM    TOTFLG09,TOTCTCON  CT EQU SYSTEM CONSOLE
         BO    RESEND             YES - EXIT
         LA    R4,TOTCTNAM        CT NAME
         LA    R7,D0              INIT COUNTER
DEVCT4   EQU   *
         CLI   D0(R4),XBLANK      END OF CT NAME
         BE    DEVCT12            YES
         LA    R7,D1(R7)          INCREMENT COUNTER
         LA    R4,D1(R4)          NEXT CHAR
         B     DEVCT4             CHECK NEXT CHAR
DEVCT12  EQU   *
         BCTR  R7,R0              DECREMENT COUNT FOR EXECUTE
         EX    R7,CTNAME          MOVE CT NAME TO BLDL PLIST
         BAL   R5,CDSCHK          LOAD CDS
         SPACE
****************************************************************
*        EXIT TO APPROPRIATE MODULE                            *
****************************************************************
RESEND   EQU   *
         MVI   READNAME,CAP       BLANK OUT NAME FIELD           S05331
         MVC   READNAME+D1(D7),READNAME  IN BLDL LIST            S05331
         BAL   R5,OLTACT          SEND OLT ACTIVE MSG            S22024
         L     R13,D4(R13)        CALLER'S SAVE AREA
         LM    R14,R0,D12(R13)    RESTORE REGS
         TM    TOTFLG01,TOTCONFG  CONFIGURATOR REQUEST
         BO    RESCON             YES - EXIT
         BAL   R3,CDSENQ          ENQ CDS DATA SETS              S22024
         TM    TOTFLG01,TOTPRMPT  PROMPT REQUEST                 S22024
         BO    RESPROM            YES - EXIT                     S22024
         SPACE
         XCTL  (2,12),EP=IEDQWC1  CONTINUE TRM PROCESSING
         SPACE
RESCON   EQU   *
         XCTL  (2,12),EP=IEDQWI   GO TO CONFIGURATOR
         SPACE
RESPROM  EQU   *
         XCTL  (2,12),EP=IEDQWJ         GO TO PROMPTER
         EJECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *@YA07679
*                                                             *@YA07679
*              SEND ON-LINE TESTING ACTIVE MESSAGE            *@YA07679
*                                                             *@YA07679
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *@YA07679
OLTACT   EQU   *                                               @YA07679
         IEDQMSG MSGID=002,FUNCT=CEC   ISSUE THE MESSAGE       @YA07679
         MVC   TRMCTID(D2),TOTOLTID
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         ST    R2,TMID+16
         TCONV FROM=TMID+16,TO=TMID+8,COUNT=4,TYPE=HE
         MVC   TMID+16(4),TMID+21
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         SPACE
         IEDQMSG FUNCT=CEC,OTBUF=TMID,OTCNT=TMIDE-TMID
         SPACE
         BR    R5                 RETURN
#EXIT    L     R15,$TABLE
         L     R15,28(R15)
         BR    R15
         EJECT
****************************************************************
*        GET AND EVALUATE CDS RECORD FOR AP/CT.                *
*        CHECK FOR DEVICE PROTECT FEATURE.                     *
****************************************************************
CDSCHK   EQU   *
         BLDL  0,BLDLLIST         BUILD DIRECTOY ENTRY POINT LIST
         SPACE
         LTR   R15,R15            WAS BUILD SUCCESSFUL
         BZ    CDSLOAD            YES
         CH    R15,H4             RECORD FOUND
         BE    D0(R5)             NO - EXIT                      S22024
****************************************************************
*        ASSUME I/O ERROR                                      *
****************************************************************
         IEDQMSG MSGID=041,FUNCT=CEC,LINK=YES
         SPACE
CDSERR   EQU   *
         L     R13,D4(R13)        CALLER'S SAVE AREA
         OI    TOTFLG06,TOTOTERM  SET TERMINATE FLAG             S22024
         LM    R14,R1,D12(R13)    RESTORE REGISTERS 14-1         S22024
         XCTL  (2,12),EP=IEDQWE   TERMINATE TRM                  S22024
         SPACE
         SPACE
CDSLOAD  EQU   *
         LOAD  DE=READNAME        LOAD THE ENTRY
         SPACE
         LR    R8,R0              ENTRY POINT ADDR
         MVC   CDSBFR(CDSLEN),D0(R8) SAVE ENTRY
         DELETE DE=READNAME       DELETE THE ENTRY
         SPACE
         TM    CDSBFR+D10,RESDP   DEVICE PROTECTED
         BZ    D0(R5)             NO - RETURN
CDSDVP   EQU   *                                                 S22024
         MVC   DEVNAM(D8),READNAME PUT NAME IN MSG
         MVC   MSG+D16(D8),READNAME MOVE NAME TO MSG
         WTO   'IED213D CAN ON LINE TEST USE PROTECTED DEVICE',        *
               ROUTCDE=10,DESC=7
         SPACE
         XC    ECB(D4),ECB        CLEAR ECB
         SPACE
MSG      WTOR  '          (YES OR NO)',ANSWER,3,                       *
               ECB,ROUTCDE=(8,10),DESC=2
         SPACE
         STIMER REAL,NOANS,DINTVL=TIMOUT SET TIMER FOR TIMEOUT
         SPACE
         WAIT  ECB=ECB            WAIT FOR REPLY
         SPACE
         TTIMER CANCEL            CANCEL TIMER
         SPACE
         OI    ANSWER,CAP         XLATE RESPONSE TO CAP
         CLI   ANSWER,YES         ANSWER YES
         BE    D0(R5)             YES - RETURN
         SPACE
         MVC   REJMSG+D8(REJECTE-REJECTA),REJECTA MSG TO PLIST   S22024
REJMSG   WTO   '                                                       *
                               ',ROUTCDE=10,DESC=7               S22024
         SPACE
         B     CDSERR             EXIT
         EJECT
****************************************************************
*        TIMER COMPLETION ROUTINE FOR WTOR                     *
****************************************************************
NOANS    EQU   *
         STM   R14,R12,D12(R13)   SAVE CALLER'S REGS
         LR    R4,R15             SET BASE
         USING NOANS,R4
         LA    R12,TRMSAV2        GET MY SAVE AREA ADDR
         ST    R13,D4(R12)        SAVE HIS SAVE AREA ADDR
         ST    R12,D8(R13)        SAVE MY SAVE AREA ADDR
         LR    R13,R12            SET SAVE AREA POINTER
         SPACE
         POST  ECB                POST WTOR ECB
         L     R13,D4(R13)        GET CALLER'S SAVE AREA ADDR
         LM    R14,R12,D12(R13)   RESTORE REGS
         BR    R14                RETURN
         DROP  R4
         SPACE
NUMERIC  EQU   *                                                SA41614
         NI    TOTFLG04,XFF-TOTNUMDV CLEAR NUMERIC ENTRY FLAG   SA41614
         BAL   R5,OLTACT          SEND ACTIVE MSG               SA41614
         L     R13,D4(R13)        RESTORE SAVE REG              SA41614
         LM    R14,R0,D12(R13)    RESTORE REG 14-0              SA41614
         XCTL  (2,12),EP=IEDQWH   EXIT TO NUMERIC HANDLER       SA41614
         EJECT
**************************************************************** S22024
*        ENQUEUE CDS DATA SETS SHARED                          * S22024
**************************************************************** S22024
CDSENQ   DS    0H                 BOUNDARY ALLIGNMENT            S22024
         ENQ   ,MF=(E,ENQLIST)    REQUEST USE OF RESOURCE        S22024
         LTR   R15,R15            WAS IT ASSIGNED                S22024
         BZ    CDSENQE            YES                            S22024
         CLI   D3(R15),D8         WAS IT PREVIOUSLY ASSIGNED     S22024
         BE    CDSENQE            YES                            S22024
         SPACE
         IEDQMSG MSGID=109,FUNCT=CEC CDS DATA SET IN             S05331
*                                 UNSHAREABLE STATE              S05331
         LA    R13,TOTSAVE4       MY SAVE AREA ADDR              S22024
         SPACE
         B     CDSERR             CLEANUP EXIT                   S22024
CDSENQE  DS    0H                 BOUNDARY ALLIGNMENT            S22024
         LM    R14,R1,D12(R13)    RESTORE CALLER REG             S22024
         BR    R3                 RETURN                         S22024
         EJECT
************************************************************** @Y17XAUU
*        ROUTINE TO FIND NCP TTE ADDRESS. INPUT: REG 1 =     * @Y17XAUU
*        TERMINAL TTE ADDRESS. OUTPUT: REG 1 = NCP TTE PREFIX* @Y17XAUU
*        ADDRESS. RETURN TO CALLER VIA REG 14 .        .       @Y17XAUU
************************************************************** @Y17XAUU
NCPTTE   EQU   *                                               @Y17XAUU
         ST    R14,TRMSAVE2       SAVE RETURN ADDRESS          @Y17XAUU
         L     R14,AVTRNMPT       TTCIN TO TTE CONVERSION      @Y17XAUU
*                                 ROUTINE ADDRESS              @Y17XAUU
         LA    R8,TRMPRFSZ        GET TTE PREFIX SIZE          @Y17XAUU
         SLR   R1,R8              POINT TO TTE PREFIX          @Y17XAUU
         USING IEDNTRM,R1         SET TTE ADDRESSABILITY       @Y17XAUU
NCPLOOP  EQU   *                                               @Y17XAUU
         LH    R1,TRMCOHRT        GET COHORT POINTER           @Y17XAUU
         L     R15,AVTRNMPT       TTCIN TO TTE CONVERSION      @Y17XAUU
*                                 ROUTINE ADDRESS              @Y17XAUU
         BALR  R14,R15            FIND TTE ADDRESS             @Y17XAUU
         SLR   R1,R8              POINT TO TTE PREFIX          @Y17XAUU
         CLI   TRMTYPE,TRMLNCP    NCP ENTRY                    @Y17XAUU
         BNE   NCPLOOP            NO - CHECK NEXT ENTRY        @Y17XAUU
         L     R14,TRMSAVE2       RESTORE RETURN ADDRESS       @Y17XAUU
         BR    R14                RETURN TO CALLER             @Y17XAUU
         DROP   R1                                             @Y17XAUU
         EJECT
**************************************************************** S22024
*        C O N S T A N T S                                     * S22024
**************************************************************** S22024
PLIST    DS    F                  PLACE FOR IEDQWC PARAMETERS
REQLCB   DS    F                  REQUEST LINE LCB
*
TPOSTPAR DS    0F                 TPOST PARMLIST                 S22024
         DC    X'0C'              FIRST ELEMENT FLAG             S22024
         DC    AL3(0)             FIRST ELEMENT                  S22024
         DC    X'80'              LAST ELEMENT FLAG              S22024
         DC    AL3(0)             LAST ELEMENT                   S22024
WORK2    DS    F                  WORK AREA
WORK5    DS    F                  WORK AREA
*
PATCH    DC    100XL1'40'         PATCH AREA
*
TRMSRCE  DS    H                  INDEX TO TERMNAME TABLE
TRMTST   DC    H'72'              TRM BUFFER LENGTH
X0100    DC    X'0100'            MASK SETTING FOR SCB
ORFLD    DC    8XL1'40'           BLANK FIELD
NUMER    DC    2CL1'9'            NUMERIC TRM PREFIX
*
THREE    DC    X'03'              VALUE OF 3
NUMFLG   DC    X'00'              PROGRAM FLAG
TRMKEY   DC    X'00'              PROGRAM FLAG
*
ENQLIST  ENQ   (QNAME,RNAME,S,8,STEP),RET=USE,MF=L ENQ PLIST     S22024
         SPACE
QNAME    DC    CL8'CDSLIB'        MAJOR NAME                     S22024
RNAME    DC    CL8'OLT2LIB'       MINOR NAME                     S22024
ENDTEST  TM    THREE,D0           TEST FOR LAST BUFFER UNIT
TRMMOVE  MVC   D0(D0,R3),D0(R14)  MOVE TRM TO BUFFER
DPARM    DS    F                  WORK AREA
         SPACE
ECB      DC    F'0'               ECB
TRMSAV2  DS    18F                TIMER RTN SAVE AREA
TRMSAVE2 DS    F                  REGISTER SAVE AREA           @Y17XAUU
H4       DC    H'4'               VALUE OF 4
         DS    0F                  ALIGN ON FULLWORD             S22024
*                                 THE FOLLOWING MESSAGE GIVES    S22024
*                                 THE ID OF THE OLT RUNNING      S22024
TMID     DC    C'IED335I                   ***CONTROL TERMINAL ID IS' *
         DC    X'40'              BLANK CHAR                     S22024
TRMCTID  DC    CL5'XX***'         MESSAGE TO PRINT CT ID
TMIDE    DS    0C                 END OF MESSAGE
ANSWER   DS    CL3                RESPONSE BUFFER
REJECTA  DC    C'IED345I '        MSG ID                         S22024
*                                 THE FOLLOWING MESSAGE REJECTS  S22024
*                                 THE OLT REQUEST                S22024
REJECT   DC    C'TRM REJECTED - OLT CAN NOT USE PROTECTED DEVICE' MSG
SPACE    DC    X'40'              SPACE
DEVNAM   DC    8XL1'40'           DEVICE NAME
REJECTE  DS    0C                 END OF MESSAGE
TIMOUT   DC    X'F0F0F0F3F0F0F0F0' TIMER FOR WTOR
*
BLDLLIST DS    0F                 BLDL PARAMETER LIST
         DC    XL2'0001'          NUMBER OF LIST ENTRIES
         DC    XL2'003A'          LENGTH OF EACH ENTRY
READNAME DC    8XL1'40'           TERMINAL NAME
         DC    XL16'0'            ADDITIONAL DIRECTORY INFO
CORNEED  DC    XL3'0'             MAIN STORAGE NEEDED FOR ENTRY
         DC    XL31'0'            ADDITIONAL DIRECTORY INFO
CDSBFR   DS    CL256              CDS BUFFER
CTNAME   MVC   READNAME(D0),TOTCTNAM MOVE CONTROL TERM NAME      S22024
ADDRESS  DC    F'0'               ADDRESS OF TERMINAL ENTRY    @YA07679
TERNMTAB DS    A                  SAVE AREA                    @Y17XAUU
         EJECT
         TTRMD                                                 @YA07679
         EJECT
         TLGBD                                                   S22024
         EJECT
         OLTCB
         EJECT
         TQCBD
         EJECT
         TSCBD
         EJECT
         TLCBD
         EJECT
RESPL    RESPL
         EJECT
         TAVTD
         EJECT
         TPRFD
         EJECT
         TPRIOR
         EJECT
DCBDSECT DCBD  DSORG=TX
         END
