         TITLE 'IFG0192A - VSAM OPEN/CLOSE/EOV/II LOAD ROUTINE'
IFG0192A CSECT
*
* MODULE PROLOGUE
***********************************************************************
*                                                                     *
*   MODULE NAME =  IFG0192A                                           *
*                                                                     *
*   DESCRIPTIVE NAME =                                                *
*      VSAM OPEN/CLOSE/EOV/II LOAD ROUTINE                            *
*                                                                     *
*   COPYRIGHT = NONE                                                  *
*                                                                     *
*   STATUS = RELEASE 2, LEVEL 0                               @Y021139*
*                                                                     *
*   FUNCTION =                                                        *
*      LOADS SELECTED I/O SUPPORT VSAM STRING DEPENDING ON THE SVC    *
*      NUMBER AND WHETHER AN ACB OR DCB (ISAM INTERFACE) IS BEING     *
*      PROCESSED.                                                     *
*      GETS STORAGE FOR A SAVEAREA AND THEN BALR'S TO THE SELECTED    *
*      ROUTINE.                                                       *
*      OPEN/CLOSE-                                                    *
*      UPON RETURN FREES THE SAVEAREA, DELETES THE VSAM ROUTINE AND   *
*      RETURNS CONTROLS TO THE COMMON I/O SUPPORT ROUTINES BY         *
*      BRANCHING TO THE RESIDENT ROUTINE.                             *
*      END-OF-VOLUME-                                                 *
*      UPON RETURN DELETES VSAM EOV, DELETES GTF ROUTINE IF LOADED,   *
*      DEQUEUES TIOT IF TSO TASK, FREES RESIDENT ROUTINE WORKAREA,    *
*      FREES COMMON EOV WORKAREA (FORCORE), FREES SAVEAREA & RETURNS  *
*      CONTROL TO THE CALLER (RECORD MANAGEMENT).                     *
*      BLDVRP/DLVRP-                                           @Y30DSDT
*      THE BLDVRP ENTRY POINT RECEIVES CONTROL FROM IGC0001I.  @Y30DSDT
*      ON ENTRY THE ESTAE ENVIRONMENT IS ESTABLISHED BEFORE    @Y30DSDT
*      THE BLDVRP ROUTINE IS LOADED AND BALR'ED TO.  UPON      @Y30DSDT
*      RETURN, FREES THE VRP WORKAREA (FORCORE), REMOVES THE   @Y30DSDT
*      ESTAE EXIT, AND RETURNS CONTROL TO CALLER VIA THE EXIT  @Y30DSDT
*      PROLOG. NOTE THAT THE VRP WORKAREA INCLUDES THE COMMON  @Y30DSDT
*      EOV WORKAREA AS THE INTERFACE BETWEEN THE BLDVRP AND    @Y30DSDT
*      OPEN ROUTINES.                                          @Y30DSDT
*                                                                     *
*   NOTES =  REQUIRES IEHIOSUP BE RUN AFTER LINKEDIT (VS1 ONLY)       *
*   THE ABOVE APPLIES TO VS1 ONLY. LPALIB IS USED IN VS2.     @Y021139*
*                                                                     *
*     DEPENDENCIES =  SYSTEM/370                                      *
*                     OS/VS1-2                                        *
*                     OS/VS2-1.6                                      *
*                     OS/VS2-2                                @Y021139*
*                                                                     *
*     RESTRICTIONS =  NONE                                            *
*                                                                     *
*     REGISTER CONVENTIONS =  SEE 'REGISTER EQUATES'                  *
*                                                                     *
*     PATCH LABEL =  IHB0026Z                                         *
*                                                                     *
*     SPECIAL TERMS AND ACRONYMS =  WTG - WHERE-TO-GO TABLE           *
*                                                                     *
*   MODULE TYPE =  CSECT                                              *
*                                                                     *
*     PROCESSOR = XF ASSEMBLER                                        *
*                                                                     *
*     MODULE SIZE =  1024 BYTES                                       *
*                                                                     *
*     ATTRIBUTES =                                                    *
*        REENTRANT,SUPERVISOR STATE,PRIVILEGED,PROTECTION KEY = 0,    *
*        ENABLED.                                                     *
*                                                                     *
*   ENTRY POINT =  IFG0192A,IFG0192Y(BLDVRP)                   @Y30DSDT
*      LINKEDITED WITH ALIASES IFG0200T,IFG0231T,IFG0557A,            *
*                              IFG0192I,IFG0200S,IFG0192Y      @Y30DSDT
*                                                                     *
*     PURPOSE =  LOAD SELECTED VSAM MODULE                            *
*                                                                     *
*     LINKAGE =                                                       *
*        ENTERED BY XCTL (RESIDENT ROUTINE) FROM:                     *
*          OPEN - IFG0193A                                            *
*          CLOSE - IFG0200V                                           *
*          TCLOSE - IGC0002C                                          *
*          EOV - IFG0551F                                             *
*          BLDVRP/DLVRP - IGC0001I                             @Y30DSDT
*        BALR TO VSAM STRINGS:                                        *
*          OPEN VSAM - IDA0192A                                       *
*          OPEN ISAM INTERFACE - IDA0192I                             *
*          CLOSE VSAM - IDA0200T                                      *
*          CLOSE ISAM INTERFACE - IDA0200S                            *
*          TCLOSE VSAM - IDA0231T                                     *
*          EOV VSAM - IDA0557A                                        *
*          BLDVRP/DLVRP - IDA0192Y                             @Y30DSDT
*        RETURNS BY XCTL (RESIDENT ROUTINE) TO:                       *
*          OPEN - IFG0198N                                            *
*          CLOSE - IFG0202L                                           *
*          TCLOSE - IFG0232Z                                          *
*          EOV - EXITS TO CALLER VIA SVC EXIT                         *
*          BLDVRP/DLVRP - EXITS TO CALLER VIA EXIT PROLOG      @Y30DSDT
*                                                                     *
*   INPUT =                                                           *
*      REG 2 - ACB ADDRESS (OPEN/CLOSE)                               *
*              AMB ADDRESS (EOV)                                      *
*      REG 4 - COMMON OPEN/CLOSE/EOV WORKAREA                         *
*              JFCBDSNM - DATA SET NAME ON DD STATEMENT               *
*              DXCCW8 - TIOT DD ENTRY ADDRESS (1ST 4 BYTES)           *
*                       0 IF OPEN & NON-CATALOG DATA SET              *
*      REG 5 - RESIDENT ROUTINE ADDRESS                               *
*      REG 6 - WTG TABLE ADDRESS                                      *
*      REG 8 - CURRENT WTG ENTRY ADDRESS                              *
*                                                                     *
*   OUTPUT =                                                          *
*      TO VSAM ROUTINES:                                              *
*                                                                     *
*      REG 2 - ACB ADDRESS (OPEN/CLOSE)                               *
*              AMB ADDRESS (EOV)                                      *
*      REG 4 - COMMON OPEN/CLOSE/EOV WORKAREA                         *
*              JFCBDSNM - DATA SET NAME ON DD STATEMENT               *
*              DXCCW8 - TIOT DD ENTRY ADDRESS (1ST 4 BYTES)           *
*                       0 IF OPEN & NON-CATALOG DATA SET              *
*      REG 13 - SAVEAREA                                              *
*                                                                     *
*      RETURN TO COMMON OPEN/CLOSE:                                   *
*                                                                     *
*      REGISTERS 2-8 SAME AS INPUT                                    *
*      RETURN FOR EOV:                                                *
*                                                                     *
*      REG 15 - ERROR CODE                                            *
*                                                                     *
*     MESSAGES =  NONE                                                *
*                                                                     *
*   EXIT-NORMAL =                                                     *
*      RETURN TO COMMON OPEN/CLOSE.                                   *
*        ACBERFLG - 0 SUCCESS                                         *
*      RETURN TO CALLER FOR EOV - RETURNS VIA SVC EXIT.               *
*        REG 15 - 0 SUCCESS.                                          *
*      RETURN TO CALLER FOR VRP - RETURNS VIA EXIT PROLOG      @Y30DSDT
*        REG 15 - 0 SUCCESS.                                   @Y30DSDT
*                                                                     *
*   EXIT-ERROR =                                                      *
*      RETURN TO COMMON OPEN/CLOSE:                                   *
*      ACBERFLG - ERROR CODE                                          *
*         SEE IDA0192A FOR OPEN ERROR CODES                           *
*         SEE IDA0200T FOR CLOSE ERROR CODES                          *
*         SEE IDA0231T FOR TCLOSE ERROR CODES                         *
*      RETURN TO CALLER FOR EOV:                                      *
*         REG 15 - ERROR CODE                                         *
*            SEE IDA0557A FOR EOV ERROR CODES                         *
*      RETURN TO CALLER FOR VRP:                               @Y30DSDT
*         REG 15 - ERROR CODE                                  @Y30DSDT
*            SEE IDA0192Y FOR BLDVRP/DLVRP RETURN CODES        @Y30DSDT
*                                                                     *
*   EXTERNAL REFERENCES = SEE BELOW                                   *
*                                                                     *
*     MODULES =                                                       *
*        IDA0192A - VSAM OPEN                                         *
*        IFG0198N - OS COMMON OPEN                                    *
*        IDA0200T - VSAM CLOSE                                        *
*        IFG0202L - OS COMMON CLOSE                                   *
*        IDA0231T - VSAM TCLOSE                                       *
*        IFG0231Z - OS COMMON TCLOSE                                  *
*        IDA0557A - VSAM EOV                                          *
*        IDA0192I - ISAM INTERFACE OPEN                               *
*        IDA0200S - ISAM INTERFACE CLOSE                              *
*        IDA0192Y - BLDVRP/DLVRP                               @Y30DSDT
*                                                                     *
*     DATA AREAS =                                                    *
*        FORCORE - COMMON OS OPEN/CLOSE/EOV WORKAREA.                 *
*        SAVEAREA - FOR CALLED ROUTINE.                               *
*                                                                     *
*     CONTROL BLOCKS =                                                *
*        NAME    CREATED    MODIFIED    REFERRED TO    DELETED        *
*                                                                     *
*        CVT                                X                         *
*        WTG                   X            X                         *
*        FORCORE               X            X            X(EOV OR VRP)*
*                                                                     *
*   TABLES =  NONE                                                    *
*                                                                     *
*   MACROS =                                                          *
*      LOAD                                                           *
*      DELETE                                                         *
*      GETMAIN                                                        *
*      FREEMAIN                                                       *
*      IECRES                                                         *
*      XCTLTABL                                                       *
*      DEQ                                                            *
*      MODESET                                                @Y021139*
*                                                                     *
*   CHANGE ACTIVITY = VS2-2 CHANGES ARE FLAGGED WITH @Y021139         *
*      THE CHANGES ARE REQUIRED TO MATCH VS2-2 INTERFACE      @Y021139*
*      CHANGES (I.E. SUBPOOLS, OPERATING KEYS ETC.)           @Y021139*
*                                                                     *
*      VSAM RELEASE 2 CHANGES ARE FLAGGED WITH @Y30DSNN.      @Y30DSDT*
*      THE CHANGES ARE REQUIRED FOR CHANGES TO THE RECOVERY   @Y30DSDT*
*      ROUTINES AND FOR THE ADDITION OF THE BLDVRP AND DELVRP @Y30DSDT*
*      SUPPORT.                                               @Y30DSDT*
*      VS2-3 CHANGES TO SET MODULE ID IN SAVE AREA OF IFG0192A Z30SSRL*
*      ARE FLAGGED WITH Z30SSNN.                               Z30SSRL*
*                                                                     *
*                                                                     *
***********************************************************************
         EJECT
         IECDSECS MAIN,WTG,CVT,TCB,PREFX,RRPL,ASCB,DSAB,QDB,JSCB
*                                             SELECT DSECTS   @Y021139*
***********************************************************************
*                                                                     *
* REGISTER EQUATES-                                                   *
*                                                                     *
***********************************************************************
*
R0       EQU   0                        WORK/PARAMETER REGISTER
R1       EQU   1                        WORK/PARAMETER REGISTER
R2       EQU   2                        VRP PARAMETER REGISTER @Y30DSDT
RBASE    EQU   3                        MODULE BASE REGISTER
R3       EQU   3                        MODULE BASE REGISTER   @YM38849
RCORE    EQU   4                        COMMON O/C/E WORKAREA
RES      EQU   5                        RESIDENT ROUTINE ADDRESS
RTCB     EQU   5                        TCB POINTER FOR MODESET@Y30DSDT
RWTG     EQU   6                        WTG TABLE POINTER
RWTGC    EQU   8                        WTG CURRENT ENTRY POINTER
R8       EQU   8                        REGISTER EIGHT.        @Y021139
R9       EQU   9                        WORK/REGISTER NINE.    @Y021139
R10      EQU   10                       WORK REGISTER
R11      EQU   11                       WORK REGISTER
R12      EQU   12                       WORK REGISTER          @Y021139
RSAVE    EQU   13                       SAVEAREA POINTER
R13      EQU   13                                              @Y021139
R14      EQU   14                                              @Y021139
RET      EQU   14                       RETURN REGISTER
R15      EQU   15                       WORK/PARAMETER REGISTER
*
***********************************************************************
*                                                                     *
* GENERAL EQUATES-                                                    *
*                                                                     *
***********************************************************************
*
ZERO     EQU   0                        CONSTANT OF 0          @Y30DSDT
ONE      EQU   1                        CONSTANT OF 1          @Y30DSDT
K2       EQU   2                        CONSTANT OF 2          @Y021139
K4       EQU   4                        CONSTANT OF 4
K12      EQU   12                       CONSTANT OF 12         @Y021139
MASK     EQU   X'FF'                    MASK FOR AND IMMEDIATE @Y30DSDT
ESTAEERR EQU   8                        ESTAE FAILED RET CODE  @Y30DSDT
OFSSVC   EQU   4                        OFFSET TO SVC NO. IN NAME
RESXCTL  EQU   4                        XCTL ENTRY OFFSET IN RES. RTN.
EXIT     EQU   3                        SVC EXIT NO.
EOVWASZ  EQU   544      EOV WORKAREA SIZE NOT USED IN VS2-2    @Y021139
PERRJFCB EQU   X'04'                    JFCB READ ERROR
FVSEOV   EQU   X'C8'                    VSAM EOV FUNCTION CODE
EOVERR   EQU   4                        EOV ERROR RETURN CODE
RESRTNL  EQU   8                        RESIDENT RTN WORKAREA LENGTH
         EJECT
         USING FORCORE,RCORE            BASE WORKAREA
         USING SVAREA,RSAVE             BASE SAVEAREA
         USING WTG,RWTG                 BASE WTG RABLE
         USING WTGENTRY,RWTGC           BASE WTG ENTRY
         USING CVT,R10                  BASE CVT
         USING IECRRPL,R9               RECOVERY ROUTINE P/L.  @Y021139
*
         BALR  RBASE,0                  SET BASE REGISTER
         USING *,RBASE                  DEFINE BASE
         B     PSTART2A                 BRANCH TO START        @Y021139
MODULEID DC    C'IFG0192A  74.340'      MODULE ID               Z30SSRL
         DS    0F                       FULL WORD BOUNDRY      @Y021139
*
PSTART2A MODESET EXTKEY=ZERO            OBTAIN KEY ZERO        @Y021139
*
         XC    DXATEXC1,DXATEXC1  *     ZERO AUDIT TRAIL       @Y021139
         XC    DXATEXC2,DXATEXC2  *     ZERO AUDIT TRAIL       @Y021139
*
         MVC   RSTATRTN,IDARMAIN        NOTE IFG2A IN CONTROL  @Y30DSDT
*        RSTATOPT=IDARLOAD; /* NOTE ENTRY TO IFG0192A.       @Y30DSDT*/
         MVI   RSTATOPT,IDARLOAD * NOTE ENTRY TO IFG0192A      @Y30DSDT
*
         L     R9,WTGPREFX *      GET ADDRESS OF PREFIX.       @Y021139
         USING IECPREFX,R9 *      BASE REGISTER FOR PREFIX.    @Y021139
         L     R9,IECRRPRM *      ADDRESS OF RRPL IN R9.       @Y021139
         USING IECRRPL,R9 *       ADDRESSABILITY IS R9.        @Y021139
*
*        RRFAMSTR = ON;   /* NOTE VSAM STRING IN CONTROL.    @Y021139*/
         OI    RRFLAGS1,RRFAMSTR  VSAM IS IN CONTROL.          @Y021139
*
         L     R0,SPLNG                 GET SUB-POOL & LENGTH
*                                       AND MODULE ID LENGTH    Z30SSRL
         GETMAIN R,LV=(R0)              GETMAIN FOR SAVEAREA
         LR    RSAVE,R1                 POINT TO SAVEAREA
         XC    SVAREA(SVAREASZ),SVAREA  CLEAR SAVEAREA
         LA    R11,SVAREASZ             LOAD SAVE AREA SIZE     Z30SSRL
         ST    R11,SAVESIZE             STORE THE SIZE          Z30SSRL
         MVC   SVAREAID,MODULEID        MOVE IN MOD ID & DATE   Z30SSRL
         ST    RSAVE,DXCCW7             CHAIN WITH FORCORE      Z30SSRL
         L     R10,CVTPTR               POINT TO CVT
*
         CLC   OPSVC,WTGMODNM+OFSSVC    TEST IF WTG INDICATES OPEN
         BNE   TESTCL                   BRANCH IF NO
         CLC   IIOPENID,WTGMODID        TEST FOR ISAM INTERFACE OPEN
         BE    IIOPEN                   BRANCH IF YES
         MVC   MODNAM,OPENNM            MOVE OPEN MODULE NAME
*
*        RSTATOPT=IDAROPEN; /* NOTE OPTION IS VSAM OPEN.     @Y021139*/
         MVI   RSTATOPT,IDAROPEN * THIS IS A VSAM OPEN.        @YM38159
*
         L     R11,CVTSVDCB             POINT TO SVCLIB DCB
         LOAD  EPLOC=MODNAM,DCB=(R11)   LOAD VSAM OPEN
         LR    R15,R0                   SET ENTRY POINT IN REG 15
         BALR  RET,R15                  BRANCH TO VSAM OPEN
         DELETE EPLOC=MODNAM            DELETE VSAM OPEN
         MODESET EXTKEY=DATAMGT         OBTAIN KEY FIVE        @Y021139
*        THE FOLLOWING IECRES IS USED TO MOVE THE COPIED ACB   @Y021139
*        BACK TO THE USER AREA.                                @Y021139
         IECRES INIT,DCBCOPY=FRWKAR,STM=(0,14,WTGPREFX)        @Y021139
         L     R1,DXDEBXAD              POINT TO DEBXTN        @Y021139
         LH    R0,DEBXLNGH-DEBXTN(R1)   LOAD LENGTH OF DEBXTN  @Y021139
         ICM   R0,8,MDEBPOOL            INSERT DEBXTN SUBPOOL  @Y021139
         FREEMAIN R,LV=(R0),A=(R1)      FREEMAIN MODEL DEBXTN  @Y021139
         SR    R1,R1                    R1=0                   @Y021139
         ST    R1,DXDEBXAD              DXDEBXAD=0             @Y021139
         MODESET EXTKEY=ZERO            OBTAIN KEY ZERO        @Y021139
         L     R0,SPLNG                 SUB-POOL & LENGTH
         FREEMAIN R,LV=(R0),A=(RSAVE)   FREEMAIN SAVEAREA      @Y021139
*
*        RRFAMSTR = OFF; /* NOTE VSAM STRING RETURNING.      @Y021139*/
         NI    RRFLAGS1,MASK-RRFAMSTR  TURN OFF THE BIT.       @Y30DSDT
*
         MODESET EXTKEY=DATAMGT         OBTAIN KEY FIVE        @Y021139
         IECRES XCTL,OP8N,4             XCTL BY TTR TO IFG0198N
*
IIOPEN   MVC   MODNAM,IIOPENNM          MOVE ISAM INTERFACE OPEN
*                                       MODULE NAME
*
*        RSTATOPT=IDARIIOP; /* NOTE OPTION IS IIP OPEN.      @Y021139*/
         MVI   RSTATOPT,IDARIIOP * THIS IS AN II OPEN.         @YM38159
*
         L     R11,CVTSVDCB             POINT TO SVCLIB DCB
         LOAD  EPLOC=MODNAM,DCB=(R11)   LOAD ISAM INTERFACE OPEN
         LR    R15,R0                   SET ENTRY POINT IN REG 15
         BALR  RET,R15                  BRANCH TO ISAM INTERFACE OPEN
         DELETE EPLOC=MODNAM            DELETE ISAM INTERFACE OPEN
         L     R0,SPLNG                 SUB-POOL & LENGTH
         FREEMAIN R,LV=(R0),A=(RSAVE)   FREEMAIN SAVEAREA      @Y021139
*
*        RRFAMSTR = OFF; /* NOTE VSAM STRING RETURNING.      @Y021139*/
         NI    RRFLAGS1,MASK-RRFAMSTR   TURN OFF THE BIT.      @Y30DSDT
*
         MODESET EXTKEY=DATAMGT         OBTAIN KEY FIVE        @Y021139
         IECRES XCTL,OP8N,4             XCTL BY TTR TO IFG0198N
*
TESTCL   CLC   CLSVC,WTGMODNM+OFSSVC    TEST IF WTG INDICATES CLOSE
         BNE   TESTTCL                  BRANCH IF NO
         CLC   IICLOSID,WTGMODID        TEST FOR ISAM INTERFACE CLOSE
         BE    IICLOSE                  BRANCH IF YES
         MVC   MODNAM,CLOSENM           MOVE CLOSE MODULE NAME
*
*        RSTATOPT=IDARCLOS; /* NOTE CLOSE IN PROGRESS.       @Y021139*/
         MVI   RSTATOPT,IDARCLOS * THIS IS A VSAM CLOSE.       @YM38159
*
         L     R11,CVTSVDCB             POINT TO SVCLIB DCB
         LOAD  EPLOC=MODNAM,DCB=(R11)   LOAD VSAM CLOSE
         LR    R15,R0                   SET ENTRY POINT IN REG 15
         BALR  RET,R15                  BRANCH TO VSAM CLOSE
         DELETE EPLOC=MODNAM            DELETE VSAM CLOSE
         L     R0,SPLNG                 SUB-POOL & LENGTH
         FREEMAIN R,LV=(R0),A=(RSAVE)   FREEMAIN SAVEAREA
*
*        RRFAMSTR=OFF; /* NOTE STRING RETURNING.             @Y021139*/
         NI    RRFLAGS1,MASK-RRFAMSTR   TURN OFF THE BIT.      @Y30DSDT
*
         MODESET EXTKEY=DATAMGT         OBTAIN KEY FIVE        @Y021139
         IECRES XCTL,CL2L,0             XCTL BY NAME(0 TTR) TO IFG0202L
*
IICLOSE  MVC   MODNAM,IICLOSNM          MOVE ISAM INTERFACE CLOSE
*                                       MODULE NAME
*
*        RSTATOPT=IDARIICL; /* NOTE II CLOSE IN PROGRESS.    @Y021139*/
         MVI   RSTATOPT,IDARIICL * THIS IS AN II CLOSE.        @YM38159
*
         L     R11,CVTSVDCB             POINT TO SVCLIB DCB
         LOAD  EPLOC=MODNAM,DCB=(R11)   LOAD ISAM INTERFACE CLOSE
         LR    R15,R0                   SET ENTRY POINT IN REG 15
         BALR  RET,R15                  BRANCH TO ISAM INTERFACE CLOSE
         DELETE EPLOC=MODNAM            DELETE ISAM INTERFACE CLOSE
         L     R0,SPLNG                 SUB-POOL & LENGTH
         FREEMAIN R,LV=(R0),A=(RSAVE)   FREEMAIN SAVEAREA
*
*        RRFAMSTR=OFF; /* NOTE VSAM STRING RETURNING.        @Y021139*/
         NI    RRFLAGS1,MASK-RRFAMSTR   TURN OFF THE BIT.      @Y30DSDT
*
         MODESET EXTKEY=DATAMGT         OBTAIN KEY FIVE        @Y021139
         IECRES XCTL,CL2L,0             XCTL BY NAME(0 TTR) TO IFG0202L
*
TESTTCL  CLC   TCSVC,WTGMODNM+OFSSVC    TEST IF WTG INDICATES TCLOSE
         BNE   EOV                      BRANCH IF NO
         MVC   MODNAM,TCLOSENM          MOVE TCLOSE MODULE NAME
*
*        RSTATOPT=IDARTCLO; /* NOTE TCLOSE IN PROGRESS.      @Y021139*/
         MVI   RSTATOPT,IDARTCLO * THIS IS A CLOSE TYPE=T      @YM38159
*
         L     R11,CVTSVDCB             POINT TO SVCLIB DCB
         LOAD  EPLOC=MODNAM,DCB=(R11)   LOAD VSAM TCLOSE
         LR    R15,R0                   SET ENTRY POINT IN REG 15
         BALR  RET,R15                  BRANCH TO VSAM TCLOSE
         DELETE EPLOC=MODNAM            DELETE VSAM TCLOSE
         MODESET EXTKEY=DATAMGT         OBTAIN KEY FIVE        @Y021139
*        THE FOLLOWING IECRES IS USED TO COPY THE ACB BACK     @Y021139
*        TO THE USER AREA.                                     @Y021139
         IECRES INIT,DCBCOPY=FRWKAR,STM=(0,14,WTGPREFX)        @Y021139
         MODESET EXTKEY=ZERO            OBTAIN KEY ZERO        @Y021139
         L     R0,SPLNG                 SUB-POOL & LENGTH
         FREEMAIN R,LV=(R0),A=(RSAVE)   FREEMAIN SAVEAREA
*
*        RRFAMSTR = OFF; /* NOTE STRING RETURNING.           @Y021139*/
         NI    RRFLAGS1,MASK-RRFAMSTR   TURN OFF THE BIT.      @Y30DSDT
*
         MODESET EXTKEY=DATAMGT         OBTAIN KEY FIVE        @Y021139
         IECRES XCTL,TC2Z,4             XCTL BY NAME(0 TTR) TO IFG0231Z
*
EOV      EQU   *
         USING IDAPDPRM,R1
         LA    R1,DXIOB                 POINT TO PROB. DET. PARM LIST
         CLI   PDPCALL,PDPEOV           TEST PD CALLER ID
         BNE   EOVCALL                  NOT EOV FUNCTION
         CLI   PDPCODE,PERRJFCB         TEST PD PROBLEM CODE
         BNE   EOVCALL                  NOT JFCB READ ERROR
         CLI   PDPFUNC,FVSEOV           TEST PD FUNCTION CODE
         BE    SETERR                   JFCB READ ERROR-BYPASS VSAM EOV
         DROP  R1
EOVCALL  MVC   MODNAM,EOVNM             MOVE EOV MODULE NAME
*
*        RSTATOPT=IDAREOV; /* NOTE EOV IN PROGRESS.          @Y021139*/
         MVI   RSTATOPT,IDAREOV * THIS IS A VSAM EOV.          @YM38159
*
         L     R11,CVTSVDCB             POINT TO SVCLIB DCB
         LOAD  EPLOC=MODNAM,DCB=(R11)   LOAD VSAM EOV
         LR    R15,R0                   SET ENTRY POINT IN REG. 15
         BALR  RET,R15                  BRANCH TO VSAM EOV
         ST    R15,EOVERROR             SAVE EOV ERROR CODE
         DELETE EPLOC=MODNAM            DELETE VSAM EOV
         B     FMRRTN                   BR TO FREE EOV WA      @Y021139
SETERR   LA    R1,EOVERR                SET EOV ERROR FOR RETURN CODE
         ST    R1,EOVERROR              SAVE ERROR CODE IN WA  @Y30DSDT
FMRRTN   EQU   *
*        THE FOLLOWING CODE HAS BEEN ADDED DUE TO EOV          @Y021139
*        INTERFACE CHANGES IN VS2-2.                           @Y021139
*
*        THE IECRES MACRO WHICH FOLLOWS DESTROYS REGISTERS.    @Y021139
*         REGISTERS EIGHT AND NINE HOWEVER ARE NOT DESTROYED.  @Y021139
*         THUS REQUIRED REGISTERS 03,13 ARE SAVED ACROSS THE   @Y021139
*         IECRES MACRO BY HOLDING THEM IN REGISTERS 8,9.       @Y021139
*        THE FOLLOWING IECRES WILL RESET THE ESTAE.            @Y021139
*
         LR  R8,R3   SAVE REGISTER 03  ACROSS IECRES MACRO.    @Y021139
         LR  R9,R13  SAVE REGISTER 13 ACROSS IECRES MACRO.     @Y021139
         MODESET EXTKEY=DATAMGT         OBTAIN KEY FIVE        @Y021139
         IECRES  FREE,A=(RCORE),PREFIX=EOV                     @Y021139
         MODESET EXTKEY=ZERO            OBTAIN KEY ZERO        @Y021139
         LR  R3,R8  RESTORE REGISTER THREE.                   @Y021139
         LR  R13,R9  RESTORE REGISTER THIRTEEN.                @Y021139
*
         L     R10,EOVERROR             GET EOV ERROR FROM WORKAREA
*
         L     R0,SPLNG                 SUB-POOL & LENGTH
         FREEMAIN R,LV=(R0),A=(RSAVE)   FREEMAIN SAVEAREA/WORKAREA
         LR    R15,R10                  RESTORE EOV ERROR IN REG. 15
         L     RET,CVTPTR               LOAD PTR TO CVT        @Y021139
         L     RET,CVTEXPRO-CVT(RET)    LOAD PTR TO EXIT       @Y021139
         RETURN ,                       RETURN TO CALLER       @Y021139
         EJECT                                                 @Y30DSDT
***************************************************************@Y30DSDT
*                                                              @Y30DSDT
*  PROCEDURE NAME - IFG0192Y (BLDVRP, DLVRP)                   @Y30DSDT
*                                                              @Y30DSDT
*  FUNCTION - THIS ROUTINE ESTABLISHES THE RECOVERY ENVIRON-   @Y30DSDT
*             MENT, AND ADDRESS VALIDITY CHECKS AND COPIES THE @Y30DSDT
*             PARAMETER LIST TO THE WORK AREA BEFORE CALLING   @Y30DSDT
*             THE BLDVRP ROUTINE.  ON RETURN FROM BLDVRP, THE  @Y30DSDT
*             THE PARAMETER LIST IS COPIED BACK, THE VRP WORK- @Y30DSDT
*             AREA IS FREED, AND THE ESTAE IS DELETED.         @Y30DSDT
*                                                              @Y30DSDT
*  INPUT - REGISTER 2, POINTER TO BLDVRP PARAMETER LIST        @Y30DSDT
*          REGISTER 4, POINTER TO WORKAREA (FORCORE+SAVEAREA)  @Y30DSDT
*          REGISTER 7, POINTER TO SVC 19 PARAMETER LIST        @Y30DSDT
*                                                              @Y30DSDT
*  OUTPUT - (TO THE BLDVRP ROUTINE)                            @Y30DSDT
*           REGISTER 2, POINTER TO BLDVRP PARAMETER LIST       @Y30DSDT
*           REGISTER 4, POINTER TO WORKAREA (FORCORE+SAVEAREA) @Y30DSDT
*           REGISTER 7, POINTER TO SVC 19 PARAMETER LIST       @Y30DSDT
*           REGISTER 13, POINTER TO THE REGISTER SAVEAREA      @Y30DSDT
*           REGISTER 15, ENTRY POINT FOR IDA0192Y              @Y30DSDT
*                                                              @Y30DSDT
*  OUTPUT - (TO THE REQUESTOR)                                 @Y30DSDT
*           REGISTER 15, RETURN CODE FROM BLDVRP               @Y30DSDT
*                                                              @Y30DSDT
***************************************************************@Y30DSDT
         SPACE 1                                               @Y30DSDT
         ENTRY IFG0192Y                 ENTRY PT FOR ALIAS     @Y30DSDT
         EXTRN IDAOCEA4                 EXTRN FOR RECOVERY     @Y30DSDT
IFG0192Y BALR  RBASE,0                  ESTABLISH BASE REG     @Y30DSDT
         USING *,RBASE                  BASE ADDRESSIBILITY    @Y30DSDT
         B     PSTART2Y                 BRANCH TO START         Z30SSRL
IDMODULE DC    C'IFG0192Y  74.337'      MODULE ID               Z30SSRL
         DS    0F                       FULL WORD BOUNDARY      Z30SSRL
PSTART2Y MODESET EXTKEY=ZERO            GET IN KEY ZERO         Z30SSRL
         MVC   RSTATRTN,IDARMAIN        NOTE IFG2A IN CONTROL  @Y30DSDT
         MVI   RSTATOPT,IDARLOAD        INDICATE IFG0192A/2Y   @Y30DSDT
         USING TCB,RTCB                 TCB ADDRESSIBILITY     @Y30DSDT
         L     RTCB,PSATOLD-PSA         FOR FOLLOWING MODESET  @Y30DSDT
         MODESET EXTKEY=RBT234,WORKREG=1   GET IN CALLERS KEY  @Y30DSDT
         DROP  RTCB                     REMOVE USING ON TCB    @Y30DSDT
         IC    R1,ZERO(R2)              GET FIRST BYTE OF P/L  @Y30DSDT
         STC   R1,ZERO(R2)              STORE IT BACK OR ABEND @Y30DSDT
         IC    R1,BLPSIZE-ONE(R2)       GET LAST BYTE OF P/L   @Y30DSDT
         STC   R1,BLPSIZE-ONE(R2)       STORE IT BACK OR ABEND @Y30DSDT
* THE FOLLOWING MODESET WILL SAVE THE USERS KEY IN FORCORE     @Y30DSDT
         MODESET EXTKEY=ZERO,SAVEKEY=DXUKEY,WORKREG=1    KEY 0 @Y30DSDT
         MVC   IDABLPRM(BLPSIZE),ZERO(R2)  COPY THE PARM LIST  @Y30DSDT
         ST    R2,DXUDCBAD              SAVE PTR TO USERS P/L  @Y30DSDT
         LA    R2,IDABLPRM              POINT TO PROTECTED P/L @Y30DSDT
         ST    R2,DXPDCBAD              SAVE PTR TO PROTECTED  @Y30DSDT
         MVI   BLPFLAG2,ZERO            RESET I/O SUPPORT FLAGS@Y30DSDT
         LA    RSAVE,DXXEND(RCORE)      POINT TO SAVEAREA      @Y30DSDT
         ST    RSAVE,DXCCW7             CHAIN WITH FORCORE      Z30SSRL
         MVC   72(16,RSAVE),IDMODULE    MOVE IN MODULE ID       Z30SSRL
         LA    R8,92                    GET SIZE OF SAVEAREA    Z30SSRL
         ST    R8,88(,RSAVE)            STORE SIZE OF SAVEAREA  Z30SSRL
         MVC   DXXMODNM,VRPNM           MOD NAME FOR LOAD      @Y30DSDT
         LOAD  EPLOC=DXXMODNM           GET ADDRESS OF 192Y    @Y30DSDT
         ST    R0,DXXMODEP              SAVE EP IN WTG TABLE   @Y30DSDT
         LA    R1,IDAESPL               ESTAE PARMS IN FORCORE @Y30DSDT
         MVC   IDAESPL(ESTPARML),ESTPARMB  MOVE IN ESTAE PARMS @Y30DSDT
         ESTAE PARAM=(RCORE),MF=(E,(1)) ESTABLISH RECOVERY ENV @Y30DSDT
         LTR   R15,R15                  TEST ESTAE RETURN CODE @Y30DSDT
         BZ    CALL192Y                 GO TO IDA0192Y IF ZERO @Y30DSDT
         LA    R9,ESTAEERR              SET RETURN CODE        @Y30DSDT
         B     EXIT192Y                 RETURN TO CALLER       @Y30DSDT
CALL192Y L     R15,DXXMODEP             SET UP LINK REG        @Y30DSDT
         MVI   RSTATOPT,IDARVRP         INDICATE VRP IN PROG.  @Y30DSDT
         BALR  RET,R15                  BRANCH TO BLDVRP       @Y30DSDT
         LR    R9,R15                   SAVE THE RETURN CODE   @Y30DSDT
         ESTAE 0                        RELEASE RECOVERY ENV   @Y30DSDT
         L     R2,DXUDCBAD              POINT TO USERS P/L     @Y30DSDT
         MODESET KEYADDR=DXUKEY,WORKREG=1  GET IN CALLERS KEY  @Y30DSDT
         MVC   ZERO(BLPSIZE,R2),IDABLPRM  COPY THE P/L BACK    @Y30DSDT
         MODESET EXTKEY=ZERO            RUN IN KEY ZERO        @Y30DSDT
EXIT192Y DELETE EPLOC=DXXMODNM          IDA0192Y OFF LOAD LIST @Y30DSDT
         LR    R8,RBASE                 SAVE BASE IN REG 8     @Y30DSDT
         IECRES FREE,A=(RCORE),PREFIX=YES  FREE THE WORKAREA   @Y30DSDT
         LR    RBASE,R8                 RESTORE THE BASE REG   @Y30DSDT
         LR    R15,R9                   LOAD THE RETURN CODE   @Y30DSDT
         MODESET EXTKEY=ZERO            RUN IN KEY ZERO        @Y30DSDT
         L     RET,CVTPTR               LOAD PTR TO CVT        @Y30DSDT
         L     RET,CVTEXPRO-CVT(RET)    LOAD PTR TO EXIT PROLOG@Y30DSDT
         RETURN ,                       RETURN TO CALLER       @Y30DSDT
         EJECT
***********************************************************************
*                                                                     *
* CONSTANTS-                                                          *
*                                                                     *
***********************************************************************
*
SPLNG    DS    0F                       GETMAIN/FREEMAIN SP & LENGTH
         DC    AL1(252)                 SAVEAREA SUBPOOL
         DC    AL3(SVAREASZ)            SAVEAREA LENGTH
MDEBPOOL DC    AL1(230)                 MODEL DEBXTN SUBPOOL   @Y021139
EIGHT    DC    H'8'                     HW CONSTANT OF 8
OPSVC    DC    C'19'                    OPEN SVC NO.
CLSVC    DC    C'20'                    CLOSE SVC NO.
TCSVC    DC    C'23'                    TCLOSE SVC NO.
IIOPENID DC    C'2I'                    ISAM INTERFACE OPEN MODULE ID
IICLOSID DC    C'0S'                    ISAM INTERFACE CLOSE MODULE ID
CL2L     DC    C'2L'                    ID OF MODULE IFG0202L
         DC    X'00000000'              TTRL
TC2Z     DC    C'2Z'                    ID OF MODULE IFG0231Z
         DC    X'00000000'              TTRL
IDARMAIN DC    C'    '                  CONSTANT FOR RSTATRTN  @Y30DSDT
OPENNM   DC    C'IDA0192A'              VSAM OPEN MODULE NAME
IIOPENNM DC    C'IDA0192I'              ISAM INTERFACE OPEN MODULE NAME
CLOSENM  DC    C'IDA0200T'              VSAM CLOSE MODULE NAME
IICLOSNM DC    C'IDA0200S'              ISAM INTERFACE CLOSE MODULE NAM
TCLOSENM DC    C'IDA0231T'              VSAM TCLOSE MODULE NAME
EOVNM    DC    C'IDA0557A'              VSAM EOV MODULE NAME
GTFNM    DC    C'IFG055TR'              GTF MODULE NAME
VRPNM    DC    C'IDA0192Y'              BLDVRP MODULE NAME     @Y30DSDT
         SPACE 1                                               @Y30DSDT
* BUILD A PSUEDO ESTAE PARAMETER LIST FOR IDAOCEA4             @Y30DSDT
         SPACE 1                                               @Y30DSDT
ESTPARMB ESTAE IDAOCEA4,ASYNCH=YES,RECORD=YES,MF=L             @Y30DSDT
ESTPARME EQU   *                        END OF ESTAE PARM LIST @Y30DSDT
ESTPARML EQU   ESTPARME-ESTPARMB        LENGTH OF PARM LIST    @Y30DSDT
         SPACE 1                                               @Y30DSDT
C1       DEQ   (,,4,STEP),RET=HAVE,MF=L DEQ FOR TSO
C2       DC    CL8'SYSZTIOT'            QNAME                  @Y021139
C3       DC    CL6'      '              RNAME: ASID & DSAB QDB @Y021139
C4       EQU   *-C1
*
         XCTLTABL ID=(OP8N,8N),LENGTH=,BRT=YES   XCTL TABLE
         EJECT
***********************************************************************
*                                                                     *
* CONTROL BLOCKS & WORKAREAS-                                         *
*                                                                     *
***********************************************************************
*
SVAREA   DSECT
SAVEA    DS    CL72                     SAVEAREA
SVAREAID DS    CL16                     MODULE ID AND DATE      Z30SSRL
SAVESIZE DS    F                        SIZE OF TOTAL SAVE AREA Z30SSRL
MODNAM   DS    CL8                      MODULE NAME
RESRTNAD DS    F                        RESIDENT RTN WA ADDRESS
EOVERROR DS    F                        EOV ERROR CODE
SVAREASZ EQU   *-SVAREA                 SAVEAREA SIZE
*
         IHAPSA                                                @Y30DSDT
*
         IKJRB                                                 @Y30DSDT
         IECDSECS EXPAND=YES
         IEZDEB                                                @Y021139
*
         IDAPDPRM
*
         IDARTMAC ,         VSAM RECOVERY TERMINATION MACRO.   @Y30DSDT
*
         IDABLPRM ,                     VRP PARM LIST          @Y30DSDT
*
         END IFG0192A
