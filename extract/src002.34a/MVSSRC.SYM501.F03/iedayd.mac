         TITLE 'IEDAYD - TSO DESTINATION SCHEDULER'
IEDAYD   CSECT
*A450000                                                         S22029
*C420000,430000                                                  S22029
*D493000-496000                                                  S22029
*A490000,510000,530000                                           A42371
*C022000,022100,026300,410000                                    Y06327
*A022050,025620,275000,285000,331000-335000                      Y06327
*A551000-556000,700700-709100,845000                             Y06327
*A333000,472000,551000,706300-710000                           @Y17XAYP
*C472000-476000,705600                                         @Y17XAYP
***********************************************************************
*MODULE NAME = IEDAYD  (TCAM,TSO)                              @Y17XAYP
*                                                              @Y17XAYP
*DESCRIPTIVE NAME = TIME SHARING DESTINATION SCHEDULER         @Y17XAYP
*                                                              @Y17XAYP
*COPYRIGHT = NONE                                              @Y17XAYP
*                                                                     *
*STATUS -- VERSION 10.0                                        @Y17XAYP
*                                                                     *
*FUNCTION -- THE TIME SHARING DESTINATION SCHEDULER GETS CONTROL IN   *
*   PLACE OF THE TCAM DESTINATION SCHEDULER WHEN TSO IS IN THE SYSTEM.*
*   IF THIS ROUTINE WAS ACTIVATED BECAUSE A TCAM BUFFER WAS POSTED TO *
*   IT, IT LOADS THE ADDRESS OF THE TCAM DESTINATION SCHEDULER'S STCB *
*   (IEDQHM) INTO REGISTER 3, AND BRANCHES TO THE DISPATCHER BYPASS   *
*   FUNCTION TO IMMEDIATELY ACTIVATE THE STCB.                        *
*      IF THIS ROUTINE WAS ACTIVATED BY THE POSTING OF A TIME SHARING *
*   DESTINATION QCB TO ITSELF, IT SETS THE POSTED BIT OFF IN THE QCB  *
*   AND CHECKS TO SEE IF A SIMULATED ATTENTION READ WAS REQUESTED.    *
*   IF IT WAS, CONTROL IMMEDIATELY PASSES TO THE DISPATCHER DISPATCH  *
*   FUNCTION.  IF NOT, THIS ROUTINE CHECKS TO SEE IF TPUT REQUESTED   *
*   A WRITE BREAK.  IF NOT, CONTROL ALSO PASSES TO THE DISPATCH       *
*   FUNCTION.  IF A WRITE BREAK WAS REQUESTED, THIS ROUTINE LOADS THE *
*   LCB ADDRESS INTO REGISTER 4 AND BRANCHES TO THE TIME SHARING      *
*   SCHEDULER (IEDAYZ IF ATTACHED VIA 270X, IEDAYJ IF ATTACHED        *
*   VIA 3705) TO DETERMINE WHETHER A WRITE BREAK CHANNEL COMMAND CAN  *
*   BE ISSUED.  IF IT CAN, IEDAYZ OR IEDAYJ ISSUES AN IOHALT.  IN     *
*   EITHER CASE, CONTROL RETURNS TO THIS ROUTINE, WHICH THEN PASSES   *
*   CONTROL TO THE DISPATCHER DISPATCH FUNCTION.                      *
*                                                                     *
*ENTRY POINT -- IEDAYD - TO INITIATE A WRITE BREAK OPERATION, IF      *
*   NECESSARY, AND TO ASSIGN A BUFFER OR QCB TO ITS DESTINATION.      *
*   CALLING SEQUENCE          L    R15,STCBLINK-1                     *
*                             BR   R15                                *
*                                                                     *
*INPUT -- IEDAYD IS CALLED BY THE DISPATCHER WHEN A TCAM BUFFER IS    *
*   POSTED TO IT, OR WHEN A TIME SHARING DESTINATION QCB IS POSTED    *
*   TO ITSELF.  AT ENTRY THE FOLLOWING REGISTERS ARE SET.             *
*   R1 HAS THE ADDRESS OF THE TCAM BUFFER OR TIME SHARING QCB.        *
*   R3 HAS THE STCB ADDRESS.                                          *
*   R7 HAS THE DESTINATION QCB ADDRESS.                               *
*   R11 HAS THE DISPATCHER ADDRESS.                                   *
*   R13 HAS THE ADDRESS OF AVTSAVE2.                                  *
*   R15 HAS THE ENTRY POINT ADDRESS.                                  *
*                                                                     *
*OUTPUT -- ON EXIT TO THE DISPATCHER BYPASS FUNCTION, REGISTER 3 CON- *
*   TAINS THE ADDRESS OF THE TCAM DESTINATION SCHEDULER'S STCB.       *
*      ON EXIT TO THE (NON-NCP) TIME SHARING SCHEDULER, THE FOLLOWING *
*   REGISTERS ARE SET.                                                *
*   R2 HAS THE RETURN ADDRESS IN IEDAYD.                              *
*   R3 HAS THE RETURN ADDRESS IN IEDAYD.                              *
*   R4 HAS THE LCB ADDRESS.                                           *
*   R7 HAS THE DESTINATION QCB ADDRESS.                               *
*   R10 HAS THE DCB ADDRESS.                                          *
*   R11 HAS THE DISPATCHER ADDRESS.                                   *
*   R13 HAS THE ADDRESS OF AVTSAVE2.                                  *
*      ON EXIT TO THE DISPATCHER DISPATCH FUNCTION, THE POSTED BIT IN *
*   THE TIME SHARING QCB IS OFF.                                      *
*                                                                     *
*          ON EXIT TO THE (NCP) TSO SCHEDULER THE FOLLOWING           *
*   REGISTERS ARE SET.                                                *
*   R0 HAS TSO SCHEDULER ROUTINE SELECT VALUE                         *
*   R7 HAS A DESTINATION QCB ADDRESS                                  *
*   R10 HAS A LGB ADDRESS                                             *
*   R11 HAS THE ADDRESS OF THE TCAM DISPATCHER                        *
*   R13 HAS THE ADDRESS OF AVTSAVE2                                   *
*   R14 HAS THE RETURN ADDRESS                                        *
*   R15 HAS THE TSO SCHEDULER ENTRY ADDRESS                           *
*EXTERNAL ROUTINE -- IEDAYZ - TO DETERMINE WHETHER THE WRITE BREAK    *
*   CHANNEL COMMAND REQUESTED BY TPUT CAN BE ISSUED, AND TO ISSUE AN  *
*   IOHALT ON THE LINE IF IT CAN.                                     *
*                    IEDAYJ - 3705 VERSION OF TSO DESTINATION         *
*   SCHEDULER, TO ISSUE AN IOHALT IF WRITE BREAK REQUESTED CAN BE     *
*   ISSUED.                                                           *
*                                                                     *
*EXITS-NORMAL -- TO THE DISPATCHER BYPASS FUNCTION.                   *
*   TO THE DISPATCHER DISPATCH FUNCTION.                              *
*                                                                     *
*EXITS-ERROR -- NONE                                                  *
*                                                                     *
*TABLES/WORKAREAS -- AVT, LCB, QCB, DCB, STCB, TSID, LGB.             *
*                                                                     *
*ATTRIBUTES -- SERIALLY REUSABLE, REFRESHABLE, ENABLED, RESIDENT,     *
*   PROBLEM PROGRAM MODE                                              *
*                                                                     *
*NOTES -- THE OPERATION OF THIS MODULE DOES NOT DEPEND UPON A         *
*   PARTICULAR INTERNAL REPRESENTATION OF THE EXTERNAL CHARACTER      *
*   SET.                                                              *
*                                                                     *
***********************************************************************
         SPACE 2
*        REGISTER EQUATES
         SPACE
RSELECT  EQU   0                        TSO SCHED RTN SELECT   @Y17XAYP
RTRM     EQU   1                        ADDRESS OF TERM ENTRY, WORK REG
R2       EQU   2                        WORK REGISTER
RSTCB    EQU   3                        ADDRESS OF STCB
RLCB     EQU   4                        ADDRESS OF LCB
R6       EQU   6                        ADDRESS OF TS SCHEDULER
RQCB     EQU   7                        ADDRESS OF DESTINATION QCB
RNQCB    EQU   8                        ADDRESS OF QCB PREFIX    Y06327
RDCB     EQU   10                       ADDRESS OF DCB
RLGB     EQU   10                       ADDRESS OF LGB           Y06327
RDISP    EQU   11                       ADDRESS OF DISPATCHER
RBASE    EQU   12                       BASE REGISTER
R13      EQU   13                       ADDRESS OF AVTSAVE2
RETURN   EQU   14                       RETURN ADDRESS REG     @Y17XAYP
R15      EQU   15                       HAS BASE ADDRESS AT ENTRY
         SPACE 2
*        MASKS
         SPACE
LGBINDIC EQU   X'80'                    LGB INDICATOR            Y06327
         SPACE 2
*        ADDRESSABILITY
         SPACE
         USING AVTSAVE2,R13
         USING IEDQDISP,RDISP
         USING IEDQLCB,RLCB
         USING IEDQQCB,RQCB
         USING IEDQSTCB,RSTCB
         USING IEDNLGB,RLGB                                      Y06327
         DC    AL1(DSPMCPL8),AL3(0),A(0)  STCB FIELDS            S22029
         USING *,RBASE
         LR    RBASE,R15                ESTABLISH ADDRESSABILITY
IEDAYD  IEDHJN  AYD000
         CLI   QCBSTVTO-IEDQQCB(RTRM),DSPSNDSC PASSED ELEMENT A QCB?
         BE    AYD100                   BRANCH ON YES
         CLI   QCBSTVTO-IEDQQCB(RTRM),DSPSEND  IS THIS ELEMENT @Y17XAYP
*                                      A QCB FOR TERMINAL      @Y17XAYP
*                                      ATTACHED VIA NCP        @Y17XAYP
         BE    AYD100                  BRANCH YES                Y06327
         L     RSTCB,AVTTSOPT          GET THE ADDRESS OF        S22029
         L     RSTCB,TSIDEST-IEDQTSI(,RSTCB) TCAM DEST SCHED STCBS22029
         B     DSPBYPAS                 GIVE HM CONTROL
AYD100   EQU   *
         NI    QCBTSOF2,X'FF'-QCBPOSTO  TURN OFF QCB POSTED BIT
         TM    QCBTSOF1,QCBDISC         LOGGING OFF              A42371
         BO    AYD105                   BRANCH YES TO ALLOW BREAKA42371
         TM    QCBTSOF1,QCBSATRD        SIMULATED ATTENTION READ?
         BO    DSPDISP                  BRANCH ON YES TO RETURN
AYD105   EQU   *                                                 A42371
         TM    QCBTSOF1,QCBWRBRK        WRITE BREAK REQUESTED?
         BZ    DSPDISP                  BRANCH ON NO TO RETURN
         L     R6,AVTTSOPT              ADDR OF TIME SHARING QCB Y06327
         USING IEDQTSI,R6               TSINPUT QCB ADDRESS    @Y17XAYP
         L     RLGB,QCBLGBAD-1          GET ADDR OF LGB/DCB      Y06327
         TM    LGBDSORG,LGBINDIC        ATTACHED VIA A 3705      Y06327
         BO    AYD210                   YES, USE 3705 TSO SCHED  Y06327
         DROP  RLGB                                              Y06327
         USING IHADCB,RDCB                                       Y06327
         SR    RTRM,RTRM
         IC    RTRM,QCBRELLN            QCB'S STARTING RLN
         SR    R2,R2
         IC    R2,DCBEIOBX              GET LCB SIZE
         MR    RTRM-1,R2                GET STARTING LCB OFFSET
         AL    RTRM,DCBIOBAD            POINT TO IOB OF LCB
         SH    RTRM,LCBSIZE             ADJUST TO START OF LCB
         LR    RLCB,RTRM                LCB BASE
         LA    R2,AYD200                SET UP WITH RETURN ADDRESS
         L     R6,TSISCHED-IEDQTSI(,R6) GET ADDRESS OF TS SCHEDULER
         BAL   RSTCB,28(,R6)            BRANCH TO ROUTINE
AYD200   EQU   *
         B     DSPDISP                  RETURN
AYD210   EQU   *                                                 Y06327
         LA    R2,QCBPRFSZ              GET SIZE OF QCB PREFIX   Y06327
         LR    RNQCB,RQCB               GET QCB BASE             Y06327
         SR    RNQCB,R2                 BACK UP TO PREFIX        Y06327
         USING IEDNQCB,RNQCB            ESTABLISH ADDRESSABILITY Y06327
*                                       FOR THE QCB PREFIX       Y06327
         L     RLCB,QCBPLCBA-1          GET ADDRESS OF PLCB      Y06327
         L     R15,TSIRNSCH             GET THE ADDRESS OF THE @Y17XAYP
*                                       TSO 3705 SCHEDULER       Y06327
         LA    RSELECT,TSOA01           TSO SCHEDULER ROUTINE  @Y17XAYP
         BALR RETURN,R15                GOT TO TSOSCHEDULER    @Y17XAYP
*                                       TO PROCESS WRITE BREAK @Y17XAYP
         B     DSPDISP                  EXIT TO DISPATCHER     @Y17XAYP
         SPACE 2
*        CONSTANTS
         SPACE
LCBSIZE  DC    AL2(LCBFLAG1-IEDQLCB)    SIZE OF LCB FROM RCB TO IOB
         EJECT
         TLCBD
         EJECT
         TQCBD
         TSOECDD                                                 Y06327
         TTSID
         TAVTD
         TDISPD
         DCBD  DSORG=TX
         TSTCBD
         TLGBD                                                   Y06327
         END
