         TITLE 'SIMULATED ATTENTION ROUTINE - IEDAYS'
IEDAYS   CSECT
         SPACE 3
*  CHANGE ACTIVITY AS FOLLOWS
******************** MICROFICHE FLAGS *********************** SUPT CODE
*C850000-880000,888000-906000                                    S21903
*A140600-141800,181030-181360,212080-213860,219000               S22028
*A287030-287360,298080-299860,301000,400500-401500               S22028
*A442600-443200,712600-713200,906200-907400                      S22028
*C002080-002110,002130,002150,002170,002200,002220,002440        S22028
*C002520-002530,002880,002950,003090-003100,003240               S22028
*C038000-038500,455940-455960,456486-456523,457214-457266        S22028
*D106000-126000,928000                                           S22028
*C216000,260300,300000,350600-351200,456486,456965-456969        S22029
*C457214,457400-457600,496000-496800,498400,554600,555200-555800 S22029
*C923000                                                         S22029
*D260900,351800,456973,497600                                    S22029
*A784000                                                         A44022
*C038000,171000,275200,405920,455800,457086                      A44887
*D002044,944000                                                  A44887
*C394000,396000                                                   M0891
*C522000,524000                                                   M0891
*C576600                                                          M0891
*A581000,909700                                                  S22025
*C010000-018000,032000,038000,149000,171050,455920,456799,456809 S22025
*C456859,457116,690000,784500-785700,842000,843000,922000-926000 S22025
*C580000,582000                                                  S22025
*D039000                                                         S22025
*A785800,785900                                                 SA57345
*D212880-213120,298880-299120,456494-456497                     SA61067
*C070000-080000,D072000-096000C141770,D141800-147000,A144770    SA60815
*D432000-453000,A432000,442000,A597300-597600,602500-603500     SA60815
*A624400-625600D668000-720000,A668000-688000                   SA60815
*    CLEAN-UP                                                    S21903
* D268000-273600,457053-457077                                 @SA73375
*A003845,813600,818200                                          SA66950
*C002052,002630,002820,002880,002885,002930,002950,003210,003240,Y06327
*C003310,040000,213600,213680,299600,299680,405800,455934,455936,Y06327
*C456500-456508,457257-457332,584000,770000,812000               Y06327
*A002935,003852-003858,140636-140654,140870,181273-181291,       Y06327
*A213682-213692,287274-287294,299682-299692,455933,455935,455937,Y06327
*A791630-791990,811500,960600                                    Y06327
*A003840,813000,818000                                         @SA66950
*D791630-824000                                                @SA70677
*A784000,790100                                                @YA11170
*D790100-791500                                                @YA11170
*D 003840                                                      @Y17XAYZ
*C138000,140648,181160,213684,287160,299684,455934,456502,     @Y17XAYP
*C457262                                                       @Y17XAYP
*                                                              @G36XRYP
*A598000,608000,785900,932000                                  @G36XRYP
*C140900,141230,181030,181090,212080,212320,287000,287090      @G36XRYP
*C298080,298320,455868,455930,456469,457159,457220             @G36XRYP
*D003720-003750,600000-604000,610000-624000,934000-938000      @G36XRYP
*A181060,287060,644000,688000                                  @OZ24035
*A181066,181080,213520,213560,287036,287060,298240,298280      @OY18014
*A455892,455908,456475,456481,457179,457199                    @OY18014
*A003650                                                       @OZ27742
*C395500-396000,400000,401500,523000-525000,532000,576900      @OZ27742
***********************************************************************
*TITLE  'IEDAYS'  SIMULATED ATTENTION ROUTINE                         *
*                                                                     *
*  MODULE NAME = IEDAYS   (TCAM,TSO)                           @Y17XAYP
*                                                                     *
*  DESCRIPTIVE NAME = SIMULATED ATTENTION ROUTINE                     *
*                                                                     *
*  COPYRIGHT = 'NONE'                                                 *
*                                                                     *
*  STATUS:  VERSION 10.0                                       @G36XRYP
*                                                                     *
*FUNCTION -- THE SIMULATED ATTENTION ROUTINE HANDLES SIMULATED        *
*   ATTENTIONS FOR TSO.  THE THREE WAYS OF SIMULATING ATTENTIONS ARE  *
*   (1) BY CHARACTER STRING/PA1 KEY(3270), (2) BY LINE COUNT, AND (3) *
*   BY TIME DELAY INTERVAL.                                           *
*      FOR SIMULATED ATTENTION BY CHARACTER STRING/PA1 KEY, THIS      *
*   ROUTINE FIRST CHECKS TO SEE IF THE TS BUFFER WAS READ BY THE      *
*   SIMULATED ATTENTION READ CHANNEL PROGRAM.  IF IT WAS NOT, A CHECK *
*   IS MADE TO SEE IF THE DEVICE IS A 2260 OR 3270.                   *
*   IF IT IS, AND THE SCREEN                                          *
*   IS FULL, THE ERASE DISPLAY REQUEST BIT IN THE SCB ERROR WORD IS   *
*   SET ON.  THE BUFFER IS THEN SCANNED FOR A CLEAR CHARACTER STRING/ *
*   CLEAR ENTRY KEY(3270),                                            *
*   WHICH INDICATES THAT THE SCREEN IS TO BE ERASED IMMEDIATELY.  IF  *
*   THIS STRING/KEY IS PRESENT, THE BUFFER IS POSTED TO BUF-          *
*   FER RETURN, AND NO ADDITIONAL SIMULATED ATTENTION PROCESSING      *
*   TAKES PLACE.                                                      *
*      IF THE DEVICE IS NOT A 2260 OR 3270,                           *
*   OR IF NO CLEAR CHARACTER STRING/KEY                               *
*   IS PRESENT IN THE BUFFER, THIS ROUTINE CHECKS THE QCB TO SEE IF   *
*   SIMULATED ATTENTION BY CHARACTER STRING/PA1 KEY                   *
*   WAS REQUESTED.  IF IT                                             *
*   WAS NOT, CONTROL IS RETURNED TO THE RETURN INTERFACE.  CONTROL IS *
*   ALSO RETURNED IMMEDIATELY IF IT WAS REQUESTED, BUT NO VALID SIMU- *
*   LATED ATTENTION CHARACTER STRING IS FOUND IN THE BUFFER.  IF THE  *
*   CHARACTER STRING IS FOUND, THIS ROUTINE CHECKS TO SEE IF A VALID  *
*   ATTENTION LEVEL (A NUMBER, 1 THROUGH 9, FOLLOWED BY A CARRIAGE    *
*   RETURN OR NEW LINE SYMBOL) WAS ALSO ENTERED.  IF IT WAS, IT IS    *
*   SET IN THE SCB.  IF NONE WAS ENTERED, AN ATTENTION LEVEL OF ZERO  *
*   IS SET IN THE SCB.  AN INVALID ATTENTION LEVEL CAUSES AN IMMEDI-  *
*   ATE RETURN TO THE RETURN INTERFACE.  AFTER SETTING THE ATTENTION  *
*   LEVEL, THIS ROUTINE SETS ON THE SIMULATED ATTENTION REQUEST BIT   *
*   IN THE SCB ERROR WORD, POSTS THE BUFFER TO BUFFER RETURN, AND     *
*   RETURNS CONTROL VIA THE RETURN INTERFACE.                         *
*      IF THE BUFFER WAS READ BY THE SIMULATED ATTENTION READ CHANNEL *
*   PROGRAM, THE SIMULATED ATTENTION READ BIT IN THE DESTINATION QCB  *
*   IS SET OFF.  IN THIS CASE, A SIMULATED ATTENTION CHARACTER STRING *
*   IS NOT REQUIRED.  IF ONE WAS ENTERED, IT IS CHECKED FOR VALIDITY, *
*   AND THE ATTENTION LEVEL IS ALSO CHECKED.  HOWEVER, IN THIS CASE,  *
*   AN INVALID CHARACTER STRING OR ATTENTION LEVEL DOES NOT PREVENT   *
*   THE BUFFER FROM BEING POSTED.  INSTEAD, AN ATTENTION LEVEL OF     *
*   ZERO IS SET, AND POSTING AND RETURN OCCUR AS STATED ABOVE.  ALSO, *
*   IN THIS CASE, THE ERASE DISPLAY REQUEST BIT IS AUTOMATICALLY SET  *
*   ON FOR 2260'S OR 3270'S, BUT NO CHECK FOR A CLEAR CHARACTER       *
*   STRING/KEY IS MADE.                                               *
*      FOR SIMULATED ATTENTION BY LINE COUNT, EACH PHYSICAL OUTPUT    *
*   LINE IS COUNTED, AND THE COUNT KEPT IN THE DESTINATION QCB.  WHEN *
*   THE THRESHOLD SPECIFIED BY THE USER IN THE TERMINAL STATUS BLOCK  *
*   IS REACHED, A SPECIAL SIMULATED ATTENTION CHANNEL PROGRAM IS RE-  *
*   QUESTED BY SETTING ON THE SIMULATED ATTENTION READ REQUEST BIT IN *
*   THE QCB AND SETTING THE OUTPUT LINE COUNT TO ZERO BEFORE RETURN-  *
*   ING TO THE EDIT ROUTINE.  THIS IS ALSO DONE WHEN THE DEVICE IS A  *
*   2260 OR 3270 WITH A FULL SCREEN, EVEN THOUGH THE THRESHOLD MAY    *
*   NOT HAVE BEEN REACHED.                                            *
*      FOR SIMULATED ATTENTION BY TIME DELAY INTERVAL, A QCB IS       *
*   POSTED TO THE TIME DELAY QUEUE WHEN NO I/O IS IN PROGRESS WITH    *
*   THE TERMINAL.  IF NO INPUT OR OUTPUT IS REQUESTED BEFORE THE TIME *
*   DELAY INTERVAL EXPIRES, THE TIME DELAY REMOVAL ROUTINE (IEDAYY)   *
*   REMOVES THE QCB FROM THE TIME DELAY QUEUE, AND THE SIMULATED      *
*   ATTENTION ROUTINE IS DISPATCHED.  IF SIMULATED ATTENTION BY TIME  *
*   DELAY WAS NOT REQUESTED IN THE QCB, CONTROL IS RETURNED TO THE    *
*   DISPATCHER TO DISPATCH THE NEXT SUBTASK.  OTHERWISE, THE SIMU-    *
*   LATED ATTENTION READ REQUEST BIT IN THE QCB IS SET ON, AND, IF    *
*   THE LCB/PLCB IS FREE, IT IS POSTED TO ITSELF. THIS ROUTINE THEN   *
*   BRANCHES TO THE TSO MESSAGE GENERATION ROUTINE.                   *
*                                                                     *
*ENTRY POINTS -- IEDAYS - TO HANDLE SIMULATED ATTENTION BY CHARACTER  *
*   STRING.                                                           *
*   CALLING SEQUENCE          L    R12,AVTMSGS+OFFSET                 *
*                             BR   R12                                *
*   IEDAYS2 - TO HANDLE SIMULATED ATTENTION BY LINE COUNT.            *
*   CALLING SEQUENCE          L    R15,TSISIMAT                       *
*                             BAL  R14,16(R15)                        *
*   IEDAYS3 - TO HANDLE SIMULATED ATTENTION BY TIME DELAY INTERVAL.   *
*   CALLING SEQUENCE          L    R15,TSIDYQCB                       *
*                             BR   R15                                *
*                                                                     *
*INPUT -- IEDAYS IS ENTERED AT ENTRY POINT IEDAYS FROM THE TCAM USER  *
*   INTERFACE ROUTINE (IEDQUI) WHEN A SIMATTN MACRO INSTRUCTION IS    *
*   CODED IN A INBUF SUBGROUP OF AN MH.  AT ENTRY, THE FOLLOWING      *
*   REGISTERS ARE SET.                                                *
*   R3 HAS THE SCB ADDRESS.                                           *
*   R4 HAS THE LCB/PLCB ADDRESS.                                      *
*   R6 HAS THE CURRENT TCAM BUFFER ADDRESS.                           *
*   R9 HAS THE AVT ADDRESS.                                           *
*   R12 HAS THE ENTRY POINT ADDRESS.                                  *
*                                                                     *
*      IEDAYS IS ENTERED AT ENTRY POINT IEDAYS2 FROM THE TIOC EDIT    *
*   ROUTINE (IEDAYV/IEDAYW) OR THE TIOC 3270 EDIT ROUTINE (IEDAYB)    *
*   WHEN THE CURRENT BUFFER CONTAINS THE END OF AN                    *
*   OUTPUT LINE (NEW LINE SYMBOL IN BUFFER, OR BUFFER SATISFIES LINE  *
*   SIZE SPECIFIED IN THE TERMINAL STATUS BLOCK).  AT ENTRY, THE FOL- *
*   LOWING REGISTERS ARE SET.                                         *
*   R3 HAS THE SCB ADDRESS.                                           *
*   R4 HAS THE LCB/PLCB ADDRESS.                                      *
*   R5 HAS THE QCB ADDRESS.                                           *
*   R13 HAS THE AVT ADDRESS.                                          *
*   R14 HAS THE RETURN ADDRESS IN IEDAYV/IEDAYW OR IEDAYB.            *
*   R15 HAS THE ENTRY POINT ADDRESS.                                  *
*                                                                     *
*      IEDAYS IS ENTERED AT ENTRY POINT IEDAYS3 FROM THE TCAM DIS-    *
*   PATCHER WHEN A QCB IS REMOVED FROM THE TIME DELAY QUEUE, WITH THE *
*   FOLLOWING REGISTERS SET.                                          *
*   R1 HAS THE ADDRESS OF THE QCB REMOVED FROM THE TIME DELAY QUEUE.  *
*   R11 HAS THE DISPATCHER ADDRESS.                                   *
*   R13 HAS THE ADDRESS OF AVTSAVE2.                                  *
*   R15 HAS THE ENTRY POINT ADDRESS.                                  *
*                                                                     *
*OUTPUT -- FOR SIMULATED ATTENTION BY VALID CHARACTER STRING, THE     *
*   SIMULATED ATTENTION REQUEST BIT AND ATTENTION LEVEL ARE SET IN    *
*   THE SCB, AND THE BUFFER IS POSTED TO BUFFER RETURN.  IF APPLICA-  *
*   BLE FOR A 2260 OR 3270, THE ERASE DISPLAY REQUEST BIT IS ALSO SET *
*   IN THE SCB.                                                       *
*      FOR SIMULATED ATTENTION BY LINE COUNT, THE SIMULATED ATTENTION *
*   READ REQUEST BIT IN THE QCB IS SET, AND THE QCB LINE COUNT IS SET *
*   TO ZERO.  IF THE LINE COUNT THRESHOLD HAS NOT BEEN REACHED, THE   *
*   BIT IS NOT SET, AND THE QCB LINE COUNT IS INCREMENTED BY ONE.     *
*      FOR SIMULATED ATTENTION BY TIME DELAY INTERVAL, THE SIMULATED  *
*   ATTENTION READ REQUEST BIT IN THE QCB IS SET, AND THE LCB IS      *
*   POSTED TO ITSELF.  REGISTERS 1 AND 9 ARE CLEARED TO ZERO, AND     *
*   REGISTER 12 CONTAINS THE ENTRY POINT IN THE TSO MSGGEN ROUTINE.   *
*                                                                     *
*EXTERNAL ROUTINE -- IEDQTNT - TO GET THE TERMINAL ENTRY FROM THE     *
*   INDEX IN THE LCB OR THE RESOURCE ID TABLE (FOR 3705).             *
*                                                                     *
*EXITS-NORMAL -- TO THE MH VIA THE RETURN INTERFACE ROUTINE (IEDQLM). *
*   TO THE TIOC EDIT ROUTINE (IEDAYV-3705/IEDAYW-270X).               *
*   TO THE TIOC 3270 EDIT ROUTINE (IEDAYB).                           *
*   TO THE DISPATCHER DISPATCH FUNCTION.                              *
*   TO THE TSO MESSAGE GENERATION ROUTINE (IEDAYM).                   *
*                                                                     *
*EXITS-ERROR -- NONE                                                  *
*                                                                     *
*TABLES/WORKAREAS -- CVT, TSB, LCB, SCB, AVT, QCB, TSID, DCB,  @G36XRYP
*   PLCB, TERMINAL ENTRY, CURRENT BUFFER PREFIX,ASCB,ASVT.     @G36XRYP
*                                                                     *
*ATTRIBUTES -- SERIALLY REUSABLE, REFRESHABLE, ENABLED, RESIDENT,     *
*   PROBLEM PROGRAM MODE.                                             *
*                                                                     *
*NOTES -- THE OPERATION OF THIS MODULE DOES NOT DEPEND UPON A         *
*   PARTICULAR INTERNAL REPRESENTATION OF THE EXTERNAL CHARACTER      *
*   SET.                                                              *
*                                                                     *
***********************************************************************
         SPACE 2
*  REGISTER EQUATES.
         SPACE 2
R0       EQU   0                        REGISTER ZERO            S21903
R1       EQU   1                        REGISTER ONE             S21903
RCNT     EQU   2                        REGISTER TWO             S21903
RSCB     EQU   3                        REGISTER THREE           S21903
RLCB     EQU   4                        REGISTER FOUR            S21903
RTRM     EQU   5                        REGISTER FIVE            S21903
RPRF     EQU   6                        REGISTER SIX             S21903
RQCB     EQU   7                        REGISTER SEVEN           S21903
RWRK     EQU   8                        REGISTER EIGHT           S21903
RAVT     EQU   9                        REGISTER NINE            S21903
RTSB     EQU   10                       REGISTER TEN             S21903
RDSP     EQU   11                       REGISTER ELEVEN          S21903
RDATA    EQU   11                       REGISTER 11              Y06327
RBAS     EQU   12                       REGISTER TWELVE          S21903
RSAV     EQU   13                       REGISTER THIRTEEN        S21903
R14      EQU   14                       REGISTER FOURTEEN        S21903
RIC      EQU   15                       REGISTER FIFTEEN         S21903
         SPACE 2
*  OTHER EQUATES.
         SPACE 2
ZERO     EQU   X'00'                    HEX ZERO                 S21903
ONE      EQU   X'01'                    HEX ONE                  S21903
LEVMASK  EQU   X'0F'                    REMOVE BITS 0-3          S21903
LEVEL1   EQU   X'10'                    DEFAULT LEVEL ONE      @OZ27742
ENDTEXT  EQU   X'03'                    ETX                      S21903
NEWLINE  EQU   X'0D'                    NEW LINE CHARACTER       S21903
CRGRTRN  EQU   X'15'                    CARRIAGE RETURN          S21903
CHAR1    EQU   C'1'                     ONE                      S21903
CHAR9    EQU   C'9'                     NINE                     S21903
MASKALL  EQU   X'FF'                    ALL ONES                 S21903
SBACHAR  EQU   X'11'                    3270 SET BUFFER          S22028
PA1AID   EQU   X'6C'                    3270 PA1 KEY             S22028
CLEARAID EQU   X'6D'                    3270 CLEAR KEY           S22028
ENTERAID EQU   X'7D'                    3270 ENTER KEY           S22028
AUTOPL   EQU   X'01'                    AUTOPOLL INDICATOR       S22028
TWENTY1  EQU   X'21'                    HEX TWENTY-ONE           S22028
PRIORITY EQU   X'E4'                    PRIORITY                 S21903
BLANK    EQU   X'40'                    BLANK                    S21903
DELAY    EQU   X'02'                   LCB TIME DELAY BIT       SA66950
FOUR     EQU   4                        ENTRY POINT FOR RETURN   S22025
LGBINDIC EQU   X'80'                    LGB FLAG                 Y06327
RNCLRAID EQU   X'2C'                    3705 AID BYTE DISPL.     Y06327
ONEMORE  EQU   1                        DECIMAL ONE              Y06327
TWO      EQU   2                        DECIMAL TWO              Y06327
         EJECT                                                   S21903
         SPACE 2
         BALR  RBAS,0                   LOAD BASE REGISTER
         USING *,RBAS                   ADDRESSABILITY FOR MODULE
BEGIN    B     IEDAYS0                  BRANCH AROUND DC'S       S22025
         NOPR  0                        FULL WORD BOUNDRY        S22025
         DC    AL1(DSPMCPL4),AL3(*-1)   STCB FOR AYSAYS3         S22025
         USING *,RIC                    ADDRESSIBILITY FOR       S22025
IEDAYS3  B     AYSAYS3                  BRANCH TO AYSAYS3 ROUTINES22025
         DROP  RBAS
         USING IEDAYS,RIC
IEDAYS2  STM   14,12,AVTSAVE3-AVTSAVE2+12(13)  SAVE REGISTERS
         L     RBAS,AYSAYS2             LOAD BASE REGISTER
         BR    RBAS                     BRANCH TO COUNT ROUTINE
         CNOP  0,4
AYSAYS2  DC    A(AYS201)                ADDR OF AYS201 ROUTINE   S22025
         DROP  RIC
         USING BEGIN,RBAS
IEDAYS   IEDHJN ,                       ID AND DATE              S22025
         USING IEDQLCB,RLCB             ADDRESS. FOR LCB/PLCB    Y06327
         USING IEDQSCB,RSCB             ADDRESSABILITY FOR SCB
         USING IEDQAVTD,RAVT            ADDRESSABILITY FOR AVT
         USING IEDQQCB,RQCB             ADDRESSABILITY FRO QCB
         USING IEDQTRM,RTRM             ADDRESSABILITY FOR TRM ENTRY
         USING IEDQPRF,RPRF             ADDRESSABILITY FOR BUFFER
IEDAYS0  LH    R1,LCBTTCIN              GET INDEX TO TERM ENTRY
         L     RIC,AVTRNMPT             ADDRESS OF TERM NAME TABLE
         BALR  R14,RIC                  BRANCH AND LINK TO GET
*                                       TERM ENTRY ADDRESS
         LR    RTRM,R1                  RETURNED IN REGISTER 1
         L     RQCB,TRMDESTQ-1          GET ADDRESS OF DEST QCB
         TM    QCBFLAG,QCBTSSES         IS TSO SESSION IN PROGRESS
         BNO   AYS104                   NO, RETURN
         BAL   RWRK,AYSTSB         FIND TSB IF TSO ACTIVE       SA60815
         B     AYS104              TSO NOT IN SYSTEM            SA60815
         USING TSB,RTSB
         SPACE 2
         TM    LCBSTAT1,LCBRECVN        IS LINE RECEIVING
         BNO   AYS104                   NO, RETURN
         SPACE 2
         DROP  RTRM                     FREE TTE BASE REG      @Y17XAYO
         LA    RWRK,TRMPRFSZ            GET PREFIX LENGTH      @Y17XAYO
         SR    RTRM,RWRK                BACK UP TRM BASE PTR   @Y17XAYO
         USING IEDNTRM,RTRM             IEDDCT MUST HAVE PREFIX@Y17XAYO
         IEDDCT REG=RWRK,FLD=AVTPARM3                          @Y17XAYO
         LA    RTRM,TRMPRFSZ(,RTRM)     RESET TTE BASE         @Y17XAYO
         DROP  RTRM                     FREE TTE BASE REG      @Y17XAYO
         USING IEDQTRM,RTRM             BASE OF TTE ADDR       @Y17XAYO
         SR    RIC,RIC                  CLEAR REGISTER           S22028
         MVI   AVTPARM3,ZERO            CLEAR STORAGE AREA       Y06327
         TM    TRMSTATE,TRMPREF         IS THERE A PREFIX      @Y17XAYP
         BO    AYS300B                  YES. BRANCH              Y06327
*                                       NO - NOT A 3705 TERMINAL Y06327
         IC    RIC,LCBUCBX              PICK UP RLN              S22028
         SLL   RIC,2                    MULTIPLY BY FOUR         S22028
         L     R14,LCBDCBPT             GET DCB ADDR             S22028
         USING IHADCB,R14               DCB ADDRESSABILITY       S22028
         L     R14,DCBINVLI(RIC)        GET INVITATION LIST ADDR S22028
         DROP  R14                      DROP ADDRESSABILITY      S22028
         MVC   AVTPARM3(1),3(R14)       SAVE CONTROL WORD        S22028
AYS300B  EQU   *                                                 Y06327
         TM    AVTPARM3+1,DCT3270       IS THIS A 3270 TRM     @G36XRYP
         BNO   AYS310A                  NO, BRANCH               S22028
         TM    AVTPARM3+2,DCTLOCAL      IS THIS A 3270 LOCAL   @G36XRYP
         BO    AYS310A                  YES, BRANCH              S22028
         TM    PRFSTAT1,PRFNHDRN        IS THIS HEADER BUFFER    S22028
         BO    AYS310A                  NO, BRANCH               S22028
         TM    SCBERR2,SCBSOHE          IS THIS 3270 SOH%R MSG   S22028
         BO    AYS104                   YES, BRANCH              S22028
AYS310A  EQU   *                                                 S22028
         SR    RDATA,RDATA              CLEAR REGISTER           Y06327
         IC    RDATA,LCBISZE            GET NUMBER OF IDLES      Y06327
         AR    RDATA,RPRF               ADD TO BUFFER START      Y06327
         NI    QCBTSOF2,MASKALL-QCBDSSMI SET BUFFER RECEIVED     S22028
         TM    QCBTSOF1,QCBSATRD        WAS BUFFER READ BY SIM   S22028
*                                       ATTN CHAN PROGRAM        S22028
         BO    AYS107                   YES, CHECK CONTENTS      S22028
         SPACE 2
*  IF THE DEVICE IS A DISPLAY, THE LINE COUNT IS CHECKED TO      S22028
*  DETERMINE IF DATA HAS BEEN ENTERED INTO THE NEXT TO LAST      S22028
*  LINE OF THE DISPLAY.  IF SO, A SCREEN ERASURE IS REQUESTED.   S22028
         SPACE 2
         TM    TSBSTAT,TSBDSPLY         IS THIS A DISPLAY       SA60815
         BO    AYS310                   BRANCH YES              SA60815
         MVI   QCBSATCT,AVTEZERO        RESET LINE COUNT
         B     AYS307                   BYPASS 2260 SCREEN ERASE S22025
AYS310   EQU   *
         TM    PRFSTAT1,PRFNLSTN        IS THIS LAST BUFFER
         BO    AYS311                   BRANCH ON NO
         SR    R14,R14                  CLEAR REGISTER             2260
         SR    RIC,RIC                  CLEAR REGISTER
         IC    RIC,TSBLNNO              GET NUMBER OF LINES ON     2260
*                                       DISPLAY SCREEN             2260
         BCTR  RIC,0                    REDUCE LINE COUNT BY ONE   2260
         IC    R14,QCBSATCT             GET NUMBER OF LINES ENTERED2260
         CR    R14,RIC                  IS NUMBER OF LINES ENTERED 2260
*                                       EQUAL TO NUMBER OF LINES   2260
*                                       ON DISPLAY MINUS ONE       2260
         BL    AYS311                   NO, DON'T ERASE
         BH    AYS312                   NO, SCREEN WRAPPED
         OI    QCBTSOF1,QCBSATRD        REQUEST SIMULATED ATTENTION
         B     AYS311                   CONTINUE
AYS312   EQU   *
         OI    SCBSTATE,SCBERSDS        REQUEST SCREEN ERASURE   A44887
         TM    SCBERR1,SCBCUTFN         WAS THERE CUTOFF ERROR
         BO    AYS312A                  BRANCH ON YES
         TM    SCBERR4,SCBTXTTN         WAS THERE A TEXT ERROR
         BO    AYS312A                  BRANCH ON YES
         B     AYS312B                  BRANCH IF NO ERROR       S22025
AYS312A  EQU   *
         MVI   QCBSATCT,0               PREVENT FLASHBACK OF INPUT
AYS312B  EQU   *
         MVI   QCBCARCT,0               CLEAR CARRIAGE COUNT
         NI    QCBRETCT,MASKALL-QCBNL   RESET FLAGS
         SPACE 2
*  THE NUMBER OF CHARACTERS IN THE CLEAR CHARACTER STRING IS
*  CALCULATED.
         SPACE 2
AYS311   EQU   *
         LA    RWRK,PRFSHDR-IEDQPRF(RDATA) POINT TO START OF DATAY06327
         TM    PRFSTAT1,PRFNHDRN        IS THIS HEADER BUFFER
         BO    AYS104                   BRANCH ON NO
         TM    AVTPARM3+1,DCT3270       IS THIS A 3270 TRM     @G36XRYP
         BZ    AYS311B                  NO, BRANCH               S22028
         TM    TSBFLG5,TSBKEYS          TSO SESSION MANAGER    @OZ24035
         BO    AYS307                   YES, BRANCH            @OZ24035
         TM    TRMSTATE,TRMPREF         IS THERE A PREFIX      @OY18014
         BNO   AYS311C                  NO, BRANCH             @OY18014
         TM    LCBSTAT5,LCBLUNIT        IS THIS A LU           @YM09070
         BO    AYS311A                  YES, BRANCH            @YM09070
AYS311C  EQU   *                                               @OY18014
         TM    AVTPARM3+2,DCTLOCAL      IS THIS A 3270 LOCAL   @G36XRYP
         BO    AYS311A                  YES, BRANCH              Y06327
         LA    RWRK,TWO(RWRK)           SKIP TO PROPER CHAR      Y06327
         TM    TRMSTATE,TRMPREF         IS THERE A PREFIX      @Y17XAYP
         BO    AYS311A                  YES, BRANCH              Y06327
         LA    RWRK,ONEMORE(RWRK)       SKIP TO PROPER CHAR      Y06327
         TM    AVTPARM3,AUTOPL          IS THIS AUTOPOLL         Y06327
         BZ    AYS311A                  NO, BRANCH               Y06327
         LA    RWRK,ONEMORE(RWRK)       SKIP TO PROPER CHAR      Y06327
AYS311A  EQU   *                                                 Y06327
         CLI   0(RWRK),CLEARAID         IS THIS AID FOR CLEAR    Y06327
         BE    AYS309                   YES, BRANCH              S22028
AYS311B  EQU   *                                                 S22028
         L     RWRK,TSBERSDS           IS CLEAR CHARACTER
         LTR   RWRK,RWRK                STRING PRESENT
         BZ    AYS307                   NO, CHECK FOR SIMULATED    2260
*                                       ATTENTION REQUESTED        2260
         LA    RCNT,4                   MAXIMUM NUMBER OF CHARS    2260
         LA    RWRK,TSBERSDS            POINT TO CLEAR CHARACTER   2260
*                                       STRING                     2260
AYS301   CLI   0(RWRK),BLANK            IS THE CHARACTER A BLANK
         BE    AYS302                   YES, CALCULATE NUMBER      2260
*                                       OF CHARACTERS PRESENT      2260
         LA    RWRK,1(RWRK)             POINT TO NEXT CHARACTER    2260
         BCT   RCNT,AYS301              BRANCH IF NOT ALL CHECKED  2260
AYS302   LA    RIC,3                    MAXIMUM NUMBER OF CHARS    2260
*                                       MINUS ONE                  2260
         SR    RIC,RCNT                 CALCULATE NUMBER PRESENT   2260
         SPACE 2
*  THE INPUT BUFFER IS CHECKED FOR THE CLEAR CHARACTER STRING.
         TM    AVTPARM3+1,DCT3270       IS THIS A 3270 TRM     @G36XRYP
         BZ    AYS302C                  NO, BRANCH               S22028
         LA    RWRK,PRFSHDR-IEDQPRF(RDATA) POINT TO DATA START   Y06327
         TM    AVTPARM3+2,DCTLOCAL      IS THIS A 3270 LOCAL   @G36XRYP
         BZ    AYS302B                  NO, MUST BE REMOTE       S22028
AYS302A  EQU   *                                                 S22028
         CLI   3(RWRK),SBACHAR          IS THIRD CHARACTER AN    S22028
*                                       SBA OR A DATA CHARACTER  S22028
         BNE   AYS302D                  NOT SBA, GO SCAN FOR     S22028
*                                       CLEAR CHARACTER STRING   S22028
         LA    RWRK,8(RWRK)             IF SBA PRESENT SKIP TO   S22028
*                                       FIRST BYTE OF DATA + 2   S22028
         B     AYS302D                  NOW GO SCAN FOR CLEAR    S22028
*                                       CHARACTER STRING         S22028
AYS302B  EQU   *                                                 S22028
         TM    TRMSTATE,TRMPREF         IS THERE A PREFIX      @OY18014
         BNO   AYS302BB                 NO, BRANCH             @OY18014
         TM    LCBSTAT5,LCBLUNIT        IS THIS A LU           @YM09070
         BO    AYS302A                  YES,BRANCH             @YM09070
AYS302BB EQU   *                                               @OY18014
         LA    RWRK,TWO(RWRK)           ASSUME 3705 TERMINAL AND Y06327
*                                       POINT TO FIRST BYTE IF   Y06327
*                                       DATA OR TO AID BYTE      Y06327
         TM    TRMSTATE,TRMPREF         IS THERE A PREFIX      @Y17XAYP
         BO    AYS302BA                 YES. BRANCH              Y06327
         LA    RWRK,ONEMORE(RWRK)       NOT A 3705 TERMINAL      Y06327
*                                       INCLUDE STX BYTE         Y06327
AYS302BA EQU   *                                                 Y06327
         TM    AVTPARM3,AUTOPL          IS THIS AUTOPOLL         S22028
         BZ    AYS302A                  NO, BRANCH               S22028
         LA    RWRK,1(RWRK)             SKIP INDEX BYTE          S22028
         B     AYS302A                  GO CHECK FOR SBA         S22028
AYS302C  EQU   *                                                 S22028
         SPACE 2
         LA    RWRK,PRFSHDR-IEDQPRF+2(RDATA) POINT TO DATA START Y06327
*                                       DATA PLUS TWO              2260
AYS302D  EQU   *                                                 S22028
         LA    RCNT,3                   NUMBER OF CHECKS TO MAKE   2260
AYS303   EX    RIC,COMPAR2              IS CLEAR STING PRESENT     2260
         BE    AYS304                   YES, SET UP TO ERASE       2260
         BCTR  RWRK,0                   BACK UP ONE CHARACTER      2260
         BCT   RCNT,AYS303              BRANCH IF NOT ALL CHECKED  2260
         B     AYS307                   CLEAR CHARACTER STRING     2260
*                                       NOT FOUND, BRANCH          2260
         SPACE 2
*  IF THE CLEAR CHARACTER STRING IS PRESENT, A SCREEN ERASURE
*  IS REQUESTED.
         SPACE 2
AYS304   BCT   RCNT,AYS305              BRANCH IF STRING NOT       2260
*                                       FIRST IN BUFFER            2260
         B     AYS306                   BRANCH IF STRING FIRST     2260
AYS305   BCTR  RWRK,0                   POINT TO CHARACTER IN      2260
*                                       FRONT OF STRING            2260
         CLI   0(RWRK),ZERO             IS THE CHARACTER ZERO      2260
         BNE   AYS307                   NO, INVALID STRING         2260
         LA    RWRK,1(RWRK)             RESET POINTER              2260
AYS306   LA    RWRK,1(RIC,RWRK)         POINT TO CHARACTER AFTER
*                                       CLEAR STRING               2260
         LA    RCNT,PRFSUNIT            ADDR OF HDR PREFIX       S22029
         AH    RCNT,PRFSIZE             ADD DATA SIZE PLUS HDR   S22029
*                                       PREFIX SIZE              S22029
         CR    RCNT,RWRK                IS THIS END OF DATA
         BE    AYS309                   BRANCH ON YES
         CLI   0(RWRK),CRGRTRN          IS IT A CARRIAGE RETURN    2260
         BE    AYS309                   BRANCH ON YES
         CLI   0(RWRK),NEWLINE          IS IT A NEW LINE CHAR
         BNE   AYS307                   BRANCH ON NO
AYS309   OI    SCBSTATE,SCBERSDS        REQUEST SCREEN ERASURE   A44887
         MVI   QCBSATCT,AVTEZERO        CLEAR LINE COUNT
         NI    QCBRETCT,MASKALL-QCBNL   CLEAR CARRIAGE FLAGS
         MVI   QCBCARCT,ZERO            CLEAR CARRIAGE COUNT
         NI    QCBTSOF1,MASKALL-QCBSATRD NO SIMATTN NEEDED
         B     AYS110                   BRANCH TO RETURN BUFFER    2260
         SPACE 2
*  THE FOLLOWING CODE CHECKS TO SEE IF SIMULATED ATTENTION BY
*  CHARACTER STRING HAS BEEN REQUESTED.
         SPACE 2
AYS307   EQU   *
         LA    RWRK,PRFSHDR-IEDQPRF(RDATA) POINT TO DATA START   Y06327
         TM    PRFSTAT1,PRFNHDRN       IS THIS HEADER BUFFER
         BO    AYS104                  BRANCH ON NO
         TM    AVTPARM3+1,DCT3270       IS THIS A 3270 TRM     @G36XRYP
         BZ    AYS307B                  NO, BRANCH               S22028
         TM    TSBFLG5,TSBKEYS          TSO SESSION MANAGER    @OZ24035
         BO    AYS104                   YES, BRANCH            @OZ24035
         TM    TRMSTATE,TRMPREF         IS THERE A PREFIX      @OY18014
         BNO   AYS307G                  NO, BRANCH             @OY18014
         TM    LCBSTAT5,LCBLUNIT        IS THIS A LU           @YM09070
         BO    AYS307A                  YES, BRANCH            @YM09070
AYS307G  EQU   *                                               @OY18014
         TM    AVTPARM3+2,DCTLOCAL      IS THIS A 3270 LOCAL   @G36XRYP
         BO    AYS307A                  YES, BRANCH              Y06327
         LA    RWRK,TWO(RWRK)           SKIP TO PROPER CHAR      Y06327
         TM    TRMSTATE,TRMPREF         IS THERE A PREFIX      @Y17XAYP
         BO    AYS307A                  YES, BRANCH              Y06327
         LA    RWRK,ONEMORE(RWRK)       SKIP TO PROPER CHAR      Y06327
         TM    AVTPARM3,AUTOPL          IS THIS AUTOPOLL         Y06327
         BZ    AYS307A                  NO, BRANCH               Y06327
         LA    RWRK,ONEMORE(RWRK)       SKIP TO PROPER CHAR      Y06327
AYS307A  EQU   *                                                 Y06327
         CLI   0(RWRK),PA1AID           IS THIS AID FRO PA1      Y06327
         BE    AYS105A                  YES, BRANCH              S22028
AYS307B  EQU   *                                                 S22028
         TM    QCBTSOF2,QCBSATCH       IS SIMULATED ATTENTION BY
*                                       CHARACTER STRING REQUESTED
         BNO   AYS104                   NO, BRANCH
         SPACE 2
*  THE FOLLOWING CODE DETERMINES IF THE CHARACTER STRING SPECIFIED FOR
*  SIMULATED ATTENTION IS PRESENT IN THE DATA BUFFER.
         SPACE 2
         TM    AVTPARM3+1,DCT3270       IS THIS A 3270 TRM     @G36XRYP
         BZ    AYS307E                  NO, BRANCH               S22028
         LA    RWRK,PRFSHDR-IEDQPRF(RDATA) POINT TO DATA START   Y06327
         TM    TRMSTATE,TRMPREF         IS THERE A PREFIX      @OY18014
         BNO   AYS307BA                 NO, BRANCH             @OY18014
         TM    LCBSTAT5,LCBLUNIT        IS THIS A LU           @YM09070
         BO    AYS307C                  YES,BRANCH             @YM09070
AYS307BA EQU   *                                               @OY18014
         TM    AVTPARM3+2,DCTLOCAL      IS THIS A 3270 LOCAL   @G36XRYP
         BZ    AYS307D                  NO, MUST BE REMOTE       S22028
AYS307C  EQU   *                                                 S22028
         CLI   3(RWRK),SBACHAR          IS THIRD CHARACTER AN    S22028
*                                       SBA OR A DATA CHARACTER  S22028
         BNE   AYS307F                  NOT SBA GO SCAN FOR SIM  S22028
*                                       ATTN CHARACTER STRING    S22028
         LA    RWRK,8(RWRK)             IF SBA PRESENT SKIP TO   S22028
*                                       FIRST BYTE OF DATA + 2   S22028
         B     AYS307F                  NOW GO SCAN FOR SIM ATTN S22028
*                                       CHARACTER STRING         S22028
AYS307D  EQU   *                                                 S22028
         LA    RWRK,TWO(RWRK)           ASSUME 3705 TERMINAL AND Y06327
*                                       POINT TO FIRST BYTE OF   Y06327
*                                       DATA OR AID BYTE         Y06327
         TM    TRMSTATE,TRMPREF         IS THERE A PREFIX      @Y17XAYP
         BO    AYS307DA                 YES. BRANCH              Y06327
         LA    RWRK,ONEMORE(RWRK)       NOT A 3705 TERMINAL      Y06327
*                                       INCLUDE STX BYTE         Y06327
AYS307DA EQU   *                                                 Y06327
         TM    AVTPARM3,AUTOPL          IS THIS AUTOPOLL         S22028
         BZ    AYS307C                  NO, BRANCH               S22028
         LA    RWRK,1(RWRK)             SKIP OVER INDEX BYTE     S22028
         B     AYS307C                  GO CHECK FOR SBA         S22028
AYS307E  EQU   *                                                 S22028
         LA    RWRK,PRFSHDR-IEDQPRF+2(RDATA) POINT TO DATA START Y06327
AYS307F  EQU   *                                                 S22028
         IC    RIC,TSBFLG2              GET COUNT OF CHARACTER STRING
         N     RIC,CNTMASK              REMOVE ALL BUT TWO LOW ORDER
*                                       BITS - INDICATES COUNT OF
*                                       CHARACTER STRING MINUS ONE
         LA    RCNT,3                   PUT COUNT OF 3 IN REGISTER
AYS103   EX    RIC,COMPARE              SEARCH FOR CHARACTER STRING
         BE    AYS102                   EQUAL, STRING FOUND
         BCTR  RWRK,0                   BACK UP ONE CHARACTER
         BCT   RCNT,AYS103              BRANCH TO CHECK AGAIN
         B     AYS104                   STRING NOT FOUND
         SPACE 2
*  THE FOLLOWING CODE DETERMINES IF THE CHARACTER STRING IS FOLLOWED
*  BY A CARRIAGE RETURN OR A NUMBER AND A CARRIAGE RETURN.
         SPACE 2
AYS102   BCT   RCNT,AYS113              BRANCH IF CHARACTER STRING
*                                       IS NOT FIRST DATA IN BUFFER
         B     AYS114                   BRANCH IF CHARACTER STRING
*                                       IS FIRST IN BUFFER
AYS113   BCTR  RWRK,0                   POINT TO CHARACTER IN
*                                       FRONT OF STRING
         CLI   0(RWRK),ZERO             IS THE CHARACTER 0
         BNE   AYS104                   NO, STRING INVALID
         LA    RWRK,1(RWRK)             READJUST POINTER TO STRING
AYS114   LA    RWRK,1(RIC,RWRK)         POINT TO CHARACTER AFTER
*                                       CHARACTER STRING
         LA    RCNT,PRFSUNIT            ADDR OF HDR PREFIX       S22029
         AH    RCNT,PRFSIZE             ADD DATA SIZE PLUS HDR   S22029
*                                       PREFIX SIZE              S22029
         CR    RCNT,RWRK                IS THIS END OF DATA
         BE    AYS105                   BRANCH ON YES
         CLC   0(2,RWRK),LFCRTRN        IS IT LINE FEED RETURN
         BE    AYS126                   BRANCH ON YES
         CLC   0(2,RWRK),CRTRNLF        IS IT RETURN LINE FEED
         BE    AYS126                   BRANCH ON YES
         CLI   0(RWRK),CRGRTRN          IS IT A CARRAIGE RETURN
         BE    AYS127                   BRANCH ON YES
         CLI   0(RWRK),NEWLINE          IS IT A NEW LINE
         BE    AYS127                   BRANCH ON YES
         BCTR  RCNT,0                   REDUCE COUNT BY ONE
         CR    RCNT,RWRK                IS THIS END OF DATA
         BE    AYS123                   BEANCH ON YES
         CLC   1(2,RWRK),LFCRTRN        IS IT LINE FEED RETURN
         BE    AYS128                   BRANCH ON YES
         CLC   1(2,RWRK),CRTRNLF        IS IT RETURN LINE FEED
         BE    AYS128                   BRANCH ON YES
         CLI   1(RWRK),CRGRTRN          IS IT A CARRIAGE RETURN
         BE    AYS129                   BRANCH ON YES
         CLI   1(RWRK),NEWLINE          IS IT A NEW LINE
         BE    AYS129                   BRANCH ON YES
         B     AYS104                   BRANCH, INVALID
AYS126   BCTR  RCNT,0                   REDUCE COUNT BY ONE
AYS127   BCTR  RCNT,0                   REDUCE COUNT BY ONE
         CR    RCNT,RWRK                IS THIS LAST CHARACTER
         BE    AYS105                   BRANCH ON YES
         B     AYS104                   BRANCH, INVALID
AYS128   BCTR  RCNT,0                   REDUCE COUNT BY ONE
AYS129   BCTR  RCNT,0                   REDUCE COUNT BY ONE
         CR    RCNT,RWRK                IS THIS LAST CHARACTER
         BNE   AYS104                   BRANCH ON NO
AYS123   CLI   0(RWRK),CHAR1            IS THIS CHARACTER '1' OR
*                                       GREATER
         BL    AYS104                   NO, INVALID REQUEST
         CLI   0(RWRK),CHAR9            IS THIS CHARACTER '9' OR
*                                       SMALLER
         BH    AYS104                   NO, INVALID REQUEST
         SPACE 2
*  IF THE CHARACTER STRING WAS VALID, THE FOLLOWING CODE STORES THE
*  ATTENTION LEVEL (OR ZERO IF NO LEVEL SPECIFIED) IN THE SCB AND
*  RETURNS THE BUFFER TO THE BUFFER RETURN QUEUE.
         SPACE 2
*  AS OF RELEASE 21.0 TSO/TCAM, THE SIMULATED ATTENTION LEVEL     M0891
*  PASSED FROM IEDAYS TO TSO WILL BE IN THE HIGH-ORDER 4 BITS OF  M0891
*  SCBSALEV INSTEAD OF THE LOW ORDER 4. AVOIDS TCAM CLEARING LOW BIT
         SR    RCNT,RCNT                CLEAR WORK REG         @OZ27742
         SR    RIC,RIC                  CLEAR WORK REG         @OZ27742
         IC    RIC,AVTEZERO(,RWRK)      GET SIM ATTN LEVEL REQ @OZ27742
         SLL   RIC,FOUR                 MOVE TO BITS 0-3       @OZ27742
         IC    RCNT,SCBSALEV            SAVE BITS 4-7          @OZ27742
         OR    RIC,RCNT                 COMBINE TWO HALF-BYTES @OZ27742
         STC   RIC,SCBSALEV             STORE IN SCB           @OZ27742
         STC   RIC,SCBSALEV             STORE IN SCB              M0891
         B     AYS106                   BRANCH TO CONTINUE
AYS105   EQU   *                                               @OZ27742
         NI    SCBSALEV,LEVMASK         SET ATTN LEVEL TO ZERO @OZ27742
         B     AYS106                   BRANCH TO CONTINUE       S22028
AYS105A  EQU   *                                                 S22028
         OI    SCBSALEV,LEVEL1          SET ATTN LEVEL TO ONE  @OZ27742
AYS106   OI    SCBERR3,SCBSATTN         INDICATE SIMULATED ATTENTION
*                                       REQUESTED
         TM    TSBSTAT,TSBDSPLY         IS TERMINAL A DISPLAY
         BZ    AYS110                   BRANCH ON NO
         SR    RIC,RIC                  CLEAR REGISTER
         SR    R14,R14                  CLEAR REGISTER
         IC    RIC,TSBLNNO              GET NUMBER OF LINES ON SCREEN
         BCTR  RIC,0                    REDUCE BY ONE
         IC    R14,QCBSATCT             GET NUMBER LINES ENTERED
         CR    R14,RIC                  NEXT TO LAST LINE        Y06327
         BL    AYS110                   BRANCH ON NO
         OI    SCBSTATE,SCBERSDS        REQUEST SCREEN ERASURE   A44887
         NI    QCBTSOF1,MASKALL-QCBSATRD NO SIMATTN PROMPT NEEDED
AYS106A  EQU   *
         NI    QCBRETCT,MASKALL-QCBNL   CLEAR FLAGS
         MVI   QCBSATCT,0               CLEAR LINE COUNT
         MVI   QCBCARCT,0               CLEAR CARRIAGE COUNT
AYS110   LA    RWRK,AVTBFRTB            GET ADDRESS OF BUFFER RETURN
*                                       QUEUE CONTROL BLOCK
         IC    RCNT,SCBSTATE            SAVE FLAG BYTE
         ST    RWRK,SCBDESTQ-1          STORE IN SCB DESTINATION
*                                       QUEUE POINTER TO RETURN BUFFER
         STC   RCNT,SCBSTATE            RESTORE FLAG BYTE
         B     AYS104                   RETURN
         SPACE 2
*  THE FOLLOWING CODE CHECKS TO SEE IF A CARRIAGE RETURN OR A NUMBER
*  AND A CARRIAGE RETURN ARE PRESENT IN DATA READ BY THE SIMULATED
*  ATTENTION CHANNEL PROGRAM.  A CHECK IS ALSO MADE TO DETERMINE IF
*  THE SIMULATED ATTENTION CHARACTER STRING WAS ENTERED.
         SPACE 2
AYS107   EQU   *
         TM    PRFSTAT1,PRFNHDRN       IS THIS HEADER BUFFER
         BO    AYS104                  BRANCH ON NO
         NI    QCBTSOF1,MASKALL-QCBSATRD SET OFF SIMULATED ATTENTION
         NI    QCBTSOF2,MASKALL-QCBSIMRD     READ INDICATORS
         TM    TSBSTAT,TSBDSPLY         IS THIS A DISPLAY       SA60815
         BZ    AYS121                   BRANCH NO               SA60815
         SR    R14,R14                  CLEAR REGISTER
         SR    RIC,RIC                  CLEAR REGISTER
         IC    RIC,TSBLNNO              GET NUMBER OF LINES
         IC    R14,QCBSATCT             GET NUMBER LINES ENTERED
         BCTR  RIC,0                    REDUCE BY ONE
         CR    R14,RIC                  LAST LINE
         BL    AYS115H                  BRANCH ON NO
         OI    SCBSTATE,SCBERSDS        REQUEST SCREEN ERASURE   A44887
         MVI   QCBSATCT,AVTEZERO        CLEAR LINE COUNT
         NI    QCBRETCT,MASKALL-QCBNL   RESET CARRIAGE FLAGS
         MVI   QCBCARCT,0               RESET CARRIAGE COUNT
         B     AYS115X                  CONTINUE PROCESSING      S22025
AYS115H  EQU   *
         TM    AVTPARM3+1,DCT3270       IS THIS A 3270 TRM     @G36XRYP
         BZ    AYS115I                  NO, BRANCH               S22028
         LA    RWRK,PRFSHDR-IEDQPRF(RDATA) POINT TO DATA START   Y06327
         TM    TRMSTATE,TRMPREF         IS THERE A PREFIX      @OY18014
         BNO   AYS115HA                 NO, BRANCH             @OY18014
         TM    LCBSTAT5,LCBLUNIT        IS THIS A LU           @YM09070
         BO    AYS115Z                  YES, BRANCH            @YM09070
AYS115HA EQU   *                                               @OY18014
         CLI   AVTPARM3+2,DCTLOCAL      3270 LOCAL             @G36XRYP
         BO    AYS115Z                  YES, BRANCH              S22028
         LA    RWRK,TWO(RWRK)           SKIP PAST C.U. & DEV.    Y06327
         TM    TRMSTATE,TRMPREF         IS THERE A PREFIX      @Y17XAYP
         BO    AYS115R                  YES. BRANCH              Y06327
         LA    RWRK,ONEMORE(RWRK)       AND STX IF NOT 3705      Y06327
AYS115R  EQU   *                                                 Y06327
         TM    AVTPARM3,AUTOPL          IS THIS AUTOPOLL         S22028
         BZ    AYS115HH                 NO,BRANCH                S22028
         LA    RWRK,1(RWRK)             SKIP OVER INDEX BYTE     S22028
AYS115HH EQU   *                                                 S22028
         CLI   0(RWRK),ENTERAID         IS AID BYTE FOR ENTER KEYS22028
         BNE   AYS115Z                  NO, CHECK FOR CLEAR OR   S22028
*                                       PA1 AID BYTE             S22028
         CLI   3(RWRK),ENDTEXT          WAS ANY DATA ENTERED     S22028
         BE    AYS111                   IF NO, GO SET ATTN LEVEL S22028
*                                       TO ZERO                  S22028
AYS115Z  EQU   *                                                 S22028
         CLI   0(RWRK),CLEARAID         IS AID BYTE FOR CLEAR KEYS22028
         BE    AYS115G                  IF YES TREAT IT THE SAME S22028
*                                       AS IF THE CLEAR CHARACTERS22028
*                                       STRING WERE FOUND        S22028
         CLI   0(RWRK),PA1AID           IS AID BYTE FOR PA1 KEY  S22028
         BE    AYS124                   IF YES GO SET ATTN LEVEL S22028
*                                       TO ONE                   S22028
*  IF THE DEVICE IS A 3270 LOCAL AND THE AID BYTE IS NOT FOR THE S22028
*  CLEAR KEY OR THE PA1 KEY, THEN THE ENTER KEY MUST HAVE BEEN   S22028
*  DEPRESSED                                                     S22028
         CLI   PRFSIZE,TWENTY1          WAS ANY DATA ENTERED     S22028
         BE    AYS111                   IF NO, GO SET ATTN LEVEL S22028
*                                       TO ZERO                  S22028
AYS115I  EQU   *                                                 S22028
         L     RWRK,TSBERSDS            GET CLEAR STRING         S22028
         LTR   RWRK,RWRK                IS IT PRESENT
         BZ    AYS115X                  BRANCH ON NO
         LA    RCNT,4                   MAXIMUM CHAR COUNT
         LA    RWRK,TSBERSDS            POINT TO STRING
AYS115A  EQU   *
         CLI   0(RWRK),BLANK            IS IT A BLANK
         BE    AYS115B                  BRANCH ON YES
         LA    RWRK,1(RWRK)             BUMP TO NEXT
         BCT   RCNT,AYS115A             BRANCH IF NOT ALL CHECKED
AYS115B  EQU   *
         LA    RIC,3                    SET TO THREE
         SR    RIC,RCNT                 CALCULATE NUMBER OF CHARS
         TM    AVTPARM3+1,DCT3270       IS THIS A 3270 TRM     @G36XRYP
         BZ    AYS115BA                 NO, CONTINUE             S22028
         LA    RWRK,PRFSHDR-IEDQPRF(RDATA) POINT TO DATA START   Y06327
         TM    TRMSTATE,TRMPREF         IS THERE A PREFIX      @OY18014
         BNO   AYS115BE                 NO, BRANCH             @OY18014
         TM    LCBSTAT5,LCBLUNIT        IS THIS A LU           @YM09070
         BO    AYS115BC                 YES,BRANCH             @YM09070
AYS115BE EQU   *                                               @OY18014
         TM    AVTPARM3+2,DCTLOCAL      IS THIS A 3270 LOCAL   @G36XRYP
         BZ    AYS115BD                 NO, MUST BE REMOTE       S22028
AYS115BC EQU   *                                                 S22028
         CLI   3(RWRK),SBACHAR          IS THIRD CHARACTER AN SBAS22028
         BNE   AYS115BB                 NO, GO SCAN CHAR STRING  S22028
         LA    RWRK,8(RWRK)             SKIP OVER THE SBA TO DATAS22028
         B     AYS115BB                 NOW GO SCAN CHAR STRING  S22028
AYS115BD EQU   *                                                 S22028
         LA    RWRK,TWO(RWRK)           SKIP PAST C.U. & DEV.    Y06327
         TM    TRMSTATE,TRMPREF         IS THERE A PREFIX      @Y17XAYP
         BO    AYS115S                  YES. BRANCH              Y06327
         LA    RWRK,ONEMORE(RWRK)       AND STX IF NOT 3705      Y06327
AYS115S  EQU   *                                                 Y06327
         TM    AVTPARM3,AUTOPL          IS THIS AUTOPOLL         S22028
         BZ    AYS115BC                 NO. BRANCH               S22028
         LA    RWRK,1(RWRK)             SKIP OVER INDEX BYTE     S22028
         B     AYS115BC                 GO CHECK FOR SBA         S22028
AYS115BA EQU   *                                                 S22028
         LA    RWRK,PRFSHDR-IEDQPRF+2(RDATA) POINT TO DATA START Y06327
AYS115BB EQU   *                                                 S22028
         LA    RCNT,3                   NUMBER OF TIMES TO CHECK S22028
AYS115C  EQU   *
         EX    RIC,COMPAR2              LOOK FOR STRING
         BE    AYS115D                  BRANCH WHEN FOUND
         BCTR  RWRK,0                   BACK UP ONE BYTE
         BCT   RCNT,AYS115C             BRANCH IF NOT ALL CHECKED
         B     AYS115X                  BRANCH IF CHAR STRING    S22025
*                                       NOT FOUND                S22025
AYS115D  EQU   *
         BCT   RCNT,AYS115E             BRANCH IF NOT FIRST IN BUFF
         B     AYS115F                  BRANCH IF FIRST IN BUFFERS22025
AYS115E  EQU   *
         BCTR  RWRK,0                   BACK UP ONE
         CLI   0(RWRK),0                IS IT ZERO
         BNE   AYS115X                  BRANCH ON NO
         LA    RWRK,1(RWRK)             RESET POINTER
AYS115F  LA    RWRK,1(RIC,RWRK)         POINT TO END OF DATA
         LA    RCNT,PRFSUNIT            ADDR OF HDR PREFIX       S22029
         AH    RCNT,PRFSIZE             ADD DATA SIZE PLUS HDR   S22029
*                                       PREFIX SIZE              S22029
         CR    RCNT,RWRK                END OF DATA
         BE    AYS115G                  BRANCH ON YES
         CLI   0(RWRK),CRGRTRN          IS IT CARRIAGE RETURN
         BE    AYS115G                  BRANCH ON YES
         CLI   0(RWRK),NEWLINE          IS IT NEWLINE
         BNE   AYS115X                  BRANCH ON NO
AYS115G  EQU   *
         OI    SCBSTATE,SCBERSDS        REQUEST SCREEN ERASURE   A44887
         B     AYS106A                  RETURN                   S22025
AYS115X  EQU   *
         TM    AVTPARM3+1,DCT3270       IS THIS A 3270 TRM     @G36XRYP
         BZ    AYS121                   NO, BRANCH               S22028
         LA    RWRK,PRFSHDR-IEDQPRF(RDATA) POINT TO DATA START   Y06327
         TM    TRMSTATE,TRMPREF         IS THERE A PREFIX      @OY18014
         BNO   AYS115XD                 NO, BRANCH             @OY18014
         TM    LCBSTAT5,LCBLUNIT        IS THIS A LU           @YM09070
         BO    AYS115XA                 YES, BRANCH            @YM09070
AYS115XD EQU   *                                               @OY18014
         TM    AVTPARM3+2,DCTLOCAL      IS THIS A 3270 LOCAL   @G36XRYP
         BZ    AYS115XB                 NO, MUST BE REMOTE       S22028
AYS115XA EQU   *                                                 S22028
         CLI   0(RWRK),PA1AID           IS AID BYTE FOR PA1 KEY  S22028
         BE    AYS124                   YES, BRANCH              S22028
         CLI   0(RWRK),CLEARAID         IS AID BYTE FOR CLEAR KEYS22028
         BE    AYS115G                  YES, BRANCH              S22028
         CLI   0(RWRK),ENTERAID         IS AID BYTE FOR ENTER KEYS22028
         BNE   AYS111                   NO, BRANCH               S22028
         CLI   3(RWRK),SBACHAR          WAS ANY DATA ENTERED     S22028
         BNE   AYS111                   NO, BRANCH               S22028
         MVI   5(RWRK),ZERO             ZERO OUT THE CURSOR ADDR S22028
*                                       PRECEDING THE FIRST BYTE S22028
*                                       OF DATA SO IT WONT BE    S22028
*                                       MISTAKEN FOR DATA        S22028
         LA    RWRK,8(RWRK)             POINT TO FIRST BYTE      S22028
         B     AYS122                   GO SCAN FOR CHAR STRING  S22028
AYS115XB EQU   *                                                 S22028
         LA    RWRK,TWO(RWRK)           SKIP PAST C.U. & DEV.    Y06327
         TM    TRMSTATE,TRMPREF         IS THERE A PREFIX      @Y17XAYP
         BO    AYS115XC                 YES. BRANCH              Y06327
         LA    RWRK,ONEMORE(RWRK)       AND STX IF NOT 3705      Y06327
AYS115XC EQU   *                                                 Y06327
         TM    AVTPARM3,AUTOPL          IS THIS AUTOPOLL         S22028
         BZ    AYS115XA                 NO. BRANCH               S22028
         LA    RWRK,1(RWRK)             SKIP OVER INDEX BYTE     S22028
         B     AYS115XA                 GO CHECK FOR AID BYTE    S22028
AYS121   EQU   *                                                 S22029
         LA    RWRK,PRFSHDR-IEDQPRF+2(RDATA) POINT TO DATA START Y06327
AYS122   EQU   *                                                 S22028
         IC    RIC,TSBFLG2              GET CHARACTER COUNT
         N     RIC,CNTMASK              REMOVE EXTRANEOUS BITS
         LH    RCNT,PRFSIZE             GET DATA COUNT           S22029
         SH    RCNT,HDRPRFX             IS THERE ANY DATA        S22029
         BZ    AYS111                   BRANCH IF NO DATA
         LA    RCNT,3                   PUT COUNT OF THREE IN REGISTER
AYS109   L     R14,TSBATNCC             GET CHARACTER STRING
         LTR   R14,R14                  IS THERE ONE
         BZ    AYS139                   BRANCH ON NO
         EX    RIC,COMPARE              SEARCH FOR CHARACTER STRING
         BE    AYS117                   BRANCH WHEN FOUND
AYS139   CLI   0(RWRK),CHAR1            COULD THIS BE A NUMBER
         BL    AYS130                   BRANCH ON NO
         CLI   0(RWRK),CHAR9            IS IT A NUMBER
         BNH   AYS108                   BRANCH ON YES
AYS130   BCTR  RWRK,0                   BACK UP ONE CHARACTER
         BCT   RCNT,AYS109              BRANCH IF NOT ALL CHECKED
         B     AYS111                   INVALID REQUEST
         SPACE 2
*  THE FOLLOWING  CODE DETERMINES IF A VALID NUMBER IS PRESENT IN
*  FRONT OF THE CARRIAGE RETURN.
         SPACE 2
AYS108   BCT   RCNT,AYS137              BRANCH IF NUMBER NOT FIRST
*                                       DATA IN BUFFER
         B     AYS138                   BRANCH IF FIRST
AYS137   BCTR  RWRK,0                   BACK UP ONE CHARACTER
         LA    RCNT,1(RCNT)             RESET COUNTER
         CLI   0(RWRK),ZERO             IS IT A ZERO
         LA    RWRK,1(RWRK)             RESET POINTER
         BNE   AYS130                   NO, RESUME CHECKING
AYS138   SR    RIC,RIC                  CLEAR REGISTER
         LA    RCNT,PRFSUNIT            ADDR OF HDR PREFIX       S22029
         AH    RCNT,PRFSIZE             ADD DATA SIZE PLUS HDR   S22029
*                                       PREFIX SIZE              S22029
         BCTR  RCNT,ZERO                BACKUP ONE CHARACTER     S22029
         CR    RCNT,RWRK                IS THIS END OF DATA
         BE    AYS135                   BRANCH ON YES
         B     AYS136                   BRANCH TO CHECK END CHAR
AYS120   CLI   0(RWRK),CHAR1            IS THE CHARACTER LESS
*                                       THAN '1'
         BL    AYS124                   BRANCH ON YES
         CLI   0(RWRK),CHAR9            IS THE CHARACTER GREATER
*                                       THAN '9'
         BH    AYS124                   BRANCH ON YES
         SPACE 2
*  THE FOLLOWING CODE SETS THE ATTENTION LEVEL IN THE SCB TO REFLECT
*  THE LEVEL INDICATED BY THE NUMBER IN FRONT OF THE CARRIAGE RETURN.
         SPACE 2
AYS135   EQU   *                                                  M0891
         SR    RCNT,RCNT                CLEAR WORK REG         @OZ27742
         SR    RIC,RIC                  CLEAR WORK REG         @OZ27742
         IC    RIC,AVTEZERO(,RWRK)      GET SIM ATTN LEVEL REQ @OZ27742
         SLL   RIC,FOUR                 MOVE TO BITS 0-3       @OZ27742
         IC    RCNT,SCBSALEV            SAVE BITS 4-7          @OZ27742
         OR    RIC,RCNT                 COMBINE TWO HALF-BYTES @OZ27742
         STC   RIC,SCBSALEV             STORE IN SCB           @OZ27742
AYS112   OI    SCBERR3,SCBSATTN         SET SIMULATED ATTENTION
*                                       REQUESTED
         B     AYS110                   RETURN
AYS111   EQU   *                                               @OZ27742
         NI    SCBSALEV,LEVMASK         SET ATTEN LEVEL TO ZERO@OZ27742
         B     AYS110                   BRANCH
AYS117   BCT   RCNT,AYS118              BRANCH IF CHARACTER STRING
*                                       IS NOT FIRST DATA IN BUFFER
         B     AYS119                   BRANCH IF FIRST
AYS118   BCTR  RWRK,0                   POINT TO CHARACTER IN FRONT
*                                       OF STRING
         LA    RCNT,1(RCNT)             RESET COUNTER
         CLI   0(RWRK),ZERO             IS THE CHARACTER 0
         LA    RWRK,1(RWRK)             READJUST POINTER
         BNE   AYS139                   RESUME CHECKING
AYS119   LA    RWRK,1(RIC,RWRK)         POINT TO CHARACTER AFTER
*                                       CHARACTER STRING
         LA    RCNT,PRFSUNIT            ADDR OF HDR PREFIX       S22029
         AH    RCNT,PRFSIZE             ADD DATA SIZE PLUS HDR   S22029
*                                       PREFIX SIZE              S22029
         CR    RCNT,RWRK                IS THIS END OF DATA
         BE    AYS124                   BRANCH ON YES
         CLC   0(2,RWRK),LFCRTRN        IS IT LINE FEED RETURN
         BE    AYS131                   BRANCH ON YES
         CLC   0(2,RWRK),CRTRNLF        IS IT RETURN LINE FEED
         BE    AYS131                   BRANCH ON YES
         CLI   0(RWRK),CRGRTRN          IS IT A CARRAIGE RETURN
         BE    AYS132                   BRANCH ON YES
         CLI   0(RWRK),NEWLINE          IS IT A NEW LINE
         BE    AYS132                   BRANCH ON YES
         BCTR  RCNT,0                   REDUCE COUNT BY ONE
         CR    RCNT,RWRK                IS THIS END OF DATA
         BE    AYS120                   BRANCH ON YES
AYS136   CLC   1(2,RWRK),LFCRTRN        IS IT LINE FEED RETURN
         BE    AYS133                   BRANCH ON YES
         CLC   1(2,RWRK),CRTRNLF        IS IT RETURN LINE FEED
         BE    AYS133                   BRANCH ON YES
         CLI   1(RWRK),CRGRTRN          IS IT A CARRIAGE RETURN
         BE    AYS134                   BRANCH ON YES
         CLI   1(RWRK),NEWLINE          IS IT A NEW LINE
         BE    AYS134                   BRANCH ON YES
         B     AYS111                   BRANCH, INVALID
AYS131   BCTR  RCNT,0                   REDUCE COUNT BY ONE
AYS132   BCTR  RCNT,0                   REDUCE COUNT BY ONE
         CR    RCNT,RWRK                IS THIS LAST CHARACTER
         BE    AYS120                   BRANCH ON YES
         B     AYS111                   BRANCH, INVALID
AYS133   BCTR  RCNT,0                   REDUCE COUNT BY ONE
AYS134   BCTR  RCNT,0                   REDUCE COUNT BY ONE
         CR    RCNT,RWRK                IS THIS LAST CHARACTER
         BE    AYS120                   BRANCH ON YES
         B     AYS111                   BRANCH, INVALID
AYS124   EQU   *                                                  M0891
         OI    SCBSALEV,LEVEL1          SET ATTN LEVEL TO ONE  @OZ27742
         B     AYS112                   BRANCH TO REQUEST ATTENTION
         SPACE 2
AYS104   EQU   *                                                 S22025
         L     RIC,AVTUI                PICK UP RETURN ADDRESS   S22025
         B     FOUR(RIC)                RETURN TO CALLER         S22025
         EJECT
*  THE FOLLOWING CODE DETERMINES IF SIMULATED ATTENTION BY LINE
*  COUNT HAS BEEN REQUESTED.
         SPACE 2
         USING *,RBAS
         USING AVTSAVE2,RSAV
AYS201   LR    RQCB,RTRM                GET ADDRESS OF QCB
         TM    QCBFLAG,QCBTSSES         IS THIS TIME SHARING SESSION
         BNO   AYS204                   BRANCH ON NO
         LA    RWRK,AYS202              SET UP RETURN REG.      SA60815
*  THE FOLLOWING CODE TO AYS202 IS USED AS A SUBROUTINE BY IEDAYS0.
         SPACE
AYSTSB   EQU   *                                                SA60815
         L     RTSB,CVTPTR              POINTER TO COM VECT TABLE
         USING CVT,RTSB
         L     RTSB,CVTASVT             GET ASVT ADDRESS       @G36XRYP
         DROP  RTSB                                            @G36XRYP
         USING ASVT,RTSB                ASVT ADDRESSABILITY    @G36XRYP
         LA    RTSB,ASVTENTY            GET ASCB LIST ADDRESS  @ZM46782
         LH    R0,QCBTJID               GET ASID               @G36XRYP
         BCTR  R0,0                     REDUCE BY ONE
         SLL   R0,2                     MULTIPLY BY 4          @G36XRYP
         AR    RTSB,R0                                         @G36XRYP
         L     RTSB,AVTEZERO(RTSB)      GET ASCB ADDRESS       @G36XRYP
         LTR   RTSB,RTSB                                       @G36XRYP
         BZR   RWRK                                            @G36XRYP
         DROP  RTSB                                            @G36XRYP
         USING ASCB,RTSB                ASCB ADDRESSABILITY    @G36XRYP
         L     RTSB,ASCBTSB             GET TSB ADDRESS        @G36XRYP
         DROP  RTSB                                            @G36XRYP
         B     FOUR(RWRK)               RTN +4 TSB IS VALID     SA60815
* * * *   E N D  O F  T S B  S U B R O U T I N E  * * * * * *   SA60815
AYS202   EQU   *                        ERROR RETURN TO TCAM    SA60815
         B     AYS204              RETURN TSO NOT ACTIVE        SA60815
         USING TSB,RTSB
         SR    RWRK,RWRK                CLEAR REGISTER             2260
         TM    QCBTSOF2,QCBSATLC        IS SIMULATED ATTENTION BY LINE
*                                       COUNT REQUESTED
         BNO   AYS205                   NO, BRANCH                 2260
         SPACE 2
*  IF SIMULATED ATTENTION BY LINE COUNT HAS BEEN REQUESTED, THE COUNT
*  OF NEW LINE CHARACTERS IN THE QCB IS INCREMENTED BY ONE AND
*  COMPARED TO THE THRESHOLD COUNT IN THE TSB.
         SPACE 2
         TM    TSBFLG5,TSBKEYS          TSO SESSION MANAGER    @OZ24035
         BO    AYS205                   YES, BRANCH            @OZ24035
         IC    RWRK,QCBSATCT            PICK UP LINE COUNT
         LA    RWRK,1(RWRK)             INCREMENT LINE COUNT BY ONE
         STC   RWRK,QCBSATCT            STORE UPDATED VALUE
         CLC   QCBSATCT(1),TSBATNLC+1   HAS LINE COUNT REACHED
*                                       THRESHOLD
         BNL   AYS206                   YES, BRANCH                2260
         SPACE 2
*  A CHECK IS MADE TO DETERMINE IF THE TERMINAL IS A DISPLAY.  IF IT
*  IS AND THE NEXT TO LAST LINE OF THE DISPLAY CONTAINS DATA, A FLAG
*  REQUESTING A SIMULATED ATTENTION CHANNEL PROGRAM IS SET.
         SPACE 2
AYS205   EQU   *                                                SA60815
         TM    TSBSTAT,TSBDSPLY         IS THIS DISPLAY SCREEN  SA60815
         BZ    AYS204                   BR NO. COUNT NOT REQ.   SA60815
         TM    TSBFLG5,TSBKEYS          TSO SESSION MANAGER    @OZ24035
         BO    AYS204                   YES, BRANCH            @OZ24035
         SR    RWRK,RWRK                CLEAR REGISTER           S21903
         IC    RWRK,QCBSATCT            PICK UP LINE COUNT
         TM    QCBTSOF2,QCBSATLC        HAS LINE COUNT BEEN UPDATED
         BO    AYS207                   YES, BRANCH                2260
         CLI   QCBSATCT,MASKALL         IS COUNT FOR ERASE
         BE    AYS204                   BRANCH ON YES
         LA    RWRK,1(RWRK)             INCREMENT LINE COUNT       2260
         STC   RWRK,QCBSATCT            STORE UPDATED VALUE        2260
AYS207   SR    RIC,RIC                  CLEAR REGISTER             2260
         IC    RIC,TSBLNNO              GET NUMBER OF LINES ON     2260
*                                       DISPLAY SCREEN             2260
         BCTR  RIC,0                    REDUCE BY ONE              2260
         CR    RWRK,RIC                 IS NUMBER OF LINES         2260
*                                       DISPLAYED EQUAL TO NUMBER  2260
*                                       OF LINES ON DISPLAY - 1    2260
         BL    AYS204                   NO, RETURN                 2260
         MVI   QCBSATCT,MASKALL         SET COUNT FOR ERASE
         OI    QCBTSOF1,QCBSATRD        REQUEST SIMATTN READ
         B     AYS204                   RETURN
         SPACE 2
*  IF THE NUMBER OF LINES SPECIFIED FOR A SIMULATED ATTENTION HAS
*  BEEN REACHED, A FLAG REQUESTING A SIMULATED ATTENTION CHANNEL
*  PROGRAM IS SET IN THE QCB AND THE LINE COUNT IN THE QCB IS ZEROED.
         SPACE 2
AYS206   OI    QCBTSOF1,QCBSATRD        REQUEST SIMULATED ATTENTION
*                                       CHANNEL PROGRAM
         MVI   QCBSATCT,ZERO            ZERO LINE COUNT
AYS204   LM    14,12,AVTSAVE3-AVTSAVE2+12(13) RESTORE REGS
         BR    R14                      RETURN TO CALLER
         EJECT
*  THE FOLLOWING ROUTINE GAINS CONTROL FROM THE TCAM DISPATCHER WHEN
*  A QCB IS REMOVED FROM THE TIME DELAY QUEUE.  A SIMULATED ATTENTION
*  READ IS REQUESTED AND THE LCB IS POSTED TO ITSELF.
         SPACE 2
AYSAYS3  BALR  RBAS,0                   LOAD BASE REGISTER
         USING *,RBAS
         USING IEDQDISP,RDSP                                     Y06327
         LA    RQCB,0(R1)               GET ADDRESS OF QCB POSTED
         NI    QCBTSOF2,AVTEFF-QCBPOSTO RESET POSTED FLAG      @YA11170
         TM    AVTBIT3,AVTTSAB          TSO ABENDED              A44022
         BNO   NOABEND                  BRANCH NO ABEND          A44022
         NI    QCBTSOF1,AVTEFF-QCBDELAY TURN OFF DELAY BIT       A44022
         B     AYS401                   TO POST QCB              A44022
NOABEND  EQU   *                                                 A44022
         TM    QCBTSOF1,QCBDISC         IS USER LOGGING OFF     SA57345
         BO    AYS401                   BR YES TO POST QCB      SA57345
         NI    QCBTSOF2,AVTEFF-QCBPOSTO INDICATE QCB           @G36XRYP
*                                       CAN BE POSTED NOW      @G36XRYP
         TM    QCBTSOF2,QCBSATTI        IS SIMULATED ATTENTION BY
*                                       TIME DELAY REQUESTED
         BNO   DSPDISP                  NO, RETURN TO DISPATCH
         TM    QCBTSOF1,QCBTPUT         TPUT REQUESTED         @YA11170
         BO    AYS401                   BRANCH YES             @YA11170
         OI    QCBTSOF1,QCBSATRD        REQUEST SIMULATED ATTN READ
AYS401   IC    RWRK,QCBDSFLG            SAVE QCB FLAGS
         ST    RQCB,QCBELCHN-1          QCB POSTED TO ITSELF
         STC   RWRK,QCBDSFLG            RESTORE QCB FLAGS
         MVI   QCBPRI,PRIORITY          SET POSTING PRIORITY
         OI    QCBTSOF2,QCBPOSTO        SET QCB POSTED TO ITSELF
         LR    R1,RQCB                  ELEMENT TO BE POSTED
         BAL   R14,DSPPOST              BRANCH TO DISPATCHER   @Y17XAYX
         SPACE 2
*  EXECUTED INSTRUCTIONS.
         SPACE 2
COMPARE  CLC   TSBATNCC(0),0(RWRK)      COMPARE CHAR CONSTANT TO DATA
COMPAR2  CLC   TSBERSDS(0),0(RWRK)      COMPARE DATA TO ERASE    S22025
*                                       CHARACTERS               S22025
         SPACE 2
*  CONSTANTS.
         SPACE 2
CNTMASK  DC    F'3'                     REMOVE ALL BITS BUT 30,31
AYSLCBOF DC    AL2(LCBFLAG1-IEDQLCB)    OFFSET FROM LCB TO IOB   S22025
HDRPRFX  DC    H'30'                    SIZE OF LHEADER PREFIX   S22029
LFCRTRN  DC    X'250D'                  LINE FEED/CARRIAGE RETURNS22025
CRTRNLF  DC    X'0D25'                  CARRIAGE RETURN/LINE FEEDS22025
*                                       IN EBCDIC                S22025
CVT      DSECT
         CVT
         IHAASCB
         IHAASVT
         IKJTSB
         TDCTD
         TLCBD
         TSCBD
         TSIBD
         TAVTD
         TQCBD
         TTRMD
         TPRFD
         TDISPD
         DCBD  DSORG=TX
         TTSID
         TLGBD
         END
