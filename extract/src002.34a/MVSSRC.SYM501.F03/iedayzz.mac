IEDAYZZ  CSECT
* A360000,770000                                               @OY13647
* D530000                                                      @OY13647
***********************************************************************
*TITLE  'IEDAYZZ'  QTIP 25/26 SUBROUTINE                              *
*                                                                     *
*STATUS -- CHANGE LEVEL 20.0                                          *
*                                                                     *
*FUNCTION -- THE QTIP 25/26 SUBROUTINE IS A LINKEDITED SUBROUTINE OF  *
*   A LARGER ROUTINE - THE QTIP SVC 101 ROUTINE (IKJGGQT0).  SINCE    *
*   THIS SUBROUTINE IS PART OF A TYPE ONE SVC ROUTINE, IT RUNS IN DIS-*
*   ABLED STATE.  THIS PREVENTS ENABLED TSO CODE FROM TESTING CERTAIN *
*   STATUS BITS AT THE SAME TIME THAT THE BITS ARE BEING SET ON OR    *
*   OFF.                                                              *
*      IF THIS SUBROUTINE IS EXECUTED AS THE RESULT OF A QTIP MACRO   *
*   INSTRUCTION WITH AN ENTRY CODE OF 25, ITS FUNCTION IS TO PREPARE  *
*   A QCB FOR INSERTION INTO THE TIME DELAY QUEUE.  IF A SEND OPER-   *
*   ATION WAS REQUESTED, OR IF THE QCB IS ALREADY IN THE TIME DELAY   *
*   QUEUE, THIS SUBROUTINE SETS A RETURN CODE OF 4, INDICATING THAT   *
*   THE TIME DELAY SUBTASK IS NOT TO BE EXECUTED, AND RETURNS TO THE  *
*   CALLING ROUTINE VIA THE INTERRUPT HANDLER.  OTHERWISE, IT SETS    *
*   THE QCB TO BE POSTED TO THE SIMULATED ATTENTION QCB WHEN ITS TIME *
*   DELAY EXPIRES, AND SETS TIME DELAY PRIORITY.  THEN IT ACCESSES    *
*   THE TERMINAL STATUS BLOCK IN THE TSO REGION, AND UPDATES THE      *
*   QCB TIME DELAY INTERVAL FROM IT.  THE QCB TIME DELAY BIT IS SET   *
*   ON, AND THE CARRIAGE POSITION COUNT AND TSO JOB I.D. ARE SAVED IN *
*   THE QCB LINK FIELD.  FINALLY, THIS SUBROUTINE SETS A RETURN CODE  *
*   OF ZERO, INDICATING THAT THE CALLING ROUTINE MUST LINK TO THE     *
*   TIME DELAY SUBTASK TO INSERT THE QCB INTO THE TIME DELAY QUEUE,   *
*   AND RETURNS TO THE CALLING ROUTINE VIA THE INTERRUPT HANDLER.     *
*      IF THIS SUBROUTINE IS EXECUTED AS THE RESULT OF A QTIP MACRO   *
*   INSTRUCTION WITH AN ENTRY CODE OF 26, ITS FUNCTION IS TO DETER-   *
*   MINE WHETHER OR NOT A SEND OPERATION (TPUT) HAS BEEN REQUESTED.   *
*   IT FIRST INDICATES THAT THE QCB IS NO LONGER POSTED TO ITSELF.    *
*   IF A TPUT WAS REQUESTED, A RETURN CODE OF 4 IS SET.  IF NOT, A    *
*   RETURN CODE OF ZERO IS SET.  THEN THIS SUBROUTINE RETURNS TO THE  *
*   CALLING ROUTINE VIA THE INTERRUPT HANDLER.                        *
*                                                                     *
*ENTRY POINTS -- QTIP0250 - TO PREPARE A QCB FOR INSERTION INTO THE   *
*   TIME DELAY QUEUE                                                  *
*   CALLING SEQUENCE          L    R15,SVC TABLE + OFFSET (101)       *
*                             BR   R15                                *
*   QTIP0260 - TO DETERMINE WHETHER OR NOT A TPUT HAS BEEN REQUESTED  *
*   CALLING SEQUENCE          L    R15,SVC TABLE + OFFSET (101)       *
*                             BR   R15                                *
*                                                                     *
*INPUT -- IEDAYZZ IS ENTERED FROM THE SVC INTERRUPT HANDLER AT ENTRY  *
*   POINT QTIP0250 WHEN THE TIME SHARING SCHEDULER (IEDAYZ) ISSUES A  *
*   QTIP MACRO INSTRUCTION WITH AN ENTRY CODE OF 25.  AT ENTRY, THE   *
*   FOLLOWING REGISTERS ARE SET.                                      *
*   R0 HAS THE ENTRY CODE (25)                                        *
*   R1 HAS THE ADDRESS OF AVTSAVE2                                    *
*   R7 HAS THE QCB ADDRESS                                            *
*   R12 HAS THE ADDRESS OF THIS ENTRY POINT WITHIN IKJGGQT0           *
*   R13 HAS THE ADDRESS OF AVTSAVE2                                   *
*   R14 HAS THE RETURN ADDRESS IN THE SVC INTERRUPT HANDLER           *
*   R15 HAS THE ENTRY POINT ADDRESS FOR IKJGGQT0                      *
*                                                                     *
*   IEDAYZZ IS ENTERED FROM THE SVC INTERRUPT HANDLER AT ENTRY POINT  *
*   QTIP0260 WHEN THE SIMULATED ATTENTION ROUTINE (IEDAYS) ISSUES A   *
*   QTIP MACRO INSTRUCTION WITH AN ENTRY CODE OF 26.  AT ENTRY, THE   *
*   FOLLOWING REGISTERS ARE SET.                                      *
*   R0 HAS THE ENTRY CODE (26)                                        *
*   R1 HAS THE ADDRESS OF AVTSAVE2                                    *
*   R7 HAS THE QCB ADDRESS                                            *
*   R12 HAS THE ADDRESS OF THIS ENTRY POINT WITHIN IKJGGQT0           *
*   R14 HAS THE RETURN ADDRESS IN THE SVC INTERRUPT HANDLER           *
*   R15 HAS THE ENTRY POINT ADDRESS FOR IKJGGQT0                      *
*                                                                     *
*OUTPUT -- ON RETURN TO THE SVC INTERRUPT HANDLER, A RETURN CODE OF   *
*   EITHER FOUR OR ZERO IS SET IN REGISTER 15.  IF A QCB IS TO BE IN- *
*   SERTED INTO THE TIME DELAY QUEUE, THE QCB REFLECTS THIS, AND THE  *
*   FOLLOWING REGISTERS ARE ALSO SET.                                 *
*   R2 HAS THE CVT ADDRESS                                            *
*   R3 HAS THE TIME SHARING CVT ADDRESS                               *
*   R5 HAS THE ADDRESS OF THE TIOC REFERENCE POINTER TABLE            *
*   R6 HAS THE ADDRESS OF THE TERMINAL STATUS BLOCK                   *
*   R8 HAS THE ADDRESS OF THE TSINPUT QCB                             *
*                                                                     *
*EXTERNAL ROUTINES -- NONE                                            *
*                                                                     *
*EXITS-NORMAL -- TO THE SVC INTERRUPT HANDLER                         *
*                                                                     *
*EXITS-ERROR -- NONE                                                  *
*                                                                     *
*TABLES/WORKAREAS -- CVT, TSCVT, AVT, TSB, QCB, TSID, TPRIOR ERB.     *
*                                                                     *
*ATTRIBUTES -- REENTRANT, DISABLED, RESIDENT, SUPERVISOR MODE.        *
*                                                                     *
*NOTES -- THE OPERATION OF THIS MODULE DOES NOT DEPEND UPON A         *
*   PARTICULAR INTERNAL REPRESENTATION OF THE EXTERNAL CHARACTER      *
*   SET.                                                              *
*      THE SVC INTERRUPT HANDLER BRANCHES TO THE ENTRY POINT IKJGGQT0 *
*   FOR ALL QTIP SVC'S.  IKJGGQT0 DETERMINES, FROM THE ENTRY CODE IN  *
*   REGISTER 0, WHICH ENTRY POINT IN WHICH SUBROUTINE (CSECT) IS TO   *
*   RECEIVE CONTROL.                                                  *
*                                                                     *
***********************************************************************
         ENTRY QTIP0250
         ENTRY QTIP0260
RPARM0   EQU   0                  PARAMETER REGISTER
RPARM1   EQU   1                  PARAMETER REGISTER
RBASE    EQU   12                 BASE REGISTER
RSAVE    EQU   13                 SAVE AREA REGISTER
RRETURN  EQU   14                 RETURN REGISTER
RRCODE   EQU   15                 RETURN CODE REGISTER
         USING *,RBASE
         SPACE 8
QTIP0250 EQU   *
         SPACE 4
RCVT     EQU   2                  ADDRESS OF COMM VECTOR TABLE
RTSCVT   EQU   3                  ADDR OF TIME-SHARING COMM VECTOR TABL
RTRM     EQU   4                  ADDRESS OF TERMINAL TABLE ENTRY
RTIOC    EQU   5                  ADDR OF TIOC REFERENCE PTR TABLE
RTSB     EQU   6                  ADDR OF TERMINAL STATUS BLOCK
RQCB     EQU   7                  ADDR OF DESTINATION QUEUE CTL BLOCK
RTSI     EQU   8                  ADDR OF TSINPUT QCB
R10      EQU   10
R11      EQU   11
         USING AVTSAVE2,RSAVE
         USING CVTSECT,RCVT
         USING TSCVT,RTSCVT
         USING IEDQTRM,RTRM
         USING TIOCRPT,RTIOC
         USING TSB,RTSB
         USING IEDQQCB,RQCB
         USING IEDQTSI,RTSI
         TM    QCBTSOF1,QCBTPUT+QCBDELAY  IF SEND OPERATION REQUESTED
*                                       OR ALREADY IN TIME DELAY QUEUE
         BNZ   AYZZ2601                 DO NOT PUT IN DELAY QUEUE.
         OI    QCBTSOF1,QCBDELAY        TURN ON DELAY BIT      @OY13647
         TM    QCBTSOF2,QCBPOSTO        HAS QCB BEEN POSTED?   @OY13647
         BO    AYZZ2602                 YES, DON'T ALLOW DELAY @OY13647
         L     RTSI,AVTTSOPT            OBTAIN ADDR OF TSINPUT QCB
         MVC   QCBELCHN(3),TSISIMAT+1   SET UP TO POST THIS QCB TO THE
*                                       SIMULATED ATTENTION RTN WHEN
*                                       ITS' TIME DELAY EXPIRES.
         MVI   QCBPRI,PRITIME           MOVE IN PRIORITY
         L     RCVT,CVTPTR              ADDR OF COMM VECTOR TABLE
         L     RTSCVT,CVTTSCVT          ADDR OF TIME-SHARING CVT
         L     RTIOC,TSCVTRPT           ADDR OF TIOC TABLE
         SR    R11,R11
         IC    R11,TIOCTSBS              GET SIZE OF TERMINAL STATUS BK
         LR    R10,R11
         MH    R11,QCBTJID              DEVELOP OFFSET TO TERMINAL
         SR    R11,R10                  STATUS BLK FOR THIS TERMINAL
         A     R11,TIOCTSB-1              ADD ADDRESS OF TSBS
         LR    RTSB,R11
         MVC   QCBEOLDT(2),TSBATNTC     UPDATE TIME INTERVAL IN QCB
         MVC   QCBLINK(3),QCBCARCT      SAVE CARRIAGE CNT AND TJID
         MVI   QCBRETCT,X'00'           INDICATE ELEMENT IS A QCB
         B     AYZZ2600                 INDICATE TO CALLER TO PUT THIS
*                                       QCB IN THE TIME DELAY QUEUE.
         DROP  RSAVE
         DROP  RCVT
         DROP  RTSCVT
         DROP  RTRM
         DROP  RTIOC
         DROP  RTSB
         DROP  RQCB
         DROP  RTSI
         SPACE 4
QTIP0260 EQU   *
         SPACE 4
*RQCB    EQU   7
         USING IEDQQCB,RQCB
         NI    QCBTSOF2,AVTEFF-QCBPOSTO SET QCB POSTED FLAG OFF
         TM    QCBTSOF1,QCBTPUT         HAS TPUT BEEN REQUESTED
         BO    AYZZ2601                 BRANCH ON YES
AYZZ2600 EQU   *
         SR    RRCODE,RRCODE            RETURN CODE = 0
         BR    RRETURN                  RETURN TO INTERRUPT HANDLER
AYZZ2602 EQU   *                                               @OY13647
         NI    QCBTSOF1,AVTEFF-QCBDELAY TURN OFF DELAY BIT     @OY13647
AYZZ2601 EQU   *
         LA    RRCODE,4                 RETURN CODE = 4
         BR    RRETURN                  RETURN TO INTERRUPT HANDLER
         DROP  RQCB
         SPACE 4
CVTSECT  DSECT
         CVT   TSO=YES
         IKJTIOCP
         IKJTSB
         IKJTSCVT
         TAVTD
         TPRIOR
         TQCBD
         TTRMD
         TTSID
         END
