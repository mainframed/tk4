         TITLE 'IEDAYH - TIOC HANG UP/ERROR'
IEDAYH   CSECT
         SPACE 3
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                     *
*                                                                     *
*    NAME - IEDAYH, TIOC HANG UP/ERROR ROUTINE                        *
*                                                                     *
*     TCAM VERSION 10.0 CHANGES                                @G36XRYP
*A575000,863000                                                @G36XRYP
*C131000,596000,656000                                         @G36XRYP
*D576000-585000,606000,668000-670000,774000                    @G36XRYP
*                                                              @Y17XAYP
*A207000,273000,314000,336000,430000,620000,676000,713000      @Y17XAYP
*A718000                                                       @Y17XAYP
*C158000,245000,259000,313000,469000,574000,593000,599500      @Y17XAYP
*C619000,625000,629000,676000,683000                           @Y17XAYP
*D672000                                                       @Y17XAYP
*A400000                                                       @SA77953
*A786000                                                       @OZ18790
*
*     RA0034 CHANGES
*
*A128000,716000                                                @OY14092
*C484000,721500                                                @OY14092
*C714000                                                       @OY15019
*A757000                                                       @OY15019
*A721000                                                       @OY15030
*                                                                     *
*        VS2-1.6 SU
*0000212600,283200,391421-391429                               @YA12165
*0000746500-747800                                             @YA08078
*
*    POST VS2-1.6 CHANGES
*0000283200,630000,630100                                      @YA11542
*A755100,769100-769440,785620                                   YA00689
*C370000,372000                                                 YZ04575
*A370500-371940                                                 YA04575
*C283200                                                        YA04486
*D390700,391000                                                 YA04486
*A189500,212500,390700-391484,876500                            YA04486
*D000100,178300,179500,188200,188600,202100,202500,852400,      YA04486
*D853200-855000,856030,857590,934200,935600                     YA04486
*    RELEASE 21 DELETIONS/CHANGES                                     *
*1021283200                                                      S22029
*1021282600-283300                                               A46135
*1021283200                                                      A44839
*    RELEASE 20 DELETIONS/CHANGES                                     *
*                                                                TS1628
*                                                                M2655
*                                                                M3546
*                                                                 M4019
*                                                                M4681
*                                                                 M4665
*                                                                 M5711
*                                                                     *
************************************************************** @Y17XAYP
* MODULE NAME = IEDAYH (TCAM,TSO)                              @Y17XAYP
*                                                              @Y17XAYP
* DESCRIPTIVE NAME = TIME SHARING HANG UP/ ERROR ROUTINE       @Y17XAYP
*                                                              @Y17XAYP
* COPYRIGHT = NONE                                             @Y17XAYP
*                                                              @Y17XAYP
* STATUS = VERSION 10.0                                        @G36XRYP
*                                                              @Y17XAYP
* FUNCTION -                                                          *
*    HANDLE VARIOUS TERMINAL I/O ERRORS                               *
*    DISCONNECT TERMINAL WHEN NECESSARY                               *
*                                                                     *
* ENTRY POINTS -                                                      *
*         ENTRY1 (IEDAYH), WHEN BRANCHED TO AT TNE END OF EVERY INPUT *
*         AND OUPTUT MESSAGE                                          *
*                                                                     *
*         ENTRY2 (IEDAYH+12), WHEN QPOSTED DUE TO A HANG UP OCCURRING *
*         ON A MONITORING CHANNEL PROGRAM OR ON A MSGGEN              *
*                                                                     *
* INPUT -                                                             *
*    ENTRY1                                                           *
*    REGISTER  1 HAS POST  LIST (BUFFERS TO BE POSTED TO TSINPUT) AD- *
*    DRESS OR ZERO                                                    *
*    REGISTER  3 HAS SCB ADDRESS                                      *
*    REGISTER  4 HAS LCB ADDRESS                                      *
*    REGISTER 11 HAS TCAM DISPATCHER ADDRESS                          *
*    REGISTER 12 HAS ENTRY ADDRESS                                    *
*    REGISTER 13 HAS SAVE AREA ADDRESS, AVTSAVE2                      *
*                                                                     *
*    ENTRY2                                                           *
*    REGISTER  1 HAS LCB ADDRESS                                      *
*    REGISTER  7 HAS ENTRY ADDRESS                                    *
*    REGISTER 11 HAS TCAM DISPATCHER ADDRESS                          *
*    REGISTER 13 SAVE AREA ADDRESS, AVTSAVE2                          *
*                                                                     *
*    ENTRY1 AND ENTRY2                                                *
*    SCBERRST AND LCBSENS0 HAVE VARIOUS BITS SET TO INDICATE THE TYPE *
*    OF ERRORS THAT HAVE OCCURRED                                     *
*                                                                     *
* OUTPUT -                                                            *
*    NONE                                                             *
*                                                                     *
* EXTERNAL REFERENCES -                                               *
*         QTIP 4, VIA SVC 101                                         *
*         IEDQTNT, VIA BRANCH ENTRY AT AVTTRNMPT                      *
*                                                                     *
* EXITS, NORMAL -                                                     *
*         BRANCH TO TCAM  DISPATCHER AT ADDRESS DSPCHAIN WITH ADDRESS *
*         OF ERB IN REGISTER 1 (IF TERMINAL IS STILL CONNECTED)       *
*                                                                     *
*         BRANCH TO TCAM  DISPATCHER AT ADDRESS  DSPDISP (IF TERMINAL *
*         IS, OR IS TO BE, DISCONNECTED)                              *
*                                                                     *
* EXITS, ERROR -                                                      *
*         NONE                                                        *
*                                                                     *
* TABLES/WORK AREAS -                                                 *
*    QCB (12 BYTES) PRECEDES THIS ROUTINE                             *
*    CONTROL BLOCKS ARE DESCRIBED IN THE DSECTS AT END OF LISTING     *
*                                                                     *
* ATTRIBUTES -                                                        *
*    ENABLED, PROBLEM PROGRAM MODE, REFRESHABLE, REUSABLE             *
*                                                                     *
* CHARACTER CODE DEPENDENCY -                                         *
*    THIS MODULE IS ASSEMBLED IN EBCDIC AND MUST BE RE-ASSEMBLED IF A *
*    DIFFERENT CHARACTER SET IS TO BE USED DURING EXECUTION           *
*                                                                     *
* NOTES -                                                             *
*    REGISTER EQUATES START WITH 'R'                                  *
*    LENGTH AND DISPLACEMENT EQUATES START WITH 'E'                   *
*                                                                     *
*                                                                     *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         EJECT
********
******** REGISTER EQUATES
********
EZERO    EQU   0                                               @OY14092
RWORK    EQU   0                        WORK REGISTER
R1       EQU   1                        WORK REGISTER
RASCB    EQU   2                        ASCB ADDRESS           @G36XRYP
RSCB     EQU   3                        SCB ADDRESS
RLCB     EQU   4                        LCB ADDRESS
RAT      EQU   5                        WORK REGISTER
ROT      EQU   6                        WORK REGISTER
RQCB     EQU   7                        QCB ADDRESS
RBUF     EQU   8                        BUFFER ADDRESS
RTSB     EQU   9                        TSB ADDRESS
RPLAY    EQU   10                       WORK REGISTER
RDISPA   EQU   11                       DISPATCHER ADDRESS
RBASE    EQU   12                       ENTRY1 ENT POINT AND BASE ADDR
RAVTSAVE EQU   13                       SAVE AREA ADDRESS, AVTSAVE2
RETURN   EQU   14                       RETURN ADDRESS
R15      EQU   15                       WORK AND PARAMETER REG
RWORK2   EQU   2                        WORK REGISTER           YS6327
RWORK5   EQU   5                        WORK REGISTER           YS6327
RWORK9   EQU   9                        WORK REGISTER           YS6327
         SPACE 3
********
******** MASKS
********
E1       EQU   1                        GIVES A VALUE OF 1
E2       EQU   2                        GIVES A VALUE OF 2
E3       EQU   3                        GIVES A VALUE OF 3
E1F      EQU   X'1F'                    TESTS FOR ERROR STATUS  YA04486
NOT      EQU   X'FF'                    ZEROS VARIOUS BITS
INTREQ   EQU   X'40'                    TESTS FOR INTERVENTION REQUIRED
DATACHK  EQU   X'08'                    TESTS FOR DATA CHECK
TIMEOUT  EQU   X'01'                    TESTS FOR TIME OUT
CINHIBIT EQU   X'80'                    TERMINAL HAS INHIBIT FEATURE
CBREAK   EQU   X'40'                    TERMINAL HAS BREAK FEATURE
C5041    EQU   X'10'                    TERMINAL IS ON 5041 LINE
SETINHB  EQU   X'10'                    MODE BIT FOR 3705       YS6327
ELEVEN   EQU   11                       GIVES A VALUE OF 11     YS6327
STMONITR EQU   X'02'                    MODE BIT FOR 3705       YS6327
LCBDELAY EQU   X'02'                    LCB IS IN DELAY QUEUE
HANGUP   EQU   4                        CALLS QTIP FOR HANG UP DUTIES
D7       EQU   7                        DISPLACEMENT OF 7 BYTES YA04486
D6       EQU   6                        DISPLACEMENT OF 6 BYTES@YA12165
         SPACE 3
********
******** ESTABLISH ADDRESSABILITY
********
         USING IEDAYH,RBASE             BASE ADDR IS PASSED
         USING IEDNTRM,R1               TTE ADDRESSABILITY     @Y17XAYP
         USING IEDQSCB,RSCB             SCB ADDR IS PASSED (ENTRY1)
*                                       OR DETERMINED LATER (ENTRY2)
         USING IEDQLCB,RLCB             LCB ADDR IS PASSED
         USING IEDQQCB,RQCB             QCB ADDR IS DETERMINED LATER
         USING IEDQDISP,RDISPA          DISPATCHER ADDR IS PASSED
         USING AVTSAVE2,RAVTSAVE        AVTSAVE2 ADDR IS PASSED
         USING IEDQPRF,RBUF             BFR ADDR IS DETERMINED LATER
         SPACE 3
* * * * * * * * * * * *  FIRST ('NORMAL') ENTRY * * * * * * * * * * * *
         SPACE 3
********
******** BRANCH PAST SECOND ENTRY'S QCB
********
ENTRY1   EQU   *
         B     SETUP                    QCB WORD 1 - GO TO ENTRY1
         DC    F'0'                     QCB WORD 2 - FILLER
         DC    AL1(DSPMCPL4),AL3(*-1)   QCB WORD 3 - INDEX AND CHAIN
         SPACE 3
* * * * * * * * * SECOND ('POST' OR 'HANG UP') ENTRY  * * * * * * * * *
         SPACE 3
********
******** SET UP ADDRESSES TO MATCH FIRST ENTRY'S
********
ENTRY2   EQU   *
         LR    RBASE,RQCB               SET UP BASE
         LR    RLCB,R1                  PUT LCB ADDR IN LCB REG
         L     RSCB,LCBSCBA-1           GET SCB ADDR
         SR    RPLAY,RPLAY              DENOTE ENTRY2 TAKEN (ZERO)
         B     FINDQCB                  BR PAST SETTING UP FOR ENTRY1
IEDAYH   IEDHJN ,
         SPACE 3
********
******** INITIALIZE FOR FIRST ENTRY
********
SETUP    DS    0H
         BALR  RPLAY,0                  DENOTE ENTRY1 TAKEN (NON-ZERO)
         LR    RBUF,R1                  SAVE POST LIST ADDR
         SPACE 3
* * * * * * * * * * * * CODE FOR ERROR CHECKING * * * * * * * * * * * *
         SPACE 3
********
******** FIND THE ASSOCIATED QCB
********
FINDQCB  EQU   *
         LH    R1,LCBTTCIN              GET INDEX TO TNT
         N     R1,AVTCLRHI              CLEAR HI-ORDER HALFWORD
         L     R15,AVTRNMPT             GET ADDR OF TNT
         BALR  RETURN,R15               BR TO TNT CODE TO GET ADDR
*                                       OF TERMINAL ENTRY IN REG 1
         LA    R15,TRMPRFSZ             GET SIZE OF TTE PREFIX @Y17XAYP
         SR    R1,R15                   SET TTE BASE           @Y17XAYP
         L     RQCB,TRMDESTQ-1          GET QCB ADDR FROM TERMINAL ENT
         LR    R15,RPLAY                PUT ENTRY CODE IN PARM REG
         SPACE 3
********
******** SEE IF THIS IS A TSO USER'S QCB
********
         TM    QCBFLAG,QCBTSSES         IS THIS A TSO USER
         BO    SEEIFERR                 YES - SEE IF ANY ERRS OCCURRED
         LTR   R15,R15                  EXIT ACCORDING TO ENTRY TAKEN
         BZ    DSPDISP                  ENTRY2 - GO TO TCAM DISPATCHER
         B     SAYONARA                 ENTRY1 - GO POST ERB
         SPACE 3
********
******** SEE IF HANG UP OR ERROR DURING DISCONNECT HAS OCCURRED
********
SEEIFERR EQU   *
         LTR   R15,R15                  HANG UP ENTRY, ENTRY2, TAKEN
         BZ    HANGITUP                 YES - CLEAN OUT THE USER
         TM    SCBERR4,SCBCONNN         ERROR DURING DISCONNECT
         BO    AUREVOIR                 YES - SET UP TO EXIT
         SPACE 3
********
******** SEE IF NO ERRORS HAVE OCCURRED AND EXIT IF SO
********
         CLI   SCBERR1,AVTEZERO         1ST BYTE ZERO           YA04575
         BNE   CHKMODE                  NO, ERROR               YA04575
         TM    SCBERR2,NOT-SCBCRMIN-SCBCRMAX 2ND BYTE O.K.      YA04575
*                                       NOTE THAT CRMIN AND MAX YA04575
*                                       ARE NOT ERRORS FOR TSO  YA04575
         BNZ   CHKMODE                  OTHERS ON, ERROR        YA04575
         CLI   SCBERR3,AVTEZERO         3RD BYTE ZERO           YA04575
         BNE   CHKMODE                  NO, ERROR               YA04575
         CLI   SCBERR4,AVTEZERO         4TH BYTE ZERO           YA04575
         BNE   CHKMODE                  NO, ERROR               YA04575
         NC    LCBSENS0,LCBSENS0        ANY ERRORS
         BNZ   CHKMODE                  YES - PROCESS THEM
         NI    QCBRETCT,NOT-QCBHUCT3    ZERO RETRY COUNT       @Y17XAYP
         B     AUREVOIR                 SET UP TO EXIT
         SPACE 3
********
******** SEE WHETHER LINE IS SENDING OR RECEIVING
********
CHKMODE  EQU   *
         TM    SCBERR2,SCBSOHE          IS THIS A SOH%R FOR 3270 S22028
         BZ    CHKMODE1                 NO, CHECK LINE SENDING   S22028
         L     ROT,AVTADBUF             GET CUR BUF ADDR        YA04486
         DROP  RBUF                     DROP ADDRESSABILITY     YA04486
         USING IEDQPRF,ROT                                      YA04486
         TM    PRFSTAT1,PRFNHDRN        NOT HEADER?             YA04486
         BO    CHKMODE1                 YES - SKIP TEST         YA04486
         TM    TRMSTATE,TRMPREF         IS THIS 3705?          @Y17XAYP
         BO    CHK3705                  CHECK FOR ERROR AT +6  @YA12165
         TM    PRFSHDR+D7,E1F           3270 SERIOUS ERROR?    @YA12165
         BM    COUNT                    YES GO COUNT           @YA12165
         B     CHKMODE1                 NO CONTINUE            @YA12165
CHK3705  EQU   *                                               @YA12165
         TM    PRFSHDR+D6,E1F            3270-3705 SERIOUS ERR @YA12165
         BM    COUNT                    YES GO COUNT           @YA12165
         DROP  ROT                      DROP ADDRESSABILITY     YA04486
         USING IEDQPRF,RBUF             RESTORE PREV. ADDABIL   YA04486
CHKMODE1 EQU   *                                                 S22028
         TM    LCBSTAT1,LCBSENDN        IS LINE SENDING
         BO    SENDMODE                 YES - TEST SEND ERRORS
         SPACE 3
* * * * * * * * * * CODE FOR ERRORS WHILE RECEIVING * * * * * * * * * *
         SPACE 3
         TM    TRMSTATE,TRMPREF         TRM HAVE PREFIX        @Y17XAYP
         BNO   NOTSNAER                 NO,DON'T CHECK SNA     @Y17XAYP
         TM    TRMBYTE0,TRMSNA          IS THIS A SNA TERMINAL @Y17XAYP
         BNO   NOTSNAER                 NO, SKIP CHECKING SNA  @Y17XAYP
*                                       SENSE CODES            @Y17XAYP
********                                                       @Y17XAYP
******** SNA ERRORS WHILE RECEIVING                            @Y17XAYP
********                                                       @Y17XAYP
         SPACE 3
         TM    SCBERR2,SCBRRJN          REQUEST REJECTED       @Y17XAYP
         BNO   NOTRQREJ                 NO,CHECK FOR OTHER ERR @Y17XAYP
         CLI   LCBSMIN,SNSNGRB          NEG RSP TO BID RETRY   @Y17XAYP
         BE    COUNT                    YES,COUNT ERROR        @Y17XAYP
         CLI   LCBSMIN,SNSINTRQ         INTERVENTION REQUIRED  @Y17XAYP
         BE    COUNT                    YES,COUNT ERROR        @Y17XAYP
         CLI   LCBSMIN,SNSRTRY          RETRY REQUESTED        @Y17XAYP
         BE    MSG                      YES,SEND MSG,COUNT ERR @Y17XAYP
NOTRQREJ EQU   *                        NOT REQUEST REJECTED   @Y17XAYP
         TM    SCBERR4,SCBSTERN         SNA STATE ERROR        @Y17XAYP
         BO    MSG                      YES,SEND MSG,COUNT ERR @Y17XAYP
         TM    SCBERR3,SCBFIERN         FUNCTION INTERPRETOR   @Y17XAYP
*                                       ERROR                  @Y17XAYP
         BO    MSG                      YES,SEND MSG,COUNT ERR @Y17XAYP
         TM    SCBERR3,SCBPERRN         SNA PATH ERROR         @Y17XAYP
         BO    HANGITUP                 YES, HANG UP           @Y17XAYP
         TM    SCBERR4,SCBCPMEN         SNA CPM ERROR          @Y17XAYP
         BO    HANGITUP                 YES, HANG UP           @Y17XAYP
         SPACE 3
NOTSNAER EQU   *                        NO SNA ERRORS          @Y17XAYP
********
******** HAS A PERMANENT ERROR OCCURRED IN A 3705 ATTACHED TERMINAL
********
         TM    TRMSTATE,TRMPREF         IS THIS 3705 TERMINAL  @Y17XAYP
         BNO   NOT3705                  NO, SKIP CTL UNIT TEST  YS6327
         TM    SCBERR4,SCBCTLUN         CONTROL UNIT ERROR      YS6327
         BO    HANGITUP                 YES, HANG UP NOW        YS6327
         SPACE 3                                                YS6327
********
******** HAS A 'COUNTABLE' ERROR OCCURRED (COUNT AND MESSAGE)
********
NOT3705  EQU   *                                                YS6327
         TM    SCBERR4,SCBSLCTN         IS IT ERROR DURING SELECT
         BO    COUNT                    YES - COUNT, CNCL, RET
         TM    SCBERR4,SCBUNDFN         A COUNTABLE ERROR
         BO    MSG                      YES - SEND MSG, CNT, CNCL, RET
         TM    LCBSENS0,NOT-TIMEOUT     ANY COUNTABLE ERRORS
         BNZ   MSG                      YES - SEND MSG, CNT, CNCL, RET
         SPACE 3
********
******** HAS A 'NON-COUNTABLE' ERROR OCCURRED (MESSAGE)
********
         TM    SCBERR1,SCBNOBFN+SCBCUTFN     ANY NON-COUNTABLE ERRS
         BNZ   CANCEL                   NO - CANCEL AND RET TO TCAM
         SPACE 3
********
******** FORGET 'REENTER' MESSAGE AND FORGET A RETRY COUNT
********
         B     FORGET                   TELL USER WHAT HAPPENED
         SPACE 3
* * * * * * * * * *  CODE FOR ERRORS WHILE SENDING  * * * * * * * * * *
         SPACE 3
SENDMODE EQU   *
********
******** HAS A PERMANENT ERROR OCCURRED
********
         TM    SCBERR3,SCBTMINN         TERMINAL INOPERATIVE
         BO    HANGITUP                 YES - CLEAN OUT THE USER
         TM    TRMSTATE,TRMPREF         IS THIS 3705 TERMINAL  @Y17XAYP
         BNO   SKIP3705                 NO, SKIP CTL UNIT TEST  YS6327
         TM    SCBERR4,SCBCTLUN         CONTROL UNIT ERROR      YS06328
         BO    HANGITUP                 YES - CLEAN OUT         YS6327
         TM    TRMBYTE0,TRMSNA          IS THIS A SNA TERMINAL @Y17XAYP
         BNO   SKIP3705                 NO,SKIP SNA CHECKS     @Y17XAYP
         SPACE 3
********                                                       @Y17XAYP
******** SNA ERRORS WHILE SENDING                              @Y17XAYP
********                                                       @Y17XAYP
         TM    SCBERR2,SCBRRJN          REQUEST REJECTED       @Y17XAYP
         BNO   OTHERERR                 NO,CHECK FOR OTHER ERR @Y17XAYP
         CLI   LCBSMIN,SNSRCXMT         RCVR IN TRANSMITT MODE @Y17XAYP
         BE    CHKBRK                   YES,CHECK FOR BREAK-IN @Y17XAYP
         CLI   LCBSMIN,SNSNGRB          NEGATIVE RSP TO BID    @Y17XAYP
         BE    COUNT                    YES, COUNT ERROR       @Y17XAYP
         CLI   LCBSMIN,SNSINTRQ         INTERVENTION REQUIRED  @Y17XAYP
         BE    COUNT                    YES, COUNT ERROR       @Y17XAYP
         CLI   LCBSMIN,SNSRTRY          RETRY REQUESTED        @Y17XAYP
         BE    CNTRTRY                  COUNT ERROR AND RETRY  @Y17XAYP
OTHERERR EQU   *                        NOT REQUEST REJECTED   @Y17XAYP
         TM    SCBERR4,SCBSTERN         SNA STATE ERROR        @Y17XAYP
         BO    COUNT                    YES, COUNT ERROR       @Y17XAYP
         TM    SCBERR3,SCBFIERN         FUNCTION INTERPRETOR   @Y17XAYP
*                                       ERROR                  @Y17XAYP
         BO    COUNT                    YES, COUNT ERROR       @Y17XAYP
         TM    SCBERR3,SCBPERRN         SNA PATH ERROR         @Y17XAYP
         BO    HANGITUP                 YES, HANG UP           @Y17XAYP
         TM    SCBERR4,SCBCPMEN         SNA CPM ERROR          @Y17XAYP
         BO    HANGITUP                 YES, HANG UP           @Y17XAYP
         SPACE 3
SKIP3705 EQU   *                                                YS6327
********
******** HAS A 'COUNTABLE' ERROR OCCURRED (COUNT ONLY)
********
         TM    SCBERR3,SCBXCEPN         A COUNTABLE ERROR
         BO    COUNT                    YES - COUNT AND RET TO TCAM
         TM    SCBERR4,SCBSLCTN+SCBUNDFN     ANY COUNTABLE ERRORS
         BNZ   COUNT                    YES - COUNT AND RET TO TCAM
         TM    LCBSENS0,NOT-DATACHK     ANY COUNTABLE ERRORS
         BNZ   COUNT                    YES - COUNT AND RET TO TCAM
         L     ROT,AVTADBUF             GET BUFFER ADDRESS     @SA77953
         TM    PRFSTAT1-IEDQPRF(ROT),PRFERMGN ERROR ON MESSAGE @SA77953
         BO    COUNT                    BRANCH YES             @SA77953
         SPACE 3
********
******** FORGET A RETRY COUNT AND FORGET CANCEL
********
FORGET   EQU   *
         NI    SCBERR4,NOT-SCBTXTTN     ZERO 'REENTER' MSG BIT
         NI    LCBSENS0,NOT-INTREQ      ZERO INTERVENTION REQ
         B     AUREVOIR                 RETURN TO TCAM
         SPACE 3
********                                                       @Y17XAYP
******** COUNT THIS ERROR AND REQUEST RETRY OF OUTPUT          @Y17XAYP
********                                                       @Y17XAYP
CNTRTRY  EQU   *                                               @Y17XAYP
*GET BUFFER AND SET INDICATOR FOR IEDAYO TO RETRY THIS OUTPUT  @Y17XAYP
         ICM   ROT,7,LCBLSPCI           GET BUFFER ADDRESS     @Y17XAYP
         OI    PRFSTAT1-IEDQPRF(ROT),PRFERMGN IEDAYO RETRY     @Y17XAYP
         B     COUNT                    COUNT THIS ERROR       @Y17XAYP
         SPACE 3
* * CODE FOR SENDING MESSAGE, RETRY COUNT, CANCEL AND RETURN TO TCAM  *
         SPACE 3
********
******** SEND A 'REENTER' MESSAGE AND GO MAKE A RETRY COUNT
********
MSG      EQU   *
         OI    SCBERR4,SCBTXTTN         SEND 'REENTER' MSG
         SPACE 3
********
******** SET UP A RETRY COUNT, IF ONE NOT PREVIOUSLY ESTABLISHED
********
COUNT    EQU   *
         TM    QCBRETCT,QCBHUCT3        HAS HANGUP SET UP A RETRY CNT
*                                                              @Y17XAYP
         BNZ   DECRECT                  YES - DECREMENT IT
         OI    QCBRETCT,QCBHUCT3        NO - SET ONE UP        @Y17XAYP
         B     CHKCANCL                 TELL USER WHAT HAPPENED
         SPACE 3
********
******** DECREMENT RETRY COUNT...IF COUNT HITS ZERO, HANG UP ON USER
********
DECRECT  EQU   *
         TM    QCBRETCT,QCBHUCT3        IS COUNT 3
         BO    CTOF3                    YES - GO DECREMENT COUNT
         TM    QCBRETCT,QCBHUCT1        IS COUNT 1
         BO    HANGITUP                 YES - BEGIN HANGUP PROCESS
         XI    QCBRETCT,QCBHUCT3        DECR COUNT BY 1 (IS NOW 1)
         B     CHKCANCL                 TELL USER WHAT HAPPENED
CTOF3    EQU   *
         NI    QCBRETCT,QCBHUCT2        DECR COUNT BY 1 (IS NOW 2)
         SPACE 3
********
******** SEE IF CANCELLING OF MESSAGE MUST BE SKIPPED
********
CHKCANCL EQU   *
         TM    LCBSTAT1,LCBSENDN        IS LINE SENDING
         BO    AUREVOIR                 YES - SKIP CANCEL
         SPACE 3
********
******** CANCEL THIS MESSAGE
********
CANCEL   EQU   *
         NI    SCBSTATE,NOT-SCBCKPT     CANCEL CHECKPOINT REQUEST
         LTR   RBUF,RBUF                END OF POST LIST
         BZ    SAYONARA                 YES - EXIT
TRYNEXT  EQU   *
         NC    PRFLINK,PRFLINK          ANY BFRS IN CHAIN
         BZ    HAVEBFR                  NO - BFR ADDR IN RBUF
         L     RBUF,PRFLINK-1           GET NEXT ADDR IN POST LIST
         B     TRYNEXT                  CHECK IT OUT
HAVEBFR  EQU   *
         OI    PRFSTAT1,PRFCNCLN        CANCEL THIS MSG
         SPACE 3
********
******** SEE IF MUST POST ERB TO BUFFER DISPOSITION
********
AUREVOIR EQU   *
         LTR   R15,R15                  ENTRY2 TAKEN
         BZ    ENTRY2X                  YES - SKIP THE FOLLOWING
         CLI   LCBTSTSW,AVTEFF          HAS HANGUP OCCURRED
         BNE   SAYONARA                 NO - CAN LEAVE NOW
         B     STOPMSG                  SEND NO MORE MSGGEN MSGS
ENTRY2X  EQU   *
         TM    LCBSTAT2,LCBMSGNN        MSGGEN IN PROCESS
         BO    NOSET                    YES - DON'T RESET LINE
         NI    LCBSTAT1,LCBOCNI+LCBOCWTN  CLEAR STATUS         @OY14092
         OI    LCBSTAT1,LCBRECVN        SET RECEIVE STATE      @YA11542
NOSET    EQU   *
         SPACE 3
********
******** POST ERB TO BUFFER DISPOSITION
********
         ST    R15,LCBERBLK-1           CLEAR LINK FIELD
         MVI   LCBERBPY,PRIRCQCB        SET PRIORITY
         L     RPLAY,AVTMSGS-1          GET MHR ADDR
         L     RPLAY,0(0,RPLAY)         GET ADDR OF BFR DISPOSITION
         ST    RPLAY,LCBERBQB-1         POST TO BFR DISPOSITION
         SPACE 3
********
******** BYPASS SUBSEQUENT MSGGENS
********
STOPMSG  EQU   *
         LA    RWORK,X0100              GET ADDR OF X0100 FOR
*                                       BFR DISPOSITION
         IC    RPLAY,SCBRCVCT           SAVE MSG LIMIT
         ST    RWORK,SCBMACR-1          STORE ADDR IN SCB
         STC   RPLAY,SCBRCVCT           RESTORE MSG LIMIT
         SPACE 3
********
******** EXIT TO TCAM DISPATCHER
********
SAYONARA EQU   *
         LA    R1,LCBERB                PUT ADDR OF ERB WHERE EXPECTED
         BAL   RETURN,DSPCHAIN          GO BACK                @Y17XAYX
         SPACE 3
************************************************************** @Y17XAYP
*        THIS ROUTINE SET INDICATORS THAT CAUSE THIS OUTPUT    @Y17XAYP
*        TO BE RE-SENT IF THE TERMINAL IS A BREAK TERMINAL AND @Y17XAYP
*        THE SNA STATUS (RECEIVER IN TRANSMIT MODE) WAS        @Y17XAYP
*        RECEIVED. IT ALSO SETS AN INDICATOR THAT CAUSES A     @Y17XAYP
*        SIGNAL DFC COMMAND TO BE SENT BEFORE THIS OUTPUT IS   @Y17XAYP
*        RE-TRIED.                                             @Y17XAYP
************************************************************** @Y17XAYP
CHKBRK   EQU   *                                               @Y17XAYP
         TM    QCBFLAG,QCBNOBRK         IS THIS A NOBREAK TERM @Y17XAYP
         BO    COUNT                    COUNT THE ERROR        @Y17XAYP
         ICM   ROT,7,LCBLSPCI           GET BUFFER THAT WILL   @Y17XAYP
*                                       POSTED TO IEDAYO       @Y17XAYP
         OI    PRFSTAT1-IEDQPRF(ROT),PRFERMGN  IEDAYO RETRY    @Y17XAYP
         TM    TRMBYTE2,TRMWRBRK        IS WRITE BREAK ALREADY @Y17XAYP
*                                       IN PROGRESS            @Y17XAYP
         BO    NOBRK                    YES,POST ERB TO BD     @Y17XAYP
         OI    QCBFLAG,QCBREAD          ASSUME TPUT NOBREAK    @Y17XAYP
*                                       AND SET QCBREAD TO     @Y17XAYP
*                                       PREVENT RETRY UNTIL    @Y17XAYP
*                                       EITHER DATA IS ENTERED @Y17XAYP
*                                       OR LUSTAT (COMPONENT   @Y17XAYP
*                                       AVAILABLE) IS RECEIVED @Y17XAYP
         TM    QCBTSOF1,QCBWRBRK        TPUT BREAK REQUEST     @Y17XAYP
         BNO   NOBRK                    NO,POST ERB TO BD      @Y17XAYP
         NI    QCBFLAG,NOT-QCBREAD      RESET QCBREAD          @Y17XAYP
         LA    RPLAY,QCBPRFSZ           GET SIZE OF QCB PREFIX @Y17XAYP
         SR    RQCB,RPLAY               BACK UP TO PREFIX      @Y17XAYP
         DROP  RQCB                                            @Y17XAYP
         USING IEDNQCB,RQCB             PREFIX ADDRESSABILITY  @Y17XAYP
         OI    QCBSTAT1,QCBESIG         INDICATE THAT SIGNAL   @Y17XAYP
*                                       SHOULD BE SENT         @Y17XAYP
NOBRK    EQU   *                                               @Y17XAYP
         XC    SCBERRST,SCBERRST        CLEAR ERROR WORD       @Y17XAYP
         B     SAYONARA                 POST ERB TO BD         @Y17XAYP
         SPACE 3
* * * * * * * * * * * * * DISCONNECT THE USER * * * * * * * * * * * * *
         SPACE 3
********
******** GET ASID FROM QCB AND ENSURE THAT IT IS NON-ZERO
********
HANGITUP EQU   *
         DROP  RQCB                                            @Y17XAYP
         USING IEDQQCB,RQCB             QCB ADDRESSABILITY     @Y17XAYP
         TM    QCBTSOF1,QCBDELAY        QCB IN DELAY QUEUE
         BO    DIFFASID                 YES,GET ASID FROM LINK @G36XRYP
         LH    RPLAY,QCBTJID            GET ASID               @G36XRYP
         B     TESTASID                 CHK FOR NON-ZERO TJID  @G36XRYP
DIFFASID EQU   *                                               @G36XRYP
         LH    RPLAY,QCBLINK+1          LOAD ASID              @G36XRYP
TESTASID EQU   *                                               @G36XRYP
         LTR   RPLAY,RPLAY              ASID VALID (NON-ZERO)  @G36XRYP
         BZ    AUREVOIR                 NO - WE ALREADY DID OUR THING
         SPACE 3
********
******** FIND THE ASSOCIATED ASCB AND TSB
********
         TM    AVTBIT3,AVTTSAB          TEST FOR TSO HAS ABENDED A44839
         BO    CHKERB                   YES, GOTO DEQUEUE ERB    A44839
         L     RAT,CVTPTR               GET CVT ADDR
         USING CVT,RAT
         L     RTSB,CVTASVT             GET ASVT ADDRESS       @ZM47649
         DROP  RAT                                             @G36XRYP
         BCTR  RPLAY,0                  DECREMENT ASID FOR     @G36XRYP
*                                       ZERO ORIGIN            @G36XRYP
         SLL   RPLAY,2                  MULTIPLY BY 4          @G36XRYP
         AR    RTSB,RPLAY                                      @G36XRYP
         L     RASCB,ASVTENTY-ASVT(,RTSB)   ASCB ADDRESS       @ZM46782
         USING ASCB,RASCB               ASCB ADDRESSABILITY    @G36XRYP
         L     RTSB,ASCBTSB             GET TSB ADDRESS        @G36XRYP
         USING TSB,RTSB
         SPACE 3
********
******** SEE IF ASCB/TSB HAVE BEEN THRU HANG UP OR LOGOFF ALREADY
********
         TM    TRMSTATE,TRMPREF         IS THIS 3705 TERMINAL  @Y17XAYP
         BZ    NONCP1                   NO,CONTINUE            @YA08078
         TM    SCBERR4,SCBCTLUN         CU ERROR               @UA08078
         BO    CHKERB                   YES,CONTINUE           @YA08078
NONCP1   EQU   *                                               @YA08078
         TM    TSBFLG4,TSBHUNG          HANG UP ALREADY        @G36XRYP
         BO    AUREVOIR                 YES - NO NEED TO GO THRU AGAIN
         TM    TSBSTAT,TSBDISC          LOGOFF BEEN ENTERED BEFORE
         BO    AUREVOIR                 YES - NO NEED TO GO THRU AGAIN
         SPACE 3
********
******** SEE IF ERB MUST BE DEQUEUED AND BASIC UNITS TPOSTED TO BFR RET
********
CHKERB   EQU   *                                                 A44839
         LR    RAT,R1                   SAVE TRM ADDR
         LR    ROT,R15                  SAVE ENTRY CODE
         TM    LCBERBST,LCBINQ          IS ERB ENQUEUED
         BNO   INQEXIT                  NO - NO NEED TO REMOVE
         SPACE 3
********
******** SCAN BUFFER RETURN QUEUE TO FIND ERB
********
         LA    RWORK,LCBERB             GET THIS LINE'S ERB ADDR
         LA    RPLAY,AVTBFRTB           GET ADDR OF ERB POINTER
INQSRCH  EQU   *
         CLC   E1(E3,RPLAY),AVTDELAD+1  IS THIS THE END OF QUEUE
         BZ    INQRESET                 YES - ERB NOT FOUND
         L     R15,0(0,RPLAY)           GET ADDR OF ERB (IF ANY)
         LA    R15,0(0,R15)             CLEAR HI-ORDER BYTE
         CR    R15,RWORK                ERB FOR THIS LINE
         BE    INQDEQ                   YES - GO DEQUEUE IT
         LA    RPLAY,LCBERBLK-1-LCBERB(0,R15)     GET ADDR OF
*                                       ERB POINTER
         B     INQSRCH                  CONTINUE SEARCHING
         SPACE 3
********
******** DEQUEUE ERB AND RETURN BASIC UNITS
********
INQDEQ   EQU   *
         MVC   E1(E3,RPLAY),LCBERBLK    REMOVE ERB FROM CHAIN
         NC    LCBERBCH,LCBERBCH        ANY BASIC UNITS TO FREE
         BZ    INQRESET                 NO - SHOW ERB DEQUEUED
         DROP  R1                       DROP TRM ADDRESSABILITY
         L     R1,LCBERBCH-1            GET ADDR OF BFR FRAGMENT
         DROP  RBUF
         USING IEDQPRF,R1               TCAM BFR ADDRESSABLE
         XC    LCBERBCH,LCBERBCH        CLEAR CHAIN POINTER
         SPACE 3
********
******** TPOST BASIC UNITS TO BUFFER RETURN
********
INQLOOP  EQU   *
         LA    RPLAY,AVTBFRTB           GET BFR RET QCB ADDR
         ST    RPLAY,PRFQCBA-1          PUT QCB ADDR IN ELEMENT
         MVI   PRFPRI,PRIBFRTB          SET TPOST PRIORITY
         L     RPLAY,PRFLINK-1          GET NEXT BFR'S ADDRESS
         XC    PRFLINK,PRFLINK          CLEAR BUFFER POINTER
         LR    RTSB,RQCB                SAVE QCB ADDR
         BAL   RETURN,DSPPOSTR          GO TPOST
         L     RSCB,LCBSCBA-1           RESTORE SCB ADDR
         LR    RQCB,RTSB                RESTORE QCB ADDR
         LA    RPLAY,0(0,RPLAY)         ZERO HI-ORDER BYTE
         LTR   R1,RPLAY                 MORE BASIC UNITS TO TPOST
         BNZ   INQLOOP                  YES - GO TPOST
         L     RTSB,ASCBTSB             RESTORE TSB ADDR       @G36XRYP
INQRESET EQU   *
         NI    LCBERBST,NOT-LCBINQ      SHOW ERB DEQUEUED
INQEXIT  EQU   *
         DROP  R1                       DROP BFR ADDRESSABILITY
         USING IEDQPRF,RBUF
         USING IEDQTRM,R1               TRM AGAIN ADDRESSABLE
         SPACE 3
         TM    AVTBIT3,AVTTSAB          TEST FOR TSO HAS ABENDED A44839
         BO    CHKLCB                   YES, DO NOT ISSUE QTIP   A44839
         USING TSB,RTSB                                         YA00689
         SPACE 3                                                 A44839
********
******** GO TO QTIP TO TELL TSC ABOUT THIS HANG UP AND TO INVOKE SIL
********
         LR    R1,RAVTSAVE              GET SAVE AREA ADDR FOR QTIP
         QTIP  HANGUP,(1)               TELL TSO ABOUT THIS HANG UP
         SPACE 3
********
******** REMOVE LCB FROM DELAY QUEUE IF NECESSARY
********
CHKLCB   EQU   *                                                 A44839
         TM    LCBINSRC+2,LCBDELAY      IS LCB IN DELAY QUEUE
         BZ    CHKQCB                   NO - SEE IF QCB IS
         LR    R1,RLCB                  PUT LCB ADDR WHERE NEC
         L     R15,AVTHG02              GET ADDR OF REMOVAL RTN
         BALR  RETURN,R15               GO REMOVE LCB FROM QUEUE
         SPACE 3
********
******** PUT RECEIVE SCHEDULER BACK INTO STCB CHAIN
********
         LA    RQCB,LCBRSKEY            GET ADDR OF FIRST STCB
         LR    R1,RQCB                  GET ADDR OF FIRST STCB
         BAL   RETURN,DSPPRIOR          GO CHAIN
         L     RSCB,LCBSCBA-1           RESTORE SCB ADDR
         L     RQCB,TRMDESTQ-1-IEDNTRM(,RAT)  RESTORE QCB ADDR @Y17XAYP
         SPACE 3
********
******** REMOVE QCB FROM DELAY QUEUE IF NECESSARY
********
CHKQCB   EQU   *
         TM    QCBTSOF1,QCBDELAY        IS QCB IN DELAY QUEUE
         BZ    CLEANUP                  NO - GO CLEAN CTRL BLKS
         LR    R1,RQCB                  PUT QCB ADDR WHERE NEC
         L     R15,AVTHG02              GET ADDR OF REMOVAL RTN
         BALR  RETURN,R15               GO REMOVE QCB FROM QUEUE
         SPACE 3
* * * * * * * * * * * * CONTROL BLOCK CLEAN UP  * * * * * * * * * * * *
         SPACE 3
********
******** PREPARE LCB AND SCB FOR NEXT USER
********
CLEANUP  EQU   *
         ST    RAT,AVTDOUBL+4           SAVE TERM ADDR         @OY15019
         USING IEDNTRM,R1               TTE ADDRESSABILITY     @Y17XAYP
         ST    ROT,AVTDOUBL             SAVE ENTRY CODE        @OY14092
         MVI   LCBTSTSW,AVTEFF          IND LINE IS TO BE ENABLED
         OI    LCBSTAT2,LCBNEGRP        TELL TCAM TO DISCONNECT
         NI    LCBSTAT2,NOT-LCBRESP     TERM NEEDS NO RESPONSE   A46135
         NI    LCBTSOB,NOT-LCBTSBUF-LCBSATRD-LCBCIRCD  RESET FLAG
         MVI   LCBSENS0,AVTEZERO        RESET STATUS
         MVI   LCBTPCD+ELEVEN,AVTEZERO  CLEAR TP OP CODE       @OY15030
         L     RPLAY,LCBDCBPT           GET DCB ADDR           @OY14092
         TM    DCBDSORG-IHADCB(RPLAY),TRMLGB  3705 RESOURCE    @OY14092
         BO    NOSTOP                   YES, BRANCH            @OY14092
         TM    LCBSTAT1,LCBOCWTN        STOPLINE IN PROGRESS   @OY14092
         BZ    NOSTOP                   NO, BRANCH             @OY14092
         SR    RAT,RAT                  CLEAR FOR RLN          @OY14092
         IC    RAT,LCBUCBX              GET RLN                @OY14092
         SLL   RAT,E2                   MULTIPLY BY FOUR       @OY14092
         L     RAT,DCBINVLI-IHADCB(RAT,RPLAY) GET LIST ADDR    @OY14092
         SR    RPLAY,RPLAY              CLEAR FOR              @OY14092
         IC    RPLAY,E1(,RAT)           GET NO. ACTIVE ENTRIES @OY14092
         LTR   RPLAY,RPLAY              ANY ACTIVE             @OY14092
         BZ    NOWAIT                   NO, GO SET BIT         @OY14092
         LA    ROT,D7(,RAT)             POINT TO ENTRY-ONE     @OY14092
ENTLOOP  EQU   *                                               @OY14092
         SR    R1,R1                    CLEAR                  @OY14092
         IC    R1,E2(,RAT)              GET WIDTH              @OY14092
         AR    ROT,R1                   POINT TO INDEX BYTE    @OY14092
         SR    RWORK,RWORK              CLEAR                  @OY14092
         IC    RWORK,EZERO(,ROT)        GET INDEX BYTE         @OY14092
         SLL   RWORK,E1                 MULTIPLY BY TWO        @OY14092
         LR    R1,RAT                   GET LIST ADDR          @OY14092
         SR    R1,RWORK                 BACKUP TO TNT          @OY14092
         LH    R1,EZERO(,R1)            PICK UP TNT            @OY14092
         L     R15,AVTRNMPT             GET LOOKUP ROUTINE     @OY14092
         BALR  RETURN,R15               CALL ROUTINE           @OY14092
         L     R1,TRMDESTQ-1-IEDQTRM(,R1) GET QCB ADDR         @OY14092
         TM    QCBFLAG-IEDQQCB(R1),QCBTSSES IN SESSION         @OY14092
         BNZ   NOSTOP                   YES, GET OUT           @OY14092
         BCT   RPLAY,ENTLOOP            GET NEXT               @OY14092
NOWAIT   EQU   *                                               @OY14092
         NI    LCBSTAT1,AVTEFF-LCBOCWTN RESET WAIT BIT         @OY14092
         OI    LCBSTAT1,LCBOCNI         SET STOP BIT           @OY14092
NOSTOP   EQU   *                                               @OY14092
         L     R15,AVTDOUBL             RESTORE ENTRY CODE     @OY14092
         L     R1,AVTDOUBL+4            RESTORE TRM ADDR       @OY15019
         XC    SCBERRST(SCBRGSAV-SCBERRST),SCBERRST  RESET SCB
         SPACE 3
********
******** PREPARE QCB FOR NEXT USER
********
         NI    QCBFLAG,NOT-QCBTSSES-QCBREAD-QCBNOBRK  RESET STATUS
         NI    QCBDSFLG,NOT-QCBALTMH    RESET ALTERNATE MH PATH  S22029
         MVI   QCBRETCT,AVTEZERO        ZERO RETRY COUNT
         MVI   QCBCARCT,AVTEZERO        ZERO CARRIAGE COUNT
         XC    QCBTJID,QCBTJID          ZERO ASID              @G36XRYP
         MVC   QCBSATCT(E3),AVTFZERO    ZERO COUNT AND FLAGS
         SPACE 3
********
******** FIND TERMINAL CHARACTERISTICS TABLE TO INITIALIZE QCB BITS
********
         SR    RPLAY,RPLAY              ZERO REG FOR IC INSTR
         IC    RPLAY,TRMCHCIN           GET INDEX
         BCTR  RPLAY,0                  SUBTRACT 1
         MH    RPLAY,AVTDCTLN           MULT BY DCT ENTRY SIZE @Y17XAYP
         A     RPLAY,AVTCSTCS           ADD BASE FOR CHAR TBL
         USING IEDDCT,RPLAY             DCT ADDRESSABILITY     @Y17XAYP
         SPACE 3
********
******** INITIALIZE QCB BITS ACCORDING TO TERMINAL CHARACTERISTICS TBL
********
         TM    DCTBYTE1,DCTBREAK        BREAK FEATURE PRESENT  @Y17XAYP
         BO    CHK5041                  YES - CHK 5041 STATUS
         OI    QCBFLAG,QCBNOBRK         DENOTE ABSENCE OF BREAK
         NI    LCBSTAT2,NOT-LCBSNDPR    TURN OFF SEND PRIORITY @OZ18790
CHK5041  EQU   *
         TM    DCTBYTE1,DCT5041         5041 LINE              @Y17XAYP
         BNO   SKIPRST                  NO, DON'T RESET         YS6327
         NI    LCBTSOB,NOT-LCB2741N     RESET 2741 INDICATOR    YS6327
SKIPRST  EQU   *                                                YS6327
         SPACE 3                                                YS6327
********
******** FOR 3705, SET UP DEVICE MODE FIELD TO RESET TERMINAL TO
******** MATCH DCT SPECIFICATIONS
********
         TM    TRMDEVFL+1,TRMRNTRM      IS THIS 3705 LINE       YS6327
         BNO   AUREVOIR                 NO, SKIP DEV MODE       YS6327
         TM    TRMSTATE,TRMOPTFN        ARE THERE ANY OPTN FLDS YS6327
         LA    RWORK2,TRMOPNO           ASSUME NONE             YS6327
         BNO   NOOPTION                 BRANCH ON NO OPTN FLDS  YS6327
         SR    RWORK5,RWORK5            CLEAR REGISTER FOR IC   YS6327
         IC    RWORK5,TRMOPNO           NUMBER OF OPTION FIELDS YS6327
         LA    RWORK2,TRMOPT(RWORK5)    SKIP OPTION FIELDS      YS6327
NOOPTION EQU   *                                                YS6327
         LH    RWORK5,TRMDEVFL          GET DEVICE DEP FLAGS    YS6327
         LA    RWORK9,ELEVEN            SET FOR BIT SEARCH      YS6327
         SLL   RWORK5,16                SHIFT BITS TO HI ORDER  YS6327
*                                       TWO BYTES               YS6327
         SR    ROT,ROT                  CLEAR REGISTER          YS6327
SHIFT    EQU   *                                                YS6327
         LTR   RWORK5,RWORK5            IS BIT PRESENT          YS6327
         BNM   CHKCOUNT                 BRANCH ON NO            YS6327
         LA    ROT,E1(ROT)              NUMBER OF BITS FOUND    YS6327
CHKCOUNT EQU   *                                                YS6327
         SLL   RWORK5,1                 SHIFT TO CHECK NEXT BIT YS6327
         BCT   RWORK9,SHIFT             HAVE ALL BITS BEEN CHKD YS6327
         LTR   ROT,ROT                  ANY DEV DEP FIELDS      YS6327
         BZ    SETMODE                  NO, SET DEV MODE FLDS   YS6327
         SR    RWORK5,RWORK5            CLEAR REG FOR IC        YS6327
SKIP     IC    RWORK5,AVTEZERO(,RWORK2) GET DEV DEP FLD LENGTH  YS6327
         LA    RWORK2,E1(RWORK5,RWORK2) SKIP OVER FIELD         YS6327
         BCT   ROT,SKIP                 SKIP TO NEXT FIELD      YS6327
SETMODE  EQU   *                                                YS6327
         LA    RWORK2,E1(RWORK2)        GET DEV DEP FOR SETMODE YS6327
         SPACE 3                                                YS06327
********
******** INDICATE HANGUP PROCESSING COMPLETE
********
         NI    E1(RWORK2),NOT-STMONITR  TURN OFF MONITOR MODE   YS06327
         NI    TRMPRE1,NOT-TRMLYNCH    TURN OFF TRMLYNCH BIT   @Y17XAYP
         OI    TRMPRE,TRMCMODE          INDICATE SET MODE      @Y17XAYP
*                                       SHOULD BE SENT         @Y17XAYP
         SPACE 3                                                YS06327
********
******** SET DEVICE MODE FIELDS SO 3705 MATCHES DCT
********
         MVI   0(RWORK2),AVTEZERO       ZERO BYTE 0, AND ...    YS6327
         MVI   E1(RWORK2),AVTEZERO      1, TO FORCE SET MODE    YS6327
         TM    DCTBYTE1,DCTINHIB        T.O.-SUPPRESSION       @Y17XAYP
         BNO   OFFINHIB                 NO, GO TURN BIT OFF     YS6327
         OI    E3(RWORK2),SETINHB       YES, SET T.O. SUPPRESS  YS6327
         B     AUREVOIR                 DONE                    YS6327
OFFINHIB EQU   *                                                YS6327
         NI    E3(RWORK2),NOT-SETINHB   DON'T INHIBIT TIMEOUTS  YS6327
         SPACE 3                                                YS6327
         SPACE 3
********
******** EXIT TO TCAM DISPATCHER
********
         B     AUREVOIR                 SET UP TO EXIT
         SPACE 3
********
******** CONSTANTS
********
X0100    DC    X'0100'                  MSGGEN INDICATOR
PATCH    DC    20F'0'                   PATCH AREA              YA04486
         SPACE 3
* * * * * * * * * * * *  CONTROL BLOCK DSECTS * * * * * * * * * * * * *
         SPACE 3
CVT      DSECT
         CVT
         SPACE 3
         IHAASCB
         SPACE 3
         IHAASVT
         SPACE 3
         IKJTSB
         SPACE 3
         TAVTD
         SPACE 3                                               @Y17XAYP
         TDCTD
         SPACE 3
         TLCBD
         SPACE 3
         TQCBD
         SPACE 3
         TSCBD
         SPACE 3                                               @Y17XAYP
         TSNSD
         SPACE 3
         TPRFD
         SPACE 3
         TTRMD
         SPACE 3
         TDISPD
         SPACE 3
         TPRIOR
         TLGBD
         SPACE
         DCBD  DSORG=TX
         SPACE
         END
