         TITLE 'IEDAYE - TIOC OUTPUT EDIT CONTROL ROUTINE'
IEDAYE   CSECT
         SPACE 3
***********************************************************************
*                                                                     *
* MODULE NAME = IEDAYE (TCAM,TSO)                             @Y17XAYP*
*                                                                     *
* DESCRIPTIVE NAME = TIOC OUTPUT EDIT CONTROL ROUTINE                 *
*                                                                     *
* COPYRIGHT = NONE                                                    *
*                                                                     *
* STATUS = VERSION 10.0                                       @G36XRYP*
*                                                                     *
* FUNCTION = THE TIOC OUTPUT EDIT CONTROL ROUTINE RECEIVES CONTROL    *
*            FROM THE TSOUTPUT ROUTINE (IEDAYO) AND PERFORMS INITIAL  *
*            PROCESSING REQUIRED TO SEND OUTPUT MESSAGES TO TSO       *
*            TERMINALS. THE PROCESSING OF THIS MODULE PROCEDES AS     *
*            FOLLOWS: CLEAR OUT THE INTERNAL EDIT PARAMETER LIST      *
*            (GMAIN) WHICH RESIDES IN AVTSAVE4. STORE THE MESSAGE     *
*            LENGTH IN GMAIN. STORE THE TCAM BUFFER SIZE IN GMAIN.    *
*            STORE THE ADDRESS OF THE TCAM BUFFER IN GMAIN. STORE THE *
*            TSO FLAGS IN GMAIN. THE CONNECT INDEX IS OBTAINED SO THAT*
*            THE PROPER DEVICE CHARACTERISTICS TABLE ENTRY CAN BE     *
*            LOCATED AND SAVED IN AVTPARM3. IF THE DEVICE IS A 2260   *
*            A LOCAL OR REMOTE BIT IS TURNED IN AVTPARM. IF THE DEVICE*
*            IS A 1050 A BIT IS SET IN FLAG2 OF GMAIN. IF THE DEVICE  *
*            IS A DISPLAY THE PROPER SCREEN SIZE PARAMETERS ARE STORED*
*            IN GMAIN. IF THE DEVICE IS NOT A DISPLAY A DEFAULT       *
*            CARRAIGE WIDTH OF 120 CHARACTERS IS STORED IN GMAIN. IF  *
*            THE DEVICE IS A 3270 CONTROL IS PASSED TO IEDAYB TO      *
*            PERFORM THE EDITING FUNCTION. IF THE DEVICE IS CONNECTED *
*            VIA A 3705 CONTROL IS PASSED TO IEDAYV TO PERFORM THE    *
*            EDITING FUNCTION. OTHERWISE THE DEVICE MUST BE ATTACHED  *
*            VIA A 270X AND CONTROL IS THEREFORE PASSED TO IEDAYW TO  *
*            PERFORM THE EDITING FUNCTION.                            *
*                                                                     *
* NOTES = SEE BELOW                                                   *
*                                                                     *
*    DEPENDENCIES = THE OPERATION OF THIS MODULE DOES NOT DEPEND UPON *
*                   A PARTICULAR INRERNAL REPESENTATION OF THE        *
*                   EXTERNAL CHARACTER SET. THE MESSAGE PROVIDED FROM *
*                   THE TSO MESSAGE GENERATION ROUTINE MAY BE A MSGGEN*
*                   MESSAGE, A SIMULATED ATTENTION MESSAGE, AN        *
*                   AUTOMATIC LINE NUMBERING MESSAGE, OR A PROMPT     *
*                   MESSAGE. THIS ROUNTINE PROCESSES THEM IDENTICALLY *
*                                                                     *
*    RESTRICTIONS = NONE                                              *
*                                                                     *
*    REGISTER CONVENTIONS = SEE INPUT AND OUTPUT SECTIONS             *
*                                                                     *
*    PATCH LABEL = AYEPATCH                                           *
*                                                                     *
* MODULE TYPE =                                                       *
*                                                                     *
*    PROCESSOR = ASSEMBLER F                                          *
*                                                                     *
*    MODULE SIZE = @ 500 DECIMAL BYTES                                *
*                                                                     *
*    ATTRIBUTES = SERIALLY REUSABLE, REFRESHABLE, ENABLED, RESIDENT,  *
*                 PROBLEM PROGRAM MODE                                *
*                                                                     *
* ENTRY POINT = IEDAYE                                                *
*                                                                     *
*   PURPOSE = SEE FUNCTION.                                           *
*                                                                     *
*   LINKAGE = SEE INPUT AND OUTPUT BELOW.                             *
*                                                                     *
* INPUT = REGISTER 1 POINTS TO THE TCAM BUFFER                        *
*         REGISTER 2 POINTS TO THE TSO BUFFER OR MSGGEN DATA.         *
*         REGISTER 3 POINTS TO THE SCB.                               *
*         REGISTER 4 POINTS TO THE LCB.                               *
*         REGISTER D POINTS TO AVTSAVE2.                              *
*         REGISTER E POINTS TO THE RETURN ADDRESS.                    *
*         REGISTER F POINTS TO THE ENTRY POINT OF IEDAYE.             *
*                                                                     *
* OUTPUT = REGISTER 4 POINTS TO THE LCB
*          REGISTER 6 POINTS TO THE INPUT DATA AREA                   *
*          REGISTER 7 POINTS TO THE OUTPUT DATA AREA                  *
*          REGISTER B POINTS TO AVTSAVE4(INTERNAL EDIT WORK AREA)     *
*          REGISTER D POINTS TO AVTSAVE2.                             *
*          REGISTER F POINTS TO ENTRY POINT OF IEDAYB,IEDAYV,IEDAYW   *
*                                                                     *
* EXIT-NORMAL = TO THE 3270 EDIT ROUTINE(IEDAYB).                     *
*               TO THE 3705 START/STOP EDIT ROUTINE(IEDAYV).          *
*               TO THE 270X START/STOP EDIT ROUTINE(IEDAYW).          *
*                                                                     *
* EXIT-ERROR = NONE                                                   *
*                                                                     *
* EXTERNAL REFERENCES = SEE BELOW.                                    *
*                                                                     *
*   ROUTINES = TERM NAME TABLE CODE.                                  *
*                                                                     *
*   DATA AREAS = NONE.                                                *
*                                                                     *
*   CONTROL BLOCK = AVT.                                              *
*                   ASCB                                       @G36XRYP
*                   ASVT                                       @G36XRYP
*                   CVT                                        @G36XRYP
*                   DCT                                        @Y17XAYP
*                   LCB.                                              *
*                   SCB.                                              *
*                   TRM.                                              *
*                   TSB.                                              *
*                   QCB.                                              *
*                   TSINPUT QCB.                                      *
*                                                                     *
*   MACROS = SAVE                                                     *
*            IEDHJN.                                                  *
*                                                                     *
*CHANGE ACTIVITY AS FOLLOWS
******************** MICROFICHE FLAGS *********************** SUPT CODE
*A544000                                                        SA68007
* C282000,500000                                               @SA74053
* A354000                                                      @SA74053
*A802000                                                       @SA75159
*A006000-008000,020000,188000,348000-352000,420000-422000      @Y17XAYP
*A422000                                                       @Y17XAYP
*C444000                                                       @Y17XAYP
*                                                              @G36XRYP
*A554000,844000                                                @G36XRYP
*C452000,456000,466000,480000,488000,500200,520000,526000      @G36XRYP
*C530000,550000,598000,652000                                  @G36XRYP
*D298000-306000,544600-548000,848000-850000,856000             @G36XRYP
*D332000                                                       @G36XRYP
*A490000,491200                                                @OY18014
***********************************************************************
         EJECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
* * * * *     R E G I S T E R  E Q U A T E S  * * * * * * * * * * * * *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
R0       EQU   0                      REGISTER 0
RPARM    EQU   1                        REGISTER 1
RPARM2   EQU   2                        REGISTER 2
RTIOC    EQU   2                        TSO BUFFER
RLCB     EQU   4                        LCB
RQCB     EQU   5                        QCB
RTSBUF   EQU   6                        TSO BUFFER ADDRESS
RTCBUF   EQU   7                        TCAM BUFFER ADDRESS
RTSB     EQU   8                        TSB
RTEMP    EQU   8                        RTEMP/RTEMPA MUST BE EVN/ODD PR
RTEMPA   EQU   9                        WORK REGISTER
RDCB     EQU   10                       DCB
RMAIN    EQU   11                       INTERNAL EDIT WORK AREA
RBASE    EQU   12                       BASE REGISTER
RAVT     EQU   13                       POINTER TO AVTSAVE2
RLINK    EQU   14                       LINK REGISTER
RRTN     EQU   15                       RETURN REGISTER
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         EJECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
* * * * * * * * *  M I S C E L L A N E O U S  E Q U A T E S * * * * * *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
ZERO     EQU   0                        ZERO
ONE      EQU   1                        ONE
TWO      EQU   2                        TWO
THREE    EQU   3                        THREE
FOUR     EQU   4                        FOUR
QCBDEND  EQU   8                  SPECIAL ENTRY FLAG           @SA74053
TWELVE   EQU   12                       TWELVE
SIXTEEN  EQU   16                       SIXTEEN
TWENTY4  EQU   24                       TWENTY FOUR
FORTY    EQU   40                       FORTY
FIFTYTWO EQU   52                       FIFTY TWO
EIGHTY   EQU   80                       EIGHTY
ONETWNTY EQU   120                      ONE HUNDRED TWENTY
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         EJECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
* * * * * *  A D D R E S S A B I L I T Y  * * * * * * * * * * * * * * *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         USING IEDAYE,RBASE             ESTABLISH MODULE BASE
         USING AVTSAVE2,RAVT            ESTABLISH SAVE AREA
         USING IEDQQCB,RQCB             ESTABLISH QCB ADDRESS
         USING IEDQLCB,RLCB             ESTABLISH LCB ADDRESS
         USING IHADCB,RDCB              ESTABLISH DCB ADDRESS
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         EJECT
         SAVE  (14,12)
         LR    RBASE,RRTN               SET BASE REGISTER
IEDAYE   IEDHJN AYE00100                MODIFICATION DATE
         L     RMAIN,AVTTSOPT           GET ADDRESS OF THE     @Y17XAYP
*                                       TSINPUT QCB            @Y17XAYP
         USING IEDQTSI,RMAIN            TSINPUT QCB            @Y17XAYP
*                                       ADDRESSABILITY         @Y17XAYP
         LA    RMAIN,TSITSW             GET EDIT WORKAREA      @YM05635
         DROP  RMAIN                    RELEASE TSINPUT REG    @Y17XAYP
         USING GMAIN,RMAIN              WRKAREA ADDRESSABILITY @Y17XAYP
         MVI   GMAIN,ZERO               CLEAR FIRST BYTE OF WORK AREA
         MVC   GMAIN+ONE(CARCT+ONE-GMAIN-ONE),GMAIN CLEAR WORK @SA74053
*                                 AREA                         @SA74053
         MVC   LINESV(EMGEN-LINESV),GMAIN CLEAR EXCEPT         @SA74053
*                                 CARCT+1                      @SA74053
         MVC   MSGNLNTH+TWO(10),GMAIN   EXCEPT FOR MSGNLNTH
         XC    TSCURR,TSCURR            CLEAR BUFFER FIELD
         STH   R0,MSGLEN                MESSAGE SIZE
         SRL   R0,SIXTEEN               SHIFT BUF SIZE TO LOW 2 BYTES
         STH   R0,WBUFSZ                TCAM BUF SIZE
         LA    RTCBUF,ZERO(,RPARM)      GET TCAM BUFFER ADDRESS
*                                       SCB ADDR IF MSGGEN ENTRY
         ST    RTCBUF,TCBUFR            STORE IT
         SRL   RPARM,TWENTY4            FLAGS TO LOW BYTE
         STC   RPARM,WFLGS              AND SAVE THEM
         LH    RTEMPA,LCBLNENT          USE DIAL ENTRY
         CLI   LCBFLAG1,LCBPLCB         PLCB?                 @YM07926
         BE    USETTCIN                 YES                   @YM07926
         TM    LCBSTAT2,LCBDIAL         DIAL LINE
         BO    AYE00200                 BRANCH YES
USETTCIN EQU   *                                              @YM07926
         LH    RTEMPA,LCBTTCIN          CONNECTED INDEX
         SPACE 2
AYE00200 EQU   *
         TM    WFLGS,TSOMSG             IS THIS FROM IEDAYO
         BZ    AYE00300                 BRANCH NO
*  ENTRY FROM IEDAYO. THAT MODULE HAS SAVED TRM INDEX IN AYOSRCE
* DEFINED AS SCBSRCE+16. WE WILL GET INDEX FROM THERE IN CASE
* DELAYED INTERRUPT CAUSES LCBTTBIN TO BE MODIFIED BEFORE USE.
         XC    FROMR,FROMR              CLEAR OUT FROM COUNT
         L     RTEMPA,LCBSCBA-ONE       ADDRESS OF SCB
         USING IEDQSCB,RTEMPA
         LH    RTEMPA,AYOSRCE           TERM INDEX SAVED
         DROP  RTEMPA                   NO LONGER REQUIRED
         SPACE 2
AYE00300 EQU   *
         N     RTEMPA,AVTCLRHI          CLEAR NEG PROPAGATION
         L     RTCBUF,AVTRNMPT          ADDR OF TERM NAME TABLE ROUTINE
         USING IEDQTNTD,RTCBUF
         BAL   RTSBUF,TNTDCODE          BRANCH TO GET TERM ENTRY ADDR
         DROP  RTCBUF
         USING IEDQTRM,RTEMP
         TM    TRMSTATE,TRMPREF         IS THERE A PREFIX      @Y17XAYP
         BZ    NOPREFIX                 NO, BRANCH             @Y17XAYP
         OI    FLAG2,X3705              SET NCP TRM BIT        @YM06134
NOPREFIX EQU   *                                               @Y17XAYP
         L     RQCB,TRMDESTQ-ONE        POINT TO QCB
         USING IEDQQCB,RQCB             QCB ADDRESSABILITY     @Y17XAYP
         LA    RTSBUF,TRMPRFSZ          GET TTE PREFIX SIZE    @Y17XAYO
         DROP  RTEMP                                           @Y17XAYO
         SR    RTEMP,RTSBUF             BACK UP TO PREFIX      @Y17XAYO
         USING IEDNTRM,RTEMP                                   @Y17XAYO
         IEDDCT REG=RTSBUF,FLD=AVTPARM3,LEN=4                  @Y17XAYP
         DROP  RTEMP                    RELEASE TRM REG        @Y17XAYP
         L     RTSB,FIFTYTWO(,RAVT)     RESTORE POSSIBLE TSB FROM
*                                       SAVE AREA
         MVI   AVTPARM,ZERO             CLEAR FOR LOCAL 2260 FLAGS
         TM    AVTPARM3+ONE,DCT3270     IS THIS A 3270 TRM     @G36XRYP
         BO    AYE00600                 YES, BRANCH PAST 2260 CHK
         TM    AVTPARM3+TWO,DCTLOCAL    IS IT 2260 LOCAL LINE  @G36XRYP
         BZ    AYE00500                 BRANCH NO
         OI    AVTPARM,TWO              INDICATE LOCAL 2260
         SPACE 2
AYE00500 EQU   *
         TM    AVTPARM3+THREE,DCTNOIDL  IS IT REMOTE?          @G36XRYP
         BZ    AYE00600                 NO BRANCH
         OI    AVTPARM,ONE              INDICATE REMOTE 2260
         SPACE 2
AYE00600 EQU   *
         CLI   AVTPARM,ZERO             ANY 2260
         BNE   AYE00700                 BRANCH YES
         TM    AVTPARM3+TWO,DCTTWX      OR TTY                 @G36XRYP
         BO    AYE00700                 BRANCH YES
         TM    LCBTSOB,LCB2741N         IS IT A 2741
         BO    AYE00700                 BRANCH YES
         TM    AVTPARM3+ONE,DCT3270     IS IT A 3270           @G36XRYP
         BO    AYE00700                 BRANCH YES
         TM    FLAG2,X3705              IS THERE A PREFIX      @OY18014
         BNO   AYE00601                 NO, BRANCH             @OY18014
         TM    LCBSTAT5,LCBLUNIT        IS  IT A LU           @YM08024
         BO    AYE00700                 BRANCH YES            @YM08024
AYE00601 EQU   *                                               @OY18014
*  NONE OF THE ABOVE. SET 1050 DEVICE
         OI    FLAG2,X1050              DEVICE IS A 1050
         SPACE 2
AYE00700 EQU   *
*
         TM    AVTPARM3+ONE,DCT3270   THIS A 3270 TERMINAL     @G36XRYP
         BZ    SETCARCT           NO DO NOT CHECK SECOND ENTRY @SA74053
         TM    QCBRETCT,QCBDEND   TEST SECOND ENTRY FLAG       @SA74053
         BZ    SETCARCT           NOT SECOND ENTRY             @SA74053
         MVC   CARCT(ONE),QCBCARCT SET SECOND ENTRY OFFSET     @SA74053
         B     SETSCND            BRANCH AROUND CARCT SETUP    @SA74053
SETCARCT EQU   *                                               @SA74053
         MVC   CARCT+ONE(ONE),QCBCARCT SET UP CARCT            @SA74053
SETSCND  EQU   *                                               @SA74053
         TM    AVTBIT3,AVTTSAB         TSO ABEND
         BO    AYE00800                 YES, BRANCH
         TM    WFLGS,TSOMSG             MSGGEN ENTRY?
         BO    AYE01000                 BRANCH NO
         TM    QCBFLAG,QCBTSSES         TS SESSION STARTED
         BO    AYE00900                 YES
         SPACE 2
AYE00800 EQU   *
         LA    RLINK,FORTY              LOCAL DISPLAY DEFAULT
         TM    AVTPARM3+TWO,DCTLOCAL    IS IT A LOCAL DISPLAY? @G36XRYP
         BO    AYE01100                 YES, USE 40 LINE SIZE
         LA    RLINK,EIGHTY             REMOTE DISPLAY DEFAULT
         TM    AVTPARM3+THREE,DCTNOIDL  IS IT 2260 REMOTE      @G36XRYP
         BO    AYE01100                 YES. USE 80 LINE SIZE
         LA    RLINK,FORTY              SET DEFAULT TO 40
         TM    AVTPARM3+TWO,DCTBISYN+DCTSTCTL 3270 REMOTE?     @G36XRYP
         BO    AYE01100                 YES,USE 40 FOR LINE SIZE
         LA    RLINK,ONETWNTY           DEFAULT FOR 1050/2741
         B     AYE01100                 BRANCH
         SPACE 2
AYE00900 EQU   *
         L     RRTN,CVTPTR              ADDRESS OF CVT
         USING CVT,RRTN
         LH    RLINK,QCBTJID            ASID THIS USER         @G36XRYP
         BCTR  RLINK,ZERO               FIRST WILL BE OFFSET 0
*  GET CORRECT TSB FOR THIS USER
         L     RTSB,CVTASVT             GET ASVT ADDRESS       @G36XRYP
         SLL   RLINK,2                  MULTIPLY ASID-1 BY 4   @G36XRYP
         AR    RTSB,RLINK               COMPUTE ASCB INDEX     @G36XRYP
         L     RTSB,ASVTENTY-ASVT(RTSB) GET ASCB ADDRESS       @ZM46782
         L     RTSB,ASCBTSB-ASCB(RTSB)  GET TSB ADDRESS        @G36XRYP
         DROP  RRTN
*  IF ENTRY NOT FROM MSGGEN (IEDAYM), TSB ALREADY SET UP IN REG 8
*
         SPACE 2
AYE01000 EQU   *
         SR    RLINK,RLINK              CLEAR FOR LINE SIZE
         USING TSB,RTSB
         IC    RLINK,TSBLNSZ            GET LINE SIZE FOR THIS TERM
         LTR   RLINK,RLINK              WAS LINE SIZE SPECIFIED
         BZ    AYE00800                 NO. DEFAULT
         SPACE 2
AYE01100 EQU   *
         STH   RLINK,LINESV             AND AS BACKUP FOR NEXT LINE
         TM    AVTPARM3+ONE,DCT3270     IS THIS A 3270 TRM     @G36XRYP
         BZ    AYE01500                 NO, BRANCH
         TM    WFLGS,TSOMSG             IS THIS TSO ENTRY
         BO    AYE01200                 YES, GET LINE NUMBER
         TM    QCBFLAG,QCBTSSES         TS SESSION STARTED
         BZ    AYE01300                 NO, BRANCH TO DEFAULT
         SPACE 2
AYE01200 EQU   *
         IC    RLINK,TSBLNNO            GET LINE NUMBER FOR THIS TERM
         LTR   RLINK,RLINK              WAS LINE NUMBER SPECIFIED
         BNZ   AYE01400                 YES, STORE LINE NUMBER
         SPACE 2
AYE01300 EQU   *
         LA    RLINK,TWELVE             USE DEFAULT LINE NUMBER
         SPACE 2
AYE01400 EQU   *
         STC   RLINK,LINESV             STORE LINE NUMBER
         SPACE 2
AYE01500 EQU   *
         DROP  RTSB                     USE OF TSB DONE
         LA    RTSBUF,ZERO(,RPARM2)     TS BUFFER OR MSGGEN ADDRESS
         LR    RTIOC,RTSBUF
         L     RTCBUF,TCBUFR            GET TCAM BUFFER ADDRESS
         ST    RTSBUF,TSBUFR            SAVE ADDR OF FIRST
         ST    RTSBUF,TSCURR
         L    RRTN,AVTTSOPT             POINT TO WORK AREA
         USING IEDQTSI,RRTN             ESTABLISH ADDRESSABILITY
         TM    AVTPARM3+ONE,DCT3270     IS THIS A 3270 TRM     @G36XRYP
         BZ    AYE01600                 NO, BRANCH
         L     RRTN,TSI3270             ADDRESS OF 3270 EDIT ROUTINE
         BR    RRTN                     CALL IEDAYB
         SPACE 2
AYE01600 EQU   *
         TM    FLAG2,X3705              TERMINAL ON A 3705
         BZ    AYE01700                 NO, MUST BE ON A 270X
         L     RRTN,TSI3705             ADDRESS OF 3705 EDIT MOD
         BR    RRTN                     GO TO IEDAYV
         SPACE 2
AYE01700 EQU   *
         L     RRTN,TSI270X             ADDRESS OF 270X EDIT MODULE
         BR    RRTN                     GO TO IEDAYW
         SPACE 2
AYEPATCH DC    20F'0'                   PATCH AREA
         EJECT
GMAIN    DSECT
TSBUFR   DS    F                        TSO BUF OR MSGGEN ADDRESS
TCBUFR   DS    F                        TCAM/SCB ADDRESS
MOVED    DS    H                        DATA MOVED
LINE     DS    H                        LINE SIZE REMAINING
BLNTH    DS    H                        TSO BUFFER DATA SIZE
MSGLEN   DS    H                        MESSAGE SIZE
WBUFSZ   DS    H                        TCAM BUFFER SIZE
*                                                                     *
CARCT    DS    H                        QCBCARCT
*                             BIT DEFINITIONS                         *
SBASCND  EQU   X'F0'                    SECOND ENTRY OFFSET
CUR1SCND EQU   X'F1'                    SECOND ENTRY OFFSET
CUR2SCND EQU   X'F2'                    SECOND ENTRY OFFSET
STFDSCND EQU   X'F3'                    SECOND ENTRY OFFSET
RDATSCND EQU   X'F4'                    SECOND ENTRY OFFSET
INCSCND  EQU   X'F5'                    SECOND ENTRY OFFSET
ETXSCND  EQU   X'F6'                    SECOND ENTRY OFFSET
EOTSCND  EQU   X'F7'                    SECOND ENTRY OFFSET
*                                                                     *
LINESV   DS    H                        FROM TSB OR DEFAULT
*                                                                     *
LINENUM  EQU   LINESV                   DISPLAY NUMBER OF LINES
LINESIZE EQU   LINESV+1                 DISPLAY LINE SIZE
*                                                                     *
SWT1     DS    X                        SWITCH 1
*                             BIT DEFINITIONS                         *
CUR2FLAG EQU   X'80'                    SECOND CURSOR FLAG
CUR1FLAG EQU   X'40'                    FIRST CURSOR FLAG
RDATTNLF EQU   X'20'                    READ ATTRIBUTE BYTE FLAG
SFATTSW  EQU   X'10'                    START FIELD FLAG
BYPFLAG  EQU   X'08'                    BYPASS FLAG
CRNL     EQU   X'04'                    CARRIAGE RETURN/NEWLINE FLAG
SBAENDSW EQU   X'02'                    SET BUFFER ADDRESS FLAG
INCSW    EQU   X'01'                    INSERT CURSOR FLAG
*                                                                     *
SWT2     DS    X                        SWITCH 2
*                             BIT DEFINITIONS                         *
RETCOD4  EQU   4                        CALCSIZE RETURN CODE 4
RETCOD8  EQU   8                        CALCSIZE RETURN CODE OF 8
RETCOD12 EQU   12                       CALCSIZE RETURN CODE OF 12
RETCOD16 EQU   16                       CALCSIZE RETURN CODE OF 16
RETCOD40 EQU   40                       CALCSIZE RETURN CODE NO ROOM
*                                                                     *
WFLGS    DS    H                        TSO FLAGS
*                             BIT DEFINITIONS                         *
TSOMSG   EQU   X'80'                    TSO ENTRY
CONTROLM EQU   X'40'                    CONTROL MODE
TSOASIS  EQU   X'20'                    TSO TPUT ASIS
FRSTMGEN EQU   X'10'                    FIRST TIME IN EDIT ON MSGGEN
NOLEFT   EQU   X'06'                    NO LEFT JUSTIFICATION REQUEST
LEFT     EQU   X'05'                    LEFT JUSTIFICATION REQUEST
*                                                                     *
SAVX1    DS    H                        SCREEN CURSOR ADDRESS
*                                                                     *
FLAG1    DS    X                        FLAG BYTE 1
*                             BIT DEFINITIONS                         *
TRAILER  EQU   X'80'                    TRAILER BUFFER FLAG
FLASHBCK EQU   X'40'                    FLASHBACK REQUESTED FLAG
RESTSW   EQU   X'20'                    RESTORE CHAR FLAG      @SA73817
RECALC   EQU   X'10'                    USED LOCALLY IN AYB.   @SA75159
*                                       BYPASS CODE AND RESET  @SA75159
EOTSW    EQU   X'02'                    EOT FLAG
ETXSW    EQU   X'01'                    ETX FLAG
*                                                                     *
FLAG2    DS    X                        FLAG BYTE 2
*                             BIT DEFINITIONS                         *
X3705    EQU   X'80'                    TERMINAL ON A 3705       Y06327
X1050    EQU   X'40'                    DEVICE IS A 1050         Y06237
NLCRFLAG EQU   X'10'                    NL/CR COUNT FLAG
NWLNCNT  EQU   X'08'                    LINE COUNT FLAG
CARRDATT EQU   X'04'                    READ ATTRIBUTE LINE POS FLAG
HEADERFG EQU   X'02'                    FIRST BASIC UNIT FLAG
NOMOVECT EQU   X'01'                    NO MOVECOUNT FLAG
*                                                                     *
SAVX2    DS    H                        FIRST/LAST CHARACTER
TOR      DS    F                        DATA COUNT
EMGEN    EQU   *
FROMR    DS    F                        DATA COUNT MOVED
TSCURR   DS    F                        CURRENT TSO BUFFER
MSGNLNTH DS    H                        LENGTH OF MSGGEN DATA
DEND     DS    D                        DOUBLE SAVE
SIZE     EQU   *-GMAIN
         EJECT
         IHAASCB
         EJECT
         IHAASVT                                               @ZM46782
         EJECT
CVT      DSECT
         CVT
         SPACE
         TDCTD
         SPACE
         TSIBD                                                 @Y17XAYO
         SPACE
         TTNTD
         SPACE
         TTRMD
         TTSID
         TPRFD
         TLCBD
         TQCBD
         TSCBD
         TTSWD
AYOSRCE  EQU   SCBSRCE+16               CONFORM WITH IEDAYO      A56230
         TAVTD
         DCBD  DSORG=TX
         IKJTSB
         TPRIOR                                                  A53617
         END
