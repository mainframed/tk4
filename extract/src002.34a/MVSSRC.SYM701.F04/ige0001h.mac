         TITLE '3851 ERP LOAD 1 (IGE0001H) VS2'
         MACRO
         DATEM
         LCLC  &FILL
&FILL    SETC  'CL8'.''''.'&SYSDATE'.''''
         DC    &FILL
         MEND
IGE0001H START X'00'
*
***************************************************************@Y30LB10
*                                                              @Y30LB10
*01 MODULE-NAME = IGE0001H                                     @Y30LB10
*                                                              @Y30LB10
*01 DESCRIPTIVE NAME = 3851 ERROR RECOVERY PROCEDURES          @Y30LB10
*                                                              @Y30LB10
*01 COPYRIGHT = NONE                                           @Y30LB10
*                                                              @Y30LB10
*01 STATUS = VS2 REL 3.0 VERSION 1.                            @Y30LB10
*                                                              @Y30LB10
*01 FUNCTION =                                                 @Y30LB10
*   THE ERP WILL TEST STATUS AND SENSE BITS BY PRIORITY AND    @Y30LB10
*   DO THE ACTION DESCRIBED BY THE HARDWARE ERROR RECOVERY     @Y30LB10
*   PROCEDURES.                                                @Y30LB10
*                                                              @Y30LB10
*01 NOTES                                                      @Y30LB10
*                                                              @Y30LB10
*02    DEPENDENCIES =                                          @Y30LB10
*      THE ABNORMAL END APPENDAGE WILL CALL THE ERP FOR COND   @Y30LB10
*      CODE 3.                                                 @Y30LB10
*                                                              @Y30LB10
*02    RESTRICTIONS = NONE                                     @Y30LB10
*                                                              @Y30LB10
*02    REGISTER CONVENTIONS =                                  @Y30LB10
*      REG0      0    WORK REG                                 @Y30LB10
*      IOSBPTR   1    IOSB POINTER                             @Y30LB10
*      REG2      2    WORK REG                                 @Y30LB10
*      REG3      3    WORK REG                                 @Y30LB10
*      CSDPTR    3    CSD POINTER                              @Y30LB10
*      CVTXTN    4    CVT EXTENTION POINTER                    @Y30LB10
*      CVTREG    5    CVT POINTER                              @Y30LB10
*      EWAPTR    6    EWA POINTER                              @Y30LB10
*      PRIUCB    7    ADDRESS OF PRIMARY UCB                   @Y30LB10
*      REG7      7    WORK REG FOR IOSGEN MACRO                @Y30LB10
*      ALTUCB    8    ADDRESS OF ALTERNATE UCB                 @Y30LB10
*      CCWPTR    9    ADDRESS OF CCW IN ERROR                  @Y30LB10
*      CTLPTR    10   SSC CONTROL BLOCK POINTER                @Y30LB10
*      BASEREG   11   BASE REG                                 @Y30LB10
*      REGC      12   WORK REG                                 @Y30LB10
*      REGD      13   WORK REG                                 @Y30LB10
*      BALRREG   14   BRANCH REG                               @Y30LB10
*      REGF      15   WORK REG                                 @Y30LB10
*                                                              @Y30LB10
*02    PATCH LABEL = ERPMAINT                                  @Y30LB10
*                                                              @Y30LB10
*01 MODULE TYPE = LOAD MODULE                                  @Y30LB10
*                                                              @Y30LB10
*02    PROCESSER = ASSEMBLER XF                                @Y30LB10
*                                                              @Y30LB10
*02    MODULE SIZE = 4096 BYTES                                @Y30LB10
*                                                              @Y30LB10
*02    ATTRIBUTES = RE-ENTRANT, PRIVILEGED                     @Y30LB10
*                                                              @Y30LB10
*01 ENTRY POINT = IGE0001H                                     @Y30LB10
*                                                              @Y30LB10
*02    PURPOSE =                                               @Y30LB10
*      1. INITIAL CALL OF THE ERP FOR A ERROR CONDITION.       @Y30LB10
*      2. RETURN FROM A ERP RETRY SIO.                         @Y30LB10
*                                                              @Y30LB10
*02    LINKAGE = IOS WILL CALL THE ERP WITH REG 1 = POINTER    @Y30LB10
*      TO IOSB AND REG 15 = BASE ADDRESS OF THE MODULE.        @Y30LB10
*                                                              @Y30LB10
*01 INPUT = CONTROL BLOCKS 'IOSB', 'EWA' AND THE 'UCB' WILL    @Y30LB10
*   BE INITIALIZED BY IOS PRIOR TO ENTRY.                      @Y30LB10
*                                                              @Y30LB10
*01 OUTPUT = NONE                                              @Y30LB10
*                                                              @Y30LB10
*01 EXIT NORMAL = IOSB FLAGS 'ERR' AND 'EX' WILL BE SET TO;    @Y30LB10
*   1. 'ERR' = 1, 'EX' = 1 TO DO A ERP RETRY SIO.              @Y30LB10
*   2. 'ERR' = 0, 'EX' = 1 TO EXIT PERMANENT ERROR.            @Y30LB10
*   3. 'ERR' = 0, 'EX' = 0 TO EXIT RECOVERED ERROR.            @Y30LB10
*   EXIT TO;                                                   @Y30LB10
*   XCTL TO WTO (IGE0025C) TO PRINT MESSAGES.                  @Y30LB10
*   XCTL TO OBR (IGE0025F) TO RECORD ON SYS1.LOGREC.           @Y30LB10
*   SVC-15, SVC-3 RETURN TO IOS.                               @Y30LB10
*                                                              @Y30LB10
*01 EXIT ERROR = NONE                                          @Y30LB10
*                                                              @Y30LB10
*01 EXTERNAL REFERENCES =                                      @Y30LB10
*                                                              @Y30LB10
*02    ROUTINES = SVC 35 (WRITE TO OPERATOR SVC), IGE0025C     @Y30LB10
*      (SYSTEM WTO ROUTINE) AND IGE0025F (OBR ROUTINE).        @Y30LB10
*                                                              @Y30LB10
*02    DATA AREAS = NONE                                       @Y30LB10
*                                                              @Y30LB10
*02    CONTROL BLOCKS = CVT, UCB, IOSB, EWA AND IEZSSC.        @Y30LB10
*                                                              @Y30LB10
*01 TABLES = NONE                                              @Y30LB10
*                                                              @Y30LB10
*01 MACROS = WTO AND TIME.                                     @Y30LB10
*                                                              @Y30LB10
*01 CHANGE ACTIVITY = Y30LB10                                  @Y30LB10
*   CHANGE FOR DCR 184                                         @YD00184
*   CHANGE FOR WTO                                             @ZA00663
*   CHANGE FOR ICBR BIT TEST BEFORE HOST READY                 @ZM31093
*   CHANGE FOR ICBI BIT TEST BEFORE HOST INIT.                 @ZM31094
*   CHANGE TO LOG ALL COMMAND REJECT'S MESSAGE CODE 6          @ZM31098
*   CHANGE TO SUPPORT FCR 378                                  @ZM31108
*   CHANGE FOR CHAN. RESTART                                   @ZUP0988
*   CHANGE FOR ERROR PRIORITY TESTING                          @OZ00682
*   CHANGE FOR AS12579  CORRECT ALT PATH ADDRESS               @OZ05073
*   CHANGE TO SUPPORT INT REQ OR ENV DATA DURRING SWAP         @OZ00688
*   CHANGE FOR FCR450. HANDLE DIAG CCWS AS NORMAL CCWS.        @ZA05140
*   CHANGE TO CORRECT CCW PTR IF ERROR AFTER SUSPEND           @ZA05141
*   CHANGE TO ADD NOOP CCW + PREVENT SWAP CCWS BEING DESTROYED @ZA05142
*   ADD FICHE FLAGS TO FORMAT SENSE HOOK CODE                  @Y40LB02
*   CHANGE FORMAT 0, MSG CODE 06, TERM 3F DESTINATION          @ZA09552
*   CHANGE FORMAT 0, MSG CODE 06, TERM 25 DESTINATION          @ZA10999
*   CHANGE TO EXIT PERM ERROR ON INCORRECT LENGTH ERROR     *  @OZ11017
*   CHANGE TO LOG ALL 06XX ERRORS AND EXIT PERM ERROR          @ZA13053
*   ON PROTECTION OR PROGRAM CHECK                             @ZA13053
*   CHANGE TO HANDLE ERRORS DURING IPL                         @ZA13054
*   PROPERLY HANDLE CHAINING OF 06-26 WHEN 26 IS               @ZA13055
*   UNIT CHECKED                                               @ZA13055
*   CHANGE FOR FCR 588 ADD FORMAT1 AS VALID SUU FORMAT         @ZA14725
*   DELETE 06CA AND 06CB REASON CODES (PTM  VS44017)           @G24LB22
*   ATTACHED PROCESSOR SUPPORT                                 @G24LB05
*   CHANGE FOR IOSINTRP MACRO                                  @ZA20273
*   CHANGE 23 06/16/77  SWAP ON 0630 ERR                       @ZA24432
*   CHANGE 24 10/20/77  ADD ASSOCAITE COMMAND ON INIT OF MP    @ZA28260
*   CHANGE 25 11/22/77  LEAVE UCB ONLINE ON A MOD 0630 ERR     @ZA27590
*   CHANGE 26 02/10/78  FIX INVALID SWAP CODE IN PTF UZ13257   @ZA30113
*   CHANGE 27 06/02/78  LCH POINTER NOT SWAPPED DURING MSC SWAP@ZA32810
**************************************************************@Y30LB10
         EJECT
*                                                              @Y30LB10
*              GENERAL REGISTER EQUATES                        @Y30LB10
*                                                              @Y30LB10
REG0     EQU   0                       WORK REG                @Y30LB10
IOSBPTR  EQU   1                       IOSB POINTER            @Y30LB10
REG2     EQU   2                       WORK REG                @Y30LB10
REG3     EQU   3                       WORK REG                @Y30LB10
SAVEPTR  EQU   4                       USER START SAVE POINTER @ZM30721
CVTREG   EQU   5                       CVT POINTER             @Y30LB10
EWAPTR   EQU   6                       EWA POINTER             @Y30LB10
PRIUCB   EQU   7                       PRIMARY UCB POINTER     @Y30LB10
REG7     EQU   7                       IOSGEN MACRO WORK REG   @Y30LB10
ALTUCB   EQU   8                       ALTERNATE UCB POINTER   @Y30LB10
CCWPTR   EQU   9                       USER CCW POINTER        @Y30LB10
CTLPTR   EQU   10                      SSC CTL BLOCK POINTER   @Y30LB10
BASEREG  EQU   11                      BASE REG                @Y30LB10
REGC     EQU   12                      WORK REG                @Y30LB10
REGD     EQU   13                      WORK REG                @Y30LB10
BALRREG  EQU   14                      BR REG                  @Y30LB10
REGF     EQU   15                      WORK REG                @Y30LB10
         EJECT
***************************************************************@Y30LB10
*  EWA3SAV - SET ON WHEN THE ERP MUST DO I/O TO A CCW BUILT    @Y30LB10
*  IN THE ERP FOR RECOVERY. THE USER FAILING CCW ADDRESS IS    @Y30LB10
*  SAVED PRIOR TO THE I/O AND RESTORED ON THE NEXT ERP         @Y30LB10
*  ENTRY.                                                      @Y30LB10
*                                                              @Y30LB10
*  EWA3SWAP - SET ON WHEN UCB'S HAVE BEEN SWAPPED TO NOTE THE  @Y30LB10
*  ERP IS IN THE SWAP FUNCTION.                                @Y30LB10
*                                                              @Y30LB10
*  EWA3CCH - USED AS THE CCH ERROR COUNTER.                    @Y30LB10
*                                                              @Y30LB10
*                                                              @Y30LB10
*  EWA3MSC - SET ON WHEN A SWAP IS CAUSED BY THE MSC           @Y30LB10
*                                                              @Y30LB10
***************************************************************@Y30LB10
* NOTINIT - SET ON WHEN THE ERP DETECTS THE MSC IS NOT         @Y30LB10
*  INITIALIZED AND WILL ISSUE THE SWITCH CCW,S WITHOUT         @Y30LB10
*  SWITCHING THE UCB,S.                                        @Y30LB10
***************************************************************@Y30LB10
*                                                              @Y30LB10
*        IOSB FLAG C BIT MEANINGS                              @Y30LB10
*        EWA   EWAFLAG3 3851 FLAG BIT MEANINGS                 @Y30LB10
EWA3SAVE EQU   X'80'                   ERP USER CCW SAVE FLAG  @Y30LB10
EWA3SWAP EQU   X'40'                   ERP SWAP IN PROCESS     @Y30LB10
NOTINIT  EQU   X'20'                   MSC NOT INITIALIZED FLG @ZM30721
EWA3CCH  EQU   X'10'                   ERP CCH ERROR CNTR      @Y30LB10
SPARE    EQU   X'08'                   UNUSED BIT POSITION     @ZA05141
EWA3MSC  EQU   X'02'                   MSC SWITCH              @YD00155
         SPACE 1
*         ERROR COUNTER THREE EQUATES                          @YD00155
SECENTRY EQU   X'80'                   SECOND ENTRY TO ERP     @YD00155
SECERR   EQU   X'40'                   SECOND CC=3 ERROR       @YD00155
TRDERR   EQU   X'20'                   THIRD CC=3 ERROR        @YD00155
         SPACE 1
*        ALTERNATE UCB OFFSETS                                 @Y30LB10
ALTSTAT  EQU   3                       UCBSTAT EQUATE          @Y30LB10
ALTFLA   EQU   6                       UCBFLA EQUATE           @Y30LB10
ALTCHAN  EQU   4                       UCBCHAN EQUATE          @Y30LB10
ALTLCI   EQU   10                      UCBLCI  EQUATE          @ZA32810
ALTNAME  EQU   13                      UCBNAME EQUATE          @Y30LB10
         SPACE   3
*        GENERAL PROGRAM EQUATES                               @Y30LB10
A3OP     EQU   X'A3'                   SUSPEND OP CODE         @Y30LB10
NOOP     EQU   X'03'                   NOP CCW OP CODE         @ZM31093
C0       EQU   0                       COUNT OF 0              @Y30LB10
C1       EQU   1                       COUNT OF 1              @Y30LB10
C2       EQU   2                       COUNT OF 2              @Y30LB10
C3       EQU   3                       COUNT OF 3              @Y30LB10
C4       EQU   4                       COUNT OF 4              @Y30LB10
C5       EQU   5                       COUNT OF 5              @ZA28260
C7       EQU   7                       COUNT OF 7              @Y30LB10
C8       EQU   8                       COUNT OF 8              @Y30LB10
C10      EQU   10                      COUNT OF 10             @YD00156
C12      EQU   12                      COUNT OF 12             @ZA28260
C24      EQU   24                      COUNT OF 24             @Y30LB10
C32      EQU   32                      COUNT OF 32             @Y30LB10
C37      EQU   37                      COUNT OF 37             @Y30LB10
C48      EQU   48                      COUNT OF 48             @Y30LB10
C57      EQU   57                      COUNT OF 57             @YD00156
C62      EQU   62                      COUNT OF 62             @YD00156
C63      EQU   63                      COUNT OF 63             @YD00156
CCC      EQU   X'04'                   CHANNEL CONTROL CHK     @Y30LB10
CDC      EQU   X'08'                   CHANNEL DATA CHK        @Y30LB10
CEE      EQU   X'EE'                   INVALID OP CODE FOR ERP @YD00155
CHN      EQU   X'01'                   CHAINING CHECK          @Y30LB10
CMDCHN   EQU   X'40'                   COMMAND CHAIN FLAG      @Y30LB10
COD41    EQU   X'41'                   COMPLETION CODE 41      @Y30LB10
CUBZY    EQU   X'50'                   STS MOD AND BUSY BITS   @Y30LB10
CEDEUC   EQU   X'0E'                   CH END,DEV END,UNIT CK  @OZ00688
EOT      EQU   X'FF'                   END OF TABLE INDICATOR  @Y30LB10
EXEC     EQU   X'87'                   EXECUTE COMND OP CODE   @Y30LB10
FIFTEEN  EQU   15                      SVC 15 CODE             @Y30LB10
FIVE     EQU   X'05'                   COUNT OF 5              @Y30LB10
ICC      EQU   X'02'                   INTERFACE CONTROL CHK   @Y30LB10
ILI      EQU   X'40'                   INCORRECT LENGTH        @Y30LB10
INTCOD44 EQU   X'44'                   INTERCEPT CODE 44       @Y30LB10
INTCOD7E EQU   X'7E'                   INTERCEPT CODE 7E       @Y30LB10
LOC10    EQU   16                      ADDR OF CVT             @Y30LB10
NOTON    EQU   8                       BR ZERO CODE            @Y30LB10
NONSNS   EQU   X'0A'                   SNS EXPECTED TO BE 0    @OZ00682
OBRRTN   EQU   256                     OBR RTN CALL CODE       @Y30LB10
OF       EQU   X'0F'                   MASK OF  ZERO-FOX       @Y30LB10
OPERSWAP EQU   X'FF'                   OP CODE OF X'FF'        @Y30LB10
PGM      EQU   X'20'                   PROGRAM CHK             @Y30LB10
PROT     EQU   X'10'                   PROTECTION CHK          @Y30LB10
SNSOP    EQU   X'04'                   OP CODE FOR SENSE       @Y30LB10
SUSPEND  EQU   X'06'                   DEV END + UNIT CHK      @Y30LB10
READBL   EQU   X'06'                   READ COMMAND            @ZA13055
THREE    EQU   3                       SVC 3 CODE              @Y30LB10
TIC      EQU   X'08'                   OP CODE OF TIC COMND    @Y30LB10
MSCOPVAR EQU   X'80'                   UCB OPER VARY FLAG      @YD00155
UNCK     EQU   X'02'                   UNIT CHK                @Y30LB10
WTORTN   EQU   253                     WTO RTN CALL CODE       @Y30LB10
X40      EQU   X'40'                   HEX BYTE OF 40          @Y30LB10
ZERO     EQU   X'00'                   BYTE OF ZERO            @Y30LB10
FSLD3RTN EQU   3018                    LD1 FS RTN CALL CODE    @Y40LB02
         EJECT
         USING IOSB,IOSBPTR            USING REG 1             @Y30LB10
         USING EWA,EWAPTR              USING REG 6             @Y30LB10
         USING UCB,PRIUCB              USING REG 7             @Y30LB10
         USING CVTMAP,CVTREG           USING REG 5             @Y30LB10
         USING CVTXTNT2,CTLPTR         USING REG 10            @ZM30721
         USING *,BASEREG               USING REG 11            @Y30LB10
*******************                                            @Y30LB10
*  SET UP REGS FOR BASE AND CONTROL BLOCK POINTERS.            @Y30LB10
*  GET CCW IN ERROR ADDRESS.                                   @Y30LB10
*******************                                            @Y30LB10
ERR000   EQU   *                                               @Y30LB10
         LR    BASEREG,REGF            GET BASE REG            @Y30LB10
         B     ERR001                                          @ZM31108
         DC    C'IGE0001H'                                     @ZM31108
         DATEM
ERR001   EQU   *                                               @Y30LB10
         L     PRIUCB,IOSUCB           GET UCB ADDR            @Y30LB10
         SH    PRIUCB,H512             POINT TO PREFIX         @Y30LB10
         L     EWAPTR,IOSERP           GET EWA ADDR            @Y30LB10
         L     CCWPTR,IOSCC            GET ERROR CCW ADDR      @Y30LB10
         LA    CCWPTR,C0(CCWPTR)       ZERO HI ORDER BYTE      @Y30LB10
         TM    IOSCC,IOSCC3            IS IT A COND CODE 3 ?   @ZM31108
         BO    ERR002                  YES CC-3 CCW OK AS IS   @ZM31108
         SH    CCWPTR,H8               POINT TO ERROR CCW      @Y30LB10
ERR002   EQU   *                                               @ZM31108
         L     CVTREG,LOC10            GET ADR OF CVT          @Y30LB10
         L     CTLPTR,CVTEXT2          GET ADDR OF CVT EXT     @Y30LB10
         L     CTLPTR,CVTICB           GET ADDR OF SSC CTL BLK @Y30LB10
         DROP  CTLPTR                  DROP REG 10             @ZM30721
         USING ICB2SSCB,CTLPTR         USING REG 10            @ZM30721
         L     ALTUCB,ICBAUCB          GET ADDR OF ALT UCB     @Y30LB10
         EJECT
*******************                                            @Y30LB10
*  INSURE THAT MSG AND LOG ARE ZEROED                          @Y30LB10
*  SET SECOND ERP ENTRY SWITCH ON                              @Y30LB10
*******************                                            @Y30LB10
ERR010   EQU   *                                               @Y30LB10
         NI    IOSFLB,X'FF'-IOSMSG-IOSLOG  ZERO LOG AND MSG    @Y30LB10
         OI    EWACNTR3,SECENTRY  SET 2ND ENTRY SWITCH         @ZM31108
         SPACE 1
*******************                                            @Y30LB10
*  IS THIS THE FIRST ENTRY TO THE ERP                          @Y30LB10
*  IF YES CLEAR ALL ERP FLAGS EXCEPT LOOP. RESET THE ERROR     @Y30LB10
*  COUNTERS AND SET THE ERP IN CONTROL.                        @Y30LB10
*******************                                            @Y30LB10
         TM    IOSFLA,IOSERR           1ST ENTRY TO ERP        @Y30LB10
         BO    ERR020                  BR NO                   @Y30LB10
*********                                                      @Y30LB10
         MVI   EWAFLG3,ZERO           CLR ALL                  @Y30LB10
         XC    EWACNTR2(C2),EWACNTR2   ZERO ERP COUNTERS       @ZM02150
ERR020   EQU   *                                               @Y30LB10
         OI    IOSFLA,IOSERR+IOSEX     SET ERP IN CONTROL      @Y30LB10
         SPACE 1
*******************                                            @Y30LB10
*  IS UNIT CHECK ON. IF YES, WE HAVE SENSE AND A ERR CONDITION.@Y30LB10
*******************                                            @Y30LB10
         TM    IOSTSA,UNCK             IS UNIT CHK ON          @Y30LB10
         BO    ERR040                  BR YES                  @Y30LB10
         SPACE 1
*******************                                            @Y30LB10
*  IF UNIT CHK IS NOT ON, DO WE HAVE A CONTROL UNIT BUSY COND. @Y30LB10
*  IF YES GO TO REISSUE THE OPERATION.                         @Y30LB10
*******************                                            @Y30LB10
         TM    IOSTSA,CUBZY            IS IT CU BUSY           @Y30LB10
         BO    ERR2000                 BR YES TO EXIT          @Y30LB10
         SPACE 1
*******************                                            @Y30LB10
*  WE DONT HAVE UNIT CHECK OR A CU BUSY SO ZERO THE SENSE AREA.@Y30LB10
*******************                                            @Y30LB10
ERR030   EQU   *                                               @Y30LB10
         XC    EWASNS(C32),EWASNS      ZERO SENSE AREA         @Y30LB10
         SPACE 1
*******************                                            @Y30LB10
*  DID WE SAVE THE USER CCW ADDRESS. IF YES, RESTORE THE IOSB  @Y30LB10
*  WITH THAT ADDRESS.                                          @Y30LB10
*******************                                            @Y30LB10
ERR040   EQU   *                                               @Y30LB10
         TM    EWAFLG3,EWA3SAVE        DID WE SAVE USERS CCW   @Y30LB10
         BZ    ERR050                  BR NO                   @Y30LB10
         L     REG2,UCBDEV+C4          GET SAVED CCW ADDR      @Y30LB10
         ST    REG2,IOSVST             STORE VIRT ADDR         @Y30LB10
         LRA   REG2,C0(REG2)           CONVERT TO REAL         @Y30LB10
         ST    REG2,IOSRST             STORE REAL ADDR         @Y30LB10
         NI    EWAFLG3,X'FF'-EWA3SAVE  SET SAVE FLAG OFF       @Y30LB10
         SPACE 1
*******************                                            @Y30LB10
*  IF SWAP IS ON WE ARE RETURNING FROM ISSUEING THE INVALID OP @Y30LB10
*  CODE OF X'EE' OR THE SWITCH CCW'S, GO TO SWITCH CODE.       @Y30LB10
*  IF LOOP IS ON WE HAVE ISSUED THE SWITCH CCW,S FOR THE       @Y30LB10
*  MSC NOT BEING INITIALIZED ONCE AND WILL GO TO PERM ERR.     @Y30LB10
*******************                                            @Y30LB10
ERR050   EQU   *                                               @Y30LB10
         TM    EWAFLG3,NOTINIT+EWA3SWAP IS MSC INITALIZED      @ZA24432
         BC    5,ERR600                BR IF ONES OR MIXED     @ZA24432
         EJECT
********************                                           @Y30LB10
*  IS IT A INTERCEPT CONDITION. IF YES GO TO PERM ERR          @Y30LB10
********************                                           @Y30LB10
         CLI   IOSCOD,INTCOD7E         IS IT A INTERCEPT       @Y30LB10
         BE    ERR440                  BR YES TO PERM ERR      @Y30LB10
         SPACE 1
*******************                                            @Y30LB10
*  WAS THE CSW ADDRESS ZERO. IF YES, GO TO PERMANENT ERROR.    @Y30LB10
*******************                                            @Y30LB10
ERR060   EQU   *                                               @Y30LB10
         LTR   CCWPTR,CCWPTR           IS CCW ADDR ZERO        @Y30LB10
         BM    ERR500                  BR YES                  @Y30LB10
         SPACE 1
*******************                                            @Y30LB10
*  IF COMMAND CHAINING, DID THE CCW STRING BREAK IN A          @Y30LB10
*  DIFFERENT SPOT. IF YES, RESET THE ERROR COUNTERS AND SAVE   @Y30LB10
*  THE NEW CCW IN ERROR ADDRESS.                               @Y30LB10
*  DO NOT CHANGE ANYTHING IF IT IS A INTERFACE SUSPEND COND    @ZA05141
*  OR AN 'A3' COMMAND                                          @ZA05141
*******************                                            @Y30LB10
         C     CCWPTR,IOSVST           CHAIN BREAK IN NEW SPOT @Y30LB10
         BE    ERR070                  BR NO                   @Y30LB10
         CLI   C0(CCWPTR),A3OP         TEST FOR 'A3' CMD       @ZA05141
         BE    ERR070                  BRANCH IF A3            @ZA05141
         CLI   C0(CCWPTR),READBL       TEST FOR '06' CMD       @ZA05141
         BE    ERR070                  BRANCH IF 06            @ZA05141
ERR065   EQU   *                                               @YD00154
         NI    EWAFLG3,X'FF'-EWA3CCH  CLR CCH CNTR             @Y30LB10
         MVI   EWACNTR2,ZERO           CLR ERR CNTR            @Y30LB10
         ST    CCWPTR,IOSVST           STORE NEW ADDR IN VIRT  @Y30LB10
         LRA   REG2,C0(CCWPTR)         GET REAL ADDR           @Y30LB10
         ST    REG2,IOSRST             STORE IN REAL           @Y30LB10
         SPACE 1
*******************                                            @Y30LB10
*    IS IT COND CODE 3.                                        @YD00155
*    IF YES AND ITS THE 1ST TIME, TRY ONCE MORE FOR ALT PATH   @YD00155
*   RETRY. IF ITS THE 2ND TIME GO TO SWITCH THE MSC;S          @YD00155
*******************                                            @Y30LB10
ERR070   EQU   *                                               @ZA05141
         TM    IOSCC,IOSCC3            IS IT COND CODE 3       @Y30LB10
         BNO   ERR071                  BR NO TO CCC, ICC       @OZ00682
         TM    EWACNTR3,SECERR         IS IT 2ND ERR           @YD00155
         BO    ERR565                  BR YES TO SWITCH        @ZA00663
         OI    EWACNTR3,SECERR         NO, SET 2ND ERR         @YD00155
         B     ERR2000                 GO RETRY                @YD00155
         SPACE 1
*******************                                            @Y30LB10
*  IS IT A CCH (ICC/CCC) ERROR. IF YES, GO TO CCH CODE.        @Y30LB10
*******************                                            @Y30LB10
ERR071   TM    IOSTSB,ICC+CCC          IS IT CCH ERR           @Y30LB10
         BNZ   ERR300                  BR YES TO CCH CODE      @Y30LB10
         SPACE 1
*******************                                            @Y30LB10
*  IS IT CHANNEL DATA CHECK . IF YES, GO RETRY.                @OZ00682
*******************                                            @OZ00682
         TM    IOSTSB,CDC              CHAN DATA CK ?          @OZ00682
         BNZ   ERR400                  BR YES CDC              @OZ00682
*******************                                            @Y30LB10
*  IS IT UNUSED SENSE BITS???                                  @OZ00682
*******************                                            @OZ00682
         TM    EWOSNS0,NONSNS          ARE UNUSED SENSE ON?    @OZ00682
         BNZ   ERR500                  SET PERM ,LOG, & MSG ON @OZ00682
         SPACE 1
*******************                                            @Y30LB10
* IS IT A COMMAND REJECT. IF YES, CONTINUE.                    @Y30LB10
*******************                                            @Y30LB10
ERR072   EQU   *                                               @YD00155
         TM    EWOSNS0,EWOCREJ         IS IT COMND REJ         @Y30LB10
         BZ    ERR080                  BR NO                   @Y30LB10
         SPACE 1
* IF ITS NOT FORMAT 0, GO TO PERM                              @OZ00682
         TM    EWOSNS3,EWOFOR0         IS IT FORMAT 0          @Y30LB10
         BZ    ERR078                  BR NO                   @OZ00682
         SPACE 1
* IF ITS MSG '02' GO ISSUE THE SWITCH CCW,S WITHOUT SWITCHING  @YD00155
*  THE UCB,S.                                                  @YD00155
         CLI   EWOSNS11,EWOMSG02       IS IT MSG 02            @YD00155
         BE    ERR545                  BR YES TO SW            @YD00155
         SPACE 1
* IS IT A INTERFACE SUSPEND, MSG X'0B'                         @ZM02150
* IF ITS MSG X'0B' AND ERR IN ERP, GO TO SUSPEND NORMAL.       @ZM02150
* IF ITS MSG X'0B' AND USER ERR, GO TO SUSPEND 1ST CCW.        @ZM02150
         CLI   EWOSNS11,EWOMSG0B       IS IT MSG '0B'          @ZM02150
         BNE   ERR075                  BR NO                   @ZM02150
         TM    EWACNTR3,SECENTRY       THIS 1ST ENTRY TO ERP   @ZM02150
         BO    ERR205                  BR NO TO SUSPEND        @ZM02150
         LRA   REG3,C0(CCWPTR)         GET ADR OF ERR CCW      @ZM02150
         B     ERR210                  BR TO SUSPEND           @ZM02150
         SPACE 1
ERR075   EQU   *                                               @YD00133
*  IF ITS MSG CODE '0A' ITS A SWAP CONDITION                   @YD00155
         CLI   EWOSNS11,EWOMSG0A       IS IT MSG '0A'          @YD00155
         BE    ERR550                  BR YES TO SWAP          @YD00155
         SPACE 1
* IF MSG 1,2,3 OR 4, CALL PERM ERR, PRT MSG WITH NO LOG        @YD00133
         CLI   EWOSNS11,EWOMSG04       IS IT MSG 1,2,3,4       @YD00133
         BNH   ERR495                  BR IF YES               @YD00133
         SPACE 1
* IF ITS NOT MSG 6 CALL PERM ERR, PRT MSG WITH LOG             @YD00133
         CLI   EWOSNS11,EWOMSG06       IS IT MSG 6             @YD00133
         BNE   ERR500                  BR NO                   @YD00133
         SPACE 1
* MSG 6- IF CODE IS FOR SWAP, GO TO SWAP CODE.                 @YD00155
*        IF CODE IS NOT '80', PERM ERR, PRT MSG WITH LOG       @ZM31098
         CLI   EWOSNS12,EWOCOD80       IS IT FOR SWAP          @YD00155
         BE    ERR550                  BR YES TO SWAP          @YD00155
         CLI   EWOSNS12,EWOCOD30       IS IT FOR SWAP          @ZA24432
         BE    ERR550                  BR YES TO SWAP          @ZA24432
         CLI   EWOSNS12,EWOCOD25       IS IT TERM CODE 25      @ZA13053
         BE    ERR500                  BR TO LOG IT WITH MSG   @ZA13053
         CLI   EWOSNS12,EWOCOD2D       IS IT TERM CODE 2D      @ZA13053
         BE    ERR500                  BR TO LOG IT WITH MSG   @ZA13053
         CLI   EWOSNS12,EWOCOD3F       IS IT TERM CODE 3F      @ZA28260
         BE    ERR500                  BR TO LOG IT WITH MSG   @ZA28260
         CLI   EWOSNS12,EWOCOD57       IS IT TERM CODE 57      @ZA28260
         BE    ERR077                  BR TO TEST FOR MP       @ZA28260
         CLI   EWOSNS12,EWOCODCA       IS IT TERM CODE CA      @G24LB22
         BE    ERR510                  BR TO LOG IT            @G24LB22
         CLI   EWOSNS12,EWOCODCB       IS IT TERM CODE CB      @G24LB22
         BE    ERR510                  BR TO LOG IT            @G24LB22
         CLI   EWOSNS12,EWOCODC0       IS IT TERM CODE C0      @YD00155
         BNL   ERR500                  BR TO LOG IT WITH MSG   @ZA13053
         B     ERR510                  BR TO LOG THE REST      @ZA13053
ERR077   EQU   *                                               @ZA28260
         TM    ICBFG1,ICBR             IS READY SET            @ZA28260
         BO    ERR510                  BR YES TO LOG IT        @ZA28260
ERR078   EQU   *
         NI    IOSFLA,X'FF'-IOSERR     SET TO PERMANENT ERR    @OZ00682
         B     EXIT                    PERM ERR, NO MSG OR LOG @OZ00682
         SPACE 1
*******************                                            @Y30LB10
ERR080   EQU   *                                               @Y30LB10
*  IS IT A SENSE FAILURE. IF YES CALL THE EQ CHK A PERM ERR.   @Y30LB10
*******************                                            @Y30LB10
ERR090   EQU   *                                               @Y30LB10
         TM    EWOSNS0,EWOEQU          IS IT EQ CHK            @Y30LB10
         BZ    ERR100                  BR NO                   @Y30LB10
         CLI   C0(CCWPTR),SNSOP        IS IT A SNS OP CODE     @Y30LB10
         BE    ERR500                  BR YES TO PERM ERR      @Y30LB10
         SPACE 1
ERR100   EQU   *                                               @ZM31108
*******************                                            @Y30LB10
*  IS IT HOST RETRY. IF YES RETRY THE OPERATION.               @Y30LB10
*******************                                            @Y30LB10
         TM    EWOSNS0,EWOHRET         IS IT HOST RETRY        @Y30LB10
         BO    ERR530                  BR YES TO RETRY         @ZM31108
         SPACE 1
*******************                                            @Y30LB10
*  IS IT INTERVENTION REQUIRED OR ENVIROMENTAL DATA. IF YES,   @Y30LB10
*  GO SEE IF IT IS A SUSPEND CONDITION.                        @Y30LB10
*******************                                            @Y30LB10
ERR120   EQU   *                                               @Y30LB10
         TM    EWOSNS0,EWOINT+EWOENV   INT REQ OR ENV DATA     @Y30LB10
         BNZ   ERR200                  BR IF EITHER ON         @Y30LB10
         SPACE 1
*******************                                            @Y30LB10
*  IS IT EQUIPMENT CHECK OR BUS OUT. IF YES, GO ATTEMPT A      @Y30LB10
*  RETRY.                                                      @Y30LB10
*******************                                            @Y30LB10
         TM    EWOSNS0,EWOEQU+EWOBUSO  EQ CHK OR BUS OUT       @Y30LB10
         BNZ   ERR400                  BR IF EITHER ON         @Y30LB10
         SPACE 1
*******************                                            @Y30LB10
*  IS IT CHAINING CHECK. IF YES, GO RETRY.                     @OZ00682
*******************                                            @Y30LB10
         TM    IOSTSB,CHN              IS IT CHAINING CHK?     @OZ00682
         BNZ   ERR400                  BR YES CHAINING CHK     @OZ00682
         SPACE 1
*******************                                            @Y30LB10
*  IS IT PROGRAM CHECK, PROTECTION CHECK OR INCORRECT LENGTH.  @Y30LB10
*  IS YES, GO TO PERMANENT ERROR.                              @Y30LB10
*******************                                            @Y30LB10
         TM    IOSTSB,PGM+PROT+ILI     PGM CK, PROT CK         @ZA13053
         BNZ   ERR078                  BR IF ANY BIT ON        @ZA13053
         SPACE 1
*******************                                            @Y30LB10
*  NO ERRORS HAVE BEEN FOUND. IF UNIT CHK IS ON,               @Y30LB10
*  GO TO PERM ERROR.                                           @Y30LB10
*******************                                            @Y30LB10
         TM    IOSTSA,UNCK             IS UNIT CHK ON          @Y30LB10
         BO    ERR500                  BR YES TO PERM ERR      @Y30LB10
         SPACE 1
*******************                                            @Y30LB10
*  NO STATUS OR SENSE ERRORS HAVE BEEN FOUND. CALL THE         @Y30LB10
*  ERROR RECOVERED AND EXIT THE ERP.                           @Y30LB10
*******************                                            @Y30LB10
ERR130   EQU   *                                               @Y30LB10
         MVI   EWAFLG3,ZERO           CLR ERP FLAGS            @Y30LB10
         XC    EWACNTR2(C2),EWACNTR2   ZERO ERR CNTRS          @Y30LB10
         NI    IOSFLA,X'FF'-IOSERR-IOSEX  SET ERR RECOVERED    @Y30LB10
         B     ERR2000                 BR TO EXIT              @Y30LB10
         EJECT
*******************                                            @Y30LB10
*  IS IT A SUSPEND CONDITION (DEV END + UNIT CHK ONLY).        @Y30LB10
*  IF NO, GO CHECK FURTHER. IF YES, BUILD THE SUSPEND CCW.     @Y30LB10
*  IF THE COMMAND WAS CHAINED, TIC TO THE NEXT USER CCW.       @Y30LB10
*  IF NO CHAINING, ISSUE THE 'A3' UNCHAINED.                   @Y30LB10
*******************                                            @Y30LB10
ERR200   EQU   *                                               @Y30LB10
         CLI   IOSTSA,SUSPEND          IS IT A SUSPEND COND    @Y30LB10
         BNE   ERR240                  BR NO                   @Y30LB10
ERR205   EQU   *                                               @ZM02150
         LRA   REG3,C8(CCWPTR)         GET ADR OF ERR CCW+8    @ZM02150
ERR210   EQU   *                                               @Y30LB10
         OI    EWAFLG3,EWA3SAVE        SET SAVE ON             @ZA05141
         MVC   EWOCCW3(C8),CCWSUS1     MOVE SUSPEND CCW TO EWA @ZM02151
         LRA   REG2,C0(CCWPTR)         GET REAL ERR CCW ADR    @Y30LB10
         ST    REG2,EWOCCW3            ERR CCW ADR IN A3 CMD   @ZM02151
         MVC   UCBDEV+C4(C4),IOSVST    SAVE USER CCW ADR       @Y30LB10
         LA    REG2,EWOCCW1            GET VIRT ADR OF CCWS    @Y30LB10
         ST    REG2,IOSVST             PUT IN IOSB             @Y30LB10
         LRA   REG2,EWOCCW1            GET REAL CCW ADDR       @Y30LB10
         ST    REG2,IOSRST             SET IN IOSB START FIELD @Y30LB10
         TM    C4(CCWPTR),CMDCHN       IS ERR CCW CHAINED      @ZM02151
         BO    ERR217                  BRANCH YES              @ZA05142
         MVC   EWOCCW2(C8),CCWNOOP     MOVE IN NOOP CCW        @ZA05142
         B     ERR235                                          @ZA05142
ERR217   EQU   *                                               @ZA05142
         CLI   C0(CCWPTR),A3OP         WAS A3 FAILING CMD?     @ZA05142
         BE    ERR235                  BR TO BUILD A3 ONLY     @ZA05142
         CLI   C8(CCWPTR),TIC          IS CCW+8 A TIC          @Y30LB10
         BNE   ERR230                  BR NO                   @ZM02151
ERR220   EQU   *                                               @Y30LB10
         L     REG3,C8(CCWPTR)         GET THE TIC TO ADR      @ZM02151
ERR230   EQU   *                                               @Y30LB10
         ST    REG3,EWOCCW2            TIC TO ADR IN SUS TIC   @ZM02151
         MVI   EWOCCW2,TIC             TIC OP CODE BACK IN CCW @ZM02151
         MVC   EWOCCW2+C4(C4),CCWEND   MOVE RIGHT HALF OF CCW  @ZM02151
         SPACE 1
ERR235   EQU   *                                               @Y30LB10
         MVI   EWOCCW3,A3OP            SET A3 OP CODE IN CCW   @ZM02151
         MVC   EWOCCW1(C8),EWOCCW3     MOVE CCWS TO START ADR  @ZM02151
         TM    EWOSNS0,EWOCREJ         IS IT COMND REJ         @Y30LB10
         BO    ERR520                  BR YES                  @Y30LB10
         SPACE 1
*******************                                            @Y30LB10
*  IS IT INTERVENTION REQUIRED. IF YES, GO TO PRINT MSG        @Y30LB10
*  AND RETRY. IF NO, ITS ENVIROMENTAL DATA, GO RECORD          @Y30LB10
*  AND RETRY.                                                  @Y30LB10
*******************                                            @Y30LB10
ERR240   EQU   *                                               @Y30LB10
         TM    EWOSNS0,EWOINT          IS IT INT REQ           @Y30LB10
         BNO   ERR410                  BR NO                   @ZM31108
         OI    IOSFLB,IOSLOG           SET LOG ON              @ZM31108
         B     WTOOUT                  YES INT REQ,EXIT WTO    @ZM31108
         EJECT
*******************                                            @Y30LB10
*  CCH CODE.                                                   @Y30LB10
*  IS THIS THE SECOND TIME FOR THIS ERROR. IF YES, GO TO       @Y30LB10
*  PERMANENT ERROR.                                            @Y30LB10
*******************                                            @Y30LB10
ERR300   EQU   *                                               @Y30LB10
         TM    EWAFLG3,EWA3CCH        2ND TIME FOR THIS ERR    @Y30LB10
         BO    ERR380                  BR YES                  @Y30LB10
         SPACE 1
*******************                                            @Y30LB10
*  WAS ERROR ON A TIO, HIO OR IS THE NO RETRY BIT ON IN ERPIB. @Y30LB10
*  IF ANY, GO TO PERMANENT ERROR.                              @Y30LB10
*******************                                            @Y30LB10
         TM    EWARGFG1,EWACTIO+EWACHIO+EWANORTY   HIO,TIO OR? @Y30LB10
         BNZ   ERR380                  BR IF ANY BITS ON       @Y30LB10
         SPACE 1
*******************                                            @Y30LB10
*  WAS THE ERROR ON A INTERRUPT OR A COND CODE 1. IF NEITHER   @Y30LB10
*  OR BOTH, GO TO PERMANENT ERROR.                             @Y30LB10
*******************                                            @Y30LB10
         TM    EWARGFG1,EWACSIO+EWACINT  ERR ON SIO OR INTERR  @Y30LB10
         BO    ERR380                  BR BOTH ON              @Y30LB10
         BZ    ERR380                  BR NEITHER ON           @Y30LB10
         SPACE 1
*******************                                            @Y30LB10
*  IS IT A COND CODE 1. IF YES, THE CCW CAN BE RESTARTED SO    @Y30LB10
*  BYPASS SOME ERROR TESTING.                                  @Y30LB10
*******************                                            @Y30LB10
         TM    IOSCC,IOSCC1            IS IT COND CODE 1       @Y30LB10
         BO    ERR330                  BR YES                  @Y30LB10
         SPACE 1
*******************                                            @Y30LB10
*  ARE VALIDITY BITS ALL ON. IF ANY OFF, GO TO PERM ERROR.     @Y30LB10
*******************                                            @Y30LB10
         TM    EWAXCSW1,EWACSQV+EWACUNS+EWACCMD SEQ,STS,CMD OK @Y30LB10
         BNO   ERR380                  BR IF ANY OFF           @Y30LB10
         SPACE 1
*******************                                            @Y30LB10
*  ONLY TERM CODES OF 0 OR 1 ARE RETRIABLE. IF TERM CODE IS    @Y30LB10
*  2 OR 3 GO TO PERM ERROR.                                    @Y30LB10
*******************                                            @Y30LB10
ERR330   EQU   *                                               @Y30LB10
         TM    EWAXCSW2,EWATER3        IS TERM CODE 3          @Y30LB10
         BO    ERR380                   BR YES                 @Y30LB10
         TM    EWAXCSW2,EWATER2        IS TERM CODE 2          @Y30LB10
         BO    ERR380                  BR YES                  @Y30LB10
         SPACE 1
*******************                                            @Y30LB10
*  ONLY  SEQ CODE OF 1,2 OR 3 ARE RETRIABLE. IF IT IS SEQ      @Y30LB10
*  CODE 0,4,5,6 OR 7 GO TO PERM ERROR.                         @Y30LB10
*******************                                            @Y30LB10
         TM    EWAXCSW2,EWASEQ4        IS SEQ CODE 4,5,6,7     @Y30LB10
         BO    ERR380                  BR YES                  @Y30LB10
         TM    EWAXCSW2,EWASEQ7        IS SEQ CODE 0           @Y30LB10
         BZ    ERR380                  BR YES                  @Y30LB10
         SPACE 1
*******************                                            @Y30LB10
*  ZERO THE ERPIB, SET THE CCH COUNTER BIT ON AND GO TO        @Y30LB10
*  RETRY THE OPERATION.                                        @Y30LB10
*******************                                            @Y30LB10
ERR360   EQU   *                                               @Y30LB10
         XC    EWAERPIB(C8),EWAERPIB   ZERO ERPIB              @Y30LB10
         OI    EWAFLG3,EWA3CCH        SET CCH CNTR ON          @Y30LB10
         B     ERR2000                 BR                      @Y30LB10
         SPACE 1
*******************                                            @Y30LB10
*  ZERO THE ERPIB AND GO TO PERM ERROR.                        @Y30LB10
*******************                                            @Y30LB10
ERR380   EQU   *                                               @Y30LB10
         XC    EWAERPIB(C8),EWAERPIB   ZERO ERPIB              @Y30LB10
         B     ERR500                  BR                      @Y30LB10
         EJECT
*******************                                            @Y30LB10
*  IS THE ERROR COUNTER = 5. IF NO, GO ATTEMPT A RETRY.        @Y30LB10
*  IF YES, SEE IF A SWAP IS REQUIRED.                          @Y30LB10
*******************                                            @Y30LB10
ERR400   EQU   *                                               @Y30LB10
         CLI   EWACNTR2,FIVE           IS ERR EQUAL TO FIVE    @Y30LB10
         BE    ERR470                  BR YES                  @Y30LB10
         SPACE 1
*******************                                            @Y30LB10
*  STEP THE ERROR COUNTER BY ONE.                              @Y30LB10
*******************                                            @Y30LB10
         IC    REG2,EWACNTR2           GET ERR CNTR            @Y30LB10
         LA    REG2,C1(REG2)           STEP BY ONE             @Y30LB10
         STC   REG2,EWACNTR2           PUT BACK                @Y30LB10
         SPACE 1
*******************                                            @Y30LB10
*  IS IT EQ CHK OR ENVIROMENTAL DATA. IF NEITHER GO RETRY.     @Y30LB10
*  IF EITHER GO CHECK FOR CEM AND SUU.                         @Y30LB10
*******************                                            @Y30LB10
ERR410   EQU   *                                               @Y30LB10
         TM    EWOSNS0,EWOENV          IS IT ENV DATA          @Y30LB10
         BO    ERR420                  BR YES                  @Y30LB10
         TM    EWOSNS0,EWOEQU          IS IT EQ CHK            @Y30LB10
         BZ    ERR2000                 NO TO EXIT SVC 15 RETRY @Y30LB10
         SPACE 1
*******************                                            @Y30LB10
*  IF NOT CEM GO TO RETRY. IF CEM                              @ZA28260
*  THE ERP WILL PRINT THE MESSAGE. THEN GO RETRY.              @ZA28260
*******************                                            @Y30LB10
         TM    EWOSNS1,EWOCEM          IS CE MSG ON            @ZA28260
         BZ    ERR530                  BR IF NEITHER ON        @ZM31108
         MVC   EWOMSG(C37),WTOMSG      MOVE MSG TO EWA         @ZA28260
         MVC   EWOWTOB(C3),MSGCEM      MOVE CEM TO MSG         @ZA28260
         BAL   REG3,MSG100             GO PRT CEM MSG          @ZA28260
         B     ERR520                  TO WTO TO PRT EQ CK MSG @Y30LB10
         SPACE 1
*******************                                            @Y30LB10
*  IS IT SUU. IF SO ERP WILL PRINT SUU THEN GO RETRY.          @ZA28260
*******************                                            @Y30LB10
ERR420   EQU   *                                               @Y30LB10
         TM    EWOSNS1,EWOSUU+EWOCEM   IS IT SUU OR CEM        @ZA28260
         BZ    ERR530                  IF OFF GO TO RETRY      @ZA28260
         B     ERR520                  IF EITHER ON GO TO WTO  @ZA28260
         SPACE 1
*******************                                            @Y30LB10
*  SET INTERCEPT TO PERM ERROR.                                @Y30LB10
********************                                           @Y30LB10
ERR440   EQU   *                                               @Y30LB10
         MVI   IOSCOD,INTCOD44         SET INTERCEPT PERM ERR  @Y30LB10
         SPACE 1
*********************                                          @Y30LB10
*  SET PERMANENT ERROR ON                                      @Y30LB10
*********************                                          @Y30LB10
ERR450   EQU   *                                               @Y30LB10
         NI    IOSFLA,X'FF'-IOSERR     SET PERM ERR            @Y30LB10
         B     ERR530                  SET LOG ON & EXIT OBR   @ZM31108
         SPACE 1
*******************                                            @Y30LB10
*  IS IT EQUIPMENT CHECK OR BUS OUT. IF EITHER, SET UP TO      @Y30LB10
*  GO TO THE SWAP CODE. IF NEITHER  EXIT PERM ERROR.           @Y30LB10
*******************                                            @Y30LB10
ERR470   EQU   *                                               @Y30LB10
         TM    EWOSNS0,EWOEQU+EWOBUSO  EQ CHK OR BUS OUT       @Y30LB10
         BZ    ERR500                  BR IF NEITHER ON        @Y30LB10
         SPACE 1
*******************                                            @Y30LB10
*  SET MSG ON. IF CEM ON GO PRINT CEM MESSAGE.                 @Y30LB10
*******************                                            @Y30LB10
         OI    IOSFLB,IOSMSG           SET MSG ON              @Y30LB10
         MVC   EWOMSG(C37),WTOMSG      MOVE MSG TO EWA         @ZA28260
         MVC   EWOWTOB(C3),MSGCEM      MOVE CEM TO MSG         @ZA28260
         BAL   REG3,MSG100             GO PRT CEM MSG          @ZA28260
         SPACE 1
********************                                           @Y30LB10
*  IT IS EQ CHK OR BUSOUT AND WE WANT TO SWAP
*******************                                            @Y30LB10
ERR480   EQU   *                                               @Y30LB10
         B     ERR560                  BR TO SWAP              @YD00155
         SPACE 1
*********************                                          @YD00133
* SET TO PERMANENT ERROR, SET MSG ON, GO TO WTO                @YD00133
********************                                           @YD00133
ERR495   EQU   *                                               @YD00133
         OI    IOSFLB,IOSMSG           SET MSG ON              @YD00133
         NI    IOSFLA,X'FF'-IOSERR     SET PERM ERR            @YD00133
         B     EXIT                    GO TO WTO               @ZM31108
         SPACE 1
*******************                                            @Y30LB10
*  SET MSG ON.                                                 @Y30LB10
*******************                                            @Y30LB10
ERR500   EQU   *                                               @Y30LB10
         OI    IOSFLB,IOSMSG           SET MSG ON              @Y30LB10
         SPACE 1
*******************                                            @Y30LB10
*  SET ERP TO PERMANENT ERROR.                                 @Y30LB10
*******************                                            @Y30LB10
ERR510   EQU   *                                               @Y30LB10
         NI    IOSFLA,X'FF'-IOSERR SET ERR TO PERMANENT        @Y30LB10
         B     ERR530                  BR                      @Y30LB10
         SPACE 1
*******************                                            @Y30LB10
*  SET MSG ON.                                                 @Y30LB10
*******************                                            @Y30LB10
ERR520   EQU   *                                               @Y30LB10
         OI    IOSFLB,IOSMSG           SET MSG ON              @Y30LB10
         SPACE 1
*******************                                            @Y30LB10
*  SET LOG ON.                                                 @Y30LB10
*******************                                            @Y30LB10
ERR530   EQU   *                                               @Y30LB10
         OI    IOSFLB,IOSLOG           SET LOG ON              @Y30LB10
         SPACE 1
*******************                                            @Y30LB10
*  GO TO COMMON EXIT ROUTINE.                                  @ZM31108
*******************                                            @Y30LB10
         B     EXIT                                            @ZM31108
         SPACE 1
********************                                           @YD00155
*  THE MSC IS NOT INITIALIZIED. SET FLAG AND GO TO ISSUE THE   @YD00155
*  SWITCH CCW,S WITHOUT SWITCHING THE UCB,S.                   @YD00155
********************                                           @YD00155
ERR545   EQU   *                                               @YD00155
         OI    IOSFLB,IOSMSG+IOSLOG    SET MSG & LOG ON        @YD00155
         OI    EWAFLG3,NOTINIT         SET MSC NOT INIT ON     @ZM30721
         MVI   EWACNTR2,C0             ZERO ERR CNTR           @ZM30721
         NI    EWACNTR3,X'FF'-SECERR   SET OFF 2ND ERR         @ZM30721
         B     ERR660                  GO ISSUE CCW,S          @ZM30721
         SPACE 1
********************                                           @YD00155
*  SET MSC SWITCH ON                                           @YD00155
********************                                           @YD00155
ERR550   EQU   *                                               @YD00155
ERR555   EQU   *                                               @YD00155
         OI    EWAFLG3,EWA3MSC         SET MSC SW ON           @YD00155
         SPACE 1
ERR560   EQU   *                                               @Y30LB10
         OI    IOSFLB,IOSMSG           SET MSG ON              @YD00155
ERR565   EQU   *                                               @YD00155
         OI    IOSFLB,IOSLOG           SET LOG ON              @YD00155
ERR570   EQU   *                                               @YD00155
         B     ERR600                  GO TO SWITCH CODE       @YD00155
         EJECT
*******************                                            @Y30LB10
*  COMPLETE THE ERROR MESSAGE. AND DO A WTO.                   @Y30LB10
*******************                                            @Y30LB10
MSG100   EQU   *                                               @Y30LB10
       TM   CVTOPTA,CVTNIP             SEE IF ITS IPL TIME     @ZA13054
         BO    NOMSG                   BRANCH IF SO            @ZA13054
         UNPK  EWOWORK1(C4),EWACHA(C3) GET DEV ADDRESS         @OZ05073
         TR    EWOWORK1(C3),TRTABLE-240 MAKE IT PRINTABLE      @OZ05073
         MVC   EWOWTOA(C3),EWOWORK1    MOVE DEV ADR TO MSG     @OZ05073
         SPACE 1
         LR    REG2,IOSBPTR            SAVE IOSB REG           @Y30LB10
         TIME  DEC                                             @Y30LB10
         LR    IOSBPTR,REG2            RESTORE REG 1           @Y30LB10
         SPACE 1
         ST    REG0,EWOWORK1           STORE TIME IN EWA       @Y30LB10
         OI    EWOWORK1+C3,OF          SET IN SIGN             @Y30LB10
         UNPK  EWOWORK1(C7),EWOWORK1(C4) UNPACK TO PRTABLE HEX @Y30LB10
         MVC   EWOWTOC(C2),EWOWORK1    MOVE IN HOUR            @YD00156
         MVC   EWOWTOE(C2),EWOWORK1+C2  MOVE MINS TO MSG       @YD00156
         MVC   EWOWTOF(C2),EWOWORK1+C4  MOVE SECS TO MSG       @YD00156
         SPACE 1
         LR    REG2,IOSBPTR            SAVE REG 1              @YD00156
         LA    IOSBPTR,EWOMSG          ADDR OF MESSAGE         @YD00156
         WTO   MF=(E,(1))                                      @YD00156
         LR    IOSBPTR,REG2            RESTORE REG 1           @YD00156
         SPACE 1
NOMSG    EQU   *                                               @ZA13054
         BR    REG3                    RETURN TO CALLER        @Y30LB10
         EJECT
************************************************************** @Y30LB10
************************************************************** @Y30LB10
*        CONTROL UNIT SWAP CODE                                @Y30LB10
*                                                              @Y30LB10
************************************************************** @Y30LB10
************************************************************** @Y30LB10
*  IF 'SWAP' IS ON;                                            @YD00155
*    IF THE CCW OP CODE IS 'EE' GO SWITCH THE UCB,S            @YD00155
*    IF THE CCW OP CODE IS NOT 'EE' GO CHK FOR SWITCH ERRS.    @YD00155
*  IF 'SWAP' IS NOT ON;                                        @YD00155
*    IF 'LOOP' IS ON GO ISSUE THE SWITCH CCW,S.                @YD00155
*    IF 'LOOP' IS OFF GO ISSUE THE INVALID OP CCW.             @YD00155
********************                                           @YD00155
ERR600   EQU   *                                               @Y30LB10
         TM    CVTOPTA,CVTNIP          IS NIP IN CONTROL       @ZA28260
         BO    ERR890                  BR YES                  @ZA28260
         LR    REGD,PRIUCB             GET PRIMARY UCB ADDR    @ZA24432
         AH    REGD,H512               ADD HEX 200 TO UCB ADDR @ZA24432
         CLR   REGD,ALTUCB             IS IT BACKUP MSC        @ZA24432
         BE    ERR510                  BR YES TO PREM ERROR    @ZA24432
         TM    EWAFLG3,NOTINIT         IS MSC INITIALIZED      @ZA24432
         BO    ERR700                  BR NO SET               @ZM31094
         TM    ICBFG1,ICBI             HAS MSSC SET INIT ON?   @ZM31094
         BNO   ERR450                  BR NO SET               @ZM31094
         TM    EWAFLG3,EWA3SWAP        ARE WE IN SWAP NOW      @YD00155
         BZ    ERR603                  BR NO CHK FOR LOOP      @YD00155
         CLI   C0(CCWPTR),CEE          IS IT INVALID OP        @YD00155
         BE    ERR610                  BR YES TO ISSUE CCWS    @YD00155
         B     ERR700                  BR NO CHK FOR ERRS      @YD00155
ERR603   EQU   *                                               @YD00155
         MVI   EWACNTR2,C0             ZERO ERR CNTR           @YD00155
         NI    EWACNTR3,X'FF'-SECERR   SET OFF 2ND ERR         @YD00155
         SPACE 1
*******************                                            @YD00155
*  WE MUST SWAP SO WE WILL INSURE THAT THE MSG AND LOGGING     @YD00155
*  REQUIRED IS DONE BEFOR THE UCBS ARE SWAPPED.                @YD00155
*  WE ISSUE A ERP CCW WITH OP CODE OF X'EE' TO FORCE A RETURN  @YD00155
*  FROM WTO/OBR.                                               @YD00155
*  WE WILL EXIT THRU OBR/WTO AND IOS WILL CALL US BACK BECAUSE @YD00155
*  THE ERP IS STILL IN CONTROL. THE ERP WILL                   @YD00155
*  LOOK FOR THE LOOP BIT ON WHEN IT IS CALLED AND GO           @YD00155
*  TO DO THE SWAP.                                             @YD00155
*******************                                            @YD00155
ERR605   EQU   *                                               @YD00155
         OI    EWAFLG3,EWA3SAVE+EWA3SWAP  SET SAVE+SWAP        @YD00155
         MVC   EWOCCW1(C8),CCWERP      MOVE ERP CCW TO EWA     @YD00155
         MVC   UCBDEV+C4(C4),IOSVST    SAVE USERS CCW ADDR     @YD00155
         LA    REG2,EWOCCW1            GET VIRT ADR OF ERP CCW @YD00155
         ST    REG2,IOSVST             STORE IN IOSB           @YD00155
         LRA   REG2,EWOCCW1            GET REAL ADDR OF ERP CCW@YD00155
         ST    REG2,IOSRST             STORE IN IOSB           @YD00155
         B     EXIT                    BR     TO WTO           @ZM31108
         SPACE 1
********************                                           @Y30LB10
*  IS THE ALERNATE CONTROL UNIT 'GENED', 'ONLINE' AND 'READY'. @Y30LB10
*  BRANCH IF NOT TO PRINT 'SWAP IMPOSSIBLE'.                   @Y30LB10
********************                                           @Y30LB10
ERR610   EQU   *                                               @Y30LB10
         LTR   ALTUCB,ALTUCB           IS ALT UCB GENED        @Y30LB10
         BZ    ERR850                  BR NO                   @Y30LB10
         TM    ALTSTAT(ALTUCB),UCBONLI  IS ALT UCB ONLINE      @Y30LB10
         BZ    ERR850                  BR NO TO PRT MSG        @Y30LB10
         TM    ALTFLA(ALTUCB),UCBNRY    IS ALT UCB READY       @Y30LB10
         BO    ERR850                  BR NO TO PRT MSG        @Y30LB10
         SPACE 1
********************                                           @Y30LB10
*  SET 'SWAP IN PROCESS' ON.                                   @Y30LB10
********************                                           @Y30LB10
         OI    EWAFLG3,EWA3SWAP SET SWAP IN PROCESS ON         @Y30LB10
         SPACE 1
********************                                           @Y30LB10
*  FIND THE ALTERNATE AND PRIMARY UCB SLOTS IN THE UCB LOOKUP  @Y30LB10
*  TABLE. SWAP THE CONTENTS OF THESE SLOTS.                    @Y30LB10
********************                                           @Y30LB10
         L     REG2,CVTILK2            GET ADR OF LOOKUP TABLE @Y30LB10
         LR    REGD,PRIUCB             GET PRIMARY UCB ADDR    @Y30LB10
         AH    REGD,H512               ADD HEX 200 TO UCB ADDR @Y30LB10
         SPACE 1
********************                                           @Y30LB10
*  FIND UCB LOOKUP SLOT FOR ALTERNATE FIRST                    @Y30LB10
********************                                           @Y30LB10
ERR620   EQU   *                                               @Y30LB10
         CLM   ALTUCB,C3,C0(REG2)      'TO' UCB LOOKUP SLOT    @Y30LB10
         BNE   ERR630                  BR NO CHK NEXT          @YD00137
         STH   REGD,C0(REG2)           STORE PRI IN ALT SLOT   @YD00137
         B     ERR640                  GO CHK NEXT SLOT        @YD00137
         SPACE 1
********************                                           @Y30LB10
*  FIND UCB LOOKUP SLOT FOR PRIMARY NOW                        @Y30LB10
********************                                           @Y30LB10
ERR630   EQU   *                                               @Y30LB10
         CLM   REGD,C3,C0(REG2)       'FROM' UCB LOOKUP SLOT   @Y30LB10
         BNE   ERR640                  BR NO CHK NEXT          @YD00137
         STH   ALTUCB,C0(REG2)         STORE ALT IN PRI SLOT   @YD00137
********************                                           @YD00137
*  STEP TO NEXT SLOT AND TEST FOR END OF TABLE                 @YD00137
********************                                           @YD00137
ERR640   EQU   *                                               @YD00137
         LA    REG2,C2(REG2)           STEP TO NEXT SLOT       @Y30LB10
         TM    C1(REG2),EOT            IS IT END OF TABLE      @Y30LB10
         BNO   ERR620                  BR NO TO NEXT TEST      @YD00137
         SPACE 1
********************                                           @Y30LB10
*  SWAP THE FOLLOWING CONTENTS OF THE PRIMARY AND ALTERNATE    @Y30LB10
*  UCBS. 'UCBNAME' (DEVICE ADDR IN BCD) AND 'UCBCHAN' (DEVICE  @Y30LB10
*  ADDR IN HEX).                                               @Y30LB10
********************                                           @Y30LB10
ERR650   EQU   *                                               @Y30LB10
         LH    REGC,UCBCHAN           GET HEX CUU OF PRIMARY   @Y30LB10
         LH    REGD,ALTCHAN(ALTUCB)  GET HEX CUU OF ALTERNATE  @Y30LB10
         STH   REGC,ALTCHAN(ALTUCB)  SWAP PRIMARY AND ALTERNATE@Y30LB10
         STH   REGD,UCBCHAN            UCB HEX CUU ADDRS       @Y30LB10
         IC    REGC,UCBLCI             GET LCH INDEX           @ZA32810
         IC    REGD,ALTLCI(ALTUCB)     GET ALT LCH INDEX       @ZA32810
         STC   REGC,ALTLCI(ALTUCB)     SAVE OLD LCI INDEX      @ZA32810
         STC   REGD,UCBLCI             SAVE NEW LCI INDEX      @ZA32810
         MVC   EWOWORK1(C3),UCBNAME    SAVE PRIMARY UCB BCD CUU@Y30LB10
         MVC   UCBNAME(C3),ALTNAME(ALTUCB)  ALT BCD CUU TO     @Y30LB10
         MVC   ALTNAME(C3,ALTUCB),EWOWORK1                     @Y30LB10
         SPACE 1
***************************************************************@Y30LB10
***************************************************************@Y30LB10
*  PRINT MESSAGE;                                              @Y30LB10
*  'SYS SWITCH FROM XXX TO ZZZ STARTED, XXX OFFLINE'           @YD00156
*  'MSC SWITCH FROM XXX TO ZZZ STARTED, XXX OFFLINE'           @YD00156
*  ZZZ IS THE 'NEW' PRIMARY DEVICE                             @Y30LB10
*  XXX IS THE 'NEW' ALTERNATE DEVICE.                          @Y30LB10
********************                                           @Y30LB10
         MVC   EWOMSG(C63),WTOMSGD     MOVE MSG TO EWA         @YD00156
         MVC   EWOWTOK(C3),ALTNAME(ALTUCB)  ALT UCB CUU TO MSG @YD00156
         NI    ALTSTAT(ALTUCB),X'FF'-UCBONLI  NEW ALT OFFLINE  @YD00155
         BAL   REGC,SWAPMSG            GO PRT MSG              @YD00156
         SPACE 1
********************                                           @Y30LB10
*  MOVE THE 4 'SWAP' CCWS TO THE EWA AND SET UP TO SIO TO      @ZA05142
*  THEM. THE CCWS ARE CHAINED 'SWITCH', INITIALIZE', 'HOST     @Y30LB10
*  READY FOR MESSAGES', AND 'NOOP'                             @ZA05142
********************                                           @Y30LB10
ERR660   EQU   *                                               @Y30LB10
         OI    EWAFLG3,EWA3SAVE+EWA3SWAP  SET SAVE+SWAP        @YD00155
         MVC   UCBDEV+C4(C4),IOSVST    SAVE USER VIRT ADR      @Y30LB10
         MVC   EWOCCW1(C24),CCWSW      MOVE SWAP CCWS TO EWA   @ZA05142
         MVC   EWOINIT(C4),INIT        MOVE INIT DATA TO EWA   @Y30LB10
         MVC   EWOHRM(C4),HRM          MOVE HRM DATA TO EWA    @Y30LB10
         LRA   REG2,EWOINIT            REAL ADDR OF INIT DATA  @Y30LB10
         ST    REG2,EWOCCW2            STORE ADDR IN CCW       @Y30LB10
         MVI   EWOCCW2,EXEC            RESTORE CCW OP CODE     @Y30LB10
         LRA   REG2,EWOHRM             REAL ADDR OF HRM DATA   @Y30LB10
         ST    REG2,EWOCCW3            STORE ADDR IN CCW       @Y30LB10
         MVI   EWOCCW3,EXEC            RESTORE CCW OP CODE     @Y30LB10
         LA    REG2,EWOCCW1            GET ADR OF 1ST SWAP CCW @Y30LB10
         ST    REG2,IOSVST             VIRT CCW ADR IN IOSB    @Y30LB10
         LRA   REG2,C0(REG2)           GET REAL ADR            @Y30LB10
         ST    REG2,IOSRST             REAL CCW ADR IN IOSB    @Y30LB10
         TM    ICBFG1,ICBR             HAS MSSC SET READY ON?  @ZM31093
         BO    ERR670                  BR YES READY IS SET     @ZM31093
         MVI   EWOCCW3,NOOP            MAKE HRM CCW NOP        @ZM31093
ERR670   EQU   *                                               @ZM31093
*******************                                            @Y30LB10
*  SET THE INITIALIZE COMMAND DATA UP FOR SINGLE CPU OR        @Y30LB10
*  MP COUPLED CPUS. USED FOR SWAPS.                            @Y30LB10
*******************                                            @Y30LB10
         TM    ICBFG1,ICBMSCMP         IS THIS MP              @G24LB05
         BZ    EXIT                    BR NO                   @G24LB05
         MVI   EWOINIT+C2,X40          SET COUPLED MP BIT ON   @Y30LB10
         MVC   EWOCCW4(C8),EWOCCW3     MOVE LAST CCW DOWN 1    @ZA28260
         MVC   EWOCCW3(C8),CCWASSOC    PUT ASSOC CCW IN CHAIN  @ZA28260
         LRA   REG2,EWOASSOC           GET ADDR OF ASSOC ORDER @ZA28260
         ST    REG2,EWOCCW3            PUT ADDR OF ORDER IN CCW@ZA28260
         MVI   EWOCCW3,EXEC            RESTORE OP CODE         @ZA28260
         MVC   EWOASSOC(C12),ASSOC     MOVE ASSOC ORDER TO EWO @ZA28260
         MVC   EWOASSOC+C4(C5),ICBCPUID PUT CPUID IN ORDER     @ZA28260
         B     EXIT                    SVC 15-3 EXIT           @ZM31108
         SPACE 1
         EJECT
********************                                           @Y30LB10
*  HAVE WE COMPLETED THE SWAP WITHOUT ERROR.                   @Y30LB10
*  IS IT CONDITION CODE 3 OR UNIT CHECK.                       @Y30LB10
*  BRANCH YES, TO HANDLE SWAP FAILURE.                         @Y30LB10
********************                                           @Y30LB10
ERR700   EQU   *                                               @Y30LB10
         TM    IOSCC,IOSCC3            IS IT COND CODE 3       @Y30LB10
         BNO   ERR710                  BR NO TO ERR CHK        @YD00155
         TM    EWACNTR3,SECERR         HAVE WE TRIED ONCE      @YD00155
         BO    ERR705                  BR YES                  @YD00155
         OI    EWACNTR3,SECERR         SET COUNT ON            @YD00155
         B     ERR660                  BR TO REISSUE           @YD00155
ERR705   EQU   *                                               @YD00155
         TM    EWACNTR3,TRDERR         IS IT THE 3RD ERR       @YD00155
         BO    ERR890                  BR YES TO PERM ERR      @YD00155
         OI    EWACNTR3,TRDERR         SET COUNT ON            @YD00155
         B     ERR800                  BR TO PRT MSG           @YD00155
ERR710   EQU   *                                               @Y30LB10
         CLC   IOSTSA(C2),STATOK       CSW STATUS CHAN-DEV END @Y30LB10
         BE    ERR713                  BR OK TO SWAP END MSG   @OZ00688
         TM    IOSTSA,X'FF'-CEDEUC     IS IT C.E.-D.E.-U.C.?   @OZ00688
         BNZ   ERR800                  ONES OR MIXED TO ERR CK @OZ00688
         TM    IOSTSB,X'FF'            ANY CH STATUS ON?       @OZ00688
         BNZ   ERR800                  ONES OR MIXED TO ERR CK @OZ00688
         TM    IOSTSA,UNCK             IS UNIT CHECK ON?       @OZ00688
         BZ    ERR800                  NO UNIT CHECK TO ERR CK @OZ00688
         TM    EWOSNS0,EWOCREJ+EWOHRET IS HOST RETRY OR        @OZ00688
*                                      COMMAND REJECT ON?      @OZ00688
         BNZ   ERR800                  ONES OR MIXED TO ERR CK @OZ00688
         TM    EWOSNS0,EWOINT+EWOENV   IS IT INT. REQ. OR ENV. @OZ00688
         BZ    ERR800                  BR NO TO ERR CK.        @OZ00688
         OI    EWAFLG3,EWA3SAVE        SET SAVE ON             @OZ00688
         ST    CCWPTR,IOSVST          PUT V START ADDR IN IOSB @OZ00688
         LRA   REG2,C0(CCWPTR)         CONVERT TO REAL         @OZ00688
         ST    REG2,IOSRST            PUT R START ADDR IN IOSB @OZ00688
         CLI   IOSTSA,SUSPEND          IS IT D.E. + U.C. ONLY? @OZ00688
         BNE   ERR240                  GO SET UP FOR MSG & LOG @OZ00688
         MVI   0(CCWPTR),A3OP          SET A3 IN CCW           @OZ00688
         B     ERR240                  GO SET UP FOR MSG & LOG @OZ00688
         SPACE 1
***************************************************************@Y30LB10
***************************************************************@Y30LB10
*  PRINT MESSAGE;                                              @Y30LB10
*  'SYS SWITCH FROM XXX TO ZZZ ENDED'                          @YD00156
*  'MSC SWITCH FROM XXX TO ZZZ ENDED'                          @YD00156
*  XXX IS THE 'NEW' ALTERNATE DEVICE                           @YD00156
*  ZZZ IS THE 'NEW' PRIMARY DEVICE.                            @YD00156
********************                                           @Y30LB10
ERR713   TM    EWAFLG3,NOTINIT         IS MSC INITIALIZED      @OZ00688
         BNO   ERR715                  BR YES                  @ZM30721
         NI    EWAFLG3,X'FF'-NOTINIT   SET FLAG - MSC NOT INIT @ZM30721
         NI    EWAFLG3,X'FF'-EWA3SWAP  SET SWAP OFF            @ZM30721
         B     ERR730                  BR                      @ZM30721
ERR715   EQU   *                                               @ZM30721
         MVC   EWOMSG(C48),WTOMSGA     MOVE MSG TO EWA         @YD00156
         BAL   REGC,SWAPMSG            GO PRT MSG              @YD00156
         SPACE 1
********************                                           @Y30LB10
*  GO TO DO A SVC 15, SVC 3.                                   @Y30LB10
********************                                           @Y30LB10
ERR730   EQU   *                                               @Y30LB10
         MVI   EWACNTR2,C1            SET ERR CNTR TO 1        @Y30LB10
         B     ERR900                  BR TO EXIT              @YD00155
         EJECT
***************************************************************@Y30LB10
***************************************************************@Y30LB10
*  PRINT MESSAGE;                                              @Y30LB10
*  'SYS SWITCH FROM XXX TO ZZZ FAILED, ZZZ OFFLINE'            @YD00156
*  'MSC SWITCH FROM XXX TO ZZZ FAILED, ZZZ OFFLINE'            @YD00156
*  XXX IS THE 'NEW' ALTERNATE DEVICE                           @YD00156
*  ZZZ IS THE 'NEW' PRIMARY DEVICE.                            @Y30LB10
********************                                           @Y30LB10
ERR800   EQU   *                                               @YD00155
         TM    EWAFLG3,NOTINIT         IS MSC INITIALIZED      @ZM30721
         BO    ERR810                  BR YES NOT INIT         @ZM30721
         MVC   EWOMSG(C62),WTOMSGC     MOVE MSG TO EWA         @YD00156
         MVC   EWOWTOJ(C3),UCBNAME     MOVE DEV ADDR TO MSG    @YD00156
         BAL   REGC,SWAPMSG            GO PRT MSG              @YD00156
*                                                              @Y30LB10
         NI    UCBSTAT,X'FF'-UCBONLI   SET PRIMARY UCB OFFLINE @Y30LB10
         TM    IOSCC,IOSCC3            COND CODE 3             @YD00155
         BNO   ERR810                  BR NO                   @YD00155
         MVI   IOSCOD,COD41            SET COMP CODE TO 41     @YD00155
         B     ERR814                  BR ON                   @YD00155
ERR810   EQU   *                                               @YD00155
         OI    IOSFLB,IOSMSG+IOSLOG    SET MSG + LOG ON        @YD00155
ERR814   EQU   *                                               @YD00155
         NI    IOSFLA,X'FF'-IOSERR     SET PERM ERR            @YD00155
         B     EXIT                    GO XCTL TO WTO          @ZM31108
***************************************************************@Y30LB10
***************************************************************@Y30LB10
*  PRINT MESSAGE;                                              @Y30LB10
*  'SYS SWITCH FROM AAA TO BBB, BBB NOT AVAIL'                 @YD00156
*  'SYS SWITCH FROM AAA TO ALT, ALT NOT AVAIL'                 @YD00156
*  'MSC SWITCH FROM AAA TO BBB, BBB NOT AVAIL'                 @YD00156
*  'MSC SWITCH FROM AAA TO ALT, ALT NOT AVAIL'                 @YD00156
*  AAA IS THE 'CURRENT' PRIMARY DEVICE                         @YD00156
*  BBB IS THE 'CURRENT' ALTERNATE DEVICE                       @YD00156
*  IF THE ALTERNATE IS NOT GENED THE ZZZ WILL BE 'ALT'         @Y30LB10
*******************                                            @Y30LB10
ERR850   EQU   *                                               @Y30LB10
         MVC   EWOMSG(C57),WTOMSGB     MOVE MSG TO EWA         @YD00156
         MVC   EWOWTOE(C3),UCBNAME     SET IN PRI ADR          @YD00156
ERR860   EQU   *                                               @YD00156
         MVC   EWOWTOI(C3),MSGALT      MOVE 'ALT' TO MSG       @YD00156
         MVC   EWOWTOG(C3),MSGALT      MOVE 'ALT' TO MSG       @YD00156
         LTR   ALTUCB,ALTUCB           IS ALT UCB AVALI        @Y30LB10
         BZ    ERR870                  BR IF NOT GENED         @Y30LB10
         MVC   EWOWTOI(C3),ALTNAME(ALTUCB)   MOVE ALT DEV ADDR @YD00156
         MVC   EWOWTOG(C3),ALTNAME(ALTUCB)   MOVE ALT DEV ADDR @YD00156
ERR870   EQU   *                                               @Y30LB10
         BAL   REGC,SWAPSVC            GO PRT MSG              @YD00156
         CLI   EWOSNS11,EWOMSG06       IS IT MSG CODE 06       @ZA27590
         BNE   ERR880                  BRANCH NO               @ZA27590
         CLI   EWOSNS12,EWOCOD30       IS IT ERROR CODE 30     @ZA27590
         BNE   ERR880                  BRANCH NO               @ZA27590
         LTR   ALTUCB,ALTUCB           IS ALT UCB AVAILABLE    @ZA27590
         BZ    ERR890                  BR IF NOT GENNED        @ZA27590
         SPACE 1
*******************                                            @Y30LB10
*  SET PRIMARY UCB 'OFFLINE'                                   @Y30LB10
*  SET 'PERMANENT ERROR' IN IOSB                               @Y30LB10
*******************                                            @Y30LB10
ERR880   EQU   *                                               @Y30LB10
         NI    UCBSTAT,X'FF'-UCBONLI   SET PRI UCB OFFLINE     @Y30LB10
ERR890   EQU   *                                               @Y30LB10
         NI    IOSFLA,X'FF'-IOSERR     SET TO PERM ERR         @Y30LB10
         SPACE 3
ERR900   EQU   *                                               @YD00155
         NI    EWAFLG3,X'FF'-EWA3SWAP-EWA3CCH                  @YD00155
         NI    EWAFLG3,X'FF'-NOTINIT   SET FLAG - MSC NOT INIT @ZM30721
         EJECT
EXIT     TM    IOSFLB,IOSMSG           IS MSG ON ?             @ZM31108
         BO    WTOOUT                  YES TO TO WTO           @ZM31108
         TM    IOSFLB,IOSLOG           IS LOG ON ?             @ZM31108
         BO    OBROUT                  YES TO TO OBR           @ZM31108
         B     ERR2000                 GO TO SVC 15-3 EXIT     @ZM31108
*******************                                            @Y30LB10
*  OBR EXIT                                                    @ZM31108
*******************                                            @Y30LB10
OBROUT   EQU   *                                               @ZM31108
         LA    REGD,OBRRTN             GET OBR CODE            @ZM31108
         B     XCTLOUT                 GO TO OBR               @ZM31108
*******************                                            @Y30LB10
*  WTO EXIT                                                    @ZM31108
*******************                                            @Y30LB10
WTOOUT   EQU   *                                               @ZM31108
         LA    REGD,WTORTN             GET WTO CODE            @ZM31108
*******************                                            @Y40LB02
*  IF ITS FORMAT 0 - GO TO FORMAT SENSE XCTL                   @Y40LB02
*******************                                            @Y40LB02
         TM    EWOSNS3,EWOFOR0         IS IT FORMAT 0?         @Y40LB02
         BO    ERR1100                 BRANCH YES              @Y40LB02
*******************                                            @Y40LB02
*  IF ITS FORMAT 3 - GO TO FORMAT SENSE XCTL                   @Y40LB02
*******************                                            @Y40LB02
         TM    EWOSNS3,EWOFOR3         IS IT FORMAT 3?         @Y40LB02
         BO    ERR1100                 BRANCH YES              @Y40LB02
*******************                                            @Y40LB02
         TM    EWOSNS3,EWOFOR4         IS IT FORMAT 4?         @Y40LB02
         BO    ERR1100                 BRANCH YES              @Y40LB02
*******************                                            @Y40LB02
*  IF ITS FORMAT 5 - GO TO FORMAT SENSE XCTL                   @Y40LB02
*******************                                            @Y40LB02
         TM    EWOSNS3,EWOFOR5         IS IT FORMAT 5?         @Y40LB02
         BO    ERR1100                 BRANCH YES              @Y40LB02
*******************                                            @Y40LB02
*  IF ITS FORMAT 1 - GO TO FORMAT SENSE XCTL                   @ZA14725
*******************                                            @ZA14725
         TM    EWOSNS3,EWOFOR1         IS IT FORMAT 1?         @ZA14725
         BO    ERR1100                 BRANCH YES              @ZA14725
         SPACE 1
XCTLOUT  EQU   *                                               @Y30LB10
*******************
****************************************************************
*    SYSTEM WTO/OBR XCTL
****************************************************************
         BAL   REG3,ERR3000            GO TEST FOR QUISCE      @YD00136
         L     BALRREG,CVTXTLER        GET XCTL RTN ADDR       @Y30LB10
         BR    BALRREG                 XCTL TO NEXT RTN        @Y30LB10
         SPACE 4
************************************************************** @Y40LB02
*    FORMAT SENSE XCTL                                         @Y40LB02
*************************************************************  @Y40LB02
ERR1100  BAL   REG3,ERR3000            GO TEST FOR QUISCE      @Y40LB02
         LA    REGD,FSLD3RTN           FMT SNS ROUTING ID      @Y40LB02
         L     BALRREG,CVTXTLER        GET XCTL RTN ADDR       @Y40LB02
         BR    BALRREG                 XCTL TO NEXT RTN        @Y40LB02
***************************************************************@X04AC10
*******************                                            @Y30LB10
*  EXIT SVC-15  SVC-3                                          @Y30LB10
*******************                                            @Y30LB10
ERR2000  EQU   *                                               @Y30LB10
         BAL   REG3,ERR3000            GO TEST FOR QUISCE      @YD00136
         SVC   FIFTEEN                                         @Y30LB10
         SVC   THREE                                           @Y30LB10
         SPACE 3
*********************                                          @YD00136
* IF ERP IS EXITING PERMANENT OR RECOVERED ERROR AND THE       @YD00136
* DRIVER IS EXCP THEN SET THE QUISCE BITS IN THE IOSB AND      @YD00136
* UCB OFF.                                                     @YD00136
*********************                                          @YD00136
ERR3000  EQU   *                                               @YD00136
         LR    REGC,REGD               SAVE REG 13             @YD00136
         LA    REGD,EWOMSG             SAVE AREA FOR IOSGEN    @YD00136
         TM    IOSFLA,IOSERR+IOSEX     ERP IN CONTROL          @YD00136
         BO    ERR3010                 BR YES                  @YD00136
         TM    IOSOPT,IOSQISCE         IS QUISCE ON?           @ZUP0988
         BZ    ERR3010                 BR NO                   @YD00136
         NI    IOSOPT,X'FF'-IOSQISCE   SET IOSB QUISCE BIT OFF @YD00136
         L     REG2,IOSUCB             GET UCB ADDRESS         @ZA00663
         IOSGEN  UCBFLG,UCB=(REG2),VAR=OFF,TABLE=UCBQISCE      @ZA00663
         SR    REG7,REG7               ZERO REGISTER           @ZUP0988
         L     REG2,IOSUCB             GET UCB ADDRESS         @ZUP0988
         IOSINTRP UCB=(REG2),BRANCH=YES                        @ZA20273
ERR3010  EQU   *                                               @YD00136
         LR    REGD,REGC               RESTORE REG 13          @YD00136
         BR    REG3                    RETURN                  @YD00136
         EJECT
********************                                           @YD00156
*  SET PRIMARY DEVICE ADDRESS IN MSG AT 'ZZZ' AND ALTERNATE    @YD00156
*  DEVICE ADDRESS IN MSG AT 'XXX'.                             @YD00156
********************                                           @YD00156
SWAPMSG  EQU   *                                               @YD00156
         MVC   EWOWTOG(C3),UCBNAME     SET IN PRI DEV ADDR     @YD00156
         MVC   EWOWTOE(C3),ALTNAME(ALTUCB)  SET IN ALT DEV ADR @YD00156
         SPACE 1
********************                                           @YD00156
*  SET EITHER 'SYS SWITCH' OR 'MSC SWITCH' IN MSG              @YD00156
********************                                           @YD00156
SWAPSVC  EQU   *                                               @YD00156
         MVC   EWOWTOA(C10),MSGSYS     SET IN ' SYS SWAP'      @YD00156
SWAP010  EQU   *                                               @YD00156
         TM    EWAFLG3,EWA3MSC         IS IT A MSC SWITCH      @YD00156
         BZ    SWAP020                 BR NO                   @YD00156
         MVC   EWOWTOA(C10),MSGMSC     MOVE MSC TO MSG         @YD00156
SWAP020  EQU   *                                               @YD00156
         LR    REG2,IOSBPTR            SAVE REG 1              @YD00156
         LA    IOSBPTR,EWOMSG          ADDR OF MESSAGE         @YD00156
         WTO   MF=(E,(1))                                      @YD00156
         LR    IOSBPTR,REG2            RESTORE REG 1           @YD00156
         BR    REGC                    RETURN TO CALLER        @YD00156
         EJECT
*--------------------------------------------------------------@Y30LB10
CCWSW    CCW   X'44',0,X'60',1                                 @Y30LB10
CCWINIT  CCW   X'87',INIT,X'60',4                              @Y30LB10
CCWHRM   CCW   X'87',HRM,X'20',4                               @ZA28260
CCWASSOC CCW   X'87',ASSOC,X'60',12                            @ZA28260
CCWNOOP  CCW   X'03',0,X'20',1                                 @ZA28260
CCWERP   CCW   X'EE',0,X'00',0                                 @ZM30721
*--------------------------------------------------------------@Y30LB10
INIT     DC    X'10000000'                                     @Y30LB10
ASSOC    DC    X'2C0080000000000000000000'                     @ZA28260
HRM      DC    X'20000000'                                     @Y30LB10
*--------------------------------------------------------------@Y30LB10
CCWSUS1  CCW   X'A3',0,X'60',1                                 @ZA05142
CCWEND   DC    X'00000001'        2ND HALF OF TIC CCW          @ZM02151
*--------------------------------------------------------------@Y30LB10
         DS    0F                                              @Y30LB10
H8       DC    H'8'                                            @Y30LB10
H512     DC    H'512'                                          @Y30LB10
STATOK   DC    X'0C00'                                         @Y30LB10
TRTABLE  DC    CL16'0123456789ABCDEF'                          @OZ05073
*--------------------------------------------------------------@Y30LB10
         DS    0F                                              @Y30LB10
WTOMSGA  DS    0F                                              @Y30LB10
         DC    AL2(MSGENDA-MSGNOA)                             @Y30LB10
         DC    X'8000'                                         @Y30LB10
MSGNOA   DC    C'IEA000I YYYYYYYYYY FROM XXX TO ZZZ ENDED'     @YD00156
         DC    X'10004040'                                     @Y30LB10
MSGENDA  EQU   *                                               @Y30LB10
*--------------------------------------------------------------@Y30LB10
*                                                              @Y30LB10
WTOMSGB  DS    0F                                              @Y30LB10
         DC    AL2(MSGENDB-MSGNOB)                             @Y30LB10
         DC    X'8000'                                         @Y30LB10
MSGNOB   DC    C'IEA000I ZA27590YYY FROM AAA TO BBB, '         @YD00156
         DC    C'BBB NOT AVAIL'                                @YD00156
         DC    X'10004040'                                     @Y30LB10
MSGENDB  EQU   *                                               @Y30LB10
*--------------------------------------------------------------@Y30LB10
WTOMSGC  DS    0F                                              @Y30LB10
         DC    AL2(MSGENDC-MSGNOC)                             @Y30LB10
         DC    X'8000'                                         @Y30LB10
MSGNOC   DC    C'IEA000I YYYYYYYYYY FROM XXX TO ZZZ FAILED, '  @YD00156
         DC    C'ZZZ OFFLINE'                                  @YD00156
         DC    X'10004040'                                     @Y30LB10
MSGENDC  EQU   *                                               @Y30LB10
*--------------------------------------------------------------@Y30LB10
*                                                              @Y30LB10
WTOMSGD  DS    0F                                              @Y30LB10
         DC    AL2(MSGENDD-MSGNOD)                             @Y30LB10
         DC    X'8000'                                         @Y30LB10
MSGNOD   DC    C'IEA000I ZA27590YYY FROM XXX TO ZZZ STARTED, ' @YD00156
         DC    C'XXX OFFLINE'                                  @YD00156
         DC    X'10004040'                                     @Y30LB10
MSGENDD  EQU   *                                               @Y30LB10
*--------------------------------------------------------------@Y30LB10
*                                                              @Y30LB10
WTOMSG   DS    0F                                              @Y30LB10
         DC    AL2(MSGEND-MSGNO)                               @Y30LB10
         DC    X'8000'                                         @Y30LB10
MSGNO    DC    C'IEA000I '                                     @Y30LB10
MSGTXTA  DC    C'XXX ZZZ'                                      @Y30LB10
MSGTXTB  DC    C',,,,,,'                                       @Y30LB10
MSGTXTC  DC    C'XX.XX.XX'                                     @Y30LB10
ROUTE    DC    X'10004040'                                     @Y30LB10
MSGEND   EQU   *                                               @Y30LB10
*--------------------------------------------------------------@Y30LB10
MSGALT   DC    C'ALT '                                         @Y30LB10
MSGCEM   DC    C'CEM '                                         @Y30LB10
MSGSYS   DC    C'SYS SWITCH'                                   @YD00156
MSGMSC   DC    C'MSC SWITCH'                                   @YD00156
*--------------------------------------------------------------@Y30LB10
ERPMAINT EQU   *                                               @Y30LB10
         DC    AL2(MAINTEND-ERPMAINT)                          @Y30LB10
         ORG   ERR000+4080                                     @Y30LB10
         DC    C'IGE0001H //END//'                             @Y30LB10
MAINTEND EQU   *                                               @Y30LB10
         EJECT
         IECDIOSB
         EJECT
         IECDERWA
         SPACE 3
*//////////////////////////////////////////////////////////////@Y30LB10
*                                                              @Y30LB10
*//////////////////////////////////////////////////////////////@Y30LB10
         ORG   EWAIERP                                         @Y30LB10
EWOSNS   DS    XL32                    3851 SENSE              @Y30LB10
         ORG   EWOSNS                                          @Y30LB10
EWOSNS0  DS    XL1                     SENSE BYTE 0            @Y30LB10
EWOCREJ  EQU   X'80'                   COMMAND REJECT          @Y30LB10
EWOINT   EQU   X'40'                   INTERVENTION REQUIRED   @Y30LB10
EWOBUSO  EQU   X'20'                   BUS OUT CHECK           @Y30LB10
EWOEQU   EQU   X'10'                   EQUIPMENT CHECK         @Y30LB10
EWO4     EQU   X'08'                   SPARE                   @Y30LB10
EWOHRET  EQU   X'04'                   HOST RETRY              @Y30LB10
EWOPERR  EQU   X'02'                   PERMANENT ERROR         @Y30LB10
EWOENV   EQU   X'01'                   ENVIROMENTAL DATA       @Y30LB10
EWOSNS1  DS    XL1                     SENSE BYTE 1            @Y30LB10
EWOCEM   EQU   X'80'                   CE MESSAGE              @Y30LB10
EWOSUU   EQU   X'40'                   SUBSYSTEM UNIT UNUSABLE @Y30LB10
EWOSNS2  DS    XL1                     SENSE BYTE 2            @Y30LB10
EWOSNS3  DS    XL1                     SENSE BYTE 3            @Y30LB10
EWOFOR0  EQU   X'80'                   FORMAT 0                @Y30LB10
EWOFOR1  EQU   X'40'                   FORMAT 1                @Y30LB10
EWOFOR2  EQU   X'20'                   FORMAT 2                @Y30LB10
EWOFOR3  EQU   X'10'                   FORMAT 3                @Y30LB10
EWOFOR4  EQU   X'08'                   FORMAT 4                @Y30LB10
EWOFOR5  EQU   X'04'                   FORMAT 5                @Y30LB10
EWOSNS4  DS    XL1                     SENSE BYTE 4            @Y30LB10
EWOSNS5  DS    XL1                     SENSE BYTE 5            @Y30LB10
EWOSNS6  DS    XL1                     SENSE BYTE 6            @Y30LB10
EWOSNS7  DS    XL1                     SENSE BYTE 7            @Y30LB10
EWOSNS8  DS    XL1                     SENSE BYTE 8            @Y30LB10
EWOSNS9  DS    XL1                     SENSE BYTE 9            @Y30LB10
EWOSNS10 DS    XL1                     SENSE BYTE 10           @Y30LB10
EWOSNS11 DS    XL1                     SENSE BYTE 11           @Y30LB10
EWOMSG02 EQU   X'02'                   MESSAGE '02'            @YD00133
EWOMSG04 EQU   X'04'                   MESSAGE '04'            @YD00133
EWOMSG06 EQU   X'06'                   MESSAGE '06'            @YD00133
EWOMSG0A EQU   X'0A'                   MESSAGE '0A'            @YD00133
EWOMSG0B EQU   X'0B'                   MESSAGE '0B'            @Y30LB10
EWOSNS12 DS    XL1                     SENSE BYTE 12           @Y30LB10
EWOCOD25 EQU   X'25'                   CODE '25'               @ZA10999
EWOCOD2D EQU   X'2D'                   CODE '2D'               @ZA13053
EWOCOD3F EQU   X'3F'                   CODE '3F'               @ZA09552
EWOCOD57 EQU   X'57'                   CODE '57'               @ZA28260
EWOCOD80 EQU   X'80'                   CODE '80'               @YD00133
EWOCOD30 EQU   X'30'                   CODE '30'               @ZA24432
EWOCOD8F EQU   X'8F'                   CODE '8F'               @YD00133
EWOCODC0 EQU   X'C0'                   CODE 'C0'               @YD00133
EWOCODCA EQU   X'CA'                   CODE 'CA'               @G24LB22
EWOCODCB EQU   X'CB'                   CODE 'CB'               @G24LB22
EWOSNS13 DS    XL1                     SENSE BYTE 13           @Y30LB10
EWOSNS14 DS    XL1                     SENSE BYTE 14           @Y30LB10
EWOSNS15 DS    XL1                     SENSE BYTE 15           @Y30LB10
EWOSNS16 DS    XL1                     SENSE BYTE 16           @Y30LB10
EWOSNS17 DS    XL1                     SENSE BYTE 17           @Y30LB10
EWOSNS18 DS    XL1                     SENSE BYTE 18           @Y30LB10
EWOSNS19 DS    XL1                     SENSE BYTE 19           @Y30LB10
EWOSNS20 DS    XL1                     SENSE BYTE 20           @Y30LB10
EWOSNS21 DS    XL1                     SENSE BYTE 21           @Y30LB10
EWOSNS22 DS    XL1                     SENSE BYTE 22           @Y30LB10
EWOSNS23 DS    XL1                     SENSE BYTE 23           @Y30LB10
EWOSNS24 DS    XL1                     SENSE BYTE 24           @Y30LB10
EWOSNS25 DS    XL1                     SENSE BYTE 25           @Y30LB10
EWOSNS26 DS    XL1                     SENSE BYTE 26           @Y30LB10
EWOSNS27 DS    XL1                     SENSE BYTE 27           @Y30LB10
EWOSNS28 DS    XL1                     SENSE BYTE 28           @Y30LB10
EWOSNS29 DS    XL1                     SENSE BYTE 29           @Y30LB10
EWOSNS30 DS    XL1                     SENSE BYTE 30           @Y30LB10
EWOSNS31 DS    XL1                     SENSE BYTE 31           @Y30LB10
*                                                              @Y30LB10
EWOMSG   DS    XL63                    ERP MSG AREA            @DCRXXXX
         ORG   EWOMSG                                          @Y30LB10
EWOWTO   DS    XL12                    ERP MSG                 @Y30LB10
EWOWTOA  DS    XL4                     ERP MSG                 @Y30LB10
EWOWTOB  DS    XL9                     ERP MSG                 @Y30LB10
EWOWTOC  DS    XL2                     ERP MSG                 @Y30LB10
EWOWTOD  DS    XL1                     ERP MSG                 @Y30LB10
EWOWTOE  DS    XL3                     ERP MSG                 @Y30LB10
EWOWTOF  DS    XL4                     ERP MSG                 @Y30LB10
EWOWTOG  DS    XL3                     ERP MSG                 @Y30LB10
EWOWTOH  DS    XL2                     ERP MSG                 @ZM02153
EWOWTOI  DS    XL7                     ERP MSG                 @ZM02153
EWOWTOJ  DS    XL1                     ERP MSG                 @DCRXXXX
EWOWTOK  DS    XL11                    ERP MSG                 @DCRXXXX
EWOWTORT DS    XL4                     ERP MSG                 @Y30LB10
*                                                              @Y30LB10
EWOSPARE DS    XL1                     EWA SPARE               @ZM02153
EWOWORK1 DS    XL4                     ERP WORK AREA 1         @ZA05142
EWOWORK2 DS    XL4                     ERP WORK AREA 2         @ZA05142
         ORG   EWOMSG                                          @ZA05142
EWOCCWS  DS    XL32                    ERP CCWS                @ZA05142
         ORG   EWOCCWS                                         @ZA05142
EWOCCW1  DS    XL8                     ERP CCW 1               @ZA05142
EWOCCW2  DS    XL8                     ERP CCW 2               @ZA05142
EWOCCW3  DS    XL8                     ERP CCW 3               @ZA05142
EWOCCW4  DS    XL8                     ERP CCW 4               @ZA05142
EWOINIT  DS    XL4                     INIT CCW DATA           @ZA05142
EWOASSOC DS    XL12                    ASSOC CCW DATA          @ZA28260
EWOHRM   DS    XL4                     HRM CCW DATA            @ZA05142
RSVD     DS    XL44                    EWO SPARE               @ZA28260
EWOEND   EQU   *                       EWA END                 @Y30LB10
         EJECT
         IEFUCBOB                      PREFIX=YES,LIST=YES
         EJECT
         CVT   DSECT=YES,LIST=NO
         EJECT
         IEZSSC
         EJECT
         IHAPSA
         EJECT
         IHALCCA
         EJECT
         IECDIRT
         EJECT
         IECDIOCM
         EJECT
         IECDIOCX
         END
