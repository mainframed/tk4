         TITLE 'IGG09409 - STAUTOLN ROUTINE'
         EJECT
IGG09409 CSECT
         COPY  LCGASMS2
***********************************************************************
*                                                                     *
* STATUS -                                                            *
*    CHANGE LEVEL 003                                                 *
*                                                                     *
* FUNCTION -                                                          *
*    THIS ROUTINE IS TO SERVE THE MACRO 'STAUTOLN'. IT PLACES THE     *
*    STARTING                                                         *
*    VALUE IN TSBAUTOS FIELD AND THE INCREMENT VALUE IN TSBAUTOI      *
*    FIELD. THE AUTOMATIC LINE NUMBERING BIT (TSBAULST) IS SET ON.    *
*    THE TSBAUTON AND QCBALN BITS ARE ALSO SET ON TO REFLECT AUTOMATIC*
*    PROMPT IS IN EFFECT. THE CHARACTER PROMPT BIT (TSBAUTOC) IS SET  *
*    OFF.                                                             *
*                                                                     *
*    AFTER INITIALIZATION FOR AUTOMATIC LINE NUMBERING, THE FIRST LINE*
*    NUMBER IS SENT TO THE TERMINAL VIA POSTING THE QCB AND INVOKE    *
*    TSOUTPUT                                                         *
*                                                                     *
* ENTRY POINTS -                                                      *
*         IGG09409 - CONTROL IS RECEIVED FROM MODULE IGC0009D         *
*                    VIA BRANCH, HOLDING THE LOCAL AND THE CMS        *
*                    LOCKS, AND IN SUPERVISOR KEY                     *
*                                                                     *
* INPUT -                                                             *
*    REGISTER 0 CONTAINS:                                             *
*    **********************************************************       *
*    *  ENTRY CODE  *   ADDRESS OF STARTING VALUE             *       *
*    **********************************************************       *
*    REGISTER 1 CONTAINS:                                             *
*    **********************************************************       *
*    *   FLAG       *   ADDRESS OF INCREMENT VALUE            *       *
*    **********************************************************       *
*    REGISTER 3 CONTAINS CVT ADDRESS                                  *
*    REGISTER 4 CONTAINS TCB ADDRESS                                  *
*    REGISTER 5 CONTAINS TSB ADDRESS                                  *
*    REGISTER 10 CONTAINS THE ADDRESS OF AN ARRAY OF CONSTANTS        *
*    REGISTER 12 CONTAINS THE BASE ADDRESS                            *
*    REGISTER 14 CONTAINS RETURN ADDRESS                              *
*                                                                     *
* OUTPUT -                                                            *
*    REGISTER 15 HAS RETURN CODE:                                     *
*           0 - SUCCESSFUL                                            *
*           4 - INVALID PARAMETER                                     *
*                                                                     *
* EXTERNAL REFERENCES -                                               *
*         NONE                                                        *
*                                                                     *
* EXITS, NORMAL -                                                     *
*         BR 14 WITH 0 IN REGISTER 15                                 *
*                                                                     *
* EXITS, ERROR -                                                      *
*         BR 14 WITH 4 IN REGISTER 15                                 *
*                                                                     *
* TABLES/WORK AREAS -                                                 *
*    CONTROL BLOCKS ARE DESCRIBED IN THE DSECTS AT END OF LISTING     *
*                                                                     *
* ATTRIBUTES -                                                        *
*    REENTRANT, REFRESHABLE, SUPERVISOR MODE, AND ENABLED             *
*                                                                     *
* CHARACTER CODE DEPENDENCY -                                         *
*    THIS MODULE IS NOT CHARACTER DEPENDENT                           *
* NOTES -                                                             *
*         VS01537 - REFERENCED USER CORE IN TCAMS' KEY,0C4            *
*                                                                     *
***********************************************************************
         SPACE 3
RPARM0   EQU   0                        PARAMETER REG 0
RWORK0   EQU   0                        WORK REGISTER
RPARM1   EQU   1                        PARAMETER REG 1
RWORK1   EQU   1                        WORK REGISTER
RWORK2   EQU   2                        WORK REGISTER
RWORK3   EQU   3                        WORK REGISTER
RCVT     EQU   3                        POINTS TO CVT
RTCB     EQU   4                        POINTS TO TCB
RTSB     EQU   5                        POINTS TO TSB           YS02019
RWORK6   EQU   6                        WORK REGISTER           YS02019
RCONST   EQU   10                       POINTS TO PROGRAM       YS02019
*                                       CONSTANTS               YS02019
RBASE    EQU   12                       BASE REGISTER           YS02019
R13      EQU   13                       REGISTER 13
R14      EQU   14                       REGISTER 14
RLINK    EQU   14                       LINK REGISTER
R15      EQU   15                       REGISTER 15
RBRANCH  EQU   15                       BRANCH REGISTER
RCODE    EQU   15                       RETURN CODE REGISTER
         SPACE 2
********
******** DISPLACEMENT,LENGTH AND MASK EQUATES
********
BYTE1    EQU   8                        MASK FOR BYTE 1 IN REG.
BYTE2    EQU   4                        MASK FOR BYTE 2 IN REG.
BYTE3    EQU   2                        MASK FOR BYTE 3 IN REG.
BYTE4    EQU   1                        MASK FOR BYTE 4 IN REG.
BYTE34   EQU   3                        MASK FOR BYTES 3&4 IN REG.
BYTE234  EQU   7                        MASK FOR BYTES 2&3&4 IN REG.
D0       EQU   0                        DISPLACEMENT OF 0
D1       EQU   1                        DISPLACEMENT OF 1
D2       EQU   2                        DISPLACEMENT OF 2
D3       EQU   3                        DISPLACEMENT OF 3
D4       EQU   4                        DISPLACEMENT OF 4
D5       EQU   5                        DISPLACEMENT OF 5
D6       EQU   6                        DISPLACEMENT OF 6
D14      EQU   14                       DISPLACEMENT OF 14
L1       EQU   1                        LENGTH OF 1
L2       EQU   2                        LENGTH OF 2
L3       EQU   3                        LENGTH OF 3
L4       EQU   4                        LENGTH OF 4
L8       EQU   8                        LENGTH OF 8
L16      EQU   16                       LENGTH OF 16
L20      EQU   20                       LENGTH OF 20
L24      EQU   24                       LENGTH OF 24
L31      EQU   31                       LENGTH OF 31
L32      EQU   32                       LENGTH OF 32
RETCODE4 EQU   4                        RETURN CODE OF 4
RETCODE8 EQU   8                        RETURN CODE OF 8
RETCODEC EQU   12                       RETURN CODE OF 12
HIPRI    EQU   X'E4'                    TCAM POSTING PRIORITY
COMPLETE EQU   X'40'                    TO TEST POST COMPLETE BIT
CZERO    EQU   0                        TO TEST FOR ZERO
CONE     EQU   1                        CONSTANT OF 1
CTWO     EQU   2                        CONSTANT OF 2
CTHREE   EQU   3                        CONSTANT OF 3
CFIFTEEN EQU   15                       CONSTANT OF 15
CSIXTEEN EQU   16                       CONSTANT OF 16
CFOX     EQU   X'FF'                    TO TEST FOR 'FF'
BLANK    EQU   X'40'                    TO TEST FOR BLANK
OFF      EQU   X'FF'                    TO TURN BITS OFF
IDLE     EQU   X'17'                    TERM. CTL. CHAR. - IDLE
NL       EQU   X'15'                    TERM. CTL. CHAR. - NEW LINE
CR       EQU   X'0D'                    TERM. CTL. CHAR. - CARRIAGE RET
LF       EQU   X'25'                    TERM. CTL. CHAR. - LINE FEED
HEX18    EQU   X'18'                    TERM. CTL. CHAR. - TWX CHAR.
HEX20    EQU   X'20'                    TO TEST BIT 2 OF BYTE
         SPACE 3
********
******** ESTABLISH BASE REGISTER AND ADDRESSABILITY
********
         USING *,RBASE                  BASE REGISTER ADDRESSABILITY
         USING TSB,RTSB                 TSB ADDRESSABILITY
         USING CONSTANT,RCONST          CONSTANT ADDRESSABILITY
         B     VALID                    BRANCH AROUND MODULE ID YS02019
         DC    CL8'IGG09409'            MODULE NAME             YS02019
         DC    XL2'3304'                DATE 10/31/73           YA03021
********
******** VALIDITY CHECK PARAMETERS
********
VALID    EQU   *
         LR    RWORK2,RWORK0            RESET PARM REGISTER     YS02019
         L     RWORK2,D0(,RWORK2)       GET STARTING VALUE      YS02019
         C     RWORK2,ZERO              TEST FOR NEGATIVE       YS02019
         BL    PARMBAD                  BAD PARAMETER
         C     RWORK2,NINES             TEST FOR TOO HIGH       YS02019
         BH    PARMBAD                  TOO HIGH
         L     RPARM1,D0(,RPARM1)       GET INCREMENT VALUE     YS02019
         C     RWORK1,ZERO              TEST FOR NEGATIVE       YS02019
         BL    PARMBAD                  BAD PARAMETER
         C     RWORK1,NINES             TEST FOR TOO HIGH       YS02019
         BH    PARMBAD                  BAD PARAMETER
********
******** PERFORM MAIN FUNCTION OF ROUTINE
********
         LR    RWORK0,RLINK             SAVE RETURN ADDRESS     YS02019
         MODESET EXTKEY=TCAM            GET TCAM'S KEY          YS02019
         ST    RWORK2,TSBAUTOS          SET STARTING VALUE      YM01537
         ST    RPARM1,TSBAUTOI          SET INCREMENT VALUE     YM01537
         OI    TSBFLG2,TSBAUTON+TSBAULST+TSBSTAUT
*                                       SIGNAL AUTOPROMPTING REQUESTED
*                                       AUTO LINE NUMBERING STARTED
*                                       THIS IS FIRST LINE NUMBER
         NI    TSBFLG2,OFF-TSBAUTOC     TURN OFF AUTO CHAR. PROMPT.
         NI    TSBFLG3,OFF-TSBSPIT      ALLOW PROMPTING         YA03021
********
******** TPOST THE TSB IN ORDER TO SCHEDULE TSOUTPUT TO GET
******** CONTROL AND PUT OUT THE INITIAL LINE NUMBER
********
********       POSTTSB SUBROUTINE ALTERS REG.S 6-9
********
         TM    TSBFLG3,TSBTPUT          TSOUTPUT ALREADY        YS02019
*                                       SCHEDULED               YS02019
         BO    RETURN                   YES, NO NEED TO TPOST   YS02019
         OI    TSBFLG3,TSBTPUT          IND TPUT SCHEDULED      YS02019
*                                       YS0201                  YS02019
         LA    RPARM1,TPMASK            ADDR OF TPOSTING MASK   YS02019
         L     R15,TPADDR               ADDR OF TPOSTING RTN    YS02019
         L     RWORK3,CVTPTR            ADDR OF CVT             YS02019
         BALR  R14,R15                  GO TPOST TSB            YS02019
RETURN   EQU   *
         MODESET EXTKEY=SUPR            GET KEY FOR SETLOCK     YS02019
         DROP  RBASE                    AS BASE REGISTER        YS02019
***********************************************************************
*        RELEASE CMS AND LOCAL LOCKS                                  *
***********************************************************************
RELEASE  EQU   *                        LABEL FOR SETLOCK       YS02019
         SETLOCK RELEASE,TYPE=CMS,RELATED=(TSB,IGC0009D)        YS02019
         SETLOCK RELEASE,TYPE=LOCAL,RELATED=(TSB,IGC0009D)      YS02019
         LR    RLINK,RWORK0             RESTORE RETURN ADDR     YS02019
         SR    RCODE,RCODE              SET GOOD RETURN CODE
         BR    RLINK                    RETURN                  YS02019
PARMBAD  EQU   *
         LA    RCODE,RETCODE4           INDICATE BAD PARAMETERS
         BR    RLINK                    RETURN TO CALLER        YS02019
         DC    9F'0'                    MAINTENANCE PATCH AREA  YS02019
         SPACE 3
CONSTANT DSECT                          DEFINES CONSTANTS IN 1ST CSECT
ZERO     DC    F'0'                     CONSTANT OF 0
NINES    DC    F'99999999'              CONSTANT OF 99,999,999
FIFTEEN  DC    AL1(15)                  CONSTANT OF 15
TWO      DC    XL1'02'                  CONSTANT OF 2
TEN      DC    H'10'                    CONSTANT OF 10
SIXTEEN  DC    AL1(16)                  CONSTANT OF 16
ENABLE   DC    X'FF'                    TO ENABLE INTERRUPTS
SIX      DC    H'6'                     CONSTANT OF 6
EIGHT    DC    AL1(8)                   CONSTANT OF 8
CP5041   DC    XL3'026D16'              BKSP/UNDERSCORE CHAR. PROMPT
HIGHBIT  DC    XL1'80'                  TO TEST HIGH ORDER BITS
CPTWX    DC    XL3'024B0D'              PERIOD/CAR.RETURN CHAR. PROMPT
TPUT1    DC    XL4'0E000000'            TPUT,HOLD,CONTROL
TPUT2    DC    XL4'0A000000'            TPUT,CONTROL
ONE      DC    AL1(1)                   CONSTANT OF ONE
XIDLE    DC    X'17'                    TPUT IDLE CHARACTER     YA00364
FOX      EQU   ENABLE                   CONSTANT OF 'FF'
DISABLE  EQU   ZERO                     TO DISABLE INTERRUPTS
TPADDR   DC    V(POSTTSB)               ADDR OF TPOSTING RTN    YS02019
TPMASK   DS    4F                       TPOSTING MASK           YS02019
         EJECT
         IHAPSA
         EJECT
         IKJTSB
         EJECT
         CVT
         END
