         TITLE 'IGG0940D - SPAUTOPT ROUTINE'
IGG0940D CSECT
*0000799000-800000,808000                                        M1614
*                                                                 M5113
*                                                                 M5467
***********************************************************************
*                                                                     *
*    RELEASE 23 DELETIONS/CHANGES                                     *
*    RELEASE 22 DELETIONS/CHANGES                                     *
*    RELEASE 21 DELETIONS/CHANGES                                     *
*                                                                     *
* STATUS -                                                            *
*    CHANGE LEVEL 001                                                 *
*                                                                     *
* FUNCTION -                                                          *
*    THIS ROUTINE IS TO SERVE THE MACRO 'SPAUTOPT'.  IT IS TO         *
*    SET THE BIT CONDITION IN TSB (TSBAUTOTN,TSBAUTOC,TSBAULST) TO    *
*    REFLECT THAT AUTOMATIC PROMPTING IS STOPPED THROUGH THE USE OF   *
*    THE 'SPAUTOPT' MACRO. ONCE STOPPED IN THIS MANNER, THE AUTOMATIC *
*    PROMPT MAY NOT BE RESTARTED.                                     *
*                                                                     *
*    AFTER THIS FUNCTION IS SUCCESSFULLY COMPLETED, A CODE OF '00' IS *
*    RETURNED TO THE CALLER.                                          *
*                                                                     *
* ENTRY POINTS -                                                      *
*         IGG0940D - CONTROL IS RECEIVED FROM MODULE IGC0009D         *
*                    VIA BRANCH ACCORDING ENTRY CODE 13 WITH          *
*                    THE LOCAL AND CMS LOCKS HELD AND IN              *
*                    SUPERVISOR KEY                                   *
*                                                                     *
* INPUT -                                                             *
*    REGISTER 0 CONTAINS PARAMETER                                    *
*    REGISTER 1 CONTAINS PARAMETER                                    *
*    REGISTER 3 CONTAINS CVT ADDRESS                                  *
*    REGISTER 4 CONTAINS TCB ADDRESS                                  *
*    REGISTER 5 CONTAINS TSB ADDRESS                                  *
*    REGISTER 10 CONTAINS THE ADDRESS OF AN ARRAY OF CONSTANTS
*    REGISTER 12 CONTAINS THE BASE ADDRESS
*    REGISTER 14 CONTAINS THE RETURN ADDRESS
*
* OUTPUT -                                                            *
*    REGISTER 15 HAS RETURN CODE:                                     *
*           0 - SUCCESSFUL                                            *
*           4 - INVALID PARAMETER                                     *
*           8 - AUTOMATIC PROMPT HAS NOT PREVIOUSLY BEEN STARTED      *
*                                                                     *
* EXTERNAL REFERENCES -                                               *
*         NONE                                                        *
*                                                                     *
* EXITS, NORMAL -                                                     *
*         BR 14 WITH 0 IN REGISTER 15                                 *
*                                                                     *
* EXITS, ERROR -                                                      *
*         BR 14 WITH 4 OR 8 IN REGISTER 15                            *
*                                                                     *
* TABLES/WORK AREAS -                                                 *
*    CONTROL BLOCKS ARE DESCRIBED IN THE DSECTS AT END OF LISTING     *
*                                                                     *
* ATTRIBUTES -                                                        *
*    REENTRANT, REFRESHABLE, SUPERVISOR MODE, AND ENABLED             *
*                                                                     *
* CHARACTER CODE DEPENDENCY -                                         *
*    THIS MODULE IS NOT CHARACTER DEPENDENT                           *
* NOTES -                                                             *
*    NONE                                                             *
*                                                                     *
*                                                                     *
***********************************************************************
         SPACE 3
********
******** REGISTER EQUATES
********
RPARM0   EQU   0                        PARAMETER REG 0
RWORK0   EQU   0                        WORK REGISTER
RPARM1   EQU   1                        PARAMETER REG 1
RWORK1   EQU   1                        WORK REGISTER
RWORK2   EQU   2                        WORK REGISTER
RWORK3   EQU   3                        WORK REGISTER
RCVT     EQU   3                        POINTS TO CVT
RTCB     EQU   4                        POINTS TO TCB
RTSB     EQU   5                        POINTS TO TSB           YS02019
RCONST   EQU   10                       POINTS TO PROGRAM       YS02019
*                                       CONSTANTS               YS02019
RBASE    EQU   12                       BASE REGISTER           YS02019
R13      EQU   13                       REGISTER 13
R14      EQU   14                       REGISTER 14
RLINK    EQU   14                       LINK REGISTER
R15      EQU   15                       REGISTER 15
RBRANCH  EQU   15                       BRANCH REGISTER
RCODE    EQU   15                       RETURN CODE REGISTER
         SPACE 2
********
******** DISPLACEMENT,LENGTH AND MASK EQUATES
********
BYTE1    EQU   8                        MASK FOR BYTE 1 IN REG.
BYTE2    EQU   4                        MASK FOR BYTE 2 IN REG.
BYTE3    EQU   2                        MASK FOR BYTE 3 IN REG.
BYTE4    EQU   1                        MASK FOR BYTE 4 IN REG.
BYTE34   EQU   3                        MASK FOR BYTES 3&4 IN REG.
BYTE234  EQU   7                        MASK FOR BYTES 2&3&4 IN REG.
D0       EQU   0                        DISPLACEMENT OF 0
D1       EQU   1                        DISPLACEMENT OF 1
D2       EQU   2                        DISPLACEMENT OF 2
D3       EQU   3                        DISPLACEMENT OF 3
D4       EQU   4                        DISPLACEMENT OF 4
D5       EQU   5                        DISPLACEMENT OF 5
D6       EQU   6                        DISPLACEMENT OF 6
D14      EQU   14                       DISPLACEMENT OF 14
L1       EQU   1                        LENGTH OF 1
L2       EQU   2                        LENGTH OF 2
L3       EQU   3                        LENGTH OF 3
L4       EQU   4                        LENGTH OF 4
L8       EQU   8                        LENGTH OF 8
L16      EQU   16                       LENGTH OF 16
L24      EQU   24                       LENGTH OF 24
L32      EQU   32                       LENGTH OF 32
RETCODE4 EQU   4                        RETURN CODE OF 4
RETCODE8 EQU   8                        RETURN CODE OF 8
RETCODEC EQU   12                       RETURN CODE OF 12
HIPRI    EQU   X'E4'                    TCAM POSTING PRIORITY
COMPLETE EQU   X'40'                    TO TEST POST COMPLETE BIT
CZERO    EQU   0                        TO TEST FOR ZERO
CONE     EQU   1                        CONSTANT OF 1
CTWO     EQU   2                        CONSTANT OF 2
CTHREE   EQU   3                        CONSTANT OF 3
CFIFTEEN EQU   15                       CONSTANT OF 15
CSIXTEEN EQU   16                       CONSTANT OF 16
CFOX     EQU   X'FF'                    TO TEST FOR 'FF'
BLANK    EQU   X'40'                    TO TEST FOR BLANK
OFF      EQU   X'FF'                    TO TURN BITS OFF
IDLE     EQU   X'17'                    TERM. CTL. CHAR. - IDLE
NL       EQU   X'15'                    TERM. CTL. CHAR. - NEW LINE
CR       EQU   X'0D'                    TERM. CTL. CHAR. - CARRIAGE RET
LF       EQU   X'25'                    TERM. CTL. CHAR. - LINE FEED
HEX18    EQU   X'18'                    TERM. CTL. CHAR. - TWX CHAR.
HEX20    EQU   X'20'                    TO TEST BIT 2 OF BYTE
         SPACE 3
********
******** ESTABLISH BASE REGISTER AND ADDRESSABILITY
********
         USING *,RBASE                  BASE REGISTER ADDRESSABILITY
         USING TSB,RTSB                 TSB ADDRESSABILITY
         B     VALIDITY                 BR AROUND ID AND DATE   YS02019
         DC    C'IGG0940D'              MODULE IDENTIFIER       YS02019
         DC    X'3123'                  DATE - 05/03/73         YS02019
********
******** CHECK FOR VALID PARAMETER
********
VALIDITY EQU   *                                                YS02019
         LTR   RPARM1,RPARM1            ANY PARM SPECIFIED
         BNZ   PARMBAD                  YES,INVALID
********
******** PERFORM MAIN FUNCTION OF ROUTINE
********
         TM    TSBFLG2,TSBAULST         AUTO LINE NUMBERING ON
         BO    AUTOFF                   YES,TURN OFF FUNCTION
         TM    TSBSTAT,TSBDSPLY         IS THIS A DISPLAY TERMINAL
         BO    RETURN                   RETURN TO CALLER        YS02019
TEST     EQU   *
         TM    TSBFLG2,TSBAUTOC         AUTO CHAR. PROMPT ON
         BNZ   AUTOFF                   BRANCH IF YES
         LA    RCODE,RETCODE8           SET ERROR RETURN CODE
         B     EXIT                     RETURN TO CALLER        YS02019
AUTOFF   EQU   *
         MODESET EXTKEY=TCAM            GET TCAM'S KEY          YS02019
         NI    TSBFLG2,OFF-(TSBAUTON+TSBAUTOC+TSBAULST)
*                                       TURN OFF ALL AUTOPROMPTING BITS
         MODESET EXTKEY=SUPR            GET BACK IN KEY 0       YS02019
RETURN   EQU   *
         SR    RCODE,RCODE              SET GOOD RETURN CODE
EXIT     EQU   *                                                YS02019
         BR    RLINK                    RETURN TO CALLER        YS02019
PARMBAD  EQU   *
         LA    RCODE,RETCODE4           SET BAD PARM RET. CODE
         BR    RLINK                    RETURN TO CALLER        YS02019
         DC    9F'0'                    PATCH AREA              YS02019
         EJECT
         IHAPSA
         EJECT
         IKJTSB
         END
