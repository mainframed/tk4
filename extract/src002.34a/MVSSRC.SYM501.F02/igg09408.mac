         TITLE 'IGG09408 - STATTN ROUTINE'
IGG09408 CSECT
         COPY  LCGASMS2
*
*0000                                                            M5712
*                                                                M0252
*                                                                M4283
*                                                                 M4040
*                                                                 M5113
*                                                                 M4283
*                                                                 M5467
*
***********************************************************************
*    RELEASE 23 DELETIONS/CHANGES                                     *
*    RELEASE 22 DELETIONS/CHANGES                                     *
*    RELEASE 21 DELETIONS/CHANGES                                     *
*                                                                     *
* STATUS -                                                            *
*    CHANGE LEVEL 01                                                  *
*                                                                     *
* FUNCTION -                                                          *
*    THIS ROUTINE IS TO SERVE THE MACRO 'STATTN'. IT IS TO PREPARE    *
*    DIFFERENT FIELDS IN TSB AND QCB TO REFLECT CONDITIONS WHICH THE  *
*    USER SPECIFIES WHEN SETTING ATTENTION SIMULATION.                *
*                                                                     *
*    THE VALUE SPECIFIED IN OPERANDS 'LINES' AND 'TENS' ARE MOVED INTO*
*    'TSBATNLC' AND 'TSBATNTC' LEFT JUSTIFIED. THE 2ND BYTE IN BOTH   *
*    FIELDS ARE NOT USED. 'INPUT CHARACTERS' IF SPECIFIED BY USER, IS *
*    MOVED INTO 'TSBATNCC'.                                           *
*                                                                     *
*    IF A FUNCTION IS CANCELLED, THAT IS, '0' IS SPECIFIED IN ANY     *
*    OF THE OPERAND, THE APPROPRIATE FIELD IN TSB IS ZEROED.          *
*                                                                     *
*    THE ATTENTION SIMULATION REQUESTED SWITCHES, 'TSBANR' IN THE TSB *
*    AND 'QCBSATCH', 'QCBSATTI', AND 'QCBSATLC' IN THE QCB ARE SET.   *
*                                                                     *
*    A RETURN CODE OF '00' INDICATES SUCCESSFUL COMPLETION OF THE     *
*    FUNCTION.                                                        *
*                                                                     *
* ENTRY POINTS -                                                      *
*         IGG09408 - CONTROL IS RECEIVED FROM MODULE IGC0009D         *
*                                                                     *
* INPUT -                                                             *
*    REGISTER 0                                                       *
*    ****************************************************             *
*    * ENTRY CODE = 08 *             * LINES *   * TENS *
*    ****************************************************             *
*                                                                     *
*    REGISTER 1                                                       *
*    ****************************************************             *
*    *                      * INPUT CHARACTER ADDRESS   *             *
*    ****************************************************             *
*    REGISTER 3 CONTAINS CVT ADDRESS                                  *
*    REGISTER 4 CONTAINS TCB ADDRESS                                  *
*    REGISTER 5 CONTAINS TSB ADDRESS                                  *
*    REGISTER 10 CONTAINS THE ADDRESS OF AN ARRAY OF CONSTANTS
*    REGISTER 12 CONTAINS THE BASE ADDRESS
*    REGISTER 14 CONTAINS THE RETURN ADDRESS
*                                                                     *
* OUTPUT -                                                            *
*    REGISTER 15 HAS RETURN CODE:                                     *
*           0 - SUCCESSFUL                                            *
*                                                                     *
* EXTERNAL REFERENCES -                                               *
*         NONE                                                        *
*                                                                     *
* EXITS, NORMAL -                                                     *
*         BR 14 WITH 0 IN REGISTER 15                                 *
*                                                                     *
* EXITS, ERROR -                                                      *
*         NONE                                                        *
*                                                                     *
* TABLES/WORK AREAS -                                                 *
*    CONTROL BLOCKS ARE DESCRIBED IN THE DSECTS AT END OF LISTING     *
*                                                                     *
* ATTRIBUTES -                                                        *
*    REENTRANT, REFRESHABLE, SUPERVISOR MODE, AND ENABLED             *
*                                                                     *
* CHARACTER CODE DEPENDENCY -                                         *
*    THIS MODULE IS ASSEMBLED IN EBCDIC AND MUST BE REASSEMBLED IF    *
*    A DIFFERENT CHARACTER SET IS TO BE USED DURING EXECUTION         *
* NOTES -                                                             *
*    YM03948 - QCBSATTI NOT SET ON FIRST PASS TRU CODE                *
*                                                                     *
***********************************************************************
         SPACE 3
RPARM0   EQU   0                        PARAMETER REG 0
RWORK0   EQU   0                        WORK REGISTER
RPARM1   EQU   1                        PARAMETER REG 1
RWORK1   EQU   1                        WORK REGISTER
RWORK2   EQU   2                        WORK REGISTER
RWORK3   EQU   3                        WORK REGISTER
RRB      EQU   4                        POINTS TO RB            YS02019
RTCB     EQU   4                        POINTS TO TCB
RTSB     EQU   5                        POINTS TO TSB           YS02019
RBASE    EQU   6                        BASE REGISTER           YS02019
RWORK4   EQU   9                        WORK REGISTER           YS02019
RCONST   EQU   10                       POINTS TO PROGRAM       YS02019
*                                       CONSTANTS               YS02019
R12      EQU   12                       INTERFACE REGISTER      YS02019
R13      EQU   13                       REGISTER 13
R14      EQU   14                       REGISTER 14
RLINK    EQU   14                       LINK REGISTER
R15      EQU   15                       REGISTER 15
RBRANCH  EQU   15                       BRANCH REGISTER
RCODE    EQU   15                       RETURN CODE REGISTER
         SPACE 2
********
******** DISPLACEMENT,LENGTH AND MASK EQUATES
********
BYTE1    EQU   8                        MASK FOR BYTE 1 IN REG.
BYTE2    EQU   4                        MASK FOR BYTE 2 IN REG.
BYTE3    EQU   2                        MASK FOR BYTE 3 IN REG.
BYTE4    EQU   1                        MASK FOR BYTE 4 IN REG.
BYTE34   EQU   3                        MASK FOR BYTES 3&4 IN REG.
BYTE234  EQU   7                        MASK FOR BYTES 2&3&4 IN REG.
D0       EQU   0                        DISPLACEMENT OF 0
D1       EQU   1                        DISPLACEMENT OF 1
D2       EQU   2                        DISPLACEMENT OF 2
D3       EQU   3                        DISPLACEMENT OF 3
D4       EQU   4                        DISPLACEMENT OF 4
D5       EQU   5                        DISPLACEMENT OF 5
D6       EQU   6                        DISPLACEMENT OF 6
D14      EQU   14                       DISPLACEMENT OF 14
L1       EQU   1                        LENGTH OF 1
L2       EQU   2                        LENGTH OF 2
L3       EQU   3                        LENGTH OF 3
L4       EQU   4                        LENGTH OF 4
L8       EQU   8                        LENGTH OF 8
L16      EQU   16                       LENGTH OF 16
L20      EQU   20                       LENGTH OF 20
L24      EQU   24                       LENGTH OF 24
L31      EQU   31                       LENGTH OF 31
L32      EQU   32                       LENGTH OF 32
L34      EQU   34                       LENGTH OF 34             A111
RETCODE4 EQU   4                        RETURN CODE OF 4
RETCODE8 EQU   8                        RETURN CODE OF 8
RETCODEC EQU   12                       RETURN CODE OF 12
HIPRI    EQU   X'E4'                    TCAM POSTING PRIORITY
COMPLETE EQU   X'40'                    TO TEST POST COMPLETE BIT
CZERO    EQU   0                        TO TEST FOR ZERO
CONE     EQU   1                        CONSTANT OF 1
CTWO     EQU   2                        CONSTANT OF 2
CTHREE   EQU   3                        CONSTANT OF 3
CFIFTEEN EQU   15                       CONSTANT OF 15
CSIXTEEN EQU   16                       CONSTANT OF 16
CFOX     EQU   X'FF'                    TO TEST FOR 'FF'
BLANK    EQU   X'40'                    TO TEST FOR BLANK
OFF      EQU   X'FF'                    TO TURN BITS OFF
NL       EQU   X'15'                    TERM. CTL. CHAR. - NEW LINE
CR       EQU   X'0D'                    TERM. CTL. CHAR. - CARRIAGE RET
LF       EQU   X'25'                    TERM. CTL. CHAR. - LINE FEED
HEX18    EQU   X'18'                    TERM. CTL. CHAR. - TWX CHAR.
HEX20    EQU   X'20'                    USED TO TEST BIT 2 OF BYTE
         SPACE 3
********
******** ESTABLISH BASE REGISTER AND ADDRESSABILITY
********
         USING *,RBASE                  BASE ADDRESSABILITY
         LR    RBASE,R12                LOAD BASE REGISTER      YS02019
         USING TCB,RTCB                 TCB ADDRESSABILITY
         USING TSB,RTSB                 TSB ADDRESSABILITY
         USING CONSTANT,RCONST          CONSTANT ADDRESSABILITY
         B     PASTID                   SKIP MODULE ID.         YS02019
         DC    C'IGG09408'              MODULE ID.              YS02019
         DC    X'4074'                  DATE - 03/15/74         YM08216
PASTID   EQU   *                                                YS02019
********
******** MOVE USER DATA TO SVRB EXTENDED SAVE AREA
********
         L     RRB,TCBRBP               LOCATE SVRB             YS02019
         USING RBSECT,RRB               ADDRESSABILITY          YS02019
         STCM  RPARM1,BYTE1,RBEXSAVE+D4 SAVE FLAG BYTE
         TM    RBEXSAVE+D4,HEX20        WAS INPUT ADDR. SPECIFIED
         BZ    MAIN                      NO
         CLM   RPARM1,BYTE234,ZERO      WAS INPUT=NO SPECIFIED
         BE    MAIN                     YES,BRANCH
         MVC   RBEXSAVE(L4),D0(RPARM1)  MOVE USER DATA
         LA    RPARM1,RBEXSAVE          ADJUST PARM POINTER
         ICM   RPARM1,BYTE1,RBEXSAVE+D4 RESTORE FLAG BYTE
MAIN     EQU   *
         ST    RLINK,RBXLINK            SAVE RETURN ADDRESS     YS02019
         MVC   RBXMASK,MASK             COPY TPOSTING MASK      YS02019
********
******** CHECK LINES PARAMETER
********
         SR    RWORK2,RWORK2            CLEAR WORK REG.
         LR    RWORK3,RPARM1            PUT FLAG BYTE IN WORK REG.
         SLDL  RWORK2,L1                GET FIRST FLAG BIT
         LR    RLINK,RPARM0             GET LINES & TENS PARMS   A111
         LA    RLINK,D0(,RLINK)         ZERO HIGH ORDER BYTE     A111
         SRDL  RLINK,L8                 ISOLATE LINES IN LOW     A111
*                                       ORDER BYTE AND TENS IN
*                                       HIGH ORDER BYTE OF NXT REG
         LTR   RWORK2,RWORK2            LINES PARM SPECIFIED
         BZ    CKTENS                   NO,CHECK TENS PARM
         TM    TSBSTAT,TSBDSPLY         IS THIS A 2260/2265
         BO    CKTENS                   YES,IGNORE LINES PARM
         OI    RBXMSKF2,QCBSATLC        WILL ALTER QCBSATLC     YS02019
         LTR   RLINK,RLINK              WAS LINES=NO SPECIFIED   A111
         BZ    NOLINES                  YES,BRANCH
         STC   RLINK,TSBATNLC+D1        NO,ST NEW ATN LINE COUNT A111
         OI    TSBFLG1,TSBANSR          TURN ON SIM. ATTN BIT IN TSB
         OI    RBXVALF2,QCBSATLC        TURN ON SIM. ATTN BY    YS02019
*                                       LINES                   YS02019
         B     CKTENS                   GO CHECK TENS PARM
NOLINES  EQU   *
         XC    TSBATNLC,TSBATNLC        CLEAR LINE COUNT FIELD
         NI    RBXAVLF2,OFF-QCBSATLC    TURN OFF SIM. ATTN IN   YS02019
*                                       QCB                     YS02019
********
******** CHECK TENS OF SECONDS PARAMETER
********
CKTENS   EQU   *
         SR    RWORK2,RWORK2            CLEAR LAST BIT
         SLDL  RWORK2,L1                GET FLAG BIT 2
         LTR   RWORK2,RWORK2            WAS TENS PARM SPECIFIED
         BZ    CKADDR                   NO,CHECK INPUT ADDR PARM
         SRL   RBRANCH,L24              ISOLATE TENS IN LOW ORDR A111
         LTR   RBRANCH,RBRANCH          TENS=NO SPECIFIED        A111
         BZ    NOTENS                   YES,BRANCH
         MH    RBRANCH,TEN              INTERVAL BY TEN          A111
         STH   RBRANCH,TSBATNTC         SET TENS COUNT IN TSB    A111
         OI    TSBFLG1,TSBANSR          SET SIM. ATTN IN TSB
         L     RBRANCH,TSBF2M           GET FIELDS TO UPDATE    YM03948
SWAP     EQU   *                                                YM03948
         LR    R14,RBRANCH              COPY FIELDS FOR SWAP    YM03948
         O     R14,SATTIM               TURN ON MASK AND VALUE  YM03948
         CS    RBRANCH,R14,TSBF2M       SWAP FIELDS FOR TPOST   YM03948
         BNE   SWAP                     IF FIELDS NOT EQUAL     YM03948
*                                       RESET REGS & TRY AGAIN  YM03948
         TM    TSBSTAT,TSBDSPLY         IS THIS A DISPLAY       YM08216
         BO    CKADDR                   YES, SKIP DUMMY TPUT    YM08216
         OI    TSBFLG3,TSBSPIT          PREVENT AUTOPROMPTING
         LR    RWORK4,RPARM1            SAVE ADDR PARM
         SETLOCK RELEASE,TYPE=CMS,REGS=USE,                     YS02019X
               RELATED=(TSB,IGC0009D(GETLKS))                   YS02019
         SETLOCK RELEASE,TYPE=LOCAL,REGS=USE,                   YS02019X
               RELATED=(TSB,IGC0009D(GETLKS))                   YS02019
         LA    RWORK0,L1                .ISSUE
         LA    RWORK1,IDLE              TPUT OF IDLE CHARACTER   A111
         O     RWORK1,TPUT2             .TO SYNCHRONIZE
         TPUT  (1),(0),R                .THE LINE
OBTAIN   EQU   *                                                YS02019
         SETLOCK OBTAIN,TYPE=LOCAL,MODE=UNCOND,REGS=USE,        YS02019X
               RELATED=(TSB,IGG09408(RELEASE))                  YS02019
         SETLOCK OBTAIN,TYPE=CMS,MODE=UNCOND,REGS=USE,          YS02019X
               RELATED=(TSB,IGG09408(RELEASE))                  YS02019
         L     R15,CVTPTR               GET CVT ADDR            YS02019
         L     R15,CVTAQAVT-CVT(,R15)   GET TCX ADDR            YS02019
         LTR   R15,R15                  IS TCAM STILL ACTIVE    YS02019
         BZ    RELEASE                  NO, EXIT                YS02019
         L     R15,TCXRPT-IEDQTCX(,R15) GET TIOCRPT ADDR        YS02019
         LTR   R15,R15                  IS TIME SHARING ACTIVE  YS02019
         BZ    RELEASE                  NO, EXIT                YS02019
         LR    RPARM1,RWORK4            RESTORE ADDR PARM
         NI    TSBFLG3,OFF-TSBSPIT      ALLOW AUTOPROMPTING     YS02019
         B     CKADDR                   NO,GO CHECK ADDR. PARM
NOTENS   EQU   *
         XC    TSBATNTC,TSBATNTC        CLEAR TIME COUNT FIELD
         OI    RBXMSKF2,QCBSATTI        WILL ALTER QCBSATTI     YM02882
         NI    RBXAVLF2,OFF-QCBSATTI    TURN OFF ATTN. BY TIME  YS02019
*                                       IN QCB                  YS02019
********
******** CHECK SIM. ATTN BY CHAR. STRING ADDRESS
********
CKADDR   EQU   *
         LR    RWORK3,RPARM1            RESET FLAG BYTE IN WORK REG.
         SLDL  RWORK2,L34               MOVE ADR BIT TO HI ORDR  A111
         SRL   RWORK2,L31               ISOLATE ADDR BIT & CLEAR A222
*                                       ALL OTHER BITS FOR SUBSE-
*                                       QUENT IC INSTRUCTION
         LTR   RWORK2,RWORK2            INPUT ADDR SPECIFIED
         BZ    RETURN                   NO ADDR SPECIFIED, RETRN A222
         OI    RBXMSKF2,QCBSATCH        WILL ALTER QCBSATCH     YS02019
         LA    RPARM1,D0(RPARM1)        CLEAR FLAG BYTE
         LTR   RPARM1,RPARM1            INPUT=NO
         BZ    NOINPUT                  YES,NO INPUT ADDR
         NI    TSBFLG2,OFF-TSBSATN1-TSBSATN2  RESET STRING LNGTH
         LA    RWORK3,D1(,RPARM1)        POINT TO 2ND BYTE OF STRING
         IC    RWORK2,TSBFLG2            PREPARE TO SET STRING LENGTH
         LA    RLINK,L1                 SET INCREMENT IN REGISTER
         LA    RBRANCH,D3(,RPARM1)      POINT TO END OF STRING
COMPARE  EQU   *
         CLI   D0(RWORK3),BLANK          IS THIS CHAR. A BLANK
         BE    MOVE                     YES,MOVE STRING
         ALR   RWORK2,RLINK              UPDATE STRING LENGTH COUNT
         BXLE  RWORK3,RLINK,COMPARE     CHECK NEXT CHARACTER
MOVE     EQU   *
         STC   RWORK2,TSBFLG2            RESTORE UPDATED TSBFLG2
         MVC   TSBATNCC,D0(RPARM1)      MOVE STRING TO TSB
         OI    TSBFLG1,TSBANSR          SET SIM ATTN BIT
         OI    RBXVALF2,QCBSATCH        SET SIM ATTN BY CHAR    YS02019
*                                       STRING                  YS02019
         B     RETURN                   RETURN TO CALLER
NOINPUT  EQU   *
         XC    TSBATNCC,TSBATNCC        CLEAR FIELD
         NI    RBXAVLF2,OFF-QCBSATCH    TURN OFF ATTN BY CHAR   YS02019
*                                       STRING                  YS02019
********
******** CALL POSTTSB SUBROUTINE TO TPOST THE TSB TO IEDAYP
*******  WHO WILL PERFORM THE REQUESTED BIT SETTING IN THE QCB
*******
*******        THE SUBROUTINE ALTERS REG.S 6 - 9
*******
RETURN   EQU   *
         NC    RBXMASK,RBXMASK          ANY NEED FOR TPOST TSB  YM02882
         BZ    RELEASE                  NO, RELEASE LOCKS       YM02882
*---------------------------------------------------------------------*
*   INPUT TO POSTTSB ROUTINE                                          *
*        REGISTER 1  = ADDRESS OF MASK FIELDS                         *
*        REGISTER 3  = CVT ADDRESS                                    *
*        REGISTER 5  = TSB ADDRESS                                    *
*        REGISTER 6-9 = WORK REGISTERS                                *
*---------------------------------------------------------------------*
         L     RWORK3,CVTPTR            ADDRESS OF CVT          YS02019
         LA    RPARM1,RBXMASK           ADDR OF TPOSTING MASK   YS02019
         L     R15,TPADDR               ADDR OF TPOSTING RTN    YS02019
         BALR  R14,R15                  GO TPOST TSB            YS02019
         DROP  RBASE                    ADDRESSABILITY ENDS     YS02019
***********************************************************************
*        RELEASE CMS AND LOCAL LOCKS                                  *
***********************************************************************
RELEASE  EQU   *                        SETLOCK LABEL           YS02019
         MODESET EXTKEY=SUPR            CHANGE TO KEY 0         YS02019
         SETLOCK RELEASE,TYPE=CMS,RELATED=(TSB,IGG09408(OBTAIN))YS02019
         SETLOCK RELEASE,TYPE=LOCAL,RELATED=(TSB,               YS02019,
               IGG09408(OBTAIN))                                YS02019
         L     RLINK,RBXLINK            RESTORE RETURN ADDRESS  YS02019
         SR    RCODE,RCODE              SET GOOD RETURN CODE    YS02019
         BR    RLINK                    RETURN                  YS02019
MASK     DS    0F                                               YS02019
         DC    AL1(TSBPOSTO,0,0,0)      .POST                   YS02019
         DC    F'-1'                    .FLAGS                  YS02019
         DC    F'0'                     .FOR                    YS02019
         DC    F'-1'                    .TPOSTING QCB TO IEDAYP YS02019
SATTIM   DC    AL1(QCBSATTI,QCBSATTI,0,0) MASK AND VALUE FIELDS YM03948
PATCH    DC    9F'0'                    MAINTENANCE PATCH AREA  YS02019
CONSTANT DSECT
ZERO     DC    F'0'                     CONSTANT OF 0
NINES    DC    F'99999999'              CONSTANT OF 99,999,999
FIFTEEN  DC    AL1(15)                  CONSTANT OF 15
TWO      DC    XL1'02'                  CONSTANT OF 2
TEN      DC    H'10'                    CONSTANT OF 10
SIXTEEN  DC    AL1(16)                  CONSTANT OF 16
ENABLE   DC    X'FF'                    TO ENABLE INTERRUPTS
SIX      DC    H'6'                     CONSTANT OF 6
EIGHT    DC    AL1(8)                   CONSTANT OF 8
CP5041   DC    XL3'026D16'              BKSP/UNDERSCORE CHAR. PROMPT
HIGHBIT  DC    XL1'80'                  TO TEST HIGH ORDER BITS
CPTWX    DC    XL3'024B0D'              PERIOD/CAR.RETURN CHAR. PROMPT
TPUT1    DC    XL4'0E000000'            TPUT,HOLD,CONTROL
TPUT2    DC    XL4'0A000000'            TPUT,CONTROL
ONE      DC    AL1(1)                   CONSTANT OF ONE
IDLE     DC    X'17'                    TPUT IDLE CHARACTER     YA00364
FOX      EQU   ENABLE                   CONSTANT OF 'FF'
DISABLE  EQU   ZERO                     TO DISABLE INTERRUPTS
TPADDR   DC    V(POSTTSB)               ADDR OF TPOSTING RTN    YS02019
TPMASK   DS    4A                       TPOSTING MASK           YS02019
         EJECT
CVT      DSECT                                                  YS02019
         CVT    ,                                               YS02019
         EJECT
         IHAPSA
         EJECT
         TQCBD
         EJECT
         IKJRB
********
******** MAP USAGE OF EXTENDED SAVE AREA
********
         ORG   RBEXSAVE                 SET ORIGIN              YS02019
         DS    2F                       PARAMETER WORK AREA     YS02019
RBXLINK  DS    A                        SAVE RETURN ADDR        YS02019
RBXMASK  DS    0CL16                    TPOSTING MASKS          YS02019
         DS    A                        OR MASK - WORD 1        YS02019
         DS    A                        AND MASK - WORD 1       YS02019
RBXMSKF2 DS    X                        OR MASK - QCBTSOF2      YS02019
RBXVALF2 DS    X                        OR VALUE - QCBTSOF2     YS02019
         DS    H                        REST OF OR MASK         YS02019
         DS    X                        AND MASK - QCBTSOF2     YS02019
RBXAVLF2 DS    X                        AND VALUE - QCBTSOF2    YS02019
         DS    H                        REST OF AND MASK        YS02019
         EJECT
         IKJTCB
         EJECT                                                  YS02019
         TTCXD                                                  YS02019
         EJECT
         IKJTSB
         END
