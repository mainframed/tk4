         TITLE 'IGG09402 - TS END'
IGG09402 CSECT
         COPY  LCGASMS2
         SPACE 3
***********************************************************************
*
*01* MODULE-NAME = IGG09402
*
*01* DESCRIPTIVE-NAME = TIME SHARING END
*
*01* COPYRIGHT = NONE
*
*01* STATUS = 00
*
*01* FUNCTION = FREE MAIN STORAGE FOR TIOC
*
*01* NOTES = N/A
*
*01* RESTRICTIONS = N/A
*
*02*   DEPENDENCIES = NONE
*
*02*   RESTRICTIONS = NONE
*
*02*   REGISTER-CONVENTIONS = SEE REGISTER EQUATES
*
*02*   PATCH-LABEL = TSENDEND
*
*01* MODULE-TYPE = ASSEMBLER LANGUAGE CSECT
*
*02*   PROCESSOR = ASSEMXF-370R
*
*02*   MODULE-SIZE = SEE EXTERNAL SYMBOL DICTIONARY TYPE DS ID-01
*
*02*   ATTRIBUTES = REENTRANT, REUSABLE, ENABLED
*
*01* ENTRY-POINT = IGG09402 (HOLDING THE LOCAL AND THE CMS LOCKS,
*                  IN SUPERVISOR'S KEY)
*
*02*   PURPOSE = SEE FUNCTION
*
*02*   LINKAGE = FROM IGC0009D VIA BRANCH
*
*01* INPUT = REGISTER 0 CONTAINS ENTRY CODE IN HIGH ORDER BYTE
*            REGISTER 1 CONTAINS ZERO
*            REGISTER 3 CONTAINS CVT ADDRESS
*            REGISTER 4 CONTAINS TCB ADDRESS
*            REGISTER 5 CONTAINS SVRB ADDRESS
*            REGISTER 14 CONTAINS RETURN ADDRESS
*
*01* OUTPUT = REGISTER 14 CONTAINS RETURN ADDRESS
*
*01* EXIT-NORMAL = GO BACK TO CALLER VIA BR 14
*
*01* EXIT-ERROR = NONE
*
*01* EXTERNAL REFERENCES = IGG09403
*
*02*   CONTROL BLOCKS = TCX, CVT, SCVT, SVC TABLE, TIOCRPT, TSB
*
*01* TABLES = NONE
*
*01* MACROS = FREEMAIN, MODESET, SETLOCK, WTO
*
*01* CHANGE-ACTIVITY
*        VS01246 -  05/30/73 (CHECK CALLERS KEY IN RB)
*
**********************************************************************
         SPACE 3
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
*              REGISTER EQUATES
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
R0       EQU   0                        WORK/PARAMETER REGISTER
R1       EQU   1                        WORK/PARAMETER REGISTER
RBASE    EQU   2                        BASE FOR THIS MODULE
RCVT     EQU   3                        BASE FOR CVT
R4       EQU   4                        WORK REGISTER
RRB      EQU   5                        BASE FOR RB
RTCX     EQU   6                        BASE FOR TCX
RWORK6   EQU   6                        WORK REGISTER           YM01246
RRPT     EQU   7                        BASE FOR TIOCRPT
RWORK8   EQU   8                        WORK REGISTER
RWORK9   EQU   9                        WORK REGISTER
RWORK10  EQU   10                       WORK REGISTER
RWORK11  EQU   11                       WORK REGISTER
RSVCT    EQU   12                       BASE FOR SVC TABLE
R13      EQU   13                       WORK REGISTER
R14      EQU   14                       CONTAINS RETURN ADDRESS
R15      EQU   15                       CONTAINS RETURN CODE
         SPACE 3
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
*              EQUATES FOR MASKS, DISPLACEMENTS, CONSTANTS
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
ZERO     EQU   0                        '0'
ONE      EQU   1                        '1'
FOUR     EQU   4                        '4'
E8       EQU   8                        '8'
E20      EQU   20                       '20'
E28      EQU   28                       '28'
TIMES8   EQU   3                        MUSK FOR MULTIPLY BY 8 W/SLL
QTIP     EQU   101                      SVC NUMBER FOR QTIP
TIOCSP   EQU   231                      SUBPOOL NUMBER FOR TIOC STORAGE
HEX80    EQU   X'80'                    HIGH ORDER BIT IN A BYTE
BADCALLR EQU   4                        RC FOR INVALID CALLER
TSNOTUP  EQU   8                        RC FOR TS ALREADY ENDED
         SPACE 3
         BALR  RBASE,R0                 ESTABLISH BASE
         USING *,RBASE                  MODULE ADDRESSABLE
         USING CVT,RCVT                 CVT ADDRESSABLE
         USING RBBASIC,RRB              RB ADDRESSABLE
         USING TIOCRPT,RRPT             TIOCRPT ADDRESSABLE
         SPACE 3
         B     VALIDITY                 BRANCH AROUND DATE/ID
         DC    C'IGG09402'              MODULE ID
         DC    X'3150'                  JULIAN DATE 05/30/73
         SPACE 3
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
*              VALIDITY CHECK
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
VALIDITY EQU   *
         LR    R4,R14                   SAVE RETURN ADDRESS
         LTR   R1,R1                    ANY PARAMETER PASSED
         BNZ   INVALID                  SHOULD NOT HAVE ANY
         L     RWORK6,RBLINK            CALLERS RB              YM01246
         DROP  RRB                      RB ADDRESSABILITY ENDS  YM01246
         USING RBBASIC,RWORK6           CALLERS RB ADDR         YM01246
         TM    RBOPSW+ONE,HEX80         IS CALL MADE BY SYSTEM  YM01246
         DROP  RWORK6                   RB ADDRESSABILITY ENDS  YM01246
         USING IEDQTCX,RTCX             TCX ADDRESSABLE         YM01246
         BO    INVALID                  NO, CALLER IS INVALID
         SPACE 3
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
*              ASSUME TCAM'S KEY TO ZERO OUT TCXRPT AND TCXTSI
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
         L     RTCX,CVTAQAVT            GET ADDRESS OF TCX
         MODESET EXTKEY=TCAM            CHANGE INTO TCAM'S KEY
         L     RRPT,TCXRPT              SAVE TIOCRPT ADDRESS
         LTR   RRPT,RRPT                IS TIME SHARING UP
         BZ    TSSTOPED                 NO, GIVE BAD RETURN CODE
         SR    RWORK11,RWORK11          CLEAR REGISTER
         ST    RWORK11,TCXRPT           CLEAR RPT POINTER IN TCX
         ST    RWORK11,TCXTSI           CLEAR TSINPUT'S QCB ADDRESS
         MODESET EXTKEY=SUPR            CHANGE BACK INTO KEY 0
         SPACE 3
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
*              PUT THE ADDRESS OF CVTBRET IN QTIP SLOT
*              IN SVC TABLE
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
         L     RSVCT,CVTABEND           GET SECONDARY CVT ADDRESS
         USING SCVTSECT,RSVCT           ADDRESSABILITY FOR SEC'DARY CVT
         L     RSVCT,SCVTSVCT           GET SVC TABLE ADDRESS
         DROP  RSVCT                    DROP SEC'DARY CVT BASE
         LA    RWORK9,QTIP              GET QTIP SVC NUMBER
         SLL   RWORK9,TIMES8            MULTIPLY BY SIZE OF EACH ENTRY
         LA    RWORK10,CVTBRET          GET ADDRESS OF CVTBRET
         SPACE 1
         ST    RWORK10,ZERO(RWORK9,RSVCT)  PUT IT IN SVC TABLE
         SPACE 1
FREECMS  EQU   *
         SETLOCK RELEASE,TYPE=CMS,                  * FREE CMS         X
               RELATED=(TIOCRPT,IGC0009D)           * LOCK
         SPACE 1
FREELCL  EQU   *
         SETLOCK RELEASE,TYPE=LOCAL,                * FREE LOCAL       X
               RELATED=(TIOCRPT,IGC0009D)           * LOCK
         SPACE 1
         MODESET EXTKEY=TCAM            GET INTO TCAM'S KEY
         SPACE 3
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
*              FREE TIOC STORAGE
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
         L     RWORK11,ONEPAGE          SIZE OF ONE PAGE
         LA    RWORK10,TIOCFBFL+L'TIOCFBFL  POINT TO 2ND ENTRY
*                                           IN FREE BUFFER LIST
         LH    RWORK9,TIOCNBFL          GET NUMBER OF BUFFER LISTS
*
*  FIRST SECTION OF BUFFERS IS FOLLOWED BY TIOCRPT AND TSB TABLE
*
NEXTPAGE EQU   *
         BCT   RWORK9,FREEBFPG          IS THIS THE FIRST LIST
         L     R1,TIOCFBFL              YES, GET ADDR OF IT
         LPR   R1,R1                    MAKE SURE ADDRESS IS POSITIVE
         N     R1,PAGEADDR              ONLY WANT PAGE ADDRESS
         SPACE 1
*
*  COMPUTE SIZE OF TIOCRPT AND TSB TABLE
*
         SR    RWORK10,RWORK10          CLEAR REGISTER
         IC    RWORK10,TIOCTSBS         GET TSB SIZE
         LH    RWORK11,TIOCNTSB         GET NUMBER OF TSB'S
         MR    RWORK10,RWORK10          SIZE OF TSB TABLE IN RWORK11
         L     RWORK10,TIOCTSB-ONE      GET BEGINNING OF TSB TABLE
         LA    RWORK10,ZERO(,RWORK10)   CLEAR HIGH ORDER BYTE
         SR    RWORK10,R1               GET SIZE OF RPT AND BUFFERS
         AR    RWORK11,RWORK10          GET SIZE OF BFRS/RPT/TSB'S
         B     FREE                     GO TO FREE STORAGE
         SPACE 1
FREEBFPG EQU   *
         L     R1,ZERO(,RWORK10)        GET ADDRESS OF BUFFER PAGE
         LPR   R1,R1                    MAKE SURE IT IS POSITIVE
         N     R1,PAGEADDR              ONLY WANT PAGE ADDRESS
         SPACE 1
FREE     EQU   *
         FREEMAIN R,LV=(11),A=(1),SP=TIOCSP  FREE STORAGE
         SPACE 1
         LTR   RWORK9,RWORK9            WAS ALL TIOC STORAGE FREED
         BZ    LASTMSG                  YES, SEND OUT LAST MESSAGE
         LA    RWORK10,L'TIOCFBFL(,RWORK10) POINT TO NEXT ENTRY IN
*                                           FREE BUFFER LIST
         B     NEXTPAGE                 GO TO DETERMINE SIZE TO FREE
         SPACE 3
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
*              WTO TO STATE TIME SHARING ENDED
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
LASTMSG  EQU   *
         L     R1,MSGADDR               GET MSG LIST ADDRESS
         WTO   MF=(E,(1))               INFORM OPERATOR TS HAS ENDED
         SPACE 1
         SR    R15,R15                  ZERO RETURN CODE
         SPACE 1
EXIT     EQU   *
         MODESET EXTKEY=SUPR            CHANGE INTO SUPERVISOR'S KEY
         BR    R4                       RETURN TO CALLER
         SPACE 1
INVALID  EQU   *
         LA    R15,BADCALLR             RETURN CODE FOR INVALID CALLER
         BR    R4                       RETURN TO CALLER
         SPACE 1
TSSTOPED EQU   *
         LA    R15,TSNOTUP              GIVE BAD RETURN CODE
         MODESET EXTKEY=SUPR            CHANGE INTO SUPERVISOR'S KEY
         BR    R4                       RETURN TO CALLER
         SPACE 1
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
*              CONSTANTS
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
ONEPAGE  DC    F'4096'                  SIZE OF ONE PAGE
PAGEADDR DC    X'00FFF000'              USED TO GET JUST PAGE ADDRESS
MSGADDR  DC    V(MSGLIST)               ADDRESS OF MESSAGE LIST FOR WTO
TSENDEND DC    10F'0'                   PATCH AREA
         SPACE 3
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
*              DSECT MACROS
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
CVT      DSECT
         CVT
         SPACE 3
         IHAPSA
         SPACE 3
         TTCXD
         SPACE 3
         IKJTIOCP
         SPACE 3
         IHASCVT
         SPACE 3
         IKJRB
         END
