         TITLE 'IGG09302 - SECOND LOAD OF TPUT WITH TJID'
IGG09302 CSECT
         SPACE 3                                                YS02019
*
*         VS2   030   SU
*
*0000275510                                                    @ZA05569
*
*
*                   RELEASE 3, VS/2, CHANGES                          *
*D002200                                                        ZA00968
*C202700                                                        ZA00968
*                   RELEASE 2, VS/2, DELETIONS                        *
*                                       03/24/73                YM01564
*                                       05/30/73                YM01246
*                                       07/03/73                YM01114
*                                       07/16/73                YM01176
*                                       10/11/73                YM03998
*                                       11/15/73                YM05387
*                                       04/11/74                YM08378
*     THIS MODULE HAS BEEN COMPLETELY RESEQUENCED FOR VS2-2     YS02019
*                                                                     *
*                   RELEASE 1, VS/2, DELETIONS                        *
*                                                                     *
*580260-580740,217021                                            YM0895
*1301554000                                                      A44943
*1301612550                                                      S22028
*1301516000                                                      A45242
*1301                                                            M0126
*1301035000-046000,073000-112000,191000-192400,203000-209000,    M0094
*1301217010-217020,217080,217130-217140,217220,219200-219300,    M0094
*1301223000,238500,241000,284000,290000,292000,307000-310000,    M0094
*1301314000,343000,350000,361400-363200,401000,406000-409000,    M0094
*1301422000,428000,470400-475200,499000,503000,508000,577300,    M0094
*1301579540-579720                                               M0094
*                                                                M2152
*0000927000-944000                                               M0019
*0000246000,252000-255400,256000-297000,303000,339000,340000-    TS1634
*0000346000,356000,375000-412000,463000,478000-498000,504000,    TS1634
*0000546000,555000-579000,586500                                 TS1634
*                                                                M0661
*0000258000-259000,569000                                        TS0034
*                                                                 M2677
*                                                                M3391
*                                                                M4022
*                                                                M0048
*                                                                 M5113
*                                                                 M4415
*                                                                M4696
*                                                                M6024
*1301                                                            S21A12
*1301061500,361000,363000,471000,475000                          S21008
* STATUS -                                                            *
*    CHANGE LEVEL VS2  030  SU                                 @ZA05569
*                                                                     *
* FUNCTION -                                                          *
*                                                                     *
*    PUT OUTPUT FROM USER'S BUFFER INTO TSO BUFFERS FOR LATER         *
*    TRANSFERENCE, VIA TCAM, TO A TERMINAL NOT CONNECT TO THIS TASK.  *
*                                                                     *
*    PROVIDE LIMITED EDITING OPTIONS                                  *
*                                                                     *
* ENTRY POINTS -                                                      *
*                                                                     *
*    IGG09302 - ENTRY FROM FIRST LOAD OF TERMINAL I/O SVC             *
*    ROUTINES (IGC0009C).                                             *
*                                                                     *
* INPUT -                                                             *
*                                                                     *
***********************************************************************
***                                                                 ***
***                 REGISTER CONTENTS UPON ENTRY                    ***
***                                                                 ***
***      3 - CVT POINTER      8 - ASCB OF CALLER  15 - USERID PTR   ***
***      4 - TCB POINTER      9 - TSB OF CALLER       (IF USERID    ***
***      5 - SVRB POINTER    10 - TIOCRPT POINTER      TPUT)        ***
***                                                                 ***
***                                                                 ***
***********************************************************************
*                                                                     *
* OUTPUT                                                              *
*                                                                     *
*    RETURN CODE IN REG 15                                            *
*              00 - SUCCESSFUL                                        *
*              04 - NOWAIT SPECIFIED, AND TSB WAS IN USE              *
*              08 - ATTN OCCURED DURING SVC EXECUTION                 *
*              OC - NO INTER-TERMINAL COMMUNICATION ALLOWED           *
*              14 - CALLED TASK IS NO LONGER ACTIVE                   *
*                                                                     *
* EXTERNAL REFERENCES -                                               *
*                                                                     *
*              MODESET                                          YS02019
*              SETLOCK                                          YS02019
*              OPTIMIZER                                        YS02019
*              TESTAUTH                                         YS02019
*              QTIP                                                   *
*              ENQ/DEQ                                                *
*                                                                     *
* EXITS,NORMAL -                                                      *
*                                                                     *
*    RETURN TO SUPERVISOR VIA BR14 (RETURN ADDRESS IS SAVED IN        *
*    XSARETRG BY IGC0009C)                                            *
*                                                                     *
* EXITS,ERROR -                                                       *
*                                                                     *
*    THERE ARE NO ERROR EXITS FROM THIS ROUTINE.                      *
*                                                                     *
* TABLES/WORK AREAS -                                                 *
*                                                                     *
*    THE FORMAT AND CONTENTS OF THE TABLES USED ARE                   *
*    DESCRIBED BY THE DSECTS AT THE END OF THE LISTING                *
*                                                                     *
* ATTRIBUTES -                                                        *
*                                                                     *
*    REENTRANT, PRIVILEGED, REFRESHABLE                               *
*    HOLDS THE LOCAL LOCK AT ENTRY AND GETS CMS LOCK EXPLICITLY       *
*                                                                     *
* CHARACTER CODE DEPENDENCY -                                         *
*                                                                     *
*    THE OPERATION OF THIS MODULE DEPENDS UPON THE FOLLOWING          *
*    PROPERTIES OF THE INTERNAL REPRESENTATION OF THE EXTERNAL        *
*    CHARACTER SET- BACKSPACE EQUAL TO X'16', BLANK EQUAL TO          *
*    X'40', NEW LINE EQUAL TO X'15'.                                  *
*    THE SPECIFIC INSTRUCTIONS WHICH REQUIRE MODIFICATION IF          *
*    THESE PROPERTIES OF THE CHARACTER SET ARE CHANGED MAY BE         *
*    IDENTIFIED BY BLNKCHAR, BKSPCHAR, AND NEWLINE, RESPECTIVELY.     *
*                                                                     *
* NOTES -                                                             *
*                                                                     *
*    NONE                                                             *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
******                                                            *****
******              REGISTER EQUATES                              *****
******                                                            *****
***********************************************************************
       SPACE
R0       EQU   0                        REG USED TO SPEED ASM
RP0      EQU   0                        FOR TPOST SUBROUTINE    YS02019
RSAVE    EQU   0                        WORK REGISTER
RWRK2    EQU   1                        WORK REGISTER
RP1      EQU   1                        FOR TPOSTING SUBROUTINE YS02019
RBUF     EQU   2                        POINTER TO TS BUFFER
RCVT     EQU   3                        POINTER TO CVT
RTCB     EQU   4                        POINTER TO TCB
RSVRB    EQU   5                        POINTER TO SVRB
RBASE    EQU   6                        BASE REGISTER
RSAVE2   EQU   7                        SAVE REGISTER FOR TJID
RASCB    EQU   8                        POINTER TO ASCB         YS02019
RTSB     EQU   9                        POINTER TO TSB
RRPT     EQU   10                       POINTER TO TIOCRPT
RTCX     EQU   11                       POINTER TO TCAM CVT EXT YS02019
RTJID    EQU   12                       TJID
RAVT     EQU   13                       POINTER TO AVT
RETURN   EQU   14                       RETURN ADDRESS
RWRK     EQU   14                       WORK REGISTER
RWRK1    EQU   15                       WORK REGISTER
RCODE    EQU   15                       RETURN CODE
RGOTO    EQU   15                       BRANCH ADDRESS
         SPACE 2
***
***                 CONSTANTS
***
K0       EQU   0                        OFFSET
K1       EQU   1                        OFFSET AND SIZE
K3       EQU   3                        OFFSET AND SIZE
K12      EQU   12                       SIZE OF QNAME FOR ENQ-DEQ
TIMES4   EQU   2                        SHIFT CONSTANT          YS02019
         SPACE 2
***
***                 RETURN CODES
***
RCNOWT   EQU   X'04'                    NOWAIT SPEC'D AND TSB BUSY
RCNOIT   EQU   X'0C'                    NO INTER-TERM ALLOWED
RCGONE   EQU   X'14'                    CALLED TASK NO LONGER
*                                       ACTIVE
         SPACE 2
***
***                 MASKS
***
OFF      EQU   X'FF'                    USED TO TURN OFF BITS
NDISPBIT EQU   X'400'                   NONDISPATCHABILITY MASK YS02019
SEVEN    EQU   7                        MASK USED IN STMC        Y01018
STEPSD   EQU   8                        ENTRY CODE FOR STATUS   YS02019
         SPACE 2
***
***                 OTHER EQUATES
***
MOVECODE EQU   19                       QTIP ENTRY CODE FOR MOVE
BKSPCHAR EQU   X'16'                    BACKSPACE
BLNKCHAR EQU   X'40'                    BLANK
         EJECT                                                  YS02019
***********************************************************************
***                                                             YS02019
***            THIS MODULE IS BRANCH ENTERED FROM IGC0009C      YS02019
***            HOLDING THE LOCAL LOCK                           YS02019
***                                                             YS02019
***********************************************************************
         SPACE 3
***
***                ESTABLISH ADDRESSABILITY AND
***                INITIALIZE EXTENDED SAVE AREA FIELDS
***
         BALR  RBASE,R0                 SET ADDRESSABILITY
         USING *,RBASE
         USING TCB,RTCB
         USING TIOCRPT,RRPT
         USING RBSECT,RSVRB                                      M0094
         B     INIT                     AROUND I.D.             YS02019
         DC    C'IGG09302'              MODULE I.D.             YS02019
         DC    X'5237'                  DATE - 08/25/75        @ZA05569
INIT     DS    0H                       BRANCH TARGET           YS02019
         MVC   XSAF,XSAFLAG             INITIALIZE FLAGS         M0094
         MVC   XSAUBFRS,XSAPRMSZ        ASSUME NO TRAILING       Y01018
*                                       BLANKS                   Y01018
         MVC   XSAUBFRP+K1(K3),XSAPRM1+K1 SET UP USER BFR PTR    Y01018
         STH   RTJID,XSATJID            SAVE CALLER'S ASID      YS02019
         SPACE 3                                                YS02019
***********************************************************************
*                                                                     *
*                   LOCATE DESTINATION TSB, ASCB, AND CSCB            *
*                                                                     *
***********************************************************************
         SR    RBUF,RBUF                                        YS02019
         IC    RBUF,TIOCTSBS            INCREMENT AMOUNT=SIZE   YS02019
         LR    RWRK,RBUF                FOR MULTIPLICATION      YS02019
         LH    RWRK1,TIOCNTSB           GET NO. OF TSBS         YS02019
         BCTR  RWRK1,R0                 SUBTRACT ONE            YS02019
         MR    RWRK,RWRK                GET OFFSET OF LAST TSB  YS02019
         LR    RCVT,RWRK1               CHANGE REGS FOR BXLE    YS02019
         L     RTSB,TIOCTSB-K1          GET ORIGIN OF TABLE     YS02019
         LA    RTSB,K0(,RTSB)           CLEAR HIGH ORDER BYTE   YS02019
         USING TSB,RTSB                                         YS02019
         AR    RCVT,RTSB                ADD TO OFFSET           YS02019
         SPACE 2                                                YS02019
***********************************************************************
*                                                                     *
*                   REGISTERS 2, 3, AND RTSB ARE SET FOR A BXLE LOOP  *
*                   TO SCAN THE TSB TABLE -                           *
*                    RBUF (2), INCREMENT, HAS THE TSB SIZE,           *
*                    RCVT (3), COMPARAND, POINTS TO THE LAST TSB IN   *
*                      THE TSB TABLE, AND                             *
*                    RTSB, INDEX, POINTS TO THE FIRST TSB IN THE TSB  *
*                      TABLE                                          *
*                                                                     *
*                   NOW, THE TSB'S, AND ASSOCIATED ASCB'S (AND CSCB'S)*
*                   WILL BE SCANNED FOR A MATCH ON THE ASID OR        *
*                   THE USERID.                                       *
*                                                                     *
*                  THE ASVT CANNOT BE USED, BECAUSE IT MUST BE        *
*                  STABILIZED WITH THE DISPATCHER LOCK, AND HOLDERS   *
*                  OF THIS LOCK MUST BE FIXED AND DISABLED.           *
*                                                                     *
***********************************************************************
         SPACE 3
***
***                NEED CMS LOCK FOR TSB                        YS02019
***
         SETLOCK OBTAIN,TYPE=CMS,MODE=UNCOND,                   YS02019-
               RELATED=(TSB,IGG09302(TPT0100))                  YS02019
***
***                 MAKE SURE TIME SHARING IS STILL ACTIVE
***
         L     RRPT,CVTPTR              GET CVT POINTER         YS02019
         L     RRPT,CVTAQAVT-CVT(,RRPT) GET TCX POINTER         YS02019
         LTR   RRPT,RRPT                IS TCAM UP              YM01564
         BZ    TPT7500                  NO, ABORT THIS TPUT     YM01564
         L     RRPT,TCXRPT-IEDQTCX(,RRPT) GET RPT POINTER       YS02019
         LTR   RRPT,RRPT                CHECK FOR ZERO          YS02019
         BZ    TPT7500                  ZERO, ABORT THIS TPUT   YS02019
         SPACE 1                                                YS02019
         TM    XSAOPTNS,MUSERP          WAS USERID SPECIFIED    YS02019
         BO    TPT0020                  YES, GO LOOK FOR IT     YS02019
         SPACE 2                                                YS02019
***                                                             YS02019
***                 SCAN FOR ASID MATCH                         YS02019
***                                                             YS02019
         LH    RSAVE,XSAPRMTJ           GET SPECIFIED ASID      YS02019
TPT0010  EQU   *                                                YS02019
         L     RASCB,TSBASCBA           GET ASCB POINTER        YS02019
         LTR   RASCB,RASCB              IS THERE ONE            YS02019
         BZ    TPT0015                  NO, CONTINUE SCAN       YS02019
         USING ASCB,RASCB                                       YS02019
         CH    RSAVE,ASCBASID           CHECK ASID              YS02019
         BE    TPT0027                  IF SAME, THIS IS IT     YS02019
TPT0015  EQU   *                                                YS02019
         BXLE  RTSB,RBUF,TPT0010        CONTINUE SCAN           YS02019
         B     TPT7500                  GO GIVE 'NOT HERE' CODE YS02019
         SPACE 2                                                YS02019
***                                                             YS02019
***                 SCAN FOR USERID MATCH                       YS02019
***                                                             YS02019
TPT0020  EQU   *                                                YS02019
         L     RWRK1,XSAUSERP           GET PTR TO USER ID
TPT0021  EQU   *                                                YS02019
         L     RASCB,TSBASCBA           GET ASCB POINTER        YS02019
         LTR   RASCB,RASCB              IS THERE ONE            YS02019
         BZ    TPT0022                  NO, GET NEXT            YS02019
         L     RWRK,ASCBCSCB            GET CSCB POINTER        YS02019
         USING CSCB,RWRK                                        YS02019
         CLC   CHKEY,0(RWRK1)           CHECK USERID            YS02019
         BNE   TPT0022                  NOT THIS TSB            YS02019
***
***   WHEN A MATCHING USERID IS FOUND, SAVE ASID IN TPUT WORK AREA
***
         LH    RWRK1,ASCBASID           GET DESTINATION ASID    YS02019
         STH   RWRK1,XSAPRMTJ           SAVE IT IN WORK AREA    YS02019
         B     TPT0027                  GO TO PROCESS IT        YS02019
         SPACE 1
TPT0022  EQU   *                                                YS02019
         BXLE  RTSB,RBUF,TPT0021        LOOK AT EACH TSB        YS02019
         B     TPT7500                  GO GIVE 'NOT HERE' CODE YS02019
*                                       AFTER ALL TSB'S CHECKED YS02019
         DROP  RWRK                                             YS02019
         SPACE 2                                                YS02019
TPT0027  EQU   *                                                YS02019
***                                                             YS02019
***   RTSB = DESTINATION TSB, RASCB = DESTINATION ASCB          YS02019
***                                                             YS02019
         SETLOCK RELEASE,TYPE=CMS,RELATED=('BEFORE SVC')        YS02019
         SETLOCK RELEASE,TYPE=LOCAL,RELATED=('BEFORE SVC')      YS02019
***
***                TEST AUTHORIZATION OF USER.  '+' SIGN AND CHECK FOR
***                'HIPRI' DEPEND ON THIS
***
         TESTAUTH FCTN=1,KEY=YES,STATE=YES,RBLEVEL=2,BRANCH=YES YS02019
         L     RCVT,CVTPTR              RESTORE REG 3           YS02019
         USING CVT,RCVT                                         YS02019
         LTR   RCODE,RCODE              TEST RETURN CODE        YS02019
         BNZ   TPT0028                  SKIP BIT IF NOT ZERO    YS02019
         OI    XSAF,XSAAUTH             REMEMBER RETURN CODE    YS02019
         SPACE 2                                                YS02019
TPT0028  EQU   *                                                YS02019
***********************************************************************
****                                                               ****
****                SEE IF NOWAIT OPTION CAN PROCEED               ****
****                                                               ****
***********************************************************************
         TM    XSAOPTNS,MWAIT           NOWAIT SPECIFIED         M0094
         BNO   TPT0075                  NO, PROCEED WITH TPUT   YS02019
***
***                 TEST AVAILABILITY OF ENQUEUED RESOURCES
***
         MVC   XSAENQNM(K12),QNAME      INIT ENQ/DEQ PARMS       YM0895
         LA    RWRK2,XSAENQAD           GET PT TO ENQ WORK AREA  M0094
         ENQ   (XSAENQNM,ASCBASID,E,2,SYSTEM),RET=USE,MF=(E,(1))
         SETLOCK OBTAIN,TYPE=LOCAL,MODE=UNCOND,                 YM03998*
               RELATED=(TSB,IGG09302(TGT9600))                  YM03998
         LTR   RCODE,RCODE              WAS CONTROL ASSIGNED    YM03998
*                                       FROM ENQUEUE            YM03998
         BNZ   TPT0070                  IF NOT, RETURN
         OI    XSAF,XSAIDENQ            INDICATE TO ESTAE EXIT  YS02019
*                                       ROUTINE ASID TPUT ENQ'D YS02019
***
***   CALCULATE NUMBER OF BUFFERS REQUIRED TO HOLD MESSAGE,
***   ASSUMING NO TRAILING BLANKS
***   NUMBER=(MSGLEN+(HDRSIZE-TRLSIZE))/(BUFSIZE-TRLRSIZE)
***
         SPACE 1                                                 Y01018
         SR    RWRK,RWRK                                         Y01018
         LH    RWRK1,XSAPRMSZ           GET LENGTH OF MSG        Y01018
         LA    RWRK1,BUFFHDLN-BUFFTRLN(,RWRK1) ADD EXTRA FOR     Y01018
*                                       HEADER PREFIX
         LA    R0,BUFFTRLN              GET LENGTH OF TRLR PREF  Y01018
         SH    R0,TIOCBFSZ              WHAT'S LEFT OVER (COMP)  Y01018
         LPR   R0,R0                    GET BUFSIZE - TRLR PREF  Y01018
***   R0 = AMOUNT OF ROOM IN EACH BUFFER FOR REAL DATA
***   R14 + 15 = AMOUNT OF DATA THAT MUST GO INTO BUFFERS
         DR    RWRK,R0                  GET NUMBER OF BUFFERS    Y01018
*                                       REQ                      Y01018
         LA    RWRK1,K1(,RWRK1)         ADD 1 FOR POSSIBLE       Y01018
*                                       REMAINDER                Y01018
         SPACE
TPT0030  EQU    *
***
***                 RWRK1 HAS NUMBER OF BUFFERS MESSAGE WILL NEED.
***                 TPUT NOWAIT CAN PROCEED IF
***                    1.) RWRK1 LTE TIOCNFBF - TIOCUSLW, AND
***                    2.) RWRK1 + TSBNOBF LTE TIOCAOMX OR TIOCAOMX * 3
***                       IF THE MONITOR SUBCOMMAND IS ACTIVE
***                       (TSBGETBF NE 0).
***
         SETLOCK OBTAIN,TYPE=CMS,MODE=UNCOND,                   YS02019*
               RELATED=(TSB,IGG09302(TPT9700))                  YS02019
         TM    TSBFLG1,TSBOWIP          IS A TPUT IN PROGRESS
         BNZ   TPT0072                  YES, CAN'T DO IT        YM03998
         LH    RWRK2,TIOCNFBF           NO. FREE BFRS
         SH    RWRK2,TIOCUSLW           MINUS THRESHOLD
         CR    RWRK2,RWRK1              COMPARE WITH BFRS NEEDED
         BL    TPT0072                  NEEDED GT AVAILABLE     YM03998
*                                       = BRANCH                YM03998
***
***                 ADEQUATE BFRS AVAILABLE.  CHECK AOMX.
***
         LH    RWRK2,TIOCAOMX           GET MAX VALUE
         TM    TSBFLG4,TSBGETBF         MONITOR SUBCMD ACTIVE   YS02019
         BZ    TPT0040                  NO, DON'T INCREASE AOMX
         AR    RWRK2,RWRK2              DOUBLE                   Y01018
         AH    RWRK2,TIOCAOMX           ADD 1 TO MAKE *3
TPT0040  EQU   *
         SR    RBUF,RBUF
         IC    RBUF,TSBNOBF             GET NO. ALREADY ON OUTPUT QUEUE
         SR    RWRK2,RBUF               SUBTRACT FROM NUMBER ALLOWED
         CR    RWRK2,RWRK1              COMPARE NEEDED WITH ALLOWED
         BNL   TPT0080                  ALLOWED GTE NEEDED, SO  YS02019
*                                       PROCEED WITH TPUT       YS02019
         B     TPT0072                  RELEASE LOCKS          @ZA05569
         SPACE 1
TPT0070  EQU   *
***
***                 CAN'T PUT OUT MESSAGE WITHOUT WAITING.
***                 RETURN WITH CODE = 4.
***
         LA    RCODE,RCNOWT             SET CODE
         B     TPT9650                  CLEAN UP AND RETURN
         SPACE 1                                                YM03998
TPT0072  EQU   *                                                YM03998
         LA    RCODE,RCNOWT             SET RETURN CODE         YM03998
         B     TPT9625                  FREE LOCKS AND RETURN   YM03998
         SPACE 1                                                 Y01018
TPT0075  EQU   *
         SPACE 2                                                 TS1634
***********************************************************************
****                                                               ****
****                ENQ ON ASID, SINCE ONLY 1 TPUT AT A TIME       ****
****                CAN USE TSB                                    ****
****                                                               ****
***********************************************************************
         SPACE
         MVC   XSAENQNM(K12),QNAME      INIT ENQ/DEQ PARMS       YM0895
         LA    RWRK2,XSAENQAD           GET PT TO ENQ WORK AREA  M0094
         ENQ   (XSAENQNM,ASCBASID,E,2,SYSTEM),MF=(E,(1))        YS02019
         OI    XSAF,XSAIDENQ            INDICATE TO ESTAE EXIT  YS02019
*                                       ROUTINE ASID TPUT ENQ'D YS02019
         SETLOCK OBTAIN,TYPE=LOCAL,MODE=UNCOND,                 YS02019*
               RELATED=(TSB,IGG09302(TPT0100))                  YS02019
         SETLOCK OBTAIN,TYPE=CMS,MODE=UNCOND,                   YS02019*
               RELATED=(TSB,IGG09302(TPT0100))
TPT0080  EQU   *                                                YS02019
***                                                             YS02019
***                 INITIALIZATION
***
         SPACE 2                                                 TS1634
         BAL   RBUF,TPT6000             CHECK STATUS             TS0034
         LH    RTJID,XSATJID            GET ASID FROM WHERE 9C  YS02019
*                                       SAVED IT                YS02019
         SR    RSAVE2,RSAVE2
         LR    RCODE,RSAVE2             SET RETURN CODE TO 0
         CH    RSAVE2,XSAPRMSZ          IS MESSAGE LENGTH ZERO   M0094
         BE    TPT9625                  GET OUT                 YM03998
         SPACE 2
         TM    TSBSTAT,TSBITOFF         IS INTER-TERMINAL
*                                       COMMUNICATION ALLOWED
         BZ    TPT0100                  IF ALLOWED
         LA    RCODE,RCNOIT             RETURN CODE
         TM    XSAF,XSAAUTH             IS CALLER AUTHORIZED    YS02019
         BNO   TPT9625                  NO, DON'T OVERRIDE      YM03998
         TM    XSAOPTNS,MLOWP           IF AUTHORIZED, CHECK PRTY M0094
*                                       LEVEL                    S21A12
         BO    TPT9625                  IF LOWP, ABORT TPUT     YM03998
         SPACE 2
TPT0100  EQU   *
         TM    TSBFLG1,TSBOWIP          IS NORMAL TPUT GOING     TS1634
         BNO   TPT0350                  GO AHEAD IF NOT          TS1634
***
***                 WAIT ON TSBECB FOR NORMAL TPUT TO COMPLETE
***
         MVI   TSBECB,K0                RESET WAIT+POST BITS     TS1634
         STH   RTJID,TSBWTJID           SHOW WHO'S WAITING       TS1634
         STCM  RTCB,SEVEN,TSBWTCB       STORE WAITING TCB ADDR   Y01018
         OI    TSBFLG3,TSBAWOIP         SHOW WE ARE WAITING     YS02019
         SETLOCK RELEASE,TYPE=CMS,RELATED=(TSB,IGG09302(TPT0100))
         SETLOCK RELEASE,TYPE=LOCAL,RELATED=(TSB,IGG09302(TPT0100))
         WAIT  ECB=TSBECB,LONG=YES                              YS02019
         SPACE 2                                                 TS1634
***
***                 AFTER POST, DISABLE AND CHECK AGAIN
***
         SETLOCK OBTAIN,TYPE=LOCAL,MODE=UNCOND,                 YS02019*
               RELATED=(TSB,IGG09302(TPT9600))                  YS02019
         SETLOCK OBTAIN,TYPE=CMS,MODE=UNCOND,                   YS02019*
               RELATED=(TSB,IGG09302(TPT9700))
         LH    RTJID,TSBWTJID           RESTORE TJID            YS02019
         BAL   RBUF,TPT6000             CHECK USER'S STATUS      TS1634
         B     TPT0100                  CHECK OWIP AGAIN
         SPACE 2
TPT0350  EQU   *
***
***                 CHECK EDIT OPTION
***
         OI    TSBFLG1,TSBOWIP+TSBTJIP  SIEZE TSB
         OI    XSAF,OSW                 INDICATE WE SET TSBOWIP  YM0895
         ST    RTCB,TSBCTCB             STORE CURRENT TCB ADDRESS
         STH   RTJID,TSBWTJID           STORE CURRENT TJID
         OI    XSAF,XSADMOVE            INDICATE DATA MOVEMENT  YM05387
         TM    XSAOPTNS,MCNTRL+MASIS    CONTROL OR ASIS OPTION   M0094
         BM    TPT0600                  IF EITHER SKIP EDIT
         BZ    TPT0475                  IF EDIT, GO REMOVE      SA61765
***
***      MUST BE AN ATTEMPT AT FULL-SCREEN TJID TPUT.  QUASH IT.
***
         NI    XSAOPTNS,OFF-MCNTRL      FORCE ASIS ONLY         SA61765
         B     TPT0600                  SKIP BLANK REMOVAL      SA61765
TPT0475  EQU   *                                                SA61765
         SPACE 2
***
***                 REMOVE TRAILING BLANKS
***
         L     RWRK,XSAPRM1             GET USER BUFFER POINTER  M0094
         LA    RWRK,K0(R0,RWRK)         CLEAR FIRST BYTE
         LH    RWRK1,XSAPRMSZ           USER BUFFER SIZE         M0094
         AR    RWRK,RWRK1               PTR TO USER BFR END+1
         SPACE 2
TPT0400  EQU   *
         BCTR  RWRK,R0                  DECREMENT POINTER
         CLI   K0(RWRK),BLNKCHAR        IS CHARACTER BLANK
         BNE   TPT0500                  IF NOT QUIT REMOVAL
         BCT   RWRK1,TPT0400            BR IF BFR SIZE NOT ZERO  Y01018
         SPACE 2
***
***                 NO DATA LEFT AFTER REMOVING BLANKS - SEND NL ONLY
***
         LA    RWRK1,NEWLINE            POINT TO NL CHAR
         STCM  RWRK1,SEVEN,XSAPRM1+K1   POINT AT NL CHAR         Y01018
         OI    XSAOPTNS,MASIS           FORCE ASIS OPTION SO NL  M0094
*                                       CAN GET THROUGH
         LA    RWRK1,K1                 INDICATE LENGTH OF 1
         SPACE 2
TPT0500  EQU   *
         STH   RWRK1,XSAPRMSZ           UPDATE USER BFR SIZE     M0094
         SPACE 2
TPT0600  EQU   *
***
***                 CHECK BUFFER AVAILABILITY
***
         LH    RWRK,TIOCAOMX            GET NORMAL MAX
         TM    TSBFLG4,TSBGETBF         MONITOR SUBCMD ACTIVE   YS02019
         BNO   TPT0625                  NO, DON'T INCREASE AOMX
         AR    RWRK,RWRK                DOUBLE IT                Y01018
         AH    RWRK,TIOCAOMX            ADD ONE TO GET 3
TPT0625  EQU   *
         SR    RWRK2,RWRK2
         IC    RWRK2,TSBNOBF            NUMBER ALREADY ON OUT QUEUE
         CR    RWRK2,RWRK               SHOULD BE LESS THAN MAX
         BNL   TPT0700                  IF NOT, WAIT
         SR    RWRK,RWRK                                         TS1634
         TM    TSBFLG4,TSBGETBF         MONITOR SUBCMD ACTIVE   YS02019
         BZ    TPT0650                  NO, DON'T RESTRICT
         LH    RWRK,TIOCUSLW            DON'T USE LAST BUFFERS
TPT0650  EQU   *
         CH    RWRK,TIOCNFBF            ANY FREE BUFFERS         TS1634
         BL    TPT1000                  IF SO, GO PROCESS ONE    TS1634
***
***                 NO FREE BUFFERS - SET BITS IN RPT AND TSB
***                    AND WAIT
***
         OI    TSBFLG1,TSBTJBF          SHOW WHICH TASK WAITING  TS1634
         OI    TIOCFLG,TIOCTJBF         SET SYSTEM INDICATOR     TS1634
         B     TPT0800                  WAIT                     TS1634
TPT0700  EQU   *                                                 TS1634
         OI    TSBFLG1,TSBTJOW          INDICATE WHY WAITING     TS1634
TPT0800  EQU   *
***********************************************************************
*****                                                             *****
*****               ROUTINE FOR WAITING ON ECB                    *****
*****                                                             *****
***********************************************************************
         SPACE
         TM    XSAF,PSW                 DOES TCAM NEED TO BE     M0094
*                                       POSTED FOR PARTIAL MSG
         BZ    TPT0900                  BRANCH IF ZEROES
         SPACE 2
***
***                 CHECK IF NEXT CHAR TO BE SENT IS A BACKSPACE
***
         L     RWRK,XSAUBFRP            GET PTR TO NEXT CHAR     M0094
         CLI   K0(RWRK),BKSPCHAR        IS NEXT CHAR BACKSPACE
         BNE   TPT0830                  IF NOT
         OI    TSBFLG3,TSBNBKSP         INDR FOR OUTPUT EDIT
         SPACE 2
TPT0830  EQU   *
***                                                             YS02019
***                 GO TO SUBROUTINE TO TPOST TSB AND POST TCAM YS02019
***                                                             YS02019
         BAL   RBUF,TPT9900             GO TO SUBROUTINE        YS02019
         SPACE 2
TPT0900  EQU   *
***
***                 WAIT ON ECB
***
         SR    RWRK,RWRK                                        YS02019
         ST    RWRK,TSBECB              CLEAR OUT ECB           YS02019
         IC    RWRK,TSBWTCB-K1          SAVE FLAGS
         STCM  RTCB,SEVEN,TSBWTCB       STORE WAITING TCB ADDR   Y01018
         SETLOCK RELEASE,TYPE=CMS,RELATED=('BEFORE SVC')        YS02019
         SETLOCK RELEASE,TYPE=LOCAL,RELATED=('BEFORE SVC')      YS02019
         WAIT  ECB=TSBECB,LONG=YES                              YS02019
         SPACE 2
***
***                 UPON POST OF ECB
***
         SETLOCK OBTAIN,TYPE=LOCAL,MODE=UNCOND,                 YS02019*
               RELATED=(TSB,IGG09302(TPT9700))                  YS02019
         SETLOCK OBTAIN,TYPE=CMS,MODE=UNCOND,                   YS02019*
               RELATED=(TSB,IGG09302(TPT9600))                  YS02019
         BAL   RBUF,TPT6000             CHECK USER'S STATUS      TS1634
         B     TPT0600                  RECHECK BFR AVAILABILITY
         SPACE 3
TPT1000  EQU   *
         SPACE 2
***********************************************************************
***                                                                   *
***            QTIP WILL NOW BE BRANCHED TO TO ACCOMPLISH             *
***                                                                   *
***                 1- FIND THE PROPER PLACE ON THE OUTPUT            *
***                    QUEUE TO PUT THE NEXT BFR.                     *
***                                                                   *
***                 2- PLACE FIRST FREE BUFFER ONTO OUTPUT QUEUE.     *
***                                                                   *
***                 3- UPDATE PTR AND COUNT OF OUTPUT QUEUE           *
***                                                                   *
***                 4- MOVE EITHER ALL REMAINING DATA, OR AS MUCH AS  *
***                    BUFFER WILL HOLD WHICHEVER IS LESS.            *
***                                                                   *
***                 5- UPDATE USER BUFFER SIZE AND POINTER IN SVRB    *
***                    EXTENDED SAVE AREA (XSUBFRS AND XSAUBFRP)      *
***                                                                   *
***********************************************************************
         SPACE 2
TPT1100  EQU   *
         LA    RSAVE,MOVECODE           ENTRY CODE
         L     RGOTO,TIOCQTIP           ENTRY POINT OF QTIP     YS02019
         LA    RAVT,TIOCSAVE            SAVE AREA FOR QTIP      YS02019
         BALR  RETURN,RGOTO             GO TO QTIP
         SPACE 2
***
***                 UPON RETURN FROM QTIP
***
         SPACE
         NI    XSAF,OFF-XSADMOVE        DATA MOVEMENT DONE      YS02019
         OI    XSAF,PSW+HSW             SHOW PART OF MSG ON Q    M0094
*                                       AND HEADER BFR DONE
         SPACE 2
***
***                 CHECK FOR EOM
***
         SR    RBUF,RBUF                                         M0094
         CH    RBUF,XSAUBFRS            HAS ALL DATA BEEN MOVED  M0094
         BE    TPT9000                  DONE IF SO
         LA    RBUF,TPT0600             SUBR WILL RETURN TO      Y01018
*                                       GET NEXT BUFFER
         SPACE 4
TPT6000  EQU   *                                                 TS1634
***********************************************************************
****
****                SUBROUTINE TO CHECK USER'S STATUS
****
***********************************************************************
         LA    RWRK2,TPT7500            GET ABORT ADDR           Y01018
         TM    CVTTCMFG,CVTTCRDY        IS TCAM UP               TS0034
         L     RRPT,CVTAQAVT            TCX PTR FOR LATER TEST  YS02019
         BNOR  RWRK2                    ABORT IF TCAM NOT UP     Y01018
         TM    TSBSTAT,TSBINUSE         STILL ACTIVE            YS02019
         L     RRPT,TCXRPT-IEDQTCX(,RRPT) RPT PT FOR LATER TEST YS02019
         BNOR  RWRK2                    ABORT IF NOT ACTIVE      Y01018
         TM    TSBSTAT,TSBDISC          DISCONNECTING           YS02019
         BNZR  RWRK2                    YES, ABORT              YS02019
         LTR   RRPT,RRPT                TEST VALUE OF RPT PTR   YS02019
         BZR   RWRK2                    IF ZERO, TS DOWN        YS02019
         TM    TSBFLG4,TSBHUNG          USER HUNG UP            YS02019
         BNOR  RBUF                     NO, RETURN TO CALLER     Y01018
         SPACE 2                                                 TS1634
TPT7500  EQU   *
***
***                 CALLED TASK NO LONGER ACTIVE RETURN
***
         LA    RCODE,RCGONE             RETURN CODE
         TM    XSAF,OSW                 SHOULD TSB BE RESET     YS02019
         BNO   TPT9625                  NO, DON'T RESET         YS02019
         B     TPT9600                  RETURN
         SPACE 4
TPT9000  EQU   *
***
***                 SUCCESSFUL RETURN
***
         SR    RCODE,RCODE              INDICATE SUCCESS         YM0895
         STH   RCODE,XSARC              SAVE RETURN CODE        YS02019
         SPACE
***                                                             YS02019
***                 GO TO TPOST SUBROUTINE                      YS02019
***                                                             YS02019
         BAL   RBUF,TPT9900             GO TO SUBROUTINE        YS02019
         SPACE 3
TPT9100  EQU   *
***
***                 CHECK TO SEE IF SMF IS UP
***
         ICM   RWRK,B'0111',TCBTCTB     GET & TEST TCT ADDRESS  YS02019
         BZ    TPT9200                  IF NOT
         SPACE 2
***
***                 UPDATE LINE COUNT IF UP
***
         USING SMFTCT,RWRK
         L     RWRK2,TCTLOUT            OLD COUNT
         LA    RWRK2,K1(R0,RWRK2)       ADD ONE TO COUNT
         ST    RWRK2,TCTLOUT
         DROP  RWRK
         SPACE 2
TPT9200  EQU   *
***                                                             YS02019
***        INFORM SYSTEM RESOURCE MANAGER OF SUCCESSFUL TPUT    YS02019
***                                                             YS02019
         LH    RWRK2,XSAPRMSZ           AMOUNT OF DATA SENT     YS02019
         LA    RAVT,TIOCSAVE            SAVE AREA PTR FOR SYSEV YS02019
         O     RWRK2,TSIPSIGN           HI BIT MEANS TPUT       YS02019
         SYSEVENT TGETTPUT,ASIDL=XSATJID,ENTRY=BRANCH           YS02019
         SPACE 2
TPT9400  EQU   *
***
***                 CHECK FOR WAITING ON OWAIT TASKS
***
         TM    TSBFLG1,TSBWOWIP         ANY TASKS WAITING
         BZ    TPT9600                  IF NOT
         SPACE 2
***
***                 GO TO 'STATUS' TO REMOVE WAITING TASKS FROM OWAIT
***
***                 REGISTERS FOR STATUS -
***
***
***            0 (R0) - HIGH HALF = ASID, LO HALF = ENTRY CODE (8)
***            1 (RWRK2) - HI BIT MUST BE ON, OTHERS OFF
***            13 (RAVT) - MASK OF SECONDARY BITS TO BE RESET
***            14,15 (RETURN, RGOTO) - CONVENTIONAL             YS02019
***                                                             YS02019
         LA    R0,STEPSD                ENTRY CODE TO RESET ALL YS02019
*                                       TASKS IN STEP           YS02019
         L     RGOTO,CVTABEND           PTR TO 2NDARY CVT       YS02019
         USING SCVTSECT,RGOTO                                   YS02019
         L     RWRK2,TSIPSIGN           HI BIT MEANS 'RESET'    YS02019
         ICM   R0,B'1100',XSAPRMTJ      ASID TO BE ACTIVATED    YS02019
         L     RGOTO,SCVTSTAT           ENTRY POINT OF STATUS   YS02019
         LA    RAVT,NDISPBIT            NONDISPATCHABILITY MASK YS02019
*                                       (TCBOWAIT, IN NDSP2)    YS02019
         BALR  RETURN,RGOTO             GO TO STATUS ROUTINE    YS02019
         SPACE 2
TPT9600  EQU   *
***
***                 CLEAN UP AND RETURN
***
         NI    TSBFLG1,OFF-(TSBOWIP+TSBTJIP) RELEASE TSB
         NI    TSBFLG3,OFF-TSBATTN      ATTN IGNORED INDR
         NI    TCBTSFLG,OFF-TCBTIOTG    ATTN HIT INDR
         SPACE 2                                                YS02019
TPT9625  EQU   *                                                YS02019
***                                                             YS02019
***                 ALL DONE WITH CMS LOCK                      YS02019
***                                                             YS02019
         SETLOCK RELEASE,TYPE=CMS,RELATED=(TSB,IGG09302)        YS02019
         SPACE 1
TPT9650  EQU   *
***
***                TURN OFF TCBFX IF OFF AT ENTRY
***
         TM    XSAF,XSATCBFX            SHOULD WE TURN OFF TCBFX M0094
         BZ    TPT9700                  SKIP IF NOT
         NI    TCBFLGS1,OFF-TCBFX       ALLOW ASYNCHRONOUS EXITS
TPT9700  EQU   *
***                                                             YS02019
***                 ALL DONE WITH LOCAL LOCK                    YS02019
***                                                             YS02019
         SETLOCK RELEASE,TYPE=LOCAL,RELATED=(TSB,IGG09302)      YS02019
         SPACE 2
***
***                 DEQ FROM TJID TO RELEASE ENQUEUED TASKS
***
         SPACE
         LR    RSAVE2,RCODE             SAVE RETURN CODE
         MVC   XSAENQNM(K12),QNAME      RESET ENQ/DEQ PARMS      M0094
         LA    RWRK2,XSAENQAD           GET ADDR OF ENQ WRK AREA M0094
         DEQ   (XSAENQNM,ASCBASID,2,SYSTEM),RET=HAVE,MF=(E,(1)) YS02019
         NI    XSAF,OFF-XSAIDENQ        RESET ASID ENQUEUE FLAG YS02019
         SPACE 2
         LR    RCODE,RSAVE2             RESTORE RETURN CODE
         L     RETURN,XSARETRG          RESTORE RETURN ADDRESS  YS02019
         BR    RETURN                   RETURN TO SUPERVISOR    YS02019
         SPACE 5                                                YS02019
***********************************************************************
***                                                                 ***
***                 SUBROUTINE TO TPOST TSB TO IEDAYP               ***
***                                                                 ***
***********************************************************************
TPT9900  EQU   *                                                YS02019
         LM    RP0,RP1,TSBTPOST         GET TPOSTING FIELD      YS02019
         STM   RBUF,RCVT,XSAWD7         SAVE REGISTERS 2&3      YM03998
TPT9907  EQU   *                                                YS02019
         STM   RP0,RP1,XSAWD5           MOVE TO XSA TO SET BITS YS02019
         LA    RGOTO,XSAWD5             ADDRESSABILITY          YS02019
         USING TSBTPOST,RGOTO           NEXT REFERENCES TO TSB  YS02019
*                                       TPOST & BEYOND WILL     YS02019
*                                       REALLY BE TO XSA        YS02019
         SPACE 2                                                YS02019
***                                                             YS02019
***                 IF ALL OUTPUT IS ON QUEUE, TURN OFF         YS02019
***                 QCBPARTO. ELSE, TURN IT ON                  YS02019
***                                                             YS02019
         SR    RWRK,RWRK                ZERO WORK REGISTER      YM01246
         CH    RWRK,XSAUBFRS            REMAINING SIZE ZERO     YS02019
         BNE   TPT9910                  NO, TURN ON PARTO       YS02019
         TM    XSAF,XSAPARTO            HAS PARTO BEEN SET      YM01246
         BZ    TPT9915                  NO, NO NEED TO RESET    YM01246
         NI    TSBF1V,OFF-QCBPARTO      YES, TURN OFF PARTO     YS02019
         OI    TSBF1M,QCBPARTO          ACTION BIT FOR AYP      YM01246
         NI    XSAF,OFF-XSAPARTO        RESET PARTO FLAG        YM01246
         B     TPT9915                  SKIP                    YS02019
TPT9910  EQU   *                                                YS02019
         TM    XSAF,XSAPARTO            HAS PARTO BEEN SET      YM01246
         BO    TPT9915                  YES, NO NEED TO SET     YM01246
         OI    TSBF1V,QCBPARTO          TURN ON PARTO           YS02019
         OI    TSBF1M,QCBPARTO          ACTION BIT FOR AYP      YM01246
         OI    XSAF,XSAPARTO            SET PARTO FLAG IN WA    YM01246
         SPACE 2                                                YS02019
***                                                             YS02019
***                 TURN ON QCBTPUT.  IF BREAKIN REQUEST, AND   YS02019
***                 BREAKIN IS ALLOWED, TURN ON QCBWRBRK.       YS02019
***                                                             YS02019
TPT9915  EQU   *                                                YS02019
         TM    XSAOPTNS,MBREAK          BREAKIN REQUEST         YM01176
         BO    TPT9917                  YES, SKIP TSBTPUT TEST  YM01176
         TM    TSBFLG3,TSBTPUT          ALREADY DOING A TPUT    YM01246
         BZ    TPT9917                  NO, MUST TPOST          YM01246
         TM    TSBF1M,QCBPARTO          MUST PARTO BE SET       YM01246
         BZ    TPT9930                  NO, NO NEED TO TPOST    YM08378
TPT9917  EQU   *                                                YM01246
         OI    TSBF1V,QCBTPUT           SET TPUT VALUE          YS02019
         OI    TSBF1M,QCBTPUT           ACTION BIT FOR AYP      YM03998
         TM    XSAOPTNS,MBREAK          BREAKIN REQUEST         YS02019
         BNO   TPT9920                  NI, SKIP                YS02019
         TM    TSBFLG3,TSBNOBRK         BREAKIN ALLOWED         YS02019
         BO    TPT9920                  NO, SKIP                YM01114
         OI    TSBF1V,QCBWRBRK          SET VALUE BIT           YS02019
         OI    TSBF1M,QCBWRBRK          SET ACTION BIT          YS02019
TPT9920  EQU   *                                                YS02019
         OI    TSBTPFLG,TSBPOSTO+TSBTPQCB                       YS02019
*                                       TELL AYP TO TPOST QCB,  YS02019
*                                       SHOW A TPOST IS         YS02019
*                                       OUTSTANDING             YS02019
         SPACE 2                                                YS02019
***
***                 SET BITS IN REAL TSB, USING CDS.  IF TSBPOSTO
***                 WAS ON DURING SWAP, NO NEED TO DO ACTUAL
***                 TPOST
***
         LM    RBUF,RCVT,XSAWD5         GET NEW BITS            YS02019
         DROP  RGOTO                                            YS02019
         CDS   RP0,RBUF,TSBTPOST        TRY TO RESET BITS       YS02019
         BNE   TPT9907                  IF CHANGED, DO IT AGAIN YS02019
         OI    TSBFLG3,TSBTPUT          SET INDR FOR FUTURE     YS03998
         LTR   RP0,RP0                  TEST TSBPOSTO (THIS TST YS02019
*                                       DEPENDS UPON            YS02019
*                                       TSBPOSTO EQU X'80')     YS02019
         BM    TPT9930                  IF ON, DON'T TPOST.     YS02019
*                                       IEDAYP WILL PICK UP     YS02019
*                                       THESE FLAGS ANYWAY      YS02019
         SPACE 2                                                YS02019
***
***                TPOST THE TSB TO IEDAYP
***
         L     RCVT,CVTPTR                                      YS02019
         L     RTCX,CVTAQAVT            GET TCX ADDRESS         YS02019
         USING IEDQTCX,RTCX                                     YS02019
         L     RWRK,TCXTSI              GET TSINPUT QCB ADDRESS YS02019
         USING IEDQTSI,RWRK                                     YS02019
         LA    RWRK,TSITSAP-(QCBSTVTO-IEDQQCB)                  YS02019
*                                       ADDRESS OF IEDAYP'S     YS02019
*                                       'QCB'                   YS02019
         ST    RWRK,TSBRQCB             QCB TO WHICH TSB WILL   YS02019
*                                       BE TPOSTED              YS02019
         L     RWRK,TCXREADY            GET CONTENTS OF READY Q YS02019
TPT9925  EQU   *                                                YS02019
         ST    RWRK,TSBLINKA            LINK TSB TO NEXT ELEM   YS02019
         MVI   TSBPRI,PRIDESTQ          SET PRIORITY            YS02019
         LA    RSAVE,TSBRCB             GET READY TO PUT ELEM   YS02019
*                                       ADDRESS INTO Q HEADER   YS02019
         CS    RWRK,RSAVE,TCXREADY      TRY TO ADD IT TO QUEUE  YS02019
         BNE   TPT9925                  NO GOOD, TRY AGAIN      YS02019
         SPACE 2                                           YS02019
***
***                CROSS-MEMORY POST TCAM'S ECB
***
         SR    RRPT,RRPT                COMPLETION CODE         YS02019
         LA    RTJID,CVTBRET            'ERRET' ADDRESS         YS02019
         L     RAVT,TCXASCB             GET ASCB ADDRESS        YS02019
         LR    R0,RTCX                  SAVE TCX POINTER        YS02019
         L     RTCX,TCXAVT              GET AVT ADDRESS         YS02019
         DROP  RTCX                                             YS02019
         USING IEDQAVTD,RTCX                                    YS02019
         LA    RTCX,AVTOSECB            ECB POINTER FOR POST    YS02019
         DROP  RTCX                                             YS02019
         L     RGOTO,CVT0PT01           POST ROUTINE ADDRESS    YS02019
         O     RTCX,TSIPSIGN            HI-ORDER BIT = XMPOST   YS02019
         BALR  RETURN,RGOTO             GO TO POST ROUTINE      YS02019
         SPACE 2                                                YS02019
         LR    RTCX,R0                  RESTORE TCX POINTER     YS02019
         USING IEDQTCX,RTCX                                     YS02019
         LH    RTJID,TSBWTJID           RESTORE ASID OF CALLER  YS02019
         L     RRPT,TCXRPT              RESTORE RPT POINTER     YS02019
         SPACE 2                                                YS02019
***                                                             YS02019
***                 RETURN TO SUBROUTINE CALLER                 YS02019
***                                                             YS02019
TPT9930  EQU   *                                                YS02019
         LM    RBUF,RCVT,XSAWD7         RESTORE REGISTERS       YS02019
         BR    RBUF                     RETURN                  YS02019
         EJECT                                                  YS02019
*****
*****               CONSTANTS
*****
NEWLINE  DC    X'15'                    NEW LINE CHAR FOR BLANK LINE
TSIPSIGN DS    0F                       ALIGN ON FULLWORD BDY    Y01018
         DC    X'80000000'              TPUT SIGN FOR TSIP
QNAME    DC    C'SYSZIGGI'              ENQ DEQ QNAME           YS02019
         DC    X'FF000000'              DEQ/ENQ PARM LIST
PATCHREA DC    10F'0'                   PATCH AREA               Y01018
         EJECT
*****
*****               THE FOLLOWING DSECTS ARE USED BY THIS MODULE
*****
         IHAASCB                                                YS02019
         EJECT                                                  YS02019
         IHAASVT                                                YS02019
         EJECT                                                  YS02019
         TAVTD                                                  YS02019
         EJECT                                                  YS02019
         IKJTIOCB
         EJECT
CSCB     DSECT                                                  YS02019
         IEECHAIN                                               YS02019
         EJECT                                                  YS02019
CVT      DSECT
         CVT
         EJECT
         TPRIOR                                                 YS02019
         EJECT                                                  YS02019
         IHAPSA                                                 YS02019
         EJECT                                                  YS02019
         TQCBD                                                  YS02019
         EJECT                                                  YS02019
         IKJRB
***
***      EXTENDED SAVE AREA.  THSES STATEMENTS SHOULD FOLLOW
***      THE MACRO 'IKJRB.'
***
         ORG   RBEXSAVE                 START OF SAVE AREA       M0094
XSAPRM0  DS    0F                       REG 0 AT ENTRY           M0094
XSAPRMTJ DS    H                        TJID PASSED              M0094
XSAPRMSZ DS    H                        SIZE PASSED              M0094
XSAPRM1  DS    0F                       REG 1 AT ENTRY           M0094
XSAOPTNS DS    X                        USER OPTIONS             M0094
MASIS    EQU   X'01'                    ASIS OPTION SPECIFIED    M0094
MCNTRL   EQU   X'02'                    1 MEANS CONTROL          M0094
*
*   BOTH OF THE ABOVE BITS OFF MEANS EDIT OPTION
*
MBREAK   EQU   X'04'                    BREAKIN SPECIFIED        M0094
MHOLD    EQU   X'08'                    1 MEANS HOLD             M0094
MTGET    EQU   X'80'                    1 MEANS TGET REQUESTED   M0094
MUSERP   EQU   X'40'                    USERID SPECIFIED        YS02019
MLOWP    EQU   X'20'                    0 HIGHPRI, 1 LOWPRI      M0094
MWAIT    EQU   X'10'                    1 MEANS NOWAIT           M0094
         DS    AL3                      REST OF REGISTER ONE     M0094
XSAWD3   DS    0F                       FOR SAVING REGISTERS     M0094
XSAFLAG  DS    X                        FLAG PASSED BY 9C TO 01  M0094
*                                       AND 02                   M0094
XSATCBFX EQU   X'20'                    TURN OFF TCBFX AT EXIT   M0094
         DS    AL3                      REST OF WORD             M0094
         ORG   XSAWD3+2                 INPUT ED, OUTPUT MOVE    M0094
XSAUBFRS DS    H                        AMT DATA LEFT TO MOVE    M0094
XSAUBFRP DS    0F                       LOC DATA LEFT TO MOVE    M0094
XSAESTAL DS    CL16                     LIST FOR ESTAE MACRO    YS02019
         ORG   XSAUBFRP+4               USED FOR SAVE AREA WHEN YS02019
*                                       NOT NEEDED BY ESTAE     YS02019
XSAWD5   DS    F                        FOR SAVING REGISTERS     M0094
XSAWD6   DS    F                        FOR SAVING REGISTERS     M0094
         ORG   XSAWD5                   USE WORDS 5-6 FOR NAME   M0094
XSAENQNM DS    CL8                      ENQ NAME FOR TJID TPUT   M0094
XSAWD7   DS    F                        FOR SAVING REGISTERS     M0094
XSAWD8   DS    F                        FOR SAVING REGISTERS     M0094
         ORG   XSAWD7                   USE WDS 7-8 FOR ENQ PRMS M0094
XSAENQAD DS    2F                       WORK AREA FOR ENQ/DEQ    M0094
XSAUSERP DS    0F                       PTR TO USERID, IF PRES  YS02019
XSAWD9   DS    F                        FOR SAVING REGISTERS     M0094
XSARETRG DS    0F                       FOR SAVING RETURN ADDR  YS02019
XSAWD10  DS    F                        FOR SAVING REGISTERS     M0094
         DS    CL1                      RESERVED                YS02019
XSAUSERK DS    CL1                      SVC CALLER'S KEY        YS02019
XSARC    DS    H                        RETURN CODE SAVE AREA   YS02019
XSATJID  DS    H                        CALLER'S TJID            M0094
XSAFLAG2 DS    X                        FLAGS                   YM05387
XSAATR   EQU   X'80'                    NEXT CHAR IN TS BUFFER   S22028
*                                       IS A 3270 ATTRIBUTE BYTE
XSABFRAL EQU   X'40'                    TIOC BUFFER HASS BEEN   YM05387
*                                       ALLOCATED               YM05387
XSA15D   EQU   X'20'                    CHANGE COMPLETION CODE  YM05387
*                                       TO 15D                  YM05387
*                  NEXT 5 BITS UNUSED                           YM05387
XSAF     DS    X                        FLAGS USED BY TPUT. INI- M0094
*                                       TIALIZED FROM XSAFLAG.
HSW      EQU   X'80'                    HEADER BFR DONE          M0094
PSW      EQU   X'40'                    NEED TO TPOST QCB        M0094
*SATCBFX EQU   X'20'                    TURN OFF TCBFX AT EXIT
OSW      EQU   X'10'                    TSBOWIP TURNED ON BY US  M0094
XSADMOVE EQU   X'08'                    DATA BEING MOVED        YS02019
XSAAUTH  EQU   X'04'                    USER IS AUTHORIZED      YS02019
*                                       (NOT SOME OTHER TPUT)
XSAIDENQ EQU   X'02'                    ASID TPUT ASID ENQUEUED YS02019
XSAPARTO EQU   X'01'                    QCBPARTO IS SET ON      YM01246
         EJECT
         IHASCVT                                                YS02019
         EJECT                                                  YS02019
         IKJTCB
         EJECT
         IEFTCT
         EJECT
         TTCXD                                                  YS02019
         EJECT                                                  YS02019
         IKJTIOCP
         EJECT
         IKJTSB
         EJECT
         TTSID                                                  YS02019
         END
