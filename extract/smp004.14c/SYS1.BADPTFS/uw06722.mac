++ PTF (UW06722)        /*
//UW06722  JOB 5752-06722,SC1T3,MSGLEVEL=(1,1),CLASS=A     */  .
++ VER (Z038)
   FMID(ETI1106)
   PRE  (UY49086)
   SUP  (AW03892)
 /*
   PROBLEM DESCRIPTION(S):
     OW03892 -
       GTTERM MACRO ENHANCEMENT TO RETURN TERMINAL ID

   COMPONENT:  5752-SC1T3-ETI1106
   APARS FIXED: OW03892
   SPECIAL CONDITIONS:
     DOCUMENTATION:
       Publication: TSO Extensions V2 Programming Services
       Form Number: SC28-1875-07
       Make the following changes in Chapter 7 - GTTERM.
        pp.7-3 through 7-4
       >Figure 7-5 should be changed to reflect the addition of the
                   TERMID parameter by adding 'ª,TERMID=addr³'.
       >The description of the ATTRIB parameter should be changed to
        add the additional bits:
        Byte  Setting    Meaning
         0-2             .............
          3   1... ....  This is a VTAM TSB. *
          3   .1.. ....  Break features are not allowed. *
          3   ..1. ....  The translate table is in use. *
          3   ...1 ....  The default translate table is in use. *
          3   .... 1...  Display in full screen mode.*
          3   .... .x..  Reserved.
          3   .... ..0.  The device supports EBCDIC code.
          3   .... ..1.  The device supports ASCII code.
          3   .... ...0  The Read Partition (Query) is not supported.
          3   .... ...1  The Read Partition (Query) is supported.
        * These bits are returned only for VTAM applications.
       >Figure 7-6 should be changed to reflect the TERMID parameter
        by adding the following line:
         12(C)   4   Address of 16 byte field to receive terminal name.
       >The description for the new TERMID parameter should be added
        as follows:
         TERMID = addr
           Specifies the address of a 16 byte field into which GTTERM
           returns the terminal name in the first 8 bytes and, if VTAM
           V4R1 or above, the netid in the second 8 bytes; otherwise,
           the second 8 bytes are filled with blanks. If TCAM, the
           second 8 bytes will not be changed.
     ENHANCEMENT:
       The GTTERM macro has been changed to add a new parameter which
       will obtain the terminal name. This new parameter is TERMID.
       The new parameter, coded as 'TERMID = addr', specifies the
       address of a 2-word field into which GTTERM returns the terminal
       name.
     ACTION:
       The corresponding APARs for TSO/VTAM (OW01934) and TCAM
       (OW04186) must be applied in order to utilize this function.
     ACTION:
       RE-ASSEMBLY OF THE TSO USERS PROGRAM MAY BE NECESSARY TO
       ACTIVATE THIS PTF IF THE MACRO(S) INCLUDED ARE USED.
   COMMENTS:
     CROSS REFERENCE-MODULE/MACRO NAMES TO APARS
      GTTERM    OW03892

     CROSS REFERENCE-APARS TO MODULE/MACRO NAMES
      OW03892   GTTERM

     THE FOLLOWING MODULES AND/OR MACROS ARE AFFECTED BY THIS PTF:

     MACROS
      GTTERM

     LISTEND
 */.
++ HOLD(UW06722) SYS FMID(ETI1106) REASON(DOC) DATE(94130)
   COMMENT
    (Publication: TSO Extensions V2 Programming Services
     Form Number: SC28-1875-07
     Make the following changes in Chapter 7 - GTTERM.
      pp.7-3 through 7-4
     >Figure 7-5 should be changed to reflect the addition of the
                 TERMID parameter by adding 'ª,TERMID=addr³'.
     >The description of the ATTRIB parameter should be changed to
      add the additional bits:
      Byte  Setting    Meaning
       0-2             .............
        3   1... ....  This is a VTAM TSB. *
        3   .1.. ....  Break features are not allowed. *
        3   ..1. ....  The translate table is in use. *
        3   ...1 ....  The default translate table is in use. *
        3   .... 1...  Display in full screen mode.*
        3   .... .x..  Reserved.
        3   .... ..0.  The device supports EBCDIC code.
        3   .... ..1.  The device supports ASCII code.
        3   .... ...0  The Read Partition (Query) is not supported.
        3   .... ...1  The Read Partition (Query) is supported.
      * These bits are returned only for VTAM applications.
     >Figure 7-6 should be changed to reflect the TERMID parameter
      by adding the following line:
       12(C)   4   Address of 16 byte field to receive terminal name.
     >The description for the new TERMID parameter should be added
      as follows:
       TERMID = addr
         Specifies the address of a 16 byte field into which GTTERM
         returns the terminal name in the first 8 bytes and, if VTAM
         V4R1 or above, the netid in the second 8 bytes; otherwise,
         the second 8 bytes are filled with blanks. If TCAM, the
         second 8 bytes will not be changed.
     The GTTERM macro has been changed to add a new parameter which
     will obtain the terminal name. This new parameter is TERMID.
     The new parameter, coded as 'TERMID = addr', specifies the
     address of a 2-word field into which GTTERM returns the terminal
     name.).
++ HOLD(UW06722) SYS FMID(ETI1106) REASON(ACTION) DATE(94130)
   COMMENT
    (The corresponding APARs for TSO/VTAM (OW01934) and TCAM
     (OW04186) must be applied in order to utilize this function.
     RE-ASSEMBLY OF THE TSO USERS PROGRAM MAY BE NECESSARY TO
     ACTIVATE THIS PTF IF THE MACRO(S) INCLUDED ARE USED.).
++ MAC      (GTTERM  ) DISTLIB(ATSOMAC ) SYSLIB(MACLIB) .
         MACRO
&NAME    GTTERM &PRMSZE=,&ALTSZE=,&ATTRIB=,&TERMID=,&MF=I      @G76XRYU
.*                                                             @OW03892
.* A000000-999999                                              @G76XR00
.* A034000                                                     @OZ90350
.* NOCHANGE SHIPPED WITH JCLIN                                 @OY20024
.* NOCHANGE SHIPPED WITH JCLIN                                 @OY26821
.* Added TERMID parameter                                      @OW03892
.*
         LCLC  &NDX                                            @G76XRYU
&NDX     SETC  '&SYSNDX'                                       @G76XRYU
         AIF   ('&MF' EQ 'I' AND '&PRMSZE' EQ '' AND '&ATTRIB' EQ '' AN-
               D '&TERMID' EQ '').ERROR1              @G76XRYU @OW03892
         AIF   ('&MF' EQ 'L').LFORM
         AIF   ('&MF(1)' EQ 'E').EFORM
         AIF   ('&MF' NE 'I').ERROR2
.*********************I -FORM OF MACRO*********************************
&NAME    CNOP  0,4
         BAL   1,*+20                   BRANCH AROUND PARMS    @G76XRYU
.*                                                             @OW03892
GTRM&NDX DC    A(0)                     ADDRESS OF PRIMARY     @G76XRYU
         DC    A(0)                     ADDRESS OF ALTERNATE
         DC    A(0)                     ADDRESS OF ATTRIBUTE   @G76XRYU
         DC    A(0)                     ADDRESS OF TERMINAL ID @OW03892
         AGO   .STADDR
.EFORM   ANOP
&NAME    CNOP  0,4                                             @OZ90350
         IHBOPLST ,,&NAME,MF=&MF
.STADDR  ANOP
.*********COMMON CODE FOR BOTH I AND E FORM OF MACRO******************
         AIF   ('&PRMSZE' EQ '').LABEL2
         AIF   ('&PRMSZE'(1,1) NE '(').LOADPRM
         ST    &PRMSZE(1),0(,1)         STORE PRIMARY ADDRESS
         AGO   .LABEL2
.LOADPRM ANOP
         AIF   ('&PRMSZE'(K'&PRMSZE,1) EQ ')' OR '&MF' NE 'I').LPARM
         ORG   GTRM&NDX                 PUT ADDR OF PRIMARY    @G76XRYU
         DC    A(&PRMSZE)               IN PARM LIST           @G76XRYU
         ORG
         AGO   .LABEL2                                         @G76XRYU
.LPARM   ANOP                      ..LA ADDR OR EXECUTE FORM   @G76XRYU
         LA    0,&PRMSZE                LOAD ADDRESS OF PRIMARY
         ST    0,0(,1)                  STORE ADDRESS OF PRIMARY
.LABEL2  ANOP
         AIF   ('&ALTSZE' EQ '').IEATRCK                       @G76XRYU
         AIF   ('&ALTSZE'(1,1) NE '(').LOADALT
         ST    &ALTSZE(1),4(,1)         STORE ADDRESS OF ALTERNATE
         AGO   .IEATRCK                                        @G76XRYU
.LOADALT ANOP
         AIF   ('&ALTSZE'(K'&ALTSZE,1) EQ ')' OR '&MF' NE 'I').LAALT
         ORG   GTRM&NDX+4               PUT ALTERNATE SIZE     @G76XRYU
         DC    A(&ALTSZE)               IN PARM LIST           @G76XRYU
         ORG
         AGO   .IEATRCK                                        @G76XRYU
.LAALT   ANOP                     ...LA ADDR OR EXECUTE FORM   @G76XRYU
         LA    0,&ALTSZE                LOAD ADDR OF ALTERNATE @G76XRYU
         ST    0,4(,1)                  STORE ADD OF ALTERNATE @G76XRYU
.*  PROCESS ATTRIBUTE PARM FOR I AND E FORMS WHEN NOT NULL     @G76XRYU
.IEATRCK ANOP                                                  @G76XRYU
         AIF   ('&ATTRIB' EQ '').LABEL3               @G76XRYU @OW03892
         AIF   ('&ATTRIB'(1,1) EQ '(').REGATR                  @G76XRYU
         AIF   ('&ATTRIB'(K'&ATTRIB,1) EQ ')' OR '&MF' NE 'I').LAATRIB
         ORG   GTRM&NDX+8               PUT ATTRIB BYTE ADDR   @G76XRYU
         DC    A(&ATTRIB)               IN PARM LIST           @G76XRYU
         ORG
         AGO   .LABEL3                                @G76XRYU @OW03892
.LAATRIB ANOP                       .. LA ADDR OR EXECUTE FORM @G76XRYU
         LA    0,&ATTRIB                GET ADR OF ATTRIB BYTE @G76XRYU
         ST    0,8(1)                   PUT IN 3RD PARM WORD   @G76XRYU
         AGO   .LABEL3                                @G76XRYU @OW03892
.REGATR  ANOP                                                  @G76XRYU
         ST    &ATTRIB(1),8(1)          REG => 3RD PARM WORD   @G76XRYU
.*  PROCESS TERMINAL ID PARM FOR I AND E FORMS WHEN NOT NULL   @OW03892
.LABEL3  ANOP                                                  @OW03892
         AIF   ('&TERMID' EQ '').SVCENTY                       @OW03892
         AIF   ('&TERMID'(1,1) NE '(').LOTRMID                 @OW03892
         ST    &TERMID(1),12(,1)        STORE PRIMARY ADDRESS  @OW03892
         OI    12(1),128                END OF LIST INDICATOR  @OW03892
         AGO   .SVCENT2                                        @OW03892
.LOTRMID ANOP                                                  @OW03892
         AIF   ('&TERMID'(K'&TERMID,1) EQ ')' OR '&MF' NE 'I').LTERM
.*                                                             @OW03892
         ORG   GTRM&NDX+12              PUT ADDR OF TERMID IN  @OW03892
         DC    XL1'80',AL3(&TERMID)     PARM LIST WITH END OF  @OW03892
.*                                      LIST INDICATOR         @OW03892
         ORG
         AGO   .SVCENT2                                        @OW03892
.LTERM   ANOP                      ..LA ADDR OR EXECUTE FORM   @OW03892
         LA    0,&TERMID                LOAD ADDRESS OF TERMINAL ID
.*                                                             @OW03892
         ST    0,12(,1)                 STORE ADDRESS OF TERMINAL ID
.*                                                             @OW03892
         OI    12(1),128                END OF LIST INDICATOR  @OW03892
         AGO   .SVCENT2                                        @OW03892
.SVCENTY ANOP
         OI    8(1),128                 END OF LIST INDICATOR  @G76XRYU
.SVCENT2 ANOP
         LA    0,17                     ENTRY CODE
         SLL   0,24                     SHIFT TO HIGH ORDER BYTE
         SVC   94                       ISSUE SVC
         MEXIT
.***************  L  - FORM  ***************************
.LFORM   ANOP
&NAME    DS    0F
         AIF   ('&PRMSZE' EQ '').NOPRMAD
         AIF   ('&PRMSZE'(1,1) EQ '(').NOPRMAD
         DC    A(&PRMSZE)               ADDRESS OF PRIMARY PARM ADDR
         AGO   .CHKALT
.NOPRMAD ANOP
         DC    A(0)                     ADDRESS OF PRIMARY PARM ADDR
.CHKALT  AIF   ('&ALTSZE' EQ '').NOALTAD
         AIF   ('&ALTSZE'(1,1) EQ '(').NOALTAD
         DC    A(&ALTSZE)               ADDRESS OF ALTERNATE ADDR
         AGO   .LATTCK                                         @G76XRYU
.NOALTAD ANOP
         DC    A(0)                     ADDR OF ALTERNATE      @G76XRYU
.*  PROCESS ATTRIBUTE PARM FOR LIST FORM                       @G76XRYU
.LATTCK  ANOP                                                  @G76XRYU
         AIF   ('&ATTRIB' NE '').CKATTR                        @G76XRYU
         DC    A(0)                     L-FORM--ATTRIB BYTE    @G76XRYU
         AGO   .CKTERM                                @G76XRYU @OW03892
.CKATTR  ANOP                                                  @G76XRYU
         AIF   ('&ATTRIB'(1,1) NE '(').ATTROK                  @G76XRYU
         MNOTE 12,'IHB300 INCOMPATIBLE OPERANDS: MF=L AND ATTRIB=&ATTRI*
               B'                                              @G76XRYU
         AGO   .CKTERM                                @G76XRYU @OW03892
.ATTROK  ANOP                                                  @G76XRYU
         DC    A(&ATTRIB)               L-FORM--A(ATTR BYTE)   @G76XRYU
         AGO   .CKTERM                                         @OW03892
.CKTERM  AIF   ('&TERMID' EQ '').NOTRMAD                       @OW03892
         AIF   ('&TERMID'(1,1) EQ '(').NOTRMAD                 @OW03892
         DC    A(&TERMID)               ADDRESS OF TERMINAL ID ADDR
.*                                                             @OW03892
         MEXIT                                                 @OW03892
.NOTRMAD ANOP                                                  @OW03892
         DC    A(0)                     ADDRESS OF TERMINAL ID ADDR
.*                                                             @OW03892
         MEXIT                                                 @OW03892
.ERROR1  ANOP
         IHBERMAC 1006,PRMSZE
         MEXIT
.ERROR2  IHBERMAC 54,,&MF
         MEXIT
         MEND
