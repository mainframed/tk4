         MACRO
&NAME    IEDQMSG   &MSGID=,&FUNCT=,&INBUF=,&LINK=,&INCNT=,&MSG=,&RET=, X
               &RETREG=R14,&PREG1=PARMREG1,&PREG0=PARMREG0,            X
               &OTBUF=,&OTCNT=
         LCLB      &ID,&FN,&OCT,&IBF,&OBF,&MG,&LN,&RT,&ICT
         LCLC      &FLAGS,&FLAGS1,&REPCNT,&RETURN
*
*        TOTE MESSAGE CALL MACRO
*
*        STATUS = VERSION 10.0                                 @Y17XAUU
*
&ID      SETB      (T'&MSGID NE 'O')
&FN      SETB      (T'&FUNCT NE 'O')
&ICT     SETB      (T'&INCNT NE 'O')
&IBF     SETB      (T'&INBUF NE 'O')
&OBF     SETB      (T'&OTBUF NE 'O')
&MG      SETB      (T'&MSG NE 'O')
&RT      SETB      (T'&RET NE 'O')
&LN      SETB      (T'&LINK NE 'O')
&OCT     SETB      (T'&OTCNT NE 'O')
         AIF       (&ID+&MG+&OBF GT 1).MGER010
         AIF       (&OCT+&OBF EQ 1).MGER030
         AIF       (&ID+&MG+&OBF EQ 0).BYP010
         AIF       (&FN NE 1).MGER040
.BYP010  ANOP
         AIF       (&ID+&MG+&OBF+&LN EQ 0).MGER050
         AIF       (&RT EQ 0).BYP020
         AIF       (&ID+&MG+&OBF EQ 0).MGER060
.BYP020  ANOP
         AGO       .CONT010
.MGER010 ANOP
         MNOTE 12,'ONLY ONE PARM FROM MSGID, MSG AND OTBUF ALLOWED'
         MEXIT
.MGER030 ANOP
         MNOTE 12,'OTBUF AND OTCNT MUST ALWAYS APPEAR TOGETHER'
         MEXIT
.MGER040 ANOP
         MNOTE 12,'FUNCT MUST BE SPECIFIED WITH MSGID,MSG OR OTBUF'
         MEXIT
.MGER050 ANOP
         MNOTE 12,'MSGID,MSG,OTBUF OR LINK MUST BE SPECIFIED'
         MEXIT
.MGER060 ANOP
         MNOTE 12,'RET MUST APPEAR WITH MSGID,MSG OR OTBUF'
         MEXIT
.CONT010 ANOP
&RETURN  SETC      '$MRT&SYSNDX'
         AIF   (T'&RET EQ 'O').BYP060
&RETURN  SETC  '&RET'
.BYP060  ANOP
         AIF   (&ID+&OBF+&MG EQ 0).BYP250
         AIF   (N'&FUNCT EQ 0).BYP130
&REPCNT  SETC      '0'
         AIF   (T'&INCNT EQ 'O').BYP120
&REPCNT  SETC      '&INCNT'
.BYP120  ANOP
&FLAGS   SETC      '$&FUNCT(1)'
         AIF   (N'&FUNCT EQ 1).BYP180
         AIF   (N'&FUNCT EQ 2).BYP160
.BYP130  ANOP
         MNOTE 12,'MISSING OR INVALID FUNCT= OPERAND'
         MEXIT
.BYP160  ANOP
&FLAGS1  SETC      '$&FUNCT(2)'
.BYP180  ANOP
&NAME    DS    0H .               TOTE MESSAGE CALL
         LA    &PREG0,&RETURN .        GET RETURN ADDRESS
         BAL   &PREG1,$MSGLINK .        BRANCH TO LINKAGE SUBROUTINE
         AIF   (T'&MSGID EQ 'O').BYP190
         DC    AL2(&MSGID) .         MESSAGE IDENTIFIER
         AGO   .BYP195
.BYP190  ANOP
         DC    XL2'8000' .        INDICATES MESSAGE IS SUPPLIED
* .                                 IN PARAMETER LIST
.BYP195  ANOP
         AIF   (N'&FUNCT EQ 2).BYP200
         DC    AL1(&FLAGS) .         FUNCTION FLAGS
         AGO   .BYP210
.BYP200  ANOP
         DC    AL1(&FLAGS+&FLAGS1) .  FUNCTION FLAGS           @Y17XAUU
.BYP210  ANOP
         DC    AL1(&REPCNT) .            REPLY BUFFER LENGTH
         AIF   (T'&OTBUF EQ 'O').BYP230
         DC    AL1(&OTCNT) .           OUT MESSAGE LENGTH
         DC    AL3(&OTBUF) .       OUT MESSAGE ADDRESS
         AGO   .BYP240
.BYP230  ANOP
         AIF   (T'&MSG EQ 'O').BYP240
         DC    AL1($MG&SYSNDX.E-$MSG&SYSNDX.) . OUT MESSAGE LENGTH
         DC    AL3($MSG&SYSNDX.) .    OUT MESSAGE ADDRESS
$MSG&SYSNDX DC    C&MSG
$MG&SYSNDX.E DS    0C
.BYP240  ANOP
.BYP250  ANOP
         AIF   (T'&LINK EQ 'O').BYP450
$MSGLINK DS    0H .               LINK ROUTINE FOR TOTE MSG CALL
         AIF   ('&LINK' EQ 'XCTL').BYP350
         LINK  EP=IEDQWK .        LINK TO TOTE MESSAGE MODULE
         LR    &RETREG,&PREG0 .          GET RETURN ADDRESS
         BR    &RETREG .              BRANCH TO RETURN POINT
         AGO   .BYP370
.BYP350  ANOP
         XCTL  (2,12),EP=IEDQWM2 .      GO TO TOTE TRACE MODULE  S99528
.BYP370  ANOP
*
$CEC     EQU   X'80' .            CONTROL TERMINAL MESSAGE FLAG
$REP     EQU   X'40' .            C.T. MESSAGE WITH REPLY FLAG
$PRT     EQU   X'20' .            PRINTER MESSAGE FLAG
$SRC     EQU   X'10' .            SOURCE MESSAGE FLAG          @Y17XAUU
.BYP450  ANOP
$MRT&SYSNDX DS    0H .                RETURN ADDRESS
         MEND
