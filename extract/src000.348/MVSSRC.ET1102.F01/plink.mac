         MACRO
&NAME    PLINK &LOAD=,&EPLIST=,&CALL=,&SIZE=,                          C
               &DELETE=,&RETURN=,&MF=
.*
.* MACRO NAME: PLINK
.*
.* STATUS:     CHANGE LEVEL 001
.*
.* FUNCTION:   TO PROVIDE THE LINKAGE NECESSARY TO EXECUTE THE PLINK
.*             MODULE. THE MACRO GENERATES A PARAMETER LIST AND/OR
.*             LOADS REGISTER 15 WITH THE ADDRESS OF THE PLINK MODULE
.*             AND REGISTER 1 WITH THE ADDRESS OF A PARAMETER LIST.
.*
.* REGISTER 15 RETURN CODE:    (SET BY LOAD AND DELETE FUNCTIONS ONLY.)
.*
.*             CODE       MEANING
.*
.*              00       THE REQUESTED FUNCTION HAS BEEN ACCOMPLISHED
.*                       SUCCESSFULLY.
.*
.*              04       THE PLINK FACILITY IS NOT AVAILABLE.
.*
.*              08       AN ERROR OTHER THAN THOSE DESCRIBED HAS
.*                       OCCURRED.
.*
.*              0C       LOAD - THE REQUESTED MODULE IS NOT AVAILABLE
.*                       IN THE SYSTEM.
.*                       DELETE - THE SPECIFIED MODULE IS NOT IN CORE.
.*
.*              10       LOAD - THERE IS NOT ENOUGH CORE AVAILABLE.
.*                       DELETE - NEVER RETURNS THIS CODE.
.*
.* PARAMETERS AND/OR KEYWORDS:
.*
.*             LOAD(SUBLIST)-THE ENTRY POINT NAMES OF MODULES TO BE
.*                 LOADED. IF MORE THAN ONE, SEPARATED BY COMMAS AND
.*                 ENCLOSED IN PARENTHESES.
.*
.*             EPLIST-THE ADDRESS OF A LIST WHERE ENTRY POINT ADDRESSES
.*                    OF THE LOADED MODULES WILL BE STORED. MUST
.*                    ACCOMPANY THE LOAD PARAMETER.
.*
.*             SIZE-  INTEGER DEFINING THE TOTAL AMOUNT OF CORE IN 1K
.*
.*                    MULTIPLES REQUIRED TO RUN TEST.
.*             CALL-  MODULE TO TRANSFER CONTROL TO; TWO FORMS ALLOWED,
.*
.*                    1. ADDRESS- LOCATION CONTAINING THE ENTRY POINT.
.*
.*                    2. (15)   - INDICATES REGISTER 15 CONTAINS THE
.*                                ADDRESS. R15 MUST HAVE BEEN LOADED
.*                                PRIOR TO INVOKING PLINK.
.*
.* NOTE:       CALL=(15) CANNOT BE USED WHEN LOADING OR DELETING IN THE
.*             SAME PLINK AS CALL.
.*
.*             DELETE(SUBLIST)-SAME COMMENTS AS APPLY TO LOAD.
.*
.*             RETURN=YES- INDICATES CONTROL IS TO BE RETURNED TO THE
.*                         CALLING MODULE. ITS REGISTERS ARE RESTORED
.*                         AND A BRANCH IS MADE TO THE ADDRESS STORED
.*                         IN REGISTER 14.
.*
.*             MF= E,ADDRESS/(1) INDICATES THE EXECUTE FORM OF THE
.*                 MACRO WITH THE PARAMETER LIST ADDRESS EITHER
.*                 SPECIFIED OR PREVIOUSLY LOADED IN REGISTER 1.
.*
.*
.*             MF= L  INDICATES THE LIST FORM OF THE MACRO WITH NO
.*                    EXECUTABLE CODE GENERATED.
.*
.* RESTRICTIONS: LOAD AND RETURN MAY NOT BE SPECIFIED IN THE SAME
.*               MACRO.
.*               CALL AND RETURN MAY NOT BE SPECIFIED IN THE SAME
.*               MACRO.
.*
.*
.* WARNING:    IF CALL OR RETURN AND LOAD OR DELETE ARE USED IN THE
.*             SAME MACRO AND THE RETURN CODE IS NOT ZERO, A BRANCH
.*             WILL BE MADE AROUND THE RETURN OR CALL INSTRUCTIONS.
.*
.* CONTROL BITS SET BY THIS MACRO:
.*
.*             L&SYSNDX DC B'&L1&C&B&B&D&R&B&S'
.*                            ×  × × × × × × ×
.*                            ×  × 0 0 × × 0 ×
.*                            ×  ×     × ×   SIZE
.*                            ×  ×     × RETURN
.*                            ×  ×     DELETE
.*                               CALL
.*                            LOAD
.*
.* GLOBALS SET OR RESET BY THIS MACRO:
.*
.*             &CR AND &MOD(255)
.*
.* GLOBALS EXAMINED BY THIS MACRO: NONE
.*
.* SYMBOLS GENERATED BY THIS MACRO: NONE
.*
.* MNOTES ISSUED BY THIS MACRO:
.*
.*             MNOTE 8,'DELETE AND RETURN INVALID.'
.*
.*             MNOTE 8,'DELETE AND CALL INVALID.'
.*
.*             MNOTE 8,'DELETE AND LOAD INVALID.'
.*
.*             MNOTE 8,'LOAD AND RETURN INVALID.'
.*
.*             MNOTE 8,'CALL AND RETURN INVALID.'
.*
.*             MNOTE 8,'THERE IS NO LIST EXPANSION FOR CALL.'
.*
.*             MNOTE 8,'THERE IS NO LIST EXPANSION FOR RETURN.'
.*
.*             MNOTE 8,'ENTRY POINT LIST ADDRESS MISSING.'
.*
.*             MNOTE 8,'NO MACRO OPERANDS SELECTED.'
.*
.*             MNOTE 8,'REG 15 CANNOT BE USED FOR CALL ADDRESS WHEN
.*                      LOAD OR DELETE ARE SPECIFIED.'
.*
.* REASONS:    ABOVE MNOTES ARE SELF EXPLANATORY.
.*
.* EXAMPLE:
.*
.* *******************************************
.* *  NAME  * OPERATION *       OPERAND      *
.* *******************************************
.* *        *           *                    *
.* * SYMBOL *   PLINK   * LOAD=(SUBLIST),    *
.* *        *           * EPLIST=ADDR,       *
.* *        *           * SIZE=INTEGER,      *
.* *        *           * CALL=ADDR/(15),   *
.* *        *           * DELETE=(SUBLIST),  *
.* *        *           * RETURN=YES,        *
.* *        *           * MF=(E,ADDR/(1))    *
.* *        *           *   =L               *
.* *        *           *                    *
.*
         GBLA  &CR
         GBLC  &MOD(255)
         LCLA  &C1,&C2,&CTR,&N
         LCLB  &L1,&C,&B,&D,&R,&S,&E
         LCLC  &FILEN
&L1      SETB  (T'&LOAD NE 'O')   SET TO 1 IF PARAMETER EXISTS.
&C       SETB  (T'&CALL NE 'O')
&D       SETB  (T'&DELETE NE 'O')
&R       SETB  (T'&RETURN NE 'O')
&S       SETB  (T'&SIZE NE 'O')
&E       SETB  (T'&EPLIST NE 'O')
.*
.* TEST FOR INVALID COMBINATIONS
.*
         AIF   (&L1+&R GT 1).ERR1
         AIF   (&C+&R GT 1).ERR2
         AIF   (&L1+&D GT 1).ERR8
         AIF   (&C+&D GT 1).ERR9
         AIF   (&D+&R GT 1).ERR10
.*
         AIF   (T'&NAME NE 'O').N1
&FILEN   SETC  'A&SYSNDX'
         AGO   .N2
.N1      ANOP
&FILEN   SETC  '&NAME'
.*
.N2      AIF   (&CR NE 0).T2
&CR      SETA  1
.*
.T2      ANOP
&C1      SETA  N'&LOAD            C1=# OF LOAD MODULES
&C2      SETA  N'&DELETE          C2=# OF DELETE MODULES
.*
         AIF   (T'&MF EQ 'O').STD .GO TO STD FORM IF MF OMITTED
         AIF   ('&MF(1)' EQ 'E').EXEC .GOTO EXEC FORM IF MF=E
         AIF   ('&MF(1)' EQ 'L').LISTA .GOTO LIST FORM IF MF=L
.*
.STD     AIF   (&L1 NE 0).LOADA . TEST FOR LOAD PARAM.
         AGO   .DELA .            NO, TRY DELETE
.LOADA   AIF   (&L1+&C GT 1).LOADC . IS IT LOAD&CALL?
.LOADB   ANOP
&FILEN   L     R15,#TABLE         TABLE OF EXECUTIVE ENTRY POINTS
&FILEN   SETC  ' '
         L     R15,28(R15)        ENTRY TO TRANSIENT MANAGER
         LA    R14,R&SYSNDX       RETURN ADDRESS
         CNOP  2,4
         BALR  R1,R15
         AGO   .LISTB             SET UP PARAMETER LIST
.LOADC   AIF   ('&CALL'(1,1) NE '(').LOADB  CHECK FOR REGISTER 15
         AGO   .ERR7
.*
.DELA    ANOP
         AIF   (&D EQ 0).CALLA
         AGO   .LOADB
.CALLA   ANOP
         AIF   (&C NE 1).RETC
.CALLB   AIF   ('&CALL'(1,1) EQ '(').CALLC
&FILEN   L     R15,&CALL
&FILEN   SETC  ' '
         BALR  R14,R15
         MEXIT
.CALLC   ANOP
&FILEN   L     R15,0(R15)
&FILEN         SETC ' '
&FILEN   BALR  R14,R15            BRANCH TO ADDRESS IN R15
         MEXIT
.*
.EXEC    ANOP
         AIF   (N'&MF GE 2).EXECA
         AIF   (&C EQ 1).CALLB
.RETC    ANOP
         AIF   (&R EQ 0).ERR6
&FILEN   L     R13,4(R13)         GET OLD SAVE AREA ADDRESS
&FILEN   SETC  ' '
         LM    R14,R12,12(R13)    RESTORE CALLING PROGRAM'S REGISTERS.
         BR    R14                RETURN
         MEXIT
.EXECA   AIF   ('&MF(2)'(1,1) EQ '(').R1
&FILEN   LA    R1,&MF(2) .        LOAD R1 WITH PARAMETER LIST ADDRESS.
&FILEN   SETC  ' '
.R1      ANOP
&FILEN   L     R15,#TABLE
&FILEN   SETC  ' '
         L     R15,28(R15)
         BALR  R14,R15
         AIF   (&C EQ 0).EXECB
         AIF   ('&CALL'(1,1) EQ '(').ERR7
.CALLD   ANOP
         LTR   R15,R15            TEST RETURN CODE
         BNZ   E&SYSNDX           IF NOT ZERO, BRANCH AROUND CALL
         L     R15,&CALL
         BALR  R14,R15
E&SYSNDX EQU   *
         MEXIT
.EXECB   ANOP
         AIF   (&R EQ 1).RETB
         MEXIT
.RETB    ANOP
         LTR   R15,R15            TEST RETURN CODE
         BNZ   E&SYSNDX
         L     R13,4(R13)         GET CALLING MODULES SAVE AREA
         LM    R14,R12,12(R13)    RESTORE REGISTERS
         BR    R14                RETURN TO CALLING MODULE
E&SYSNDX EQU   *
         MEXIT
.LISTA   ANOP
         AIF   (&C EQ 1).ERR3
         AIF   (&R EQ 1).ERR4
.*
.LISTB   ANOP
&FILEN   DC    A(P&SYSNDX) .      OLTEP PARAMETER LIST ADDRESS
&FILEN   SETC  ' '
         DC    A(L&SYSNDX) .      POINTER TO CONTROL BYTE
         AIF   (&L1 EQ 0).LISTC
         DC    A(L&SYSNDX+4) .    POINTER TO LOAD MODULE LIST
         AIF   (&E EQ 0).ERR5
         DC    A(&EPLIST) .       ENTRY POINT LIST ADDRESS
         AGO   .LISTD
.LISTC   ANOP
         DC    A(0)
         DC    A(0)
.LISTD   ANOP
         AIF   (&D EQ 0).LISTE
         DC    A(D&SYSNDX)        POINTER TO LIST OF DELETED MODULES
         AGO   .DELC
.LISTE   ANOP
         DC    A(0)
.DELC    ANOP
P&SYSNDX DC    B'00000000' .      INDICATORS FOR TRANSIENT MANAGER
         DC    AL1(1) .           MACRO LEVEL
         DC    CL2'28' .          IDENTITY OF MACRO EXPANSION
L&SYSNDX DC    B'&L1&C&B&B&D&R&B&S' .    CONTROL BYTE
         AIF   (T'&SIZE EQ 'O').QA
         DC    AL1(&SIZE)         CORE NEEDED IN 1K MULTIPLES
         AGO   .QB
.QA      ANOP
         DC    AL1(0)             CORE NEEDED IN 1K MULTIPLES
.QB      ANOP
         DC    AL1(&C1)           # OF LOAD MODULES
         DC    AL1(&C2)           # OF DELETED MODULES
         AIF   (&L1 EQ 0).DELB
&CTR     SETA  1
.P2      ANOP
         DC    CL8'&LOAD(&CTR)'
.P3      ANOP
&N       SETA  1
.P4      AIF   ('&LOAD(&CTR)' EQ '&MOD(&N)').EX2
         AIF   (&N EQ &CR).EX3
&N       SETA  &N+1
         AGO   .P4
.EX3     ANOP
         EXTRN &LOAD(&CTR)
&MOD(&CR) SETC '&LOAD(&CTR)'
&CR      SETA  &CR+1
.EX2     ANOP
         AIF   (&CTR EQ &C1).DELB
&CTR     SETA  &CTR+1
         AGO   .P2
.DELB    AIF   (&D EQ 0).DELD
D&SYSNDX EQU   *
&CTR     SETA  1
.PA      ANOP
         DC    CL8'&DELETE(&CTR)'
         AIF   (&CTR EQ &C2).DELD
&CTR     SETA  &CTR+1
         AGO   .PA
.DELD    ANOP
R&SYSNDX DS    0H                 RETURN ON HALFWORD BOUNDARY
.*
         AIF   (&C EQ 0).RETA
         AGO   .CALLD
.RETA    ANOP
         AIF   (&R EQ 1).RETB
         AIF   (&L1+&C+&D+&R EQ 0).ERR6
         MEXIT
.ERR1    MNOTE 8,'LOAD AND RETURN INVALID.'
         MEXIT
.ERR2    MNOTE 8,'CALL AND RETURN INVALID.'
         MEXIT
.ERR3    MNOTE 8,'THERE IS NO LIST EXPANSION FOR CALL.'
         MEXIT
.ERR4    MNOTE 8,'THERE IS NO LIST EXPANSION FOR RETURN.'
         MEXIT
.ERR5    MNOTE 8,'ENTRY POINT LIST ADDRESS MISSING.'
         MEXIT
.ERR6    MNOTE 8,'NO MACRO OPERANDS HAVE BEEN CHOSEN.'
         MEXIT
.ERR7    MNOTE 8,'REG 15 CANNOT BE USED FOR CALL ADDRESS WHEN LOAD OR  C
                DELETE ARE SPECIFIED.'
         MEXIT
.ERR8    MNOTE 8,'DELETE AND LOAD INVALID.'
         MEXIT
.ERR9    MNOTE 8,'DELETE AND CALL INVALID.'
         MEXIT
.ERR10   MNOTE 8,'DELETE AND RETURN INVALID.'
         MEXIT
         MEND
