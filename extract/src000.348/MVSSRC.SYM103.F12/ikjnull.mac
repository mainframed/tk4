IKJNULL  CSECT
*/*IKJNULL: CHART */
*/* HEADER
*/*SET ERROR CODE IN XSA, THEN XCTL TO IEE0503D     */
*/*           E  IKJNULL */
***********************************************************************
*                                                                     *
*STATUS:     CHANGE LEVEL 000                                         *
*                                                                     *
*TITLE:      IKJNULL                                                  *
*FUNCTION:   SETS THE ERROR CODE IN THE EXTENDED SAVE AREA(XSA).      *
*            THE ERROR CODE IS THEN USED AS AN INDEX INTO THE         *
*            PRE-FORMATTED MESSAGES IN IEE0503D.                      *
*                                                                     *
*ENTRY:      IKJNULL-XCTL FROM IEE0403D                               *
*                                                                     *
*INPUT:      NONE.                                                    *
*                                                                     *
*OUTPUT:     R2 POINTS TO THE EXTENDED SAVE AREA CONTAINING:          *
*              1. A ONE-BYTE MESSAGE CODE IN XAE WHICH IS ASSOCIATED  *
*                WITH A SPECIFIC PRE-FORMATTED MESSAGE IN IEE0503D.   *
*              2. A DOUBLE WORD XCTL NAME IN XAX                      *
*              3. A POINTER TO THE XCTL NAME IN XAP                   *
*                                                                     *
*EXTERNAL REFERENCES:                                                 *
*            NONE                                                     *
*                                                                     *
*EXIT:       NORMAL-XCTL TO IEE0503D THE MESSAGE ASSEMBLY MODULE.     *
*            ERROR: BR 14.                                            *
*                                                                     *
*TABLE/WORKAREAS:                                                     *
*            XSA                                                      *
*                                                                     *
*ATTRIBUTES:                                                          *
*            REUSABLE                                                 *
*                                                                     *
*NOTES:                                                               *
*            THIS MODULE IS NOT CHARACTER CODE DEPENDENT              *
***********************************************************************
          TITLE 'IKJNULL - SET UP TSO NOT SUPPORTED MESSAGE'
***********************************************************************
*              REGISTER EQUATES                                       *
***********************************************************************
R0       EQU   0
R1       EQU   1
R2       EQU   2                       BASE REGISTER FOR IEEXSA
R10      EQU   10                      BASE REG FOR MODULE
R14      EQU   14                      ADDRESS OF RETURN
R15      EQU   15                      ADDRESS OF IEE0503D-MSG MODULE
N0       EQU   0
N4       EQU   4
N8       EQU   8
N16      EQU   16
N26      EQU   26                      MESSAGE ERROR CODE
N96      EQU   96                      DISPLACEMENT OF XSA FROM THE RB
*/*           P  SET ADDRESSIBILITY FOR CSECT */
         BALR  R10,N0                  SET BASE FOR ADDRESSIBILITY
         USING *,R10
         MODID   BRANCH=YES                                     Y01886
         L     R2,N16                  ADDRESS OF THE CVT
         L     R2,N0(R2)               ADDRESS OF THE NEW/OLD TCB
         L     R2,N4(R2)               ADDRESS OF THE CURRENT TCB
         L     R2,N0(R2)               ADDRESS OF THE RB
         LA    R2,N96(R2)              ADDRESS OF THE XSA
*/*           P  SET ADDRESSIBILITY FOR EXTENDED SAVE AREA */
         USING MXSA,R2                 SET ADDRESSIBILITY FOR IEEXSA
*/*           P  SET ERROR MSG CODE IN XSA */
         MVI   XAE,N26                 SET MESSAGE INDEX IN XSA
*/*           P  SET UP REMAINING FIELDS IN XSA */
         MVC   XAX(N8),XCT0503D        SET UP XCTL NAME IN XSA
         XC    XAD(N4),XAD             SET DCB PTR TO ZERO
         LA    R15,XAX                 GET PTR TO XCTL NAME
         ST    R15,XAP                 SET POINTER TO XCTL NAME
         LA    R15,XAP                 SET POINTER TO XSA
*/*           R  XCTL TO IEE0503D TO PUT OUT ERROR MSG */
         XCTL  SF=(E,(R15))            XCTL TO IGC0503D
         BR    R14                     RETURN IF ANY ERROR
XCT0503D DC    C'IGC0503D'
MXSA     DSECT
         IEEXSA
*/*IKJNULL:   END END OF IKJNULL */
         END
