P20      TITLE 'IKJEFP20   GENERAL SCAN MODULE FOR PARSE               *
               VERSION FOUR DATE 12/7/72'
**********************************************************************
*
* TITLE -- IKJEFP20-  GENERALIZED SCAN MODULE FOR PARSE
*
* STATUS -- VERSION FOUR
*
*
* FUNCTION/OPERATION -- GENERALIZED SCAN SYNTAX CHECKS PARAMETERS FOR
*     PARSE AND ACCORDING TO CONTROL INFORMATION
*     SET UP BY THE CALLER FOR THE SCAN. THIS ROUTINE ALSO TRANSLATES
*     A PARAMETER TO UPPER CASE.
*
* ENTRY POINTS -- GENSCAN -- ENTRY POINT AT WHICH SYNTAX CHECKING OF  A
*     PARAMETER IS DONE ACCORDING TO CONTROL INFORMATION PASSED.
*    TRANSQ -- ENTRY POINT AT WHICH A PARAMETER IS TRANSLATED TO UPPER-
*     CASE.
*    TRANSX -- ENTRY POINT TO TRANSLATE TO UPPERCASE IF PARAMETER
*     IS KNOWN NOT TO BE DEFAULTED.
*    TRTAB -- ENTRY POINT TO ACCESS TABLE ASSIGNING A CHARACTER CLASS
*     TO AN EBCIDIC CHARACTER.
*    UPPERTAB -- ENTRY POINT TO ACCESS TRANSLATE TABLE ONLY.
*
* INPUT -- PDWORD CONTAINS A POINTER TO A WORD CONTAINING
*     CONTROL INFORMATION
*      **********************************************************
*      / SCAN OPTIONS / FIRST CHARACTER /OTHER CHARACTER/MAXIMUM/
*      /              /    TYPE         /    TYPE       /LENGTH /
*      **********************************************************
*      0              1                 2               3
*     REGISTER FOUR CONTAINS POINTER TO PARAMETER TO BE SCANNED.
*     --TRANSLATION FUNCTION - PPOINTER CONTAINS ADDRESS
*     OF PARAMETER TO BE TRANSLATED
*     PLENGTH CONTAINS LENGTH OF PARAMETER.
*
* OUTPUT -- THE TRANSLATED PARAMETER IN ITS ORIGINAL BUFFER.
*
* EXTERNAL REFERENCES -- NONE
*
* EXITS- NORMAL-- FROM SCAN ROUTINE EXITS TO THE LINK REGISTER
*     ADDRESS
*      +0 - IF PARAMETER IS MISSING
*      +4 - IF PARAMETER IS INVALID
*      +8 - IF END OF BUFFER IS ALSO PARAMETER END
*      +12- IF PARAMETER NOT AT END OF BUFFER
*
*      --FROM TRANSLATION ROUTINE EXITS TO LINK REGISTER ADDRESS
*
* EXITS- ERROR-- NONE
*
* TABLES/WORKAREAS -- TRTAB-TABLE WHICH ASSIGNS CHARACTER CLASS TO
*    EACH EBCIDIC CONFIGURATION
*    CHARTYPE-TABLE WHICH SELECTS MASK FOR TYPE OF CHARACTER REQUIRED
*    BY ROUTINE WHICH TEST CHARACTER TYPE.
*    IKJEFPWA-MACRO MAPPING WORK AREA.
* ATTRIBUTES -- REENTRANT
*
* CHARACTER CODE DEPENDENCY -- CLASS C. THE OPERATION OF THIS MODULE
*     IS DEPENDENT UPON AN INTERNAL REPRESENTATION OF THE EXTERNAL CHAR
*     ACTER SET WHICH IS EQUIVALENT TO THE ONE USED AT ASSEMBLY TIME.
*     THE CODING HAS BEEN ARRANGED SO THAT REDEFINITION OF 'CHARACTER'
*     CONSTANTS,BY REASSEMBLY, WILL RESULT IN A CORRECT MODULE FOR THE
*     NEW DEFINITION.
*
* RELEASE 20 SUPPORT CODE -- 20035
*
* NOTES: FLAGGING CODES ARE R=DCRR,A=APAR,M=PTM,F=PTF,S=SUPPORT CODE
*
**********************************************************************
         EJECT
IKJEFP20 CSECT
*A 006796-006799                                                 F41448
*D 005677-005682                                               @ZA29352
         EXTRN POSITX1,GETCORE,STALOC,QSTR01,RANGE               Y02666
         ENTRY GENSCAN,TRANSQ,TRANSX,TRTAB,UPPERTAB
         SPACE
*
* REGISTER EQUATES.
*
R0       EQU   0                       SCRATCH/PARAMETER REGISTER --
*                                         MUST BE 0
R1       EQU   1                       SCRATCH/PARAMETER REGISTER --
*                                         MUST BE 1
R2       EQU   2                       GENERAL SCRATCH REGISTER
R3       EQU   3                       GENERAL SCRATCH REGISTER
XINPUT   EQU   4                       NEXT CHARACTER TO SCAN
XINPUTB  EQU   5                       LAST CHARACTER SCANNED USED TO
*                                         COMPUTE LENGTH OF SCANNED
*                                         DATA
XPCE     EQU   6                       IF CALLED BY PARSE, POINTS TO
*                                         THE CURRENT PCE. IF CALLED
*                                         BY COMMAND SCAN, NOT USED.
R7       EQU   7                       USED BUT RESTORED BASE REG.
*                                                                Y02666
R8       EQU   8                       WORK REGISTER
LINK2    EQU   8                       SECOND LEVEL RETURN REGISTER
LINK1    EQU   9                       FIRST LEVEL RETURN REGISTER
R9       EQU   9                       WORK REGISTER
R10      EQU   10                      NOT USED
R11      EQU   11                      NOT USED
R12      EQU   12                      NOT USED
WORKBASE EQU   13                      BASE REGISTER FOR COMMON
*                                         WORKSPACE -- MUST BE 13
R14      EQU   14                      SCRATCH REGISTER
R15      EQU   15                      REGISTER 15               Y02666
BASE     EQU   15                      LINKAGE REGISTER USED AS
*                                         BASE REGISTER
*
* BIT SETTINGS FOR CHARACTER TYPES USED BY TYPETEST
*
HEX      EQU   X'80'                   HEX CHARACTER
OLETTER  EQU   X'40'                   LETTER NOT A HEX CHARACTER
NATL     EQU   X'20'                   NATIONAL CHARACTER
NUMBER   EQU   X'10'                   NUMBER
SEPAR    EQU   X'08'                   SEPARATOR
NSEPDLIM EQU   X'04'                   DELIMITER THAT IS NOT A
*                                         A SEPARATOR
NDLIMSPC EQU   X'02'                   SPECIAL CHARACTER THAT IS NOT
*                                         ALSO A DELIMITER OR A
*                                         SEPARATOR
CMDDLIM  EQU   X'01'                   COMMAND NAME DELIMITER
INVALID  EQU   X'00'                   INVALID CHARACTER
         SPACE
*
* BIT PATTERNS USED TO TEST THE OPTIONS SELECTED BY THE PARSE USER AND
* REFLECTED IN VARIOUS BYTES IN THE PCE.
*
PCEFPRPT EQU   B'00010000'             BIT 3  - PROMPT IS SPECIFIED
PCEFDFLT EQU   B'00001000'             BIT 4  - DEFAULT IS SPECIFIED
PCEFSUBF EQU   B'00000100'             BIT 5  - SUBFIELD IS SPECIFIED
PCEFHELP EQU   B'00000010'             BIT 6  - HELP IS SPECIFIED
PCEFVCHK EQU   B'00000001'             BIT 7  - VALIDITY CHECK EXIT IS
*                                         SPECIFIED
PCEFLIST EQU   B'10000000'             BIT 8  - LIST IS SPECIFIED
PCEFASIS EQU   B'01000000'             BIT 9  - NO TRANSLATION REQUIRED
PCEFRNGE EQU   B'00100000'             BIT 10 - RANGE IS SPECIFIED
PCEFINST EQU   B'00010000'             BIT 11 - INSERT IS SPECIFIED
PCEFASTK EQU   B'10000000'             BIT 0 - ASTERISK IS ALLOWED
PCEFMAXL EQU   B'01000000'             BIT 1  - MAXLNTH IS SPECIFIED
PCEFINTG EQU   B'00010000'             BIT 3 - INTEGER VALUE     Y02666
PCEFCHAR EQU   B'00001000'             BIT 4 - CHARACTER VALUE   Y02666
PCEFHEX  EQU   B'00000100'             BIT 5 - HEXADECIMAL VALUE Y02666
         SPACE
*
* OFFSETS FOR REQUIRED FIELDS IN PARSE PCE'S.
*
PCEPCLLN EQU   0                       PCL LENGTH FIELD IN IKJPARM
PCEPDLLN EQU   2                       PDL LENGTH FIELD IN IKJPARM
PCEKYEND EQU   4                       IKJKEYWD OR END-OF-FIELD OFFSET
*                                         IN IKJPARM
PCEFLGB1 EQU   0                       FLAG BYTE 1
PCEFLGB2 EQU   1                       FLAG BYTE 2
PCELEN   EQU   2 - 3                   PCE LENGTH
PCEPDEO  EQU   4 - 5                   PDE OFFSET INTO PDL
PCEPOST  EQU   6                       TYPE OF POSITIONAL PARAMETER
PCENAML  EQU   4                       LENGTH OF NAME FOR IKJNAME
PCENAMN  EQU   5 - N                   NAME SPECIFIED
PCEOPT   EQU   6                       IKJIDENT OPTION BYTE
PCEFIRST EQU   7                       IKJIDENT FIRST CHARACTER FLAGS
PCEOTHER EQU   8                       IKJIDENT OTHER CHARACTER FLAGS
PCEPARMT EQU   9                       IKJIDENT PARAMETER TYPE SEGMENT
         EJECT
*
* MISCELLANEOUS EQUATES
*
ZERO     EQU   0                       USED AS A 0
ONE      EQU   1                       USED AS A 1
TWO      EQU   2                       USED AS A 2
THREE    EQU   3                       USED AS 3                 Y02666
FOUR     EQU   4                       USED AS A 4
SIX      EQU   6                       USED AS SIX               Y02666
EIGHT    EQU   8                       USED AS A 8
TWELVE   EQU   12                      USED AS A 12
SIXTEEN  EQU   16                      USED AS 16                Y02666
TWENTY6  EQU   26                      USED AS 26                Y02666
TWENTY8  EQU   28                      USE TO SHIFT              Y02666
THIRTY1  EQU   31                      USED AS 31                Y02666
SEVEN    EQU   X'07'                                             Y02666
SEVENF   EQU   X'7F'                   FOR PACK INSTR.           Y02666
THIRTY2  EQU   32                                                Y02666
CC1      EQU   1                       CONDITION CODE 1
CC5      EQU   5                       CONDITION CODE 5
CC8      EQU   8                       CONDITION CODE 8
CC10     EQU   10                      CONDITION CODE 10 (EQ OR GT)
CC13     EQU   13                      CONDITION CODE 13
IPDLMAXE EQU   10                      MAXIMUM INPUT STACKING DEPTH FOR
*                                         A INPUT PUSHDOWN STACK
ASTERISK EQU   C'*'                    ASTERISK
LEFTPRN  EQU   C'('                    LEFT PARENTHESIS
RIGHTPRN EQU   C')'                    RIGHT PARENTHESIS
QUOTE    EQU   X'7D'                   QUOTE                     Y02666
HE0      EQU   X'E0'                   USED TO OBTAIN TYPE INDICATOR
*                                         FROM PCE FLAG BYTE
HF0      EQU   X'F0'                   EBCDIC ZERO               Y02666
HF1      EQU   X'F1'                   EBCDIC ONE                Y02666
HYPHEN   EQU   X'60'                   USED TO CHECK FOR HYPEN IN DATA
*                                      SET NAME                  Y02666
TWELVE0  EQU   X'C0'                   USED TO TEST FOR 12-0 MULTIPUNCH
*                                      IN A DATA SET NAME        Y02666
HFF      EQU   X'FF'                   USED TO RESET FLAGS       Y02666
DSNFLG   EQU   X'01'                   DSNAME IN PROCESS         Y02666
VALTYPE  EQU   X'1C'                   TEST FOR VALUE TYPES      Y02666
HEXFLG   EQU   X'20'                   HEXADECIMAL SPECIFIED OR ENTERED
*                                                                Y02666
DECFLG   EQU   X'10'                   DECIMAL ENTERED           Y02666
BINFLG   EQU   X'08'                   BINARY ENTERED            Y02666
CHARFLG  EQU   X'04'                   CHARACTER SPECIFIED       Y02666
NUMFLG   EQU   X'02'                   INTEGER SPECIFIED         Y02666
PDEHEX   EQU   X'01'                   INDICATE HEXADECIMAL ENTERED
VALENDF  EQU   X'40'                   INDICATE THAT END OF BUFFER WAS
*                                      NOT REACHED DURING HEX AND INTEG
*                                      PROCESSING                Y02666
*                                                                Y02666
BINSTOR  EQU   X'20'                   USED TO INDICATE WHETHER TO
*                                      STORE A BYTE OR NOT WHEN
*                                      CONVERTING FROM HEX TO BINARY
*                                                                Y02666
         SPACE
*
* PCE TYPE USED TO DETERMINE THE TYPE OF PCE BEING PROCESSED
*
KEYWDPCE EQU   X'40'                   KEYWORD PCE
RSVDPCE  EQU   X'A0'                   RESERVED WORD PCE FOR     F41448
*                                      COBOL                     F41448
     EJECT
GENSCAN  DS    0H                      GENERALIZED SCAN ROUTINE
         SPACE
*
* ESTABLISH ADDRESSABILITY USING THE CALL REGISTER (R15) AS A
* BASE REGISTER.
*
         USING *,BASE                  ESTABLISH ADDRESSABILITY
*                                         FOR GENSCAN
         MODID BRANCH=YES              FLAG                      Y01886
         USING PWORK,WORKBASE          ESTABLISH ADDRESSABILITY TO
*                                         COMMON WORKAREA
         L     R3,PDWORD               GET PTR TO GENSCAN CONTROL INFO
         USING GCONTROL,R3             ESTABLISH ADDRESSABILITY TO
*                                         GENSCAN CONTROL INFORMATION
         ST    LINK2,PLINKSV1          SAVE RETURN REGISTER      Y02666
         ST    R7,P20SAVE              SAVE BASE REG.FOR IKJEFP00Y02666
         LA    XINPUT,ONE(XINPUT)      GET NEXT CHARACTER
         TM    GOPTIONS,VALTYPE        IS VALUE TYPE PROCESSING
*                                      REQUIRED?                 Y02666
         BM    VALPROC                 YES, GO PROCESS IT        Y02666
         CLC   GFIRST(L'GFIRST+L'GOTHER),H0 IS THIS AN ANY/ANY
*                                         COMBINATION
         BZ    GS02                    IF YES BRANCH --- IF ASTERISK
*                                         WAS SUPPLIED IT IS REDUNDANT
*                                         WITH ANY/ANY OPTIONS
         SPACE
         TM    GOPTIONS,ASTKALWD       IS AN ASTERISK ALLOWED
         BZ    GS02                    NO, CONTINUE
         SPACE
         CLI   ZERO(XINPUT),ASTERISK   IS THERE AN ASTERISK IN THE
*                                         BUFFER
         BNE   GS02                    NO, CONTINUE
         SPACE
GS015    DS    0H                      ONE CHARACTER RETURN
         LA    XINPUT,ONE(XINPUT)      GET NEXT CHARACTER
         LR    XINPUTB,XINPUT          SET LAST CHAR SCANNED
         C     XINPUTB,ENDINPUT        IS SCAN AT END OF CURRENT INPUT
         BNL   VALTST                  YES,RETURN INDICATING THE Y02666
*                                         PARAMETER IS GOOD AND ENDED
*                                         AT THE END OF THE BUFFER
         TM    PFLAGS6,HEXFLG+NUMFLG   PROCESSING HEX OR NUMBER? Y02666
         BM    VALIDVAL                YES,GO CHECK DELIMITER    Y02666
         BO    VALIDVAL                                          Y02666
         SPACE
GSRET    DS    0H                                                Y02666
         NI    PFLAGS6,HFF-CHARFLG-HEXFLG-NUMFLG-DECFLG-BINFLG   Y02666
         B     TWELVE(LINK2)           NO, RETURN +12 INDICATING THE
*                                         PARAMETER IS GOOD BUT THE
*                                         DELIMITER MUST BE CHECKED
         SPACE
GS02     DS    0H                      CHECK FIRST CHARACTER
         XR    R2,R2                   CLEAR DEPTH METER
         XR    R1,R1                   CLEAR WORK REG
         TM    PFLAGS6,CHARFLG         CHARACTER VALUE TYPE?     Y02666
         BO    GS11                    YES, PROCESS 'ANY','ANY'  Y02666
         TM    PFLAGS6,NUMFLG+HEXFLG      ANY VALUE TYPE PROCESSING?
*                                                                Y02666
         BM    GS12                    YES,PROCESS AS 'ANY' TYPE Y02666
         BO    GS12                                              Y02666
         IC    R1,GFIRST               GET FIRST CHARACTER TYPE
*                                         INDICATOR AND USE AS INDEX
*                                         INTO TYPE DESIRED TABLE
GS11     DS    0H                                                Y02666
         IC    R1,CHARTYPE(R1)         SET TYPE DESIRED FOR TYPETEST
GS12     DS    0H                                                Y02666
         BAL   LINK1,TYPETEST          TEST FIRST CHARACTER SYNTAX
         SPACE
         B     GSMISS                  +0 RETURN - THE FIRST CHARACTER
*                                         IS INVALID.  RETURN IS TO
*                                         LINK2+0 INDICATING THE
*                                         PARAMETER IS MISSING
*
*                                      +4 RETURN - THE FIRST CHARACTER
*                                         IS VALID, CONTINUE
         SPACE
         CLI   ZERO(XINPUT),LEFTPRN    IS THE CHARACTER A LEFT PAREN
         BNE   GS03                    IF NO SKIP SETTING OF DEPTH
*                                         METER
         SPACE
         LA    R2,ONE                  SET DEPTH METER TO ONE
         SPACE
GS03     DS    0H                      * * * *
         ST    XINPUT,PPOINTR          SAVE PTR TO PARM
         TM    GOPTIONS,MAXLSPEC       IS A MAX LENGTH SPECIFIED
         BZ    GS04                    NO, CONTINUE
         SPACE
         CLI   GMAXLNTH,ONE            IS THE MAX LENGTH 1
         BE    GS015                   YES, RETURN AS WITH ASTERISK
         SPACE
         XR    R0,R0                   ZERO LOOP CONTROL REG
         IC    R0,GMAXLNTH             USE MAXLENGTH FOR CONTROL OF
*                                         SCAN LOOP
         SPACE
GS04     DS    0H                      * * * *
         TM    PFLAGS6,CHARFLG+NUMFLG+HEXFLG SPECIAL VALUE TYPE? Y02666
         BM    GSLOOP                  YES,INDICATOR ALREADY SET Y02666
         XR    R1,R1                   CLEAR WORK REG
         IC    R1,GOTHER               GET OTHER CHARACTER TYPE
*                                         INDICATOR AND USE AS INDEX
*                                         INTO TYPE DESIRED TABLE
         IC    R1,CHARTYPE(R1)         SET TYPE DESIRED FOR TYPETEST
         SPACE
GSLOOP   DS    0H                      OTHER LOOP
*
*        UPDATE POINTER TO CHECK THE NEXT INPUT CHARACTER        Y02666
*
         LA    XINPUT,ONE(XINPUT)      GET NEXT CHARACTER
         LR    XINPUTB,XINPUT          SET LAST CHARACTER SCANNED
         C     XINPUT,ENDINPUT         IS SCAN AT END OF CURRENT INPUT
         BNL   VALTST
         LA    R14,ONE(XINPUT)         GET NEXT BYTE ADDRESS     YM3610
         C     R14,ENDINPUT            IS END OF BUFFER NEXT     YM3610
         BC    CC10,GSTEST             BYPASS COMMENT DELIM TEST  M4540
         CLC   ZERO(TWO,XINPUT),SLASHAST COMMENT DELIMITER?      Y02666
         BNE   GSTEST                  NO, CONTINUE              Y02666
COMBYPAS DS    0H                                                YM3610
         TM    PFLAGS6,HEXFLG+NUMFLG   PROCESSING HEX OR INTEGER Y02666
         BZ    GSRET                   NO,RETURN                 Y02666
         B     VALIDVAL                GO PROCESS VALUE          Y02666
         SPACE
GSTEST   DS    0H                                                Y02666
         BAL   LINK1,TYPETEST          TEST CHARACTER SYNTAX
         SPACE
         B     GS06                   +0 RETURN - THE CHARACTER IS
*                                         AN INVALID OTHER CHARACTER.
*                                         SEE IF THE DEPTH METER
*                                         SHOULD BE CHECKED
         SPACE
*                                      +4 RETURN - THE CHARACTER IS
*                                         A VALID OTHER CHARACTER,
*                                         CONTINUE
         TM    PFLAGS6,CHARFLG         CHARACTER PROCESSING?     Y02666
         BO    GS09                    PROCESS AS 'ANY' TYPE     Y02666
         TM    PFLAGS6,HEXFLG+NUMFLG   HEX OR INTEGER?           Y02666
         BM    GS05                    GET MAXIMUM LENGTH        Y02666
         BO    GS05                                              Y02666
         SPACE
         CLI   GOTHER,ZERO             IS THE OTHER CHARACTER AN ANY
         BNE   GS05                    IF NO SKIP OVER FURTHER CHECKS
GS09     DS    0H                                                Y02666
         SPACE
         CLI   ZERO(XINPUT),LEFTPRN    IS THE NEXT CHARACTER A LEFT
*                                         PARENTHESIS
         BNE   GS05                    IF NO SKIP SETTING DEPTH METER
         SPACE
         LA    R2,ONE(R2)              INCREMENT DEPTH METER BY ONE
         SPACE
GS05     DS    0H                      * * * *
         TM    GOPTIONS,MAXLSPEC       IS A MAX LENGTH SPECIFIED
         BZ    GSLOOP                  NO, SCAN NEXT CHAR
         SPACE
         BCT   R0,GSLOOP               YES, DECREMENT LENGTH AND TEST
*                                         FOR 0, IF GREATER THAN 0 SCAN
*                                         NEXT CHARACTER
GS4      DS    0H                                                Y02666
         NI    PFLAGS6,HFF-HEXFLG-CHARFLG-NUMFLG-BINFLG-DECFLG
*                                      RESET VALUE TYPE FLAGS    Y02666
         SPACE
         B     FOUR(LINK2)             IF R0 REACHES 0, THE
*                                      MAXIMUM LENGTH HAS BEEN
*                                      EXCEEDED.  RETURN IS TO LINK2+4
*                                      INDICATING THE PARAMETER IS
*                                      INVALID
VALTST   DS    0H                                                Y02666
*
*      END OF BUFFER WAS REACHED. CHECK FOR PARAMETER TYPE       Y02666
*
         TM    PFLAGS6,NUMFLG+HEXFLG   HEX OR INTEGER?           Y02666
         BZ    GSRET8                  RETURN + 8, PARAMETER
*                                         IS GOOD AND ENDED AT
*                                         THE END OF THE BUFFER  Y02666
         B     VALINTGR                TEST FOR VALID INTEGER    Y02666
GSRET8   DS    0H                                                Y02666
         NI    PFLAGS6,HFF-CHARFLG-HEXFLG-DECFLG-NUMFLG-BINFLG   Y02666
         B     EIGHT(LINK2)
         SPACE
GS06     DS    0H                      * * * *
         CLI   ZERO(XINPUT),HYPHEN     IS CHARACTER A HYPHEN     Y02666
         BE    GS05                    YES, CHARACTER IS VALID   Y02666
         CLI   ZERO(XINPUT),TWELVE0    IS CHARACTER A 12-0 PUNCH Y02666
         BE    GS05                    YES, CHARACTER IS VALID   Y02666
GS08     DS    0H
         TM    PFLAGS6,CHARFLG         CHARACTER VALUE TYPE      Y02666
         BO    GS10                    YES,PROCESS AS 'ANY' TYPE Y02666
         TM    PFLAGS6,HEXFLG+NUMFLG  HEXADEC/INTEGER SPECIFIED? Y02666
         BO    VALIDVAL                PROCESS VALID VALUE       Y02666
         BM    VALIDVAL                                          Y02666
         CLI   GOTHER,ZERO             IS THE OTHER CHARACTER AN ANY
         BNE   GSRET                   RETURN TO LINK2+12 INDICATING
*                                         THE PARAMETER IS GOOD BUT THE
*                                         DELIMITER MUST BE CHECKED
         SPACE
GS10     DS    0H                                                Y02666
         CLI   ZERO(XINPUT),RIGHTPRN   IS CHARACTER A RIGHT PAREN
         BNE   GSRET                   IF NO EXIT
         SPACE
         BCTR  R2,ZERO                 DECREMENT DEPTH METER
         LTR   R2,R2                   IS THIS THE END OF THE PARAMETER
         BM    GSRET                   IF YES EXIT
         SPACE
         B     GS05                    NOT PARAMETER END CONTINUE SCAN
GSMISS   DS    0H
         NI    PFLAGS6,HFF-CHARFLG-HEXFLG-NUMFLG-DECFLG-BINFLG
*                                      TURN OFF FLAGS IN CASE NOPROMPT
         B     ZERO(LINK2)             RETURN TO NSI
         EJECT
***********************************************************************
*                                                                     *
*           ENTRY FOR PROCESSING THE SPECIFIC VALUE TYPES.            *
*        WHENEVER THE CHAR, HEX, AND INTEG OPERANDS ARE SPECIFIED     *
*        ON THE IKJIDENT MACRO, THIS ROUTINE WILL GET CONTROL TO      *
*        PERFORM THE NEEDED PROCESSING. HERE IT IS DETERMINED WHICH OF*
*        THE OPTIONS WAS SPECIFIED.                                   *
*                                                                     *
***********************************************************************
VALPROC  DS    0H                                                Y02666
         TM    GOPTIONS,PCEFINTG       WAS INTEGER OPTION SPEC.? Y02666
         BO    VALNUMT                 YES, PROCESS INTEGER TYPE Y02666
         TM    GOPTIONS,PCEFCHAR       IS CHARACTER SPECIFIED?   Y02666
         BO    VALCHAR                 YES, GO PROCESS           Y02666
         CLC   ZERO(TWO,XINPUT),EXQTE  IS INPUT A HEX VALUE      Y02666
         BE    VALHEXT                 YES, PROCESS A HEX  VALUE Y02666
         CLC   ZERO(TWO,XINPUT),EXXQTE IS INPUT A HEX VALUE      Y02666
         BE    VALHEXT                 YES, PROCESS A HEX  VALUE Y02666
***********************************************************************
*                                                                     *
*    A VALUE TYPE IS PROCESSED AS A CHARACTER VALUE IF:               *
*    1. THE 'CHAR' PARAMETER IS SPECIFIED ON THE IKJIDENT MACRO.      *
*    2. IF 'HEX' IS SPECIFIED AND THE USER'S INPUT IS NOT DELIMITED   *
*       BY THE X'...' CHARACTERS.                                     *
*                                                                     *
***********************************************************************
VALCHAR  DS    0H                                                Y02666
         OI    PFLAGS6,CHARFLG         INDICATE CHARACTER VALUE TYPE
*                                      PROCESSING                Y02666
         CLI   ZERO(XINPUT),QUOTE      IS FIRST CHAR. A QUOTE?   Y02666
         BE    VAL01                   PROCESS AS QUOTED STRING  Y02666
         B     GS02                    PROCESS AS 'ANY' TYPE     Y02666
VAL01    DS    0H                                                Y02666
*
*      PROCESS THE QUOTED STRING BY USING THE QSTRING ROUTINE IN MODULE
*      IKJEFP00                                                  Y02666
*
         L     R2,QSTRNGAD             GET QSTRING RTN PTR       Y02666
         ST    R15,P20SAVE+FOUR        SAVE P20 BASE REG.        Y02666
         BALR  LINK2,R2                GO PROCESS                Y02666
         L     R15,P20SAVE+FOUR        RESTORE BASE REG.         Y02666
         L     R3,PDWORD               RESTORE OPTIONS PTR.      Y02666
*                                      IT WAS DESTROYED BY QSTRING RTN
         NI    PFLAGS6,HFF-CHARFLG     TURN OFF FLAG             Y02666
         TM    GOPTIONS,MAXLSPEC       MAXIMUM LENGTH SPECIFIED? Y02666
         BZ    VALTEXIT                NO, RETURN                Y02666
         LR    R1,XINPUTB              GET SCAN PTR              Y02666
         S     R1,PPOINTR              COMPUTE LENGTH            Y02666
         XR    R0,R0                   CLEAR WORK REG.           Y02666
         IC    R0,GMAXLNTH             GET MAXIMUM LENGTH        Y02666
         L     LINK2,PLINKSV1          RESTORE RETURN REG.       Y02666
         CR    R1,R0                   CHECK LENGTH              Y02666
         BH    GS4                     IF TO0 LONG, - INVALID    Y02666
         B     VALEXIT1                NO,RETURN AS VALID        Y02666
         EJECT
***********************************************************************
*                                                                     *
*    THIS IS THE HEXADECIMAL NUMBER PROCESSING ROUTINE. CONTROL IS    *
*   RECEIVED HERE ONLY IF THE NUMBER IS ENTERED IN THE X'...' FORMAT  *
*   AND THE PARAMETER, 'HEX' OR 'INTEG' WAS SPECIFIED.                *
*                                                                     *
***********************************************************************
VALHEXT  DS    0H                                                Y02666
         LA    XINPUT,TWO(XINPUT)      UPDATE PAST HEX INDICATOR Y02666
         ST    XINPUT,PPOINTR          SAVE PTR TO HEX STRING    Y02666
         LR    XINPUTB,XINPUT          SET BACKUP REGISTER       Y02666
         OI    PFLAGS6,HEXFLG          INDICATE HEX PROCESSING   Y02666
         LA    R1,HEX+NUMBER           GET TABLE INDEX TYPE      Y02666
         SR    R2,R2                   CLEAR LEVELS CTR          Y02666
         B     GS12                    GO PROCESS                Y02666
VALIDHEX DS    0H                                                Y02666
         SPACE
*    A VALID HEXADECIMAL VALUE HAS BEEN FOUND.                        *
         SPACE
         LR    R7,XINPUTB              GET DATA END PTR          Y02666
         S     R7,PPOINTR              COMPUTE LENGTH            Y02666
         STH   R7,P20SAVE+FOUR         SAVE LENGTH               Y02666
         TM    GOPTIONS,PCEFINTG       PROCESSING INTEGER?       Y02666
         BZ    HEXNINT                 NO, GET CORE              Y02666
         BCTR  R1,ZERO                 DECREMENT INTEGER PTR.    Y02666
         LR    R14,R1                  GET OUTPUT PTR            Y02666
         TM    PFLAGS6,BINFLG          A BINARY NUMBER?          Y02666
         BZ    GET16                   NO,GET 16 AS LENGTH       Y02666
         LA    R7,THIRTY2              YES, GET 32 AS MAXLENGTH  Y02666
         B     SETLEN                  RESET LENGTH              Y02666
GET16    DS    0H                                                Y02666
*      GET LENGTH OF HEX OR DECIMAL INPUT AREA                   Y02666
         LA    R7,SIXTEEN              GET LENGTH OF INPUT AREA  Y02666
SETLEN   DS    0H                                                Y02666
*       SET THE LENGTH OF THE INTEGER                            Y02666
         LR    R2,R7                   GET LENGTH OF OUTPUT AREA Y02666
         SR    R8,R8                   CLEAR WORK REG            Y02666
         B     CONVT1                  GO CONVERT                Y02666
HEXNINT  DS    0H                                                Y02666
*      PREPARE TO GET CORE FOR AND CONVERT A HEXADECIMAL NUMBER  Y02666
         LR    R1,R7                   SAVE LENGTH OF INPUT      Y02666
         LA    R1,ONE(R1)              COMPUTE LENGTH OF OUTPUT  Y02666
         SRL   R1,ONE                  DIVIDE BY TWO             Y02666
         L     R7,P20SAVE              RESTORE IKJEFP00 BASE REG Y02666
         LR    R3,R15                  SAVE BASE REG. AROUND GETMAIN
*                                                                Y02666
         STH   R1,PLENGTH              SAVE LENGTH               Y02666
         L     R2,STALOCP              GET GETMAIN RTN PTR       Y02666
         BALR  LINK2,R2                GO TO GET CORE            Y02666
         LR    R15,R3                  RESTORE BASE REG.         Y02666
         LH    R7,P20SAVE+FOUR         GET DATA LENGTH           Y02666
         EJECT
***********************************************************************
*                                                                     *
*   THIS ROUTINE USES A TABLE TO CONVERT HEXADECIMAL CHARACTERS TO    *
*    VALID HEXADECIMAL DIGITS. THE LENGTH OF THE VALUE IS NOT         *
*    LIMITED BY THIS ROUTINE.                                         *
*                                                                     *
***********************************************************************
CONVERTX DS    0H                                                Y02666
         L     R3,PDWORD               RESTORE REG. 3            Y02666
         LH    R2,PLENGTH              GET LENGTH OF STRING      Y02666
         MVI   ZERO(R1),ZERO           ZERO CORE                 Y02666
         L     R14,PPOINTR             GET STRING PTR            Y02666
         BCTR  R14,ZERO                DECREMENT PTR FOR INDEXNG Y02666
         SR    R8,R8                   CLEAR TEMP. WORK REG.     Y02666
         BCTR  R1,ZERO                 DECREMENT PTR FOR INDEXNG Y02666
CONVT1   DS    0H                                                Y02666
*      GET THE FIRST BYTE FOR FIRST HALF OF THE HEXADECIMAL NUMBER
*                                                                Y02666
         IC    R8,ZERO(R7,R14)         GET LOWORDER BYTE         Y02666
         IC    R8,CONVTAB(R8)          GET TRUE HEX VALUE        Y02666
         SRDL  R8,THIRTY2              SHIFT DIGIT TO NEXT REG   Y02666
         BCT   R7,CONVA                DECREMENT LENTH -CONTINUE Y02666
         LA    R7,ONE                  REST FROM ZERO FOR NEXT
*                                      DECREMENT                 Y02666
         B     VALXSTOR                STORE LAST BYTE           Y02666
CONVA    DS    0H                                                Y02666
*      GET NEXT BYTE FOR 2ND HALF OF HEXADECIMAL NUMBER          Y02666
         IC    R8,ZERO(R7,R14)         GET NEXT BYTE             Y02666
         IC    R8,CONVTAB(R8)          GET TRUE VALUE            Y02666
VALXSTOR DS    0H                                                Y02666
*      STORE THE HEXADECIMAL BYTE                                Y02666
         SLL   R9,TWENTY8              PLACE DIGIT IN HIGH BYTE  Y02666
         SLDL  R8,FOUR                 MAKE DIGITS CONTIGUOUS    Y02666
         STC   R8,ZERO(R2,R1)          STORE BYTE                Y02666
         BCTR  R2,ZERO                 DECREMENT AFTER STORING   Y02666
         BCT   R7,CONVT1               DECREMENT AND CONTINUE    Y02666
         LA    R1,ONE(R1)              RESET PTR AFTER DECREMENT Y02666
         TM    PFLAGS6,NUMFLG          INTEGER VALUE?            Y02666
         BO    BINCOMP                 YES,GO COMPARE            Y02666
CONVEND  DS    0H                                                Y02666
*      PREPARE TO RETURN TO MAINLINE ROUTINE                     Y02666
         ST    R1,PPOINTR              SAVE PTR TO GOTTEN CORE   Y02666
         OI    PFLAGS6,PDEHEX          INDICATE HEX INPUT        Y02666
         B     VALTEXIT                RETURN TO IKJIDENT PROCESSOR
*                                                                Y02666
         EJECT
***********************************************************************
*                                                                     *
*    DETERMINE THE KIND OF INTEGER ENTERED AND SET CONTROL FOR        *
*    FOR DETERMINING A VALID DECIMAL INTEGER.                         *
*                                                                     *
***********************************************************************
VALNUMT  DS    0H                                                Y02666
         OI    PFLAGS6,NUMFLG          INDICATE AN INTEGER       Y02666
         CLC   ZERO(TWO,XINPUT),BINQTE TEST FO BINARY            Y02666
         BE    VALBINT                 PROCESS BINARY            Y02666
         CLC   ZERO(TWO,XINPUT),BINNQTE BINARY?                  Y02666
         BE    VALBINT                 PROCESS BINARY            Y02666
         CLC   ZERO(TWO,XINPUT),EXQTE  HEX INTEGER?              Y02666
         BE    VALHEXT                 PROCESS AS HEX            Y02666
         CLC   ZERO(TWO,XINPUT),EXXQTE HEX INTEGER               Y02666
         BE    VALHEXT                 PROCESS HEX               Y02666
         OI    PFLAGS6,DECFLG          INDICATE A DECIMAL INTEGR Y02666
         B     VALNUMBR                PROCESS DECIMAL           Y02666
VALBINT  DS    0H                                                Y02666
*      INDICATE A BINARY INTEGER IS IN PROCESS                   Y02666
         OI    PFLAGS6,BINFLG          INDICATE BINARY           Y02666
         LA    XINPUT,TWO(XINPUT)      BYPASS INDICATOR          Y02666
VALNUMBR DS    0H                                                Y02666
*      SET INPUT POINTER AND GO TO VALIDATE CHARACTERS           Y02666
*      PPOINTER IS USED TO GET THE LENGTH OF THE INPUT           Y02666
         ST    XINPUT,PPOINTR          RESET PTR TO STRING       Y02666
         LA    R1,NUMBER               GET TABLE INDEX VALUE     Y02666
         SR    R2,R2                   CLEAR LEVELS CTR.         Y02666
         B     GS12                    GO PROCESS                Y02666
         EJECT
***********************************************************************
*                                                                     *
*    THIS IS THE ABNORMAL OR ERROR EXIT ROUTINE FOR THE 'INTEG'       *
*    AND 'HEX' PROCESSING ROUTINES.                                   *
*                                                                     *
***********************************************************************
VALINV   DS    0H                                                Y02666
         NI    PFLAGS6,HFF-HEXFLG-NUMFLG-BINFLG-DECFLG-CHARFLG RESET
         NI    PFLAGS7,HFF-VALENDF     FLAGS                     Y02666
         L     R7,P20SAVE              RESTORE BASE REG.         Y02666
         L     LINK2,PLINKSV1          RESTORE RETURN PTR        Y02666
         L     R2,INVPSAVE             GET PTR TO INPUT          Y02666
         ST    R2,PPOINTR              RESET DATA PTR            Y02666
         B     FOUR(LINK2)             RETURN - INVALID          Y02666
         EJECT
***********************************************************************
*                                                                     *
*      CONTROL IS RECEIVED HERE FOR THE PURPOSE OF CHECKING THE       *
*      DELIMITER. IF IT IS VALID, CONTROL IS PASSED TO THE VALINTGR   *
*      ROUTINE. IF INVALID, CONTROL IS PASSED TO THE VALINV ROUTINE.  *
*                                                                     *
***********************************************************************
VALIDVAL DS    0H                                                Y02666
         OI    PFLAGS7,VALENDF         INDICATE NOT END OF BUFFERY02666
         L     R2,RANGEP               GET RANGE PROCESSOR PTR.  Y02666
         BALR  LINK1,R2                CHECK FOR POSSIBLE RANGE  Y02666
         B     VALDELC                 NO RANGE, CHECK DELIMITR  Y02666
         B     VALNQTE                 YES, SEE IF PERMISSABLE   Y02666
VALDELC  DS    0H                                                Y02666
*      THE NEXT CHARACTER IS NOT A COLON. CHECK FOR VALID DELIMITER
*      OR A SEPARATOR.                                           Y02666
         LA    R14,ONE(XINPUT)         GET NEXT BYTE ADDRESS     YM3610
         C     R14,ENDINPUT            IS IT END OF BUFFER       YM3610
         BC    CC10,COMBYPS2           YES,BYPASS COMMENT DELIM TEST
*                                                                 M4540
         CLC   ZERO(TWO,XINPUT),SLASHAST CHECK FOR COMMENT       Y02666
         BE    VALNQTE                 YES,SEE IF ALLOWED        Y02666
COMBYPS2 DS    0H                                                YM3610
         LA    R1,SEPAR+NSEPDLIM       GET INDEX FOR SEPARATOR
*                                      OR DELIMITER TEST         Y02666
         BAL   LINK1,TYPETEST          TEST                      Y02666
         B     VALQTEST                INVALID DELIMITER,TEST FOR QUOTE
*                                                                Y02666
VALNQTE  DS    0H                                                Y02666
*      A VALID DELIMITER OR SEPARATOR WAS FOUND                  Y02666
         TM    PFLAGS6,HEXFLG+BINFLG   PROCESSING BINARY/HEX?    Y02666
         BM    VALINV                  NO, DELIMITR IS INVALID   Y02666
         LR    XINPUTB,XINPUT          GET BACKUP SCAN PTR       Y02666
         B     VALINTGR                PROCESS VALID INTEGER     Y02666
         SPACE
VALQTEST DS    0H                                                Y02666
*  THE QUOTE IS THE ONLY INVALID DELIMITER ALLOWED. THIS IS SO ONLY IF*
*    IF THE NUMBER IS BINARY OR HEXADECIMAL.                          *
         SPACE
         CLI   ZERO(XINPUT),QUOTE      IS INVALID A QUOTE        Y02666
         BNE   VALINV                  PROCESS AS INVALID        Y02666
         TM    PFLAGS6,HEXFLG+BINFLG   IS QUOTE NEEDED?          Y02666
         BZ    VALINV                  NO,PROCESS AS INVALID     Y02666
         LR    XINPUTB,XINPUT          SET BACKUP FOR LENGTH     Y02666
         LA    XINPUT,ONE(XINPUT)      UPDATE PAST QUOTE         Y02666
         EJECT
***********************************************************************
*                                                                     *
*    CONTROL IS RECEIVED HERE WHEN IT IS DETERMINED THAT A VALID      *
*    INTEGER EXISTS. PROCESSING CONTINUES TO VALIDATE THE SIZE AND    *
*    TO CONVERT THE NUMBER TO A VALID HEXADECIMAL INTEGER WHICH CAN BE*
*    BE USED IN ANY NUMERIC COMPUTATION OR AS THE USER SEES FIT.      *
*                                                                     *
***********************************************************************
VALINTGR DS    0H                                                Y02666
         TM    GOPTIONS,PCEFHEX         A HEX VALUE?             Y02666
         BO    VALIDHEX                YES, PROCESS AS VALID     Y02666
         L     R8,PPOINTR              GET DATA PTR              Y02666
         LR    R2,XINPUTB              GET PTR                   Y02666
         SR    R2,R8                   COMPUTE DATA LENGTH       Y02666
         TM    PFLAGS6,BINFLG          BINARY NUMBER             Y02666
         BZ    CHECKHEX                NO, CHECK DECIMAL         Y02666
         CH    R2,MAXBINL              IS LENGTH TOO BIG?        Y02666
         BH    VALINV                  YES, INVALID              Y02666
         B     BINTST                  CONTINUE BINARY PROCESS   Y02666
CHECKHEX DS    0H                                                Y02666
*
*      CHECK FOR MAXIMUM HEXADECIMAL INTEGER LENGTH              Y02666
*
         TM    PFLAGS6,HEXFLG          IS HEX NUMBER             Y02666
         BZ    CHECKDEC                NO,CHECK INTEGER          Y02666
         CH    R2,MAXHEXL              IS NUMBER TOO BIG         Y02666
         BH    VALINV                  YES, INVALID.             Y02666
         B     VALPTR                  CONTINUE HEX PROCESS      Y02666
CHECKDEC DS    0H                                                Y02666
*
*      CHECK FOR MAXIMUM DECIMAL INTEGER LENGTH                  Y02666
*
         CH    R2,MAXNUML              IS NUMBER TOO LARGE?      Y02666
         BH    VALINV                  YES, PROCESS AS INVALID   Y02666
         B     VALPTR                                            Y02666
BINTST   DS    0H                                                Y02666
*      THE FOLLOWING CODE INSURES THAT EACH DIGIT IS A VALID BINARY
*      DIGIT (0-1).                                              Y02666
         LR    R1,R8                   GET PTR FOR LOOP          Y02666
         LR    R9,R2                   KEEP LENGTH FOR LOOP CTL. Y02666
         SPACE
VALBIN   DS    0H                                                Y02666
*    DETERMINE IF THE BINARY DIGITS ARE VALID                         *
         SPACE
         CLI   ZERO(R1),HF0            IS VALID BINARY CHAR.?    Y02666
         BE    VALBCT                  YES CONTINUE              Y02666
         CLI   ZERO(R1),HF1            VALID BINARY CHAR.?       Y02666
         BNE   VALINV                  PROCESS AS INVALID        Y02666
VALBCT   DS    0H                                                Y02666
         LA    R1,ONE(R1)              UPDATE TO NEXT CHARACTER  Y02666
         BCT   R9,VALBIN               VALID INPUT,CONTINUE      Y02666
         SPACE
VALPTR   DS    0H                                                Y02666
*
*    PREPARE TO CONVERT THE NUMBER TO A VALID HEXADECIMAL INTEGER     *
*                                                                Y02666
         MVI   INTEGER,HF0             CLEAR CORE                Y02666
         MVC   INTEGER+ONE(THIRTY1),INTEGER  CLEAR               Y02666
         TM    PFLAGS6,BINFLG          A BINARY NUMBER?          Y02666
         BZ    GETLEN                  GO GET MAX LENGTH         Y02666
         LA    R9,THIRTY2              GET MAX BINARY LENGTH     Y02666
         B     GETPTR                  GO GET PTR                Y02666
GETLEN   DS    0H                                                Y02666
*      IF NOT BINARY, THE MAXIMUM INTEGER LENGTH IS 16.          Y02666
         LA    R9,SIXTEEN              GET MAX. PACK LENGTH      Y02666
GETPTR   DS    0H                                                Y02666
*      INITIALIZE POINTERS FOR PACKING THE INTEGER               Y02666
         SR    R9,R2                   COMPUTE OFFSET            Y02666
         LA    R1,INTEGER              GET PTR TO AREA           Y02666
         AR    R9,R1                   COMPUTE ACTUAL ADDRESS    Y02666
         BCTR  R2,ZERO                 DECREMENT FOR EX.INSTR.   Y02666
         EX    R2,MVCDATA              MOVE INPUT TO WORKAREA    Y02666
         TM    PFLAGS6,HEXFLG+BINFLG   BINARY OR HEX?            Y02666
         BM    VALIDHEX                BYPASS MAXIMUM TEST       Y02666
         CLC   INTEGER+FOUR(TWELVE),MAXNUMC MAXIMUM NUMERIC?     Y02666
         BH    VALINV                  YES, TOO BIG              Y02666
VALPACK  DS    0H                                                Y02666
*      USE THE PACK INSTRUCTION TO PACK THE DECIMAL INTEGER      Y02666
         LA    R2,SEVENF               GET LENGTH FOR EX. PACKINGY02666
         EX    R2,PACKIT               PACK NUMBER               Y02666
         LA    R1,INTEGER+EIGHT        GET CONVERTED DATA PTR    Y02666
         CVB   R2,ZERO(R1)             CONVERT INPUT TO BINARY   Y02666
         ST    R2,INTEGER+TWELVE       SAVE DATA                 Y02666
         B     GETSTORE                GET CORE FOR NUMBER       Y02666
BINCOMP  DS    0H                                                Y02666
*      PREPARE TO CONVERT HEXADECIMAL DIGITS TO BINARY DIGITS    Y02666
         TM    PFLAGS6,BINFLG          A BINARY NUMBER?          Y02666
         BZ    GETSTORE                NO, CONTINUE              Y02666
         LA    R1,INTEGER              GET WORKAREA PTR.         Y02666
         BCTR  R1,ZERO                 DECREMENT FOR INDEXING    Y02666
         LA    R7,THIRTY2              GET LENGTH                Y02666
         LR    R14,R7                  GET MAX LENGTH            Y02666
         EJECT
BINTOHEX DS    0H                                                Y02666
***********************************************************************
*
*     THE FOLLOWING CODE IS THE HEX-TO-BINARY CONVERSION ROUTINE Y02666
*
***********************************************************************
         OI    PFLAGS7,BINSTOR         SET FOR FIRST DIGITOF BYTEY02666
         SR    R8,R8                   CLEAR WORK REG.           Y02666
BINHCONV DS    0H                                                Y02666
*      BEGIN TO PROCESS A NEW DIGIT.                             Y02666
         SR    R2,R2                   CLEAR COUNTER             Y02666
         IC    R8,ZERO(R1,R7)          GET A BYTE                Y02666
         SRDL  R8,FOUR                 GET FOUR DIGITS           Y02666
         SRL   R9,TWENTY8              GET LOWORDER BIT          Y02666
         SLL   R8,ONE                  GET 2ND LOWORDER BIT      Y02666
         AR    R2,R8                   GET SUM                   Y02666
         AR    R2,R9                   GET SUM                   Y02666
         BCT   R7,NEXTBYTE             INCREMENT FOR DECREMENT   Y02666
         LA    R7,ONE                  INCREMENT FOR DECREMENT   Y02666
         B     STORBIN                 GO TO STORE LAST DIGIT    Y02666
NEXTBYTE DS    0H                                                Y02666
*      BEGIN TO PROCESS THE 2ND HALF OF A DIGIT                  Y02666
         IC    R8,ZERO(R1,R7)          GET NEXT BYTE FOR NEXT 2 BITS
*                                                                Y02666
         SRDL  R8,FOUR                 ISOLATE FOUR DIGITS       Y02666
         SRL   R9,TWENTY6              GET 3RD LOWORDER BIT      Y02666
         SLL   R8,THREE                GET 4TH LOWORDER BIT      Y02666
         AR    R2,R8                   GET SUM                   Y02666
         AR    R2,R9                   GET SUM                   Y02666
         TM    PFLAGS7,BINSTOR         SHOULD BYTE BE STORED?    Y02666
         BZ    STORBIN                 YES,GO STORE              Y02666
         NI    PFLAGS7,HFF-BINSTOR     INDICATE BYTE IS TO BE STORED
*                                                                Y02666
         LR    R3,R2                   SAVE FIRST HALF OF BYTE   Y02666
         BCT   R7,BINHCONV             POINT TO NEXT BYTE        Y02666
         LA    R7,ONE                  INCREMENT FOR DECREMENT   Y02666
STORBIN  DS    0H                                                Y02666
*      STORE THE DIGITS AS A SINGLE BYTE                         Y02666
         SLL   R3,TWENTY8              PUT 2ND HALF BYTE I       Y02666
         SLDL  R2,FOUR                 GET BYTE FOR STORE        Y02666
         STC   R2,ZERO(R1,R14)         STORE BYTE                Y02666
         BCTR  R14,ZERO                DECREMENT STORAGE INDEX   Y02666
         BCT   R7,BINTOHEX             DECREMENT INPUT INDEX     Y02666
         MVC   INTEGER(SIXTEEN),INTEGER+SIXTEEN ALLIGN DATA      Y02666
         L     R3,PDWORD               RESTORE  OPTIONS PTR      Y02666
GETSTORE DS    0H                                                Y02666
*      GET PERMANENT STORAGE FOR THE CONVERTED DATA              Y02666
         L     R7,P20SAVE              RESTORE BASE REG.         Y02666
         LA    R1,FOUR                 GET MAX LENGTH            Y02666
         STH   R1,PLENGTH              SAVE AS RETURN LENGTH     Y02666
         L     R14,STALOCP             GET GETMAIN RTN^PTR       Y02666
         LR    R2,R15                  SAVE ADDRESSABILIYT REG.  Y02666
         BALR  LINK2,R14               GO GET CORE               Y02666
         LR    R15,R2                  RESTORE ADDRESS. REG      Y02666
         LA    R2,INTEGER+TWELVE       GET DATA PTR              Y02666
         ST    R1,PPOINTR              RESTORE                   Y02666
         MVC   ZERO(FOUR,R1),ZERO(R2)  MOVE DATA TO NEW AREA     Y02666
         EJECT
***********************************************************************
*                                                                     *
*   THIS IS THE NORMAL EXIT ROUTINE FOR THE HEXADECIMAL NUMBER AND THE*
*    BINARY OR DECIMAL INTEGER PROCESSING ROUTINES. HERE IT IS        *
*    DETERMINED IF THE END OF BUFFER HAS BEEN REACHED. IF SO, A RETURN*
*    OF EIGHT PAST THE RETURN ADDRESS IS MADE. IF NOT, A RETURN OF    *
*    TWELVE PAST THE RETURN ADDRESS IS MADE. A FLAG, VALENDF, IS      *
*    USED TO MAKE THIS THIS DECISION.                                 *
*                                                                     *
***********************************************************************
VALTEXIT DS    0H                                                Y02666
         L     R7,P20SAVE              RESTORE BASE REG          Y02666
         L     LINK2,PLINKSV1          RESTORE RETURN PTR        Y02666
VALEXIT1 DS    0H                                                Y02666
         NI    PFLAGS6,HFF-HEXFLG-BINFLG-CHARFLG-DECFLG   Y02666
*      DETERMINE IF END OF INPUT WAS REACHED.                    Y02666
         C     XINPUT,ENDINPUT         END OF INPUT?             Y02666
         BNL   VAL08                   YES, RETURN,CHECK DELIM.  Y02666
VALR12   DS    0H                                                Y02666
*      END OF INPUT WAS NOT REACHED. RETURN +12.                 Y02666
*
         NI    PFLAGS7,HFF-VALENDF     RESET END FLAG            Y02666
         B     TWELVE(LINK2)           RETURN TO IKJIDENT PROC.  Y02666
VAL08    DS    0H                                                Y02666
*      END OF FILE WAS REACHED.RETURN + 8 TO IDENT PCE PROCESSOR.Y02666
*
         B     EIGHT(LINK2)            RETURN +8                 Y02666
         EJECT
***********************************************************************
*                                                                     *
*                 CHARACTER TYPE TEST ROUTINE                         *
*                                                                     *
* THIS IS AN INTERNAL SUBROUTINE WHICH CHECKS THE CURRENT CHARACTER   *
* FOR BELONGING TO A SPECIFIED CHARACTER CLASS.                       *
*                                                                     *
* INPUT - R1=MASK OF DESIRED CHARACTER CLASS.                         *
*         XINPUT=POINTER TO CHARACTER TO BE CHECKED.                  *
*                                                                     *
* OUTPUT - THE RESULT OF THE TEST IS INDICATED BY THE                 *
*          LOCATION TO WHICH CONTROL IS RETURNED.                     *
*                                                                     *
* EXISTS - LINK1+0=THE CHARACTER DOES NOT BELONG TO THE SPECIFIED     *
*                  CHARACTER CLASS.                                   *
*                                                                     *
*        - LINK1+4=THE CHARACTER BELONGS TO THE CHARACTER CLASS       *
*                  SPECIFIED.                                         *
*                                                                     *
***********************************************************************
         SPACE
TYPETEST DS    0H                      * * * *
         XR    R14,R14                 CLEAR WORK REG
         IC    R14,ZERO(XINPUT)        USE CURRENT CHARACTER AS
*                                         OFFSET INTO TESTING TABLE
         LA    R14,TRTAB(R14)          GET ADDRESS IN TABLE
*                                         CORRESPONDING TO CHARACTER IN
*                                         QUESTION
         EX    R1,TYPETM               EXECUTE TEST UNDER MASK OF
*                                         TYPEBYTE FOR CHARACTER
*                                         SPECIFICATION GIVEN IN R1
*
         BC    CC5,FOUR(LINK1)         TYPE MATCHES, RETURN +4
*
         BR    LINK1                   TYPE DOESN'T MATCH, RETURN +0
         EJECT
***********************************************************************
*                                                                     *
*                       TRANSLATE INPUT DATA SUBROUTINE               *
*                                                                     *
*   THIS ROUTINE TRANSLATES THE INPUT DATA TO UPPERCASE UNLESS THE    *
* ASIS OPTION WAS SELECTED BY THE USER. THE INPUT DATA ADDRESS IS     *
* TAKEN FROM PPOINTR AND THE LENGTH FROM PLENGTH. IF THE DATA LIES    *
* WITHIN THE PCE (DEFAULTED) NO TRANSLATION TAKES PLACE.              *
*                                                                     *
***********************************************************************
         SPACE
TRANSQ   DS    0H                      TRANSLATION SUBROUTINE
         USING *,BASE                  ESTABLISH ADDRESSABILITY
*                                         FOR TRANSLATE ROUTINE
         SPACE
         MVC   PDWORD(ONE),PCEFLGB1(XPCE) MOVE PCE FLAG
*                                         BYTE TO WORKAREA
         NI    PDWORD,HE0              ISOLATE PCE TYPE INDICATORS
         CLI   PDWORD,KEYWDPCE         IS IT A KEYWORD PCE
         BE    TRANS                   YES, TRANSLATE
         SPACE
         CLI   PDWORD,RSVDPCE          IS IT A RESERVED WORD     F41448
*                                      PCE?                      F41448
         BO    TRANS                   IF YES, TRANSLATE         F41448
*                                      REGARDLESS                F41448
         SPACE
         TM    PCEFLGB2(XPCE),PCEFASIS SHOULD THE DATA BE TRANSLATED TO
*                                         UPPERCASE
         BCR   CC1,LINK1               NO RETURN TO CALLER
         SPACE
TRANS    DS    0H                      BRANCHED TO FOR TRANSLATION OF A
*                                         KEYWORD PARAMETER
         NC    PPOINTR+ONE(L'PPOINTR-ONE),PPOINTR+ONE IS THERE ANY DATA
*                                         TO CONVERT
         BCR   CC8,LINK1               IF NO RETURN TO CALLER
         SPACE
         CLC   PPOINTR+ONE(L'PPOINTR-ONE),PTABLEAD+ONE DOES THE DATA
*                                         LIE WITHIN THE PCL
         BL    TRANS1                  NO BRANCH - CANNOT BE DEFAULT
         SPACE
         CLC   PPOINTR+ONE(L'PPOINTR-ONE),PTABLEND+ONE DOES THE DATA
*                                         LIE WITHIN THE PCL
         BCR   CC13,LINK1              YES WITHIN PCL MUST BE DEFAULT
         B     TRANS1                  BRANCH AROUND TRANSX CODE
         EJECT
***********************************************************************
*                                                                     *
* IF TRANSLATE ROUTINE IS ENTERED AT ENTRY POINT TRANSX, ADJUST       *
* THE VALUE OF THE BASE REGISTER (WHICH IS ALSO THE CALL REGISTER)    *
* SO THAT LOCATIONS ABOVE THIS ENTRY POINT WILL BE ADDRESSABLE        *
* BY INSTRUCTIONS BELOW THIS ENTRY POINT.                             *
***********************************************************************
         SPACE
TRANSX   DS    0H                      * * * *
         LA    R14,TRANSX-TRANSQ       LOAD SCRATCH REG WITH
*                                         DIFFERENCE IN ENTRY POINTS
         SR    BASE,R14                ADJUST VALUE OF BASE REGISTER
*                                         TO THAT AT ENTRY POINT TRANSQ
         SPACE
*
* PROCEED TO TRANSLATE DATA TO UPPERCASE.
*
TRANS1   DS    0H                      * * * *
         LH    R14,PLENGTH             LOAD LENGTH OF DATA
         LTR   R14,R14                 ANY DATA TO TRANSLATE
         BCR   CC8,LINK1               NO ZERO RETURN TO CALLER
         SPACE
         BCTR  R14,ZERO                REDUCE FOR 'EX' INSTRUCTION
         L     R1,PPOINTR              LOAD ADDRESS OF DATA
         EX    R14,TRANSTR             TRANSLATE TO UPPERCASE
         BR    LINK1                   RETURN TO CALLER
         EJECT
H0       DC    H'0'                    HALFWORD OF ZEROES FOR CLC
         SPACE
DATE     DC    C'IKJEFP20-8/7/70'      MODULE LEVEL IDENTITY CONSTANT
         SPACE
*
* TRANSLATION TABLE USED BY TRANSLATE TO UPPERCASE ROUTINE.
*
UPPERTAB DC    256AL1(*-UPPERTAB)      GENERATE BASIC TABLE
         ORG   UPPERTAB+C'A'-X'40'     POSITION INST. CTR. TO SMALL A-Z
         DC    (C'Z'-C'A'+1)AL1(*-UPPERTAB+X'40') RAISE LOWER TO UPPER
         ORG   UPPERTAB+256            RESET INSTRUCTION COUNTER
         EJECT
*
* THIS 256 BYTE TABLE IS USED TO TEST IF A GIVEN INPUT CHARACTER
* BELONGS TO A CHARACTER CLASS. EACH EBCIDIC CONFIGURATION IS
* ASSIGNED A CHARACTER CLASS
*
TRTAB    DC    256AL1(INVALID)         INITIALIZE TYPETEST TABLE
         ORG   TRTAB+X'05'
         DC    AL1(SEPAR+CMDDLIM)      HORIZONTAL TAB = SEPARATOR AND
*                                         COMMAND DELIMITER
         ORG   TRTAB+C'
'
         DC    AL1(NSEPDLIM+CMDDLIM)   NEW LINE CHAR = DELIMITER AND
*                                         COMMAND DELIMITER
         ORG   TRTAB+C' '
         DC    AL1(SEPAR+CMDDLIM)      BLANK = SEPARATOR AND COMMAND
*                                         DELIMITER
         ORG   TRTAB+C''
         DC    AL1(NDLIMSPC)           CENT SIGN = SPECIAL
         ORG   TRTAB+C'.'
         DC    AL1(NDLIMSPC+CMDDLIM)   PERIOD = SPECIAL
*                                         AND COMMAND DELIMITER
         ORG   TRTAB+C'<'
         DC    AL1(NDLIMSPC)           LESS THAN = SPECIAL
         ORG   TRTAB+C'('
         DC    AL1(NDLIMSPC+CMDDLIM)   LEFT PAREN = SPECIAL
*                                         AND COMMAND DELIMITER
         ORG   TRTAB+C'+'
         DC    AL1(NDLIMSPC)           PLUS = SPECIAL
         ORG   TRTAB+C''
         DC    AL1(NDLIMSPC)           OR = SPECIAL
         ORG   TRTAB+C'&&'
         DC    AL1(NDLIMSPC+CMDDLIM)   AMPERSAND = SPECIAL
*                                         AND COMMAND DELIMITER
         ORG   TRTAB+C'!'
         DC    AL1(NDLIMSPC)           EXCLAMATION = SPECIAL
         ORG   TRTAB+C'$'
         DC    AL1(NATL)               DOLLAR SIGN = NATIONAL
         ORG   TRTAB+C'*'
         DC    AL1(NDLIMSPC)           ASTERISK= SPECIAL
*                                         AND COMMAND DELIMITER
         ORG   TRTAB+C')'
         DC    AL1(NSEPDLIM+CMDDLIM)   RIGHT PAREN = SPECIAL
*                                         AND COMMAND DELIMITER
         ORG   TRTAB+C';'
         DC    AL1(NSEPDLIM+CMDDLIM)   SEMICOLON = DELIMITER
*                                         AND COMMAND DELIMITER
         ORG   TRTAB+C'^'
         DC    AL1(NDLIMSPC)           NOT SIGN = SPECIAL
         ORG   TRTAB+C'-'
         DC    AL1(NDLIMSPC+CMDDLIM)   MINUS = SPECIAL
*                                         AND COMMAND DELIMITER
         ORG   TRTAB+C'/'
         DC    AL1(NDLIMSPC+CMDDLIM)   SLASH = DELIMITER
*                                         AND COMMAND DELIMITER
         ORG   TRTAB+C','
         DC    AL1(SEPAR+CMDDLIM)      COMMA = SEPARATOR
*                                         AND COMMAND DELIMITER
         ORG   TRTAB+C'%'
         DC    AL1(NDLIMSPC)           PERCENT = SPECIAL
         ORG   TRTAB+C'_'
         DC    AL1(NDLIMSPC)           DASH = SPECIAL
         ORG   TRTAB+C'>'
         DC    AL1(NDLIMSPC)           GREATER-THAN = SPECIAL
         ORG   TRTAB+C'?'
         DC    AL1(NDLIMSPC)           QUESTION MARK = SPECIAL
         ORG   TRTAB+C':'
         DC    AL1(NDLIMSPC)           COLON = SPECIAL
         ORG   TRTAB+C'#'
         DC    AL1(NATL)               POUND SIGN = NATIONAL
         ORG   TRTAB+C'@'
         DC    AL1(NATL)               AT SIGN = NATIONAL
         ORG   TRTAB+C''''             QUOTE
         DC    AL1(NDLIMSPC+CMDDLIM)   QUOTE = SPECIAL
*                                         AND COMMAND DELIMITER
         ORG   TRTAB+C'='
         DC    AL1(NDLIMSPC+CMDDLIM)   EQUAL = SPECIAL
*                                         AND COMMAND DELIMITER
         ORG   TRTAB+C'"'
         DC    AL1(NDLIMSPC)           DOUBLE QUOTE = SPECIAL
         ORG   TRTAB+X'81'
         DC    (C'F'-C'A'+1)AL1(HEX)   SMALL A THRU F = HEX
         DC    (C'I'-C'G'+1)AL1(OLETTER) SMALL G THRU I = OTHER LETTER
         ORG   TRTAB+C'J'
         ORG   TRTAB+X'91'
         DC    (C'R'-C'J'+1)AL1(OLETTER) SMALL J THRU R = OTHER LETTER
         ORG   TRTAB+C'S'
         ORG   TRTAB+X'A2'
         DC    (C'Z'-C'S'+1)AL1(OLETTER) SMALL S THRU Z = OTHER LETTER
         ORG   TRTAB+C'A'
         DC    (C'F'-C'A'+1)AL1(HEX)   A THRU F = HEX
         DC    (C'I'-C'G'+1)AL1(OLETTER) G THRU I = OTHER LETTER
         ORG   TRTAB+C'J'
         DC    (C'R'-C'J'+1)AL1(OLETTER) J THRU R = OTHER LETTER
         ORG   TRTAB+C'S'
         DC    (C'Z'-C'S'+1)AL1(OLETTER) S THRU Z = OTHER LETTER
         ORG   TRTAB+C'0'
         DC    (C'9'-C'0'+1)AL1(NUMBER) 0 THRU 9 = NUMBER
         ORG   TRTAB+256
         SPACE
TRANSTR  TR    ZERO(*-*,R1),UPPERTAB   TO CONVERT TO UPPERCASE
         SPACE
TYPETM   TM    ZERO(R14),*-*           USED BY TYPETEST TO TEST
*                                         FOR A CHARACTER TYPE
         SPACE
*
* TABLE USED BY GENSCAN TO SELECT THE CORRECT MASK TO BE PASSED
* TO TYPETEST GIVEN THE TYPE OF CHARACTER REQUIRED IN THE FORM
* OF A HEX NUMBER.
*
CHARTYPE DC    0H'0'                   * * * *
ANY      DC    AL1(HEX+OLETTER+NATL+NUMBER+NDLIMSPC) ALL ALPHABETIC
*                                         NUMERIC,NATIONAL AND
*                                         ENTERABLE SPECIAL CHARACTERS
*                                         THAT ARE NOT DELIMITERS
ALPHA    DC    AL1(HEX+OLETTER+NATL)   ALL ALPHABETIC CHARACTERS
NUMERIC  DC    AL1(NUMBER)             ALL NUMERIC CHARACTERS
ALPHANUM DC    AL1(HEX+OLETTER+NATL+NUMBER) ALL ALPHABETIC AND
*                                         NUMERIC CHARACTERS
NONATABC DC    AL1(HEX+OLETTER)        ALL ALPHABETICS EXCLUDING  R0301
*                                      THE NATIONAL CHARACTERS    R0301
NONATNUM DC    AL1(HEX+OLETTER+NUMBER) ALL ALPHABETICS AND        R0301
*                                      NUMERICS EXCLUDING THE     R0301
*                                      NATIONAL CHARACTERS        R0301
         EJECT
*
*   CONSTANTS FOR VALUE TYPE SUPPORT                             Y02666
*
EXQTE    DC    X'E77D'                 X - QUOTE                 Y02666
EXXQTE   DC    X'A77D'                 SMALL X - QUOTE           Y02666
         SPACE
BINQTE   DC    X'827D'                 SMALL B - QUOTE           Y02666
BINNQTE  DC    X'C27D'                 B - QUOTE                 Y02666
MAXNUMC  DC    CL12'002147483647'      MAXIMUM DECIMAL NUMBER    Y02666
NUMMAX   DC    F'2147483647'           MAXIMUM NUMERIC VALUE     Y02666
MAXBINL  DC    H'32'                   MAXLENGTH FOR BINARY      Y02666
MAXNUML  DC    H'10'                   MAXLENGTH FOR INTEGER     Y02666
MAXHEXL  DC    H'08'                   MAXLENGTH FO HEXADECIMAL  Y02666
         DS    0F                                                Y02666
BYTEON   DC    X'80000000'             HIGH ORDER BIT ON         Y02666
STALOCP  DC    V(STALOC)                                         Y02666
POSITXP  DC    V(POSITX1)                                        Y02666
RANGEP   DC    V(RANGE)                                          Y02666
*        BINARY AND HEX CONVERSION TABLE                         Y02666
CONVTAB  DC    256AL1(INVALID)         VALUE TYPE CONVERSION TABLE
*                                                                Y02666
         ORG   CONVTAB+X'81'
         DC    X'0A0B0C0D0E0F'
         ORG   CONVTAB+X'C1'
         DC    X'0A0B0C0D0E0F'
         ORG   CONVTAB+X'F0'
         DC    X'00010203040506070809'
GETCOREP DC    V(GETCORE)              PTR TO GETMAIN RTN.       Y02666
QSTRNGAD DC    V(QSTR01)               QUOTED STRING ROUTINE     Y02666
SLASHAST DC    C'/*'                   COMMENT DELIMITER         Y02666
*          EXECUTED INSTRUCTIONS                                 Y02666
PACKIT   PACK  EIGHT(*-*,R1),ZERO(*-*,R1) PACK DATA              Y02666
MVCDATA  MVC   ZERO(*-*,R9),ZERO(R8)   MOVE DATA TO PACK         Y02666
         EJECT
*
* MAPPING OF THE CONTROL INFORMATION PASSED TO GENSCAN BY THE CALLER
* THE ADDRESS OF THE CONTROL INFORMATION IS CONTAINED IN PDWORD
* IN THE COMMON WORKAREA.
*
GCONTROL DSECT
GOPTIONS DS    X                       FLAG BYTE WHICH INDICATES THE
*                                         SCAN OPTIONS
GFIRST   DS    X                       FIRST CHARACTER TYPE - A HEX
*                                         NUMBER
GOTHER   DS    X                       OTHER CHARACTERS TYPE - A HEX
*                                         NUMBER
GMAXLNTH DS    X                       MAXIMUM LENGTH (OPTIONAL)
         SPACE
*
* GENSCAN OPTIONS FLAGS.
*
ASTKALWD EQU   X'80'                   AN ASTERISK MAY BE SUBSTITUTED
*                                         FOR THE PARM
MAXLSPEC EQU   X'40'                   MAXIMUM LENGTH IS SPECIFIED
         EJECT
PWORK    IKJEFPWA
         END
