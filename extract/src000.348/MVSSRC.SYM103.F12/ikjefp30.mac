P30      TITLE 'IKJEFP30   TSO COMMAND SCAN PROGRAM   VERSION 3 DATE 6/*
               14/73'
***********************************************************************
*                                                                     *
* TITLE -- 'IKJEFP30 - COMMAND SCAN FOR TSO COMMANDS'                 *
*                                                                     *
* STATUS -- VERSION 3                                                 *
*                                                                     *
* FUNCTION/OPERATION -- COMMAND SCAN SCANS THE TSO COMMAND BUFFER FOR *
*    THE COMMAND NAME, OPTIONALLY SYNTAX CHECKS IT, INDICATES         *
*    IF THERE ARE PARAMTERS ON THE COMMAND, AND TRANSLATES THE        *
*    COMMAND NAME TO UPPER CASE.  SYNTAX CHECKING IS DONE BY USING    *
*    GENSCAN ROUTINE AND CHARACTER TYPE TABLE OF MODULE IKJEFP40.     *
*    TRANSLATION TO UPPERCASE IS DONE USING THE TRANSX ROUTINE OF     *
*    MODULE IKJEFP40.                                                 *
*    COMMAND SCAN ALSO CHECKS THE FIRST NON-SEPARATOR CHARACTER FOR A *
*    SPECIAL CHARACTER, %, FOR THE EXEC COMMAND. A FLAG, CSOAEXEC, IS *
*    SET TO INDICATE THE PRESENCE OF THIS CHARACTER.                  *
*    COMMAND SCAN SKIPS OVER THIS CHARACTER AND SYNTAX CHECKS THE     *
*    REMAINDER OF THE BUFFER. THIS CHARACTER IS NOT INCLUDED AS A PART*
*    OF THE COMMAND NAME. THUS, THE COMMAND NAME CAN CONTAIN A MAXIMUM*
*    OF EIGHT (8) CHARACTERS BESIDES IT.                              *
*    THIS SPECIAL CHARACTER IS NOT CHECKED FOR A COMMAND THAT IS      *
*    NOT TO BE SYNTAX CHECKED BY COMMAND SCAN.                        *
*                                                                     *
* ENTRY POINTS --                                                     *
*                                                                     *
*         IKJSCAN - PURPOSE:  TO OBTAIN ALL OF THE ABOVE FUNCTION     *
*                 - CALLING SEQUENCE:  LA    R1,CSPL                  *
*                                      LINK  EP=IKJSCAN               *
*                                                                     *
* INPUT -- REGSITER 1 POINTS TO THE COMMAND SCAN PARAMETER LIST       *
*    (CSPL).  THE CSPL CONTAINS THE FOLLOWING:                        *
*                                                                     *
*         /-----------------------------------------------/           *
*         / A POINTER TO THE UPT                          /           *
*         /-----------------------------------------------/           *
*         / A POINTER TO THE ECT                          /           *
*         /-----------------------------------------------/           *
*         / A POINTER TO THE CP'S ECB                     /           *
*         /-----------------------------------------------/           *
*         / A POINTER TO A FLAG WORD                      /           *
*         /-----------------------------------------------/           *
*         / A POINTER TO THE OUTPUT AREA                  /           *
*         /-----------------------------------------------/           *
*         / A POINTER TO THE COMMAND BUFFER               /           *
*         /-----------------------------------------------/           *
*         0                                               4           *
*                                                                     *
*    THE UPT, ECT, AND CP'S ECB ARE NOT USED BY COMMAND SCAN.         *
*    THE FLAG WORD IS OBTAINED AND FREED BY THE CALLER. BIT 0 SET TO  *
*    0 = SYNTAX CHECKING.  BIT 0 SET TO 1 = NO SYNTAX CHECKING.       *
*    THE OUTPUT AREA IS TWO WORDS IN LENGTH AND IS OBTAINED AND       *
*    FREED BY THE INVOKER.  IT IS USED FOR THE RESULTS OF THE SCAN.   *
*    THE COMMAND BUFFER CONTAINS THE COMMAND TO BE SCANNED.           *
*    IT HAS THE FOLLOWING FORMAT:                                     *
*                                                                     *
*         /-----------------------------------------------/           *
*         / LENGTH / OFFSET / TEXT                        /           *
*         /-----------------------------------------------/           *
*         0        2        4                                         *
*                                                                     *
*    THE FIRST 2 BYTES CONTAIN THE TOTAL LENGTH OF THE COMMAND        *
*    AND HEADER. THE SECOND 2 BYTES CONTAIN THE OFFSET INTO THE       *
*    TEXT AT WHICH THE SCAN SHOULD BEGIN.                             *
*                                                                     *
* OUTPUT -- THE RESULTS OF THE SCAN ARE PLACED IN THE OUTPUT AREA.    *
*    A POINTER TO THE OUTPUT AREA IS IN THE CSPL. IT HAS THE          *
*    FOLLOWING FORMAT:                                                *
*                                                                     *
*         /-----------------------------------------------/           *
*         / A POINTER TO THE COMMAND NAME                 /           *
*         /-----------------------------------------------/           *
*         / LENGTH OF CMD. NAME  / FLAGS     / RESERVED   /           *
*         /-----------------------------------------------/           *
*         0                      2           3            4           *
*                                                                     *
*    THE POINTER AND LENGTH ARE ZERO IF THE BUFFER IS EMPTY OR        *
*    THE COMMAND NAME IS INVALID.  THE FLAGS ARE SET AS FOLLOWS:      *
*                                                                     *
*         X'80'  VALID COMMAND WITH PARAMETERS                        *
*         X'40'  VALID COMMAND WITH NO PARAMETERS                     *
*         X'20'  QUESTION MARK                                        *
*         X'10'  BUFFER IS EMPTY                                      *
*         X'08'  INVALID COMMAND NAME                                 *
*                                                                     *
*    THE OFFSET IN THE COMMAND BUFFER HEADER IS ALSO UPDATED.         *
*    IT IS SET TO THE FIRST PARAMETER FOLLOWING THE COMMAND           *
*    OR THE END OF THE BUFFER IF THERE ARE NO PARAMETERS              *
*    OR THE BUFFER WAS EMPTY.  IF THE COMMAND IS INVALID THE          *
*    OFFSET IS UNCHANGED.                                             *
*                                                                     *
* EXTERNAL REFERENCHES --
*         GENSCAN:  A SUBROUTINE OF MODULE IKJEFP40 USED TO SYNTAX    *
*                   CHECK THE COMMAND NAME.                           *
*         TRANSX:  A SUBROUTINE OF MODULE IKJEFP40 USED TO TRANSLATE  *
*                  THE COMMAND NAME TO UPPER CASE.                    *
*         TRTAB:  A 256 BYTE TABEL IN MODULE IKJEFP40 USED TO CHECK A *
*                 CHARACTER FOR A SPECIFIED CHARACTER TYPE.           *
*                                                                     *
* EXISTS,NORMAL -- THE FOLLOWING RETURN CODES ARE SET IN REGISTER 15. *
*    0  SUCCESSFUL COMPLETION                                         *
*    4  THE CSPL CONTAINS INVALID PARAMETERS                          *
*                                                                     *
*    FOR A NON-ZERO RETURN CODE, THE OUTPUT AREA AND COMMAND BUFFER   *
*    OFFSET ARE UNCHANGED.                                            *
*                                                                     *
* EXISTS,ERROR -- N/A                                                 *
*                                                                     *
* TABLES/WORK AREAS --                                                *
*    IKJCSPL:  MAPPING OF COMMAND SCAN PARAMETER LIST                 *
*    IKJCSOA:  MAPPING OF COMMAND SCAN OUTPUT AREA                    *
*    IKJEFPWA:  MAPPING OF WORKAREA USED BY COMMAND SCAN AND          *
*               UTILITY MODULE IKJEFP40.                              *
*                                                                     *
* ATTRIBUTES -- REENTRANT                                             *
*                                                                     *
* CHARACTER CODE DEPENDENCY -- CLASS C.  THE OPERATION OF THIS MODULE *
*    DEPENDS UPON AN INTERNAL REPRESENTATION OF THE EXTERNAL          *
*    CHARACTER SET WHICH IS EQUIVALENT TO THE ONE USED AT             *
*    ASSEMBLY TIME.  THE CODING HAS BEEN ARRANGED SO THAT             *
*    REDEFINITION OF 'CHARACTER' CONSTANTS, BY REASSEMBLY,            *
*    WILL RESULT IN A CORRECT MODULE FOR THE NEW DEFINITION.          *
*                                                                     *
* RELEASE 20 SUPPORT CODE -- 20035                                    *
*                                                                     *
* RELEASE 2.0 (VS/2) SUPPORT CODE ---Y02666                           *
*                                                                     *
* RELEASE 3.0 SUPPORT ---Y30PQJN                                      *
*                                                                     *
***********************************************************************
         SPACE
IKJEFP30 CSECT
         ENTRY IKJSCAN                 ESTABLISH ENTRY POINT FOR
*                                         COMMAND SCAN
         EXTRN TRTAB                   EXTERNAL TABLE USED BY TYPETEST
IKJSCAN  DS    0H                      * * * *
         SPACE
*
* REGISTER EQUATES.
*
R0       EQU   0                       SCRATCH/PARAMETER REGISTER --
*                                         MUST BE 0
R1       EQU   1                       SCRATCH/PARAMETER REGISTER --
*                                         MUST BE 1
R2       EQU   2                       GENERAL SCRATCH REGISTER
R3       EQU   3                       GENERAL SCRATCH REGISTER
XINPUT   EQU   4                       NEXT CHARACTER TO SCAN
XINPUTB  EQU   5                       LAST CHARACTER SCANNED USED TO
*                                         COMPUTE LENGTH OF SCANNED
*                                         DATA
XFLAGS   EQU   6                       PTR TO INPUT FLAG WORD
CSOAPTR  EQU   7                       ADDRESS OF OUTPUT AREA
LINK2    EQU   8                       SECOND LEVEL LINKAGE REGISTER
*                                         FOR LINKAGE BETWEEN
*                                         SUBROUTINES
LINK1    EQU   9                       FIRST LEVEL LINKAGE REGISTER FOR
*                                         LINKAGE BETWEEN MAINLINE AND
*                                         SUBROUTINES
CSPLPTR  EQU   10                      ADDRESS OF COMMAND SCAN
*                                         PARAMETER LIST
CBUFPTR  EQU   11                      ADDRESS OF COMMAND BUFFER
BASE     EQU   12                      BASE REGISTER FOR COMMAND SCAN
*                                         CSECT
WORKBASE EQU   13                      BASE REGISTER FOR COMMAND SCAN
*                                         WORKSPACE -- MUST BE 13
R14      EQU   14                      SCRATCH/CALL REGISTER -- MUST BE
*                                         14
R15      EQU   15                      SCRATCH/CALL REGISTER -- MUST BE
*                                         15
         SPACE
*
* BIT SETTINGS FOR CHARACTER TYPES USED BY TYPETEST
*
HEX      EQU   X'80'                   HEX CHARACTER
OLETTER  EQU   X'40'                   LETTER NOT A HEX CHARACTER
NATL     EQU   X'20'                   NATIONAL CHARACTER
NUMBER   EQU   X'10'                   NUMBER
SEPAR    EQU   X'08'                   SEPARATOR
NSEPDLIM EQU   X'04'                   DELIMITER THAT IS NOT A
*                                         A SEPARATOR
NDLIMSPC EQU   X'02'                   SPECIAL CHARACTER THAT IS NOT
*                                         ALSO A DELIMITER OR A
*                                         SEPARATOR
CMDDLIM  EQU   X'01'                   COMMAND NAME DELIMITER
INVALID  EQU   X'00'                   INVALID CHARACTER
         SPACE
*
* MISCELLANEOUS EQUATES
*
ZERO     EQU   0                       USED AS A ZERO
ONE      EQU   1                       USED AS A ONE
TWO      EQU   2                       USED AS A TWO
THREE    EQU   3                       USED AS A THREE
FOUR     EQU   4                       USED AS A FOUR
EIGHT    EQU   8                       USED AS AN EIGHT
QUESTMRK EQU   C'?'                    QUESTION MARK
PERCENT  EQU   C'%'                    PER CENT SIGN INDICATES AN EXEC
*                                      COMMAND                  Y30PQJN
CC5      EQU   5                       CONDITION CODE 5
         SPACE
*
* COMMAND SCAN INPUT FLAGS
*
NOSYNCHK EQU   X'80'                   INDICATES NOT TO SYNTAX CHECK
*                                         THE COMMAND NAME
         SPACE
*
* RETURN CODES
*
RCGOOD   EQU   0                       SUCESSFUL COMPLETION
RCINVP   EQU   4                       INVALID PARAMETERS - THE OFFSET
*                                         IN THE COMMAND BUFFER IS
*                                         GREATER THAN THE LENGTH-4
         EJECT
*
* ENTRY CODE:  SAVE REGISTERS AND ESTABLISH ADDRESSABILITY TO COMMAND
*              SCAN CSECT AND COMMAND SCAN PARAMETER LIST.
*
         SAVE  (14,12)                 SAVE CALLERS REGISTERS
         SPACE
         BALR  BASE,ZERO               ESTABLISH ADDRESSABILITY TO
         USING *,BASE                     COMMAND SCAN CSECT
         MODID BRANCH=YES              FLAG                      Y01886
         SPACE
         LR    CSPLPTR,R1              SAVE INPUT PARAMETER LIST ADDR
         USING CSPL,CSPLPTR            ESTABLISH ADDRESSABILITY TO
*                                         COMMAND SCAN PARMATER LIST
         SPACE
*
* DO UNCONDITIONAL GETMAIN FOR SAVEAREA AND FOR WORKSPACE
* NECESSARY FOR COMMAND SCAN PROCESSING.
*
         GETMAIN R,LV=CSWORKSZ         UNCONDITIONAL REQUEST FOR
*                                         COMMAND SCAN WORKAREA
         SPACE
*
* STANDARD LINKAGE CONVENTIONS.
*
         LR    R2,WORKBASE             SAVE ADDRESS OF CALLER'S SAVE
*                                         AREA
         LR    WORKBASE,R1             ESTABLISH COMMAND SCAN SAVEAREA
*                                         ADDRESS AND INITIALIZE
*                                         WORKSPACE BASE REGISTER
         USING CSWORK,WORKBASE         ESTABLISH ADDRESSABILITY TO
*                                         COMMAND SCAN WORKSPACE
         ST    R2,FOUR(WORKBASE)       BACK CHAIN TO CALLER'S SAVEAREA
         ST    WORKBASE,EIGHT(R2)      FORWARD CHAIN TO COMMAND SCAN'S
*                                         SAVEAREA
         SPACE
*
* ESTABLISH ADDRESSABILITY TO COMMAND SCAN OUTPUT AREA
* OBTAINED BY THE INVOKER.
*
         NI    RETCODE,RCGOOD          SET RETURN CODE TO 0
         L     XFLAGS,CSPLFLG          GET ADDRESS OF FLAG WORD
         L     CSOAPTR,CSPLOA          GET ADDRESS OF OUTPUT AREA
         USING CSOA,CSOAPTR            ESTABLISH ADDRESSABILITY TO
*                                         OUTPUT AREA PROVIDED BY USER
         SPACE
*
* ESTABLISH ADDRESSABILITY TO COMMAND BUFFER PASSED IN PARM LIST
* AND CHECK THAT THE OFFSET IS NOT GREATER THAN THE LENGTH-4.
*
         L     CBUFPTR,CSPLCBUF        GET ADDRESS OF COMMAND BUFFER
         USING CBUF,CBUFPTR            ESTABLISH ADDRESSABILITY TO
*                                         COMMAND BUFFER
         XC    CSOA(EIGHT),CSOA        CLEAR OUTPUT AREA
         MVC   PDWORD(FOUR),CBUFLEN    ALIGN BUFFER LENGTH ON FULL
*                                         WORD BOUNDARY AND OFFSET ON
*                                         HALF WORD BOUNDARY
         LH    R2,PDWORD               LOAD LENGTH
         LA    R3,FOUR                 LOAD A FOUR FOR SUBTRACT
         SR    R2,R3                   SUBTRACT FOUR FROM LENGTH
         STH   R2,PDWORD+FOUR          PUT ADJUSTED LENGTH IN
*                                         PDWORD+4
         CLC   PDWORD+FOUR(TWO),PDWORD+TWO IS THE LENGTH-4 GREATER
*                                         OR EQUAL TO THE OFFSET
         BNL   CS01                    YES,CONTINUE
         SPACE
         OI    RETCODE,RCINVP          NO, SET THE RETURN CODE TO
*                                         INVALID PARAMETERS
         B     CSEXIT                  EXIT
         SPACE
*
* START SCAN AT OFFSET INDICATED IN COMMAND BUFFER HEADER.
*
CS01     DS    0H                      * * * *
         LR    R2,CBUFPTR              COPY COMMAND BUFFER ADDRESS
         AH    R2,PDWORD               COMPUTE ADDRESS OF END OF BUFFER
         ST    R2,ENDINPUT             SAVE FOR FUTURE COMPARES
         LH    R2,PDWORD+TWO           LOAD CURRENT BUFFER OFFSET
         LA    XINPUT,THREE(R2,CBUFPTR) SET STARTING ADDRESS OF SCAN AT
*                                         ONE BEFORE THE LOCATION
*                                         INDICATED BY THE CURRENT
*                                         BUFFER OFFSET
         SPACE
*
* CHECK ALL COMMANDS FOR A POSSIBLE QUESTION MARK IN PLACE OF THE
* COMMAND NAME.
*
         LA    XINPUT,ONE(XINPUT)      GET FIRST CHARACTER
         LR    XINPUTB,XINPUT          SET LAST CHARACTER SCANNED
         CLI   ZERO(XINPUT),QUESTMRK   IS FIRST CHAR A QUESTION MARK
         BE    CSEXIT20                YES,SET FLAG TO INDICATE
*                                         QUESTION MARK IS PRESENT
         BCTR  XINPUT,ZERO             NO CHECK FOR VALID COMMAND
         SPACE
*
* SKIP SEPARATORS TO BEGINNING OF COMMAND NAME.
* A +4 RETURN FROM SKIPB INDICATES THE COMMAND BUFFER IS EMPTY.
*
         BAL   LINK2,SKIPB             SKIP BLANKS TO BEGINNING OF
*                                         COMMAND NAME
         B     CSEXIT10                RETURN +0, END OF BUFFER REACHED
*                                         TAKE NULL EXIT
*                                      RETURN +4, START OF COMMAND NAME
*                                         HAS BEEN ESTABLISHED
         LA    XINPUT,ONE(XINPUT)      GET FIRST CHARACTER
         LR    XINPUTB,XINPUT          SET LAST CHARACTER SCANNED
         SPACE
*
* ONCE IT IS ESTABLISHED THAT THERE IS A COMMAND NAME AND IT IS NOT
* A QUESTION MARK, CHECK THE FLAG WORD SET BY THE INVOKER TO
* DETERMINE IF THE COMMAND NAME SHOULD BE SYNTAX CHECKED.
*
         TM    ZERO(XFLAGS),NOSYNCHK   TEST INPUT FLAG BYTE TO
*                                         DETERMINE IF SYNTAX CHECKING
*                                         IS REQUESTED
         BO    CSNOSYN                 NO, GO SCAN FOR END ONLY
         CLI   ZERO(XINPUT),PERCENT    IS THE FIRST CHARACTER A
*                                      PERCENT SIGN?            Y30PQJN
         BE    CSPERCNT                YES, SKIP OVER AND SET FLAG
*                                      IN THE CSOA              Y30PQJN
CSSKIPS  DS    0H                                               Y30PQJN
         BCTR  XINPUT,ZERO             BACK UP INPUT PTR FOR GENSCAN
         SPACE
*
* THE GENERAL SCAN ROUTINE OF MODULE IKJEFP40 IS USED TO SYNTAX
* CHECK THE COMMAND. PDWORD IN THE COMMON WORK AREA MUST CONTAIN THE
* ADDRESS OF THE CONTROL BITS FOR SYNTAX CHECKING. THE FIRST CHARACTER
* OF A COMMAND NAME MUST BE ALPHABETIC. THE OTHER CHARACTERS MUST
* BE ALPHAMERIC. THE MAXIMUM LENGTH IS 8.
*
         LA    R3,CMDSYNTX             GET ADDR OF CMND SYNTAX REQD
         ST    R3,PDWORD               SAVE FOR GENSCAN
         L     R15,AGSCAN              GET ADDRESS OF GENSCAN ROUTINE
         BALR  LINK2,R15               LINK TO GENSCAN TO
*                                         SYNTAX CHECK COMMAND NAME
*
         B     CSEXIT08                RETURN +0, FIRST CHAR IS INVALID
*                                         TAKE ERROR EXIT
*
         B     CSEXIT08                RETURN +4, TOO LONG, TAKE ERROR
*                                         EXIT
*
         B     CS03                    RETURN +8, OK, REACHED END, GO
*                                         SET PTR AND LENGTH
*
*                                      RETURN +12, OK, CHECK DELIMITER
*
         SPACE
*
* GENSCAN RETURNS CONTROL WITH XINPUT POINTING TO THE DELIMITER
* CHARACTER OF THE COMMAND NAME. THE DELIMITER MUST BE A SEPARATOR OR
* THE END OF THE BUFFER.  IF THE END OF THE BUFFER WAS NOT REACHED
* DURING GENSCAN, THE DELIMITER MUST BE CHECKED.  TYPETEST IS
* USED TO PERFORM THIS TEST.
*
         LA    R14,ONE(XINPUT)         GET NEXT INPUT CHARACTER  YM3610
         C     R14,ENDINPUT            IS IT THE END?            YM3610
         BH    COMBYPS1                BYPASS COMMENT TEST       YM3610
         CLC   ZERO(TWO,XINPUT),SLASHAST A COMMENT DELIMITER?    Y02666
         BE    CS03                    YES, A VALID COMMAND NAME
*                                      SEPARATOR                 Y02666
COMBYPS1 DS    0H                      TEST FOR SEPARATOR        YM3610
         LA    R1,SEPAR                SET UP TEST FOR VALID COMMAND
*                                         NAME SEPARATOR
         BAL   LINK1,TYPETEST          LINK TO TYPETEST TO
*                                         PERFORM TEST
*
         B     CSEXIT08                RETURN  +0, INVALID, TAKE ERROR
*                                         EXIT
*
*                                      RETURN  +4, VALID, CONTINUE
         SPACE
*
* IF THE COMMAND NAME IS VALID, IT IS TRANSLATED TO UPPERCASE
* USING THE TRANSX ROUTINE OF MODULE IKJEFP40.  PPOINTR AND PLENGTH
* IN THE COMMON WORKAREA MUST CONTAIN A POINTER TO THE COMMAND
* NAME AND THE LENGTH.
*
CS03     DS    0H                      * * * *
         MVC   CSOACNM,PPOINTR         PUT PTR TO COMMAND NAME IN
*                                         OUTPUT AREA
         S     XINPUTB,PPOINTR         COMPUTE LENGTH OF COMMAND NAME
         STH   XINPUTB,CSOALNM         PUT LENGTH IN OUTPUT AREA
         STH   XINPUTB,PLENGTH         STORE LENGTH FOR TRANSLATE
         L     R15,ATRANSX             GET ADDRESS OF TRANSLATE ROUTINE
         BALR  LINK1,R15               LINK TO TRANSX TO TRANSLATE
*                                         THE COMMAND TO UPPER CASE
         SPACE
*
* SKIP SEPARATORS TO THE FIRST PARAMETER OF THE COMMAND OF THE END
* OF THE BUFFER.  THE OUTPUT FLAGS ARE SET ACCORDING TO THE RESULTS.
*
         BCTR  XINPUT,ZERO             BACK UP FOR SKIPB
         BAL   LINK2,SKIPB             SKIP BLANKS TO FIRST PARAMETER
         B     CSEXIT40                RETURN +0, REACHED END OF BUFFER
*                                         TAKE VALID EXIT 2
         B     CSEXIT80                RETURN +4, REACHED FIRST
*                                         PARAM,TAKE VALID EXIT 1
         SPACE
*
* IF THE COMMAND NAME IS NOT SYNTAX CHECKED IT IS SCANNED AS FOLLOWS
* USING THE TYPETEST ROUTINE:
*        1. THE FIRST CHARACTER MUST BE AN ENTERABLE CHARACTER
*     THAT IS NOT A COMMAND DELIMITER.
*        2. THE OTHER CHARACTERS MUST BE ENTERABLE CHARACTERS.
*        3. THE SCAN STOPS WHEN A COMMAND DELIMITER IS FOUND.
*
* NOTE:  THE FOLLOWING ARE COMMAND DELIMITERS -- BLANK, TAB, COMMA,
*        EQUAL SIGN, MINUS SIGN, SLASH, PERIOD, SEMI-COLON, QUOTE,
*        AMPERSAND, LEFT PAREN, RIGHT PAREN.
*
CSNOSYN  DS    0H                      NO SYNTAX CHECKING REQUESTED
         LA    R1,CMDDLIM              SET UP TO TEST FOR VALID
*                                         COMMAND DELIMITER
         BAL   LINK1,TYPETEST          LINK TO TYPETEST TO
*                                         PERFORM TEST
*
         B     CS11                    +0 RETURN - NOT A COMMAND
*                                         DELIMITER, CHECK IF GOOD
*                                         CHARACTER
*
         B     CSEXIT08                +4 RETURN - A COMMAND DELIMITER,
*                                         THEN COMMAND NAME IS INVALID
         SPACE
CS11     DS    0H                      * * * *
***
*
* DEPENDS UPON CONTENTS OF R15 FROM TYPETEST
*
***
         CLI   ZERO(R15),INVALID       IS CURRENT CHARACTER AN
*                                         INVALID CHARACTER
         BE    CSEXIT08                YES, TAKE ERROR EXIT
         SPACE
         LA    XINPUT,ONE(XINPUT)      NO, GET NEXT CHARACTER
         LR    XINPUTB,XINPUT          SET LAST CHARACTER SCANNED
         C     XINPUT,ENDINPUT         IS SCAN AT END OF BUFFER
         BNL   CS03                    YES, EXIT NORMALLY
         SPACE
         LA    R14,ONE(XINPUT)         GET NEXT INPUT CHARACTER  YM3610
         C     R14,ENDINPUT            AT END OF INPUT?          YM3610
         BH    COMBYPS2                BYPASS COMMENT TEST       YM3610
         CLC   ZERO(TWO,XINPUT),SLASHAST  A COMMENT DELIMITER?   Y02666
         BE    CS03                    A VALID DELIMITER         Y02666
COMBYPS2 DS    0H                      TEST DELIMITER            YM3610
         LA    R1,CMDDLIM              SET UP TO TEST FOR VALID
*                                         COMMAND DELIMITER
         BAL   LINK1,TYPETEST           LINK TO TYPETEST TO
*                                         PERFORM TEST
*
         B     CS11                    +0 RETURN - NOT A COMMAND
*                                         DELIMITER, CHECK IF GOOD
*                                         CHARACTER
*
         B     CS03                    +4 RETURN - A COMMAND DELIMITER,
*                                         EXIT NORMALLY
         SPACE
CSEXIT80 DS    0H                      * * * *
         OI    CSOAFLG,CSOAVWP         SET FLAG IN OUTPUT AREA TO
*                                         INDICATE COMMAND NAME IS
*                                         VALID AND THERE ARE NON-
*                                         SEPARATORS REMAINING
         B     SETOFF                  GO SET OFFSET
         SPACE
CSEXIT40 DS    0H                      * * * *
         OI    CSOAFLG,CSOAVNP         SET FLAG IN OUTPUT AREA TO
*                                         INDICATE COMMAND NAME IS
*                                         VALID AND THERE ARE NO NON-
*                                         SEPARATORS REMAINING
         B     SETOFF                  GO SET OFFSET
         SPACE
CSEXIT20 DS    0H                      * * * *
         OI    CSOAFLG,CSOAQM          SET FLAG IN OUTPUT AREA TO
*                                         INDICATE COMMAND NAME IS A
*                                         QUESTION MARK
         B     CSEXIT                  EXIT
         SPACE
CSEXIT10 DS    0H                      * * * *
         OI    CSOAFLG,CSOANOC         SET FLAG IN OUTPUT AREA TO
*                                         INDICATE BUFFER IS EMPTY
         B     SETOFF                  GO SET OFFSET
         SPACE
CSEXIT08 DS    0H                      * * * *
         OI    CSOAFLG,CSOABAD         SET FLAG IN OUTPUT AREA TO
*                                         INDICATE COMMAND NAME IS
*                                         INVALID
         B     CSEXIT                  EXIT
CSPERCNT DS    0H                                               Y30PQJN
         LA    XINPUT,ONE(XINPUT)      UPDATE PAST PERCENT SIGN Y30PQJN
         ST    XINPUT,PPOINTR          RESET NAME POINTER       Y30PQJN
         LR    XINPUTB,XINPUT          SAVE BACKUP POINTER      Y30PQJN
         OI    CSOAFLG,CSOAEXEC        INDICATE IMPLICIT EXEC COMMAND
*                                      NAME                     Y30PQJN
         B     CSSKIPS                 CONTINUE NORMAL PROCESS  Y30PQJN
         SPACE
SETOFF   DS    0H                      RESET OFFSET WITHIN COMMAND
*                                         BUFFER
         LA    R2,CBUFTEXT             GET ADDR OF COMMAND BUFFER TEXT
         SR    XINPUTB,R2              COMPUTE CURRENT OFFSET
         STH   XINPUTB,PDWORD          STORE CURRENT OFFSET IN
         MVC   CBUFOFST,PDWORD            COMMAND BUFFER HEADER
         SPACE
CSEXIT   DS    0H                      SUCCESSFUL COMPLETION EXIT
         XR    R3,R3                   CLEAR WORK REG
         IC    R3,RETCODE              SAVE RETURN CODE
         LR    R1,WORKBASE             LOAD CORE ADDRESS FOR FREEMAIN
         L     WORKBASE,FOUR(WORKBASE) RESTORE CALLERS SAVE AREA ADDR
         FREEMAIN R,LV=CSWORKSZ,A=(1)  RELEASE WORKAREA
         LR    R15,R3                  LOAD RETURN CODE REGISTER
         RETURN (14,12),RC=(15)        EXIT FROM COMMAND SCAN
         EJECT
***********************************************************************
*                                                                     *
*                       SKIP BLANKS SUBROUTINE                        *
*                                                                     *
*   THE FUNCTION OF THIS ROUTINE IS TO SKIP BLANKS, COMMAS AND TABS IN*
* THE COMMAND BUFFER.
*
*
* INPUT - XINPUT=POINTER TO THE LAST CHARACTER SCANNED.
*                (THE SCAN FOR SEPARATORS BEGINS AT THE NEXT
*                CHARACTER.)
*
* OUTPUT - IF A NON-SEPARATOR WAS FOUND:
*               XINPUT=POINTER TO THE LAST SEPARATOR SCANNED.
*               PPOINTR=POINTER TO THE FIRST NON-SEPARATOR.
*
*        - IF THE END OF THE BUFFER WAS REACHED BEFORE A
*          NON-SEPARATOR WAS FOUND:
*               XINPUT=POINTER TO THE LAST CHARACTER IN THE BUFFER.
*               PPOINTR=POINTER TO THE END OF THE BUFFER.
*
* EXITS - LINK2+0=THE END OF THE BUFFER WAS REACHED BEFORE A
*                 NON-SEPARATOR WAS FOUND.
*
*       - LINK2+4=A NON-SEPARATOR CHARACTER WAS FOUND.
*                                                                     *
***********************************************************************
         SPACE
SKIPB    DS    0H                      SKIP SEPARATORS SUBROUTINE
         LA    XINPUT,ONE(XINPUT)      BUMP INPUT PTR BY ONE
         LR    XINPUTB,XINPUT          SET BACKUP REGISTER
         C     XINPUT,ENDINPUT         END OF INPUT
*
         BNL   SKIPBX                  YES - EXIT
*
*                                      NO - TEST FOR A SEPARATOR
         LA    R1,SEPAR                SET UP TEST FOR VALID SEPARATOR
         BAL   LINK1,TYPETEST          LINK TO TYPETEST TO
*                                         PERFORM TEST
*
         B     SKIPB2                  +0 RETURN - NON-SEPARATOR, GO
*                                         SET PPOINTR AND EXIT
*
         B     SKIPB                   +4 RETURN - SEPARATOR, CONTINUE
*                                         LOOP
         SPACE
SKIPB2   DS    0H                      * * * *
         LA    R14,ONE(XINPUT)         GET NEXT CHARACTER PTR    YM3610
         C     R14,ENDINPUT            IS AT END OF INPUT?       YM3610
         BH    COMSKIP                 BYPASS COMMENT TEST       YM3610
         CLC   ZERO(TWO,XINPUT),SLASHAST   A COMMENT DELIMITER?  Y02666
         BE    SKIPCOMT                YES, PROCESS COMMENT      Y02666
COMSKIP  DS    0H                      SET INPUT PTR             YM3610
         ST    XINPUT,PPOINTR          STORE ADDRESS OF NON-SEPARATOR
         BCT   XINPUT,FOUR(LINK2)      BACK UP INPUT PTR BY ONE AND
*                                         RETURN TO NSI +4
         SPACE
SKIPBX   DS    0H                      * * * *
         ST    XINPUT,PPOINTR          STORE ADDRESS
         BCTR  XINPUT,LINK2            BACK UP INPUT PTR BY ONE AND
*                                         RETURN TO NSI +0
*
*   COMMENT PROCESSING ROUTINE                                   Y02666
*
SKIPCOMT DS    0H                                                Y02666
         LA    XINPUT,TWO(XINPUT)      INCREMENT PTR PAST COMMENT
*                                      DELIMITER                 Y02666
SKIPBA   DS    0H                                                Y02666
         LA    R14,ONE(XINPUT)         GET NEXT CHARACTER PTR    YM3610
         C     R14,ENDINPUT            AT END OF INPUT?          YM3610
         BH    COMSKP2                 BYPASS COMMENT TEST       YM3610
         CLC   ZERO(TWO,XINPUT),ASTSLASH COMMENT END DELIMITR?   Y02666
         BE    SKIPBC                  YES, END COMMENT SCAN     Y02666
COMSKP2  DS    0H                      TEST END OF INPUT         YM3610
         C     XINPUT,ENDINPUT         AT END OF INPUT?          Y02666
         BNL   SKIPBX                  YES, END SCAN             Y02666
         LA    XINPUT,ONE(XINPUT)      UPDATE TO NEXT CHARACTER  Y02666
         B     SKIPBA                  CONTINUE COMMENT SCAN     Y02666
SKIPBC   DS    0H                                                Y02666
         LA    XINPUT,ONE(XINPUT)      UPDATE SCAN PTR. PAST COMMENT
*                                      DELIMITER                 Y02666
         B     SKIPB                   RESUME SEPARATOR SCAN     Y02666
         EJECT
***********************************************************************
*                                                                     *
*                 CHARACTER TYPE TEST ROUTINE                         *
*                                                                     *
*   THIS SUBROUTINE CHECKS THE CURRENT INPUT CHARACTER FOR
*   CHANGING TO A SPECIFIED CHARACTER CLASS.                          *
*                                                                     *
* INPUT - R1=MASK OF DESIRED CHARACTER CLASS.                         *
*         XINPUT=POINTER TO CHARACTER TO BE CHECKED.                  *
*                                                                     *
* OUTPUT - THE RESULT OF THE TEST IS INDICATED BY THE                 *
*          LOCATION TO WHICH CONTROL IS RETURNED.                     *
*                                                                     *
* EXISTS - LINK1+0=THE CHARACTER DOES NOT BELONG TO THE SPECIFIED     *
*                  CHARACTER CLASS.                                   *
*                                                                     *
*        - LINK1+4=THE CHARACTER BELONGS TO THE CHARACTER CLASS       *
*                  SPECIFIED.                                         *
*                                                                     *
***********************************************************************
         SPACE
TYPETEST DS    0H                      * * * *
         XR    R15,R15                 CLEAR WORK REG
         IC    R15,ZERO(XINPUT)        USE CURRENT CHARACTER AS
*                                         OFFSET INTO TESTING TABLE
         A     R15,ATRTAB              GET ADDRESS IN TABLE
*                                         CORRESPONDING TO CHARACTER IN
*                                         QUESTION
         EX    R1,TYPETM               EXECUTE TEST UNDER MASK OF
*                                         TYPEBYTE FOR CHARACTER
*                                         SPECIFICATION GIVEN IN R1
*
         BC    CC5,FOUR(LINK1)         TYPE MATCHES, RETURN +4
*
         BR    LINK1                   TYPE DOESN'T MATCH, RETURN +0
         EJECT
***********************************************************************
*                                                                     *
*                          CONSTANT AREA                              *
*                                                                     *
***********************************************************************
         SPACE
*
* CONTROL INFORMATION FOR GENSCAN WHEN SCANNING FOR A COMMAND NAME
*
CMDSYNTX EQU   *                       * * * *
         DC    X'40'                   ASTERISK NOT ALLOWED, MAXIMUM
*                                         LENGTH SPECIFIED
         DC    X'01'                   FIRST CHARACTER MUST BE
*                                         ALPHABETIC
         DC    X'03'                   OTHER CHARACTERS MUST BE
*                                         ALPHAMERIC
         DC    X'08'                   MAXIMUM LENGTH = 8
*
* MISCELLANEOUS DEFINITIONS                                      Y02666
ASTSLASH DC    C'*/'                   CLOSE COMMENT DELIMITER   Y02666
SLASHAST DC    C'/*'                   OPEN COMMENT DELIMITER    Y02666
* VCON'S FOR EXTERNAL UTILITY ROUTINES
*
AGSCAN   DC    V(GENSCAN)              ADDRESS OF GENERALIZED SCAN
*                                         UTILITY ROUTINE USED TO
*                                         SYNTAX CHECK THE COMMAND NAME
         SPACE
ATRANSX  DC    V(TRANSX)               ADDRESS OF TRANSLATE UTILITY
*                                         ROUTINE USED TO TRANSLATE THE
*                                         COMMAND NAME TO UPPER CASE
         SPACE
*
* ADCON FOR EXTERNAL TABLE USED BY TYPETEST TO TEST FOR A
* GIVEN CHARACTER CLASS
*
ATRTAB   DC    A(TRTAB)                ADDRESS OF EXTERNAL TABLE USED
*                                         BY TYPETEST
         SPACE
TYPETM   TM    ZERO(R15),*-*           USED BY TYPETEST TO TEST
*                                         FOR A CHARACTER TYPE
         EJECT
CSWORK   IKJEFPWA
         SPACE
         IKJCSPL
         SPACE
         IKJCSOA
         SPACE
***********************************************************************
*                                                                     *
*        THE COMMAND BUFFER IS A BUFFER POINTED TO BY THE CSPL.       *
*        IT CONTAINS THE COMMAND TO BE SCANNED.                       *
*                                                                     *
***********************************************************************
         SPACE
CBUF     DSECT                         * * * *
CBUFLEN  DS    CL2                     LENGTH OF COMMAND BUFFER
CBUFOFST DS    CL2                     OFFSET INTO BUFFER TEXT
CBUFTEXT EQU   *                       TEXT
         END
