         TITLE  'IEFSCAN-BIT PATTERN SCAN ROUTINE'
IEFSCAN  CSECT
***********************************************************************
*                                                                     *
*  FUNCTION: 1.SCAN BIT PATTERN AND RETURN A UCB ADDRESS TO THE       *
*              CALLER WHEN A REAL UCB (BIT=1) IS FOUND                *
*            2.CONTINUE SCAN IF CALLER WISHES TO                      *
*                                                                     *
*  INPUT:                                                             *
*            R1 PTS TO LUT                                            *
*                                                                     *
*  OUTPUT:                                                            *
*            R1  CONTAINS UCB ADDR                                    *
*                      OR 0 IF UCB'S HAVE BEEN SEARCHED               *
*                                                                     *
***********************************************************************
          ENTRY UCBSCAN
         ENTRY  UCBCONT
WORDSIZE EQU   32
BYTESIZE EQU   7
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12                      LCT BASE
R13      EQU   13
R14      EQU   14
R15      EQU   15
X0       EQU   0
ONE      EQU   1
THREE    EQU   3
OFFSET   EQU   12
FOUR     EQU   4
EIGHT    EQU   8
         SAVE  (14,12)                 SAVE CALLER'S ENVIRONMENT
         USING DEVINFO,R8              DEVICE INFORMATION TABLE BASE
         BALR  R9,R0                   ESTABLISH BASE
         USING *,R9                    **
         MODID BR=YES                                            Y01886
         LA    R11,SCANSAVE            INCREMENT TO NEXT SAVE AREA
         ST    R11,EIGHT(R13)          SAVE FORWARD LINK
         ST    13,FOUR(R11)        SAVE BACKWARD LINK
         LR    R13,R11                 PT TO THIS ROUTINES SAVE AREA
         LR    R12,R1               SAVE PTR TO LUT
         L     R8,VCONBP               **
*
         LA    R2,ONE              INCREMENT FOR OUTER LOOP
         SR    R3,R3
         LH    R3,DVPTSIZE         COMPARAND-SIZE BIT PATTERN
         BCTR  R3,0
         SR    R7,R7               INDEX FOR OUTER LOOP
         SR    R10,R10             INITIALIZE WORD POINTER
         B     SC002
SC001    LA    R10,4(R10)          INCREMENT TO NEXT WORD
SC002    LA    R5,WORDSIZE         COMPARAND FOR INNER LOOP
         L     R6,DVBITS(R10)      WORD FROM BIT PAT FOR TEST
SC003    LTR   R6,R6               TEST IF BIT IS ON (HIGH BIT)
         BM    SC004               YES-BRANCH
SC006    EQU   *
         BXH   R7,R2,SC005         OUTER LOOP CONTROL
         SLL   R6,ONE              NO-GET NEXT BIT
         BCT   R5,SC003            INNER LOOP CONTROL
         B     SC001               GET NEXT WORD IN PATTERN
SC004    LR    R1,R7               GET ENTRY NUMBER
         SLL   R1,ONE             ENTRIES TWO BYTES LONG
         AR    R1,R12              ADD START OF LUT
         LH    R1,X0(R1)               PLACE UCB ADDR IN PARM REG
         N     R1,UCBMASK         TURN OFF PROPAGATED SIGN OF   Z30ANPE
*                                 16 BIT UCB ADDR               Z30ANPE
         B     RETURN             RETURN
SC005    EQU   *
         SR    R1,R1              INDICATE END OF SCAN
RETURN   STM   R14,R12,OFFSET(R13)     SAVE SUBRTNS RGS
         LR    R15,R7
         L     R13,FOUR(R13)           RETURN TO CALLER'S SAVE AREA
         LM    R2,R12,28(R13)          **
          L    R14,12(R13)
         BR    R14                     RETURN
*
* ENTRY TO CONTINUE SCAN
UCBSCAN  EQU  *
         USING *,R15
UCBCONT  STM   R14,R12,OFFSET(R13)     SAVE CALLERS REGS
         LA    R11,SCANSAVE        GET ADDR OF SAVE AREA
         ST    13,FOUR(R11)        SAVE BACKWARD LINK
         LR    R13,R11             POINT TO CURRENT SAVEAREA
         LM    R14,R12,OFFSET(R13)     RESTORE SUBRTN'S REGS
         DROP R15
         B     SC006               RETURN TO PROCESSING
* CONSTANTS
         DS    0F
UCBMASK  DC    XL4'0000FFFF'                                    Z30ANPE
SCANSAVE DS    18F
VCONBP   DC    V(IEFDEVPT)
DEVINFO  DSECT
DVPTLEN  DS    CL2           NUMBER OF BYTES CONTAINED IN BIT PATRN
DVPTSIZE DS    CL2                 NUMBER OF BITS IN MAIN PAT
DVPTDEV  DS    CL2           NUMBER OF REAL DEVICES SYSGENNED
DVPT2321 DS    CL2           NUMBER OF DATACELLS GENNED
DVPT     DS    CL4           RESERVED
DVBITS   DS    CL1           BIT PATTERN (VARIABLE LENGTH)
         END
