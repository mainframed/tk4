         TITLE 'IGE0204G TCAM CONTROL UNIT NOT OPERATIONAL && START/STOC
               P UNIT EXCEPTION && TIME OUT ERP'
IGE0204G CSECT
*A-000000-999999                                               @Y02X6E0
*CHANGE ACTIVITY= AS FOLLOWS:
******************** MICROFICHE FLAGS *********************** SUPT CODE
*A744770-744820,755930-755960,756966-756984                      A48248
*A300000,431000-435000,490300-492700,580400-581600               S22025
*A740060-748940,755000-758700                                    S22025
*C010000,060000-110000,220000                                    S22025
*C742040,742340,743720,745880,755700,756500,757100-758100        S22025
*C757990                                                        SA55420
*C741020,741080,755800                                          SA55958
*A741080,741110,755850                                          SA55958
*C055000,060000                                                  S21903
*D491200,748160,755600                                           S21903
*A740660,744780,755850                                          SA59006
*C744780                                                        SA59006
*D740900-740960                                                 SA59006
*A212000,213000-213500,252000,307000,315000,434100,482000-484000,Y02027
*A567000,710300,775000                                           Y02027
*C150000,180000,212500,218000-220000,240000,246000,310000,450000,Y02027
*C710000,712000,712700-713000,717800,746096-746104,748520        Y02027
*C741460                                                        OY00469
*C070000,580000                                                @Z30AAEE
*D530000                                                       @Z30AAEE
*A744900                                                       @SA70884
*A580000,581600                                                @SA74263
*D740300                                                       @SA74263
*C744784                                                       @YA10219
*C746104                                                       @OZ07810
*C744784-744790                                                @OY12267
*C596000                                                       @OY14092
         SPACE 3
***********************************************************************
*                                                                     *
*MODULE-NAME= IGE0204G                                           S21903
*                                                                S21903
*DESCRIPTIVE-NAME= START/STOP ERP (THIRD LOAD)                   S21903
*                                                                S21903
*COPYRIGHT= 'NONE'                                               S21903
*                                                                     *
*  STATUS: CHANGE LEVEL 8                                      @Z30AAEE
*                                                                     *
* FUNCTION -- TO ISSUE WTO REFLECTING CONTROL UNIT STATUS.  FOR  S22025
*   UNIT EXCEPTIONS, TO ATTEMPT TO CLEAR ERROR SITUATION BY      S22025
*   ISSUING A BREAK COMMAND OR READ SKIP.  THE ACTION TAKEN IS   S22025
*   DEVICE DEPENDENT.  FOR TIME OUTS, ACTION IS ALSO DEVICE AND  S22025
*   OPERATION DEPENDENT.  THE FAILING CCW MAY BE RETRIED OR A    S22025
*   BREAK COMMAND MAY BE ISSUED.                                 S22025
*                                                                     *
*ENTRY POINTS -- FIRST EXECUTABLE INSTRUCTION.                        *
*                                                                     *
*INPUT --                                                             *
*   R1 - POINTER TO I/O SUPERVISOR BLOCK                         Y02027
*   R15 - ENTRY POINT ADDRESS                                         *
*                                                                     *
*OUTPUT -- R1 POINTS TO I/O SUPERVISOR BLOCK                     Y02027
*                                                                     *
*EXTERNAL ROUTINES - 'WTO' - FOR MESSAGE TO SYSTEM CONSOLE            *
*                  - IGG019QE (AOS/TCAM SIO APPENDAGE) - TO CONVERT   *
*   VIRTUAL CCW ADDRESSES TO REAL BEFORE RETRY.                       *
*                                                                     *
*EXITS-NORMAL -- R1 POINTS TO I/O SUPERVISOR BLOCK               Y02027
*        SVC   15   'ERP IN CONTROL (IOSERR)' AND 'EXCEPTIONAL   Y02027
*                CONDITION(IOSEX)'. RETURN TO IOS TO RETRY THE   Y02027
*                   ERROR.                                       Y02027
*                SVC  3                                          S22025
*                                                                     *
*        SVC   15   'NO ERROR FLAGS'. RETURN TO LINE END         Y02027
*                   APPENDAGE. ERROR CLEARED                     Y02027
*        SVC   3                                                 Y02027
*   TO SCHEDULE NEXT LOAD OF ERP                                 S22025
*        L     R13,DECIMAL VALUE OF NEXT LOAD                    S22025
*        L     R14,X'10'      CVT ADDRESS                        S22025
*        L     R14,X'2C'(14)  ADDRESS OF XCTL ROUTINE            S22025
*        BR    R14            EXIT TO XCTL                       S22025
*EXITS-ERROR -- R1 POINTS TO I/O SUPERVISOR BLOCK                Y02027
*        SVC   15   'EXCEPTIONAL CONDITION(IOSEX)'. RETURN TO    Y02027
*                   LINE END APPENDAGE. ERROR PERMANENT.         Y02027
*        SVC   3                                                      *
*                                                                     *
*TABLES/WORK AREAS --                                                 *
*   TAVTD                                                             *
*   TLCBD                                                             *
*   TCCWD                                                        S22025
*   TTRMD                                                        S22025
*   TSCBD                                                        S22025
*   IECDIOSB                                                     Y02027
*                                                                     *
*ATTRIBUTES -- SERIALLY REUSABLE, REFRESHABLE, SUPERVISOR KEY,   Y02027
*              SUPERVISOR MODE, ENABLED                          Y02027
*                                                                     *
*NOTES -- THE OPERATION OF THIS MODULE DOES NOT DEPEND UPON A         *
*   PARTICUALR INTERNAL REPRESENTATION OF THE EXTERNAL CHARACTER      *
*   SET.                                                              *
*                                                                     *
*   R1 REMAINS TRANSPARENT TO ALL LOADS OF ERP.                       *
*                                                                     *
***********************************************************************
         EJECT
         USING *,R15
         USING IEDQLCB,RLCB
         USING IEDQAVTD,RAVT                                     S22025
         USING IEDQCCW,RCCW                                      S22025
         USING IEDQSCB,RSCB                                      S22025
         USING IHADCB,RDCB                                       S22025
         USING IOSB,RIOSB                                        Y02027
         SPACE 2
*        EQUATES
         SPACE 1
         SPACE 2
R0       EQU   0                        WORK REGISTER            Y02027
RIOSB    EQU   1                        ADDR OF I/O SUPVR BLOCK  Y02027
RIOB     EQU   2                        IOB REGISTER - REQUIRED BY
*                                       SIO APPENDAGE.
RUCB     EQU   3                        UCB REGISTER
RLCB     EQU   4                        LCB REG.
RWKA     EQU   7                        WORK REGISTER
RTST     EQU   7                        ADDR OF REQUEST QUEUE    Y02027
*                                       ELEMENT                  Y02027
RSIO     EQU   8                        SIO APPENDAGE ADDRESS.
RWKB     EQU   9                        WORK REGISTER
RSAVE    EQU   13                       REGISTER FOR SAVING RQE
*                                       ADDRESS.
RAVT     EQU   2                        AVT BASE REGISTER        S22025
REG5     EQU   5                        WORK REGISTER            S22025
RCCW     EQU   6                        POINTER TO FAILING CCW   S22025
REG8     EQU   8                        WORK REGISTER            S22025
RSCB     EQU   10                       SCB BASE REGISTER        S22025
RDCB     EQU   11                       DCB BASE REGISTER        S22025
RLINK    EQU   13                       WORK REGISTER            S22025
RXCTL    EQU   14                       REGISTER FOR XCTL ADD    S22025
         SPACE 7
R15      EQU   15                       ADDRESSABILITY REGISTER
EREXCP   EQU   15                       ERROR EXCP
RETURN   EQU   3                        ETURN SVC FOR TRANSIENT S
IOBOFFST EQU   4                        IOB OFFSET IN RQE
X20      EQU   X'20'                    ERP IN CONTROL
EIGHT    EQU   8                   CONSTANT FOR CSW
IGE0204G IEDHJN BEGIN                   MODULE ID AND DATE       S22025
         EJECT
         L     RTST,IOSUSE              PICKUP ADDR OF RQE       Y02027
         L     RLCB,IOBOFFST(R0,RTST)   ADDRESS OF IOB
         SH    RLCB,LCBBKUP             SET LCB BASE
         L     RUCB,IOSUCB              ADDRESS OF UCB         @Z30AAEE
         L     RSCB,LCBSCBA-1           SET SCB BASE           @SA74263
         TM    LCBSIOCC,CC3             IS CONTROL UNIT UP       S22025
         BNO   UNEXUNCK                 BRANCH IF CONT UNIT IS   S22025
*                                       UP - MUST BE UNIT CHK OR S22025
*                                       UNIT EXCEPTION           S22025
         OI    SCBERR4,SCBCTLUN         SET CONTROL UNIT ERROR @SA74263
         OI    LCBCHAIN,LCBNORTY        SET NO RTRY POSSIBLE
         L     RWKA,LCBSTART-1          START ADDRESS
         LA    RWKA,EIGHT(,RWKA)        BUMP BY 8
         ST    RWKA,LCBCSW-1            SET FOR LINEEND
         NI    LCBSIOCC,AVTEFF-CC3      TURN OFF CONTROL UNIT    Y02027
*                                         BUSY FOR LINEEND       Y02027
         CLI   LCBTPCD,TPOPEN           OPEN OR START LINE
         BNE   NOTOPEN                  BRANCH IF NO
         SPACE
         NI    LCBSTAT1,LCBOCNI+LCBOCWTN RESET ALL BUT OPCTL   @OY14092
*                                         FORCE  STOPLINE
NOTOPEN  EQU   *
         MVC   WTO+22(3),13(RUCB)       LINE ID FROM UCB
         LR    RWKA,RIOSB               SAVE IOSB                Y02027
         LR    RWKB,R15                 SAVE BASE
WTO      WTO   'IED064I  LINE     CONTROL UNIT NOT OPERATIONAL',       X
               ROUTCDE=(8),DESC=4
         LR    RIOSB,RWKA        N      RESTORE IOSB             Y02027
         LR    R15,RWKB                 RESTORE BASE
         NI    IOSFLA,AVTEFF-IOSERR     POST PERMANENT ERROR     Y02027
*                                       TO ABNORMAL END APPENDAG Y02027
***********************************************************************
*                                                                     *
*        THE FOLLOWING CODE HAS BEEN ADDED FOR AOS.                   *
*                                                                     *
SVCS     EQU   *
         CLI   LCBECBCC,HIOCC           IOHALT ISSUED            Y02027
         BNE   HIOSUB                   BRANCH NO                Y02027
         SPACE 1                                                 Y02027
         NI    IOSFLA,X'FF'-(IOSERR+IOSEX) RESET ERP IN CTL      Y02027
         MVI   LCBINCAM+1,HIOCC         SET FOR LINEEND          Y02027
HIOSUB   EQU   *                                                 Y02027
**************************************************************** Y02027
*        THE FOLLOWING CODE MAPS THE NECESSARY FIELDS FROM THE   Y02027
*        LINE CONTROL BLOCK TO THE I/O SUPERVISOR BLOCK FOR EXCP Y02027
*        COMPATABILITY DURING RETRY OPERATIONS FROM ERP          Y02027
**************************************************************** Y02027
         MVC   IOSCSW(SEVEN),LCBCSW     MAP CSW FROM LCB TO IOSB Y02027
         MVC   IOSSNS(TWO),LCBSENS0     MAP SENSE INFO FROM LCB  Y02027
*                                       INTO IOSB                Y02027
         MVC   IOSCC(ONE),LCBSIOCC      MAP START I/O CONDITION  Y02027
*                                       CODE FROM LCB TO IOSB    Y02027
         TM    IOSFLA,IOSERR+IOSEX      ARE WE DOING A RETRY     Y02027
         BNO   NOTRETRY                 BRANCH IF NO.            X01004
         LR    RSAVE,RIOSB              SAVE IOSB ADDRESS        Y02027
         L     RTST,IOSUSE              PICKUP ADDR OF RQE       Y02027
         SR    RIOB,RIOB                CLEAR FOR ICM.
         ICM   RIOB,7,IOBPTR(RTST)      GET IOB ADDRESS FROM RQE.
*                                       FOR USE BY SIO APPENDAGE.
         SR    RSIO,RSIO                CLEAR FOR ICM.
         ICM   RSIO,7,DEBPTR(RTST)      GET DEB ADDRESS FROM RQE.
         LA    R15,DEBNMSUB-IEDQDEB     GET APDG. TABLE SIZE     Y02027
         SLR   RSIO,R15                 POINT TO APDG. TABLE     Y02027
         L     RSIO,SIOPTR(ZERO,RSIO)   GET ADDR. OF SIO APPEND- X01004
*                                       AGE FROM APPENDAGE VECTOR
*                                       TABLE.
         SR    R15,R15                  CLEAR R15 TO TELL SIO AP-
*                                       PENDAGE IT'S A SUBROUTINE.
         BAL   RXCTL,SIOENTRY(ZERO,RSIO) LNK TO SIO APPENDAGE AT X01004
*                                       OFFSET +16 TO CONVERT CCW'S
*                                       FROM VIRTUAL TO REAL.
         LR    RIOSB,RSAVE              RESTORE IOSB ADDR        Y02027
*                                                                     *
*        END OF CODE ADDED FOR AOS.                                   *
*                                                                     *
***********************************************************************
NOTRETRY EQU   *                                                 X01004
         SVC   EREXCP                   CONTROL TO NORMAL LINEEND
         SPACE
         SVC   RETURN                   FREE TRANSIENT AREA
         SPACE 5
UNEXUNCK EQU   *
         L     RWKB,LCBSTART-1          ASSUME RESTART FROM      S22025
*                                       FAILING CCW              S22025
         L     RCCW,LCBCSW-1            GET FAILING CCW+8        S22025
         SH    RCCW,H8                  ADJUST TO FAILING CCW    S22025
         L     RAVT,CVTPTR              GET CVT ADDRESS          S22025
         L     RAVT,AVTCVTPT(,RAVT)     GET LOAD LIST            S22025
         L     RAVT,0(,RAVT)            SET UP AVT BASE          S22025
         SPACE 2
         MVC   LCBERCCW+7(1),D19(RUCB)  MOVE ADAPTER TYPE        S22025
         NI    LCBERCCW+7,X'F0'         CLEAR CONT UNIT TYPE     S22025
         TM    LCBCSW+3,UNITCHK         IS IT TIMEOUT ?
         BO    TIMEOUT                  BRANCH = YES, ELSE FALL  S22025
*                                       THROUGH FOR UNIT EXCPTN  S22025
         CLI   LCBERCCW+7,TTYTYPE1     TTY TYPE 1 ADAPTER       SA55958
         BE    BREAK                   BRANCH = YES             SA55958
         CLI   LCBERCCW+7,TTYTYPE2     TTY TYPE 2 ADAPTER       SA55958
         BE    BREAK                   BRANCH = YES             SA55958
         MVC   LCBERCCW+7(1),D19(RUCB)  MOVE ADAPTER TYPE        S22025
         NI    LCBERCCW+7,X0F           CLEAR ADAPTER TYPE       S22025
         CLI   LCBERCCW+7,X02           2701 CONTROL UNIT ?      S22025
         BE    SKIPEXIT                 IF YES, BRANCH TO READ   S22025
*                                       SKIP                     S22025
         SPACE 2
         TM    LCBTSOB,LCBTSBUF         TSO SESSION ?            S22025
         BO    CHKCOUNT                 YES - CHECK RETRY COUNT  Y00469
         TM    LCBTSOB,LCB2741N         IS THIS A 2741 TERMINAL  S22025
         BNO   GORETRY                  BRANCH IF NO             S22025
         TM    LCBSTAT1,LCBSENDN        WAS LINE SENDING  ?      S22025
         BNO   BREAK                    BRANCH IF NO             S22025
         ST    RLCB,LCBERCCW+8          GET ANY VALID ADDRESS    S22025
         MVI   LCBERCCW+8,CCWBREAK      SET BREAK COMMAND CODE   S22025
         MVC   LCBERCCW+12(12),UNXFLCNT SET FLAGS AND COUNT FOR  S22025
*                                       BREAK AND WRITE CIRC D   S22025
         LA    REG5,LCBERCCW            GET DATA ADD FOR CIRC D  S22025
         ST    REG5,LCBERCCW+16         MOVE ADDRESS INTO CCW
         MVI   LCBERCCW+16,CCWWRITE     SET WRITE COMMAND CODE   S22025
         MVC   LCBERCCW(2),EOASHFT      SET UP EOA AND SHIFT     S22025
         LA    REG5,LCBERCCW+8          SET UP ADDRESS FOR EXCP  S22025
         MVI   LCBINCAM+1,BRKRET        RETURN INDEX FOR 504G    S22025
         B     BREAK3                   GO EXCP                  S22025
         SPACE 2
GORETRY  EQU   *                                                 S22025
         CLI   LCBINCAM,RETRY           RETRY LIMIT REACHED ?    S22025
         BNE   TRYRETRY                 BRANCH = NO              S22025
         OI    SCBERR3,SCBXCEPN         TURN ON UNIT EXCPTN BIT  S22025
         TM    LCBTSOB,LCBTSBUF         IS THIS  TSO ?           S22025
         BZ    SKIPEXIT                 BRANCH = NO              S22025
         B     PERMEXIT                 DON'T SET NORETRY FLAG
         SPACE 2
NORETRY  EQU   *                                                 S22025
         OI    LCBCHAIN,LCBNORTY        SET ON NORETRY FLAG      S22025
PERMEXIT EQU   *                                                 S22025
         MVI   LCBINCAM+1,PERMRET       SET INDICATOR FOR 504G   S22025
LOADSDR  EQU   *                                                 S22025
         LH    RLINK,POSTMOD            POST MODULE              S22025
         L     RXCTL,CVTPTR             GET CVT POINTER          S22025
         L     RXCTL,XCTLADD(,RXCTL)    GET XCTL ROUTINE ADDRESS S22025
         BR    RXCTL                    EXIT TO 504G             S22025
         SPACE 2
SKIPEXIT EQU   *
         MVI   LCBINCAM+1,RDSKIP        SET INDEX CODE FOR 504G  S22025
         ST    RLCB,LCBERCCW            USE LCB ADDRESS          S22025
         MVI   LCBERCCW,CCWREAD         SET UP READ CCW          S22025
         MVC   LCBERCCW+4(4),FLAGCNT    SET UP FLAG AND COUNT    S22025
         B     COMMON                   ISSUE EXCP               S22025
         SPACE 2
TIMEOUT  EQU   *
         TM    CCWOPCDE,CCWWRITE        WRITE CCW ?              S22025
         BZ    TOREAD                   BRANCH IF NO             S22025
         MVC   LCBERCCW+7(1),D19(RUCB)  MOVE ADAPTER TYPE        S22025
         NI    LCBERCCW+7,X'F0'         CLEAR CONT UNIT TYPE     S22025
         CLI   LCBERCCW+7,X'80'         TYPE 3 ADAPTER (2260 R)  S22025
         BNE   SNO                      BRANCH = NO, SHOULD      S22025
*                                       NOT OCCUR                S22020
         MVC   LCBERCCW(16),BRK2848     SET 2848 BREAK SEQUENCE  S22025
         B     BREAK2                   GO EXCP                  S22025
         SPACE 2
SNO      EQU   *                                                 S22025
         OI    SCBERR4,SCBCTLUN         SET CONTROL UNIT ERROR   S22025
         MVI   LCBINCAM+1,POSNO         SET RETURN CODE FOR 504G S22025
         B     LOADSDR                  EXIT TO 504G             S22025
         SPACE 2
TOREAD   EQU   *
         CLI   LCBRESTR,TPRDSKIP        TEXT RELATED CCW ?       S22025
         BNL   TRY2741                  YES, SEE IF TERM IS 2741 S22025
         CLI   LCBRESTR,TPRDRPEB        TEXT RESPONSE ?          S22025
         BE    NORETRY                  BRANCH IF YES            S22025
         CLI   LCBRESTR,TPRDRSPD        READ RESP TO ADDRESSING  S22025
         BE    CHKCOUNT                 BRANCH =YES, GO CHECK    S22025
*                                       RETRY AND REDIAL         S22025
         CLI   LCBRESTR,TPRDRESP        POLLING RESPONSE ?       S22025
         BNE   TRYRETRY                 BRANCH IF NO             S22025
         SPACE 1
         TM    LCBTSOB,LCB2741N         2741 TERMINAL ?          S22025
         BO    CHK2741                  BRANCH IF YES            S22025
         CLI   LCBERCCW+7,TYPE1         TYPE 1 ADAPTER           A48248
         BE    NOTTWX                   BR YES, CANNOT BE TWX   SA59006
         CLI   LCBERCCW+7,TYPE5         DIAL TWX ADAPTER        SA59006
         BNE   NOTTWX                   NO,BRANCH              @OY12267
         TM    LCBTSOB,LCBTSBUF         TSO SESSION            @OY12267
         BZ    CHKCOUNT                 NO,RETRY               @OY12267
         B     NORETRY                  ALLOW HANG UP          @OY12267
NOTTWX   EQU   *                                               @OY12267
         CLI   D16(RUCB),T2740          2740 TERMINAL          @OY12267
         BNE   TRYRETRY                 BRANCH IF NOT            A48248
         TM    D17(RUCB),STATION+TRANSMIT     BASIC 2740         A48248
         BNZ   TRYRETRY                 BRANCH IF NOT            A48248
         B     CHKCLS                   PREPARE TO RETURN TO     S22025
*                                       LINE END APPENDAGE       S22025
*                                       OR TO RETRY CCW          S22025
         SPACE 2
CHK2741  EQU   *                                                 S22025
         TM    LCBTSOB,LCBTSBUF         TSO SESSION ?            S22025
         BO    CHKCLS                   BRANCH IF YES            S22025
         TM    LCBSTAT2,LCBDIAL         IS IT DIAL?            @SA70884
         BO    NORETRY                  ALLOW TO HANG-UP       @SA70884
         ST    RCCW,LCBSTART-1          ASSUME RETRY ON FAILING  S22025
*                                       CCW                      S22025
         TM    SCBSTATE,SCBLCK1N+SCBMSGLN    IS TERM IN LOCK OR  S22025
*                                            EXTENDED LOCK MODE? S22025
         BNZ   SVCS                     BRANCH IF EITHER OR BOTH S22025
         SPACE 1
         TM    AVTBIT1,AVTDLAYN+AVTCLOSN  CLOSEDOWN REQUESTED ?  S22025
         BNZ   BREAK                    IF YES, BRANCH TO BREAK  S22025
         SPACE 1
         TM    LCBSTAT1,LCBOCNI         STOPLINE COMPLETE REQ ?  S22025
         BNZ   BREAK                    IF YES, BRANCH TO BREAK  S22025
         SPACE 1
         LA    REG5,LCBSTCBA-1          SET UP TO TEST FOR       S22025
         L     REG8,LCBSTCBA-1             RECEIVE OR SEND       S22025
         LA    REG8,0(,REG8)                  SCHEDULER          S22025
         CR    REG5,REG8                         IN LCB          S22025
         BNE   ITS2741                  BRANCH IF SEND SCHEDULER S22025
*                                       IN LCB - GO ISSUE BREAK  S22025
*                                       TO PREPARE CONTROL UNIT  S22025
*                                       AND TERMINAL TO RECEIVE  S22025
*                                       OUTPUT                   S22025
         B     SVCS                     ISSUE EREXCP             Y01004
CHKCLS   EQU   *                                                 S22025
         MVI   LCBCSW+3,NORMAL          SET NORMAL ENDING STATUS S22025
         TM    AVTBIT1,AVTDLAYN+AVTCLOSN  CLOSEDOWN REQUESTED ?  S22025
         BNZ   GOODEXIT                 BRANCH IF YES            S22025
         LR    RWKB,RCCW                RESTART ON READ RESPONSE S22025
         TM    LCBSTAT1,LCBOCNI         STOPLINE COMPLETE REQ ?  S22025
         BZ    NOSTOP                   BRANCH IF NO             S22025
GOODEXIT EQU   *                                                 S22025
         NI    IOSFLA,AVTEFF-(IOSERR+IOSEX) SET NORMAL ENDING    Y02027
*                                       STATUS                   Y02027
         B     SVCS                                            @OZ07810
         SPACE 2
         SPACE 2
TRY2741  EQU   *                                                 S22025
         TM    LCBTSOB,LCB2741N         2741 TERMINAL ?          S22025
         BNO   NORETRY                  BRANCH IF NO             S22025
ITS2741  EQU   *                                                 S22025
         OI    LCBCHAIN,LCBNORTY        SET NO RETRY FLAG        S22025
         MVI   LCBINCAM,RTRYMAX         SET MAXIMUM RETRY COUNT  S22025
         SPACE 2
BREAK    EQU   *                                                 S22025
         ST    RLCB,LCBERCCW            USE LCB ADDRESS          S22025
         MVI   LCBERCCW,CCWBREAK        SET UP BREAK CCW         S22025
         MVC   LCBERCCW+4(4),FLGCT41    SET UP FLAGS AND COUNT   S22025
*                                       FOR 2741 BREAK           S22025
         TM    LCBTSOB,LCB2741N         2741 TERMINAL ?          S22025
         BO    BREAK2                   BRANCH IF YES            S22025
         MVC   LCBERCCW+4(4),BRKFLGCT   SET UP FLAGS AND COUNT   S22025
*                                       FOR TELETYPE BREAK       S22025
BREAK2   MVI   LCBINCAM+1,BRKRET        RETURN INDEX FOR 504G    S22025
COMMON   EQU   *                                                 S22025
         LA    REG5,LCBERCCW            SAVE EXCP START ADDRESS  S22025
BREAK3   EQU   *                                                 S22025
         MVC   LCBSNSV,LCBSENS0         SAVE CURRENT SENSE       S22025
         MVC   LCBCSWSV,LCBCSW          SAVE CURRENT CSW         S22025
         LR    RWKB,REG5                SET START ADDRESS        S22025
         B     NOSTOP                   GO EXCP                  S22025
         SPACE 2
         SPACE 2
TRYRETRY EQU   *                                                 S22025
         CLI   LCBRESTR,TPTWXID         TWX ID ?                 S22025
         BE    REDIAL                   BRANCH = YES             S22025
         TM    LCBCPA+16,X'20'          DIAL INITIAL CONTACT     S22025
         BZ    CHKCOUNT                 BRANCH IF NO             S22025
         LA    RWKB,LCBCPA+32           SKIP TO WRITE COMMAND    S22025
CHKCOUNT EQU   *                                                 S22025
         CLI   LCBINCAM,RETRY           RETRY LIMIT REACHED ?    S22025
         BNL   PERMEXIT                 BRANCH IF YES            S22025
         IC    RWKA,LCBINCAM            INSERT RETRY COUNT       S22025
         LA    RWKA,1(,RWKA)            ADD ONE                  S22025
         STC   RWKA,LCBINCAM            SAVE COUNT IN LCB        S22025
NOSTOP   EQU   *                                                 S22025
         ST    RWKB,LCBSTART-1          SET START ADDRESS        S22025
         MVI   LCBSENS0,AVTEZERO        CLEAR SENSE FOR RETRY    Y02027
         B     SVCS                     ISSUE EREXCP             Y01004
         SPACE 2
REDIAL   EQU   *                                                 S22025
         LA    RWKB,LCBCPA+16           SET UP TO REDIAL         S22025
         B     CHKCOUNT                 GO CHECK RETRY COUNT     S22025
         SPACE 5
         EJECT
LCBBKUP  DC    AL2(LCBFLAG1-IEDQLCB)    TO ADJUST LCB BASE
BRKRET   EQU   8                        RETURN INDEX FOR 504G    S22025
RDSKIP   EQU   4                        RETURN INDEX FOR 504G    S22025
CC3      EQU   X'30'                    FLAG = CONTROL UNIT NOT  S22025
*                                       UP                       S22025
UNITCHK  EQU   X'02'                    UNIT CHECK MASK          S22025
H8       DC    H'8'                     TO ADJUST CCW POINTER
ONE      EQU   1                                                 Y02027
TWO      EQU   2                                                 Y02027
SEVEN    EQU   7                                                 Y02027
HIOCC    EQU   X'48'                    IOHALT COMPLETION CODE   Y02027
TTYTYPE1 EQU   X'40'                   TTY TYPE 1 ADAPTER FLAG  SA55958
TTYTYPE2 EQU   X'50'                   TTY TYPE 2 ADAPTER FLAG  SA55958
TYPE5    EQU   X'50'                    DIAL TWX ADAPTER        SA59006
D19      EQU   19
D16      EQU   16                       UCB INDEX TO TERM TYPE   A48248
D17      EQU   17                       AND TO TERM FEATURES     A48248
RTRYMAX  EQU   X'02'                    MAXIMUM RETRY COUNT      S22025
X0F      EQU   X'0F'                    MASK OFF ADAPTER TYPE    S22025
X02      EQU   X'02'                    MASK FOR 2701            S22025
RETRY    EQU   X'02'                    MAXIMUM RETRY COUNT      S22025
PERMRET  EQU   20                       RETURN INDEX FOR 504G    S22025
POSTMOD  DC    AL2(5047)                POST MODULE
NORMAL   EQU   X'0D'                    NORMAL ENDING STATUS     S22025
POSNO    EQU   16                       RETURN INDEX FOR 504G    S22025
CVTPTR   EQU   X'10'                    CVT ADDRESS              S22025
XCTLADD  EQU   X'2C'                    CVT-XCTL ROUTINE OFFSET  S22025
EOASHFT  DC    X'169C'                  EOA AND SHIFT CHARACTER  S22025
*        TO INSURE PROPER CONTROL UNIT AND TERMIANL SETTINGS     S22025
T2740    EQU   X'54'                    UCB TYPE MASK FOR 2740   A48248
STATION  EQU   X'08'                    STATION CONTROL          A48248
TRANSMIT EQU   X'04'                    TRANSMIT CONTROL         A48248
TYPE1    EQU   X'10'                    TYPE 1 ADAPTER MASK      A48248
ERPOFF   EQU   X'DB'                    TO RESET ERP FLAG        S22025
ZERO     EQU   0                                                 X01004
IOBPTR   EQU   5                        OFFSET OF IOB ADDR. IN RQE.
DEBPTR   EQU   9                        OFFSET OF DEB ADDR. IN RQE.
SIOPTR   EQU   4                        OFFSET OF SIO APDG. ADDRESS
*                                       IN APDG. VECTOR TABLE.
SIOENTRY EQU   34                       OFFSET OF SUBRTNE. ENTRY Y02027
*                                       POINT IN SIO APPENDAGE.
         SPACE 2
***      FLAGS AND COUNT FOR TELETYPE BREAK CCW              *** S22025
BRKFLGCT DC    AL1(CCWSLI),AL2(0),XL1'30'                        S22025
***      FLAGS AND COUNT FOR 2741 BREAK CCW IF TIME OUT      *** S22025
FLGCT41  DC    AL1(CCWSLI),AL2(0),XL1'05'                        S22025
***      FLAGS AND COUNT FOR 2741 BREAK CCW IF UNIT EXCPTN   *** S22025
UNXFLCNT DC    AL1(CCWCC+CCWSLI),AL2(0),XL1'05'                  S22025
         DC    XL1'01',AL3(0),AL1(CCWSLI),AL2(0),XL1'02'         S22025
***      FLAGS AND COUNT FOR READ SKIP                       *** S22025
FLAGCNT  DC    AL1(CCWSKIP),XL1'0',X'FFFF'                       S22025
***      FLAGS AND COUNT FOR 2260 REMOTE BREAK SEQUENCE      *** S22025
BRK2848  DC    X'42000001',AL1(CCWCC+CCWSLI+CCWSKIP),X'0003C0'  SA55420
         DC    X'41000001',AL1(CCWSLI),X'0003E8'                 S22025
         EJECT
         TDEBD                          DEB DSECT
         EJECT
         SPACE 3
         TTPD
         TCCWD                          S22025
         TTRMD
         TTNTD
         TQCBD
         TSCBD
         DCBD  DSORG=TX
         TAVTD
         TLCBD
         IECDIOSB                                                Y02027
         END
