GE4G     TITLE 'IGE0404G - AUTOPOLL-UNIT CHECK AND UNIT EXCEPTION'
IGE0404G CSECT
         SPACE 3
*CHANGE-ACTIVITY = AS FOLLOWS:
******************** MICROFICHE FLAGS *********************** SUPT CODE
*C412000                                                        OY03838
*D820000,868000-872000,888000-896000,908000-912000              OY03838
*D920000-924000,940000,952000                                   OY03838
*
*                                                                     *
*A100000-102000,114000,152000,214000,222000,282000,313000-314000,Y02027
*A338000,351000,962000                                           Y02027
*C068000,080000,096000,098000,112000,148000,150000,220000,288000,Y02027
*C708000,708500,708700-708800,710300                             Y02027
*A 086000,708050-709350,825000-826000,932100-932900              X01004
*C 088000                                                        X01004
*A676000,816000,935200                                        SA62375
*C623000                                                       @SA73340
*A344000                                                       @SA72487
*C312000-328000,372000-376000,412000,476000,492000-680000,     @SA72487
*C696000-708000,780000-800000,904000-924000                    @SA72487
*D280000,820000,828400,868000-872000,888000-896000,940000,     @SA72487
*D948000-952000                                                @SA72487
*C324000                                                       @SA73147
*C700000                                                       @SA74960
*C584000,A588000                                               @YA10055
*   PACKAGING PROBLEM                                          @OZ09954
*A612000                                                       @OS77930
*A679600,A692000,A860000                                       @OX20161
         SPACE 3
***********************************************************************
*                                                                     *
*MODULE-NAME = IGE0404G                                               *
*                                                                     *
*DESCRIPTIVE-NAME = ERP FOR AUTOPOLL AND RESPONSE TO AUTOPOLL         *
*                                                                     *
*COPYRIGHTS = 'NONE'                                                  *
*                                                                     *
*STATUS:  CHANGE LEVEL 5                                              *
*                                                                     *
*FUNCTION -- TO ATTEMPT RETRY ON UNIT CHECK AND UNIT EXCEPTION ERRORS *
*   ON A POLL COMMAND OR READ RESPONSE TO AUTOPOLL.                   *
*                                                                     *
*ENTRY POINTS -- FIRST EXECUTABLE INSTRUCTION.                        *
*                                                                     *
*INPUT --                                                             *
*   R1 - POINTER TO I/O SUPERVISOR BLOCK                         Y02027
*   R15 - ENTRY POINT ADDRESS                                         *
*                                                                     *
*OUTPUT -- R1 POINTS TO I/O SUPERVISOR BLOCK                     Y02027
*                                                                     *
*EXTERNAL ROUTINE -- IGG019QE (AOS/TCAM SIO APPENDAGE) -- TO CONVERT  *
*   VIRTUAL CCW ADDRESSES TO REAL BEFORE RETRY.                       *
*                                                                     *
*EXITS-NORMAL -- R1 POINTS TO I/O SUPERVISOR BLOCK               Y02027
*        SVC   15   'ERP IN CONTROL (IOSERR)' AND 'EXCEPTIONAL   Y02027
*                CONDITION(IOSEX)'. RETURN TO IOS TO RETRY THE   Y02027
*                   ERROR.                                       Y02027
*        SVC   3                                                      *
*                                                                     *
*        SVC   15   'NO ERROR FLAGS'. RETURN TO LINE END         Y02027
*                   APPENDAGE. ERROR CLEARED                     Y02027
*        SVC   3                                                      *
*                                                                     *
*   TO SCHEDULE NEXT LOAD OF ERP                                      *
*        L     13,DECIMAL VALUE OF NEXT LOAD                          *
*        L   14,X'10'    CVT ADDRESS                                  *
*        L     14,X'2C'(14)    ADDRESS OF XCTL ROUTINE                *
*        BR    14   EXIT TO XCTL                                      *
*                                                                     *
*EXITS-ERROR -- R1 POINTS TO I/O SUPERVISOR BLOCK                Y02027
*        SVC   15   'EXCEPTIONAL CONDITION(IOSEX)'. RETURN TO    Y02027
*                   LINE END APPENDAGE. ERROR PERMANENT.         Y02027
*        SVC   3                                                      *
*                                                                     *
*        L     13,DECIMAL VALUE OF NEXT LOAD                          *
*        L   14,X'10'    CVT ADDRESS                                  *
*        L     14,X'2C'(14)    ADDRESS OF XCTL ROUTINE                *
*        BR    14   EXIT TO XCTL                                      *
*                                                                     *
*   TO SCHEDULE ERROR POST                                            *
*                                                                     *
*TABLES/WORK AREAS --                                                 *
*   TAVTD                                                             *
*   TCCWD                                                             *
*   TLCBD                                                             *
*   TSCBD                                                             *
*   IECDIOSB                                                     Y02027
*                                                                     *
*ATTRIBUTES -- SERIALLY REUSABLE, REFRESHABLE, SUPERVISOR KEY,   Y02027
*              SUPERVISOR MODE, ENABLED                          Y02027
*                                                                     *
*NOTES -- THE OPERATION OF THIS MODULE DOES NOT DEPEND UPON A         *
*   PARTICUALR INTERNAL REPRESENTATION OF THE EXTERNAL CHARACTER      *
*   SET.                                                              *
*                                                                     *
*   R1 REMAINS TRANSPARENT TO ALL LOADS OF ERP.                       *
*                                                                     *
***********************************************************************
         EJECT
         USING *,R15
         USING LCBFLAG1,RLCB
         USING IEDQSCB,RSCB
         USING IEDQCCW,RCCW
         USING IOSB,RIOSB                                        Y02027
R0       EQU   0
RIOSB    EQU   1                        ADDR OF I/O SUPVR BLOCK  Y02027
RSCB     EQU   2                        SCB REGISTER
RUCB     EQU   3                        UCB REGISTER
RLCB     EQU   4                        LCB REG.
RDCB     EQU   5                        DCB BASE
RCCW     EQU   6                        CCW REGISTER
RWKA     EQU   7
R7       EQU   7                        WORK REGISTER          @SA72487
RWKB     EQU   8
RWKC     EQU   9
R8       EQU   8                        WORK REGISTER          @SA73147
R9       EQU   9                        WORK REGISTER          @SA72487
R10      EQU   10                       WORK REGISTER          @SA72487
R11      EQU   11                                              @SA72487
RTST     EQU   12                       TST ADDRESS            @SA72487
RLINK    EQU   13                       LINKAGE FOR SUBSEQUENT LOAD
RSAVE    EQU   13                       SAVE REGISTER FOR IOSB   Y02027
RXCTL    EQU   14                       XCTL REGISTER
R15      EQU   15                       ADDRESSABILITY REGISTER
         EJECT
IGE0404G IEDHJN TAG
         L     RTST,IOSUSE              PICKUP ADDR OF RQE       Y02027
         L     RLCB,IOBOFFST(R0,RTST)   IOB ADDRESS
         L     RSCB,LCBSCBA-1           SCB BASE
         L     RCCW,LCBCSW-1            FAILING CCW+8
         SH    RCCW,H8                  ENDING CCW
         L     R10,LCBCPA+48            ADDR OF ENTIRE POLLIST @SA72487
         SH    R10,H8                   BACK UP TO CONTROL WORD@SA72487
         SR    R7,R7                    CLEAR                  @SA72487
         SR    R11,R11                      FOR IC             @SA72487
         IC    R11,WIDTH(R10)           WIDTH OF ENTRY         @SA72487
         SPACE
         CLI   CCWOPCDE,CCWPOLL         POLL CCW
         BNE   READ                     BRANCH NO-READ
         SPACE
         TM    LCBSENS0,EQUIPCHK+LOSTD  EQUIPMENT CHECK OR LOST D.
         BNZ   SNO                      BRANCH EITHER
         SPACE
         TM    LCBSENS0,TIMEOUT+INTREQ  TIMEOUT OR INT,. REQ.
         BNZ   POLLER                   RETRY POLL             @SA72487
         SPACE
         TM    LCBSENS0,BUSOUT+OVERRUN  BUSOUT OR OVERRUN?     @SA72487
         BZ    POLLER                   BRANCH NO, RETRY       @SA72487
SNO      EQU   *                                               @SA72487
         OI    SCBERR4,SCBCTLUN         SET ERROR IN CTL UNIT  @SA72487
         MVI   LCBINCAM+1,POSNO         SET RETURN             @SA72487
         B     LOADSDR                  EXIT TO IGE0504G       @SA72487
READ     EQU   *
         TM    LCBSENS0,EQUIPCHK+BUSOUT EQUIP CHECK OR BUSOUT
         BNZ   SNO                      BRANCH ETIHER
         L     R9,CCWADDR-1             READ RESPONSE ADDRESS  @SA72487
         IC    R7,0(,R9)                INDEX BYTE             @SA72487
         LA    RCCW,LCBCPA+48           USE ADDR OF FULL POLL C@SA72487
         SR    R0,R0                    CLEAR                  @SA72487
         IC    R0,ACTIVE(,R10)          NO. OF ACTIVE ENTRIES  @SA72487
         LR    R8,R11                   SET FOR FIRST TIME     @SA72487
LOOP     EQU   *                                               @SA72487
         LA    R9,SEVEN(R8,R10)         FIRST/NEXT INDEX BYTE  @SA72487
         CLM   R7,LO,0(R9)              IS THIS THE ENTRY      @SA72487
         BE    FOUND                    BRANCH IF YES          @SA72487
         SPACE
         AR    R8,R11                   ADD ENTRY SIZE         @SA72487
         BCT   R0,LOOP                  BR IF MORE ENTRIES     @SA72487
         LR    R8,R11                   ENTRY WIDTH            @SA72487
         B     USEFIRST                 USE FIRST ENTRY        @SA72487
POLLER   EQU   *                                               @SA72487
         LR    R8,R11                   SET FOR FIRST ENTRY    @SA72487
         LH    R9,CCWCOUNT              ORIGINAL COUNT         @SA72487
         SH    R9,LCBCSW+5              RESIDUAL COUNT         @SA72487
         BNP   USEFIRST                 BRANCH NO DATA TRANSFER@SA72487
         SR    R8,R8                    CLEAR EVEN REG         @SA72487
         DR    R8,R11                   DETERMINE ENTR;Y NO.   @SA72487
         MR    R8,R11                   CONVERT TO OFFSET      @SA72487
         LTR   R8,R9                    COUNT/ADDR ADJUSTMENT  @YA10055
*                                         TO REG EIGHT         @SA72487
         BNZ   USEFIRST                 BRANCH NOT FIRST ENTRY @YA10055
         LR    R8,R11                   SET FOR FIRST ENTRY    @YA10055
USEFIRST EQU   *                                               @SA72487
         L     R9,CCWADDR-1             USE FAILING POLL DATA  @SA72487
         LA    R9,0(R8,R9)              PT TO START OF DATA    @SA72487
         BCTR  R9,0                     BACK UP ONE            @SA72487
         IC    R7,0(,R9)                PICK UP INDEX BYTE     @SA72487
FOUND    EQU   *                                               @SA72487
         SR    R9,R9                    CLEAR REGISTER         @OS77930
         IC    R9,LCBCPA+5              LAST TIMEOUT TERMINAL  @OS77930
         CLR   R9,R7                    IS THIS THE SAME ONE?  @OS77930
         BE    SAMETRM                  BRIF YES               @OS77930
         STC   R7,LCBCPA+5              NO PRESERVE THIS INDEX @OS77930
         MVI   LCBINCAM,ZERO            AND ZERO RETRY FOR HIM @OS77930
SAMETRM  EQU   *                                               @OS77930
         AR    R7,R7                    DOUBLE                 @SA72487
         SR    R10,R7                   PT TO CORRECT INDEX    @SA72487
         MVC   LCBTTCIN,0(R10)          SET UP TTCIN NO RETRY  @SA72487
         L     R10,CCWADDR-1            ORGINAL POLL ADDR      @SA72487
         SR    R8,R11                   SUBTRACT ENTRY SIZE    @SA72487
         ALR   R10,R8                   BUMP TO REPOLL ERROR TE@SA72487
         STCM  R10,ADDR,LCBCPA+25       SET UP FIRST POLL CCW  @SA72487
         STCM  R10,ADDR,LCBINVPT        NEW POLL POINTER       @SA72487
         LH    R10,CCWCOUNT             ORIGINAL COUNT         @SA72487
         SR    R10,R8                   DECREMENT              @SA72487
         STH   R10,LCBCPA+30            SET UP NEW COUNT       @SA72487
         SPACE
         L     RWKA,LCBDCBPT            GET DCB ADDR           @SA62375
         USING IHADCB,RWKA                                     @SA62375
         SR    RWKC,RWKC                CLEAR FOR IC           @SA62375
         IC    RWKC,DCBEIOBX            GET IOB SIZE           @SA62375
         LA    RWKA,0(RLCB,RWKC)        GET END OF IOB         @SA62375
         SH    RWKA,LCBXSIZE            BACK UP TO EXTENSION   @SA62375
         DROP  RWKA                                            @SA62375
         USING IEDQLCBX,RWKA                                   @SA62375
         MVC   LCBERADR(3),LCBINVPT     SET INVLIST POINTER    @SA62375
TRYRETRY EQU   *                                               @SA72487
         TM    LCBSTAT2,LCBSYNC         IS THIS BISYNCH?       @OX20161
         BZ    TESTTWO                  BRANCH IF NO           @OX20161
         CLI   LCBINCAM,RETRYBSC        SIX RETRIES YET?       @OX20161
         BL    BUMPNCAM                 BRANCH IF NOT          @OX20161
TESTTWO  EQU   *                                               @OX20161
         CLI   LCBINCAM,RETRY           RETRY LIMIT REACHED
         BNL   PERMEXIT                 YES
         SPACE
BUMPNCAM EQU   *                                               @OX20161
         IC    R8,LCBINCAM              RETRY COUNT            @SA72487
         LA    R8,ONE(,R8)              ADD ONE                @SA72487
         STC   R8,LCBINCAM              RESTORE                @SA72487
         XC    LCBTTCIN,LCBTTCIN        CLEAR CURRENTLY CONCTD @SA72487
         L     R10,LCBCPA+64            RESPONSE AREA ADDRESS  @SA74960
         MVI   0(R10),0                 CLEAR INDX BYTE RECVD  @SA72487
         MVI   LCBSENS0,0               CLEAR SENSE BYTE       @SA72487
***********************************************************************
*                                                                     *
*        THE FOLLOWING CODE IS ADDED FOR AOS.                         *
*                                                                     *
         CLI   LCBECBCC,HIOCC           IOHALT ISSUED            Y02027
         BNE   SVCS                     BRANCH NO                Y02027
         SPACE 1                                                 Y02027
         NI    IOSFLA,X'FF'-(IOSERR+IOSEX) RESET ERP IN CTL      Y02027
         MVI   LCBINCAM+1,HIOCC         SET FOR LINEEND          Y02027
SVCS     EQU   *                                                 Y02027
**************************************************************** Y02027
*        THE FOLLOWING CODE MAPS THE NECESSARY FIELDS FROM THE   Y02027
*        LINE CONTROL BLOCK TO THE I/O SUPERVISOR BLOCK FOR EXCP Y02027
*        COMPATABILITY DURING RETRY OPERATIONS FROM ERP          Y02027
**************************************************************** Y02027
         MVC   IOSCSW(SEVEN),LCBCSW     MAP CSW FROM LCB TO IOSB Y02027
         MVC   IOSSNS(TWO),LCBSENS0     MAP SENSE INFO FROM LCB  Y02027
*                                       INTO IOSB                Y02027
         MVC   IOSCC(ONE),LCBSIOCC      MAP START I/O CONDITION  Y02027
*                                       CODE FROM LCB TO IOSB    Y02027
         TM    IOSFLA,IOSERR+IOSEX      ARE WE DOING A RETRY     Y02027
         BNO   NOTRETRY                 BRANCH IF NO.            X01004
         LR    RSAVE,RIOSB              SAVE IOSB ADDRESS        Y02027
         L     RTST,IOSUSE              PICKUP ADDR OF RQE       Y02027
         SR    RSCB,RSCB                CLEAR FOR ICM.
         ICM   RSCB,SEVEN,IOBPTR(RTST)  GET IOB ADDR FROM RQE,   X01004
*                                       FOR USE BY SIO APPENDAGE.
         SR    RWKB,RWKB                CLEAR FOR ICM.
         ICM   RWKB,SEVEN,DEBPTR(RTST)  GET DEB ADDR FROM RQE    X01004
         LA    R15,DEBNMSUB-IEDQDEB     GET APDG. TABLE SIZE     Y02027
         SLR   RWKB,R15                 POINT TO APDG. TABLE     Y02027
         L     RWKB,SIOPTR(ZERO,RWKB)   GET ADDR FROM SIO APDG   X01004
*                                       FROM APDG. VECTOR TABLE.
         SR    R15,R15                  CLEAR R15 TO TELL SIO
*                                       APDG. IT'S A SUBROUTINE.
         BAL   RXCTL,SIOENTRY(ZERO,RWKB)  LNK TO SIO APDG AT     X01004
*                                       OFFSET +16 TO CONVERT CCW'S
*                                       FROM VIRTUAL TO REAL.
         LR    RIOSB,RSAVE              RESTORE IOSB ADDR        Y02027
*                                                                     *
*        END OF CODE ADDED FOR AOS.                                   *
*                                                                     *
***********************************************************************
NOTRETRY EQU   *                                                 X01004
         SVC   EREXCP                   RETRY
         SPACE
         SVC   RETURN                   FREE UP TRANSIENT AREA
         SPACE 3
SNO1     EQU   *
         MVI   LCBINCAM+1,POSNO         SET RETURN
         B     LOADSDR                  GO PREPARE TO EXIT       Y02027
PERMEXIT EQU   *
         MVI   LCBINCAM+1,POPERM        SET RETURN
LOADSDR  EQU   *
         LH    RLINK,POSTMOD            504G ID
         L     RXCTL,CVTPTR             CVT ADDRESS
         L     RXCTL,XCTLADD(,RXCTL)    XCTL ADDRESS
         BR    RXCTL                    LINK TO IT
         SPACE
         EJECT
*              CONSTANTS
POSTMOD  DC    AL2(5047)                IGE0504G ID              Y02027
H8       DC    H'8'                     HALFWORD = 8             Y02027
LCBXSIZE DC    AL2((LCBFLAG1-IEDQLCB)+L'LCBXCON) EXTENSION SIZE@SA62375
ONE      EQU   1                        OFFSET VALUE             Y02027
ZERO     EQU   0                        LENGTH                   X01004
TWO      EQU   2                        OFFSET VALUE             Y02027
HIOCC    EQU   X'48'                    HALT I/O COMPLETION CODE Y02027
SEVEN    EQU   7                        CONSTANTS                X01004
LOSTD    EQU   X'02'                    LOST DATA MASK
TIMEOUT  EQU   X'01'                    TIMEOUT MASK
INTREQ   EQU   X'40'                    INTERVENTION REQUIRED
BUSOUT   EQU   X'20'                    BUSOUT MASK
OVERRUN  EQU   X'04'                    OVERRUN MASK
DATACHCK EQU   X'08'                    DATA CHECK IN SENSE BYTE
EQUIPCHK EQU   X'10'                    MASK FOR EQUIPMENT CHECK
RETRY    EQU   X'02'                    RETRY LIMIT
RETRYBSC EQU   X'06'                    BISYNCH RETRY COUNT    @OX20161
CVTPTR   EQU   X'10'                    CVT LOCATION POINTER
XCTLADD  EQU   X'2C'                    OFFSET IN CVT-XCTL ROUTINE
EREXCP   EQU   15                       ERROR EXCP
RETURN   EQU   3                        RETURN VALUE             Y02027
IOBOFFST EQU   4                        IOB OFFSET IN RQE
WIDTH    EQU   2                        OFFSET IN ILIST TO SIZE@SA72487
ACTIVE   EQU   1                        OFFSET TO NO OF ACTIVE @SA72487
LO       EQU   1                        LOW BYTE FOR CLM       @SA72487
ADDR     EQU   7                        MASK FOR STCM          @SA72487
POPERM   EQU   20                       RETURN INDICATOR FOR 504G
POSNO    EQU   16                       RETURN INDICATOR
IOBPTR   EQU   5                        OFFSET OF IOB ADDR. IN RQE.
DEBPTR   EQU   9                        OFFSET OF DEB ADDR. IN RQE.
SIOPTR   EQU   4                        OFFSET OF SIO APDG. ADDRESS
*                                       IN APDG. VECTOR TABLE.
SIOENTRY EQU   34                       OFFSET OF SUBRTNE. ENTRY Y02027
*                                       POINT IN SIO APPENDAGE.
FIVE     EQU   5                        OFFSET 5               @SA73340
         EJECT
         DCBD  DSORG=TX                                        @SA62375
         EJECT
         TDEBD                          DEB DSECT
         EJECT
         TLCBD
         TCCWD
         TSCBD
         IECDIOSB                                                Y02027
         END
