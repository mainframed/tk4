19RL     TITLE '''IGG019RL'' - TAM CHECK ROUTINE'
IGG019RL CSECT
         SPACE 3                                                SA64702
*  CHANGE ACTIVITY AS FOLLOWS
********************MICROFICHE FLAGS ************************ SUPT CODE
*C212000,256000,280000,288000,436000-448000,508000-512000,540000 Y02027
*C584000,588000,816000                                           Y02027
*A779070-779210,779700-779900,826000                             Y02027
*D260000,284000,292000,456000,832000                             Y02027
*D376400-379200                                                @ZA02620
********************MICROFICHE FLAGS ************************ SUPT CODE
*A504000                                                        SA52955
*A778000                                                        SA52483
*C236000,568000                                                 SA52483
*C772000                                                        SA64702
*A532000,564000,596000,655000,779000                            SA63961
*A216000,264000,376000,779000,832000                            SA60797
*A496000                                                       @SA75438
*D565000-566000,597000-598000                                  @SA75438
*                                       PECB DSECT CHG         @OZ07830
         SPACE 3                                                SA64702
***********************************************************************
*                                                                     *
*TITLE: 'IGG019RL' CHECK ROUTINE                                      *
*                                                                     *
*MODULE NAME = IGG019RL                                               *
*                                                                     *
*DESCRIPTIVE NAME = CHECK ROUTINE                                     *
*                                                                     *
*COPYRIGHT = 'NONE'                                                   *
*                                                                     *
*STATUS:  CHANGE LEVEL 5                                              *
*                                                                     *
*FUNCTION:  THIS ROUTINE TESTS THE DECB COMPLETION AND TAKES          *
*   APPROPRIATE ACTION.  FOR NORMAL COMPLETION(X'7F'), CONTROL IS     *
*   IMMEDIATELY RETURN TO THE USER.  IF DATA HAS APPEARED ON THE      *
*   READ-AHEAD QUEUE SINCE THE READ ROUTINE LOOKED AT IT(X'40'),      *
*   THE READ ROUTINE IS INVOKED VIA BALR.  THE DECB COMPLETION CODE   *
*   IS RECHECKED WHEN THE READ ROUTINE RETURNS.  FOR SETEOF, THE      *
*   EODAD EXIT, IF SPECIFIED, IS TAKEN.  OTHERWISE, THE USER GETS A   *
*   RETURN CODE.  IF A READ OR WRITE ERROR IS DETECTED, THE SYNAD     *
*   EXIT, IF SPECIFIED, IS TAKEN.  OTHERWISE, THE USER GETS A RETURN  *
*   CODE.  IF NONE OF THE ABOVE CODES IS FOUND, CHECK WAITS FOR A     *
*   MESSAGE ON THE READ-AHEAD QUEUE.                                  *
*ENTRY POINT:                                                         *
*        IGG019RL                                                     *
*INPUT:                                                               *
*   1-   DATA EXTENT BLOCK ADDRESS                                    *
*   13-  REGISTER SAVE AREA ADDRESS                                   *
*   14-  RETURN ADDRESS                                               *
*   15-  ENTRY POINT ADDRESS                                          *
*OUTPUT: RETURN CODE IN REG 15                                        *
*EXTERNAL ROUTINES:                                                   *
*        IGG019RG (GET/READ)                                          *
*EXITS-NORMAL: RETURN CODE OF X'00'                                   *
*EXITS-ERROR:  RETURN CODE OF X'04' FOR SETEOF WITHOUT EODAD          *
*              RETURN CODE OF X'08' FOR WORKAREA OVERFLOW(READ)       *
*              RETURN CODE OF X'0C' FOR INVALID DESTINATION           *
*              RETURN CODE OF X'04' FOR SEQUENCE ERROR (WRITE)        *
*              RETURN CODE OF X'10' FOR THRESHOLD REACHED             *
*              RETURN CODE OF X'04' FOR QUICK CLOSE                   *
*TABLES/WORK AREAS:                                                   *
*   DECB                                                              *
*   DCB                                                               *
*   DEB                                                               *
*   PECB                                                              *
*ATTRIBUTES -- SERIALLY REUSABLE,REENTERANT,PROBLEM PROGRAM MODE      *
*                                                                     *
*NOTES: THE OPERATION OF THIS MODULE DEPENDS UPON AN INTERNAL         *
*   REPRESENTATION OF THE EXTERNAL CHARACTER SET WHICH IS EQUIVALENT  *
*   TO THE ONE USED AT ASSEMBLY TIME.  THE CODING HAS BEEN ARRANGED   *
*   SO THAT REDEFINITION OF 'CHARACTER' CONSTANTS, BY REASSEMBLY,     *
*   WILL RESULT IN A CORRECT MODULE FOR THE NEW DEFINITIONS.          *
*                                                                     *
***********************************************************************
         EJECT
RZERO    EQU   0
RPARM    EQU   1                        ADDRESS OF DECB OR ECB
RDCB     EQU   2                        ADDRESS OF DCB
RDEB     EQU   3                        DATA EXTENT BLOCK
RDECB    EQU   4                        ADDRESS OF DECB
RPECB    EQU   5                        PECB BASE                Y02027
RTEMP    EQU   6                        GENERAL WORK REGISTER
RAVT     EQU   7                        AVT BASE REGISTER       SA60797
RBASE    EQU   12                       MODULE BASE REGISTER
RSAVE    EQU   13                       REGISTER SAVEAREA POINTER
RETURN   EQU   14                       RETURN POINT REGISTER
RENTRY   EQU   15                       ENTRY POINT REGISTER
         USING IGG019RL,RENTRY          TEMPORARY BASE          SA52483
IGG019RL IEDHJN INIT19RL                MODULE NAME AND DATE    SA52483
         STM   RETURN,RBASE,12(RSAVE)   SAVE REGISTERS          SA52483
         DROP  RENTRY                   DROP TEMPORARY BASE     SA52483
         BALR  RBASE,RZERO              ESTABLISH ADDRESSABILITY
START    EQU   *
         USING *,RBASE                  CSECT BASE REGISTER
 EJECT
         USING IEDQPECB,RPECB           PECB ADDRESSABILITY      Y02027
         USING IHADECB,RDECB            DECB BASE REGISTER
         USING IEDQAVTD,RAVT            AVT BASE                SA60797
         LR    RDECB,RPARM              ADDRESS OF DECB
         USING IHADCB,RDCB              DCB BASE REGISTER
         L     RDCB,DECDCBAD            GET DCB ADDRESS FROM DECB
         L     RPECB,DCBPECB            GET PECB ADDRESS         Y02027
         STCM  RETURN,SEVEN,PECBRET     SAVE USERS RETURN ADDRY02027
RCODE    EQU   *
         CLI   DECSDECB,COMP            ECB POSTED COMPLETE,I.E.,      X
                                        READ OR WRITE OPERATION        X
                                        COMPLETED
         BE    NORMAL                   BRANCH IF POSTED
         SPACE 3
         CLI   DECSDECB,RDAHDC          DATA ON READ-AHEAD QUEUE
*                                       SINCE READ ROUTINE LOOKED
*                                       AT IT
         BE    READX                    BRANCH IF YES
         CLI   DECSDECB,EODAD           SETEOF ISSUED IN MCP
         BE    EODTST                   BRANCH IF YES
 EJECT
EXITTST  EQU   *
         CLI   DECSDECB,RETERR          MESSAGE NOT FOUND
         BE    RETOUT                   BRANCH IF YES
         TM    DECSDECB,ERR             LOGICAL READ OR WRITE ERROR
         BO    SYNEXIT                  BRANCH IF YES TO PREPARE
*                                       FOR EXIT TO SYNAD
 EJECT
READ     EQU   *
         LA    RPARM,DECSDECB           ADDRESS OF ECB
         WAIT  ECB=(1)                  WAIT FOR TAM SCHEDULER         X
                                        TO TERMINATE
READX    EQU   *
         L     RENTRY,DCBREAD           ADDRESS OF READ ROUTINE
         LM    RZERO,RBASE,20(RSAVE)    RESTORE USER'S REGISTERS
         BALR  RETURN,RENTRY            INVOKE READ ROUTINE
OFFSET   EQU   *
         LR    RBASE,RETURN             PREPARE TO RESET BASE REG
         LA    RBASE,0(0,RBASE)         CLEAR HIGH-ORDER BYTE
         LA    RENTRY,OFFSET-START      OFFSET BACK TO BALR/USING
         SR    RBASE,RENTRY             RESET BASE REGISTER
         LR    RDECB,RPARM              DECB ADDRESS
         L    RDCB,DECDCBAD             DCB ADDRESS
         L     RPECB,DCBPECB            GET PECB ADDRESS         Y02027
         MVC   REG14(4,RSAVE),PECBRET   RESTORE USERS RETURN ADDRY02027
         MVC   REG14+ONE(THREE,RSAVE),PECBRET RESTORE USERS      Y02027
*                                       RETURN ADDRESS           Y02027
         B     RCODE                    GO CHECK ECB CODE        Y02027
 EJECT
EODTST   EQU   *
         TM    DCBEODAD+3,ODD           EODAD EXIT SPECIFIED
         BO    ABORTE                   BRANCH IF NO
         MVC   16(4,RSAVE),DCBEODAD     SET UP BRANCH REGISTER         X
                                        FIELD IN SAVEAREA
UEXIT    EQU   *
         LM    RETURN,RBASE,12(RSAVE)   RESTORE USER'S REGISTERS
         BR    RENTRY                   TAKE USER EXIT
 EJECT
SYNEXIT  EQU   *
         CLI   DECSDECB,STOPQCC         DISABLED TPROCESS      @SA75438
         BE    STOPQX                   BR YES                 @SA75438
         TM    DCBSYNAD+3,ODD           SYNAD EXIT SPECIFIED
         BO    ABORT                    BRANCH IF NO
         CLI   DECSDECB,NOBUF           THRESHOLD CONDITION     SA52955
         BE    CUTOFF                   BRANCH IF YES           SA52955
         XC    PECBSTAT(14),PECBSTAT    CLEAR FIELD              Y02027
         LA    RTEMP,PECBSTAT           GET ADDRESS OF STATUS    Y02027
         ST    RTEMP,DECIOBPT           INDICATORS AND STORE     Y02027
         LA    RDECB,0(0,RDECB)         CLEAR HIGH-ORDER BYTE
         ST    RDECB,20(RSAVE)          ADDR OF DECB IN REG 0 SLOT     X
                                        IN USER'S SAVEAREA
         CLI   DECSDECB,INERR           READ ERROR
         BNE   WRITERR                  BRANCH IF NO
STOPQ    EQU   *                                                SA63961
         MVI   24(RSAVE),BITON          INPUT ERROR FLAG
         MVI   PECBSTAT+THIRTEEN,BIT    WORKAREA OVERFLOW        Y02027
CLEANUP  EQU   *
         MVC   25(3,RSAVE),DECDCBAD+1   ADDRESS OF DCB TO REG 1
*                                       SLOT IN REGISTER SAVEAREA
         MVC   16(4,RSAVE),DCBSYNAD     ADDR OF SYNAD ROUTINE
         B     UEXIT                    GO TO SYNAD
WRITERR  EQU   *
         MVI   24(RSAVE),DSTERFLG       ASSUME INVALID DEST     SA52483
         CLI   DECSDECB,DESTERR         INVALID DESTINATION
         BE    CLEANUP                  BRANCH IF YES
         MVI   24(RSAVE),SEQERR         SEQUENCE ERROR FLAG
         MVI   PECBSTAT+TWO,BITON       SEQUENCE ERROR
         B     CLEANUP                  RETURN
 EJECT
ABORT    EQU   *
         CLI   DECSDECB,INERR           WORKAREA OVERFLOW
         BNE   WRTERR                   BRANCH IF NO
OFLOFLG  EQU   *
         LA    RENTRY,OFLO              ERROR CODE FOR OFLO
         B     EXIT                     RETURN TO USER
WRTERR   EQU   *
         CLI   DECSDECB,DESTERR         INVALID DESTINATION
         BNE   SERR                     BRANCH IF NO
RETOUT   EQU   *
         LA    RENTRY,DESTINV           ERROR CODE
         B     EXIT                     RETURN TO USER
SERR     EQU   *
         CLI   DECSDECB,NOBUF           THRESHOLD CONDITION
         BE    CUTOFF                   BRANCH IF YES
         LA    RENTRY,SEQ               SEQUENCE ERROR
         B     EXIT                     RETURN TO CALLER
CUTOFF   EQU   *
         LA    RENTRY,BOUT              ERROR RETURN CODE
         B     EXIT                     RETURN TO USER
STOPQX   EQU   *                                                SA63961
         LA    RENTRY,STOPQRC           DISABLED ENTRY CODE     SA63961
         B     EXIT                     RETURN TO USER          SA63961
 EJECT
NORMAL   EQU   *
         LA    RENTRY,GOODRC            PUT NORMAL COMPLETION CODE     X
                                        IN RETURN-CODE REGISTER        X
                                        WORD IN USER'S SAVEAREA
EXIT     EQU   *
         RETURN (14,12),T,RC=(15)       RESTORE USER'S REGISTERS       X
                                        AND RETURN CONTROL TO HIM      X
                                        AT NSI FOLLOWING CHECK
ABORTE   EQU   *
         LA    RENTRY,NEOD              SET RETURN CODE FOR USER
*                                       WITHOUT AN EODAD EXIT
         B     EXIT                     RETURN TO USER
 EJECT
COMP     EQU   X'7F'                    NORMAL COMPLETION CODE
BIT      EQU   X'40'                    OUTPUT ERROR BIT
BITON    EQU   X'80'                    INPUT ERROR BIT
EODFLG   EQU   X'41'                    SETEOF INDICATOR IN ECB
DESTINV  EQU   12
OFLO     EQU   8
GOODRC   EQU   0
NEOD     EQU   4
DESTERR  EQU   X'54'
SEQERR   EQU   X'58'
EODAD    EQU   X'70'
RDAHDC   EQU   X'40'
INERR    EQU   X'52'                    READ ERROR FLAG(WRKA OFLO)
ERR      EQU   X'50'                    GENERAL SYNAD INDICATOR
ODD      EQU   X'01'
SEQ      EQU   8                        SEQUENCE ERROR CODE     SA64702
RETERR   EQU   X'50'
BOUT     EQU   16
NOBUF    EQU   X'5C'
DSTERFLG EQU   X'48'                    INVALID DEST FLAGS      SA52483
CVT      EQU   16                       CVT ADDRESS             SA60797
REG14    EQU   12                       REG 14 IN SAVE AREA      Y02027
C1       EQU   1                        CONSTANT                SA60797
TWO      EQU   2                        DISPLACEMENT             Y02027
C3       EQU   3                        CONSTANT                SA60797
THIRTEEN EQU   13                       DISPLACEMENT             Y02027
STOPQCC  EQU   X'51'                    DISABLED QUEUE C.C.     SA63961
STOPQRC  EQU   20                       DISABLED QUEUE R.C.     SA63961
ONE      EQU   1                        OFFSET FOR MOVE          Y02027
THREE    EQU   3                        LEN FOR ADDR MOVE        Y02027
SEVEN    EQU   7                        MASK FOR STCM INST       Y02027
 EJECT
IHADECB  DSECT                          DATA EXTENT CONTROL BLOCK
DECSDECB DS    F                        EVENT CONTROL BLOCK
DECTYPE  DS    H                        OPTION BYTES
DECLNGTH DS    H                        LENGTH OF WORKAREA
DECDCBAD DS    F                        ADDR OF DATA CONTROL BLOCK
DECAREA  DS    F                        ADDR OF USER'S WORKAREA
DECIOBPT DS    F                        ADDR OF IOB
 EJECT
         TPECBD
         TPEWAD                                                  Y02027
 EJECT
         DCBD  DSORG=PS
         TDEBAPD                        APPL PROG DEB            Y02027
DCBPECB  EQU   DCBIOBAD                 TCAM FIELD               Y02027
         EJECT
         EJECT
         TAVTD
         END
