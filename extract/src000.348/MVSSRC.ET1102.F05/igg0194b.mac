194B     TITLE '''IGG0194B'' - TCAM APPL PROG OPEN ERROR INTERFACE'
IGG0194B CSECT
         SPACE 4
*CHANGE-ACTIVITY = AS FOLLOWS:                                        *
*********************** MICROFICHE FLAGS ******************** SUPT CODE
*C256000,368000,376000,388000,436000-728000,752000,852000        Y02027
*C856000-868000,876000-900000,948000,952000                      Y02027
*A732000,948200-951800,952200-954200                             Y02027
*D360000,756000-780000,816000,840000,844000                      Y02027
*C260000,316000,384000-388000,424000,432000,538000,553000      @ZA02601
*C625000,696000-724000,824000-836000,891000-900000             @ZA02601
*A300000,372000,559000,571000,598000,951900                    @ZA02601
*C573700                                                       @OZ07798
*C542200                                                       @OZ09306
*C580000                                                        SA65395
*A538500,538700,539600,539900,872000                           @OZ17630
         SPACE 3
***********************************************************************
*                                                                     *
*TITLE: 'IGG0194B' TCAM APPLICATION PROGRAM OPEN ERROR INTERFACE      *
*                                                                     *
*MODULE NAME = IGG0194B                                         SA21903
*                                                                     *
*DESCRIPTIVE NAME = APPL PROG OPEN ERROR INTERFACE ROUTINE      SA21903
*                                                                     *
*COPYRIGHT = 'NONE'                                             SA21903
*                                                                     *
*STATUS: CHANGE LEVEL 5                                         SA65395
*                                                                     *
*FUNCTION: THIS MODULE PERFORMS CLEANUP FOR PARTIALLY OPENED DCB'S    *
*   THAT EXIST IN THIS UNFINISHED STATE AS A RESULT OF AN OPEN ERROR  *
*   OCCURRING FOR OTHER THAN THE FIRST DCB IN A MULTIPLE OPEN MACRO.  *
*   THE DEB CHAIN IS SCANNED FOR APPL PROG DEB'S FOR WHICH A PARTIAL  *
*   OPEN HAS BEEN DONE.  THIS ROUTINE INTERFACES WITH OPEN/CLOSE      *
*   SUBTASK AS DOES NORMAL APPL PROG CLOSE.  AFTER ALL DEB'S HAVE     *
*   BEEN PROCESSED, CONTROL IS TRANSFERRED TO OPEN ERROR HANDLER      *
*   (IGG01933).                                                       *
*                                                                     *
*ENTRY POINT:                                                         *
*        IGG0194B                                                     *
*                                                                     *
*INPUT: REGISTERS 5,6,7 AND 8 CONTAIN THE FOLLOWING VALUES:           *
*   5 - ADDRESS OF FIRST ENTRY IN DCB PARAMETER LIST                  *
*   6 - ADDRESS OF BEGINNING OF WHERE-TO-GO TABLE                     *
*   7 - ADDRESS OF CURRENT ENTRY IN DCB PARAMETER LIST                *
*   8 - ADDRESS OF CURRENT ENTRY IN WHERE-TO-GO TABLE                 *
*                                                                     *
*OUTPUT: SEE 'INPUT'                                                  *
*                                                                     *
*EXTERNAL ROUTINES:                                                   *
*        NONE                                                         *
*                                                                     *
*EXITS-NORMAL: THIS MODULE XCTLS TO NEXT ENTRY IN WTG TABLE           *
*                                                                     *
*EXITS-ERROR:                                                         *
*        NONE                                                         *
*                                                                     *
*TABLES/WORK AREAS:                                                   *
*        CVT                                                          *
*        AVT                                                          *
*        TCB                                                          *
*        DCB                                                          *
*        DEB                                                          *
*        PEWA                                                         *
*        PROCESS ENTRY                                                *
*        OPEN WORKAREA                                                *
*                                                                     *
*ATTRIBUTES: REENTRANT,REFRESHABLE,TRANSIENT                          *
*                                                                     *
*NOTES: THE OPERATION OF THIS MODULE DEPENDS UPON AN INTERNAL         *
*   REPRESENTATION OF THE EXTERNAL CHARACTER SET WHICH IS EQUIVALENT  *
*   TO THE ONE USED AT ASSEMBLY TIME.  THE CODING HAS BEEN ARRANGED   *
*   SO THAT REDEFINITION OF 'CHARACTER' CONSTANTS, BY REASSEMBLY,     *
*   WILL RESULT IN A CORRECT MODULE FOR THE NEW DEFINITIONS.          *
*                                                                     *
***********************************************************************
         EJECT
*                                                                     *
*              SYMBOLIC REGISTER SPECIFICATION                        *
*                                                                     *
         SPACE
RZERO    EQU   0                        GENERAL WORK REGISTER
RPARM    EQU   1                        PARAMETER/WORK REGISTER
RRCB     EQU   2                        RCB BASE                 Y02027
RPEWA    EQU   3                        PROCESS ENTRY WORKAREA @ZA02601
RDCB     EQU   4                        DATA CONTROL BLOCK BASE REG
RPAR     EQU   5                        ADDRESS OF FIRST ENTRY IN
*                                       DCB PARAMETER LIST
RWTG     EQU   6                        ADDRESS OF WHERE-TO-GO
*                                       (WTG) TABLE
RPARC    EQU   7                        ADDRESS OF CURRENT ENTRY
*                                       IN DCB PARAMETER LIST
RWTGC    EQU   8                        ADDRESS OF CURRENT ENTRY
*                                       IN WHERE-TO-GO (WTG) TABLE
RAVT     EQU   9                        TCAM ADDRESS VECTOR TABLE BASE
RTCX     EQU   9                        AVT EXTENSION BASE     @ZA02601
RCORE    EQU   10                       OPEN WORKAREA BASE REG
RTCB     EQU   11                       TASK CONTROL BLOCK BASE REG
RBASE    EQU   12                       CSECT BASE REG
RDEB     EQU   13                       DATA EXTENT BLOCK BASE @ZA02601
RETURN   EQU   14
RENTRY   EQU   15
         SPACE
*                                                                     *
*        ESTABLISH CONTROL BLOCK AND CSECT ADDRESSABILITY             *
*                                                                     *
         BALR  RBASE,RZERO              ESTABLISH CSECT
         USING *,RBASE                  ADDRESSABILITY
         USING IHADCB,RDCB              DCB
         USING IEDQDEB,RDEB             DEB
         USING FORCORE,RCORE            OPEN WORKAREA
         USING IEDQRECB,RRCB            RCB ADDRESSABILITY       Y02027
         USING IEDQTCB,RTCB             TCB
         USING PSA,RZERO                                       @ZA02601
         EJECT
IGG0194B IEDHJN START                                          @ZA02601
         MODESET  EXTKEY=ZERO                                    Y02027
         L     RCORE,AVTECD4(,RWTGC)    OPEN WORKAREA ADDRESS
         L     RAVT,AVTECD16            CVT ADDRESS
         L     RTCB,AVTEZERO(,RAVT)     POINTER TO NEXT AND
*                                       CURRENT TCB ADDRESSES
         L     RTCB,AVTECD4(,RTCB)      CURRENT TCB ADDRESS
         ICM   RTCX,ALL,CVTAQAVT-CVT(RTCX) IS MCP ACTIVE       @ZA02601
         BZ    EXIT                     NO, BYPASS POST TO MCP @ZA02601
         TM    TCXFLAG1-IEDQTCX(RTCX),TCXMCPCD MCP TERMINATING @ZA02601
         BO    EXIT                     YES, BYPASS POST TO MCP@ZA02601
         L     RENTRY,PSAAOLD           ADDRESS OLD ASCB PTR   @ZA02601
         CLC   ASCBASID-ASCB(TWO,RENTRY),TCXASID-IEDQTCX(RTCX) @ZA02601
*                                       IS APPL A SUBTASK OF   @ZA02601
*                                       MCP                    @ZA02601
         BNE   MCPOK                    BR, NOT IN SAME MEMORY @ZA02601
         L     RENTRY,TCXTCB-IEDQTCX(RTCX) GET MCP TCB ADDRESS @ZA02601
         TM    TCBFLGS-IEDQTCB(RENTRY),TERM IS MCP TERMINATING @ZA02601
         BM    EXIT                     YES, BYPASS POST TO MCP@ZA02601
MCPOK    EQU   *                                               @ZA02601
         SR    RZERO,RZERO              CLEAR FOR STORE        @ZA02601
         ST    RZERO,DXCCW10+FOUR       CLEAR RCB POINTER      @ZA02601
         ICM   RZERO,AD,TCXOCQ-IEDQTCX+ONE(RTCX) GET QCB ADDR  @ZA02601
         BZ    EXIT                     NO INTERFACE FOR APPL  @ZA02601
         L     RDEB,TCBDEB              DEB CHAIN HEAD ADDRESS
*                                                                     *
*        SCAN DEB CHAIN FOR TCAM APPL PROG DEBS THAT ARE ASSOCIATED   *
*        WITH DCBS FLAGGED AS NOT YET OPEN.                           *
*                                                                     *
         SPACE
DEBLOOP  DS    0H
         LA    RDEB,AVTEZERO(,RDEB)     CLEAR HIGH-ORDER BYTE
         LTR   RDEB,RDEB                END OF DEB CHAIN
         BZ    EXIT                     BRANCH IF YES
         TM    DEBTAMID,ID              TCAM APPL PROG DEB
         BO    TCAMDEB                  BRANCH IF YES
NEXTDEB  DS    0H
         L     RDEB,DEBDEBAD-1          NEXT DEB IN CHAIN
         B     DEBLOOP                  GO CHECK THIS ONE
         SPACE
TCAMDEB  DS    0H
         L     RDCB,DEBDCBAD-1          APPL PROG DCB ADDRESS
         TM    DCBOFLGS,OPEN            IS DCB OPENED
         BO    NEXTDEB                  BRANCH IF YES
         ICM   RPEWA,ALL,DEBPEWA-ONE    GET PEWA ADDRESS         Y02027
         BZ    NEXTDEB                  BRANCH IF NO PEWA        Y02027
*                                       INDICATING NO MCP FLAGS  Y02027
*                                       TO RESET                 Y02027
*              SET UP RCB FOR CLOSE SUBTASK                      Y02027
         XC    DXCCW1(4),DXCCW1         CLEAR ECB                Y02027
         ICM   RRCB,ALL,DXCCW10+FOUR    GET RCB ADDRESS        @ZA02601
         BNZ   HAVERCB                  BYPASS RCB GETMAIN     @ZA02601
         LA    RPARM,TCXAIBC-IEDQTCX(RTCX) TCX AIB PTR         @ZA02601
AIBGET   EQU   *                                               @ZA02601
         ICM   RPARM,AD,AIBCHAIN-IEDQAIB(RPARM) ANY AIB'S AVAIL@ZA02601
         BNZ   GOTAIBS                  YES, GET ONE           @ZA02601
         LH    RZERO,TCXSZAIB-IEDQTCX(RTCX)   GET SIZE OF AIB  @OZ17630
         CH    RZERO,FOURK              SIZE GREATER THAN 4K   @OZ17630
         BH    GETCORE                  YES, BRANCH            @OZ17630
         LA    RZERO,HPAGE              2K VALUE               @ZA02601
         AR    RZERO,RZERO              DOUBLE TO PAGE SIZE    @ZA02601
GETCORE  EQU   *                                               @OZ17630
         LR    RCORE,RPARC              SAVE REG ACROSS GETMAIN@ZA02601
         ICM   RDCB,AD,DEBTCBAD         TCB ADDR FOR GETMAIN   @ZA02601
         L     RPARC,PSAAOLD            ASCB ADDR FOR GETMAIN  @ZA02601
         GETMAIN RU,LV=(0),SP=CSA,KEY=KEY6X,BNDRY=PAGE,BRANCH=YES
         LR    RPARC,RCORE              RESTORE RPARC          @ZA02601
         L     RCORE,FOUR(,RWTGC)       RESTORE WORKAREA ADDR  @ZA02601
         L     RDCB,DEBDCBAD-ONE        RESTORE DCB ADDRESS    @ZA02601
         L     RPEWA,DEBPEWA-ONE        RESTORE PEWA ADDRESS   @ZA02601
         LH    RETURN,TCXSZAIB-IEDQTCX(RTCX) SIZE OF AIB       @ZA02601
         CH    RETURN,FOURK             SIZE GREATER THAN 4K   @OZ17630
         BNH   SMALLAIB                 NO, BRANCH             @OZ17630
         LR    RENTRY,RPARM             GET ADDR OF ONLY AIB   @OZ17630
         B     CLRBYTE                  BRANCH TO CLEAR BYTE   @OZ17630
SMALLAIB EQU   *                                               @OZ17630
         LA    RENTRY,HPAGE             2K VALUE               @ZA02601
         LA    RENTRY,HPAGE(RENTRY,RPARM) ADDR OF END OF AIBS  @ZA02601
         SR    RENTRY,RETURN            ADDR OF LAST AIB       @ZA02601
CLRBYTE  EQU   *                                               @OZ17630
         LA    RZERO,ZERO(RPARM)        CLEAR HI ORDER BYTE    @ZA02601
         B     BXLE                     BRANCH TO INITIALIZE   @ZA02601
CHAINAIB EQU   *                                               @ZA02601
         ST    RZERO,AIBCHAIN-ONE-IEDQAIB(RPARM) STORE AIB PTR @ZA02601
         LR    RPARM,RZERO              SAVE CURRENT PTR       @ZA02601
BXLE     EQU   *                                               @ZA02601
         BXLE  RZERO,RETURN,CHAINAIB    LOOP THRU NEW CORE     @ZA02601
*                                       TILL ALL AIBS LINKED   @ZA02601
         LR    RENTRY,RPARM             LAST AIB PTR           @ZA02601
         N     RPARM,CLRLOW             START OF GOTTEN CORE   @ZA02601
         L     RZERO,TCXAIBC-IEDQTCX(RTCX) GET CURRENT AIB PTR @ZA02601
AIBLOOP  EQU   *                                               @ZA02601
         ST    RZERO,AIBCHAIN-ONE-IEDQAIB(RENTRY) LINK AIBS    @ZA02601
         CS    RZERO,RPARM,TCXAIBC-IEDQTCX(RTCX) CHAIN AIBS    @ZA02601
         BNE   AIBLOOP                  RETRY IF CS FAILED     @ZA02601
GOTAIBS  EQU   *                                               @ZA02601
         TS    AIBSTATE-IEDQAIB(RPARM)  ALLOCATE AIB           @ZA02601
         BNZ   AIBGET                   IN USE, RETRY          @ZA02601
         MVC   AIBECBA-IEDQAIB(FOUR,RPARM),CLOS SET ID         @ZA02601
         LA    RRCB,AIBKEY-IEDQAIB(RPARM) BUMP TO RCB          @ZA02601
         ST    RRCB,DXCCW10+FOUR        SAVE RCB ADDRESS       @ZA02601
HAVERCB  EQU   *                                               @ZA02601
         XC    ZERO(RCBSIZE,RRCB),ZERO(RRCB)   CLEAR RCB       @OZ09306
         MVI   RECBKEY,AVTEZERO         SET FLAG FOR CLOSE       Y02027
         MVI   RECBPRI,CLOSPRI          SET CLOSE PRIORITY       Y02027
         MVC   RCBDCBMA,DCBMACRF        MOVE MACRF TO RCB        Y02027
         MVC   RCBTCBFL,TCBFLGS-IEDQTCB(RTCB) MOVE TCB FLAGS   @ZA02601
         LA    RZERO,DXCCW1             LOAD CLOSE ECB ADDRESS   Y02027
         ST    RZERO,RCBPECB            STORE ECB IN RCB         Y02027
         L     RZERO,PSAAOLD            ADDRESS OF CURRENT ASCB@ZA02601
         ST    RZERO,RCBASCB            STORE IN RCB           @ZA02601
         ST    RPEWA,RCBDSNM            PASS PEWA ADDR IN RCB    Y02027
         MVC   RECBQCBA(3),TCXOCQ-IEDQTCX+ONE(RAVT) MOVE OPEN/   Y02027
*                                       CLOSE QCB TO RCB         Y02027
         MVC   RCBTCB+ONE(3),DEBTCBAD   MOVE TCB ADDR TO RCB     Y02027
         STM   RDCB,RENTRY,RCBSAVE      SAVE REGS OVER SETLOCKS@ZA02601
LOCALGET SETLOCK OBTAIN,TYPE=LOCAL,MODE=UNCOND,                @ZA02601X
               RELATED=(PEBPEWA,IEDQEU(LOCALREL),IEDQOT01(LOCALREL),IGGX
               02046(LOCALREL))                                @ZA02601
CMSGET   SETLOCK OBTAIN,TYPE=CMS,MODE=UNCOND,                  @ZA02601X
               RELATED=(PEBPEWA,IEDQEU(CMSREL),IEDQOT01(CMSREL),IGG0204X
               6(CMSREL))                                      @ZA02601
         LA    RPARM,TCXPEWAC-IEDQTCX-PEWALINK+IEDQPEWA(RTCX)  @ZA02601
*                                       FIRST PEWA ADDRESS     @ZA02601
         LA    RPEWA,ZERO(RPEWA)        CLEAR HIGH BYTE        @ZA02601
LOOPPEWA EQU   *                                               @ZA02601
         ICM   RPARM,ALL,PEWALINK-IEDQPEWA(RPARM) GET PEWA     @ZA02601
         BZ    ENDPEWA                  END OF CHAIN           @ZA02601
         CR    RPARM,RPEWA              PEWA IN CHAIN          @ZA02601
         BNE   LOOPPEWA                 NO, CHECK NEXT PEWA    @ZA02601
ENDPEWA  EQU   *                                               @ZA02601
CMSREL   SETLOCK RELEASE,TYPE=CMS,                             @ZA02601X
               RELATED=(PEBPEWA,IEDQEU(CMSGET),IEDQOT01(CMSGET),IGG0204X
               6(CMSGET))                                      @ZA02601
LOCALREL SETLOCK RELEASE,TYPE=LOCAL,                           @ZA02601X
               RELATED=(PEBPEWA,IEDQEU(LOCALGET),IEDQOT01(LOCALGET),IGGX
               02046(LOCALGET))                                @ZA02601
         LM    RDCB,RENTRY,RCBSAVE      RETORE REGS            @ZA02601
         LTR   RPARM,RPARM              PEWA FOUND IN CHAIN    @ZA02601
         BZ    NEXTDEB                  NO, BYPASS POST TO MCP @ZA02601
         OI    PEWAFLG-IEDQPEWA(RPEWA),CFLG SET CLOSE FLAG     @ZA02601
         ST    RRCB,PEWARCB-IEDQPEWA(RPEWA)  STORE RCB IN PEWA @OZ07798
*              POST RCB TO ASYNCHRONOUS READY QUEUE              Y02027
         L     RENTRY,TCXREADY-IEDQTCX(RAVT) ELEMENT ON QUEUE    Y02027
POSTLOOP EQU   *                                                 Y02027
         STCM  RENTRY,SEVEN,RECBLINK    POINT NEW ELEMENT TO     Y02027
*                                       CURRENT ELEMENT          Y02027
         CS    RENTRY,RRCB,TCXREADY-IEDQTCX(RAVT) CURRENT        Y02027
*                                       ELEMENT UPDATED          Y02027
         BNE   POSTLOOP                 BRANCH IF YES, TRY AGAIN Y02027
*                                       NO, QUEUE IS UPDATED     Y02027
         TS    RCBERR                   SET QEU POSTED         @ZA02601
         XC    DEBPEWA(L3),DEBPEWA      CLEAR PEWA ADDRESS     @ZA02601
         XC    DEBPEBAD(L3),DEBPEBAD    CLEAR PEB ADDRESS      @ZA02601
         XC    DEBPCBAD(L3),DEBPCBAD    CLEAR PCB ADDRESS      @ZA02601
         XC    DEBLCMWA(L3),DEBLCMWA    ZER0 LOCATE MODE       @ZA02601
*                                       WORKAREA               @ZA02601
         MVI   DEBTAMID,ZERO            CLEAR TCAM ID          @ZA02601
*                                                                Y02027
*              POST MCP ECB COMPLETE THROUGH CROSS MEMORY POST   Y02027
*                                                                Y02027
         L     RENTRY,TCXAVT-IEDQTCX(RAVT) GET AVT ADDRESS       Y02027
         LA    RENTRY,AVTOSECB-IEDQAVTD(RENTRY) MCP ECB ADDRESS  Y02027
         ST    RENTRY,DXCCW4            STORE IN PARAMETER LIST  Y02027
         L     RENTRY,CVTPTR            ADDRESS OF CVT           Y02027
         LA    RENTRY,CVTBRET-CVT(RENTRY) ERROR ADDRESS        @ZA02601
         ST    RENTRY,DXCCW4+EIGHT      AND STORE IN LIST        Y02027
         MVC   DXCCW4+FOUR(4),TCXASCB-IEDQTCX(RAVT) STORE ASCB   Y02027
*                                       ADDR OF MCP IN LIST      Y02027
         SR    RZERO,RZERO              ZERO COMPLETION CODE     Y02027
         LA    RPARM,DXCCW4             ADDRESS OF LIST          Y02027
         POST  ,(0),MF=(E,(1))
         LA    RPARM,DXCCW1             LOAD ECB ADDRESS         Y02027
         WAIT  ECB=(1)                  WAIT FOR IEDQEU TO END
         B     NEXTDEB                  GO CHECK NEXT DEB
         EJECT
EXIT     DS    0H
*              FREEMAIN RCB                                      Y02027
         ICM   RPARM,ALL,DXCCW10+FOUR   LOAD RCB ADDRESS         Y02027
         BZ    XCTLRTN                  BRANCH IF NO RCB         Y02027
         LA    RZERO,AIBKEY-IEDQAIB     RCB OFFSET IN AIB      @ZA02601
         SR    RPARM,RZERO              ADDRESS OF AIB         @ZA02601
         MVI   AIBSTATE-IEDQAIB(RPARM),AIBFREE FREE AIB        @ZA02601
XCTLRTN  DS    0H
         MVC   AVTEZERO(2,RWTGC),OPERR  ERROR HANDLER IGG01933   Y02027
         LA    RENTRY,DXCCW12           GET ADDRESS OF A WORKAREA
*                                       FOR USE BY XCTL SVC
         MVC   C6(2,RWTG),AVTEZERO(RWTGC) MOVE EXECUTOR ID INTO
*                                       THE PARAMETER LIST
         XCTL  EPLOC=(RWTG),SF=(E,(15))
         EJECT
*                                                                     *
*             C O N S T A N T S   A N D   E Q U A T E S               *
*                                                                     *
         SPACE
ID       EQU   X'C0'                    APPL PROG DEB IDENTIFIER
OPEN     EQU   X'10'                    DCB OPEN FLAG
CLOSPRI  EQU   X'C0'                    CLOSE ELEMENT PRIORITY
C6       EQU   6                        CONSTANT
ZERO     EQU   0                        CONSTANT OF ZERO       @ZA02601
TWO      EQU   2                        CONSTANT OF TWO        @ZA02601
L3       EQU   3                        LENGTH OF THREE        @ZA02601
TERM     EQU   X'C0'                    TCB TERMINATE FLAGS    @ZA02601
AD       EQU   7                        ICM/STCM MASK          @ZA02601
CSA      EQU   231                      COMMON SUBPOOL         @ZA02601
HPAGE    EQU   2048                     HALF PAGE SIZE         @ZA02601
KEY6X    EQU   6                        KEY 6                  @ZA02601
CLRLOW   DS    0F                       MASK USED TO           @ZA02601
         DC    X'FFFFF000'              CLEAR 3 LOW BYTES      @ZA02601
CLOS     DC    C'CLOS'                  CLOSE ID               @ZA02601
ZAPAREA  DC    25H'0'                   ZAP AREA
ONE      EQU   1                        DISPLACEMENT             Y02027
OPERR    DC    C'33'                    OPEN ERROR HANDLER
FOURK    DC    H'4096'                  LENGTH OF A PAGE       @OZ17630
FOUR     EQU   4                        DISPLACEMENT             Y02027
SEVEN    EQU   7                        MASK OF 0111             Y02027
EIGHT    EQU   8                        DISPLACEMENT             Y02027
NINE     EQU   9                        DISPLACEMENT             Y02027
ALL      EQU   15                       MASK 1111                Y02027
         IHAASCB                                               @ZA02601
CVT      DSECT                                                 @ZA02601
         CVT                                                   @ZA02601
         IHAPSA                                                @ZA02601
         TPEWAD                                                @ZA02601
         TAIBD                                                 @ZA02601
         EJECT
*                                                                     *
*        D U M M Y    C O N T R O L    S E C T I O N S                *
*                                                                     *
         SPACE 2
         DCBD  DSORG=PS
         TAVTD
         TTCBD
FORCORE  DSECT
         IECDSECT
         TDEBAPD
         TRECBD                                                  Y02027
*****************************************************************Y02027
*****    RCB EXTENSION FOR TCAM APPLICATION OPEN/CLOSE.          Y02027
*****************************************************************Y02027
RCBPECB  DS    F                        APPLIC. PGM PECB ADDR.   Y02027
RCBASCB  DS    F                        APPLIC. PGM ASCB ADDR.   Y02027
RCBERRET DS    0F                       ERROR RTN ADDR           Y02027
RCBASID  DS    F                        APPLIC. PGM ASID ADDR.   Y02027
RCBTCB   DS    F                        APPLIC. PGM TCB ADDR.    Y02027
RCBDCBMA DS    H                        DCBMACRF FROM APPLIC.    Y02027
RCBTCBFL DS    0X                       APPLIC. TCB FLAGS.       Y02027
RCBDCBDS DS    H                        DCBDSORG FROM APPLIC.    Y02027
RCBDSNM  DS    CL8                      PROCESS QUEUE NAME.      Y02027
RCBDEBAD DS    F                        DEB ADDR FOR APPLIC. DCB.Y02027
RCBDCBBU DS    H                        DCBBUFL FROM APPLIC.     Y02027
RCBDCBLR DS    H                        DCBLRECL FROM APPLIC.    Y02027
RCBDCBOP DS    X                        DCBOPTCD FROM APPLIC.    Y02027
RCBDCBRF DS    X                        DCBRECFM FROM APPLICATIONY02027
RCBDCBF1 DS    X                        DCBFLAG1 FROM APPLICATIONY02027
RCBERR   DS    X                        ERROR INDICATOR          Y02027
RCBJOBNM DS    CL8                      JOBNAME                  Y02027
RCBSAVE  DS    18F                      SAVE AREA FOR IEDQEU   @ZA02601
RCBEND   DS    0X                       END OF DSECT.            Y02027
         ORG   RCBTCB
RCBDEBTA DS    H                        OFFSET IN T.T. FOR ENTRY.Y02027
RCBOPECB DS    0AL3                     OPCTL ECB ADDRESS        Y02027
RCBDEBPW DS    AL3                      ADDR. OF PEWA.           Y02027
RCBDEBPC DS    AL3                      ADDR. OF PCB.            Y02027
         DS    2F                       QNAME                    Y02027
RCBSQ    DS    0X                       SECURE QUEUE FLAG        Y02027
RCBAVTBT DS    0X                       AVTBIT1 ON RETURN FROM EUY02027
RCBPEBAD DS    AL3                      ADDR OF PEB IN CSA.      Y02027
RCBSIZE  EQU   RCBEND-RECBKEY           SIZE OF DSECT.           Y02027
         TTCXD                                                   Y02027
         END
