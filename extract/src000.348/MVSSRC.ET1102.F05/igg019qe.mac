         TITLE 'IGG019QE - START I/O APPENDAGE '
IGG019QE CSECT
         SPACE 3
*  CHANGE ACTIVITY AS FOLLOWS
******************** MICROFICHE FLAGS *********************** SUPT CODE
*A696000                                                         Y01004
*C470000-474000,486000                                           Y01004
*D478000                                                         Y01004
*A722000,747640                                                 OY01862
*A141000,150000,171200-173600,306000-310800,760000-761000        Y02027
*D306000-309000                                                  Y02027
         SPACE
***********************************************************************
*  MODULE NAME = IGG019QE                                             *
*                                                                     *
*  DESCRIPTIVE NAME = START I/O APPENDAGE                             *
*                                                                     *
*  COPYRIGHT = 'NONE'                                                 *
*                                                                     *
*  STATUS:  CHANGE LEVEL 5                                            *
*                                                                     *
*FUNCTION -- 1 CONVERT VIRTUAL ADDRESS IN CCW STRING TO REAL AND      *
*              BUILD INDIRECT ADDRESS LIST AS REQUIRED                *
*            2 CONVERT REAL ADDRESS TO VIRTUAL                        *
*                                                                     *
*ENTRY POINTS -- 1 IGG019QE - CONVERT VIRTUAL ADDRESS IN CCW STRING   *
*              TO REAL AND BUILD INDIRECT ADDRESS LIST AS REQUIRED    *
*              (FROM IOS BEFORE SIO)                                  *
*                2 IGG019QE+4 - CONVERT REAL ADDRESS IN CCW STRING TO *
*              VIRTUAL (FROM LINEEND IGE0004G IGE0004H)               *
*                3 IGG019QE+34 - CONVERT VIRTUAL ADDRESSES IN    Y02027
*              CCW STRING TO REAL AND BUILD INDIRECT ADDRESS     Y02027
*              LIST AS REQUIRED (FROM ALL ERP BEFORE RETRY)      Y02027
*                                                                     *
*INPUT  -- R2  IOB ADDRESS                                            *
*   R14 RETURN ADDRESS                                                *
*   R15 ENTRY POINT ADDRESS                                      X02004
*   R12 RETURN ADDRESS FOR ENTRY 2                                    *
*                                                                     *
*OUTPUT -- REGISTERS  0-11,14,15 REMAIN TRANSPARENT TO IOS UPON EXIT  *
*   FROM THIS MODULE                                                  *
*                                                                     *
*EXTERNAL ROUTINES - IEAPTRV  CONVERT REAL ADDRESS TO VIRTUAL         *
*                                                                     *
*EXITS - NORMAL -- 1 BR 14  RETURN TO IOS                             *
*                  2 BR 12 - RETURN TO CALLER                         *
*                  3 BR 14 TO RETURN TO ERP TO ISSUE ERREXCP     Y02027
*                                                                     *
*EXIT-ERROR -- NONE                                                   *
*                                                                     *
*TABLES/WORK AREAS -- DSECTS OF CONTROL BLOCKS GENERATED BY MACROS:   *
**  TCCWD                                                             *
*   TLCBD                                                             *
*   TPRFD                                                        X02004
*   TAVTD                                                        X02004
*   DCBD                                                         X02004
*   CVT                                                               *
*                                                                     *
*ATTRIBUTES -- REFRESHABLE, SUPERVISOR MODE, DISABLED                 *
*                                                                     *
*NOTES -- THE OPERATION OF THIS MODULE DOES NOT DEPEND UPON A         *
*   PARTICULAR INTERNAL REPRESENTATION OF THE EXTERNAL CHARACTER SET. *
*                                                                     *
***********************************************************************
         EJECT
R0       EQU   0
X        EQU   1                        CCW ADDRESS
Y        EQU   2                        IOB ADDRESS
Z        EQU   3
T        EQU   4
U        EQU   5
V        EQU   6
R7       EQU   7                        LINK FOR SUBROUTINE
R8       EQU   8                        BASE REGISTER
W        EQU   9
RIOSB    EQU   13                       ADDR OF I/O SUPVR BLOCK  Y02027
R14      EQU   14                       RETURN ADDRESS
R15      EQU   15
         USING LCBFLAG1,Y
         USING IOSB,RIOSB                                        Y02027
         DC    A(END19QE-IGG019QE)      LENGTH OF SIO ADDENDAGE  X02004
         USING *,R15
         B     SIOENTRY                 ENTRY FROM IOS ON SIO    X02004
         SPACE 1                                                 X02004
         B     INTENTRY                 ENTRY FROM ERP OR LINEND X02004
*                                       AFTER AN I/O INTERRUPT   X02004
         SPACE 1                                                 X02004
IGG019QE IEDHJN
SIOENTRY DS    0H                                                X02004
         DROP  R15                                               X02004
         L     R15,CVTLOC               CVT ADDRESS
         L     R15,AVTCVTPT(,R15)       ADDRESS OF LOAD LIST
         L     R15,0(,R15)              AVT BASE
         USING IEDQAVTD,R15
         STM   R0,W,AVTSAVEX            SAVE REGISTERS
ERPENTRY EQU   *                        ENTRY FROM ERP PRIOR TO  Y02027
*                                       RETRY.                   Y02027
         BALR  R8,R0                    ESTABLISH ADDRESSABILITY
         USING *,R8
         LTR   R15,R15                  IS ENTRY FROM ERP        Y02027
         BNZ   IOSENTRY                 NO,CONTINUE              Y02027
         LR    V,R14                    SAVE RETURN ADDRESS      Y02027
         LR    W,RIOSB                  SAVE I/O SUPVR BLOCK ADD Y02027
SETERP   SETLOCK OBTAIN,TYPE=LOCAL,MODE=UNCOND,RELATED=CHANNEL PROGRAM,X
               IGE0004G(SETERP),IGE0004H(SETERP),IEDQGT(SETTRAN),      X
               IEDQGA(SETASSC,SETCONC),'PCI LINEEND,AND START I/O      X
               APPENDAGES ARE LOCKED ON THIS RESOURCE THRU THE EXCP    X
               PROCESSOR ISSUING SETLOCK (ERP,BUFFER ASSOCIATION AND   X
               TRANSPARENT CCW BUILD MUST ISSUE SETLOCK'
         LR    RIOSB,W            RESTORE IOSB ADDR              Y02027
         LR    R14,V                    RESTORE RETURN ADDRESS   Y02027
IOSENTRY EQU   *                                                 Y02027
         TM    LCBFLAG3,LCBRLAD         REAL ADDRESSES           X02004
         BO    END                      BRANCH YES
*
         L     X,LCBDCBPT               DCB ADDRESS
         SR    V,V                      CLEAR FOR IC
         IC    V,DCBEIOBX-IHADCB(X)     LCB SIZE
         LA    X,LCBFLAG1-IEDQLCB+EIGHT ACCOUNT FOR LCB PRE+EXT  X02004
         SR    V,X                      IOB
         LA    V,0(V,Y)                 ADDRESS OF END OF LCB
         OI    LCBFLAG3,LCBRLAD         SET REAL ADDRESSES       X02004
         ICM   X,R7,LCBSTART            PICK UP ADDR OF 1ST CCW
         B     CKBFR                    IS FIRST CCW IN BUFFER
*                                       IF NOT RETURN
         SPACE 1                                                 S22026
         SPACE 2
         USING IEDQCCW,X
LOOP     EQU   *
         TM    CCWFLAGS,CCWSKIP         RD SKP LOOP
         BO    CKFLAG                   BRANCH YES
*
LOOPX    EQU   *
         BAL   R7,VRTORL                CONVERT DATA ADDRESS
         SPACE
         BAL   R7,CKIDA                 CHECK FOR INDIECT ADDR LIST
         SPACE
* TEST FOR END OF CCW CHAINING
         SPACE
         TM    CCWFLAGS,CCWCD+CCWCC     LAST CCW
         BZ    END                      BRANCH IF LAST
         SPACE
PLUS8    EQU   *
         LA    X,CCW+EIGHT              ADDRESS OF NEXT CCW
*  IF TIC COMMAND THEN GO TO SPECIAL ROUTINE
         SPACE
         CLI   CCWOPCDE,CCWPOLL         AUTOPOLL CCW
         BE    AUTOP                    BRANCH YES
         CLI   CCWOPCDE,CCWNOP          WAS IT PREVIOUSLY AN AUTO-
*                                       POLL CCW (ERP ENTRY ONLY)
         BE    AUTOP                    BRANCH IF YES.
*
         TM    CCWOPCDE,CCWTIC+CCWREAL  REAL TIC CCW             X02004
         BNO   CKTIC                    BRANCH IF NO
*
END      EQU   *
         NI    LCBLSPCI+TWO,X'FF'-THREE RESET FIRST TIME
         LTR   R15,R15                  IS ENTRY FROM ERP        Y02027
         BNZ   IOSLINK                  NO, RETURN TO IOS        Y02027
         L     W,LCBSTART-1             PICKUP RESTART ADDRESS   Y02027
         LRA   W,ZERO(,W)               CONVERT VIRTUAL ADDRESS  Y02027
*                                       OF STARTING CCW TO REAL  Y02027
         ST    W,IOSRST                 SETUP REAL RETRY ADDRS   Y02027
*                                       FOR IOS IN IOSB          Y02027
         XC    IOSSNS,IOSSNS            CLEAR SENSE BYTES        Y02027
         LR    V,R14                    SAVE RETURN ADDRESS      Y02027
         LR    W,RIOSB                  SAVE I/O SUPVR BLOCK ADD Y02027
RESETERP SETLOCK RELEASE,TYPE=LOCAL,RELATED=CHANNEL PROGRAM,           X
               IGG019QE(SETERP)
         LR    R14,V                    RESTORE RETURN ADDRESS   Y02027
         LR    RIOSB,W            RESTORE IOSB ADDR              Y02027
         BR    R14                      RETURN TO ERP            Y02027
IOSLINK  EQU   *                                                 Y02027
* RETURN TO IOS
         LM    R0,W,AVTSAVEX            RESTORE REGISTERS
         BR    R14                      RETURN TO IOS
         SPACE
CKFLAG   EQU   *
         LA    X,0(,X)                  CLEAR HIGH ORDER
         LA    R7,LCBCPA                ADDRESS OF SKIP IDLE LOOP
         CLR   R7,X
         BNE   LOOPX                    BRANCH NO
         TM    CCWFLAGS,CCWCD+CCWCC     RRAL ADDRESS
         BO    PLUS8                    BRANCH YES
*
         OI    CCWFLAGS,CCWCC           SET REAL ADDRESS FLAG
         BAL   R7,VRTORL                CONVERT TO REAL
*
         B     PLUS8                    PICK UP NEXT CCW
*
CKTIC    EQU   *
         TM    CCWOPCDE,R7              IS CCW A VIRTUAL TIC
         BNZ   LOOP                     BRANCH NO
         SPACE
         L     T,CCWADDR-ONE            TIC TO ADDRESS
         LRA   W,0(,T)                  CONVERT TO REAL
         STCM  W,R7,CCWADDR             STORE REAL ADDRESS
         OI    CCWOPCDE,AVTE80          SET REAL TIC OP CODE
         TM    CCWADDR+TWO,THREE        INVALID TIC
         BNZ   END                      BRANCH IF YES
         LR    X,T
         TM    LCBLSPCI+TWO,X           FIRST TIME SET
         BO    LOOP                     BRANCH YES
*
         OC    LCBLSPCI,LCBLSPCI        LSPCI = 0
         BZ    LOOP                     BRANCH YES - FOLLOR TIC CHAIN
*
CKBFR    EQU   *
         LA    R7,0(,X)                 CLEAR HIGH ORDER CCW ADDRESS
         LA    W,LCBERCCW               START OF CCW'S IN LCB.
         SR    W,R7                     IS TIC TO BEFORE LCB
         BP    PKLSPCI                  BRANCH YES
*
         LR    T,V                      ADDRESS OF IDA AFTER CPA X02004
         SR    T,R7                     TIC TO AFTER LCB
         BP    LOOP                     BRANCH NO - CONTINUE
*
PKLSPCI  EQU   *
         CLM   X,AD,LCBLSPCI            IS SIO ON LSPCI BFR
         BE    SETSW                    YES, SET FIRST TIME
          SPACE
         USING IEDQPRF,R7                                        Y01004
         L     R7,LCBLSPCI-1            ADDRESS OF FIRST BFR     Y01004
         BAL   T,SETOPCD                ENTER LOOP               Y01004
*                                                                Y01004
         ICM   R7,ALL,PRFTIC            ADDRESS OF NEXT UNIT/BFR Y01004
         BM    END                      BRANCH IF SIMULATED CHANLY01004
*                                       PROGRAM CHECK - CCW'S AREY01004
*                                       ALREADY REAL FROM IEDQGA Y01004
SETOPCD  EQU   *
         MVI   PRFOPCDE,ZERO            SET OP CODE TO ZERO
*                                           FOR LINEEND APPENDAGE
         CLM   X,AD,PRFTIC+ONE          IS SIO ON NEXT UNIT
         BCR   CCNEQ,T                  NO, CONTINUE SEARCH      Y01004
         SPACE
         LRA   T,0(,X)                  YES, SET TIC TO
         STCM  T,AD,PRFTIC+ONE              SIO UNIT TO REAL ADDR
         OI    PRFTIC,CCWREAL           SET REAL TIC OP CODE     X02004
SETSW    EQU   *
         OI    LCBLSPCI+TWO,ONE         SET FIRST TIME SWITCH
         B     LOOP                     BR TO CONVERT REMAINDER
*                                        OF CCW CHAIN TO REAL
         DROP  R7
         SPACE
*
AUTOP    EQU   *
         TM    CCWFLAGS,CCWCD+CCWCC     IS THIS END OF CHAIN
         BZ    END                      YES, EXIT
         BAL   R7,VRTORL                CONVERT FIRST POLL
*
         BAL   R7,CKIDA                 BCHECK INDIRECT ADDRESSING
         BAL   R7,TICCK                 CONVERT TIC
*
         BAL   R7,TICCK                 CONVERT TIC2
*
         BAL   R7,VRTORLP               CONVERT POLL2 CCW
*
         BAL   R7,CKIDA                 CHECK INDIRECT ADDRESSING
*
         BAL   R7,TICCK                 CONVERT TIC
*
         B     PLUS8                    CHECK NEXT CCW
*
TICCK    EQU   *
         TM    CCWOPCDE+EIGHT,R7        IS THIS REALLY A TIC
         BNZ   VRTORLP                  BRANCH NO
*
         OI    CCWOPCDE+EIGHT,CCWREAL   SET REAL TIC OP CODE     X02004
VRTORLP  EQU   *
         LA    X,CCW+EIGHT
VRTORL   EQU   *
         L     T,CCWADDR-ONE            PICK UP CCW ADDRESS
         LRA   W,0(,T)                  CONVERT TO REAL ADDRESS
         STCM  W,R7,CCWADDR             STORE REAL ADDRESS
         CLI   CCWOPCDE,ZERO            ERP RETRY OF ZEROED CCW
         BCR   CCNEQ,R7                 BRANCH NO - RETURN
*
         MVI   CCWOPCDE,CCWREAD         ASSUME READ STATE
         TM    LCBSTAT1,LCBRECVN        ARE WE RECEIVING
         BCR   CCON,R7                  BRANCH YES - RETURN
*
         MVI   CCWOPCDE,CCWWRITE        SET WRITE OP CODE
         BR    R7                       RETURN
*
CKIDA    EQU   *
* TEST FOR INDIRECT ADDRESS LIST REQUIREMENT
* SAVING VIRTUAL 2K BLOCK ADDRESS IN REGISTER T
         L     Z,CVTLOC                 CVT ADDRESS
         TM    CVTDCB-CVT(Z),CVT2SPS    TEST SYSTEM TYPE
         SPACE
         LA    Z,NTWOK
         BO    VS1                      BRANCH VS1 - 2K PAGE SIZE
         LA    Z,ONE(Z,Z)               SET Z = 4K MINUS 1
VS1      EQU   *
         LR    U,Z                      SAVE PAGE SIZE MINUS 1
         NR    Z,W
         XR    T,Z
         BCTR  Z,0                      DECREMENT RESIDUAL BY 1
*                                       TO ACCOUNT FOR PAGE SIZE 1
         AH    Z,CCWCOUNT
         SR    Z,U                      SUBTRACT 2K(4K) -1 FROM
*                                       DATA ENDING ADDRESS RESIDUAL
         BCR   13,R7                    RETURN IF NOT CROSS PAGE
         TM    CCWFLAGS,CCWIDA          IS IDA FLAG SET          X02004
         BCR   CCON,R7                  BRANCH IF YES - SET BY   X02004
*                                       IGG019RE IF COMBUF AREA  X02004
*                                       CROSSES A PAGE BOUNDARY  X02004
*                                       WHICH IS DETERMINED BY   X02004
*                                       OPEN WHO BUILT THE IDA   X02004
         SH    V,H8                     FIRST TIME - BACK UP TO
*                                       END OF LCB MINUS 8 -
*                                       SECOND TIME (ONLY AUTOPOLL)
*                                       BACK UP TO END -16
         ST    W,0(,V)
         SPACE
* STORE INDIRECT ADDRESS LIST POINTER IN CCW
         SPACE
         LRA   W,0(,V)
         STCM  W,R7,CCWADDR
         SPACE
* BUILD REST OF INDIRECT ADDRESS LIST
         SPACE
         LRA   U,ONE(U,T)               REAL ADDRESS OF SECOND PAGE
         ST    U,FOUR(,V)
         OI    CCWFLAGS,CCWIDA
         BR    R7                       RETURN
         EJECT
R1       EQU   1                        RQE ADDRESS
RIOB     EQU   2                        IOB ADDRESS
RCCW     EQU   5                        CCW BASE
RLNK     EQU   6                        SUBROUTINE LINK REGISTER
RRET     EQU   12                       RETURN REGISTER
R11      EQU   11                       RQE SAVE REGISTER
RDCB     EQU   4                        DCB ADDRESS
INTENTRY DS    0H                                                X02004
         BALR  R8,0                     SET UP BASE REG          X02004
         USING *,R8                     SET ADDRESSABILITY       X02004
         DROP  X
         USING LCBFLAG1,RIOB
         USING IEDQCCW,RCCW
         LR    R11,R1                  SAVE RQE ADDRESS
         ICM   R1,AD,LCBSTART           CHANNEL PROGRAM START
*                                       FROM LCB.
         B     CHECKBUF                 BRANCH TO DETERMINE THE
*                                       LOCATION OF THE CCW - IN
*                                       A BUFFER OR IN THE LCB.
         SPACE 3
CHKREAL  EQU   *
         TM    CCWOPCDE,INVALID         IS CCW A VIRTUAL TIC     X01004
         BZ    CKTICX                   IF YES GO SEE IF THIS ISX01004
*                                       THE END OF THE CCW CHAIN.
*                                       (THERE MAY BE MORE REAL
*                                       CCW'S BEYOND THE VIRTUAL
*                                       TIC).
         SPACE 2
LOOP1A   EQU   *
         TM    CCWFLAGS,CCWSKIP         DOES CHANNEL PROGRAM IN-
*                                       CLUDE WRITE IDLES LOOP   X01004
         BZ    NOIDLES                  BRANCH IF NO TO CONVERT
*                                       CCW ADDRESS.
         LA    RLNK,LCBCPA              GET ADDR OF WRITE IDLES  X02004
*                                       LOOP.
         LA    RCCW,0(,RCCW)            CLEAR HIGH ORDER BYTE.
         CLR   RLNK,RCCW                IS CURRENT CCW THE WRITE X02004
*                                       IDLES/READ SKIP CCW      X01004
         BNE   NOIDLES                  BRANCH IF NO TO CONVERT
*                                       CCW ADDRESS.
         TM    CCWFLAGS,CCWCC+CCWCD     DO CHAINING FLAGS INDICATE
*                                       REAL ADDRESSES IN THE WRITE
*                                       IDLES LOOP               X01004
         BNO   CHKDONE                  BRANCH IF NO TO SEE IF
*                                       CONVERSION IS COMPLETE.
         NI    CCWFLAGS,X'FF'-CCWCC     SET OFF REAL ADDRESS FLAG
*                                       IN CCW.
         SPACE
NOIDLES  EQU   *
         TM    LCBFLAG3,LCBRLAD         REAL CCWS IN LCB         X02004
         BZ    CKCDCC                   BRANCH NO-CHECK CC + CD  X01004
*                                                                X01004
         BAL   RLNK,CONVERT             GO TO SUBROUTINE TO CONVERT
*                                       CCW ADDRESS TO VIRTUAL.
         BAL   RLNK,CHECKIDA            GO TO SUBROUTINE TO CHECK
*                                       FOR INDIRECT ADDRESSING. X02004
         SPACE
CKCDCC   EQU   *                                                 X01004
         TM    CCWFLAGS,CCWCC+CCWCD     ARE CHAINING FLAGS ON    X01004
         BZ    DONE                     BRANCH IF NO - THIS IS
*                                       THE LAST CCW, SO WE'RE
*                                       FINISHED.
         SPACE
NEXTCCW  EQU   *
         LA    RCCW,CCW+EIGHT           ADDRESS NEXT CCW IN CHAIN.
         CLI   CCWOPCDE,CCWPOLL         IS CCW AN AUTOPOLL COMND X01004
         BE    AUTOPOLL                 BRANCH IF YES TO CONVERT
*                                       AUTOPOLL CCW ADDRESSES.
         CLI   CCWOPCDE,CCWNOP          WAS IT AN AUTOPOLL COMMAND
*                                       PREVIOUSLY               X01004
         BNE   CHKREAL                  BRANCH IF NO
AUTOPOLL EQU   *
         TM    CCWFLAGS,CCWCD+CCWCC     IS THIS END OF CHAIN
         BZ    DONE                     YES, EXIT
         TM    LCBFLAG3,LCBRLAD         REAL CCWS IN LCB         X02004
         BO    AUTOOK                   BRANCH YES - CONVERT
         LA    RCCW,CCW+THIRTY2         BUMP PAST POLL SEQUENCE
         B     NEXTCCW                  CONVERT LAST TIC
*
AUTOOK   EQU   *
         BAL   RLNK,CONVERT             GO TO SUBROUTINE TO CONVERT
*                                       FIRST POLL CCW ADDRESS TO
*                                       VIRTUAL.
         BAL   RLNK,CHECKIDA            GO TO SUBROUTINE TO CHECKX02004
*                                       FOR INDIRECT ADDRESSING.
         BAL   RLNK,TICTEST             TO SUBROUTINE TO CONVERT X02004
*                                       THE NEXT CCW - A TIC.
         BAL   RLNK,TICTEST             TO SUBROUTINE TO CONVERT X02004
*                                       THE NEXT CCW - A TIC.
         BAL   RLNK,PRECONV             TO SUBROUTINE TO CONVERT X02004
*                                       THE SECOND POLL CCW.
         BAL   RLNK,CHECKIDA            TO SUBROUTINE TO CHECK   X02004
*                                       FOR INDIRECT ADDRESSING.
         BAL   RLNK,TICTEST             TO SUBROUTINE TO CONVERT X02004
*                                       THE NEXT CCW - A TIC.
         B     NEXTCCW                  BRANCH TO GET THE NEXT CCW.
         SPACE 3
         EJECT
* THE FOLLOWING SUBROUTINE CHECKS FOR INDIRECT ADDRESSING.
         SPACE
CHECKIDA EQU   *
         TM    CCWFLAGS,CCWIDA          IS INDIRECT ADDR USED    X01004
         BCR   8,RLNK                   RETURN TO CALLER IF NOT. X02004
         L     R1,0(,R1)                GET REAL INDIRECT ADDRESS
         LA    R1,0(,R1)                CLEAR HIGH ORDER FOR VS2
         NI    CCWFLAGS,X'FF'-CCWIDA    SET OFF INDIRECT ADDRESSING
*                                       FLAG.
         L     R15,CVTLOC               CVT ADDRESS
         L     R15,CVTPTRV-CVT(,R15)    REAL TO VIRTUAL SURTN
         BALR  R14,R15                  LINK TO SUBROUTINE TO CONX02004
*                                       VERT INDIRECT ADDRESS TO
*                                       VIRTUAL.
         STCM  R1,AD,CCWADDR            STORE VIRT. ADDR IN CCW  X01004
         BR    RLNK                     RETURN TO CALLER.        X02004
         SPACE
* END OF SUBROUTINE.
CKTICX   EQU   *
         ICM   R1,ALL,CCWADDR-1         TIC TO ADDRESS
         BNM   LOOP2A                   BRANCH IF VIRTUAL TO CHECK
*                                       NEXT CCW IN CHAIN.
         BAL   R14,PTRV               GO TO SUBROUTINE TO CONVERT
*                                       TIC-TO ADDRESS TO VIRTUAL.
         OI    LCBFLAG3,LCBRLAD         SET REAL ADDRESS FLAG    X02004
         STCM  R1,SEVEN,CCWADDR      STORE VIRTUAL TIC-TO     X0100
*                                       ADDRESS IN CCW.          X01004
         NI    CCWOPCDE,X'FF'-CCWREAL     T VIRTUAL TIC OP CODE. X01004
LOOP2A   EQU   *                                                 Y01004
         TM    CCWADDR+2,INVALID        IS TIC INVALID          X01004
         BNZ   DONE                     BRANCH IF YES           X01004
CHECKBUF EQU   *
         LA    RCCW,0(,R1)              GET NEXT CCW ADDRESS.
         TM    LCBLSPCI+2,ONE           HAS LCBLSPCI BEEN USED   X02004
*                                       ALREADY                  X01004
         BO    TEST2                    BRANCH IF YES - WE'VE AL-
*                                       READY TIC'D OUT OF THE LCB.
         LA    R14,LCBERCCW             RWKC - ADDR OF ERP CCWS  X02004
*                                       IN LCB.
         SR    R14,RCCW                 DOES THE CURRENT CCW     X02004
*                                       PRECEDE CCW'S IN LCB     X01004
         BP    NOTINLCB                 BRANCH IF YES TO GET BUF-
*                                       FER ADDRESS FROM LCBLSPCI.
         SR    R14,R14                  CLEAR FOR IC.
         IC    R14,DCBEIOBX-IHADCB(RDCB)  T LCB SIZE FROM DCB    X0200
         LA    R14,0(RIOB,R14)          CALCULATE ENDING ADDRESS X02004
*                                       OF LCB.
         SH    R14,OFFLCB               ACCOUNT FOR LCB PRE+EXT OY01862
         SR    R14,RCCW                 DOES THE CURRENT CCW
*                                       FOLLOW  CCW'S IN LCB     X01004
         BP    LOOP1A                   BRANCH IF THE CCW IS IN
*                                       THE LCB.
         SPACE
NOTINLCB EQU   *
         ICM   RCCW,SEVEN,LCBLSPCI      GET BUFFER ADDR FROM LCB X01004
         BZ    DONE                     BRANCH IF BUFFER IS NOT
*                                       ASSIGNED - WE'RE DONE.
         OI    LCBLSPCI+2,ONE           SET SWITCH TO INDICATE   X
*                                       FIRST USE OF LCBLSPCI
*                                       (FIRST TIC OUT OF LCB).
         SPACE
TEST2    EQU   *
         CLI   CCWOPCDE,ZERO            IS THIS THE RESULT OF A
*                                       RESTART ON OTHER THAN THE
*                                       FIRST UNIT OF A BUFFER   X01004
         BNE   LOOP1A                   BRANCH IF NO.
         B     NEXTCCW                  FOLLOW TIC CHAIN         Y01004
         EJECT
* THE FOLLOWING SUBROUTINE CONVERTS REAL ADDRESSES TO VIRTUAL.
         SPACE
TICTEST  EQU   *                        SPECIAL SUBROUTINE ENTRY
*                                       FOR TIC CCW'S AFTER AUTO-
*                                       POLL CCW'S.
         TM    CCWOPCDE+EIGHT,CCWTIC+CCWREAL   S CCW A REAL TIC  X01004
         BNO   PRECONV                  BRANCH IF NO - IT'S BEEN
*                                       SET TO A NO-OP.
         NI    CCWOPCDE+EIGHT,X'FF'-CCWREAL T VIRTUAL TIC OP CODE.
         SPACE
PRECONV  EQU   *                        SPECIAL SUBROUTINE ENTRY
*                                       POINT FOR AUTOPOLL CCW'S.
         LA    RCCW,CCW+EIGHT           BUMP PAST FIRST POLL CCW.
         SPACE
CONVERT  EQU   *
         BAL   R14,PTRV                 CONVERT ADDRESS
         SPACE 1
         STCM  R1,AD,CCWADDR            SET VIRTUAL ADDRESS
         BR    RLNK                     RETURN TO CALLER
         SPACE 1
PTRV     EQU   *
         SR    R1,R1
         ICM   R1,AD,CCWADDR            ADDRESS TO BE CONVERTED
         L     R15,CVTLOC               CVT ADDRESS
         L     R15,CVTPTRV-CVT(,R15)    REAL TO VIRTUAL SURTN
         BR    R15                      LINK TO SUBROUTINE.      X02004
         SPACE
* NOTE:  RETURN FROM 'IEAPTRV' IS VIA BR 14.  IF THIS SUBROUTINE WAS
*        ENTERED AT ENTRY POINT 'TICTEST', ENTRY POINT 'PRECONV', OR
*        ENTRY POINT 'CONVERT', RETURN WILL BE TO STATEMENT 'NSI'.
*        IF THIS SUBROUTINE WAS ENTERED AT ENTRY POINT 'PTRV',
*        RETURN WILL BE DIRECTLY TO THE CALLER AT THE NEXT SEQUENTIAL
*        INSTRUCTION AFTER THE BAL TO 'PTRV'.
         SPACE
         EJECT
CHKDONE  EQU   *                        ENTER HERE WITH VIRTUAL
*                                       ADDRS. IN WRITE IDLES LOOP.
         CLM   RCCW,SEVEN,LCBCPA+NINE   IS CURRENT CCW IN WRITE  X01004
*                                       IDLES LOOP               X01004
         BNE   NEXTCCW                  BRANCH IF NO TO GET THE
*                                       NEXT CCW.
         SPACE 2
* AT THIS POINT, ALL APPLICABLE CCW ADDRESSES HAVE BEEN CONVERTED TO
* VIRTUAL.
         SPACE
DONE     EQU   *
         NI    LCBLSPCI+2,X'FF'-ONE     TURN OFF LSPCI FLAG
         NI    LCBFLAG3,X'FF'-LCBRLAD    RESET REAL ADDR FLAG    X02004
         LR    R1,R11                   RESTORE RQE ADDRESS
         BR    RRET                     RETURN
         SPACE 1
THIRTY2  EQU   32
INVALID  EQU   X'07'                    INVALID TIC FLAG.
AD       EQU   X'07'                    MASK FOR ADDRESS
*
H8       DC    H'8'                     ONE HALF WORD OF 8
OFFLCB   DC    AL2(LCBFLAG1-IEDQLCB+EIGHT) LENGTH OF LCB PREFIX OY01862
*                                       AND EXTENSION           OY01862
ALL      EQU   15                       MASK FOR ICM OF 4 BYTES  Y01004
CCNEQ    EQU   7                        NOT EQUAL CONDITION
CCON     EQU   7                        BIT ON CONDITION CODE
ONE      EQU    1
TWO      EQU    2
THREE    EQU    3
FOUR     EQU    4
FIVE     EQU    5
SIX      EQU    6
SEVEN    EQU    7
EIGHT    EQU    8
NINE     EQU    9
TEN      EQU   10
ELEVEN   EQU   11
NTWOK    EQU   2047
CVTLOC   EQU   16
ZERO     EQU   X'00'
         CNOP  0,8                      ALIGN TO DOUBLEWORD      X02004
END19QE  EQU   *                        END OF LOAD MODULE       X02004
         EJECT
         TPRFD
         TCCWD
         TLCBD
         DCBD  DSORG=TX
         TAVTD
         EJECT
         IHAPSA
         CVT   DSECT=YES
         IECDIOSB
         END
