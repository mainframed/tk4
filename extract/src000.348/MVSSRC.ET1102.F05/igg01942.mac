         TITLE 'IGG01942-CHECKPOINT DISK INITIALIZATION'
IGG01942 CSECT
******************** MICROFICHE FLAGS *********************** SUPT CODE
*CHANGE ACTIVITY AS FOLLOWS:
*A227150                                                        SA56217
*C206000,772000,774000,792000,794000                            SA59025
*A844300                                                        SA59025
*C015000,046000,050000,194000-206000,217200,217250,217500-217560 Y02027
*C582600-583200,594000,595000                                    Y02027
*A227230-227260,626200,839400-839800,896000,906000               Y02027
*A257000                                                        SA64710
*C530000,559200                                                 SA64712
*D558000                                                        SA64712
*A787400,844200                                                @SA75746
*C388000,794000                                                @SA75746
*D792000                                                       @SA75746
* PACKAGING ERROR                                              @OZ08378
*        TCAM LEVEL 10.0 MVS CHANGES                           @G36XRNP
*A029000,098000,120000,134000,566000,715000,787000,844301      @G36XRNP
*A845200,715000                                                @G36XRNP
*C011000,014200,028500                                         @G36XRNP
*D715500-718000,788000-790000                                  @G36XRNP
*A380000                                                       @OY20541
*                                                              @G36XRNP
         SPACE 3
***********************************************************************
*                                                                     *
*TITLE: 'IGG01942' CHECKPOINT DISK INITIALIZATION MODULE              *
*                                                                     *
*MODULE NAME: IGG01942  (TCAM,INITIALIZATION)                  @G36XRNP
*                                                                     *
*DESCRIPTIVE NAME: CHECKPOINT DISK INITIALIZATION                     *
*                                                                     *
*COPYRIGHT: NONE                                                      *
*                                                                     *
*STATUS = CHANGE LEVEL 10.0                                    @G36XRNP
*                                                                     *
*FUNCTION:THIS MODULE FORMATS THE CHECKPOINT DATA                     *
*   SET WITH DUMMY RECORDS. THE CPRCDS OPERAND ON INTRO INDICATES     *
*   THE NUMBER OF ENVIRONMENT CHECKPOINTS THE USER WANTS ON DISK.     *
*   THE CKREQS OPERAND INDICATES THE NUMBER OF CKREQ RECORDS TO       *
*   WRITE ON DISK. THERE IS ONE CONTROL RECORD. THE REMAINDER OF      *
*   THE SPACE ALLOCATED TO THE CHECKPOINT DATA SET IS USED FOR        *
*   INCIDENT RECORDS. WHEN IT IS DETERMINED THAT EITHER THE    @G36XRNP
*   TCAM OR 3705 CHECKPOINT DCB HAS BEEN OPENED SUCCESSFULLY   @G36XRNP
*   CHECKPOINT ACTIVE INDICATORS ARE SET IN THE TCAM AVT.      @G36XRNP
*                                                                     *
*ENTRY POINT:                                                         *
*                                                                     *
*        IGG01942                                                     *
*                                                                     *
*INPUT: REGISTERS 5,6,7, AND 8 CONTAIN THE FOLLOWING VALUES           *
*                                                                     *
*   5 -ADDRESS OF THE BEGINNING OF THE WTG TABLE PREFIX          Y02027
*   6 -ADDRESS OF BEGINNING OF WHERE-TO-GO TABLE                      *
*   7 -ADDRESS OF THE COPY OF THE CURRENT DCB                    Y02027
*   8 -ADDRESS OF CURRENT ENTRY IN WHERE-TO-GO TABLE                  *
*   THE CHECKPOINT WORKAREA(INCLUDING IOB AND CCWS) IS INITIALIZED.   *
*                                                                     *
*OUTPUT: REGISTERS 5 AND 6 ARE SAME AS INPUT. REGISTERS 7 AND 8 ARE   *
*   UPDATED TO THE NEXT ENTRIES.                                      *
*   DUMMY RECORDS ARE WRITTEN ON THE CHECKPOINT DATA SET.             *
*                                                                     *
*EXTERNAL ROUTINES:                                                   *
*                                                                     *
*        IECPCNVT-CONVERTS RELATIVE TRACK ADDRESS TO ACTUAL ADDRESS   *
*                                                                     *
*EXITS-NORMAL: THIS MUDULE XCTLS TO THE NEXT ENTRY IN THE WHERE-TO-   *
*   GO TABLE.                                                         *
*                                                                     *
*EXITS-ERROR: THIS MODULE ISSUES AN ERROR MESSAGE VIA WTO AND XCTLS   *
*   TO THE NEXT ENTRY IN THE WHERE-TO-GO TABLE. IT RECOGNIZES THE     *
*   FOLLOWING ERRORS:                                                 *
*   1.DISK ERROR WHILE WRITING                                        *
*   2.INSUFFICIENT DISK SPACE FOR MINIMUM CHECKPOINT RECORDS:         *
*      2 ENVIRONMENT RECORDS                                          *
*      1 CONTROL RECORD                                               *
*      CKREQ RECORDS-NUMBER SPECIFIED BY CKREQS ON INTRO              *
*   THE DCB IS MARKED UNOPENED.                                       *
*                                                                     *
*MACROS - IEDHJN                                               @G36XRNP
*         WTO                                                  @G36XRNP
*         XCTL                                                 @G36XRNP
*         EXCP                                                 @G36XRNP
*         WAIT                                                 @G36XRNP
*         LOAD                                                 @G36XRNP
*         DELETE                                               @G36XRNP
*                                                              @G36XRNP
*MODULE TYPE - PROCEDURE                                       @G36XRNP
*                                                              @G36XRNP
*PROCESSOR - ASSEMBLER                                         @G36XRNP
*                                                              @G36XRNP
*MODULE SIZE - 1K                                              @G36XRNP
*                                                              @G36XRNP
*PURPOSE - SEE FUNCTION                                        @G36XRNP
*                                                              @G36XRNP
*LINKAGE - XCTL'ED FROM IGG01943 AND IGG01949                  @G36XRNP
*                                                              @G36XRNP
*TABLES/WORK AREAS:                                                   *
*                                                                     *
*   CVT                                                               *
*   AVT (AVTCKGET,AVTOPTPT,AVTRNMPT,AVTCLRHI,AVTBITS,AVTNCKPR,AND     *
*        AVTCPRCD)                                                    *
*   CHECKPOINT WORK AREA                                              *
*   DEB                                                               *
*   DCB                                                               *
*                                                                     *
*ATTRIBUTES: RE-ENTRANT                                               *
*                                                                     *
*NOTES - SEE BELOW                                             @G36XRNP
*                                                                     *
*      THE OPERATION OF THIS MODULE DEPENDS UPON AN INTERNAL          *
*   REPRESENTATION OF THE EXTERNAL CHARACTER SET WHICH IS             *
*   EQUIVALENT TO THE ONE USED AT ASSEMBLE TIME. THE CODING HAS       *
*   BEEN ARRANGED SO THAT REDEFINITION OF 'CHARACTER' CONSTANTS, BY   *
*   REASSEMBLY, WILL RESULT IN A CORRECT MODULE FOR THS NEW           *
*   DEFINITIONS                                                       *
*                                                                     *
*RESTRICTIONS - NONE                                           @G36XRNP
*                                                              @G36XRNP
*REGISTER CONVENTIONS - SEE REGISTER EQUATES                   @G36XRNP
*                                                              @G36XRNP
*PATCH - PATCH                                                 @G36XRNP
*                                                              @G36XRNP
***********************************************************************
         SPACE 3
*REGISTER ASSIGNMENTS
         SPACE
R0       EQU   0                        WORK REG6LENGTH OF LCB
R1       EQU   1                        PARM REG OR WORK REG;INVLEN
R2       EQU   2                        ADDRESS OF CKP WORK AREA
R3       EQU   3                        LENGTH OF TOTAL CKPT RECORD
R4       EQU   4                        REG 4                    S22024
RLCB     EQU   4                        ADDRESS OF LCB OR INV LIST
R5       EQU   5                        LENGTH OF TERMNAME
R6       EQU   6                        ADDRESS OF TERMNAME ENTRY
R7       EQU   7                        REG 7                    S22024
RQCB     EQU   7                        ADDRESS OF QCB;LNGTH-TOTSEG
R8       EQU   8                        REG 8                    S22024
RTERM    EQU   8                        ADDRESS OF TERM ENTRY:UCB
RAVT     EQU   9                        ADDRESS OF AVT
R9       EQU   9                        REG 9                    S22024
RDCB     EQU   10                       ADDRESS OF DCB;BRANCH ADDR
R10      EQU   10                       REG 10                   S22024
RIOB     EQU   11                       ADDRESS OF IOB;INDEX-TNT
R11      EQU   11                       REG 11                   S22024
RBASE    EQU   12                       BASE
R12      EQU   12                       REG 12                   S22024
R13      EQU   13                       SAVE AREA;BYTES /RECORD
R14      EQU   14                       RETURN REG ;ADDRESS OF DEB
R15      EQU   15                       ENTRY REG;INDEX-NO. EXTENTS
         SPACE 3
*STANDARD LINKAGE
         USING IGG01942,R15             SET TEMPORARY BASE       Y02027
         BAL   RBASE,QMB00              SET BASE REGISTER        Y02027
         DROP  R15                      DROP 15 AS BASE          Y02027
         USING *,RBASE                  ESTABLISH ADDRESSABILITY Y02027
         USING IEDQAVTD,RAVT            BASE FOR AVT
         SPACE
         USING IHADCB,RDCB              DCB ADDRESSABILITY       S22024
         USING IEDQCKPD,R8              CHANGE BASE FOR CKPT DSECT 0129
         SPACE 2
G01942   IEDHJN ,                       SET THE DATE             Y02027
QMB00    EQU   *                                                 Y02027
         SPACE 2
         USING FORCORE,R4               OPEN WORK AREA DSECT     Y02027
         L     R4,FOUR(R8)              ADDR OF OPEN WORK AREA   Y02027
         L     RDCB,DXUDCBAD            GET DCB ADDRESS          Y02027
         DROP  R4                                                Y02027
         L     R8,DCBIOBAD              GET WORK AREA ADDRESS    S22024
         LM    R14,R11,CKPSAVE1+12      RESTORE REGS SAVED BY    S22024
*                                        PREVIOUS LOAD           S22024
         LR    R8,R2                    RESET WORKAREA BASE      S22024
         MVC   AVTDPARM(1),DCBOPTCD     SAVE FOR LATER TESTS     S22024
         SPACE 3
*CHECK FOR UNRECOVERABLE ERROR ENCOUNTERED BY IGG01943 AT RESTART  0819
         SPACE
         TM    CKPFLAGS,CKPERR          CHECK ERROR FLAG           0819
         BNZ   QMB9804                  BRANCH IF ERROR FOUND      0819
         SR    R3,R3                    CLEAR                      1109
         IC    R3,CKPERRCT              GET ERR COUNT SET BY  1943 1109
         LTR   R3,R3                    CHECK FOR ERRORS           ×109
         BZ    QMB20                    BRANCH IF NO ERRORS        1109
         SPACE 3
*BEGINNING OF LOOP THROUGH COUNT OF ERROR MESSAGES                 1109
         SPACE
QMB10    EQU   *                                                   1109
         MVC   AVTSAVE4(68),QMBMSG      MOVE MSG OUT OF TRANSNT ARA0115
         LA    R1,AVTSAVE4              ADDR OF MSG                0115
         WTO   MF=(E,(1))               ISSUE ERROR MSG            0115
         BCT   R3,QMB10                 SUBTRACT FROM COUNT OF ERRS1109
         LM    R14,R11,CKPSAVE1+12     RSTR REGS SVD BY 1941    SA56217
         MVC   0(5,R8),QMBWARM          SET ID FOR IGG01944        1109
         L     R15,FOUR(R8)             GET ADDRESS OF OPEN      Y02027
*                                       WORK AREA                Y02027
         B     QMB875                   XCTL TO NEXT LOAD FOR WARM 1109
         SPACE 3
*THE NUMBER OF RECORDS REQUIRED FOR A TOTAL CHECKPOINT HAS BEEN
*DETERMINED. NOW FORMAT THE DISK WITH DUMMY RECORDS. WRITE ONE
*CONTROL RECORD, THEN WRITE THE NUMBER OF RECORDS NEEDED FOR TWO
*TOTAL RECORDS. DETERMINE THE NUMBER OF CKREQ RECORDS NEEDED (THE
*VALUE GIVEN ON INTRO +3 FOR DISK ERROR RECOVERY) AND WRITE THESE.
*DETERMINE IF THERE ARE ENOUGH TRACKS LEFT TO WRITE ANOTHER TOTAL
*PLUS ONE TRACK OF INCIDENTS.IF SO, WRITE THE RECORDS FOR ANOTHER
*TOTAL. WRITE AS MANY INCIDENT RECORDS AS THE REMAINING SPACE
*ALLOWS.
         SPACE 3
*WRITE CONTROL RECORD
         SPACE
QMB20    EQU   *                                                   1109
         XC    CKPTTRLI,CKPTTRLI                                SA64710
         MVI   CKPTTRCT,1               INITIALIZE CURNT ENV RCD   0826
         LA    R13,CKPCYLNO             PUT ADDR OF COUNT AREA INTO
         ST    R13,CKPRW                CCW TO WRITE COUNT,KEY,DATA
         MVI   CKPRW,CKPWCKD            PUT IN CODE FOR FORMAT WRIT
         SR    R0,R0                    RELATIVE TTR FOR R0
         LM    R1,R2,CKPCPARM           CONVERSION RESULT AND DEBAD
         XC    CKPKEYLN(2),CKPKEYLN     CLEAR LENGTH FIELD
         BAL   R13,QMB9905             GET SEEK ADDR FOR IOB
         MVC   CKPCYLNO(4),CKPIOCC      MOVE CYL AND TRACK TO COUNT
         MVC   CKPDATLN+1(1),CKPCRLEN   DATA LENGTH-CONTROL RECORD
         LA    R2,CKPCYLNO-3            ADDRESS FOR CONVERSION RSLT
         MVI   CKPRCDNO,1               PUT RECORD NO. INTO COUNT
         LA    R0,1                     RELATIVE TTR
         BAL   R13,QMB98                WRITE CONTROL RCD AND WAIT
         SPACE 3
*DETERMINE THE NUMBER OF ENVIRONMENT CHECKPOINTS TO WRITE.  THIS
*DEPENDS UPON THE CPRCK OPERAND ON INTRO AND THE AMOUNT OF SPACE
*IN THE CHECKPOINT DATA SET.  THE SMALLER VALUE IS USED.
         SPACE
         LH    R5,CKPTRKSA              NO. OF TRACKS IN DATA SET
         SR    R10,R10
         IC    R10,CKPRPERT             RCD SEGMENTS  PER TRACK
         MR    R4,R10                   NO. ENVIORNMENT RCD SEGMNTS    X
                                        WHICH CAN BE WRITTEN THERE
         BCTR  R5,0                     SUBTRACT ONE RCD FOR           X
                                        THE CONTROL RECORD
         TM    AVTDPARM,CKP3705         3705 CHECKPOINT DCB      S22024
         BO    QMB708                   YES, BRANCH              S22024
         SR    R14,R14
         SR    R15,R15
         SR    R11,R11
         IC    R15,CKPCPERT             NO. CKREQ RCDS PER TRACK   0529
         MH    R15,QMB100H              MULTIPLY BY 100            0909
         DR    R14,R10                  GET INC PER ENV SEG        0909
         STH   R15,CKPIPERE             SAVE
         LR    R11,R15                  SAVE RESULT
         SR    R14,R14                  CLEAR FOR DIVISION
         SR    R15,R15                  CLEAR                      0910
         IC    R15,AVTNCKPR             VALUE OF CKREQS
         STC   R15,CKPCKRQS             SAVE VALUE OF CKREQS-INTRO
         LA    R15,CKPCKRNO+1(,R15)     NO. OF INCIDENTS +CKREQS   0910
         LH    R10,CKPINCLN             LENGTH OF INCIDENT RCDS    0928
QMB55    EQU   *                                                   0928
         SH    R10,CKPCKRLN             SUBTRACT LENGTH OF CKREQ RC0928
         BNP   QMB60                    BRANCH NOT POSITIVE        0928
         LA    R15,1(R15)               ADD 1 TO NO. CKREQ         0928
         B     QMB55                    BRANCH IF INCIDENT RCD LARG0928
QMB60    EQU   *                                                   0928
         MH    R15,QMB100H              MULTIPLY BY 100            0909
         DR    R14,R11                  NO. OF ENV RCDS
         LTR   R14,R14                  CHECK FOR REMAINDER
         BZ    QMB706                   C BRANCH IF NO REMAINDER
         LA    R15,1(0,R15)
QMB706   EQU   *
         SPACE 3
         SR    R5,R15                   NO. OF RCDS WHICH CAN BE USED  X
                                        FOR ENV CHECKPOINT RCDS
         BC    12,QMB90                 BARNCH NOT POSITIVE
         LH    R10,CKPRCDSR             NO. OF SEGMENTS REQUIRED FOR   X
                                        ONE ENV CHECKPOINT
         DR    R4,R10                   GET NO OF ENV CHECKPT RCDS     X
                                        WHICH CAN BE WRITTEN
         SPACE 3
         SR    R10,R10                  CLEAR REGISTER         @OY20541
         IC    R10,AVTCPRCD             NO OF ENV RCDS REQUESTED
         CR    R10,R5                   COMPARE
         BNH   QMB707                   USE THE SMALLER VALUE
         LA    R15,ABENDA               SET 045 ABEND SUBCODE  @SA75746
         B     ABENDCDE                 BRANCH TO ABEND        @SA75746
         SPACE 3
*SET UP FOR LOOP TO BUILD ALL THE ENVIORNMENT RECORD SEGMENTS
         SPACE
QMB707   EQU   *
         LH    R5,CKPRCDSR              NO. RCDS REQUIRED-TOTAL  S22024
QMB708   EQU   *                                                 S22024
         STC   R10,CKPCPRCD             SAVE COUNT OF ENV RCDS     0909
         CH    R10,AVTHA2               CHECK FOR MINIMUM OF 2
         BL    QMB90                    BRANCH IF LESS THAN MINIMUM
         MVI   CKPIOR,1                 PUT RECORD NO. INTO IOB
         LA    R0,2                     RELATIVE TTR
         MVC   CKPDATLN,CKPBPERR        PUT DATA LENGTH INTO COUNT
         MVI   CKPRCDNO,2               PUT RCD NO. INTO COUNT AREA
         SR    R3,R3
         IC    R3,CKPRPERT              GET TCDS PER TRACK
         LA    R4,CKPTTRT1
         LR    R7,R3                    NO. OF RECORDS ON TRACK
         BCTR  R7,0                     SUBTRACT 1 FOR CONTROL RCD
         SPACE 3
*BEGINNING OF LOOP TO WRITE ALL ENVIORNMENT CHECKPOINTS
         SPACE
QMB71    EQU   *
         ST    R0,CKPTRMAD
         MVC    0(3,R4),CKPTRMAD+1      MOVE TTR TO CONTROL RECORD
         MVC   3(2,R4),CKPTRKLN         SAVE DATA ON TRACK PRECEDING415X
                                        THIS ENV SEGMENT           G415
         BAL   R11,QMB91                WRITE RECORD
         LA    R4,CKPTTRLN(,R4)         MOVE TO NEXT TTR SLOT      0415
         BCT   R10,QMB71                SUBTRACT FROM NO OF RCDS
         SPACE 3
*SET UP FOR LOOP TO WRITE CKREQ RECORDS
         SPACE
         CR    R7,R3                    CHECK FOR NEW TRACK        0529
         IC    R3,CKPCPERT              NO. OF CKREQ RCDS PER TRACK
         BNE   QMB7105                  BRANCH IF NEW TRACK        0529
         LR    R7,R3                    INITIALIZE TO NO RCDS/TRCK
         B     QMB72                     NO WORRY ABOUT PARTIAL TRK0529
QMB7105  EQU   *                                                   0529
         LH    R6,CKPIPERE              GET COUNT OF INCIDENT PER E
         MR    R6,R6
         D     R6,QMB100F               DIVIDE BY 100              0909
QMB72    EQU   *
         ST    R0,CKPTTRCR-1            TTR OF 1ST CKREQ RECORD
         LTR   R11,R7                   GET COUNT OF CKREQ ON TRK  0114
         BNZ   QMB73                    BRANCH IF COUNT IS NOT ZERO0114
         LA    R7,1                     SET COUNT TO 1             0114
         B     QMB72                    TRY AGAIN
QMB73    EQU   *                                                   0115
         BCTR  R11,0                    SUBTRACT ONE               0824
         AR    R11,R0                   GET TTR OF LAST RCD ON TRK 0824
         STC   R11,CKPCRRNO             SAVE TTR OF LAST RCD       0824
         L     R10,CKPCTTRB             BEGINNING OF CKTTR-TABLE
         OI    CKPFLAGS,CKPOPNCR        TURN ON OPEN CKREQ FLAG FOR    *
                                        SUBROUTINE
         MVC   CKPDATLN(2),CKPCKRLN     LENGTH OF CKREQ RCD
         SR    R5,R5                    CLEAR
         IC    R5,AVTNCKPR              GET NO. OF CKREQ QUEUES
         LTR   R5,R5                    CHECK FOR NO CKREQS SPECIFIED
         BZ    QMB75                    BRANCH IF ZERO
         LA    R5,CKPCKRNO(0,R5)        ADD EXTRAS(FOR DISK ERRORS)
         SPACE
         BAL   R11,QMB91                WRITE RECORDS
QMB75    EQU   *
         NI    CKPFLAGS,X'FF'-CKPOPNCR  TURN OFF OPEN CKREQ FLAG
         SPACE 3
*SET UP FOR LOOP TO WRITE INCIDENT RECORDS ON ALL REMAINING DISK
*SPACE
         SPACE
         SR    R13,R13                  CLEAR                      0626
         IC    R13,CKPIPERT             GET NO. INC PER TRACK      0626
         SR    R3,R13                   GET DIFFERENCE FROM CKREQS 0626
         BNP   QMB80                    BRANCH IF MORE INCIDENT RCD0626X
                                        FIT ON ONE TRACK           0626
         SR    R7,R3                    GET NO. INC RCDS ON 1ST TRK0626
         BP    QMB80                    BRANCH IF POSITIVE RESULT  0910
         LA    R7,1                     TRY WRITING 1 RCD ON TRK   0910
QMB80    EQU   *                                                   0626
         LA    R5,X'FF'                 MAXIMUM NO OF INCIDENTS SA64712X
                                        TO WRITE. LET CONVERSION       X
                                        ROUTINE FIGURE END OF DATA
         LA    R11,QMB82                BRANCH ADDRESS
         BAL   R13,QMB9905              CHECK FOR END OF DATA SET
         ST    R0,CKPTTRIN-1            TTR OF 1ST INCIDENT RCD
         MVC   CKPSECIN,CKPTRKLN        SAVE TRK LENGTH FOR 1ST INC0504
         LR    R11,R7                   COPY TTR OF 1ST RCD        0824
         BCTR  R11,0                    SUBTRACT ONE               0824
         AR    R11,R0                   GET TTR OF LAST RCD ON TRAK0824
         STC   R11,CKPINRNO             SAVE TTR OF LAST RCD       0824
         SR    R3,R3                    CLEAR REG                  0625
         IC    R3,CKPIPERT              COUNT OF INCIDENTS PER TRACK
         MVC   CKPDATLN(2),CKPINCLN     LENGTH OF INCIDENT RCD
         BAL   R11,QMB91                WRITE RECORDS
         LTR   R6,R6                    CHECK FOR 255 RCDS WRITTEN 1202
         BNM   QMB81                    BRNNCH IF LESS THAN 255 RCD1202
         SR    R6,R6                    ZERO REG FOR SUBTRACTIONSA64712
QMB81    EQU   *
         SR    R5,R6                    NUMBER OF RECORDS WRITEN
QMB82    EQU   *
         STC   R5,CKPINCNO              SAVE NO. AVAILABLE RECORDS
         STC   R5,CKPINCNT              SAVE NO. AVAILABLE RECORDS
         OI    AVTCKFLG,AVTCKTAC        SET TCAM CKPT ACTIVE   @G36XRNP
         EJECT
         SPACE 3
*OPEN PROVIDES SECTION TO SET UP XCTL TO NEXT MODULE
         SPACE
QMB87    EQU   *
         XC    CKPCYLNO(CKPCTTRB-CKPCYLNO),CKPCYLNO  CLEAR THE PART    X
                                        OF THE WORK AREA USED BY       X
                                        OPEN
         LM    R14,R11,CKPSAVE1+12     RSTR REGS SVD BY 1941
         L     R15,FOUR(R8)             GET ADDRESS OF OPEN      Y02027
*                                       WORK AREA                Y02027
QMB873   EQU   *
         XC    0(2,R8),0(R8)            CLEAR TO INDICATE COMPLETE
QMB875   EQU   *
         LA    R8,8(R8)                 INCREMENT CURRENT WTO ENTRY
         LA    R7,4(R7)                 INCREMENT CURRENT DCB LIST
         CLC   ZERO(TWO,R8),AMIDCNST    IS THIS EXECUTOR NEEDED  Y02027
*                                         AGAIN                  Y02027
         BE    QMB00                    YES, RETURN TO PROCESS ITS22024
         CLC   0(2,R8),QMB0S            END OF WTG TABLE
         BNE   QMB875                   NO,GO CHECK NEXT ENTRY
         LR    R7,R5                    RESET PARM LIST ADDR
         LA    R8,32(R6)                RESET WTO TABLE POINTER
         SPACE
QMB88    EQU   *
         CLI   0(R8),0                  IS THIS ENTRY ZERO
         BNE   QMB89                    NO,XCTL
         LA    R8,8(0,R8)               ADDRESS OF NEXT ENTRY
         LA    R7,4(0,R7)               ADDRESS OF NEXT DCB
         B     QMB88                    BRANCH TO TEST NEXT ENTRYS22024
         SPACE 3
*TRANSFER CONTROL TO NEXT EXECUTOR OF FIRST INCOMPLETE DCB
         SPACE
QMB89    EQU   *
         SPACE 1
         MODESET EXTKEY=DATAMGT         RETURN TO KEY 5          Y02027
         SPACE 2
         DROP  R8                       DROP 8 AND USE 2 AS        0216
         USING IEDQCKPD,R2              BASE FOR CKPT WORK AREA    0216
         LA    R15,DXCCW12-FORCORE(R15) GET PARM LIST            Y02027
         DROP  R2                       SWITCH BACK TO             0219
         USING IEDQCKPD,R8              REGISTER 8                 0219
         MVC   6(2,R6),0(R8)            EXECUTOR ID TO PARM
         MVC   14(3,R6),2(R8)           TTR TO WTG TABLE
         SPACE
         XCTL  DE=(R6),SF=(E,(15))
         EJECT
         SPACE
         SPACE 3
*SUBROUTINE TO SET UP LOOP AND WRITE THE NEEDED NUMBER OF RECORDS.
*R11 IS USED AS THE BRANCH REG
         SPACE
QMB91    EQU   *
         LR    R6,R5                    INIT. COUNT OR RCDS
         SPACE 3
*BEGINNING OF LOOP TO WRITE RECORDS
         SPACE
QMB92    EQU   *
         TM    CKPFLAGS,CKPOPNCR        CHECK FOR CKREQ BEING WRITN
         BZ    QMB93                    BRANCH IF NOT CKREQ
         ST    R0,CKPTRMAD              PUT TTR IN CKREQ TTR TABLE
         MVI   CKPCTFLG(R10),0          CLEAR FLAG
         MVC   CKPCTTTR(3,R10),CKPTRMAD+1  PUT TTR INCKREQ-TTR TBL
         XC    CKPCTOFF(2,R10),CKPCTOFF(R10) CLEAR OFFSET
         MVC   CKPCTSEC(2,R10),CKPTRKLN SAVE DATA ON TRACK        0415X
         LA    R10,CKPCTTRL(0,R10)      MOVE TO NEXT TABLE ENTRY
QMB93    EQU   *
         BAL   R13,QMB98                WRITE RECORDS
         BCT   R7,QMB94                 DECREMENT RCDS TO WRITE ON     *
                                        THIS TRACK, BRANCH NOT ZERO
         SPACE
*END OF THRACKHAS BEEN REACHED,UPDATE TTR TO NEXT TRACK
         SPACE
         IC    R0,CKPKEYLN              CLEAR OUT RCD NUMBER
         LR    R15,R0
         LA    R0,CKP101(0,R15)         BUMP TRACK NUMBER
         LR    R7,R3                    REINTIALIZE COUNT/TRACK
         XC    CKPTRKLN,CKPTRKLN        RESET DATA ON TRACK TO 0   0415
         BAL   R13,QMB9905             GET SEEK ADDR FOR IOB-R0
QMB94    EQU   *
         MVC   CKPIOM(8),CKPCYLNO-3     UPDATE MBBCCHHR IN IOB
         LR    R15,R0                   TTR
         LA    R0,1(R15)                BUMP RECORD NUMBER
         BAL   R13,QMB9905              CONVERT TTR
         BCT   R6,QMB92                 DECREMENT RCDS TO WRITE,       *
                                        BRANCH IF NOT ALL WRITTEN
         SPACE
*END OF LOOP
QMB95    EQU   *                                                 S22024
         TM    AVTDPARM,CKP3705         3705 CHECKPOINT DCB      S22024
         BZR   R11                      NO, EXIT               @G36XRNP
         OI    AVTCKFLG,AVTCKNAC        SET 3705 CKPT ACTIVE   @G36XRNP
         B     QMB87                    PREPARE TO EXIT        @G36XRNP
         SPACE
*END OF SUBROUTINE
         SPACE 3
*SUBROUTINE TO CONVERT TTR, ISSUE EXCP, AND WAIT (USES R13 AS
*BRANCH REG.)
         SPACE
QMB98    EQU   *
         STM   R0,R1,CKPSAVE1           SAVE SUPERVISOR REGS
QMB9802  EQU   *                                                   0415
         LH    R1,CKPTRKLN              GET DATA ON TRACK SO FAR   0415
         AH    R1,CKPDATLN              ADD DATA JUST WRITTEN      0415
         STH   R1,CKPTRKLN              RESTORE DATA LENGTH        0415
         EXCP  CKPIOB                   START I/O
         SPACE
         WAIT ECB=CKPECB                WAIT FOR COMPLETION
         SPACE
         LM    R0,R1,CKPSAVE1           RESTORE SUPERVISOR REGS
         TM    CKPECB,X'7F'             CHECK FOR DISK ERROR
         BCR   1,R13                   BRANCH IF NO ERROR
         SPACE 3
*PERMANANT DISK ERROR-WTO
         SPACE
QMB9804  EQU   *                                                   0819
         LA    R13,DISKERR              SET OFFSET TO MESSAGE    S22024
         B     QMB99                    BRANCH                   S22024
         SPACE 3
*INSUFFICIENT DISK SPACE FOR MINIMUM  CHECKPOINT REQUIREMENTS
         SPACE
QMB90    EQU   *
         LA    R13,DISKALOC             SET OFFSET TO MESSAGE    S22024
         SPACE
QMB99    EQU   *
         MVC   AVTSAVE4(DDL),IEDNMG     MOVE MSG MODULE NAME     S22024
         LOAD  EPLOC=AVTSAVE4           LOAD MSG MODULE          S22024
         ALR   R13,R0                   GET ADDR OF PTR TO MSG   S22024
         L     R1,AVTEZERO(R13)         GET ADDR OF MESSAGE      S22024
         L     R13,AVTTCB               GET TCB ADDRESS          S22024
         L     R13,TCBTIO(R13)          GET TIOT ADDRESS         S22024
         L     RDCB,CKPIODCB-ONE        RELOAD DCB ADDRESS       S22024
         AH    R13,DCBTIOT              GET TIOE ADDRESS         S22024
         MVC   DDNAME(DDL,R1),TIOEDDNM(R13) MOVE DDNAME TO MSG   S22024
         WTO   MF=(E,(1))               WRITE MSG
         DELETE EPLOC=AVTSAVE4          DELETE MSG MODULE        S22024
         TM    AVTDPARM,CKP3705         3705 CHECKPOINT DCB      S22024
         BZ    QMB9903                  NO, BRANCH               S22024
         L     RDCB,CKPIODCB-1          RELOAD DCB ADDRESS       S22024
         XC    DCBIOBAD+ONE(THREE),DCBIOBAD+ONE ZERO TO SHOW NOT S22024
*                                        OPENED DUE TO I/O ERROR S22024
         LA    R0,CKPLNGTH+CKPTTRLN     GET LENGTH OF AREA     @G36XRNP
         LR    R1,R8                    GET ADDRESS OF AREA    @G36XRNP
         ICM   R0,HI,AVTGETMN+NINE      GET SUBPOOL ADDRESS    @G36XRNP
         FREEMAIN R,LV=(0),A=(1)        FREE THE WORKAREA      @G36XRNP
         B     QMB87                    AND EXIT                 S22024
QMB9903  EQU   *                                                 S22024
         LA    R15,ABEND9               SET 045 ABEND SUBCODE  @SA75746
ABENDCDE EQU   *                                               @SA75746
         LA    R13,AVTSAVE2             GET SAVE AREA FOR ABEND SA59025
         BAL   R14,AVTABEND             ABEND WITH 045-9 OR -A @SA75746
         SPACE 3
*CONVERT TTR
         SPACE
QMB9905  EQU   *
         STM   RAVT,R0,CKPSAVE2         SAVE REGS USED BY CONVERT  0224
         L     R15,16                   GET CVT
         L     R15,28(0,R15)            GET CONVERSION RTN
         SLL   R0,8                     POSITION TTR
         BALR  R14,R15                  BRANCH TO CONVERT
         SPACE
         LTR   R15,R15                  CHECK FOR VALID TTR
         SPACE
         LM    RAVT,R0,CKPSAVE2         RESTORE REGS               0224
         BNZ   QMB95                    BR IF END OF DATA SET    S22024
         BR    R13                      RETURN
         SPACE
*END OF SUBROUTINE
         SPACE 3
         DS    0F                                                  0909
QMB100F  DC    H'0'                     USED TO GET ACCURATE TO    0909
QMB100H  DC    H'100'                   NEAREST 100TH OF RCD COUNT 0909
QMB0S    DC    C'0S'                    ID FOR SYSTEM OPEN       S22024
QMBFLAG  DC    X'8000'                  FLAG FOR CONVRT RTN 0507 S21101
AMIDCNST DC    C'42'                    ID FOR THIS EXECUTOR     Y02027
ZERO     EQU   0                        OFFSET                   Y02027
TWO      EQU   2                        OFFSET                   Y02027
FOUR     EQU   4                        OFFSET                   Y02027
DISKERR  EQU   28                       OFFSET TO DISK ERROR MSG S22024
DISKALOC EQU   24                       OFFSET TO DISK ALLOCATIONS22024
*                                        ERROR MESSAGE           S22024
TCBTIO   EQU   12                       TIOT IN TCB              S22024
TIOEDDNM EQU   4                        DDNAME IN TIOE           S22024
DDNAME   EQU   12                       OFFSET TO DDNAME IN MSG  S22024
DDL      EQU   8                        LENGTH OF DDNAME         S22024
IEDNMG   DC    CL8'IEDNMG'              NAME OF MESSAGE MODULE   S22024
ONE      EQU   1                        OFFSET OF ONE            S22024
THREE    EQU   3                        LENGTH OF THREE          S22024
ABENDA   EQU   X'A'                     045 ABEND SUBCODE      @SA75746
ABEND9   EQU   9                        045 ABEND SUBCODE       SA59025
CKP3705  EQU   X'10'                    OPCTD=I                  S22024
NINE     EQU   9                        DISPLACEMENT           @G36XRNP
HI       EQU   8                        HI-ORDER MASK          @G36XRNP
QMBMSG   WTO   'IED083I CHECKPOINT DISK ERROR-RECOVERY FROM PREVIOUS REX
               CORD',ROUTCDE=11,DESC=4,MF=L                        0115
PATCH    DC    4F'0'                    PATCH AREA             @G36XRNP
         EJECT
*CODE TO ROUND OUT MOUDULE TO 1024 BYTES IN LENGTH
         SPACE
NENT     EQU   1                        VALUE 1                  S22024
LENGTH   EQU   NENT*6+6                 LENGTH                   S22024
NODBWDS  EQU   (LENGTH+7)/8             LENGTH                   S22024
ORGIDTTR EQU   1024-NODBWDS*8           ADDRESS                  S22024
         ORG   IGG01942+ORGIDTTR        EXTEND TO FULL SIZE      S22024
QMBWARM  DC    C'44'                    ID FOR WARM START          1109
         DC    XL4'0'                   TTR AND LENGTH             1109
IDEND    DC    X'0000'                  END OF MODULE NAMELIST   S22024
         CNOP  4,8
LOADID   DC    C'019'                   OPEN ID                  S22024
         DC    YL1(X'80'-NODBWDS)       ADJUST MODULE LENGTH     S22024
         SPACE 3
         TAVTD
         TCKPD 3330                     MERLIN VERSION OF DSECT    0426
         DCBD  DSORG=TX
         EJECT
FORCORE  DSECT                                                   Y02027
         IECDSECT
         EJECT
         END
