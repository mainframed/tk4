1946     TITLE '''IGG01946'' - TCAM APPL PROG OPEN EXECUTOR(LOAD1)'
IGG01946 CSECT
         SPACE 3
*  CHANGE ACTIVITY AS FOLLOWS
******************** MICROFICHE FLAGS *********************** SUPT CODE
*A015500,044500,504500                                           X01004
*C015000,044000,103000-105000                                    X01004
*A012500,013500,018200-018800,040200-040800,041200,105500,171500,Y01004
*A239200-239800,245200-245600,251500,252500,259100-259600,       Y01004
*A271200-271800,277200-277600,285600-285800,296100-296700,       Y01004
*A441200-443600,445400-448200,449400-459400,505608-505648        Y01004
*C012000,013000,018000,040000,041000,107000,149000,155000,159000,Y01004
*C188000,207000,251000,252000,441000,445000,449000,505600        Y01004
*D098000,108000,110000,120000,133000,150000-153000,156000,160000,Y01004
*D209000-209800,339000-340000,469000,540000                      Y01004
*C132000                                                         X02004
*C118000,545000                                                  Y01948
*D054000,084000,505900,532000-533000,546000-547000,551000        Y01948
*A098600-098800,165500,383500,419500                             Y05330
*C098500,165000,167000,259100,381000,383000,419000,420000        Y05330
*D369000-371000,391000-394000,467000                             Y05330
*A102000,137000,231600,266000,470500                             Y01948
*C091000,146000-148000,202000-203000,237000-240000,256000,260000-Y01948
*C262000,268000-269000,271200-271400,285800-290000,408300-408600 Y01948
*D0985000,139000-141000,242000-245600,274000-280000,296200       Y01948
*A452200,459000,505600,530700,552000                            OY01838
*C457000-457400,505420                                          OY01838
*C012000-023200,040000,041200,052000,080000,080000,091300,097000 Y02027
*C101500-103200,105500,107000,146000,159000,161000,168000,169000 Y02027
*C170000,227000-231000,232000,233000-236000,237000,239000,241000 Y02027
*C246000-251000,252000,253000-254000,285600,305000,307000,337000 Y02027
*C361000,374000,382100-418700,419000,470000,503000,505616,512000 Y02027
*C530100-530700                                                  Y02027
*D040200-040400,041500,053000,077500,094000-095000,106000,107000 Y02027
*D109000-138000,149000,162000,171000,172000-173000,176000-221000 Y02027
*D231300-231600,232400,236300-236600,251500,252500,256000-272000 Y02027
*D285700-285800,286800,287800,291000,292000-296100,306000,303000 Y02027
*D309000,338000,346000,358000,372400-372600,381000,419500,420000 Y02027
*D441000,442000-460000,468000,471000-495200,505060,505180,505420 Y02027
*D505450,505600,505624,505632,505640,505648,505720,507000-508000 Y02027
*D530750,541000,549000,549300,549600,550000,552000,582000        Y02027
*A041400,041600,082500,099900,108500,111500-117500,120500,123500 Y02027
*A126500,129500,148000,150000,160000,168500,169500,170500,238000 Y02027
*A171600,171700,228000,240000,242000-245000,286600,287600,288600 Y02027
*A289600-291600,296350,304500,332050-332850,368800-370400        Y02027
*A371680-371920,374300-380900,381200-381800,470070-470350        Y02027
*A470420-470560,503200-503600,536020-538880                      Y02027
*A182000                                                        SA55391
*C441000                                                        SA57692
*C133000                                                         22027
*A098000                                                         S22025
*C041000,109000-110000,240000,256000,331000,362000,371000,468000 S22025
*C477000,479000,492000,494000-495000                             S22025
*A092000,469000,475000,504000,549000                             A47156
*C237000,242000,249000,257000,269000,275000,425000-459800,471000 A47156
*D480000-491000                                                  A47156
*A201000,209000,236000,505000                                    A47157
*A077000,079000,084000,099000,142000,145000,193000,195000,223000 CLUP21
*A226000,285000,298000,302000,347000,350000,372000,421000,505000 CLUP21
*C092000,102000,139000,144000,151000,156000-157000,164000,194000 CLUP21
*C204000,214000,282000-284000,292500,300000-301000,327000,349000 CLUP21
*C513000,530100                                                  CLUP21
*D087000-090000,123000,135000,188000,192000,222000,232000,244000 CLUP21
*D253000,263000,273000,341000-345000,375000-380000,399000-402000 CLUP21
*D410000,413000,423000-424000,465000,470000,496000,505000        CLUP21
*D510000-511000,515000,518000-519000,521000-522000,524000-524500 CLUP21
*D528000                                                         CLUP21
*C527000,529000                                                  A44886
*C478000                                                         A42380
*C151000,204000,287000-289000,291000-292000                      A47158
*C106000,133000,146000-150000,156000-163000,167000-168000        S22024
*C172000-173000,176000-182000,186000-188000,198000,200000,204000 S22024
*C209000,210000-211000,215000-218000,227000-228200,246000,254000 S22024
*C265000,278000,289000,292500,307000,327000-330000,339000,361000 S22024
*C374000,393000,396000-400000,416000                             S22024
*A408300-408600,498100-498700                                    S22024
*C328000,475000                                                  S21903
*C146000,157000,505900                                          SA60016
*C239000                                                       @ZA00205
*A239100-239600,470630                                         @ZA00205
*D240000                                                       @ZA00205
*A105000,470700                                                @SA69652
*A412700,415100                                                @ZA02619
*A101500                                                       @SA69652
*C523000                                                       @SA70001
*A105000                                                       @ZA02083
*A105000,370400,394100,403100,502000,530000,536490,538880      @ZA02601
*C332050-332750,382400-385100,385700,304500,359500             @ZA02601
*C394320                                                       @ZA02601
*D393500,105500,400400-400700                                  @ZA02601
*C383240                                                       @ZA05047
*C380000                                PECB DSECT CHG         @OZ07830
*C157000                                                       @OZ14150
*A332450                                                       @OZ11209
*A382760,382880,383480,383660,478000                           @OZ17630
*                                       PECB DSECT CHG         @OZ18744
         SPACE 4
***********************************************************************
*                                                                     *
*TITLE: 'IGG01946' TCAM APPLICATION PROGRAM OPEN EXECUTOR(LOAD 1)     *
*                                                                     *
*  MODULE NAME = IGG01946                                             *
*                                                                     *
*  DESCRIPTIVE NAME = TCAM APPLICATION PROGRAM OPEN EXECUTOR (LOAD 1) *
*                                                                     *
*  COPYRIGHT = 'NONE'                                                 *
*                                                                     *
*  STATUS:  CHANGE LEVEL 5                                            *
*                                                                     *
*FUNCTION: THIS MODULE INITIATES ACTIVATION OF A DATA TRANSFER        *
*   COMMUNICATION LINK BETWEEN AN APPLICATION PROGRAM AND THE MCP.    *
*   OPEN EXECUTES IN KEY 5, DATA MANAGEMENT KEY. IF TCAM IS ACTIVE,   *
*   AN AUTHORIZATION CHECK IS MADE.  THE EXECUTOR GETS MAIN FOR THE   *
*   DEB IN SUBPOOL 230, KEY 5. A DEBCHK IS ISSUED TO ADD THE DEB TO   *
*   LIST.  THE DEB IS INITIALIZED AND ENQUEUED ON THE TCB CHAIN       *
*   USING COMPARE & SWAP. NEXT, APPROPRIATE ACCESS METHOD MODULES     *
*   ARE LOADED - IGG019RG, IGG019RI, IGG019RM, IGG019RL. CORE IS      *
*   GOTTEN FOR THE LOCATE MODE WORKAREA IF NEEDED AND THE PROCESS     *
*   ECB IN USER KEY. ONCE PER OPEN MACRO, CORE IS GOTTEN FOR AN RCB   *
*   IN KEY 6 IN CSA.  THE RCB IS INITIALIZED WITH INFORMATION FROM    *
*   THE APP. PROG REGION NEEDED BY IEDQEU IN MCP. OPEN THEN BUILDS    *
*   AN ELEMENT AND ISSUES SVC 102 TO POST RCB TO OPEN/CLOSE SUBTASK.  *
*   A WAIT IS ISSUED FOR COMPLETION OF IEDQEU WHICH POSTS EDB COMPLETE*
*   UPON RETURN, THE DEB IS FURTHER INITIALIZED WITH INFO FROM THE    *
*   RCB.  THE COPIED DCB IS MOVED TO USER DCB.  AFTER ALL DCB'S       *
*   ARE OPENED, THE CORE FOR RCB IS FREED.                            *
*                                                                     *
*ENTRY POINT:                                                         *
*        IGG01946                                                     *
*                                                                     *
*INPUT: REGISTERS 5,6,7 AND 8 CONTAIN THE FOLLOWING VALUES:           *
*   5 - ADDRESS OF FIRST ENTRY IN DCB PARAMETER LIST                  *
*   6 - ADDRESS OF BEGINNING OF WHERE-TO-GO TABLE                     *
*   7 - ADDRESS OF CURRENT ENTRY IN DCB PARAMETER LIST                *
*   8 - ADDRESS OF CURRENT ENTRY IN WHERE-TO-GO TABLE                 *
*                                                                     *
*OUTPUT: REGISTERS 5,6,7 AND 8 CONTAIN THE FOLLOWING VALUES:          *
*   5 - SEE 'INPUT'                                                   *
*   6 - SEE 'INPUT'                                                   *
*   7 - ADDRESS OF NEXT ENTRY IN DCB PARAMETER LIST                   *
*   8 - ADDRESS OF NEXT ENTRY IN WHERE-TO-GO TABLE                    *
*                                                                     *
*EXTERNAL ROUTINES.  DEBCHK-- USED TO ADD DEB TO LIST.                *
*                   IEDQEB -- AQCTL SVC 102 ROUTINE -- TO TPOST A     *
*   SPECIAL ELEMENT TO THE MCP READY QUEUE.                           *
*                   AOS LOAD ROUTINE (SVC 8) -- TO LOAD REQUIRED      *
*                    TESTAUTH-- USED TO CHECK FOR APF AUTHORIZAION    *
*                    GETMAIN-- USED TO GET CORE FOR CONTROL BLOCKS    *
*   A MODESET IS USED GET GET IN CORRECT KEY BEFORE GETMAIN           *
*EXITS-NORMAL: THIS MODULE XCTLS TO NEXT ENTRY IN WTG TABLE           *
*                                                                     *
*EXITS-ERROR: THIS MODULE XCTLS TO THE OPEN ERROR HANDLER ROUTINE     *
*             (IGG0194B).                                             *
*                                                                     *
*TABLES/WORK AREAS:                                                   *
*   CVT                                                               *
*   AVT                                                               *
*   DCB                                                               *
*   TCB                                                               *
*   DEB                                                               *
*   PECB                                                              *
*   OPEN/CLOSE WORK AREA                                              *
*                                                                     *
*ATTRIBUTES: REENTRANT, REFRESHABLE                                   *
*                                                                     *
*NOTES: THE OPERATION OF THIS MODULE DEPENDS UPON AN INTERNAL         *
*   REPRESENTATION OF THE EXTERNAL CHARACTER SET WHICH IS EQUIVALENT  *
*   TO THE ONE USED AT ASSEMBLY TIME.  THE CODING HAS BEEN ARRANGED   *
*   SO THAT REDEFINITION OF 'CHARACTER' CONSTANTS, BY REASSEMBLY,     *
*   WILL RESULT IN A CORRECT MODULE FOR THE NEW DEFINITIONS.          *
*                                                                     *
***********************************************************************
         EJECT
RZERO    EQU   0                        GENERAL WORK REGISTER
RPARM    EQU   1                        PARAMETER/WORK REGISTER
RPENTRY  EQU   2                        ADDRESS OF PROCESS ENTRY
RCORE    EQU   3                        ADDRESS OF DCB WORKAREA
RDCB     EQU   4                        DCB BASE REGISTER
RPAR     EQU   5                        ADDRESS OF FIRST ENTRY IN
*                                       DCB PARAMETER LIST
RWTG     EQU   6                        ADDRESS OF WHERE-TO-GO
*                                       (WTG) TABLE
RPARC    EQU   7                        ADDRESS OF CURRENT ENTRY
*                                       IN DCB PARAMETER LIST
RWTGC    EQU   8                        ADDRESS OF CURRENT ENTRY
*                                       IN WHERE-TO-GO TABLE
RTCX     EQU   9                        TCX BASE REGISTER        Y02027
RWORK    EQU   10                       WORKAREA BASE REGISTER
RDEB     EQU   11                       DEB BASE REGISTER
RDCBMOVE EQU   11                       WORK REG TO MOVE DCB     Y02027
RBASE    EQU   12                       CSECT BASE REGISTER
RTCB     EQU   13                       ADDR TCB                 S22024
RETURN   EQU   14                       RETURN POINT REGISTER
RENTRY   EQU   15                       RETURN CODE REGISTER
         SPACE 3
         USING *,RENTRY                                          Y01948
         BAL   RBASE,BEGIN              BRANCH AROUND DC'S       Y02027
         DROP  RENTRY
         USING *,RBASE                  ADDRESSIBILITY           CLUP21
         USING IHADCB,RDCB              DCB ADDRESSABILITY
         USING FORCORE,RCORE            DCB WORKAREA ADDRESSABILITY
         USING IEDQTCB,RTCB             BASE FOR TCB.            Y02027
         DC    C'IGG019'                MODULE IDENTIFIER        Y05330
TAMOID   DC    C'46'                    ID OF THIS OPEN EXECUTOR Y05330
         IEDHJN
         EJECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *CLUP21
*                                                                CLUP21
* THE FOLLOWING SECTION OF CODE IS EXECUTED FOR EACH DCB BEING   CLUP21
* PROCESSED. IT VERIFIES THAT TCAM IS ACTIVE AND THAT A PROCESS  CLUP21
* ENTRY EXISTS AND IS NOT IN USE. WHEN ANY OF THESE CONDITIONS   CLUP21
* ARE NOT MET, THE APPLICATION PROGRAM IS ABENDED.               CLUP21
*                                                                CLUP21
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *CLUP21
BEGIN    SR    RPARM,RPARM              ZERO RCB PTR.            Y02027
START    EQU   *
         L     RCORE,FOUR(RWTGC)        ADDRESS OF OPEN WORKAREA
         ST    RPARM,DXCCW10+FOUR       STORE RCB ADDRESS        Y02027
         MODESET EXTKEY=ZERO            GET IN KEY ZERO        @SA69652
TESTRDY  EQU   *                                               @SA69652
         L     RTCX,CVTPTR              GET ADDRESS OF THE CVT   Y02027
         L     RDCB,NULL(RTCX)          GET PTR TO TCB           Y02027
         ICM   RTCX,ALL,CVTAQAVT-CVT(RTCX)  TCX ACTIVE - IS  TCAMY02027
*                                       MCP ACTIVE               X01004
         BZ    ABORT                    BRANCH IF NO.            X01004
         TM    TCXFLAG1-IEDQTCX(RTCX),TCXMCPCD TCAM TERMINATED @ZA02601
         BO    ABORT                    YES, REJECT OPEN       @ZA02601
         ICM   RPARM,AD,TCXOCQ+ONE-IEDQTCX(RTCX)   APPLICATION @ZA02083
*                                       PROG INTERFACE EXIST   @ZA02083
         BZ    ABORT                    NO, REJECT OPEN        @ZA02083
         TM    TCXBIT1-IEDQTCX(RTCX),AVTREADN  TCAM READY      @SA69652
         BO    TIMEUP                   BRANCH YES             @SA69652
         STIMER WAIT,DINTVL=INTVL                              @SA69652
         B     TESTRDY                  CKECK IF TCAM RDY      @SA69652
TIMEUP   EQU   *                                               @SA69652
         TM    TCXFLAG1-IEDQTCX(RTCX),TCXAUTH  TEST AUTHORIZE    Y02027
         BNO   AUTHORZD                 APPL. DONT REQ AUTHOR.   Y02027
         MODESET  EXTKEY=DATAMGT                                 Y02027
         TESTAUTH FCTN=1                CHECK APF.               Y02027
         LTR   RENTRY,RENTRY            CHECK RETURN CODE.       Y02027
         BNZ   ABORT7                   INVOKER NOT AUTHORIZED   Y02027
AUTHORZD EQU   *                                                 Y02027
         MODESET  EXTKEY=DATAMGT                                 Y02027
         L     RTCB,FOUR(RDCB)          ADDRESS OF CURRENT TCB   Y01948
         EJECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *CLUP21
*                                                                     *
* THE FOLLOWING SECTION OF CODE GETMAINS FOR AND INITIALIZES A   CLUP21
* DATA EXTENT BLOCK (DEB)                                        CLUP21
*                                                                     *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *CLUP21
         LA    RZERO,DEBSIZE+XTRADEB    SIZE OF DEB FOR GETMAIN  Y02027
         LA    RPENTRY,SPDEB            SUBPOOL 230 FOR DEB      Y02027
         BAL   RETURN,BLDLIST           GETMAIN FOR DEB          Y02027
         USING IEDQDEB,RDEB             DEB ADDRESSABILITY
         LR    RDEB,RPARM               SET DEB BASE REGISTER    Y01004
         MVI   12(RDEB),EIGHT           DEB SIZE IN DOUBLEWORDS@OZ14150
         LA    RDEB,SIXTEEN(ZERO,RDEB)  POINT TO DEB PROPER      S22024
         ST    RTCB,DEBTCBAD-ONE        STORE TCB ADDR IN DEB    Y02027
         L     RDCB,DXUDCBAD            GET ADDR OF REAL DCB
         STCM  RDCB,AD,DEBDCBAD         PUT REAL DCB ADDR IN DEB Y02027
         OI    DEBDCBAD-ONE,DEBID       SET THE DEB IDENTIFIER   S22024
         SR    RPARM,RPARM              CLEAR FOR ICM            Y05330
         ICM   RPARM,AD,TCBDEB+ONE      GET ADDRESS OF FIRST DEB Y05330
*                                       IN TCB DEB CHAIN
         MODESET  EXTKEY=ZERO    KEY 0 TO UPDATE DEB             Y02027
         STCM  RDEB,AD,DCBDEBAD+ONE     PUT DEB ADDR IN REAL DCB Y02027
CHAINDEB ST    RPARM,DEBDEBAD-ONE       PT. NEW DEB TO OLD HEAD  Y02027
         CS    RPARM,RDEB,TCBDEB        CHAIN NEW DEB TO TCB IF  Y02027
         BNE   CHAINDEB                   TCBDEB HAS NOT CHANGED Y02027
         MODESET  EXTKEY=DATAMGT        BACK TO KEY 5            Y02027
         DEBCHK  (RDCB),TYPE=ADD,AM=TCAMAP  INSERT DEB IN CHAIN  Y02027
         L     RDCB,NULL(RPARC)         ADDR OF COPIED DCB       Y02027
         STCM  RDEB,AD,DCBDEBAD+ONE     DEB ADDR IN COPIED DCB   Y02027
         MVI   DEBTAMID,DEBIDO          INITIALIZE DEB TAM FLAG
*                                       FIELD TO PUT/WRITE VALUE
         EJECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *CLUP21
*
* DETERMINE ACCESS MODULES TO BE LOADED AND LOAD THEM
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *CLUP21
         MVI   SIX(RWTG),MODULEID       CONSTANT ID              Y02027
         MVC   DCBMACRF(TWO),DCBMACR    GO TO POST OPEN STATE    S22024
         CLI   DCBMACR,NULL             IS BYTE 1 ZERO,I.E.,IS
*                                       THIS AN OUTPUT DCB
         BE    OUTPUT                   BRANCH IF YES
         MVI   DEBTAMID,DEBIDI          SET DEB FLAG FOR INPUT   Y01948
         LA    RPENTRY,DCBMACRF         ADDR OF INPUT DCB MACRO  Y02027
*                                       INSTRUCTION REFERENCE
         MVI   SEVEN(RWTG),GETREAD      ID FOR GET/READ MODULE   Y02027
CHECKL   EQU   *
         BAL   RETURN,LOADER            GO LOAD THE ROUTINE      Y02027
         STCM  RZERO,AD,DCBGET+ONE      STORE EP ADDR IN DCB     Y02027
         TM    ZERO(RPENTRY),READFLG    IS THIS A BSAM DCB     @ZA00205
         BO    BSAM                     BR YES                 @ZA00205
         A     RZERO,CONFOUR            ADJUST ENTRY PT ADDR FOROZ00205
*                                       QSAM ENTRY IN 19RG &19RIOZ00205
         STCM  RZERO,AD,DCBGET+ONE      STORE ENTRY PT IN DCB  @ZA00205
         B     POINT                    CONT QSAM PROCESSING   @ZA00205
BSAM     EQU   *                                               @ZA00205
         MVI   SEVEN(RWTG),CHECK        ID FOR CHECK MODULE      Y02027
         BAL   RETURN,LOADER            GO LOAD THE ROUTINE      Y02027
         STCM  RZERO,AD,DCBCHECK+ONE    STORE EP ADDRESS INTO DCBY01004
POINT    EQU   *                                                 Y02027
         TM    ZERO(RPENTRY),POINTFLG   POINT SPECIFIED          Y02027
         BZ    SVCTIME                  BRANCH IF NO             Y02027
         MVI   SEVEN(RWTG),POINTR       ID FOR POINT MODULE      Y02027
         BAL   RETURN,LOADER            GO LOAD THE ROUTINE
         ST    RZERO,DCBPOINT           STORE EP ADDR IN DCB
         B     SVCTIME                  GO INVOKE OPEN SUBTASK   Y02027
OUTPUT   EQU   *
         LA    RPENTRY,DCBMACRF+ONE     ADDR OF OUTPUT DCB       Y02027
         MVI   SEVEN(RWTG),PUTWRITE     ID FOR PUT/WRITE MODULE  Y02027
         B     CHECKL                   GO LOAD CHECK ROUTINE    Y02027
         EJECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *CLUP21
*                                                                CLUP21
* THIS SUBROUTINE SETS UP A PARAMETER LIST TO BE USED FOR        CLUP21
* DYNAMIC ALLOCATION OF MAIN STORAGE FOR DEB'S IOB'S AND         CLUP21
* LOCATE MODE WORKAREAS                                          CLUP21
*                                                                     *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *CLUP21
BLDLIST  EQU   *                                                 Y02027
*              FOR GETMAIN, REGISTER 0 = SIZE AND REGISTER 2=    Y02027
*              SUBPOOL, RETURN CODE IN REGISTER 15,              Y02027
*              AND ADDRESS OF GOTTEN AREA RETURNED IN REGISTER 1 Y02027
         LR    RWORK,RZERO              SAVE LEN                 Y02027
*                                       CLEAR GOTTEN CORE        Y02027
         GETMAIN     RC,LV=(0),SP=(2)
         SPACE
         LTR   RENTRY,RENTRY            SUCCESSFUL GETMAIN
         BNZ   ABORT3                   BRANCH IF NO
         LR    RENTRY,RPARM             COPY ADDR. OF STORAGE    Y01004
         LR    RZERO,RWORK              GET LEN OF GETMAIN REQ   Y02027
BLDLIST2 EQU   *                                                 Y01004
         MVI   NULL(RENTRY),NULL        CLEAR BYTE OF STORAGE    Y01004
         LA    RENTRY,ONE(RENTRY)       BUMP TO NEXT BYTE        Y01004
         BCT   RZERO,BLDLIST2           CLEAR NEXT BYTE          Y01004
         BR    RETURN                   RETURN TO CALLER
         EJECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *CLUP21
*                                                                     *
* SET UP TO TRANSFER CONTROL TO THE NEXT LOAD OF OPEN            CLUP21
*                                                                     *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *CLUP21
EXITTST  EQU   *
         MODESET EXTKEY=ZERO                                   @ZA02601
         L     RPARM,DXCCW10+FOUR       LOAD RCB ADDRESS         Y02027
         XC    NULL(TWO,RWTGC),NULL(RWTGC) CLEAR ID FOR THIS DCB Y02027
LOOP     EQU   *
         LA    RWTGC,EIGHT(RWTGC)       BUMP TO NEXT ENTRY IN
*                                       WTG TABLE
         LA    RPARC,FOUR(RPARC)        BUMP TO NEXT ENTRY IN
*                                       DCB LIST
         CLC   NULL(TWO,RWTGC),TAMOID   ANOTHER TAM SAM DCB TO OPEN
         BE    START                    BRANCH IF YES TO RESTART
*                                       THIS EXECUTOR
         CLC   NULL(TWO,RWTGC),OSID     END OF WTG TABLE
         BNE   LOOP                     BRANCH IF NO
*
ABEXIT   EQU   *
         LR    RPARC,RPAR               RESET POINTER TO BEGINNING
*                                       OF DCB PARAMETER LIST
         LA    RWTGC,C32(RWTG)          RESET POINTER TO BEGINNING
*                                       OF WTG TABLE
LABEL    EQU   *
         CLI   ZERO(RWTGC),NULL         THIS DCB PROCESSED       S22024
         BNE   XCTLRTN                  YES, DO XCTL             S21903
         LA    RWTGC,EIGHT(RWTGC)       BUMP BY EIGHT            S22024
         LA    RPARC,FOUR(RPARC)        BUMP BY FOUR             S22024
         B     LABEL                    CHECK NEXT DCB           S22025
XCTLRTN  EQU   *
         ICM   RPARM,ALL,DXCCW10+FOUR   GET RCB ADDRESS        @ZA02601
         BZ    MODESET                  NO RCB TO DEALLOCATE   @ZA02601
         LA    RZERO,AIBKEY-IEDQAIB     RCB OFFSET IN AIB      @ZA02601
         SR    RPARM,RZERO              ADDRESS OF AIB         @ZA02601
         MVI   AIBSTATE-IEDQAIB(RPARM),AIBFREE FREE AIB        @ZA02601
         SR    RZERO,RZERO              CLEAR REG              @OZ11209
         ST    RZERO,DXCCW10+FOUR       CLEAR PTR TO AIB       @OZ11209
MODESET  EQU   *                                                 Y02027
         MODESET  EXTKEY=DATAMGT
         LA    RENTRY,DXCCW12           GET ADDRESS OF A WORKAREA
*                                       FOR USE BY XCTL SVC
         MVC   SIX(TWO,RWTG),NULL(RWTGC) MOVE EXECUTOR ID TO THE
*                                       PARAMETER LIST
         XCTL  EPLOC=(RWTG),SF=(E,(15))                          Y02027
         EJECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *CLUP21
*                                                                     *
* ERROR PROCESSING                                               CLUP21
*                                                                     *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *CLUP21
ABORT    EQU   *
         LA    RWORK,NOTCAM             TCAM NOT IN SYSTEM
         B     OUTERRX                  PREPARE TO ABORT
ABORT1   EQU   *
         LA    RWORK,DDNERR             INVALID QNAME
OUTERR   EQU   *
OUTERRX  EQU   *
         MODESET EXTKEY=ZERO            GET IN KEY ZERO        @ZA02619
         ST    RWORK,DXCCW10            PUT ERROR CODE IN OPEN WRKA
         MVC   NULL(2,RWTGC),OPERR      ID OF ERROR EXECUTOR     Y02027
         B     ABEXIT                   GO RESET POINTERS        S22025
ABORT2   EQU   *
         LA    RWORK,USERR              PROCESS ENTRY IN USE
         B     OUTERR                   PREPARE TO EXIT
ABORT3   EQU   *
         LA    RWORK,GMERR2             GETMAIN FAILURE IN APPL PROG
         B     OUTERR                   PREPARE TO EXIT
ABORT4   EQU   *                                                 Y02024
         LA    RWORK,GMERR1             GETMAIN FAILURE IN MCP   Y02024
         B     OUTERR                   PREPARE TO EXIT          Y02024
ABORT5   EQU   *                                               @ZA02601
         LA    RWORK,PCBERROR           PCB IN USE FOR ANOTHER @ZA02601
*                                       APPLICATION            @ZA02601
         B     OUTERR                   PREPARE TO EXIT        @ZA02601
ABORT6   EQU   *                                                 S22024
         LA    RWORK,SIX*FOUR           SECURED QUEUE CODE       S22024
         B     OUTERR                   PREPARE TO EXIT          S22024
ABORT7   EQU   *                                                 Y02027
         LA    RWORK,NOTAUTHC           NOT AUTH ERROR CODE      Y02027
         B     OUTERRX                  EXIT TO ERROR CODE       Y02027
NOTAUTHC EQU   X'1C'                    NOT AUTH  043-7  ABEND   Y02027
         EJECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *CLUP21
*                                                                CLUP21
* THIS SECTION OF CODE DEALS WITH THE MCP OPEN SUBTASK INTERFACE CLUP21
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *CLUP21
SVCTIME  EQU   *
**************************************************************   Y02027
* GETMAIN FOR LOCATE MODE WORK AREA.                             Y02027
**************************************************************   Y02027
         TM    DCBDSORG,PHYSSEQ         PHYSICAL SEQUENTIAL-     Y02027
         BNO   CKPTTST                  BRANCH NO.               Y02027
         TM    ZERO(RPENTRY),READFLG    TEST FOR BSAM            Y02027
         BO    CKPTTST                  BRANCH IF BSAM           Y02027
         TM    ZERO(RPENTRY),LOCMFLG    TEST FOR LOCATE MODE     Y02027
         BNO   CKPTTST                  BRANCH IF NOT LOCATE MODEY02027
         SR    RZERO,RZERO              CLEAR REGISTER           Y02027
         ICM   RZERO,THREE,DCBBLKSI     LOAD WORKAREA SIZE       Y02027
         LA    RPENTRY,SPREGUSR         SUBPOOL FOR WORKAREA     Y02027
         MODESET  EXTKEY=RBT234,WORKREG=1
         BAL   RETURN,BLDLIST           GET CORE FOR WK AREA     Y02027
         MODESET  EXTKEY=DATAMGT
         STCM  RPARM,AD,DEBLCMWA        MOVE WK AREA ADDR        Y02027
         MVC   DEBSOWA,DCBBLKSI         MOVE WK AREA SIZE        Y02027
CKPTTST  EQU   *                                                 Y02027
**************************************************************   Y02027
* GETMAIN FOR PECB.     **************************************   Y02027
         LA    RZERO,PECBSIZE           LOAD PECB SIZE           Y02027
         LA    RPENTRY,SPREGUSR         SUBPOOL FOR PECB         Y02027
         MODESET  EXTKEY=RBT234,WORKREG=1
         BAL   RETURN,BLDLIST           GET PECB CORE            Y02027
         MODESET  EXTKEY=DATAMGT
         ST    RPARM,DCBPECB            PTR TO PECB.             Y02027
         EJECT                                                   Y02027
**************************************************************   Y02027
         MODESET EXTKEY=ZERO                                   @ZA02601
         ICM   RPARM,ALL,DXCCW10+FOUR   GET RCB ADDRESS        @ZA02601
         BNZ   HAVERCB                  BYPASS RCB GETMAIN     @ZA02601
         LA    RPARM,TCXAIBC-IEDQTCX(RTCX) TCX AIB PTR         @ZA02601
AIBGET   EQU   *                                               @ZA02601
         ICM   RPARM,AD,AIBCHAIN-IEDQAIB(RPARM) ANY AIB'S AVAIL@ZA02601
         BNZ   GOTAIBS                  YES, GET ONE           @ZA02601
         LH    RZERO,TCXSZAIB-IEDQTCX(RTCX)   GET SIZE OF AIB  @OZ17630
         CH    RZERO,FOURK              SIZE GREATER THAN 4K   @OZ17630
         BH    GETCORE                  YES, BRANCH            @OZ17630
         LA    RZERO,HPAGE              2K VALUE               @ZA02601
         AR    RZERO,RZERO              DOUBLE TO PAGE SIZE    @ZA02601
GETCORE  EQU   *                                               @OZ17630
         USING PSA,ZERO                                        @ZA02601
         LR    RPENTRY,RPARC            SAVE REG               @ZA02601
         ICM   RDCB,AD,DEBTCBAD         TCB ADDRESS FOR GETMAIN@ZA02601
         L     RPARC,PSAAOLD            ASCB ADDR FOR GETMAIN  @ZA02601
*                                       GETMAIN CSA KEY6       @ZA02601
         MODESET   EXTKEY=TCAM          GET IN TCAM KEY        @ZA05047
         GETMAIN  RU,LV=(0),BNDRY=PAGE,SP=CSA                  @ZA05047
         MODESET   EXTKEY=ZERO          GO BACK TO KEY 0       @ZA05047
         LR    RPARC,RPENTRY            RESTORE REG            @ZA02601
         L     RDCB,NULL(RPARC)         RESTORE DCB ADDRESS    @ZA02601
         L     RCORE,FOUR(RWTGC)        RESTORE WORK AREA PTR  @ZA02601
         LH    RETURN,TCXSZAIB-IEDQTCX(RTCX) SIZE OF AIB       @ZA02601
         CH    RETURN,FOURK             SIZE GREATER THAN 4K   @OZ17630
         BNH   SMALLAIB                 NO, BRANCH             @OZ17630
         LR    RENTRY,RPARM             GET ADDR OF ONLY AIB   @OZ17630
         B     CLRBYTE                  BRANCH TO CLEAR BYTE   @OZ17630
SMALLAIB EQU   *                                               @OZ17630
         LA    RENTRY,HPAGE             2K VALUE               @ZA02601
         LA    RENTRY,HPAGE(RENTRY,RPARM) ADDR OF END OF AIBS  @ZA02601
         SR    RENTRY,RETURN            ADDR OF LAST AIB       @ZA02601
CLRBYTE  EQU   *                                               @OZ17630
         LA    RZERO,ZERO(RPARM)        CLEAR HI ORDER BYTE    @ZA02601
         B     BXLE                     BRANCH TO INITIALIZE   @ZA02601
CHAINAIB EQU   *                                               @ZA02601
         ST    RZERO,AIBCHAIN-ONE-IEDQAIB(RPARM) STORE AIB PTR @ZA02601
         LR    RPARM,RZERO              SAVE CURRENT PTR       @ZA02601
BXLE     EQU   *                                               @ZA02601
         BXLE  RZERO,RETURN,CHAINAIB    LOOP THRU NEW CORE     @ZA02601
*                                       TILL ALL AIBS LINKED   @ZA02601
         LR    RENTRY,RPARM             LAST AIB PTR           @ZA02601
         N     RPARM,CLRLOW             START OF GOTTEN CORE   @ZA02601
         L     RZERO,TCXAIBC-IEDQTCX(RTCX) GET CURRENT AIB PTR @ZA02601
AIBLOOP  EQU   *                                               @ZA02601
         ST    RZERO,AIBCHAIN-ONE-IEDQAIB(RENTRY) LINK AIBS    @ZA02601
         CS    RZERO,RPARM,TCXAIBC-IEDQTCX(RTCX) CHAIN AIBS    @ZA02601
         BNE   AIBLOOP                  RETRY IF CS FAILED     @ZA02601
GOTAIBS  EQU   *                                               @ZA02601
         TS    AIBSTATE-IEDQAIB(RPARM)  ALLOCATE AIB           @ZA02601
         BNZ   AIBGET                   IN USE, RETRY          @ZA02601
         MVC   AIBECBA-IEDQAIB(FOUR,RPARM),OPEN SET ID         @ZA02601
*                                       FOR  FAILURE           @ZA02601
         LA    RPARM,AIBKEY-IEDQAIB(RPARM) BUMP TO RCB         @ZA02601
         ST    RPARM,DXCCW10+FOUR       SAVE RCB ADDRESS       @ZA02601
HAVERCB  EQU   *                                                 Y02027
         XC    ZERO(RCBSIZE,RPARM),ZERO(RPARM) CLEAR RCB       @ZA02601
**************************************************************   Y02027
*****  INITIALIZE RCB WITH DATA REQ'D BY IEDQEU IN MCP MEMORY.   Y02027
**************************************************************   Y02027
         USING IEDQRECB,RPARM           ESTABLISH RCB BASE.      Y02027
         MVI   RECBKEY,PROCKEY          SET RCB KEY.             Y02027
         MVI   RECBPRI,PROCPRI          SET RCB PRIORITY.        Y02027
         MVC   RECBQCBA(3),TCXOCQ-IEDQTCX+ONE(RTCX) MOVE OPEN/   Y02027
*                                       CLOSE QCB ADDR TO RCB    Y02027
         MVC   RCBTCB+ONE(THREE),DEBTCBAD MOVE TCB ADDR TO RCB.  Y02027
         L     RENTRY,TIOTPTR(RTCB)     LOAD TIOT ADDR           Y02027
         MVC   RCBJOBNM,0(RENTRY)       MOVE JOBNAME TO RCB      Y02027
         MVC   RCBPECB,DCBPECB          MOVE PECB ADDR TO RCB.   Y02027
         MVC   RCBDSNM,JFCBQNAM         MOVE PROC. QUEUE NAME.   Y02027
         ST    RDEB,RCBDEBAD            STORE DEB ADDR IN RCB.   Y02027
         MVC   RCBDCBMA,DCBMACRF        MOVE MACRF TO RCB.       Y02027
         MVC   RCBDCBDS,DCBDSORG        MOVE DSORG TO RCB.       Y02027
         MVC   RCBDCBBU,DCBBUFL         MOVE BUF. LGTH TO RCB.   Y02027
         MVC   RCBDCBLR,DCBLRECL        MOVE LOG REC SIZE TO RCB.Y02027
         ICM   RENTRY,EIGHT,DCBOPTCD    PUT DCBOPTCD             Y02027
         ICM   RENTRY,FOUR,DCBRECFM     DCBRECFM                 Y02027
         ICM   RENTRY,TWO,DCBFLAG1      DCBFLAG1                 Y02027
         ST    RENTRY,RCBDCBOP          IN RCB FOR EU            Y02027
         USING PSA,ZERO                                          Y02027
         L     RENTRY,PSAAOLD           ADDR OF CURRENT ASCB     Y02027
         ST    RENTRY,RCBASCB           STORE ASCB IN RCB        Y02027
         LH    RENTRY,ASCBASID-ASCB(RENTRY) ADDR OF SPACE IDENT. Y02027
         ST    RENTRY,RCBASID           STORE ASID IN RCB        Y02027
*              PUT RCB ON ASYCHRONOUS READY QUEUE TO ACTIVATE    Y02027
*              IEDQEU SUBTASK IN MCP                             Y02027
         TM    TCXFLAG1-IEDQTCX(RTCX),TCXMCPCD TCAM TERMINATED @ZA02601
         BO    ABORT                    YES, REJECT OPEN       @ZA02601
         CLC   RCBASID,TCXASID-IEDQTCX(RTCX) MCP SAME ASID     @ZA02601
         BNE   MCPOK                    NO, BRANCH             @ZA02601
         L     RENTRY,TCXTCB-IEDQTCX(RTCX) MCP TCB ADDRESS     @ZA02601
         TM    TCBFLGS-IEDQTCB(RENTRY),TERM MCP TERMINATED     @ZA02601
         BM    ABORT                    YES, REJECT OPEN       @ZA02601
MCPOK    EQU   *                                               @ZA02601
         L     RENTRY,DCBPECB           GET ECB ADDRESS        @ZA02601
         XC    ZERO(FOUR,RENTRY),ZERO(RENTRY)  CLEAR ECB       @ZA02601
         L     RENTRY,TCXREADY-IEDQTCX(RTCX)  FIRST ELEMENT      Y02027
POSTLOOP STCM  RENTRY,AD,RECBLINK-IEDQRECB(RPARM) POINT NEW      Y02027
*                                       ELEMENT TO FIRST ELEMENT Y02027
         CS    RENTRY,RPARM,TCXREADY-IEDQTCX(RTCX) CURRENT       Y02027
*                                       ELEMENT UPDATED?         Y02027
         BNE   POSTLOOP                 BRANCH IF YES, TRY AGAIN Y02027
*                                       NO, ASYCN READY QUEUE    Y02027
*                                       UPDATED WITH ELEMENT     Y02027
*                                                                Y02027
*              POST MCP ECB COMPLETE THROUGH CROSS MEMORY POST   Y02027
*                                                                Y02027
         L     RENTRY,TCXAVT-IEDQTCX(RTCX)  GET AVT ADDRESS      Y02027
         LA    RENTRY,AVTOSECB-IEDQAVTD(RENTRY) MCP-ECB ADDRESS  Y02027
         ST    RENTRY,DXCCW4            STORE IN PARAMETER LIST  Y02027
         L     RENTRY,CVTPTR            ADDRESS OF CVT           Y02027
         LA    RENTRY,CVTBRET-CVT(RENTRY) ERROR ADDRESS          Y02027
         ST    RENTRY,DXCCW4+EIGHT      AND STORE IN LIST        Y02027
         MVC   DXCCW4+FOUR(4),TCXASCB-IEDQTCX(RTCX) STORE ADDRESSY02027
*                                       OF ASCB IN LIST          Y02027
         SR    RZERO,RZERO              ZERO COMPLETION CODE     Y02027
         LA    RPARM,DXCCW4             ADDRESS OF LIST          Y02027
         POST  ,(0),MF=(E,(1))
         L     RPARM,DCBPECB            LOAD ECB ADDR.           Y02027
         WAIT  ECB=RECBKEY              WAIT FOR EU.             Y02027
         CLI   THREE(RPARM),USERR       WAS PROC ENTRY IN USE    Y02027
         BE    ABORT2                   YES-ERROR.               Y02027
         CLI   THREE(RPARM),ALL         WAS PROC. ENTRY FND.     Y02027
         BE    ABORT1                   NO-ENTRY NOT FOUND.      Y02027
         CLI   THREE(RPARM),NOTCAM      TCAM ABENDED           @ZA02601
         BE    ABORT                    YES, REJECT OPEN       @ZA02601
         CLI   THREE(RPARM),PCBERROR    PCB INVAID             @ZA02601
         BE    ABORT5                   YES, TAKE ERROR EXIT   @ZA02601
         CLI   THREE(RPARM),ZERO        DID GETMAIN FAIL.        Y02027
         BNE    ABORT4                  YES GETMAIN FAILED.      Y02027
         L     RPARM,DXCCW10+FOUR       RESTORE RCB ADDRESS      Y02027
         MVC   DEBTAMOS,RCBDEBTA        MOVE TT OFFSET           Y02027
         MVC   DEBPEWA,RCBDEBPW         MOVE PEWA ADDR           Y02027
         MVC   DEBPCBAD(3),RCBDEBPC     MOVE PCB ADDRESS         Y02027
         CLI   DCBRECFM,NULL            WAS RECFM SPECIFIED.     Y02027
         BNE   RECFMOK                  NO-RECFM NOT SPECIFID.   Y02027
         MVI   DCBRECFM,UFLG            DEFAULT TO UNDEFINED.    Y02027
         OI    DEBPCBAD-ONE,FLGU        INDICATE NOT SPECIFIED.  Y02027
RECFMOK  EQU   *                                                 Y02027
         MVC   DEBPEBAD,RCBPEBAD        PUT PEB ADDR IN DEB      Y02027
         TM    RCBSQ,SECURE             SECURE QUEUE             Y02027
         BZ    MOVEDCB                  BRANCH IF NO             Y02027
         ST    RPARM,DXCCW2             SAVE RCB ADDR THRU       Y02027
*                                       GETMAIN                  Y02027
         LA    RZERO,AREASIZE           MSG SIZE + REPLY         Y02027
         LA    RPENTRY,SP250             SUBPOOL                 Y02027
         BAL   RETURN,BLDLIST           LINK TO GETMAIN RTN      Y02027
         LR    RPENTRY,RPARM            GET MSG AREA             Y02027
         USING XWORK,RPENTRY                                     Y02027
         MVC   MSG,REQMSG               INIT WORKAREA            Y02027
         L     RWORK,DXCCW2             RELOAD RCB ADDR          Y02027
         MVC   QUEOFF(EIGHT,RPENTRY),RCBDSNM-IEDQRECB(RWORK)     Y02027
*                                       MOVE QNAME TO MSG        Y02027
         L     RPARM,TCBTIO             TIOT ADDR                Y02027
         MVC   JOBOFF(EIGHT,RPENTRY),ZERO(RPARM) JOBNAME TO MSG  Y02027
REQREPLY EQU   *                                                 Y02027
         XC    TECB(EIGHT),TECB         CLEAR ECB + REPLY        Y02027
         WTOR    ,TRTNAREA,1,TECB,MF=(E,(RPENTRY))               Y02027
         WAIT  ECB=TECB                                          Y02027
         OI    TRTNAREA,BLANK           FOLD TO CAPS             Y02027
         LA    RETURN,MOVEDCB           SET RETURN ADDR        @ZA02619
         CLI   TRTNAREA,YES             OK TO OPEN               Y02027
         BE    FREECORE                 BRANCH IF YES            Y02027
         CLI   TRTNAREA,NO              REPLAY NO                Y02027
         BNE   REQREPLY                 NO, ASK AGAIN            Y02027
         DROP RPENTRY                                            Y02027
         LA    RETURN,ABORT6            RETURN ADDRESS IF NO OPENY02027
FREECORE EQU   *                                                 Y02027
         FREEMAIN  RC,LV=AREASIZE,A=(RPENTRY),SP=SP250           Y02027
         BR    RETURN                   RETURN TO CALLER       @ZA02619
MOVEDCB  EQU   *                        MOVE IF NO ERRORS        Y02027
         L     RDCBMOVE,DXUDCBAD        LOAD USER DCB ADDR       Y02027
         SR    RENTRY,RENTRY            ZERO REG                 Y02027
         IC    RENTRY,DXUDCBPL+ONE      LEN OF NULL PREFIX       Y02027
         LA    RDCBMOVE,ZERO(RENTRY,RDCBMOVE) PT PAST NULL PREFIXY02027
         AH    RDCB,DXUDCBPL            INCREMENT PAST PREFIX
         IC    RENTRY,DXUDCBML+ONE      LEN OF MOVED DCB         Y02027
         MODESET   KEYADDR=DXUKEY,WORKREG=2  GET INTO USER KEY
         BCTR  RENTRY,ZERO              DEC FOR MOVE             Y02027
         EX    RENTRY,DCBMOVE           MOVE DCB COPY TO USER DCBY02027
         SH    RDCB,DXUDCBPL           POINT TO PREFIX           Y02027
         B     EXITTST                  EXIT                     Y02027
DCBMOVE  MVC   ZERO(ZERO,RDCBMOVE),ZERO(RDCB)  MOVE DCB TO REAL  Y02027
         EJECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *CLUP21
*                                                                CLUP21
* LOAD THE SPECIFIED ACCESS METHOD MODULE                        CLUP21
*                                                                CLUP21
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *CLUP21
LOADER   EQU   *
         L     RPARM,CVTPTR             ADDRESS THE CVT          A47156
         L     RPARM,SVCDCB(RPARM)      POINT TO DCB FOR SVCLIB  A47156
         LA    RZERO,0(RWTG)            MODULE ADDRESS           A47156
         LOAD  EPLOC=(0),DCB=(1)        LOAD THE MODULE         SA57692
         BR    RETURN                   AND RETURN               A47156
         EJECT                                                   Y01004
*                                                                     *
*  E Q U A T E S   A N D   C O N S T A N T S                          *
*                                                                     *
         SPACE 2
OSID     DC    C'0S'                    ID OF OS OPEN EXECUTOR
MODULEID EQU   C'R'                     LOAD MODULE ID CONSTANT  Y02027
         DS    0F                       FULL WORD ALIGN.         Y02027
SPCSANFP EQU   241                      SUBPOOL 241.             Y02027
SPREGUSR EQU   251                      SUBPOOL 251.             Y02027
FREETYPE EQU   X'20'                    FREEMAIN INDICATOR       Y02027
SPDEB    EQU   230                      SUB POOL FOR DEB.        Y02027
REQMSG   WTOR  'IED1690 QNAME          SECURE, MAY          OPEN, REPLYC
               ''Y'' OR ''N''',MF=L,ROUTCDE=8,DESC=2
EREQMSG  DS    0F                                                Y02027
CONFOUR  DC    F'4'                     CONSTANT               @ZA00205
XTRADEB  EQU   24                       EXTRA CORE FOR DEB APP.  Y01948
INTVL    DS    0D                                              @SA69652
         DC    C'00000500'              5 SECOND DELAY         @SA69652
OPERR    DC    C'4B'                    OPEN ERROR HANDLER       A42380
FOURK    DC    H'4096'                  LENGTH OF A PAGE       @OZ17630
         EJECT
FOUR     EQU   4                        CONSTANT
NULL     EQU   0                        CONSTANT
ZERO     EQU   0                        OFFSET                   S22024
FIVE     EQU   5                        OFFSET                   S22024
SEVEN    EQU   7                        OFFSET                   S22024
NINE     EQU   9                        OFFSET                   S22024
TWELVE   EQU   12                       OFFSET                   S22024
SIXTEEN  EQU   16                       OFFSET                   S22024
TWENTY2  EQU   22                       OFFSET                   S22024
TWO      EQU   2                        CONSTANT
EIGHT    EQU   8                        CONSTANT
C32      EQU   32                       CONSTANT
SIX      EQU   6                        CONSTANT
CSA      EQU   231                      COMMON SUBPOOL         @ZA02601
HPAGE    EQU   2048                     HALF PAGE SIZE         @ZA02601
KEY6     EQU   6                        KEY 6                  @ZA02601
CLRLOW   DS    0F                       MASK USED TO           @ZA02601
         DC    X'FFFFF000'              CLEAR 3 LOW BYTES      @ZA02601
OPEN     DC    C'OPEN'                  OPEN ID                @ZA02601
LOCMFLG  EQU   X'08'                    LOCATE MODE INDICATOR.   Y02027
PHYSSEQ  EQU   X'40'                    PHYSICAL SEQUENTIAL.     Y02027
UFLG     EQU   X'C0'                    RECFM IS UNDEFINED.      Y02027
FLGU     EQU   X'80'                    RECFM NOT SPECIFIED.     Y02027
TIOTPTR  EQU   12                       TIOT PTR IN TCB          Y02027
THREE    EQU   3                        CONSTANT
ALL      EQU   15                       4-BYTE ICM MASK          X01004
GETREAD  EQU   C'G'                     GET/READ MODULE          A47156
PUTWRITE EQU   C'I'                     PUT/WRITE MODULE         A47156
CHECK    EQU   C'L'                     CHECK MODULE             A47156
POINTR   EQU   C'M'                     POINT MODULE             A47156
ONE      EQU   1                        TO INITIALIZE TRT        A47157
SVCDCB   EQU   84                       SVCLIB DCB OFFSET        A47156
SP250    EQU   250                      SUBPOOL 250 ID           CLUP21
AD       EQU   7                        MASK TO ICM ADDRESSES    Y01004
TEN      EQU   10                       CONSTANT                 Y02027
TRTLEN   EQU   256                      TRT TABLE SIZE           A47157
DEBID    EQU   X'0F'                    DEB IDENTIFIER           CLUP21
GETTYPE  EQU   X'20'                    TO INDICATE GETMAIN      CLUP21
DEBIDI   EQU   X'C0'                    TAM DEB ID FOR GET/READ
POINTFLG EQU   X'04'                    DCBMACR FLAG(POINT)
AFLAG    EQU   X'8C'                    POST FLAG FOR 102        Y02027
DEBIDO   EQU   X'CF'                    TCAM DEB ID FOR PUT/WRITECLUP21
*                                       WORKAREA
READFLG  EQU   X'20'                    BSAM MACRF
WRITEFLG EQU   READFLG                  DCBMACR BSAM MASK
PROCKEY  EQU   X'08'                    KEY AND PRIORITY FIELDS
PROCPRI  EQU   X'F3'                    OPEN ELEMT PRIORITY    @SA70001
DDNERR   EQU   8                        INVALID QNAME
NOTCAM   EQU   4                        TCAM NOT IN SYSTEM
USERR    EQU   12                       PROCESS ENTRY IN USE     A44886
GMERR1   EQU   16                       GETMAIN FAILURE - MCP    A44886
GMERR2   EQU   20                       GETMAIN FAILURE - APPL PROG
PCBERROR EQU   X'20'                    PCB IN USE BY ANOTHER  @ZA02601
*                                       APPL PROG   043-8      @ZA02601
TERM     EQU   X'C0'                    TERNINATION FLAGS      @ZA02601
QUEOFF   EQU   26                       OFFSET INTO MSG OF QNAME Y02027
JOBOFF   EQU   47                       OFFSET OF JOBNAME        Y02027
NO       EQU   C'N'                     REPLY FOR NO             Y02027
YES      EQU   C'Y'                     REPLY FOR YES            Y02027
SECURE   EQU   1                        SECURE QUEUES            Y02027
BLANK    EQU   C' '                     TO FOLD TO CAPS          Y02027
AREASIZE EQU   EREQMSG-REQMSG+EIGHT     SIZE OF WORKAREA         Y02027
         EJECT
*                 *
*   D S E C T S   *
*                 *
XWORK    DSECT                                                   Y02027
MSG      DS    CL(EREQMSG-REQMSG)       MSG SPACE                Y02027
TECB     DS    F                        WTOR ECB                 Y02027
TRTNAREA DS    F                        REPLY AREA               Y02027
         TRECBD                                                  Y02027
*****************************************************************Y02027
*****    RCB EXTENSION FOR TCAM APPLICATION OPEN/CLOSE.          Y02027
*****************************************************************Y02027
RCBPECB  DS    F                        APPLIC. PGM PECB ADDR.   Y02027
RCBASCB  DS    F                        APPLIC. PGM ASCB ADDR.   Y02027
RCBERRET DS    0F                       ERROR RTN ADDR           Y02027
RCBASID  DS    F                        APPLIC. PGM ASID ADDR.   Y02027
RCBTCB   DS    F                        APPLIC. PGM TCB ADDR.    Y02027
RCBDCBMA DS    H                        DCBMACRF FROM APPLIC.    Y02027
RCBTCBFL DS    0X                       APPLIC. TCB FLAGS.       Y02027
RCBDCBDS DS    H                        DCBDSORG FROM APPLIC.    Y02027
RCBDSNM  DS    CL8                      PROCESS QUEUE NAME.      Y02027
RCBDEBAD DS    F                        DEB ADDR FOR APPLIC. DCB.Y02027
RCBDCBBU DS    H                        DCBBUFL FROM APPLIC.     Y02027
RCBDCBLR DS    H                        DCBLRECL FROM APPLIC.    Y02027
RCBDCBOP DS    X                        DCBOPTCD FROM APPLIC.    Y02027
RCBDCBRF DS    X                        DCBRECFM FROM APPLICATIONY02027
RCBDCBF1 DS    X                        DCBFLAG1 FROM APPLICATIONY02027
RCBERR   DS    X                        ERROR INDICATOR          Y02027
RCBJOBNM DS    CL8                      JOBNAME                  Y02027
RCBSAVE  DS    18F                      SAVE AREA FOR IEDQEU   @ZA02601
RCBEND   DS    0X                       END OF DSECT.            Y02027
         ORG   RCBTCB
RCBDEBTA DS    H                        OFFSET IN T.T. FOR ENTRY.Y02027
RCBOPECB DS    0AL3                     OPCTL ECB ADDRESS        Y02027
RCBDEBPW DS    AL3                      ADDR. OF PEWA.           Y02027
RCBDEBPC DS    AL3                      ADDR. OF PCB.            Y02027
         DS    2F                       QNAME                    Y02027
RCBSQ    DS    0X                       SECURE QUEUE FLAG        Y02027
RCBAVTBT DS    X                        AVTBIT1 ON RETURN FROM EUY02027
RCBPEBAD DS    AL3                      READ-AHEAD QCB ADDR.     Y02027
RCBSIZE  EQU   RCBEND-RECBKEY           SIZE OF DSECT.           Y02027
         SPACE 2
         DCBD DSORG=PS                  DATA CONTROL BLOCK
         ORG   DCBIOBAD                                          Y02027
DCBPECB  DS    A                        ADDR OF PECB             Y02027
         IHAPSA                                                  Y02027
         IHAASCB
         IHAASVT                                                 Y02027
CVT      DSECT                                                   Y02027
         CVT                                                     Y02027
         IEZJSCB                                                 Y02027
         TPECBD                                                  Y02027
         IHARB
         TTCXD                                                   Y02027
         TAIBD                                                 @ZA02601
         TAVTD                          ADDRESS VECTOR TABLE
         TTCBD                          TASK CONTROL BLOCK
FORCORE  DSECT                          OPEN WORKAREA
         IECDSECT
         EJECT
         TDEBAPD
         END
