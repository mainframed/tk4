204H     TITLE 'IGE0204H DATA CHECK, OVERRUN, COMMAND REJECTRD/WR'
IGE0204H CSECT
         SPACE 3
*CHANGE-ACTIVITY = AS FOLLOWS:
******************** MICROFICHE FLAGS *********************** SUPT CODE
*
*A024200-025000,492000,656000                                    S22025
*C764000-766000                                                  S22025
*D018000-020000,028000-032000,818000                             S22025
*A188600-189200,390600-391200,814400-815600                      X01004
*C610000,618000                                                 SA65419
*C390000,552000-555600                                           X01004
*A492000,656000
*D552400-553600,554800-555600,814400                             X02004
*C552000-554000                                                  X02004
*C220000,A804000                                               @SA74968
*A388600                                                       @ZA05034
*C244000                                                       @XA11313
*C658000-664000,A814000,A836000@OY10984
*C244000-245600                                                @OY12439
*A312000                                                       @OS76716
*C663800,D658600-663600,814040-814080,836500                   @OY14057
*C019000,504000                                                @G36XREV
*A626000,628000                                                @OY19601
*                                                                     *
***********************************************************************
*                                                                     *
*MODULE-NAME = IGE0204H                                               *
*                                                                     *
*DESCRIPTIVE-NAME = ERP FOR BSC DATA CHECK, OVERRUN, COMMAND REJECT   *
*                   ON READ AND WRITE CCW'S                           *
*                                                                     *
*COPYRIGHTS = 'NONE'                                                  *
*                                                                     *
*STATUS: CHANGE LEVEL 10                                       @G36XREV
*                                                                     *
*FUNCTION -- TO ATTEMPT RETRY ON FAILING CCW SEQUENCE AND TO     S22025
*   TAKE INTERMEDIATE ERP ACTION IF REQUIRED.  ALL ERP MODULES   S22025
*   ATTEMPT TO RECOVER FROM ALL ERRORS.  CERTAIN TEXT ERRORS     S22025
*   REQUIRE RETURN TO LINE END TO SCHEDULE A SUBSEQUENT RECALL   S22025
*   OPERATION                                                    S22025
*                                                                     *
*ENTRY POINTS -- FIRST EXECUTABLE INSTRUCTION.                        *
*                                                                     *
*                                                                     *
*INPUT --                                                             *
*   R1 - POINTER TO I/O SUPERVISOR BLOCK                         Y02027
*   R15 - ENTRY POINT ADDRESS                                         *
*                                                                     *
*OUTPUT -- R1 POINTS TO I/O SUPERVISOR BLOCK                     Y02027
*                                                                     *
*EXTERNAL ROUTINE -- IGG019QE (AOS/TCAM SIO APPENDAGE) -- TO CONVERT  *
*   VIRTUAL CCW ADDRESSES TO REAL BEFORE RETRY.                       *
*                                                                     *
*EXITS-NORMAL -- R1 POINTS TO I/O SUPERVISOR BLOCK               Y02027
*        SVC   15   'ERP IN CONTROL (IOSERR)' AND 'EXCEPTIONAL   Y02027
*                CONDITION(IOSEX)'. RETURN TO IOS TO RETRY THE   Y02027
*                   ERROR.                                       Y02027
*        SVC   3                                                      *
*                                                                     *
*        SVC   15   'NO ERROR FLAGS'. RETURN TO LINE END         Y02027
*                   APPENDAGE. ERROR CLEARED                     Y02027
*        SVC   3                                                      *
*                                                                     *
*   TO SCHEDULE NEXT LOAD OF ERP                                      *
*        L     13,DECIMAL VALUE OF NEXT LOAD                          *
*        L   14,X'10'    CVT ADDRESS                                  *
*        L     14,X'2C'(14)    ADDRESS OF XCTL ROUTINE                *
*        BR    14   EXIT TO XCTL                                      *
*                                                                     *
*EXITS-ERROR -- R1 POINTS TO I/O SUPERVISOR BLOCK                Y02027
*        SVC   15   'EXCEPTIONAL CONDITION(IOSEX)'. RETURN TO    Y02027
*                   LINE END APPENDAGE. ERROR PERMANENT.         Y02027
*        SVC   3                                                      *
*                                                                     *
*        L     13,DECIMAL VALUE OF NEXT LOAD                          *
*        L   14,X'10'    CVT ADDRESS                                  *
*        L     14,X'2C'(14)    ADDRESS OF XCTL ROUTINE                *
*        BR    14   EXIT TO XCTL                                      *
*                                                                     *
*   TO SCHEDULE ERROR POST                                            *
*                                                                     *
*TABLES/WORK AREAS --                                                 *
*   TAVTD                                                             *
*   TTRMD                                                             *
*   TCCWD                                                             *
*   TLCBD                                                             *
*   DCBD                                                              *
*   TSCBD                                                             *
*   IECDIOSB                                                     Y02027
*                                                                     *
*ATTRIBUTES -- SERIALLY REUSABLE, REFRESHABLE, SUPERVISOR KEY,   Y02027
*              SUPERVISOR MODE, ENABLED                          Y02027
*                                                                     *
*NOTES -- THE OPERATION OF THIS MODULE DOES NOT DEPEND UPON A         *
*   PARTICUALR INTERNAL REPRESENTATION OF THE EXTERNAL CHARACTER      *
*   SET.                                                              *
*                                                                     *
*   R1 REMAINS TRANSPARENT TO ALL LOADS OF ERP.                       *
*   IF INTERMEDIATE ERP ACTION IS REQUIRED, THE SEQUENCE IS BUILT     *
*   IN LCBERCCW AND A RETURN INDICATOR IS SET IN LCBINCAM+1 FOR       *
*   IGE0404H.                                                         *
*                                                                     *
***********************************************************************
         EJECT
         USING IEDQCCW,RCCW
         USING IEDQLCB,RLCB
         USING IEDQSCB,RSCB
         USING IHADCB,RDCB
         USING IEDQAVTD,RAVT
         USING IOSB,RIOSB                                        Y02027
         USING *,R15
R0       EQU   0
RIOSB    EQU   1                        ADDR OF I/O SUPVR BLOCK  Y02027
RLCB     EQU   2                        LCB CBASE
RCCW     EQU   5                        CCW BASE
RWKD     EQU   3                        WORK REGISTER
RDCB     EQU   4                        DCB BASE
RWKA     EQU   6                        WORK REGISTER
RWKB     EQU   7                        WORK REGISTER
RTERM    EQU   8                        TERMINAL BASE
RWKC     EQU   9                        WORK REGISTER
RTST     EQU   9                        ADDR OF REQST QUEUE ELEM Y02027
RSCB     EQU   10                       SCB BASE
RAVT     EQU   11                       AAVT BASE
R2       EQU   12                       WORK REGISTER
RLNK     EQU   13                       WORK AND LINAGE REGISTER
RSAVE    EQU   13                       SAVE REGISTER FOR IOSB   Y02027
RXCTL    EQU   14                       XCTL REGISTER
R15      EQU   15                       ADDRESSABILITY REGISTER
         EJECT
IGE0204H  IEDHJN  ENTER                                          X01004
         L     RTST,IOSUSE              PICKUP ADDR OF RQE       Y02027
         SPACE
         L     RLCB,IOBOFFST(,RTST)     ADDRESS OF IOB Z(LCB)
         SH    RLCB,LCBBKUP             SET BASE
         L     RDCB,LCBDCBPT            SET DCB BASE
         L     RSCB,LCBSCBA-1           CURRENT SCB
         L     RCCW,LCBCSW-1       FAILING CCW+8
         SH    RCCW,H8             BACK UP TO FAILING CCW
         CLI   CCWOPCDE,CCWTIC          TIC (CHANNEL PROGAM CHECK)
         BNE   NOPC                     NO
         SH    RCCW,H8                  BACK U TO TEXT CCW
         MVI   LCBCSW+6,0               CLEAR RESIDUAL COUNT
NOPC     EQU   *
         L     RAVT,CVTPTR              CVT ADDRESS
         L     RAVT,AVTCVTPT(,RAVT)     AVT LOAD LIST
         L     RAVT,0(,RAVT)            AVT BASE SET
         LR    RLNK,RCCW                ASSUME RESTART ON FAILING
         TM    LCBSENS0,DATCHK+TMOUT    DATA CHK OR TIMEOUT    @SA74968
         BZ    ITSOVRN                  NO, IT'SOVERRUN
         SPACE
         CLI   CCWOPCDE,CCWWRITE        WRITE COMMAND
         BE    SNO                      YES, SNO ERROR
         CLI   LCBRESTR,TPRDLC          READ LC OUT
         BE    DCTXT                    YES
         SPACE
         CLI   LCBRESTR,TPTEXT          TEXT ERROR
         BNE   NODCTXT                  NO
DCTXT    EQU   *
         CLI   LCBINCAM,RETRY           RETRY EXHAUSTED
         BNL   PERMEXIT                 YES, PERM ERROR        @OY12439
         TM    LCBCSW+3,UNEX            UNIT EXCEPTION
         BNZ   NORETRY                  YES, RETRY NOT POSSIBLE
         USING IEDQPRF,RWKA
         LA    RCCW,0(,RCCW)            THIS THE INTERRUPT
         L     RWKB,LCBCPA+12           POSSIBLE RESTART TEXT CCW
         L     RWKA,LCBLSPCI-1          BUFFER ADDRESS
         TM    PRFSTAT1,PRFNHDRN        IS IT A HEADER
         BO    NOTFIRST                 BRANCH IF NO
         SPACE
         CLC   LCBLSPCI(3),LCBCPA+13    WORKING ON FIRST UNIT
         BNE   NOTFIRST                 NO, ALREADY RCV'D A BLOCK
         SPACE
         TM    PRFFLAGS,CCWSLI          WAS CONTINUE OP DONE ON1ST
*                                         UNIT OF BUFFER
         BO    BLDBUFF                  BRANCH IF NO
*        I/O GENERATOR  TURNS OF SLI ON CONTINUE OPERATIONS
NOTFIRST EQU   *
         SPACE
         LA    RWKB,AVTEZERO(,RWKB)     CLEAR HI ORDER BYTE
LOOPBUFF EQU   *
         LA    RWKA,AVTEZERO(,RWKA)     "LEAR HI ORDER BYTE
*              RWKB POINTS TO FIRST CCW IN CHAIN AFTER LAST EOB
*        RESTART.  IT MIGHT HAVE BEEN FRRED BY PCI.  A TEST IS ALSO
*        MADE TO DETERMINE IF IT HAS BEEN FREED AND RE-ASSIGNED.
         CLR   RWKB,RWKA                LAST EOB RESTART THIS BUFFER
         BE    TESTWRAP                 BRANCH IF YES, SEE IF IT
*                                         HAS BEEN RE-ASSIGNED
         SPACE
         CLR   RWKA,RCCW                END OF SEARCH (RESTART CCW
*                                         NO LONGER IN BUFFER CHAIN
         BE    CHKRETRY                 BRANCH IF YES, CHECK RETRIES
         SPACE
         L     RWKA,PRFTIC              GET NEXT UNIT (BUFFER)
         B     LOOPBUFF                 TEST NEX CCW IN CHAIN
         SPACE 2
TESTWRAP EQU   *
*              THE FOLLOWING TEST CHECK SIF RESTART CCW HAS BEEN
*        RE-ASSIGNED.
         TM    CCWFLAGS-CCW(RWKB),CCWSLI I/O GENERATOR TURNS OFF SLI
*                                         ON A RESTART OR RECALL
         BO    CHKRETRY                 BRANCH IF BUFFER HAS BEEN
*                                       RE-USED. A RE-CALL IS NEEDED
         SPACE
         B     WRNAK                    ATTEMPT RETRY
         SPACE 3
BLDBUFF  EQU   *
         SPACE
*        HEADER BUFFER MUST BE RECONSTRUCTEDTO ACCOUNT
*  FOR POLLING ,IDLES, AND LCIN,
         SPACE
         SR    RWKC,RWKC                CLEAR WORK REGISTER
         STH   RWKC,PRFSIZE             RESET PRFSIZE          @OS76716
         IC    RWKC,LCBISZE             IDLES
         LA    RWKC,PRFSHDR-PRFSUNIT(,RWKC)
         LH    RWKB,AVTKEYLE            GET UNIT SIZE
         CH    RWKB,DCBBUFSI            COMPARE WITH BUFFER SIZEE
         BNH   SETSIZE                  BRANCH LESS OR EQUAL
         LH    RWKB,DCBBUFSI            GET BUFFER SIZE
SETSIZE  EQU   *
         SR    RWKB,RWKC                SET COUNT
         STH   RWKB,PRFCOUNT
         LA    RWKC,PRFSUNIT-IEDQPRF(RWKC,RWKA)
         IC    R0,PRFIOADR-1            SAVE OP CODE
         ST    RWKC,PRFIOADR-1          SET ADDRESS
         STC   R0,PRFIOADR-1            SET  OP CODE
         LR    RWKB,RWKA                GET BUFFER ADDRESS
WRNAK    EQU   *
         SR    RWKC,RWKC                CLEAR WORK REGISTER
         L     R2,DCBSCTAD-1            SCT ADDRESS
         IC    RWKC,NAKCH(,R2)          GET SEQUENCE
         LA    RWKC,1(RWKC,R2)          GET NAK ADDRESS
         ST    RWKC,LCBCPA+16           SET WRITE NAK ADDDRESS
         MVI   LCBCPA+16,CCWWRITE       SET WRITE COMMAND
         MVI   LCBCPA+20,CCWCC+CCWSLI   SET FLGAS
         MVI   LCBCPA+23,1              SET COUNT
         LA    RWKA,LCBCPA+24           ASSUME NO LC OUT
         TM    SCBBSCFM,TRANSP          TRANSPARENT MODE
         BZ    NOTRANP                  NO
         SPACE
         MVI   LCBCPA+31,2              SET COUNT
LCOUT    EQU   *
         MVI   LCBCPA+28,CCWCD+CCWSLI
         LA    RWKC,LCBERCCW+12         DATA ADDRESS
         ST    RWKC,LCBCPA+24           SET IN CCCW
         MVI   LCBTPCD+3,TPRDLC         SET TP OP CODE
         MVI   LCBCPA+24,CCWREAD        SET COMMAND CODE
         LA    RWKA,8(,RWKA)            BUMP TO NXT CCW
         B     BUFFEXIT                 EXIT
         SPACE
NOTRANP  EQU   *
         TM    SCBBSCFM,NONTR           NONTRANSPARENT SET
         BZ    BUFFEXIT                 BR NO,NOT SET YET      @ZA05034
         L     RTST,IOSUSE              PICKUP RQE ADDR          Y02027
         L     RWKC,DEBPTR-1(RTST)      GET DEB ADDR FROM RQE    X01004
         LA    RWKC,Z(RWKC)             CLEAR HIGH-ORDER BYTE    Y02027
         SH    RWKC,APPENSZ             POINT TO APPENDAGE TABLE Y02027
*                                       PREFIXING THE DEB        Y02027
         TM    0(RWKC),LCIN             LCIN SPECIFIED           X01004
         BO    BUFFEXIT                 YES
         SPACE
         MVI   LCBCPA+31,1              SET COUNT
         B     LCOUT                    LCOUT
         SPACE
BUFFEXIT EQU   *
         NI    LCBMSGFM,ACKI+NAK+OLT    RESET SCAN BITS
         ST    RWKB,0(,RWKA)            SET IN TIC ADDRESS
         NI    CCWFLAGS-CCW(RWKB),AVTEFF-CCWSLI SET WRAP SWITCH
         MVI   0(RWKA),CCWTIC           SET TIC COMMAND CODE
         LA    RLNK,LCBCPA+16           FOR RESTART
         B     SETCOUNT                 BUMP RETRY OCOUT AND EXIT
NORETRY  EQU   *
         OI    LCBCHAIN,LCBNORTY        SET NO RETRY POSSIBLE
         B     PERMEXIT                 PERM ERROR
         EJECT
TXTERR   EQU   *
CHKRETRY EQU   *
         CLI   LCBINCAM,RETRY           RETRY EXHAUSTED
         BE    PERMEXIT                 YES
         NI    IOSFLA,AVTEFF-IOSERR     POST PERMANENT ERROR     Y02027
*                                       TO ABNORMAL END APPENDAG Y02027
         B     CONVERT                  BRANCH TO SET UP FOR RETRY.
NODCTXT  EQU   *
         CLI   LCBRESTR,TPRDENQ         READ ENQ
         BE    TRYRETRY                 YES, F ATTEMPT RETRY
         SPACE
         CLI   LCBRESTR,TPRDIDNQ        READ ID ENQ
         BE    TRYRETRY                 RETRY READ
         SPACE
         CLI   LCBRESTR,TPRDIDAK        READ ID ACK
         BNE   TRYADDR                  NO, TRY ADDRESSING A ERROR
         SPACE
DIALTEST EQU   *
         LA    RLNK,LCBCPA+16           ASSUME RESTART POINT
         TM    LCBCPA+24,X'20'          DIAL SEQUENCE
         BZ    TRYRETRY                 NO, RETRY
         LA    RLNK,24(,RLNK)           BUMP PAST DIAL SEQUENCE
         B     TRYRETRY                 ATTEMPT RETRY
         SPACE 2
TRYADDR  EQU   *
         CLI   LCBRESTR,TPRDRSPD        ADDRESSING RESPOSNE
         BNE   TRYRSPQ                  NO, TRY RSP ENQ
         SPACE
ADDERR   EQU   *
         LH    RWKC,LCBTTCIN            CURRENTLY CONNECTED
         LTR   RWKC,RWKC                IS THE SOURCE ON A DIAL  S22025
*                                         LINE SET               S22025
         BZ    RESET                    NO, RESET THE LINE AND   S22025
*                                         TREAT AS A PERM ERROR  S22025
         BAL   RWKD,FINDTERM            GET TERMINAL ADDRESS
         USING IEDQTRM,RTERM
         SR    RWKB,RWKB                CLEAR FOR INDEX
         IC    RWKB,TRMCHCIN            INDEX TO CHARACTERISTICS
         BCTR  RWKB,0                   DECREMENT FOR INDEX
         MH    RWKB,AVTDCTLN            MULTIPLY BY DCT LENGTH @G36XREV
         AL    RWKB,AVTCSTCS            CHARCTERISTICS BASE
         TM    2(RWKB),X'10'            MULTIPOINT
         BO    DIALTEST                 RESTART FROM TOP
         SPACE
         TM    3(RWKB),X'20'            END-END CONTROL
         BNZ   WRENQ                    TREAT SAME AS TEXT RESPONSE
BACKUP   EQU   *
         SPACE
         SH    RLNK,H8                  BACK UP TO WRITE ENQ
         B     TRYRETRY                 ATTEMPT RETRY
TRYRSPQ  EQU   *
         CLI   LCBRESTR,TPRSPENQ        RESPONSE TO ENQ
         BE    BACKUP                   BRANCH YES
         SPACE
         CLI   LCBRESTR,TPRDRESP        POLLING RESPONSE
         BE    DIALTEST                 YES, RESTART AT TOP
         CLI   LCBRESTR,TPRDRPEB        TEXT RESPONE
         BNE   SNO                      NO
WRENQ    EQU   *
         LA    RWKC,ENQCH               INDEX PARAMETER
         BAL   RWKD,CCWBLD              LINK TO ROUTINE
         MVI   LCBINCAM+1,DCRET         SET 404H RETURN
         MVI   LCBERCCW+4,CCWCC+CCWSLI  SET FLAGS
         ST    RCCW,LCBERCCW+8          SET TIC TO FAILING CCW
         MVI   LCBERCCW+8,CCWTIC        SET TIC COMMAND CODE
         B     COMEXIT                  USE COMMON EXIT
         SPACE
ITSOVRN  EQU   *
         TM    LCBSENS0,OVERRUN         OVERRUN SET
         BZ    ITSCMDRJ                 NO, ITS COMMAND REJECT
         CLI   CCWOPCDE,CCWWRITE        WIRTE COMMAND
         BE    SNO                      SHOULD NOT OCCUR
         SPACE
         CLI   LCBRESTR,TPRDRPEB        RESPONSE TO ETB
         BE    WRENQ                    YES
         SPACE
         CLI   LCBRESTR,TPRDENQ         READ ENQ
         BE    TRYRETRY                 YES, RESTRY POSSIBLE
         SPACE
         CLI   LCBRESTR,TPRDIDNQ        READ ID ENQ
         BE    TRYRETRY                 YES
         SPACE
         CLI   LCBRESTR,TPRDRSPD        RESPONSE TO ADDRESSING/ENQ
         BE    ADDERR                   YES, RECOVERY SAME AS DC
         SPACE
         CLI   LCBRESTR,TPRDIDAK        READ ID AK
         BE    DIALTEST                 YES
         SPACE
         CLI   LCBRESTR,TPRSPENQ        RESPONSE TO ENQ
         BE    DIALTEST                 YES
         SPACE
         CLI   LCBRESTR,TPRDLC          READ LC OUT
         BE    DCTXT                    BR YES, WRITE NAK       SA65419
         CLI   LCBRESTR,TPRDRESP        POLLING RESPONSE
         BE    DIALTEST                 YES, RESTART AT TOP
         CLI   LCBRESTR,TPTEXT          TEXT ERROR
         BE    DCTXT                    BR YES, WRITE NAK       SA65419
         SPACE
         B     SNO                      'SHOULD NOT OCCUR'
         SPACE 5
ITSCMDRJ EQU   *
         CLI   LCBRESTR,TPOPEN          OPCON WRITE DLEEOT     @OY19601
         BE    OPCONDE                  BRANCH YES             @OY19601
         CLI   LCBRESTR,TPWRDLET        WRITE DLE EOT
         BNE   SDDLEENQ                 NO, WRITE DLE ENQ
OPCONDE  EQU   *                                               @OY19601
         LA    RLNK,8(,RLNK)            RESTART ON DISABLE
         B     TRYRETRY                 ATTEMPT ETRY
SDDLEENQ EQU   *
         LA    RWKC,DLEENQCH            INDEX PARAMETER
         BAL   RWKD,CCWBLD              BUILD WR DLE ENQ
         MVI   LCBERCCW+4,0             NO FLAGS
         MVI   LCBINCAM+1,DLENQRET      SET 404H RETURN
         B     COMEXIT                  USE COMMON EXIT
         SPACE 5
TXTRTRY  EQU   *
         CLI   LCBINCAM,RETRY           RETRY LIMIT REACHED
         BL    TRYRETRY                 NO
         SPACE
RESET    EQU   *                                                 S22025
         MVI   LCBINCAM+ONE,TXTRESET    404H RETURN            @OY10984
         SR    RWKC,RWKC                SET EOT OFFSET FOR SCT @OY10984
         STH   RWKC,LCBERCCW+4          CLEAR CHAIN FLAGS      @OY10984
         BAL   RWKD,CCWBLD              BUILD WRITE EOT        @OY14057
         B     COMEXIT                  COMMON CODE
         SPACE 3
TRYRETRY EQU   *
         CLI   LCBINCAM,RETRY           CHECK RETRY COUNT
         BE    PERMEXIT                 LIMTI REACHED
SETCOUNT EQU   *
         IC    RWKB,LCBINCAM            RETRY COUNT
         LA    RWKB,1(,RWKB)            BUMP
         STC   RWKB,LCBINCAM            SET BACK
COMEXIT  EQU   *
         ST    RLNK,LCBSTART-1          SET STARTING CCW ADDRESS
         MVI   LCBSENS0,AVTEZERO        CLEAR SENSE FOR ETRY
***********************************************************************
*                                                                     *
*        THE FOLLOWING CODE IS ADDED FOR AOS.                         *
*                                                                     *
CONVERT  EQU   *
         CLI   LCBECBCC,HIOCC           IOHALT ISSUED            Y02027
         BNE   SVCS                     BRANCH NO                Y02027
         SPACE 1                                                 Y02027
         NI    IOSFLA,X'FF'-(IOSERR+IOSEX) RESET ERP IN CTL      Y02027
         MVI   LCBINCAM+1,HIOCC         SET FOR LINEEND          Y02027
SVCS     EQU   *                                                 Y02027
**************************************************************** Y02027
*        THE FOLLOWING CODE MAPS THE NECESSARY FIELDS FROM THE   Y02027
*        LINE CONTROL BLOCK TO THE I/O SUPERVISOR BLOCK FOR EXCP Y02027
*        COMPATABILITY DURING RETRY OPERATIONS FROM ERP          Y02027
**************************************************************** Y02027
         MVC   IOSCSW(SEVEN),LCBCSW     MAP CSW FROM LCB TO IOSB Y02027
         MVC   IOSSNS(TWO),LCBSENS0     MAP SENSE INFO FROM LCB  Y02027
*                                       INTO IOSB                Y02027
         MVC   IOSCC(ONE),LCBSIOCC      MAP START I/O CONDITION  Y02027
*                                       CODE FROM LCB TO IOSB    Y02027
         TM    IOSFLA,IOSERR+IOSEX      ARE WE DOING A RETRY     Y02027
         BNO   NOTRETRY                 BRANCH IF NO.            X01004
         LR    RSAVE,RIOSB              SAVE IOSB ADDRESS        Y02027
         L     RTST,IOSUSE              PICKUP ADDR OF RQE       Y02027
         SR    RLCB,RLCB                CLEAR FOR ICM.
         ICM   RLCB,7,IOBPTR(RTST)      GET IOB ADDRESS FROM RQE,
*                                       FOR USE BY SIO APPENDAGE.
         SR    RTERM,RTERM              CLEAR FOR ICM.
         ICM   RTERM,7,DEBPTR(RTST)     GET DEB ADDRESS FROM RQE.
         LA    R15,DEBNMSUB-IEDQDEB     GET APDG. TABLE SIZE     Y02027
         SLR   RTERM,R15                POINT TO APDG. TABLE     Y02027
         L     RTERM,SIOPTR(Z,RTERM)    GET ADDRESS OF SIO APDG. X01004
*                                       FROM APDG. VECTOR TABLE.
         SR    R15,R15                  CLEAR R15 TO TELL SIO APDG.
*                                       IT'S A SUBROUTINE.
         BAL   RXCTL,SIOENTRY(Z,RTERM)  LINK TO SIO APPENDAGE    X01004
*                                       AT OFFSET                X01004
*                                       +16 TO CONVERT CCW'S FROM
*                                       VIRTUAL TO REAL.
         LR    RIOSB,RSAVE              RESTORE IOSB ADDR        Y02027
*                                                                     *
*        END OF CODE ADDED FOR AOS.                                   *
*                                                                     *
***********************************************************************
NOTRETRY EQU   *                                                 X01004
         SVC   EREXCP
         SPACE
         SVC   RETURN
         SPACE
PERMEXIT EQU   *
         MVI   LCBINCAM+1,PERMRET       SET RETURN INDICATOR
         B     LOADMOD                  GO TO 504H
SNO      EQU   *
         MVI   LCBINCAM+1,SNORET        SET RETURN INDICATOR FOR
*                                         504H
LOADMOD  EQU   *
         LH    RLNK,POSTMOD            MODULE ID
         L     RXCTL,CVTPTR             GET CVT ADDRESS
         L     RXCTL,XCTLADD(,RXCTL)    SCTL ROUTINE ADDRESS
         BR    RXCTL                    LINK TO POSTMODULE
         SPACE
CCWBLD   EQU   *
         MVC   LCBCSWSV,LCBCSW          SAVE CURRENT STATUS
         MVC   LCBSNSV,LCBSENS0         SAVE CURRENT SENSE
         L     RWKA,DCBSCTAD-1          C SCT ADDRESS
         SR    RWKB,RWKB                CLEAR INDEX REGISER
         IC    RWKB,0(RWKC,RWKA)        POINT TO SEQUENCE
         IC    RWKC,0(RWKB,RWKA)        GET COUNT BYTE
         STH   RWKC,LCBERCCW+6          SET COUNT
         LA    RWKA,1(RWKB,RWKA)        POINT TO SEQUENCE
         ST    RWKA,LCBERCCW            SET IN CCW
         MVI   LCBERCCW,CCWWRITE        SET COMMAND CODE
         LA    RLNK,LCBERCCW            FOR EXIT CODE
         BR    RWKD                     RETURN
         SPACE 3
         SPACE 2
FINDTERM EQU   *
         L     RWKB,AVTRNMPT            ADDRESS OF CODE BASE
         USING IEDQTNTD,RWKB
         BAL   RWKA,TNTDCODE            LINK TO IT
         BR    RWKD                     RETURN
         SPACE 2
H8       DC    H'8'                     USED TO ADJUST CCW PTR   S22025
LCBBKUP  DC    AL2(LCBFLAG1-IEDQLCB)    ADJUSTMENT TO BACK UP TO S22025
*                                       BEGINNING OF LCB         S22025
POSTMOD  DC    AL2(5048)                POST MODULE ID
         DS    0H                                                Y02027
APPENSZ  DC    AL2(DEBNMSUB-IEDQDEB)    APPENDAGE TABLE SIZE     Y02027
RETRY    EQU   6                        RETRY COUNT
CVTPTR   EQU   X'10'                    POINTER TO CVT
XCTLADD  EQU   X'2C'                    OFFSET IN CVT-XCTL ROUTINE
IOBOFFST EQU   4                        IOB OFFSET IN RQE
OVERRUN  EQU   X'04'                    OVERRUN MASK
ERPCTL   EQU   X'20'                    ERP IN CONTRO BIT
ENQCH    EQU   7                        INDEX INTO SCT
NAKCH    EQU   6                        INDEX TO SEQUENCE
DLEENQCH EQU   17                       INDEX TO SEQUENCE
PERMRET  EQU   8                        504H TETURN
EREXCP   EQU   15                       ERROR EXCP
DCRET    EQU   4                        404H RETURN
ONE      EQU   1                                                 Y02027
TWO      EQU   2                                                 Y02027
SEVEN    EQU   7                                                 Y02027
HIOCC    EQU   X'48'                    HALT I/O COMPLETION      Y02027
RETURN   EQU   3                        MASK FOR RETURN SVC
UNEX     EQU   X'01'                    UNIT E CEPTION MASK
ACKI     EQU   X'40'                    ACK COUNTER MASK
NAK      EQU   X'80'                    NAK MASK
OLT      EQU   X'01'                    ONL LINE TEST MASK
DATCHK   EQU   X'08'                    DATA CHECK MASK
TMOUT    EQU   X'01'                    TIME OUT SENSE         @SA74968
RCVPCI   EQU   X'22'                    PCI RECEIE OPTIONS
SNORET   EQU   4                        504H RETURN
TXTRESET EQU   40                       404H RETURN INDICATOR
DLENQRET EQU   28                       404H RETURN
NONTR    EQU   X'40'                    SCB FLAG FOR RECEIPT OF BSC
TRANSP   EQU   X'80'                    TRANSPARENT MODE RECEIVE
SIOPTR   EQU   4                        OFFSET OF SIO APDG. ADDRESS
*                                       IN APDG. VECTOR TABLE.
SIOENTRY EQU   34                       OFFSET OF SUBRTNE. ENTRY Y02027
*                                       POINT IN SIO APPENDAGE.
RETRYFLG EQU   X'24'                    RETRY FLAG IN LCBFLAG1.  X01004
Z        EQU   0                                                 X01004
IOBPTR   EQU   5                        OFFSET OF IOB ADDR. IN RQE
DEBPTR   EQU   9                        OFFSET OF DEB ADDR. IN RQE.
LCIN     EQU   X'80'                    LCIN SPECIFIED           X01004
         EJECT
         TDEBD                          DEB DSECT
         EJECT
         TCCWD
         TTNTD
         TAVTD
         DCBD  DSORG=TX
         TLCBD
         TSCBD
         TTRMD
         TTPD
         TPRFD
         IECDIOSB                                                Y02027
         END
