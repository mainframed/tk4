19RI    TITLE '''IGG019RI'' - TAM PUT/WRITE ROUTINE'
IGG019RI CSECT
*C358600,384000                                                 SA52960
*C812800                                                        SA52955
*C920000                                                        SA52483
*C183000                                                         A48275
*A820000,947900                                                  A48275
*A640000,994000                                                  A44891
*A069000-069500,077000,112600-113300,115000,124200-125700,       Y01004
*A178200-179800,                                                 Y01004
*A180200-180800,284050-284450,340000,578005-578300,830100-831600,Y01004
*A834200-835400,836200-837800,903000,904600-905800               Y01004
*A906200-908600,958500-959500                                    Y01004
*C068000,178000,180000,278000,336000,524000-536000,546000,578000,Y01004
*C904000,906000,964000                                           Y01004
*D142000,182000-183500,202000,270000-272000,280000,338000,342000,Y01004
*D382000-488000,508000-510000,538000-540000,548000-550000,       Y01004
*D554000-556000,630000-652000,775600,874000,884000,890000,       Y01004
*D908000,950000,986000                                           Y01004
*A303400-303600                                                  A50196
*C359200,578105                                                  Y01948
*D966000                                                         Y01948
*A303200                                                         A50196
*A178000-182800,546000-552000                                    S22025
*D142000,178000-183500,202000,530000,524000-528000               S22025
*D532000-540000,546000-554000                                    S22025
*C068000,378000,486000,520000,578000,606000,616000,628000,652000 S22025
*C672000,678000,842000-856000                                    S22025
*D775600,948000                                                  S22025
*C014000-042000,068000,069200,074000-088000,110000,146000,150000 Y02027
*C158000,174000,178200,194000,244000,252000,262000,268000,278000 Y02027
*C284030,318000,322000,332000,360000,378000-686000,770000-817000 Y02027
*C870000-880000,947930-951530,962000,988000                      Y02027
*A245000,253000,264000,318100-319200,857000,963000               Y02027
*D094000-096000,100000,104000-106000-112000-112600,124400-125000 Y02027
*D148000,152000,160000-162000,178400-179800,190000,196000-204000 Y02027
*D302000-312000,324000-358000,334000-358000,362000,688000-756000 Y02027
*D818000-837200,842000-850000,864000-866000,886000,894000        Y02027
*D898000-908000,918000,924000,928000-930000.934000-942000        Y02027
*D946000-947000,947600-947800,994000-995000                      Y02027
*A820000,821000,828000                                          SA56213
*A778000                                                        SA69644
*C178200-180200,208000-230000                                  @ZA00205
*A855000                                                       @ZA00205
*C771000                                                       @XA06333
*A777000                                                       @XA06333
*C797000                                                       @SA70205
*C414000,*                                                     @ZA01132
*A896000                                                       @ZA01132
*A498000,506000                                                @ZA03092
*A390000,785000,856000,951530                                  @YA08121
*A366000,374000                                                @ZA04032
*C494000-506000                                                @ZA06186
*C367000,375000                                                @OY11959
*C526000-534000                         PECB DSECT CHG         @OZ07830
*C782600                                                       @OX17151
*C245000,253000                                                @OZ26386
* DUMMY APAR                                                   @OZ27328
         SPACE 4
***********************************************************************
*                                                                     *
*TITLE: 'IGG019RI' SAM COMPATIBLE PUT/WRITE ROUTINE                   *
*FUNCTION: THIS ROUTINE SETS A PARAMETER LIST FOR THE PUT-MCP         *
*   ROUTINE. FOR LOCATE MODE, THE WORKAREA ADDRESS IS RETURNED TO     *
*   THE USER.  THE WORKAREA ADDRESSED PASSED BY THE USER IS VALIDITY  *
*   CHECKED.  IF THE DESCRIPTOR BYTE IS SPECIFIED, THE VALIDITY OF    *
*   SEQUENCE IS EXAMINED. SVC 102 IS ISSUED TO ACTIVATE MCP SUBTASK.  *
*   UPON RETURN, RETURN OR COMPLETION CODES ARE SET. IF A CHECKPOINT  *
*   HAS BEEN TAKEN TAKEN IN THE MCP SINCE THE LAST PUT OR WRITE AND   *
*   THE USER HAS SPECIFIED AN EXLST ENTRY OFFSET, THE USER'S  CHECK-  *
*   POINT EXIT IS GIVEN CONTROL. IF SYNAO IS SPECIFIED, USERS ROUTINE *
*   IS GIVEN CONTROL ON AN ERROR.                                     *
*ENTRY POINT:                                                         *
*        IGG019RI                                                     *
*INPUT:                                                               *
*   0  - ADDRESS OF WORKAREA(ONLY FOR QSAM NOT IN LOCATE MODE)        *
*   1  - DCB ADDRESS(QSAM) OR DECB ADDRESS(BSAM)                      *
*   13 - REGISTER SAVEAREA ADDRESS                                    *
*   14 - RETURN ADDRESS                                               *
*   15 - ENTRY POINT ADDRESS                                          *
*OUTPUT:                                                              *
*   1  - WORKAREA ADDRESS(PUT IN LOCATE MODE ONLY)                    *
*   15 - RETURN CODE                                                  *
*EXTERNAL ROUTINES:                                                   *
*        IEDQEB - AQCTL (SVC 102) ROUTINE                        Y01004
*        USER'S CHECKPOINT EXIT ROUTINE, IF SPECIFIED                 *
*EXITS-NORMAL:                                                        *
*        RETURNS TO CALLER WITH RETURN CODE OF 0 IN REG 15(QSAM)      *
*         OR DECB COMPLETION CODE OF X'7F' IF BSAM                    *
*EXITS-ERROR:                                                         *
*         RETURN CODE OF X'80' FOR TCAM NOT ACTIVE                    *
*         RETURN CODE OF X'90' FOR INVALID SVC 102                    *
*        USER'S SYNAD EXIT ROUTINE, IF SPECIFIED                 Y01004
*        RETURNS TO CALLER WITH NON-ZERO RETURN CODE IN REG 15(QSAM)  *
*         X'04'- CLOSE IN PROGRESS                                    *
*        X'0C'- INVALID DESTINATION TERMNAME                          *
*        X'08'- WORK UNIT SEQUENCE ERROR                              *
*         X'10'- THRESHOLD REACHED, PUT IN PROGRESS, WORKAREA TOO     *
*         BIG FOR AIB                                                 *
*         RETURNS TO CALLER WITH DECB COMPLETION CODE                 *
*         X'52'- WORKAREA TOO BIG FOR AIB                             *
*         X'54'- INVALID DESTINATION TERMNAME                         *
*         X'58'- WORK UNIT SEQUENCE ERROR                             *
*         X'5C'- THRESHOLD                                            *
*         X'5E'- CLOSE IN PROGRESS                                    *
*        X'48'- WORK UNIT SEQUENCE ERROR                              *
*TABLES/WORKAREAS:                                                    *
*        DCB                                                          *
*        DECB                                                         *
*        DEB                                                          *
*         PECB                                                        *
*                                                                Y01004
*ATTRIBUTES:REENTRANT, REFRESHABLE, PROBLEM PROGRAM MODE              *
*                                                                Y01004
*NOTES: THE OPERATION OF THIS MODULE DEPENDS UPON AN INTERNAL         *
*   REPRESENTATION OF THE EXTERNAL CHARACTER SET WHICH IS EQUIVALENT  *
*   TO THE ONE USED AT ASSEMBLY TIME.  THE CODING HAS BEEN ARRANGED   *
*   SO THAT REDEFINITION OF 'CHARACTER' CONSTANTS, BY REASSEMBLY,     *
*   WILL RESULT IN A CORRECT MODULE FOR THE NEW DEFINITIONS.          *
*                                                                Y01004
*                                                                Y01004
***********************************************************************
         EJECT
         SPACE 2
* S Y M B O L I C   R E G I S T E R S
         SPACE
RZERO    EQU   0                        GENERAL WORK/BCTR REGISTER
RPARM    EQU   1                        PARAMETER REGISTER
RTEMP    EQU   2                        GENERAL WORK REGISTER
RWORK    EQU   3                        GENERAL WORK REGISTER
RPECB    EQU   4                        PECB BASE REGISTER       Y02027
RSTORE   EQU   5
RWORKA   EQU   6                        WORKAREA ADDRESS REGISTER
RDECB    EQU   7                        DECB BASE REGISTER(BSAM)
RCOUNT   EQU   9                        COUNTER                  Y02027
RDCB     EQU   10                       DCB BASE REGISTER
RDEB     EQU   11                       DEB BASE REGISTER
RBASE    EQU   12                       PUT/WRITE BASE REGISTER
RSAVE    EQU   13                       SAVEAREA ADDRESS REGISTER
RETURN   EQU   14                       RETURN ADDRESS REGISTER
RENTRY   EQU   15                       ENTRY POINT ADDR REGISTER
*                                                                Y02027
*        ADDRESSABILITY FOR CSECT                                Y02027
*                                                                Y02027
         SPACE 2
         USING *,RENTRY                 ADDRESSABILITY.          Y01004
*        ENTRY IS AT OFFSET 0 FOR BSAM DCB AND +4 FOR QSAM DCB @ZA00205
*        DCB+31 IS FILLED IN BY IGG01946 TO REFLECT THE CORRECT@ZA00205
*        ENTRY POINT.                                          @ZA00205
         B     BSAM                     BSAM ENTRY PT          @ZA00205
         DROP  RENTRY                                          @ZA00205
         USING *,RENTRY                                        @ZA00205
IGG019RI IEDHJN QSAM                    QSAM ENTRY PT          @ZA00205
         STM   RETURN,RBASE,DEST(RSAVE)  SAVE REGS             @ZA00205
         L     RBASE,ENTRYPT            GET CORRECT BASE       @ZA00205
         DROP  RENTRY                   DISCARD OLD BASE.        Y01004
         USING IGG019RI,RBASE           CSECT ADDRESSABILITY.    Y01004
         EJECT
         USING IHADCB,RDCB              DCB ADDRESSABILITY
         USING IEDQDEB,RDEB             DEB ADDRESSABILITY
         USING IHADECB,RDECB            DECB ADDRESSABILITY
         USING IEDQPECB,RPECB           PECB ADDRESSABILITY      Y02027
         SPACE 2
         LR    RDCB,RPARM               ADDR OF DCB            @ZA00205
         LR    RWORKA,RZERO             ADDR OF PUT WORKAREA   @ZA00205
         B     LOCMTST                  CONT QSAM PROCESS      @ZA00205
         DROP  RBASE                                           @ZA00205
         USING IGG019RI,RENTRY          BASE                   @ZA00205
BSAM     EQU   *                                               @ZA00205
         STM   RETURN,RBASE,DEST(RSAVE)   SAVE REGS            @ZA00205
         L     RBASE,ENTRYPT            GET CORRECT BASE       @ZA00205
         DROP  RENTRY                                          @ZA00205
         USING IGG019RI,RBASE                                  @ZA00205
         LR    RDECB,RPARM              ADDR OF DECB
         XC    DECSDECB(4),DECSDECB     CLEAR ECB TO ZERO
         MVI   DECSDECB,POSTER          SET NORMAL COMPLETION CODE
         L     RWORKA,DECAREA           ADDRESS OF WRITE WORKAREA
         L     RDCB,DECDCBAD            ADDR OF DCB FROM DECB
         L     RDEB,DCBDEBAD            ADDRESS OF DEB
         L     RPECB,DCBPECB            ADDRESS OF PECB          Y02027
         MVI   PECBERR,ZERO             INIT TO GOOD RETURN    @OZ26386
         B     START                    SKIP QSAM OVERHEAD
LOCMTST  EQU   *
         L     RDEB,DCBDEBAD            ADDRESS OF DEB
         L     RPECB,DCBPECB            ADDRESS OF PECB          Y02027
         MVI   PECBERR,ZERO             INIT TO GOOD RETURN    @OZ26386
         TM    DCBMACRF+1,LOCMFLG       PUT IN LOCATE MODE
         BZ    START                    BRANCH IF NO
         L     RWORKA,DEBLCMWA-1        GET ADDR OF GETMAIN WORK-      X
                                        AREA FROM DEB
         TM    PECBPUTF,FIRSTPUT        FIRST PUT ISSUED SINCE   Y02027
*                                       OPEN                     Y02027
         BO    START                    BRANCH IF NOT FIRST TIME
         OI    PECBPUTF,FIRSTPUT        TURN ON FIRST TIME SWITCHY02027
         MVC   25(3,RSAVE),DEBLCMWA     MOVE ADDR OF WORKAREA TO       X
                                        USER'S SAVEAREA
         B     GOODOUT                  BR TO SET RETURN CODE    Y02027
         EJECT
START    EQU   *
         LR    RTEMP,RWORKA             SAVE ADDRESS OF START    Y02027
*                                       OF WORKAREA              Y02027
         LH    RWORK,DCBLRECL           GET WORK UNIT SIZE FROM  Y02027
*                                       DCB                      Y02027
*                                       OF WORKAREA
         TM    DCBRECFM,UNDFLG          UNDEFINED RECORD FORMAT
         BNO   NOTUNDEF                 BRANCH IF NO             Y02027
         TM    DCBMACRF+ONE,BSAMFLG     WRITE OPERATION          Y02027
         BZ    SZETST                   BRANCH IF NO - SIZE OK   Y02027
         TM    DECTYPE,S                IS 'S' CODED FOR LENGTH  Y02027
         BO    SZETST                   BRANCH IF YES - SIZE OK  Y02027
         LH    RWORK,DECLNGTH           GET SIZE FOR UNDEFINED   Y02027
*                                       BSAM RECORD FROM DECB    Y02027
SZETST   EQU   *                                                 Y02027
         N     RWORK,HIOFF              CLEAR HI-ORDER HALFWORD  Y02027
         LTR   RWORK,RWORK              ZERO LENGTH WORK UNIT    Y02027
         BZ    GOODOUT                  BRANCH IF YES            Y02027
         B     OPTCDTST                 CHECK TYPE               Y02027
NOTUNDEF EQU   *                                                 Y02027
         TM    DCBRECFM,FIXFLG          FIXED FORMAT SPECIFIED
         BO    SZETST                   BRANCH IF YES            Y02027
*                                       VARIABLE FORMAT          Y02027
         CLC   0(2,RWORKA),HIOFF        ZERO LENGTH WORK UNIT   SA52960
         BE    GOODOUT                  BRANCH IF YES.           Y01948
         ICM   RWORK,THREE,ZERO(RWORKA) INITIALIZE COUNTER       Y02027
         N     RWORK,HIOFF              CLEAR HIGH-ORDER HALF
         SH    RWORK,PRFSZE             DECREMENT CTR BY PRFLEN
         BM    ABORT4                   BRANCH IF NEGATIVE     @OY11959
         LA    RWORKA,PRFLEN(0,RWORKA)  ADDR OF BYTE FOLLOWING PRF
         TM    DCBMACRF+1,BSAMFLG       BSAM REQUEST
        BZ     OPTCDTST                 BRANCH IF NO
         TM    DCBRECFM,BLKFLG          BLOCKED PREFIX AS WELL
         BNO   OPTCDTST                 BRANCH IF NO
         SH    RWORK,PRFSZE             DECREMENT CTR BY PRFLEN
         BM    ABORT4                   BRANCH IF NEGATIVE     @OY11959
         LA    RWORKA,PRFLEN(0,RWORKA)  ADDR OF BYTE FOLLOWING PRF
*                                                                Y02027
*        CHECK FOR VALIDITY OF SEQUENCE IF DESCRIPTOR BYTE SPEC'DY02027
*                                                                Y02027
OPTCDTST EQU   *                                                 Y02027
         NC    DCBBLKSI,DCBBLKSI        BLOCK SIZE SPECIFIED   @YA08121
         BZ    NOBLKSI                  BRANCH IF NO           @YA08121
         CH    RWORK,DCBBLKSI           INVALID WORK UNIT LNGTH@YA08121
         BH    ABORT4                   ABORT IF YES           @YA08121
         CLC   DCBBLKSI,MAX             LENGTH TOO MUCH        @YA08121
         BH    ABORT4                   ABORT IF YES           @YA08121
NOBLKSI  EQU   *                                               @YA08121
         TM    DCBOPTCD,CTLBYTE         WORKAREA CONTENTS        Y02027
*                                       DESCRIPTOR BYTE PROVIDED Y02027
         BZ    VALIDITY                 BRANCH IF NO             Y02027
         MVC   PECBFLAG(1),ZERO(RWORKA) MOVE CONTROL BYTE TO     Y02027
*                                       TEMPORARY STORAGE        Y02027
         NI    PECBFLAG,XFF-ENDOR       CLEAR END-OF RECORD    @ZA01132
         CLI   PECBFLAG,HDRFLG          HEADER SEGMENT           Y02027
         BE    VALIDITY                 BRANCH IF YES            Y02027
         CLI   PECBFLAG,EOMFLAG         END OF MESSAGE           Y02027
         BE    VALIDITY                 BRANCH IF YES            Y02027
         CLI   PECBFLAG,MESSAGE         ENTIRE MESSAGE           Y02027
         BE    VALIDITY                 BRANCH IF YES            Y02027
         CLI   PECBFLAG,ISEGFLAG        INTERMEDIATE SEGMENT     Y02027
         BE    VALIDITY                 BRANCH IF YES            Y02027
         CLI   PECBFLAG,NEWINIT         EOR INT SEGMENT          Y02027
         BE    OK                       BRANCH IF YES            Y02027
         MVI   PECBERR,SEQERR           SET SEQUENCE ERROR       Y02027
         B     GOODOUT                  BR TO SET RETURN CODE    Y02027
OK       EQU   *                                                 Y02027
         MVI   PECBFLAG,ISEGFLAG        OLD INT SEGMENT          Y02027
*                                                                Y02027
*              VALIDITY CHECK WORKAREA ADDRESS PASSED BY USER    Y02027
*              BY DOING FETCHES AND STORES IN FIRST & LAST BYTES Y02027
*                                                                Y02027
VALIDITY EQU   *                                                 Y02027
         IC    RSTORE,ZERO(RWORKA)      FETCH START OF WORKAREA@ZA06186
         BCTR  RWORK,RZERO              BACK UP TO LAST BYTE   @ZA03092
*                                        IN USER'S WORKAREA    @ZA03092
         IC    RSTORE,ZERO(RWORK,RWORKA)  FETCH END OF WORKAREA@ZA06186
         LA    RWORK,ONE(,RWORK)        RESET WORKAREA LENGTH  @ZA03092
*                                                                Y02027
*              SET UP AQCTL PARAMETER LIST                       Y02027
*                                                                Y02027
         LA    RSTORE,PECBECB           GET ECB ADDRESS AND      Y02027
         ST    RSTORE,PECBPARM          STORE INTO PARAMETER LISTY02027
         MVI   PECBPARM,PUTCODE         SET PUT CODE IN PARM LISTY02027
         LA    RPARM,PECBPARM           SET UP PARAMETER REGISTERY02027
         AQCTL                          ISSUE SVC 102 TO PUT MSG Y02027
         EJECT
*                                                                Y02027
*        CONTROL RETURNED FROM PUTWRITE FUNCTION IN IEDQEB       Y02027
*                                                                Y02027
*              CHECK FOR SVC 102 ERRORS                          Y02027
*                                                                Y02027
         LA    RCOUNT,EIGHTY            LOAD X'80'               Y02027
         CR    RENTRY,RCOUNT            CHECK FOR RETURN OF X'80'Y02027
*                                       OR X'90' -INVALID SVC 102Y02027
         BNL   EXIT                     BRANCH IF YES            Y02027
*                                                                Y02027
*              CHECK FOR CHECK POINT EXIT NEEDED                 Y02027
*                                                                Y02027
         CLI   PECBFLAG,CKPTFLG         CHECK FOR EXIT NEEDED    Y02027
         BNE   GOODOUT                  BRANCH IF NO             Y02027
         ICM   RENTRY,SIX,DCBEXLST      CK FOR EXIT LIST SPEC    Y02027
         BZ    GOODOUT                  BR IF NO                 Y02027
         L     RENTRY,DCBEXLST          EXIT LIST ADDRESS        Y02027
CKLOOP   EQU   *                                                 Y02027
         TM    ZERO(RENTRY),CKPTFLG     THIS ENTRY FOR CHECKPOINTY02027
         BO    CKEXIT                   BRANCH IF YES            Y02027
         LA    RENTRY,FOUR(RENTRY)      BUMP TO NEXT ENTRY       Y02027
         B     CKLOOP                   TEST NEXT ENTRY          Y02027
CKEXIT   EQU   *                                                 Y02027
         ST    RDECB,PECBSAVA           SAVE DECB ADDRESS        Y02027
         L     RENTRY,ZERO(RENTRY)      GET ROUTINE ADDRESS      Y02027
         LR    RPARM,RDCB               DCB ADDRESS              Y02027
         LM    RTEMP,RBASE,REG2(RSAVE)  RESTORE USERS REGISTERS  Y02027
         L     RZERO,REG0(RSAVE)        RESTORE REG 0            Y02027
         BALR  RETURN,RENTRY            BRANCH TO CHECK POINT    Y02027
*                                       ROUTINE                  Y02027
         LR    RDCB,RPARM               GET DCB ADDRESS          Y02027
         L     RPECB,DCBPECB            GET PECB ADDRESS         Y02027
         L     RDECB,PECBSAVA           RESTORE DECB ADDRESS     Y02027
         L     RDEB,DCBDEBAD            RESTORE DEB ADDRESS      Y02027
         EJECT
*                                                                Y02027
*              SET RETURN CODE OR COMPLETION CODE                Y02027
*                                                                Y02027
GOODOUT  EQU   *
         SR    RENTRY,RENTRY            SET NORMAL COMPLETION CODE
         TM    DCBMACRF+1,LOCMFLG       LOCATE MODE
         BNO   CLMCP                    BRANCH IF NO
         MVC   25(3,RSAVE),DEBLCMWA     LOCATE MODE AREA ADDR
CLMCP    EQU   *
         TM    DCBMACRF+ONE,BSAMFLG     WRITE REQUEST            Y02027
         BZ    SETRET                   BR NO                  @XA06333
         MVC   DECSDECB(1),PECBERR      MOVE COMPLETION CODE TO  Y02027
*                                       DECB                     Y02027
         CLI   DECSDECB,ZERO            GOOD RETURN              Y02027
         BNE   SETRET                   BRANCH IF NO             Y02027
         MVI   DECSDECB,POSTER          SET NORMAL COMPLETION C. Y02027
        B      EXIT                     RETURN                   Y02027
SETRET   EQU   *                                                 Y02027
         CLI   PECBERR,ZERO             ANY ERRORS               Y02027
         BE    EXIT                     BRANCH IF NO             Y02027
         CLI   PECBERR,NOBUF            THRESH OR PUT IN PROGRESSY02027
         BE    RETURN10                 BRANCH IF YES            Y02027
         CLI   PECBERR,SOWACC           WORKAREA TOO BIG FOR AIB Y02027
         BNE   CONTCK                   BRANCH IF NO             Y02027
         LA    RENTRY,FOURTEEN          SET RETURN CODE 14       Y02027
         MVI   REG1(RSAVE),LENECC       INVALID LEN FOR SYNAD  @OX17151
         B     CKSYNAD                  CHECK FOR SYNAD        @OX17151
RETURN10 EQU   *                                                 Y02027
         LA    RENTRY,TEN               SET RETURN CODE X'10'    Y02027
         B     EXIT                     RETURN                   Y02027
ABORT4   EQU   *                                               @YA08121
         MVI   PECBERR,LENECC           ERROR COMPLETION CODE  @YA08121
         B     CLMCP                                           @YA08121
CONTCK   EQU   *                        SYNAD ERRORS             Y02027
         CLI   PECBERR,CLOSECC          QUICK CLOSE              Y02027
         LA    RENTRY,CLOSERR           QUICK CLOSE RETURN CODE  Y02027
         BE    CKSYNAD                  BRANCH IF YES            Y02027
         LA    RENTRY,DEST              DESTINATION ERROR RET.C. Y02027
         MVI   REG1(RSAVE),PWERR        INVALID DEST FOR SYNAD   Y02027
         CLI   PECBERR,DESTERR          INVALID DESTINATION      Y02027
         BE    CKSYNAD                  BRANCH IF YES            Y02027
         CLI   PECBERR,SEQERR           SEQUENCE ERROR           Y02027
         BNE   EXIT                     BRANCH IF NO - SNO       Y02027
         LA    RENTRY,SEQ               SEQ. ERROR RETURN CODE   Y02027
         XC    PECBSTAT(14),PECBSTAT    CLEAR FIELD            @SA70205
         OI    PECBSTAT+TWO,EOL         SEQUENE ERROR FLAG IN    Y02027
*                                       STATUS INDICATORS        Y02027
         MVI   REG1(RSAVE),SEQERR       SEQUENCE ERROR FOR SYNAD Y02027
CKSYNAD  EQU   *                                                 Y02027
         TM    DCBMACRF+ONE,BSAMFLG     WRITE REQUEST            Y02027
         BO    EXIT                     BRANCH IF YES- NO SYNAD  Y02027
         TM    DCBSYNAD+THREE,NOEXIT    SYNAD EXIT SPECIFIED     Y02027
         BO    EXIT                     BRANCH IF NO             Y02027
*                                                                Y02027
*              TAKE  SYNAD EXIT                                  Y02027
*                                                                Y02027
         MVC   REG15(4,RSAVE),DCBSYNAD  PUT ADDRESS OF SYNAD     Y02027
         LA    RCOUNT,PECBSTAT          GET ADDR OF STATUS INFO  Y02027
         ST    RCOUNT,REG0(RSAVE)       STORE PTR TO ADDR OF     Y02027
*                                       STATUS INDICATORS        Y02027
         STCM  RDCB,SEVEN,REG1+ONE(RSAVE) STORE DCB ADDR IN REG1 Y02027
         LM    RETURN,RBASE,REG14(RSAVE) RESTORE REGISTERS       Y02027
         BR    RENTRY                   BRANCH TO USERS ROUTINE  Y02027
EXIT     EQU   *                                                 Y02027
         RETURN (14,12),T,RC=(15)
         EJECT
* R E A D - O N L Y   C O N S T A N T S
         SPACE 2
PRFSZE   DC    H'4'                     SIZE OF WORKAREA PREFIX  S22025
         DS    0F
ENTRYPT  DC    A(IGG019RI)                                     @ZA00205
HIOFF    DC    X'0000FFFF'              MASK TO CLEAR HI 2 BYTES S22025
MAX      DC    H'32760'                 MAX WORK UNIT LENGTH   @YA08121
PATCH    DC    25H'0'                   PATCH AREA               Y02027
         EJECT
* S Y M B O L I C   E Q U A T E S
         SPACE 2
PRFLEN   EQU   4
COMPLETE EQU   X'40'                    POSTED COMPLETE FLAG     Y02027
PUTCODE  EQU   X'94'                    PUT FUNCTION FOR SVC 102 Y02027
EIGHTY   EQU   X'80'                    SVC 102 RETURN CODE      Y02027
FOUR     EQU   4                        DISPLACEMENT             Y02027
TEN      EQU   X'10'                    RETURN CODE X'10'        Y02027
FOURTEEN EQU   X'14'                    RETURN CODE 14           Y02027
SOWACC   EQU   X'52'                    INVALID WORKAREA SIZE    Y02027
S        EQU   X'80'
LOCMFLG  EQU   X'08'                    LOCATE MODE FLAG
POSTER   EQU   X'7F'                    WORK UNIT IN WORKAREA
CKPTFLG  EQU   X'0F'                    CHECKPOINT EXIT LIST OFFSET
ENDOR    EQU   X'04'                    END-OF-RECORD          @ZA01132
         EJECT
DESTERR  EQU   X'54'
SEQ      EQU   8
DEST     EQU   12
NOEXIT   EQU   X'01'
PWERR    EQU   X'48'                    INVALID DEST FLAGS      SA52483
SEQERR   EQU   X'58'
BSAMFLG  EQU   X'20'                    DCBMACRF FLAG FOR WRITE
EOL      EQU   X'80'
CLOSERR  EQU   4
NOBUF    EQU   X'5C'
CLOSECC  EQU   X'5E'
XFF      EQU   X'FF'                    MASK TO TURN BIT OFF     Y02027
ZERO     EQU   0                        DISPLACEMENT             Y02027
ONE      EQU   1                        DISPLACEMENT             Y02027
TWO      EQU   2                        DISPLACEMENT             Y02027
THREE    EQU   3                        DISPLACEMENT             Y02027
SIX      EQU   6                        MASK FOR ICM             Y02027
SEVEN    EQU   7                        MASK FOR ICM             Y02027
REG0     EQU   20                       REG 0 IN USERS SAVE AREA Y02027
REG1     EQU   24                       REG 1 IN USERS SAVE AREA Y02027
REG2     EQU   28                       REG 2 IN USERS SAVE AREA Y02027
REG14    EQU   12                       REG 14 IN USERS SAVE AREAY02027
REG15    EQU   16                       REG 15 IN SAVE AREA      Y02027
LENECC   EQU   X'52'                    INVALID WORK UNIT LNGTH@YA08121
         EJECT
*                                                                     *
*                   D S E C T S                                       *
*                                                                     *
         SPACE 2
         DCBD  DSORG=PS,DEVD=
DCBPECB  EQU   DCBIOBAD                                          Y02027
         EJECT
IHADECB  DSECT                          DATA EXTENT CONTROL BLOCK
         SPACE 2
DECSDECB DS    F                        EVENT CONTROL BLOCK
DECTYPE  DS    H                        OPTION BYTES
DECLNGTH DS    H                        LENGTH OF WORKAREA
DECDCBAD DS    F                        DATA CONTROL BLOCK ADDRESS
DECAREA  DS    F                        WRITE WORKAREA ADDRESS
DECIOBPT DS    F                        IOB ADDRESS
         EJECT
         TPECBD
         TDEBAPD
         TPEWAD
         END
