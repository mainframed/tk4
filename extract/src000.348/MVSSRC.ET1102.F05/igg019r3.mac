19R3     TITLE ' IGG019R3 - RECEIVE SCHEDULER '
IGG019R3 CSECT
         SPACE 3
*CHANGE-ACTIVITY = AS FOLLOWS:
******************** MICROFICHE FLAGS *********************** SUPT CODE
*A450000,452400,780000                                           S99228
*A228000,276000,327000,334600,357000,450000,546000,579700,580400 S22025
*A633000,693000,750000,777000,780000,789000,795000,798000        S22025
*C300000,303000,657000                                           S22025
*D330000-334200,335000-348000,360000-420000,453000-465000        S22025
*D581100-591000,753000                                           S22025
*A648000                                                        SA59024
*C331800-332000,357600,357900-359800,618000-621000              SA56606
*D336000-351000,360600,360800,363000-402000,402900,             SA56606
*D403140-403200,403560-404340,404880,432000,444000,543000,      SA56606
*D547000-548000,603000,693010-693560                            SA56606
*A558000,592400                                                 SA65379
*C576000                                                        SA65379
*D329400,330200                                                 SA66613
*A330520                                                        SA66613
*A332000,335300                                                @SA73381
*C360500                                                       @SA73380
*A404940                                                       @SA73490
*D410940-416940                                                @SA75437
*A324000,357900,615000,782000                                  @OX13516
*A429000                                                       @OZ14167
*C429000                                                       @OZ14196
*C513000,752000                                                @OY14092
         SPACE 3
***********************************************************************
*                                                                     *
*TITLE -- 'IGG019R3',LEASED RECEIVE SCHEDULER                         *
*                                                                     *
*        MODULE NAME = IGG019R3                                       *
*                                                                     *
*        DESCRIPTIVE NAME = RECIEVE SCHEDULER                         *
*                                                                     *
*        COPYRIGHT='NONE'                                             *
*                                                                     *
*STATUS: CHANGE LEVEL 5                                               *
*                                                                     *
*FUNCTION -- TO INITIATE A RECEIVE OPERATION IF POSSIBLE AND TO MAKE  *
*   PROPER DISPOSITION OF THE LINE WHEN THE LAST STCB IN THE CHAIN    *
*   (QEVENT) GAINS CONTROL.                                           *
*                                                                     *
*ENTRY POINTS --                                                      *
*   FOR RECEIVE SCHEDULER - FIRST EXECUTABLE INSTRUCTION              *
*                                                                     *
*   'QEVENT' - LAST STCB IN LCB CHAIN CONTENDING FOR LCB              *
*                                                                     *
*INPUT --                                                             *
*   R1  POINTS TO LCB                                                 *
*   R11  DISPATCHER BASE                                              *
*   R13  ADDRESS OF AVTSAVE2                                          *
*   R15  ENTRY POINT ADDRESS                                          *
*                                                                     *
*OUTPUT --                                                            *
*   R1  POINTS TO ELEMENT IF POSTING IS TO BE DONE                    *
*   R11 DISPATCHER BASE                                               *
*   R13  ADDRESS OF AVTSAVE2                                          *
*                                                                     *
*EXITS-NORMAL --                                                      *
*   R1  ADDRESS OF LCB OR ERB                                         *
*   R13  ADDRESS OF AVTSAVE2                                          *
*   R11  DISPATCHER BASE                                              *
*   RETURN IS ON R11 WITH AND OFFSET TO HAVE THE DISPATCHER PROVID
*   RETURN IS ON R11 TO HAVE THE DISPATCHER PERFORM THE DESIRED       *
*   FUNCTION.                                                         *
*                                                                     *
*   FROM 'QEVENT'                                                     *
*   R11  ADDRESS OF DISPATCHER                                        *
*   R13  ADDRESS OF AVTSAVE2                                          *
*   B    DSPDISP                                                      *
*                                                                     *
*EXTERNAL ROUTINES --                                                 *
*   'POST' MACRO - TO POST OPERATOR CONTROL ECB FOR CLOSE             *
*                                                                     *
*   'DSPUNAVR' - TO ADJUST STCB CHAIN OF LCB                          *
*                                                                     *
*EXITS-ERROR -- NONE                                                  *
*                                                                     *
*TABLES/WORK AREAS --                                                 *
*   TSTCBD                                                            *
*   TAVTD                                                             *
*   TLCBD                                                             *
*   TRECBD                                                            *
*   DCBD                                                              *
*   TSCBD                                                             *
*                                                                     *
*                                                                     *
*ATTRIBUTES -- ENABLED, PROBLEM PROGRAM MODE, RE-USABLE, REFRESHABLE  *
*                                                                     *
*NOTES -- THE OPERATION OF THIS MODULE DOES NOT DEPEND UPON A         *
*   PARTICUALR INTERNAL REPRESENTATION OF THE EXTERNAL CHARACTER      *
*   SET.                                                              *
*                                                                     *
*   THE LAST STCB INA LCB CHAIN TO GAIN CONTROL IS 'QEVENT'.  ITS     *
*   ADDRESS IS COMPUTED BY OPEN USING THE TWO BYTE OFFSET  THAT       *
*   MUST ALWAYS BE THE FIRST TWO BYTES OF THIS MODULE.                *
*                                                                     *
***********************************************************************
         EJECT
*                                       REGISTER EQUATES
         SPACE 1
R0       EQU   0
RRECB    EQU   1                        ELEMENT BASE
RA       EQU   2                        WORK REGISTER
R2       EQU   2                  TEMPORARY WORK REG
RSTCB    EQU   3                  STCB BASE
RLCB     EQU   4                        LCB BASE
RSCB     EQU   5                        SCB BASE REGISTER        S22025
RWORK    EQU   6                  TEMPORARY WORK REG
RQCB     EQU   7                        ENTRY QCB ADDRESS (LCB)
RCURRENT EQU   8                  CURRENT INVITATION LIST ENTRY
RILIST   EQU   9                        INVITATION LIST ADDRESS
RDCB     EQU   10                 DCB ADDR
RSUPBASE EQU   11                       DISPATCHER BASE REGISTER
RBASE    EQU   12                 SUBTASK BASE REG
R13      EQU   13                       SAVE AREA ADDRESS (AVTSAVE2
R14      EQU   14                 RETURN ADDR REG
R15      EQU   15                       ENTRY BASE REGISTER
         EJECT
         DC    AL2(QEVENT-IGG019R3)     OFFSET USED Y OPEN
         USING *,R15
         SPACE 1
         USING IEDQSTCB,RSTCB
         USING IEDQLCB,RLCB
         USING IEDQSCB,RSCB             SCB DSECT                S22025
         USING IEDQQCB,RQCB
         USING IHADCB,RDCB
         USING IEDQDISP,RSUPBASE
         USING AVTSAVE2,R13
         USING IEDQLCBX,R2              LCB EXTENSION ADDR       S99228
         EJECT
IGG019R3 IEDHJN ID                                               S22025
         LA    RLCB,AVTEZERO(,RRECB)    GET LCB FOR BASE
         L     RDCB,LCBDCBPT      GET DCB ADDR
         SR    RWORK,RWORK              CLEAR LENGTH REGISTER    S99228
         IC    RWORK,DCBEIOBX           INSERT LCB LENGTH        S99228
         SH    RWORK,AVTHA4             REDUCE LCB LENGTH BY     S99228
         SH    RWORK,AVTHA4             8 BYTES TO POINT TO EXT  S99228
         LA    R2,ZERO(RWORK,RLCB)      SET ADDR LCB EXTENSION   S99228
         ST    R2,AVTSAVE3              SAVE LCB EXTENSION ADDR  S99228
         SR    RWORK,RWORK        ZERO WORK REG
         IC    RWORK,LCBUCBX      RLN - 1
         SLL   RWORK,2            MULTIPLY BY FOUR
         L     RILIST,DCBINVLI(RWORK)   ILIST FOR THIS LINE
         NI    THREE(RILIST),X'FF'-INVEND TURN OFF SWITCH      @OX13516
         SR    RWORK,RWORK        ZERO WORK REG
         L     RSCB,LCBSCBA-1           GET THE SCB ADDRESS      S22025
         NI    LCBSTAT2,AVTEFF-LCBSNDPR TURN OFF THE SEND        S22025
*                                         PRIORITY BIT           S22025
         XC    AVTDOUBL(EIGHT),AVTDOUBL CLEAR A WORK AREA        S22025
         MVI   AVTPARM,ZERO             INITIALIZE FOR LOOP CTL  S22025
         TM    LCBXFLAG,LCBGPCTV        GENERAL POLL IN PROGRESS?S99228
         BO    RETURN                   BRANCH YES               S99228
         L     RCURRENT,LCBXRADR        LOAD ERP POLL ADDR       S99228
         TM    LCBXFLAG,LCBERPND        SOH%R PENDING?           S99228
         BO    GENTSIZE                 BRANCH YES               S99228
         TM    AVTBIT1,AVTDLAYN+AVTCLOSN IS THIS CLOSEDOWN OR   SA66613
*                                        SYSTEM DELAY           SA66613
         BNZ   SETDOUBL                 YES, BRANCH             SA66613
         TM    LCBSTAT1,LCBOCNI         IS IT STOPLINE           S22025
         BZ    RETURN                   NO, BRANCH               S22025
SETDOUBL EQU   *                                                 S22025
         LA    RCURRENT,ILSTPRF(RILIST) ADDR OF 1ST ENTRY       SA56606
         ST    RCURRENT,AVTDOUBL        TEMPORARY MOVE          SA56606
         MVC   LCBINVPT(THREE),AVTDOUBL+1 PUT PTR IN LCB        SA56606
NEXTSTCB EQU   *                                               @SA73381
         LR    RRECB,RLCB               ELEMENT ADDR            SA56606
         LR    RQCB,RLCB                ADDR QCB CONTAINING STCBSA56606
         L     RSTCB,LCBRSLNK-1         ADDR NEXT STCB          SA56606
         B     DSPBYPAS                 BYPAS TO NEXT STCB(SEND SA56606
*                                       SCHEDULER OR QEVENT)    SA56606
RETURN   EQU   *                                                 S22025
         L     RCURRENT,LCBINVPT-1      GET THE CURRENT INVLIST  S22025
*                                         ENTRY ADDRESS          S22025
         TM    SCBSTATE,SCBLCK1N+SCBMSGLN TEST FOR EXTENDED LOCK
         BM    GENTSIZE                 BRANCH IF LOCK           S22025
         BO    NEXTSTCB                 BRANCH IF MESSAGE LOCK @SA73381
         L     R2,AVTSAVE3              LOAD LCB EXTENSION ADDR  S99228
         TM    LCBXFLAG,LCBERPND        SOH%R MESSAGE PENDING?   S99228
         BO    GENTSIZE                 BRANCH YES               S99228
         CLI   ACTIVE(RILIST),AVTEZERO ANY ACTIVE ENTRIES
         BE    FREE                     NO, BRANCH               S22025
LOOP     EQU   *                                                 S22025
         L     RCURRENT,LCBINVPT-1      GET THE CURRENT ILIST    S22025
*                                         ENTRY ADDRESS          S22025
         CLI   EOL(RCURRENT),FE         IS THIS END OF LIST      S22025
         BE    ENDOLIST                 YES-BRANCH              SA56606
         CLI   ONE(RCURRENT),FE         IS THIS END OF BSC LIST  S22025
         BNE   GENTSIZE                 NO, BRANCH               S22025
         B     ENDOLIST                 END OF LIST             SA56606
FREE1    EQU   *                                               @OX13516
         NI    THREE(RILIST),X'FF'-INVEND TURN OFF 2ND PASS SW @OX13516
FREE     EQU   *                                                 S22025
         LR    RRECB,RLCB               SET THE RCB ADDRESS      S22025
         LR    RQCB,RLCB                SET THE QCB ADDRESS      S22025
         L     RSTCB,LCBRSLNK-1         SET THE ADDRESS OF THE   S22025
*                                         NEXT STCB IN THE CHAIN S22025
         CLI   STCBPRI,AVTEZERO         IS THIS THE LAST STCB    S22025
         BNE   DELAYIT                  NO,FORCE A DELAY       @SA73380
         L     RWORK,LCBSTCBA-1         GET FIRST STCB ADDR    @SA73380
         CLI   STCBVTO-IEDQSTCB(RWORK),DSPRCVSC IS ONLY STCB   @SA73380
*                                        THE RECEIVE SCHEDULER @SA73380
         BE    DSPBYPAS                 YES,BYPASS TO QEVENT   @SA73380
         B     DELAYIT                  FORCE A 1 SECOND DELAY   S22025
GENTSIZE EQU   *                                                 S22025
         BAL   R14,ENTSIZE              GET THE SIZE OF AN       S22025
*                                         INVITATION LIST ENTRY  S22025
         BCTR  RWORK,0                  SUBTRACT 1 TO FIND THE   S22025
*                                         INDEX BYTE             S22025
         IC    RWORK,0(RCURRENT,RWORK)  GET THE INDEX BYTE       S22025
         AR    RWORK,RWORK              DOUBLE THE INDEX         S22025
         LR    R2,RILIST                SAVE THE INVITATION LIST S22025
*                                         ADDRESS                S22025
         SR    R2,RWORK                 LOCATE THE TERMINAL NAME S22025
*                                         TABLE INDEX            S22025
         LH    RRECB,0(,R2)             GET THE NAME TABLE INDEX S22025
         ST    RRECB,AVTDOUBL+4         SAVE THE INDEX FOR LATER S22025
         LR    RBASE,R15                SAVE R3'S BASE REGISTER  S22025
         L     R15,AVTRNMPT             GET THE ADDRESS OF THE   S22025
*                                         IEDQTNT ROUTINE        S22025
         BALR  R14,R15                  BRANCH TO IT             S22025
         LR    R15,RBASE                RESTORE THE BASE REG.    S22025
         L     RQCB,TRMDESTQ-1-IEDQTRM(,RRECB)    LOAD THE       S22025
*                                         DESTINATION QCB ADDR.  S22025
         TM    QCBSTAT,QCBSEND          ARE WE CURRENTLY SENDING S22025
*                                         TO THIS STATION        S22025
         BO    GETNEXT                  YES, CHECK NEXT ENTRY    S22025
         SR    R0,R0                    CLEAR TWO REGISTERS FOR  S22025
         SR    RRECB,RRECB              THE MULTIPLICATION       S22025
         IC    R0,QCBSCBOF              GET THE QCB SCB INDEX    S22025
         IC    RRECB,AVTSCBSZ           GET THE SCB SIZE         S22025
         MR    R0,R0                    COMPUTE THE SCB OFFSET   S22025
         LR    RSCB,RRECB               COPY THE SCB OFFSET      S22025
         AL    RSCB,LCBSCBDA-1          ADD THE OFFSET TO THE    S22025
*                                         ABSOLUTE SCB ADDRESS   S22025
         IC    R0,LCBSCBA-ONE           SAVE LCBMSGFM            S99228
         ST    RSCB,LCBSCBA-1           STORE THE SCB ADDRESS    S22025
         STC   R0,LCBSCBA-ONE           RESTORE LCBMSGFM         S99228
         SPACE 2
         TM    FIVE(RILIST),AUTOPOLL    IS AUTOPOLL REQUESTED    S22025
         BZ    TESTBIT3                 NO, BRANCH               S22025
         NI    THREE(RILIST),FE         TURN OFF AUTOPOLL BIT    S22025
         CLI   FOUR(RILIST),AVTEZERO    ARE WE CURRENTLY         S22025
*                                             SENDING TO ANY     S22025
*                                             STATIONS           S22025
         BNE   TESTBIT3                 YES, BRANCH              S22025
         OI    THREE(RILIST),AUTOPOLL   RESET THE AUTOPOLL BIT   S22025
TESTBIT3 EQU   *                                                 S22025
         TM    AVTBIT3,AVTRECVN+AVTRFULN IS RECEIVE OPERATION
*                                         POSSIBLE
         BZ    GOTUNIT                  BRANCH IF YES
         TM    QCBFLAG,QCBTSSES         IS IT TSO SESSION      @OZ14167
         BNO   STOPOLL                  NO, STOP POLLING       @OZ14167
         TM    QCBTSOF1,QCBSATRD        IS SIMULATED ATTENTION @OZ14196
*                                       READ IN PROGRESS       @OZ14167
         BO    GOTUNIT                  YES, PROCEED WITH READ @OZ14167
STOPOLL  EQU   *                                               @OZ14167
         L     R2,AVTSAVE3              LOAD LCB EXTENSION ADDR  S99228
         NI    LCBXFLAG,LCBGPSTP        STOP GENERAL POLL        S99228
         NI    LCBXFLAG,LCBERSTP        STOP ERP POLL            S99228
DELAYIT  EQU   *
         LA    RWORK,1                  SET FOR 1 SECOND DELAY
         B     GOTIME                   PUT ON TIME Q
GOTUNIT  EQU   *
         XC    LCBRECAD,LCBRECAD        RESET 1ST EOB RECORD ADDRESS
         XC    LCBRECOF(TWO),LCBRECOF   RESET THE OFFSET INTO    S22025
*                                         THE RECORD             S22025
         MVC   LCBTTBIN(TWO),AVTDOUBL+6 SET THE TERMINAL NAME    S22025
*                                         TABLE INDEX IN THE LCB S22025
         L     R2,AVTSAVE3              LOAD LCB EXTENSION ADDR  S99228
         TM    LCBXFLAG,LCBERPND        SOH%R MESSAGE PENDING?   S99228
         BO    TSO1                     BRANCH YES               S99228
         TM    AVTBIT1,AVTTSON          IS TSO IN SYSTEM?           TSO
         BZ    TSO1                     BRANCH ON NO                TSO
         L     RWORK,AVTTSOPT           GET ADDRESS OF TSO SCHED    TSO
         L     RWORK,TSISCHED-IEDQTSI(,RWORK)                       TSO
         BAL   RSTCB,0(,RWORK)          BRANCH TO ROUTINE           TSO
         B     ENDOLIST                 BRANCH TO END OF LIST RTN   TSO
         SPACE 1                                                    TSO
TSO1     EQU   *                                                    TSO
         MVI   LCBTSTSW,0               RESET CONNECT SWITCH
         MVI   LCBCSWSV,AVTEFF          SET SENSE FIELD
         LA    RA,AVTBFREB              BUFFER REQUEST QUEUE
         ST    RA,LCBERB                POST ADDRESS
         NI    LCBSTAT1,LCBOCNI+LCBOCWTN SAVE THE STOPLINE FLAG@OY14092
         OI    LCBSTAT1,LCBRECVN        PRESERVE STOP LINE REQUEST
*                                         UNTIL LOCK SESSION IS OVER
         MVI   LCBERBPY,PRIINTRQ        INITIAL REQUEST PRIORITY
         SR    RWORK,RWORK              CLEAR AGIN -MORE THAN 128
*                                         ENTRIES IN INV LIST
         IC    RWORK,DCBBUFIN           INITIAL COUNT
         SRL   RWORK,4                  SHIFT FOR HI ORDER BITS
         STC   RWORK,LCBERBCT           ESTABLISH INIT REQUEST
         LA    RRECB,LCBERB             ELEMENT FOR POST
         BAL   R14,DSPPOST        GO QPOST REQUEST FOR BUFFERS @Y17XAIX
ENDOLIST EQU   *
         LA    RCURRENT,ILSTPRF(0,RILIST) GET ADDR OF FIRST ENTRY
         IC    R0,LCBINVPT-1
         ST    RCURRENT,LCBINVPT-1 SET AS CURRENT ENTRY
         STC   R0,LCBINVPT-1
         SR    RWORK,RWORK              CLEAR FOR IC            SA65379
         IC    RWORK,DCBINTVL           TIME DELAY
         LTR   RWORK,RWORK              NO DEALY
         BZ    NODELAY                  BRANCH IF NO DEALY
GOTIME   EQU   *
         LR    RA,RWORK                 SAVE DELAY TIME         SA65379
         MVI   LCBTDL,LCBINSRC-1-IEDQLCB SET INDEX FOR TIME DELAY
         L     RWORK,LCBSTCBA-1         ADDRESS OF FIRST STCB
         LA    R14,LCBLINK-1            INITIALIZE FOR LOOP      S22025
*                                         CONTROL                S22025
STCBSCAN EQU   *                                                 S22025
         CLI   0(RWORK),DSPRCVSC        IS IT RECEIVE SCHEDULER
         BE MOVE                        YES, BRANCH              S22025
         CLI   FOUR(RWORK),ZERO         IS THIS THE LAST ELEMENT S22025
*                                         IN THE STCB CHAIN      S22025
         BE    NODELAY                  YES, BRANCH              S22025
         LR    R14,RWORK                COPY THE STCB ADDRESS    S22025
         L     RWORK,FOUR(RWORK)        GET THE ADDRESS OF THE   S22025
*                                         NEXT STCB              S22025
         B     STCBSCAN                 CONTINUE SEARCHING THE   S22025
*                                         STCB CHAIN             S22025
MOVE     EQU   *                                                 S22025
         MVC   FIVE(THREE,R14),FIVE(RWORK)  REMOVE THE RECEIVE   S22025
*                                         SCHEDULER FROM CHAIN   S22025
         STH   RA,LCBEOLTD              TIME DELAY PARAMETER    SA65379
         LA    RA,AVTDELYB              TIME DELAY QUEUE
         ST    RA,LCBQCBA-1             FOR POST
         B     DLYPOST                  POST LCB
NODELAY  EQU   *
         LR    RSTCB,RLCB         SET LCB AS QCB DESIRED
         LR    RQCB,RLCB          SET LCB AS CURRENT QCB
         BAL   R14,DSPUNAVR       GO USE SEND VS. RCV PRIORITY
         L     RWORK,LCBSTCBA-1         GET FIRST STCB ADDRESS @OX13516
         CLI   STCBVTO-IEDQSTCB(RWORK),DSPRCVSC IS IT RCV SCHED@OX13516
         BNE   POSTSND                  NO,POST SEND SCHEDULER @OX13516
         TM    THREE(RILIST),INVEND     2ND PASS TO END OF LIST@OX13516
         BO    FREE1                    YES, CAN'T RCV OR SEND @OX13516
         OI    THREE(RILIST),INVEND     SET FOR SECOND PASS    @OX13516
         B     LOOP                     SCAN INVLIST AGAIN     @OX13516
POSTSND  EQU   *                                               @OX13516
         ST    RLCB,LCBQCBA-1           LCB POST                SA56606
DLYPOST  EQU   *
         MVI   LCBPRI,PRILNFRE          POST LCB TO ITSELF
         LR    RRECB,RLCB               POST LCB TO ITLSELF
         BAL   R14,DSPPOST        GO QPOST                     @Y17XAIX
         EJECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *  S22025
*                                                             *  S22025
*        THIS SUBROUTINE WILL COMPUTE THE SIZE OF AN          *  S22025
*                  INVITATION LIST ENTRY                      *  S22025
*                                                             *  S22025
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *  S22025
ENTSIZE  EQU   *                                                 S22025
         SR    RWORK,RWORK              CLEAR THE SIZE REGISTER  S22025
         IC    RWORK,ILSTSIZE(,RILIST)  GET THE SIZE OF AN       S22025
*                                         INVITATION LIST ENTRY  S22025
         BR    R14                      RETURN                   S22025
         EJECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *  S22025
*                                                             *  S22025
*        THIS SUBROUTINE WILL COMPUTE THE ADDRESS OF          *  S22025
*           THE NEXT ENTRY IN AN INVITATION LIST              *  S22025
*                                                             *  S22025
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *  S22025
GETNEXT  EQU   *                                                 S22025
         BAL   R14,ENTSIZE              GET THE ENTRY SIZE       S22025
         SPACE 2
         AR    RCURRENT,RWORK           POINT TO THE NEXT ENTRY  S22025
         IC    R0,LCBINVPT-1            SAVE THE ERROR RECORDING S22025
*                                         MASK                   S22025
         ST    RCURRENT,LCBINVPT-1      SAVE THE ADDRESS IN THE  S22025
*                                         LCB                    S22025
         STC   R0,LCBINVPT-1            RESTORE THE ERROR MASK   S22025
         B     LOOP                     CHECK THE NEXT ENTRY     S22025
         EJECT
*** THIS STCB IS THE LAST STCB IN THE CHAIN OF SCHEDULERS CONTENDING
*        FOR THE LINE.  THIS SUBTASK WILL CHECK TO SEE IF ANYTHING ELSE
*        SHOULD BE DONE WITH THE LINE AND, IF NOT, WILL SET THE LINE
*        'FREE'.
*
*  THE QEVENT STCB MUST BE ON A FULL WORD BOUNDARY              SA59024
*
         DS    0F                       QEVENT BOUNDARY ALIGN   SA59024
*
         SPACE 2
QEVENT   EQU   *
         DC    AL1(DSPMCPL6),XL5'00'    LAST STCB IN CHAIN       S22025
         SPACE 1
         USING *,R15
         DROP  RLCB
         USING IEDQLCB,RRECB
         TM    AVTBIT1,AVTTSON          IS TSO IN SYSTEM?           TSO
         BZ    TSO2                     BRANCH ON NO                TSO
         L     RWORK,AVTTSOPT           GET ADDRESS OF TSO SCHED    TSO
         LR    RLCB,RRECB               SAVE LCB ADDRESS            TSO
         L     RWORK,TSISCHED-IEDQTSI(,RWORK)                       TSO
         BAL   RSTCB,16(,RWORK)         BRANCH TO ROUTINE           TSO
         LR    RRECB,RLCB               RESTORE LCB ADDRESS        TSO
TSO2   EQU   *                                                      TSO
         MVC   LCBQCBA,AVTHK+1          SET QCB ADDRESS FOR POST
         TM    LCBSTAT1,LCBOCNI         STOPP LINE REQUEST
         BO    POSTHK                   BRANCH IF YES
         SPACE
         TM    AVTBIT1,AVTCLOSN         CLOSE IN PROGRESS
         BZ    NOCLOSE                  NO
         SPACE
         MVI   LCBSTAT1,LCBFREEN        SET LCB FREEE
         LA    1,AVTOPCOB+4             POST OPERATOR CONTROL
*                                       CONTINUE CLOSE FUNCTION
         POST  (1)
         B     DSPDISP                  DISPATCH NEXT STCB
NOCLOSE  EQU   *
         TM    AVTBIT1,AVTDLAYN         SYS DELAY IN EFFECT
         BZ    NOTDL                    NO
         SPACE
         CLI   LCBRSKEY,LEASED          LEASED LINE
         BNE   EXIT                     NO
         MVC   LCBQCBA,AVTHI+1          SET QCB ADDRESS
POSTHK   EQU   *
         MVI   LCBPRI,PRILNFRE          SET POST PRIORITY
         BAL   R14,DSPPOST              POST LCB               @Y17XAIX
NOTDL    EQU   *
EXIT     EQU   *
         NI    LCBSTAT1,LCBOCNI+LCBOCWTN SAVE THE STOPLINE FLAG@OY14092
         OI    LCBSTAT1,LCBFREEN        MARK THE LCB AS FREE     S22025
         B     DSPDISP                  DISPATCH NEXT SUBTASK
         EJECT
*                                       OTHER EQUATES
         SPACE 1
EOL      EQU   0                 END OF LIST INDICATOR WOULD APPEAR
*                                      AT THIS OFFSET WITHIN AN
*                                      INVITATION LIST ENTRY
ACTIVE   EQU   1                  OFFSET OF NUM OF ACTIVE ENTRIES
ZERO     EQU   0                                                 S22025
ONE      EQU   1                                                 S22025
TWO      EQU   2                                                 S22025
ILSTSIZE EQU   2                  LENGTH OF AN INVITATION LIST ENTRY
THREE    EQU   3                                                 S22025
FOUR     EQU   4                                                 S22025
FIVE     EQU   5                                                 S22025
AUTOPOLL EQU   X'01'                                             S22025
INVEND   EQU   2                        SWITCH FOR SECOND PASS @OX13516
*                                       THRU INVITATION LIST   @OX13516
SWITCH   EQU   X'01'                                             S22025
RCVPRI   EQU   X'80'                    RECEIVE PRIORITY LINE
SENSMASK EQU   X'FF'                    SENSE FIELD FOR CONTENTION
LEASED   EQU   X'0C'                    STVTO FOR LEASED LCB
DIAL     EQU   X'18'                    STVTO FOR A DIAL LCB     S22025
TIMEQ    EQU   X'02'                    MASK FOR LCB ON TIME Q
ILSTPRF  EQU   8                        SIZE OF ILIST COTNROL AREA
EIGHT    EQU   8                                                 S22025
FE       EQU   X'FE'              END OF LIST INDICATOR
PRITYON  EQU   X'10'                                             S22025
PRITYOFF EQU   X'EF'                                             S22025
PATCH    DC    XL48'00'                 12 WORD PATCH AREA       S22025
         EJECT
         TPRIOR
         TITLE 'AVT DSECT'
         TAVTD
         TITLE 'DCB DSECT'
         DCBD  DSORG=TX
         TITLE 'DISPATCHER DSECT'
         TDISPD
         TITLE 'LCB DSECT'
         TTSID                                                      TSO
         EJECT
         TLCBD
         TITLE 'QCB DSECT'
         TQCBD
         TITLE 'RCB DSECT'
         TRECBD
         TITLE 'STCB DSECT'
         TSTCBD
         TITLE 'TERMINAL TABLE ENTRY DSECT'
         TTRMD
         TSCBD
         END
