         TITLE 'IGE0904H - OBR/SDR INTERFACE FOR TPER RECORDING'
*                                                                     *
IGE0904H CSECT
         SPACE 3
*  CHANGE ACTIVITY AS FOLLOWS
******************** MICROFICHE FLAGS *********************** SUPT CODE
*A146000,292000,382000,488000,513000,582000,782000               S99228
*D513000,532000                                                  S99228
*A016000-028600,361000,404600-405200,439000,488800-489200        S99238
*A512700-513500,568800-569200,62100-626600,700200-701500         S99238
*A710600-711200,782200-783200                                    S99238
*C24200,250000-332000,440000-442000,710000,750000                S99238
*D016000-028000,334000-342000,438000                             S99238
*A488180                                                        SA60011
*A006000,330000                                                 SA61058
*C006600-007700,008000,012000                                   SA61058
*C488000                                                         S21903
*A144000,226000,752000,786000                                   SA65392
*C162000                                                        SA65392
*D230000,496000,701125,701280,736000,740000-742000              SA65392
*D746000-748000,776000,780000,782720,783000,788000,798000       SA65392
*C696000                                                        SA66340
         SPACE 3
*C660000,696000                                                  Y01004
*D 782640-782720                                                 X02004
*A226030,226060                                                @ZA00237
*C536000,580000,700500                                         @ZA00237
*D772000                                                       @ZA00237
*A782600                                                       @ZA02623
*C227000                                                       @ZA02623
*A227100                                                       @YA10697
*D232000-234000                                                @YA10697
*C226800,A226900                                               @OZ24288
***********************************************************************
*  TITLE: IGE0904H - OBR/SDR INTERFACE FOR TPER RECORDING             *
*                                                                     *
*  MODULE NAME = IGE0904H                                             *
*                                                                     *
*  DESCRIPTIVE NAME = OBR/SDR INTERFACE FOR TPER RECORDING            *
*                                                                     *
*  COPYRIGHT = 'NONE'                                                 *
*                                                                     *
*  STATUS:  CHANGE LEVEL 6                                            *
*                                                                     *
* FUNCTION -- THIS MODULE HANDLES SOH % ERROR MESSAGES FOR PROPERS99238
*    RECORDING.  IT INTERFACES TO OBR/SDR RECORDING MODULES TO   S99238
*    RECORD THE ERROR MESSAGES THAT ARE TO BE LOGGED ON          S99238
*    SYS1.LOGREC.  ERROR MESSAGES THAT ARE TO BE WRITTEN TO THE  S99238
*    OPERATOR ARE WRITTEN OUT OF THIS MODULE VIA WTO.  A SCAN OF S99238
*    THE BUFFER IS MADE TO DETERMINE IF IT IS A VALID SOH %      S99238
*    MESSAGE, IF IT IS NOT THE MESSAGE IS NOT RECORDED AND       S99238
*    CONTROL IS GIVEN BACK TO THE SYSTEM.  IF IT IS A VALID SOH% S99238
*    ERROR MESSAGE A SCAN OF THE BUFFERS ARE MADE TO DETERMINE   S99238
*    HOW MUCH DATA IS TO BE RECORDED.  A GETMAIN IS ISSUED TO    S99238
*    GET ENOUGH CORE FOR RECORDING (INCLUDING PROPER HEADER      S99238
*    RECORD).  THE GOTTEN CORE IS FORMATTED ACCORDING TO THE     S99238
*    TYPE OF SOH % MESSAGE, THE DATA TO BE RECORDED IS MOVED     S99238
*    INTO THE GOTTEN CORE AND CONTROL IS PASSED TO THE PROPER    S99238
*    RECORDING MODULES.                                          S99238
*                                                                     *
* ENTRY POINTS -- FIRST EXECUTABLE INSTRUCTION                        *
*                                                                     *
* INPUT --                                                            *
*   R1 - POINTER TO I/O SUPERVISOR BLOCK                         Y02027
*    R15 - ENTRY POINT ADDRESS                                        *
*                                                                     *
*OUTPUT -- R1 POINTS TO I/O SUPERVISOR BLOCK                     Y02027
*                                                                     *
* EXTERNAL REFERENCES -- NONE                                         *
*                                                                     *
*EXITS-NORMAL -- R1 POINTS TO I/O SUPERVISOR BLOCK               Y02027
*        SVC   15   'NO ERROR FLAGS'. RETURN TO LINE END         Y02027
*                   APPENDAGE. ERROR CLEARED                     Y02027
*         SVC   3                                                     *
*                                                                     *
*         TO SCHEDULE NEXT LOAD                                       *
*         L     13,DECIMAL VALUE OF NEXT LOAD                         *
*         L     14,X'10'     CVT ADDRESS                              *
*         L     14,X'2C'(14) ADDRESS OF XCTL ROUTINE                  *
*         BR    14           EXIT TO XCTL                             *
*                                                                     *
* EXITS, ERROR -- NONE                                                *
*                                                                     *
* TABLES/WORK AREAS --                                                *
*    TAVTD                                                            *
*    TTRMD                                                            *
*    TCCWD                                                            *
*    TLCBD                                                            *
*    DCBD                                                             *
*    TSCBD                                                            *
*    TPRFD                                                            *
*    TPERD                                                            *
*   IECDIOSB                                                     Y02027
*                                                                     *
*ATTRIBUTES -- SERIALLY REUSABLE, REFRESHABLE, SUPERVISOR KEY,   Y02027
*              SUPERVISOR MODE, ENABLED                          Y02027
*                                                                     *
* CHARACTER CODE DEPENDENCY --                                        *
*                                                                     *
*    THE OPERATION OF THIS MODULE DOES NOT DEPEND UPON A PARTICULAR   *
*    INTERNAL REPRESENTATION OF THE EXTERNAL CHARACTER SET.           *
*                                                                     *
* NOTES --
*    PARAMETERS PASSED TO TPER RECORDING MODULE:                      *
*                                                                     *
*   EWTCBUFF - THE ADDRESS OF THE TPER BUFFER. THE BUFFER IS     Y02027
*    FORMATTED BY THIS MODULE BEFORE CALLING TPER.                    *
*                                                                     *
*    TPER PROCESSING FLAG (LCBOBRRD) SHOULD NOT BE ON WHEN THIS       *
*    MODULE IS ENTERED.                                               *
*                                                                     *
***********************************************************************
         EJECT
         USING IEDQLCB,RLCB             LCB
         USING IHADCB,RDCB              DCB
         USING IEDQAVTD,RAVT            AVT
         USING IEDQTRM,RTERM            TERMINAL ENTRY
         USING IEDQSCB,RSCB             SCB
         USING DEBTCBAD,RDEB            DEB                     SA65392
         USING *,R10                    BASE REGISTER
         USING TPERD,RWKD               TPER RECORD ADDRESS      S99228
         USING IOSB,RTST                                         Y02027
         USING EWA,RERPWA                                        Y02027
         LR    R10,RWKD                 ADDRESSABILITY
*
*        REGISTERS
*
R0       EQU   0
RTST     EQU   1                        ADDR OF I/O SUPVR BLOCK  Y02027
RLCB     EQU   2                        LCB BASE
RDEB     EQU   3                        DEB REGISTER            SA65392
RWKF     EQU   3                        WORK REGISTER
RDCB     EQU   4                        DCB BASE
RWKE     EQU   4                        WORK REGISTER
RCCW     EQU   5                        CCW BASE
RWKA     EQU   6                        WORK REGISTER
RWKB     EQU   7                        WORK REGISTER
RTERM    EQU   8                        TERMINAL BASE
RWKC     EQU   9                        WORK REGISTER
R10      EQU   10                       BASE REGISTER
RAVT     EQU   11                       AVT BASE
RSCB     EQU   12                       CURRENT SCB
RLINK    EQU   13                       LINKAGE FOR NEXT LOAD
RXCTL    EQU   14                       XCTL REGISTER
RERPWA   EQU   14                       ADDR OF ERP WORK AREA    Y02027
RWKD     EQU   15                       WORK REGISTER
R15      EQU   15
         EJECT
***********************************************************************
*                                                                     *
*        DSECT ADDRESSABILITY                                         *
*                                                                     *
***********************************************************************
IGE0904H IEDHJN TPERREC                 MODULE ID AND DATE       S99238
         L     RWKB,IOSUSE              PICKUP ADDR OF RQE       Y02027
         L     RERPWA,IOSERP            PICKUP ADDR OF ERP WORK  Y02027
*                                       AREA                     Y02027
         L     RLCB,IOBOFFST(,RWKB)      LCB ADDRESS             Y02027
         SH    RLCB,LCBSIZE             ADJUST BASE
         L     RSCB,LCBSCBA-1           CURRENT SCB
         L     RCCW,LCBCSW-1            FAILING CCW
         SH    RCCW,H8                  BACK TO TO FAILING CCW
         L     RDCB,LCBDCBPT            DCB BASE
         L     RAVT,CVTPTR              CVT ADDR
         L     RAVT,AVTCVTPT(,RAVT)     LOAD LIST ADDR
         L     RAVT,AVTEZERO(,RAVT)     AVT BASE
         LR    RLINK,RTST               SAVE RQE ADDR.
         DROP  RTST                                            @ZA00237
         USING IOSB,RLINK                                      @ZA00237
         L     RDEB,DCBDEBAD            DEB ADDRESSIBILITY      SA65392
         SR    RWKA,RWKA                CLEAR WORK REGISTER     SA65392
         LR    RWKB,RWKA                CLEAR WORK REGISTER     SA65392
         IC    RWKA,DEBNMEXT            NUMBER OF EXTENTS       SA65392
         SLL   RWKA,TWO                 SIZE OF UCB ADDRESSES   SA65392
         L     RWKA,DEBUCBAD(RWKA)      THRESHHOLD AREA         SA65392
         IC    RWKB,LCBUCBX             RLN MINUS ONE           SA65392
         SLL   RWKB,THREE               THRESHOLD INDEX        @OZ24288
         AR    RWKA,RWKB                THRESHHOLD COUNTERS     SA65392
         SRL   RWKB,1                   ADD HALF AGAIN FOR     @OZ24288
         AR    RWKA,RWKB                ERRMSG SAVE AREA       @OZ24288
         IC    RWKB,THREE(RWKA)         GET TIMEOUT COUNT      @ZA02623
         LA    RWKB,ONE(RWKB)           INCREMENT               SA65392
         TM    LCBTSTSW,LCBSECON        IS THIS SECOND ENTRY?  @YA10697
         BO    FREECORE                 YES, FREE CORE         @YA10697
         STC   RWKB,THREE(RWKA)         SET NEW VALUE           SA65392
         CLC   THREE(ONE,RWKA),AVTHRESH+THREE MAXIMUM TIMEOUTS  SA65392
         BL    EXIT                     BRANCH NO, NO MESSAGE   SA65392
         MVI   THREE(RWKA),ZERO         INITIALIZE COUNTER      SA65392
         EJECT
*
***********************************************************************
*                                                                     *
*        SCAN BUFFER(S) FOR STX AND EXIT IF NOT FOUND            S99238
*                                                                     *
***********************************************************************
         L     RWKA,LCBLSPCI-1          LAST SERVICED PCI        S99238
         USING IEDQPRF,RWKA                                      S99238
         SPACE
         L     RWKC,DCBSCTAD-1          SPECIAL CHAR TABLE ADDR  S99238
         LR    RWKD,RWKA                LOAD BUFFER ADDRESS      S99238
         SR    RWKB,RWKB                CLEAR REGISTER           S99238
         IC    RWKB,STXENQCH(,RWKC)     GET OFFSET TO SEQUENCE   S99238
         LA    RWKB,X01(RWKB,RWKC)      POINT TO SEQUENCE        S99238
         LA    R0,SEVEN                 MAXIMUM SCAN LENGTH      S99238
         CH    R0,PRFCOUNT              SEVEN DATA BYTES IN UNIT?S99238
         BH    ADJUST                   NO                       S99238
         SPACE
         LR    RWKF,R0                  YES, SET MAX SCAN LNGTH  S99238
         SPACE
         B     ENTER                    ENTER SCAN LOOP          S99238
ADJUST   EQU   *                                                 S99238
         LH    RWKF,PRFCOUNT            SET LOOP CONTROL         S99238
         N     RWKF,AVTCLRHI            CLEAR HIGH               S99238
         SPACE
ENTER    EQU   *                                                 S99238
         SR    R0,RWKF                  NO. OF BYTES SCANNED     S99238
         L     RWKD,PRFIOADR-1          LOAD DATA ADDR           S99238
         TM    LCBFLAG3,LCBSOHR         SOH%R MESSAGE?           S99228
         BZ    LOOP                     BRANCH NO                S99228
         LA    RWKD,ZERO(R0,RWKD)       CLEAR HIGH ORDER BYTE    S99228
BACKUP   EQU   *                                                 S99228
         CLI   ZERO(RWKD),X01           SOH CHARACTER?           S99228
         BE    LOOP                     BRANCH YES               S99228
         BCT   RWKD,BACKUP              CHECK NEXT CHARACTER     S99228
LOOP     EQU   *                                                 S99238
         CLC   X01(X01,RWKB),0(RWKD)    IS THIS BYTE STX         S99238
         BE    FOUND                    YES, ACCUMULATE DATA     S99238
         SPACE
         LA    RWKD,X01(RWKD)           NO, BUMP DATA ADDR       S99238
         BCT   RWKF,LOOP                SCAN COMPLETE            S99238
         SPACE
         LTR   R0,R0                    ANY BYTES LEFT           S99238
         BZ    BYPASS                   NO, ERROR RETURN         S99238
         SPACE
         TM    PRFTIC+3,X02             LAST UNIT OR BUFFER      S99238
         BO    BYPASS                   YES, RETURN              S99238
         SPACE
         L     RWKA,PRFTIC              GET ADDR OF NEXT UNIT    S99238
         LR    RWKF,R0                  SET REMAINING BYTES      S99238
         B     ENTER                    RE-ENTER LOOP            S99238
         SPACE
FOUND    EQU   *                                                 S99238
         LA    RWKD,X01(RWKD)           ADDRESS OF ID            S99238
         LA    RWKA,0(RWKA)             CLEAR HI ORDER BYTE     SA61058
         LR    R0,RWKA                  SAVE START DATA ADDR     S99238
***********************************************************************
*                                                                     *
*        DETERMINE THE AMOUNT OF DATA TO RECORD                       *
*                                                                     *
***********************************************************************
         SR    RWKB,RWKB                ZERO ACCUMULATOR
         LA    RCCW,0(RCCW)             CLEAR HIGH ORDER BYTE
TOTAL    EQU   *
         LA    RWKA,0(RWKA)             CLEAR HIGH ORDER
         AH    RWKB,PRFCOUNT            ACCUM AMT DATA IN UNIT   S99238
         CR    RWKA,RCCW                IS THIS THE LAST BUFFER UNIT
         BE    FINALSUM                 YES, HANDLE LAST UNIT
*
         L     RWKA,PRFTIC              ADDRESS OF NEXT BUFFER UNIT
         B     TOTAL                    PROCESS THIS UNIT
*
FINALSUM EQU   *
         SH    RWKB,LCBCSW+FIVE         CALC ACTUAL DATA IN UNIT S99238
         LA    RWKB,HDRLEN(RWKB)        ALLOW CORE FOR TPER HDR
         TM    LCBFLAG3,LCBSOHR         SOH % R MESSAGE?         S99228
         BNO   ENUF                     BRANCH NO                S99228
         SPACE
         LA    RWKB,FIVE(R0,RWKB)       INCREMENT GETMAIN COUNT  S99228
ENUF     EQU   *
***********************************************************************
*                                                                     *
*        GET TPER BUFFER                                              *
*                                                                     *
***********************************************************************
         LR    RWKC,RWKD                SAVE ADDRESS OF ID       S99238
         LR    RWKA,R0                  RESTORE BEGINNING OF DATAS99238
         BAL   RWKF,GETMAIN             GET CORE FOR TPER BUFFER
*
         BNZ   ERRCORE                  CORE UNAVAIL. ,TRY ERROR RECORD
*
         BAL   RWKF,BUILD              INIT TPER BUFFER
*
***********************************************************************
*                                                                     *
*        MOVE DEVICE DEPENDENT DATA TO GOTTEN TPER BUFFER             *
*                                                                     *
***********************************************************************
         L     RWKF,EWTCBUFF            GET TOTAL LENGTH OF TPER Y02027
*                                       BUFFER                   Y02027
         LH    RWKF,0(RWKF)
         LA    R0,HDRLEN                ADJUST BY LENGTH OF TPER HDR
         SR    RWKF,R0                  AMOUNT OF DEVICE DATA TO BE
*                                       MOVED
         L     RWKC,PRFIOADR-1          LOAD ADDR OF DATA IN UNITS99238
         LH    R0,PRFCOUNT              LOAD NO. BYTES IN UNIT   S99238
         AR    RWKC,R0                  ADJUST ADDRESS           S99238
         SR    RWKC,RWKE                ANY DATA LEFT IN UNIT    S99238
         BNP   UPDATE                   NO DEVICE DATA IN THIS UNIT
*
MOVE     EQU   *
         CR    RWKA,RCCW                LAST UNIT
         BE    LASTMVE                  YES, FINISH MOVING THE DATA
*
         BCTR  RWKC,0                   DECREMENT COUNT
         EX    RWKC,DATAMVE             MOVE DATA TO TPER BUFFER
         LA    RWKC,X01(RWKC)           UPDATE COUNT MOVED
         LA    RWKD,0(RWKC,RWKD)        UPDATE POINTER IN TPER BUFFER
         SR    RWKF,RWKC                DECREMENT TOTAL COUNT BY THE
*                                       COUNT JUST MOVED
UPDATE   EQU   *
         L     RWKA,PRFTIC              GET ADDRESS OF NEXT BUFFER UNIT
         LA    RWKA,0(RWKA)             CLEAR HIGH ORDER
         L     RWKE,PRFIOADR-1          ADDR OF DATA IN THIS BUFF UNIT
         LH    RWKC,PRFCOUNT            AMT OF DATA IN THIS BUFFER UNIT
         B     MOVE                     MOVE DATA IN THIS BUFFER UNIT
*
LASTMVE  EQU   *
         BCTR  RWKF,0                   MOVE REMAINING DATA
         EX    RWKF,DATAMVE             MOVE DATA TO TPER BUFFER
         NI    IOSFLB,AVTEFF-IOSLOG     TURN OFF LOG BIT.        Y02027
         SPACE 1
         TM    LCBFLAG3,LCBSOHR         SOH%R MESSAGE?           S99228
         BZ    RETEST                   BRANCH NO                S99228
         L     RWKE,LCBDCBPT            LOAD DCB ADDRESS         S99228
         L     RWKE,DCBTRANS-X01        LOAD TRANS TABLE ADDR    S99228
         L     R0,ZERO(R0,RWKE)         LOAD OUTPUT TRANS TABLE  S99228
*                                       ADDRESS                  S99228
         LTR   R0,R0                    EBCDIC MESSAGE?          S99228
         BZ    TPERLINK                 BRANCH TO XCTL           S99228
         L     RWKD,EWTCBUFF            LOAD DSECT BASE          Y02027
         LA    RWKE,FOUR(R0,RWKE)       LOAD ADDR INPUT TRANS TBLS99228
         TR    TPERDVED(FOUR),ZERO(RWKE)  TRANSLATE MESSAGE      S99228
         B     TPERLINK                 BRANCH TO XCTL           S99228
RETEST   EQU   *                                                 S99228
         SPACE
         TM    LCBFLAG3,LCBSOHC+LCBSOHR IS THIS SOH % E          S99238
         BZ    TPERLINK                 YES                      S99238
         SPACE
         TM    LCBFLAG3,LCBSOHC         IS THIS A SOH % C MSG    S99238
         BO    WTO                      YES, BRANCH FOR WRITE    S99238
         SPACE
         SPACE
TPERLINK EQU   *
         LR    RTST,RLINK               RESTORE RQE ADDR
         LH    RLINK,H6256              TPER XCTL ID
         OI    EWAFLG2,EWAMDR           INDICATE MDR RECORDING   Y02027
         L     RXCTL,CVTPTR             PICKUP ADDR OF CVT       Y02027
         L     RXCTL,XCTLADD(,RXCTL)    XCTL ADDRESS
         BR    RXCTL                    LINK TO XCTL
*
***********************************************************************
*                                                                     *
*        GET CORE FOR ERROR MSG                                       *
*                                                                     *
***********************************************************************
         SPACE
ERRCORE  EQU   *                                                 S99238
         TM    LCBFLAG3,LCBSOHC+LCBSOHR SOH%C OR SOH%R MSG?      S99228
         BO    BYPASS                   YES, RETURN              S99238
         LA    RWKB,EMSGLEN             ERROR RECORD GETMAIN LENGTH
*                                       INCLUDING HALF WORD SAVE AREA
*                                       AND STD TPER HDR
         BAL   RWKF,GETMAIN             GET CORE FOR TPER ERROR RECORD
         BNZ   BYPASS                   NO CORE, BYPASS RECORDIMG
*
         BAL   RWKF,BUILD              INITIALIZE THE BUFFER
*
         MVC   TPERSERN,0(RWKE)         MOVE THE SERIAL NUMBER
         L     RWKA,IOSUCB              PICKUP UCB ADDR        @ZA00237
         MVC   TPERCUU(TWO),FOUR(RWKA)  PUT CUU IN TPER RECORD
         B     TPERLINK                 LINK TO TPER
*
         DROP  RWKA
***********************************************************************
*                                                                     *
*        INITIALIZE TPER BUFFER WITH CONTROL INFORMATION              *
*                                                                     *
***********************************************************************
BUILD    EQU   *
         L     RWKD,EWTCBUFF            ADDRESS OF GOTTEN CORE   Y02027
         STH   RWKB,TPERTLEN            SAVE TOTAL LENGTH OF CORE
         LA    RWKD,FOUR(RWKD)          START OF TPER BUFFER
         ST    RWKD,EWTCBUFF            SAVE ADDR OF TPER BUFFER Y02027
         OI    LCBTSTSW,LCBSECON        SET SECOND ENTRY SWITCH
         SH    RWKB,AVTHA4              LENGTH OF TPER BUFFER
         SPACE
         TM    LCBFLAG3,LCBSOHC         SOH % C MESSAGE          S99238
         BO    BUILDC                   YES, SET OF BUFFER       S99238
         SPACE
         BCTR  RWKB,0                   DECREMENT LENGTH
         EX    RWKB,CLEAR               CLEAR CORE
         LA    RWKB,X01(RWKB)           ADD ONE
         STH   RWKB,TPERBLEN            SAVE LENGTH OF TPER BUFFER
         MVC   TPERXCTL(2),H9048        INITIALIZE XCTL INFORMATION
         L     RWKE,IOSUCB              PICKUP UCB ADDR        @ZA00237
         MVC   TPERCUU(TWO),FOUR(RWKE)  PUT CUU IN TPER RECORD   S99238
         TM    LCBFLAG3,LCBSOHR         SOH%R MESSAGE?           S99228
         BZ    IDINDATA                 BRANCH NO                S99228
         MVI   TPERRDID,SOHRID          SET SOH%R RECORD ID      S99228
         LA    RWKD,HDRLEN(RWKD)        ADJUST FOR HEADER        S99228
         LR    RWKE,RWKC                SAVE DATA ADDRESS        S99228
         BR    RWKF                     RETURN TO CALLER         S99228
IDINDATA EQU   *                                                 S99228
         MVC   TPERRDID,0(RWKC)         MOVE RECORD ID TO TPER BUFFER
         LA    RWKD,HDRLEN(RWKD)        ADJUST FOR SOH % E HDR   S99238
         CLI   0(RWKC),X'08'            2715 RECORD              S99238
         BNE   STANDARD                 NO                       S99238
         LA    RWKC,X02(RWKC)           BUMP PAST BLANKS         S99238
STANDARD EQU   *                                                 S99238
         LA    RWKE,X01(RWKC)           BUMP PAST ID             S99238
         BR    RWKF                     RETURN TO CALLER
*
***********************************************************************
*                                                                     *
*        FREE CORE GOTTEN FOR TPER BUFFER ON INITIAL ENTRY            *
*                                                                     *
***********************************************************************
FREECORE EQU   *
         L     RWKA,EWTCBUFF            ADDRESS OF TPER BUFFER   Y02027
         LA    RWKA,0(RWKA)             CLEAR HIGH ORDER
         SH    RWKA,AVTHA4              START OF GOTTEN CORE
         LH    RWKB,0(RWKA)             LENGTH OF GOTTEN CORE
         LA    RWKB,SEVEN(RWKB)         ROUND UP TO A MULTIPLE OF 8
         SRL   RWKB,THREE               FOR FREEMAIN
         SLL   RWKB,THREE               (CLEAR LOW ORDER BITS)
         FREEMAIN R,LV=(RWKB),A=(RWKA),SP=234  FREE TPER BUFFER. Y01004
         NI    LCBTSTSW,AVTEFF-LCBSECON RESET SECOND ENTRY SWITCH
BYPASS   EQU   *
EXIT     EQU   *
         NI    LCBFLAG3,AVTEFF-LCBOBRRD RESET TPER PROCESSING
         OI    SCBERR2,SCBSOHE          SET SOH %E ERROR BIT
         NI    IOSFLA,AVTEFF-(IOSERR+IOSEX) INDICATE ERROR IS    Y02027
*                                       CLEARED                  Y02027
         LR    RTST,RLINK               RESTORE RQE ADDR
         SVC   EREXCP                   RETURN TO LINE,ERROR CLEARED
         SVC   RETURN                   EXIT
*
***********************************************************************
*                                                                     *
*        GETMAIN ROUTINE                                              *
*                                                                     *
***********************************************************************
GETMAIN  EQU   *
         LA    RWKD,EWTCBUFF            LOCATION TO SAVE ADDRESS Y02027
*                                       OF GOTTEN CORE           Y02027
         GETMAIN EC,LV=(RWKB),A=(RWKD),SP=234,MF=(E,AVTGETMN)   SA66340
         LTR   R15,R15                  SET COND. CODE BEFORE RETURNING
         BR    RWKF                     RETURN
         EJECT
BUILDC   EQU   *                                                 S99238
         MVI   0(RWKD),BLANK            SET BLANK IN BUFFER      S99238
         BCTR  RWKB,0                   SUBTRACT ONE             S99238
         BCTR  RWKB,0                   SUBTRACT ONE             S99238
         EX    RWKB,BLANKS              BLANK OUTPUT BUFFER      S99238
         STH   RWKB,0(RWKD)             STORE LENGTH IN BUFFER   S99238
         MVC   TWO(10,RWKD),WTOFLAGS    SET FLAGS & MSG ID NO.   S99238
         L     RWKE,IOSUCB              PICKUP UCB ADDR        @ZA00237
         MVC   CUUOFF(3,RWKD),EBCDCUU(RWKE) EBCIDIC CUU ADDR     S99238
         LA    RWKD,SOHCHDR(RWKD)       ADJUST FOR SOH % C HDR   S99238
         LR    RWKB,RWKC                SAVE ROUTE CODE ADDR     S99238
         LA    RWKE,X01(RWKC)           BUMP PAST ROUTE CODE     S99238
         BR    RWKF                     RETURN TO CALLER         S99238
WTO      EQU   *                                                 S99238
         LA    RWKD,1(RWKF,RWKD)        UPDATE POINTER           S99238
         MVC   0(4,RWKD),WTOENDFG       MOVE WTO END FLAGS       S99238
         LR    R0,RWKD                  SET FOR SUBTRACT         S99238
         LA    RWKD,X02(RWKD)           ADD DESCRIPTOR LENGTH    S99238
         SPACE
         OI    0(RWKD),CDE2             INDICATE MASTER CONSOLE  S99238
         TM    0(RWKB),BIT6             ROUTE CODE 8 SPECIFIED   S99238
         BNO   TEST10                   NO, TEST FOR 10          S99238
         OI    0(RWKD),CDE8             INDICATE TP CONSOLE      S99238
         NI    0(RWKD),AVTEFF-CDE2      TURN OFF MASTER CONSOLE  S99238
         B     SETUPWRT                 SET UP FOR WRITE         S99238
TEST10   EQU   *                                                 S99238
         TM    0(RWKB),BIT5             ROUTE CODE 10 SPECIFIED  S99238
         BNO   SETUPWRT                 SET UP FOR WRITE         S99238
         OI    1(RWKD),CDE10            INDICATE SYS MAINT CON   S99238
         NI    0(RWKD),AVTEFF-CDE2      TURN OFF MASTER CONSOLE  S99238
SETUPWRT EQU   *                                                 S99238
         L     RWKB,EWTCBUFF            PICKUP BUFFER ADDRESS    Y02027
         LA    RWKB,0(RWKB)             CLEAR HIGH               S99238
         SR    R0,RWKB                  GET CORRECT LENGTH       S99238
         STH   R0,0(RWKB)               SET MSG LENGTH           S99238
         WTO   MF=(E,(RWKB))            WRITE MSG ON CONSOLE     S99238
         NI    LCBFLAG3,AVTEFF-LCBSOHC  RESET C MSG BIT          S99238
         B     FREECORE                 PREPARE FOR EXIT         S99238
*
*
CLEAR    XC    TPERBLEN(0),TPERBLEN     CLEAR BUFFER
*
DATAMVE  MVC   0(0,RWKD),0(RWKE)        MOVE DATA                S99238
*
BLANKS   MVC   X01(0,RWKD),0(RWKD)      MOVE BLANKS              S99238
*
         EJECT
***********************************************************************
*                                                                     *
*        CONSTANTS AND EQUATES                                        *
*
***********************************************************************
H8       DC    H'8'                     CONSTANT OF EIGHT
H6256    DC    H'6256'                  XCTL INFO FOR IGE0625F
H9048    DC    H'9048'                  XCTL INFO FOR IGE0904H
LCBSIZE  DC    AL2(LCBFLAG1-IEDQLCB)    DISP. OF IOB IN LCB
PATCH    DC    10F'0'                   PATCH AREA
STXENQCH EQU   11                       OFFSET TO STX ENQ SEQ.
LCBSECON EQU   X'01'                    GETMAIN SWITCH
HDRLEN   EQU   26                       LENGTH OF TPER HEADER    S99238
EMSGLEN  EQU   32                       LENGTH OF ERROR MSG
ONE      EQU   1                        ONE                     SA65392
TWO      EQU   2                        TWO
SEVEN    EQU   7                        SEVEN
THREE    EQU   3                        THREE
LINEEND  EQU   X'C2'                    SW TO RETURN CONTROL TO LINEEND
X01      EQU   1                        ONE
EREXCP   EQU   15                       ERROR EXCP
RETURN   EQU   3                        SVC 3
XCTLADD  EQU   X'2C'                    DISP OF XCTL ADDRESS IN CVT
IOBOFFST EQU   4                        OFFSET OF IOB ADDR IN RQE
CVTPTR   EQU   16                       CVT POINTER ADDRESS
FIVE     EQU   5                        FIVE
FOUR     EQU   4                        FOUR
ZERO     EQU   0                                                 S99228
SOHRID   EQU   X'03'                    SOH%R TPER RECORD ID     S99228
X02      EQU   X'02'                    END OF  UNIT OR BUFFER   S99238
CUUOFF   EQU   12                       CUU OFFSET IN BUFFER     S99238
EBCDCUU  EQU   13                       OFFSET TO EBCIDIC CUU    S99238
SOHCHDR  EQU   15                       LENGTH OF SOH % C HDR    S99238
CDE2     EQU   X'40'                    MASTER CONSOLE           S99238
CDE8     EQU   X'01'                    TP CONSOLE               S99238
CDE10    EQU   X'40'                    MAINTANCE CONSOLE        S99238
BIT5     EQU   X'04'                    CODE 8                 @ZA02623
BIT6     EQU   X'02'                    CODE 2                 @ZA02623
BLANK    EQU   X'40'                    BLANK FOR CLEARING BUFFERS99238
WTOFLAGS DC    X'8000'                  MCS FLAGS                S99238
WTOENDFG DC    X'10000000'              DESCR AND ROUTE FLAGS    S99238
         EJECT
         TAVTD
         TDEBD
         DCBD  DSORG=TX
         TLCBD
         TPRFD
         TSCBD
         TTRMD
         IECDIOSB                                                Y02027
         IECDERWA EWTC
TPERD    DSECT
TPERTLEN DS    H                        TOTAL LENGTH OF GOTTEN CORE
         DS    H                        UNUSED
*  THE FOLLOWING IS A DSECT OF THE BUFFER PASSED TO TPER, THE ADDRESS
* IS SAVED AT LCBTSTSW
         ORG   TPERD                    RESET TO ZERO
TPERBLEN DS    H                        LENGTH OF TPER BUFFER
TPERSW   DS    XL1                      SWITCHES
TPERXCTL DS    XL2                      XCTL INFO
TPERRDID DS    X                        RECORD IDENTITY
         DS    18C                      USED FOR STD HEADER INFO
TPERCUU  DS    XL2                      CUU ADDRESS
TPERDVED EQU   *                        START OF DEVICE DEPENDENT DATA
TPERSERN DS    XL2                      DEVICE SERIAL NO.
         END
