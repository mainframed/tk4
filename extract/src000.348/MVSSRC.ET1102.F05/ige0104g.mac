104G     TITLE 'UNIT CHECK (EXCEPT TIME OUT) ON READ/WRITE CCWS - ERP'
IGE0104G CSECT
*A-000000-999999                                               @Y02X6E0
         SPACE 3
*CHANGE ACTIVITY= AS FOLLOWS:
*A677300                                                         S22029
*D678000-686000                                                  S22029
*C212000-224000                                                  A48248
*A443100                                                         S22025
*C002000,014000-018000,242000                                    S22025
*D228000-230000,382000-421300,496000-564000,776000,786000-788000 S22025
*C065000,682000,683000,778000-787000                             S22025
*C251000                                                         A47148
*A168000,720000,853600                                           A47148
*A440000                                                         A44916
*A853600                                                         CLUP21
*C048000,062000-068000,102000,114000,138000,148000-150000        CLUP21
*C160000,162000,178000,196000-200000,204000-206000,216000,230000 CLUP21
*C250000,258000,272000,282000,286000,290000-292000,308000,326000 CLUP21
*C330000,344000-346000,356000-358000,362000-368000,382000,386000 CLUP21
*C392000-394000,398000,402000,414000,420500,420620-420980,421000 CLUP21
*C454000,458000-464000,468000,480000-482000,492000-494000,518400 CLUP21
*C556000,564000,582000,598600,602000,640226,620430,620452,620458 CLUP21
*C670000-672000,676400-676500,677500,678000-680000,704000        CLUP21
*C740000-742000                                                  CLUP21
*D268000,318000,572000,798000-800000,806000,832000,842000-844000 CLUP21
*C184000-186000,394000,460000,480000-484000,490000,498000         M5576
*C504000-508000,606080,606200                                     M5576
*A420500                                                          M6239
*D420960                                                          M6239
*C014000,016000                                                  S21903
*440000                                                         SA59015
*C251000,296000                                                 SA57694
*A441500,760000,816000                                          SA57694
*A677000,677280                                                 SA61794
*C194000-196000,677240-677245,677270                             X02004
*A677030-677060,853610-853620,853907-853984                      X02004
*D677120,677190                                                  X02004
*C014000,016000                                                  S21903
*A440200-440300                                                  A59015
*A 338100-339200,390000-424100,853610,853908-853988              X01004
*C 194000-196000,282000,306000,332000-335000,374000-377000       X01004
*C 442900-445600,620120,708000                                   X01004
*D 283000-284000,308000-314000,336000,378000,426000-442000       X01004
*D 710000-712000,754000-760000                                   X01004
*A047000,048100,054500,074000,092500,096500,137000,154600-155200,Y02027
*A165000,171000,331000,367000,677010,870900                      Y02027
*C032000,038000,046000,046700,354000,072000-073000,096000,142000,Y02027
*C372000,396000-412000,422000,422200-422300,423700               Y02027
*C018000,172000                                                @Z30AAEE
*D838000                                                       @Z30AAEE
*A677340                                                       @SA61084
*A677260                                                       @SA74549
*D677360,677380                                                @SA74549
*C332000                                                       @SA73344
*A442900                                                       @SA73344
*C442960                                                       @YA10251
*   PACKAGING PROBLEM                                          @OZ09954
*D677005,A677060                                               @OZ09950
*C677060,D677340                                               @OZ24139
*C677050,D677065-677090                                        @OY19517
*C677060                                                       @OZ31467
*C677050,677053-677055                                         @OZ29398
         SPACE 3
***********************************************************************
*                                                                     *
*MODULE-NAME= IGE0104G                                           S21903
*                                                                S21903
*DESCRIPTIVE-NAME= START/STOP ERP (SECOND LOAD)                  S21903
*                                                                S21903
*COPYRIGHT= 'NONE'                                               S21903
*                                                                     *
*  STATUS: CHANGE LEVEL 8                                      @Z30AAEE
*                                                                     *
*FUNCTION -- TO ATTEMPT RETRY ON START STOP READ/WRITE CCW'S          *
*                                                                     *
*ENTRY POINTS -- FIRST EXECUTABLE INSTRUCTION.                        *
*                                                                     *
*INPUT --                                                             *
*   R1 - POINTER TO I/O SUPERVISOR BLOCK                         Y02027
*   R15 - ENTRY POINT ADDRESS                                         *
*                                                                     *
*OUTPUT -- R1 POINTS TO I/O SUPERVISOR BLOCK                     Y02027
*                                                                     *
*EXTERNAL ROUTINE -- IGG019QE (AOS/TCAM SIO APPENDAGE) -- TO CONVERT  *
*   VIRTUAL CCW ADDRESSES TO REAL BEFORE RETRY.                       *
*                                                                     *
*EXITS-NORMAL -- R1 POINTS TO I/O SUPERVISOR BLOCK               Y02027
*        SVC   15   'ERP IN CONTROL (IOSERR)' AND 'EXCEPTIONAL   Y02027
*                CONDITION(IOSEX)'. RETURN TO IOS TO RETRY THE   Y02027
*                   ERROR.                                       Y02027
*        SVC   3                                                      *
*                                                                     *
*        SVC   15   'NO ERROR FLAGS'. RETURN TO LINE END         Y02027
*                   APPENDAGE. ERROR CLEARED                     Y02027
*        SVC   3                                                      *
*                                                                     *
*   TO SCHEDULE NEXT LOAD OF ERP                                      *
*        LH    13,FIELD                 FIELD HAS DECIMAL VALUE  CLUP21
*                                         OF NEXT LOAD           CLUP21
*        L     14,CVTPTR                ADDRESS OF CVT           CLUP21
*        L     14,44(14)                BRANCH-AND-LINK ENTRY    S22025
*                                         TO XCTL ROUTINE        CLUP21
*        BR    14                       ENTER XCTL ROUTINE       CLUP21
*                                                                     *
*EXITS-ERROR -- R1 POINTS TO I/O SUPERVISOR BLOCK                Y02027
*        SVC   15   'EXCEPTIONAL CONDITION(IOSEX)'. RETURN TO    Y02027
*                   LINE END APPENDAGE. ERROR PERMANENT.         Y02027
*        SVC   3                                                      *
*                                                                     *
*TABLES/WORK AREAS --                                                 *
*   TAVTD                                                             *
*   TTRMD                                                             *
*   TCCWD                                                             *
*   TLCBD                                                             *
*   DCBD                                                              *
*   TSCBD                                                             *
*   IECDIOSB                                                     Y02027
*                                                                     *
*ATTRIBUTES -- SERIALLY REUSABLE, REFRESHABLE, SUPERVISOR KEY,   Y02027
*              SUPERVISOR MODE, ENABLED                          Y02027
*                                                                     *
*NOTES -- THE OPERATION OF THIS MODULE DOES NOT DEPEND UPON A         *
*   PARTICULAR INTERNAL REPRESENTATION OF THE EXTERNAL CHARACTER CLUP21
*   SET.                                                              *
*                                                                     *
*   R1 REMAINS TRANSPARENT TO ALL LOADS OF ERP.                       *
*                                                                     *
*   EXIT FROM ERP IS MADE WHENEVER THE RETRY COUNT IS EXHAUSTED, OR   *
*   A 'SHOULD-NOT-OCCUR' ERROR IS DETECTED. TEXT ERRORS NOT      CLUP21
*   RECOVERABLE BY ERP ARE POSTED TO LINE END APPENDAGE WITH ERROR    *
*   (VIA SVC 15).                                                     *
*                                                                     *
***********************************************************************
         EJECT
         USING *,R15
         USING IEDQLCB,RLCB
         USING IEDQCCW,RCCW
         USING IEDQAVTD,RAVT
         USING IEDQSCB,RSCB
         USING IHADCB,RDCB
         USING IOSB,RIOSB                                        Y02027
         SPACE 1
*        REGISTER USAGE                                          CLUP21
R0       EQU   0
RIOSB    EQU   1                        ADDR OF I/O SUPVR BLOCK  Y02027
R2       EQU   2                        WORK REGISTER
RUCB     EQU   3                        UCB REGISTER
RLCB     EQU   4                        LCB REGISTER             CLUP21
RSCB     EQU   5                        SCB REGISTER             CLUP21
RCCW     EQU   6                        CCW REGISTER
RWKA     EQU   7                        WORK REGISTER
RTST     EQU   8                        ADDR OF REQUEST QUEUE    Y02027
*                                       ELEMENT                  Y02027
RWKC     EQU   8                        WORK REGISTER
RWKB     EQU   9                        WORK REGISTER
RDCB     EQU   10                       DCB REGISTER             CLUP21
RAVT     EQU   11                       AVT REGISTER             CLUP21
RLINK    EQU   13                       LINKAGE FOR SUBSEQUENT LOAD
RSAVE    EQU   13                       SAVE REGISTER FOR IOSB   Y02027
RXCTL    EQU   14                       XCTL REGISTER
R15      EQU   15                       ADDRESSABILITY REGISTER
IGE0104G IEDHJN DATE                    MODULE ID AND DATE       S22025
         EJECT
         L     RTST,IOSUSE              PICKUP ADDR OF RQE       Y02027
         L     RUCB,IOSUCB              ADDRESS OF UCB         @Z30AAEE
         L     RLCB,IOBOFFST(R0,RTST)   ADDRESS OF IOB
         SH    RLCB,LCBBKUP             BACK UP TO LCB
         MVI   LCBCSW+5,AVTEZERO        1ST BYTE OF RESIDUAL CNT CLUP21
*                                         UNPREDICATABLE-PROGRAM CK
         L     RDCB,LCBDCBPT            DCB BASE
         MVC   LCBERCCW+7(1),D19(RUCB)  ADAPTER TYPE              M5576
         NI    LCBERCCW+7,XF0           CLEAR CONTROL UNIT TYPE   M5576
         L     RSCB,LCBSCBA-1           SCB BASE SET
         TM    SCBQTYPE,SCBCONC         CONC                     S22026
         BZ    NOCONC                   BRANCH IF NO             S22026
         L     RSCB,LCBSCBDA-1          SET LINE SCB ADDR        S22026
NOCONC   EQU   *                                                 S22026
         L     RCCW,LCBCSW-1            FAILING CCW+8
         SH    RCCW,H8                  ADJUST TO ENDING CCW
         SPACE
         TM    CCWOPCDE,SEVEN           TIC (CHANNEL PROGRAM     X01004
*                                         CHECK)                 X01004
         BNZ   NOPC                     BRANCH IF NO.            X01004
         SPACE
         SH    RCCW,H8                  BACK UP TO TEXT CCW      CLUP21
         MVI   LCBCSW+6,AVTEZERO        CLEAR RESIDUAL COUNT     CLUP21
         SPACE
NOPC     EQU   *
         L     RWKB,LCBSTART-1          ASSUME RESTART FROM FIRSTCLUP21
         L     RAVT,CVTPTR              PICK UP CVT ADDRESS      CLUP21
         L     RAVT,AVTCVTPT(,RAVT)     LOAD LIST
         L     RAVT,0(,RAVT)            AVT BASE
         CLI   LCBINCAM+1,AVTEZERO      TEST INDEX BYTE          A48248
         MVI   LCBINCAM+1,AVTEZERO      CLEAR INDEX BYTE         A48248
         BNE   TXTERR                   TEXT ERROR RETRY         A48248
         SPACE
         SPACE
         TM    LCBSENS0,EQUIPCHK        EQUIPMENT CHECK SET
         BO    SNO                      YES, TREAT AS PERMANENT
         SPACE
         TM    LCBSENS0,LOSTDATA        LOST DATA SET
         BZ    TRYIR                    NO TRY INTERVENTION REQ  S22025
         SPACE
         TM    CCWOPCDE,CCWWRITE        WRITE COMMAND
         BO    SNO                      BRANCH YES
         SPACE
         CLI   LCBERCCW+7,TWX          TWX                      SA57694
         BNE   NOTTWX                   BRANCH NO
         SPACE
         CLI   LCBRESTR,TPTWXID         TWX ID
         BNE   NOTTWX                   BRANCH IF NO             CLUP21
         SPACE
REDIAL   EQU   *
         LA    RWKB,LCBCPA+16           RE-DIAL
         B     CHKCOUNT                 CHECK RETRY COUNT
         SPACE
NOTTWX   EQU   *
         CLI   LCBRESTR,TPRDRPEB        TEXT RESPONSE
         BE    NORETRY                  BRANCH IF YES            CLUP21
         SPACE
         CLI   LCBRESTR,TPRDSKIP        TEXT RELATED CCW
         BL    CHKCOUNT                 BRANCH NO
         SPACE
         OI    LCBCHAIN,LCBNORTY        NO RETRY POSSIBLE
         LH    RWKA,LCBCSW+5            PICK UP RESIDUAL COUNT   CLUP21
         SPACE 1
         LTR   RWKA,RWKA                IS IT ZERO
         BNZ   CHKRETRY                 NO, READ SKIP NOT NEEDED CLUP21
         SPACE
SKIPEXIT EQU   *
         MVI   LCBINCAM+1,RDSKIP        SET RETURN FOR IGE0504G  CLUP21
         ST    RLCB,LCBERCCW            SET ANY VALID ADDRESS    CLUP21
         MVI   LCBERCCW,CCWREAD         SET COMMAND CODE
         MVC   LCBERCCW+4(4),FLAGCNT    SET FLAGS AND COUNT
COMMON   EQU   *                                                SA57694
         SPACE
         MVC   LCBSNSV,LCBSENS0         SAVE CURRENT SENSE
         MVC   LCBCSWSV,LCBCSW          SAVE CURRENT CSW
         LA    RWKB,LCBERCCW            START ADDRESS
         B     NOSTOP                   BRANCH TO SET UP FOR RETRY.
         SPACE 2
CHKRETRY EQU   *
         OI    SCBERR4,SCBTXTTN         TEXT ERROR
         SPACE
         CLI   LCBINCAM,RETRY           RETRY LIMIT REACHED
         BE    PERMEXIT                 BRANCH IF YES            CLUP21
         SPACE
         NI    IOSFLA,AVTEFF-IOSERR     INDICATE PERMANENT ERROR Y02027
         B     CONVERT                  POST TO LINEEND        @SA73344
         SPACE 2
TRYRETRY EQU   *
         CLI   LCBRESTR,TPTWXID         TWX ID
         BE    REDIAL                   BRANCH YES               CLUP21
         SPACE
         TM    LCBCPA+16,DIAL           DIAL INITIAL CONTACT     CLUP21
         BZ    CHKCOUNT                 BRANCH NO
         SPACE
         LA    RWKB,LCBCPA+32           SKIP TO WRITE COMMAND
         SPACE
CHKCOUNT EQU   *
         CLI   LCBINCAM,RETRY           RETRY LIMIT REACHED      CLUP21
         BNL   PERMEXIT                 BRANCH IF YES            CLUP21
         SPACE
         IC    RWKA,LCBINCAM            GET RETRY COUNT          CLUP21
         LA    RWKA,1(,RWKA)            INCREMENT                CLUP21
         STC   RWKA,LCBINCAM            AND SAVE                 CLUP21
         SPACE
NOSTOP   EQU   *                                                 Y02027
         ST    RWKB,LCBSTART-1          START ADDRESS
         MVI   LCBSENS0,AVTEZERO        CLEAR FOR RETRY
         B     CONVERT                  GO CONVERT CCW ADDRESSES TO
*                                       REAL FOR RETRY.
         SPACE 2
TRYIR    EQU   *
         TM    LCBSENS0,INTREQ          INTERVETNION REQUIRED
         BZ    BOERR                    NO, MUST BE BUSOUT
         SPACE
         CLI   LCBRESTR,TPRDSKIP        TEXT RELATED CCW
         BNL   CKTSO                    BRANCH IF TEXT RELATED
         SPACE
         CLI   LCBRESTR,TPRDRPEB        RESPONSE TO TEXT
         BE    NORETRY                  BRANCH YES
         SPACE 1
         CLI   CCWOPCDE,CCWREAD         READ COMMAND            SA59015
         BNE   NORETRY                  BR NO, FORGET RETRY     SA59015
         SPACE
         MVI   LCBCSWSV,AVTEFF          SET SENSE INDICATOR FOR  A44916
*                                         STOPLINE               A44916
         CLI   LCBERCCW+7,TWX          TWX                      SA57694
         BE    BREAK                   BREAK COMMAND            SA57694
         B     TRYRETRY                 ATTEMPT RETRY
         SPACE
CKTSO    EQU   *                                                 CLUP21
*        INTERVENTION REQUIRED ON WRITE TEXT IS NORMAL FOR TSO
         SPACE
         TM    LCBTSOB,LCBTSBUF         TSO IN CONTROL
         BZ    NORETRY                  BRANCH IF NO
         SPACE
         CLI   CCWOPCDE,CCWWRITE        WRITE COMMAND
         BNE   NORETRY                  BRANCH IF NOT WRITE TEXT
         SPACE
         CLI   LCBRESTR,TPTEXT          TEXT CCW
         BNE   NORETRY                  BRANCH IF NOT TEXT TRANSFER
         NI    IOSFLA,AVTEFF-(IOSERR+IOSEX)  SET NORMAL ENDING @SA73344
         NI    LCBCSW+3,AVTEFF-X'02'    RESET U.C. FLAG        @YA10251
***********************************************************************
*                                                                     *
*        THE FOLLOWING CODE IS ADDED FOR AOS.                         *
*                                                                     *
CONVERT  EQU   *
**************************************************************** Y02027
*        THE FOLLOWING CODE MAPS THE NECESSARY FIELDS FROM THE   Y02027
*        LINE CONTROL BLOCK TO THE I/O SUPERVISOR BLOCK FOR EXCP Y02027
*        COMPATABILITY DURING RETRY OPERATIONS FROM ERP          Y02027
**************************************************************** Y02027
         MVC   IOSCSW(SEVEN),LCBCSW     MAP CSW FROM LCB TO IOSB Y02027
         MVC   IOSSNS(TWO),LCBSENS0     MAP SENSE INFO FROM LCB  Y02027
*                                       INTO IOSB                Y02027
         MVC   IOSCC(ONE),LCBSIOCC      MAP START I/O CONDITION  Y02027
*                                       CODE FROM LCB TO IOSB    Y02027
         CLI   LCBECBCC,HIOCC           IOHALT ISSUED            Y02027
         BNE   SVCS                     BRANCH NO                Y02027
         MVI   LCBINCAM+1,HIOCC         SET FOR LINEEND          Y02027
         NI    IOSFLA,X'FF'-(IOSERR+IOSEX) PREVENT RETRY         Y02027
SVCS     EQU   *                                                 Y02027
         TM    IOSFLA,IOSERR+IOSEX      ARE WE DOING A RETRY     Y02027
         BNO   NOTRETRY                 BRANCH IF NO.            X01004
         L     RTST,IOSUSE              PICKUP ADDR OF RQE       Y02027
         LR    RSAVE,RIOSB              SAVE IOSB ADDRESS        Y02027
         L     R2,IOBOFFST(,RTST)       GET IOB ADDRESS FROM RQE X01004
*                                       FOR USE BY SIO APPENDAGE.
         L     RWKC,DEBPTR(,RTST)       GET DEB ADDRESS FROM RQE X01004
         LA    R15,DEBNMSUB-IEDQDEB     GET APDG. TABLE SIZE     Y02027
         SLR   RWKC,R15                 POINT TO APDG. TABLE     Y02027
         L     RWKC,SIOPTR(ZERO,RWKC)   GET ADDR OF SIO APPEND-  X01004
*                                       AGE FROM APPENDAGE VECTOR
*                                       TABLE.
         SR    R15,R15                  CLEAR R15 TO TELL SIO AP-
*                                       PENDAGE IT'S A SUBROUTINE.
         BAL   RXCTL,SIOENTRY(ZERO,RWKC)  LINK TO SIO APPNDG AT  X01004
*                                       OFFSET +16 TO CONVERT CCW'S
*                                       FROM VIRTUAL TO REAL.
         LR    RIOSB,RSAVE              RESTORE IOSB ADDR        Y02027
*                                                                     *
*        END OF CODE ADDED FOR AOS.                                   *
*                                                                     *
***********************************************************************
NOTRETRY EQU   *                                                 X01004
         SVC   EREXCP                                            S22025
         SVC   RETURN                                            S22025
         SPACE
BOERR    EQU   *
         OI    SCBERR4,SCBCHANN         SET CHANNEL ERROR
         LR    RWKB,RCCW                RESTART ON FAILING
         SPACE
         TM    LCBSIOCC,SIOCC1          SIO COND CODE=1
         BO    CHKCOUNT                 BRANCH YES TO RETRY      CLUP21
         SPACE
         TM    CCWOPCDE,CCWREAD         READ COMMAND
         BO    SNO                      BRANCH YES               CLUP21
         SPACE
         CLI   LCBERCCW+7,WTTA          WTTA                     CLUP21
         BE    CHKRETRY                 BRANCH YES TO RETRY      CLUP21
         SPACE
         TM    CCWOPCDE+8,CCWREAD       NEXT CCW A READ
         BO    SKIPEXIT                 BRANCH YES               CLUP21
         SPACE
         CLI   LCBRESTR,TPRDRPEB        RESPONSE TO TEXT
         BE    NORETRY                  BRANCH IF YES
         SPACE
         CLI   LCBRESTR,TPRDSKIP        TEXT RELATED CCW
         BNL   NORETRY                  BRANCH IF YES
         SPACE
         MVC   LCBERCCW+7(1),D19(RUCB)  ADAPTER TYPE              M5576
         NI    LCBERCCW+7,XF0           CLEAR CONTR UNIT BITS     M5576
         SPACE
         CLI   LCBERCCW+7,X20           TYPE ONE OR TWO           M5576
         BNH   SKIPEXIT                 BRANCH EITHER
         SPACE
         CLI   LCBERCCW+7,X80           TYPE THREE ADAPTER        M5576
         BE    SKIPEXIT                 BRANCH YES               CLUP21
         SPACE
         B     CHKRETRY                 ELSE RETRY               CLUP21
         SPACE
         SPACE
SNO      EQU   *
         OI    SCBERR4,SCBCTLUN         CONTROL UNIT ERROR
         MVI   LCBINCAM+1,POSNO         SET RETURN
         B     LOADSDR                  USE COMMON CODE
         SPACE
PERMEXIT EQU   *
         MVI   LCBINCAM+1,POPERM        SET RETURN TO IGE0504G   CLUP21
         SPACE
LOADSDR  EQU   *
         LH    RLINK,POSTMOD            POST MODULE
         L     RXCTL,CVTPTR             CVT ADDRESS
         L     RXCTL,XCTLADD(,RXCTL)    XCTL ROUTINE ADDRESS
         BR    RXCTL                    EXIT
         SPACE
TXTERR   EQU   *
         CLI   LCBINCAM,RETRY           RETRY LIMIT REACHED      CLUP21
         BNL   PERMEXIT                 BRANCH IF YES
         SPACE
         TM    LCBSTAT1,LCBRECVN        RECEVING
         BZ    CHKRETRY                 BRANCH NO                CLUP21
         SPACE
         TM    LCBCSW+3,UNEX            UNIT EXCEPTION
         BNZ   NORETRY                  YES, RETRY NOT POSSIBLE
         SPACE
         CLI   LCBERCCW+7,TYPE3         2260 REMOTE, TYPE 3 ADPTR M5576
         BE    GORETRY                  BRANCH IF YES
         SPACE
         CLI   LCBERCCW+7,TYPE1         TYPE ONE ADAPTER          M5576
         BNE   NORETRY                  BRANCH IF NO
         SPACE
         TM    LCBTSOB,LCBTSBUF         IF TSO USE, ALLOW NO RETRY
         BO    NORETRY                  BRANCH IF YES
         SPACE
         CLI   UCBTYP(RUCB),T1060       IS IT A 1060
         BE    NORETRY                  BRANCH IF YES
         SPACE
GORETRY  EQU   *
         CLI   LCBRESTR,TPRDSKIP        TEXT RELATED ERROR
         BE    NORETRY                  YES, NO RETRY FOR READ SKIP
         SPACE
         USING IEDQPRF,RWKA
         LA    RCCW,0(,RCCW)            THIS THE INTERRUPT
         L     RWKB,LCBCPA+12           POSSIBLE RESTART TEXT CCW
         L     RWKA,LCBLSPCI-1          BUFFER ADDRESS
         SPACE
         TM    PRFSTAT1,PRFNHDRN        IS IT HEADER
         BO    NOTFIRST                 BRANCH IF NO
         SPACE
         CLM   RWKB,SEVEN,LCBLSPCI      DID WE START ON 1ST UNIT X01004
         BNE   NOTFIRST                 NO, WE ALREADY RCV'D A BLK
         SPACE
*        I/O GENERATOR TURNS OFF SLI BIT ON CONTINUES OPERATIONS
         SPACE
         TM    PRFFLAGS,CCWSLI          RECEIVE A BLOCK IN HEADER
         BO    BLDBUFF                  BRANCH IF WE ARE ON 1ST BLK
         SPACE
NOTFIRST EQU   *
         LA    RWKB,AVTEZERO(,RWKB)     CLEAR HI ORDER BYTE
         SPACE
LOOPBUFF EQU   *
         LA    RWKA,AVTEZERO(,RWKA)     CLEAR HIGH ORDER BYTE    CLUP21
*
*              RWKB POINTS TO FIRST CCW IN CHAIN AFTER LAST EOB
*        RESTART. IT MIGHT HAVE BEEN FREED BY PCI. A TEST IS ALSO
*        MADE TO DETERMINE IF IT HAS BEEN FREED AND RE-ASSIGNED.
*
         CLR   RWKB,RWKA                LAST EOB RESTART THIS BUFFER
         BE    TESTWRAP                 BRANCH IF YES, SEE IF IT
*                                         HAS BEEN RE-ASSIGNED
         SPACE
         CLR   RWKA,RCCW                END OF SEARCH (RESTART CCW
*                                         NO LONGER IN BUFFER CHAIN
         BE    CHKRETRY                 BRANCH IF YES, CHECK RETRIES
         SPACE
         L     RWKA,PRFTIC              GET NEXT UNIT (BUFFER)
         B     LOOPBUFF                 TEST NEXT CCW IN CHAIN   CLUP21
         SPACE
TESTWRAP EQU   *
*              THE FOLLOWING TEST CHECKS IF RESTART CCW HAS BEEN CLUP21
*        RE-ASSIGNED.
         SPACE
         TM    CCWFLAGS-CCW(RWKB),CCWSLI I/O GENERATOR TURNS OFF SLI
*                                         ON A RESTART OR RECALL
         BO    CHKRETRY                 BRANCH IF BUFFER HAS BEEN
*                                       RE-USED. A RE-CALL IS NEEDED
         SPACE
         B     WRNAK                    ATTEMPT RETRY
         SPACE
BLDBUFF  EQU   *
         SPACE
*              HEADER BUFFER MUST BE RECONSTRUCTED TO ACCOUNT    CLUP21
*        FOR POLLING, IDLES, AND LCIN.                           CLUP21
         SPACE
         SR    RWKC,RWKC                CLEAR WORK REGISTER
         IC    RWKC,LCBISZE             IDLES
         SPACE
         CLI   LCBCPA+24,CCWPREP        CONTENTION
         BE    NOBUMP                   BRANCH YES
         SPACE
         TM    LCBSTAT2,LCBDIAL         DIAL LINE                CLUP21
         BZ    NOTDIAL                  BRANCH NO                CLUP21
         SPACE
         CLI   LCBCPA+40,CCWPREP        DIAL CONTENTION
         BE    NOBUMP                   BRANCH YES
         SPACE
NOTDIAL  EQU   *
         SR    RWKB,RWKB                CLEAR WORK REGISTER     SA61794
         IC    RWKB,LCBUCBX             RLN-1                   SA61794
         SLL   RWKB,X4                  MULTIPLY BY FOUR        SA61794
         L     RWKB,DCBINVLI(RWKB)      INVLIST ADDRESS         SA61794
         L     RTST,IOSUSE              GET RQE ADDRESS        @OY19517
         L     R2,DEBPTR(,RTST)         GET DEB ADDR FROM RQE  @OY19517
         LA    R0,DEBNMSUB-IEDQDEB      GET APDG TABLE SIZE    @OZ29398
         SLR   R2,R0                    POINT TO APDG TABLE    @OZ29398
         TM    AUTOFLG(RWKB),AUTOPL     AUTOPOLL SPECIFIED     @OZ29398
         BO    OWNMH                    BRANCH YES             @OZ31467
         L     RTST,IOSUSE              GET RQE ADDRESS        @OZ09950
         LH    RWKB,LCBTTCIN            GET CONNECTED INDEX      S22029
         LTR   RWKB,RWKB                IS IT ZERO               S22029
         BZ    OWNMH                    BRANCH IF INDEX IS ZERO  S22029
         LR    R0,RWKA                  SAVE BUFFER ADDRESS      S22029
         L     RWKA,AVTRNMPT            GET TNT CODE BASE        S22029
         LH    RWKB,LCBTTCIN            GET CONNECTED INDEX      S22029
         BAL   RCCW,TNTDCODE-IEDQTNTD(RWKA) GET TERM TABLE ENTRY S22029
         L     RWKC,AVTEZERO(RWKC)      GET DEST QCB ADDRESS     S22029
         LR    RWKA,R0                  RESTORE BUFFER ADDRESS   S22029
         TM    QCBDSFLG-IEDQQCB(RWKC),QCBALTMH IS TERMINAL       S22029
*                                       CONNECTED TO ALTMH       S22029
         BZ    OWNMH                    NO - USE OWNING MH LC    S22029
         LA    R2,ONE(,R2)              SET TO ALTERNATE MH      X02004
*                                         OPTION BITS
OWNMH    SR    RWKC,RWKC                CLEAR WORK REGISTER      S22029
         IC    RWKC,LCBISZE             GET IDLES COUNT          S22029
         CLI   LCBERCCW+7,TYPE3         ISIT 2260              @SA74549
         BNE   NOT2260                  BRANCH NO              @SA74549
         BCTR  RWKC,0                   DECREMENT ISIZE        @SA74549
NOT2260  EQU   *                                               @SA74549
         TM    0(R2),LCIN               LCIN SPECIFIED           X02004
         BZ    NOBUMP                   BRANCH NO, IT'S LC=OUT   S22029
         SPACE
         LA    RWKC,1(,RWKC)            BUMP IDLES, DON'T        CLUP21
*                                         OVERLAY THE EOA        CLUP21
         SPACE
NOBUMP   EQU   *
         LA    RWKC,PRFSHDR-PRFSUNIT(,RWKC)
         SPACE
         LH    RWKB,AVTKEYLE            GET UNIT SIZE
         CH    RWKB,DCBBUFSI            COMPARE WITH BUFFER SIZEE
         BNH   SETSIZE                  BRANCH LESS OR EQUAL
         SPACE
         LH    RWKB,DCBBUFSI            GET BUFFER SIZE
         SPACE
SETSIZE  EQU   *
         SR    RWKB,RWKC                SET COUNT
         STH   RWKB,PRFCOUNT            AND SAVE                 CLUP21
         LA    RWKC,PRFSUNIT-IEDQPRF(RWKC,RWKA)
         STCM  RWKC,SEVEN,PRFIOADR      SET ADDRESS.             X01004
         LR    RWKB,RWKA                GET BUFFER ADDRESS
         SPACE
WRNAK    EQU   *
         SR    RWKC,RWKC                CLEAR WORK REGISTER
         L     R2,DCBSCTAD-1            SCT ADDRESS
         IC    RWKC,NAKCH(,R2)          GET NAK ADDR OFFSET      A47148
         SPACE
         LA    RWKC,1(RWKC,R2)          GET NAK ADDRESS
         ST    RWKC,LCBCPA+16           SET WRITE NAK ADDDRESS
         MVI   LCBCPA+16,CCWWRITE       SET WRITE COMMAND
         MVI   LCBCPA+20,CCWCC+CCWSLI   SET FLAGS                CLUP21
         MVI   LCBCPA+23,X01            SET COUNT                CLUP21
         ST    RWKB,LCBCPA+24           SET TIC ADDRESS
         MVI   LCBCPA+24,CCWTIC         SET TIC COMMAND
         LA    RWKB,LCBCPA+16           RESTART ADDRESS
         MVI   LCBTPCD+2,TPWRAKNK       SET TP OP CODE
         B     CHKCOUNT                 CHECK RETRY COUNT
         SPACE
NORETRY  EQU   *
         OI    LCBCHAIN,LCBNORTY        SET NO RETRY POSSIBLE
         B     PERMEXIT                 PERM ERROR
BREAK    EQU   *                                                SA57694
         ST    RLCB,LCBERCCW           VALID ADDRESS FOR BREAK  SA57694
         MVI   LCBERCCW,CCWBREAK       COMMAND CODE             SA57694
         MVC   LCBERCCW+4(4),BRKFLGCT  SET FLAGS & COUNT        SA57694
         MVI   LCBINCAM+1,BRKRET       SET RETURN INDEX         SA57694
         B     COMMON                  BRANCH TO COMMON IO      SA57694
BRKFLGCT DC    AL1(CCWSLI),AL2(0),XL1'30'  COUNT=48             SA57694
         EJECT
         SPACE
FLAGCNT  DC    AL1(CCWSKIP),XL1'0',X'FFFF' FLAG/CT FOR READ/SKIP S22025
LCBBKUP  DC    AL2(LCBFLAG1-IEDQLCB)    OFFSET FROM LCB TO IOB   S22025
H8       DC    H'8'                     SIZE OF CCW              S22025
POSTMOD  DC    AL2(5047)                ERROR POST 504G          S22025
X01      EQU   X'01'                    FLAG TO CALL OBR MODULE
FOUR     EQU   4                        DECIMAL FOUR             S22029
LOSTDATA EQU   X'02'                    LOST DATA MASK
INTREQ   EQU   X'40'                    INTERVENTION REQUIRED
RETRY    EQU   X'02'                    RETRY LIMIT
CVTPTR   EQU   X'10'                    CVT LOCATION POINTER
D19      EQU   19
X80      EQU   X'80'                    MASK TYPE 3 ADAPTER
X20      EQU   X'20'                    MASK TYPE 2 ADAPTER
XF0      EQU   X'F0'                    USED TO MASK OFF CONTROL
*                                         UNIT YTPE
BRKRET   EQU   8                                                SA57694
RDSKIP   EQU   4                        SKIP RETURN TO 504G
UNEX     EQU   X'01'                    UNIT E CEPTION MASK
XCTLADD  EQU   X'2C'                    OFFSET IN CVT-XCTL ROUTINE
EREXCP   EQU   15                       ERROR EXCP
LCIN     EQU   X'80'                    MASK FOR LCBIN ON MH QCB
RETURN   EQU   3                        ETURN SVC FOR TRANSIENT S
EQUIPCHK EQU   X'10'                    MASK FOR EQUIPMENT CHECK
IOBOFFST EQU   4                        IOB OFFSET IN RQE
POPERM   EQU   20                       RETURN INDICATOR FOR 504G
POSNO    EQU   16                       RETURN INDICATOR FOR 504G
SIOCC1   EQU   X'10'                    SIO CONDITON CODE 1
TYPE1    EQU   X'10'                    TYPE 1 ADAPTER MASK INUCB
TYPE3    EQU   X'80'                    MASK FOR TYPE 3 ADAPTER
UCBTYP   EQU   X'10'                    UCB TYPE OFFSET IN UCB
T1060    EQU   X'52'                    UCB TYPE MASK FRO 1060
ZERO     EQU   0                        LENGTH EQUATE FOR ADDR   X01004
ONE      EQU   1                        INCREMENT VALUE FOR MH   X02004
TWO      EQU   2                                                 Y02027
HIOCC    EQU   X'48'                    HALT I/O COMPLETION CODE Y02027
NAKCH    EQU   6                        INDEX TO SEQUENCE IN SCT CLUP21
DIAL     EQU   X'20'                    TO TEST FOR DIAL LCB     CLUP21
TWX      EQU   X'50'                    TO TEST FOR TWX DEVICE   CLUP21
WTTA     EQU   X'60'                    TO TEST FOR WTTA DEVICE  CLUP21
PERMERR  EQU   X'DF'                    TO SET PERMANENT ERROR   CLUP21
DEBPTR   EQU   8                        OFFSET OF DEB ADDR IN RQEX01004
SIOPTR   EQU   4                        OFFSET OF SIO APDG. ADDR.X01004
*                                       IN APDG. VECTOR TABLE.   X01004
SIOENTRY EQU   34                       OFFSET OF SUBRTNE. ENTRY Y02027
*                                       POINT IN SIO APPENDAGE.  X01004
RETRYFLG EQU   X'24'                    RETRY FLAG IN LCBFLAG1.  X01004
SEVEN    EQU   7                                                 X01004
THREE    EQU   3                                                 X01004
FIVE     EQU   5                                                 X01004
X4       EQU   2                        MULTIPLE OF FOUR        SA61794
AUTOFLG  EQU   3                        OFFSST IN INVLIST       SA61794
AUTOPL   EQU   1                        FLAG FOR AUTO POLL      SA61794
         EJECT
         TDEBD                          DEB DSECT
         EJECT
         DCBD  DSORG=TX
         TPRFD
         TLCBD
         TCCWD
         TAVTD
         TSCBD
         TTPD
         TQCBD                                                   S22029
         IECDIOSB                                                Y02027
         TTNTD                                                   S22029
         END
