         TITLE 'IGC0310D - OP CONTROL ERROR MESSAGE GENERATOR'
IGC0310D CSECT
*A000000-999999                                               @Y17XA50
***********************************************************************
*                                                                     *
*MODULE NAME=IGC0310D (TCAM, OPERATOR CONTROL)                        *
*                                                                     *
*DESCRIPTIVE NAME=                                                    *
*        IGC0310D - OPERATOR CONTROL ERROR MESSAGE MODULE 1.          *
*                                                                     *
*COPYRIGHT=NONE                                                       *
*                                                                     *
*STATUS=VERSION 10.0                                                  *
*                                                                     *
*                                                                     *
*FUNCTION -                                                           *
*        THIS MODULE TAKES THE ERROR MESSAGE NUMBER PASSED IN         *
*        REGISTER ZERO AND DETERMINES IF THAT ERROR MESSAGE IS CONTAIN*
*        -ED IN THIS MODULE.  IF NOT, AN XCTL TO IGC0410D IS EXECUTED *
*        FOR MESSAGE LOCATION.  IF THE MESSAGE IS CONTAINED HERE,  THE*
*        MESSAGE IS MOVED TO THE RESIDENT WORKAREA AT OFFSET 4.  THE  *
*        LENGTH OF THE MESSAGE IS PLACED IN THE WORKAREA AT OFFSET 0. *
*        ANY INFORMATION NEEDED IN THE MESSAGE IS PLACED DIRECTLY IN  *
*        THE WORKAREA.  CONTROL IS THEN PASSED TO IGC0710D FOR OUTPUT.*
*                                                                     *
*        A SPECIAL CASE OCCURS WHEN REGISTER 0 IS PASSED NEGATIVE AT  *
*        ENTRY.  THIS INDICATES THAT THE ERROR INFORMATION IS STILL IN*
*        THE WORKAREA AND PROCESSING MUST BE HANDLED DIFFERENTLY.     *
*        AFTER PROCESSING, REGISTER 0 IS AGAIN SET NEGATIVE TO        *
*        INDICATE SPECIAL PROCESSING TO IGC0710D.                     *
*                                                                     *
*ENTRY POINTS -                                                       *
*        IGC0310D IS THE ONLY ENTRY POINT OF THIS MODULE.             *
*                                                                     *
*INPUT -                                                              *
*        POINTER TO THE OPERATOR CONTROL AVT IN REGISTER 1 AND        *
*        AN ERROR MESSAGE NUMBER IN REGISTER 0.                       *
*                                                                     *
*OUTPUT -                                                             *
*        ERROR MESSAGE IN RESIDENT WORKAREA.                          *
*                                                                     *
*EXTERNAL REFERENCES --                                               *
*        SVC 7 (XCTL), IEDQCA(TRMNMLOC)                               *
*                                                                     *
*EXITS,NORMAL --                                                      *
*              IGC0710D, IGC0410D                                     *
*EXITS,ERROR -- NONE.                                                 *
*                                                                     *
*TABLES/WORKAREAS -                                                   *
*        TCAM AVT, OP CONTROL AVT, ERRORTAB,OPCE                      *
*                                                                     *
*ATTRIBUTES -                                                         *
*        REFRESHABLE,REUSABLE,DISABLED,TRANSIENT(TYPE 4),SUPERVISOR   *
*        MODE                                                         *
*                                                                     *
*              NONE                                                   *
*        THE OPERATION OF THIS MODULE DEPENDS UPON AN INTERNAL        *
*        REPRESENTATION OF THE EXTERNAL CHARACTER SET WHICH IS        *
*        EQUIVALENT TO THE ONE USED AT ASSEMBLY TIME.  THE CODING     *
*        HAS BEEN ARRANGED SO THAT REDEFINITION OF CHARACTER          *
*        CONSTANTS, BY REASSEMBLY WILL RESULT IN A CORRECT MODULE     *
*        FOR THE NEW DEFINITION.                                      *
*                                                                     *
*NOTES -                                                              *
*        COMMUNICATIONS BETWEEN THE VARIOUS TRANSIENT MODULES IS      *
*        PRIMARILY THROUGH THE OPERATOR CONTROL AVT.                  *
*                                                                     *
*        THIS IS A MODULE RE-WRITE OF IGC0310D AS OF OS/TCAM LEVEL II *
***********************************************************************
         EJECT
         SPACE
*                          E Q U A T E S
         SPACE
RZERO    EQU   0                        PARAMETER REGISTER
RPARM    EQU   1                        PARAMETER REGISTER
ROPCAVT  EQU   2                        POINTER TO OP CONTROL AVT
ROPCE    EQU   3                        POINTER TO CURRENT ELEMENT
RAVT     EQU   4                        POINTER TO TCAM AVT
RMSGLEN  EQU   5                        LENGTH OF ERROR MESSAGE
RTNT     EQU   6                        TNT BASE
RMSGNO   EQU   7                        NO. OF ERROR MESSAGE
RWORK    EQU   8                        WORK REGISTER
RWORK1   EQU   9                        WORK REG                 S22024
RMSGPT   EQU   10                       POINTER TO MESSAGE DATA AREA
RWORKA   EQU   11                       POINTER TO RESIDENT WORKAREA
RBASE    EQU   12                       BASE REGISTER
         SPACE 2
ADDROFF  EQU   2                        OFFSET TO ADDRESS OF MESSAGE
ALPHAOFF EQU   1                        OFFSET TO EBCDIC VERB IN TABLE
CLEARCOM EQU   25                       POSITION OF DC'ED COMMA
COMMA    EQU   C','                     COMMA FOR OUTPUT
DDOFF    EQU   17                       POSITION OF DDNAME IN MSG
DDSIZE   EQU   8                        MAX DDNAME SIZE
EIGHT    EQU   8                        LENGTH OF 8
END      EQU   20                       LAST TWO WORDS OF SAVEAREA
ENDTABLE EQU   X'FF'                    END OF TABLE INDICATOR
FOUR     EQU   4                        OFFSET OF 4
INFOOFF  EQU   1                        NO EXTRA DATA REQUIRED
LASTCHAR EQU   7                        LAST BYTE UNPACKED
LENOFF   EQU   0                        OFFSET TO MESSAGE LENGTH
LINEOFF  EQU   17                       POSITION OF LINE NO. IN MSG
LNOSIZE  EQU   3                        3 DIGIT LINE NUMBER
LTABENT  EQU   4                        LENGTH OF ERROR TABLE ENTRY
MESSAGE  EQU   1                        VALUE 1                  S22024
MSG      EQU   14                       OFFSET TO MSG INFO
MSGLEN   EQU   8                        BYTES TO SAVE
MSGLOC   EQU   4                        PLACEMENT OF MSG IN WORKAREA
MSGOFF   EQU   8                        VERB IN WORKAREA
MSGOFSET EQU   0                        OFFSET TO MESSAGE ADDRESS
NAMEOFF  EQU   2                        OFFSET TO OPT FIELD NAME
NEXTPOS  EQU   2                        LENGTH OF VERB TABLE ENTRY
NLEN     EQU   2                        LENGTH OF XCTL VARIABLE NAME
NORLN    EQU   X'40'                    FLAG FOR NO RLN IN MSG @YM08035
NUMBER   EQU   X'F0'                    COVER SIGN TO MAKE PRINTABLE
OPTOFF   EQU   28                       LOCATION OF NAME OF OPT IN MSG
OPTLEN   EQU   8                        LONGEST POSSIBLE NAME
OPTSIZE  EQU   10                       SIZE OF OPTION CHAR TABLE ENTRY
PICKUP   EQU   5                        OFFSET TO CONVERTED RLN
RESOFF   EQU   20                       OFFSET TO NAME IN MSG  @YM08035
RESCOM   EQU   28                       OFFSET TO COMMA IN MSG @YM08035
RLNOFF   EQU   1                        PLACE FOR RLN IN MSG   @YM08035
RLNSIZE  EQU   3                        MAX SIZE OF RLN
STATLEN  EQU   8                        MAXIMUN STATNAME LENGTH
STATNOT  EQU   20                       OFFSET FOR STATNAME NOT FOUND
STATOFF  EQU   12                       NORMAL STATNAME OFFSET
THREE    EQU   3                        LENGTH OF THREE
VARINAME EQU   3                        OFFSET TO MODULE ID
VERB     EQU   12                       VERB POSITION IN WORKAREA
VERBPOS  EQU   0                        VERB OFFSET IN TABLE
VERBLEN  EQU   1                        LENGTH OF SHORT VERB
WTGLEN   EQU   4                        LENGTH OF WTG INFO
ZERO     EQU   0                        NULL VALUE
         EJECT
*   BASE REGISTERS FOR TCAM CONTROL BLOCKS
         USING IEDQAVTD,RAVT            AVT ADDRESSABILITY
         USING IEDQOPCD,ROPCAVT         ADDRESSABILITY FOR OPC AVT
         USING IEDQOPCE,ROPCE           ADDRESSABILITY FOR ELEMENT
         USING IEDQTNTD,RTNT            TNT ADDRESSABILITY
         SPACE 2
         BALR  RBASE,ZERO               SET BASE REGISTER
         USING *,RBASE                  ADDRESSABILITY
BASE     EQU   *
IGC0310D IEDHJN SKIPID
         LR    ROPCAVT,RPARM            SET BASE FOR OPC AVT
         L     RAVT,OPCAVTPT            PICK UP TCAM AVT POINTER
         L     RWORKA,OPCWORK           GET ADDRESS OF RESIDENT WA
         L     ROPCE,OPCCOPCE           GET CURRENT ELEMENT ADDRESS
         MVC   OPCLDNME+VARINAME(NLEN),RSTLOAD SET RESTART LOAD
         TM    OPCSPEC,OPCRSTRT         IS RESTART IN PROGRESS
         BO    XCTL                     YES, GO TO RESTART LOAD
         XC    OCPARM(WTGLEN),OCPARM    CLEAR WHERE TO GO DATA
         MVC   OPCSAVE+END(MSGLEN),MSGOFF(RWORKA) SAVE BYTES
         MVC   OPCLDNME+VARINAME(NLEN),LOADID MOVE ID TO NAME
         LR    RMSGNO,RZERO             SAVE MESSAGE NUMBER
         LA    RMSGPT,ERRORTAB          GET ADDR OF ERROR MSG TABLE
CHEKTABL EQU   *
         CLI   MSGOFSET(RMSGPT),ENDTABLE END OF TABLE?
         BE    XCTL                     CALL NEXT ERROR MESSAGE LOAD
         SPACE
         N     RMSGNO,AVTCLRHI          CLEAR HI ORDER 2 BYTES
         CH    RMSGNO,MSGOFSET(RMSGPT)  CORRECT MESSAGE FOUND?
         BE    PROCMSG                  YES, PROCESS IT TO WORKAREA
         SPACE
         LA    RMSGPT,LTABENT(RMSGPT)   POINT TO NEXT ENTRY
         B     CHEKTABL                 CHECK NEXT LOCATION IN TABLE
         SPACE
PROCMSG  EQU   *
         LH    RMSGPT,ADDROFF(RMSGPT)   PICK UP MESSAGE OFFSET
         AR    RMSGPT,RBASE             ADD BASE TO OFFSET
         SR    RMSGLEN,RMSGLEN          CLEAR FOR LENGTH PICK-UP
         IC    RMSGLEN,LENOFF(RMSGPT)   PICK UP MESSAGE LENGTH
         STH   RMSGLEN,LENOFF(RWORKA)   PLACE LENGTH IN WORKAREA
         BCTR  RMSGLEN,RZERO            DECREMENT ONE FOR EXECUTE
         EX    RMSGLEN,MOVEMSG          MOVE ERROR MESSAGE TO WORKAREA
         SPACE
         CH    RMSGNO,MSGNO16           INVALID STATION NAME?
         BNE   CHECK17                  NO, CHECK NEXT ERROR
         SPACE
         MVC   STATNOT(STATLEN,RWORKA),OPCDOUBL PLACE NAME IN MESSAGE
         B     WRTXCTL                  GO TO MESSAGE WRITER
         SPACE
MOVEMSG  MVC   MSGLOC(ZERO,RWORKA),MESSAGE(RMSGPT) EXECUTED MOVE
         SPACE
CHECK17  EQU   *
         CH    RMSGNO,MSGNO400          DEACT 3705               S22024
         BNE   CHKMSG17                 NO                       S22024
         MVC   RESOFF(STATLEN,RWORKA),OCBACKUP  MOVE IN        @YM08035
*                                        RESOURCE NAME         @YM08035
         LA    RWORK1,RESCOM(RWORKA)    ADDR OF COMMA INMSG    @YM08035
         TM    OCSWITCH,NORLN           RLN TO BE INSERTED     @YM08035
         BO    WRTXCTL                  NO, EXIT TO MSG WRITER @YM08035
         B     CONVERT                  CONVERT IF NECESSARY   @Y17XARR
CHKMSG17 EQU   *                                                 S22024
         CH    RMSGNO,MSGNO17           CLOSED DCB?
         BNE   CHECK18                  NO, CHECK NEXT ERROR
         SPACE
         MVC   LINEOFF(LNOSIZE,RWORKA),OPCDOUBL PLACE LINE NO IN MSG
         TM    OCFLAG,OCLINE            LINE PROCESSED IN DATA
         BO    WRTXCTL                  YES, DATA HAS BEEN SET
         SPACE
         MVC   DDOFF(DDSIZE,RWORKA),OPCDOUBL PLACE DDNAME IN MSG
         LA    RWORK1,CLEARCOM(RWORKA)  ADDR OF COMMA IN MSG   @YM08035
CONVERT  EQU   *
         SR    RWORK,RWORK              CLEAR FOR INSERT
         IC    RWORK,OCRLN              PICK UP RELATIVE LINE NO
         MVI   AVTEZERO(RWORK1),COMMA   SET COMMA IN DATA      @YM08035
         MVC   RLNOFF(RLNSIZE,RWORK1),ALL SET RLN TO ALL       @YM08035
         LTR   RWORK,RWORK              RLN SPECIFIED?
         BZ    WRTXCTL                  PUT OUT MESSAGE
         SPACE
         CVD   RWORK,OPCDOUBL           CONVERT RLN
         UNPK  OPCDOUBL(EIGHT),OPCDOUBL(EIGHT) UNPACK RLN
         OI    OPCDOUBL+LASTCHAR,NUMBER COVER SIGN
         MVC   RLNOFF(RLNSIZE,RWORK1),OPCDOUBL+PICKUP PLACE RLN@YM08035
         B     WRTXCTL                  WRITE MESSAGE
         SPACE
*   CHECK FOR SCREW UPS
CHECK18  EQU   *
         CH    RMSGNO,MSGNO18           INVALID COMMAND MESSAGE?
         BNE   CHECK201                 NO, CHECK NEXT ERROR     S22024
         SPACE
DO201    IC    RWORK1,OCOPTCDE          GET CODE FROM ELEMENT    S22024
         LA    RWORK,VERBTABL           POINT TO VERB TABLE
CHEKVERB EQU   *
         CLI   VERBPOS(RWORK),ENDTABLE  END OF TABLE?
         BE    BADVERB                  YES, INVALID CONDITION
         SPACE
         EX    RWORK1,VERBCHEK          LOCATE VERB IN TABLE
         BE    VERBFIND                 FOUND, SET UP MESSAGE
         SPACE
         LA    RWORK,NEXTPOS(RWORK)     NEXT TABLE POSITION
         B     CHEKVERB                 CONTINUE CHECKING
CHECK201 EQU   *
         CH    RMSGNO,MSGNO201          INVALID FOR 3705 MESSAGE S22024
         BNE   NORMERR                  NO, LOCATE MESSAGE       S22024
         SPACE
         B     DO201                    PROCESS MSG 201          S22024
         SPACE
         SPACE
VERBCHEK CLI   VERBPOS(RWORK),ZERO      EXECUTED VERB LOCATOR
         SPACE
BADVERB  EQU   *
         MVC   VERB(OPTLEN,RWORKA),OPCDOUBL MOVE VERB TO MESSAGE
         B     WRTXCTL                  PUT OUT MESSAGE
         SPACE
VERBFIND EQU   *
         MVC   VERB(VERBLEN,RWORKA),ALPHAOFF(RWORK) SET VERB IN AREA
         MVC   MSG(MSGLEN,RWORKA),OPCSAVE+END PLACE SAVED BYTES
         B     WRTXCTL                  GO TO MESSAGE WRITER
         SPACE
NORMERR  EQU   *
         CH    RMSGNO,MSGNO39           CK FOR MSG NUM 39
         BE    WRTXCTL                  YES,NO DATA REQ. - WRT IT
         CH    RMSGNO,MSGNO63           CK FOR MSG NUM 63
         BE    WRTXCTL                  YES,NO DATA REQ. - WRT IT
         SPACE
         LH     RWORK,OCTRMTBL          GET TERM NAME TABLE OFFSET
         N     RWORK,AVTCLRHI           CLEAR HIGH BYTES
         BCTR  RWORK,RZERO              SUBTRACT ONE FROM OFFSET
         SR    RWORK1,RWORK1            CLEAR REG FOR ENTRY LENGTH
         L     RTNT,AVTRNMPT            PICK UP TNT ADDRESS
         IC    RWORK1,TNTENLEN          PICK UP LENGTH OF NAMES
         LA    RWORK1,THREE(RWORK1)     ADD THREE FOR ADDRESSES
         MR    RWORK,RWORK              CALCULATE OFFSET
         LA    RPARM,TNTFIRST           GET FIRST ENTRY ADDRESS
         AR    RPARM,RWORK1             ADD OFFSET TO ADDRESS
         IC    RWORK,TNTENLEN           GET NAME LENGTH
         BCTR   RWORK,RZERO             DECREMENT FOR EXECUTE
         EX    RWORK,MOVENAME           MOVE ENTIRE NAME TO MSG
         SPACE
PLACEIN  EQU   *
         MVC   OPTOFF(OPTLEN,RWORKA),OCBACKUP       PLACE OPT NAME
WRTXCTL  EQU   *
         MVC   OPCLDNME+VARINAME(NLEN),WRITE SET XCTL NAME
XCTL     EQU   *
         LR    RPARM,ROPCAVT            POINT TO AVT
         NI    OPCFLAG1,ZERO            CLEAR FLAGS
         SPACE 2
         XCTL  SF=(E,OPCXCTL)           CALL NEXT MODULE
         SPACE
MOVENAME MVC   STATOFF(ZERO,RWORKA),ZERO(RPARM) MOVE STATION NAME
         EJECT
*   THE FOLLOWING TABLE CONTAINS THE MESSAGE IDENTIFIERS AND THE
*     OFFSET FROM THE BEGINNING OF THIS MODULE TO THE APPROPRIATE
*     ERROR MESSAGES.
         SPACE 2
ERRORTAB DS    0F
         DC    AL2(16),AL2(MSG16-BASE)
         DC    AL2(17),AL2(MSG17-BASE)
         DC    AL2(18),AL2(MSG18-BASE)
         DC    AL2(34),AL2(MSG34-BASE)
         DC    AL2(39),AL2(MSG39-BASE)
         DC    AL2(56),AL2(MSG56-BASE)
         DC    AL2(62),AL2(MSG62-BASE)
         DC    AL2(63),AL2(MSG63-BASE)
         DC    AL2(77),AL2(MSG77-BASE)
         DC    AL2(201),AL2(MSG201-BASE)                         S22024
         DC    AL2(400),AL2(MSG400-BASE)                         S22024
         DC    X'FF'                    END OF TABLE             S22024
         SPACE 2
MSG16    DC    AL1(END16-*-1)
         DC    C'IED016I STATION          NOT FOUND'
*                                                                     *
END16    EQU   *
         SPACE
MSG17    DC    AL1(END17-*-1)
         DC    C'IED017I LINE              NOT OPEN'
*                                                                     *
END17    EQU   *
         SPACE
MSG18    DC    AL1(END18-*-1)
         DC    C'IED018I            COMMAND INVALID'
*                                                                     *
END18    EQU   *
         SPACE
MSG34    DC    AL1(END34-*-1)
         DC    C'IED034I          HAS NO          OPTION'
*                                                                     *
END34    EQU   *
         SPACE
MSG39    DC    AL1(END39-*-1)
         DC    C'IED039I NO STATIONS INTERCEPTED'
*                                                                     *
END39    EQU   *
         SPACE
MSG56    DC    AL1(END56-*-1)
         DC    C'IED056I          OPTION          DATA FORMAT INVALID'
*                                                                     *
END56    EQU   *
         SPACE
MSG62    DC    AL1(END62-*-1)
         DC    C'IED062I          OPTION          CANNOT ACCEPT SPECIFIX
               ED DATA'
*                                                                     *
END62    EQU   *
         SPACE
MSG63    DC    AL1(END63-*-1)
         DC    C'IED063I CLOSEDOWN IN PROGRESS-COMMAND(S) REJECTED'
END63    EQU   *
         SPACE
MSG77    DC    AL1(END77-*-1)
         DC    C'IED077I          OPTION          DATA CHARACTER INVALIX
               D'
END77    EQU   *
MSG201   DC    AL1(END201-*-1)                                   S22024
         DC    C'IED201I            COMMAND INVALID FOR 3705'    S22024
END201   EQU   *                                                 S22024
         SPACE
MSG400   DC    AL1(END400-*-1)                                   S22024
         DC    C'IED400I NCP FOR              NOT ACTIVE, CMD REJECTED'
END400   EQU   *                                               @Y17XARR
***********************************************************************
         SPACE 2
MSGNO16  DC    H'16'                    MSG NO 16
MSGNO17  DC    H'17'                    SPECIAL MSG 17
MSGNO18  DC    H'18'                    MESSAGE NUMBER 18
MSGNO34  DC    H'34'                    MESSAGE NO 34
MSGNO39  DC    H'39'                    MESSAGE NUMBER 39
MSGNO63  DC    H'63'                    MESSAGE NUMBER 63
MSGNO201 DC    H'201'                   MESSAGE NUMBER 201       S22024
MSGNO400 DC    H'400'                   MSG NO 400 - 3705        S22024
LOADID   DC    C'04'                    NEXT ERROR MESSAGE LOAD
RSTLOAD  DC    C'06'                    NAME OF RESTART LOAD
WRITE    DC    C'07'                    NAME OF OUTPUT WRITER
ALL      DC    C'ALL'                   RLN WAS FOR ALL
         SPACE 2
VERBTABL DC    X'28',C'V'               VARY
         DC    X'44',C'F'               MODIFY
         DC    X'3C',C'Z'               HALT
         DC    X'68',C'D'               DISPLAY
         DC    X'6C',C'H'               HOLD
         DC    X'70',C'A'               RELEASE
         DC    X'FF'                    END OF TABLE
         SPACE 2
         TITLE 'IGC0310D - DSECTS USED IN THIS MODULE'
         TAVTD
         EJECT
         TOPCAVTD
         EJECT
         TOPCED
         EJECT
         TTNTD
         END
