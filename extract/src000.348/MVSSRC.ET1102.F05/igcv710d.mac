         TITLE 'IGCV710D - STOP TERMINAL MIDDLE OF MESSAGE ROUTINE'
IGCV710D CSECT
         SPACE 3
* CHANGE ACTIVITY = AS FOLLOWS:
******************** MICROFICHE FLAGS *********************** SUPT CODE
*A354000                                                        SA62999
*C370000,376000,614000                                          SA62999
*D372000                                                        SA62999
*A192000,680000                                                 SA63000
*C402000,404000                                                 SA63000
         SPACE 3
***********************************************************************
*                                                                     *
* MODULE NAME = IGCV710D                                              *
*                                                                     *
* DESCRIPTIVE NAME = STOP TERMINAL MIDDLE OF MESSAGE ROUTINE          *
*                                                                     *
* COPYRIGHT = NONE                                                    *
*                                                                     *
*  STATUS:  CHANGE LEVEL 5                                            *
*                                                                     *
* FUNCTION -- THIS ROUTINE TURNS OFF THE MIDDLE-OF-MESSAGE            *
*    BIT IN THE SCB(S) ASSOCIATED WITH THE TERMINAL BEING STOPPED.    *
*                                                                     *
* ENTRY POINTS -- IGCV710D TO TURN OF MIDDLE-OF-MESSAGE BIT IN SCB    *
*                                                                     *
* INPUT --                                                            *
*    REGISTER 1 - ADDRESS OF OPERATOR CONTROL AVT                     *
*                                                                     *
* OUTPUT --                                                           *
*    NONE                                                             *
*                                                                     *
* EXTERNAL REFERENCES --                                              *
*    NONE                                                             *
*                                                                     *
* EXITS,NORMAL --                                                     *
*    XCTL TO IGC0610D WITH MIDDLE-OF-MESSAGE BIT(S) OFF               *
*                                                                     *
* EXITS,ERROR --                                                      *
*    XCTL TO IGC0610D WITH MIDDLE-OF-MESSAGE BIT(S) ON                *
*                                                                     *
* TABLES/WORK AREAS --                                                *
*    OPERATOR CONTROL AVT, OPERATOR CONTROL ELEMENT, TERMNAME TABLE,  *
*    DCB, SCB, QCB, LCB, TERMINAL TABLE, CONCENTRATOR DEVICE ID TABLE *
*                                                                     *
* ATTRIBUTES --                                                       *
*    SERIALLY REUSABLE, REFRESHABLE, ENABLED, TRANSIENT,              *
*    SUPERVISOR MODE.                                                 *
*                                                                     *
* CHARACTER CODE DEPENDENCY --                                        *
*    THE OPERATION OF THIS MODULE DEPENDS UPON AN INTERNAL            *
*    REPRESENTATION OF THE EXTERNAL CHARACTER SET WHICH IS            *
*    EQUIVALENT TO THE ONE USED AT ASSEMBLY TIME. THE CODING HAS      *
*    BEEN ARRANGED SO THAT REDEFINITION OF CHARACTER CONSTANTS, BY    *
*    REASSEMBLY WILL RESULT IN A CORRECT MODULE FOR THE NEW           *
*    DEFINITION.                                                      *
*                                                                     *
* NOTES --                                                            *
*    COMMUNICATION BETWEEN THE VARIOUS TRANSIENT MODULES IS PRIMARILY *
*    THROUGH THE OPERATOR CONTROL AVT.                                *
*                                                                     *
***********************************************************************
         EJECT
*        REGISTER EQUATES                                             *
         SPACE 1
RZERO    EQU   0                        PARAMETER REGISTER
RFLAGS   EQU   0                        DEV. DEP. FLAGS PTR
RPARM    EQU   1                        PARAMETER REGISTER
RTERM    EQU   1                        TERMINAL TABLE BASE REGISTER
ROPCAVT  EQU   2                        PTR TO OP CNTRL AVT
ROPCE    EQU   3                        CURRENT ELEMENT POINTER
RAVT     EQU   4                        TCAM AVT
RDCB     EQU   5                        DCB BASE POINTER
RQCB     EQU   5                        QCB ADDRESS
ROPT     EQU   6                        OPTION FLD BASE PTR
RWORK7   EQU   7                        WORK REGISTER
RLCB     EQU   7                        LCB BASE POINTER
RWORK8   EQU   8                        WORK REGISTER
RSCB     EQU   8                        SCB BASE REGISTER
RWORK9   EQU   9                        WORK REGISTER
RDVCID   EQU   10                       CONC. DEVICE ID TABLE PTR
RWORK10  EQU   10                       WORK REGISTER
ROCTNT   EQU   11                       SAVE OPCE REGISTER
RBASE    EQU   12                       BASE REGISTER
RRET     EQU   14                       RETURN REGISTER
RBRNCH   EQU   15                       BRANCH REGISTER
         SPACE 1
*        MISCELLANEOUS EQUATES                                        *
         SPACE 1
STATSTOP EQU   26                       RESPONSE MSG NUMBER
VARINAME EQU   3                        OFFSET TO CHANGE LOAD NAME
NAMELEN  EQU   2                        NUMBER OF NAME CHANGE CHARS.
EIGHT    EQU   8                        CONTSTANT
THREE    EQU   3                        CONTSTANT
D15      EQU   15                       SHIFT CONSTANT
ONE      EQU   1                        CONSTANT
TWO      EQU   2                        CONSTANT
FOUR     EQU   4                        CONSTANT
NINE     EQU   9                        COUNTER
BM       EQU   X'26'                    MIDMSG BOTH FLAG
EM       EQU   X'25'                    MIDMSG ENTRY ONLY
INDEX    EQU   X'20'                    LCB INDEX
MIDMSG   EQU   X'20'                    MIDMSG BIT IN OCSWITCH  SA63000
OFF      EQU   X'FE'                    BIT OFF
KEYOFF   EQU   2                        LMD SCB KEY
TABEND   EQU   X'FE'                    DEVICE TABLE END
         EJECT
*        SETUP ADDRESSABILITY AND MODULE IDENTIFICATION               *
         SPACE 1
         USING IEDQOPCD,ROPCAVT         OP CNTRL AVT ADDRESSABILITY
         USING IEDQOPCE,ROPCE           ELEMENT ADDRESSABILITY
         USING IEDQAVTD,RAVT            TCAM AVT ADDRESSABILITY
         SPACE 1
         BALR  RBASE,RZERO              MODULE ADDRESSABILITY
         USING *,RBASE
         SPACE 1
IGCV710D IEDHJN SKIPID
         SPACE 1
         LR    ROPCAVT,RPARM            PASSED OP CNTRL AVT ADDRESS
         L     ROPCE,OPCCOPCE           CURRENT ELEMENT ADDRESS
         L     RAVT,OPCAVTPT            TCAM AVT POINTER
         SPACE 1
*        FIND TERMINAL TABLE ENTRY FOR TERMINAL                       *
         SPACE 1
         LH    ROCTNT,OCTRMTBL          SAVE TNT OFFSET FROM OPCE
         MVC   OPCDOUBL(EIGHT),OCBACKUP SETUP TERMINAL NAME
         L     RBRNCH,OPCTOFLK          GET TERMINAL ENTRY ADDRESS
         BALR  RRET,RBRNCH              LINK TO SUBROUTINE IN IEDQCA
         SPACE 1
         B     GOODRTN                  VALID TERMINAL ENTRY
         SPACE 1
         B     EXIT                     INVALID TERMINAL ENTRY
         EJECT
GOODRTN  EQU   *
         USING IEDQTRM,RTERM            TERMINAL ENTRY ADDRESSABILITY
         TM    TRMDEVFL+ONE,TRMCONC     TERM. IN CONC. NETWORK
         BZ    FINDLCB                  NO. GO FIND SINGLE TERM.
         SPACE 1
*        THIS IS A CONCENTRATOR TERMINAL. FIND THE CONCENTRATOR       *
*        DEVICE ID TABLE                                              *
         SPACE 1
         L     RQCB,TRMDESTQ-ONE        ADDRESS OF QCB
         SPACE 1
         USING IEDQQCB,RQCB             QCB ADDRESSABILITY
         SPACE 1
         TM    QCBDSFLG,QCBDRQQ         IS IT A DRQ (CONC)
         BZ    EXIT                     NO. GET OUT OF MODULE
         DROP  RQCB
         SPACE 1
         SR    RWORK10,RWORK10          CLEAR WORK REGISTER
         LA    ROPT,TRMOPNO             PTR TO DEV FLD IF NO OPT FLD
         TM    TRMSTATE,TRMOPTFN        CHECK IF OPT FIELDS IN USE
         BZ    NOOPT                    NO. GO FIND DEV. DEP. FIELDS
         SPACE 1
         IC    RWORK10,TRMOPNO          GET NUMBER OF OPTION ENTRIES
         LA    ROPT,THREE(ROPT,RWORK10) BUMP DEV DEP. FLD PTR PAST
*                                       OPTION OFFSETS
NOOPT    EQU   *
         LH    RFLAGS,TRMDEVFL          GET DEV. DEP FLD FLAGS
         SLL   RFLAGS,D15               SHIFT FLAGS TO HI ORDER
         LA    RWORK9,EIGHT             SET CTR TO TEST 8 FLAGS
         SPACE 1
LOOP     EQU   *
         SLL   RFLAGS,ONE               SHIFT TO TEST FOR PRESENCE
*                                       OF NEXT DEVICE DEPEND. FIELD
         LTR   RFLAGS,RFLAGS            IS THIS FIELD PRESENT
         BNM   BCTIT                    NO. DON'T BUMP PTR
         SPACE 1
         IC    RWORK10,AVTEZERO(ROPT)   GET LNG OF THIS FIELD
         LA    ROPT,ONE(ROPT,RWORK10)   BUMP PTR PAST THIS FIELD
         SPACE 1
BCTIT    EQU   *
         BCT   RWORK9,LOOP              REACHED DVCID CHAR TAB PTR.
*                                       IF NOT, GO BACK
         EJECT
*        ROPT NOW POINTS TO THE CONCENTRATOR DEVICE ID TABLE IN       *
*        THE DEVICE DEPENDENT FIELDS OF THE TERMINAL ENTRY            *
         SPACE 1
         XC    OPCDOUBL(EIGHT),OPCDOUBL CLEAR WORK AREA AND
         MVC   OPCDOUBL+1(THREE),TWO(ROPT) MOVE TABLE ADDRESS
         L     RDVCID,OPCDOUBL          FOR LOAD TO REGISTER
         SPACE 1
         USING IEDQDVCT,RDVCID          ADDRESSABILITY FOR DVCID TABLE
         SPACE 1
         XC    OCPARM,OCPARM            CLEAR SAVE FIELD        SA62999
         MVC   OCPARM+ONE,DVCENLTH      SAVE LENGTH OF ENTRY    SA62999
         LA    RDVCID,FOUR(RDVCID)      GET CONCENTRATOR ENTRY
         SPACE 1
*        RDVCID NOW POINTS TO THE FIRST ENTRY IN THE CONCENTRATOR     *
*        DEVICE ID TABLE (CONCENTRATOR ITSELF)                        *
         SPACE 1
NEXTID   EQU   *
         IC    RWORK9,DVCIDLTH          PICKUP ID LENGTH
         LA    RWORK9,ONE(RWORK9,RDVCID) POINTER TO TNT OFFSET  SA62999
         XC    OPCDOUBL(EIGHT),OPCDOUBL CLEAR WORK AREA AND
         MVC   OPCDOUBL+TWO(TWO),AVTEZERO(RWORK9) MOVE OFFSET   SA62999
         LH    RPARM,OPCDOUBL+TWO       FOR LOAD TO RPARM
         DROP  RTERM
         SPACE 1
*        FIND TERMINAL TABLE ENTRY FOR THIS OFFSET                    *
         SPACE 1
         L     RBRNCH,AVTRNMPT          TNT CODE
         BALR  RRET,RBRNCH              GO GET TERMINAL ENTRY ADDR
         EJECT
*        FIND PROPER LCB                                              *
         SPACE 1
         USING IEDQTRM,RTERM            TERMINAL ENTRY ADDRESSABILITY
         SPACE 1
         TM    OCSWITCH,EM              STOP ENTRY ONLY         SA63000
         BO    LMD                      BR YES                  SA63000
         SPACE 1
FINDLCB  EQU   *
         L     RQCB,TRMDESTQ-ONE        ADDRESS OF QCB
         SPACE 1
         USING IEDQQCB,RQCB             QCB ADDRESSABILITY
         SPACE 1
         SR    RWORK8,RWORK8            CLEAR WORK REGISTER
         IC    RWORK8,QCBRELLN          GET RELATIVE LINE NUMBER
         ST    RQCB,OPCDOUBL            SAVE QCB POINTER
         L     RDCB,QCBDCBAD-ONE        GET DCB ADDRESS
         DROP  RQCB
         SPACE 1
         USING IHADCB,RDCB
         SPACE 1
         L     RLCB,DCBIOBAD            GET ADDRESS OF IOB
         SR    RWORK9,RWORK9            CLEAR WORK REGISTER
         IC    RWORK9,DCBEIOBX          INDEX TO IOB
         MR    RWORK8,RWORK8            INDEX TO PROPER IOB
         LA    RLCB,AVTEZERO(RLCB)      CLEAR HI ORDER BYTE
         AR    RLCB,RWORK9              GET ADDRESS OF PROPER IOB
         LA    RWORK8,INDEX             GET LCB INDEX
         SR    RLCB,RWORK8              GET LCB ADDRESS
         SPACE 1
         USING IEDQLCB,RLCB             LCB ADDRESSABILITY
         DROP  RDCB
         SPACE 1
*        FIND OUTPUT SCB                                              *
         SPACE 1
         L     RQCB,OPCDOUBL            RESTORE QCB PTR
         SR    RWORK9,RWORK9            CLEAR WORK REGISTER
         TM    TRMDEVFL+ONE,TRMCONC     IS IT A CONC TERMINAL
         BZ    SINGLE                   NO. GO TO SINGLE HANDLER
         SPACE 1
         USING IEDQQCB,RQCB             QCB ADDRESSABILITY
         SPACE 1
         SR    RSCB,RSCB                CLEAR SCB REGISTER
         IC    RSCB,QCBSCBOF            GET OFFSET TO PROPER SCB
         IC    RWORK9,AVTSCBSZ          GET SCB SIZE
         MR    RSCB,RSCB                MULTIPLY FOR SCB OFFSET
         L     RSCB,LCBSCBA-ONE         ADDRESS OF SCB
         LA    RSCB,AVTEZERO(RSCB)      CLEAR HI ORDER BYTE
         AR    RSCB,RWORK9              GET PROPER SCB
         SPACE 1
*        TURN OFF MIDDLE-OF-MESSAGE BIT IN OUTPUT SCB                 *
         SPACE 1
         USING IEDQSCB,RSCB             SCB ADDRESSABILITY
         SPACE 1
         NI    SCBQTYPE,OFF             TURN BIT OFF
         DROP  RSCB
         EJECT
*        CHECK NEXT DEVICE DEPENDENT FIELD FOR LMD. IF LMD ENTRY,     *
*        SCB FOR INPUT ALSO EXISTS.                                   *
         SPACE 1
LMD      EQU   *
         SR    RWORK8,RWORK8            CLEAR WORK REGISTER
         LA    ROPT,TRMOPNO             PTR TO DEV FLD IF NO OPT FLD
         SPACE 1
         TM    TRMSTATE,TRMOPTFN        CHECK IF OPT FLDS IN USE
         BZ    NOOPTLMD                 NO. GO FIND DEV. DEP. FLD
         SPACE 1
         IC    RWORK8,TRMOPNO           GET NUMBER OF OPTION ENTRIES
         LA    ROPT,THREE(ROPT,RWORK8)  BUMP DEV. DEP. FLD PTR
*                                       PASSED OPTION OFFSETS
NOOPTLMD EQU   *
         LH    RFLAGS,TRMDEVFL          GET DEV. DEP FLD FLAGS
         SLL   RFLAGS,D15               SHIFT FLAGS TO HI ORDER
         LA    RWORK9,NINE              SET CTR TO TEST 9 FLAGS
         SPACE 1
LOOPLMD  EQU   *
         SLL   RFLAGS,ONE               SHIFT TO TEST FOR PRESENCE OF
*                                       NEXT DEVICE DEPEND. FIELD
         LTR   RFLAGS,RFLAGS            IS THIS FIELD PRESENT
         BNM   BCTLMD                   NO. DON'T BUMP PTR
         SPACE 1
         IC    RWORK8,AVTEZERO(ROPT)    GET LENGTH OF THE FIELD
         LA    ROPT,ONE(ROPT,RWORK8)    BUMP PTR PASSED THE FIELD
         SPACE 1
BCTLMD   EQU   *
         BCT   RWORK9,LOOPLMD           REACHED LMD FLAG
*                                       IF NOT, GO BACK
         SLL   RFLAGS,ONE               THIS SHOULD BE LMD FLAG
         LTR   RFLAGS,RFLAGS            IS IT
         BNM   CONTINUE                 NO. NO LMD FLAG
         SPACE 1
*        LMD DEVICE DEPENDENT FLAG ON                                 *
         SPACE 1
         SR    RSCB,RSCB                CLEAR SCB REGISTER
         IC    RWORK9,KEYOFF(ROPT)      GET LMD SCB OFFSET
         SR    RWORK8,RWORK8            CLEAR WORK REGISTER
         IC    RWORK8,AVTSCBSZ          GET SCB SIZE
         MR    RWORK8,RWORK8            MULTIPLY FOR OFFSET
         L     RSCB,LCBSCBDA-ONE        GET SCB DIRECTORY
         LA    RSCB,AVTEZERO(RSCB)      CLEAR HI ORDER BYTE
         AR    RSCB,RWORK9              OFFSET+BGN=PTR TO SCB
         SPACE 1
*        TURN OFF MIDDLE-OF-MESSAGE BIT IN INPUT SCB                  *
         SPACE 1
         USING IEDQSCB,RSCB             SCB ADDRESSABILITY
         NI    SCBQTYPE,OFF             TURN BIT OFF
         SPACE 1
*        SETUP FOR NEXT CONCENTRATOR DEVICE ID FIELD                  *
         SPACE 1
CONTINUE EQU   *
         SR    RWORK9,RWORK9            CLEAR WORK REGISTER
         AH    RDVCID,OCPARM            BUMP TO NEXT ENTRY      SA62999
         CLI   AVTEZERO(RDVCID),TABEND  END OF DVCID TABLE
         BNE   NEXTID                   NO. DO NEXT TERMINAL
         B     EXIT                     YES. EXIT
         DROP RSCB
         EJECT
*        THIS SECTION TURNS OFF THE MIDDLE-OF-MESSAGE BIT IN A        *
*        SINGLE TERMINAL                                              *
         SPACE 1
SINGLE   EQU   *
         USING IEDQQCB,RQCB             QCB ADDRESSABILITY
         SPACE 1
         IC    RWORK9,QCBSCBOF          GET QCB SCB OFFSET
         SR    RWORK8,RWORK8            CLEAR WORK REGISTER
         IC    RWORK8,AVTSCBSZ          GET SCB SIZE
         MR    RWORK8,RWORK8            MULTIPLY FOR OFFSET
         A     RWORK9,LCBSCBDA-ONE      OFFSET+BGN=PTR TO SCB
         LR    RSCB,RWORK9              SET SCB BASE
         SPACE 1
*        TURN OFF MIDDLE-OF-MESSAGE BIT IN SCB                        *
         SPACE 1
         USING IEDQSCB,RSCB             SCB ADDRESSABILITY
         NI    SCBQTYPE,OFF             TURN BIT OFF
         EJECT
*        EXIT SECTION OF ROUTINE                                      *
         SPACE 1
EXIT     EQU   *
         MVC   OPCDOUBL(EIGHT),OCBACKUP RESTORE CURRENT ELEMENT
         STH   ROCTNT,OCTRMTBL
         MVC   OCWTG(NAMELEN),WRTMSG    WTG TO MESSAGE MODULE
         MVC   OPCLDNME+VARINAME(NAMELEN),CHECKPT SET CHECKPOINT NAME
         LA    RZERO,STATSTOP           RESPONSE MSG NUMBER
         STH   RZERO,OCPARM
         NI    OPCFLAG1,AVTEZERO        RESET FLAGS
         NI    OCSWITCH,OCFOXES-MIDMSG  RESET MID MSG BIT       SA63000
         LR    RPARM,ROPCAVT            OP CNTRL AVT
         XCTL  SF=(E,OPCXCTL)
CHECKPT  DC    C'06'                    CHECKPOINT MODULE NAME
WRTMSG   DC    C'09'                    HOLD,RELEASE,VARY MSG
         TAVTD
         TOPCAVTD
         TOPCED
         DCBD  DSORG=TX
         TLCBD
         TQCBD
         TSCBD
         TTRMD
         TDVCIDTD
         END
