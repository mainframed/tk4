604G     TITLE 'IGE0604G - AUDIO AND 2260 LOCAL ERP'
IGE0604G CSECT
*A-000000-999999                                               @Y02X6E0
*A112000-114000,126000,164000,226000,234000,290000,321000-322000,Y02027
*A334000,345200,446000,874000                                    Y02027
*C080000,092000,108000-110000,124000,160000-162000,232000,300000,Y02027
*C444000,676000,680600,680800-680900,682400                      Y02027
*A344000,504000,848000                                          SA51090
*A636000                                                        SA67073
*D375500                                                        SA67073
*A301000-302000,322000,452000,680100-682800,822000,848400-851600 X01004
*C098000-100000                                                  X01004
*D448000-456000                                                  X01004
*C372770                                                        OY00492
*C032000,348000                                                @Z30AAEE
*D824000                                                       @Z30AAEE
*A644800                                                       @OS80057
*                                                                     *
***********************************************************************
*                                                                     *
*TITLE -- 'IGE0604G', AUDIO, 2260 AND 3270 LOCAL ERP MODULE      S99228
*                                                                     *
*STATUS -- CHANGE LEVEL 8                                      @Z30AAEE
*                                                                     *
*FUNCTION -- TO ATTEMPT RETRY FOR ALL CCW'S ASSOCIATED WITH THE       *
*   AUDIO RESPONSE UNIT, 3270 AND 2260 LOCAL DEVICES.            S99228
*                                                                     *
*ENTRY POINTS -- FIRST EXECUTABLE INSTRUCTION.                        *
*FUNCTION -- ALL ERP MODULES ATTEMPT TO RECOVER FROM ALL ERRORS.      *
*   CERTAIN TEXT ERRORS REQUIRE RETURN TO LINE END TO SCHEDULE A      *
*   SUBSEQUENT RECALL OPERATION.                                      *
*                                                                     *
*                                                                     *
*INPUT --                                                             *
*   R1 - POINTER TO I/O SUPERVISOR BLOCK                         Y02027
*   R15 - ENTRY POINT ADDRESS                                         *
*                                                                     *
*OUTPUT -- R1 POINTS TO I/O SUPERVISOR BLOCK                     Y02027
*                                                                     *
*EXTERNAL ROUTINE -- IGG019QE (AOS/TCAM SIO APPENDAGE) -- TO CONVERT  *
*   VIRTUAL CCW ADDRESSES TO REAL BEFORE RETRY.                       *
*                                                                     *
*EXITS-NORMAL -- R1 POINTS TO I/O SUPERVISOR BLOCK               Y02027
*        SVC   15   'ERP IN CONTROL (IOSERR)' AND 'EXCEPTIONAL   Y02027
*                CONDITION(IOSEX)'. RETURN TO IOS TO RETRY THE   Y02027
*                   ERROR.                                       Y02027
*        SVC   3                                                      *
*                                                                     *
*        SVC   15   'NO ERROR FLAGS'. RETURN TO LINE END         Y02027
*                   APPENDAGE. ERROR CLEARED                     Y02027
*        SVC   3                                                      *
*                                                                     *
*   TO SCHEDULE NEXT LOAD OF ERP                                      *
*        L     13,DECIMAL VALUE OF NEXT LOAD                          *
*        L     14,X'10'    CVT ADDRESS                                *
*        L     14,X'2C'(14)    ADDRESS OF XCTL ROUTINE                *
*        BR    14   EXIT TO XCTL                                      *
*                                                                     *
*EXITS-ERROR -- R1 POINTS TO I/O SUPERVISOR BLOCK                Y02027
*        SVC   15   'EXCEPTIONAL CONDITION(IOSEX)'. RETURN TO    Y02027
*                   LINE END APPENDAGE. ERROR PERMANENT.         Y02027
*        SVC   3                                                      *
*                                                                     *
*        L     13,DECIMAL VALUE OF NEXT LOAD                          *
*        L     14,X'10'     CVT ADDRESS                               *
*        L     14,X'2C'(14)    ADDRESS OF XCTL ROUTINE                *
*        BR    14   EXIT TO XCTL                                      *
*                                                                     *
*   TO SCHEDULE ERROR POST                                            *
*                                                                     *
*TABLES/WORK AREAS --                                                 *
*   TAVTD                                                             *
*   TCCWD                                                             *
*   TLCBD                                                             *
*   DCBD                                                              *
*   TSCBD                                                             *
*   IECDIOSB                                                     Y02027
*                                                                     *
*ATTRIBUTES -- SERIALLY REUSABLE, REFRESHABLE, SUPERVISOR KEY,   Y02027
*              SUPERVISOR MODE, ENABLED                          Y02027
*                                                                     *
*NOTES -- THE OPERATION OF THIS MODULE DOES NOT DEPEND UPON A         *
*   PARTICULAR INTERNAL REPRESENTATION OF THE EXTERNAL CHARACTER      *
*   SET.                                                              *
*                                                                     *
*   R1 REMAINS TRANSPARENT TO ALL LOADS OF ERP.                       *
*                                                                     *
***********************************************************************
         EJECT
         USING *,R15
         USING LCBFLAG1,RLCB
         USING IEDQCCW,RCCW
         USING IEDQAVTD,RAVT
         USING IEDQSCB,RSCB
         USING IOSB,RIOSB                                        Y02027
*              REGISTERS
R0       EQU   0
RIOSB    EQU   1                        ADDR OF I/O SUPVR BLOCK  Y02027
RIOB     EQU   2                        IOB REGISTER - FOR USE BY
*                                       SIO APPENDAGE.
RUCB     EQU   3                        UCB REGISTER
RLCB     EQU   4                        LCB REG.
RSCB     EQU   5                        SCB REGISTER
RCCW     EQU   6                        CCW REGISTER
RWKA     EQU   7                        WORK REGISTER
RTST     EQU   7                        ADDR OF REQUEST QUEUE    Y02027
*                                       ELEMENT                  Y02027
RSIO     EQU   8                        ENTRY POINT OF SIO APDG.
RWKB     EQU   9                        WORK REGISTER
RAVT     EQU   11                       AVT BASE
RLINK    EQU   13                       LINKAGE FOR SUBSEQUENT LOAD
RSAVE    EQU   13                       SAVE REGISTER FOR IOSB   Y02027
RXCTL    EQU   14                       XCTL REGISTER
R15      EQU   15                       ADDRESSABILITY REGISTER
         EJECT
IGE0604G  IEDHJN   ENTER
         L     RTST,IOSUSE              PICKUP ADDR OF RQE       Y02027
IGE0604G IEDHJN INIT604G                MODULE NAME AND DATE    SA51090
         L     RUCB,IOSUCB              ADDRESS OF UCB         @Z30AAEE
         L     RLCB,IOBOFFST(R0,RTST)   ADDRESS OF IOB
         L     RSCB,LCBSCBA-1           SET SCB BASE
         L     RCCW,LCBCSW-1            FAILING CCW+8
         SH    RCCW,H8                  ADJUST TO ENDING CCW
         CLI   18(RUCB),X'40'           AUDIO OR LOCAL
         BE    AUDIO                    ITS AUDIO
         CLI   UCBTYPE3(RUCB),LCL3277   3277 OR 3284 LOCAL?      S99228
         BL    OLDLOCAL                 BRANCH 2260 LOCAL        S99228
         SPACE 2
*                                                                S99228
* THE FOLLOWING SECTION HANDLES ERRORS ON 3270 LOCAL DEVICES     S99228
*                                                                S99228
         SPACE 1
         TM    LCBSIOCC,SIOCC1          SIO CONDITION CODE=1?    S99228
         BZ    ENDSTAT                  BRANCH NOT INITIAL STATUSS99228
         SPACE 1
         TM    LCBSENS0,AVTEFF-(COMREJ+INTREQ+BUSOUT)           OY00492
*                                       CHECK, DATA CHECK,UNIT   S99228
*                                       SPECIFY, CONTROL CHECK,  S99228
*                                       OPERATION CHECK?         S99228
         BNZ   SNO                      BRANCH YES               S99228
         SPACE 1
         TM    LCBSENS0,COMREJ+INTREQ+BUSOUT   COMMAND REJECT,   S99228
*                                       INTERVENTION REQUIRED,   S99228
*                                       BUSOUT CHECK?            S99228
         BNZ   PERMEXIT                 BRANCH YES               S99228
         SPACE 1
         TM    LCBCSW+CSWSTAT1,BUSY     DEVICE BUSY?             S99228
         BO    NEWTRY                   BRANCH YES               S99228
         SPACE 1
         B     SNO                      NO OTHER ERRORS SHOULD   S99228
*                                       OCCUR
         SPACE 2
ENDSTAT  EQU   *                                                 S99228
         TM    LCBCSW+CSWSTAT1,UNITCHK  UNIT CHECK?              S99228
         BZ    UNEXCEP                  BRANCH NO                S99228
         SPACE 1
         TM    LCBSENS0,COMREJ          COMMAND REJECT?          S99228
         BO    SNO                      BRANCH YES               S99228
         SPACE 1
         B     PERMEXIT                 ALL UNIT CHECK ERRORS    S99228
*                                       ARE PERMANENT            S99228
         SPACE 2
UNEXCEP  EQU   *                                                 S99228
         SPACE 1
         TM    LCBCSW+CSWSTAT1,UNEX     UNIT EXCEPTION?          S99228
         BZ    CONUEND                  BRANCH NO                S99228
         SPACE 1
         TM    LCBSTAT1,LCBRECVN        RECEIVE STATE?           S99228
         BO    PERMEXIT                 BRANCH YES               S99228
         B     NEWTRY                   RETRY OUTPUT BUSY        S99228
         SPACE 2
CONUEND  EQU   *                                                 S99228
         SPACE 1
         TM    LCBCSW+CSWSTAT1,CUE      CONTROL UNIT END?        S99228
         BO    TRYCUE                   YES- CLEAR RETRY COUNT  SA67073
*                                       AND RETRY OPERATION     SA67073
         B     SNO                      NO OTHER ERRORS SHOULD   S99228
*                                       OCCUR                    S99228
         EJECT
OLDLOCAL EQU   *                                                 S99228
         SPACE
         TM    LCBSENS0,X'0F'           DATA CHECK, OVERRUN, LOST
*                                         DATA, TIME OUT
         BNZ   SNO                      SHOULD NOT OCCUR ERRORS
         SPACE
         TM    LCBCSW+3,UNITCHK         UNIT CHECK SET
         BZ    SNO                      IT'S UNIT EXCEPTION
         SPACE
         TM    LCBSENS0,EQUIPCHK        EQUIPMENT CHECK SET
         BZ    TRYIR                    NO, TRY INTERVENTION REQ
         SPACE
         TM    CCWOPCDE,CCWREAD         READ COMMAND
         BZ    SNO                      NO, SHOULD NOT OCCUR
         SPACE
TXTERR   EQU   *
         CLI   LCBINCAM,RETRY           RETRY LIMIT REACHED
         BNL   PERMEXIT                 YES
         NI    IOSFLA,AVTEFF-IOSERR     POST PERMANENT ERROR TO  Y02027
*                                       ABNORMAL END APPENDAGE   Y02027
         B     CONVERT                  BRANCH TO SET UP FOR RETRY.
         SPACE 3
TRYIR    EQU   *
         TM    LCBSENS0,INTREQ          INTERVENTION REQUIRED
         BNZ   SNO                      YES
         SPACE
         TM    LCBSENS0,BUSOUT          BUSOUT SET
         BZ    SNO                      NO, BRANCH               Y02027
         TM    LCBSIOCC,X'10'           SIO COND CODE =1
         BZ    SNO                      NO
         B     TRYRETRY                 ATTEMPT RETRY
         EJECT
AUDIO    EQU   *
         TM    LCBCSW+STAT1,SCBXCEPN    UNIT EXCEPTION SET      SA51090
         BNO   CHKOPCDE                 IF NOT, CHECK OP CODE   SA51090
         OI    SCBERR3,SCBXCEPN         FLAG ERROR IN SCB       SA51090
         B     PERMEXIT                 EXIT TO 504G            SA51090
         SPACE 2
CHKOPCDE EQU   *                                                SA51090
         TM    CCWOPCDE,X'20'           DISABLE ENABLE
         BO    PERMEXIT                 EXIT ON ENABLE OR DISABLE
         SPACE
         CLI   LCBSENS0,EQUIPCHK        EQUIPMENT CHECK
         BE    PERMEXIT                 YES
         SPACE
         CLI   CCWOPCDE,CCWREAD         READ COMMAND
         BH    SNO                      NO
         SPACE
         TM    LCBSENS0,OVERRUN         OVERRUN
         BZ    NOTOVRN                  NO
         SPACE
         CLI   CCWOPCDE,CCWREAD         READ COMMAND
         BNE   SNO                      NO
         CLC   CCWCOUNT,LCBCSW+5        ORIGINAL COUNT=RESIDUAL
         BE    TRYRETRY                 YES
         CLI   LCBRESTR,TPTEXT          TEXT ERROR
         BE    TXTERR                   YES
         B     TRYRETRY                 ATTEMPT RETRY
NOTOVRN  EQU   *
         CLI   CCWOPCDE,CCWWRITE        WRITE COMMAND
         BNE   SNO                      SHOULD NOT OCCUR
         SPACE
         TM    LCBSENS0,X'C3'           UNUSUAL ERRORS
         BNZ   SNO                      YES, SHOULD NOT OCCUR
         SPACE
         TM    LCBSENS0,BUSOUT          BUSOUT SET
         BZ    SNO                      NO
         SPACE
         TM    LCBSIOCC,X'10'           SIO CONDITION CODE=1
         BZ    SNO                      NO
         EJECT
         B     TRYRETRY                 RETRY THE FAILURE
TRYCUE   EQU   *                                                SA67073
         MVI   LCBINCAM,AVTEZERO        CLEAR RETRY COUNTER     SA67073
         EJECT
NEWTRY   EQU   *                                                 S99228
         ICM   RWKA,7,LCBCSW            INTERRUPT ADDR = 0     @OS80057
         BNZ   CEDETOG                  BRANCH NO              @OS80057
         L     RWKA,LCBSTART-1          LOAD START ADDRESS     @OS80057
         CLI   0(RWKA),CCWEAU           3270 EAU COMMAND       @OS80057
         BNE   MAYBESEL                 BRANCH NO              @OS80057
         LA    RWKA,LCBCPA+16           LOAD ADDR EAU CCW      @OS80057
         B     INTERSET                 SET INTERRUPT ADDRESS  @OS80057
MAYBESEL EQU   *                                               @OS80057
         CLI   0(RWKA),CCWSEL           3270 SELECT CHANNEL PGM@OS80057
         BNE   MAYBEGEN                 BRANCH NO              @OS80057
         L     RWKA,LCBLSPCI-1          LOAD BUFFER ADDRESS    @OS80057
         B     BEGIN                    ENTER LOOP             @OS80057
MAYBEGEN EQU   *                                               @OS80057
         LA    RWKA,LCBCPA              LOAD ADDR MSGGEN CCW   @OS80057
         B     INTERSET                 SET INTERRUPT ADDRESS  @OS80057
NEWCCW   EQU   *                                               @OS80057
         L     RWKA,8(,RWKA)            LOAD TIC CCW           @OS80057
BEGIN    EQU   *                                               @OS80057
         CLI   11(RWKA),2               END OF CHANNEL PROGRAM @OS80057
         BNE   NEWCCW                   BRANCH NO              @OS80057
INTERSET EQU   *                                               @OS80057
         LR    RCCW,RWKA                SET INTERRUPT ADDRESS  @OS80057
         LA    RCCW,0(,RCCW)                                   @OS80057
         LA    RWKA,8(,RWKA)            INCREASE ADDR FOR CSW  @OS80057
         STCM  RWKA,7,LCBCSW            SET INTERRUPT ADDRESS  @OS80057
CEDETOG  EQU   *                                               @OS80057
         TM    LCBSTAT2,LCBMSGNN        MSGGEN MESSAGE?          S99228
         BO    TRYRETRY                 BRANCH YES               S99228
         LA    RCCW,LCBCPA+16           RESTART AT SELECT CMD    S99228
TRYRETRY EQU   *
         CLI   LCBINCAM,RETRY           RETRY LIMIT REACHED?
         BNL   PERMEXIT                 YES
         SPACE
         IC    RWKA,LCBINCAM            RETRY COUNT
         LA    RWKA,1(,RWKA)            BUMP
         STC   RWKA,LCBINCAM            SAVE BACK
         ST    RCCW,LCBSTART-1          RESTRT ON FAILING CCW
***********************************************************************
*                                                                     *
*        THE FOLLOWING CODE IS ADDED FOR AOS.                         *
*                                                                     *
CONVERT  EQU   *
         CLI   LCBECBCC,HIOCC           IOHALT ISSUED            Y02027
         BNE   SVCS                     BRANCH NO                Y02027
         SPACE 1                                                 Y02027
         NI    IOSFLA,X'FF'-(IOSERR+IOSEX) RESET ERP IN CTL      Y02027
         MVI   LCBINCAM+1,HIOCC         SET FOR LINEEND          Y02027
SVCS     EQU   *                                                 Y02027
**************************************************************** Y02027
*        THE FOLLOWING CODE MAPS THE NECESSARY FIELDS FROM THE   Y02027
*        LINE CONTROL BLOCK TO THE I/O SUPERVISOR BLOCK FOR EXCP Y02027
*        COMPATABILITY DURING RETRY OPERATIONS FROM ERP          Y02027
**************************************************************** Y02027
         MVC   IOSCSW(SEVEN),LCBCSW     MAP CSW FROM LCB TO IOSB Y02027
         MVC   IOSSNS(TWO),LCBSENS0     MAP SENSE INFO FROM LCB  Y02027
*                                       INTO IOSB                Y02027
         MVC   IOSCC(ONE),LCBSIOCC      MAP START I/O CONDITION  Y02027
*                                       CODE FROM LCB TO IOSB    Y02027
         TM    IOSFLA,IOSERR+IOSEX      ARE WE DOING A RETRY     Y02027
         BNO   NOTRETRY                 BRANCH IF NO.            X01004
         LR    RSAVE,RIOSB              SAVE IOSB ADDRESS        Y02027
         L     RTST,IOSUSE              PICKUP ADDR OF RQE       Y02027
         SR    RIOB,RIOB                CLEAR FOR ICM.
         ICM   RIOB,7,IOBPTR(RTST)      GET IOB ADDRESS FROM RQE,
*                                       FOR USE BY SIO APPENDAGE.
         SR    RSIO,RSIO                CLEAR FOR ICM.
         ICM   RSIO,7,DEBPTR(RTST)      GET DEB ADDRESS FROM RQE.
         LA    R15,DEBNMSUB-IEDQDEB     GET APDG. TABLE SIZE     Y02027
         SLR   RSIO,R15                 POINT TO APDG. TABLE     Y02027
         L     RSIO,SIOPTR(ZERO,RSIO)   GET ADDR OF SIO APDG.    X01004
*                                       FROM APDG. VECTOR TABLE.
         SR    R15,R15                  CLEAR R15 TO TELL SIO
*                                       APDG. IT'S A SUBROUTINE.
         BAL   RXCTL,SIOENTRY(ZERO,RSIO)  LINK TO SIO APDG AT    X01004
*                                       OFFSET +16 TO CONVERT CCW'S
*                                       FROM VIRTUAL TO REAL.
         LR    RIOSB,RSAVE              RESTORE IOSB ADDR        Y02027
*                                                                     *
*        END OF CODE ADDED FOR AOS.                                   *
*                                                                     *
***********************************************************************
NOTRETRY EQU   *                                                 X01004
         SPACE
         SVC   EREXCP
         SPACE
         SVC   RETURN                   RETURN TO IOS
         SPACE 5
SNO      EQU   *
         OI    SCBERR4,SCBCTLUN         CONTROL UNIT ERROR
         MVI   LCBINCAM+1,POSNO         SET RETURN FOR 504G
         B     LOADSDR                  USE COMMON CODE
         SPACE
PERMEXIT EQU   *
         MVI   LCBINCAM+1,POPERM        SET RETURN FOR 504G
LOADSDR  EQU   *
         LH    RLINK,POSTMOD            504G ID
         L     RXCTL,CVTPTR             CVT ADDRESS
         L     RXCTL,XCTLADD(,RXCTL)
         BR    RXCTL                    LINK TO SCTL
         EJECT
*              CONSTANTS
POSTMOD  DC    AL2(5047)                IGE0504G
H8       DC    H'8'                     CCW LENGTH
UNITCHK  EQU   X'02'                    MASK FOR UNIT CHECK
BUSY     EQU   X'10'                    BUSY                     S99228
UNEX     EQU   X'01'                    UNIT EXCEPTION           S99228
CUE      EQU   X'20'                    CONTROL UNIT END         S99228
LOSTD    EQU   X'02'                    LOST DATA MASK
TIMEOUT  EQU   X'01'                    TIMEOUT MASK
COMREJ   EQU   X'80'                    COMMAND REJECT           S99228
INTREQ   EQU   X'40'                    INTERVENTION REQUIRED
BUSOUT   EQU   X'20'                    MASK IN SENSE BYTE
OVERRUN  EQU   X'04'                    OVERRUN MASK IN SENSE BYTE
RETRY    EQU   X'02'                    RETRY LIMIT
CVTPTR   EQU   X'10'                    CVT LOCATION POINTER
XCTLADD  EQU   X'2C'                    OFFSET IN CVT-XCTL ROUTINE
DATACHCK EQU   X'08'                    DATA CHECK IN SENSE BYTE
EREXCP   EQU   15                       ERROR EXCP
TPTEXT   EQU   X'FF'                    MASK TP OPCDE TEXT
RETURN   EQU   3                        RETURN SVC FOR TRANSIENTS
EQUIPCHK EQU   X'10'                    MASK FOR EQUIPMENT CHECK
ZERO     EQU   0                        ADDRESS CONSTANT         X01004
ONE      EQU   1                                                 Y02027
TWO      EQU   2                                                 Y02027
SEVEN    EQU   7                                                 Y02027
HIOCC    EQU   X'48'                    IOHALT COMPLETION CODE   Y02027
IOBOFFST EQU   4                        IOB OFFSET IN RQE
ERPCTL   EQU   X'20'                    ERP IN CONTROL BIT
WTOMSG   EQU   253                      LINK VALUE FOR WTO MODULE
SDRLOAD  EQU   256                      OBR/SDR MODULE ID
POSNO    EQU   16                       RETURN INDICATOR
POPERM   EQU   20                       RETURN INDICATOR FOR 504G
IOBPTR   EQU   5                        OFFSET OF IOB ADDR. IN RQE.
DEBPTR   EQU   9                        OFFSET OF DEB ADDR. IN RQE.
SIOPTR   EQU   4                        OFFSET OF SIO APDG. ADDRESS
*                                       IN APDG. VECTOR TABLE.
SIOENTRY EQU   34                       OFFSET OF SUBRTNE. ENTRY Y02027
*                                       POINT IN SIO APPENDAGE.
LCL3277  EQU   X'09'                    3277 LOCAL MODEL CODE    S99228
SIOCC1   EQU   X'10'                    SIO CONDITION CODE 1     S99228
STAT1    EQU   3                        FIRST CSW STATUS BYTE   SA51090
CSWSTAT1 EQU   3                        CSWSTAT1 OFFSET          S99228
UCBTYPE3 EQU   19                       UCB BYTE 19 OFFSET
         EJECT
         TDEBD                          DEB DSECT
         EJECT
         TSCBD
         TLCBD
         TCCWD
         TAVTD
         IECDIOSB                                                Y02027
         END
