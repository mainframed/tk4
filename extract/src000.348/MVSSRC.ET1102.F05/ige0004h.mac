004H     TITLE ' IGE0004H - TCAM/EXCP BSC ERP CONTROL MODULE '
IGE0004H CSECT
*A000000-999999                                                @Y02X6E0
*CHANGE ACTIVITY= AS FOLLOWS:                                    S21903
*A312050-312100,440300-440600,450500                             S22026
*D545000                                                         S22026
*C312000                                                         A42406
*A116000,137000,144000,151000,552000                             S21903
*C327000,414000,461000                                           S21903
*D139000,261000,295000,306000,416000,425000-427000,430000-431000 S21903
*D437000-438000,464000-465000,486000,488000-489000,491000-492000 S21903
*D495000,498000,503000,511000-512000,524000-527000,530000-531000 S21903
*D534000,536000-537000,541000,545000                             S21903
*C204000,223000,382000,465000,466000,471000,472000               S22025
*C402500                                                        SA58994
*C007000,332000                                                @Z30AAEE
*A144796-144814,308200-311800,552030                           @ZA00227
*D144850                                                       @ZA00227
*A151000                                                       @SA72456
*A144770                                                       @ZA02622
*A326000                                                       @ZA02613
*A232000                                                       @SA74968
*C316000                                                       @OS76278
*C232000-232800                                                @OY12439
*A194000                                                       @OY14057
* A120000,129500,135100,144060,144240,144330                   @G36XREV
* C002020,007000,144180,144300                                 @G36XREV
* D142230,144030,144270,144796                                 @G36XREV
*A313800                                                       @OY18043
*                                                                     *
***********************************************************************
*                                                                S21903
*MODULE-NAME= IGE0004H                                           S21903
*                                                                S21903
*DESCRIPTIVE-NAME= TCAM BSC ERROR RECOVERY (FIRST LOAD)          S21903
*                                                                S21903
*COPYRIGHT= 'NONE'                                               S21903
*                                                                     *
*STATUS -- CHANGE LEVEL 10                                     @G36XREV
*                                                                     *
*FUNCTION -- ALL ERP MLDULES ATTEMPT TO RECOVER FROM ALL ERRORS.      *
*   CERTAIN TEXT ERRORS REQUIRE RETURN TO LINE END TO SCHEDULE A      *
*   SUBSEQUENT RECALL OPERATION.                                      *
*                                                                     *
*ENTRY POINTS -- FIRST EXECUTABLE INSTRUCTION.                        *
*                                                                     *
*INPUT --                                                             *
*   R1 - POINTER TO I/O SUPERVISOR BLOCK                         Y02027
*   R15 - ENTRY POINT ADDRESS                                         *
*                                                                     *
*OUTPUT -- R1 POINTS TO I/O SUPERVISOR BLOCK                     Y02027
*                                                                     *
*EXTERNAL ROUTINES -- NONE                                            *
*                                                                     *
*EXITS-NORMAL -- R1 POINTS TO I/O SUPERVISOR BLOCK               Y02027
*        SVC   15   'ERP IN CONTROL (IOSERR)' AND 'EXCEPTIONAL   Y02027
*                CONDITION(IOSEX)'. RETURN TO IOS TO RETRY THE   Y02027
*                   ERROR.                                       Y02027
*        SVC   15   'NO ERROR FLAGS'. RETURN TO LINE END         Y02027
*                   APPENDAGE. ERROR CLEARED                     Y02027
*EXITS-ERROR -- R1 POINTS TO I/O SUPERVISOR BLOCK                Y02027
*        SVC   15   'EXCEPTIONAL CONDITION(IOSEX)'. RETURN TO    Y02027
*                   LINE END APPENDAGE. ERROR PERMANENT.         Y02027
*                                                                     *
*   TO SCHEDULE NEXT LOAD OF ERP                                      *
*        L     13,DECIMAL VALUE OF NEXT LOAD                          *
*        L   14,X'10'    CVT ADDRESS                                  *
*        L     14,X'2C'(14)    ADDRESS OF XCTL ROUTINE                *
*        BR    14   EXIT TO XCTL                                      *
*                                                                     *
*EXITS-ERROR -- R1 POINTS TO REQUEST ELEMENT                          *
*        SVC   15  LCBFLAG1 X'C6'                                     *
*        SVC   3                                                      *
*                                                                     *
*        L     13,DECIMAL VALUE OF NEXT LOAD                          *
*        L   14,X'10'    CVT ADDRESS                                  *
*        L     14,X'2C'(14)    ADDRESS OF XCTL ROUTINE                *
*        BR    14   EXIT TO XCTL                                      *
*                                                                     *
*   TO SCHEDULE ERROR POST                                            *
*                                                                     *
*TABLES/WORK AREAS --                                                 *
*   TAVTD                                                             *
*   TTRMD                                                             *
*   TCCWD                                                             *
*   TLCBD                                                             *
*   DCBD                                                              *
*   TSCBD                                                             *
*                                                                     *
*ATTRIBUTES -- SERIALLY REUSABLE, REFRESHABLE, SUPERVISOR KEY,   Y02027
*              SUPERVISOR MODE, ENABLED                          Y02027
*                                                                     *
*NOTES -- THE OPERATION OF THIS MODULE DOES NOT DEPEND UPON A         *
*   PARTICUALR INTERNAL REPRESENTATION OF THE EXTERNAL CHARACTER      *
*   SET.                                                              *
*                                                                     *
*   R1 REMAINS TRANSPARENT TO ALL LOADS OF ERP.                       *
*                                                                     *
*   EXIT FROM ERP IS MADE WHENEVER THE RETRY COUNT IS EXHAUSTED, OR   *
*   A SHOULD NOT OCCUR ERROR IS PRESETNED.  TEXT ERRORS NOT           *
*   RECOVERABLE BY ERP ARE POSTED TO LINE END APPENDAGE WITH ERROR    *
*   (VIA SVC 15).                                                     *
*                                                                     *
*   IF ANOTHER ERP LOAD IS PERFORMING INTERMITTENT ERP RECOVERY, EXIT *
*   IS MADE TO THE SPECIAL RETURN ERP LOADS.  THIS IS INDICATED BY A  *
*   NON-ZERO VALUE IN LCBINCAM+1.  RETRY COUNT IS MAINTAINED IN       *
*   LCBINCAM.                                                         *
*                                                                     *
*   ON FIRST OCCURRENCE OF AN ERROR, THE CSW, SENSE BYTE, AND TP OP   *
*   CODE ARE SAVED IN LCBCSWSV, LCBSENS1, AND LCBRESTR+1              *
*   RESPECTIVELY.  IF THE ERROR IS CLEARED THROUGH ERP ACTION, THE    *
*   TEMPORARY ERROR COUNTER IN THE TERMINAL ENTRY IS UPDATED.  FOR    *
*   2740/2, THE LEADING GRAPHIC RESPONSE IS SAVED IN LCBSENS1.        *
*                                                                     *
*   THE USER HAS THE OPTION TO RECORD THESE INTERMITTENT ERRORS VIA   *
*   THE OPERATOR CONTROL FACILITY WHCIH SETS THE MASK IN LCBERMSK     *
*   (LINE BASIS) OR TRMSENSE (TERMINAL BASIS).  LINE MASK TAKES       *
*   PRECEDENCE OVER TERMINAL MASK.  THE LOW ORDER 4 BITS INDICATE     *
*   THE NUMBER OF RECORDINGS TO BE PERFORMED FOR THIS TYPE OF ERROR.  *
*   (THE COUNT IS DECREMENTED FOR EACH RECORDING.)                    *
*                                                                     *
*   THE RECORDING MASK IS AS FOLLOWS:                                 *
*   X'10' - TIME OUT                                                  *
*   X'20' - LOST DATA                                                 *
*   X'30' - OVERRUN                                                   *
*   X'40' - DATA CHECK                                                *
*   X'50' - EQUIPMENT CHECK                                           *
*   X'60' - BUSOUT CHECK                                              *
*   X'70' - INTERVENTION REQUIRED                                     *
*   X'80' - COMMAND REJECT                                            *
*   X'90' - UNIT EXCEPTION                                            *
*   X'F0' - INTENSIVE MODE (ANY OF ABOVE)                             *
*   X'A0' - 2740/2 GRAPHIC RESPONSE                                   *
*   PARAMETERS PASSED TO OBR/SDR RECORDING MODULE:                    *
*   LCBERCCW+16 - 1 BYTE INDICATES TYPE OF RECORDING                  *
*      X'00' - OBR                                                    *
*      X'80' - RECORD INTERMITTENT ERROR                              *
*      X'40' - OVERFLOW OF SIO COUNTER OR TEMPORARY ERROR COUNTER     *
*      X'20' - 2740/2 RECORDING                                       *
*   LCBERCCW+17 - 3 BYTES ADDRESS OF TERMINAL COUNTERS                *
*   LCBERCCW+20 - 1 BYTE - SIZE OF TERMINAL NAME                      *
*   LCBERCCW+21 - 3 BYTES - ADDRESS OF TERMINAL NAME                  *
*                                                                     *
*   NOTE THAT ANY CHANGES IN THE OFFSET FROM LCBFLAG1 TO LCBERCCW+16  *
*   MUST BE PRESENTED TO THE OWNERS OF OBR/SDR MODULES.               *
***********************************************************************
         EJECT
         USING IEDQCCW,RCCW
         USING LCBFLAG1,RLCB
         USING IHADCB,RDCB
         USING IEDQAVTD,RAVT
         USING IEDQSCB,RSCB
         USING IEDQTRM,RTERM
         USING IOSB,RIOSB                                        Y02027
         USING CVTDSECT,RCVT                                     Y02027
         USING EWA,RERPWA                                        Y02027
*              REGISTERS
R0       EQU   0
RIOSB    EQU   1                        ADDR OF I/O SUPVR BLK    Y02027
RPARM    EQU   1                        PARAMETER REG          @G36XREV
RUCB     EQU   3                        UCB REGISTER
RLCB     EQU   2                        LCB ADDRESS              X02004
RDEB     EQU   3                        DEB ADDRESS              X02004
RDCB     EQU   4                        DCB ADDRESS              X02004
RCCW     EQU   5                        CCW ADDRESS              X02004
RWKA     EQU   6                        WORK REGISTER
RSUB     EQU   6                        REAL-TO-VIRTUAL SUBROUTINE
*                                       ADDRESS.
RWKB     EQU   7                        WORK REGISTER
RTST     EQU   7                         ADDRESS OF RQE          Y02027
RTERM    EQU   8                        TERMINAL BASE
*                                       IN CCW CONVERT SUB-ROUTING
RCCW2    EQU   8                        SECOND CCW ADDRESS.
RWKC     EQU   9                        WORK REGISTER
RWKD     EQU   10                       WORK REGISTER
RCVT     EQU   10                       ADDR OF CVT              Y02027
R10      EQU   10                       WORK REG               @G36XREV
RAVT     EQU   11                       AAVT BASE
RBASE    EQU   12                       BASE REGISTER.
RLINK    EQU   13                       LINKAGE FOR SUBSEQUENT LOAD
RSAVE    EQU   13                       SAVE REGISTER FOR IOSB   Y02027
RXCTL    EQU   14                       XCTL REGISTER
*                                       ALSO, RETURN ADDRESS FROM
*                                       'IEAPTRV'.
RERPWA   EQU   14                       ADDR OF ERP WORK AREA    Y02027
R15      EQU   15                       ADDRESSABILITY REGISTER
*                                       LINK-TO ADDRESS OF IEAPTRV.
RSCB     EQU   15                       SCB REGISTER
RNTRY    EQU   15                       ENTRY REG              @G36XREV
         EJECT
         USING *,R15                    TEMPORARY BASE.          X02004
IGE0004H IEDHJN ID                                               X02004
         L     RTST,IOSUSE               PICKUP ADDR OF RQE      Y02027
         LM    RLCB,RDEB,IOBPTR(RTST)   PICKUP DEB AND LCB PTRS  Y02027
*                                       FROM THE RQE             Y02027
         L     RDCB,LCBDCBPT            PICKUP ADDR OF DCB       Y02027
         LA    R15,ZERO(RDEB)           COPY DEB ADDRESS         X02004
         LA    RDEB,DEBNMSUB-DEBEOEA    OFFSET TO DEB START      X02004
         SR    R15,RDEB                 ADDRESS OF SIO APPENDAGE X02004
         L     R15,SIOPTR(,R15)         SIO APPENDAG ADDRESS     X02004
SETERP   SETLOCK OBTAIN,TYPE=LOCAL,MODE=UNCOND,RELATED=CHANNEL PROGRAM,X
               IGE0004G(SETERP),IGG019QE(SETERP),IEDQGT(SETTRAN),      X
               IEDQGA(SETASSC,SETCONC),'PCI LINEEND,AND START I/O      X
               APPENDAGES ARE LOCKED ON THIS RESOURCE THRU THE EXCP    X
               PROCESSOR ISSUING SETLOCK (ERP,BUFFER ASSOCIATION AND   X
               TRANSPARENT CCW BUILD MUST ISSUE SETLOCK'
         BAL   RBASE,INTENTRY(R15)      LINK TO SIO APPENDAGE    X02004
*                                       + FOUR TO CONVERT REAL   X02004
*                                       TO VIRTUAL.              X02004
         SPACE 1                                                 X02004
         USING *,RBASE                  USE NEW BASE REGISTER    X02004
         SH    RLCB,LCBSIZE             BACKUP TO LCB            Y02027
         USING IEDQLCB,RLCB                                      Y02027
**************************************************************** Y02027
*        THE FOLLOWING CODE MAPS THE NECESSARY FIELDS FROM THE   Y02027
*        I/O SUPERVISOR BLOCK TO THE LINE CONTROL BLOCK FOR EXCP Y02027
*        COMPATABILITY DURING RETRY I/O OPERATIONS FROM ERP      Y02027
**************************************************************** Y02027
         MVC   LCBCSW(SEVEN),IOSCSW     MAP CSW FROM IOSB TO LCB Y02027
         MVC   LCBSENS0(TWO),IOSSNS     MAP SENSE INFO FROM IOSB Y02027
*                                       TO LCB                   Y02027
         MVC   LCBSIOCC(ONE),IOSCC      MAP START I/O CONDITION  Y02027
*                                       CODE FROM IOSB TO LCB    Y02027
         LR    RWKA,RBASE               SAVE BASE OVER SETLOCK @ZM46665
         LA    R0,LOAD904G              SET BRANCH REGISTER      Y02027
         L     RCVT,CVTPTR              CVT ADDRESS              Y02027
         L     RDEB,CVTAQAVT            TCX ADDRESS              Y02027
         L     RDEB,0(,RDEB)            AVT ADDRESS              Y02027
         CLI   LCBINCAM+1,AVTEFF        CLOSE DOWN ENTRY
         BE    NOTRACE                  BRANCH YES               Y02027
         LA    R0,CONTINUE              SET BRANCH REGISTER      Y02027
         TM    IOSFLA,IOSERR            ERP ALREADY IN CONTROL   Y02027
         BNO   NOTRACE                  BRANCH NOT ERP           Y02027
         LA    R0,SPECIAL               SET BRANCH REGISTER      Y02027
         SLR   RNTRY,RNTRY              CLEAR HI BYTE FOR Q0   @G36XREV
         ICM   RNTRY,AD,AVTIOT+1-IEDQAVTD(RDEB) I/O TRACE ACTIVE Y02027
         BZ    NOTRACE                  BRANCH NO                Y02027
         OI    LCBFLAG1,IOSERR+IOSEX    SET FOR IGG019Q0         Y02027
         LR    R10,RIOSB                SAVE IOSB ADDR         @G36XREV
         L     RPARM,IOSUSE             CLEAR HI BYTE          @G36XREV
         LA    RPARM,0(RPARM)           CLEAR HI BYTE          @G36XREV
         BALR  RXCTL,RNTRY              LINK TO IGG019Q0         Y02027
         SPACE 1                                                 Y02027
         LR    RIOSB,R10                RESTORE IOSB BASE      @G36XREV
         NI    LCBFLAG1,X'FF'-(IOSERR+IOSEX) RESET FLAGS         Y02027
         SPACE 1                                                 X02004
NOTRACE  EQU   *                                                 X02004
RESETERP SETLOCK RELEASE,TYPE=LOCAL,RELATED=CHANNEL PROGRAM,           X
               IGE0004H(SETERP)
         LR    RBASE,RWKA               RESTORE BASE AFTER LOCK@ZM46665
         OI    IOSFLA,IOSERR+IOSEX      INDICATE ERP IN CONTROL  Y02027
         L     RCVT,CVTPTR              RELOAD CVT POINTER     @ZA02622
         LR    RAVT,RDEB                AVT ADDRESS TO RAVT      Y02027
         TM    LCBSIOCC,CC3             SIO COMP 3             @ZA00227
         BO    PATHAVAL                 YES, CHECK ALT. PATH   @ZA00227
NOPATH   EQU   *                                               @ZA00227
         SR    RLINK,RLINK              SET FOR INDEXING         Y02027
         L     RUCB,IOSUCB              UCB ADDRESS              Y02027
         LR    R15,R0                   SET BRANCH REGISTER      Y02027
         BR    R15                      TO PROPER ROUTINE        Y02027
SPECIAL  EQU   *                                                 Y02027
         SPACE
         TM    LCBINCAM+1,AVTEFF        SPECIAL RETURN
         SPACE
         BNZ   LOAD404H                 OTHER INTERMEDIATE ERPN
         SPACE
CONTINUE EQU   *
         MVI   LCBINCAM+1,AVTEZERO      ZERO RETURN INDICATOR  @SA72456
         L     RERPWA,IOSERP            PICKUP ADDR OF ERP WORK  Y02027
*                                       AREA                     Y02027
         L     RSCB,LCBSCBA-1           SCB BASE                 S21903
         CLI   LCBECBCC,HIO             WAS HIO DONE
         BE    HIORET                   YES, RETURN TO NORMAL END
         TM    LCBSIOCC,CC3             SIO COMP CODE 3
         BO    LOAD204G                 YES-CONTROL UNIT NOT UP
         SPACE
         BZ    CHECKCAT                 BRANCH CC=0 CONTINE TESTING
         SPACE
         OI    EWTCFLG,EWTCSEL          INDICATE INTIAL          Y02027
*                                       SELECTION ERROR          Y02027
         L     RWKA,LCBSTART-1          START ADDRESS
         LA    RWKA,EIGHT(R0,RWKA)      BUMP BY 8
         STCM  RWKA,SEVEN,LCBCSW         TRUE FAILING CCW + 8    Y02027
CHECKCAT EQU   *                        CATASTROPIC ERRORS
         SR    RCCW,RCCW                 CLEAR FOR ICM           Y02027
         ICM   RCCW,SEVEN,LCBCSW         ADDRESS FAILURE CCW + 8 Y02027
         SH    RCCW,H8                  BACK UP TO FAILURE CCW
         LA    RCCW,0(,RCCW)            CLEAR HI ORDER BYTE
         LA    RWKA,LCBCPA              START OF CHAN PROGRAM AREA
         SR    RWKA,RCCW                INTERRUPT BEFORE LCBCPA
         BP    TEXTCCW                  BRANCH FOR TEXT CCW
         SPACE
         SR    RWKB,RWKB                CLEAR WORK REGISTER
         IC    RWKB,DCBEIOBX            SIZE OF AN LCB
         LA    RWKB,ZERO(RLCB,RWKB)     POINT TO END OF LCBCPA
         SR    RWKB,RCCW                INTERRUPT OUT OF LCBCPA
         BNP   TEXTCCW                  BRANCH FOR TEXT CCW
         SPACE
         LPR   RWKA,RWKA                MAKE DIFFERENCE POSITIVE
         SRL   RWKA,THREE               DIVIDE BY 8 FOR INDEXING
         IC    RWKA,LCBTPCD(RWKA)       CORRESPONDING TP OF CODE
         STC   RWKA,LCBRESTR            SAVE TP OP CODE
         B     TESTCAT                  TEST CATASTROPHIC ERRORS
TEXTCCW  EQU   *
         MVI   LCBRESTR,TPTEXT          INDICATE TEXTS CCW
TESTCAT  EQU   *
         CLI   LCBINCAM,AVTEZERO        RETRY COUNT EQ 0
         BNE   NOT1ST                   NO
         SPACE
         MVC   LCBRESTR+1(1),LCBRESTR   SAVE TP OP CODE-1ST FAILURE
         IC    RWKA,LCBSENS0            CURRENT SENSE BYTE
         STC   RWKA,LCBSNSV             SAVE FOR RECORDING
         MVC   LCBCSWSV,LCBCSW          SAVE CURRENT CSW
         MVC   EWTCCSW(SEVEN),LCBCSW    MOVE CSW INTO THE ERP    Y02027
*                                       WORK AREA                Y02027
         MVC   EWTCCCW(EIGHT),ZERO(RCCW) MOVE CCW INTO THE ERP   Y02027
*                                       WORK AREA                Y02027
NOT1ST   EQU   *
         CLI   LCBTPCD+3,TPRS3270       RESET 3270             @OY14057
         BE    PERMEXIT                 YES,TREAT AS PERM ERROR@OY14057
         TM    LCBCSW+4,CONTCHK+INTCHK  SEVERE CHANNEL ERRORS
         BNZ   LOAD804H                 YES SEVERE ERRORS
         TM    LCBCSW+4,CHDATCHK        CHANNEL DATA CHECK
         BO    UNUSUAL                  YES
         TM    LCBCSW+3,ATSMCUBY        ATTENTION,STATUS MODIRIER,
*                                         CONTROL UNIT END, BUSY
         BZ    TRYNEXT                  BRANCH NONE OF THESE
UNUSUAL  EQU   *
         OI    LCBINCAM+1,X04           INDICATE UNUSUAL STATUS
         B     LOAD504H                 EXIT TO ERROR POST MOD   S22025
TRYNEXT  EQU   *                        TEST 2ND STATUS BYTE
         TM    LCBCSW+4,CHCKPROT        CHAINING CECK ,PROT CHECK
         BNZ   UNUSUAL                  YES
         SPACE
         TM    LCBCSW+4,PC              PROGRAM CHECK
         BZ    NOPC                     NO
         CLI   CCWOPCDE,CCWTIC          TIC
         BNE   UNUSUAL                  NO
         SPACE
         SH    RCCW,H8                  BACK UP TO TEST CCW
NOPC     EQU   *
         SPACE
         TM    LCBCSW+3,UNITCHK         UNIT CHECK
         BZ    TRYUNEX                  BRANCH NO
         SPACE
         CLI   CCWOPCDE,CCWREAD         READ OR WRITE CCW
         BH    TESTPOLL                 FALL THROUGH-READ OR WRITE
         CLI   LCBRESTR,TPRDRESP        RESPONSE TO POLLING
         BNE   NOPOLRSP                 BRANCH IF NO             S22025
         CLI   LCBCPA+24,CCWPOLL        THIS RESPONSE TO AUTOPOLL
         BE    LOAD404G                 USE AUTOPOLL ERP
NOPOLRSP EQU   *
         LR    R0,RCCW                  ASSUME RESTART ON FAILING
         TM    LCBSENS0,EQCHK+LOSTD     EQUIPMENT CHECK,LOST DATA
         BNZ   LOAD104H                 BRANCH EITHER
         TM    LCBSENS0,TIMEOUT         TIMEOUT SET
         SPACE
         BNZ   RDWRTO                   YES, BRANCH            @OY12439
         TM    LCBSENS0,INTREQ+BUSOUT   INTERVENTION REQ OR BUSOUT
         BNZ   LOAD104H                 EITHER OR BOTH
         SPACE
         B     LOAD204H                 DATA CHECK,OVERRUN,CMD REJ
         SPACE
         SPACE
TESTPOLL EQU   *
         CLI   CCWOPCDE,CCWPOLL         POLL COMMAND
         BE    LOAD404G                 BRANCH YES NEXT LOAD
         SPACE
         B     LOAD304G                 HANDLE UNIT CHECKS FOR NON
*                                         READ, WRITE, POLL CCW'S
         SPACE 3
TRYUNEX  EQU   *
         TM    LCBCSW+3,UNEX            UNIT EXCEPTION SET
         BZ    ERRCLEAR                 NO, ERROR IS CLEARED
         CLI   CCWOPCDE,CCWREAD         READ COMMAND
         BE    ERRCLEAR                 YES
         BL    LOAD104H                 UNIT EXCEIPTION ON EIRTE
         SPACE
         CLI   CCWOPCDE,CCWPOLL         POLL COMMAND
         BE    LOAD404G                 YES
         SPACE
         B     LOAD304G                 NON READ WRITE OR POLL CCW
         SPACE
RDWRTO   EQU   *
         CLI   LCBRESTR,TPRDRESP        RESPONSE TO POLLING
         BNE   CHKRDENQ                 RESPONSE TO POLLING/ENQ
         L     R0,LCBSTART-1            RESTART FROM TOP
         B     TRYRETRY                 TEST RETRY COUNT
         SPACE
CHKRDENQ EQU   *
         CLI   LCBRESTR,TPRDENQ         READ ENQ COMMAND
         BNE   TRYIDAK                  NO
         CLI   LCBINCAM,2               RD ENQ RETRY LIMIT
         BL    TRYRETRY           NO
*        TIME OUT  ON READ ENQ ON DIAL IS NORMAL - NO OBR OR
* ERROR MESSAGE
         NI    IOSFLA,AVTEFF-IOSERR     POST PERMANENT ERROR TO  Y02027
*                                       ABNORMAL END APPENDAGE   Y02027
         B     NOTRETRY                 RETURN                   Y02027
TRYIDAK  EQU   *
         CLI   LCBRESTR,TPRDIDAK        READ ID ACK
         BNE   TRYRSPQ                  NO,
         SPACE
         LA    R0,LCBCPA+40             CPU ID ENQ
         B     TRYRETRY                 ATTEMPT RETRY
TRYRSPQ  EQU   *
         SPACE
         CLI   LCBRESTR,TPRSPENQ        RESPONSE TO ENQ
         BE    BACKUP                   BACK UP TO WRITE ENQ
         SPACE
         CLI   LCBRESTR,TPRDIDNQ        READ ID ENQ
         BNE   LOAD104H                 EXIT FOR OTHER TIME OUT
         B     TRYRETRY                 RETRY READ ID Q
         SPACE
         SPACE
BACKUP   EQU   *
         LR    R0,RCCW                  COPY CCW ADDRES
         SH    R0,H8                    BACK UP TO PREVIOUS
         SPACE
         SPACE
TRYRETRY EQU   *
         CLI   LCBINCAM,RETRY           RETRY LIMIT REACHED
         BE    PERMEXIT                 YES, EXIT TO POST ERROR
         MVI   LCBSENS0,AVTEZERO        CLEAR SENSE FRO RETRY
         SR    RWKB,RWKB                CLEAR WORK REGISTER
         IC    RWKB,LCBINCAM            RETRY COUNT
         LA    RWKB,1(,RWKB)            BUMP
         STC   RWKB,LCBINCAM            SAVE BACK
         ST    R0,LCBSTART-1            RESTART ADDRESS
         B     APPRET                   RETRY THE OPERATION      Y02027
PATHAVAL EQU   *                                               @ZA00227
         L     RUCB,CVTAQAVT            PICK UP TCX            @ZA00227
         USING IEDQTCX,RUCB                                    @ZA00227
CC3LOOP  EQU   *                                               @ZA00227
         TS    TCXCC3TS                 RESERVE SAVE AREA IN TCXOZ00227
         BNZ   CC3LOOP                  LOOP TILL TCX AVAILABLE@ZA00227
         STM   R15,RIOSB,TCXERPSV       SAVE REGS DESTROYED    @ZA00227
         LA    RLINK,TCXESAVE           SAVE AREA FOR IECVIOPM @ZA00227
         L     RIOSB,IOSUCB             UCB ADDRESS            @ZA00227
         LA    RIOSB,ZERO(RIOSB)        CLEAR HIGH BYTE        @ZA00227
         ST    RIOSB,TCXERPPM           SET PARM LIST          @ZA00227
         LA    RIOSB,TCXERPPM           ADDRESS OF PARM LIST   @ZA00227
         LINK     EP=IECVIOPM,MF=(E,(1)) CHECK AVAIL PATHS     @ZA00227
         LTR   R15,R15                  PATH AVAILABLE         @ZA00227
         LM    R15,RIOSB,TCXERPSV       RESTORE REGS           @ZA00227
         MVI   TCXCC3TS,AVTEZERO        CLEAR TS LOCK          @ZA00227
         BNZ   NOPATH                   NO,BRANCH              @ZA00227
         NI    LCBSIOCC,AVTEFF-CC3      RESET CONDITION CODE   @ZA00227
         B     APPRET                                          @ZA00227
ERRCLEAR EQU   *
         TM    LCBFLAG3,LCBOBRRD        PROCESSING TPER RECORD   S22026
         BO    LOAD904H                 YES, LOAD TPER INTERFACE S22026
         MVI   LCBSENS0,AVTEZERO        CLEAR SENSE BYTE SINCE E A42406
*                                         IS CLEARED AND PREVENT
*                                         INVALID ERROR MASK AT
*                                         INMSG/OUTMSG TIME
         NI    IOSFLA,AVTEFF-(IOSERR+IOSEX) RESET ' ERP IN CTL'  Y02027
*                                       AND 'PERMANENT ERROR'    Y02027
*                                       INDICATORS               Y02027
         TM    LCBSTAT1,LCBRECVN        RECEIVE STAT0E
         BZ    RESET                    NO
         CLI   LCBRESTR,TPTEXT          TEXT ERROR
         BE    NORESET                  IF YES, LINE AND WILL RESET
*                                         RETRY COUNT - PREVENT LOOP
*                                         FOR CASE OF CONTINUOUS
*                                         INVALID START OR END
*                                         CHARACTER
         CLI   LCBRESTR,TPRDIDNQ        READ ID AND ENQ?       @OY18043
         BNE   RESET                    NO- BRANCH             @OY18043
         MVI   LCBTSTSW,AVTEZERO        TSTSW 0 IF INCAM 0     @OY18043
RESET    EQU   *
         MVI   LCBINCAM,AVTEZERO        CLEAR RETRY OCUNT
NORESET  EQU   *
         TM    LCBCSWSV+3,UNITCHK+UNEX  UNIT CHECK OR UNIT EXCEPTION
         BZ    APPRET                   BRANCH NO              @OS76278
         SR    RWKD,RWKD                CLEAR WORK REGISTER
         CH    RWKD,LCBTTCIN            SPECIFIC CONNECTION
         BNE   TERM                     BRANCH IF LINE ENRTY
         TM    LCBSTAT2,LCBDIAL         DIAL LINE
         BZ    USEUCB                   BRANCH NO
         LH    RWKC,LCBLNENT            LINE ENTRY USED
         LTR   RWKC,RWKC                LINE ENTRY PRESENT
         BNZ   TERM1                    BRANCH NOT DIAL
         SPACE
USEUCB   EQU   *
         L     RCVT,CVTPTR              CVT ADDRESS            @ZA02613
         LA    RWKA,THREE               UNITNAME LENGTH        @ZA02613
         L     RTERM,CVTSTB             STATISTICS TABLE ADDRESS S21903
         SR    RWKC,RWKC                CLEAR FOR INDEX
         L     RWKB,UCBEXTPT-UCBOB(,RUCB) UCB EXTENSION          Y02027
         IC    RWKC,UCBSTI-UCBCMEXT(,RWKB) STATAB INDEX          Y02027
         LR    RWKB,RTERM               COPY TABLE ADDRESS
LOOP1    EQU   *
         CLM   RUCB,THREE,ZERO(RWKB)    COMPARE UCB ADDRESSES  @Z30AAEE
         BC    4,LOOP2                  YES
         SPACE
         LA    RWKB,2(,RWKB)            NEXT ENTRY
         LA    RWKC,256(,RWKC)          NEXT NEXT STAT TABLE IN
*                                         NEXT ENTRY
         B     LOOP1                    CHECK THIS ENTRY
         SPACE
LOOP2    EQU   *
         MH    RWKC,H10                 MULTIPLY BY 10
         AR    RTERM,RWKC               STAT TABLE ADDRESS SET
         LA    RWKC,UCBNAME-UCBOB(,RUCB) EBCIDIC NAME            Y02027
         B     TCOMMON                  USE COMMON COD
TERM     EQU   *
         LH    RWKC,LCBTTCIN            CURRENTLY CONNECTED
TERM1    EQU   *
         L     RWKB,AVTRNMPT            ADDR OF TERM NAME CODE   Y02027
         USING IEDQTNTD,RWKB                                     Y02027
         BAL   RWKA,TNTDCODE            LOCATE ADDRESS OF THE    Y02027
*                                       TERMINAL TABLE ENTRY.
         LA    RWKA,THREE               SIZE OF UCB NAME         Y02027
         LA    RTERM,TRMSIO             RESTET BASE  ADJUST FOR DIAL
         USING TRMSIO,RTERM
         IC    RWKA,TNTENLEN            SIZE OF EACH NAME        Y02027
         LA    RWKC,79(RWKB,RWKC)       ADDRESS OF TERM ADDRESS
         SR    RWKC,RWKA                SUB SIZE TO GET ADDRESS  Y02027
*                                       OF NAME                  Y02027
TCOMMON  EQU   *
         MVI   EWTCTRM,BLANK                                     Y02027
         MVC   EWTCTRM+ONE(SEVEN),EWTCTRM BLANK OUT TERMINAL     Y02027
*                                       NAME FIELD IN EWA        Y02027
         BCTR  RWKA,ZERO                REDUCE NAME CONT BY ONE  Y02027
*                                       FOR EXECUTE INSTRUCTION  Y02027
         EX    RWKA,MOVENAME            MOVE THE TERMINAL NAME   Y02027
*                                       TO THE ERP WORK AREA     Y02027
         CLC   1(1,RTERM),2(RTERM)      CHECK COUNTS
         BH    NOADD                    OKAY
         MVC   1(1,RTERM),2(RTERM)      SET REALISTIC COUNT
NOADD    EQU   *
         SR    RWKD,RWKD                CLEAR WORK REGISTER
         IC    RWKA,TRMTEMPR            COUNT FO TEMP ERRORS
         LA    RWKA,1(,RWKA)            BUMP COUNT
         STC   RWKA,TRMTEMPR            DAVE BACK
         IC    RWKD,LCBERMSK            ASSUME LINE MASK USED
         LA    RWKB,LCBERMSK            ASSUME LINE MASK
         TM    LCBERMSK,X0F             LINE MASK SET
         BNZ   UPDATE                   BRANCH YES
         SPACE
         IC    RWKD,TRMSENSE            MASK BYTE
         LA    RWKB,TRMSENSE            ADJUST FOR TERMINAL LMASK
UPDATE   EQU   *
         TM    0(RWKB),X0F              ANY RECORDING TO BE DONE
         BZ    NORECORD                 BRANCH NO
         STC   RWKD,LCBERCCW            SAVE MASK IN WORK AREA
         TM    0(RWKB),ANYERR           INTENSIVE MOSDE SET
         BZ    NORECORD                 BRANCH NO MASK SET
         SPACE
         BO    GOSDR                    BRANCH INTENSIVE
         SPACE
         NI    LCBERCCW,XF0             CLEAR COUNT FIELD
         CLI   LCBERCCW,UNEXMSK         IS UNEX OR UNIT CHECK CHOSEN
         BH    NORECORD                 NO RECORDING TO BE DONE  S22025
         SPACE
         LR    RWKA,RWKD                SAVE MASK
         BL    TESTUNCK                 TEST SENSE BYTE
         SPACE
         TM    LCBCSWSV+3,UNEX          UNIT EXCEPTION ERROR
         BZ    NORECORD                 BRANCH NO
         SPACE
         B     GOSDR                    RECORD ERROR
TESTUNCK EQU   *
         SRL   RWKA,4                   AHKFT MASK FOR INDEX
         IC    RWKA,TABLE-1(RWKA)       GET ASSOCIATED SENSE MASK
         EX    RWKA,TMMASK              THIS THE ERROR
         BZ    NORECORD                 BRANCH NO
GOSDR    EQU   *
         MVC   LCBRESTR(1),LCBRESTR+1   TP OP CODE OF 1ST FAILURE
         BCTR  RWKD,0                   DECREMENT RECORDING COUNT
         STC   RWKD,0(RWKB)             SAVE MASK BACK
         SPACE
         STC   RWKD,EWTCMSK             MOVE RECORDING MASK TO   Y02027
*                                       RECORD                   Y02027
         OI    EWTCFLG,EWTCTEM          INDICATE TEMPORARY ERROR Y02027
LOADSDR  EQU   *
         MVC   EWTCSIO(THREE),TRMSIO    MOVE START I/O AND TEMP  Y02027
*                                       ERROR COUNTERS TO THE    Y02027
*                                       ERP WORK AREA            Y02027
         XC    TRMSIO(THREE),TRMSIO     CLEAR ERROR COUNTERS IN  Y02027
*                                       THE TERMINAL TABLE       Y02027
         MVC   EWTCTPF(ONE),LCBRESTR    MOVE 1ST TP OP CODE TO   Y02027
*                                       OBR/SDR RECORD           Y02027
         MVC   EWTCTPL(ONE),LCBRESTR+ONE MOVE LAST TP OP CODE TO Y02027
*                                       OBR/SDR RECORD           Y02027
         MVC   EWTCSNF(ONE),LCBSNSV     MOVE 1ST BYTE OF SENSE   Y02027
*                                       DATA TO OBR/SDR RECORD   Y02027
         MVC   EWTCSNL(ONE),LCBSENS0    MOVE LAST BYTE OF SENSE  Y02027
*                                       DATA TO OBR/SDR DATA     Y02027
         LA    RWKA,EWTCSIO             INDICATE ADDR OF START   Y02027
         STCM  RWKA,SEVEN,EWADDISP      DEVICE DEPENDENT INFO    Y02027
*                                       FOR OBR/SDR              Y02027
         MVI   EWADCNT,OBRDATA          INDICATE SIZE OF DEVICE  Y02027
*                                       DEPENDENT DATA FOR OBR   Y02027
         OI    IOSFLB,IOSLOG            INDICATE REQUEST FOR     Y02027
*                                       OBR/SDR                  Y02027
         LA    RLINK,SDRLOAD            SDR ID
         L     RCVT,CVTPTR              CVT ADDRESS              Y02027
         L     RXCTL,CVTXTLER           XCTL ROUTINE             Y02027
HIOSUB   EQU   *                                                 Y02027
**************************************************************** Y02027
*        THE FOLLOWING CODE MAPS THE NECESSARY FIELDS FROM THE   Y02027
*        LINE CONTROL BLOCK TO THE I/O SUPERVISOR BLOCK FOR EXCP Y02027
*        COMPATABILITY DURING RETRY OPERATIONS FROM ERP          Y02027
**************************************************************** Y02027
         MVC   IOSCSW(SEVEN),LCBCSW     MAP CSW FROM LCB TO IOSB Y02027
         MVC   IOSSNS(TWO),LCBSENS0     MAP SENSE INFO FROM LCB  Y02027
*                                       INTO IOSB                Y02027
         MVC   IOSCC(ONE),LCBSIOCC      MAP START I/O CONDITION  Y02027
*                                       CODE FROM LCB TO IOSB    Y02027
         CLI   LCBECBCC,HIOCC           IOHALT ISSUED            Y02027
         BNER  RXCTL                    BRANCH NO - RETURN       Y02027
         NI    IOSFLA,X'FF'-(IOSERR+IOSEX) RESET ERP IN CTL      Y02027
         MVI   LCBINCAM+1,HIOCC         SET FOR LINEEND          Y02027
         BR    RXCTL                    LINK/RETURN TO CALLER
         SPACE
NORECORD EQU   *
         CLI   TRMTEMPR,AVTEFF          OVERFLOW CONDITION
         BE    OVERFL                   BRANCH YES
         CLC   TRMSIO,AVTCLRHI+2        SIO COUNTER OVERFLOW     S21903
         BNE   APPRET                   BRANCH NO
         SPACE
OVERFL   EQU   *
         OI    EWTCFLG,EWTCOVF          INDICATE SIO COUNTER     Y02027
*                                       OVERFLOW                 Y02027
         B     LOADSDR                  USE COMMON CODE
HIORET   EQU   *
         NI    IOSFLA,X'FF'-(IOSERR+IOSEX) RESET ERP IN CTL      Y02027
APPRET   EQU   *
         BAL   RXCTL,HIOSUB             CHECK FOR IOHALT
***********************************************************************
*                                                                     *
*        THE FOLLOWING CODE IS ADDED FOR AOS.                         *
*                                                                     *
         TM    IOSFLA,IOSERR+IOSEX      ARE WE DOING A RETRY     Y02027
         BNO   NOTRETRY                 BRANCH IF NO.            X01004
         L     RTST,IOSUSE              PICKUP ADDR OF RQE       Y02027
         LR    RSAVE,RIOSB              SAVE IOSB ADDRESS        Y02027
         L     RLCB,IOBOFFST(,RTST)     GET IOB ADDRESS FROM RQE Y02027
*                                       FOR USE BY SIO APPENDAGE.X01004
         L     RTERM,EIGHT(,RTST)       GET DEB ADDRESS FROM RQE X01004
         LA    R15,DEBNMSUB-IEDQDEB     GET APDG. TABLE SIZE     Y02027
         SLR   RTERM,R15                POINT TO APDG. TABLE     Y02027
         L     RTERM,SIOPTR(,RTERM)     GET ADDRESS OF SIO APDG. X01004
*                                       FROM APDG. VECTOR TABLE. X01004
         SR    R15,R15                  CLEAR R15 TO TELL SIO    X01004
*                                       APDG. IT'S A SUBROUTINE. X01004
         BAL   RXCTL,SIOENTRY(,RTERM)   LINK TO SIO APPENDAGE AT X01004
*                                       OFFSET +16 TO CONVERT    X01004
*                                       CCW'S TO REAL.           X01004
         LR    RIOSB,RSAVE              RESTORE IOSB ADDR        Y02027
*                                                                     *
*        END OF CODE ADDED FOR AOS.                                   *
*                                                                     *
***********************************************************************
NOTRETRY EQU   *                                                 X01004
         SVC   EREXCP
         SPACE
         SVC   RETURN
         SPACE 2
PERMEXIT EQU   *
         MVI   LCBINCAM+1,POPERM        RETURN TO 504H
         B     LOAD504H                 NEXT MODULE
         SPACE 2
         SPACE
* THE VALUE OF RLINK INDICATES TO IOS THE NEXT ERP MODULE TO BE
* LOADED
LOAD904H EQU   *                                                 S22026
         LA    RLINK,D1000(,RLINK)      INDEX TO NEXT MODULE     S22026
LOAD804H EQU   *
         LA    RLINK,3000(,RLINK)       INDEX FOR NEXT MODULE
LOAD504H EQU   *
         LA    RLINK,D1000(,RLINK)      INDEX - NIEXT LOAD
LOAD404H EQU   *
         LA    RLINK,2000(,RLINK)       INDEX FOR NEXT LOAD
LOAD204H EQU   *
         LA    RLINK,D1000(,RLINK)      INDEX NEXT LOAD
LOAD104H EQU   *
         LA    RLINK,D1000+X4H(,RLINK)  INDEX + ERP ID
         NI    LCBFLAG3,AVTEFF-LCBOBRRD RESET TPER FLAG          S22026
         B     NEXTLOAD                 USE COMMON CODE
LOAD904G EQU   *
         LH    RLINK,FIVEK              INITAL VALUE FOR 904G
LOAD404G EQU   *
         LA    RLINK,D1000(R0,RLINK)    INDEX-NEXT LOAD
LOAD304G EQU   *
         LA    RLINK,D1000(R0,RLINK)    INDEX-NEXT LOAD
LOAD204G EQU   *
         LA    RLINK,2047(,RLINK)       INDEX + ERP ID
NEXTLOAD EQU   *
         L     RCVT,CVTPTR              PICKUP ADDR OF CVT       Y02027
         L     RXCTL,CVTXTLER           XCTL ROUTINE ADDRESS     S21903
         BR    RXCTL                    CALL NEXT ERP LOAD
         EJECT
*              CONSTANTS
LCBSIZE  DC    AL2(LCBFLAG1-IEDQLCB)    AIZE OF LCB UP TO IOB
H8       DC    H'8'                     SIZE OF CCW              S22025
TMMASK   EQU   *
         TM    LCBSNSV,ZERO             TEST FOR RECORDING
MOVENAME MVC   EWTCTRM(ZERO),ZERO(RWKC) MOVE TERMINAL NAME INTO  Y02027
*                                       THE ERP WORK AREA        Y02027
FIVEK    DC    AL2(5000)                INITIAL VALUE FOR 904G   S22025
H10      DC    H'10'                    STAT TABLE INDEX         S22025
TABLE    EQU   *
         DC    X'01'                    TIME OUT
         DC    X'02'                    LOST DATA
         DC    X'04'                    OVERRUN
         DC    X'08'                    DATA CHECK
         DC    X'10'                    EQUIPMENT CHECK
         DC    X'20'                    BUS OUT
         DC    X'40'                    INTERVENTION REQUIRED
         DC    X'80'                    COMMAND REJECT
         SPACE 3
* EQUATES.
         SPACE
AD       EQU   7                        MASK FOR ICM             Y02027
SEVEN    EQU   7                        ADDRESSING               X01004
IOBPTR   EQU   4                        OFFSET OF IOB ADDRESS IN
THIRTY2  EQU   32
*                                       RQE.
FF       EQU   X'FF'
NINE     EQU   9                        OFFSET OF WRITE IDLES
*                                       LOOP IN LCBCPA.
REALTIC  EQU   X'80'                    FLAG INDICATING THAT A TIC
*                                       CCW HAS A REAL ADDRESS.
INVALID  EQU   X'07'                    INVALID TIC FLAG.
LCBSZOFF EQU   36                       OFFSET OF LCB SIZE FIELD IN
*                                       DCB.
X00      EQU   X'00'
ALL      EQU   15                       ICM MASK - ALL 4 BYTES.
RECORD   EQU   X'80'                    FLAG FOR SDR TO RECORD
*                                         SIO AND ERROR COUNTERS
OVERFLOW EQU   X'40'                    FLAG FOR SDR TO RECORD *
SDRLOAD  EQU   256                      SDR MODULE ID
X04      EQU   X'04'                    'UNUSUAL' RETURN TO 504H
XF0      EQU   X'F0'                    UCB CONTROL UNIT MASK
RETRY    EQU   6                        RETRY COUNT
IOBOFFST EQU   4                        IOB OFFSET IN RQE
ERPCTL   EQU   X'24'                    MASK ERP IN CONTROL
X0F      EQU   X'0F'                    WORK MASK FOR RECORDING
ANYERR   EQU   X'F0'                    MASK FOR INTENSIVE MODE
UNEXMSK  EQU   X'90'                    UNIT CHECK UNEX RECORD MASK
CHDATCHK EQU   X'08'                    MASK-CHANNEL DATA CHECK
CONTCHK  EQU   X'04'                    MASK-CHANNEL CONTROL CHECK
INTCHK   EQU   X'02'                    MASK-INTERFACE CHECK
ATSMCUBY EQU   X'F0'                    MASK-UNUSUAL STATUS
UNITCHK  EQU   X'02'                    UNIT CHECK MASK IN CSW
UCBOFFST EQU   2                        OFFSET TO UCB ADDRESS -RQE
EIGHT    EQU   8
THREE    EQU   3
CC3      EQU   X'30'                    SIO CONDITION CODE TEST
ZERO     EQU   0
UNEX     EQU   X'01'                    UNIT EXCEPTION-CSW STATUS
EREXCP   EQU   15                       ERROR EXCP
CHCKPROT EQU   X'11'                    CHAING OR PROT CHECK
PC       EQU   X'20'                    PROGRAM CHECK
RETURN   EQU   3                        MASK FOR RETURN SVC
D1000    EQU   1000
INTREQ   EQU   X'40'                    MASK FOR INTERVENTION REQ
BUSOUT   EQU   X'20'                    MASK FOR BUSOUT
TIMEOUT  EQU   X'01'                    TIMEOUT MASK I SENSE
EQCHK    EQU   X'10'                    EQUIPMENT CHECK MSSK
LOSTD    EQU   X'02'                    LOST DATA MASK
POPERM   EQU   8                        INDEX FOR 504H RETURN
HIO      EQU   X'48'                    ECB COMPLETION FOR HIO
X4H      EQU   48                       INDEX VALUE FOR BSC ERP
ONE      EQU   1                        CONSTANT OD 1            Y02027
TWELVE   EQU   12                        CONSTANT OF 12          Y02027
BLANK    EQU   X'40'                                             Y02027
OBRDATA  EQU   18                       SIZE OF DEVICE DEPENDENT Y02027
*                                       DATA FOR OBR/SDR         Y02027
SIOPTR   EQU   4                        LOCATION OF SIO APPENDAGEX02004
TWO      EQU   2                                                 Y02027
DEBPTR   EQU   9                        OFFSET OF DEB ADDR. IN RQE.
*                                       ADDRESS IN VECTOR TABLE  X02004
INTENTRY EQU   4                        OFFSET INTO IGG019QE OF  X02004
*                                       CCW TRANSLATION LOGIC    X02004
SIOENTRY EQU   34                       OFFSET OF SUBRTNE. ENTRY Y02027
*                                       POINT IN SIO APPENDAGE.
HIOCC    EQU   X'48'                    HALT I/O COMPLETION      Y02027
         EJECT
         TTNTD
         EJECT
         TDEBD                          X02004
         TCCWD
         TLCBD
         DCBD  DSORG=TX
         TTRMD
         TSCBD
         TAVTD
         TTPD
         TTCXD                                                 @ZA00227
         IECDIOSB                                                Y02027
         IHAPSA
         IECDERWA EWTC
         EJECT
CVTDSECT CVT   DSECT=YES
         EJECT
         DSECT
         IEFUCBOB
         END
