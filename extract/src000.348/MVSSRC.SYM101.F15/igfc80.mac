CC80     TITLE 'IGFC80 - 2880 LOGOUT ANALYSIS ROUTINE'
IGFC80   CSECT
***********************************************************************
*STATUS - OS/VS2 REL3.7 SU851
*
*    APARS FIXED - OZ20318, OZ32238
*
*
*FUNCTION --
*   THIS ROUTINE ANALYZES THE CHANNEL LOGOUT CREATED BY THE 2880
*   CHANNEL ON CHANNEL DETECTED ERRORS-INTERFACE CONTROL CHECK,
*   CHANNEL CONTROL CHECK,AND ALSO CHANNEL DATA CHECK. FROM THIS
*   ANALYSIS IT SETS BITS IN THE WORK ERPIB. IF THE LOGOUT AREA
*   HAS A WORD OF ALL ONES IN IT,THE CHANNEL HAS FAILED TO LOG.
*   IN THIS CASE,THE I/O RESTART(SYSTERM) BIT IS SET. ALSO           CF
*   IF THE LOGOUT OCCURRED ON A MAJOR SCAN BUT THE UNIT CONTROL
*   WORD ADDRESS REGISTER PARITY BIT IS NOT ON,THE SYSTERM BIT IS
*   SET. IT IS ALSO SET IF A TEST I/O TO A BLOCK NULTIPLEX
*   RESULTED IN A SUBCHANNEL STATUS PARITY CHECK,OR IF THE
*   CONDITION WAS NOT A MAJOR OPERATION(SIO,TIO,ETC),NO DATA WAS
*   BEING TRANSFERRED AND WAS NOT AN INTERRUPT RESPONSE,OR IF AN
*   UNUSUAL CONDITION WAS DETECTED WHILE A HALT I/O WAS ISSUED TO
*   A BUSY SUBCHANNEL.
*
*   THE RECORD ONLY BIT IS SET IF A LOGOUT OCCURRED ON A MAJOR
*   SCAN OPERATION AND THE UNIT CONTROL WORD ADDRESS REGISTER
*   (UCWAR) PARITY CHECK BIT WAS ON, OR IF ON AN INTERRUPT,THE
*   INTERRUPT CONTROL BITS ARE UP.
*
*   OTHERWISE, TERMINATION CODES OF FROM ZERO TO THREE AND
*   RETRY CODES OF FROM ZERO TO FIVE ARE SET.
*
*   HAVING COMPLETED THE WORK ERPIB THE MODULE RETURNS TO            CF
*   THE FILL ERPIB SUBROUTINE(IGFCCHFE).                             CF
         SPACE
*ENTRY POINTS --
*        IGFC80  IS THE PRIMARY AND ONLY ENTRY POINT. IT IS          CF
*   ENTERED ANYTIME THE 2880 CHANNEL LOGS OUT AND THE CHANNEL
*   CHECK HANDLER IS IN THE SYSTEM.
         SPACE
*INPUT
*   1. GPR6     -IOS UNIT ADDRESS                                    CF
*   2. GPR8     -ADDRESS OF THE PCCA                                 CF
*   3. GPR14     -RETURN ADDRESS                                     CF
*   4. GPR15     -BASE ADDRESS FOR THIS MODULE                       CF
*   5. THE CHANNEL LOGOUT PER LOCATION 172(DEC)                      CF
*   6. CSW                                                           CF
         SPACE
*OUTPUT
*   1. LOGOUT LENGTH AND ADDRESS TO PCCA                             CF
*   2. WORK ERPIB COMPLETED WITH VALIDITY BITS AND RETRY AND
*   TERMINATION CODES.
*   3. SYSTEM OR RECORD ONLY BITS SET IF CERTAIN CONDITIONS
*   DESCRIBED IN THE FUNCTION SECTION ARE MET.
         SPACE
*EXTERNAL REFERENCES --
*       PCCA                                                         CF
*       MODEL 165/168 LOGOUT                                         CF
*       CSW                                                          CF
         SPACE
*EXITS,NORMAL --
*       RETURN TO CALLER (IGFCCHFE SUBROUTINE OF CCH CENTRAL)        CF
         SPACE
*EXITS,ERROR --
*   NONE
         SPACE
*TABLES/WORKAREAS --
*   1. PCCAWERP (WORK ERPIB)                                         CF
*   2. MODEL 165/168 LOGOUT
*   3. PATCH AREA NAME = C80PATCH                              @ZA32238
*
*ATTRIBUTES --
*   DISABLED, PARALLEL REENTRANT FOR IOS                             CF
         SPACE
*NOTES --
*   THIS ROUTINE IS CHARACTER CODE INDEPENDENT.
*
* CHANGE AREA          APPROX
*    TYPE    APAR     SEQUENCE
*     AC    OZ20318   219500-220100
*     A     OZ20318   090100
*     C     @G51BPLC  103800,269050,279700
*     CD    OZ32238   281000
*     A     OZ32238   103200
*
*
**********************************************************************
         EJECT
*****************************************************************
*REGISTER DEFINITION                                            *
*****************************************************************
         SPACE
RTNADRG  EQU   3                        BASE REG FOR 2880 RTN        CF
LOGBASE  EQU   4                        BASE FOR I/O LOGOUT(2880)
CUAREG   EQU   6                        HOLDS CHANNEL UNIT ADDR
WORKREG5 EQU   5                        WORK REGISTER
WKERPIB  EQU   9                        BASEREG FOR WORK ERPIB
LINKRG2  EQU   14                       SECONDARY LINK REGISTER
LINKRG3  EQU   15                       BASE ADDR FOR THIS ROUTINE   CF
WORKREGB EQU   11                       WORK REGISTER
WORKREGA EQU   10                       WORK REGISTER
CCAREG   EQU   8                        BASE REG FOR THE PCCA
         SPACE
*****************************************************************
*   DEFINITIONS -- THOSE NOT IN STANDARDS                       *
*                                                               *
*   BEG    --  BEGINNING                                        *
*   CC     --  CONTROL CHECK                                    *
*   CMD    --  COMMAND                                          *
*   CPUID  --  CENTRAL PROCESSING UNIT IDENTIFICATION           *
*   CUR    --  CONTROL UNIT REGISTER                            *
*   CVT    --  COMMUNICATIONS VECTOR TABLE                      *
*   DISP   --  DISPLACEMENT                                     *
*   ID(S)  --  IDENTIFICATION(S)                                *
*   SCS    --  SUBCHANNEL STATUS                                *
*   SYS    --  SYSTEM                                           *
*   UA     --  UNIT ADDRESS                                     *
*   UCWAR  --  UNIT CONTROL WORD ADDRESS REGISTER               *
*****************************************************************
         SPACE
*****************************************************************
*GENERAL EQUATES                                                *
*****************************************************************
         SPACE
ZERO     EQU   0                        DISPLACEMENT
O        EQU   0                        DISPLACEMENT
ONE      EQU   1                        CONSTANT-DISPLACEMENT
TWO      EQU   2                        CONSTANT-DISPLACEMENT
THREE    EQU   3                        LOOP CONTROL,DISPLACEMENT
FOUR     EQU   4                        CONSTANT-DISPLACEMENT
FIVE     EQU   5                        DISPLACEMENT
SIX      EQU   6                        DISPLACEMENT
SEVEN    EQU   7                        CONSTANT-ANDING MASK
EIGHT    EQU   8                        CONSTANT- MASK
TWELVE   EQU   12                       DISPLACEMENT
FOURTEEN EQU   14                       CONSTANT-MASK
FIFTEEN  EQU   15                       ANDING MASK
FORTY    EQU   40                  DISPLACEMENT
IOCA     EQU   184                      LOCATION OF UNIT ADDR
CVTPTR   EQU   16                       POINTER TO CVT
LOGREC   EQU   116                      CVT DISP FOR LOGREC DCB PTR
OLDIOPSW EQU   X'38'                    I/O OLD PSW LOCATION
CSWLOC   EQU   64                       CSW LOCATION
ONE08    EQU   108                      DISP OF LAST LOGWORD
ONE11    EQU   111                      LENGTH
FOXFOX   EQU   X'FF'                    CONSTANT-TO FILL LOG
CCC      EQU   X'04'                    CHANNEL CONTROL CHECK
ICC      EQU   X'02'                    INTERFACE CONTROL CHECK
TWO55    EQU   255                 ANDING MASK
         EJECT
         SPACE
*****************************************************************
*2880 -- LOGOUT BIT INTERPRETATIONS                             *
*****************************************************************
         SPACE
CCHDCIN  EQU   X'01'                    DISCONNECT IN INDICATION
SCANMAJR EQU   X'80'                    SCAN MAJOR BIT
UCWARCK  EQU   X'20'               UCWAR UNIT ADDR CHECK
HIOBUSY  EQU   X'02'                    HALT I/O BUSY
TIOSYNCL EQU   X'40'                    TIO SYNCH LATCH
PSEUDOCC EQU   X'80'                    PSEUDO CONTROL CHECK
SCSPCK   EQU   X'04'                    SCS PARITY CHECK
PRIORCK  EQU   X'80'                    PRIORITY CHECK
HIONBUSY EQU   X'08'                    HIO NOT BUSY
OPIN     EQU   X'20'                    OP-IN BIT
SIOMAJOR EQU   X'20'                    SIO MAJOR OP
IRPTRESP EQU   X'20'                    INTERRUPT RESPONSE BIT
CCRESELM EQU   X'02'                    CC RESELECT MAJOR
CCWLATCH EQU   X'80'                    CCW LATCH BIT
CMDADDRV EQU   X'04'                    CMD ADDRESS VALID
USVALID  EQU   X'08'                    UNIT STATUS VALID
CMDOUT   EQU   X'80'                    CMD OUT SENT
DATAXFER EQU   X'40'                    DATA TRANSER INDICATOR
INITDATA EQU   X'10'                    INITIAL DATA
DATAXMAJ EQU   X'04'                    DATA XFER MAJOR
CURMAJOR EQU   X'40'                    CUR MAJOR OP
UCW0111  EQU   X'18'                    UCW0,1 ON
TIOMAJOR EQU   X'10'                    TIO MAJOR OP
ICBD     EQU   10                       ICB EQUAL 5
UNITSEL  EQU   X'80'                    UNIT SELECT
BLKMPX   EQU   X'10'                    BLOCK MULTIPLEX BIT
SHARSUBC EQU   X'10'                    SHARED SUBCHANNEL BIT
CMNDOUT  EQU   X'10'                    COMMAND OUT BIT        @ZA20318
STATIN   EQU   X'80'                    STATUS IN BIT          @ZA20318
         EJECT
*****************************************************************
*THIS IS THE ENTRYPOINT FOR THIS ROUTINE                        *
*****************************************************************
         SPACE
         USING IGFC80,RTNADRG           CSECT BASE                   CF
         USING PCCA,CCAREG         BASE FOR THE PCCA                 CF
         USING IGFERPIB,WKERPIB           BASE FOR WORK ERPIB
         USING IOLOG,LOGBASE            BASE FOR LOGOUT
         SPACE
*****************************************************************
*   THIS SECTION INITIALIZES THE CHANNEL LOGOUT POINTER         *
*****************************************************************
         SPACE 2
         SPACE
         OI    PCCACHF2,PCCACF21        SET FOOTPRINT (2880 ENTERED) CF
CCH02880 LR    RTNADRG,LINKRG3          SET UP BASE REG.             CF
         MODID                                                 @ZA32238
         LA    WKERPIB,PCCAWERP         THE WORK ERPIB               CF
         L     LOGBASE,PCCAELAD         GET LOGOUT LOCATION    @G51BPLC
         SPACE
*****************************************************************
*   THIS SECTION INSURES THAT THE CHANNEL HAS LOGGED. IF IT HAS *
*   FAILED,NOLOG & I/O RESTART BITS WILL BE SET IN THE PCCA.
*****************************************************************
         SPACE
CCHLOGOK CLC   ZERO(FOUR,LOGBASE),ALLONES    DID CHAN FAIL TO LOG
         BE    SETNOLOG                      YES-BR-SET NOLOG        CF
         CLC   ONE08(FOUR,LOGBASE),ALLONES   TEST FAILURE AGAIN
         BE    SETNOLOG                      YES-BR-SET NOLOG        CF
         SPACE
*****************************************************************
*THIS IS WHERE THE LOGOUT ANALYSIS BEGINS                       *
*****************************************************************
         SPACE
         TM    CSWLOC+FIVE,CCC+ICC      INTERFACE OR CHANNEL ERR
         BZ    CKTIOADR                 NO-BR-MUST BE CDC
         TM    LW1,CCHDCIN              WAS UNIT DISCONNECTED
         BZ    ANALBEG                  NO-BRANCH-SKIP SETTING
         OI    IGTERMSQ,CCHDI           SET DISCONNECT IN BIT
         SPACE
ANALBEG  TM    LW3,SCANMAJR             IS THIS MAJOR SCAN
         BC    FOURTEEN,CHECKCLR        NO-BRANCH
SET0301  OI    IGTERMSQ,RTCODE3+COMPFES                              CF
         TM    LW1,UCWARCK              IS THIS UCWAR UA CHECK
         BNO   SETNOLST                 NO, GO SET RECORD ONLY       CF
SET0303  NI    IGVALIDB,FOXFOX-CCHDAV   RESET DEVICE ADDRESS INVALID CF
         B     SETNOLST                 GO RECORD ONLY               CF
*
         SPACE
CHECKCLR TM    RECBYTE,CCHINTB          WAS THIS AN INTERRUPT
         BC    ONE,CKPSEUDO             YES-BRANCH-CK PSEUDO CC
         TM    RECBYTE,CCHTIOB          WAS THIS A TEST I/O
         BC    ONE,TIOSYNCH             YES-BRANCH-TEST SYNCH LCH
         TM    LW3+THREE,HIOBUSY        WAS THIS HALT I/O BUSY
         BC    FOURTEEN,CKPSEUDO        NO-BRANCH-SEE IF PSEUDOCC
         SPACE
SETRCINV NI    IGVALIDB,FOXFOX-CCHRCV   SET RETRY CODE INVALIC
         B     SETSYSTR                 YES-BR-SET I/O RESTART
*
SETNOLOG OI    CCHSW2,NOLOG             SET THE NOLOG FLAG IN PCCA   CF
         B     SETRCINV                 GO SET SEQUENCE CODE INVALID CF
*
         SPACE
TIOSYNCH TM    LW27,TIOSYNCL            TIO SYNCH LATCH UP
         BC    ONE,CKPSEUDO             YES-BRANCH-CK PSEUDO CC
         LH    WORKREGB,LW2             GET TIO UA
         N     WORKREGB,ARMASK          CLEAR UNWANTED
         LH    WORKREGA,LW2+TWO         GET UCWAR AND IAB
         N     WORKREGA,ARMASK          CLEAR UNWANTED
         CR    WORKREGB,WORKREGA        TIO UA EQUALS UCWAR+IAB
         BNE   CCHDAINV                 NO-SET I/O RESTART
CKPSEUDO OI    IGVALIDB,CCHDAV          SET UNIT ADDRESS VALID
CCHDAINV TM    LW0+ONE,PSEUDOCC         IS THIS PSEUDO CONTROL CK
         BC    FOURTEEN,HIONBCK         NO-GO CHECK IF HIO BUSY
         LA    WORKREGA,FIFTEEN         GET ANDING MASK
         IC    WORKREGB,LW23            GET DIAGNOSE INDICATORS
         NR    WORKREGB,WORKREGA        IS THIS A DIAGNOSE
         BC    EIGHT,HIONBCK            NO-BRANCH-TEST HIO
         B     CKDIAGN                  SEE IF DIAGNOSE
*
         SPACE
HIONBCK  TM    LW3,HIONBUSY             IS HIO NOT BUSY STATE SET
         BC    FOURTEEN,TESTSIOM        NO-BRANCH-TEST SIO MAJOR
         TM    LW7+ONE,OPIN             IS OP-IN UP
         BC    FOURTEEN,SET0401         NO- GO SET CODES
SET0410  OI    IGTERMSQ,RTCODE4+COMPSEL SET RETRY 4,TERM 10
         B     LASTVAL                  GO CK LAST VALIDITY BITS
*
         SPACE
TESTSIOM TM    LW3,SIOMAJOR             IS THIS SIO MAJOR OP
         BC    ONE,CKCCWLCH             YES-BRANCH-TEST CCW LCA
         TM    LW3,CCRESELM             IS THIS CC RESELECT MAJOR
         BC    FOURTEEN,CKDATATR        NO-GO CK DATA XFER
CKCCWLCH TM    LW3+TWO,CCWLATCH         IS CCW LATCH SET
         BC    ONE,CKCMDOUT             YES-BRANCH-SEE IF CMD OUT
         SPACE
SET0401  OI    IGTERMSQ,RTCODE4+COMPFES SET RETRY TO 4,TERM TO 01
LASTVAL  TM    LW0+TWO,SCSPCK           IS THIS SCS P CHECK
         BC    ONE,SETSYSTR             YES-SET I/O RESTART
         TM    LW2,CMDADDRV             IS CMD ADDRESS VALID
         BC    FOURTEEN,CKUSVAL         NO-BRANCH-CHECK US VALID.
         OI    IGVALIDB,CCHCMDV         SET CMD ADDRESS VALID BIT
CKUSVAL  TM    LW2,USVALID              IS UNIT STATUS VALID
         BC    FOURTEEN,SETRCVAL        NO-BRANCH-SET RC VALID
         OI    IGVALIDB,CCHUSV          SET UNIT STATUS VALID BIT
SETRCVAL OI    IGVALIDB,CCHRCV          SET RETRY CODE VALID BIT
CKDCIN   TM    LW1,CCHDCIN              WAS UNIT DISCONNECTED
         BZ    KPHOUSE                  NO-GO HOUSEKEEP LOG,EXIT
         NI    IGTERMSQ,FOXFOX-COMPSYS  RESET TERMINATION CODE
         OI    IGTERMSQ,COMPSEL         SET SELECTIVE RESET
         B     KPHOUSE                  HOUSEKEEP LOG-THEN EXIT
*
CKCMDOUT TM    LW3+THREE,CMDOUT         HAS CMD OUT BEEN SET
         BC    ONE,CKDATAXR             YES-BRANCH-CK DATA XFER
         B     SET0410                  GO SET RETRY 4,TERM 10
*
CKDATAXR TM    LW9,DATAXFER             HAS DATA BEEN XFERRED
         BC    ONE,CKINDATA             YES-BRANCH-TEST IF INIT
SET0110  OI    IGTERMSQ,RTCODE1+COMPSEL SET RETRY 1,TERM 10
         B     LASTVAL                  GO CK LAST VALIDITY
*
CKINDATA TM    LW9,INITDATA             INITIAL DATA
         BC    ONE,SET0210              YES-BRANCH-SET CODES
SET0310  OI    IGTERMSQ,RTCODE3+COMPSEL SET RETRY 3,TERM 10
         B     LASTVAL                  CHECK LAST OF VALIDITY
*
SET0210  OI    IGTERMSQ,RTCODE2+COMPSEL SET RETRY 2,TERM 10
         B     LASTVAL                  GO CK LAST VALIDITY
*
         SPACE
CKDATATR TM    LW3,DATAXMAJ             IS THIS DATA XFER MAJOR
         BC    ONE,CKINDATA             YES-BRANCH-CK INIT DATA
         TM    LW3,CURMAJOR             CUR MAJOR UP
         BC    FOURTEEN,CKTIOMAJ        NO-BRANCH-CK TIO MAJOR
         TM    LW7,CMNDOUT              CMDOUT TAG UP IF YES   @ZA20318
         BC    ONE,TSHRSUB              ADDRESS REG IS VALID   @ZA20318
         TM    LW7+ONE,STATIN           STATUS IN TAG UP YES   @ZA20318
         BC    FOURTEEN,SETUAINV        ADDRESS REG IS VALID   @ZA20318
TSHRSUB  TM    LW2+TWO,SHARSUBC         IS THIS A SHARED SUBCHAN
         BC    FOURTEEN,SET0510         NO-BRANCH-SET CODES
         TM    LW3+THREE,UCW0111        UCW 0,1 BOTH ONES
         BC    ONE,SET0510              YES-BRANCH-SET CODES
         SPACE
SETUAINV NI    IGVALIDB,FOXFOX-CCHDAV   SET UNIT ADDRESS INVALID
SET0510  OI    IGTERMSQ,RTCODE5+COMPSEL SET RETRY TO 5,TERM TO 10
         B     LASTVAL                  GO CK LAST VALIDITY,EXIT
*
SET0501  OI    IGTERMSQ,RTCODE5+COMPFES SET RETRY TO 5,TERM TO 1
         B     LASTVAL                  GO CHECK VALIDITY
*
         SPACE
CKTIOMAJ TM    LW3,TIOMAJOR             IS THIS TIO MAJOR OP
         BC    ONE,CKSCS                YES-BRANCH-CHECK SCS
         TM    LW3+ONE,IRPTRESP         IRPT RESPONSE UP
         BC    FOURTEEN,SET0303         NO-SET RECORD ONLY           CF
         TM    LW2+TWO,FOURTEEN         ICB EQUAL 7
         BC    ONE,SET0501              YES-BRANCH-SET CODES
         TM    LW2+TWO,ICBD             ICB EQUAL D
         BC    ONE,SET0501              YES-BRANCH-SET CODES
CKUNITSL TM    LW9,UNITSEL              UNIT SELECT
         BC    ONE,SET0010              YES-BR-HARDWARE TIO
SET0001  OI    IGTERMSQ,RTCODE0+COMPFES SET RETRY TO 0,TERM TO 01
         B     LASTVAL                  GO CK LAST VALIDITY
*
SET0010  OI    IGTERMSQ,RTCODE0+COMPSEL SET RETRY 0,TERM 10
         B     LASTVAL                  GO FINISH LAST VALIDITY
         SPACE
CKSCS    IC    WORKREGB,LW22            GET SCS INDICATORS
         LA    WORKREGA,FOURTEEN        GET ANDING MASK
         NR    WORKREGB,WORKREGA        CLEAR UNWANTED
         IC    WORKREGB,SCSLIST(WORKREGB)  GET SCS EQUALS 1 IND
         CR    WORKREGB,WORKREGA        SCS EQUAL 1
         BNE   SET0001                  NO-BRANCH-SET CODES
         B     CKUNITSL                 GO SEE IF UNIT SELECTED
*
         SPACE
********                                                       ********
*        THE BELOW INSTR. IS FOR FUTURE RELEASE SUPPORT.              *
********                                                       ********
CKDIAGN  B     HIONBCK                  GO SEE IF HIO NOT BUSY
         SPACE
CKTIOADR TM    RECBYTE,CCHTIOB          WAS THIS TIO
         BZ    SETNOLST                 NO-GO RECORD ONLY
         LA    WORKREGB,TWO55      GET ANDING CONSTANT          GB1/71
         NR    WORKREGB,CUAREG          GET UNIT ADDRESS ONLY
         SR    WORKREGA,WORKREGA        CLEAR GARBAGE                CF
         IC    WORKREGA,LW2+THREE       GET IAB ADDRESS              CF
         CR    WORKREGA,WORKREGB        PROPER ADDRESS               CF
         BE    SETNOLST                 YES-BR-GO RECORD
         B     SET0303                  GO SET DEVICE ADDR INVALID   CF
         SPACE
*****************************************************************
*THIS SECTION SETS THE I/O RESTART(SYSTERM) BIT AND/OR          *
*THE RECORD ONLY(CCHNOLST) BIT AND PERFORMS A HOUSEKEEPING      *
*FUNCTION ON THE CHANNEL LOGOUT.                                *
*RECORD THE BLAME FIELD AND RETURN VIA LINKRG2.                 *
*****************************************************************
         SPACE
SETSYSTR OI    CCHSW2,SYSTERM           SET I/O RESTART
SETNOLST OI    CCHSW1,CCHNOLST          SET RECORD ONLY SWITCH
*
KPHOUSE  IC    WORKREGB,BLAME           GET SOURCE OF ERROR
         SLL   WORKREGB,THREE           FORMAT IT
         STC   WORKREGB,IGBLAME         RECORD IT
         ST    LOGBASE,PCCALOGA         PUT ADDR OF LOGOUT IN PCCA   CF
         MVC   PCCALOGL,LOGLEN          PUR LOG LENGTH IN PCCA @G51BPLC
         BR    LINKRG2                  RETURN TO CCH CENTRAL        CF
         EJECT
*****************************************************************
*THE FOLLOWING CONSTANTS DO NOT CHANGE DURING THE EXECUTION OF  *
*THIS ROUTINE                                                   *
*****************************************************************
         SPACE
ALLONES  DC    F'-1'                    FOR LOG HOUSEKEEPING
ARMASK   DC    X'000001FF'              ANDING MASK
SCSLIST  DC    X'0E'                    SUBCHANNEL STATUS BITS
         DC    5X'00'                   PADDING
         DC    X'0E000E'                SUBCHANNEL STATUS BITS
         DC    6X'00'                   PADDING
HFORTY   DC    H'40'               LENGTH OF CCH CHAN TAB PREFIX
LOGLEN   DC    H'112'              LENGTH OF LOGOUT            @G51BPLC
         SPACE 2
         DS    0F
C80PATCH DC    50X'00'             MAINTENENCE PATCH AREA      @ZA32238
MAINT    EQU   C80PATCH            FOR XREF                    @ZA32238
         EJECT
         SPACE
*****************************************************************
*THE FOLLOWING DSECT IS OF THE LOGOUT AREA FOR THE 2880         *
*****************************************************************
         SPACE
IOLOG    DSECT                          2880 CHANNEL LOGOUT AREA
LW0      DS    F                        I/O LOGOUT WORD 0
LW1      DS    F                        I/O LOGOUT WORD 1
LW2      DS    0F                       I/O LOGOUT WORD 2
CHANLID DS     C                        CHANNEL NUMBER,BITS 3-6
         DS    3C                       REMAINDER OF WORD 2
LW3      DS    F                        I/O LOGOUT WORD 3
LW4      DS    F                        I/O LOGOUT WORD 4
LW5      DS    F                        I/O LOGOUT WORD 5
LW6      DS    F                        I/O LOGOUT WORD 6
LW7      DS    F                        I/O LOGOUT WORD 7
LW8      DS    F                        I/O LOGOUT WORD 8
LW9      DS    F                        I/O LOGOUT WORD 9
LW10     DS    F                        I/O LOGOUT WORD 10
LW11     DS    F                        I/O LOGOUT WORD 11
LW12     DS    F                        I/O LOGOUT WORD 12
LW13     DS    F                        I/O LOGOUT WORD 13
LW14     DS    F                        I/O LOGOUT WORD 14
LW15     DS    F                        I/O LOGOUT WORD 15
LW16     DS    F                        I/O LOGOUT WORD 16
LW17     DS    F                        I/O LOGOUT WORD 17
LW18     DS    F                        I/O LOGOUT WORD 18
LW19     DS    F                        I/O LOGOUT WORD 19
LW20     DS    F                        I/O LOGOUT WORD 20
LW21     DS    F                        I/O LOGOUT WORD 21
LW22     DS    F                        I/O LOGOUT WORD 22
LW23     DS    F                        I/O LOGOUT WORD 23
LW24     DS    F                        I/O LOGOUT WORD 24
LW25     DS    F                        I/O LOGOUT WORD 25
LW26     DS    F                        I/O LOGOUT WORD 26
LW27     DS    0F                       I/O LOGOUT WORD 27
OPERATN  DS    CL1                      MAJOR OPERATION INDICATOR
BLAME    DS    CL1                      SOURCE OF ERROR
         DS    CL2                      LAST TWO BYTES N/A
         EJECT
         SPACE
         IGFERPIB   1
         EJECT
         SPACE
         IHAPCCA
         SPACE 2
         END
