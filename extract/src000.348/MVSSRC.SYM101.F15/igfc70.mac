CC70     TITLE 'IGFC70 - 2870 CHANNEL ANALYSIS ROUTINE'
IGFC70   CSECT
         USING *,RTNADRG                                             CF
         USING PCCA,CCAREG           BASE FOR THE PCCA               CF
         USING IGFERPIB,WKERPIB      BASE FOR WORK ERPIB DSECT
******************************************************************
*
*FUNCTION
*    THIS MODULE PERFORMS THE CHANNEL DEPENDENT ANALYSIS FOR THE 2870
*     CHANNEL. IT RECEIVES CONTROL FROM THE FILL ERPIB SUBROUTINE
*     (IGFCCHFE) OF THE CCH CENTRAL MODULE.
*
*     USING THE INFORMATION CONTAINED WITHIN THE LOGOUT, THIS MODULE
*     DETERMINES THE CORRECT SETTINGS FOR THE VALIDITY BITS,SOURCE OF
*     ERROR INDICATORS, TERMINATION CODES, AND SEQUENCE CODES OF THE
*     WORK ERPIB. HAVING COMPLETED THE WORK ERPIB THE MODULE RETURNS
*     TO THE FILL ERPIB SUBROUTINE(IGFCCHFE).
*
* STATUS  OS/VS2 REL3.7 SU851
*
*     APARS FIXED - OZ00516, OZ18585, OZ32238
*
*ENTRY POINTS
*   IGFC70 -  FROM THE CENTRAL ERPIBSET ROUTINE.                     CF
*
*INPUT
*   1. CHANNEL LOG
*   2. GPR6     -IOS UNIT ADDRESS
*   3. GPR8     -ADDRESS OF THE PCCA
*   4. GPR14     -RETURN ADDRESS
*   5. GPR15     -BASE ADDRESS FOR THIS MODULE
*   6. CSW
*
*OUTPUT
*   1. APPROPRIATE BITS IN ERPIB
*   2. LOGOUT LENGTH AND ADDRESS TO PCCA
*
*EXTERNAL REFERENCES
*   CHANNEL LOG
*   PCCA
*   CSW
*
*EXIT,NORMAL
*   RETURN IS VIA LINK REGISTER 14 TO A POINT IN THE                 CF
*   CENTRAL ERPIB FILL SUBROUTINE(IGFCCHFE).                         CF
*
*
*EXIT,ERROR
*   NONE
*
*TABLES/WORK AREA
*   PCCAWERP (WORK ERPIB)                                            CF
*   C70PATCH MODULE PATCH AREA
*
*ATTRIBUTES
*   IGFC70 IS DISABLED,PRIVILEGED AND REUSABLE
*
*NOTES
*
*   ERPIB ERROR RECOVERY PROCEDURE INTERFACE BYTES
*
*   BYTE 0 - ZERO
*   BYTE 1,2,3 - UCB ADDRESS POINTER
*   BYTE 4 - PROGRAM FLAG BITS
*        BIT 0 - SIO INST STORED A CSW
*        BIT 1 - I/O INTERRUPT STORED A CSW
*        BIT 2 - TIO INST STORED A CSW
*        BIT 3 - HIO INST STORED A CSW
*        BIT 4 - NOT USED
*        BIT 5 - SENSE DATA STORED
*        BIT 6 - VALID COUNT APPEARS IN CSW
*        BIT 7 - UNCONDITIONAL NO RETRY
*   BYTE 5 - PROBABLE SOURCE OF ERROR INDICATOR BITS
*        BIT 0 - CPU ERROR
*        BIT 1 - CHANNEL ERROR
*        BIT 2 - STORAGE CONTROL UNIT ERROR
*        BIT 3 - STORAGE ERROR
*        BIT 4 - CONTROL UNIT ERROR
*        BIT 5,6,7 - NOT USED
*   BYTE 6 - VALIDITY INDICATOR BITS
*        BIT 0 - INTERFACE ADDRESS
*        BIT 1,2 - NOT USED  MUST BE ZERO
*        BIT 3 - RETRY CODE
*        BIT 4 - DEVICE STATUS
*        BIT 5 - CHANNEL PROGRAM ADDRESS AND KEY
*        BIT 6 - CHANNEL ADDRESS
*        BIT 7 - DEVICE ADDRESS
*   BYTE 7 - TERMINATION/RETRY CODE BITS
*        BIT 0,1 - TERMINATION CODE BITS
*        BIT 2,3 - NOT USED
*        BIT 4 - DISCONNECT-IN
*        BIT 5,6,7 - RETRY BITS
*   CHARACTER CODE DEPENDENCIES
*        THIS ROUTINE IS CHARACTER CODE INDEPENDENT
*
* CHANGE AREA          APPROX
*    TYPE    APAR     SEQUENCE                                 @ZA18585
*     A     OZ18585    041000
*     A     OZ18585    206460
*     A     OZ18585    050100
*     C     @G51BPLC   301200
*     DA    OZ32238    081200,086600
*     CD    OZ32238    307000,308600
*
*
******************************************************************
         EJECT
****************************************************************
*
*****    REGISTER DEFINITION       *****************************
*
****************************************************************
RTNADRG  EQU   3              BASE REG FOR THIS ROUTINE              CF
XLATEREG EQU   1              REG USED BY THE TRANSLATE ROUTINE      CF
UAREG    EQU   6 .            UNIT ADDRESS
WKREG1   EQU   10 .           MAIN WORKING REGISTER
WKREG2   EQU   5 .            WORKING REGISTER                       CF
WKREG3   EQU   4 .            SPACE WORK REGISTER                    CF
WKERPIB  EQU   9 .            ADDRESS OF WORK ERPIB                  CF
LINKRG1  EQU   2              LINK REGISTER                          CF
LINKRG2  EQU   14 .           SECONDARY LINK REGISTER
WORK4    EQU   LINKRG1 .      WORK REG TO FIND INTERCEPT PT.
CCAREG   EQU   8              BASEREG FOR PCCA                       CF
LINKRG3  EQU   15             REG USED TO LINK TO THE ROUTINE        CF
REG0     EQU   0              ASID RTN FROM XLATE RTN          @ZA18585
TESTWK   EQU   7              WORK REGISTER                    @ZA18585
         SPACE 2
****************************************************************
*
*****    FIXED CORE LOCATIONS     ******************************
*
****************************************************************
CSWLOC   EQU   64 .      CSW LOCATION
LOGLOC   EQU   304 .                    LOCATION OF LOG OUT AREA
LOGADDR  EQU   X'130'                   ADDR OF THE LOGOUT           CF
LOGWD1A  EQU   304 .                    1ST HALF OF LOG WORD 1
LOGWD1B  EQU   308 .                    2ND HALF OF LOG WORD 1
LOGWD2A  EQU   312 .                    1ST HALF OF LOG WORD 2
LOGWD2B  EQU   316 .                    2ND HALF OF LOG WORD 2
LOGWD3A  EQU   320 .                    1ST HALF OF LOG WORD 3
LOGWD3B  EQU   324 .                    2ND HALF OF LOG WORD 3
         SPACE 2
****************************************************************
*
*        CSW STATUS INDICATORS
*
****************************************************************
CCCBIT   EQU   X'04' .                  CHANNEL CONTROL CHECK
ICCBIT   EQU   X'02' .                  INTERFACE CONTROL CHECK
*
*****************************************************************
*
*        GENERAL EQUATES
*
CCH23    EQU   23 .                     CONSTANT FOR DISPLACEMENT
CCH3     EQU   3 .                      CONSTANT FOR DISPLACEMENT
CCH4     EQU   4 .                      CONSTANT FOR DISPLACEMENT
ALLFS    EQU   X'FF' .                  CONSTANT FOR MASK
ZERO     EQU   0 .                      CONSTANT FOR DISPLACEMENT
SIX      EQU   6 .                      CONSTANT NUMBER OF LOGOUT WORDS
THREE    EQU   3 .                      CONSTANT FOR DISPLACEMENT
FOUR     EQU   4                        DISPLACEMENT
HEX06    EQU   X'06' .                  CONSTANT FOR MASK
HEX40    EQU   X'40' .                  CONSTANT FOR MASK
HEX70    EQU   X'70' .                  CONSTANT FOR MASK
HEX82    EQU   X'82'                    CONSTANT FOR MASK
FIVE     EQU   5 .                      CONSTANT FOR DISPLACEMENT
TWO      EQU   2 .                      CONSTANT FOR DISPLACEMENT
HEX02    EQU   X'02' .                  CONSTANT FOR DISPLACEMENT
ONE      EQU   1 .                      CONSTANT FOR DISPLACEMENT
HEX20    EQU   X'20' .                  CONSTANT FOR MASK
HEX04    EQU   X'04' .                  CONSTANT FOR MASK
HEX80    EQU   X'80' .                  CONSTANT FOR MASK
HEX10    EQU   X'10' .                  CONSTANT FOR MASK
HEX08    EQU   X'08' .                  CONSTANT FOR MASK
HEX03    EQU   X'03' .                  CONSTANT FOR MASK
HEX01    EQU   X'01' .                  CONSTANT FOR MASK
HEXE0    EQU   X'E0' .                  MODE TRIGGERS 9,10,11
HEX0C    EQU   X'0C'                    CONSTANT FOR MASK
HEXC0    EQU   X'C0'                    CONSTANT FOR MASK
SEVEN    EQU   7                        CONSTANT FOR DISPLACEMENT
*****************************************************************
         SPACE 2
*****************************************************************
*****    2870 DEPENDENT ERPIB FILL    **************************
*
*        THIS PORTION OF THE ERPIBSET ROUTINE ANALYZES A 2870
*        CHANNEL LOG AND SETS THE APPROPRIATE BITS IN THE ERPIB.
*
****************************************************************
         SPACE 2
         OI    PCCACHF1,PCCACF18  SET FOOTPRINT ( IGFC70 ENTERED )   CF
         LR    RTNADRG,LINKRG3    SET UP THE BASE REG               CF
         MODID                                                 @ZA32238
         LA    WKERPIB,PCCAWERP         WORK ERPIB                  CF
CCH02870 LA    WKREG2,SIX         CTRL NUMBER OF WORDS TO CHECK
         LA    WORK4,LOGLOC        GET BEGIN OF CHAN LOGOUT AREA
CCH029A  CLC   ZERO(CCH4,WORK4),ALLONES   DID CHAN FAIL TO LOG
         BE    SETSYSTR            YES, BRANCH TO SET TERMINATION
         LA    WORK4,CCH4(WORK4)   POINT TO NEXT LOG WORD
         BCT   WKREG2,CCH029A      GO CONTINUE TEST
         TM    LOGWD3B+ONE,HEX40   WORD 2 CHECK
         BZ    CCH140 .                 NO,GO SET COMMAND ADDR VALID
         TM    LOGWD2B,HEX70 .          ANY PARITY BITS
         BZ    CCH140 .                 NO,GO SET COMMAND ADDR VALID
         OI    IGBLAME,CCHCHNL .       YES,CHANNEL IS SOURCE OF ERROR
         B     CCH141 .
         SPACE 2
CCH140   TM    LOGWD3B+ONE,HEX10        IS STORAGE CHECK SET
         BZ    CCH140A                  NO,GO SET CMD ADDR VALID
         TM    LOGWD3A,HEX80            IS THIS CAW FETCH
         BO    CCH141                   YES, COMMAND ADDR INVALID
CCH140A  OI    IGVALIDB,CCHCMDV         SET COMMAND ADDR VALID
         SPACE 2
CCH141   TM    CSWLOC+FIVE,CCCBIT .     IS CCC IN CSW
         BO    CCH141B .                IF YES,CHK LS ADDER
CCH141H  TM    LOGWD3B+TWO,HEX02 .      IS ADDR-IN CHK UP
         BZ    CCH141D .                NO,BRANCH
CCH141A  OI    IGBLAME,CCHINTFC .      I/F IS SOURCE OF ERROR
         B     CCH141F .                TEST FOR STG CHECK
         SPACE 1
CCH141B  TM    LOGWD3B+ONE,HEX20 .      IS LOCAL STORE ADDER CHK UP
         BZ    CCH141C .                NO,BRANCH
         TM    LOGWD3B,HEX04            BIT 37,LOGWD 3 SET
         BZ    CCH141E .                NO,BRANCH. CHANNEL ERROR
         OI    IGBLAME,CCHCPU .        YES,CPU(UAB0) ERROR
         B     CCH141F .                TEST FOR STG CHECK
         SPACE 1
CCH141C  TM    LOGWD3B+TWO,HEX80 .      UNIT ADDR CHECK SET
         BO    CCH141E .                YES,BRANCH. CHANNEL ERROR
         B     CCH141H .                SEE IF ICC IN LOG
CCH141D  LA    WKREG2,LOGWD3A+THREE .   GET UA ADDR IN LOG AREA.
         SPACE 1
*****************************************************************
*        THIS ROUTINE CHECKS TO INSURE THAT THE CORRECT UNIT ADDRESS
*        IS SET VALID. IF IOS UNIT ADDRESS IS NOT EQUAL TO THE UNIT
*        ADDRESS IN THE LOG, THE IOS UNIT ADDRESS IS INVALID
*****************************************************************
         SPACE 2
UAVALSET TM    RECBYTE,CCHTIOB .        IS THIS AN ERROR ON A TIO
         BO    CCHTSTUA .               BRANCH TO TEST IF UA OK
CCHDAVAL OI    IGVALIDB,CCHDAV .        SET UA VALID BIT
         B     CCH141F                  TO RETURN TO NORMAL FLOW
         SPACE 1
*                                      DOES THE UA FROM IOS EQ THE
TSTLOGUA CLI   ZERO(WKREG2),ZERO       UA IN THE LOG
         SPACE 1
CCHTSTUA EX    UAREG,TSTLOGUA .         SEE IF HAVE CORRECT UA
         BE    CCHDAVAL                YES,GO SET DEVICE ADDRESS VALID
         B     CCH141F .                GO TEST FOR STORAGE CHK
         SPACE 2
CCH141E  OI    IGBLAME,CCHCHNL .       INTERNAL CHANNEL ERROR
         SPACE 1
***************************************************************
*
*        CHECK FOR SOURCE OF ERROR ON STORAGE CHECK
*
****************************************************************
         SPACE 2
CCH141F  TM    LOGWD3B+ONE,HEX10 .      STORAGE CHECK
         BZ    CCH142 .                 NO,GO CHECK COUNT VALID
         TM    LOGWD3B,HEX02 .          BIT 38 UP
         BZ    CCH141G                 NO,BRANCH PROBABLE MEMORY ERR
         OI    IGBLAME,CCHCHNL .       CHANNEL IS SOURCE OF ERR
         TM    LOGWD3A,HEX40            CCW REQUIRED SET
         BZ    CCH142                   NO,CHECK COUNT VALID
         B     CCH143A                  DO NOT SET COUNT VALID
         SPACE 1
CCH141G  OI    IGBLAME,CCHSTG .        STORAGE IS SOURCE OF ERR
         B     CCH143A                  DO NOT SET COUNT VALID
         SPACE 1
*****************************************************************
*        CHECK FOR VALID COUNT
*****************************************************************
         SPACE 2
CCH142   TM    LOGWD3B+ONE,HEX08 .      IS BYTE COUNT PARITY BAD
         BO    CCH142A .                YES,BRANCH.INVALID COUNT
         TM    LOGWD3B+TWO,HEX20 .      BYTE COUNT(SSC ONLY) BAD
         BO    CCH142A .                YES,BRANCH
         TM    LOGWD3B+ONE,HEX80 .      UCW COUNT CHECK
         BZ    CCH143 .                 NO,BRANCH-SET COUNT VALID
         TM    LOGWD2B,HEX03 .          ANY UCW PARITY BITS
         BZ    CCH143 .                 NO,BRANCH-SET COUNT VALID
         SPACE 1
CCH142A  OI    IGBLAME,CCHCHNL .       CHANNEL IS SOURCE OF ERR
         B     CCH143A                  BY PASS COUNT VALID BIT
         SPACE 2
CCH143   OI    IGPRGFLG,CCHCNTB .       SET COUNT VALID BIT
CCH143A  TM    LOGWD3B+ONE,HEX06 .      IS BIT 45 OR 46 ON,LOGWD 3
         BZ    CCH144 .                 NO,BRANCH
         OI    IGBLAME,CCHCHNL .       YES,PROG CHANNEL ERR
         SPACE 1
*****************************************************************
*        TEST  FOR VALID UNIT STATUS
*****************************************************************
         SPACE 1
CCH144   TM    LOGWD3B+TWO,HEX01 .      IS STATUS-IN CHECK UP
         BNO   CCH145 .                 GO CHK FOR SYSTEM RESET
         SPACE 1
CCH144A  OI    IGBLAME,CCHINTFC .      I/F IS SOURCE OF ERROR
         B     CCH145A             GO CHK FOR CHANNEL CHECK
         SPACE 1
CCH145   TM    CSWLOC+FIVE,ICCBIT .     IS THIS ICC
         BO    CCH145A .                NO,BRANCH
         OI    IGVALIDB,CCHUSV .        NO,SET UNIT STATUS VALID
*                                       GET LOGWORD FOR TYPE TWO
CCH145A  L     WKREG2,LOGWD3B           CHANNEL CHECK
         N     WKREG2,SYSRESET          CHECK FOR A TYPE II ERROR
         BZ    CCH146 .                 NO, CONTINUE
         NI    IGPRGFLG,ALLFS-CCHCNTB   TURN OFF VALID COUNT FLAG    CF
         NI    IGVALIDB,ALLFS-CCHCMDV   TURN OFF CMD. ADDR. VALID FLGCF
         TM    LOGWD3B+TWO,HEX82        Q. ADDR-IN OR UNIT ADDR CHECKCF
         BO    CCH146H                  YES. RESET DEV ADDR & UA INV CF
         TM    LOGWD3B,HEX01            Q. SECOND CHECK              CF
         BO    CCH146I                  YES. SET CHAN AS SOURCE OF ERCF
         TM    LOGWD3B+ONE,HEX20        Q. LOCAL STOR ADDR CHECK     CF
         BO    CCH146J                  YES. MAKE UABO CHECK TEST    CF
         B     CCH146M                  SEND SELECTIVE RESET         CF
*
         SPACE 2
CCH146   TM    CSWLOC+FIVE,CCCBIT+ICCBIT . CHECK FOR BOTH CATS
         BM    CCH147 .                 EITHER CCC OF ICC, BRANCH
CCH146A  OI    IGTERMSQ,COMPSEL+RTCODE7 .    SEL RESET+RETRY
         B     CCHRETRN .               TO COMMON RETURN POINT
*
         SPACE 2
CCH146H  NI    IGVALIDB,ALLFS-CCHDAV    SET UNIT ADDR INVALID IN ERP CF
         TM    LOGWD3B+TWO,HEX02        Q. ADDRESS-IN CHECK          CF
         BNO   CCH146I                  NO. SET CHAN SRC OF ERROR    CF
         OI    IGBLAME,CCHINTFC         SET INTERFACE SRC OF ERROR   CF
         B     CCH146K                  GO SET TERM & SEQ. CODES     CF
*
         SPACE 1
CCH146I  OI    IGBLAME,CCHCHNL          SET THE CHAN SRC OF ERROR    CF
         B     CCH146L                                               CF
*
        SPACE 1
CCH146J  TM    LOGWD3B+TWO,HEX80        Q. UABO CHECK                CF
         BNO   CCH146M                  NO. CHECK FOR SEL. RESET SENTCF
         OI    IGBLAME,CCHCPU           SET CPU SRC OF THR EEROR     CF
         NI    IGVALIDB,ALLFS-CCHDAV    SET DEVICE ADDRESS INVALID   CF
         OI    IGTERMSQ,COMPFES+RTCODE4 SET FES TERM & RC OF 100     CF
         B     CCHRETRN                 RETURN                       CF
*
        SPACE 1
CCH146M  TM    LOGWD3B,HEX80            SELECTIVE SENT?              CF
         BO    CCH146K                                               CF
*
CCH146L OI    IGTERMSQ,COMPFES+RTCODE7  SET FES TERM & 111 RC        CF
         B CCHRETRN                     RETURN                       CF
*
CCH146K  OI    IGTERMSQ,COMPSEL+RTCODE7 SET SEL TERM & 111 RC        CF
         B     CCHRETRN                 RETURN                       CF
*
         SPACE 2
CCH147   TM    CSWLOC+FIVE,ICCBIT .        ICC ONLY
         BO    CCH148 .                 YES,GO AND PROCESS
         OI    IGTERMSQ,COMPFES .       SET COMP CODE TO SHOW FES
         TM    RECBYTE,CCHTIOB .        IS CAUSE A TIO INSTRUCTION
         BO    CCH149 .                 YES, CHECK
         TM    RECBYTE,CCHSIOB .        IS CAUSE A SIO INSTRUCTION
         BO    CCH150 .                 YES, CHECK
         SPACE 2
CCH162   TM    LOGWD3A,HEX01         PSUEDO SIO
         BZ    CCH151 .                 NO, SKIP TIO BIT TEST
         TM    LOGWD3A,HEX04         IS TIO BIT SET IN LOG
         BO    CCH152 .                 YES, GO TO SET RETRY CODE
CCH151   TM    LOGWD3A+ONE,HEX04 .      MODE TRIGGER 6
         BO    CCH153 .                 YES, CHECK COMMAND CHK SET
         TM    LOGWD3A+TWO,HEXE0 .      MODE TRIGGERS 9,10 OR 11
         BNZ   CCH153 .                 YES, CHECK COMMAND CHK SET
         TM    LOGWD3A+ONE,HEX70 .      MODE TRIGGERS 2,3 OR 4
         BZ    CCH154 .                 NONE, CHECK COUNT
CCH157   OI    IGTERMSQ,RTCODE3 .       SET RETRY CODE TO 011
         B     CCHRETRN .               TO COMMON RETURN POINT
*
         SPACE 2
CCH153   TM    LOGWD3B+TWO,HEX40       COMMAND CHECK IN LOG
         BO    CCH165B .                YES,GO SET SOURCE OF ERROR
*                                       HAS THE COUNT VALIDITY BIT
CCH154   TM    IGPRGFLG,CCHCNTB .       BEEN SET
         BO    CCH155 .                 YES, CHECK COMMAND ADDR
CCH156   OI    IGTERMSQ,RTCODE5 .       SET RETRY CODE TO 101
         B     CCHRETRN .               RETURN TO COMMON RETURN POINT
*
CCH156A  OI    IGTERMSQ,COMPSEL    SET TERM CODE = '10'
         B     CCH156              GO SET SEQ CODE
*
CCH155   TM    IGVALIDB,CCHCMDV .       IS THE COMMAND ADDR VALID
         BZ    CCH156 .                 INVALID COMMAND ADDR, BRANCH
         L     XLATEREG,CSWLOC          GET COMMAND ADDR             CF
         LA    XLATEREG,ZERO(XLATEREG)  CLEAR HIGH ORDER BYTE        CF
         SH    XLATEREG,CCHLENGH        GET CUURENT CCW ADDRESS      CF
         BM    CCH156                   BR IF NEGATIVE CMD ADDR
         ST    LINKRG2,PCCACHW2         SAVE THE FE RETURN ADDR      CF
         L     WKREG1,CVTPTR            GET POINTER TO CVT           CF
         USING CVTMAP,WKREG1            GET ADDRESSABILITY TO CVT    CF
         L     LINKRG3,CVTPTRV          CET PTR TO XLATE RTN         CF
         DROP  WKREG1                                                CF
         BALR  LINKRG2,LINKRG3          TRANSLATE THE VIRTUAL ADDR   CF
         L     LINKRG2,PCCACHW2         RESTORE RETURN REG           CF
         LTR   LINKRG3,LINKRG3          Q. THIS A VALID VIRTUAL ADDR.CF
         BNZ   CCH156                   NO. CCW ADDR INVALID.        CF
         L     TESTWK,PCCAPSAV         GET ADDR. OF PSA(V)     @ZA18585
         L     TESTWK,PSAAOLD-PSA(TESTWK) GET ADDR. OF PSAOLD  @ZA18585
         LH    TESTWK,ASCBASID-ASCB(TESTWK) GET ASID NUMBER    @ZA18585
         CR    TESTWK,REG0             COMPARE ASID            @ZA18585
         BNZ   CCH156                  NO - ASID'S NOT EQUAL   @ZA18585
         LR    WKREG1,XLATEREG          YES, GET ADDR IN REG 10.     CF
         LR    WKREG3,WKREG1 .          SAVE CCW ADDR
         LH    WKREG1,SIX(WKREG1)       GET CURRENT CCW COUNT
         N     WKREG1,CCHMASK1 .        AND OUT UNWANTED BITS
         LH    WKREG2,CSWLOC+SIX        GET CSW COUNT
         N     WKREG2,CCHMASK1 .        AND OUT UNWANTED BITS
         CR    WKREG2,WKREG1 .          COMPARE CSW CNT AND CCW CNT
*                                       BRANCH IF CSW COUNT GREATER
         BH    CCH155B .                THAN CCW COUNT
         BNE   CCH157 .                 DATA TRANSFER, SET RETRY CODE
CCH155A  OI    IGTERMSQ,RTCODE2 .       SET RETRY CODE TO 010
         B     CCHRETRN .               RETURN TO COMMON RETURN POINT
*
CCH155B  TM    IGVALIDB,CCHDAV .        UNIT ADDRESS VALID
         BO    CCH155C .                YES,BRANCH
         OI    IGTERMSQ,RTCODE7 .       SET NO RETRY CODE
         B     CCHRETRN .               RETURN TO COMMON POINT
         SPACE 2
CCH155C  BAL   LINKRG1,CCH164A .        CHECK FOR MPX CHAN
         BNL   CCH156D .                NOT MPX-BRANCH CC SET IN SUBRTN
         NI    IGVALIDB,ALLFS-CCHCNTB . SET COUNT INVALID
         B     CCH156 .                 MPX-SET RETRY CODE TO 101
CCH156D  IC    WKREG2,THREE(WKREG3) .   GET LOW DATA ADDR BITS
         N     WKREG2,CCHMASK2 .        AND OUT UNWANTED BITS
         IC    WKREG1,LOGWD3B+THREE .   GET LOG BYTE COUNT BITS
         SRL   WKREG1,CCH4 .            ISOLATE IN LOW ORDER
         N     WKREG1,CCHMASK2 .        AND OUT UNWANTED BITS
         CR    WKREG2,WKREG1 .          CCW BITS TO LOG BITS
         BNE   CCH157 .                 NOT EQUAL-SET RETRY TO 011
         B     CCH155A .                SET RETRY CODE TO 010
*
         SPACE 2
CCH148   OI    IGBLAME,CCHINTFC .      I/F IS SOURCE OF ERROR
         TM    LOGWD1A,HEX03 .          ARE THESE BITS SET(EITHER)
         BNZ   CCH148A .                YES,BRANCH
CCH148C  TM    LOGWD3A+ONE,HEX08   WORD 3 BIT 12 ON
         BO    CCH148A             YES, GO SET CODES
CCH148B  OI    IGTERMSQ,COMPSEL+RTCODE3 .SEL RESET + RETRY 011
         B     CCHRETRN .               TO COMMON RETURN POINT
         SPACE 1
CCH148A  TM    LOGWD3B+TWO,HEX04 .      INCORRECT TAG SEQUENCE
         BZ    CCH158 .                 NO, NEXT TEST
         TM    LOGWD3A+ONE,HEX08   WORD 3 BIT 12 ON
         BO    CCH156A             YES, GO SET CODES
         B     CCH146A .                GO SET SEL TERM AND RETRY 111 I
*
CCH158   TM    LOGWD3B+TWO,HEX10        INCORRECT SELECTION
         BZ    CCH159 .                 NO, NEXT TEST
         OI    IGTERMSQ,COMPSEL+RTCODE4 SET SEL TERM AND RETRY CODE 100
         B     CCHRETRN .               RETURN
*
CCH159   TM    LOGWD3B+TWO,HEX08        NO RESPONSE BIT
         BZ    CCH160 .                 NO, NEXT TEST
         OI    IGTERMSQ,COMPFES+RTCODE4 SET FES TERM AND RETRY CODE 100
         OI    IGVALIDB,CCHCMDV .       SET COMMAND ADDRESS VALID
         B     CCHRETRN .               RETURN
*
         SPACE 2
CCH160   TM    LOGWD3B+TWO,HEX01 .      STATUS-IN CHECK
         BO    CCH160A .                YES,BRANCH
         SPACE 1
*****************************************************************
*        CPU TIME-OUT CONDITION
*****************************************************************
         SPACE 1
         TM    LOGWD3B,HEX01           Q. DISCONNECT-IN IN LOG       CF
         BZ    CCH160D                  NO. GO SET CODES AND RETURN  CF
         OI    IGTERMSQ,CCHDI           SET DISCONNECT-IN IN ERPIB   CF
         OI    IGTERMSQ,COMPSEL+RTCODE7  SET SEL TERM & RC OF 111    CF
         B     CCHRETRN .               TO COMMON RETURN POINT
         SPACE 1
CCH160A  TM    RECBYTE,CCHSIOB .        IS ERROR CAUSED BY SIO
         NOP   CCH160B .                ALL STATUS IN CKS RESULT IN    *
                              TERM CODE 10 AND SEQ CODE 001 JE @ZA00156
CCH160B  EQU   *                                            JE @ZA00516
         OI    IGTERMSQ,COMPSEL+RTCODE1 SET SEL TERM AND RETRY CODE 001
         B     CCHRETRN .
*
CCH160D  OI    IGTERMSQ,COMPSEL+RTCODE5 SET SEL TERM & RC 101        CF
         B     CCHRETRN                 RETURN                       CF
*
         SPACE 2
CCH149   TM    LOGWD3A,HEX04            TEST I/O BIT IN LOG
         BO    CCH162 .                 YES, BRANCH
         TM    IGVALIDB,CCHUSV .        IS UNIT STATUS VALID
*                                       NO BRANCH, DO NOT CHECK
         BZ    CCH163 .                 CHANNEL END OR DEVICE END
         TM    CSWLOC+FOUR,HEX0C        CHANNEL END OR DEVICE END
         BC    FIVE,CCH162 .            YES, CHECK PSUEDO SIO
         OI    IGTERMSQ,RTCODE3 .       NEW CODE.ORIGINAL WAS 000
         B     CCHRETRN .               RETURN
*
CCH163   OI    IGTERMSQ,RTCODE7 .       SET RETRY CODE TO 111
         B     CCHRETRN .               RETURN
*
         SPACE 2
CCH150   TM    LOGWD3A,HEX80            CAW ERROR
         BZ    CCH164 .                 NO, NEXT TEST
CCH152   OI    IGTERMSQ,RTCODE4 .       SET RETRY CODE TO 100
         B     CCHRETRN .               RETURN
*
CCH164   TM    IGVALIDB,CCHDAV          WAS VALID UNIT ADDR SET ?
         BZ    CCH152                   BRANCH, NO
         LA    LINKRG1,CCH164B .        SET RETURN PT FROM MPX TEST
*
*     THE FOLLOWING IS AN IN-LINE SUBROUTINE WHICH MAY BE CALLED FROM
*     'CCH155C' OR FALLEN INTO FROM ABOVE.  IT SETS THE CONDITION CODE
*      WHICH IS TESTED BY THE CALLING ROUTINE.
*
CCH164A  SR    WKREG1,WKREG1 .          CLEAR REGISTER
         LA    WKREG1,HEXC0             LOAD MPX UNIT ADDR COMPARE CONT
         LA    WKREG2,ALLFS             LOAD AND OUT VALUE
         NR    WKREG2,UAREG .           LOAD UA AND DELETE CHANNEL &DDR
         CR    WKREG2,WKREG1 .          IS THIS AN MPX CHAN
         BR    LINKRG1 .                RETURN TO CALLER OR FALL THRU
*                                       TO NSI
CCH164B  BNL   CCH165 .                 NOT MPX,BRANCH CC SET IN SUBRTN
         TM    LOGWD3A,HEX40            CCW REQUIRED ON
         BO    CCH152 .                 YES, GO TO SET RETRY CODE
CCH165   TM    LOGWD3A,HEX04            TIO BIT ON
         BO    CCH152 .                 YES, GO TO SET RETRY CODE
CCH165A  OI    IGBLAME,CCHCHNL .       CHANNEL IS SOURCE OF ERROR
         NI    IGTERMSQ,ALLFS-COMPFES . DELETE TERM=01 ALREADY SET
         OI    IGTERMSQ,COMPSEL .       SET TERM CODE=10
         B     CCH152 .                 SET RETRY TO 100,RETURN
*
         SPACE 2
CCH165B  OI    IGBLAME,CCHCHNL .       CHANNEL IS SOURCE OF ERROR
         B     CCH152 .                 GO SET RETRY TO 100
*
         SPACE 2
SETSYSTR OI    CCHSW1,CCHNOLST     SET RECORD ONLY SWITCH
         OI    CCHSW2,SYSTERM+NOLOG SET SYSTEM TERMINATION SWITCH    CF
CCHRETRN LA    WKREG1,LOGADDR           GET THE ADDR OF LOGOUT       CF
         ST    WKREG1,PCCALOGA          AND PLACE IT IN THE CCA      CF
         MVC   PCCALOGL,LOGLNTH      PUT LENGTH OF LOG IN PCCA @G51BPLC
         BR    LINKRG2                  RETURN TO CCH CENTRAL        CF
*
         EJECT
*        CONSTANTS
         SPACE
         DS    0F
CCHMASK1 DC    X'0000FFFF' MASK FOR CLEARING OUT UNWANTED BITS
CCHMASK2 DC    X'00000007' MASK FOR CLEARING OUT UNWANTED BITS
SYSRESET DC    X'05268200'         SYSTEM RESET CONSTANT
LOGLNTH  DC    X'0018'             LENGTH OF LOGOUT            @G51BPLC
ALLONES  DC    F'-1'              FOR LOG OUT COMPARISON
CCHLENGH DC    H'8'                CORRECTION FACTOR FOR CMD ADDR
*
********************************************************************
*                                                                  *
*         MAINTAINENCE AREA                                        *
*                                                                  *
********************************************************************
*
         DS    0F
C70PATCH DC    50X'00'         PATCH AREA
MAINT    EQU   C70PATCH        FOR XREF                        @ZA32238
         EJECT
         CVT   DSECT=YES                                       @ZA32238
         EJECT
         IHAPSA                                                @ZA18585
         EJECT
         IHAASCB                                               @ZA18585
         EJECT
         IGFERPIB 1
         EJECT
         IHAPCCA
         END
