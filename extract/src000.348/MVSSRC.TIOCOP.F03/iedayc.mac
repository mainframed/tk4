CARR     TITLE '''IEDAYC''  -  TSO CARRIAGE ROUTINE.'
IEDAYC   CSECT
         SPACE 3
*  CHANGE ACTIVITY AS FOLLOWS
******************** MICROFICHE FLAGS *********************** SUPT CODE
*C113000                                                         S21903
*C010000,024000,028000,032000-042000,060000-072000,076600-079600,S21903
*C081000-081450,081600-081700,084000-094000,285300-285350        S21903
*A124000                                                         S21903
*A076200-078700,081300-081450,312600-312800,373300-373600,398500,S22028
*A460500-461500,488500,509600,564600-565200,580500-581500,687000,S22028
*A701500                                                         S22028
*C004460,124000,352000-362000,420000-429000,511242               S22028
*D430000-433000                                                  S22028
*A081600                                                         S22029
*C285300                                                         S22029
*D258000-262000,269600-272000,298000-304000                      S22029
*C005060,005180,204000,286000,269600,304000,306600,307300,308400 S22025
*C309600,320000,327600,386000,388000,438000,492000,511800,518000 S22025
*C582000,682000,688000-694000,699000,702000                      S22025
*D706000                                                         S22025
*C511242-511246                                                 SA53605
*A112000                                                         A44022
*A285210,285220                                                  A44875
*A031000,081550,081800,103000                                    S22025
*A511239,511242                                                   M1191
*C030000,038000,090000,102000,130000,226000-230000,466000-468000 S22025
*C 401800-402400                                                SA59324
*C 077200,360000-361000                                         SA61786
*C 401800-402400                                                SA61787
*C004160,004460,004466,004472,005000,005100,005120,005260,005280,Y06327
*C016000,088000,106000,125000,420500                             Y06327
*A004165-004175,004476,081820-081940,328500-329500,353070-353210,Y06327
*A400620-401020,420509-420590,511810-511940,512450,513110        Y06327
*A355410,355420,468100-468600                                    Y06327
*C214900-215200                                                 SA67755
*D217000                                                        SA67755
*C200000-204000                                                @YA00488
*A081940                                                       @ZA02057
*C400640-402600                                                @ZA02057
*A317200                                                       @YA11532
*C511188                                                       @YA12281
*A511400                                                       @YA12281
*A079600,353000                                                @OZ12513
*D 076500,080400,080800,080900                                 @Y17XAYZ
*A133500,134000,124000,128000,081200-081300,401100-401200      @Y17XAYP
*A50200-50400,278000                                           @Y17XAYP
*C133500,125000,400700,420518,468200,511820,511880             @Y17XAYP
*                                                              @G36XRYP
*A734000                                                       @G36XRYP
*C220000-234000,216000,285100,312600,313800,352000,354000      @G36XRYP
*C373300,374000,378000,400720,400740,420000,420200,420400      @G36XRYP
*C425300,425440,426930,460500,468400,474000,511243,511245      @G36XRYP
*C511248,511440,511910,512200,512900,564600,566000,570000      @G36XRYP
*C580500                                                       @G26XRYP
*D072000-076400,081880-081920                                  @G36XRYP
*D212000-214000,112500-113000,720000-722000                    @G36XRYP
*C284000-285000                                                @OZ29032
*                                                              @G36XRYP
         SPACE 3
***********************************************************************
*                                                                     *
*  TITLE:  'IEDAYC'  TSO CARRIAGE ROUTINE                             *
*                                                                     *
*  MODULE NAME = IEDAYC  (TCAM,TSO)                            @Y17XAYP
*                                                                     *
*  DESCRIPTIVE NAME = TSO CARRIAGE ROUTINE                            *
*                                                                     *
*  COPYRIGHT = 'NONE'                                                 *
*                                                                     *
*  STATUS:  VERSION 10.0                                       @G36XRYP
*                                                                     *
*FUNCTION -- THE CARRIAGE SUBROUTINE MAINTAINS A COUNT IN THE QCB OF  *
*   CARRIAGE POSITIONS FOR KEYBOARD DEVICES, SO THAT WHEN OUTPUT IS   *
*   SENT, IDLE CHARACTERS CAN BE INSERTED PROPERLY, FOR DEVICES       *
*   ATTACHED TO A 270X. FOR KEYBOARD TERMINALS ATTACHED TO A 3705     *
*   (IDLE CHARACTERS ARE INSERTED BY THE 3705) A CARRIAGE RETURN IS   *
*   INDICATED AFTER ALL DATA HAS BEEN CHECKED. IF A TRANSLATION       *
*   ERROR HAS OCCURRED, OR IF THE CURRENT BUFFER IS A ZERO-LENGTH     *
*   BUFFER, CONTROL IS PASSED TO THE MH VIA THE RETURN INTERFACE.     *
*   OTHERWISE, THE BUFFER IS SCANNED CHARACTER BY CHARACTER FOR NEW   *
*   LINE CHARACTERS AND BACKSPACE CHARACTERS.  WHEN A BACKSPACE CHAR- *
*   ACTER IS FOUND, THE CARRIAGE POSITION COUNT IS DECREMENTED BY ONE,*
*   UNLESS THE BACKSPACE IS THE FIRST CHARACTER IN THE BUFFER.  WHEN  *
*   A NEW LINE CHARACTER IS FOUND, THE CARRIAGE POSITION COUNT IS RE- *
*   SET TO ZERO, AND FOR 2260'S, THE QCB SIMULATED ATTENTION LINE     *
*   COUNT IS UPDATED, UNLESS THE NEW LINE IS THE LAST CHARACTER IN    *
*   THE BUFFER.  IF IT IS, THE QCB RETRY COUNT FIELD IS SET TO RE-    *
*   FLECT THIS, SO THAT, WHEN THIS SUBROUTINE IS ENTERED TO SCAN THE  *
*   NEXT BUFFER, SCANNING WILL BEGIN AT THE FIRST CHARACTER IN THE    *
*   BUFFER.  THE CARRIAGE SUBROUTINE ALSO ZEROES OUT CIRCLE D'S FROM  *
*   ALL DEVICES EXCEPT 2741'S AND STX/ADDRESSING CHARACTER SEQUENCES  *
*   FROM 2260'S, IF THEY APPEAR IN INPUT BUFFERS. FOR 3270 REMOTES,   *
*   ATTACHED TO A 270X, IT ZEROS OUT STX/CU/DEV CONTROL CHARACTERS    *
*   AND REDUCES DATA LENGTH BY ONE FOR ETX. FOR 3270 REMOTES, ATTACHED*
*   TO A 3705, IT ZEROS OUT CU/DEV CONTROL CHARACTERS. IT ALSO COUNTS *
*   OUTPUT LINES FOR SIMULATED ATTENTION BY LINE COUNT, AND SETS ON   *
*   THE SIMULATED ATTENTION BIT IN THE SCB WHEN REQUIRED.  AFTER ALL  *
*   UNITS OF THE BUFFER HAVE BEEN SCANNED, THE CARRIAGE POSITION      *
*   COUNT IN THE QCB IS UPDATED, AND CONTROL RETURNS TO THE MH VIA    *
*   THE RETURN INTERFACE. FOR SNA DEVICES THE HDR BUFFERS ARE  @Y17XAYP
*   SCANNED FOR DATA FLOW CONTROL COMMANDS. CANCEL IS PROCESSED@Y17XAYP
*   A LINE DELETE AND ESIG IS PROCESSED AS ATTENTION.          @Y17XAYP
*                                                                     *
*ENTRY POINT -- IEDAYC - TO MAINTAIN A COUNT OF CARRIAGE MOVEMENT IN  *
*   EITHER DIRECTION.                                                 *
*   CALLING SEQUENCE          L    R12,AVTMSGS+OFFSET                 *
*                             BR   R12                                *
*                                                                     *
*INPUT -- IEDAYC IS ENTERED FROM THE TCAM USER INTERFACE ROUTINE      *
*   (IEDQUI) WHEN A CARRIAGE MACRO INSTRUCTION IS CODED IN AN INBUF   *
*   SUBGROUP OF AN MH. AT ENTRY, THE FOLLOWING REGISTERS ARE SET      *
*   R3 HAS THE ADDRESS OF THE SCB.                                    *
*   R4 HAS THE ADDRESS OF THE LCB                                     *
*   R6 HAS THE CURRENT BUFFER ADDRESS                                 *
*   R12 HAS THE ENTRY POINT ADDRESS                                   *
*   R13 HAS THE AVT ADDRESS                                           *
*                                                                     *
*OUTPUT -- ON EXIT TO THE RETURN INTERFACE, THE QCB CARRIAGE POSITION *
*   COUNT IS UPDATED.  IF REQUIRED, THE QCB SIMULATED ATTENTION LINE  *
*   COUNT MAY ALSO BE UPDATED, AND THE SCB SIMULATED ATTENTION BIT    *
*   MAY BE ON.  IF A NEW LINE CHARACTER WAS THE LAST CHARACTER IN THE *
*   BUFFER, THE QCB WILL INDICATE THIS.  AT EXIT, THE FOLLOWING       *
*   REGISTERS ARE SET.                                                *
*   R3 HAS THE SCB ADDRESS                                            *
*   R4 HAS THE LCB/PLCB ADDRESS                                       *
*   R10 HAS THE QCB ADDRESS                                           *
*   R11 HAS THE BUFFER ADDRESS                                        *
*   R15 HAS THE ENTRY POINT ADDRESS OF IEDQUI                    S22025
*                                                                     *
*EXTERNAL ROUTINE -- IEDQTNT - TO GET THE TERMINAL ENTRY FROM THE     *
*   PREFIX SOURCE INDEX.                                              *
*                                                                     *
*EXIT-NORMAL -- TO THE MH VIA THE RETURN INTERFACE (IEDQUI+4)    S22025
*                                                                     *
*EXITS-ERROR -- NONE                                                  *
*                                                                     *
*TABLES/WORK AREAS -- CVT, AVT, TSB, LCB/PLCB, SCB, DCB, QCB,  @G36XRYP
*   BUFFER PREFIX, TERMINAL ENTRY, ASVT, ASCB                  @G36XRYP
*                                                                     *
*ATTRIBUTES -- REENTRANT, ENABLED, RESIDENT, PROBLEM PROGRAM MODE.    *
*                                                                     *
*NOTES -- THE OPERATION OF THIS MODULE DOES NOT DEPEND UPON A         *
*   PARTICULAR INTERNAL REPRESENTATION OF THE EXTERNAL CHARACTER SET. *
*      THIS MODULE TREATS NEW LINE CHARACTERS (X'15') AND CARRIAGE    *
*   RETURN CHARACTERS (X'0D') IDENTICALLY.                            *
*                                                                     *
***********************************************************************
*       R E G I S T E R  E Q U A T E S
         SPACE
RWORK    EQU   0                        WORK REGISTER            S22025
RPARM    EQU   1                        REGISTER EQUATE          S21903
RLIM     EQU   1                        LIMIT                    S21903
RNDX     EQU   2                        DATA INDEX
RSCB     EQU   3                        SCB                      S21903
RLCB     EQU   4                        LCB/PLCB ADDR. FROM PREFXY06327
RCNT     EQU   5                        DATA COUNT FOR CARR POSN
RRH      EQU   5                        RH BASE REG            @YM06988
RPREFIX  EQU   6                        TCAM PREFIX
RSCAN    EQU   7                        DATA POINTER
RUNDX    EQU   8                        UNIT INDEX
RAVT     EQU   9                        AVT ADDRESS              S22025
RQCB     EQU   10                       QCB ADDRESS              S21903
RBUF1    EQU   11                       BUFFER1 POINTER          S21903
RBASE    EQU   12                       BASE                     S21903
R13      EQU   13                       REGISTER EQUATE          S21903
REXIT    EQU   14                       EXIT REGISTER            S21903
RLINK    EQU   15                       LINKING REGISTER         S21903
         SPACE
*       M I S C E L L A N E O U S  E Q U A T E S
         SPACE
XXNL     EQU   X'15'                    NEW LINE CHAR
XXBSP    EQU   X'16'                    BACKSPACE CHARACTER
XXTAB    EQU   X'05'                    HORIZONTAL TAB
XXEOT    EQU   X'37'                    CIRCLE C
XXEOA    EQU   X'7B'                    # = CIRCLE D
XXEOB    EQU   X'26'                    EOB                      S21903
XXCR     EQU   X'0D'                    CARRIAGE RETURN          S21903
XXLF     EQU   X'25'                    LINE FEED                S21903
NOMASK   EQU   X'00'                    MASK                     S22028
XXFM     EQU   X'1E'                    3270 FIELDMARK          SA61786
XXETBEBC EQU   X'26'                    EBCDIC ETB               S22028
XXSBA    EQU   X'11'                    SET BUFFER ADDRESS       S22028
IDCARD   EQU   X'E6'                    ID CARD READER AID       S22028
XXSTX    EQU   X'02'                    START OF TEXT            S21903
XXETX    EQU   X'03'                    END OF TEXT              S21903
XXON     EQU   X'11'                    TWX-ON                 @OZ12513
XXOFF    EQU   X'3C'                    3705 TWX X-OFF           Y06327
NOT      EQU   X'FF'                    MASK                     S21903
EZERO    EQU   0                        DECIMAL ZERO             S21903
EONE     EQU   1                        DECIMAL ONE              S21903
ETWO     EQU   2                        DECIMAL TWO              S21903
ETHREE   EQU   3                        DECIMAL THREE            S21903
EFOUR    EQU   4                        DECIMAL FOUR           @Y17XAYP
EFIVE    EQU   5                        DECIMAL FIVE           @Y17XAYP
ESEVEN   EQU   7                        DECIMAL SEVEN            S22028
BYTE234  EQU   7                        ADDRESS MASK FOR STCM  @Y17XAYP
E128     EQU   128                      DECIMAL                  S22028
FOUR     EQU   4                        OFFSET FOR RETURN ROUT   S22025
E80      EQU   80                       DECIMAL                  S21903
FORTY4   EQU   44                       OFFSET TO R6 IN SAVE AREAS22025
RN3705   EQU   X'EF'                    TURN OFF 3270 REMOTE FLG Y06327
LF3705   EQU   X'25'                    3705 TWX LF CODE         Y06327
CR3705   EQU   X'0D'                    3705 TWX CR CODE         Y06327
ESIX     EQU   6                        DECIMAL 6                Y06327
UNEX     EQU   X'01'                                           @ZA02057
         EJECT
         USING IEDQPRF,RPREFIX          BUFFER BASE              S21903
         USING IEDQQCB,RQCB             QCB BASE                 S21903
         USING IEDQLCB,RLCB             LCB/PLCB BASE            Y06327
         USING IEDQAVTD,RAVT            AVT BASE                 S22025
         USING IEDQSCB,RSCB             SCB BASE                 S21903
         USING IEDNTRM,RPARM            WORK REGISTER          @YM04685
         SPACE 3
         USING *,RBASE
IEDAYC   IEDHJN ID,HJN                                         @Y17XAYP
         TM    SCBERR2,SCBCODER         HAS TRANSLATION ERROR OCCURRED
         BO    EXIT                     BRANCH ON YES
         TM    AVTBIT3,AVTTSAB          IS TSO ABENDING          A44022
         BO    EXIT                     BRANCH YES               S21903
         LH    RPARM,PRFSRCE            INDEX TO TNT
         L     RLINK,AVTRNMPT           TERM NAME TABLE ROUTINE
         BALR  REXIT,RLINK              EXECUTE IT
         L     RQCB,EZERO(,RPARM)       GET QCB ADDRESS
         TM    QCBFLAG,QCBTSSES         IN TSO SESSION         @YM05704
         BZ    EXIT                     BR NO                  @YM05704
         SPACE
         LR    RBUF1,RPARM              SAVE TRM TABLE ENTRY ADDRS22028
         LA    RNDX,TRMPRFSZ            GET TRM PREFIX SIZE    @Y17XAYP
         SR    RPARM,RNDX               BACK UP TO PREFIX      @Y17XAYP
         STCM  RPARM,ESEVEN,AVTDOUBL+EFIVE  SAVE TRM PREFIX    @Y17XAYP
*                                       ADDRESS                @Y17XAYP
         MVC   AVTDOUBL+EFOUR(EONE),TRMSTATE SAVE TRMSTATE     @Y17XAYP
*                                       FOR ACCESS             @Y17XAYP
         SPACE 3
         LR    RNDX,RPREFIX             SAVE TCAM BUF PTR       SA67755
         IEDDCT REG=RUNDX,FLD=AVTPARM3,LEN=4                   @Y17XAYP
OKTSB    EQU   *
         L     RPREFIX,CVTPTR           ADDRESS OF CVT
         USING CVT,RPREFIX
         TM    QCBTSOF1,QCBDELAY        IS QCB ON DELAY QUEUE
         BNO   NODELAY                  NO, GET TJID AS USUAL
         LR    RPARM,RQCB               ADDR OF QCB TO MOVE     SA67755
         L     RLINK,AVTHG02            TIME DELAY REMOVAL      SA67755
         BALR  REXIT,RLINK              REMOVE QCB FROM DELAY   SA67755
************************************************************** @G36XRYP
*        FIND TSB                                              @G36XRYP
************************************************************** @G36XRYP
NODELAY  EQU   *
         L     RPREFIX,CVTPTR           GET CVT ADDRESS        @G36XRYP
         L     RPREFIX,CVTASVT-CVT(RPREFIX) GET ASVT ADDRESS   @G36XRYP
         LH    RSCAN,QCBTJID            GET ASID FROM QCB      @G36XRYP
         BCTR  RSCAN,EZERO              REDUCE BY 1
         SLL   RSCAN,2                  MULTIPLY ASID BY 4     @G36XRYP
         L     RPREFIX,ASVTENTY-ASVT(RSCAN,RPREFIX) GET ASCB   @ZM46782
         L     RPREFIX,ASCBTSB-ASCB(RPREFIX) GET TSB ADDRESS   @G36XRYP
         USING TSB,RPREFIX
         MVC   AVTPARM3(1),TSBLNSZ      GET LINE SIZE FROM TSB
         SR    RWORK,RWORK              CLEAR REGISTER
         IC    RWORK,TSBLNNO            GET NUMBER OF LINES IF DISPLAY
         BCTR  RWORK,0                  REDUCE BY ONE
         STC   RWORK,AVTDOUBL+2         STORE IN AVT
         MVI   AVTDOUBL+1,EZERO         CLEAR BYTE FOR FLAGS
         TM    TSBSTAT,TSBATNLD         IS ATTN FOR LINE DELETE
         BZ    NODELE                   BRANCH NO
         OI    AVTDOUBL+1,TSBATNLD      SAVE BIT IN AVT FOR NOW
NODELE   EQU   *
         TM    TSBFLG2,TSBAULST+TSBAUTON  IF ON MOVE TO AVT BYTE
*                                       ELIMINATES NEED FOR TSB REG
         BNO   CONTIN                   BRANCH IF NOT ON
         OI    AVTDOUBL+1,TSBAULST+TSBAUTON SAVE BITS
CONTIN   EQU   *
         LR    RPREFIX,RNDX             RETRIEVE TCAM BUF PTR  SA67755
         ST    RPREFIX,FORTY4(R13)      SAVE 1ST TCAM BUF ADR  SA67755
         USING IEDQPRF,RPREFIX
         SPACE
         SR    RUNDX,RUNDX
         IC    RUNDX,PRFNBUNT           NO. UNITS/BUFFER
         LH    RNDX,PRFSIZE             SIZE OF DATA
         TM    SCBERR3,SCBATTN          WAS ATTN SET IN LINE END
         BZ    EXIT1050                 BR IF NO
         TM    SCBERR4,SCBSLCTN          WAS ATTN ON READ RESPONSE
         BZ    EXIT1050                 BR IF NO
         OI    SCBERR3,SCBSATTN          YES SET NULL LINE IND.
         NI    SCBERR4,255-SCBSLCTN      TURN OFF SELECT ERROR
*
EXIT1050 EQU   *
*
         LTR   RNDX,RNDX                ZERO LENGTH BUFFER?
         BZ    LOCCHK                   CHK FOR 2260L NULL LINE
         SR    RLINK,RLINK             CLEAR FOR IC OF ISIZE     Y06327
         IC    RLINK,LCBISZE           GET NUMBER OF RESERVES    Y06327
         TM    PRFSTAT1,PRFNHDRN        HDR BUFFER
         BO    NOTHDR                   NO
         LA    RSCAN,PRFSHDR            POINT BEYOND TCAM PFX
         AR    RSCAN,RLINK             START OF DATA             Y06327
         LA    RLIM,AVTHDRSZ(RLINK)    SIZE HDR PRF AND RESERVES Y06327
         B     TSOB1                    BRANCH FOR HEADER BUFFER S22025
NOTHDR   EQU   *
         LA    RSCAN,PRFSTXT            START TCAM TEXT
         AR    RSCAN,RLINK             START OF DATA             Y06327
         LA    RLIM,AVTTXTSZ(RLINK)    SIZE TXT PRF AND RESERVES Y06327
TSOB1    EQU   *
         SR    RNDX,RLIM                REDUCE SIZE FOR PREFIX
         BZ    LOCCHK                   CHK FOR 2260L NULL LINE
         EJECT
***************************************************************@YM06629
*                                                              @YM06629
*        THIS SECTION OF CODE DOES THE SPECIAL PROCESSING      @YM06629
*        NECESSARY FOR DFC COMMANDS AND NULL RU'S.             @YM06629
*                                                              @YM06629
***************************************************************@YM06629
         SPACE
         USING IEDRH,RRH                RH ADDRESSABILITY      @YM06988
         SPACE
         CLI   LCBFLAG1,LCBPLCB         NCP RESOURCE?          @YM06988
         BNE   PROCDATA                 BR NO                  @YM06629
         TM    LCBSTAT5,LCBLUNIT        LU?                    @YM06988
         BNO   PROCDATA                 BR NO                  @YM06629
         LA    RRH,PRF1LEN              LNG OF NEG PREFIX      @YM06988
         LNR   RRH,RRH                  MAKE IT NEGATIVE       @YM06988
         AR    RRH,RPREFIX              BACK UP TO RH          @YM06988
         TM    TRHBYTE0,TRHDFC          IS THIS A DFC COMMAND  @Y17XAYP
         BNO   NOTDFC                   BR NO                  @YM06629
         TM    TRHBYTE0,TRHSDI          IS SENSE DAT INCLUDED  @Y17XAYP
         BNO   NOTEXR                   NO, FIRST BYTE IS CMD  @YM06988
         LA    RSCAN,L'SNSSYSTM+L'SNSUSER(RSCAN)  POINT        @YM06988
*                                       TO FIRST BYTE          @Y17XAYP
NOTEXR   CLI   AVTEZERO(RSCAN),CD1CANCL CANCEL DFC CMD         @YM06988
         BNE   CKESIG                   CHECK FOR SIGNAL CMD   @Y17XAYP
         OI    SCBERR3,SCBXPD           INDICATE LINE DELETE   @Y17XAYP
         NI    SCBERR3,NOT-SCBXPI-SCBSATTN-SCBATTN  RESET      @Y17XAYP
         OI    PRFSTAT1,PRFCNCLN        INDICATE TO IEDAYI     @YM07261
*                                       THAT THE LINE IS TO BE @YM07261
*                                       CANCELLED              @YM07261
         B     EXIT                     EXIT                   @YM07261
CKESIG   CLI   AVTEZERO(RSCAN),CD1SIG   SIGNAL DFC CMD         @YM06988
         BNE   CKLUSTAT                 CHECK FOR LUSTAT CMD   @Y17XAYP
         OI    SCBERR3,SCBATTN          INDICATE HDWRE ATTN    @Y17XAYP
         B     RTNBFR                   GO SET BFR RTN         @YM06629
CKLUSTAT CLI   AVTEZERO(RSCAN),CD1LSTAT LUSTATUS DFC CMD       @YM06988
         BNE   RTNBFR                   BR NO                  @YM06629
*                                       COMPONENT AVAILABLE    @YM06629
         CLC   EONE(L'COMPAV,RSCAN),COMPAV                     @YM06629
         BNE   CKPERM                   BR NO                  @YM06629
         NI    QCBFLAG,X'FF'-QCBREAD    ALLOW SEND NEXT TIME   @YM06629
         B     RTNBFR                   GO SET BFR RTN         @YM06629
CKPERM   EQU   *                                               @YM06629
*                                       PERMANENT ERROR STATUS @YM07002
         CLC   EONE(L'PERMERR,RSCAN),PERMERR                   @YM07002
         BNE   CKNOFMD                  BR NO                  @YM06629
         OI    SCBERR4,SCBCTLUN         INDICATE HANGUP PROCESS@Y17XAYP
         B     RTNBFR                   GO SET BFR RTN         @YM06629
CKNOFMD  EQU   *                                               @YM06629
*                                       NO FM DATA TO XMIT     @YM06629
         CLC   EONE(L'NOFMD,RSCAN),NOFMD                       @YM06629
         BNE   RTNBFR                   BR NO                  @YM06629
         OI    SCBERR4,SCBCTLUN         FORCE HANGUP           @YM06629
         B     RTNBFR                   GO SET BFR RTN         @YM06629
         SPACE
NOTDFC   EQU   *                                               @YM06629
         CH    RNDX,H1                  ONE BYTE OF DATA       @YM07001
         BNE   PROCDATA                 BR NO                  @YM06629
         CLI   AVTEZERO(RSCAN),AVTEZERO IS IT A NULL RU        @YM07001
         BNE   PROCDATA                 BR NO                  @YM06629
RTNBFR   EQU   *                                               @YM06629
         LA    RPARM,AVTBFRTB           ADDRESS OF BFR RETURN  @YM06629
         STCM  RPARM,BYTE234,SCBDESTQ   SET DESTINATION TO     @YM06629
*                                       BUFFER RETURN          @Y17XAYP
         B     EXIT                     EXIT                   @Y17XAYP
         SPACE
         DROP  RRH
         SPACE
COMPAV   DC    X'0001'                  COMPONENT AVAILABLE    @YM06629
NOFMD    DC    X'0002'                  NO FM DATA TO XMIT     @YM06629
PERMERR  DC    X'081C'                  PERMANENT ERROR        @YM06629
         EJECT
PROCDATA EQU   *                                               @Y17XAYP
         SR    RCNT,RCNT                INITIALIZE CARR POSITION
         TM    PRFSTAT1,PRFNHDRN        THIS HEADER?
         BO    TEXT                     NO. LEAVE BITS ON
         TM    QCBRETCT,QCBCR           NL SEEN LAST TIME
         BO    NLAST                    BRANCH NO
         B     TEXT                     NO PICK UP CARRIAGE CNT@OZ29032
         SPACE
LOCCHK   EQU   *
         TM    AVTPARM3+2,DCTLOCAL      LOCAL 2260             @G36XRYP
         BZ    EXIT                     BRANCH IF NO
         TM    PRFSTAT1,PRFNHDRN        IS THIS A HEADER BFR     A44875
         BO    EXIT                     BRANCH NO. IS NOT NULL   A44875
         MVC   SCBDESTQ(3),AVTTSOPT+1   SET TO POST TO TSINPUT
         MVI   PRFSIZE+1,X'1F'          SET SIZE TO HDR SIZE+1   S22029
         MVI   PRFSHDR,X'15'            PUT NEW LINE IN BUFFER   S22029
         B     GIETX2                   GO COUNT IT
         SPACE
TEXT     EQU   *
         IC    RCNT,QCBCARCT            PRESET LINE POSITION
         B     NCNT                     START SCAN
NLAST    EQU   *
         NI    QCBRETCT,NOT-QCBNL       TURN OFF CR/LF AT HDR TIME
NCNT     EQU   *
         MVI   QCBCARCT,EZERO           CLEAR FOR NEW LINE
         LH    RLIM,AVTKEYLE            UNIT SIZE
         SR    RLIM,RLINK              UNIT SIZE MINUS RESERVES  Y06327
         TM    PRFSTAT1,PRFNHDRN        IS THIS A HEADER BUFFER  S22025
         BO    TXTIN                    BRANCH IF NO             S22025
         SH    RLIM,H30                 SUBTRACT FOR TCAM HEADER PREFIX
         B     TSX                      BRANCH FOR HEADER BUFFER S22025
TXTIN    EQU   *
         SH    RLIM,H23                 SUBTRACT FOR TCAM TEXT BUF
         B     MAINLOOP                 BYPASS EOT/EOA CHECK     S22025
TSX      EQU   *
         TM    PRFSTAT1,PRFNHDRN        THIS A HEADER BFR
         BO    MAINLOOP                 BRANCH ON NO
         MVI   AVTDOUBL,0
         TM    AVTPARM3+1,DCT3270       IS THIS A 3270 TRM     @G36XRYP
         BO    CHKDISP                  YES, BRANCH              S22028
*  THIS IS A HEADER BUFFER. IF TWX LINE, MAKE SPECIAL TEST
         TM    AVTPARM3+2,DCTTWX+DCTLOCAL  TWX OR 2260 LOCAL   @G36XRYP
*                                          NO EOA              @G36XRYP
         BM    MAINLOOP                 CAN BYPASS EOT/EOA CHECK
         SPACE
         CLI   0(RSCAN),XXEOT           LOOK FOR EOT ONLY
         BE    NULL                     NULL LINE
         CLI   LCBFLAG1,EZERO           3705 ATTACHED DEVICE   @YA11532
         BE    CHKDISP                  BRANCH YES             @YA11532
         CLI   EZERO(RSCAN),XXEOA       IS FIRST CHAR CIRCLE D
         BNE   CHKDISP                  BRANCH NO TO CHECK ON STXS22025
         TM    LCBTSOB,LCB2741N         IS THIS A 2741?
         BO    MAINLOOP                 YES. FIRST BYTE IS DATA
         MVI   0(RSCAN),0               ZERO INPUT CIRCLE D
         B     NEXT                     DONT COUNT CIRCLE D
NULL     EQU   *
         OI    SCBERR3,SCBATTN+SCBSATTN BOTH ON FOR NULL LINE
         OI    QCBRETCT,QCBNL
         B     GIEOB                    BRANCH TO STRIP END CNTRLS22025
         SPACE
MAINLOOP EQU *
         CLI   EZERO(RSCAN),LF3705      3705 TWX LF CODE         Y06327
         BE    TWXCHK                   YES. TEXT FOR 3705 TWX   Y06327
MAIN1    EQU   *                                                 Y06327
         CLI   EZERO(RSCAN),XXNL        SEE IF NEW LINE CHAR
         BE    REPLNL                   TRANS TABL REPLACES LF WITH NL
         CLI   0(RSCAN),XXBSP           BACKSPACE?
         BE    GIBKSP                   YES
         CLI   0(RSCAN),XXTAB           HOW ABOUT HORIZ. TAB
         BE    GITAB                    GOT ONE
         TM    AVTPARM3+1,DCT3270       THIS A 3270 TERMINAL?  @G36XRYP
         BO    TESTETX                  YES, BRANCH              S22028
         CLI   EZERO(RSCAN),XXON        IS IT TWX X-ON         @OZ12513
         BE    GIEOT                    YES...TREAT AS X-OFF   @OZ12513
         CLI   EZERO(RSCAN),CR3705      3705 TWX CR CODE         Y06327
         BE    TWXCHK1                  YES. TEST FOR 3705 TWX   Y06327
MAIN2    EQU   *                                                 Y06327
         CLI   EZERO(RSCAN),XXEOB       EOB RECEIVED             S22028
         BE    REPLEOB                  YES, BRANCH              S22028
         TM    AVTPARM3+3,DCTNOIDL      IS THIS A 2260 REMOTE? @G36XRYP
         BO    TESTETX                  YES, BRANCH              S22028
         CLI   0(RSCAN),XXEOT           IS IT AN EOT             S22028
         BE    GIEOT                    YES, BRANCH              S22028
         CLI   0(RSCAN),XXOFF           IS IT 3705 TWX X-OFF     Y06327
         BE    TWXCHK2                  BRANCH IF YES            Y06327
         B     NOTREM                   NO CONTINUE              S22028
TESTETX  CLI   0(RSCAN),XXETX           ETX RECEIVED ?           S22028
         BE    GIETX                    ETX SAME AS TWX XOFF     S22028
         CLI   EZERO(RSCAN),XXETBEBC    EBCDIC ETB RECEIVED      S22028
         BE    NEXT                     YES, DO NOT COUNT        S22028
         CLI   EZERO(RSCAN),XXFM        IS THIS 3270 FIELDMARK  SA61786
         BE    REPLCR                   YES - CHANGE TO NL      SA61786
         CLI   EZERO(RSCAN),XXSTX       STX RECEIVED             S22028
         BE    NEXT                     YES, DO NOT COUNT        S22028
         CLI   0(RSCAN),XXCR            END OF LINE?
         BE    REPLCR                   YES. CHANGE TO NL AND CHK
NOTREM   EQU   *
         LA    RCNT,1(,RCNT)            COUNT 1 POSITION
         MVI   AVTPARM,NOT              INDICATE DATA SEEN
         NI    QCBRETCT,NOT-QCBNL       TURN OF LF/CR INDICATORS
         TM    AVTPARM3+1,DCT3270       IS THIS A 3270 TRM     @G36XRYP
         BO    CLRLINK                  YES, BRANCH              S22028
         TM    AVTPARM3+3,DCTNOIDL      IS DEVICE 2260 REMOTE  @G36XRYP
         BO    CLRLINK                  YES, BRANCH
         TM    AVTPARM3+2,DCTLOCAL      IS DEVICE 2260 LOCAL   @G36XRYP
         BNO   NEXT                     NO, BRANCH
CLRLINK  SR    RLINK,RLINK              CLEAR REGISTER
         IC    RLINK,AVTPARM3           GET LINE SIZE
         CR    RCNT,RLINK               COUNT GREATER THAN LINE  S22025
         BNL   RESET                    BRANCH YES TO RESET COUNTS22025
NEXT     EQU   *
         MVC   AVTDOUBL(1),0(RSCAN)
         LA    RSCAN,1(,RSCAN)          STEP BUF POINTER
         BCT   RNDX,NXTBYTE             MORE TO GO
**  ALL DONE
         L     RBUF1,FORTY4(R13)        RESTORE BUFFER ADDRESS   S22028
         TM    PRFSTAT1-IEDQPRF(RBUF1),PRFNLSTN IS THIS LAST BUFFER
         BNZ   NEXT01                   DO HAVE ANOTHER BFR
         SPACE 1                                                 Y06327
*
*        THIS SECTION OF CODE SETS UP FOR AUTOMATIC CARRIAGE RETURN
*        FOR NON-DISPLAY DEVICES ATTACHED TO A 3705 (WHICH DOES NOT
*        SEE EOB/EOT)
*
         SPACE 1                                                 Y06327
         TM    AVTDOUBL+EFOUR,TRMPREF   TRM HAVE PREFIX        @Y17XAYP
         BZ    RNCHK                    NO. CONTINUE             Y06327
         TM    AVTPARM3+ETHREE,DCT1050  1050 TERMINAL          @G36XRYP
         BO    KEYBD                    YES. CONTINUE            Y06327
         TM    AVTPARM3+EONE,DCT5041+DCT2741 5041/2741 TERM    @G36XRYP
         BNM   GIETX1                   NO, DISPLAY OR TWX     @ZA02057
*                                       YES, 5041 OR 2741      @ZA02057
         SPACE 2
KEYBD    EQU   *
         CLI   AVTDOUBL,XXNL            WAS LAST CHAR NEW LINE @ZA02057
         BE    EOB05                    YES, DON'T SET ATTN    @ZA02057
         TM    QCBRETCT,QCBCR           WAS NEW LINE ENTERED   @ZA02057
         BO    EOB05                    YES, BRANCH            @ZA02057
         TM    LCBCSW+3,UNEX            WAS EOT RECEIVED       @ZA02057
         BZ    EOB05                    NO BRANCH              @ZA02057
         OI    SCBERR3,SCBATTN          SET ATTN               @ZA02057
         L     RBUF1,FORTY4(R13)        RESTORE BUFFER ADDR    @ZA02057
         TM    PRFSTAT1-IEDQPRF(RBUF1),PRFNHDRN THIS IS HDR BUF@ZA02057
         BO    EOB05                    NO, MUST HAVE DATA     @ZA02057
         LTR   RCNT,RCNT                IS DATA COUNT ZERO     @ZA02057
         BNZ   EOB05                    NO, BRANCH             @ZA02057
         OI    SCBERR3,SCBSATTN         SET NO DATA            @ZA02057
EOB05    EQU   *                                               @ZA02057
         LTR   RCNT,RCNT                NEED CARRIAGE RETURN     Y06327
         BNZ   ETXX                     BRANCH YES               Y06327
         TM    AVTDOUBL+EFOUR,TRMPREF   IS THERE A PREFIX      @Y17XAYP
         BZ    NOPREFIX                 NO,SKIP PREFIX PROCESS @Y17XAYP
         ICM   RBUF1,ESEVEN,AVTDOUBL+EFIVE  GET TRM PREFIX     @Y17XAYP
*                                       ADDRESS                @Y17XAYP
         NI    TRMBYTE2-IEDNTRM(RBUF1),NOT-TRMWRBRK INDICATE   @YM04685
*                                       WRITE BREAK NOT IN     @Y17XAYP
*                                       PROGRESS               @Y17XAYP
NOPREFIX EQU   *                                               @Y17XAYP
         NI    LCBTSOB,X'FF'-LCBWRBRK   NO. SET WRITE BRK BIT OFFY06327
         NI    SCBERR1,SCBCUTFF-SCBNOBFN AND ERROR BITS          Y06327
         B     FINAL                    EXIT                     Y06327
RNCHK    EQU   *                                                 Y06327
         LTR   RCNT,RCNT                WAS NL LAST
         BNZ   GIETX1                   B NO. COUNT LINE IF NEEDSA59324
         TM    QCBRETCT,QCBNL           NL OR CR/LF RCVD        SA59324
         BO    FINAL                    B YES. NO SPCL ACTION   SA59324
         SPACE 1                                                SA61787
         B     ETXX                     FLAG FOR NL/CR/LF INSERTSA59324
NEXT01   EQU   *
*  ANOTHER BUFFER IS COMING. SAVE INDIC OF NL IF LAST IN BFR.
         CLI   AVTDOUBL,XXNL            WAS LAST CHAR NL
         BNE   FINAL                    NO
         OI    QCBRETCT,QCBCR           INDICATE NL IN THIS BFR
         B     FINAL                    RETURN
CHKDISP  EQU   *
         TM    AVTPARM3+3,DCTNOIDL      2260 REMOTE ?          @G36XRYP
         BO    CHKSTX                   IF YES CHK FOR STX       S22028
         TM    AVTPARM3+1,DCT3270       IS THIS A 3270 TRM     @G36XRYP
         BNO   MAINLOOP                 NO, BRANCH               S22028
         TM    AVTPARM3+2,DCTBISYN+DCTSTCTL 3270 REMOTE ?      @G36XRYP
         BNO   CHKLOC                   NO. BRANCH FOR 3270 LOCALY06327
*                                       YES. REMOTE 3270         Y06327
         TM    AVTDOUBL+EFOUR,TRMPREF   TRM HAVE PREFIX        @Y17XAYP
         BNO   CKAUTOPL                 NO. CHECK FOR AUTOPOLL   Y06327
         NI    AVTPARM3+ETWO,RN3705     YES. TREAT 3705/3270     Y06327
*                                       REMOTE AS A 3270 LOCAL   Y06327
         XC    EZERO(ETWO,RSCAN),EZERO(RSCAN) ZERO OUT CU & DEV  Y06327
         LA    RSCAN,ETWO(RSCAN)        INCREMENT SCAN PTR BY 2  Y06327
         SH    RNDX,H2                  REDUCE DATA SIZE BY 2    Y06327
         SH    RLIM,H2                  REDUCE BUFFERSIZE BY 2   Y06327
CHKLOC   EQU   *                                                 Y06327
         TM    PRFSTAT1,PRFNHDRN        IS THIS A HEADER BUFFER  S22028
         BO    NXTBYTE                  NO, GO ADVANCE SCAN PTR  S22028
SAVEADDR LA    RWORK,ESEVEN             GET # OF BITS TO CHECK   S22028
         DROP  RPARM                    DROP ADDRESSABILITY      S22028
         USING IEDQTRM,RBUF1            SET UP ADDRESSABILITY    S22028
         LR    REXIT,RWORK              GET # OF BITS TO CHECK   S22028
         LA    RLINK,E128               SET FOR TM EX INSTR      S22028
CHKBITS  EQU   *                                                 S22028
         EX    RLINK,TESTBITS           IS THIS TRMDEVFL ON ?    S22028
         BO    SHIFTMSK                 YES, DONT DECREMENT      S22028
*                                       NUMBER OF BITS ON        S22028
         BCTR  RWORK,0                  DECR # OF BITS ON        S22028
SHIFTMSK EQU   *                                                 S22028
         SRL   RLINK,EONE               SHIFT MASK RIGHT ONE     S22028
         BCT   REXIT,CHKBITS            BRANCH IF ALL BITS       S22028
*                                       NOT CHECKED              S22028
         LA    RLINK,TRMOPNO            ADDR OF NUMBER OF        S22028
*                                       OPTIONS IF ANY, ELSE     S22028
*                                       ADDRESS OF FIRST         S22028
*                                       DEVICE DEPENDENT FIELD   S22028
         SR    REXIT,REXIT              ZERO REG FOR IC INSTR    S22028
         TM    TRMSTATE,TRMOPTFN        ANY OPTION FIELDS ?      S22028
         BZ    CHECKDDF                 NO, CHECK FOR            S22028
*                                       DEVICE DEPENDENT FIELDS  S22028
         IC    REXIT,TRMOPNO            GET NUMBER AND           S22028
*                                       SIZE OF OPTIONS          S22028
         LA    RLINK,ETHREE(REXIT,RLINK) BUMP PAST REST OF TERM  S22028
*                                        ENTRY AND ANY OPTIONS   S22028
CHECKDDF EQU   *                                                 S22028
         LTR   RWORK,RWORK              ANY TRMDEVFL BITS ON ?   S22028
         BZ    STORINFO                 NO - RLINK NOW POINTS TO S22028
*                                       FIRST DEV DEP FIELD      S22028
BUMPDDF  EQU   *                                                 S22028
         IC    REXIT,0(0,RLINK)         GET LENGTH OF DDF        S22028
         LA    RLINK,EONE(REXIT,RLINK)  BUMP PAST LENGTH FIELD   S22028
*                                       AND DEV DEP FIELD        S22028
         BCT   RWORK,BUMPDDF            BRANCH IF MORE FIELDS    S22028
STORINFO EQU   *                                                 S22028
         LA    RLINK,ETHREE(RLINK)      BUMP PAST USED BYTES     S22028
         MVC   0(1,RLINK),EZERO(RSCAN)  SAVE AID BYTE            S22028
ADDINCR  EQU   *                                                 S22028
         LR    RWORK,RNDX               GET DATA SIZE            S22028
         TM    AVTPARM3+2,DCTBISYN+DCTSTCTL  3270 REMOTE?      @G36XRYP
         BNO   ADDINCR1                 NO, BRANCH               S22028
         BCTR  RWORK,0                  REDUCE BY ONE FOR ETX    S22028
ADDINCR1 EQU   *                                                 S22028
         CH    RWORK,H1                 IS THIS AID BYTE ONLY    S22028
         BE    AIDONLY                  YES, REDUCE BUFFERSIZE   S22028
         CH    RWORK,H3                 IS THIS ENTER KEY ONLY   S22028
         BNE   CHKSBA                   NO, BRANCH TO CHECK SBA  S22028
         MVI   3(RSCAN),XXNL            INSERT NEW LINE IN BUFFERS22028
         LA    RNDX,1(RNDX)             ADD 1 TO DATA SIZE       S22028
         LA    RLIM,1(RLIM)             ADD 1 TO BUFFERSIZE      S22028
         LH    RLINK,PRFSIZE            GET SIZE OF DATA         S22028
         LA    RLINK,1(RLINK)           ADD ONE TO IT            S22028
         STH   RLINK,PRFSIZE            STORE SIZE OF DATA       S22028
         TM    AVTPARM3+2,DCTBISYN+DCTSTCTL  3270 REMOTE?      @G36XRYP
         BNO   ADDINCR3                 NO, REDUCE BUFFERSIZE    S22028
         MVI   4(RSCAN),XXETX           INSERT ETX FOR REMOTE    S22028
ADDINCR3 EQU   *                                                 S22028
         LA    RSCAN,ETWO(RSCAN)        INCREMENT SCAN PTR BY 2  S22028
         BCTR  RNDX,0                   REDUCE DATA SIZE BY 1    S22028
         BCTR  RLIM,0                   REDUCE BUFFERSIZE BY 1   S22028
         B     ONEMORE                  REDUCE BY ONE MORE       S22028
CHKSBA   EQU   *                                                 S22028
         CLI   3(RSCAN),XXSBA           IS SBA PRESENT IN DATA   S22028
         BNE   ADDINCR3                 NO, BRANCH TO INCREMENT  S22028
         CLI   0(RSCAN),IDCARD          ID CARD READER DATA      S22028
         BNE   CHKSBA1                  NO, BRANCH TO INCREMENT  S22028
         CLI   6(RSCAN),XXSBA           IS SECOND SBA PRESENT    S22028
         BNE   CHKSBA1                  NO, BRANCH TO INCREMENT  S22028
         MVC   3(3,RSCAN),EZERO(RSCAN)  OVERLAY FIRST SBA/ADR/ADRS22028
*                                       WITH AID/CUR/CUR         S22028
         XC    0(3,RSCAN),0(RSCAN)      ZERO OUT AID/CUR/CUR     S22028
         LA    RSCAN,ETHREE(RSCAN)      INCREMENT SCAN PTR BY 3  S22028
         SH    RNDX,H3                  REDUCE DATA SIZE BY 3    S22028
         SH    RLIM,H3                  REDUCE BUFFERSIZE BY 3   S22028
CHKSBA1  EQU   *                                                 S22028
         LA    RSCAN,ETHREE(RSCAN)      INCREMENT SCAN PTR BY 3  S22028
         SH    RNDX,H3                  REDUCE DATA SIZE BY 3    S22028
         SH    RLIM,H3                  REDUCE BUFFERSIZE BY 3   S22028
         B     ADDINCR3                 BRANCH TO INCREMENT SCAN S22028
         SPACE
CHKSTX   CLI   0(RSCAN),XXSTX           STX ?                    S22028
         BNE   MAINLOOP                 NO, BRANCH               S22028
         XC    0(2,RSCAN),0(RSCAN)      ZERO STX AND ADDR CHAR   S22028
         LA    RSCAN,1(,RSCAN)                                   S22028
ONEMORE  EQU   *                                                 S22028
         BCTR  RNDX,0                   REDUCE LINE SIZE BY      S22028
*                                       ONE FOR SMI              S22028
         BCT   RLIM,NEXT                REDUCE BU SIZE AND       S22028
*                                       GO TO NEXT CHARACTER     S22028
         B     NXTBYTE                  GO ADVANCE SCAN POINTER  S22028
         SPACE
AIDONLY  EQU   *                                                 S22028
         TM    AVTPARM3+2,DCTBISYN+DCTSTCTL  3270 REMOTE?      @G36XRYP
         BO    GIETX                    YES, STRIP ETX           S22028
         B     GIETX1                   COUNT LINE               S22028
         SPACE
CKAUTOPL EQU   *                                                 S22028
         CLI   0(RSCAN),NOMASK          IS THIS A HEX ZERO       S22028
         BNE   CHKSOH                   NO, CHECK FOR SOH%R MSG  S22028
         LA    RSCAN,EONE(RSCAN)        IF YES SKIP OVER IT      S22028
         BCTR  RNDX,EZERO               AND ADJUST DATA AND      S22028
         BCTR  RLIM,EZERO               BUFFER SIZE BY ONE.      S22028
CHKSOH   EQU   *                                                 S22028
         TM    SCBERR2,SCBSOHE          IS THIS 3270 SOH%R MSG   S22028
         BZ    CHKSTX1                  NO, BRANCH TO CHECK STX  S22028
         OI    QCBRETCT,QCBIEND         SET 3270 SCREEN FORMAT BIT22028
         B     EXIT                     BRANCH TO EXIT           S22028
CHKSTX1  CLI   0(RSCAN),XXSTX           STX?                     S22028
         BNE   MAINLOOP                 NO, BRANCH               S22028
         XC    0(3,RSCAN),0(RSCAN)      ZERO OUT STX,CU AND DEV  S22028
         LA    RSCAN,ETHREE(RSCAN)      INCREMENT SCAN PTR BY 3  S22028
         SH    RNDX,H3                  REDUCE DATA SIZE BY 3    S22028
         SH    RLIM,H3                  REDUCE BUFFERSIZE BY 3   S22028
         B     SAVEADDR                 GO SAVE AID BYTE         S22028
NXTBYTE  EQU   *
         BCT   RLIM,MAINLOOP            REDUCE UNIT SIZE, CONT.  S22025
**  END OF UNIT
         BCT   RUNDX,NXU                ANY MORE UNITS THIS BUFFER?
         B     FINAL                    NO
NXU      EQU   *
         L     RPREFIX,PRFTIC           ADDRESS OF NEXT UNIT
         LA    RSCAN,AVTUMALN(,RPREFIX) SCAN BEYOND CCW-TIC
         LH    RLIM,AVTKEYLE            UNIT SIZE
         B     MAINLOOP                 START NEXT UNIT
FINAL    EQU   *
**  LAST UNIT AND BUFFER CHECKED. WE CAN EXIT NOW
         STC   RCNT,QCBCARCT            STORE CURRENT POSITION
         TM    AVTPARM3+1,DCT3270       IS THIS A 3270 TRM     @G36XRYP
         BZ    EXIT                     NO, EXIT                 S22028
         NI    QCBRETCT,NOT-QCBNL       TURN OFF NL INDICATOR    S22028
         SPACE
EXIT     EQU   *
         L     RLINK,AVTUI              ADDR OF RETURN ROUT      S22025
         B     FOUR(RLINK)              RETURN TO CALLER         S22025
TWXCHK2  EQU   *
         TM    AVTDOUBL+EFOUR,TRMPREF   TRM HAVE PREFIX        @Y17XAYP
         BZ    NOTREM                   NO. CONTINUE CHECKING    Y06327
         TM    AVTPARM3+2,DCTTWX        IS THIS A TWX LINE     @G36XRYP
         BO    TWXEOT                   BRANCH IF YES            Y06327
         B     NOTREM                   NO. CONTINUE CHECKING    Y06327
         SPACE 3
GIEOT    EQU   *
         TM    AVTPARM3+2,DCTTWX        IS THIS A TWX LINE     @G36XRYP
         BO    TWXEOT                   BRANCH YES
         CLI   AVTDOUBL,XXNL
         BE    GIEOB                    LAST WAS NL. DO NOT SET ATTN
         TM    QCBRETCT,QCBCR           HAVE WE SEEN THE NL
         BO    GIEOB                    BRANCH YES
         OI    SCBERR3,SCBATTN          SET ATTENTION BIT
         L     RBUF1,FORTY4(R13)        RESTORE BUFFER ADDRESS   S22028
         TM    PRFSTAT1-IEDQPRF(RBUF1),PRFNHDRN  THIS HDR BFR
         BO    GIEOB                    NO. MUST HAVE HAD DATA
         LTR   RCNT,RCNT                IF HDR, CONTAINS COUNT OF DATA
         BNZ   GIEOB                    BRANCH IF COUNT          S22025
         OI    SCBERR3,SCBSATTN         NO DATA
         SPACE
GIEOB    EQU   *
         BAL   REXIT,MINUS1             STRIP END CONTROL
         LTR   RCNT,RCNT                NEED CARR REUTRN
         BNZ   ETXX                     BRANCH YES
         TM    AVTDOUBL+EFOUR,TRMPREF   TRM HAVE A PREFIX      @Y17XAYP
         BZ    NOPRF                    NO,SKIP PREFIX PROCESS @Y17XAYP
         ICM   RBUF1,ESEVEN,AVTDOUBL+EFIVE  GET TRM PREFIX     @Y17XAYP
*                                       ADDRESS                @Y17XAYP
         NI    TRMBYTE2-IEDNTRM(RBUF1),NOT-TRMWRBRK  INDICATE  @YM04685
*                                       WRITE BREAK NO LONGER  @Y17XAYP
*                                       IN PROGRESS            @Y17XAYP
NOPRF    EQU   *                                               @Y17XAYP
         NI    LCBTSOB,NOT-LCBWRBRK     SET WRITE BREAK BIT OFF  S21903
         NI    SCBERR1,SCBCUTFF-SCBNOBFN
         B     NEXT                     TO GET NEXT CHAR IF ANY
TWXEOT   EQU   *
GIETX    EQU   *                        LOOK FOR ETX ONLY
         BAL   REXIT,MINUS1             STRIP EOT
GIETX1   EQU   *
         TM    QCBRETCT,QCBCR+QCBLF     WER BOTH LF AND CR SEEN
         BO    FINAL                    IF SO BRANCH
         SR    RLINK,RLINK              CLEAR FOR INSERT CHAR
         IC    RLINK,AVTPARM3           GET LINE SIZE
         CR    RCNT,RLINK               IF END LINE, HAS BEEN COUNTED
         BE    ENDLINE                  DECIDE IF CR NEEDED    @YA12281
*  NOT FULL LINE. LINE HAS BEEN COUNTED IF LAST CHAR WAS NL.
         TM    QCBRETCT,QCBNL           WAS LAST CHAR NL
         BO    ET00                     IF YES, NONE NEEDED
GIETX2   EQU   *
         CLI   QCBSATCT,NOT             FF COMPARE               S21903
         BE    ETXX                     BRANCH IF YES
         TM    QCBTSOF2,QCBSIMRD        RESP TO SIM ATTN READ?    M1191
         BZ    GIETX3                   NO, COUNT LINE          SA53605
         TM    AVTPARM3+1,DCT3270       IS THIS A 3270 TRM     @G36XRYP
         BO    GIETX3                   YES, COUNT LINE          S22028
         TM    AVTPARM3+3,DCTNOIDL      IS THIS A 2260 REMOTE  @G36XRYP
         BO    GIETX3                   YES, COUNT LINE         SA53605
         TM    AVTPARM3+2,DCTLOCAL      IS THIS A 2260 LOCAL   @G36XRYP
         BZ    ETXX                     NO, DO NOT COUNT LINE   SA53605
GIETX3   EQU   *                                                SA53605
         IC    RLINK,QCBSATCT           CURRENT LINE COUNT      SA53605
         LA    RLINK,EONE(,RLINK)       ADD ONE
         STC   RLINK,QCBSATCT           STORE IT BACK
ETXX     EQU   *
         LA    RCNT,NOT                 IF NOT, SET INPUT FLAG FOR AYE
         B     FINAL                    END OF DATA IS X-OFF (EOT)
ENDLINE  EQU   *                                               @YA12281
         TM    AVTPARM3+2,DCTTWX        IS THIS A TWX LINE?    @G36XRYP
         BO    ETXX                     YES, NEED A CR         @YA12281
         SPACE
ET00     EQU   *
         SR    RCNT,RCNT                CLEAR LINE COUNT
         B     FINAL                    RETURN TO MH             S22025
TWXCHK   EQU   *                                                 Y06327
         TM    AVTDOUBL+EFOUR,TRMPREF   TRM HAVE PREFIX        @Y17XAYP
         BZ    MAIN1                    NO. CONTINUE CHECKING    Y06327
         TM    AVTPARM3+ETWO,DCTTWX     YES. IS THIS A TWX LINE@G36XRYP
         BZ    MAIN1                    NO. CONTINUE CHECKING    Y06327
         B     REPLNL1                  YES. SET QCB LF BIT      Y06327
         SPACE 1                                                 Y06327
TWXCHK1  EQU   *                                                 Y06327
         TM    AVTDOUBL+EFOUR,TRMPREF   TRM HAVE PREFIX        @Y17XAYP
         BZ    MAIN2                    NO. CONTINUE CHECKING    Y06327
         TM    AVTPARM3+ETWO,DCTTWX     YES. IS THIS A TWX LINE@G36XRYP
         BZ    MAIN2                    NO. CONTINUE CHECKING    Y06327
         B     REPLEOB1                 YES. SET QCB CR BIT      Y06327
         SPACE 1                                                 Y06327
         SPACE
REPLNL   EQU   *
         TM    AVTPARM3+2,DCTTWX        IS THIS A TWX LINE     @G36XRYP
         BZ    GINL                     BRANCH NO
         MVI   0(RSCAN),XXLF            CHANGE NL TO LF
REPLNL1  EQU   *                                                 Y06327
         OI    QCBRETCT,QCBLF
         B     REP01                    USE COMMON CODE
         SPACE
REPLEOB  EQU   *
         TM    AVTPARM3+2,DCTTWX        IS THIS A TWX LINE     @G36XRYP
         BZ    GIEOB                    BRANCH NO
         MVI   0(RSCAN),XXCR            REPLACE EOB WITH TTY CR
REPLEOB1 EQU   *                                                 Y06327
         OI    QCBRETCT,QCBCR           INDICATE CR RECEIVED
         SR    RCNT,RCNT                INDICATE CARR AT LEFT MARGIN
REP01    EQU   *
         MVI   AVTPARM,EZERO            TURN OFF DATA INDICATOR
         B     NEXT                     GET NEXT CHARACTER
         SPACE
GITAB    EQU   *                        DUMMY FOR NOW
         LA    RCNT,132                 ASSUME WORST CASE
         B     NEXT                     GET NEXT CHARACTER       S22025
         SPACE
REPLCR   EQU   *
         MVI   0(RSCAN),XXNL            2260R CR TO NL
         SPACE
GINL     EQU   *
         LA    RLINK,2                  SEE IF MORE THAN 2 CHAR LEFT
         CR    RNDX,RLINK               INCLUDING THIS ONE
         BH    RESET                    YES. RESET COUNT TO 0
         SR    RCNT,RCNT                RESET COUNT REGISTER
         OI    QCBRETCT,QCBNL           INDICATE NL RECEIVED
RESET    EQU   *
         TM    AVTPARM3+1,DCT3270       IS THIS A 3270 TRM     @G36XRYP
         BO    BALUPL                   YES, BRANCH              S22028
         TM    AVTPARM3+3,DCTNOIDL      IS DEVICE 2260 REMOTE  @G36XRYP
         BO    BALUPL                   YES, BRANCH
         TM    AVTPARM3+2,DCTLOCAL      IS DEVICE 2260 LOCAL   @G36XRYP
         BNO   NOTDISP                  NO, BRANCH
BALUPL   EQU   *
         CLI   QCBSATCT,NOT             FF COMPARE               S21903
         BE    NOTDISP                  BRANCH IF YES
         IC    RLINK,QCBSATCT           SIM ATTN LINE COUNT
         LA    RLINK,1(,RLINK)          INCR BY 1
         STC   RLINK,QCBSATCT           PUT IT BACK
NOTDISP  EQU   *
         SR    RCNT,RCNT                RESET COUNT REG
         TM    AVTPARM3+1,DCT3270       IS THIS A 3270 TRM     @G36XRYP
         BZ    NEXT                     NO, GET NEXT CHARACTER   S22028
         TM    QCBRETCT,QCBNL           WAS NL RECEIVED          S22028
         BZ    NEXT                     NO, GET NEXT CHARACTER   S22028
         LA    RCNT,NOT                 SET N/L FLAG FOR AYB     S22028
         B     NEXT                     GET NEXT CHARACTER       S22025
         SPACE
GIBKSP   EQU   *
         LTR   RCNT,RCNT                SEE IF ANY COUNTED
         BZ    NEXT                     DON'T BSP PAST 0
         BCT   RCNT,NEXT                DELETE 1
         B     NEXT                     GET NEXT CHARACTER       S22025
         SPACE
MINUS1   EQU   *
         L     RBUF1,FORTY4(R13)        RESTORE BUFFER ADDRESS   S22028
         LH    RLINK,PRFSIZE-IEDQPRF(RBUF1) GET SIZE OF DATA     S22025
         BCTR  RLINK,0                  SUBTRACT ONE FROM SIZE   S22025
         STH   RLINK,PRFSIZE-IEDQPRF(RBUF1) OF DATA IN BUFFER    S22025
         BR    REXIT                    UPDATE PRFSIZE & RETURN  S22025
H1       DC    H'1'                     HALFWORD OF ONE          S22025
H2       DC    H'2'                     HALFWORD OF TWO          Y06327
H3       DC    H'3'                     HALFWORD OF THREE        S22025
H23      DC    H'23'                    TCAM TEXT PREFIX SIZE    S22025
H30      DC    H'30'                    TCAM HEADER BUFFER PREFIX SIZE
TESTBITS TM    TRMDEVFL,NOMASK          EXECUTED DEVFL BIT TEST  S22028
ZAPSPACE DC    6F'0'                    PATCH AREA               S22025
         EJECT
         TPRFD
         TQCBD
         TAVTD
         TCD1D
         TDCTD
CVT      DSECT
         CVT
         IKJTSB
         TSCBD
         TLCBD
         TRHD
         TSIBD
         TSNSD
         TTNTD
         TTRMD
         IHAASCB
         IHAASVT                                               @ZM46782
         END
