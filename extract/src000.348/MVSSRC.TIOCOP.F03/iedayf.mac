         TITLE 'IEDAYF - IOHALT ROUTINE.'
IEDAYF   CSECT
*A669100,670200                                                  S22029
*C300000-310000,413000                                           S22029
*D670500,670800,675000                                           S22029
*A020000                                                         S21903
*C050000-200000,280000-286000,422200,630000,665460,668400,677400,S21903
*C677490,677530,677610,678000,678300,678900,677530               S21903
*A 036300                                                        X01004
*C 473000                                                        X01004
*A454000-456000,678910-678920                                    X02004
*A668000,679200                                                  Y01948
*C340000,660000,677530                                           Y01948
*D668400-669000                                                  Y01948
*A600000,640000                                                @ZA05011
*A445000                                                       @SA75753
*                                                              @G36XRYP
*C421000,673000                                                @G36XRYP
*A417400                                                       @OZ30648
*
***********************************************************************
*TITLE  'IEDAYF'  IOHALT ROUTINE                                      *
*                                                              @G36XRYP
*MODULE NAME = IEDAYF (TCAM,TSO)                               @G36XRYP
*                                                              @G36XRYP
*DESCRIPTIVE NAME = TSO IOHALT ROUTINE                         @G36XRYP
*                                                              @G36XRYP
*COPYRIGHT = NONE                                              @G36XRYP
*                                                              @G36XRYP
*STATUS -- CHANGE LEVEL 10.0                                   @G36XRYP
*                                                                     *
*FUNCTION -- THE IOHALT ROUTINE GETS CONTROL WHEN EITHER AN LCB OR AN *
*   ERB IS POSTED FROM LINE END APPENDAGE.  FOR AN LCB, THIS ROUTINE  *
*   MONITORS FOR AN ATTENTION IF THE LCB OPEN CHECK OP CODE HAS BEEN  *
*   SET AND IF A PREPARE HAS BEEN ISSUED FOR THE LINE.  WHEN AN ATTEN-*
*   TION OCCURS, THIS ROUTINE ISSUES AN IOHALT SVC (SVC 33) ON THE    *
*   PREPARE.  IT THEN EXITS TO THE DISPATCHER DISPATCH FUNCTION TO    *
*   DISPATCH THE NEXT SUBTASK.                                        *
*      AN ERB IS POSTED WHEN A HARDWARE ATTENTION OCCURS OR WHEN A    *
*   2741 HANGS UP.  THIS ROUTINE ACCESSES THE LCB FROM THE ERB AND    *
*   SETS OFF THE PREPARE BIT.  THEN, IF THE LCB IS IN THE TIME DELAY  *
*   QUEUE, THIS ROUTINE LINKS FIRST TO THE TIME DELAY SUBTASK (IEDQHG)*
*   TO REMOVE IT, AND THEN TO THE DISPATCHER PRIORITY FUNCTION TO     *
*   INSERT THE RECEIVE SCHEDULER'S STCB INTO THE LCB'S STCB CHAIN BY  *
*   PRIORITY.  IF THE LCB COMPLETION CODE INDICATES THAT I/O ENDED ON *
*   THE LINE BECAUSE OF AN IOHALT, LINE FREE PRIORITY IS SET, AND THE *
*   LCB IS SET TO BE POSTED TO ITSELF.  IF THE LINE IS CONNECTED TO   *
*   A 2741, AND A PREPARE CCW WAS INTERRUPTED, THIS ROUTINE INDICATES *
*   IN THE LCB THAT A CIRCLE D WAS SENT AND CHECKS TO SEE IF THE 2741 *
*   HAS HUNG UP.  IF IT HAS, THE LCB IS SET TO BE POSTED TO THE       *
*   HANGUP ROUTINE.  IF A PREPARE CCW WAS NOT INTERRUPTED, BUT A      *
*   CIRCLE D WAS SENT, THE LCB IS SET TO INDICATE CIRCLE D SENT.  FOR *
*   2741'S THAT HAVE NOT HUNG UP, AND FOR ALL OTHER DEVICES, THE SCB  *
*   HARDWARE ATTENTION BIT IS SET ON, AND THE LCB IS SET TO BE POSTED *
*   TO THE ATTENTION ROUTINE.  IN ALL CASES EXCEPT WHEN I/O WAS ENDED *
*   BY IOHALT, PCI PRIORITY IS SET.  FINALLY, THIS ROUTINE LINKS ALL  *
*   PREVIOUS ELEMENTS IN THE CHAIN, SETS THE QCB ADDRESS IN THE LCB,  *
*   INDICATES THAT THE LCB IS THE NEW FIRST ELEMENT IN THE CHAIN, AND *
*   EXITS TO THE DISPATCHER CHAIN FUNCTION TO POST ALL THE ELEMENTS   *
*   IN THE CHAIN TO THE APPROPRIATE QCBS.                             *
*                                                                     *
*ENTRY POINT -- IEDAYF - TO MONITOR FOR AN ATTENTION, OR TO HANDLE A  *
*   HARDWARE ATTENTION OR HANGUP SITUATION                            *
*   CALLING SEQUENCE          L    R15,STCBLINK-1                     *
*                             BR   R15                                *
*                                                                     *
*INPUT -- IEDAYF IS CALLED BY THE DISPATCHER WHEN AN LCB OR AN ERB IS *
*   POSTED TO IT FROM LINE END APPENDAGE.  AT ENTRY, THE FOLLOWING    *
*   REGISTERS ARE SET.                                                *
*   R1 HAS THE ADDRESS OF THE LCB OR ERB                              *
*   R11 HAS THE DISPATCHER ADDRESS.                                   *
*   R13 HAS THE ADDRESS OF AVTSAVE2                                   *
*   R15 HAS THE ENTRY POINT ADDRESS.                                  *
*                                                                     *
*OUTPUT -- ON LINKING TO THE IOHALT SVC ROUTINE, THE FOLLOWING REGIS- *
*   TERS ARE SET.                                                     *
*   R2 HAS THE UCB ADDRESS                                            *
*   R8 HAS THE DEB ADDRESS                                            *
*   R10 HAS THE DCB ADDRESS                                           *
*      ON LINKING TO THE TIME DELAY SUBTASK, THE FOLLOWING REGISTERS  *
*   ARE SET.                                                          *
*   R1 HAS THE LCB ADDRESS                                            *
*   R14 HAS THE RETURN ADDRESS IN IEDAYF                              *
*   R15 HAS THE ENTRY POINT ADDRESS OF THE TIME DELAY REMOVAL ROUTINE *
*   IN THE TIME DELAY SUBTASK                                         *
*      ON LINKING TO THE DISPATCHER PRIORITY FUNCTION, THE FOLLOWING  *
*   REGISTERS ARE SET.                                                *
*   R1 HAS THE ADDRESS OF THE RECEIVE SCHEDULER KEY                   *
*   R7 HAS THE ADDRESS OF THE FIRST STCB IN THE LCB'S STCB CHAIN      *
*   R14 HAS THE RETURN ADDRESS IN IEDAYF                              *
*      ON EXIT TO THE DISPATCHER DISPATCH FUNCTION, REGISTER 4 CON-   *
*   TAINS THE LCB ADDRESS.                                            *
*      ON EXIT TO THE DISPATCHER CHAIN FUNCTION, THE LCB IS SET TO BE *
*   POSTED TO THE ATTENTION QCB, THE HANGUP QCB, OR ITSELF.  THE LCB  *
*   PRIORITY IS SET, AND THE FOLLOWING REGISTERS ARE SET.             *
*   R1 HAS THE ADDRESS OF THE LCB TO BE POSTED, AS FIRST ELEMENT IN   *
*   THE CHAIN                                                         *
*   R3 HAS THE SCB ADDRESS                                            *
*   R4 HAS THE LCB ADDRESS                                            *
*   R5 HAS THE ADDRESS OF THE QCB TO WHICH THE LCB IS TO BE POSTED    *
*   R10 HAS THE DCB ADDRESS                                           *
*   R14 HAS THE ENTRY POINT ADDRESS IN THE DISPATCHER                 *
*                                                                     *
*EXTERNAL ROUTINES -- IOHALT SVC ROUTINE - TO HALT I/O ON A LINE      *
*   IEDQHG - TO REMOVE AN LCB FROM THE TIME DELAY QUEUE               *
*   DISPATCHER PRIORITY FUNCTION - TO INSERT THE RECEIVE SCHEDULER'S  *
*   STCB INTO THE LCB'S STCB CHAIN                                    *
*   EXCPVR SVC ROUTINE - TO START A PREPARE CHANNEL PROGRAM           *
*                                                                     *
*EXITS-NORMAL -- TO THE DISPATCHER DISPATCH FUNCTION                  *
*   TO THE DISPATCHER CHAIN FUNCTION                                  *
*                                                                     *
*EXITS-ERROR -- NONE                                                  *
*                                                                     *
*TABLES/WORKAREAS -- AVT, DCB, LCB, SCB, DEB, TSID, TPRIOR ERB.       *
*                                                                     *
*ATTRIBUTES -- REENTRANT, ENABLED, RESIDENT, PROBLEM PROGRAM MODE     *
*                                                                     *
*NOTES -- THE OPERATION OF THIS MODULE DOES NOT DEPEND UPON A         *
*   PARTICULAR INTERNAL REPRESENTATION OF THE EXTERNAL CHARACTER      *
*   SET.                                                              *
*                                                                     *
***********************************************************************
         SPACE 2
R1       EQU   1                        REGISTER EQUATE          S21903
R2       EQU   2                        REGISTER EQUATE          S21903
RSCB     EQU   3                        REGISTER EQUATE          S21903
RLCB     EQU   4                        REGISTER EQUATE          S21903
R5       EQU   5                        REGISTER EQUATE          S21903
R6       EQU   6                        REGISTER EQUATE          S21903
RWORK    EQU   7                        REGISTER EQUATE          S21903
RDEB     EQU   8                        REGISTER EQUATE          S21903
R9       EQU   9                        REGISTER EQUATE          S21903
RDCB     EQU   10                       REGISTER EQUATE          S21903
RDSP     EQU   11                       REGISTER EQUATE          S21903
RBAS     EQU   12                       REGISTER EQUATE          S21903
R13      EQU   13                       REGISTER EQUATE          S21903
R14      EQU   14                       REGISTER EQUATE          S21903
R15      EQU   15                       REGISTER EQUATE          S21903
         SPACE 2
OPNCHK   EQU   X'AC'                    OPEN CHECK               S21903
         SPACE 2
         USING IEDQDISP,RDSP            DISPATCHER BASE          S21903
         USING AVTSAVE2,R13             SAVE AREA BASE           S21903
         USING IEDQLCB,RLCB             LCB BASE                 S21903
         USING IEDQSCB,RSCB             SCB BASE                 S21903
         USING IHADCB,RDCB              DCB BASE                 S21903
         USING IEDQTSI,RWORK            WORK BASE                S21903
X48      EQU   X'48'                    EQUATE                   S21903
IR       EQU   X'40'                    INTERVENTION REQUIRED
DELAYBIT EQU   X'02'                    DELAY BIT                S21903
TO       EQU   X'01'                   TIME OUT
PREPARE  EQU   6                        PREPARE                  S21903
IEDAYF   IEDHJN
         DC    AL1(DSPMCPL4),AL3(*-1)   STCB
         BALR  RBAS,0                   LOAD BASE REGISTER       Y01948
         USING *,RBAS
         LA    RLCB,0(R1)               LCB ADDRESS
         CLI   LCBPRI-IEDQLCB(R1),PRIDSPLB IS IT THE ERB?
         BE    ITSERB                   BRANCH ON YES
         SH    RLCB,ERBOF               ASSUME ERB
         TM    PRFTIC-IEDQPRF(R1),X08   IS ELEMENT A BUFFER
         BZ    OKERB                    BRANCH NO - MUST BE ERB
*
         LR    R6,R1                    SAVE BUFFER ADDRESS
         L     RLCB,PRFLCB-IEDQPRF-1(R1)  LCB ADDRESS FROM BUFFER
         MVC   PRFOPCDE-IEDQPRF(8,R1),PRFSHDR-IEDQPRF(R1) RESTORES22029
*                                                         CCW    S22029
         TM    LCBSTAT1,LCBSENDN        SEND STATE
         BZ    OKERB                    BRANCH NO
*
         LA    R15,LCBERB               ERB ADDRESS
         LA    R2,AVTBFRTB-4              INITIALIZE FOR LOOP
*        REMOVE ERB FROM BUFFER RETURN QUEUE IF THERE
SCNERB   EQU   *
         LR    R1,R2                    NEW LAST ELEMENT
         USING IEDQQCB,R1
         L     R2,QCBLINK-1            FIRST/NEXT ELEMENT ON Q
         LA    R2,0(,R2)                CLEAR HIGH ORDER BYTE
         CL    R2,AVTDELAD              DUMMY LAST ELEMENT
         BE    OKERB                    BRANCH YES - NOT THERE
*
         CLR   R2,R15                   IS THIS THE ERB
         BNE   SCNERB                   BRANCH NO - CONTINUE SCAN
*
         MVC   QCBLINK,QCBLINK-IEDQQCB(R2)  REMOVE ERB
         OI    LCBERBST,LCBDLNKN        MAKE LCB POSTABLE
OKERB    EQU   *
         LH    R1,LCBTTCIN              OFFSET TO LINE ENTRY
         TM    LCBSTAT2,LCBDIAL         IS THIS DIAL TERMINAL  @OZ30648
         BZ    NODIAL                   BRANCH NO              @OZ30648
         LH    R1,LCBLNENT              GET LINE INDEX         @OZ30648
NODIAL   EQU   *                                               @OZ30648
         N     R1,AVTCLRHI              CLEAR HIGH ORDER HALF
         L     R15,AVTRNMPT             ADDRESS OF TNT
         BALR  R14,R15                  LINK TO TNT
*
         USING IEDQTRM,R1
         SR    R2,R2
         IC    R2,TRMCHCIN              CHARACTERISTICS INDEX
         BCTR  R2,0                     MINUS 1
         MH    R2,AVTDCTLN              TIMES DCT ENTRY SIZE   @G36XRYP
         AL    R2,AVTCSTCS
         TM    1(R2),X'20'              ATTENTION FEATURE
         BZ    NOATTN                   BRANCH ON NO ATTENTION   S21903
         TM    LCBTSOB,LCBSATRD         SENDING TRANSLATE ERR MSG
         BO    NOATTN1                  BRANCH ON YES
*
         MVC   LCBCSWSV(7),LCBCSW       SAVE CSW
         MVC   LCBCPA(8),PREPCCW        SET UP PREPARE CCW
         MVI   LCBTPCD,X'10'            SET TP OP CODE
         MVI   LCBTPCD+11,OPNCHK        SET OPEN LINE CHECK FLAG
         OI    LCBTSOB,LCBTSBUF         SET SESSION FOR        @SA75753
*                                       LINE END               @SA75753
         LA    R1,LCBCPA               CHANNEL PROGRAM ADDRESS
         STCM  R1,ADDR,LCBCPA+ONE       SET ADDR. OF FIXED CORE  X02004
*                                       IN CCW FOR SIO APPENDAGE X02004
         ST    R1,LCBSTART-1            SET START ADDRESS
         LA    R1,LCBFLAG1              IOB ADDRESS
         EXCPVR (1),SUBSYS                                       X01004
*
         L     RDCB,LCBDCBPT            DCB ADDRESS
         USING IHADCB,RDCB
         SR    RWORK,RWORK              CLEAR REGISTER
         IC    RWORK,LCBUCBX            PICK UP UCB INDEX
         SLL   RWORK,2                  MULTIPLY BY FOUR
         L     RDEB,DCBDEBAD            DEB ADDRESS
HIO1     EQU   *                                               @ZA05011
         L     R1,DEBUCBAD-DEBNMSUB(RWORK,RDEB)  UCB ADDRESS
         MVI   LCBECBCC,HIOCC           SET HALT I/O COMPLETION  Y02027
         IOHALT (1)
         LTR   R15,R15                  HIO COMPLETE?          @ZA05011
         BZ    DSPDISP                  BRANCH YES             @ZA05011
         OC    LCBCSW,LCBCSW            CSW STORED             @ZA05011
         BZ    HIO1                     BRANCH NO- REISSUE HIO @ZA05011
         B     DSPDISP                  RETURN TO DISPATCHER.    Y01948
ITSERB   EQU   *
         SH    RLCB,ERBOF               BACK UP TO LCB
         NI    LCBTSOB,X'FF'-LCBPREP    TURN OFF PREPARE BIT
         TM    LCBINSRC+2,DELAYBIT      IS LCB IN THE DELAY QUEUE?
         BZ    NOTINQ                   BRANCH ON NO
         LR    R1,RLCB                  INDICATE WANT LCB REMOVED
         L     R15,AVTHG02              TIME DELAY REMOVAL ROUTINE
         BALR  R14,R15                  REMOVE LCB FROM QUEUE
         LA    RWORK,LCBRSKEY           LINK RECEIVE SCHED INTO STCB CH
         LR    R1,RWORK
         BAL   R14,DSPPRIOR             DISPATCHER WILL DO LINK
NOTINQ   EQU   *
         L     RSCB,LCBSCBA-1           ADDRESS OF SCB
         L     RDCB,LCBDCBPT            ADDRESS OF DCB
         CLI   LCBECBCC,X48             WAS IT ENDED BY IOHALT?
         BE    LCBTOLCB                 BRANCH ON YES
         TM    LCBTSOB,LCB2741N         IS IT A 2741?
         BO    IS2741                   BRANCH IF 2741           S21903
*
         CLC   LCBTPCD(3),LCBTPCD+1     1050 MONITOR SEQUENCE
         BE    POSTATTN                 BRANCH YES
*
         B     OKERB                    CHECK FOR ATTN
*
IS2741   EQU   *
         L     R5,LCBCSW-1              INTERRUPTED CCW+8
         LA    R5,0(,R5)
         SH    R5,EIGHT                 BACK UP TO INTERRUPTED CCW
         CLI   0(R5),PREPARE            DID WE INTERRUPT ON A PREPARE?
         BE    SETD                     BRANCH ON YES
         CLC   6(2,R5),AVTFZERO         WAS CIRCLE D SENT?
         BNE   POSTATTN                 BRANCH ON NO
SETD     EQU   *                                                 Y01948
         OI    LCBTSOB,LCBCIRCD         INDICATE CIRCLE D SENT
POSTATTN EQU   *
         L     RWORK,AVTTSOPT          GET ADDR OF TSINPUT       S22029
         TM    LCBSENS0,IR+TO           HAS TERMINAL HUNG UP?
         BNZ   HANGUP                   BRANCH ON YES
         OI    SCBERR3,SCBATTN                                      TSO
         L     R5,TSIATTEN             GET ADDR OF ATTEN RTN     S22029
POSTLCB  EQU   *
         LA    R9,PRIFSPCI-1            SET PRIORITY
POST    EQU   *
         LR    R1,RLCB                  LCB TO BE POSTED
         ST    R5,0(R1)                 SET QCB ADDRESS
         STC   R9,4(R1)                 SET ELEMENT PRIORITY
         BAL   R14,DSPPOST              EXIT TO DISPATCHER     @G36XRYP
*
HANGUP   EQU   *
         L     R5,TSIHANG               ADDRESS OF HANG UP ROUTINE
         B     POSTLCB                   POST LCB TO HANGUP
*
LCBTOLCB EQU   *
         LR    R5,RLCB                  ADDRESS OF QCB
         LA    R9,PRILNFRE              PRIORITY
         B     POST                     BRANCH TO POST           S21903
NOATTN1  EQU   *
         L     RSCB,LCBSCBA-1           SCB ADDRESS
         NI    SCBERR2,X'FF'-SCBCODER   RESET TRANSLATE ERROR
         MVI   SCBMRFSD,AVTEZERO        RESET TABLE COUNT
         NI    LCBTSOB,X'FF'-LCBSATRD-LCBTSBUF RESET POST FLAG
NOATTN   EQU   *
         TM    LCBSTAT2,LCBMSGNN        MESSAGE GEN
         BZ    NOTMGN                   BRANCH NO
*
         LA    R9,PRIRCQCB              GET PRIORITY
         L     R5,AVTMSGS-1
         L     R5,0(,R5)                MH ADDRESS
         LA    RLCB,LCBERB              ERB TO BE POSTED
         B     POST                     BRANCH TO POST           S21903
*
NOTMGN   EQU   *
         TM    LCBSTAT1,LCBRECVN        RECEIVE STATE
         BO    NOCLEAR                  BRANCH IF IT IS RECEIVE. Y01948
         USING IEDQPRF,R6
         XC    PRFSIZE,PRFSIZE          SIZE = 0
NOCLEAR  EQU   *
         OI    SCBERR4,SCBTXTTN         SET TEXT ERROR
         LA    R9,PRIMHBFR-1            SET PRIORITY
         L     R5,LCBDCBPT              DCB ADDRESS
         L     R5,DCBMH-IHADCB-1(R5)    MH ADDRESS
         LR    RLCB,R6                  POST BUFFER
         B     POST                     BRANCH TO POST           S21903
*
         EJECT
EIGHT    DC    H'8'                     CONSTANT                 S21903
ERBOF    DC    AL2(LCBERBFS)            CONSTANT                 S21903
PREPCCW  DC    X'06',AL3(*),X'200000BC'   PREPARE CCW
ONE      EQU   1                        CONSTANT - OFFSET        X02004
ADDR     EQU   7                        MASK TO STCM ADDRESSES   X02004
HIOCC    EQU   X'48'                    HALT I/O COMPLETION CODE Y02027
X08      EQU   8
         EJECT
         TTRMD
         TPRFD
         EJECT
         TQCBD
         TDISPD
         TLCBD
         TDEBD
         DCBD  DSORG=TX
         TPRIOR
         TTSID
         TSCBD
         TAVTD
         END
