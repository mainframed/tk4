         TITLE 'IEDAYL - TIOC LOGON'
IEDAYL   CSECT
         SPACE 3
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
*        TCAM VERSION 10.0 CHANGES                             @G36XRYP
*A090000,488700,633600,715500,723600,900000,984600             @G36XRYP
*C067500-068400,708300,713700,988200                           @G36XRYP
*D489600-490500,634500-639900,648000-650700                    @G36XRYP
*                                                              @G36XRYP
*A045000,260100,270900,486000,487800,511200,527400,595800      @Y17XAYP
*A597600,606600,614700,693900,894600,901800,909000             @Y17XAYP
*C720900                                                       @Y17XAYP
*D151200,167400,527700-528000,552600-553500                    @Y17XAYP
*
*F
*                                                              @OY14092
*        RA0034 CHANGES                                        @OY14092
*                                                              @OY14092
*C203400,662400                                                @OY14092
*D236700,237600                                                @OZ24216
*A663300                                                       @OZ24216
*                                                                     *
*                                                                     *
*    NAME - IEDAYL, TIOC LOGON                                        *
*                                                                     *
*    POST RELEASE VS2-1.6 CHANGES
*0000158121,169800,492081-492085                               @YA13121
*0000169800,208101-208110                                      @YA12438
*C169800                                                       @YA03653
*A158310,489010,489020,492010-492080                           @YA03653
*C169800,246700                                                 YA00536
*D001300,536500-537300,537700                                   YA00536
*D537100                                                        YA00677
*A320020-320060                                                 YA00677
*    RELEASE 21 DELETIONS/CHANGES                                     *
*D604200-606900                                                 SA61123
*D235000                                                        SA56309
*0690169800                                                      S22028
*0690618700                                                      S22029
*0690023000,035000,058000,070000,071000,072000,120000,148000,    S22029
*0690155000,169800,180000,185000-187000,187060,187080,187600,    S22029
*0690187900,188000-195000,200000,215000-222000,229000-234000,    S22029
*D337000                                                        SA59527
*0690241000,246700,254000,263000,265000,270000,274000,290000,    S22029
*0690292000,296000,301000,304000,306000,311000-313000,316000,    S22029
*0690321000,323000,350000,353000,355000,358000,360000,361500-    S22029
*0690431800,442000-443000,462000,464000,475000-476000,480000-    S22029
*0690484000,491000,496000,497000-506000,517000-518000,525800-    S22029
*0690526000,527000-528000,537000,539000-540000,575000,616000,    S22029
*0690618000,660000-681000                                        S22029
*0690041000,168000,169800,187900,504000,505000,506000,575000,    S22025
*0690022000-023000,169800,537000,573000                          Y01018
*0690582000,584500-599500                                        S22025
*0690169800,575000                                               S22025
*0690169800                                                      A44839
*    RELEASE 20 DELETIONS/CHANGES                                     *
*                                                                 M3293
*                                                                 M0716
*0000009500,014000,029000,052000-058000,082000-084000,246400-    TS0030
*0000246600,286000,385000,393000,441000,641000                   TS0030
*0000                                                             M0019
*                                                                 M2655
*                                                                 M2662
*                                                                 M4681
*                                                                 M3611
*                                                                 M4659
*                                                                 M4660
***************************************************************@Y17XAYP
*                                                              @Y17XAYP
* MODULE NAME = IEDAYL (TCAM,TSO)                              @Y17XAYP
*                                                              @Y17XAYP
* DESCRIPTIVE NAME = TIME SHARING LOGON ROUTINE                @Y17XAYP
*                                                              @Y17XAYP
* COPYRIGHT = NONE                                             @Y17XAYP
*                                                              @Y17XAYP
* STATUS = VERSION 10.0                                        @G36XRYP
*                                                                     *
* FUNCTION -                                                          *
*    START CREATION OF A NEW MEMORY FOR THIS USER, IF HE       @G36XRYP
*    REQUESTS A 'LOGON' AND CONDITIONS ARE SET FOR A           @G36XRYP
*    SUCCESSFUL LOGON                                          @G36XRYP
*    PROMPT USER WHEN HE ENTERS INVALID LOGON REQUESTS                *
*    NOTIFY USER IF IT IS IMPOSSIBLE TO LOGON                         *
*                                                                     *
* ENTRY POINTS -                                                      *
*         IEDAYL, TO IDENTIFY VALID TSO LOGON ATTEMPTS                *
*                                                                     *
* INPUT -                                                             *
*    REGISTER  1 HAS PARAMETER LIST ADDRESS                           *
*    REGISTER  3 HAS SCB ADDRESS                                      *
*    REGISTER  4 HAS LCB ADDRESS                                      *
*    REGISTER  6 HAS TCAM BUFFER ADDRESS                              *
*    REGISTER  9 HAS AVT ADDRESS                                      *
*    REGISTER 12 HAS BASE ADDRESS                                     *
*    REGISTER 13 HAS AVTSAVE3 ADDRESS                                 *
*                                                                     *
* OUTPUT -                                                            *
*    NONE                                                             *
*                                                                     *
* EXTERNAL REFERENCES -                                               *
*         IEDQTNT, TERMINAL ENTRY ADDRESS FINDER, VIA BRANCH ENTRY AT *
*         AVTRNMPT                                                    *
*         QTIP ENTRY 10 VIA SVC 101                                   *
*         IEDQHG02, TIME DELAY QUEUE REMOVAL ROUTINE, VIA BRANCH ENT- *
*         RY AT AVTHG02                                               *
*         MEMORY CREATE, VIA SVC 34 AND CIB                    @G36XRYP
*                                                                     *
* EXITS, NORMAL -                                                     *
*         RETURN TO TS MH VIA TCAM RETURN INTERFACE  ROUTINE (IEDQLM) *
*         BRANCH TO ALTERNATE MH VIA LOGON MACRO EXIT ADDRESS         *
*                                                                     *
* EXITS, ERROR -                                                      *
*         NONE                                                        *
*                                                                     *
* TABLES/WORK AREAS -                                                 *
*    CONTROL  BLOCKS AND PARAMETER  LISTS ARE DESCRIBED IN THE DSECTS *
*    AT THE END OF THE LISTING                                        *
*    A TABLE OF 'OGON' IN LOWER AND IN UPPERCASE CALLED 'LOGCON'      *
*    A TABLE OF 'CAMON' IN LOWER AND IN UPPERCASE CALLED 'TCAMON'     *
*    REGISTERS ARE SAVED IN ADDRESS PLUS 72 PASSED IN REGISTER 13     *
*    AVTDOUBLE IS USED AS A WORK AREA                                 *
*                                                                     *
* ATTRIBUTES -                                                        *
*    ENABLED, PROBLEM PROGRAM MODE, REFRESHABLE, REUSABLE             *
*                                                                     *
* CHARACTER CODE DEPENDENCY -                                         *
*    THE OPERATION OF THIS MODULE IS DEPENDENT UPON CERTAIN  INTERNAL *
*    REPRESENTATIONS OF ITS CHARACTER SET...IT IS ASSEMBLED IN EBCDIC *
*    AND MUST BE SPECIALLY MODIFIED IF RE-ASSEMBLY IN ANOTHER CHARAC- *
*    TER SET IS DESIRED....THE FIELDS REQUIRING THIS MODIFICATION ARE *
*    FOUND UNDER THE HEADING 'MASKS AND CHARACTERS' (HERE, LOWERL AND *
*    LOWERT) AND IN THE CONSTANTS LOGCON (THE LOWERCASE 'O', 'G', AND *
*    'N') AND TCAMCON (THE LOWERCASE 'C', 'A', 'M', 'O', AND 'N')
*                                                                     *
* NOTES -                                                             *
*    REGISTER EQUATES START WITH 'R'                                  *
*    LENGTH AND DISPLACEMENT EQUATES START WITH 'E'                   *
*    CERTAIN CONSTANTS START WITH 'KON'                               *
*                                                                     *
*                                                                     *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         EJECT
********
******** REGISTER EQUATES FOR 'LOGON' SCAN ONLY
********
RBUF1    EQU   2                        SAVES FIRST B.U. ADDR DURING
*                                       'LOGON' SCAN
RDATA1   EQU   3                        SAVES FIRST DATA ADDR DURING
*                                       'LOGON' SCAN
RBUSZ1   EQU   4                        SAVES FIRST B.U. SIZE DURING
*                                       'LOGON' SCAN
RLOGCHAR EQU   5                        ADDR OF CHAR IN 'OGON'
RHDR     EQU   7                        SIZE OF FIRST B.U.'S HDR
RHERE    EQU   8                        RET ADDR AFTER GETTING NEW B.U.
RAT      EQU   10                       ADDR OF CHAR BEING SCANNED
RCNT     EQU   11                       NUMBER OF CHARS SCANNED
RBUSZ    EQU   14                       SIZE OF BASIC UNIT
RMSGSZ   EQU   15                       LENGTH OF DATA TO SCAN
         SPACE 3
********
******** REGISTER EQUATES FOR STRING SCAN AND/OR REST OF ROUTINE
********
R0       EQU   0                        WORK REGISTER           SA56309
R1       EQU   1                        WORK REG
RTSCVT   EQU   2                        TSCVT ADDRESS
R2       EQU   2                        4K BASE ADDR INCREMENT   S22029
RSCB     EQU   3                        SCB ADDRESS
RLCB     EQU   4                        LCB ADDRESS
RDCB     EQU   5                        DCB ADDRESS
RWORK5   EQU   5                        WORK REGISTER            Y06327
RBUF     EQU   6                        BUFFER ADDRESS
RQCB     EQU   7                        QCB ADDRESS
RTERM    EQU   8                        TERMINAL TABLE ENTRY ADDR
RAVT     EQU   9                        AVT ADDRESS
RCH      EQU   10                       WORK REGISTER
RIS      EQU   11                       WORK REGISTER
RBASE    EQU   12                       BASE ADDRESS/ENTRY ADDRESS
R13      EQU   13                       AVT SAVE AREA ADDR
R14      EQU   14                       WORK REGISTER
R15      EQU   15                       WORK REG, TJID PASSED FROM
*                                       QTIP, AND RETURN CODE
         SPACE 3
********
******** MASKS AND CHARACTERS
********
CATTEN   EQU   X'20'                    TERM HAS ATTENTION FTR
TSMH     EQU   X'01'                    DENOTES A TS MH
DEBOPATB EQU   12                       OFFSET TO OPATB FLD IN DEB
DEBOUT   EQU   X'03'                    DEB SPECIFIES OUTPUT OR INOUT
CINHIBIT EQU   X'80'                    READ INHIBIT CCW CAN BE USED
C3270    EQU   X'04'                    3270 TERMINAL (TCT)      S22028
QCBLOGCT EQU   X'C0'                    TESTS LOGON'S RETRY COUNT
LOWERL   EQU   X'93'                    LOWERCASE 'L' - 'LOGON' TEST
UPPERL   EQU   C'L'                     UPPERCASE 'L' - 'LOGON' TEST
LOWERT   EQU   X'A3'                    LOWERCASE 'T'            S22029
UPPERT   EQU   C'T'                     UPPERCASE 'T'            S22029
BLANK    EQU   C' '                     BLANKS OUT GARBAGE IN BFR
SCANEND  EQU   C'X'                     STOPS STRING SCAN        S22029
INBLOCK  EQU   X'01'                    INBLOCK                  S22029
LOGON    EQU   10                       CALLS QTIP FOR LOGON DUTIES
NOT      EQU   X'FF'                    ZEROS VARIOUS BITS
E1       EQU   1                        GIVES A VALUE OF 1
E2       EQU   2                        GIVES A VALUE OF 2
SETINHB  EQU   X'10'                    3705 MODE SET FLAG
STMONITR EQU   X'02'                    3705 MODE SET FLAG      YS06327
E3       EQU   3                        GIVES A VALUE OF 3       S22029
E8       EQU   8                        GIVES A VALUE OF 8     @YA03653
E32      EQU   32                       GIVES A VALUE OF 32      S22029
ELEVEN   EQU   11                       DECIMAL ELEVEN           S22029
E12      EQU   12                       SCAN LENGTH DEFAULT     SA56309
LCBOFFS  EQU   36                       OFFSET TO RLCB IN S.A.  YA00677
AUTOPOLL EQU   X'01'                    AUTOPOLL TEST MASK     @YA13121
         SPACE 3
********
******** ESTABLISH ADDRESSABILITY AND INITIALIZE REGISTERS
********
         USING IEDAYL,RBASE             USE ENTRY ADDR AS BASE ADDR
         USING IEDNTRM,RTERM            TRM ADDRESSABILITY     @YM08512
         USING IEDQSCB,RSCB             USE RSCB AS SCB BASE REG
         USING IEDQLCB,RLCB             USE RLCB AS LCB BASE REG
         L     RDCB,LCBDCBPT            GET DCB ADDR
         USING IHADCB,RDCB
         USING IEDQPRF,RBUF             USE RBUF AS BFR BASE REG
         USING IEDQAVTD,RAVT            USE RAVT AS AVT BASE REG
         XC    AVTDOUBL,AVTDOUBL        INITIALIZE WORK AREA     S22029
         LR    R2,R1                    SAVE LOGON PARM LIST    SA56309
         USING LOGPARM,R1                                        S22029
         L     R1,LOGPMH-1              PICKUP ADDR OF CURRENT   S22029
*                                       MH PROCESSING THIS MSG
         DROP  R1                                                S22029
         USING CURMHQCB,R1                                       S22029
         B     PASTID                   GET PAST MODULE ID
IEDAYL   IEDHJN ,                                              @OY14092
PASTID   DS    0H
         SPACE 3
* * * * *  SEE IF CONDITIONS ARE SET FOR A SUCCESSFUL LOGON * * * * * *
         SPACE 3
********
******** IF TERMINAL IS IN EXTENDED LOCK MODE, DON'T TOUCH ANYTHING
********
         TM    SCBSTATE,SCBLCK1N        TEST FOR LOCK MODE      SA61128
         BO    BYEBYE                   IF IN LOCK MODE, EXIT   SA61128
         SPACE 3                                                SA61128
********
******** CHECK FOR HEADER BUFFER
********
         TM    PRFSTAT1,PRFNHDRN        IS IT A HEADER BFR       S22029
         BO    CONCAN                   NO, BRANCH               S22029
         SPACE 3
********
******** SEE IF TSO SUPPORTS THIS TERMINAL
********
         TM    LCBSTAT2,LCBDIAL         IS IT A DIAL TERMINAL
         BZ    NOTDIAL                  NO - FOLLOWING CODE IS INVALID
         NC    LCBLNENT(E2),LCBLNENT    IS INDEX VALID (NON-ZERO)
         BZ    CONCAN                   NO - GET RID OF USER     S22029
         SPACE 3
********
******** PUT TNT INDEX IN LCB AND BUFFER PREFIX
********
********
         MVC   AVTDOUBL+4(E2),LCBLNENT  SAVE CONNECT INDEX       S22029
         B     CONNECT                  SKIP NON-DIAL CODE       S22029
NOTDIAL  EQU   *                                                 S22029
         MVC   AVTDOUBL+4(E2),LCBTTCIN  SAVE CONNECT INDEX       S22029
CONNECT  EQU   *                                                 S22029
         SPACE 3
******** FIND PROPER QCB VIA LCBTTCIN (IN REG 1) AND TNT CODE
********
         LR    RIS,R1                   SAVE CURRENT MH'S QCB    S22029
         LH    R1,AVTDOUBL+4            PICK UP CONNECT INDEX    S22029
         N     R1,AVTCLRHI              CLEAR HI-ORDER HALFWORD
         LTR   R1,R1                    SOURCE DETERMINATION     S99228
*                                       ERROR                    S99228
         BZ    BADCIN                   YES, BRANCH              S99228
         L     R15,AVTRNMPT             GET ADDR OF TNT
         BALR  R14,R15                  BR TO CODE IN TNT TO GET ADDR
*                                       OF TERMINAL TBL ENT IN REG 1
         LR    RTERM,R1                 SAVE TERMINAL TBL ENT ADDR
         LA    R1,TRMPRFSZ              PREFIX SIZE            @YM08512
         SR    RTERM,R1                 SET TRM PREFIX BASE    @YM08512
         LR    R1,RIS                   RESTORE CURRENT MH'S QCB S22029
         L     RQCB,TRMDESTQ-1          GET QCB ADDR FROM TERM ENTRY
         LA    RQCB,0(0,RQCB)           ZERO HI-ORDER BYTE       S22029
         USING IEDQQCB,RQCB             USE RQCB AS QCB BASE ADDR
         SPACE 3                                                 S22029
********                                                       @YA12438
******** CHECK FOR STOPPED TERMINAL                            @YA12438
********                                                       @YA12438
         TM    TRMSTATE,TRMPREF         IS THERE A PREFIX      @Y17XAYP
         BZ    OXOCNI                   TERM 0X EP             @Y17XAYP
         TM    TRMBYTE2,TRMRSACT        IS TERMINAL ACTIVE     @Y17XAYP
         BNO   CONCAN                   NO, IGNORE INPUT       @Y17XAYP
         TM    TRMBYTE0,TRMSNA          IS THIS SNA TERMINAL   @Y17XAYP
         BNO   NOTSNA                   NO, BRANCH NOT SNA     @Y17XAYP
         ICM   R14,7,TRMSIBPT           GET ADDR OF SIB        @Y17XAYP
         USING IEDSIBD,R14              SIB ADDRESSABILITY     @Y17XAYP
         TM    SIBSESSN,SIBLGOFF        SNA LOGOFF REQUESTED   @Y17XAYP
         BO    CONCAN                   SET DEST TO BFR RET    @Y17XAYP
NOTSNA   EQU   *                                               @Y17XAYP
         DROP  R14                                             @Y17XAYP
         TM    TRMPRE1,TRMOCNI          IS STOP TERM,LINE,GP   @Y17XAYP
*                                       IN PROGRESS            @Y17XAYP
         BNO   CKOLTR                   NO, CONTINUE LOGON     @YM08526
         TM    QCBFLAG,QCBTSSES         TERM IN TSO SESSION    @YM08526
         BNO   CONCAN                   NO, DON'T ALLOW LOGON  @YM08526
         TM    TRMPRE,TRMSTMM           THIS STOPLINE I        @YM08526
         BO    CONCAN                   YES, END SESSION       @Y17XAYP
         B     CKOLTR                   NO, CONTINUE LOGON     @YM08526
         SPACE 3                                               @YA12438
OXOCNI   EQU   *                                               @YM08526
********
******** SEE IF THIS LOGON SHOULD BE INTERRUPTED
********
CKOLTR   EQU   *                                               @YM08526
         TM    SCBERR2,SCBOLTR          INVALID ONLINE TEST REQ
         BO    CONCAN                   YES - IGNORE THIS BFR    S22029
         TM    SCBERR2,SCBCODER         ERROR IN MSG TRANSLATION S22029
         BO    CHKMH                    YES - TELL USER SO       S22029
         TM    SCBERR2,SCBSOHE          CHECK 0 LENGTH BFR       S22028
         BO    CONCAN                   YES, NO DATA             S22028
         SPACE 3
********
******** SEE IF ANY DATA IN BUFFER
********
         CLC   PRFSIZE,AVTFZERO         IS DATA LENGTH 0
         BE    CONCAN                   YES - NO DATA TO PROCESS S22029
         SPACE 3                                                 A44839
********
******** CHECK FOR TSC ABEND
********
         TM    AVTBIT3,AVTTSAB          DID TSO ABEND            A44839
         BO    CONCAN                   YES - NO SYS TO LOGON TO S22029
         SPACE 3
********
******** CHECK FOR TSO SESSION ALREADY BEGUN
********
OXTERM   EQU   *                                               @YA12438
         TM    QCBFLAG,QCBTSSES         TSO SESSION ACTIVE       S22029
         BO    SETTSIN                  YES - NO NEED TO LOGON   S22029
         TM    TRMSTATE,TRMPREF         IS THERE A PREFIX      @Y17XAYP
         BNO   NOTLOGD                  NO,CAN'T BE SNA TERM   @Y17XAYP
         TM    TRMBYTE0,TRMSNA          IS THIS A SNA TERM     @Y17XAYP
         BNO   NOTLOGD                  NO,CAN'T BE SNA LOGON  @Y17XAYP
*                                       DATA                   @Y17XAYP
         LA    R14,PRF2LEN              GET LENGTH OF NEGATIVE @Y17XAYP
*                                       PREFIX                 @Y17XAYP
         LR    RIS,RBUF                 GET BUFFER ADDRESS     @Y17XAYP
         SR    RIS,R14                  BACK UP TO NEG PREFIX  @Y17XAYP
         USING IEDPF1,RIS               BFR PREFIX ADDR        @Y17XAYP
         TM    PRF1FLG1,PRF1LOGD        IS THIS LOGON DATA     @Y17XAYP
         BZ    NOTLOGD                  NO, SCAN FOR LOGON     @Y17XAYP
         TM    CURMHFG2,CURMHTS         IS THIS TSO SNA LOGON  @Y17XAYP
         BO    LOGONFND                 YES, PROCESS LOGON     @Y17XAYP
         B     TCAMFND                  NO,PROCESS FOR TCAM MH @Y17XAYP
         SPACE 3
NOTLOGD  EQU   *                                               @Y17XAYP
********
******** SAVE REGISTERS FOR USE AFTER LOGON SCAN
********
         SAVE  (14,12)                  SAVE REGS FOR LATER USE
         SPACE 3
* * * * * * * * * *  PREPARE FOR SCAN FOR 'LOGON' * * * * * * * * * * *
         SPACE 3
********
******** POINT PAST BUFFER HEADER TO DATA WITHIN BUFFER
********
         LR    RAT,RBUF                 GET BFR ADDR FOR SCAN POINTER
         SR    RHDR,RHDR                CLEAR FOR INSERTION     SA56309
         IC    RHDR,LCBISZE            GET NUMBER OF RESERVES   SA56309
         LA    RAT,AVTUMALN+AVTHDRSZ(RHDR,RAT) ADD SIZE OF TCAM SA56309
*                                       HEADER                  SA56309
         LA    RHDR,AVTHDRSZ            GET TCAM HDR SIZE (FIRST B.U.)
         SPACE 3
********
******** GET SIZE OF DATA TO SCAN (IN BASIC UNIT AND IN WHOLE BUFFER)
********
         LH    RBUSZ,AVTKEYLE           GET SIZE OF BASIC UNIT
         SR    RBUSZ,RHDR               GET DATA SIZE ONLY (SUB FIRST
*                                       B.U.'S HDR SIZE)
         LR    RBUSZ1,RBUSZ             SAVE FIRST B.U. SIZE
         LH    RMSGSZ,PRFSIZE           GET LENGTH OF DATA TO SCAN
         SR    RMSGSZ,RHDR              GET DATA SIZE ONLY (SUB  M0019
*                                       FIRST B.U.'S HDR SIZE)   M0019
         BNP   NOSTRING                 EXIT IF DATA LNG IS 0   YA00536
         SPACE 3                                                SA56309
********
******** DETERMINE HOW FAR TO SCAN
********
         SR    R0,R0                    CLEAR FOR INSERTION     SA56309
         IC    R0,E2(R2)                GET SCAN VALUE FROM     SA56309
*                                       LOGON MACRO             SA56309
         LTR   R0,R0                    WAS A VALUE SPECIFIED   SA56309
         BNZ   SCANVAL                  YES, SKIP DEFAULT       SA56309
         LA    R0,E12                   NO, GIVE DEFAULT OF 12  SA56309
SCANVAL  EQU   *
         SPACE 3
********
******** PREPARE SCAN AIDS
********
         SR    RCNT,RCNT                ZERO REG FOR FUTURE CNTING
         LR    RBUF1,RBUF               SAVE BFR ADDR FOR LATER USE
         LR    RDATA1,RAT               SAVE DATA ADDR FOR LATER USE
         SPACE 3
* * * * * * * * * * * * *  SCAN FOR 'LOGON' * * * * * * * * * * * * * *
         SPACE 3
********
******** SCAN FOR LOWER AND UPPERCASE 'L'
********
LSCAN    EQU   *
         MVI   AVTDOUBL+6,UPPERL        INDICATE 'LOGON' SCAN    S22029
         LA    RLOGCHAR,LOGCON          POINT TO 'OGON'          S22029
         CLI   0(RAT),LOWERL            DO WE SEE A LOWERCASE 'L' HERE
         BE    CONTSCAN                 YES - SEE IF 'OGON'      S22029
*                                       FOLLOWS
         CLI   0(RAT),UPPERL            DO WE SEE AN UPPERCASE 'L' HERE
         BE    CONTSCAN                 YES - SEE IF 'OGON'      S22029
*                                       FOLLOWS
         SPACE 3                                                 S22029
********
******** SCAN FOR LOWER AND UPPERCASE 'T'
********
TSCAN    EQU   *                                                 S22029
         MVI   AVTDOUBL+6,UPPERT        INDICATE 'TCAMON' SCAN   S22029
         LA    RLOGCHAR,TCAMCON         POINT TO 'CAMON'         S22029
         CLI   0(RAT),LOWERT            LOWERCASE 'T' HERE       S22029
         BE    CONTSCAN                 YES - SEE IF 'CAMON'     S22029
*                                       FOLLOWS
         CLI   0(RAT),UPPERT            UPPERCASE 'T' HERE       S22029
         BE    CONTSCAN                 YES - SEE IF 'CAMON'     S22029
*                                       FOLLOWS
         SPACE 3
********
******** CHECK FOR HAVING SCANNED ALL THE DATA OR A WHOLE BASIC UNIT
********
         LA    RCNT,E1(0,RCNT)          ADD 1 TO CHAR COUNT
         LA    RAT,E1(0,RAT)            POINT TO NEXT CHAR IN BFR
STAYHERE EQU   *
         CR    RCNT,R0                  SCAN LIMIT REACHED?     SA56309
         BE    NOSTRING                 YES, STRING NOT FOUND   SA56309
         CR    RCNT,RMSGSZ              HAS ALL THE DATA BEEN SCANNED
         BE    NOSTRING                 YES - STRING NOT FOUND   S22029
         CR    RCNT,RBUSZ               HAS THE WHOLE B.U. BEEN SCANNED
         BL    LSCAN                    NO - CHECK NEXT CHAR
         LA    RHERE,LSCAN              GET PROPER RET ADDR
         SPACE 3
********
******** GET NEXT BASIC UNIT SINCE ALL THE DATA HASN'T BEEN SCANNED
********
GETBU    EQU   *
         L     RAT,PRFTIC               GET ADDR OF NEXT B.U.
         LR    RBUF,RAT                 GET BFR BASE REG FOR NEXT SCAN
         LA    RAT,AVTUMALN(0,RAT)      POINT PAST PREFIX TO DATA
         AH    RBUSZ,AVTKEYLE           ADD SIZE OF SUBSEQUENT   TS0030
*                                       B.U.S                    TS0030
         BR    RHERE                    CHECK NEXT CHAR
         SPACE 3
********
******** SCAN FOR LOWER AND UPPERCASE 'OGON' OR 'CAMON'
********
CONTSCAN EQU   *                                                 S22029
         LA    RCNT,E1(0,RCNT)          ADD 1 TO CHAR COUNT
         LA    RAT,E1(0,RAT)            POINT TO NEXT CHAR IN BFR
         CR    RCNT,RMSGSZ              HAS ALL THE DATA BEEN SCANNED
         BE    NOSTRING                 YES - STRING NOT FOUND   S22029
         CR    RCNT,RBUSZ               HAS THE WHOLE B.U. BEEN SCANNED
         BL    COMPARE                  NO - CHECK NEXT CHAR
         BAL   RHERE,GETBU              YES - TRY NEXT B.U.
COMPARE  EQU   *
         CLC   0(E1,RAT),0(RLOGCHAR)    IS THIS CHAR IN STRING   S22029
*                                       (LOWERCASE)
         BE    POINTUP                  YES - POINT TO UPPERCASE CHAR
         LA    RLOGCHAR,E1(0,RLOGCHAR)  POINT TO UPPERCASE CHAR
         CLC   0(E1,RAT),0(RLOGCHAR)    IS THIS CHAR IN STRING   S22029
*                                       (UPPERCASE)
         BE    POINTNEX                 YES - POINT TO NEXT CHAR
         B     STAYHERE                 SEE IF END OF SCAN
POINTUP  EQU   *
         LA    RLOGCHAR,E1(0,RLOGCHAR)  GET UPPERCASE CHAR
POINTNEX EQU   *
         LA    RLOGCHAR,E1(0,RLOGCHAR)  GET NEXT CHAR IN STRING  S22029
*                                      (LOWERCASE)
         CLI   0(RLOGCHAR),SCANEND      HAS STRING BEEN SCANNED  S22029
         BNE   CONTSCAN                 NO - CONTINUE SCAN       S22029
         LA    RCNT,E1(0,RCNT)          ADD 1 FOR LAST CHAR SCANNED
         SPACE 3
* * * * * * BLANK ANY GARBAGE PRECEDING 'LOGON' OR 'TCAMON' * * * * * *
         SPACE 3
********
******** INITIALIZE AS NECESSARY AND SEE IF THERE IS ANYTHING TO BLANK
********
         L     RLCB,LCBOFFS(,R13)       RELOAD RLCB FROM S.A.   YA00677
         SR    RAT,RAT                  CLEAR FOR INSERT CHAR   YA00677
         IC    RAT,LCBISZE              GET NO. RESERVES        YA00677
         AR    RCNT,RAT                 ADD TO COUNT            YA00677
         SR    RDATA1,RAT               SUBTR FR START ADDR     YA00677
         CLI   AVTDOUBL+6,UPPERT        WAS 'TCAMON' FOUND       S22029
         BNE   BLANKLOG                 NO - BLANK 'LOGON'       S22029
         SH    RCNT,KON6                DON'T BLANK 'TCAMON'     S22029
         B     BLANKMSG                 GO BLANK OUT             S22029
BLANKLOG EQU   *                                                 S22029
         SH    RCNT,KON5                DON'T BLANK 'LOGON'      S22029
BLANKMSG EQU   *                                                 S22029
         LTR   RCNT,RCNT                ANY CHARS TO BLANK
         BZ    STRNGFND                 NO - PROCESS THIS STRING S22029
         LR    RBUF,RBUF1               GET ADDR OF FIRST B.U.
         LR    RAT,RDATA1               GET ADDR OF DATA IN FIRST B.U.
         LR    RBUSZ,RBUSZ1             GET SIZE OF FIRST B.U.
         SPACE 3
********
******** BLANK WHOLE BASIC UNIT
********
BLANKHOL EQU   *
         CR    RCNT,RBUSZ               NO. CHARS SCANNED GT ONE B.U.
         BNH   BLANKHAF                 NO - GO BLNK RCNT NO. OF CHARS
         MVI   0(RAT),BLANK             PUT A BLANK IN FIRST BYTE
         SR    RCNT,RBUSZ               NO. CHARS LEFT TO BLANK SA59527
         SH    RBUSZ,AVTHA2             SUB 1 FOR MVI AND 1 FOR EX
         EX    RBUSZ,BLANKIT            BLANK WHOLE B.U.
         LH    RBUSZ,AVTKEYLE           GET BASIC UNIT SIZE     SA59527
         L     RAT,PRFTIC               GET ADDR OF NEXT B.U.
         LR    RBUF,RAT                 GET BFR BASE FOR NEXT TIME
         LA    RAT,AVTUMALN(0,RAT)      POINT PAST PREFIX TO DATA
         B     BLANKHOL                 BR TO CONT BLANKING
         SPACE 3
********
******** BLANK PARTIAL BASIC UNIT
********
BLANKHAF EQU   *
         MVI   0(RAT),BLANK             PUT A BLANK IN FIRST BYTE
         CH    RCNT,KON1                IS THERE ONLY 1 CHAR TO BLANK
         BE    STRNGFND                 YES-PROCESS THIS STRING  S22029
         SH    RCNT,AVTHA2              SUB 1 FOR MVI AND 1 FOR EX
         EX    RCNT,BLANKIT             BLANK RCNT PORTION OF B.U.
         B     STRNGFND                 PROCESS THIS STRING      S22029
         SPACE 3
* * * * * * * * * * *  CODE FOR NO STRING FOUND * * * * * * * * * * * *
         SPACE 3
********
******** RESTORE REGISTERS TO CONTENTS BEFORE STRING SCAN
********
NOSTRING EQU   *                                                 S22029
         LM    R14,RBASE,AVTECD12(R13)  RESTORE REGS
         TM    CURMHFG2,CURMHTS         IS CURRENT MH A TCAM MH  S22029
         BZ    BYEBYE                   YES - RETURN MSG INTACT  S22029
         SPACE 3
********
******** SEE IF TSO IS UP - IF NOT, SEND MESSAGE AND RETURN
********
         L     RCH,CVTPTR               GET CVT ADDR
         L     RCH,CVTAQAVT-CVT(RCH)    GET TCX ADDRESS        @G36XRYP
         USING IEDQTCX,RCH              TCX ADDRESSABILITY     @G36XRYP
         ICM   RCH,7,TCXRPT+1           GET TIOCRPT ADDRESS    @ZM46725
         BNZ   RETRYCT                  IF NOT ZERO,TSO IS UP  @G36XRYP
         DROP  RCH                                             @G36XRYP
         OI    AVTDOUBL,SCBNOTSO        SEND 'TSO INACTIVE' MSG  S22029
         B     CHKMH                    ATTEMPT TO PROMPT        S22029
         SPACE 3
********
******** SET UP QCB RETRY COUNT, IF NECESSARY
********
RETRYCT  EQU   *
         TM    QCBRETCT,QCBLOGCT        IS RETRY CNT ALREADY SET UP
         BO    DECR3                    BR IF YES - CNT IS 3
         BM    DECR1OR2                 BR IF YES - CNT IS 1 OR 2
         OI    QCBRETCT,QCBLOGCT        SET UP RETRY CNT
         B     PROMPTEM                 BR TO PROMPT USER
         SPACE 3
********
******** DECREMENT RETRY COUNT ... IF COUNT HITS ZERO, BID USER GOODBYE
********
DECR1OR2 EQU   *
         TM    QCBRETCT,QCBLOG1         IS RETRY CNT 1
         BZ    DECR2                    BR IF NO - DECR CNT BY 1
         NI    QCBRETCT,NOT-QCBLOGCT    ZERO RETRY COUNT
         OI    AVTDOUBL,SCBHANG         SEND 'LOGON FAILED' MSG  S22029
         OI    LCBSTAT2,LCBNEGRP        INSTIGATE TCAM HANG-UP PROCESS
         B     CHKMH                    ATTEMPT TO PROMPT        S22029
         SPACE 3
********
******** PROMPT USER VIA 'TRY AGAIN' MESSAGE
********
DECR2    EQU   *
         XI    QCBRETCT,QCBLOGCT        DECR CNT BY 1 (IS NOW 1)
         B     PROMPTEM                 BR TO PROMPT USER
DECR3    EQU   *
         NI    QCBRETCT,NOT-QCBLOG1     DECREMENT COUNT BY 1 (NOW 2)
PROMPTEM EQU   *
         OI    AVTDOUBL,SCBNOLOG        SEND 'ENTER LOGON' MSG   S22029
         B     CHKMH                    ATTEMPT TO PROMPT        S22029
         SPACE 3                                                 S22029
* * * * * * * * * * * * CODE FOR A VALID STRING * * * * * * * * * * * *
         SPACE 3                                                 S22029
********
******** RESTORE REGISTERS TO CONTENTS BEFORE STRING SCAN
********
STRNGFND EQU   *                                                 S22029
         LM    R14,RBASE,AVTECD12(R13)  RESTORE REGISTERS        S22029
         ICM   R1,7,LCBMHA              ASSUME PLCB AND GET    @Y17XAYP
*                                       MH ADDRESS             @Y17XAYP
         CLI   LCBFLAG1,LCBPLCB         IS THIS A PLCB         @Y17XAYP
         BE    GETALTMH                 YES, BRANCH            @Y17XAYP
         L     R1,DCBMH-1               UPDATE CURRENT MH QCB TO S22029
*                                       OWNING MH QCB
GETALTMH EQU   *                                               @Y17XAYP
         L     RIS,CURMHALT-1           SETUP ALTERNATE MH QCB   S22029
         USING ALTMHQCB,RIS                                      S22029
         NI    QCBDSFLG,NOT-QCBALTMH    SETUP QCB CONNECTION TO  S22029
*                                       OWNING MSG HANDLER
         CLI   AVTDOUBL+6,UPPERT        WAS 'TCAMON' FOUND       S22029
         BNE   LOGONFND                 NO - PROCESS 'LOGON'     S22029
         SPACE 3                                                 S22029
* * * * * * * * * * * * CODE FOR A VALID TCAMON * * * * * * * * * * * *
         SPACE 3                                                 S22029
********
******** SEND THIS MESSAGE TO ALTERNATE MH, IF POSSIBLE
********
TCAMFND  EQU   *                                                 S22029
         TM    QCBDSFLG,QCBDISK+QCBCORE CAN TERMINAL USE TCAM    S22029
         BZ    CONCAN                   NO - CANCEL THIS MSG     S22029
TCMHSRCH EQU   *                                                 S22029
         TM    CURMHFG2,CURMHTS         IS OWNING MH TCAM        S22029
         BZ    BYEBYE                   YES - CONT IN TCAM MH    S22029
         NC    CURMHALT,CURMHALT        IS THERE AN ALTERNATE MH S22029
         BZ    CONCAN                   NO - CONTINUE INHDR MSG  S22029
*                                       PROCESSING IN OWNING MH
         TM    ALTMHFG2,ALTMHTS         IS ALTERNATE MH TCAM     S22029
         BO    CONCAN                   NO - CANCEL THIS MSG     S22029
         NC    ALTMHLEX,ALTMHLEX        IS THERE A LOGON MACRO   S22029
*                                       EXIT ADDR IN THE ALT MH
         BZ    CONCAN                   NO - CANCEL THIS MSG     S22029
         TM    TRMSTATE,TRMPREF         IS THERE A PREFIX      @Y17XAYP
         BNO   NOPREF                   NO,CAN'T BE SNA        @Y17XAYP
         TM    TRMBYTE0,TRMSNA          IS THIS A SNA TERM     @Y17XAYP
         BO    CONCAN                   YES, CONTINUE          @Y17XAYP
*                                       PROCESSING IN OWNING MH@Y17XAYP
************************************************************** @Y17XAYP
*        ALTERNATE MH NOT PROVIDED FOR SNA TERMINALS           @Y17XAYP
************************************************************** @Y17XAYP
NOPREF   EQU   *                                               @Y17XAYP
         OI    QCBDSFLG,QCBALTMH        SETUP ALTERNATE DEST FOR S22029
*                                       THIS TERMINAL
         LR    R1,RIS                   SETUP ALTERNATE MH AS    S22029
*                                       CURRENT MH
         B     BYEBYE                   CONT IN TCAM MH          S22029
         SPACE 3
* * * * * * * * * * * * CODE FOR A VALID LOGON  * * * * * * * * * * * *
         SPACE 3
         SPACE 3
********
******** SEE IF TERMINAL CAN OPERATE UNDER TSO - IF NOT, SEND MESSAGE.
******** TERMINAL CANNOT IF IT HAS NO TSO QUEUEING, IF IT IS NOT OPEN
******** FOR OUTPUT, OR IF GENERAL POLLING HAS BEEN SPECIFIED.
********
LOGONFND EQU   *                                                 S22029
         TM    QCBDSFLG,QCBTSQ          IS TERMINAL DEFINED FOR  S22029
*                                       TSO QUEUEING
         BZ    NONTERM                  NO - TELL USER SO
         TM    TRMSTATE,TRMPREF         IS THERE A PREFIX      @Y17XAYP
         BNO   CKLCB                    NO,CHECK FOR GEN POLL  @Y17XAYP
         SR    R2,R2                    ZERO FOR NCP OFFSET    @Y17XAYP
         TM    TRMBYTE0,TRMUDEF         TERM ON 5041, BUT NOT  @Y17XAYP
*                                       1050 OR 2741           @Y17XAYP
         BO    NONTERM                  YES, INDICATE CAN'T    @Y17XAYP
*                                       USE WITH TSO           @Y17XAYP
         TM    TRMBYTE0,TRMOUTPT        CAPABLE FOR OUTPUT     @Y17XAYP
         BO    CHKTERM                  YES, CONT CHECKING     @Y17XAYP
         B     NONTERM                  NO, CAN'T LOGON        @Y17XAYP
CKLCB    EQU   *                                               @Y17XAYP
         SR    RCH,RCH                  CLEAR WORK REG.        @YA03653
         LA    R2,E8                    GET SUBTRACTION VALUE  @YA03653
         IC    RCH,DCBEIOBX             GET LENGTH OF LCB      @YA03653
         SR    RCH,R2                   SUBTRACT 8             @YA03653
         LA    RCH,0(RCH,RLCB)          POINT TO LCB EXTENTION @YA03653
         USING IEDQLCBX,RCH                                    @YA03653
         TM    LCBXFLAG,LCBGPCTV        IS TERM GENERAL POLLED @YA03653
         BO    NONTERM                  YES,CANT LOG HIM ON    @YA03653
         IC    R2,QCBRELLN              GET LINE NUMBER        @YA13121
         SLL   R2,E2                    X4=INV. LIST POS.      @YA13121
         L     RCH,DCBRESER(R2)         LOAD ITS STATUS ADD.   @YA13121
         TM    3(RCH),AUTOPOLL          IS TERM. AUTOPOLL?     @YA13121
         BO    NONTERM                  YES, CANT LOG HIM ON   @YA13121
CHKDEB   EQU   *
         L     RCH,DCBDEBAD             GET DEB ADDR
         TM    DEBOPATB(RCH),DEBOUT     DOES DEB SPECIFY OUTPUT
         BNZ   CHKTERM                  YES - CONT CHECKING
NONTERM  EQU   *
         OI    AVTDOUBL,SCBNOTRM        SEND 'NON-TSO TERM' MSG  S22029
         B     CHKMH                    ATTEMPT TO PROMPT        S22029
         SPACE 3
********
******** CHECK FOR TERMINAL BEING ABLE TO RECEIVE MESSAGES
********
CHKTERM  EQU   *
         TM    TRMSTATE,TRMACPTN        CAN TERMINAL ACCEPT MSGS
         BZ    NOMSG                    BR IF NO - SEND 'CANT GET MSGS'
         TM    TRMSTATE,TRMHELDN        IS TERMINAL IN HOLD MODE
         BZ    CHKTSO                   BR IF NO - CONT CHECKING
NOMSG    EQU   *
         OI    AVTDOUBL+2,SCBTMINN      SEND 'CANT GET MSGS' MSG S22029
         B     CHKMH                    ATTEMPT TO PROMPT        S22029
         SPACE 3
********
******** CHECK FOR TSO BEING OPERATIVE
********
CHKTSO   EQU   *
         L     RCH,CVTPTR               GET CVT ADDR
         L     RCH,CVTAQAVT-CVT(RCH)    GET TCX ADDRESS        @G36XRYP
         USING IEDQTCX,RCH              TCX ADDRESSABILITY     @G36XRYP
         ICM   RCH,7,TCXRPT+1           GET TIOCRPT ADDRESS    @ZM46725
         BZ    NOTUP                    ZERO TIOCRPT ADDRESS   @G37XRYP
*                                       IF TSO NOT UP          @G36XRYP
         DROP  RCH                                             @G36XRYP
         USING TIOCRPT,RCH              TIOCRPT ADDRESSABILITY @G36XRYP
         TM    TIOCFLG,TIOCSTOP         IS TSO STOPPING        @G36XRYP
         BNO   CHKMAX                   NO, CK IF USER LIMIT   @G36XRYP
*                                       EXCEEDED               @G36XRYP
NOTUP    EQU   *
         OI    AVTDOUBL,SCBNOTSO        SEND 'TSO INACTIVE' MSG  S22029
         B     CHKMH                    ATTEMPT TO PROMPT        S22029
         SPACE 3
********
******** CHECK FOR MAXIMUM USERS ALREADY LOGGED ONTO TSO
********
CHKMAX   EQU   *
         CLC   TIOCUSCT,TIOCNTSB        IS CURRENT NUMBER OF   @G36XRYP
*                                       USERS LESS THAN LIMIT  @G36XRYP
         BL    TSMHSRCH                 YES, BRANCH            @G36XRYP
         DROP  RCH                                             @G36XRYP
NOROOM   EQU   *
         ICM   R1,7,LCBMHA              ASSUME PLCB AND GET    @Y17XAYP
*                                       MH ADDRESS             @Y17XAYP
         CLI   LCBFLAG1,LCBPLCB         IS THIS A PLCB         @Y17XAYP
         BE    RESETALT                 YES, BRANCH            @Y17XAYP
         L     R1,DCBMH-1               RESTORE OWNING MH ADDR   S22029
*                                       IF TJB IS UNAVAILABLE
RESETALT EQU   *                                               @Y17XAYP
         NI    QCBDSFLG,NOT-QCBALTMH    RESET ALT DEST FLAGS     S22029
         OI    AVTDOUBL,SCBNOVAC        SEND 'TRY LATER' MSG     S22029
         B     CHKMH                    ATTEMPT TO PROMPT        S22029
TSMHSRCH EQU   *                                                 S22029
*        TM    LCBSTAT1,LCBOCWTN        STOPLINE IN PROGRESS   @OY14092
*        BO    CONCAN                   YES, GET OUT           @OY14092
         TM    LCBSTAT1,LCBOCNI         STOPLINE IN PROGRESS   @OZ24216
         BO    CONCAN                   YES, CANCEL THIS MSG   @OZ24216
         TM    CURMHFG2,CURMHTS         IS CURRENT MH TSO        S22029
         BO    TURNON                   YES - INIT CTL BLKS      S22029
         NC    CURMHALT,CURMHALT        IS THERE AN ALTERNATE MH S22029
         BZ    CONCAN                   NO - CANCEL THIS MSG     S22029
         TM    ALTMHFG2,ALTMHTS         IS ALTERNATE MH TSO      S22029
         BZ    CONCAN                   NO - CANCEL THIS MSG     S22029
         TM    TRMSTATE,TRMPREF         IS THERE A PREFIX      @Y17XAYP
         BNO   NOPREF1                  NO, BRANCH             @Y17XAYP
         TM    TRMBYTE0,TRMSNA          IS THIS A SNA TERM     @Y17XAYP
         BO    CONCAN                   NO,CONTINUE PROCESSING @Y17XAYP
*                                       IN OWNING MH           @Y17XAYP
************************************************************** @Y17XAYP
*        ALTERNATE MH NOT PROVIDED FOR SNA TERMINALS           @Y17XAYP
************************************************************** @Y17XAYP
NOPREF1  EQU   *                                               @Y17XAYP
         OI    QCBDSFLG,QCBALTMH        SET ALTERNATE MH         S22029
         LR    R1,RIS                   MAKE ALT MH CURRENT      S22029
         SPACE 3
* * * * * * * * * *  CONTROL BLOCK INITIALIZATION * * * * * * * * * * *
         SPACE 3
********
******** LOCATE TCT SO TO SET QCB BITS AND PASS TCT ADDRESS TO QTIP
********
TURNON   EQU   *
         L     RIS,AVTTSOPT             GET TSINPUT QCB ADDR   @Y17XAYP
******   MOVE THE DCT ENTRY TO TSWDCT IN TSO WORK AREA         @Y17XAYP
 IEDDCT  REG=RCH,FLD=TSITSW+TSWDCT-IEDQTSI-IEDQTSW(RIS),LEN=6  @Y17XAYP
         SPACE 3                                                 S22025
********
******** ZERO VARIOUS QCB FIELDS
********
         MVI   QCBRETCT,AVTEZERO        ZERO LOGON RETRY COUNT   S22025
         MVI   QCBSATCT,AVTEZERO        ZERO SIM ATTN LINE CT    S22025
         MVI   QCBTSOF1,AVTEZERO        ZERO QCB TSO FLAGS       S22025
         MVI   QCBTSOF2,AVTEZERO        ZERO QCB TSO FLAGS       S22025
         MVI   QCBCARCT,AVTEZERO        ZERO CARRIAGE POS COUNT  S22025
         SPACE 3                                                 S22025
********
******** SET NECESSARY QCB BITS
********
         TM    E1(RCH),DCT3270          IS THIS A 3270 TRM     @G36XRYP
         BZ    CHKREST                  NO - CONT QCB INIT       S22028
         OI    QCBRETCT,QCBIEND         SET QCBIEND ON FOR       S22028
*                                       SCREEN FORMAT ON OUTPUT
CHKREST  EQU   *                                                 S22028
         TM    E1(RCH),DCTINHIB         IS CINHIBIT ON         @G36XRYP
         BZ    CALLMGCR                 NO - LEAVE QCBINHBN ZERO S22025
         OI    QCBTSOF2,QCBINHBN        USE OF INHIBIT OK        S22025
         SPACE 3
************************************************************** @G36XRYP
*        START LOGON PROCESS VIA SVC 34 (MGCR)                 @G36XRYP
************************************************************** @G36XRYP
CALLMGCR EQU   *                                                 S22025
********                                                       @G36XRYP
******** ISSUE MGCR MACRO AND CHECK RETURN CODE                @G36XRYP
********
         LA    RTERM,TRMPRFSZ(RTERM)    SET UP TRM FOR QTIP    @YM08512
         MVC   PRFSRCE,AVTDOUBL+4       SET UP INDEX FOR IEDAYLL S22029
         LR    RIS,R1                   SAVE MH QCB ADDR         S22029
         L     R0,MGCRMASK              GET MASK FOR SVC 34    @ZM47806
         LA    R1,CIB                   POINT TO COMMAND BFR   @G36XRYP
         MGCR  (1)                                             @G36XRYP
         LTR   R15,R15                  IS LOGON POSSIBLE      @G36XRYP
         BNZ   UNINIT                   NO, UNDO INIT          @G36XRYP
         STH   R0,QCBTJID               PUT ASID IN QCB        @G36XRYP
********
******** TSO CONTROL BLOCK INIT - CALL QTIP ENTRY 10 (IEDAYLL)
********
         LA    R1,AVTECD12(0,R13)       GET SAVE AREA FOR QTIP   S22029
         QTIP  LOGON,(1)                INIT TSO CONTROL BLOCKS
         LA    R1,TRMPRFSZ              TRM PREFIX SIZE        @YM08512
         SR    RTERM,R1                 BACK TRM ADDR TO PREF  @YM08512
         LR    R1,RIS                   RESTORE MH QCB ADDR      S22029
         SPACE 3
********
******** SEE IF QTIP FOUND A FREE TSB - IF SO, SET QCBTSSES    @ZM46725
********
         LTR   R15,R15                  ANY FREE TSB           @ZM46725
         BZ    TSOGO                    YES - END CTL BLK INIT @ZM46725
UNINIT   EQU   *                                               @G36XRYP
         MVI   QCBTSOF2,AVTEZERO        UNDO PREVIOUS INIT       S22025
         B     NOROOM                   GO SEND 'MAX USERS' MSG  S22025
TSOGO    EQU   *                                                 S22025
         OI    QCBFLAG,QCBTSSES         MAKE SURE TSO IND IS ON
         SPACE 3
* * * * * * * * * * * 3705 MODE INITIALIZATION * * * * * * * * * * * *
         SPACE 3
********
******** FOR 3705, SET DEVICE MODE BITS
********
         TM    TRMDEVFL+1,TRMRNTRM      IS THIS A 3705 LINE
         BNO   DONE3705                 NO, DO NOTHING
         TM    TRMSTATE,TRMOPTFN        ANY OPTION FLDS         Y06327
         LA    R14,TRMOPNO              ASSUME NONE
         BNO   NOOPTION                 BRANCH ON NO OPTION FLDS Y06327
         SR    R15,R15                  CLEAR REGISTER FOR IC    Y06327
         IC    R15,TRMOPNO              NUMBER OF OPTION FIELDS  Y06327
         LA    R14,TRMOPT(R15)          SKIP OPTION FIELDS       Y06327
NOOPTION EQU   *                                                 Y06327
         LH    R15,TRMDEVFL             GET DEVICE DEP FLAGS     Y06327
         LA    R13,ELEVEN               SET FOR BIT SEARCH       Y06327
         SLL   R15,16                   SHIFT BITS TO HIGH ORDER Y06327
*                                       TWO BYTES                Y06327
         SR    RWORK5,RWORK5            CLEAR REGISTER FOR IC    Y06327
SHIFT    EQU   *                                                 Y06327
         LTR   R15,R15                  IS BIT PRESENT           Y06327
         BNM   CHKCOUNT                 BRANCH ON NO             Y06327
         LA    RWORK5,E1(RWORK5)        NUMBER OF BITS FOUND     Y06327
CHKCOUNT EQU   *                                                 Y06327
         SLL   R15,1                    SHIFT TO CHECK NEXT BIT  Y06327
         BCT   R13,SHIFT                HAVE ALL BITS BEEN CHKED Y06327
         LTR   RWORK5,RWORK5            ANY DEV DEP FIELDS       Y06327
         BZ    SETMODE                 NO GO SET DEV MODE FIELDS Y06327
         SR    R13,R13                  CLEAR REGISTER FOR IC    Y06327
SKIP     EQU   *                                                 Y06327
         IC    R13,AVTEZERO(,R14)       GET DEV DEP FIELD LENGTH Y06327
         LA    R14,E1(R13,R14)          SKIP OVER FIELD          Y06327
         BCT   RWORK5,SKIP              SKIP TO NEXT FIELD       Y06327
SETMODE  EQU   *                                                 Y06327
         LA    R14,E3(R14)              GET DEV DEP FOR SETMODE  Y06327
         LA    R13,AVTSAVE3             RESTORE SAVEAREA ADDRESS Y06327
********
******** SET MODE BIT FOR MONITOR MODE, AND BIT FOR TIMEOUT-SUPPRESSION
******** DEPENDING ON DCT SETTING
********
         OI    TRMPRE,TRMCMODE          INDICATE SET MODE REQD @Y17XAYP
         OI    E1(R14),STMONITR         SET MONITOR MODE        YS06327
         TM    E1(RCH),DCTINHIB         IS INHIBIT SET IN DCT  @G36XRYP
         BNO   NOINHB                   NO, GO TURN OFF FLAG
         OI     E1(R14),SETINHB         SET T.O. SUPPRESS FLAG   Y06327
         B     DONE3705                 DONE WITH SPECIAL STUFF
NOINHB   EQU   *
         NI    E1(R14),NOT-SETINHB      OFF T.O. SUPRESS FLAG
DONE3705 EQU   *
         SPACE 3                                                 S22029
********
******** REMOVE QCB FROM TIME-DELAY QUEUE, IF NECESSARY
********
         TM    QCBTSOF1,QCBDELAY        IS QCB IN DELAY QUEUE    S22029
         BZ    NODELAY                  NO - CONTINUE            S22029
         LR    RIS,R1                   SAVE CURRENT MH'S QCB    S22029
         LR    R1,RQCB                  PASS QCB ADDR TO TIME    S22029
*                                       DELAY REMOVAL RTN
         L     R15,AVTHG02              GET ADDR OF T.D.R. RTN   S22029
         BALR  R14,R15                  GO REMOVE ELEMENT        S22029
         LR    R1,RIS                   RESTORE CURR MH'S QCB    S22029
         NI    QCBSTAT,NOT-QCBTIME      RESET REQUEST FOR ADDTL  S22029
*                                       12-HOUR REQUEST
NODELAY  EQU   *                                                 S22029
         SPACE 3                                                 S22029
********
******** REMOVE QCB FROM DIALOUT CALL QUEUE, IF NECESSARY
********
         TM    LCBSTAT2,LCBDIAL         IS THIS A DIAL LINE      S22029
         BZ    NODIALQ                  NO - SKIP CALLQ CODE     S22029
         TM    TRMSTATE,TRMPREF         IS TERM VIA 3705       @Y17XAYP
         BO    NODIALQ                  YES - SKIP CALLQ CODE
         TM    QCBDSFLG,QCBDISK+QCBCORE PORT DEDICATED TO TSO    S22029
         BZ    NODIALQ                  YES - SKIP CALLQ CODE    S22029
         L     R15,DCBIOBAD             FIRST IOB IN LINE GROUP  S22029
         SR    R14,R14                  ZERO REG FOR IC INSTR    S22029
         IC    R14,DCBEIOBX             LCB SIZE                 S22029
         AR    R15,R14                  ADDR OF DESIRED LCB      S22029
         SH    R15,DIALOUT              BACK UP TO BEGINNING OF  S22029
*                                       FIRST LCB MINUS 8 BYTE
*                                       PREFIX TO CALLQ
         LA    RIS,QCBSTVTO             UPDATE QCB ADDR TO QCB'S S22029
*                                       STCB ADDR FOR COMPARE
*                                       WITH QCBS ON CALLQ
         USING IEDQSTCB,R14                                      S22029
LOOPCALL EQU   *                                                 S22029
         LR    R14,R15                  UPDATE PEVIOUS STCB ADDR S22029
*                                       WITH CURRENT STCB ADDR
         L     R15,STCBLINK-1           GET NEXT STCB FOR        S22029
*                                       NEXT QCB ON CALLQ
         CLI   STCBPRI-IEDQSTCB(R15),AVTEZERO     END OF CALLQ   S22029
         BE    NODIALQ                  YES - CONTINUE           S22029
         LA    R15,0(0,R15)             CLEAR HI BYTE OF QCB'S   S22029
*                                       STCB ADDR FOR COMPARE
         CLR   R15,RIS                  IS THIS QCB FOR THE      S22029
*                                       TERMINAL LOGGING ONTO TS
         BNE   LOOPCALL                 NO - CONTINUE CALLQ SRCH S22029
         MVC   STCBLINK(E3),STCBLINK-IEDQSTCB(R15)     REMOVE    S22029
*                                       THIS QCB FROM THE CALLQ
         SPACE 3                                                 S22029
********
******** SETUP QCB FOR SEND OPERATION
********
MOVESCH  EQU   *                                                 S22029
         DROP  R14                                               S22029
         USING IEDQTSI,R14                                       S22029
         L     R14,AVTTSOPT             GET TSINPUT QCB          S22029
         MVC   QCBSLINK(E3),TSITSDST+1  SET UP TSO DEST SCHED    S22029
NODIALQ  EQU   *                                                 S22029
         CLC   QCBSTCHN(E3),QCBSLINK    IS QCB SET UP FOR SEND   S22029
         BNE   SENDPRI                  SET UP SEND PRIORITY     S22029
         LA    R15,QCBSTCHN-1           DENOTE INVOKE SEND SCHED S22029
         IC    R14,QCBSTCHN-1           SAVE INDEX               S22029
         ST    R15,QCBSTCHN-1           SEND SCHED WILL BE       S22029
*                                       INVOKED VIA INDEX
         STC   R14,QCBSTCHN-1           RESTORE INDEX            S22029
SENDPRI  EQU   *                                                 S22029
         MVI   QCBPRI,PRIDSKRQ          SET UP SEND PRIORITY     S22029
         SPACE 3
********
******** ROUTE THIS BUFFER TO TSO
********
SETTSIN  EQU   *                                                 S22029
         L     RIS,AVTTSOPT             GET ADDR OF TSINPUT QCB  S22029
         B     SETDEST                  SET UP THIS TERMINAL'S   S22029
*                                       DESTINATION AND SOURCE
         SPACE 3                                                 S22029
********
******** LEAVE - CONTINUE INHDR PROCESSING
********
         USING ALTMHQCB,RIS             ADDRESSABILITY AFTER
*                                       PREVIOUS CODE WIPED IT OUT
CHKMH    EQU   *                                                 S22029
         TM    CURMHFG2,CURMHTS         IS CURRENT MH TSO        S22029
         BO    SENDMSG                  YES - PROMPT USER        S22029
         NC    CURMHALT,CURMHALT        IS THERE AN ALTERNATE MH S22029
         BZ    BYEBYE                   NO - CANNOT PROMPT USER  S22029
         L     RIS,CURMHALT-1           GET ADDR OF ALT MH       S22029
         TM    ALTMHFG2,ALTMHTS         IS ALT MH TSO            S22029
         BZ    BYEBYE                   NO - CANNOT PROMPT USER  S22029
BADCIN   EQU   *                                                 S99228
         LR    R1,RIS                   USE ALT MH TO PROMPT     S22029
CONCAN   EQU   *                                                 S22029
         TM    CURMHFG2,CURMHTS         IS CURRENT MH TSO        S22029
         BZ    BYEBYE                   NO - LEAVE INPUT DEST    S22029
*                                       ADDR INTACT
SENDMSG  EQU   *                                                 S22029
         OC    SCBERRST,AVTDOUBL        PUT BITS INTO SCB        S22029
         LA    RIS,AVTBFRTB             ADDRESS OF BUFFER RETURN S22029
SETDEST  EQU   *                                                 S22029
         IC    RCH,SCBDESTQ-1           SAVE HIGH-ORDER BYTE     S22029
         ST    RIS,SCBDESTQ-1           SETUP BUFFER RETURN FOR  S22029
*                                       INPUT DESTINATION
         STC   RCH,SCBDESTQ-1           RESTORE HIGH-ORDER BYTE  S22029
         NC    AVTDOUBL+4(E2),AVTDOUBL+4     CONNECT INDEX FOUND S22029
         BZ    BYEBYE                   NO - DON'T SET UP LINE   S22029
*                                       WITH CONNECT INDEX
         MVC   LCBTTCIN,AVTDOUBL+4      SET LINE CONNECT INDEX   S22029
         MVC   PRFSRCE,LCBTTCIN         SET LINE CONNECT INDEX   S22029
BYEBYE   EQU   *                                                 S22029
         L     R13,AVTECD4(0,R13)       RESTORE SAVE AREA ADDR   S22029
         L     R14,CURMHLEX-1           LOGON EXIT ADDRESS       S22029
         LA    R2,ELEVEN                USE REG 2 TO BACK UP     S22029
         SR    R14,R2                   POINT TO PARM BYTE       S22029
         TM    AVTEZERO(R14),INBLOCK    IS LOGON IN INBLOCK      S22029
         BZ    NOTINBLK                 NO, BRANCH               S22029
         NI    SCBSTATE,AVTEFF-SCBCODE  TURN OFF CODE BIT        S22029
NOTINBLK EQU   *                                                 S22029
         AR    R14,R2                   RESET REG 14             S22029
         LH    R2,KON4K                 GET 4K BASE ADDR INCR    S22029
         LM    RSCB,RBASE,E32(R13)      RESTORE REGS             S22029
         MVI   AVTECD12(R13),AVTEFF     SET AVAILABILITY BYTE    S22029
         SR    RBASE,RBASE              ZERO REG FOR IC INSTR    S22029
         IC    RBASE,CURMHLEN           LNG OF QCB PRECEDING MH  S22029
         LA    RBASE,0(R1,RBASE)        POINT PAST QCB TO MH     S22029
         BR    R14                      CONT INHDR PROCESSING    S22029
         SPACE 3
********
******** CONSTANTS
********
BLANKIT  MVC   E1(0,RAT),0(RAT)         BLANK GARBAGE
DIALOUT  DC    AL2(LCBFLAG1-IEDQLCB+8)  OFFSET FROM IOB OF FIRST S22029
*                                       LCB FOR THIS LINE GROUP
*                                       TO DIALOUT CALLQ PREFIX
MGCRMASK DS    0F                       FORCE FULLWORD BNDRY   @G36XRYP
         DC    X'80001000'              MASK FOR SVC 34        @G36XRYP
CIB      DS    0A                       FORCE FULLWORD BNDRY   @G36XRYP
         DC    AL2(CIBEND-CIB)          LENGTH                 @G36XRYP
         DC    XL2'0'                                          @G36XRYP
         DC    C'LOGON '                LOGON,BLANK            @G36XRYP
CIBEND   EQU   *                                               AG36XRYP
KON1     DC    H'1'                     GIVES A VALUE OF 1
KON5     DC    H'5'                     GIVES A VALUE OF 5
KON6     DC    H'6'                     GIVES A VALUE OF 6       S22029
KON72    DC    H'72'                    GIVES A VALUE OF 72
KON4K    DC    H'4096'                  4K INCREMENT             S22029
         SPACE 3
********
******** 'OGON' IN LOWER AND UPPERCASE EBCDIC, RESPECTIVELY
********
LOGCON   EQU   *
         DC    X'96'                    LOWERCASE 'O'
         DC    C'O'                     UPPERCASE 'O'
         DC    X'87'                    LOWERCASE 'G'
         DC    C'G'                     UPPERCASE 'G'
         DC    X'96'                    LOWERCASE 'O'
         DC    C'O'                     UPPERCASE 'O'
         DC    X'95'                    LOWERCASE 'N'
         DC    C'N'                     UPPERCASE 'N'
         DC    C'X'                     MARKS THE END OF 'LOGCON'
         SPACE 3                                                 S22029
********
******** 'CAMON' IN LOWER AND UPPERCASE EBCDIC, RESPECTIVELY
********
TCAMCON  EQU   *                                                 S22029
         DC    X'83'                    LOWERCASE 'C'            S22029
         DC    C'C'                     UPPERCASE 'C'            S22029
         DC    X'81'                    LOWERCASE 'A'            S22029
         DC    C'A'                     UPPERCASE 'A'            S22029
         DC    X'94'                    LOWERCASE 'M'            S22029
         DC    C'M'                     UPPERCASE 'M'            S22029
         DC    X'96'                    LOWERCASE 'O'            S22029
         DC    C'O'                     UPPERCASE 'O'            S22029
         DC    X'95'                    LOWERCASE 'N'            S22029
         DC    C'N'                     UPPERCASE 'N'            S22029
         DC    C'X'                     MARKS END OF 'TCAMCON'   S22029
AYLPATCH DC    20F'0'                                            S22029
         SPACE 3
* * * * * * * * * * * *  CONTROL BLOCK DSECTS * * * * * * * * * * * * *
         SPACE 3
         TAVTD
         SPACE 3                                                 S22029
CVT      DSECT
         CVT
         SPACE 3                                                 S22029
         DCBD  DSORG=TX                                          S22029
         SPACE 3                                                 S22029
         TLCBD
         SPACE 3                                                 S22029
******** PARAMETER LISTS
         SPACE 3                                                 S22029
LOGPARM  DSECT
         SPACE 2                                                 S22029
LOGPLEN  DS    AL1                      LENGTH OF PARAMETER LIST S22029
LOGPINDX DS    AL1                      INDEX TO ROUTINE         S22029
LOGPRSVR DS    AL2                      RESERVED                 S22029
         DS    AL1                      RESERVED                 S22029
LOGPMH   DS    AL3                      ADDR OF CURRENT MSG      S22029
*                                       HANDLER PROCESSING LOGON
         SPACE 3                                                 S22029
CURMHQCB DSECT
         SPACE 2                                                 S22029
CURMHFG1 DS    AL1                      FLAG BYTE - UNUSED       S22029
CURMHALT DS    AL3                      ADDR OF ALTERNATE MH     S22029
CURMHFG2 DS    AL1                      FLAG BYTE                S22029
*        BIT DEFINITIONS
CURMHTS  EQU   X'01'                    INDICATES A TSO MH       S22029
CURMHLEX DS    AL3                      EXIT ADDRESS OF LOGON    S22029
*                                       MACRO IN THIS MH
CURMHLEN DS    AL1                      LENGTH OF STARTMH QCB    S22029
         SPACE 3                                                 S22029
ALTMHQCB DSECT
         SPACE 2                                                 S22029
ALTMHFG1 DS    AL1                      FLAG BYTE - UNUSED       S22029
ALTMHALT DS    AL3                      ADDR OF ALTERNATE MH     S22029
ALTMHFG2 DS    AL1                      FLAG BYTE                S22029
*        BIT DEFINITIONS
ALTMHTS  EQU   X'01'                    INDICATES A TSO MH       S22029
ALTMHLEX DS    AL3                      EXIT ADDRESS OF LOGON    S22029
*                                       MACRO IN THIS MH
ALTMHLEN DS    AL1                      LENGTH OF STARTMH QCB    S22029
         SPACE 3                                               @Y17XAYP
         TDCTD
         SPACE 3                                                 S22029
         TPRFD
         SPACE 3                                                 S22029
         TPRIOR
         SPACE 3                                                 S22029
         TQCBD
         SPACE 3                                                 S22029
         TSCBD
         SPACE 3                                               @Y17XAYP
         TSIBD
         SPACE 3                                                 S22029
         TSTCBD
         SPACE 3                                               @G36XRYP
         TTCXD
         SPACE 3
         TTRMD
         SPACE 3                                               @G36XRYP
         IKJTIOCP
         SPACE 3                                                 S22029
         TTSID
         SPACE 3                                               @Y17XAYP
         TTSWD
         END
