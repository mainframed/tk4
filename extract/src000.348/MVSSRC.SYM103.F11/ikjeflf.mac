IKJEFLF  TITLE 'SIC SCHEDULE ROUTINE'
**********************************************************************
*
* MODULE NAME = IKJEFLF
*
* DESCRIPTIVE NAME = SYSTEM INITIATED CANCEL SCHEDULE
*                    ROUTINE.
*
* COPYRIGHT = NONE
*
* STATUS = OS/VS2 - RELEASE 3
*
* FUNCTION = THE FUNCTION OF THIS MODULE IS TO SCHEDULE AN SRB
*            ROUTINE (IKJL4T00) AND TO PERFORM INITIAL VALIDITY
*            CHECKING ON THE INPUT.
*            THE FUNCTION OF THE SRB ROUTINE IS TO HANDLE CANCEL-
*            LATION OF A TIME SHARING USER BY THE OPERATOR, OR
*            BY A LINE DISCONNECT SITUATION, AND TO PROVIDE
*            VARIOUS INTERFACES TO INSURE THAT THE CANCELLATION
*            IS ORDERLY. FOR EXAMPLE, IF ANOTHER TIME SHARING
*            USER HAS ISSUED A TPUT WITH  ASID TO THE TASK
*            BEING CANCELLED, A SPECIAL RETURN  CODE MUST
*            BE SENT BACK TO THE TPUT ISSUER.
*
*   OPERATION = SAVE CALLERS REGISTERS.
*               VALIDITY CHECK ASCBCSCB FIELD IN INPUT ASCB.
*               IF INVALID, RETURN TO CALLER.
*               A GETMAIN IN SUBPOOL 239 WILL BE ISSUED FOR CORE
*               FOR AN SRB.
*               THE SRB WILL BE INITIALIZED AND THE SCHEDULE MACRO
*               USED TO ADD THE NEW BLOCK TO THE SRB CHAIN
*               THE ROUTINE BEING SCHEDULE IS IKJL4T00.
*               THE RMTR ROUTINE ADDRESS PROVIDED IN THE SRB
*               IS IKJL4T01.
*               A PARM AREA FOR THE SRB ROUTINE WILL BE INITIALIZED
*               WITH THE INPUT POST CODE AND THE ADDRESS OF THE CSCB.
*               A POINTER TO THE PARMAREA IS PASSED IN THE SRB.
*               THE DISCONNECT BIT IN THE CSCB WILL BE TURNED ON.
*               (CHDISC FIELD.)
*               REGISTERS WILL BE RESTORED AND CONTROL RETURNED TO
*               THE CALLER.
*
* NOTES = THE LOCAL AND CMS LOCKS MUST BE HELD ON ENTRY AND THE
*         CALLER MUST BE IN KEY ZERO, SUPERVISOR STATE.
***********************************************************************
         EJECT
***********************************************************************
*
*    DEPENDENCIES = SERIALIZATION OF ASCBCSCB AND CSCB IN STC,
*                   QEDIT, LOGON SCHEDULER.
*                   GETMAIN, RC BRANCH ENTRY
*                   SCHEDULE
*                   IEEVIPL TO INITIALIZE CVTSIC ADDRESS.
*                   EBCDIC CHARACTER CODE DEPENDENCE CORRECTABLE
*                   BY REASSEMBLY.
*
*    RESTRICTIONS = SEE NOTES.
*
*    REGISTER CONVENTIONS = R0 CONTAINS RIGHT JUSTIFIED POST
*                           CODE.
*                           R1 CONTAINS ADDRESS OF ASCB TO BE
*                           CANCELLED.
*                           R13 CONTAINS REG SAVE AREA ADDRESS
*                           R14 CONTAINS RETURN ADDRESS
*                           R15 CONTAINS ENTRY POINT ADDRESS AS
*                           CONTAINED IN CVTSIC.
*
*    PATCH LABEL = PTCHEFLF A DC STATEMENT.
*
* MODULE TYPE = CSECT
*
*   MODULE SIZE = EE HEX
*
*   ATTRIBUTES = REENTRANT, PAGED LPA, ZERO PSW KEY, SUPERVISOR
*              MODE.
***********************************************************************
         EJECT
***********************************************************************
*
* ENTRY POINT = IKJEFLF FROM IEE3703D AND FROM TIOC.
*
*   PURPOSE = SEE FUNCTION
*
*   LINKAGE = SEE REGISTER CONVENTIONS FOR BRANCH ENTRY
*             DEFINITION.
*
*   INPUT = R0 CONTAINS RIGHT JUSTIFIED POST CODE.
*           R1 CONTAINS POINTER TO ASCB TO BE CANCELLED.
*           R13 CONTAINS SAVE AREA ADDRESS.
*           R14 CONTAINS RETURN ADDRESS.
*   OUTPUT = SEE EXIT-NORMAL.
*
*   REGISTERS SAVED = ALL.
*
*   REGISTER CONTENTS DURING PROCESSING =
*                                    R0 =INPUT POST CODE
*                                    R1 =INPUT ASCB ADDRESS
*                                    R2 =CSCB ADDRESSIBILITY
*                                    R3 =SRB ADDRESSIBILITY,GETMAIN
*                                    R4 =USED BY GETMAIN
*                                    R5 =PROGRAM ADDRESSIBILITY
*                                    R6 =SRB CREATE WORK REG
*                                    R7 =ASCB FOR GETMAIN
*                                    R8 =ASCB ADDRESSIBILITY
*                                    R10=POST CODE SAVE REGISTER
*                                    R12=STM SAVE INSTRUCTION
*                                    R13=INPUT SAVE AREA ADDRESS
*                                    R14=INPUT RETURN ADDRESS
*                                    R15=ENTRY POINT REGISTER
*
*   REGISTERS RESTORED = ALL.
**********************************************************************
         EJECT
**********************************************************************
*
* EXIT - NORMAL = RETURN TO CALLER VIA BR14.
*
*   CONDITIONS = COMPLETION OF FUNCTION
*
*   OUTPUT DATA = CHDISC BIT ON IN CSCB.
*                 SRB CREATED AND CHAINED.
*                 SRBPARM FIELD POINTS TO IKJL4T00 PARMAREA.
*                 IKJL4T00 PARMAREA CONTAINS ADDRESS OF CANCEL ECB
*                 IN FIRST WORD AND POST CODE IN SECOND WORD.
*   RETURN CODE = NONE.
**********************************************************************
*
* EXIT - ERROR = RETURN TO CALLER VIA BR14.
*
*   CONDITIONS = CSCB INVALID.
*
*   OUTPUT DATA = NONE.
*
*   RETURN CODES = NONE.
**********************************************************************
        EJECT
**********************************************************************
*
* EXTERNAL REFERENCES = SEE BELOW.
*
*    ROUTINES = IKJL4T00,IKJL4T01
*
*    DATA AREAS = NONE
*
*    CONTROL BLOCKS = IEECHAIN     CSCB    READ,WRITE
*                     IHAASCB      ASCB    READ
*                     IHASRB       SRB     CREATE
*                     CVT          CVT     READ
*                     IHAPSA       PSA     READ                @YM3521P
*
* TABLES = NONE
*
* MACROS = GETMAIN BRANCH ENTRY
*          SCHEDULE
*
*    SERIALIZATION = LOCAL AND CMS LOCKS HELD ON ENTRY.
*
* MESSAGES = NONE.
*
* CHANGE ACTIVITY = Y02651 VS2-2 MP SUPPORT
*                   OZ07126, ZA06994                           @ZA06994
*
* SYSGEN = SGIKJ441 MACRO. DLIB AOST4. TARGLIB IS SYS1.LPALIB.
*          LOAD IS IKJEFLF.
*
* LIBRARIES = NONE.
*
**********************************************************************
         TITLE 'COMMAND SCHEDULING CONTROL BLOCK: CSCB'
CSCB     DSECT
         IEECHAIN
         TITLE 'ADDRESS SPACE CONTROL BLOCK: ASCB'
         IHAASCB
         TITLE 'SERVICE REQUEST BLOCK: SRB'
         IHASRB
         TITLE 'CVT'
         CVT   DSECT=YES
         TITLE 'MODULE DEFINED DSECT FOR SRBPARM AREA'
         TITLE 'PREFIX STORAGE AREA'
         IHAPSA
***********************************************************************
*
* THE FOLLOWING DSECT, PARMSECT,DEFINES AN AREA WHICH IS POINTED
* TO BY THE SRBPARM FIELD IN THE SRB. THE GETMAIN FOR IT IS DONE
* IN IKJEFLF AND IT IS PART OF THE GETMAIN WHICH IS DONE FOR THE
* SRB ITSELF: THE TWO AREAS ARE CONTIGUOUS IN CORE. THE FREEMAIN
* IS DONE IN THE SRB ROUTINE, IKJL4T00, OR IN THE FRR ROUTINE OF
* THE SAME MODULE (ENTRY POINT IKJL4T02), OR IN THE RMTR ROUTINE
* OF THE SAME MODULE (ENTRY POINT IKJL4T01). THE SRB AND THE
* PARMAREA ARE IN SUBPOOL 239.
*
**********************************************************************
PARMSECT DSECT                          SRB PARM AREA
PARMCSCB DS    F                        PTR TO CSCB TO BE POSTED
PARMPOST DS    F                        POST CODE
         DS    D                        RESERVED FOR FUTURE USE
PARMEND  EQU   *
PARMSIZE EQU   PARMEND-PARMSECT         LENGTH OF PARMAREA
         TITLE 'SIC: REGISTER EQUATES AND DISPLACEMENTS'
IKJEFLF  CSECT
* A 000000-999999                                                Y02651
* D 05399802-05399902  DO NOT CHECK FOR INITIATOR CSCB         @ZA07126
* A 055008,056098 INIT. SRB BLOCK ID                           @ZA06994
**********************************************************************
*
*                 REGISTER EQUATES
*                 R9 AND R11 UNUSED
*
**********************************************************************
R0       EQU   0                        INPUT POST CODE
R1       EQU   1                        INPUT ASCB ADDRESS
R2       EQU   2                        CSCB ADDRESSIBILITY
R3       EQU   3                        SRB ADDRESSIBILITY,GETMAIN
R4       EQU   4                        USED BY GETMAIN
R5       EQU   5                        PROGRAM ADDRESSIBILITY
R6       EQU   6                        SRB CREATE WORK REG
R7       EQU   7                        ASCB FOR GETMAIN
R8       EQU   8                        ASCB ADDRESSIBILITY
R10      EQU   10                       POST CODE SAVE REGISTER
R12      EQU   12                       STM SAVE INSTRUCTION
R13      EQU   13                       INPUT SAVE AREA ADDRESS
R14      EQU   14                       INPUT RETURN ADDRESS
R15      EQU   15                       ENTRY POINT REGISTER
         SPACE
**********************************************************************
*
*                 DISPLACEMENTS AND LENGTHS
*
**********************************************************************
         SPACE
D0       EQU   0                        ESTABLISH ADDRESSIBILITY
D4       EQU   4                        PROGRAM CONSTANT       @ZA06994
D12      EQU   12                       OFFSET IN SAVING REGISTERS
         EJECT
**********************************************************************
*
*     ESTABLISH ADDRESSIBILITY, SAVE CALLERS REGISTERS AND DO
*     INITIAL SIC VALIDITY CHECKING ON INPUT POINTERS.
*
**********************************************************************
         STM   R14,R12,D12(R13)         SAVE CALLERS REGISTERS
         BALR R5,D0                     ESTABLISH ADDRESSIBILITY
         USING *,R5                     PROGRAM BASEREG IS R5
         MODID BRANCH=YES
         LR    R8,R1                    SAVE INPUT ASCB OF     @YM3521P
*                                       USER TO BE CANCELLED   @YM3521P
         USING PSA,R0                   SET UP PSA             @YM3521P
*                                       ADDRESSABILITY         @YM3521P
         L     R7,PSAAOLD               GET CURRENT ASCB FOR   @YM3521P
*                                       GETMAIN                @YM3521P
         USING ASCB,R8                  SET UP ASCB ADDRESSIBILITY
         L     R2,ASCBCSCB              GET CSCB POINTER
         LTR   R2,R2                    IS CSCB INACTIVE?
         BZ    RESTREGS                 IF ZERO, NO JOB TO CANCEL
         USING CHAIN,R2                 SET UP CSCB ADDRESSIBILITY
         OI    CHACT,CHDISC             SET DISCONNECT BIT ON
         EJECT
**********************************************************************
*
*     DO GETMAIN IN SP 239 FIXED CORE FOR SRB CONTROL BLOCK
*     GETMAIN IS ALSO FOR FOUR WORD PARMAREA TO BE PASSED TO
*     THE SRB ROUTINE. THIS PARMAREA IS DEFINED IN THIS MODULE'S
*     DSECT NAMED PARMSECT.
*
**********************************************************************
         LR    R10,R0                   SAVE POSTCODE
         SR    R4,R4                    CLEAR REG FOUR FOR GETMAIN
         LA    R0,SRBSIZE+PARMSIZE      SET LENGTH FOR GETMAIN
         GETMAIN RU,LV=(0),SP=239,BRANCH=YES
         EJECT
**********************************************************************
*
*     ESTABLISH SRB ADDRESSIBILITY IN GOTTEN CORE AND INITIALIZE
*     SRB AS REQUIRED BY SCHEDULING ROUTINE.
*
**********************************************************************
         LR    R3,R1                    SET SRB ADDRESSIBILITY
         USING SRBSECT,R3               SRB BASE IS R3
*                                       ZERO SRB AND PARMAREA
         XC    SRB(SRBSIZE+PARMSIZE),SRB
         ST    R8,SRBASCB               STORE ASCB ADDRESS
         LH    R6,ASCBASID              GET ASID FROM ASCB
         STH   R6,SRBPASID              STORE ASID IN SRB
         L     R6,ADL4T00               GET ENTRY POINT SRB RTN.
         ST    R6,SRBEP                 STORE IKJL4T00 ENTRY POINT
         L     R6,ADL4T01               GET ENTRY POINT RMTR RTN.
         ST    R6,SRBRMTR               STORE IKJL4T01 ENTRY POINT
         MVC   SRBID(D4),NAMESRB        INIT BLOCK ID          @ZA06994
         EJECT
***********************************************************************
*
*     ESTABLISH ADDRESSIBILITY TO FOUR WORD PARMAREA BY ADDING
*     LENGTH OF SRB TO SRB BASE REGISTER. STORE PARMAREA ADDRESS
*     IN SRB BLOCK. INITIALIZE SRBPARM WITH ADDRESS OF CSCB TO BE
*     POSTED AND WITH POST CODE. THEN RESTORE SAVED REGISTERS AND
*     RETURN TO CALLER.
*
***********************************************************************
         LA    R6,SRBSIZE(R3)           GET TOTAL LENGTH SRB
         USING PARMSECT,R6              BEGINNING OF SRB PARMAREA
         ST    R6,SRBPARM               STORE PARMAREA PTR IN SRB
         ST    R2,PARMCSCB              CSCB ADDRESS IN PARMAREA
         ST    R10,PARMPOST             POST CODE IN PARMAREA
         SCHEDULE SRB=(3)
RESTREGS EQU   *
         LM    R14,R12,D12(R13)         RESTORE CALLERS REGISTERS
         BR    R14                      RETURN TO CALLER
         EJECT
***********************************************************************
*
*     PROGRAM CONSTANTS AND SRB PARMAREA DSECT
*
***********************************************************************
ADL4T00  DC    V(IKJL4T00)              SRB ROUTINE ENTRY POINT
ADL4T01  DC    V(IKJL4T01)              RMTR ROUTINE ENTRY POINT
NAMESRB  DC    CL4'SRB '                SRB BLOCK ID           @ZA06994
         SPACE
PTCHEFLF DC   CL50' * * * IKJEFLF * * *'
         END
