TKAA     TITLE 'IGTKAA -- VPSS SVC-111 ROUTER'
IGTKAA   CSECT ,
*/* * START OF SPECIFICATIONS ************************************** */
*/*                                                                  */
*/*   MODULE-NAME = IGTKAA                                           */
*/*                                                                  */
*/*   DESCRIPTIVE-NAME = VPSS SVC-111 ROUTER                         */
*/*                                                                  */
*/*   COPYRIGHT = NONE                                               */
*/*                                                                  */
*/*   STATUS = RELEASE 1, MODIFICATION LEVEL 0.                      */
*/*                                                                  */
*/*   FUNCTION = TO PASS CONTROL TO THE APPROPRIATE VPSS SVC         */
*/*       ROUTINE BASED UPON THE FUNCTION CODE SPECIFIED IN          */
*/*       GENERAL REGISTER 0.                                        */
*/*                                                                  */
*/*   NOTES = THIS ROUTINE RUNS AS A TYPE-2 SVC.  CONTROL IS         */
*/*       GIVEN TO THIS ROUTINE BY SVC 111.                          */
*/*                                                                  */
*/*     DEPENDENCIES = THE ROUTINE MUST BE POINTED TO BY THE         */
*/*                    END-OF-BLOCK ROUTINE POINTER IN THE           */
*/*                    ASSOCIATED VPSS SUBSYSTEM DEB.  SVC-111       */
*/*                    LOADS THIS ADDRESS (AFTER VERIFYING THE DEB)  */
*/*                    AND BRANCHS TO THE ROUTINE.                   */
*/*                                                                  */
*/*     RESTRICTIONS = THIS ROUTINE MUST OBEY ALL THE STANDARD       */
*/*         PROCESSING RULES FOR A TYPE-2 SVC.                       */
*/*                                                                  */
*/*     REGISTER-CONVENTIONS = NONE                                  */
*/*                                                                  */
*/*     PATCH-LABEL = APPATCH                                        */
*/*                                                                  */
*/*   MODULE-TYPE = CSECT                                            */
*/*                                                                  */
*/*     PROCESSOR = VS/2 ASSEMBLER                                   */
*/*                                                                  */
*/*     MODULE SIZE = SEE END OF SOURCE LISTING                      */
*/*                                                                  */
*/*     ATTRIBUTES = RE-ENTRANT                                      */
*/*                                                                  */
*/*   ENTRY-POINT = IGTKAA                                           */
*/*                                                                  */
*/*     PURPOSE = ROUTE VPSS SVC-111 REQUEST TO THE PROPER           */
*/*               PROCESSING ROUTINE.                                */
*/*                                                                  */
*/*     LINKAGE = BRANCH FROM SVC-111, WHICH WAS ENTERED VIA AN      */
*/*               SVC REQUEST.                                       */
*/*                                                                  */
*/*     INPUT = THE FOLLOWING REGISTERS ARE INITIALIZED ON ENTRY --  */
*/*         R0  - FUNCTION CODE (SET BY USER)                        */
*/*         R1  - APRL ADDRESS OR DEB ADDRESS (COMPLEMENTED)         */
*/*               (SET BY USER)                                      */
*/*         R2  - ADDRESS OF DEB                                     */
*/*         R3  - ADDRESS OF CVT                                     */
*/*         R4  - ADDRESS OF CURRENT TCB                             */
*/*         R7  - ADDRESS OF CURRENT ASCB                            */
*/*         R14 - RETURN ADDRESS (ADDRESS OF AN SVC 3)               */
*/*         R15 - ADDRESS OF THIS ROUTINE                            */
*/*         REGISTER  0 - SRB ADDRESS (COMPLEMENTED IF POST STATUS)  */
*/*         REGISTER  1 - IOSB ADDRESS                               */
*/*         REGISTER 14 - RETURN ADDRESS                             */
*/*         REGISTER 15 - ENTRY POINT                                */
*/*         ROUTINE IS ENTERED IN KEY-0, SUPERVISOR STATE,           */
*/*              UNLOCKED, TASK MODE.                                */
*/*                                                                  */
*/*     OUTPUT = THE FOLLOWING REGISTERS ARE PASSED TO THE           */
*/*         SPECIFIED VPSS SVC PROCESSING ROUTINE --                 */
*/*         R0  - FUNCTION CODE (SET BY USER)                        */
*/*         R1  - APRL ADDRESS OR DEB ADDRESS (COMPLEMENTED)         */
*/*               (SET BY USER)                                      */
*/*         R2  - ADDRESS OF DEB                                     */
*/*         R3  - ADDRESS OF CVT                                     */
*/*         R4  - ADDRESS OF CURRENT TCB                             */
*/*         R7  - ADDRESS OF CURRENT ASCB                            */
*/*         R11 - ADDRESS OF APDX ASSOCIATED WITH THIS DEB           */
*/*         R15 - ADDRESS OF THE PROCESSING ROUTINE                  */
*/*         ROUTINE IS ENTERED IN KEY-0, SUPERVISOR STATE,           */
*/*              UNLOCKED, TASK MODE.                                */
*/*         NOTE THAT REGISTER 14 IS NOT VALID.                      */
*/*                                                                  */
*/*   EXIT-NORMAL = EXIT IS VIA A BRANCH TO ONE OF THE FOLLOWING     */
*/*                 ROUTINES (BASED ON REGISTER 0 FUNCTION CODE)--   */
*/*                 REG-0   MODULE   FUNCTION                        */
*/*                   0     IGTKBA   ISSUE VPSS STARTIO REQUEST      */
*/*                   1     IGTKCA   PROCESS 'XLTE' CALL (TRANSLATE) */
*/*                   2     IGTKDA   PROCESS 'RLSE' CALL (RELEASE)   */
*/*                                                                  */
*/*   EXIT-ERROR = CONTROL RETURNS TO THE USER VIA SVC-3 IF          */
*/*                THE SPECIFIED FUNCTION CODE IS INVALID.           */
*/*                APRLFDBK IS SET TO REFLECT THE ERROR.             */
*/*                REGISTER 15 IS SET NON-ZERO.                      */
*/*                                                                  */
*/*   EXTERNAL-REFERENCES = THE FOLLOWING SYSTEM ROUTINES AND        */
*/*                         CONTROL BLOCKS ARE USED.                 */
*/*                                                                  */
*/*     ROUTINES = NONE                                              */
*/*                                                                  */
*/*     DATA-AREAS = NONE                                            */
*/*                                                                  */
*/*     CONTROL-BLOCKS =                                             */
*/*         DEB      - R                                             */
*/*         APRL     - R, M                                          */
*/*         APCT     - R                                             */
*/*         APDX     - R                                             */
*/*                                                                  */
*/*   TABLES = NONE                                                  */
*/*                                                                  */
*/*   MACROS =                                                       */
*/*       MODESET (IN-LINE EXPANSION)                                */
*/*       IGTXEPLG                                                   */
*/*                                                                  */
*/*   CHANGE-ACTIVITY = NONE                                         */
*/*                                                                  */
*/*   MESSAGES = NONE                                                */
*/*                                                                  */
*/*   ABEND-CODES = NONE                                             */
*/*                                                                  */
*/* * END OF SPECIFICATIONS **************************************** */
         EJECT ,
         COPY  IGTXSYSO            SCP DEFINITION
         SPACE 2
         USING IGTKAA,APENTRY      BASE REGISTER
         B     STARTUP             SKIP IN-CORE ID
         DC    YL1(COREIDE-*-1)    * ID LENGTH
         DC    C'IGTKAA'           * PROGRAM NAME
         DC    C' &SYSDATE'        * COMPILE DATE
COREIDE  EQU   *                   END OF ID
         SPACE 2
STARTUP  DS    0H                  START OF PROCESSING CODE
         SPACE ,
*
** ESTABLISH CONTROL BLOCK POINTERS TO
** APRL, APDX, APCT, DEB
*
         USING APRL,R1             APRL PTR SET ON INPUT
         USING DEBBASIC,R2         DEB PTR SET ON INPUT
         L     APDXPTR,DEBIRBAD    VPSS DEB EXTENSION
         USING APDX,APDXPTR        * TELL ASSEMBLER
         L     APCTPTR,APDXAPCT    VPSS CONTROL TABLE
         USING APCT,APCTPTR        * TELL ASSEMBLER
*
** LOCATE PROPER PROCESSING ROUTINE BASED UPON FUNCTION
** CODE SPECIFIED IN REGISTER-0. ALLOWABLE VALUES ARE
** 0-EXECUTE, 1-TRANSLATE, 2-RELEASE.
*
         N     R0,CLEARBIT         CLEAR UNUSED BITS
*                                  * R0= 0-3 NOW
         AR    APENTRY,R0          CHANGE BASE REGISTER BY
         AR    APENTRY,R0          * 2*FUNC_CODE
*
** BASE REGISTER CAN ONLY BE USED TO ACCESS DISPTAB NOW SINCE
** APENTRY VALUE DEPENDS ON FUNCTION CODE (REGISTER 0). ALTHOUGH
** THE NEXT INSTRUCTION APPEARS TO ONLY REFERENCE THE 1ST
** ENTRY IN DISPTAB, IT ACTUALLY LOADS THE PROPER FUNCTION
** ENTRY SINCE THE BASE REGISTER HAS BEEN CHANGED.
*
         LH    APENTRY,DISPTAB     LOAD OFFSET INTO APCT OF
*                                  * ADCON POINTING TO THE
*                                  * PROPER PROCESSING ROUTINE.
         DROP  APENTRY             NO BASE REGISTER EXISTS
         L     APENTRY,APCT(APENTRY) ADDRESS PROCESSING ROUTINE
*                                  * (POINTS TO APCTBR14 IF
*                                  * INVALID FUNCTION CODE).
         BALR  APRETURN,APENTRY    GO TO PROCESSING ROUTINE
*
** CONTROL RETURNS ONLY IF INVALID FUNCTION CODE. VALID
** ROUTINES EXIT VIA AN SVC-3. INVALID FUNCTION CODES
** CAUSE CONTROL TO BE PASSED TO 'APCTBR14' WHICH RETURNS
** IMMEDIATELY.
*
         EJECT ,
*
** INVALID FUNCTION CODE PASSED IN REGISTER-0. FLAG THE
** APRL AND EXIT TO THE CALLER VIA AN SVC 3.
*
         BALR  APCODE,*-*          ESTABLISH BASE REGISTER
         USING *,APCODE            * TELL ASSEMBLER
         AIF   ('&VPSSSYS' NE 'MVS').VS1B MVS EXPANSION ?
         MODESET KEYADDR=DEBPROTG, SWITCH TO USER KEY                  *
               WORKREG=13          * TO MODIFY APRL.
         AGO   .SYSB               SKIP VS1 EXPANSION
.VS1B    ANOP  ,                   VS1 EXPANSION
         MODESET KEY=NZERO         SWITCH TO USER KEY                  *
                                   TO MODIFY APRL.
.SYSB    ANOP  ,                   NOW IN USER PROTECT KEY
         TM    APRLOPT0,APRLNRC    IF RETURN CODES NOT WANTED,
         BO    NOPOST              * SKIP SETTING ERROR INDICATORS
         MVI   APRLRCT,APCCTLOG    LOGICAL ERROR
         MVI   APRLRCC,APCCI       DETECTED BY SVC I/O PROCESSOR
         MVI   APRLRCN,APCCI001    ERROR CODE
         LA    R0,APCMI001         ERROR MESSAGE NUMBER
         STH   R0,APRLMSGN         * IN APRL.
         LA    R0,APCMK000         NO KEYWORD SPECIFIED
         STH   R0,APRLVALN         * SO ELIMINATE IT.
         MVI   APRLECBC,APRLECBP   SHOW POSTED (=X'40)
NOPOST   EQU   *                   APRL POSTING SKIPED
         LA    APRCODE,APCCRERR    REG 15 RETURN CODE (ERROR)
         SVC   3                   EXIT TO CALLER
         SPACE 2
         DROP  ,                   ELIMINATE BASE REGISTERS
         EJECT ,
***********************************************************************
**                                                                   **
**       CONSTANTS                                                   **
**                                                                   **
***********************************************************************
         SPACE ,
CLEARBIT DC    A(X'00000003')      ONLY LEAVE BITS 30-31
         SPACE 3
*
** THIS TABLE CONTAINS THE OFFSETS WITHIN THE APCT OF
** AN ADCON FOR THE ROUTINE TO PROCESS THE FUNCTION
** SPECIFIED IN REGISTER-0. ENTRY 0 CORRESPONDS TO
** FUNCTION CODE OF 0, ETC. INVALID FUNCTION CODES MAP
** TO PASSING CONTROL TO 'APCTBR14' WITH CAUSES AN
** IMMEDIATE RETURN TO THIS PROGRAM FOR ERROR PROCESSING.
*
         USING APCT,0              S-CON BASE REGISTER = 0
DISPTAB  DS    00S                 REG0 FUNCTION
         DC    01S(APCTZKBA)       00 - STARTIO
         DC    01S(APCTZKCA)       01 - TRANSLATE
         DC    01S(APCTZKDA)       02 - RELEASE
         DC    01S(APCTABR)        03 - INVALID FUNCTION
         EJECT ,
***********************************************************************
**                                                                   **
**       DSECTS                                                      **
***********************************************************************
         SPACE ,
         IGTXDSCT IGTXAPRL         VPSS ACB REQUEST
         IGTXDSCT IGTXAPCC         VPSS CALL RETURN CODES
         IGTXDSCT IGTXAPCM         CALL ERROR MESSAGE VALUES
         IGTXDSCT IGTXAPCT         VPSS CONTROL TABLE
         IGTXDSCT IGTXAPDX         VPSS DEB EXTENSION
         EJECT ,
         IEZDEB ,                  DATA EXTENT BLOCK
         EJECT ,
IGTKAA   CSECT ,                   RESUME CSECT CODE
         IGTXEPLG ,                VPSS DSECTS & REGISTERS
TCBPTR   EQU   R4                  POINTER TO CURRENT TCB
         SPACE 5
         END   IGTKAA              MODULE END
