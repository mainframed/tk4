TKBB     TITLE 'IGTKBB - VPSS I/O DRIVER DIE ROUTINE'
*/* * START OF SPECIFICATIONS ************************************** */
*/*                                                                  */
*/*   MODULE-NAME = IGTKBB                                           */
*/*                                                                  */
*/*   DESCRIPTIVE-NAME = DISABLED INTERRUPT EXIT FOR VPSS            */
*/*                      I/O DRIVER                                  */
*/*                                                                  */
*/*   COPYRIGHT = NONE                                               */
*/*                                                                  */
*/*   STATUS = RELEASE 1, MODIFICATION LEVEL 0.                      */
*/*                                                                  */
*/*   FUNCTION = PROCESS THE COMPLETED I/O REQUESTS.  IF THE         */
*/*       I/O EVENT COMPLETED SUCCESSFULLY, RETURN TO IOS+8          */
*/*       INDICATING NO FURTHER IOS PROCESSING IS NECESSARY.         */
*/*       IF THERE IS AN I/O ERROR, RETURN TO IOS+0 WHO WILL         */
*/*       SCHEDULE POST STATUS AND THE ERP TO ATTEMPT TO RESOLVE     */
*/*       THE I/O ERROR. IF COMPLETION IS NORMAL (IOS+8), IOS        */
*/*       WILL RETURN TO THE SECOND ENTRY OF THE DIE, WHO WILL       */
*/*       SCHEDULE NOTIFICATION OF THE USER.                         */
*/*                                                                  */
*/*   NOTES = THE GENERAL INTERFACE SPECIFICATIONS ARE DETERMINED    */
*/*       BY VS/2 (MVS) IOS INTERFACES AS DOCUMENTED IN THE          */
*/*       IOS DESIGN NOTEBOOK.                                       */
*/*                                                                  */
*/*     DEPENDENCIES = NONE                                          */
*/*                                                                  */
*/*     RESTRICTIONS = THIS ROUTINE IS ENTERED PHYSICALLY DISABLED   */
*/*         AND MUST NOT ENABLE SINCE IT EXECUTES AS AN EXTENSION    */
*/*         OF IOS.  THEREFORE THIS MODULE MUST BE FIXED IN REAL     */
*/*         STORAGE.  MODULE IGTUJA WILL INSURE THIS MODULE IS       */
*/*         IN STORAGE.  NO ADDRESSABILITY EXISTS TO THE ADDRESS     */
*/*         SPACE ASSOCIATED WITH THE I/O REQUEST.                   */
*/*                                                                  */
*/*     REGISTER-CONVENTIONS = NONE                                  */
*/*                                                                  */
*/*     PATCH-LABEL = APPATCH.                                       */
*/*                                                                  */
*/*   MODULE-TYPE = CSECT.                                           */
*/*                                                                  */
*/*     PROCESSOR = VS ASSEMBLER.                                    */
*/*                                                                  */
*/*     MODULE SIZE = SEE END OF SOURCE LISTING.                     */
*/*                                                                  */
*/*     ATTRIBUTES = RE-ENTRANT.                                     */
*/*                                                                  */
*/*   ENTRY-POINT = IGTKBB.                                          */
*/*                                                                  */
*/*     PURPOSE = PROCESS THE VPSS COMPLETED I/O REQUESTS.  THIS     */
*/*         DISABLED INTERRUPT EXIT SERVES AS A NORMAL AND           */
*/*         ABNORMAL I/O APPENDAGE.                                  */
*/*                                                                  */
*/*     LINKAGE = BALR FROM IOS.                                     */
*/*                                                                  */
*/*     INPUT =                                                      */
*/*         REGISTER  2 - IOSB ADDRESS                               */
*/*         REGISTER  7 - UCB ADDRESS                                */
*/*         REGISTER 13 - ADDRESS OF 16 WORD SAVE AREA               */
*/*         REGISTER 14 - RETURN ADDRESS                             */
*/*         REGISTER 15 - ENTRY POINT ADDRESS (IGTKBB)               */
*/*         PSW IS KEY-0, SUPERVISOR STATE, DISABLED. LOCK           */
*/*             FOR THE UCB ASSOCIATED WITH THE I/O IS HELD.         */
*/*             SINCE THIS PROGRAM RUNS AS AN EXTENSION TO IOS,      */
*/*             NO ADDRESSIBILITY TO THE ASSOCIATED ADDRESS          */
*/*             SPACE EXISTS.                                        */
*/*                                                                  */
*/*     OUTPUT =                                                     */
*/*         REGISTER  1 - ADDRESS OF NEW SRB TO PLACE ON SOME LCH    */
*/*                       FOR RETURN TO IOS+4 (REDRIVE). OTHERWISE   */
*/*                       THIS REGISTER IS NOT USED.                 */
*/*         REGISTER 0, 2-15 - MUST BE THE SAME AS WHEN THIS         */
*/*                            ROUTINE WAS ENTERED.                  */
*/*         PSW MUST BE THE SAME AS ON INPUT. THE UCB LOCK MUST      */
*/*             STILL BE HELD.                                       */
*/*                                                                  */
*/*   EXIT-NORMAL = RETURN TO IOS AT ADDRESS GIVEN IN REGISTER 14    */
*/*                 PLUS OFFSET OF EITHER 0, 4 OR 8.                 */
*/*       R14+0  - PURSUE NORMAL I/O PROCESSING OF SRB/IOSB          */
*/*                (SCHEDULE POST STATUS).                           */
*/*       R14+4  - PURSUE NORMAL I/O PROCESSING OF SRB/IOSB,         */
*/*                BUT REGISTER 1 CONTAINS THE ADDRESS OF ANOTHER    */
*/*                SRB/IOSB TO BE ENQUEUED ON THE APPROPRIATE        */
*/*                LOGICAL CHANNEL QUEUE.  IT MAY SPECIFY ANOTHER    */
*/*                UCB ADDRESS. IF THE DEVICE CONTROL UNIT AND       */
*/*                CHANNEL ARE AVAILABLE, THE REQUEST WILL BE        */
*/*                STARTED BEFORE EXITING BASIC IOS.  THE FOLLOWING  */
*/*                FIELDS ARE RESET TO ZERO BY IOS PRIOR TO          */
*/*                ENQUEUING AND MUST NOT BE RESET BY THE DIE:       */
*/*                    IOSEX, IOSCC, IOSCSW, IOSPCHN                 */
*/*       R14+8  - IMPLIES IGNORE FURTHER PROCESSING OF THIS         */
*/*                SRB/IOSB. THAT IS, POST STATUS WILL NOT BE        */
*/*                SCHEDULED, NO FURTHER EXIT PROCESSING WILL        */
*/*                BE PERFORMED, AND ERROR RECOVERY WILL NOT BE      */
*/*                ATTEMPTED.  IN THIS INSTANCE, BOTH IOS AND        */
*/*                THE DIE ROUTINE HAVE RESOURCES TO FREE.           */
*/*                THEREFORE TWO ENTRIES TO THE DIE ROUTINE ARE      */
*/*                DEFINED.  FIRST, THE DIE ROUTINE IS ENTERED       */
*/*                FOR NORMAL PROCESSING AND IT RETURNS TO OFFSET    */
*/*                8 INDICATING TO IOS TO DISPOSE OF ANY RESOURCES   */
*/*                ALLOCATED TO THE SRB/IOSB (ERP WORKAREA, ETC.).   */
*/*                AFTER IOS FREES ITS RESOURCES, THE DIE ROUTINE    */
*/*                IS ENTERED AGAIN FOR DISPOSITION OF THE SRB/IOSB. */
*/*                THE DISPOSITION CAN BE OF ANY FORM, E.G. --       */
*/*                RETURNING IT TO A POOL, SCHEDULING THE SRB,       */
*/*                OR RETURNING IT TO IOS AS A NEW REQUEST.  THE     */
*/*                STANDARD RETURN TO IOS FROM THE SECOND EXIT       */
*/*                IS TO REGISTER 14+0 IMPLYING CONTINUE WITHOUT     */
*/*                THE USUAL POST STATUS SCHEDULING.  HOWEVER,       */
*/*                REGISTER 14+4 MAY ALSO BE TAKEN PASSING TO IOS    */
*/*                AN SRB/IOSB (COULD BE THE ONE JUST DISPOSED OF)   */
*/*                WHICH IS TO BE INTRODUCED AS A NEW I/O REQUEST.   */
*/*                   SINCE THE DIE IS AN EXTENSION OF IOS, CARE     */
*/*                MUST BE TAKEN TO INSURE THAT THE IGNORED          */
*/*                REQUEST HAS BEEN ACCOUNTED FOR IF INVOLVED IN     */
*/*                A PURGE QUIESCE OPERATION.  IF THE IOSB           */
*/*                INDICATES THAT A PURGE QUIESCE WAS ACTIVE FOR     */
*/*                THE REQUEST, THE QUIESCE COUNT IN THE IOS PURGE   */
*/*                INTERFACE BLOCK MUST BE DECREMENTED.              */
*/*                   THE IOSB INDICATOR (IOSDIESE) IS USED TO       */
*/*                DETERMINE IF THIS IS THE FIRST OR SECOND TIME     */
*/*                THAT THE DIE HAS BEEN ENTERED FOR THIS INTERRUPT. */
*/*                                                                  */
*/*   EXIT-ERROR = NONE                                              */
*/*                                                                  */
*/*   EXTERNAL-REFERENCES =                                          */
*/*                                                                  */
*/*     ROUTINES = NONE                                              */
*/*                                                                  */
*/*     DATA-AREAS = NONE                                            */
*/*                                                                  */
*/*     CONTROL-BLOCKS =                                             */
*/*         PSA      - R                                             */
*/*         CVT      - R                                             */
*/*         LSMQ     - M (VIA SCHEDULE MACRO)                        */
*/*         SRB      - M                                             */
*/*         IOSB     - R,M                                           */
*/*         UCB      -                                               */
*/*         APIO     - R,M                                           */
*/*                                                                  */
*/*   TABLES = NONE                                                  */
*/*                                                                  */
*/*   MACROS =                                                       */
*/*       SCHEDULE                                                   */
*/*       IGTXEPLG                                                   */
*/*                                                                  */
*/*   CHANGE-ACTIVITY = NONE                                         */
*/*                                                                  */
*/*   MESSAGES = NONE                                                */
*/*                                                                  */
*/*   ABEND-CODES = NONE                                             */
*/*                                                                  */
*/* * END OF SPECIFICATIONS **************************************** */
         EJECT ,
IGTKBB   CSECT ,                   VPSS DIE ROUTINE
         USING *,R15               INPUT BASE
         B     STARTUP             SKIP IN-CORE ID
         DC    YL1(COREIDE-*-1)    * ID LENGTH
         DC    C'IGTKBB'           * MODULE NAME
         DC    C' &SYSDATE'        * ASSEMBLY DATE
COREIDE  EQU   *                   * END OF ID
         SPACE 3
STARTUP  DS    0H                  START OF CODE
*
** INPUT REGISTERS
*
         USING IGTKBB,R15          BASE REGISTER
         USING APIO,R2             APIO FOR CURRENT I/O
*                                  * NOTE: IOSB IS START OF APIO
         USING APSVRG14,R13        SAVEAREA (16-WORDS ONLY)
         USING UCBOB,R7            UCB ASSOCIATED WITH THE I/O
         USING FLC,*-*             0-4095 NEEDS NO BASE
         PUSH  USING               SAVE INPUT USING STATUS
         EJECT ,
***********************************************************************
**                                                                    *
** IF THIS IS SECOND ENTRY, GO TO CLEAN-UP PROCESSING.                *
** IF ERP IN CONTROL OR I/O COMPLETED IN ERROR,                       *
** RETURN TO IOS+0 TO SCHEDULE NORMAL POST STATUS                     *
** PROCESSING. IF I/O WAS SUCCESSFUL (ONLY CE AND                     *
** DE ON), RETURN TO IOS+8 TO BYPASS POST STATUS.                     *
** THIS WILL CAUSE SECOND ENTRY TO DIE WHICH WILL                     *
** SCHEDULE SRB TO POST THE CALLER. THIS IS THE                       *
** MAINLINE PATH OF VPSS I/O PROCESSING.                              *
** IF CHANNEL END AND DEVICE END SEPARATED, ATTEMPT                   *
** TO RECOMBINE, AND THEM PROCESS AS IF THEY OCCURRED                 *
** ON THE SAME INTERRUPT (OR THE CSWS TOGETHER).                      *
**                                                                    *
***********************************************************************
         SPACE 3
         TM    IOSFLB,IOSDIESE     SECOND ENTRY TO DIE ?
         BO    DIE2EXIT            * YES -- GO FREE RESOURCES
         STCK  2(R13)              SAVE BYTES 4-7 OF TOD CLOCK
         L     R1,4(,R13)          * USED BY IGTKBC TO
         ST    R1,APIOTMEE         * CALCULATE I/O LOAD
         TM    IOSFLA,IOSERR       IF ERP IN CONTROL, DO NOT
         BOR   R14                 * PROCESS (NON-MAINLINE)
XRETRY1  EQU   *                   RETRY COMBINED CE/DE STATUS
         LH    R1,CDECE            SEE IF CHANNEL & DEVICE END
         CH    R1,IOSTATUS         * WITH ALL OTHER INDICATORS OFF
         BE    8(,R14)             * (GOOD I/O), SKIP IOS PROCESSING
         EJECT ,
***********************************************************************
**                                                                    *
**       EITHER I/O ERROR OCCURRED OR CHANNEL AND DEVICE END          *
**       SEPARATED.  IF THE FORMER, RETURN TO IOS TO PASS CONTROL     *
**       TO THE ERP.  IF CHANNEL END WITHOUT DEVICE END, SAVE THE     *
**       CURRENT CSW.  IF DEVICE END WITHOUT CHANNEL END, AND THE     *
**       CHANNEL END CSW SAVED, "OR" THE TWO CSW'S TOGETHER AND       *
**       RETRY THE PROCESS.                                           *
**                                                                    *
***********************************************************************
         SPACE 3
         TM    IOSTSA,CXCE+CXDE    CHANNEL & UNIT END SEPARATED ?
         BNMR  R14                 * NO -- RETURN FOR ERP PROCESSING
         TM    IOSTSA,CXCE         CHANNEL END ?
         BO    XCHANEND            * YES -- GO SAVE
*                                  MUST BE DEVICE END
         TM    APIOFLG2,APIOCSWT   CSW SAVED ON CHANNEL END ?
         BZR   R14                 * NO RETURN TO IOS
         NI    APIOFLG2,X'FF'-APIOCSWT CLEAR FLAG
         OC    IOSCSW,APIOTCSW+1   COMBINE CE & DE CSW
         B     XRETRY1             RE-CHECK I/O STATUS
         SPACE 2
XCHANEND EQU   *                   CHANNEL END WITHOUT DEVICE END
         MVC   APIOTCSW,IOSCSW-1   SAVE CSW FOR DE PROCESSING
         OI    APIOFLG2,APIOCSWT   SHOW CSW SAVED ON CE
         BR    R14                 RETURN TO IOS
         SPACE 2
         POP   USING               RESTORE INPUT USING STATUS
         EJECT ,
***********************************************************************
**                                                                    *
**       SECOND EXIT LOGIC FOR VPSS I/O DRIVER DIE.                   *
**       SCHEDULE THE SRB ASSOCIATED WITH THE IOSB TO POST            *
**       OR RESUME THE USER.  THE STANDARD VPSS TERMINATION           *
**       ROUTINE (USED BY POST STATUS) IS ENTERED TO PERFORM          *
**       THE POST OR RESUME (IGTKBC).  USE OF THE DIE BYPASSES        *
**       THE OVERHEAD OF POST STATUS FOR THE MAINLINE I/O PATH        *
**       (NO ERRORS DETECTED).                                        *
**                                                                    *
***********************************************************************
         SPACE 3
         PUSH  USING               SAVE INPUT USING STATUS
         SPACE 3
DIE2EXIT EQU   *                   2ND EXIT LOGIC FOR DIE
** SCHEDULE IGTKBC USING SRB ASSOCIATED
** WITH THE IOSB IN REGISTER 2.
*
** INPUT REGISTERS:
**    1  - ADDRESS OF SRB TO SCHEDULE
** DESTROYED REGISTERS:
**    14, 15, 0, 1
*
         STM   R14,R0,0(APSVPTR)   SAVE DESTROYED REGISTERS
         LR    R14,R15             SWITCH ENTRY POINTS
         DROP  R15                 * TELL THE ASSEMBLER
         USING IGTKBB,R14          * OF THE NEW BASE
         L     R1,APIOTERM         SET TERMINATION ROUTINE ADDR
         ST    R1,SRBEP-SRB+APIOSRB * IN THE SRB (IGTKBC)
         LA    R1,APIOSRB          POINT TO SRB
         SCHEDULE SRB=(1),         SRB ADDRESS IN REGISTER 1           *
               SCOPE=LOCAL         PUT ON LOCAL PRIORITY QUEUE         *
                                   BYPASS POST STATUS
         LM    R14,R0,0(APSVPTR)   RELOAD DESTROYED REGISTERS
         BR    R14                 RETURN TO IOS
         SPACE 2
         POP   USING               RESTORE INPUT USING STATUS
         EJECT ,
***********************************************************************
**                                                                    *
**       CONSTANTS                                                    *
**                                                                    *
***********************************************************************
         SPACE 2
CDECE    DC    01Y(X'0C00')        CHANNEL/DEVICE END ON,
*                                  * ERROR INDICATORS OFF
         EJECT ,
***********************************************************************
**                                                                    *
**       EQUATES                                                      *
**                                                                    *
***********************************************************************
         SPACE 2
CXCE     EQU   X'08'               CHANNEL END INDICATOR IN CSW
CXDE     EQU   X'04'               DEVICE END INDICATOR IN CSW
         EJECT ,
***********************************************************************
**                                                                    *
**       VPSS DSECTS REQUIRED                                         *
**                                                                    *
***********************************************************************
         SPACE 2
         IGTXDSCT IGTXAPIO         VPSS I/O ELEMENT
         IGTXDSCT IGTXAPSV         SAVEAREA FORMAT
         EJECT ,
***********************************************************************
**                                                                    *
**       MAKE IOSB PART OF APIO CONTROL BLOCK                         *
**                                                                    *
***********************************************************************
         SPACE 2
IOSB     DSECT ,                   APIO/IOSB DSECT
         PRINT NOGEN
         IECDIOSB ,                I/O SUPERVISOR BLOCK (IN APIO)
         PRINT GEN
         SPACE 2
IOSB     DSECT ,                   APIO/IOSB DSECT
         ORG   IOSB                APIO STARTS WITH THE IOSB
         IGTXAPIO DSECT=NO         APIO IS PART OF IOSB DSECT
         EJECT ,
         IHASRB ,                  SERVICE REQUEST BLOCK (IN APIO)
         EJECT ,
         PRINT NOGEN
         IHAPSA ,                  LOW CORE (0-4095)
         PRINT GEN
         EJECT ,
         CVT   DSECT=YES           COMMUNICATIONS VECTOR TABLE
         EJECT ,
         PRINT NOGEN
         IHAASVT ,                 ASCB VECTOR TABLE
         PRINT GEN
         EJECT ,
         IEFUCBOB ,                UNIT CONTROL BLOCK FOR I/O
         EJECT ,
IGTKBB   CSECT ,                   RESUME CONTROL SECTION
         SPACE 2
         IGTXEPLG ,                REGISTERS & VPSS DSECTS
         SPACE 3
RTAPSV   EQU   R9                  TEMPORARY SAVEAREA POINTER
         END   ,                   MODULE END
