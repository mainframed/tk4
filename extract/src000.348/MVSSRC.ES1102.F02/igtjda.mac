         TITLE ' IGTJDA - END OF ADDRESS SPACE EXIT ROUTINE.'
*/* * START OF SPECIFICATIONS ************************************** */
*/*                                                                  */
*/*   MODULE-NAME = IGTJDA                                           */
*/*                                                                  */
*/*   DESCRIPTIVE-NAME = END OF ADDRESS SPACE EXIT ROUTINE.          */
*/*                                                                  */
*/*   COPYRIGHT = NONE                                               */
*/*                                                                  */
*/*   STATUS = RELEASE 1, MODIFICATION LEVEL 0.                      */
*/*                                                                  */
*/*   FUNCTION = DETERMINE IF THE TERMINATING ADDRESS SPACE WAS      */
*/*              A USER OF THE 3838, OR THE 3838 MONITOR.            */
*/*                                                                  */
*/*   NOTES =                                                        */
*/*                                                                  */
*/*     DEPENDENCIES = NONE                                          */
*/*                                                                  */
*/*     RESTRICTIONS = NONE                                          */
*/*                                                                  */
*/*     REGISTER-CONVENTIONS = VPSS STANDARD                         */
*/*                                                                  */
*/*     PATCH-LABEL = APPATCH                                        */
*/*                                                                  */
*/*   MODULE-TYPE = EXECUTABLE                                       */
*/*                                                                  */
*/*     PROCESSOR = ASMFX                                            */
*/*                                                                  */
*/*     MODULE SIZE = NONE                                           */
*/*                                                                  */
*/*     ATTRIBUTES = REENTRANT, PAGEABLE                             */
*/*                                                                  */
*/*   ENTRY-POINT = IGTJDA                                           */
*/*                                                                  */
*/*     PURPOSE = DETERMINE IF ADDRESS SPACE WAS A 3838 USER.        */
*/*                                                                  */
*/*     LINKAGE = CALL                                               */
*/*                                                                  */
*/*     INPUT = NONE                                                 */
*/*                                                                  */
*/*     OUTPUT = NONE                                                */
*/*                                                                  */
*/*   EXIT-NORMAL = 1.  IF ADDRESS SPACE WAS NOT A 3838 USER,        */
*/*                     RETURN TO CALL WITH RC = 0.                  */
*/*                                                                  */
*/*                 2.  IF ADDRESS SPACE WAS A 3838 USER, BRANCH     */
*/*                     TO IGTJDG.                                   */
*/*                                                                  */
*/*                 3.  IF ADDRESS SPACE WAS THE 3838 MONITOR,       */
*/*                     BRANCH TO IGTJDD.                            */
*/*                                                                  */
*/*   EXIT-ERROR = NONE                                              */
*/*                                                                  */
*/*   EXTERNAL-REFERENCES =                                          */
*/*                                                                  */
*/*     ROUTINES = IGTJDD END OF ADDRESS SPACE FOR THE 3838 MONITOR. */
*/*                IGTJDG END OF ADDRESS SPACE FOR A 3838 USER.      */
*/*                                                                  */
*/*     DATA-AREAS = APVU                                            */
*/*                                                                  */
*/*     CONTROL-BLOCKS = ASID                                        */
*/*                                                                  */
*/*   TABLES = CVT, APCT, SSCT                                       */
*/*                                                                  */
*/*   MACROS = CVT, IGTXAPCT, IGTXAPVU, IHAASCB, IEFJSCVT            */
*/*                                                                  */
*/*   CHANGE-ACTIVITY = NONE                                         */
*/*                                                                  */
*/*   MESSAGES = NONE                                                */
*/*                                                                  */
*/*   ABEND-CODES = NONE                                             */
*/*                                                                  */
*/* * END OF SPECIFICATIONS **************************************** */
IGTJDA   CSECT ,                  START OF END OF TASK PROCESSOR
         SAVE  (14,12),,*         SAVE THE REGS.
         BALR  APCODE,0
         USING *,APCODE
         LR    R4,R0               ADDRESS OF SSCT.
         USING SSCT,R4
         USING APCT,APCTPTR
         USING APVU,R11
         USING SSOB,R1             PRT TO THE SSOB
         USING SSOBEXT,R10         PRT TO THE SSOB EXTENSION FOR EOM.
         L     APCTPTR,SSCTSUSE   PTR TO THE APCT.
         L     R11,APCTAPVU      PTR TO THE APVU.
         LTR   R11,R11             VALIDATE EXTANT APVU.
         BZ    RETURN              EXIT IF NOT THERE.
         CLC   APVUID,=C'APVU'     SEE IF IDENTIFIER FIELD OK
         BNE   RETURN              EXIT IF NOT.
         L     R10,SSOBINDV        ADDRESS OF SSOB EXTENSION.
         LH    R2,SSENASID         GET THE ADDRESS SPACE ID OF THE
*                                 TERMINATING TASK.
         AR    R2,R2            MULTIPLY BY 4.
         AR    R2,R2
         LA    R2,APVUAUSE-4(R2)   ADDRESS OF SLOT FOR THIS ADDR SPACE
         TM    0(R2),APVUVUSE+APVUAPIO+APVUMON IS IT A USER OR MONITR
         BC    5,GOTOIT           BRANCH IF IT IS.
RETURN   RETURN (14,12),RC=0       RETURN TO CALLER.
         EJECT
GOTOIT   DS    0H                 TERMINATING ADDRESS SPACE WAS A 3838
*                                 USER OR THE MONITOR.  MUST DETERMINE
*                                 WHICH ONE.
         TM    0(R2),APVUVUSE+APVUAPIO SEE IF THIS IS A 3838 USER
         BC    5,GOTOJDG          BRANCH TO USER ROUTIE IF IT IS USER.
GOTOJDD  DS    0H                 TERMINATING TASK WAS A 3838 MONITOR.
*        WHEN WE GO TO IGTJDD THE FOLLOWING WILL BE TRUE.
*        R15 CONTAINS THE ADDRESS OF IGTJDD.
*        THE REMAINDER OF THE REGS ARE THOSE AT THE TIME OF THE CALL
*              OF THIS ROUTINE(IGTJDA).  THEREFORE, IGTJDD MAY RETURN
*              DIRECTLY TO THE CALLER.
         L     R15,APCTZJDD      ADDRESS OF IGTJDD.
         L     R14,12(,R13)     RESTORE R14.
         LM    0,12,20(R13)        RESTORE REGS 0 THRU 12.
         BR    R15
         EJECT
GOTOJDG  DS    0H                 TERMINATING TASK WAS A 3838 USER.
*        WHEN WE GO TO IGTJDG THE FOLLOWING WILL BE TRUE.
*        R15 CONTAINS THE ADDRESS OF IGTJDG.
*        THE REMAINDER OF THE REGS ARE THOSE AT THE TIME OF THE CALL
*              OF THIS ROUTINE(IGTJDA).  THEREFORE, IGTJDG MAY RETURN
*              DIRECTLY TO THE CALLER.
         L     R15,APCTZJDG      ADDRESS OF IGTJDG.
         L     R14,12(,R13)     RESTORE R14.
         LM    0,12,20(R13)        RESTORE REGS 0 THRU 12.
         BR    R15
         EJECT
         IGTXAPCT LIST=YES
         EJECT
         IGTXAPVU LIST=YES
         EJECT
         IEFJSCVT
         EJECT
         IEFJSSOB EN,CONTIG=NO
         EJECT
IGTJDA   CSECT ,                   OUR  CSECT
         IGTXEPLG
         END
