         TITLE ' IGTJDJ - END OF TASK EXIT ROUTINE.'
*/* * START OF SPECIFICATIONS ************************************** */
*/*                                                                  */
*/*   MODULE-NAME = IGTJDJ                                           */
*/*                                                                  */
*/*   DESCRIPTIVE-NAME = END OF TASK EXIT ROUTINE.                   */
*/*                                                                  */
*/*   COPYRIGHT = NONE                                               */
*/*                                                                  */
*/*   STATUS = RELEASE 1, MODIFICATION LEVEL 0.                      */
*/*                                                                  */
*/*   FUNCTION = DETERMINE IF ADDRESS SPACE OF THE TERMINATING       */
*/*              TASK WAS A USER OF THE 3838.                        */
*/*                                                                  */
*/*   NOTES =                                                        */
*/*                                                                  */
*/*     DEPENDENCIES = NONE                                          */
*/*                                                                  */
*/*     RESTRICTIONS = NONE                                          */
*/*                                                                  */
*/*     REGISTER-CONVENTIONS = VPSS STANDARD                         */
*/*                                                                  */
*/*     PATCH-LABEL = APPATCH                                        */
*/*                                                                  */
*/*   MODULE-TYPE = EXECUTABLE                                       */
*/*                                                                  */
*/*     PROCESSOR = ASMFX                                            */
*/*                                                                  */
*/*     MODULE SIZE = NONE                                           */
*/*                                                                  */
*/*     ATTRIBUTES = REENTRANT, PAGEABLE                             */
*/*                                                                  */
*/*   ENTRY-POINT = IGTJDJ                                           */
*/*                                                                  */
*/*     PURPOSE = DETERMINE IF ADDRESS SPACE WAS A 3838 USER.        */
*/*                                                                  */
*/*     LINKAGE = CALL                                               */
*/*                                                                  */
*/*     INPUT = NONE                                                 */
*/*                                                                  */
*/*     OUTPUT = NONE                                                */
*/*                                                                  */
*/*   EXIT-NORMAL = 1.  IF ADDRESS SPACE WAS NOT A 3838 USER,        */
*/*                     RETURN TO CALL WITH RC = 0.                  */
*/*                                                                  */
*/*                 2.  IF ADDRESS SPACE WAS A 3838 USER, BRANCH     */
*/*                     TO IGTJDM.                                   */
*/*                                                                  */
*/*   EXIT-ERROR = NONE                                              */
*/*                                                                  */
*/*   EXTERNAL-REFERENCES =                                          */
*/*                                                                  */
*/*     ROUTINES = IGTJDM                                            */
*/*                                                                  */
*/*     DATA-AREAS = APVU                                            */
*/*                                                                  */
*/*     CONTROL-BLOCKS = ASID                                        */
*/*                                                                  */
*/*   TABLES = CVT, APCT, SSCT                                       */
*/*                                                                  */
*/*   MACROS = CVT, IGTXAPCT, IGTXAPVU, IHAASCB, IEFJSCVT            */
*/*                                                                  */
*/*   CHANGE-ACTIVITY = NONE                                         */
*/*                                                                  */
*/*   MESSAGES = NONE                                                */
*/*                                                                  */
*/*   ABEND-CODES = NONE                                             */
*/*                                                                  */
*/* * END OF SPECIFICATIONS **************************************** */
IGTJDJ   CSECT ,                  START OF END OF TASK PROCESSOR
         SAVE  (14,12),,*         SAVE THE REGS.
         BALR  APCODE,0
         USING *,APCODE
         LR    R4,R0               ADDRESS OF SSCT.
         USING SSCT,R4
         USING APCT,APCTPTR
         USING APVU,R11
         USING SSOB,R1             PTR TO THE SSOB.
         USING SSOBEXT,R10         PRT TO THE SSOB EXTENSION FOR EOT.
         L     APCTPTR,SSCTSUSE   PTR TO THE APCT.
         L     R11,APCTAPVU        PTR TO THE APVU.
         LTR   R11,R11             VALIDATE THE EXISTANCE OF THE APVU.
         BZ    RETURN              IF NOT EXTANT, RETURN
         CLC   APVUID,=C'APVU'     SEE IF ID FIELD IS VALID.
         BNE   RETURN              EXIT IF NOT.
         L     R10,SSOBINDV        PTR TO THE SSOB EXTENSION.
         LH    R2,SSETASID         GET THE ADDRESS SPACE ID OF THE
*                                 TERMINATING TASK.
         AR    R2,R2               MULTIPLY BY 4.
         AR    R2,R2
         LA    R2,APVUAUSE-4(R2)   ADDRESS OF SLOT FOR THIS ADDR SPACE
         TM    0(R2),APVUVUSE+APVUAPIO SEE IF THIS IS A 3838 USER
         BC    5,GOTOJDM           BRANCH IF YES
RETURN   RETURN (14,12),RC=0       RETURN TO CALLER.
         EJECT
GOTOJDM  DS    0H                 TERMINATING TASK WAS A 3838 USER.
*        WHEN WE GO TO IGTJDM THE FOLLOWING WILL BE TRUE.
*        R15 CONTAINS THE ADDRESS OF IGTJDM.
*        THE REMAINDER OF THE REGS ARE THOSE AT THE TIME OF THE CALL
*              OF THIS ROUTINE(IGTJDJ).  THEREFORE, IGTJDM MAY RETURN
*              DIRECTLY TO THE CALLER.
         L     R15,APCTZJDM        ADDRESS OF IGTJDM.
         L     R14,12(,R13)        RESTORE R14.
         LM    0,12,20(R13)        RESTORE REGS 0 THRU 12.
         BR    R15
         EJECT
         IGTXAPCT LIST=YES
         EJECT
         IGTXAPVU LIST=YES
         EJECT
         IEFJSCVT
         EJECT
         IEFJSSOB ET,CONTIG=NO
         EJECT
IGTJDJ   CSECT ,                   OUR CSECT
         IGTXEPLG
         END
