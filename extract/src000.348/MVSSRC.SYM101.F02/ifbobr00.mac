 TITLE 'IFBOBR00 - IGE0025F/IGE0625F - OBR/MDR RECORDER'
         EJECT
*STATUS: CHANGE LEVEL 000 (MODULE SUPPORT CODE Y02021)                *
*FUNCTION:                                                            *
*        THE OBR/MDR MODULE FORMATS RECORDS ON A PERMANENT I/O        *
*        DEVICE ERROR (LONG OBR), ON A STATISTICS COUNTER OVERFLOW    *
*        CONDITION (SHORT OBR), OR A MISCELLANEOUS DATA RECORDING     *
*        (MDR).  IF PROCESSING AN OBR RECORDING, LONG OR SHORT,       *
*        OBR/MDR WILL INTERROGATE THE DEFERRED INCIDENT RECORDING     *
*        QUEUE (PAGING DATA SET ERROR RECORDINGS), AND WILL FORMAT    *
*        ANY THAT ARE PRESENT.                                        *
*        THE RECORDING TO SYS1.LOGREC IS TEMPORARILY QUEUED BY THE    *
*        RECORDING REQUEST ROUTINE OF THE ASYNCHRONOUS RECORDING      *
*        FACILITY VIA THE RECORD MACRO.  THE ACTUAL RECORDING OF THE  *
*        RECORD IS DONE BY SVC76 VIA THE RECORDING TASK ROUTINE OF    *
*        THE ASYNCHRONOUS RECORDING FACILITY.                         *
*        OBR/MDR IS CALLED BY IBM SUPPLIED ERROR RECOVERY PROCEDURE   *
*        ROUTINES.  WORK STORAGE IS OBTAINED VIA THE GETMAIN MACRO.   *
*        AN ESTAE EXIT IS ESTABLISHED IF THE GETMAIN IS SUCCESSFUL.   *
*        THE ESTAE EXIT IS USED TO FREEMAIN IF A PROGRAM CHECK OCCURS *
*        WITHIN OBR/MDR.                                              *
*        THE TYPE OF RECORD TO BE PROCESSED (OBR OR MDR) IS           *
*        DETERMINED BY THE EWAMDR FLAG OF EWAFLG2.                    *
*        IF OBR, THE RECORD IS FORMATTED, AND QUEUED VIA THE RECORD   *
*        MACRO.                                                       *
*        IF MDR, THE RECORD IS COMPLETED, AND QUEUED VIA THE RECORD   *
*        MACRO.  MDR RECORDS MAY BE PARTIALLY PREFORMATTED DEPENDING  *
*        UPON THE DEVICE TYPE OR ACCESS METHOD IN USE.                *
*                                                                     *
*                                                                     *
*ENTRY POINT:                                                         *
*        CONTROL IS PASSED TO THE FIRST EXECUTABLE STATEMENT OF THIS  *
*        MODULE FROM THE ERP THROUGH A SPECIAL XCTL INTERFACE.        *
*                                                                     *
*                                                                     *
*INPUT:                                                               *
*        IOSB POINTER IN REGISTER 1.                                  *
*                                                                     *
*                                                                     *
*OUTPUT:                                                              *
*        SYS1.LOGREC RECORDING VIA THE RECORD MACRO.                  *
*                                                                     *
*                                                                     *
*EXTERNAL REFERENCES:                                                 *
*        RECORDING REQUEST ROUTINE, IO CORE MANAGER, ESTAE, GETMAIN.  *
*                                                                     *
*                                                                     *
*EXITS,NORMAL: OBR                                                    *
*              1. IF DDR IS REQUESTED OR ERROR WAS A TCAM EOD, THE    *
*              BINARY EQUIVALENT OF THE MODULE NAME IS LOADED INTO    *
*              REGISTER 13, AND OBR/MDR BRANCHES TO THE SPECIAL ERROR *
*              XCTL ROUTINE.                                          *
*              2. ISSUES SVC15 FOLLOWED BY SVC3.                      *
*                                                                     *
*              MDR                                                    *
*              1. PLACES 'TPR' IN EBCDIC IN THE FIRST 3 BYTES OF THE  *
*              BUFFER.  PLACES X'00' IN FOURTH BYTE OF THE BUFFER TO  *
*              INDICATE SUCCESS.                                      *
*                 A. ISSUES SVC15 FOLLOWED BY SVC3, OR:               *
*                 B. XCTL'S TO CALLING ROUTINE.
*                                                                     *
*                                                                     *
*EXITS,ERROR:  OBR                                                    *
*              1. NO SPECIAL ERROR EXITS.                             *
*              MDR                                                    *
*              1. PLACES ERROR CODE IN FOURTH BYTE OF BUFFER.         *
*                   X'0F' - SIZE OF FIELD 24 BYTES OR LESS.           *
*                   X'FF' - UNSUCCESSFUL RECORDING.                   *
*                 A. ISSUES SVC15 FOLLOWED BY SVC3.                   *
*                 B. XCTL'S TO CALLING ROUTINE.                       *
*                                                                     *
*                                                                     *
*TABLES/WORK AREAS:                                                   *
*              OBR: GETMAIN AREA IS USED TO BUILD RECORD.             *
*                                                                     *
*              MDR: FOR 2715 AND 3270 (BTAM ACCESS METHOD) THE RECORD *
*              IS FORMATTED IN THE GETMAIN AREA.  ALL OTHER DEVICES   *
*              THE HEADER IS COMPLETED IN THE PREFORMATTED RECORD     *
*              BUFFER PROVIDED BY THE CALLING ROUTINE.                *
*ATTRIBUTES: REENTRANT, PRIVILEGED, NON-RESIDENT, ENABLED             *
*
*CHARACTER CODE DEPENDENCY: THIS MODULE IS CHARACTER CODE DEPENDENT   *
*                            AND MUST BE REASSEMBLED IF MODE CHANGED  *
*                            FROM EBCDIC.                             *
*                                                                     *
* NOTES  - NOT APPLICABLE
         EJECT
*                        LONG OBR RECORD                              *
*    ******************************************************************
*    * CLASS  * SYS.  *                              *                *
* +0 *SOURCE  * RELE- *   S W I T C H E S            *   SPARES       *
*    *        *  ASE  *                              *                *
*    ******************************************************************
*    *                               *                                *
* +8 *          DATE                 *         TIME                   *
*    *                               *                                *
*    ******************************************************************
*    *        *                      *     CPU       *                *
* +16*  NOT   *   CPU SERIAL NO.     *   IDENTITY    *   NOT USED     *
*    *  USED  *                      *               *                *
*    ******************************************************************
*    *                                                                *
* +24*                   JOB IDENTITY                                 *
*    *                                                                *
*    ******************************************************************
*    *                                                                *
* +32*                        FAILING CCW                             *
*    *                                                                *
*    ******************************************************************
*    *                                                                *
* +40*                      CHANNEL STATUS WORD                       *
*    *                                                                *
*    ******************************************************************
*    * DEVICE *                *                                      *
* +48* DENP.  *SECONDARY       *              DEVICE TYPE             *
*    * DATA   *   CUA          *                                      *
*    ******************************************************************
*    * SDR    *                *               *                      *
* +56* BYTE   *PRIMARY         *      I/O      *     SENCE BYTE       *
*    * CNT.   *  CUA           *     RETRIES   *        COUNT         *
*    ******************************************************************
*    *                                                                *
* +64*                        DEVICE                                  *
*    *                                                                *
*    *                                                                *
*    /                                                                /
* +72/                     DEPENDENT                                  /
*    *                                                                *
*    **                                                               *
*    *                                                                *
* +80*                      INFORMATION                               *
*    *                                                                *
*    ******************************************************************
*    *                                                                *
* +88*                   STATISTICS COUNTER AREA                      *
*    *                                                                *
*    ******************************************************************
*    *   STATISTICS   *         STATISTICS COUNTER AREA               *
* +96*   CNTR. AREA   *            (IF MORE THAN 10 BYTES)            *
*    *     (CONT.)    *                                               *
*    ******************************************************************
*    *                               *                                *
*+104*   STATISTICS COUNTER AREA     *       SENSE BYTE DATA          *
*    *          (CONTINUE)           *         (VARIABLE ON DEVICE)   *
*    *********************************                                *
*    *                                                                *
*    /                                                                /
*    /                                                                /
*    *                                                                *
*    ******************************************************************
       EJECT
*                         OBR SHORT RECORD                            *
*     *****************************************************************
*     * CLASS * SYSTEM *                             *                *
* +0  *-------*RELEASE *  S W I T C H E S            *                *
*     *SOURCE *        *                             *                *
*     *****************************************************************
*     *                               *                               *
* +8  *           DATE                *           TIME                *
*     *                               *                               *
*     *****************************************************************
*     *       *                       *               *               *
* +16 *  NOT  *  CPU SERIAL NUMBER    *  CPU IDENTITY *   NOT USED    *
*     *  USED *                       *               *               *
*     *****************************************************************
*     *                               *  SDR   *                      *
* +24 *        DEVICE TYPE            * LENDTH *   CHANNEL UNIT       *
*     *                               *        *            ADDRESS   *
*     *****************************************************************
*     *                                                               *
* +32 /             STATISTIC COUNTER AREA                            /
*     /                                                               /
*     *               (10 OR 20 BYTES)                                *
*     *                                                               *
*     *****************************************************************
         EJECT
*                        T TYPE NON-STANDARD RECORD                   *
*                          FOTMATTED BY - MDR                         *
***********************************************************************
*       *  CLASS/ * SYS./ *             * REC. *                      *
*  +0   *   SOURCE* REL-  *  SWITCHES   *  ID  *   SPARES             *
*       *   (91)  *  EASE *             *      *                      *
*       ***************************************************************
*       *                               *                             *
*  +8   *           DATE                *         TIME                *
*       *                               *                             *
*       ***************************************************************
*       *  NOT    *      CPU            *   CPU      *   NOT          *
*  +16  *   USED  *      SERIAL         *  IDENTITY  *     USED       *
*       *         *        NUMBER       *            *                *
*       ***************************************************************
*       *                                                             *
*  +24  *        DEVICE DEPENDENT DATA  --- VARIABLE                  *
*       *                                                             *
***********************************************************************
       EJECT
*
*      THE FOLLOWING EQUATES ARE FOR GENERAL PURPOSE REGISTERS
*
ZEROREG  EQU   0                       REGISTER  0 USED FOR ADDR'BITITY
R0PARM   EQU   0                       REGISTER  0 PARAMETER
R1PARM   EQU   1                       REGISTER  1 PARAMETER
R2CVTBAS EQU   2                       REGISTER  2 CVT BASE
R3CBASE  EQU   3                       REGISTER  3 CSECT BASE
R4UCB    EQU   4                       REGISTER  4 UCB POINTER
R5WORK   EQU   5                       REGISTER  5 WORK REG
R6WORK   EQU   6                       REGISTER  6 WORK REG
R7WORK   EQU   7                       REGISTER  7 WORK REG
R8RECADR EQU   8                       REGISTER  8 ADDR OF MDR RECORD
R8WORK   EQU   8                       REGISTER  8 WORK REG - ADDR OF
*                                                   SDR AREA OF RECORD
*                                                   PASSED FROM 1STLOAD
R9LOGBAS EQU   9                       REGISTER  9 DSECT BASE
R10SIZE  EQU   10                      REGISTER 10 RECORD SIZE
R10WORK  EQU   10                      REGISTER 10 WORK REG
R11BUFSV EQU   11                      REGISTER 11 MDR BUFFER ADDRESS
R11CNTRL EQU   11                      REGISTER 11 CONTROL BAL
R11WORK  EQU   11                      REGISTER 11 WORK REG
R12IOB   EQU   12                      REGISTER 12 ADDR OF IOB
R13PARM  EQU   13                      REGISTER 13 PARM TO XCTL
R14EXIT  EQU   14                      REGISTER 14 EXIT ADDR
R14LINK  EQU   14                       LINK REGISTER            Y02021
R15CODE  EQU   15                      REGISTER 15 COND CODE
*
*      THE FOLLOWING EQUATES ARE FOR ABSOLUTE VALUES,CONDITION CODE
*        SETTINGS, AND SVC NUMBERS
*
ADDONE   EQU   1                       TO ADD ONE TO ANY COUNT
HALFBYTE EQU   4                       NUM OF BITS IN HALF A BYTE
MAKBIN   EQU   9                       NUM TO MAKE BINARY REPRESENTAION
*                                       OF HEX ALPHABETICS(EG.C1+9=CA)
ENDREG   EQU   20                      NUM OF BITS TO SHIFT CUA TO LOW
*                                       ORDER BYTES OF REG
*                                       ARE 'ON'
EQUAL    EQU   8                       EQUAL CONDITION CODE
TCRECLEN EQU   82                      LEN OF A TCAM RECORD
*                                       ONLY 4 BITS IN A REG
NXTSDRST EQU   256                     NUM OF BYTES IN A STAT TAB SECTN
TCAMREC  EQU   X'34'                   RECORD CLASS/SOURCE - TCAM
ATCMEXT1 EQU   X'28'                   STCAM ERP CODE          @Y30APDZ
ATCMEXT2 EQU   X'6A'                    ATCAMERP CODE          @Y30APDZ
ATCAMREC EQU   X'36'                    RECORD CLASS/COURSE - VTAM
EXIT     EQU   3                       SVC NUM TO RETURN TO SUPERVISOR
ERREXCP  EQU   15                      SVC NUM TO FREE AN RQE
         EJECT
*      THE FOLLOWING EQUATES ARE FOR LENGTHS & DISPLACEMENTS
*
ZERO     EQU   0                       DIS OF ZERO
TWO      EQU   2                       DIS OF TWO
*                                       AND WORD CONTAINING ADDR OF SNS
*                                       TCAM FLAGS
*                                       FLAG BYTE & ADDR OF CCH REC BUF
SIZE     EQU   2                       DIS TO SIZE OF RECORD   Z16953VM
*                                      IN CCH BUFFER RECORD    Z16953VM
ONE      EQU   1                       LENGHT OF ONE
THREE    EQU   3                       LENGHT OF THREE
CUA      EQU   3                       LENGHT OF CUA
FOUR     EQU   4                       LENGHT OF FOUR
SIX      EQU   6                       FOR PACKING PCCA CPUID  Z15459VM
FIVE     EQU   5                        LENGTH OF FIVE         @G30HIMD
SEVEN    EQU   7                                               @Z40RSVS
HEX00    EQU   X'00'                                           @Z40RSVS
EIGHT    EQU   X'08'                    MASK                     Y02021
HEXDF     EQU   X'DF'                                          @AZ08966
HEXC0    EQU   X'C0'                                           @AZ08966
TWELVE   EQU   12                      CONSTANT OF 12          @YM6451P
SIXTEEN  EQU   16                      CONSTANT OF 16          @YM6451P
TWENTY6  EQU   26                      FOR SSL ALL BUT 4 BITS  Z15459VM
TWENTY8  EQU   28                       & MULTIPLYING BY 4.    Z15459VM
OLDSDR   EQU   10                      LENGTH OF OLD STAT TAB ENTRY
NXTSTAD  EQU   2                       LENGTH OF UCB ADDRESSES
DEVTYPE  EQU   2                       LENGTH OF DEV CLASS FLD IN UCB
HDR      EQU   32                       LENGTH OF SHORT RE HDR   X03127
*                                       OF EXPANDED SENSE DATA
*                                       MAGNETIC TAPE EXTENSION
*                                       UCB EXTENSION
SNB040   EQU   4                       DIS TO 4TH SENSE BYTE
SNB020   EQU   2                   DIS TO 2ND SENSE BYTE       @Z40RSVS
EMULATE  EQU   X'08'               DEVICE EMULATION ACTIVE     @Z40RSVS
*                                       ZEUS IN THE ERP WORK AREA
SNSBYT   EQU   1                       LENGTH OF ONE SENSE BYTE
RELLN    EQU   2                       LENGTH OF REL # IN CVT
CSWLEN   EQU   7                       LENGTH OF THE CSW
DBLWRD   EQU   8                       LENGTH OF 2 FULL WORDS
JOBLEN   EQU   8                       LENGTH OF JOB NAME FROM TIOT
CCWLEN   EQU   8                       LENGTH OF A CCW
THREEDBL EQU   24                      LENGTH OF THREE DOUBLE WORDS
FOURDBL  EQU   32                      LENGTH OF 4 DOUBLE WDS  @YM6451P
FIVEDBL  EQU   40                       LENGTH OF 5 DOUBLE WDS  YM5956P
SAVLEN   EQU   72                       LENGTH OF SAVE REG AREA
LOGLEN   EQU   256                     LENGTH OF LOG DSECT - WORK AREA
INCOM    EQU   X'40'                   BIT IN SW1 FLD=SHORT 3670 REC
FOX      EQU   X'0F'                   INSTRUCTION MASK        @YM6451P
VS2IND   EQU   X'80'                    VS2 SYSTEM RELEASE IND.  Y02021
TPEREC   EQU   X'91'                   RECORD CLASS & SOURCE - MDR
LOGCLR   EQU   X'FE'                    CLEAR IOSLOG FLAG      @YM2957P
HEX20    EQU   X'20'                    160 BYTE INDICATOR     @YM2957P
TPERRUNG EQU   X'FE'                   MDR RUNNING FLAG
IOBERCCW EQU   40                      DIS IN IOB=ADDR OF BUFF-2715
SUCCESS  EQU   3                       DIS IN BUFFER=SUCCESS SYM RETRND
RELEN    EQU   2                       LENGTH OF REL # IN CVT PREFIX
MSG      EQU   3                       LENGTH OF 'TPR' SUCCESS MSG
MDRINFO  EQU   6                       LENGTH OF INFO DESCRIBING THE RC
REMHDR   EQU   18                      LENGTH OF THE HEADER REC MINUS 6
REMAINDR EQU   214                     LENGTH OF THE BODY OF THE REC
         EJECT
*
*      THE FOLLOWING EQUATES ARE FOR ABSOLUTE VALUES,COND CODE
*        SETTINGS,AND SVC NUMBERS
*
HALFREG  EQU   16                      NUM OF BITS IN HALF A REGISTER
OK       EQU   X'00'                   SUCCESS SYMBOL=REC WRITTEN OK
TOSMALL  EQU   X'0F'                   SUCCESS SYMBOL=REC LENGTH <24
NOTSUC   EQU   X'FF'                   SUCCESS SYMBOL=REC NOT WRITTEN
ID3670   EQU   X'06'                   RECORD ID FOR A 3670
ID2715   EQU   X'08'                   RECORD ID FOR A 2715
         EJECT
*
*      THE FOLLOWING EQUATES ARE FOR BIT AND MASK SETTINGS
*
OBRREC   EQU   X'30'                   UNIT CHECK - OBR RECORD TYPE
TEMPERR  EQU   X'40'                   BIT IN RECORD TO SIG A TEMP ERR
TIMEBIT  EQU   X'08'                   BIT IN REC TO SIG THAT THE TIME
*                                       MACRO WAS USED
TCAMEXT1 EQU   X'2F'                   TCAM ERP SUFFIX - EXIT ROUTINE
TCAMEXT2 EQU   X'30'                   TCAM ERP SUFFIX - EXIT ROUTINE
TCAMEXT3 EQU   X'31'                   TCAM ERP SUFFIX - EXIT ROUTINE
HIGHBIT  EQU   X'07'                   BITS TO SAVE ONLY THE PHYSICAL
*                                       UNIT ADDR IN MERLIN SENSE BYTE
MERZEUS  EQU   X'01'                   BIT IN INTERNAL FLAGS FOR MER/ZU
TEMPSHRT EQU   X'60'                   BITS IN RECORD TO SIGNIFY A
*                                       TEMPORARY ERROR & A SHORT REC
SAVEUNIT EQU   X'0F'                   MASK TO SAVE UNIT ADDR
SAVECHAN EQU   X'F8'                   MASK TO SAVE CHANNEL ADDR + HIGH
*                                       ORDER BIT OF UNIT ADDR   S99204
NOTVALID EQU   X'0F'                   INVALID SENSE FOR 2314
NUMERIC  EQU   X'F0'                   MASK IN BYTE IF A NUMBER (FO-F9)
NOSDR    EQU   X'08'                   INTERNAL SW IF NO SDR INFO
*                                       IS NEEDED FOR THIS DEVICE
TCAMFLG  EQU   X'0F'                   BITS APPENDED TO ERPFLG IN REC
CLROPT   EQU   X'80'                   BIT IN STAT TAB=DO NOT CLEAR
*                                       OPTION LIMIT BYTE
SHORT    EQU   X'20'                   BIT IN SW FLD OF REC=SHORT OBR
         EJECT
*
*      THE FOLLOWING EQUATES ARE FOR DEVICE CLASSES AND TYPES
*
DASD     EQU   X'20'                   DIRECT ACCESS DEV CLASS
         SPACE
DA23051  EQU   X'06'                   UNIT TYPE - 2305-1 (ZEUS)
DA23052  EQU   X'07'                   UNIT TYPE - 2305-2 (ZEUS)
DA2314   EQU   X'08'                    UNIT TYPE - 2314/2319
DA3330   EQU   X'09'                                           @AZ08966
DA3340   EQU   X'0A'                   UNIT TYPE - 3340 WINCHSR XL03130
DA3350   EQU   X'0B'               UNIT TYPE - 3350            @Z40RSVS
HC0      EQU   X'C0'               MASK FOR CU ADDR            @Z40RSVS
         SPACE
T3400    EQU   X'03'                   UNIT TYPE - 3400(ASPEN)
         SPACE
UR3505   EQU   X'06'                   UNIT TYPE - 3505 - REDLAKE
UR3211   EQU   X'09'                   UNIT TYPE - 3211
UR3525   EQU   X'0C'                   UNIT TYPE - 3525 - PIKLAKE
UR3838   EQU   X'4C'                   GUSHER-3838 DEVICE CODE @ZA26472
UR3886   EQU   X'17'                    UNIT TYPE-3886-SHARK     X03127
UR3850   EQU   X'42'                    UNIT TYPE-3850-SS/1     Y30LPDG
UR3895   EQU   X'19'                    UNIT TYPE-3895-MOHAWK  @G30HIMD
UR3540   EQU   X'44'                    UNIT TYPE-3540-ERIC     Y30OPDP
ARGO     EQU   X'0E'               UNIT TYPE-3800-ARGO         @Z402IVS
ARGERADR EQU   36                  3800 BUFFER ADDRESS         @Z402IVS
         EJECT
IGE0025F CSECT
         USING LOGDSECT,R9LOGBAS
LOGDSECT DSECT
         SPACE
*      STANDARD RECORD HEADER AREA
BLDAREA  DS    CL144                   RECORD FORMAT AREA
RECINFO  EQU   BLDAREA+24              RECORD INFO
CLASRC   EQU   BLDAREA                 CLASS/SOURCE
FLG1EWA  EQU   CLASRC                   PLACE FOR EWA FLAG
SYSREL   EQU   BLDAREA+1               SYS REL NUM
SWITCHES EQU   BLDAREA+2               RECORD INDEPENDENT SWITCHES
SW1      EQU   BLDAREA+3               RECORD
SW2      EQU   BLDAREA+4                      DEPENDENT
SW3      EQU   BLDAREA+5                                SWITCHES
DATE     EQU   BLDAREA+8               DATE
TIME     EQU   BLDAREA+12              TIME
CPUSER   EQU   BLDAREA+16              CPU SERIAL NUMBER
CPUMOD   EQU   BLDAREA+20              CPU MODEL NUMBER
         SPACE
*       SHORT OBR RECORD FIELDS
SDEVTYP  EQU   BLDAREA+24              DEVICE TYPE
SSDRCNT  EQU   BLDAREA+28              # OF SDR BYTES
SCUA     EQU   BLDAREA+29              CHANNEL UNIT ADDRESS
SSDR     EQU   BLDAREA+32              SDR DATA - STAT COUNTERS
         SPACE
*      FULL OBR RECORD FIELDS - UNIT CHECK
JOBID    EQU   BLDAREA+24              JOB NAME
FAILCCW  EQU   BLDAREA+32              FAILING CCW
CSW      EQU   BLDAREA+40              CHANNEL STATUS WORD FIELD
CSW1     EQU   BLDAREA+41              CHANNEL STATUS WORD FIELD + 1
DEVDEPC  EQU   BLDAREA+48              # OF DBL WRDS IN DEV DEP DATA AR
SECUA    EQU   BLDAREA+49              SECONDARY/LOGICAL CHAN UNIT ADDR
SECUA0C  EQU   BLDAREA+50              CHANNEL PORTION OF SEC/LOG CUA
DEVTYP   EQU   BLDAREA+52              DEVICE TYPE
SDRCNT   EQU   BLDAREA+56              # OF SDR BYTES
PCUA     EQU   BLDAREA+57              PRIMARY CHAN UNIT ADDR
PCUA0C   EQU   BLDAREA+58              CHANNEL PORTION OF PRIM/PHYSICAL
PCUAUA   EQU   BLDAREA+59              UNIT ADDR PORTION OF PRIM/PHYS
IORETRY  EQU   BLDAREA+60              # OF I/O RETRIES(DOS ONLY)
SENSCNT  EQU   BLDAREA+62              # OF BYTES OF
SENSCNT1 EQU   BLDAREA+63                             SENSE DATA
         SPACE
*        DEVICE DEPENDENT DATA AREA - DIRECT ACCESS - 3 DOUBLE WORDS
VOLABEL  EQU   BLDAREA+64              VOLUME ID
LASTSK   EQU   BLDAREA+72              LAST SEEK ADDRESS
HOMADDR  EQU   BLDAREA+80              HOME ADDRESS READ
SDR      EQU   BLDAREA+88              SDR WORK AREA - STAT COUNTERS
         SPACE
*        DEVICE DEPENDENT DATA AREA - 3211 PRINTER - 1 DOUBLE WORD
IDTYPE  EQU    BLDAREA+64              CORRELATION NUMBER
         EJECT
*        DEVICE DEPENDENT DATA AREA - 3400 TAPE - ASPEN - 3 DBL WORDS
AVOLABEL EQU   BLDAREA+64              VOLUME LABEL
ADEVDPDT EQU   BLDAREA+72              SPECIAL IN-CORE CNTRS FOR 3400'S
         SPACE
*        DEVICE DEPENDENT DATA AREA - TCAM OBR
TSIOCNT  EQU   BLDAREA+64              # OF START I/O INST
TTEMPERR EQU   BLDAREA+66              # OF TEMPORARY ERRORS
TMASK    EQU   BLDAREA+67              LINE MASK
TOP1     EQU   BLDAREA+68              TCAM OP
TOP2     EQU   BLDAREA+69                      CODES
TFLAGS   EQU   BLDAREA+70              FLAG BYTE
TGRAPHIC EQU   BLDAREA+71              GRAPHIC CHARACTER -FROM LCBSENS1
TTERMNAM EQU   BLDAREA+72              TERMINAL NAME
TSENSE0  EQU   BLDAREA+80              SENCE BYTE ZERO - FROM LCBSENS0
TSENSE1  EQU   BLDAREA+81              SENCE BYTE ONE - FROM LCBSNSV
*      AREA USED FOR CHANNEL ERROR MESSAGE
MSGAREA  EQU   BLDAREA+16              CHANNEL ERROR MSG
MSGCUA   EQU   MSGAREA+38              CUA PORTION OF MSG
MSGCUA1  EQU   MSGCUA+1                 CUA OFFSET IN MSG        XM0462
CUAVAL   EQU   1                        CUA UNIT ADDR VALID      XM0462
MSGBLAME EQU   MSGAREA+51              BLAME FIELD PORTION OF MSG
MSGOPCD  EQU   MSGAREA+56              FAILING OP CODE PORTION OF MSG
MSGSTAT  EQU   MSGAREA+59              FAILING DEV/CHAN STATUS
MSGTIME  EQU   MSGAREA+63              TIME STAMP
         SPACE
*      THE FOLLOWING ARE USED AS WORK AREAS FOR INITIALIZING THE MSG
UNPKAREA EQU   MSGAREA+79              UNPACK WORK AREA
STAT     EQU   UNPKAREA-1              PACKED STATUS
OPCD     EQU   UNPKAREA-2              PACKED OP CODE
UNPKOPCD EQU   UNPKAREA                UNPACKED OP CODE POSITION
UNPKSTAT EQU   UNPKAREA+2              UNPACKED STATUS POSITION
UNPKCUA   EQU  UNPKAREA+3              UNPACK CUA AREA
         SPACE
*      THE FOLLOWING MSG AREAS ARE USED TO FORMAT THE TIME STAMP
TIMEAREA EQU   MSGTIME                 TIME WORK AREA
*              UNPACKED TIME =   HHMMSSTH
UNPKHH   EQU   TIMEAREA+3              UNPACKED HOURS - HH
UNPKMM   EQU   TIMEAREA+5              UNPACKED MINUTES - MM
UNPKSS   EQU   TIMEAREA+7              UNPACKED SECONDS - SS
UNPKTEN  EQU   TIMEAREA+9              UNPACKED TENTHS OF A SECOND - T
UNPKHUN  EQU   TIMEAREA+10             UNPACKED HUNDREDS OF A SECOND -H
*              FORMATED TIME = HH.MM.SS
FORHH    EQU   TIMEAREA+1              FORMATED HOURS - HH
FRSTDOT  EQU   TIMEAREA+3              FIRST PERIOD - AFTER HH
FORMM    EQU   TIMEAREA+4              FORMATED MINUTES - MM
SECDOT   EQU   TIMEAREA+6              SECOND PERIOD - AFTER MM
FORSS    EQU   TIMEAREA+7              FORMATED SECONDS - SS
         SPACE 2
SAVEREGS DS    CL20                     SAVE AREA                Y02021
WORKAREA EQU   SAVEREGS                 WORK AREA                Y02021
STAELIST DS    CL16                     STAE LIST AREA           Y02021
ESTAPARM DS    2F                       STAE PARAM LIST AREA     Y02021
         DS    CL50                     ALIGHNMENT
FLAGS    DS    CL1'0'                   INTERNAL FLAGS
*        MRRLIN/ZEUS     -  X'01'
*        CCH MESSAGE     -   X'02'
*        BYPASS WRITTING REC-X'04'     FOR 2ND LOAD
*        BYPASS SDR INFO    -X'08'     FOR 2ND LOAD
INTSW    DS    CL1                      INTERNAL SWITCH          Y02021
GOTCORE  EQU   X'01'                    GETMAIN OK IND           Y02021
TCAMTYPE EQU   X'04'                    TCAM RECORDING           Y02021
MDRTYPE  EQU   X'08'                    MDR RECORDING            Y02021
CLRDIR   EQU   X'FD'                    CLEAR DIR BIT            Y02021
IOSBADDR DS    CL4                      IOSB POINTER             Y02021
OBRECB   DS    CL4                      SAVE
REG2715  EQU   OBRECB                   2715 SAVE AREA
BINXCTL  DS    CL3                      SPECIAL XCTL SAVE AREA
         DS    CL1                      SPARE                    Y02021
SAVEPTR  DS    CL4                      POINTER TO REG SAVE AREA Y02021
REGSAVE  DS    CL72                     STANDARD REG SAVE AREA @YM2957P
WRK1AREA EQU   REGSAVE                  MDR WORK AREA            Y02021
         EJECT
         USING MDRBUFFR,R8RECADR
MDRBUFFR DSECT
         SPACE
BUFFER   DS    CL238                   BUFFER LENGTH           @YM6451P
*      THE FOLLOWING FIELDS ARE COMMON TO ALL MDR BUFFERS - SIX BYTES
BUFSIZE  EQU   BUFFER                  SIZE OF THIS RECORD
BUFSUBID EQU   BUFFER+2                VARIABLE SUB ID
BUFXCTL  EQU   BUFFER+3                SPECIAL XCTL INFO
BUFRECID EQU   BUFFER+5                ID OF DEVICE FOR WHICH RECORD
*                                       WILL BE GENERATED
*      THE FOLLOWING FIELDS ARE AVAILABLE IN A FORMATTED BUFFER
EXTRA18  EQU   BUFFER+6                18 BYTES OF ZEROS-FOR HEADER
FBUFBODY EQU   BUFFER+24               BODY OF RECORD IN FORMATTED BUFF
         SPACE
*      THE FOLLOWING HEADER WILL BE PUT IN THE FIRST 24 BYTES OF BUFFER
MCLASRC  EQU   BUFFER                   RECORD CLASS AND SOURCE  Y02021
MSYSREL  EQU   BUFFER+1                 SYSTEM RELEASE           Y02021
MSWITCHS EQU   BUFFER+2                 SWITCHES                 Y02021
MSW1     EQU   BUFFER+3                 SWITCH FLD               Y02021
MRECID   EQU   BUFFER+4                 RECORD ID                Y02021
MSUBID   EQU   BUFFER+5                 VAR SUBID FIELD LENGTH   Y02021
SPARE    EQU   BUFFER+6                SPARE FIELD
MDATE    EQU   BUFFER+8                 DATE                     Y02021
MTIME    EQU   BUFFER+12                TIME                     Y02021
MCPUSER  EQU   BUFFER+16                CPU SERIAL #             Y02021
MCPUMOD  EQU   BUFFER+20                CPU MODEL #              Y02021
BUFINFO  EQU   BUFFER+24               DEVICE DEPENDENT BODY OF RECORD
*      THE FOLLOWING FIELD IS AVAILABLE FOR A NON-FORMATTED BUFFER
*        IE-2715,3270(BTAM)
BUFBODY  EQU   BUFFER+6                BODY OF RECORD
         SPACE
       EJECT
         USING CVTMAP,R2CVTBAS                                   Y02021
         CVT   DSECT=YES,LIST=YES
         EJECT
         USING UCBOB,R4UCB                                       Y02021
         USING UCBUCS,R6WORK
         USING UCBCMEXT,R10WORK                                @YM00142
         IEFUCBOB LIST=YES,PREFIX=YES
         EJECT
         IECDIOCM
         EJECT
         IHASRB
         EJECT
         IHAPSA
         EJECT
         USING IOSB,R12IOB
         IECDIOSB
         EJECT
         USING EWA,R1PARM
         IECDERWA  (ALL)                                        YM7629P
         EJECT
         USING ASCB,R5WORK                                       Y02021
         IHAASCB
         EJECT
IGE0025F CSECT
***********************************************************************
*                                                                     *
*****    ENTRY  TO  OUTBOARD  RECORDER  (OBR)  ROUTINE            *****
*                                                                     *
*                        XCTL LOAD NAME - 025F                        *
*                                                                     *
***********************************************************************
         SPACE 2
         SPACE 2                                               @Y17CSWT
**** MODULE DISTRIBUTION LIBRARY ID AND CREATION DATE.         @Y17CSWT
         USING *,15                                            @Y17CSWT
OBRSTART BAL   R3CBASE,OBRSTL2        INIT BASE REG & BRANCH   @Y17CSWT
         USING *,R3CBASE                AROUND ID & DATE       @Y17CSWT
         DROP  15                      DROP TEMP BASE REG      @Y17CSWT
         DC    AL1(OBRSTL2-OBRSTL1)    LENGTH OF MOD ID & DATE @Y17CSWT
OBRSTL1  DC    CL8'IFBOBR00'           MODULE ID               @Y17CSWT
         DC    C'&SYSDATE'             DATE OF COMPILATION     @Y17CSWT
OBRSTL2  DS    0H                      CONTINUE PROCESSING     @Y17CSWT
         SPACE 2                                               @Y17CSWT
         USING OBRPGMCK,R15CODE
         L     R2CVTBAS,CVTPTR         LOAD CVT POINTER
         LR    R12IOB,R1PARM            GET IOSB POINTER         Y02021
         GETMAIN RC,LV=328,SP=255                                Y02021
         LR    R9LOGBAS,R1PARM          GET WORK AREA POINTER    Y02021
         LR    R1PARM,R12IOB            RESTORE REG 1            Y02021
         LTR   R15CODE,R15CODE          TEST FOR RETURN CODE     Y02021
         BZ    YESGET              YES,GETMAIN IS GOOD         @ZA02772
         TM    IOSPKEY,IOSIDR      NO, DIR RECORDING?          @ZA02772
         BO    NOMAIN1             YES,EXIT--IOSB/SRB/EWA CANNOT BE
**                                 FREED                       @ZA02772
         TM    CVTOPTA,CVTNIP      NIP RUNNING?                @ZA02772
         BO    NOMAIN              FREE IOSB                   @ZA02772
*
**     TEST FOR MDR RECORD OR GO CHECK FOR TCAM EOD RTN OR DDR
*
         L     R1PARM,IOSERP            GET EWA ADDRESS          Y02021
         L     R4UCB,IOSUCB             GET UCB ADDRESS          Y02021
         TM    EWAFLG2,EWAMDR           TEST FOR MDR             Y02021
         BZ    TCREC1                   NO-GO TEST FOR TCAM      Y02021
         B     MDR1                     BRANCH TO SPECIAL ENTRY  Y02021
         SPACE 1
YESGET   TM    CVTOPTA,CVTNIP      NIP RUNNING?                @ZA02772
         BZ    CLRMAIN             YES,GO PROCESS              @ZA02772
         TM    IOSPKEY,IOSIDR      DIR RECORDING?              @ZA02772
         BO    DIRTST              YES, GO DELETE IOSB/SRB/EWA @ZA02772
         B     NOMAIN              NO, GO FREE IOSB            @ZA02772
CLRMAIN  XC    BLDAREA(LOGLEN),BLDAREA  CLEAR GOTTEN CORE        Y02021
         XC    REGSAVE(SAVLEN),REGSAVE  CLEAR GOTTEN CORE        Y02021
         ST    R1PARM,IOSBADDR          SAVE IOSB POINTER        Y02021
         MVC   STAELIST(SIXTEEN),ESTALIST    GET LIST TO MAIN    Y02021
         LA    R5WORK,ESTAPARM          GET PARAM LIST ADDR      Y02021
         STM   R2CVTBAS,R12IOB,ESTAPARM SAVE REGISTERS FOR ESTAE Y02021
         LA    R1PARM,STAELIST          GET STAE LIST ADDR       Y02021
         L     R6WORK,PGMCKADD          GET ESTAE RTN ADDR       Y02021
         ESTAE (6),PARAM=(5),MF=(E,(1))
         L     R1PARM,IOSERP            GET EWA ADDRESS          Y02021
DIRENTRY L     R4UCB,IOSUCB             GET UCB ADDRESS          Y02021
         SR    R15CODE,R15CODE          CLEAR CODE REG           Y02021
         TM    EWAFLG2,EWAMDR           TEST FOR MDR             Y02021
         BO    MDR                      YES - BRANCH             Y02021
*
**     ROUTINE TO BUILD THE RECORD HEADER - FIRST 24 BYTES
*
         TM    IOSFLB,IOSSDR       NO OBR RECORDING?           @Z402IVS
         BO    DIRTST              YES,EXIT                    @Z402IVS
         MVI   CLASRC,OBRREC           SET RECORD TYPE - CLASS/SOURCE
         TM    IOSFLA,IOSEX+IOSERR PERMANENT ERROR?            @ZA06044
         BM    SETREL              YES, BYPASS SETTING TEMP FLAG
         OI    SW1,TEMPERR             SET TEMPERARY ERROR SW    A44509
         SPACE
SETREL   LR    R11WORK,R2CVTBAS        LOAD CVT ADDR
         SH    R11WORK,CONST4          SUBTRACT TO ADDR OF REL #
         PACK  WORKAREA(DBLWRD),ZERO(RELLN,R11WORK)   PACK REL #
         CVB   R11WORK,WORKAREA         CONVERT REL # TO BINARY  Y02021
         STC   R11WORK,SYSREL          STORE REL # IN REC
         OI    SYSREL,VS2IND            SET VS2 IND IN RECORD    Y02021
         TIME  DEC                     GET TIME AND DATE
         ST    R0PARM,TIME             STORE TIME IN REC
         ST    R1PARM,DATE             STORE DATE IN REC
         MVI   SWITCHES,TIMEBIT        SET'TIME MACRO USED' SW IN REC
         EJECT                                                 Z15459VM
*                                                              Z15459VM
**** OBTAIN CPUID OF MACHINE ON WHICH ERROR OCCURED.           Z15459VM
*                                                              Z15459VM
         LA    R4UCB,CPUSER            R4=@ CPUID FIELD IN OBR Z15459VM
         BAL   R6WORK,CPUIDRTE         OBTAIN CPUID FOR ERP CPUZ15459VM
         SPACE
         MVC   SECUA0C(TWO),UCBCHA      MOVE SEC/ALT CUA TO REC  Y02021
         MVC   DEVTYP(FOUR),UCBTYP      MOVE DEV TYPE TO REC     Y02021
         SPACE 2
*
*      FIND PRIMARY/PHYSICAL CHANNEL UNIT ADDRESS OF FAILING DEVICE
*
         MVC   PCUA0C(TWO),EWACHA  MOVE IN EWA CUA             @ZA03185
         OC    PCUA0C(TWO),PCUA0C  IS IT ZERO?                 @ZA03185
         BNZ   EWACUA              NO, EXIT                    @ZA03185
         LA    R5WORK,CUA              LOAD NUM OF CHAR IN A CUA
         L     R6WORK,UCBNAME-ONE       LOAD NUM OF CHAR IN CUA  Y02021
LABL1    STC   R6WORK,SAVEREGS         STORE CHAR OF CUA
         TM    SAVEREGS,NUMERIC        IS IT NUMERIC
         BO    LABL2                   YES - BR TO CONTINUE
         LA    R6WORK,MAKBIN(R6WORK)   ADD 9 TO LOW ORDER BITS OF BYTE
LABL2    SRDL  R6WORK,HALFBYTE         SHIFT 4 LOW ORDER BITS TO NXT RG
         SRL   R6WORK,HALFBYTE         SHIFT OUT UNWANTED BITS
         BCT   R5WORK,LABL1            BR IF THERE ARE MORE CHAR'S
         SRL   R7WORK,ENDREG           SHIFT BINARY CUA TO LOW ORDER
         STH   R7WORK,PCUA0C           STORE PRIM/PHY CUA IN REC
         SPACE
         EJECT
*
*      THE FOLLOWING CHECKS FOR TCAM AND IF A FULL OBR RECORD IS NEEDED
*
EWACUA   EQU   *                                               @ZA03185
         L     R10WORK,UCBEXTPT         UCB EXTENSION POINTER  @YM00142
         CLI   UCBETI,TCAMEXT1          WAS A TCAM ERP USED      Y02021
         BL    TCAM1                   TEST FOR ATCAM CODE     @Y30APDZ
         CLI   UCBETI,TCAMEXT3          WAS A TCAM ERP USED 3705 Y02021
         BH    TCAM2                    TEST FOR ATCAM  ODE    @Y30APDZ
TCREC    MVI   CLASRC,TCAMREC          SET REC TYPE CLASS/SOURCE - TCAM
         TM    EWTCFLG,EWTCOVF+EWTCEOD  TEST FOR OVRFLO OR EOD   Y02021
         BZ    FULLREC                 NO - BR TO MAKE A FULL OBR REC
         B     TAMOFL                  YES - BR TO INIT TCAM ONLY FLDS
TCAM1    CLI   UCBETI,ATCMEXT1          WAS AN ATCAM ERP USED  @Y30APDZ
         BE    ATCMREC                  YES, PUT IN NEW CLASS REC
TCAM2    CLI   UCBETI,ATCMEXT2         WAS AN ATCAM ERP USED   @Y30APDZ
         BNE   TSTPERM                 NO, GO ON PROCESSING    @Y30APDZ
ATCMREC  MVI   CLASRC,ATCAMREC         PUT IN ATCAM CLASS REC ID
         TM    EWAFLG3,X'01'           PERM ERROR              @Y30APDZ
         BO    FULLREC                 YES                     @Y30APDZ
         TM    EWTCFLG,EWTCOVF+EWTCEOD  OVERFLOW-EOD           @Y30APDZ
         BNZ   TAMOFL                  YES                     @Y30APDZ
         OI    EWTCFLG,EWTCOVF         TURN ON OVERFLOW        @Y30APDZ
         B     TAMOFL                                          @Y30APDZ
         SPACE
*      A CHECK IS MADE FOR A FULL OBR RECORD IF ONE OF THE FOLLOWING:
*        .PERMANENT ERROR,I.E.,IOSEX × IOSERR(EXCLUSIVE OR)
*        .LONG OBR REQUESTED,I.E. IOSLOG BIT ON.
*        .ROTATIONAL POSITION SENSING (RPS) DEVICE.(3800 NOT SUPPORTED)
*        .COUNTER OVERFLOW FOR VARIABLE LENGTH SDR DEVICE.
*
TSTPERM  TM    IOSFLA,IOSEX+IOSERR     PERMANENT ERROR?        @ZA06044
         BM    FULLREC             YES, OUTPUT FULL RECORD     @ZA06044
         TM    IOSFLB,IOSLOG           LONG OBR REQUESTED?     Z12990VM
         BO    FULLREC                 YES-CREATE LONG OBR     Z12990VM
         CLI   UCBTBYT3,UCB3UREC   IS THIS A UNIT RECORD DEV?  @Z402IVS
         BNE   TSTPERM1            NO-BYPASS 3800 CHECK        Z12032VM
         CLI   UCBTBYT4,UCB3800    IS IT A 3800 DEVICE?        @Z402IVS
         BE    FULLREC             YES,GO TEST FOR FULL RECORD @Z402IVS
         B     TSTOVFL             NO-BYPASS DASD/RPS CHECK    Z12032VM
TSTPERM1 EQU   *                   CONTINUE W DASD/RPS CHK     Z12032VM
         CLI   UCBTBYT3,UCB3DACC     IS IT DASD?               @AZ07944
         BNE   TSTOVFL               NO TEST CNTR. OVFL.       @AZ07944
         TM    UCBTBYT2,UCBRPS       IS IT RPS?                @AZ07944
         BO    FULLREC               YES FULL OBR              @AZ07944
TSTOVFL  TM    EWAFLG2,EWACOVF       TEST FOR CNTR. OVFL.      @AZ07944
         BZ    TERM                    NO CHECK FOR TCAM       @Z40RSVS
         TM    UCBFL5,UCBVSDR           CHECK FOR VAR LENGTH SDR Y02021
         BZ    SHOBR                   NO - BR TO PROCESS A SHORT OBR
         OI    SW1,TEMPERR             SET TEMP ERROR SW FOR NEW DEVICE
         EJECT
*
*      ROUTINE TO FIND THE FAILING JOD IDENTITY
*
FULLREC  TM    IOSPKEY,IOSIDR      TEST FOR FULL RECORD        @ZA02772
         BZ    FULLRECA                 NO-BRANCH AROUND         Y02021
         MVC   JOBID(JOBLEN),DIRJOB     PUT IN DIR JOB           Y02021
         B     SETPTR                   CONTINUE                 Y02021
         SPACE 1
FULLRECA L     R5WORK,CVTTCBP           GET TABLE POINTER        Y02021
         L     R5WORK,TWELVE(R5WORK)    GET CURRENT ASCB PTR     Y02021
         L     R5WORK,ASCBJBNI          GET PTR TO JOB ID        Y02021
         LTR   R5WORK,R5WORK            TEST FOR ADDRESS         Y02021
         BZ    SETPTR                   NO JOBNAME IF ZERO       Y02021
         MVC   JOBID(JOBLEN),ZERO(R5WORK)    MOVE JOB NAME TO RE Y02021
         SPACE
SETPTR   LA    R8WORK,VOLABEL          LOAD ADDR OF DEVICE DEPENDENT
         SPACE
*
*      ROUTINE TO DETERMINE DEVICE TYPE OF FAILING UNIT
*
         CLI   UCBTBYT3,UCB3UREC        TEST FOR UNIT RECORD     Y02021
         BE    CK3505                  YES - BR TO CK TYPE
         CLI   UCBTBYT3,UCB3TAPE        TEST FOR TAPE            Y02021
         BE    TAPES                   YES - BR TO CK TYPE
         CLI   UCBTBYT3,UCB3DACC        TEST FOR DIRECT ACCESS   Y02021
         BNE   CCWCSW                  NO - BR TO GET CCW/CCW-NO DDD
         L     R5WORK,EWADCNT           GET POINTER TO DEV DEP   Y02021
         LA    R5WORK,FIVEDBL(R5WORK)   POINTER TO DA SENSE     YM5956P
         SPACE
         CLI   UCBTBYT4,DA2314          TEST FOR 2314            Y02021
         BH    MERL                    NO - BUT ITS 370 DA I/O
         BL    MOVESEEK                NO - BR TO MOVE SEEK ADDR
*                                      YES - THIS IS A 2314
         MVC   OBRECB(SNSBYT),SNB040(R5WORK) GET SENSE BYTE 4    Y02021
*                                                      BYTE(CONTAINS
*                                                      PHY UNIT ADDR)
         B     PHYSDEV                 BRANCH AROUND 3330 CODE
OI       OI    OBRECB,HEX00        EXECUTED INSTRUCTION        @Z40RSVS
         SPACE
*                                       I/O AND NO STAT CNTRS   XL03130
MERL     OI    FLAGS,MERZEUS+NOSDR     SET FLAG FOR 2ND LOAD= 370 DA
         CLI   UCBTBYT4,DA3350     TEST FOR                    @Z40RSVS
         BE    MOVESEEK            YES - BR - PHY CUA OK       @Z40RSVS
         CLI   UCBTBYT4,DA3340          TEST FOR WINCHESTER      Y02021
         BE    WIN                 YES                         @AZ08966
         MVC   OBRECB(SNSBYT),SNB040(R5WORK) GET SENSE BYTE 4    Y02021
         TM    SNB020(R5WORK),EMULATE EMULATION ACTIVE ?       @Z40RSVS
         BNO   NOEMU               NO, DONT CONVERT SNSB 4     @Z40RSVS
         CLI   UCBTBYT4,DA3330      3330?                      @AZ08966
         BNE   MOVESEEK                                        @AZ08966
         NI    PCUAUA,HEXDF              MASK OFF BIT 2        @AZ08966
         B     MOVESEEK                                        @AZ08966
WIN      TM    SNB020(R5WORK),EMULATE    IS IT 3344?           @AZ08966
         BNO   MOVESEEK                  UA OK FOR 3340        @AZ08966
         SR    R6WORK,R6WORK             3344 NEEDS SPCL CONV  @AZ08966
         IC    R6WORK,SNB040(R5WORK)     PICKUP SENSE BYTE 4   @AZ08966
         SRDL  R6WORK,THREE               MOVE STRING AND      @AZ08966
         SRL   R6WORK,THREE               DEVISE CODE TO       @AZ08966
         SLDL  R6WORK,THREE               BITS 3 TO 7.         @AZ08966
         NI    PCUAUA,HEXC0          STRIP BITS 2 TO 7         @AZ08966
         STC   R6WORK,OBRECB             SAVE STRING DEV CODE  @AZ08966
         OC    PCUAUA(ONE),OBRECB        MOVE STRING DEV CODE  @AZ08966
         B     MOVESEEK                                        @AZ08966
         SPACE 2
NOEMU    NI    OBRECB,HIGHBIT      AND OUT 5 HIGH ORDER BITS   @Z40RSVS
         SPACE
PHYSDEV  BAL   R11CNTRL,SETPHYUN       BRANCH TO PUT PHYSICAL/PRIMARY
*                                       UNIT ADDR IN RECORD
MOVESEEK MVC   IORETRY(TWO),EWACNTR1    MOVE RETRY COUNT         Y02021
*
*      THE FOLLOWING CHECKS FOR PARTICULAR DEVICES(2305,2321,ETC),
*        FINDS THERE VOL SERS,AND PUTS IT IN THE RECORD
*
         CLI   UCBTBYT4,DA23051         TEST FOR 2305-1          Y02021
         BE    ZVOL                    YES - BR TO GET VOL SER
         CLI   UCBTBYT4,DA23052         TEST FOR 2305-2          Y02021
         BNE   ASPTAP                   NO-GO MOVE DEV DEP       Y02021
ZVOL     OI    FLAGS,MERZEUS+NOSDR      SET FLAGS                Y02021
         B     ASPTAP                   NO-GO MOVE DEV DEP       Y02021
         EJECT
*
*      THE FOLLOWING CHECKS FOR 3400 TAPES AND MOVES VOL SER'S TO REC
*
TAPES    CLI   UCBTBYT4,T3400           TEST FOR ASPEN           Y02021
         BE    ASPTAP                  YES - BR TO GET DEV DEP DATA
         LA    R8WORK,DBLWRD(R8WORK)   BUMP POINTER TO SDR AREA FOR 2ND
         MVI   DEVDEPC,ONE             SET LEN OF DEV DEP AREA IN REC
         SPACE
VOLID    BAL   R14LINK,GETDEVD          GET DEV DEP DATA         Y02021
         B     CCWCSW                  BRANCH TO GET FAILING CCW & CSW
         SPACE
ASPTAP   LA    R8WORK,THREEDBL(R8WORK)  BUMP COUNTER FOR DEV DEP Y02021
         MVI   DEVDEPC,THREE           PUT DBL WRD SIZE OF DEV DEP AREA
         B     VOLID                   BRANCH TO GET VOL SER OF TAPE
         SPACE 3
*
*      ROUTINE TO INITIALIZE FIELDS FOR A SHORT OBR RECORD --- STAT
*        COUNTER OVERFLOW ON A DEVICE WITHOUT VARIABLE LENGTH STATS
*
SHOBR    MVC   SDEVTYP(FOUR),UCBTYP     GET DEV TYPE FLD TO RE   Y02021
         OI    SW1,TEMPSHRT            SET SW FOR TEMP ERR/SHORT RECORD
         CLC   TY2314(DEVTYPE),UCBTBYT3 TEST FOR 2314            Y02021
         BNE   MOVCUA                  NO - BR TO MOVE CUA
         L     R5WORK,EWADCNT           GET POINTER TO DEV DEP   Y02021
         LA    R5WORK,FOURDBL(R5WORK)   GET POINTER TO SENSE     Y02021
         MVC   OBRECB(SNSBYT),FOUR(R5WORK)   GET 4TH SNS BYTE    Y02021
         BAL   R11CNTRL,SETPHYUN       BR TO DETERMINE PHYSICAL CUA
         SPACE
MOVCUA   MVC   SCUA(CUA),PCUA          MOVE PHYSICAL CUA TO PROPER
*                                       PLACE FOR SHORT RECORD
         LA    R8WORK,SSDR             LOAD ADDR OF SDR AREA FOR
*                                       2ND LOAD
         B     OBR1                     BRANCH TO COMPLETE       Y02021
*
*      PICK UP DEVICE DEPENDENT DATA
*
GETDEVD  CLI   EWADCNT,ZERO            CHECK FOR ZERO COUNT    @YM6451P
         BCR   EIGHT,R14LINK           YES-RETURN              @YM6451P
         IC    R5WORK,EWADCNT          GET COUNT               @YM6451P
         BCTR  R5WORK,ZERO              DECREMENT FOR EXECUTE    Y02021
         L     R6WORK,EWADCNT           GET POINTER TO DATA      Y02021
         EX    R5WORK,MOVEDEVD          MOVE DATA                Y02021
         BR    R14LINK                  RETURN                   Y02021
         SPACE 1
MOVEDEVD MVC   IDTYPE(ZERO),ZERO(R6WORK)    MOVE DEV DEP DATA    Y02021
         SPACE 1
         EJECT
*
*      THE FOLLOWING CHECKS FOR CERTAIN UNIT RECORD DEVICES IF:
*        3505,3525 = SET FLAG FOR 125F FOR NO SDR CNTRS
*        3211 = MAINTAIN COUNTER TO RELATE 'T' TYPE ERRORS TO OBR REC'S
*
CK3505   CLI   UCBTBYT4,UR3505          TEST FOR 3505-REDLAKE    Y02021
         BE    SETFLG                  YES - BR TO SET FLG
         CLI   UCBTBYT4,UR3850          TES FOR 3850(OAK,SS/1)  Y30LPDG
         BE    SETFLG                   YES--BR                 Y30LPDG
         CLI   UCBTBYT4,UR3895          TEST FOR 3895          @G30HIMD
         BE    SETFLG                   YES BRANCH             @G30HIMD
         CLI   UCBTBYT4,UR3525          TEST FOR 3525-PIKLAKE    Y02021
         BNE   CK3211                  NO - BR AROUND SETTING FLG
SETFLG   OI    FLAGS,NOSDR             SET FLAG FOR 2ND LOAD-NO STAT
*                                       COUNTERS FOR THIS DEVICE
CK3211   L     R6WORK,UCBXTADR          ADDR OF UCB UCS EXT.     Y02021
         CLI   UCBTBYT4,UR3211          TEST FOR 3211            Y02021
         BE    ROUT3211                 YES BRANCH               X03127
         CLI   UCBTBYT4,UR3540          TEST FOR 3540           Y30OPDP
         BE    ROUT3540                                         Y30OPDP
         CLI   UCBTBYT4,UR3886          TEST FOR 3886            Y02021
         BE    ROUT3540                 YES BRANCH             @G30HIMD
         CLI   UCBTBYT4,UR3895          TEST FOR 3895          @G30HIMD
         BNE   CCWCSW                   NO- GET CCW AND CSW      X03127
         BAL   R14LINK,GETDEVD          GET DEV DEP DATA       @G30HIMD
         LA    R8WORK,FIVEDBL(R8WORK)   BUMP PTR TO SDR AREA   @G30HIMD
         MVI   DEVDEPC,FIVE             SET LENGTH TO 5 DBLWDS @G30HIMD
         B     CCWCSW                   THEN GET CCW AND CSW   @G30HIMD
ROUT3540 BAL   R14LINK,GETDEVD          GET DEV DEP DATA        Y30OPDP
         B     INDVDPCT                 BR TO INC DEV DEP COUNT  X03127
ROUT3211 XR    R5WORK,R5WORK            CLEAR REG                X03127
         IC    R5WORK,UCBERCNT          GET ERR CNT FROM EXT     Y02021
         STC   R5WORK,IDTYPE           STORE ERR CNT IN REC
         LA    R5WORK,ADDONE(R5WORK)   UPDATE ERR CNT(CORRELATION #)
         STC   R5WORK,UCBERCNT          STORE ERR CNT IN EXT     Y02021
         SPACE
INDVDPCT LA    R8WORK,DBLWRD(R8WORK)    BUMP PTR TO SDR AREA     X03127
         MVI   DEVDEPC,ONE             SET LENGTH IN DOUBLE WORDS OF
*                                       THE DEVICE DEPENDENT DATA AREA
         EJECT
*
*      ROUTINE TO FIND CSW AND FAILING CCW FOR ALL DEVICES
*
CCWCSW   CLI   UCBTBYT3,UCB3DACC        TEST FOR DIRECT ACCESS  YM5956P
         BNE   CCWCSW1                  NO-BRANCH AROUND        YM5956P
         L     R5WORK,EWADCNT           GET DEV DEP POINTER     YM5956P
         MVC   FAILCCW(CCWLEN),FOURDBL(R5WORK) GET FAILING CCW  YM5956P
         MVC   CSW1(CSWLEN),THREEDBL+ONE(R5WORK)  GET CSW TO RE  Y02021
         B     OBR1                     GO COMPLET RECORD        Y02021
         SPACE 1
CCWCSW1  CLI   IOSCOD,IOSINTC           TEST FOR INTERCEPT      YM5956P
         BE    CSWMOVE                  YES-GET CSW             YM5956P
CCWCSW2  L     R5WORK,IOSVST            GET CHAN PROG START    @YM2957P
         SPACE
TESTTCAM CLI   CLASRC,TCAMREC          IS THIS A TCAM RECORD
         BE    MVCSWCCW                YES-GET CSW,CCW         @ZA28495
         CLI   CLASRC,ATCAMREC         VTAM RECORD?            @ZA28495
         BNE   CSWTEST            NO - BR AROUND FURTHER TESTS @ZA28495
MVCSWCCW MVC   CSW1(CSWLEN),EWTCCSW     GET TCAM CSW          @ZA28495
         MVC   FAILCCW(CCWLEN),EWTCCCW  GET TCAM CCW             Y02021
         B     TAMOFL                   GO COMPLETE RECORD       Y02021
         SPACE
CSWTEST  CLC   ZEROS(THREE),IOSCSW      CHECK IF CCW ADD ZERO   YM5956P
         BE    CCWMOVE                 YES - BR TO MOVE 1ST CCW IN THE
*                                       CHAN PGM CHAIN-NOT THE FAILING
         L     R5WORK,IOSCC             LOAD CMD ADD FROM CSW    Y02021
CSWTEST2 LA    R7WORK,CCWLEN            LOAD LENGTH OF A CCW     Y02021
         SR    R5WORK,R7WORK           SUBTRACT TO ADDR OF FAILING CCW
         SPACE
CCWMOVE  MVC   FAILCCW(CCWLEN),ZERO(R5WORK) MOVE FAILING CCW TO RECORD
CSWMOVE  MVC   CSW1(CSWLEN),IOSCSW      MOVE CSW TO RECORD       Y02021
         EJECT
TAMOFL   CLI   CLASRC,TCAMREC          IS THIS A TCAM OBR
         BE    TAMOFL1                 YES                     @Y30APDZ
         CLI   CLASRC,ATCAMREC         VTAM RECORD?            @Y30APDZ
         BNE   OBR1                    NO                      @Y30APDZ
*                                      TEST REMOVED   @ZA28495@Y30APDZ
         BAL   R14LINK,GETDEVD         GET DEVICE DEP DATA     @Y30APDZ
         MVI   DEVDEPC,TWO             DEVICE DEP DATA         @Y30APDZ
         TM    EWTCFLG,EWTCOVF+EWTCEOD  TEST FOR EOD OR OVERFLOW
         BNZ   WRTTCAM                 BRANCH IF ONES OR MIZED @Y30APDZ
         MVC   TSENSE0(TWO),EWTCSNL     MOVE IN SENSE          @Y30APDZ
         B     WRTTCAM                 GO WRITE                @Y30APDZ
TAMOFL1  EQU   *                                               @Y30APDZ
         BAL   R14LINK,GETDEVD          GET DEV DEP DATA         Y02021
         OI    TFLAGS,TCAMFLG           SET TCAM FLAG FOR EREP   Y02021
         OI    INTSW,TCAMTYPE           SET TCAM TYPE RECORD     Y02021
         MVI   DEVDEPC,TWO             DBL WRD LEN OF DEV DEP DATA AREA
         TM    EWTCFLG,EWTCOVF+EWTCEOD  TEST FOR EOD OR OVERFLOW Y02021
         BNZ   WRTTCAM                  BRANCH IF ONES OR MIXED  Y02021
         MVC   TSENSE0(TWO),EWTCSNL     MOVE IN SENSE          @YM2957P
         SPACE
*
*       WRITE TCAM RECORD ON LOGREC
*
WRTTCAM  LA    R0PARM,TCRECLEN          LOAD LENGTH OF RECORD    Y02021
         B     SETADR                   BRANCH TO WRITE RECORD   Y02021
*                                       TO SYS1.LOGREC
*
*      ROUTINE TO PUT PHYSICAL/PRIMARY CUA IN OBR RECORD
*
SETPHYUN NI    OBRECB,SAVEUNIT         SAVE UNIT ADDR IN WORK AREA
         NI    PCUAUA,SAVECHAN         SAVE CHANNEL ADDR AND HIGH ORDER
*                                       BIT OF UNIT ADDR IN PCUA S99204
         CLI   OBRECB,NOTVALID         IS SENSE INFO VALID - 2314
         BCR   EQUAL,R11CNTRL          NOT VALID - RETURN
         OC    PCUAUA(ONE),OBRECB      INSERT PHYSICAL UNIT ADDR IN REC
         BR    R11CNTRL                RETURN TO CALLING ROUTINE
         EJECT
OBR1     TM    FLAGS,NOSDR              ANY STAT COUNTERS?       Y02021
         BO    SENSE                   NO - GO GET SENSE DATA
         SPACE 2
*
****     ROUTINE TO CHECK,RECORD, AND CLEAR STATISTICS TABLE       ****
****             FOR THE FAILING DEVICE                            ****
*
         L     R10WORK,UCBEXTPT         UCB EXTENSION POINTER  @YM00142
         XR    R7WORK,R7WORK           CLEAR REG
         IC    R7WORK,UCBSTI            INSERT STAT TAB OFFSET   Y02021
         SPACE 1
         CLC   TY2314(DEVTYPE),UCBTBYT3 TEST FOR 23214           Y02021
         BNE   NOT2314                  NO-BRANCH                Y02021
         LA    R5WORK,OLDSDR            GET COUNT                Y02021
         B     NEWSTAT2                 GO COMPLETE RECORD       Y02021
         SPACE
NOT2314  L     R5WORK,CVTSTB           LOAD ADDR OF STAT TAB
LOOP1    CLM   R4UCB,X'3',ZERO(R5WORK)  IS DEV IN THIS SEC OF STATS
         BL    LOOP2                   YES - INCR ADJUSTED CORRECTLY
         LA    R5WORK,NXTSTAD(R5WORK)  LOAD ADDR OF FIRST UCB IN THE
*                                       NEXT STAT TAB SECTION
         LA    R7WORK,NXTSDRST(R7WORK) BUMP OFFSET PAST THIS SECTION
         B     LOOP1                   BRANCH BACK TO COMPARE ADDRESSES
LOOP2    MH    R7WORK,MULTICON         MULTIPLY OFFSET BY 10
         AL    R7WORK,CVTSTB           ADD ADDR OF STAT TABS TO OFFSET
         SPACE
         TM    UCBFL5,UCBVSDR           DOES THIS DEV HAVE VAR   Y02021
*                                       LENGTH SDR'S
         BO    NEWSTAT                 YES - PROCESS NEW STATS
         MVC   ZERO(OLDSDR,R8WORK),ZERO(R7WORK)  MOVE STAT CTRS TO REC
         XC    ZERO(OLDSDR,R7WORK),ZERO(R7WORK)  CLEAR STAT CTRS
         LA    R5WORK,OLDSDR            GET COUNT OF 10          X03127
         B     NEWSTAT2                 BRANCH TO HANDLE RECORD  X03127
         SPACE
NEWSTAT  IC    R5WORK,ZERO(R7WORK)      GET FLAGS BYTE           X03127
         N     R5WORK,MASK              SAVE LOW ORDER COUNT     X03127
         MH    R5WORK,MULTICON          MULTIPLY BY 10           X03127
         AH    R5WORK,MULTICON          ADD 10                   X03127
         LR    R6WORK,R5WORK            SAVE COUNT               X03127
         BCTR  R5WORK,ZERO              REDUCE COUNT BY 1        X03127
         EX    R5WORK,MOVESTAT          MOVE STAT CTRS TO RECORD X03127
         BCTR  R5WORK,ZERO              REDUCE COUNT BY 1        X03127
         BCTR  R5WORK,ZERO              REDUCE COUNT BY 1        X03127
         EX    R5WORK,CLRSTAT           CLEAR STATS FROM +2      X03127
         TM    ZERO(R7WORK),CLROPT      TEST CLEARING CE MODE    X03127
         BO    NEWSTAT1                 NO-BRANCH AROUND         X03127
         MVI   ONE(R7WORK),ZERO         CLR CE MODE BYTE         X03127
NEWSTAT1 LR    R5WORK,R6WORK            GET COUNT OF STATS       X03127
NEWSTAT2 TM    SW1,SHORT                TEST FOR SHORT RECORD    X03127
         BO    NEWSTAT3                 YES-BRANCH AROUND        X03127
         LA    R8WORK,ZERO(R5WORK,R8WORK)   BUMP POINTER         X03127
         STC   R5WORK,SDRCNT            GET CNT TO LONG RECORD   X03127
         B     SENSE                    CONT. BUILDING RECORD    X03127
         SPACE
NEWSTAT3 STC   R5WORK,SSDRCNT           GET COUNT IN SHORT RE    X03127
         LA    R0PARM,HDR(R5WORK)       GET COUNT OF SHORT RE    X03127
         SPACE
SETADR   LA    R1PARM,BLDAREA          LOAD ADDRESS OF RECORD
SETADRA  LA    R13PARM,REGSAVE          POINTER TO SAVE AREA     Y02021
         SPACE
       RECORD TYPE=LOGREC,DATAADR=(1),LENGTH=(0),BUFFER=YES,RCVRY=ESTAE
         ESTAE 0                                               @ZA06003
DIRTST   TM    IOSPKEY,IOSIDR      TEST FOR DIR RECORD         @ZA02772
         BZ    TERM                                            @ZA02772
         BAL   R6WORK,FREEIOSB     GO FREE IOSB/SRB/EWA        @ZA02772
         BAL   R14LINK,FREECORA    GO FREE GOTTEN CORE         @ZA02772
         SVC   EXIT                RETURN TO SUPERVISOR        @ZA02772
**                                                             @ZA02772
*
*      FREE IOSB AND EWA
*
FREEIOSB L     R7WORK,IOSSRB       GET SRB PTR                 @ZA02772
         MVC   ZERO(FOUR,R7WORK),IOSERP PUT EWA PTR IN CHAIN FLD
         LR    R1PARM,R7WORK       GET SRB POINTER             @ZA02772
         SPACE 1                                               @YM2957P
*     GET LOCAL LOCK TO PROTECT FRR STACK FOR CORE MGR         @YM2957P
*        REGS 11-14 ARE DESTROYED BY SETLOCK                   @YM2957P
*                                                              @YM2957P
         SPACE 1                                               @YM2957P
GETLOCK  SETLOCK   OBTAIN,TYPE=LOCAL,MODE=UNCOND,              @YM2957PX
               RELATED=(IECVSMGR,IGE0025F(FREELOCK))           @YM2957P
         SPACE 1                                               @YM2957P
         USING IOCOM,R5WORK                                    @ZA02772
         L     R5WORK,CVTIXAVL          GET IOS TABLE PTR        Y02021
         L     R15CODE,IOCORMGT         GET IO CORE MGT PTR      Y02021
         LA    R11WORK,HEX20            INDICATE 160 BYTES     @YM2957P
         LA    R13PARM,REGSAVE          SAVE AREA POINTER        Y02021
         BAL   R14LINK,FOUR(R15CODE)    LINK TO CORE MGR         Y02021
         SPACE 1                                               @YM2957P
FREELOCK SETLOCK   RELEASE,TYPE=LOCAL,                         @YM2957PX
               RELATED=(IECVSMGR,IGE0025F(GETLOCK))            @YM2957P
         SPACE 1                                               @YM2957P
         BR    R6WORK              RETURN                      @ZA02772
         DROP  R5WORK                                          @ZA02772
         SPACE 2
         EJECT
*
****     ROUTINE TO PUT SENSE BYTES OF FAILING UNIT IN RECORD      ****
*
SENSE    MVI   SENSCNT,ZERO             INITIALIZE COUNT TO ZERO Y02021
         L     R10WORK,UCBEXTPT         UCB EXTENSION POINTER  @YM00142
         SR    R6WORK,R6WORK            CLEAR WORK REG           Y02021
         IC    R6WORK,UCBSNSCT          GET SENSE COUNT          Y02021
         CLI   UCBTBYT3,UCB3DACC        TEST FOR DIRECT ACCESS   Y02021
         BE    DASNS                    YES-BRANCH TO HANDLE     Y02021
         LA    R5WORK,EWAIERP           GET PTR TO SENSE         Y02021
         B     DASNS1                   BRANCH TO COMPLETE       Y02021
         SPACE
DASNS    L     R5WORK,EWADCNT           GET PTR TO DEV DEP       Y02021
         LA    R5WORK,FIVEDBL(R5WORK)   GET POINTER TO SENSE    YM5956P
DASNS1   LTR   R6WORK,R6WORK            TEST FOR ANY COUNT       Y02021
         BZ    SVCPRE                   YES-NO SENSE             Y02021
         STH   R6WORK,SENSCNT           COUNT TO RECORD          Y02021
         BCTR  R6WORK,ZERO              DECREMENT COUNT FOR MOVE Y02021
         EX    R6WORK,MOVESENS         MOVE SENSE BYTES TO RECORD
         SPACE
SVCPRE   SR    R8WORK,R9LOGBAS          LENGTH OF RE LESS SENSE  Y02021
         AH    R8WORK,SENSCNT          ADD LENGTH OF SENSE
         LR    R0PARM,R8WORK           LOAD LENGTH OF RECORD
         B     SETADR                   BRANCH TO RECORD REC     Y02021
*
***      ROUTINE TO FREE GOTTEN CORE
*
         SPACE
FREECORE L     R12IOB,IOSBADDR          GET ORIGINAL IOSB PTR    Y02021
FREECORA LR    R6WORK,R14LINK           SAVE RETURN REG          Y02021
         LR    R1PARM,R9LOGBAS          POINTER TO GOTTEN CORE   Y02021
         SPACE
         FREEMAIN  RC,A=(1),LV=328,SP=255                        Y02021
         LR    R14LINK,R6WORK           RESTORE RETURN REG       Y02021
         BR    R14LINK                 RETURN TO CALLER.Z15459VM Y02021
         SPACE
*                                                              Z15459VM
**** ROUTINE TO OBTAIN CPUID DIGITS FROM PCCA ENTRY, FOR CPU   Z15459VM
**** ON WHICH ERROR OCCURED.                                   Z15459VM
*                                                              Z15459VM
CPUIDRTE EQU   *                                               Z15459VM
         L     R1PARM,IOSERP           SET R1= @EWA            Z15459VM
         IC    R7WORK,EWACPU           STO CCA OFFSET          Z15459VM
         SLL   R7WORK,TWENTY8          MASK OUT ALL BUT LO 4   Z15459VM
         SRL   R7WORK,TWENTY6            BITS & MULT BY 4      Z15459VM
         L     R5WORK,CVTPCCAT     R5= @ PHYSICAL CCA VECT TBL Z15459VM
         L     R5WORK,ZERO(R7WORK,R5WORK) R5= @ PCCA ENTRY     Z15459VM
         PACK  ZERO(FOUR,R4UCB),SIX(SIX,R5WORK) PACK PCCA CPUIDZ15459VM
         L     R5WORK,ZERO(ZERO,R4UCB) R5= PACKED CPUID        Z15459VM
         SRL   R5WORK,FOUR             SHIFT OUT SIGN BITS     Z15459VM
         STIDP ZERO(R4UCB)             STO CPUID THIS CPU      Z15459VM
         STCM  R5WORK,X'7',ONE(R4UCB)  STO CPUID DIGITS ERP CPUZ15459VM
         L     R4UCB,IOSUCB            RESET R4 TO UCB         Z15459VM
         BR    R6WORK                  RETURN TO OBR/MDR STIPD Z15459VM
         EJECT
*        HOUSEKEEPING - FREE CORE - TEST FOR PROPER EXIT       @YM6451P
         SPACE
TERM     BAL   R14LINK,FREECORE    GO FREE GOTTEN CORE         @ZA02772
         TM    IOSFLB,IOSSDR       NO OBR RECORDING?           @Z402IVS
         BO    NOMAIN              YES,EXIT                    @Z402IVS
*
*        TEST FOR RETURN TO EOD MODULE
*
         L     R4UCB,IOSUCB             GET ORIG UCB ADDR        Y02021
         L     R1PARM,IOSERP            GET ORIG EWA ADDR        Y02021
TCREC1   L     R10WORK,UCBEXTPT         UCB EXTENSION POINTER  @YM00142
         NI    IOSFLB,LOGCLR            CLEAR IOSLOG FLAG      @YM2957P
         CLI   UCBETI,TCAMEXT1          WAS TCAM ERP USED        Y02021
         BE    TCREC2                   YES-GO TEST FOR EOD      Y02021
         CLI   UCBETI,TCAMEXT3          WAS A TCAM ERP USED 3705 Y02021
         BNE   DDRTEST                  NO-CONTINUE              Y02021
TCREC2   TM    EWTCFLG,EWTCEOD          TCAM EOD RECORD          Y02021
         BZ    DDRTEST                  NO-GO TEST FOR DDR       Y02021
         LR    R1PARM,R12IOB            IOSB TO REG 1            Y02021
         LH    R13PARM,EODMOD           GET MODULE NAME IN REG   Y02021
         B     EXIT1                    GO XCTL TO MODULE        Y02021
         EJECT                                                 @AZ09708
*        THE FOLLOWING ROUTINE CHECKS TO SEE IF THE DYNAMIC    @AZ09708
*        DEVICE REALLOCATION ROUTINE(DDR) IS TO BE INVOKED     @AZ09708
*        OR IF THE OTHER EXITS TO THE SVC, ETC ARE TO BE DONE  @AZ09708
*                                                              @AZ09708
DDRTEST  EQU   *                                               @AZ09708
         CLI   UCBTBYT3,UCB3UREC       UNIT RECORD DEVICE?     @ZA26472
         BNE   NOTUREC                 NO BYPASS UNIT RECORD   @ZA26472
         CLI   UCBTBYT4,UR3838         3838 DEVICE?            @ZA26472
         BE    NOMAIN                  YES-NO DDR SWAP.        @ZA26472
         CLC   UCBTBYT3(DEVTYPE),UR3800  ARGONAUT?    Z12990VM @Z402IVS
         BE    NOMAIN                  YES-NO DDR SWAP.        @ZA26472
         B     DDRIN1                  CHECK FOR PERMANENT ERR @ZA26472
NOTUREC  EQU   *                                               @ZA26472
         CLI   UCBTBYT3,UCB3DACC        DASD DEVICE            @AZ09708
         BNE   DDRIN1                   NO-BYPASS EMULATION CHK@AZ09708
         CLI   UCBTBYT4,DA2314          2314/2319 DEVICE       @AZ09708
         BE    DDRIN1                   YES-BYPASS EMULATION CK@AZ09708
         TM    EWDSNS2,EWD24            DEV. EMULATION ACTIVE  @AZ08966
         BO    NOMAIN                 YES, DON'T SWAP
         CLC   UCBTBYT3(DEVTYPE),UR3350 DEVICE 3350?           @Z40RSVS
         BE    NOMAIN              YES, STD EXIT               @Z40RSVS
DDRIN1   EQU   *                                               @AZ09708
         TM    IOSFLA,IOSEX+IOSERR     TEST PERM ERR OR ERP CNTL
         BNM   NOMAIN              EXIT TO SUPERVISOR          @ZA02799
DDRIN    EQU   *                                               Z12032VM
         CLI   UCBTBYT3,UCB3DACC                               Z12032VM
         BNE   DDRIN2                                          Z12032VM
         TM    UCBSTAB,UCBPGFL                                 Z12032VM
         BO    NOMAIN                                          Z12032VM
DDRIN2   EQU   *                                               Z12032VM
         TM    CVTOPTA,CVTDDR          IS DDR IN THIS SYSTEM
         BO    DDREXIT                 YES - BR TO EXIT TO DDR
         SPACE
*        THE FOLLOWING IS THE STANDARD EXIT - RETURN TO SUPERVISOR
         SPACE
NOMAIN   LR    R1PARM,R12IOB             GET IOSB ADDR TO REG 1@AZ08966
         SVC   ERREXCP                   FREE IOSB             @AZ08966
NOMAIN1  SVC   EXIT                RETURN TO SUPERVISOR        @ZA02772
         SPACE
*        THE FOLLOWING SETS PARAMETERS FOR EXIT TO DDR
         SPACE
DDREXIT  EQU   *                   Z12032VM                    Z12032VM
         LR    R1PARM,R12IOB                                   Z12032VM
         LH    R13PARM,DDRMOD                                  Z12032VM
         B     EXIT1                    GO TO DDR                Y02021
         SPACE
         EJECT
*******************************************************************
*                                                                 *
*****    ENTRY TO THE NON-STANDARD T TYPE RECORDER ROUTINE        *****
*                                                                 *
*                                                                 *
*******************************************************************
         SPACE
MDR      MVI   IOSBADDR,TPERRUNG        SET MDR IN-PROGRESS IND  Y02021
         SPACE
*
*      ROUTINE TO DETERMINE IF THIS RECORD HAS A FORMATED BUFFER
*
*      MDR1 ENTRY IS FROM AN UNSUCCESSFUL GETMAIN
*
MDR1     TM    UCBTBYT3,UCB3TAPE+UCB3COMM+UCB3DISP
         BNZ   ADDRIOB       LOG BIT NOT ON - BRANCH TO
*                            GET BUFFER ADDRESS
*                                      YES - ITS A BUFFERED LOG DEVICE
         CLI   UCBTBYT3,DASD            TEST FOR DIRECT ACCESS   Y02021
         BE    DABUFPTR                 YES-GO CHK UNIT TYPE     Y02021
         CLI   UCBTBYT4,UR3895          IS THIS A 3895?        @G30HIMD
         BE    DABUFPTR                 YES,BRANCH             @G30HIMD
*                                      NO - THEN ITS A 3211 PRINTER
*      UNIT RECORD DEVICE,CHARACTER READER,FUTURE OTHER
         L     R6WORK,UCBXTADR          GET PRT TO UCS EXT       Y02021
         CLI   UCBTBYT4,ARGO       3800?                       @Z402IVS
         BE    BUF3800             YES, GO                     @Z402IVS
         L     R8RECADR,UCBERADR        LOAD BUFFER ADDR         Y02021
         B     SAVEADR                 BRANCH TO FORMAT RECORD
BUF3800  L     R8RECADR,ARGERADR(R6WORK) LOAD BUFFER ADDRESS   @Z402IVS
         B     SAVEADR             BRANCH TO FORMAT RECORD     @Z402IVS
         SPACE
DABUFPTR L     R8RECADR,EWADCNT         GET PTR TO BUFFER        Y02021
         B     SAVEADR                 BRANCH TO FORMAT RECORD
         EJECT
*
*      DEVICE CLASSES:TAPE,COMMUNIC,DISPLAY OR CHAN TO CHAN ADAPTER.
*
ADDRIOB  L     R8RECADR,EWTCBUFF        LOAD BUFFER ADDR         Y02021
*                                      NOTE: IF TCAM IS THE ACCESS
*                                       METHOD THIS IS A FORMATED BUFF
*                                       IF NOT-ITS A NON FORMATED BUFF
         L     R10WORK,UCBEXTPT         UCB EXTENSION POINTER  @YM00142
         CLI   UCBETI,TCAMEXT1          IS IT TCAM               Y02021
*                                    TP=X'2F',X'30',X'31'      Z16952VM
         BL    NOTFORM                 NOT TP-UNFORMATTED BUF  Z16952VM
         CLI   UCBETI,TCAMEXT3          IS IT TCAM               Y02021
         BH    NOTFORM                 NOT TP-UNFORMATTED BUF  Z16952VM
SAVEID   LR    R11BUFSV,R8RECADR       SAVE RECORD/BUFFER ADDR
         B     SAVEXCTL                BR TO BYPASS SUB ID ZEROING
*
*      THE FOLLOWING CODE HANDLES NON-FORMATTED BUFFER-RECORD IS
*      FORMATTED IN THE GETMAIN AREA
*      TAPE,COMMUNIC OR DISPLAY,BUT NOT TCAM
*
NOTFORM  LR    R11BUFSV,R8RECADR        SAVE BUFFER ADDR         Y02021
*
*      THE FOLLOWING TEST IS FOR UNSUCCESSFUL GETMAIN
*
         LTR   R15CODE,R15CODE          TEST FOR COMPLETION CODE Y02021
         BNZ   SAVEXCTL                 YES-GO TEST FOR XCTL     Y02021
         MVC   BLDAREA(MDRINFO),BUFFER  MOVE FIRST 6 BYTES       Y02021
         SR    R10SIZE,R10SIZE          CLEAR REGISTER         @YM6451P
         LH    R10SIZE,BUFSIZE          GET BUFFER SIZE        @YM6451P
         SH    R10SIZE,CONST7           ADJUST COUNT BY 7      @YM6451P
         EX    R10SIZE,MOVEBUFF         MOVE REST OF BUFFER    @YM6451P
         LR    R8RECADR,R9LOGBAS        USE GOTTEN CORE TO       Y02021
*                                       FORMAT THE HEADER
         LH    R10SIZE,BUFSIZE         LOAD SIZE OF BUFF
         LA    R10SIZE,REMHDR(R10SIZE) ADD 18 FOR HEADER
         STH   R10SIZE,BUFSIZE         STORE BACK UPDATED SIZE
         SPACE
         CLI   BUFRECID,ID2715         IS THIS A 2715
         BE    CLRSUBID                YES - BR TO CLEAR SUB ID
         B     SAVEXCTL                NO - BRANCH-DONT CLEAR SUB ID
         SPACE 2
SAVEADR  LR    R11BUFSV,R8RECADR       SAVE RECORD/BUFFER ADDRESS
CLRSUBID MVI   BUFSUBID,ZERO           CLEAR VARIABLE SUB ID
SAVEXCTL LTR   R15CODE,R15CODE          TEST FOR COMPLETION CODE Y02021
         BZ    SAVXCTL1                 NO-BRANCH AROUND         Y02021
         ICM   R13PARM,THREE,BUFXCTL    GET XCTL NAME IN REG 13  Y02021
         ICM   R12IOB,EIGHT,MDRINPRG    SET MDR IN PROG IND      Y02021
         MVI   SUCCESS(R11BUFSV),NOTSUC MARK NOT SUCCESSFUL      Y02021
         B     TERM1A                   GO FINISH TERM TESTS     Y02021
SAVXCTL1 MVC   BINXCTL(TWO),BUFXCTL     MOVE XCTL INFO TO RECORD Y02021
         SPACE
*
*      ROUTINE TO SAVE AND CHECK RECORD SIZE
*
         LH    R10SIZE,BUFSIZE         LOAD AND SAVE SIZE OF RECORD
         CLC   BUFSIZE(SIZE),HDRLEN    IS RECORD SIZE GREATER THEN 24
         BH    INITHDR                 YES- BR TO FORMAT HEADER
         SPACE
         MVI   SUCCESS(R11BUFSV),TOSMALL    PUT SUCCESS SYMBOL IN BUFF
         B     TERM1                   BRANCH TO TERMINATE
         EJECT
*
*      ROUTINE TO INITIALIZE THE MDR HEADER RECORD
*
INITHDR  MVC   MRECID(ONE),BUFRECID     MOVE RECORD ID TO RE     Y02021
         MVC   MSUBID(ONE),BUFSUBID     MOVE SUB ID TO RE        Y02021
         XC    MSWITCHS(TWO),MSWITCHS   CLEAR SWITCH FIELD       Y02021
         XC    SPARE(TWO),SPARE        CLEAR UNUSED SPACE
         SPACE
         MVI   MCLASRC,TPEREC           SET RECORD CLS AND SRC   Y02021
         SPACE
         LR    R5WORK,R2CVTBAS         LOAD CVT ADDR
         SH    R5WORK,CONST4           SUBTRACT TO ADDR OF REL #
         PACK  WRK1AREA(DBLWRD),ZERO(RELEN,R5WORK)    PACK REL #
         CVB   R6WORK,WRK1AREA          CONVERT REL # TO BINARY  Y02021
         STC   R6WORK,MSYSREL           STORE REL # IN RECORD    Y02021
         OI    MSYSREL,VS2IND           SET VS2 RELEASE IND      Y02021
         SPACE
**** OBTAIN CPUID OF MACHINE ON WHICH ERROR OCCURED.           Z15459VM
         LA    R4UCB,WRK1AREA          R4=@ WRKAREA IN MDR     Z15459VM
         BAL   R6WORK,CPUIDRTE         OBTAIN CPUID FOR ERP CPUZ15459VM
         MVC   MCPUSER(DBLWRD),WRK1AREA  STO IN CPUID AREA/MDR Z15459VM
         SPACE
         CLI   MRECID,ID3670            TEST FOR 3670            Y02021
         BNE   SETIME                  NO - BR TO CONTINUE
         CH    R10SIZE,MIN3670         IS REC SIZE LESS THEN NORMAL
         BNL   SETIME                  NO - BR TO CONTINUE
         OI    MSW1,INCOM               SET SHORT RECORD SW      Y02021
         SPACE
SETIME   TIME  DEC                     GET TIME AND DATE
         ST    R0PARM,MTIME             STORE TIME IN REC        Y02021
         ST    R1PARM,MDATE             STORE DATE IN REC        Y02021
         OI    MSWITCHS,TIMEBIT         SET TIME MACRO SW        Y02021
         SPACE
*
*      ROUTINE TO WRITE RECORD TO SYS1.LOGREC
*
         LR    R0PARM,R10SIZE           GET COUNT                Y02021
         LR    R1PARM,R8RECADR         LOAD ADDR OF RECORD
         LA    R13PARM,REGSAVE          LOAD SAVE AREA ADDR      Y02021
       RECORD TYPE=LOGREC,DATAADR=(1),LENGTH=(0),BUFFER=YES,RCVRY=ESTAE
         LTR   R15CODE,R15CODE         WAS RECORD WRITTEN
         BZ    RECOK                   YES - BR TO INSERT SUCCESS SYM
         SPACE
         MVI   SUCCESS(R11BUFSV),NOTSUC PUT SUCCESS SYMBOL IN BUFF
         B     TERM1                   BR TO TERMINATE
         SPACE
RECOK    MVC   ZERO(MSG,R11BUFSV),TPR  MOVE 'TPR' TO BUFFER
         MVI   SUCCESS(R11BUFSV),OK    PUT SUCCESS SYMBOL IN BUFF
         EJECT
*
*      ROUTINE TO TERMINATE - EXIT TO ERROR XCTL ROUTINE OR SUPERVISOR
*
TERM1    ESTAE 0                                               @ZA06003
         XR    R13PARM,R13PARM     CLEAR R13                   @ZA06003
         ICM   R13PARM,THREE,BINXCTL LOAD XCTL MOD NAME        @ZA02772
TERM1A   DS    0H                                              @ZA02772
         LTR   R13PARM,R13PARM         IS THERE XCTL INFO
         BZ    EXIT3                   NO - BR TO EXIT TO SUPERVISOR
         CLI   SUCCESS(R11BUFSV),NOTSUC UNSUCCESSFUL RECDG?    @ZA02772
         BE    EXIT1               YES, BYPASS FREE CORE       @ZA02772
         BAL   R14LINK,FREECORE    GO FREE GOTTEN CORE         @ZA02772
         LR    R1PARM,R12IOB       GET IOSB ADDR               @ZA02772
         SPACE
*        EXIT TO SPECIAL XCTL ROUTINE
EXIT1    L     R14EXIT,CVTXTLER         LOAD ADDR OF ERR XCTL RT Y02021
         BR    R14EXIT                 BR TO SPECIAL XCTL ENTRY
         SPACE
**       FREE IOSB AND RETURN TO SUPERVISOR                    @ZA02772
EXIT3    TM    IOSPKEY,IOSIDR      DIR RECORDING?              @ZA02772
         BO    EXIT4               YES, GO TO SPECIAL EXIT     @ZA02772
         BAL   R14LINK,FREECORE    GO FREE GOTTEN CORE         @ZA02772
         LR    R1PARM,R12IOB       GET IOSB ADDR               @ZA02772
         LA    R1PARM,ZERO(R1PARM) CLEAR HIGH ORDER BYTE OF RQE ADDR
         SVC   ERREXCP                  FREE IOSB                Y02021
         SVC   EXIT                    EXIT TO SUPERVISOR
         SPACE 1                                               @ZA02772
**       DIR EXIT ROUTINE                                      @ZA02772
EXIT4    EQU   *                                       @ZA26915@ZA02772
         CLI   SUCCESS(R11BUFSV),NOTSUC  UNSUCCESS?    @ZA26915@ZA02772
         BE    EXIT5                   YES-BYPSSFREEMN @ZA26915@ZA02772
         BAL   R6WORK,FREEIOSB       FREE IOSB/SRB/EWA @ZA26915@ZA02772
         BAL   R14LINK,FREECORE    GO FREE GOTTEN CORE @ZA26915@ZA02772
         SVC   EXIT                    RETURN TO SVC   @ZA26915@ZA02772
EXIT5    EQU   *                                       @ZA26915@ZA02772
         BAL   R6WORK,FREEIOSB       FREE IOSB/SRB/EWA @ZA26915@ZA02772
         SVC   EXIT                    RETURN TO SVC   @ZA26915@ZA02772
         SPACE 1
MIN3670  DC    H'37'                   MINIMUM RECORD SIZE FOR 3670
HDRLEN   DC    H'24'                   BYTES IN A MDR RECORD HEADER
TPR      DC    C'TPR'                  CHARACTERS PLACED IN BUFFER
*                                       IF RECORD WAS WRITTEN
MDRINPRG DC    X'FE'                    MDR IN PROGRESS IND      Y02021
CONST4   DC    H'4'                    CONSTANT OF FOUR
CONST7   DC    H'7'                     BUFFER COUNT ADJUST    @YM6451P
TY2314   DC    X'2008'                 2314 DEVICE CLASS AND TYPE CODE
UR3800   DC    X'080E'             ARGONAUT-3800               @Z402IVS
UR3350   DC    X'200B'             3350                        @Z40RSVS
ZEROS    DC    X'000000'               CONSTANT OF ZEROS
         SPACE
*      THE FOLLOWING INSTRUCTIONS ARE EXECUTED BY EXECUTE -EX-
         SPACE
MOVESENS MVC   ZERO(ZERO,R8WORK),ZERO(R5WORK)    MOVE SENSE TO RECORD
         SPACE
MOVESTAT MVC   ZERO(ZERO,R8WORK),ZERO(R7WORK)    MOVE STATS TO REX03127
         SPACE
CLRSTAT  XC    TWO(ZERO,R7WORK),TWO(R7WORK)  CLEAR STAT COUNTERS X03127
         SPACE
MOVEBUFF MVC   RECINFO(ZERO),BUFBODY    MOVE REMAINDER OF BFR  @YM6451P
         SPACE 1                                               @YM6451P
         DS    0H
DDRMOD   DC    XL2'19C9'               BINARY EQUIV OF DDR MOD NAME
EODMOD   DC    XL2'2357'               BINARY EQUIV OF EOD MOD NAME
MULTICON DC    H'10'                   LENGTH OF NORM STAT COUNTER
         SPACE 3
MASK     DC    F'15'                   STAT TABLE COUNT MASK   @YM6451P
DIRJOB   DC    C'PAGE ERR'             JOB NAME FOR DIR        @YM6451P
         SPACE 1
ESTALIST ESTAE MF=L
PGMCKADD DC    A(OBRPGMCK)              ESTAE ENTRY POINT        Y02021
         SPACE 1
         EJECT
*
****   WARNING-DO NOT PUT ANY CODE BEYOND THIS POINT
*
*
***      STAE EXIT ROUTINE TO FREE CORE
*
         SPACE
OBRPGMCK C     R0PARM,STAETEST          TEST FOR WORK AREA       Y02021
         BE    GETREGS                  NO-USE REG 2             Y02021
         L     R2CVTBAS,ZERO(R1PARM)    GET POINTER INTO REG 2   Y02021
GETREGS  LM    R2CVTBAS,R12IOB,ZERO(R2CVTBAS)     GET REGS       Y02021
         L     R13PARM,FRECORAD         GET BRANCH ADDRESS       Y02021
         BR    R13PARM                  BRANCH TO FREE CORE      Y02021
*                                       R14 CONTAINS RETURN ADDR Y02021
FRECORAD DC    A(FREECORA)             PTR TO FREE CORE RTN    @YM6451P
STAETEST DC    A(12)                    STAE STORAGE TEST        Y02021
PTCHAREA DC    30F'0'                  PGM PATCH AREA
PATCHEND EQU   *                                                Z40HIVS
         END
