         TITLE 'IGG019RE -- COMMON BUFFER SEND SCHEDULER'
IGG019RE CSECT
*A332000,556000                                                  Y01948
*C844000-868000                                                  Y01948
*A364000,884000                                                  X02004
*C676000-684000                                                  X02004
*D692000,696000                                                  X02004
*C640000,812000                                                @OY14092
***********************************************************************
* STATUS -- CHANGE LEVEL 0                                            *
* FUNCTION -- TO SCHEDULE A BROADCAST SEND OPERATION FROM A COMMON    *
*    BUFFER DATA AREA                                                 *
*                                                                     *
* ENTRY POINTS --                                                     *
*         FIRST EXECUTABLE INSTRUCTION                                *
*                                                                     *
* INPUT --                                                            *
*    WHEN ACTIVATED BY THE DISPATCHER -                               *
*    R1 POINTS TO A LCB OR ERB                                        *
*    R3 ADDRESS OF STCB                                               *
*    R7 ADDRESS OF QCB                                                *
*    R11 DISPATCHER BASE                                              *
*    R13 ADDRESS OF AVTSAVE2                                          *
*    R15 ENTRY POINT ADDRESS                                          *
*                                                                     *
* OUTPUT --                                                           *
*    R1 POINTS TO A LCB OR ERB                                        *
*    R3 ADDRESS OF STCB                                               *
*    R11 DISPATCHER BASE                                              *
*    R13 ADDRESS OF AVTSAVE2                                          *
*                                                                     *
* EXTERNAL REFERENCES --                                              *
*         NONE                                                        *
* EXITS,NORMAL --                                                     *
*         DSPPOST                                                     *
*                                                                     *
* EXITS,ERROR --                                                      *
*         NONE                                                        *
*                                                                     *
* TABLES/WORK AREAS --                                                *
*    AVT                                                              *
*    LCB                                                              *
*    QCB                                                              *
*    SCB                                                              *
*    STCB                                                             *
*    TERMINAL ENTRY                                                   *
*    COMMBUF MASTER QCB                                               *
*    COMMBUF DATA AREAS                                               *
*                                                                     *
* ATTRIBUTES --                                                       *
*    RE-ENTRANT,REFRESHABLE,PROBLEM PROGRAM MODE                      *
*                                                                     *
* CHARACTER CODE DEPENDENCY --                                        *
*    THE OPERATION OF THIS MODULE DOES NOT DEPEND UPON A PARTICULAR   *
*    INTERNAL REPRESENTATION OF THE EXTERNAL CHARACTER SET.           *
*                                                                     *
* NOTES --                                                            *
*    STANDARD TCAM LINKAGES AND DEVIATIONS FROM STANDARDS.            *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*        SYMBOLIC REGISTERS                                           *
***********************************************************************
         SPACE 1
R0       EQU   0                        WORK REG
R1       EQU   1                        WORK REG
RPARM    EQU   1                        PARM REG
R2       EQU   2                        WORK REG
RSTCB    EQU   3                        STCB
RLCB     EQU   4                        LCB
R5       EQU   5                        WORK REG
RCBDA    EQU   6                        COMMON BUFFER DATA PREFIX
RQCB     EQU   7                        QCB
RTERM    EQU   8                        TERMINAL ENTRY
RSCB     EQU   9                        SCB
RCMBF    EQU   10                       COMMON BUFFER QCB
RDISP    EQU   11                       DISPATCHER BASE
RBASE    EQU   12                       CSECT BASE
RAVT     EQU   13                       AVT BASE
R14      EQU   14                       WORK REG
RRETURN  EQU   14                       RETURN REG
R15      EQU   15                       WORK REG
RENTRY   EQU   15                       ENTRY POINT ADDR
         SPACE 1
***********************************************************************
*        EQUATES                                                      *
***********************************************************************
         SPACE 1
AD       EQU   7                        MASK FOR STCM OF ADDRESS Y01948
ZERO     EQU   0                        ZERO
ONE      EQU   1                        ONE
TWO      EQU   2                        TWO
THREE    EQU   3                        THREE
FOUR     EQU   4                        FOUR
SIX      EQU   6                        SIX
WRITE    EQU   X'01'                    WRITE COMMAND CODE
RSVTO    EQU   X'0C'                    RECEIVE SCHED VTO
HI       EQU   8                        MASK TO STCM HIGH BYTE   X02004
         SPACE 1
***********************************************************************
*        ESTABLISH CSECT AND CONTROL BLOCK ADDRESSABILITY             *
***********************************************************************
         SPACE 1
         USING IEDQDISP,RDISP           DISP
         USING AVTSAVE2,RAVT            AVT
         USING IEDQQCB,RQCB             QCB
         USING IEDQSCB,RSCB             SCB
         USING IEDQLCB,RLCB             LCB
         USING IEDQTRM,RTERM            TERMINAL ENTRY
         USING IEDQSTCB,RSTCB           STCB
         USING IEDCMB,RCMBF             COMMON BUFFER MASTER QCB
         USING IEDCBDA,RCBDA            COMMON BUFFER DATA AREA PREFIX
         SPACE 1
         DC    H'0'                     FILLER
         USING *,RENTRY                 TEMP BASE
         B     SETBASE                  BRANCH/DUMMY QCB
         DC    H'0'                     FILLER
         DC    AL1(DSPMCPL6)            STCB
         DC    AL3(*-1)                 STCB ADDR
         DC    H'0'                     FILLER
SETBASE  BALR  RBASE,0                  SET BASE REG
         DROP  RENTRY
         USING *,RBASE
         LR    RLCB,RPARM               ASSUME LCB
         CLI   LCBRSKEY-IEDQLCB(RPARM),RSVTO
*                                       ELEMENT A LCB
         BE    SETREGS                  BRANCH IF YES
         LA    R5,LCBERB-IEDQLCB        ERB OFFSET INTO LCB
         SR    RLCB,R5                  COMPUTE LCB
SETREGS  EQU   *
         L     RSCB,LCBSCBA-1           SCB
         L     RCMBF,AVTCBQCB           CB MASTER QCB
         LA    RCMBF,ZERO(RCMBF)        CLEAR HI BYTE
         L     RCBDA,CMBFIRST-1         ADDR OF FIRST DATA AREA
         CLI   LCBRSKEY-IEDQLCB(RPARM),RSVTO
*                                       ELEMENT A LCB
         BNE   ERB                      BRANCH IF NO
         SPACE 1
***********************************************************************
*        ELEMENT IS A LCB                                             *
***********************************************************************
         SPACE 1
         TM    AVTBIT1,AVTCLOSN+AVTQUCKN
*                                       QUICK CLOSE
         BO    REMOVE                   BRANCH IF YES
         LH    RPARM,STCBTCIN           TNT OFFSET
         N     RPARM,AVTCLRHI           CLEAR HIGH ORDER HALF    Y01948
         L     R15,AVTRNMPT             TERM NAME RTN
         BALR  R14,R15                  ADDR OF TERMINAL ENTRY
         LR    RTERM,RPARM              SET BASE FOR TERM ENTRY
         L     RQCB,TRMDESTQ-1          DESTINATION QCB
         LA    RQCB,ZERO(RQCB)          CLEAR HI BYTE
         TM    QCBSTAT,QCBTRMHO         IS TERM HELD
         BO    RMVCBST                  BRANCH IF YES
         SR    R0,R0                    CLEAR WORK REG
         IC    R0,STCBINDX              GET INDEX TO DATA AREA
         LA    R1,CBDPRFSZ              SIZE OF DATA PREFIX
         MR    R0,R0                    INDEX X SIZE
         AR    RCBDA,R1                 CURRENT DATA PRF  FOR THIS STCB
         LH    R0,CBDAUSCT              GET USE COUNT
         BCTR  R0,ZERO                  DECR
         STH   R0,CBDAUSCT              AND SAVE
         SPACE 1
***********************************************************************
*        SET UP LCB TO SEND                                           *
***********************************************************************
         SPACE 1
         NI    LCBSTAT1,LCBOCNI+LCBOCWTN RESET ALL BUT STOPLINE@OY14092
         OI    LCBSTAT1,LCBSENDN        SET SENDING
         XC    LCBRECAD,LCBRECAD        ZERO FIELD
         XC    LCBRECOF,LCBRECOF        ZERO FIELD
         MVI   LCBERBPY,PRIINTRQ        INITIAL REQUEST PRI
         MVC   LCBTTBIN,STCBTCIN        SET TO BE CONNECTED
         OI    LCBSTAT2,LCBMSGNN        SET MSGGEN
         SR    R5,R5                    CLEAR WORK REG
         STH   R5,LCBTTCIN
         ST    R5,LCBTPCD               SET TP OP CODE TO ZERO
         ICM   R5,AD,CBDAADDR           DATA ADDRESS             X02004
         SH    R5,H8                    BACK UP TO ADDRESS       X02004
         L     R5,ZERO(,R5)             ADDRESS OF DATA OR       X02004
*                                       INDIRECT ADDRESS LIST    X02004
         XC    LCBCPA+FOUR(THREE),LCBCPA+FOUR CLEAR CCW AREA     X02004
         ST    R5,LCBCPA                SET CCW ADDRESS          X02004
         STCM  R5,HI,LCBCPA+FOUR        SET FLAGS                X02004
         MVI   LCBCPA,WRITE             SET WRITE OP CODE
         LH    R5,CBDALEN               LENGTH OF DATA
         STH   R5,LCBCPA+SIX            SET COUNT
         SPACE 1
***********************************************************************
*        SET TO POST ERB TO I/O GENERATOR                             *
***********************************************************************
         LA    R5,AVTACTIB              I/O GENERATOR QCB
         ST    R5,LCBERBQB-1            SET ERB QCB ADDR
         LA    RPARM,LCBERB             ERB ADDR
         XC    LCBERBLK,LCBERBLK        ZERO LINK
         BAL   R14,DSPPOST              POST                   @Y17XAIX
         SPACE 1
***********************************************************************
*        ELEMENT IS AN ERB                                            *
***********************************************************************
         SPACE 1
ERB      EQU   *
         LR    RPARM,RLCB               SET POST REG
         ST    RPARM,LCBRCB             SET TO POST LCB TO ITSELF
         MVI   LCBPRI,PRILNFRE          SET POST PRTY
         MVI   LCBSENS0,ZERO            CLEAR FOR IOS
         NI    LCBSTAT2,AVTEFF-LCBMSGNN RESET MSGGEN BIT
         XC    LCBTTCIN,LCBTTCIN        CLEAR TTCIN
         XC    LCBTTBIN,LCBTTBIN        RESET TO BE CONNECTED
         B     REMOVE                   REMOVE STCB FROM LCB
         SPACE 1
RMVCBST  EQU   *
         XC    LCBLINK,LCBLINK          CLEAR LINK FIELD
         NI    LCBSTAT1,LCBOCNI+LCBOCWTN RESET ALL BUT STOPLINE@OY14092
         OI    LCBSTAT1,LCBRECVN        SET LINE RECVN
         LR    RPARM,RLCB               SET PARM REG
         SPACE 1
REMOVE   EQU   *
         L     RSTCB,LCBSTCBA-1         GET STCB ADDR
         MVC   LCBSTCBA(THREE),STCBLINK DELINK STCB FROM LCB
         L     R2,CMBSTCB-1             ADDR FIRST STCB
         STCM  RSTCB,AD,CMBSTCB         SET NEW FIRST STCB       Y01948
         STCM  R2,AD,STCBLINK           SET LINK FIELD           Y01948
         LH    R2,CMBASTCB              GET NO OF STCBS
         LA    R2,ONE(R2)               INCR NO OF STCBS
         STH   R2,CMBASTCB              RESTORE COUNTER
         BAL   R14,DSPPOST              POST                   @Y17XAIX
         EJECT
*****************************************************************X02004
*        CONSTANTS                                               X02004
*****************************************************************X02004
         SPACE 2
H8       DC    H'8'                     CONSTANT OF 8            X02004
         EJECT
         SPACE 1
***********************************************************************
*        DSECTS                                                       *
***********************************************************************
         TPRIOR
         TAVTD
         TLCBD
         TSTCBD
         TQCBD
         TSCBD
         TDISPD
         TTRMD
         IEDCMB
         IEDCBDA
         END
