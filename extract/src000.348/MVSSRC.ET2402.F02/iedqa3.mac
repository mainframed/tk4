DQA3     TITLE 'IEDQA3 - DYNAMIC TRANSLATION DETERMINATION ROUTINE'
IEDQA3   CSECT
*
*C513000                                                         S22029
*D504000,510000                                                  S22029
*A336000,547000-554000                                           S22025
*D126000-132000,333000,339000                                    S22025
*D366000,780000,814000-819000                                    A56610
*C309000,351000-363000,372000-395000,747000,780000               A56610
*A667000-743000,781000-782000,834700-836100                      A56610
*D804000                                                         S21903
*C003000,198000,243000,363000,375000,381000,402000,432000,498000,S21903
*C501000,516000,519000,534000,537000,579000,606000,660000,666000,S21903
*C690000,696000,780000                                           S21903
*C389000-391000                                                @YA05454
*A402000,807000                                                @OY19796
*A389600                                                       @OZ39371
* $21=OZ56316  ETC2402 81.12.14 797350: D402400-404400             @21A
***********************************************************************
*                                                                     *
* $MOD(IEDQA3) COMP(M@) PROD(TCAM):                                @21A
*                                                                     *
* COPYRIGHT: NONE                                                  @21A
*                                                                     *
*STATUS -- CHANGE LEVEL                                               *
*ENTRY POINTS--
*        'IEDQA3' TO DETERMINE THEPROPER TRANSLATE TABLE TO BE
*    USED IN TRANSLATING INCOMING MESSAGES. CALLING SEQUENCE FROM
*    TRANSLATION MODULE (IEDQAW) IS:
*
*        LR    R0,RBASE                 SAVE BASE. (IEDQA3 MUST RESTORE
*                                       THIS BASE, REG 12, BEFORE
*                                       RETURNING TO IEDQAW)
*        L     RBASE,AVTA3              ADDR OF IEDQA3.
*        BALR  R14,RBASE                LINKAGE
*        B     EXIT                     +0 RETURN
*        B     TRANS                    +4 RETURN
*RTTBL   EQU   *                        +8 RETURN
*                                                                     *
*INPUT --                                                             *
*  REGISTER 0 - ADDRESS (BASE) OF CALLING ROUTINE (IEDQAW). THIS
*                 ADDRESS IS STORED INTO AVTSAVE2+0 SINCE THE CALL
*                 TO THE TERM NAME TABLE ROUTINE DESTROYS REG 0.
*
*   REGISTER 3 - ADDRESS OF THE SCB.                                  *
*                                                                     *
*   REGISTER 4 - ADDRESS OF THE LCB.                                  *
*                                                                     *
*   REGISTER 6 - ADDRESS OF THE BUFFER.                               *
*                                                                     *
*   REGISTER 8 - ADDRESS OF THE TRANSLATE LIST.
*
*   REGISTER 9 - ADDRESS OF THE AVT.                                  *
*                                                                     *
*   REGISTER 12 - ENTRY POINT ADDRESS AND BASE REGISTER.              *
*                                                                     *
*   REGISTER 14 - RETURN ADDRESS IN IEDQAW.
*
*EXTERNAL REFERENCES --                                               *
*                                                                     *
*   AVT - ADDRESS VECTOR TABLE.                                       *
*                                                                     *
*   LCB - LINE CONTROL BLOCK.                                         *
*                                                                     *
*   SCB - STATION CONTROL BLOCK.                                      *
*                                                                     *
*   CURRENT BUFFER.                                                   *
*                                                                     *
*   TRANSLATION TABLE.                                                *
*                                                                     *
*                                                                     *
*ATTRIBUTES -- SERIALLY REUSABLE,  REFRESHABLE,  ENABLED,             *
*   RESIDENT,  PROBLEM PROGRAM MODE.                                  *
*                                                                     *
*NOTES -- THE OPERATION OF THIS MODULE DOES NOT DEPEND UPON AN        *
*   INTERNAL REPRESENTATION OF THE EXTERNAL CHARACTER SET.            *
*                                                                     *
***********************************************************************
         EJECT
********* REGISTER EQUATES *********
         SPACE
R0       EQU   0                        REGISTER EQUATE          S21903
         SPACE
R1       EQU   1                        PARAMETER LIST
         SPACE
RWORK    EQU   2                        WORK
         SPACE
RSCB     EQU   3                        ADDRESS OF SCB
         SPACE
RLCB     EQU   4                        BASE OF LCB
         SPACE
RSCAN    EQU   5                        ADDR OF START OF TRANSLATIO
         SPACE
RPREFIX  EQU   6                        BASE OF BUFFER
         SPACE
RBUF     EQU   7                        ADDRESS OF BUFFER        S21903
         SPACE
RTBL     EQU   8                        ADDRESS OF TRANSLATE TABLE
         SPACE
RAVT     EQU   9                        BASE OF AVT
         SPACE
RSIZE    EQU   10                       SIZE OF DATA
         SPACE
REX      EQU   11                       EXECUTE REGISTER
         SPACE
RBASE    EQU   12                       BASE OF ROUTINE
         SPACE
R13      EQU   13                       SAVE AREA
R14      EQU   14
R15      EQU   15                       ENTRY AND RETURN CODE
RSTG     EQU   15                       STRING ADDR FOR DYN TRANSS56610
         SPACE
********* MISCELLANEOUS EQUATES *********
         SPACE
ONE      EQU   1                        DUMMY LENGTH FOR EXECUTED
*                                         MOVE INSTRUCTION
FOUR     EQU   4                        OFFSET TO INPUT TRANS TABLE
EIGHT    EQU   8
E0       EQU   0                        EQUATED ZERO             A56610
         EJECT
         USING IEDQAVTD,RAVT            ESTABLISH ADDRESSABILITY
         USING IEDQSCB,RSCB
         USING IEDQA3,RBASE             ESTABLISH ADDRESSABILITY
         USING IEDQPRF,RPREFIX          ESTABLISH ADDRESSABILITY
         USING IEDQLCB,RLCB             ESTABLISH ADDRESSABILITY
IEDQA301 EQU   *
IEDQA3   IEDHJN FINDTBL                 ID AND DATE              S22025
         ST    R0,AVTSAVE2              TERM NAME TBL RTN DESTROYS
         ST    R14,AVTPARM              SAVE RETURN REG.
         ST    RTBL,AVTDOUBL            SAVE POINTER TO TRANLIST
         LH    R1,LCBTTCIN              ASSUME LEASED            A56610
         TM    LCBSTAT2,LCBDIAL         IS IT A DIAL LINE        A56610
         BZ    NODIAL                   BRANCH NO                A56610
         LH    R1,LCBLNENT              LINE INDEX              SA56610
NODIAL   EQU   *
         LTR   R1,R1                    MAKE SURE IS THERE       A56610
         BZ    NOPT                     IF NOT, ERROR RETURN     A56610
         L     R15,AVTRNMPT             ENTRY TO TERM NAME RTNE  A56610
         BALR  R14,R15                  GET TERM ENTRY          SA56610
         USING IEDQTRM,R1               ADDRESSABILITY           A56610
         TM    TRMSTATE,TRMOPTFN        OPTION FIELD USED        A56610
         BZ    NOPT                     BR NO FOR ERROR          A56610
         CLC   1(1,RTBL),TRMOPNO        VALID OPTION OFFSET      A56610
         BH    NOPT                     BR IF NOT FOR ERROR      A56610
         SR    RWORK,RWORK              CLEAR FOR INSERT        SA56610
         IC    RWORK,1(,RTBL)           OFFSET FROM TRANLIST     A56610
         SR    RSCAN,RSCAN              CLR OPT OFFSET REG       A56610
         IC    RSCAN,TRMOPT-1(RWORK)    GET TRUE OFFSET          A56610
         IC    RWORK,AVTHFF+1           HEX 'FF'                 A56610
         CR    RSCAN,RWORK              IS OFFSET 'FF' (NULL)    A56610
         BE    NOPT                     BRANCH YES FOR ERROR     A56610
*                                                                A56610
         LH    RWORK,TRMOPTBL           GET ENTRY OFFSET       @YA05454
         N     RWORK,AVTCLRHI           CLEAR HI HALF          @YA05454
         SLL   RWORK,2                  MULTIPLY OFFSET BY FOUR@OZ39371
         L     RSIZE,AVTOPTPT           GET OPT TABLE ADDR     @YA05454
         AR    RWORK,RSIZE              ADD OPT TABLE ADDR     @YA05454
         L     RSIZE,4(,RSIZE)          GET OPT CSECT TBL ADDR @YA05454
         AR    RWORK,RSCAN              ADD OFFSET THIS OPTION   A56610
         LA    R15,E0(,RWORK)           POINT TO OPTION FIELD    A56610
         ST    R15,AVTSAVE1             SAVE TO STORE TBL IF REQ A56610
         L     RTBL,E0(,R15)            ADDRESS OF TRANS TBL IF STORED
         LTR   RTBL,RTBL                ZERO IF TABLE ADR NOT STORED
         BZ    WHICH                    GO MEANS NOT FOUND YET   S21903
         SPACE
         L     RBASE,AVTSAVE2           RESTORE AW BASE REG
         L     R14,AVTPARM              RESTORE RETURN REG.
         B     EIGHT(R14)               RETURN TO IEDQAW AT +8 RETURN
*                                       FOR TRANSLATION. IEDQAW WILL
*                                       SELECT PROPER TABLE
*
WHICH    EQU   *
         TM    LCBSTAT1,LCBSENDN        ARE WE SENDING
         BZ    SEARCH                   NOT IF THIS GOES         S21903
** IF THIS IS AN OUTPUT MSG, WE CAN'T TRANSLATE SINCE THE
**  PROPER TABLE HAS NOT YET BEEN DETERMINED.
         SPACE
NOXLT    EQU   *
         LA    R15,FOUR
         L     RSCB,LCBSCBA-1           RESTORE SCB ADDRESS
NOXLT1   EQU   *
         OI    SCBERR2,SCBCODER         SET ERROR BIT FOR TSO
         L     RBASE,AVTSAVE2           RESTORE AW BASE REG
         L     R14,AVTPARM              RESTORE RETURN REG.
         BR    R14                      RETURN TO IEDQAW AT +0 RETURN
*                                       FOR EXIT
         SPACE
NOPT     EQU   *
         LA    R15,8                    NO OPTION FIELD
         B     NOXLT1                   LEAVE
         SPACE
SEARCH   EQU   *
**  TRANSLATE STRINGS WITH TABLES SPECIFIED AND COMPARE
**  TO INPUT DATA . COUNT OF CHARACTERS COMPARED IS BASED ON THE
**  SIZE OF THE STRING BEING COMPARED
         SR    RSCAN,RSCAN              CLEAR WORK REGISTER AND  S21903
         IC    RSCAN,LCBISZE            GET SIZE                 S21903
         LA    RSCAN,AVTTXTSZ(,RSCAN)  GET TEXT PREFIX SIZE      S22029
         TM    PRFSTAT1,PRFNHDRN        IS THIS A HEADER         S21903
         BO    SIZER                    BRANCH IF YES            S21903
         SPACE
         LA    RSCAN,AVTHDRSZ-AVTTXTSZ(,RSCAN)
         SPACE
SIZER    EQU   *
         STH   RSCAN,AVTSAVE4           SAVE SIZE TO DELETE FROM BU SZE
         LH    RSIZE,PRFSIZE            GET PREFIX SIZE          S21903
         SR    RSIZE,RSCAN              SUBTRACT SCAN POINTER    S21903
         BNP   NOXLT                    NO DATA. ERROR RETURN
         LR    RWORK,RPREFIX            LOAD PREFIX ADDR         S22025
         SPACE
         LA    RSCAN,ONE(,RSCAN)
         B     ENTERLOP                 BRANCH TO FIND ADDRESS   S22025
ADDRLOOP EQU   *                                                 S22025
         L     RWORK,PRFTIC-IEDQPRF(RWORK) POINT TO NEXT UNIT    S22025
         SH    RSCAN,AVTKEYLE           SUBT NO OF BYTES PASSED  S22025
ENTERLOP EQU   *                                                 S22025
         CH    RSCAN,AVTKEYLE           IS ITEM IN THIS UNIT     S22025
         BH    ADDRLOOP                 NO, TEST NEXT UNIT       S22025
         LA    RSCAN,AVTUMALN-1(RSCAN,RWORK)  YES, UNIT ADDR     S22025
         L     RLCB,PRFLCB-1            RESTORE LCB ADDRESS         TSO
*  THERE MUST BE AT LEAST 10 CHARACTERS TO CHECK FOR A MAXIMUM SIZE
*  CHARACTER STRING OF 8 CHARACTERS. IF THE FIRST UNIT DOES NOT CONTAIN
*  CASE, 10 CHARACTERS WILL BE MOVED TO THE LOCATION NAMED AVTSCRAT.
*  THE MAIN BODY OF THE CODE THEN CAN UNCONDITIONALLY MOVE 10
* CHARACTERS FROM THERE TO AVTWORKL WHERE THE TRANSLATION AND ACTUAL
*  COMPARE ARE DONE.
         LR    R15,RSCAN                COPY DATA POINTER
         MVC   WORKS(10),0(RSCAN)       ASSUME THERE ARE 10     SA56610
         LH    RSIZE,AVTKEYLE           GET SIZE OF A BASIC UNIT
         SH    RSIZE,AVTSAVE4           PFX SIZE OF FIRST
         CH    RSIZE,H10                THEN SEE IF WE HAD 10
         BNL   QA300                    BRANCH IF WE GOT 10 CHAR
*  DID NOT GET 10. MAKE SURE THERE IS A SECOND UNIT
         CLI   PRFNBUNT,1               ONLY 1 UNIT PER BUFFER
         BE    NOXLT                    BRANCH YES FOR ERROR RETURN
         L     R15,PRFTIC               POINT TO NEXT UNIT
         LA    R15,12(,R15)             POINT TO DATA SECOND UNIT
         LA    R14,9                    AVOIDS BCTR FOR EXECUTE
         SR    R14,RSIZE                FIND OUT HOW MANY MORE TO MOVE
         LA    RWORK,WORKS(RSIZE)       POINT TO FIRST TO MOVE  SA56610
         EX    R14,MOVEREST             MOVE THE REMAINDER
QA300    EQU   *                        10 CHAR NOW IN AVTSCRAT
**  RSCAN NOW POINTS TO DATA IN THE BUFFER
**  SET UP LOOP TO LOOK AT:
**       1. EACH STRING FOR ONE TRANSLATE TABLE
**       2. LOOP TO NEXT TABLE
**
         L     R1,AVTDOUBL              POINT TO TRANLIST
         LA    R14,4(,R1)               POINT TO LIST OF TABLES
         SR    RWORK,RWORK              CLEAR WORK REGISTER      S21903
         IC    RWORK,3(,R1)             NUMBER OF TABLES
         LR    RSTG,RWORK               HAVE TO FIND STRINGS
         SLL   RSTG,2                   NO. TBLS. TIMES 4
         LA    RSTG,4(R1,RSTG)          PLUS 4 POINTS TO STRINGS
         LR    RBUF,RSTG                SAVE POINTER TO STRINGS
         SPACE
LP1      EQU   *
         LR    RSTG,RBUF                RESTORE STRING POINTER
         SR    RSIZE,RSIZE              CLEAR WORK REGISTER      S21903
         IC    RSIZE,2(,R1)             NUMBER OF STRINGS
         CLI   0(R14),0                 5041 LIST?
         BE    USEIT                    BRANCH NO
         TM    LCBTSOB,LCB2741N         IF 2741 USE 2741 TABLES ONLY
         BZ    TRY1050                  BRANCH NOT 2741
         CLI   0(R14),1                 THIS A 2741 TABLE
         BE    USEIT                    BRANCH YES
         B     NEXTBL                   NO. TRY THE NEXT
TRY1050  EQU   *
         CLI   0(R14),2                 IS IT A 1050 TABLE
         BNE   NEXTBL                   BRANCH NO TO TRY NEXT
*  YES.  USE THE CURRENT TABLE
USEIT    EQU   *
         L     RTBL,0(,R14)             ADDR OF A TBL
         LA    RTBL,4(,RTBL)            INPUT SIDE
         SPACE
LP2      EQU   *
         SR    REX,REX                  CLEAR WORK REGISTER      S21903
         IC    REX,0(RSTG)              SIZE OF A STRING
         BCTR  REX,0                    DECREMENT FOR EXECUTE    S21903
         MVC   WORKL(10),WORKS          MOVE IN DATA RECEIVED   SA56610
* ADD TEST FOR EBCDIC TO EBCDIC TRANSLATE TABLE (IEDQ25)
         TR    WORKL(10),0(RTBL)        TRANSLATE DATA          SA56610
         OC    WORKL(10),UPPER          FORCE UPPER CASE        SA56610
         EX    REX,COMPEX1              COMPARE TO INPUT
         BE    FOUNDTBL                 GO ON A MATCH
         EX    REX,COMPEX2              BUFFR +1
         BE    FOUNDTBL                 BRANCH  ON TABLE FOUND   S21903
         EX    REX,COMPEX3              BUFFR +2
         BE    FOUNDTBL                 BRANCH ON TABLE FOUND    S21903
         SPACE
         AR    RSTG,REX                 UPDATE STRING POINTER
         LA    RSTG,2(,RSTG)            POINT TO NEXT STRING COUNT
         BCT   RSIZE,LP2                GO IF MORE STRINGS THIS TBL
NEXTBL   EQU   *
         LA    R14,4(,R14)              POINT TO NEXT TABLE ENTRY
         BCT   RWORK,LP1                GO IF MORE TABLES
**  NO MATCHES FOUND, SO RETURN.
**  SET REG. 15 RC=4 TO INDICATE NO TRANSLATION
**  STORE TRANLIST POINTER IN OPTION FIELD.
         B     NOXLT                    COULD NOT FIND MATCH
**  FOUND A MATCH. STORE TABLE ADDRESS IN SCB AND RETURN TO
** IEDQAW TO PERFORM NORMAL TRANSLATION
FOUNDTBL EQU   *
         L     RBUF,0(,R14)             RETRIEVE TABLE ADDRESS
         L     R15,AVTSAVE1             RETRIEVE OPTION FIELD PTRA56610
         ST    RBUF,0(,R15)             STORE ADR OF TBL IN OPT FLD
         LA    RTBL,FOUR(,RBUF)         ADDR TO REG USED BY TRANSLATION
         L     RBASE,AVTSAVE2           RESTORE AW BASE REG
         L     R14,AVTPARM              RESTORE RETURN REG.
         B     FOUR(R14)                RETURN TO IEDQAW AT +4 RETURN
*                                       FOR TRANSLATION. THE ADDR OF
*                                       THE SELECTED INPUT TRANSLATE
*                                       TABLE IS PASSED IN RTBL
MOVEREST MVC   0(0,RWORK),0(R15)        MOVE FROM UNIT 2 TO A5TSCRAT
H10      DC    H'10'                    REQUIRED SIZE OF DATA MOVE
COMPEX1  CLC   WORKL(0),1(RSTG)         START AT BYTE 0 OF BUFFERA56610
COMPEX2  CLC   WORKL+1(0),1(RSTG)       START AT BYTE 1 OF BUFFERA56610
COMPEX3  CLC   WORKL+2(0),1(RSTG)       START AT BYTE 2 OF BUFFERA56610
WORKL    DC    10X'00'                  TRANSLATE DATA WORKAREA SA56610
WORKS    DC    10X'00'                  TRANSLATE DATA SAVEAREA SA56610
UPPER    DC    10X'40'                  BITS TO FORCE UPPER CASE
*
         SPACE 5
         EJECT
********* DSECTS *********
         SPACE
         TQCBD                                                 @OY19796
         SPACE
         TAVTD
         DCBD  DSORG=TX
         TLCBD
         TPRFD
         SPACE
         TSCBD
         TTRMD                                                   A56610
         SPACE
         END
