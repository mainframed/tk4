         TITLE 'IGCV310D - OPERATOR CONTROL START LINE MODULE'
IGCV310D CSECT
         SPACE 3
*  CHANGE ACTIVITY AS FOLLOWS:
******************** MICROFICHE FLAGS *********************** SUPT CODE
*A000000-999999                                                  S99240
*A258500,721150                                                 SA60314
*A046000,047200,106000-107000,168600-169200,178500,285570-285710 S22024
*A285850-285960,301000,374600-375200,652030-652510,652600-653350 S22024
*A653410,653470-653830,752500-753500                             S22024
*A041000,645000                                                  X02004
*C040000,042000,076000,644000                                    X02004
*D034000,174000,240000-242000,248000-250000,260000,284500-285000,X01948
*D746000                                                         X01948
*A226500,457600-459400,652271-652272                             Y06328
*C568000                                                       SA68020
*A478000                                                        SA67777
*D566000-574000                                                @XA03985
*C478000                                                       @SA75429
*A638000                                                       @SA75727
*A354000,360960,360980,361500,713000,730000,080000,753200,     @Y17XARX
*A745200                                                       @Y17XARX
*C010600,011000,046200-047400                                  @Y17XARX
*D010200-010300,110000-140000,374600-375200,458200-459400,     @Y17XARX
*D652030-653800,686100-687500,669000                           @Y17XARX
*D354500                                                       @Y17XART
*A361520,730040                                                @Y17XART
*C644000-645000                                                @OY15473
*A186000,645000                                                @OY15473
*C285500                                                       @OY14092
*C718000                                                       @OZ35627
         SPACE 3
***********************************************************************
*                                                                     *
*  MODULE NAME = IGCV310D (TCAM,OPERATOR CONTROL)              @Y17XARX
*                                                                     *
*  DESCRIPTIVE NAME = OPERATOR CONTROL START LINE                     *
*                                                                     *
*  COPYRIGHT =  NONE                                                  *
*                                                                     *
*  STATUS = VERSION 10.0                                       @Y17XARX
*                                                                     *
* FUNCTION -- THIS ROUTINE PROCESSES OPERATOR CONTROL COMMANDS WHICH  *
*    REQUEST THE STARTING OF ACTIVITY ON A LINE OR LINE GROUP.  THE   *
*    LCB ADDRESS IS OBTAINED FROM DCBIOBAD.  IF IT IS NOT STOPPED AND *
*    THE REQUEST IS FROM TOTE, THE LINE IS POSTED TO ITSELF TO RETURN *
*    IT FROM TOTE TO TCAM.  IF IT IS NOT FROM TOTE, THE NEXT LCB, IF  *
*    ANY, IS ACCESSED.  WHEN ALL THE LCBS HAVE BEEN PROCESSED, IF ANY *
*    HAVE BEEN STARTED A MESSAGE NUMBER IS SET UP SUCH THAT THE       *
*    MESSAGE MODULE WILL OUTPUT THE APPROPRIATE MESSAGE.  OTHERWISE A *
*    MESSAGE NUMBER IS SET UPSUCH THAT AN 'ALREADY STARTED' MESSAGE   *
*    WILL BE PRINTED OUT.  IF THE LINE IS STOPPED, ITS RECEIVE BIT IS *
*    SET ON.  A NOP COMMAND IS BUILT IF IT IS A DIAL LINE.  A SAD IS  *
*    BUILT IF IT IS A 2702 CONTROL UNIT.  AN ENABLE IS BUILT FOR A    *
*    NON-DIAL LINE, AND THE CHANNEL PROGRAM IS COMPLETED.  SVC   X02004
*    114 (EXCPVR) IS ISSUED TO START THE LINE, AND A LINE        X02004
*    STARTED BIT IS TURNED ON.                                   X02004
*    THE NEXT LCB IS ACCESSED AND PROCESSING CONTINUES UNTIL ALL LINES*
*    HAVE BEEN PROCESSED, AS ABOVE.  FOR COMMANDS ASSOCIATED WITHS22024
*    WITH DEVICES OFF AN RN A DIFFERENT APPROACH IS TAKEN.  FOR AS22024
*    AN LGB AS OPPOSED TO A DCB, THIS MODULE PASSES CONTROL    @Y17XARX
*    TO IGCVI10D.  IGCVI10D PROCESSES STARTLINE REQUESTS FOR   @Y17XARX
*    3705 RESOURCES.                                           @Y17XARX
*                                                              @Y17XARX
* NOTES = COMMUNICATION BETWEEN THE VARIOUS TRANSIENT MODULES  @Y17XARX
*    IS PRIMARILY THROUGH THE OPERATOR CONTROL AVT.            @Y17XARX
*    RESTRICTIONS = NA                                         @Y17XARX
*    REGISTER CONVENTIONS = STANDARD                           @Y17XARX
*    PATCH LABEL = ZAPPATCH                                    @Y17XARX
*                                                              @Y17XARX
* MODULE TYPE = PROCEDURE                                      @Y17XARX
*    PROCESSOR=ASSEMBLER                                       @Y17XARX
*    MODULE SIZE =  844 BYTES                                  @Y17XARX
*    ATTRIBUTES = SERIALLY REUSEABLE, REFRESHABLE,ENABLED,     @Y17XARX
*    TRANSIENT, SUPERVISOR MODE.                               @Y17XARX
*                                                                     *
* ENTRY POINTS --                                                     *
*    IGCV310D TO START A LINE.
*                                                                     *
* INPUT --                                                            *
*    REGISTER 1 - ADDRESS OF OPERATOR CONTROL AVT.  RELEVANT FIELDS   *
*    ARE OPCDOUBL.  IT CONTAINS EITHER THE DDNAME OR UCB ADDRESS LEFT *
*    JUSTIFIED.  OPCCOPCE IS THE ADDRESS OF THE CURRENT ELEMENT.      *
*                                                                     *
* OUTPUT --                                                           *
*    REGISTER 0 - NUMBER OF AN ERROR MESSAGE.                         *
*    REGISTER 1 - RETURN CODE.                                        *
*                                                                     *
* EXTERNAL REFERENCES --                                              *
*         EXCPVR (SVC 114)                                       X02004
*                                                                     *
* EXITS,NORMAL --                                                     *
*         EXIT TO IGCVI10D FOR 3705 STARTLINE REQUESTS.        @Y17XARX
*         OCWTG CONTAINS THE ID OF THE REQUESTOR - THIS        @Y17XARX
*         FIELD IS NOT CHANGED BY IGCV310D.   OCTRMTBL         @Y17XARX
*         CONTAINS THE TTCIN OF THE FIRST LINE OR THE          @Y17XARX
*         TTCIN OF THE GROUP AS SET BY THE SUBROUTINE,         @Y17XARX
*         DCBLOCAT.                                            @Y17XARX
*         ALLRLN(X'20') IS SET IN OPCFLAG1 FOR GRP,ALL REQUEST.@Y17XARX
*         LINE STARTED.  APPROPRIATE MESSAGE NUMBER SET UP TO GO TO   *
*         MESSAGE MODULE, X'00' RETURN CODE SET.                      *
*         LINE ALREADY STARTED.  APPROPRIATE MESSAGE NUMBER SET UP TO *
*         TO MESSAGE MODULE, X'08' RETURN CODE SET.                   *
*                                                                     *
* EXITS,ERROR --                                                      *
*         TOTE IN CONTROL OF LINE, DUAL CODES WHEN TWO CODES NOT      *
*         ALLOWED.  ERROR NUMBER SET UP IN REGISTER, X'04' RETURN CODE*
*         SET.                                                        *
*                                                                     *
* TABLES/WORK AREAS --                                                *
*    OPERATOR CONTROL AVT, OPERATOR CONTROL ELEMENT, STOPLINE REQUEST *
*    ELEMENT, DEB, DCB, LCB, QCB, TERMNAME TABLE, TERMINAL ENTRY,S22024
*    AVT, BTU, AND RSID TABLE.                                   S22024
*    AVT.                                                             *
*                                                                     *
*                                                                     *
*    THIS IS A MODULE RE-WRITE OF IEDQCU AS OF OS/TCAM LEVEL II       *
***********************************************************************
         EJECT
         SPACE
*                                 E Q U A T E S
         SPACE
RZERO    EQU   0                        PARAMETER REGISTER
RPARM    EQU   1                        PARAMETER REGISTER
ROPCAVT  EQU   2                        OP CONTROL AVT BASE
ROPCE    EQU   3                        CURRENT ELEMENT BASE
RAVT     EQU   4                        TCAM AVT POINTER
RDCB     EQU   5                        BASE FOR DCB DSECT
RUCB     EQU   6                        UCB POINTER
RDEB     EQU   7                        DEB POINTER
RPRF     EQU   7                        PREFIX BASE REGISTER     S22024
RBTU     EQU   6                        ADDR OF BTU              S22024
RWORK    EQU   8                        WORK REGISTER
RALSTAT  EQU   8                        REGISTER FOR SAVING    @ZM08380
*                                       ALL OPTION STATUS FLAG @ZM08380
RRLN     EQU   9                        RELATIVE LINE NUMBER (BINARY)
RLCB     EQU   10                       POINTER TO LCB
RLOOP    EQU   11                       LOOP COUNTER FOR START ALL
RTERM    EQU   11                       TERM ENTRY ADDRESS       S22024
RWORK1   EQU   11                       WORK REG
RBASE    EQU   12                       MODULE BASE REGISTER
RRET     EQU   14                       SUBROUTINE RETURN ADDRESS
RBRNCH   EQU   15                       SUBROUTINE ENTRY
*                                                                     *
         SPACE 2
AD       EQU   7                        MASK FOR STCM          @OY15473
ADDRLEN  EQU   3                        LENGTH OF ADDRESS FIELD
ADDROFF  EQU   4                        OFFSET TO CCW OFFSET FIELD
ALLRLN   EQU   X'20'                    RLN SPECIFIED AS 'ALL'
BISYNC   EQU   X'90'                    BSC ADAPTER ON CONTROL UNIT
CHAIN    EQU   0                        OFFSET TO UPDATE CHAINING FLAGS
CMDCHAIN EQU   X'40'                    CCW COMMAND CHAINING BIT
CODES    EQU   4                        DUAL CODE IN UCB
C2703    EQU   3                        CONTROL UNIT IS 2703
DIAL     EQU   X'90'                    LINE HAS AUTOCALL-AUTOANSW
DISABLE  EQU   X'2F'                    DISABLE CCW OP CODE
DLE      EQU   11                       OFFSET TO DISABLE WRITE DLE-EOT
DOUBLWD  EQU   8                        LENGTH OF DOUBLE WORD
DUALCODE EQU   8                        DUAL CODE SET IN DCB
DUALINT  EQU   X'20'                    DUAL INTERFACE IN UCB
EIGHT    EQU   8                        LENGTH OF EIGHT IN UNPACK
ENABLE   EQU   X'27'                    ENABLE CCW OP CODE
ERROR19  EQU   19                       ERROR-LINE ALREADY STARTED
ERROR49  EQU   49                       ERROR-TOTE CONTROLS LINE
ERROR47  EQU   47                        ERROR-SYSTEM DELAY ACTIVE
ERROR92  EQU   92                       ERROR-BSC INCOMPATABLE MODE
ERROR443 EQU   443                      ERROR-LINE IN EP MODE    Y06328
ERR210   EQU   210                      ACTIVATE DEACT ERROR MSG S22024
FIRSTRLN EQU   1                        FIRST VALID RLN FOR DCB
FLAGS    EQU   8                        OFFSET TO LAST OF CCW
FOUR     EQU   4                        OFFSET OF 4
GOODSTRT EQU   20                       STARTED MESSAGE
INTFB    EQU   X'20'                    INTERFACE B SET IN DCB
INTRUPT  EQU   4                        PERMIT INTERRUPT MODE OP
LINK     EQU   0                        OFFSET TO LINK FIELD
LOCAL    EQU   X'10'                    DEVICE CLASS OF 2260 LOCAL
NAMELEN  EQU   2                        LENGTH OF NAME CHANGE
NEXT     EQU   1                        UPDATE FOR RLN START ALL
NEXTCCW  EQU   8                        LENGTH TO GET TO NEXT CCW
NOP      EQU   3                        NOP FOR STARTLINE
NOTCA    EQU   2                                                SA60314
NUMBER   EQU   X'F0'                    COVER SIGN AFTER UNPACK
NWRTEOT  EQU   X'FE'                    NO DLE-EOT WRITTEN
ONE      EQU   1                        OFFSET OF ONE
OPCODE   EQU   4                        OFFSET TO CCW COMMAND CODE
REMOTE   EQU   X'82'                    2701 WITH IBM III ADAPTER
SETMODE  EQU   X'23'                    SETMODE CCW OP CODE
STARTONE EQU   128                      AT LEAST ONE LINE STARTED
TOTE     EQU   X'F1'                    TOE HAS LCB FOR LINE
TWO      EQU   2                        LENGTH OF TWO
UCBCLASS EQU   18                       OFFSET TO DEVICE CLASS IN UCB
UCBCNTRL EQU   19                       OFFSET TO UCB CONTROL UNIT TYPE
UCBFEAT  EQU   17                       OFFSET TO UCB FEATURES IN TYPE
VARINAME EQU   3                        OFFSET TO CHANGEABLE NAME
ERROR17  EQU   17                       DCB NOT OPEN           @OY14092
ERROR18  EQU   18                       COMMAND INVALID          S22024
ERR400   EQU   400                      NOT ACTIVE MESSAGE       S22024
ZERO     EQU   0                        INIT QUANTITY            S22024
DSLGB    EQU   X'80'                    LGB IDENTIFIER           S22024
POST     EQU   X'0C'                    INDICATE THAT THIS IS A TPOST
ENDOFLST EQU   X'80'                    END OF LIST INDICATOR    S22024
CLRSZ    EQU   22                       FOR BTU AREA             S22024
THREE    EQU   3                        LENGTH OF COMMAND        S22024
         EJECT
         USING IEDQAVTD,RAVT            TCAM AVT ADDRESSABILITY
         USING DEBNMSUB,RDEB            DEB ADDRESSABILITY
         USING IHADCB,RDCB              DCB ADDRESSABILITY
         USING IEDQLCB,RLCB             LCB ADDRESSABILITY
         USING IEDQOPCE,ROPCE           ELEMENT ADDRESSABILITY
         USING IEDQOPCD,ROPCAVT         OP CONTROL AVT ADDRESSABILITY
         SPACE
         USING IEDQTRM,RTERM                                     S22024
         BALR  RBASE,RZERO              SET BASE REGISTER
         USING *,RBASE                  SET ADDRESSABILITY
IGCV310D IEDHJN  SKIPID
*              INITIAL SETUP                                          *
         LR    ROPCAVT,RPARM            ADDRESS OF OP CONTROL AVT
         L     RAVT,OPCAVTPT            ADDRESS OF TCAM AVT
         L     ROPCE,OPCCOPCE           CURRENT ELEMENT POINTER
         MVC   OPCLDNME+VARINAME(NAMELEN),ERRXCTL SET FOR ERROR MODULE
         LA    RZERO,ERROR47            SET DELAY ERROR MESSAGE
         TM    AVTBIT1,AVTDLAYN         SYSTEM DELAY IN PROGRESS?
         BO    ERRRTN                   YES, ERROR MSG NEEDED    S22024
         SPACE
         MVC   OPCDOUBL(DOUBLWD),OCBACKUP PLACE INPUT FOR VERIFY
         SR    RPARM,RPARM              CLEAR FOR RLN
         TM    OCFLAG,OCLINE            LINE NUMBER IN DATA?
         BO    VERIFY                   YES, ALL PARAMETERS ARE SET
         SPACE
         IC    RPARM,OCRLN              PICK UP RELATIVE LINE NUMBER
         LTR   RPARM,RPARM              ALL SPECIFIED?
         BNE   VERIFY                   NO, FIND DCB AND UCB
         SPACE
         LA    RPARM,FIRSTRLN           SET RLN TO ONE
         OI    OPCFLAG1,ALLRLN          INDICATE RLN OF ALL
VERIFY   EQU   *
         IC    RALSTAT,OCALSTAT         SAVE THE 'ALL OPTION'  @ZM08380
*                                       STATUS FLAG            @ZM08380
         L     RBRNCH,OPCDCBLK          GET LOOK-UP ADDRESS
         BALR  RRET,RBRNCH              LOCATE UCB AND DCB
         B     PROCESS                  NORMAL RETURN GO START LINE
         SPACE
         LTR   RDCB,RPARM               SET DCB BASE REG
         BZ    SET17                    BRANCH IF DDNAME NOT FOUND
         TM    DCBOFLGS,OPEN            WAS DCB OPEN
         BZ    SET17                    NO,SEND ERROR MESSAGE
         TM    DCBDSORG,DSLGB           IS THIS AN LGB         @Y17XARX
         BO    RLNOK                    YES, BYPASS RLN CHECK  @Y17XARX
         BCT   RZERO,SET17              BRANCH IF RLN TOO HIGH
RLNOK    EQU   *                                               @Y17XARX
         TM    OPCFLAG1,ALLRLN          RLN OF 'ALL'
         BO    PROCESS                  YES,ENTER LOOP
SET17    EQU   *
         LA    RZERO,ERROR17            SET ERROR MSG NUMBER
ERRRTN   EQU   *                                                 S22024
         LA    RPARM,FOUR               SET ERROR RETURN CODE
         B     ERROR                    AND SEND ERROR MSG
OPEN     EQU   X'10'
         SPACE
PROCESS  EQU   *
         SPACE 1                                               @Y17XARX
         STC   RALSTAT,OCALSTAT         RESTORE 'ALL OPTION'   @ZM08380
*                                       STATUS FLAG            @ZM08380
         TM    OPCFLAG1,ALLRLN          ALL PROCESSING ?       @Y17XART
         BNO   NOTALL                   BRANCH IF NOT ALL      @ZM08380
         OI    OCALSTAT,OCALLFLG        SET ALL INDICATOR      @Y17XART
         B     AROUND                   GO GET DCB/LGB ADDRESS @ZM08380
NOTALL   EQU   *                        NOT PROCESSING ALL     @ZM08380
         NI    OCALSTAT,OCFOXES-OCALLFLG  SET 'ALL' FLAG OFF   @ZM08380
AROUND   EQU   *                                               @Y17XART
         LR    RDCB,RPARM               SET UP DCB/LGB ADDR    @Y17XARX
         TM    DCBDSORG,DSLGB           IS THIS AN LGB         @Y17XARX
         BO    CCU3705A                 YES, BRANCH            @Y17XARX
         SPACE
         TM    OPCFLAG1,ALLRLN          RLN OF 'ALL'?
         BNO   GOODRLN                  NO, DO NOT RESET
         MVI   OCRLN,AVTEZERO           SET RLN TO ZERO
         LA    RWORK,OPCWAITL           WAIT LIST
LOCATEND EQU   *
         L     RWORK1,AVTEZERO(RWORK)   GET NEXT ELEM
         LA    RWORK1,AVTEZERO(RWORK1)  CLEAR HIGH BYTE
         LTR   RWORK1,RWORK1            IS THIS ELEM
         BZ    GOODRLN                  NO PROCESS COMMAND
         CLC   OCBACKUP-IEDQOPCE(EIGHT,ROPCE),OCBACKUP-IEDQOPCE(RWORK1)
         BE    ATTACH                   YES
         LR    RWORK,RWORK1             NEW ELEMENT BECOMES OLD
         B     LOCATEND                 FIND END OF LIST
GOODRLN  EQU   *
         LR    RDCB,RPARM               SAVE RETURNED DCB ADDRESS
         L     RDEB,DCBDEBAD            PICK UP DEB ADDRESS FROM DCB
         LA    RRLN,FIRSTRLN            SET RLN TO ONE
         SR    RLOOP,RLOOP              CLEAR FOR NUMBER TO EXTENTS
         IC    RLOOP,DEBNMEXT           GET EXTENTS FROM DEB
         CLI   OCRLN,AVTEZERO           ALL FOR RELATIVE LINE NUMBER
         BE    START                    YES, START THE LINES
         SPACE
         LR    RLOOP,RRLN               SET LOOP COUNTER TO ONE
         IC    RRLN,OCRLN               PICK UP ACTUAL RLN
*              START LINE                                             *
START    EQU   *
         SLL   RRLN,TWO                 RLN TIMES FOUR
         L     RUCB,DEBUCBAD-FOUR(RRLN) PICK UP UCB ADDRESS
         LA    RUCB,AVTEZERO(RUCB)      CLEAR HIGH BYTE
         SRL   RRLN,TWO                 RESET RLN VALUE
         LTR   RUCB,RUCB                OPENED DD DUMMY?
         BZ    CHEKLAST                 YES, CHECK FOR MORE TO PROCESS
         SPACE
         L     RLCB,DCBIOBAD            GET IOB BASE ADDRESS FROM DCB
         LA    RLCB,AVTEZERO(RLCB)      CLEAR HIGH ORDER BYTE
         LR    RZERO,RRLN               SAVE RELATIVE LINE NUMBER
         SR    RPARM,RPARM              CLEAR FOR IOB SIZE
         IC    RPARM,DCBEIOBX           PICK UP IOB/LCB LENGTH
         MR    RZERO,RZERO              MULTIPLY RLN TIMES IOB SIZE
         AR    RLCB,RPARM               ADD OFFSET TO BASE ADDRESS
         LA    RPARM,LCBFLAG1-IEDQLCB   GET OFFSET TO IOB IN LCB
         SR    RLCB,RPARM               SUBTRACT OFFSET TO GET LCB ADDR
         TM    OCFLAG,OCTOTE            IS THIS A REQUEST FROM TOTE
         BZ    NOTTOTE                  NO
         SPACE
         CLI   LCBCPA,TOTE              LCB UNDER TOTE'S CONTROL
         BNE   NOTOTE                   NO
         SPACE
         NI    LCBCPA,OCFOXES-TOTE      TURN OFF TOTE CONTROL FLAG
NOTTOTE  EQU   *
         CLI   LCBCPA,TOTE              LCB UNDER TOTE CONTROL?
         BNE   NOTOTE                   NO, TOTE DOES NOT HAVE LINE
TOTE3705 EQU   *                                                 S22024
         SPACE
         LA    RZERO,ERROR49            SET TOTE CONTROLS LINE ERROR
         B     ERRRTN                   COMMAND NOT VALID        S22024
         SPACE
         SPACE
NOTOTE   EQU   *
         CLI   LCBSTAT1,AVTEZERO        LINE STOPPED?
         BNE   CHEKLAST                 NO, CHECK FOR MORE LINES
         SPACE
READY    EQU   *
         CLC   ONE(ADDRLEN,RLCB),AVTSUPPT+ONE LCB ON START UP READY Q?
         BE    READY                    YES, WAIT UNTIL OFF
         SPACE
         MVI   LCBSTAT1,LCBRECVN+LCBSENDN SET IF NO END STATUS @SA75429
         NI    LCBERBST,AVTEFF-LCBPRCPG  TURN OFF FLAG          SA67777
         ST    RLCB,LINK(RLCB)          POST LCB TO ITSELF
         OI    OPCFLAG1,STARTONE        INDICATE ONE LIEN STARTED
         LA    RPARM,LCBCPA             GET CHANNEL PROGRAM AREA ADDR
         ST    RPARM,LCBSIOCC           SET CHANNEL PROG START ADDRESS
         SH    RPARM,AVTHA4             BACKUP BEFORE CCW        S22024
         ST    ROPCE,ADDROFF(RPARM)     SET VALID ADDRESS IN CCW
         MVI   OPCODE(RPARM),NOP        SET NOP COMMAND CODE
         MVC   FLAGS(FOUR,RPARM),VALIDCCW FINISH CCW FLAGS AND LENGTH
         CLI   UCBCLASS(RUCB),LOCAL     LOCAL 2260 LINE?
         BE    EXCP                     YES, ISSUE EXCP FOR LINE
         SPACE
         CLI   UCBCNTRL(RUCB),REMOTE    2701 WITH IBM III (2260R)
         BE    EXCP                     YES, START THE LINE
         SPACE
         IC    RWORK,UCBCNTRL(RUCB)     PICK UP CONTROL UNIT FIELD
         N     RWORK,CNTRLMSK           CLEAR ALL BUT CU BITS
         BZ    EXCP                     NO CONTROL UNIT, START THE 7770
         SPACE
         MVI   OPCODE(RPARM),DISABLE    SET DISABLE COMMAND
         LA    RPARM,NEXTCCW(RPARM)     POINT FOR NEXT CCW
         BCT   RWORK,NOT02              BRANCH FOR 2703 OR 2701
         SPACE
*              2702 CONTROL UNIT                                      *
         IC    RWORK,UCBFEAT(RUCB)      GET FEATURES BYTE
         N     RWORK,CNTRLMSK           CLEAR ALL EXCEPT SAD BUTS
         IC    RWORK,SADCODES(RWORK)    INSERT ACTUAL SAD COMMAND
         ST    ROPCE,ADDROFF(RPARM)     PLACE VALID ADDRESS IN CCW
         STC   RWORK,OPCODE(RPARM)      PLACE SAD COMMAND IN CCW
         MVC   FLAGS(FOUR,RPARM),VALIDCCW FINISH CCW FLAGS AND LENGTH
         OI    CHAIN(RPARM),CMDCHAIN    SET COMMAND CHAIN IN PREV CCW
         LA    RPARM,NEXTCCW(RPARM)     POINT TO NEXT CCW
*              2703 OR 2701 CONTROL UNIT                              *
NOT02    EQU   *
         TM    UCBCNTRL(RUCB),BISYNC    BSC ADAPTER?
         BNO   NOBSC                    NO, SKIP SETMODE PROCESSING
         SPACE
         NI    LCBTPCD+DLE,NWRTEOT      DO NOT WRITE DLE-EOT
         LA    RWORK,DCBINVLI           LOAD FIRST INVL ADDR FROM DCB
         LR    RZERO,RRLN               SAVE RLN
         BCTR  RRLN,RZERO               DECREMENT BY ONE FOR OFFSET
         SLL   RRLN,TWO                 MULTIPLY BY FOUR FOR OFFSET
         AR    RWORK,RRLN               ADD OFFSET TO FIRST ADDR
         LR    RRLN,RZERO               RESTORE RLN FOR LATER
         TM    UCBCNTRL(RUCB),C2703     CONTROL=2703?
         BO    MODESET                  YES, SET UP SETMODE COMMAND
         SPACE
         TM    AVTEZERO(RWORK),INTFB    INTERFACE B SET IN DCB?
         BNO   TESTCODE                 NO, CHECK DUAL CODE
         SPACE
         TM    UCBFEAT(RUCB),DUALINT    DUAL INTERFACE SET IN UCB?
         BZ    ERR92                    NO, INTERFACE IN DCB IS WRONG
         SPACE
TESTCODE EQU   *
         TM    AVTEZERO(RWORK),DUALCODE DUAL CODE INDICATED?
         BZ    MODESET                  NO, DO THE SET MODE
         SPACE
         TM    UCBFEAT(RUCB),CODES      TWO CODES ALLOWED?
         BZ    ERR92                    NO, UCB NOT AGREEABLE WITH DCB
         SPACE
MODESET  EQU   *
         ST    RWORK,ADDROFF(RPARM)     PLACE MODE BYTE ADDR IN CCW
         MVI   OPCODE(RPARM),SETMODE    PLACE SETMODE OP CODE
         MVC   FLAGS(FOUR,RPARM),VALIDCCW FINISH CCW FLAGS AND LENGTH
         OI    CHAIN(RPARM),CMDCHAIN    SET COMMAND CHAIN IN LAST CCW
         LA    RPARM,NEXTCCW(RPARM)     POINT TO NEXT CCW
*              NO BISYNC ADAPTER                                      *
NOBSC    EQU   *
         TM    UCBFEAT(RUCB),DIAL       DIAL LINE?
         BNZ   EXCP                     YES, SKIP ENABLE CCW
         SPACE
         ST    ROPCE,ADDROFF(RPARM)     SET VALID ADDRESS IN CCW
         MVC   FLAGS(FOUR,RPARM),VALIDCCW FINISH CCW FLAGS AND LENGTH
         MVI   OPCODE(RPARM),ENABLE     SET OP CODE TO ENABLE
         OI    CHAIN(RPARM),CMDCHAIN    CHAIN THIS TO PREVIOUS CCW
EXCP     EQU   *
         SR    RRET,RRET                CLEAR A REGISTER       @SA75727
         STH   RRET,LCBTTCIN            CLEAR TTCIN            @SA75727
         MVC   LCBTPCD(FOUR),TPOPCODE   SET TP OP CODES IN LCB
         SPACE
         L     RPARM,AVTHK              GET ADDRESS OF IEDQHK  @OY15473
         ST    RPARM,LCBQCBA-1          SET TO POST LCBTO HK   @OY15473
         XC    LCBLINK,LCBLINK          CLEAR LINK FIELD       @OY15473
         MVI   LCBPRI,PRIPRIME          SET LCB PRIORITY       @OY15473
         STCM  RLCB,AD,OPCAQFST         SET FIRST AQCTL PARM   @OY15473
         STCM  RLCB,AD,OPCAQSEC         SET SECOND PARM        @OY15473
         MVI   OPCAQREQ,POST            SET TO POST LCB        @OY15473
         MVI   OPCAQRQ2,ENDOFLST        SET END OF LIST        @OY15473
         LA    RPARM,OPCAQCTL           POINT TO AQCTL LIST    @OY15473
         AQCTL ,                        POST LCB TO IEDQHK     @OY15473
         SPACE 2
CHEKLAST EQU   *
         LA    RRLN,NEXT(RRLN)          UPDATE RLN NUMBER FOR ALL
         BCT   RLOOP,START              LOOP UNTIL ALL STARTED
         SPACE
         LA    RPARM,EIGHT              SET ALREADY DONE RET CODE
         TM    OCFLAG,OCMPP             MPP INPUT
         BZ    TESTART                  NO
         SR    RPARM,RPARM              SET ZERO COND CODE FOR ALREADY
TESTART  EQU   *
         LA    RZERO,ERROR19            SET POSSIBLE ERROR NUMBER
         TM    OPCFLAG1,STARTONE        AT LEAST ONE LINE STARTED?
         BNO   ERROR                    NO, ALL ALREADY STARTED
         SPACE
*              CHECK ALL EXIT SITUATIONS                              *
         SR    RPARM,RPARM              SET GOOD RETURN
         LH    RZERO,OCPARM             SET RETURN PARAMETER
         MVC   OPCLDNME+VARINAME(NAMELEN),OCWTG SET RETURN NAME
         CH    RPARM,OCWTG              RETURN ASKED ?           S22024
         STH   RPARM,OCWTG              CLEAR OCWTG              S22024
         BNE   XCTL                     YES, RETURN TO CALLING   S22024
*                                       MODULE                   S22024
         SPACE
         MVC   OPCLDNME+VARINAME(NAMELEN),CHECKPT SET CHECKPOINT NAME
         MVC   OCWTG(NAMELEN),WRTMSG    WTG TO MESSAGE MODULE
         MVI   OCPARM,AVTEZERO          CLEAR FIRST BYTE
         MVI   OCPARM+ONE,GOODSTRT      SET MESSAGE NUMBER
         TM    OCFLAG,OCTOTE            TOTE COMMAND             S22024
         BO    TOTEEXIT                 YES, BRANCH              S22024
ERROR    EQU   *
         TM    OCFLAG,OCTOTE+OCMPP      TOTE OR MPP INPUT?
         BZ    XCTL                     NO, GO TO ERROR MSG MODULE
TOTEEXIT EQU   *                                                 S22024
         SPACE
         MVC   OCWTG(NAMELEN),WRITE     SET TO GO TO WRITER
         STH   RPARM,OCPARM             GET PARAMETER FIELD
         LTR   RPARM,RPARM              GOOD RETURN?
         BZ    XCTL                     YES, GO ON TO CHECKPOINT
         SPACE
         LR    RZERO,RPARM              NO, SET RETURN CODE IN ZERO
         MVC   OPCLDNME+VARINAME(NAMELEN),WRITE GO DIRECTLY TO WRITER
XCTL     EQU   *
         NI    OPCFLAG1,AVTEFF-ALLRLN-STARTONE CLEAR FLAGS SET HERE
         MVC   OCUNIT+ONE(VARINAME),INVTIC  SET TIC COMMAND
IMMDXCTL EQU   *                                               @Y17XARX
         LR    RPARM,ROPCAVT            PASS OP CONTROL AVT ADDRESS
         SPACE
         IEDXCTL                                               @OZ35627
         SPACE 2
ATTACH   EQU   *
         TM    OCFLAG-IEDQOPCE(RWORK1),OCATTACH ATTACHED ELEMENT
         BNO   ATTACHIT                 NO QUEUE IT UP
         L     RWORK1,FOUR(RWORK1)      GET NEXT ELEMENT IN CHAIN
         B     ATTACH                   ATTACH ELEM TO LIST
         SPACE
ATTACHIT EQU   *
         ST    ROPCE,FOUR(RWORK1)       QUEUE UP NEW ELEMENT
         OI    OCFLAG-IEDQOPCE(RWORK1),OCATTACH UNIT IS ATTACHED
         MVC   OCWTG(NAMELEN),STRTLINE  START LINE MODULE
         MVC   OPCLDNME+VARINAME(NAMELEN),FRSTLOAD SET TO GO TO 00
         LA    RZERO,NOTCA              ENTRY CODE FOR NOT CA   SA60314
         B     XCTL                     GO TO NEXT MOD
         SPACE   3
*    THE DCB AND UCB PARAMETERS FOR DUAL CODE DO NOT AGREE
ERR92    EQU   *
         LA    RZERO,ERROR92            SET POSSIBLE ERROR CODE
         B     ERRRTN                   RETURN ERROR INDICATION  S22024
         SPACE 2                                               @Y17XARX
CCU3705A EQU   *                                               @Y17XARX
         NI    OCALSTAT,AVTEFF-OCRENTRD CLEAR VI 1ST ENTRY FLG @Y17XART
         MVC   OPCLDNME+VARINAME(NAMELEN),NCPLOAD   SET FOR VI @Y17XARX
         MVI   OPREPRI-IEDOPRED(ROPCE),AVTEZERO CLEAR PRIORITY @Y17XARX
         MVI   OPCFLAG1,AVTEZERO        CLEAR FLAG BYTE        @Y17XARX
         NI    OCFLAG,AVTEFF-OCRESP     CLEAR RESPONSE BIT     @Y17XARX
         XC    OCPARM,OCPARM            CLEAR PARM FIELD       @Y17XARX
         B     IMMDXCTL                 XCTL TO IGCVI10D       @Y17XARX
         SPACE 2
CNTRLMSK DC    F'3'                     MASK TO ISOLATE LOW ORDER BITS
ERRXCTL  DC    C'03'                    FIRST ERROR LOAD NAME
CHECKPT  DC    C'06'                    CHECKPOINT MODULE NAME
WRITE    DC    C'07'                    MESSAGE WRITER/ROUTER
WRTMSG   DC    C'09'                    GOOD MESSAGES FOR VARY MODULE
STRTLINE DC    C'V3'                    START LINE MODULE
FRSTLOAD DC    C'00'                    FIRST LOAD OF OP CTL
NCPLOAD  DC    C'VI'                    ID OF 3705 PROCESSOR   @Y17XARX
TPOPCODE DC    X'02020202'              TP OP CODES FOR LCB
SADCODES DC    X'13171B1F'              ACTUAL SAD COMMAND CODES
VALIDCCW DC    X'20000001'              VALID LAST HALF OF CCW
ACTLINE  DC    X'089880'                ACTIVATE LINE AND FLAGS  S22024
INVTIC   DC    X'000002'                INVALID TIC ADDRESS
ZAPPATCH DC    25X'FF'                  PATCH AREA             @Y17XARX
         TITLE 'IGCV310D - DSECTS REFERENCED BY THIS MODULE'
         TAVTD
         EJECT
         TDEBD
         EJECT
         DCBD  DSORG=TX
         EJECT
         TLCBD
         EJECT
         TOPCED
         EJECT
         TOPCAVTD
         TOPRED                                                @Y17XARX
         TTRMD                                                   S22024
         EJECT
         TPRFD                                                   S22024
         EJECT
         TPRIOR
         EJECT
         TLGBD                                                   S22024
         END
