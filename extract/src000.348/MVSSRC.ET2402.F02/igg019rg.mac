19RG     TITLE '''IGG019RG'' - TAM GET/READ ROUTINE'
IGG019RG CSECT
*CHANGE-ACTIVITY = AS FOLLOWS:                                  SA21903
**************************** MICROFICHE FLAGS *************** SUPT CODE
*A187200                                                        SA55389
*A236800,853600,920000                                          SA52981
*A647200                                                         A48268
*C070400                                                         A48268
*D647200-649600                                                  A48268
*A090400                                                         A44906
*C073200,508800                                                  A44906
*A560000                                                        SA59040
*A096400                                                        SA57699
*C155200                                                        SA57699
*D161600-170400,173600-180800,566000,568260-568740,816800-822400SA57699
*A182400                                                        SA55389
*D187200                                                        SA55389
*C187600                                                        SA55389
*A822520                                                        SA55392
*C823000                                                        SA55392
*C073200,164000,167200                                           A41028
*D164800                                                         A41028
*C070400,492000,610400,865600                                    A47157
*D073000-073400,855200-857600,920000                             A47157
*C073200                                                         A42386
*A822400                                                         A42386
*A509600                                                         A42397
*A633300,637300-637599,785300-785400,788500-788788,885300        S21101
*D660800-687200                                                  S21101
*D510400-511200                                                  A42397
*C073200                                                         S21101
*C508800-509600                                                  A49207
*A152000,919200                                                  A44910
*A264100-264700,264900-265500,265700-266300,279600,281800-       S22025
*A282200,282600-286250,368100-368500,870800,919400-919800        S22025
*C070400-073200,221600,228800,248000,264000,264800,265600,266400 S22025
*C267200,281600,282400,302400,448000,475200,484800,537600,574400 S22025
*C698400,706400,711200,716000,753600,777200,859200,861600.864800 S22025
*C872000-872800                                                  S22025
*D276200,366400,920000                                           S22025
*A559200                                                        SA49225
*C213600,459200,499200,815200                                   SCLUP21
*D454400                                                        SCLUP21
*A565600,568000                                                 SA51083
*D570400,688000                                                 SA51083
*A133060-133120,182600-182800,850000                            SA60004
*A296200-296600                                                  S21903
*A116800,133000,920600                                          SA63961
*C000000-999999
*A033200,055300-055700,070450-070750,071300-071500,118470-119160,Y01004
*A206800-208000,209200,267000-267600,331220-331900,339300-339600,Y01004
*A340200-340600,463260-463860,564900-565200,591230-591900,       Y01004
*A621000-621200,622000,854400-856490,872510,915600,              Y01004
*A920060-920195,920300-920360,920420-920720,920960-921260,922800,Y01004
*A929200,930000,930800,931600,932400,933200,934000,934800,935600 Y01004
*C070400,071200,072000,097600,108000,127200-128000,177600,206400,Y01004
*C208800,266400,266700,332800,339200,463200,535200,564800,601600,Y01004
*C617600,620800,                                                 Y01004
*C621600,633600-634400,785400,788720,788760,817600-820000,858400 Y01004
*D108800-116800,128800,136800,172000,266500-266600,266800,       Y01004
*D473600-475600,488000-496800,576000-577600,581600,692000,       Y01004
*D717600-721600,749600,778400-784000,789600-795200,873000,       Y01004
*D879200-880800,891200,903200,911200,916800,920800               Y01004
*A203060                                                         X02004
*C202440,203040                                                  X02004
*D202480                                                         X02004
*A072000,340600                                                 SA66593
*C070450-071600,076000-083200                                  @ZA00205
*A873400-873500                                                @ZA00205
*C414400                                                       @ZA05013
*A492400,921800                                                @ZA05013
*C228400-246400                         PECB DSECT CHG         @OZ07830
         SPACE 4
***********************************************************************
*TITLE: 'IGG019RG' SAM COMPATIBLE GET/READ ROUTINE                    *
*MODULE-NAME = IGG019RG                                         SA21903
*DESCRIPTIVE NAME =TCAM GET/READ ROUTINE                        SA21903
* COPYRIGHTS = 'NONE'                                           SA21903
*STATUS:  CHANGE LEVEL 5                                              *
*FUNCTION: THIS ROUTINE IS ACTIVATED WHEN A GET OR READ IS ISSUED IN  *
*   THE USER'S SAM COMPATIBLE APPLICATION PROGRAM. AFTER VALIDITY     *
*   CHECKING THE USERS WORKAREA, AN RCB IS SET UP WITH ECB ADDRESS.   *
*   SVC 102 IS ISSUED FOR THE GET/READ FUNCTION. FOR QSAM, IF R-AHEAD *
*   QUEUE IS EMPTY AND SETEOF IS NOT SPECIFIED, IT WAITS FOR MORE     *
*   DATA.  FOR BSAM, A COMPLETION CODE IS STORED IN THE DECB.  THE    *
*   USER'S EODAD EXIT IS ENTERED ONLY WHEN THE USER ISSUES GET OR     *
*   READ/CHECK AND THE EOM SEGMENT OF A SETEOF MESSAGE WAS SEEN ON    *
*   THE PREVIOUS OPERATION.                                           *
*   IF THE USER HAS SPECIFIED A CHECKPOINT EXIT, THIS EXIT IS TAKEN   *
*   WHEN A HEADER BUFFER IS PROCESSED, AND A CHECKPOINT HAS BEEN      *
*   TAKEN IN THE MCP SINCE THE LAST HEADER BUFFER WAS EXAMINED.  IF   *
*   AN ERROR OCCURS AND SYNAD IS SPECIFIED,THE EXIT IS TAKEN; FOR     *
*   BSAM, A CODE IS STORED IN THE DECB.                               *
*ENTRY POINT:                                                         *
*        IGG019RG                                                     *
*INPUT:                                                               *
*   0 -  ADDRESS OF USER'S WORKAREA(GET)                              *
*   1 -  DCB ADDRESS(GET) OR DECB ADDRESS(READ)                       *
*   13-  ADDRESS OF 18-WORD REGISTER SAVEAREA                         *
*   14-  RETURN ADDRESS                                               *
*   15-  ENTRY POINT ADDRESS                                          *
*OUTPUT:                                                              *
*   15-  RETURN CODE(GET); DECB COMPLETION CODE(READ)                 *
*EXTERNAL ROUTINES:                                                   *
*        SVC 102                                                      *
*EXIT-NORMAL: X'00' IN REG 15(GET) OR COMP CODE OF X'7F' IN DECB      *
*EXIT-ERROR:  SYNAD EXIT, IF SPECIFIED, IS TAKEN.  FOR QSAM, CODE=8   *
*        FOR WORKAREA OVERFLOW AND 4 FOR SETEOF WITHOUT EODAD.        *
*        FOR QSAM, RETURN CODE OF X'10' IS SET FOR SOWA ERROR.        *
*        RETURN CODE OF X'80' FOR MCP NOT ACTIVE.                     *
*        RETURN CODE OF X'90' FOR INVALID SVC 102.                    *
*        FOR BSAM, THE FOLLOWING COMPLETION CODES EXIST:              *
*         X'70' - SETEOF                                              *
*         X'52' - WORKAREA OVERFLOW                                   *
*        CODES SET BY IEDQEB:                                         *
*         X'02' - EMPTY QUEUE                                         *
*         X'01' - EMPTY READ-AHEAD QUEUE, NOT DESTINATION QUEUE       *
*TABLES/WORK AREAS:                                                   *
*   DECB                                                              *
*   DEB                                                               *
*   DCB                                                               *
*   PECB                                                         Y02027
*ATTRIBUTES: REENTRANT, REFRESHABLE, PROBLEM PROGRAM MODE             *
*                                                                     *
*NOTES: THE OPERATION OF THIS MODULE DEPENDS UPON AN INTERNAL         *
*   REPRESENTATION OF THE EXTERNAL CHARACTER SET WHICH IS EQUIVALENT  *
*   TO THE ONE USED AT ASSEMBLY TIME.  THE CODING HAS BEEN ARRANGED   *
*   SO THAT REDEFINITION OF 'CHARACTER' CONSTANTS, BY REASSEMBLY,     *
*   WILL RESULT IN A CORRECT MODULE FOR THE NEW DEFINITIONS.          *
*                                                                     *
*                                                                     *
***********************************************************************
         EJECT
RWORKA   EQU   0                        ADDRESS OF WORKAREA (QSAM)
RPARM    EQU   1                        ADDR OF DCB(QSAM) OR DECB
R1       EQU   1                        WORK REG                 S22027
RSIZE    EQU   2                        SIZE OF WORKAREA         Y02027
R3       EQU   3                        WORK REGISTER            Y02027
R4       EQU   4                        WORK REGISTER            Y02027
RWORK    EQU   5                        WORKAREA ADDRESS         Y02027
RPECB    EQU   6                        PECB ADDRESS             Y02027
RCOUNT   EQU   7                        COUNTER                  Y02027
RDEB     EQU   8                        DEB BASE REGISTER
RDECB    EQU   9                        DECB BASE REGISTER
RTEMP    EQU   10                       TEMPORARY WORK REGISTER
RDCB     EQU   11                       DCB BASE REGISTER
RBASE    EQU   12                       GET/READ BASE REGISTER
RSAVE    EQU   13                       SAVEAREA REGISTER
RETURN   EQU   14                       RETURN REGISTER
RENTRY   EQU   15                       ENTRY POINT/RETURN CODE REG
         USING *,RENTRY                 ADDRESSABILITY.          Y01004
*        MODULE WILL BE ENTERED AT OFFSET 0 FOR A BSAM DCB AND @ZA00205
*        AT OFFSET 4 FOR A QSAM DCB. THE DCB IS INTEROGATED AND@ZA00205
*        ADJUSTED (DCB+31) BY IGG01946 TO POINT TO THE CORRECT @ZA00205
*        ENTRY.                                                @ZA00205
         B     BSAM                     ENTRY IF BSAM          @ZA00205
         DROP  RENTRY                                          @ZA00205
         USING *,RENTRY                 ADDRESSSABILITY TO +4  @ZA00205
IGG019RG IEDHJN QSAM                    ENTRY IF QSAM          @ZA00205
         STM   RETURN,RBASE,REG14(RSAVE)  SAVE REGS            @ZA00205
         L     RBASE,ENTRYPT            SET ADDRESSABILITY     @ZA00205
         DROP  RENTRY                                          @ZA00205
         USING IGG019RG,RBASE           CSECT ADDRESSABILITY.    Y01004
 EJECT
         USING IHADECB,RDECB            DECB ADDRESSABILITY
         USING IEDQDEB,RDEB             DEB ADDRESSABILITY       Y02027
         USING IEDQPECB,RPECB           PECB ADDRESSABILITY      Y02027
         USING IHADCB,RDCB              DCB ADDRESSABILITY       Y02027
         SPACE
         LR    RDCB,RPARM               ADDR OF DCB            @ZA00205
         B     EODADTST                 CONT QSAM PROCESS      @ZA00205
         DROP  RBASE                                           @ZA00205
         USING IGG019RG,RENTRY                                 @ZA00205
BSAM     EQU   *                                               @ZA00205
         STM   RETURN,RBASE,REG14(RSAVE)                       @ZA00205
         L     RBASE,ENTRYPT            GET CORRECT ADDRESSABILIZA00205
         DROP  RENTRY                                          @ZA00205
         USING IGG019RG,RBASE           ADDRESSABILITY         @ZA00205
         LR    RDECB,RPARM              ADDRESS OF DECB
         L     RDCB,DECDCBAD            ADDRESS OF DCB FROM DECB
         L     RWORKA,DECAREA           ADDR OF WORKAREA FROM DECB
         XC    DECSDECB(4),DECSDECB
         TM    DCBRECFM,UNDFLG          UNDEFINED FORMAT
         BNO   EODADTST                 BRANCH IF NO
         TM    DECTYPE,S                IS 'S' CODED FOR LENGTH
         BO    EODADTST                 BRANCH IF YES
         MVC   DCBBLKSI(2),DECLNGTH     GET UNIT LENGTH FROM DECB
EODADTST EQU   *
         L     RPECB,DCBPECB            GET ADDRESS OF PECB      Y02027
         L     RDEB,DCBDEBAD            GET DEB ADDRESS          Y02027
         ST    RSAVE,PECBSAVE           SAVE ADDR OF USERS SAVE  Y02027
*                                       AREA                     Y02027
         LH    RSIZE,DCBBLKSI           GET WORKAREA SIZE        Y02027
         TM    DCBMACRF,LOCMFLG         LOCATE MODE              Y02027
         SPACE 2
         BZ    NOLOC                    BRANCH IF NO             Y02027
         L     RWORKA,DEBLCMWA-ONE      ADDR OF WORKAREA GOTTEN  Y02027
*                                       BY OPEN FROM DEB         Y02027
         LH    RSIZE,DEBSOWA            LENGTH OF LOCATE MODE    Y02027
*                                                                Y02027
*        VALIDITY CHECK WORKAREA BY DOING FETCHES AND STORES     Y02027
*        INTO THE FIRST AND LAST BYTES.                          Y02027
*                                                                Y02027
NOLOC    EQU   *                                                 Y02027
         LR    RCOUNT,RWORKA            ADDRESS OF WORKAREA      Y02027
         IC    RTEMP,ZERO(RCOUNT)       FETCH AND STORE          Y02027
         STC   RTEMP,ZERO(RCOUNT)       INTO THE FIRST BYTE      Y02027
         LR    R3,RSIZE                 SIZE OF WORK AREA        Y02027
         BCTR  R3,ZERO                  ADJUST TO LAST BYTE      Y02027
         IC    RTEMP,ZERO(R3,RCOUNT)    FETCH FROM LAST BYTE     Y02027
         STC   RTEMP,ZERO(R3,RCOUNT)    STORE INTO LAST BYTE     Y02027
*                                                                Y02027
*        SET UP PARAMETER LIST                                   Y02027
*                                                                Y02027
         LR    R3,RWORKA                SAVE WORKAREA ADDRESS    Y02027
         LA    RTEMP,PECBECB            GET ECB ADDRESS          Y02027
         ST    RTEMP,PECBPARM           STORE IN PARAMETER LIST  Y02027
         MVI   PECBPARM,GETCODE         SET GET CODE IN PARM LISTY02027
         LA    RPARM,PECBPARM           SET UP PARAMETER REG     Y02027
         AQCTL                          ISSUE SVC 102 TO GET MSG Y02027
         EJECT
*                                                                Y02027
*              CONTROL RETURNED FROM GETREAD FUNCTION IN IEDQEB  Y02027
*                                                                Y02027
*              CHECK FOR ERRORS                                  Y02027
*                                                                Y02027
         LA    RCOUNT,EIGHTY            LOAD X'80'               Y02027
         CR    RENTRY,RCOUNT            CHECK FOR RETURN OF X'80'Y02027
*                                       OR X'90' -INVALID SVC 102Y02027
         BNL   EXIT                     BRANCH IF YES            Y02027
         LA    RCOUNT,STOPQRC           DISABLED ENTRY CODE      Y02027
         CR    RENTRY,RCOUNT            PROCESS ENTRY DISABLED   Y02027
         BNE   CKSOWA                   NO BR                    Y02027
         TM    DCBMACRF,QSAMFLG         QSAM                     Y02027
         BO    EXIT                     BR YES                   Y02027
*                                       RETURN CODE ALREADY SET  Y02027
         MVI   DECSDECB,STOPQCC         SET COMPLETION CODE      Y02027
         B     GOODEXIT                 EXIT RETURN CODE ZERO    Y02027
CKSOWA   EQU   *                                                 Y02027
         LA    RCOUNT,SOWAERR           LOAD X'10'               Y02027
         CR    RENTRY,RCOUNT            CHECK FOR INVALID SIZE   Y02027
         BNE   CKEODAD                  BRANCH IF OK             Y02027
         TM    DCBMACRF,QSAMFLG         CHECK FOR QSAM           Y02027
         BO    EXIT                     IF YES, RETURN CODE IS   Y02027
*                                       ALREADY SET UP           Y02027
         MVI   DECSDECB,SOWACC          SET ERROR IN DECB        Y02027
         B     GOODEXIT                 EXIT RETURN CODE ZERO    Y02027
         EJECT
*                                                                Y02027
*              CHECK FOR EXITS                                   Y02027
*                                                                Y02027
*              EODAD                                             Y02027
*                                                                Y02027
CKEODAD  EQU   *                                                 Y02027
         LA    RCOUNT,FOUR              RETURN CODE OF 4         Y02027
         CR    RENTRY,RCOUNT            EODAD SET BY EB          Y02027
         BNE   CKRET                    BRANCH IF NO           @ZA05013
         TM    DCBMACRF,QSAMFLG         QSAM                     Y02027
         BO    EODEXIT                  BRANCH IF YES            Y02027
         MVI   DECSDECB,EODFLG          SET FLAG IN DECB         Y02027
         B     GOODEXIT                 ZERO RETURN CODE & EXIT  Y02027
EODEXIT  EQU   *                                                 Y02027
         TM    DCBEODAD+THREE,NOEXIT    EXIT SPECIFIED           Y02027
         BO    EXIT                     BRANCH IF NO - RETURN    Y02027
*                                       CODE ALREADY SET         Y02027
         L     RENTRY,DCBEODAD          GET ADDRESS OF ROUTINE   Y02027
         ST    RENTRY,REG15(RSAVE)      STORE IN REG 15 IN USERS Y02027
*                                       SAVE AREA                Y02027
         LM    RETURN,RBASE,REG14(RSAVE) RESTORE REGISTERS       Y02027
         BR    RENTRY                   BRANCH TO ROUTINE        Y02027
CKRET    EQU   *                                               @ZA05013
*                                       *                      @ZA05013
*                RETRIEVE               *                      @ZA05013
*                                       *                      @ZA05013
         SPACE
         LA    RCOUNT,RETERR            RETURN CODE OF X'0C'   @ZA05013
         CR    RCOUNT,RENTRY            RETRIEVE ERROR         @ZA05013
         BNE   CKEXLIST                 BRANCH IF NO           @ZA05013
         TM    DCBMACRF,QSAMFLG         QSAM                   @ZA05013
         BO    EXIT                     BRANCH IF YES          @ZA05013
         MVI   DECSDECB,RETCC           SET COMPLETION CODE    @ZA05013
         B     GOODEXIT                 EXIT                   @ZA05013
*                                                                Y02027
*              CHECKPOINT                                        Y02027
*                                                                Y02027
CKEXLIST EQU   *                                                 Y02027
         CLI   PECBFLAG,CKPTFLG         CHECK FOR EXIT NEEDED    Y02027
         BNE   CKSYNAD                  BRANCH IF NO             Y02027
         ST    RENTRY,PECBERR           SAVE RETURN CODE         Y02027
         L     RENTRY,DCBEXLST          EXIT LIST ADDRESS        Y02027
CKLOOP   EQU   *                                                 Y02027
         TM    ZERO(RENTRY),CKPTFLG     THIS ENTRY FOR CHECKPOINTY02027
         BO    CKEXIT                   BRANCH IF YES            Y02027
         LA    RENTRY,FOUR(RENTRY)      BUMP TO NEXT ENTRY       Y02027
         B     CKLOOP                   TEST NEXT ENTRY          Y02027
CKEXIT   EQU   *                                                 Y02027
         STM   RWORKA,RBASE,PECBSA+TWENTY  SAVE REGS  0-12
         L     RENTRY,ZERO(RENTRY)      GET ROUTINE ADDRESS      Y02027
         LR    RPARM,RDCB               DCB ADDRESS              Y02027
         LM    RSIZE,RBASE,REG2(RSAVE)  RESTORE USERS REGISTERS  Y02027
         L     RWORKA,REG0(RSAVE)       RESTORE REG0             Y02027
         BALR  RETURN,RENTRY            BRANCH TO CHECK POINT    Y02027
*                                       ROUTINE                  Y02027
         L     RPECB,DCBPECB-IHADCB(RPARM)  GET PECB ADDR        Y02027
         LM    RWORKA,RWORK,PECBSA+TWENTY         REG  0-5       Y02027
         LM    RCOUNT,RBASE,PECBSA+FORTY8          REGS  7-12    Y02027
         L     RENTRY,PECBERR           RESTORE RETURN CODE      Y02027
*                                                                Y02027
*              SYNAD                                             Y02027
*                                                                Y02027
CKSYNAD  EQU   *                                                 Y02027
         LA    RCOUNT,SYNADERR          GET X'08'                Y02027
         CR    RENTRY,RCOUNT            SYNAD TO BE TAKEN        Y02027
         BNE   SETREG                   BRANCH IF NO             Y02027
         TM    DCBMACRF,QSAMFLG         QSAM DCB                 Y02027
         BZ    BSAMSYN                   BR IF NO - BSAM         Y02027
         TM    DCBSYNAD+THREE,NOEXIT    SYNAD EXIT SPECIFIED     Y02027
         BO    EXIT                     BRANCH IF NO             Y02027
         XC    PECBSTAT(14),PECBSTAT    CLEAR FIELD              Y02027
         MVI   PECBSTAT+THIRTEEN,GRERR  SET ERROR FLAG           Y02027
         LA    RCOUNT,PECBSTAT          GET ADDR OF STATUS IND.  Y02027
         MVC   REG15(4,RSAVE),DCBSYNAD  PUT ADDRESS OF SYNAD     Y02027
*                                       IN REG 15 IN USERS SAVE  Y02027
         ST    RCOUNT,REG0(RSAVE)       STORE PTR TO ADDR OF     Y02027
*                                       STATUS INDICATORS        Y02027
         ST    RDCB,REG1(RSAVE)         STORE DCB ADDRESS IN REG1Y02027
         MVI   REG1(RSAVE),GETERR       BIT FOR INPUT ERROR      Y02027
         LM    RETURN,RBASE,REG14(RSAVE) RESTORE REGISTERS       Y02027
         BR    RENTRY                   BRANCH TO USERS ROUTINE  Y02027
BSAMSYN  EQU   *                                                 Y02027
         MVI   DECSDECB,SOWACC           SET OVERFLOW IN DECB    Y02027
SETREG   EQU   *                                                 Y02027
         TM    DCBMACRF,QSAMFLG         QSAM DCB                 Y02027
         BNO   GOODEXIT                 BRANCH IF BSAM           Y02027
         ST    R3,REG1(RSAVE)           STORE WORKAREA ADDR IN   Y02027
*                                       USERS SAVE AREA          Y02027
         EJECT
GOODEXIT EQU   *                                                 Y02027
         SR    RENTRY,RENTRY            ZERO RETURN CODE REGISTERY02027
EXIT     EQU   *                                                 Y02027
         RETURN (14,12),T,RC=(15)
*                                       FIELDS TO BLANKS.        S22025
MOVELEN  DC    X'0003'                  MOVE LENGTH              S22027
         DS    0F                                              @ZA00205
ENTRYPT  DC    A(IGG019RG)                                     @ZA00205
 EJECT
CKPTFLG  EQU   X'0F'                    CHECKPOINT EXIT LIST OFFSET
TWENTY   EQU   20                       OFFSET IN SAVE AREA      Y02027
FORTY8   EQU   48                       OFFSET IN SAVE AREA      Y02027
EODFLG   EQU   X'70'                    SETEOF(EODAD) FOR READ
NOEXIT   EQU   X'01'
QSAMFLG  EQU   X'40'                    QSAM DCB IDENTIFIER MASK
LOCMFLG  EQU   X'08'                    LOCATE MODE FLAG
UNDFLG   EQU   X'C0'                    UNDERFINED FORMAT
GRERR    EQU   X'40'
SYNADERR EQU   8                        SYNAD EXIT NEEDED        Y02027
EIGHTY   EQU   X'80'                    MCP NOT ACTIVE RC        Y02027
GETERR   EQU   X'80'                    INPUT ERROR TO SYNAD     Y02027
GETCODE  EQU   X'90'                    GET CODE FOR SVC 102     Y02027
ZERO     EQU   0                        ZERO OFFSET              Y02027
ONE      EQU   1                        DISPLACEMENT             Y02027
THREE    EQU   3                        LOW ORDER BYTE OF ADDRESSY02027
FOUR     EQU   4                        DISPLACEMENT             Y02027
THIRTEEN EQU   13                       DISPLACEMENT             Y02027
REG0     EQU   20                       REG 0 IN SAVE AREA       Y02027
REG1     EQU   24                       REG 1 IN SAVE AREA       Y02027
REG2     EQU   28                       REG 2 IN SAVE AREA       Y02027
REG14    EQU   12                       REG 14 IN SAVE AREA      Y02027
REG15    EQU   16                       REG 15 IN SAVE AREA      Y02027
S        EQU   X'80'
SOWAERR  EQU   16                       ERROR CODE              SA52981
ADDR     EQU   7                        ICM/STCM MASK FOR        Y01004
SOWACC   EQU   X'52'                    ERROR CODE              SA52981
STOPQRC  EQU   20                       DISABLED ENTRY R.C.      Y02027
STOPQCC  EQU   X'51'                    DISABLED ENTRY C.C.      Y02027
RETERR   EQU   X'0C'                    RETRIEVE ERROR R.C.    @ZA05013
RETCC    EQU   X'50'                    RETRIEVE ERROR R.C.    @ZA05013
         EJECT
IHADECB  DSECT                          DATA EXTENT CONTROL BLOCK MAP
DECSDECB DS    F                        EVENT CONTROL BLOCK
DECTYPE  DS    H                        OPTION BYTES
DECLNGTH DS    H                        LENGTH OF WORKAREA
DECDCBAD DS    F                        ADDRESS OF DATA CONTROL
DECAREA  DS    F                        ADDR OF READ/WRITE WORKAREA
DECIOBPT DS    F                        ADDR OF IOB
         EJECT
         TDEBAPD
         EJECT
         DCBD  DSORG=PS                 DATA CONTROL BLOCK DSECT MACRO
         EJECT
DCBPECB  EQU   DCBIOBAD                 ADDRESS OF PECB FOR TCAM Y02027
         TPECBD
         END
