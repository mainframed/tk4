         TITLE 'IEDQA9  -  REDIAL ROUTINE'
IEDQA9   CSECT
*A652000                                                        SA68666
*A508000                                                       @OX12501
***********************************************************************
*                                                                     *
* STATUS -- CHANGE LEVEL 10                                           *
*                                                                     *
* FUNCTION -- IF THE CPU FAILS TO INITIATE CONTACT WITH A             *
*    SWITCHED TERMINAL AND THE RETRY MACRO HAS BEEN CODED IN          *
*    THE INMSG SUBGROUP, THE REDIAL ROUTINE WILL CAUSE THE CPU        *
*    TO TRY AGAIN TO INITIATE CONTACT AT AN INTERVAL OF TIME          *
*    SPECIFIED IN THE RETRY MACRO. THIS ROUTINE IS ACTIVATED BY       *
*    BUFFER DISPOSITION, (IEDQBD), AND GOES TO THE TIME DELAY         *
*    ROUTINE TO PLACE A QCB ON THE TIME DELAY QUEUE.                  *
*                                                                     *
* ENTRY POINTS --                                                     *
*                                                                     *
*         FIRST EXECUTABLE INSTRUCTION.                               *
*                                                                     *
* INPUT --                                                            *
*                                                                     *
*    R1 HAS THE ADDRESS OF A LIST OF THINGS TO BE POSTED              *
*    R4 HAS THE LCB ADDRESS                                           *
*    R11 HAS THE DISPATCHER ADDRESS                                   *
*    R12 HAS THE ENTRY POINT ADDRESS                                  *
*    R13 HAS THE ADDRESS OF AVTSAVE2                                  *
*                                                                     *
* OUTPUT --                                                           *
*                                                                     *
*    THE QCB WILL BE GIVEN TO THE TIME DELAY ROUTINE.                 *
*                                                                     *
* EXTERNAL REFERENCES --                                              *
*                                                                     *
*         .IEDQHG01 -- TIME DELAY SUBTASK - TO INSERT A QCB ON THE    *
*         TIME DELAY QUEUE.                                           *
*                                                                     *
*         .IEDQTNT -- TO FIND THE TERMINAL ENTRY FROM THE DESTINATION *
*         OFFSET.                                                     *
*                                                                     *
*         .IGG019RB OR IGG019RO -- TCAM DISPATCHER                    *
*                                                                     *
* EXITS,NORMAL --                                                     *
*                                                                     *
*         RETURN IS MADE ON R11 WITH AN OFFSET TO HAVE THE DISPATCHER *
*         PERFORM THE DESIRED FUNCTION.                               *
*                                                                     *
* EXITS,ERROR --                                                      *
*                                                                     *
*         NONE                                                        *
*                                                                     *
* TABLES/WORK AREAS --                                                *
*    AVT                                                              *
*    QCB                                                              *
*    LCB                                                              *
*    SCB                                                              *
*    TERMINAL TABLE                                                   *
*                                                                     *
* ATTRIBUTES --                                                       *
*    REUSABLE,ENABLED,RESIDENT                                        *
*                                                                     *
* CHARACTER CODE DEPENDENCY --                                        *
*    THE OPERATION OF THIS MODULE DOES NOT DEPEND UPON A              *
*    PARTICULAR INTERNAL REPRESENTATION OF THE EXTERNAL               *
*    CHARACTER SET.                                                   *
*                                                                     *
* NOTES --                                                            *
*    STANDARD TCAM LINKAGES AND DEVIATIONS FROM STANDARDS.            *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*                      REGISTER EQUATES                               *
***********************************************************************
*                                                                     *
R0       EQU   0                        WORK REGISTER
R1       EQU   1                        LIST OF ELEMENTS TO BE POSTED
R2       EQU   2                        WORK REGISTER
R3       EQU   3                        SCB ADDRESS
R4       EQU   4                        LCB ADDRESSABILITY REGISTER
RLCB     EQU   4                        LCB ADDRESS
R5       EQU   5                        WORK REGISTER
RPREFIX  EQU   6                        WORK REGISTER
RQCB     EQU   7                        QCB ADDRESS
R8       EQU   8                        ADDRESS OF PARAMETER LIST
R9       EQU   9                        WORK REGISTER
R10      EQU   10                       WORK REGISTER
RDISP    EQU   11                       TCAM DISPATCHER ADDRESS
RBASE    EQU   12                       BASE REGISTER
RAVT     EQU   13                       SAVE AREA ADDRESS
R14      EQU   14                       RETURN ADDRESS
R15      EQU   15                       ENTRY POINT ADDRESS
*                                                                     *
***********************************************************************
*                      USING STATEMENTS                               *
*                                                                     *
***********************************************************************
         USING IEDQQCB,RQCB             QCB
         USING IEDQDISP,RDISP           DISPATCHER
         USING IEDQTRM,R1               TERMINAL ENTRY
         USING IEDQSCB,R3               SCB
         USING IEDQLCB,R4               LCB
         USING AVTSAVE2,RAVT            AVT
         USING *,RBASE                  CSECT BASE
***********************************************************************
         DS    0F
REDIAL   EQU   *
         B     REDIAL1                  BRANCH AROUND MODULE ID
         DC    C'IEDQA9'                MODULE NAME
         DC    X'2054'                  02/23/72
REDIAL1  EQU   *
         ST    R1,LCBERBLK-1            SAVE POST LIST
         L     R5,AVTMSGS-1             ADDR OF LIST OF VCONS
         L     R5,0(R5)                 ADDR OF BD
         ST    R5,LCBERBQB-1            TPOST ERB TO BD QCB
         MVI   LCBERBPY,PRIRCQCB        SET ERB PRIORITY
         XC    LCBERBCH(3),LCBERBCH     ADDR CHAIN TO BE ASSIGND
*                                         BUFFERS
         LH    R1,LCBTTCIN              DESTINATION OFFSET
         N     R1,AVTCLRHI              CLEAR HIGH ORDER HALF WORD
         BZ    TPOSTERB                 IF ZERO, TPOST ERB
         L     R15,AVTRNMPT
         BALR  R14,R15                  GET TERMINAL TABLE ENTRY
         L     RQCB,TRMDESTQ-1          GET QCB ADDRESS
         CLI   QCBSTPRI,CALLOPT         THIS CLOCK OR CINTVL   @Y17XAMG
         BL    TPOSTERB                 BR NO                  @YM08436
         L     R3,LCBSCBA-1             GET SCB ADDRESS
         TM    SCBERR4,SCBCONNN         ERROR IN CONNECTION
         BZ    TPOSTERB                 IF NOT, TPOST ERB TO BD
         SR    R9,R9                    CLEAR REGISTER
         LH    R9,TRMDEVFL              GET DEVICE FLAGS
         SLL   R9,TWEN6                 SHIFT TO RETRY BIT
         LTR   R9,R9                    RETRY VALUE SPECIFIED
         BNL   TPOSTERB                 IF NOT, GO TPOST ERB
         TM    TRMSTATE,TRMOPTFN        OPTION FIELDS SPECIFIED
         BO    OPTFL                    BRANCH IF YES
         LA    R5,TRMOPNO               GET ADDR OF FIRST DEVICE
*                                         DEPENDENT FIELD
         B     SCAN                     GO FIND RETRY COUNT FIELD
OPTFL    EQU   *
         SR    R2,R2                    CLEAR REGISTER TO ZERO
         IC    R2,TRMOPNO               NUMBER OF OPTION FIELDS
         LA    R5,TRMOPT                ADDR OF OPTION FLDS TABLE
         AR    R5,R2                    ADDR OF FIRST DEVICE DEP
*                                         FIELD
SCAN     EQU   *
         SR    R2,R2                    CLEAR REGISTER TO ZERO
         SR    R3,R3                    CLEAR REGISTER TO ZERO
         LH    R2,TRMDEVFL              DEV DEP FIELD FLAGS
         N     R2,AVTCLRHI              CLEAR HIGH ORDER 2 BYTE@OX12501
         SR    R10,R10                  CLEAR CNTR OF D.D. FIELDS
         SRL   R2,FIVE                  ELIMINATE FIVE BITS
SHIFT    EQU   *
         SRDL  R2,ONE                   MOVE IN ONE BIT
         SRL   R3,THIRTY1               MAKE REG ONE OR ZERO
         AR    R10,R3                   BUMP D.D. COUNTER
         LTR   R2,R2                    ALL BITS TESTED
         BNZ   SHIFT                    BRANCH IF NOT
BUMP     EQU   *
         CH    R10,HONE                 RETRY COUNT FOUND
         BE    UPDATE                   BRANCH IF YES
         IC    R2,0(0,R5)               SIZE OF D.D. FIELD
         LA    R5,1(R2,R5)              BUMP PAST THIS D.D.FIELD
         BCT   R10,BUMP                 CONTINUE LOOP TO RETRY COUNT
UPDATE   EQU   *
         IC    R10,1(R5)                GET SPECIFIED RETRY COUNT
         SR    R3,R3                    CLEAR REGISTER TO ZERO
         IC    R3,2(R5)                 GET NO. TIMES ALREADY
*                                         RETRIED
         CR    R3,R10                   COMPARE THE TWO VALUES
         BL    DELAY                    BRANCH IF RETRY COUNT HAS
*                                         NOT BEEN REACHED
         MVI   2(R5),AVTEZERO           ZERO OUT RETRY COUNT
         B     TPOSTERB                 GO TPOST ERB
DELAY    EQU   *
         LA    R3,1(R3)                 UPDATE RETRY COUNT
         STC   R3,2(R5)                 STORE COUNT
         LR    R2,RLCB                  SAVE LCB ADDRESS
         LR    R10,RQCB                 SAVE QCB ADDR
         LR    R3,RQCB                  SET FOR UNAVR
         LR    RQCB,RLCB                FOR UNAVAR
         BAL   R14,DSPUNAVR             REMOVE SEND SCHEDULER
         LR    RQCB,R10                 RESTORE QCB BASE
         LH    R10,2(R8)                GET AMOUNT OF DELAY
*                                         FROM PARAMETER LIST
         N     R10,AVTCLRHI             CLEAR HIGH ORDER HALF   SA68666
         NI    QCBSTAT,AVTEFF-QCBTIME   RESET TWELVE HOUR DELAY SA68666
         C     R10,TWELVHRS             IS DELAY OVER 12 HRS    SA68666
         BNH   LESS12                   YES, QCB OK             SA68666
         S     R10,TWELVHRS             DECREMENT BY 12 HRS     SA68666
         OI    QCBSTAT,QCBTIME          INDICATE MORE THAN      SA68666
*                                        12 HOUR DELAY          SA68666
LESS12   EQU   *                                                SA68666
         STH   R10,QCBEOLDT             SET DELAY REQUEST
         MVI   QCBLKRLN,AVTEZERO        SET THE OFFSET TO THE QCB
         IC    R9,QCBELCHN-1            SAVE HIGH ORDER BYTE
         ST    RQCB,QCBELCHN-1          QCB FOR POST
         STC   R9,QCBELCHN-1            SET HIGH ORDER BYTE
         MVC   QCBSTCHN,QCBSLINK        REMOVE SEND SCHEDULER
         MVI   QCBPRI,PRILNFRE          SET PRIORITY
         LR    R1,RQCB                  TIME QUEUE PARAMETER
         L     R15,AVTHG01              GET ADDR OF TIME DELAY ROUTINE
         BALR  R14,R15                  GO TO TIME DELAY RTN
TPOSTERB EQU   *
         LA    R1,LCBERB                LOAD ADDRESS OF ERB
         BAL   R14,DSPCHAIN             EXIT                   @Y17XAMX
***********************************************************************
HONE     DC    H'1'                     HALF WORD OF ONE
TWELVHRS DC    F'43200'                 12 HOUR DELAY           SA68666
TWEN6    EQU   26                       VALUE OF 26
FIVE     EQU   5                        VALUE OF 5
ONE      EQU   1                        VALUE OF 1
THIRTY1  EQU   31                       VALUE OF 31
CALLOPT  EQU   X'70'                    STCB PIRORITY FOR CINTVL
***********************************************************************
         EJECT
***********************************************************************
*                              DSECTS                                 *
***********************************************************************
         TPRIOR
         SPACE 4
         TAVTD
         SPACE 4
         TLCBD
         SPACE 4
         TTRMD
         SPACE 4
         TDISPD
         SPACE 4
         TQCBD
         SPACE 4
         TSCBD
***********************************************************************
         END
