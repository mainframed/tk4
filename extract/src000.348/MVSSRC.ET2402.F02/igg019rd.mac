         TITLE '''IGG019RD'' - BUFFERED TERMINAL SCHEDULER'
IGG019RD CSECT
*A-000000-999999                                               @X31X8I0
         SPACE 3
*CHANGE ACTIVITY=AS FOLLOWS:
******************** MICROFICHE FLAGS *********************** SUPT CODE
*A231000,337100,337600,339150,339350,339415,354000,369000,375000 S21101
*A380000,409000,422000,445000,470000,584000,635000,639000,642000 S21101
*A660000,697000,821000,845000,850000,851000,852000               S21101
*C151300,197000-227000,280000-309000,333000-334000,402000,409000 S21101
*C505000,532300,566000                                           S21101
*D329000,337200-337300,355000-366000,376000-377000,382000,410000 S21101
*D419000,446000,448000-452600,454000-456000,478000-482000,485000 S21101
*D510000,636000-638000,661000-668000,698000-703000,780000,844000 S21101
*D846000-849000                                                  S21101
*C458000,586000-587000                                           A48267
*A608000                                                         A48267
*A127000,169000,279180,279390,339350,368000,372300,611000,617000 S22025
*A639200,841500                                                  S22025
*C151300,177500,196160,279350,279390,422044,468000-469000,634000 S22025
*C673000,681000,821400,821600-821650,826000,826600               S22025
*D196280,279200-279280,279360,369000-369900,483000-487000        S22025
*D639210-639230                                                  S22025
*C532400                                                        SA52985
*A371400                                                        SA56605
*A279240,639223                                                 SA56606
*D735000,815000                                                 SA61794
*A732025-732525,A805025-805025                                   Y05331
*A584200                                                        SA52515
*A660600                                                        SA68697
*D532100-532500                                                @SA70320
*A657000,669000                                                 OX03968
*C673000                                                        OX03968
*A002000,842700                                                @Z30X8IM
*C008000,169400,265000,391200,422082,422086,498200,573000      @Z30X8IM
*D169600-169800,266000-267000,391400-391600,422084,422088      @Z30X8IM
*D498400-498600,571000,574000,639064-639066                    @Z30X8IM
*C639063                                                       @Z30X8IM
*A165000,676200,842700                                         @XA08074
*D732150,732200-732275,733000                                  @OY12394
*A584400,704000,706000,717000                                  @OX12490
*C322600,338000,339650,413600,470200,505600,726600,777600      @OY14092
*C504800                                                       @OX17153
*D805225,806000                                                @OZ33657
*C279410,279480                                                @OY21468
*A269200,372300,841650                                         @OY21468
*A528000                                                       @OZ34395
* $01=OZ42506    JTC2202  79.11.07  098061:                        @01A
* $02=OZ42847    JTC2202  79.12.19  098061:                        @02A
* $03=OZ43405    JTC2202  79.12.19  098061:                        @03A
* $11=OZ45902    ETC2302  80.07.15  723465:                        @11A
*
         SPACE 4
*
         DC    AL2(TAG-IGG019RD)        DESTINATION SCHEDULER
*                                       LOGIC OFFSET
         SPACE 4
***********************************************************************
*                                                                     *
*TITLE: 'IGG019RD' BUFFERED TERMINAL SCHEDULER                        *
*                                                                     *
* $MOD(IGG019RD) COMP(I@) PROD(TCAM):                              @01C
*                                                                     *
* DESCRIPTIVE NAME=BUFFERED TERMINAL SCHEDULER                        *
*                                                                     *
* COPYRIGHT='NONE'                                                    *
*                                                                     *
*  STATUS=CHANGE LEVEL 8                                       @Z30X8IM
*                                                                     *
*FUNCTION:                                                            *
*   THE PURPOSE OF THIS MODULE IS TO SCHEDULE SEND AND RECIEVE        *
*   OPERATIONS FOR BUFFERED TERMINALS,E.G., 2740 MODEL 2 AND 2770.    *
*   IN GENERAL, IT PERFORMS IN A MANNER ANALOGOUS TO THE SEND AND     *
*   RECIEVE SCHEDULERS FOR NON-BUFFERED TERMINALS.  THE SEND FUNCTION *
*   IS DIFFERENT DUE TO THE TERMINAL'S HAVING A HARDWARE BUFFER.      *
*   WHEN A BLOCK OF TEXT IS SENT TO A BUFFERED TERMINAL, THE          *
*   TRANSMISSION IS COMPLETE.  HOWEVER, TCAM MUST OBSERVE A TIME      *
*   DELAY EQUIVALENT TO THE TIME REQUIRED FOR THE TERMINAL TO EMPTY   *
*   ITS BUFFER ONTO ITS OUTPUT DEVICE.  THIS SCHEDULER MUST TRY TO    *
*   UTILIZE THE LINE FOR SENDING TO OR RECIEVING FROM OTHER TERMINALS *
*   ON THE LINE DURING THE TIME DELAY FOR THE TERMINAL TO WHICH THE   *
*   LAST BLOCK OF TEXT WAS SENT.  FLAGS IN THE DESTINATION QCBSTAT    *
*   INDICATE WHETHER THE TERMINAL IS IN SEND OR RECIEVE MODE.  THESE  *
*   STATES ARE MUTUALLY EXCLUSIVE.                                    *
*                                                                     *
*   THIS ROUTINE WAITS IN THE DESTINATION QCB AND THE LCB.  THERE IS  *
*   A PHYSICAL STCB FOR EACH DESTINATION QCB AND LCB.  WHEN A BUFFER  *
*   IS POSTED TO THE DESTINATION QCB, THE BTS PASSES IT TO THE DESTI- *
*   NATION SCHEDULER(IEDQHM).  WHEN IEDQHM RECOGNIZES END-OF-MSG,     *
*   IT BRANCHES TO AN ENTRY POINT IN BTS(TAG).  IF THE LINE IS FREE,  *
*   THE LCB IS POSTED TO ITSELF UNING DSPPOSTR.  IF NOT AND AFTER     *
*   RETURN FROM THE DISPATCHER, THE QCB'S STCB FOR BTS IS LINKED INTO *
*   THE LCB'S STCB CHAIN.  THIS ACTION INDICATES THAT THERE IS A      *
*   MESSAGE FOR THE ASSOCIATED TERMINAL.  IF AUTOPOLL IS IN PROGRESS, *
*   IT IS STOPPED AND EXIT IS MADE TO IEDQHM.  IF NOT, AN EXIT IS MADE*
*   TO IEDQHM.                                                        *
*                                                                     *
*   WHEN THE LCB IS POSTED TO BTS, A TEST IS MADE FOR TYPE OF LAST    *
*   OPERATION.  IF THE LAST OPERATION WAS A RECIEVE, A TEST IS MADE   *
*   FOR SCHEDULING PRIORITY.  IF EQUAL PRIORITY IS SPECIFIED, A TEST  *
*   IS MADE FOR END OF INVITATION LIST.  IF NOT THE END, THE ERB IS   *
*   SET UP TO RECIEVE FROM THE NEXT ENTRY IN THE LIST, AND THE ERB IS *
*   POSTED TO THE BUFFER REQUEST QCB.                                 *
*                                                                     *
*   IF END OF INVITATION LIST, THE CURRENT INVLIST POINTER IS RESET   *
*   TO THE BEGINNING AND A TEST IS MADE FOR SOMETHING TO SEND TO A    *
*   TERMINAL ON THE LINE.  IF THE PRIORITY TEST INDICATED SEND PRTY,  *
*   A SEND TEST IS ALSO MADE.  IF NOTHING TO SEND, BTS INITIATES A    *
*   RECIEVE OPERATION ON THE FIRST ENTRY IN THE INVITATION LIST.      *
*                                                                     *
*   IF THERE IS SOMETHING TO SEND, THE NEXT BTS STCB IN THE LCB'S     *
*   STCB CHAIN IS DISPATCHED.  IF THE QCB REFLECTS AN EMPTY STATUS,   *
*   THE STCB IS REMOVED FROM THE LCB'S STCB CHAIN, QCBSEND IS TURNED  *
*   OFF, AND BTS EXITS TO THE DISPATCHER BY POSTING THE LCB TO ITSELF.*
*                                                                     *
*   IF THE QCB IS NOT EMPTY, A TEST IS MADE FOR SEND STATUS(QCBRECEV= *
*   0).  IF NON-ZERO, THE LOOP FOR TESTING FOR SOMETHING TO SEND IS   *
*   REENTERED.  IF NOT RECIEVE, QCBSEND IS TURNED ON.  THE ERB TO     *
*   INITIATE THE SENDING OPERATION IS MADE, AND BTS EXITS TO THE      *
*   DISPATCHER BY POSTING THE ERB TO DISK I/O QCB.                    *
*                                                                     *
*   IF THE LAST OPERATION ON THE LINE WAS A SEND, BTS MUST OBSREVE A  *
*   TIME DELAY FOR THE DESTINATION OF THE LAST BLOCK.  THE TIME DELAY *
*   IS STORED IN THE QCB, AND ITS STCB IS DELINKED FROM THE LCB'S STCB*
*   CHAIN.  THE QCB IS PASSED VIA BALR TO THE TIME DELAY ROUTINE      *
*   BRANCH ENTRY POINT(IEDQHG01).  THE 'SOMETHING-ELSE-TO-SEND' LOOP  *
*   IS ENTERED NOW.                                                   *
*                                                                     *
*   WHEN THE TIME DELAY EXPIRES, IEDQHG POSTS THE QCB TO A QCB IN THE *
*   BTS CSECT(BTSTDQCB).  BTS RETURNS THE QCB'S BTS STCB TO THE LCB'S *
*   STCB CHAIN.  IF THE LINE IS FREE, IT POSTS THE LCB TO ITSELF AND  *
*   EXITS TO THE DISPATCHER.  IF NOT, IT MERELY EXITS TO DISPATCHER.  *
*ENTRY POINT:                                                         *
*        IGG019RD                                                     *
*INPUT:                                                               *
*   1  - LINE CONTROL BLOCK OR BUFFER ADDRESS                         *
*   3  - SUBTASK CONTROL BLOCK ADDRESS                                *
*   7  - QUEUE CONTROL BLOCK ADDRESS                                  *
*   11 - DISPATCHER'S ADDRESS                                         *
*   13 - AVTSAVE2 ADDRESS                                             *
*   15 - ENTRY POINT ADDRESS                                          *
*OUTPUT:                                                              *
*        NONE                                                         *
*EXTERNAL ROUTINES:                                                   *
*        DSPPOSTR                                                     *
*        IEDQHG01 - TIME DELAY SUBTASK                                *
*EXITS-NORMAL:                                                        *
*        DSPBYPAS                                                     *
*        DSPPOST                                                      *
*        DSPDISP                                                      *
*EXIT-ERROR:                                                          *
*           NONE                                                      *
*TABLES/WORKAREAS:                                                    *
*        AVT                                                          *
*        LCB                                                          *
*        QCB                                                          *
*        SCB                                                          *
*        DCB                                                          *
*        DEB                                                          *
*        ILIST                                                        *
*        TERMINAL ENTRY                                               *
*ATTRIBUTES: RE-ENTRANT, REFRESHABLE, PROBLEM PROGRAM MODE            *
*NOTES:  THE OPERATION OF THIS MODULE DEPENDS UPON AN INTERNAL        *
*   REPRESENTATION OF THE EXTERNAL CHARACTER SET WHICH IS EQUIVALENT  *
*   TO THE ONE USED AT ASSEMBLY TIME.  THE CODING HAS BEEN ARRANGED   *
*   SO THAT REDEFINITION OF 'CHARACTER' CONSTANTS, BY REASSEMBLY,     *
*   WILL RESULT IN A CORRECT MODULE FOR THE NEW DEFINITIONS.          *
***********************************************************************
         EJECT
*                                                                     *
*           S Y M B O L I C     R E G I S T E R S                     *
*                                                                     *
RZERO    EQU   0                        WORK REGISTER
RPARM    EQU   1                        PARAMETER REGISTER
RTEMP    EQU   2                        WORK REGISTER
RSTCB    EQU   3                        SUBTASK CONTROL BLOCK
RILIST   EQU   4                        INVITATION LIST
RPQCB    EQU   5                        PRIORITY LEVEL QCB
RSCB     EQU   9                        STATION CONTROL BLOCK
RQCB     EQU   7                        QUEUE CONTROL BLOCK
RDCB     EQU   8                        DATA CONTROL BLOCK
RTERM    EQU   6                        TERMINAL ENTRY
RLCB     EQU   10                       LINE CONTROL BLOCK
RDISP    EQU   11                       DISPATCHER BASE REGISTER
RBASE    EQU   12                       CSECT BASE REGISTER
RAVT     EQU   13                       AVT BASE REGISTER
RETURN   EQU   14                       RETURN ADDRESS
RENTRY   EQU   15                       ENTRY POINT ADDRESS
         SPACE 3
CLEAR0F  EQU   X'0F'                    TO CLEAR HBFNO           S22025
         SPACE 2
*                                                                     *
*        ESTABLISH CSECT AND CONTROL BLOCK ADDRESSABILITY             *
*                                                                     *
         BALR  RBASE,RZERO              ESTABLISH CSECT
         USING *,RBASE                            ADDRESSABILITY
         USING IEDQDISP,RDISP           TCAM DISPATCHER
         USING IEDQAVTD,RAVT            ADDRESS VECTOR TABLE
         USING IEDQQCB,RQCB             QUEUE CONTROL BLOCK
         USING IEDQSCB,RSCB             STATION CONTROL BLOCK
         USING IEDQLCB,RLCB             LINE CONTROL BLOCK
         USING IEDQTRM,RTERM            TERMINAL TABLE ENTRY
         USING IHADCB,RDCB              DATA CONTROL BLOCK
         USING IEDQSTCB,RSTCB           SUBTASK CONTROL BLOCK
         EJECT
         CLI   RECBPRI-IEDQRECB(RPARM),PRILNFRE IS ELEMENT AN LCB
         BE    LASTOPN                  BRANCH IF YES
*                                                                     *
*   PASSED ELEMENT IS A BUFFER POSTED TO A DESTINATION QCB.  IT       *
*   MUST NOW BE PASSED TO THE DESTINATION SCHEDULER FOR PROCESSING.   *
*                                                                     *
         L     RSTCB,QCBSLINK-1         ADDR OF DEST SCHED STCB
         B     DSPBYPAS                 ACTIVATE DEST SCHED
IGG019RD IEDHJN SELF,HJN
         SPACE 2
LASTOPN  DS    0H
         LA    RPARM,0(0,RPARM)         CLEAR HIGH-ORDER BYTE
         LA    RQCB,0(0,RQCB)           CLEAR HIGH-ORDER BYTE
         CR    RPARM,RQCB               IS POSTED ELEMENT AN LCB
         BE    LCBGO                    BRANCH IF YES
*                                                                     *
*        INPUT ELEMENT IS AN ERB WITH RECALLED BUFFER                 *
*                                                                     *
         LA    RTEMP,LCBERB-IEDQLCB     ERB OFFSET INTO LCB
         SR    RPARM,RTEMP              COMPUTE LCB ADDRESS
         LR    RLCB,RPARM               LCB ADDRESSABILITY
         L     RSCB,LCBSCBA-1           SCB ADDRESSABILITY
         L     RDCB,LCBDCBPT            LINE GROUP DCB ADDRESS
         TM    LCBERBST,LCBRDERR        LOGICAL READ ERROR     @XA08074
         BO    ABEND045                 BRANCH IF YES          @XA08074
         L     RTEMP,LCBERBCH-1         RECALLED BUFFER ADDRESS
         OI    PRFSTAT1-IEDQPRF(RTEMP),PRFCNCLN SET CANCEL MSG FLAG
         MVC   LCBISZE(1),PRFSCAN+1-IEDQPRF(RTEMP) SET IDLE COUNT
         MVC   LCBTTBIN,SCBDCHDR        SET UP TNT OFFSET
         NI    SCBHBFNO,CLEAR0F         RESET HBFNO              S22025
         LA    RPARM,AVTINOUT           INMSG/OUTMSG DELIMITER
         STCM  RPARM,ADR,SCBMACR        RESET MACRO ADDRESS    @Z30X8IM
         MVC   SCBUNTCT(1),SCBDEOB
         MVC   SCBDEOB(4),LCBCPA        RESTORE AFTER RECALL
         TM    LCBERBST,LCBEOMSG        EOM IN RECALLED BUFFER
         BNO   BUILDERB                 BRANCH IF NO
         LA    RTEMP,AVTACTIB           ACTIVATE ROUTINE QCB ADDR
         B     ACTIVATE                 GO SEND EOM BUFFER
         EJECT
LCBGO    EQU   *
         LR    RLCB,RPARM               GET LCB ADDRESS
         NI    LCBSTAT2,AVTEFF-LCBSNDPR TURN OFF SEND PRIORITY   S22025
         L     RSCB,LCBSCBA-1           SCB ADDRESS
*                                                                     *
*        FIND TERMNAME TABLE OFFSET FOR CURRENT INVLIST ENTRY         *
*                                                                     *
         L     RDCB,LCBDCBPT            LINE GROUP DCB ADDRESS
         L     RTEMP,LCBINVPT-1         CURRENT ILIST ENTRY ADDR
         SR    RPARM,RPARM              CLEAR REGISTER TO ZERO
         IC    RPARM,LCBUCBX            RLN-1
         SLL   RPARM,2                  MULTIPLY BY FOUR
         L     RILIST,DCBINVLI(RPARM)   INVITATION LIST ADDRESS
         TM    LCBSTAT1,LCBSENDN        LAST OPERATION A SEND
         BO    DELAY                    BRANCH IF YES
         L     RPARM,DCBDEBAD           DEB ADDRESS
         TM    DEBOPATB-DEBNMSUB(RPARM),OUTONLY OUTPUT ONLY LINE
         BO    FREE                     BRANCH IF YES
         CLI   0(RTEMP),FE              END OF ILIST
         BE    PRITST                   BRANCH IF YES
         CLI   1(RTEMP),FE              END OF LIST
         BE    PRITST                   BRANCH IF YES
         SPACE
         TM    LCBSTAT2,LCBNEGRP        DID THE STATION HAVE     S21101
*                                         ANYTHING TO ENTER      S21101
         BO    PRITST                   NO, BRANCH               S21101
         L     RTEMP,LCBINVPT-1         GET CURRENT ENTRY        S21101
         LA    RTEMP,AVTEZERO(,RTEMP)   CLEAR HI BYTE            S21101
         BAL   RPQCB,GETQCBAD           GET QCB ADD FOR ENTRY    S21101
         SPACE 2
         BAL   RPQCB,QCBRTNE            COMPUTE THE SCB ADDRESS  S21101
         SPACE
         TM    SCBQTYPE,SCBBFMM         IS THIS MIDDLE OF MESSAGES21101
*                                         TO A BUFFERED TERMINAL S21101
         BZ    PRITST                   BRANCH NO                S21101
         L     RTEMP,LCBINVPT-1         CURRENT ENTRY RESET      S21101
         BAL   RPQCB,NEXTENT            BUMP ILIST PINTER,WEDONT S21101
*                                         WANT TO RE-POLL SAME   S21101
         EJECT
PRITST   EQU   *
*                                                                     *
         NI    LCBSTAT2,AVTEFF-LCBNEGRP RESET RESPONSE SWITCH    S21101
*        DECIDE WHETHER TO TRY TO SEND OR RECEIVE NOW                 *
*                                                                     *
         TM    DCBCPRI,EQPRI            EQUAL PRIORITY SPECIFIED
         BO    INVLTST                  BRANCH IF YES
*                                                                     *
*    SINCE SEND PRIORITY IS SPECIFIED, MUST TEST FOR A QCB            *
*    FROM WHICH WE CAN SEND.                                          *
*                                                                     *
QCBCHECK EQU   *
         CLC   LCBRSLNK,AVTDELAD+1      IS THERE A DEST QCB LINKED
*                                       TO THIS LCB, I.E., IS THERE
*                                       A BLOCK OF DATA TO BE SENT
         BNE   SENDTST                  BRANCH IF YES
         EJECT
INVLTST  EQU   *
*                                                                     *
*     CHECK FOR END OF INVITATION LIST.  IF YES, RESET LCBINVPT       *
*     TO BEGINNING TO RESTART THE POLLING PASS AND SEE IF THERE       *
*     IS A BLOCK OF DATA TO BE SENT.                                  *
*                                                                     *
         SPACE 2
         L     RTEMP,LCBINVPT-1         GET CURRENT ILIST ENTRY
         SR    RPARM,RPARM              CLEAR REGISTER TO ZERO
         IC    RPARM,LCBUCBX            RLN-1
         SLL   RPARM,2                  MULTIPLY BY FOUR
         L     RILIST,DCBINVLI(RPARM)   INVITATION LIST ADDRESS
         CLI   EOL(RTEMP),FE            END OF ILIST
         BE    RESET                    BRANCH IF YES
         CLI   1(RTEMP),FE              END OF LIST FOR BISYNC
         BNE   RCV                      BRANCH IF NO
RESET    EQU   *
         LA    RTEMP,ILISTPRF(0,RILIST) BUMP PAST ILIST PREFIX TO
*                                       FIRST ENTRY IN ILIST
         STCM  RTEMP,ADR,LCBINVPT       RESET NEW CURRNT ENTRY @Z30X8IM
         CLI   1(RILIST),0              ANY ACTIVE ENTRIES
         BE    FREE                     BRANCH IF NO
         CLC   LCBRSLNK,AVTDELAD+1      MSG TO SEND
         BNE   SENDTST                  BRANCH IF YES
         SR    RPARM,RPARM              CLEAR REGISTER TO ZERO
         IC    RPARM,DCBINTVL           POLLING DELAY FROM DCB
         LTR   RPARM,RPARM              DELAY SPECIFIED
         BNZ   SETDELAY                 BRANCH IF YES
         B     QCBCHECK                 CHECK FOR BLOCK TO SEND
         EJECT
RCV      EQU   *
*                                                                     *
*      BUILD ERB TO INITIATE A RECEIVE OPERATION ON THE LINE          *
*                                                                     *
         TM    LCBINSRC+2,LCBDLY        LCB IN TIME DELAY
         BO    FREE                     BRANCH IF YES
         SPACE 2
         L     RPARM,DCBDEBAD           GET DEB ADDRESS          S21101
         TM    DEBOPATB-DEBNMSUB(RPARM),OUTONLY       IS THIS AN S21101
*                                         OUTPUT ONLY LINE       S21101
         BO    FREE                     BRANCH IF YES            S21101
         SPACE
         TM    AVTBIT1,AVTDLAYN+AVTCLOSN    IS THIS CLOSEDOWN OR S22025
*                                         SYSTEM DELAY           S22025
         BNZ   SETINDEX                 YES, BRANCH              S22025
         TM    LCBSTAT1,LCBOCNI         REQUEST FOR STOPLINE     S22025
         BZ    SETLOOP                  NO, BRANCH               S22025
SETINDEX EQU   *                                                 S22025
         OI    LCBSTAT2,LCBSNDPR        SET THE PRIORITY BIT     S22025
SETLOOP  EQU   *                                                 S22025
         L     RTEMP,LCBINVPT-1         CURRENT ENTRY            S21101
         LA    RTEMP,AVTEZERO(,RTEMP)   CLEAR THE HIGH ORDER BYTES21101
         ST    RTEMP,AVTDOUBL           SAVE FOR LOOP CONTROL    S21101
         B     STLOOP                   ENTER LOOP               S21101
         SPACE
LOOPEXIT EQU   *                                                 S21101
         C     RTEMP,AVTDOUBL           WRAPPED ILIST ENTRIES    S21101
         BE    FREE                     BRANCH IF YES            S21101
         SPACE
STLOOP   EQU   *                                                 S21101
         BAL   RPQCB,GETQCBAD           QCB ADDRESS FOR ENTRY    S21101
         TM    QCBSTAT,QCBSEND          SENDING TO THIS          S21101
         BO    GETNEXT                  YES, GO TO NEXT ENTRY    S21101
         SPACE
         BAL   RPQCB,QCBRTNE            COMPUTE THE SCB ADDRESS  S22025
         TM    LCBSTAT1,LCBOCNI         REQUEST FOR STOPLINE?   SA56606
         BO    GETNEXT                  BRANCH YES              SA56606
         SPACE 2
         TM    SCBQTYPE,SCBBFMM         IS THIS MIDDLE OF MSG TO S22025
*                                         THIS STATION           S22025
         BO    GORCV                    YES, BRANCH              S22025
         TM    LCBSTAT2,LCBSNDPR        IS THIS AN UNUSUAL       S22025
*                                         CONDITION              S22025
         BZ    GORCV                    NO, BRANCH               S22025
         EJECT
GETNEXT  EQU   *                                                 S21101
         SR    RZERO,RZERO              CLEAR WORK REGISTER      S21101
         LR    RPARM,RTEMP              COPY ILIST POINTER       S21101
         IC    RZERO,ILSTSIZE(,RILIST)  SIZE OF EACH ENTRY       S21101
         AR    RPARM,RZERO              NEXT ENTRY               S21101
         CLI   AVTEZERO(RPARM),FE       END OF LIST              S21101
         BE    SETIT                    YES, BRANCH              S22025
         CLI   1(RPARM),FE              END OF LIST              S21101
         BNE   CONT                     NO, CONTINUE SCAN        S21101
         EJECT
SETIT    EQU   *                                                 S21101
         CLC   LCBRSLNK,AVTDELAD+1      ANYTHING TO SEND?          @02C
         BE    CONT                     BRANCH IF NO               @02C
         BAL   RPQCB,NEXTENT            UPDATE TO NEXT ENTRY IN
*                                       THE INVITATION LIST    @OY21468
         B     SENDTST                  ATTEMPT TO SEND            @02C
         SPACE 1                                                 S21101
CONT     EQU   *                                                 S21101
         BAL   RPQCB,NEXTENT            GET THE NEXT ILIST ENTRY S21101
         B     LOOPEXIT                 SCAN THE NEXT ENTRY      S21101
         SPACE 1                                                 S21101
GORCV    EQU   *                                                 S21101
         MVC   LCBTTBIN,AVTDOUBL+4      SET TNT OFFSET           S21101
         BAL   RPQCB,QCBRTNE            COMPUTE THE SCB ADDRESS  S21101
         SPACE
         ST    RSCB,LCBSCBA-1           STORE IT IN THE LCB      S21101
         TM    5(RILIST),AUTO           AUTOPOLL REQUESTED
         BNO   NOAUTO                   BRANCH IF NO
         NI    3(RILIST),AVTEFF-STRTAUTO
         NC    4(1,RILIST),4(RILIST)    SENDING TO A STATION
         BNZ   NOAUTO                   BRANCH IF YES
         OI    3(RILIST),STRTAUTO       SET AUTOPOLL FLAG
         EJECT
NOAUTO   EQU   *
*                                                                     *
*           COMPLETE ERB INITIALIZATION                               *
*                                                                     *
         LA    RTEMP,AVTBFREB           BUFFER REQUEST QUEUE ADDR
         ST    RTEMP,LCBERB             STORE IN QCB FIELD OF ERB
         NI    LCBSTAT1,LCBOCNI+LCBOCWTN PRESERVE STOPLINE BITS@OY14092
         OI    LCBSTAT1,LCBRECVN        SET RECEIVE STATUS IN LCB
         MVI   LCBERBPY,PRIINTRQ        INITIAL REQUEST PRIORITY
         SR    RTEMP,RTEMP              CLEAR REGISTER TO ZERO
         IC    RTEMP,DCBBUFIN           INITIAL BUFFER COUNT
         SRL   RTEMP,4                  ISOLATE FOUR HI ORDER BITS
         STC   RTEMP,LCBERBCT           ESTABLISH INIT REQUEST
         XC    LCBRECAD,LCBRECAD
         XC    LCBRECOF,LCBRECOF
         LA    RPARM,LCBERB             PREPARE TO POST ERB
         B     FULLQ                    PROCEED WITH RECEIVE     S21101
FREE     EQU   *
         CLC   LCBRSLNK,AVTDELAD+1      SOMETHING TO SEND NOW
         BNE   SENDTST                  BRANCH IF YES TO SEND LOGIC
FREETST  EQU   *
         TM    AVTBIT1,AVTCLOSN+AVTDLAYN     IS THIS CLOSE OR    S21101
*                                            SYSTEM DELAY        S21101
         BNZ   NOSEND                   BRANCH IF EITHER OR BOTH S21101
         TM    LCBSTAT1,LCBOCNI         STOPLINE REQUESTED
         BO    NOSEND                   BRANCH IF YES
SETFREE  EQU   *
         OI    LCBSTAT2,LCBNEGRP        SET SWITCH               S21101
         NI    LCBSTAT1,LCBOCNI+LCBOCWTN PRESERVE STOPLINE BITS@OY14092
         OI    LCBSTAT1,LCBFREEN        SET LINE FREE
         B     DSPDISP                  EXIT TO DISPATCHER
FULLQ    EQU   *
         TM    AVTBIT3,AVTRECVN+AVTRFULN QUEUE THRESHOLD CONDITION
         BZ    DSPPOST                  BRANCH IF NO
         TM    SCBQTYPE,SCBBFMM         IS THIS ALSO MIDDLE      S21101
*                                         OF MESSAGE             S21101
         BO    SETTIME                  YES, SET DELAY TIME      S21101
         CLC   LCBRSLNK,AVTDELAD+1      SOMETHING TO SEND
         BNE   SENDTST                  BRANCH IF YES
         CLI   4(RILIST),0              SENDING TO A STATION
         BNE   SETFREE                  BRANCH IF YES
         EJECT
SETTIME  EQU   *                                                 S21101
         LA    RPARM,1                  ONE-SECOND DELAY
SETDELAY EQU   *
         TM    LCBINSRC+2,LCBDLY        LCB IN TIME DELAY
         BO    FREE                     BRANCH IF YES
         OI    LCBSTAT2,LCBNEGRP        SET SWITCH               S21101
         OI    LCBINSRC+2,LCBDLY        SET LCB IN TIME DELAY
         STH   RPARM,LCBEOLTD           TIME DELAY PARAMETER
         MVI   LCBTDL,LCBINSRC-1-IEDQLCB TIME DELAY INDEX
         ST    RLCB,LCBQCBA-1           SET UP QCB ADDRESS
         NI    LCBSTAT1,LCBOCNI+LCBOCWTN PRESERVE STOPLINE BITS@OY14092
         OI    LCBSTAT1,LCBFREEN        SET LINE FREE
         LR    RPARM,RLCB               TIME DELAY PARAMETER
         L     RENTRY,AVTHG01           TIME DELAY SUBTASK
         BALR  RETURN,RENTRY            PUT LCB IN TIME DELAY
         B     DSPDISP                  EXIT TO DISPATCHER
         EJECT
SENDTST  EQU   *
*                                                                     *
*         THERE IS A DEST QCB IN THE CHAIN OF WAITING QCB'S FOR       *
*      THIS LCB. PREPARE THE ERB FOR A SEND OPERATION IF THERE        *
*      IS UNSENT DATA ON THE QCB.  IF THERE IS NO DATA ON THE         *
*      QCB, REMOVE QCB FROM CHAIN AND POST LCB TO ITSELF.             *
*                                                                     *
         SPACE 2
         L     RSTCB,STCBLINK-1         ADDR OF A BUFFERED TERMINAL
*                                       SCHEDULER'S STCB THAT IS
*                                       ASSOCIATED WITH A DEST QCB
         LA    RPARM,QCBSTVTO-IEDQQCB   STCB OFFSET INTO QCB
         LR    RQCB,RSTCB               COMPUTE STARTING ADDRESS
         SR    RQCB,RPARM                 OF DEST QCB
         BAL   RPQCB,QCBRTNE            COMPUTE THE SCB ADDRESS  S21101
         SPACE
         ST    RSCB,LCBSCBA-1           STORE ADDRESS OF CURRENT
         TM    SCBQTYPE,SCBBFMM         IS THIS STATION IN       S22025
*                                         MIDDLE OF MESSAGE      S22025
         BZ    TRYSEND                  NO, BRANCH               S22025
         TM    QCBSTAT,QCBSEND          IS A SEND OPERATION IN   S22025
*                                         PROGRESS               S22025
         BO    TRYSEND                  YES, BRANCH              S22025
         L     RTEMP,STCBLINK-1         GET THE ADDRESS OF THE   S21101
*                                         NEXT STCB ON THE CHAIN S21101
         CLI   STCBPRIT(RTEMP),AVTEZERO IS THIS THE LAST ELEMENT S21101
*                                         ON THE STCB CHAIN      S21101
         BNE   SENDTST                  BRANCH NO                S21101
         TM    LCBSTAT1,LCBOCNI        IS IT STOPLINE           SA56606
         BO    TSTSTOPL                BR YES                   SA56606
         TM    LCBINSRC+2,LCBDLY       LCB IN END POLL DELAY    SA56605
         BO    SETFREE                  YES-DONT TRY TO POLL    SA56605
         L     RTEMP,LCBINVPT-1         GET CURRENT ILIST ENTRY SA56606
         SR    RPARM,RPARM             CLEAR REG TO ZERO        SA56606
         IC    RPARM,LCBUCBX            RLN-1                   SA56606
         SLL   RPARM,TWO                MULTIPLY BY 4           SA56606
         L     RILIST,DCBINVLI(RPARM)   INVLIST ADDRESS         SA56606
         CLI   ONE(RILIST),ZERO         ANY ACTIVE ENTRIES      SA56606
         BNE   RSETSTCB                 YES, TRY TO RECEIVE     SA56606
         TM    AVTBIT1,AVTCLOSN         FLUSH OR QUICK CLOSE    SA56606
         BO    POSTOPC                  YES-NOTIFY OP CNTRL     SA56606
         B     SETTIME                 PUT LCB IN TIME DELAY    SA56606
RSETSTCB EQU   *                                                SA56606
         SPACE
         LA    RSTCB,LCBRSKEY          RESET STCB BASE           S21101
         B     INVLTST                  FINISH RCV OPERATION       @02C
         EJECT
TRYSEND  EQU   *                                                 S21101
         TM    AVTBIT1,AVTCLOSN+AVTQUCKN  FLUSH CLOSE
         BM    BYPASS                   BRANCH IF YES
         BO    NOSEND                   BRANCH IF QUICK CLOSE    S21101
         TM    AVTBIT1,AVTDLAYN         IS IT SYSTEM DELAY       S21101
         BO    NOSEND                   YES, BRANCH              S21101
         TM    LCBSTAT1,LCBOCNI         STOPLINE
         BO    NOSEND                   BRANCH IF YES
BYPASS   EQU   *
         EJECT
         SPACE 2
         TM    SCBQTYPE,SCBBFMM         ARE WE READY TO SEND A
*                                       SUBSEQUENT(NON-HEADER)
*                                       BLOCK TO THIS DESTINATION
         BNO   INITIAL                  BRANCH IF NO TO PERFORM
*                                       SCAN AND SCB INITIALIZATION
         EJECT
SENDSEG  EQU   *
*        DO SPECIAL PROCESSING FOR THE MULTI-BLOCK MESSAGE SITUATION  *
*                                                                     *
         SPACE 2
         STCM  RQCB,ADR,SCBDESTQ        SET UP QCB ADDRESS     @Z30X8IM
         MVC   LCBTTCIN(2),SCBDCHDR     RESTORE TTCIN              @11A
         NC    SCBDEOB,SCBDEOB          NEED TO DO A RECALL
*                                       FOR EMBEDDED EOB BUFFER
         BZ    BUILDERB                 BRANCH IF NO TO SET UP
*                                       REQUEST FOR NEXT BUFFER
         SPACE
*                                                                     *
*        PREPARE ERB TO RECALL BUFFER WITH EMBEDDED EOB               *
*                                                                     *
*              SEE IF LAST BUFFER OF MESSAGE HAS BEEN READ FROM DISK  *
         TM    SCBQTYPE,SCBCOREQ        CORE QUEUEING
         BO    COREONLY                 BRANCH IF YES
         TM    SCBSTAT1,PRFNLSTN        EOM BUFFER
         BO    NOTLAST                  BRANCH IF NO
         CLC   SCBDEOB+1(3),SCBCRCD     SENDING LAST SEGMENT
         BNL   COREONLY                                         SA60792
         NC    SCBXTRA(THREE),SCBXTRA                           SA60792
         BZ    RSET                                             SA60792
         CLC   SCBDEOB+ONE(THREE),SCBXTRA                       SA60792
         BNL   COREONLY                                         SA60792
RSET     EQU   *                                                SA60792
         OI    SCBSTAT1,PRFNLSTN        FLAG AS NOT LAST YET
NOTLAST  EQU   *
         XC    SCBXTRA(3),SCBXTRA       CLEAR FOR RECALL         S21101
         XC    SCBNTXT(3),SCBNTXT       CLEAR FOR RECALL         S21101
COREONLY EQU   *                                                 S21101
         MVZ   SCBHBFNO,SCBQTYPE        SET QTYPE FLGS FOR FA   SA52971
         MVC   LCBCPA(4),SCBDEOB        SAVE OVER RECALL
         NI    QCBSTAT,AVTEFF-QCBBUFRD  TURN OFF HEADER FLAG
         LA    RTEMP,AVTDSIOB           DISK I/O QCB ADDRESS
         ST    RTEMP,LCBERB             SET QCB ADDR IN ERB
         NI    LCBSTAT1,LCBOCNI+LCBOCWTN PRESERVE STOPLINE BITS@OY14092
         OI    LCBSTAT1,LCBSENDN        SET LCB IN SEND STATE
         MVI   LCBERBPY,PRIDKEOB        RECALL PRIORITY
         MVI   LCBERBCT,1               RECALL BUFFER COUNT
         MVI   LCBERBCT+1,AVTEZERO      CLEAR DISABLED COUNT
         OI    LCBSTAT1,LCBRCLLN        SET RECALL BIT IN LCB
         ST    RLCB,LCBRCQCB            SET UP ERB RETURN ADDRESS
         LA    RPARM,LCBERB             ERB ADDR FOR DISPATCHER
         B     DSPPOST                  POST ERB FOR RECALL
         EJECT
GETQCBAD EQU   *                                                 S21101
*        SUBROUTINT WILL COMPUTE ADDRESS OF QCB BASED ON ILIST   S21101
* POINSTER IN RTEMP                                              S21101
*        RILIST POINTS TO ILIST CONTROL WORDS                    S21101
*        QCB IS SET IN RQCB                                      S21101
*        TNT OFFSET IS SET AT AVTDOUBL+4                         S21101
         SR    RPARM,RPARM              CLEAR WORK REGISTER      S21101
         IC    RPARM,ILSTSIZE(,RILIST)  GET THE SIZE OF EACH     S21101
*                                         INVITATION LIST ENTRY  S21101
         BCTR  RPARM,RZERO              DECREMENT TO INDEX BYTE  S21101
         IC    RPARM,0(RTEMP,RPARM)     GET INDEX BYTE           S21101
         AR    RPARM,RPARM              DOUBLE FOR NEG INDEX     S21101
         LR    RTERM,RILIST             COPY ILIST ADDRESS       S21101
         SR    RTERM,RPARM              ADDRESS OF TNTOFFSET     S21101
         LH    RPARM,AVTEZERO(,RTERM)   DET TNT OFFSET           S21101
         STH   RPARM,AVTDOUBL+4         SAVE TNT OFFSET          S21101
         L     RENTRY,AVTRNMPT          TNT CONVERSION           S21101
         BALR  RETURN,RENTRY            LINK TO IT               S21101
         L     RQCB,TRMDESTQ-1-IEDQTRM(RPARM)     LOAD THE       S21101
*                                         DESTINATION QCB ADDR   S21101
         BR    RPQCB                    RETURN                   S21101
         EJECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *  S21101
*                                                             *  S21101
*        THIS SUBROUTINE WILL GET THE ADDRESS OF THE NEXT     *  S21101
*        INVITATION LIST ENTRY.  RTEMP POINTS TO THE          *  S21101
*                            CURRENT ENTRY                    *  S21101
*                                                             *  S21101
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *  S21101
NEXTENT  EQU   *                                                 S21101
         SR    RPARM,RPARM              CLEAR WORK REGISTER      S21101
         IC    RPARM,ILSTSIZE(,RILIST)  SIZE OF EACH             S21101
         AR    RTEMP,RPARM              BUMP TO NEXT             S21101
         CLI   AVTEZERO(RTEMP),FE       END OF LIST              S21101
         BE    RESETIT                  BRANCH YES TO RESET      S21101
         CLI   1(RTEMP),FE              END OF BSC LIST          S21101
         BNE   SETINVPT                 NO, BRANCH               S21101
RESETIT  EQU   *                                                 S21101
         LA    RTEMP,ILISTPRF(,RILIST)  TO FIRST ENTRY           S21101
SETINVPT EQU   *                                                 S21101
         STCM  RTEMP,ADR,LCBINVPT       STORE THE ADDRESS OF   @Z30X8IM
*                                         THE NEXT ILIST ENTRY @Z30X8IM
         BR    RPQCB                    RETURN                   S21101
         EJECT
INITIAL  EQU   *
*                                                                     *
*        FIND PRIORITY LEVEL QCB WITH A MESSAGE TO BE SENT            *
*                                                                     *
         USING IEDQPQCB,RPQCB           PRIORITY LEVEL QCB
         L     RPARM,DCBDEBAD           DEB ADDRESS
         TM    DEBOPATB-DEBNMSUB(RPARM),OUTPUT OPEN FOR OUTPUT
         BNO   DEQ                      BRANCH IF NO
        TM    QCBSTAT,QCBTRMHO         IS TERMINAL HELD
         BO    DEQ                      BRANCH IF YES
         TM    QCBDSFLG,QCBHELD         REUSABILITY RUNNING
         BO    DEQ                      BRANCH IF YES
         SR    RTEMP,RTEMP              INITIALIZE PQCB COUNTER
         LA    RPQCB,IEDQPQCB-IEDQQCB(0,RQCB) ADDR OF FIRST PRTY QCB
LOOP     EQU   *
         NC    QCBFFEFO,QCBFFEFO        IS THERE AN UNSENT MESSAGE
*                                       FOR THIS PRTY LEVEL QCB
         BNZ   BUILDSCB                 BRANCH IF YES TO COMPLETE
*                                       SCB INITIALIZATION
         CLI   QCBPRIPQ,0               LAST PRTY LEVEL QCB
         BE    DEQ                      BRANCH IF YES TO REMOVE
*                                       THIS QCB FROM LCB CHAIN
         LA    RPQCB,QCBPEND            ADDR OF NEXT PRTY LEVEL QCB
         LA    RTEMP,1(0,RTEMP)         INCREMENT PQCB COUNTER
         B     LOOP                     TEST THIS PRTY LEVEL QCB
         EJECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *  S21101
*                                                             *  S21101
*        REMOVE THE QCB FROM THE STCB CHAIN OF THE LCB        *  S21101
*                                                             *  S21101
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *  S21101
DEQ      EQU   *
         SPACE 4
         NI    QCBSTAT,X'FF'-QCBSEND    TURN OFF SEND MODE FLAG
         BAL   RETURN,SEARCH            FIND AND REMOVE STCB     A48267
         MVC   13(3,RQCB),9(RQCB)       PUT DEST SCHED ON BOTTOM
         LA    RPARM,8(0,RQCB)          ADDRESS OF BTS STCB
         ST    RPARM,8(0,RQCB)          PUT BTS STCB ON TOP OF QCB
         MVI   8(RQCB),X'1A'            RESTORE BTS VTO
         SR    RTEMP,RTEMP              CLEAR REG TO ZERO
         IC    RTEMP,LCBUCBX            RLN-1
         SLL   RTEMP,2                  MULTIPLE BY FOUR
         L     RILIST,DCBINVLI(RTEMP)   INVITATION LIST ADDRESS
         SR    RTEMP,RTEMP              CLEAR REG TO ZERO
         IC    RTEMP,4(RILIST)          SUBTRACT ONE FROM COUNT  S22025
         BCTR  RTEMP,0                  OF TERMINALS CURRENTLY   S22025
         STC   RTEMP,4(RILIST)          SENDING                  S22025
         ST    RLCB,LCBQCBA-1           POST ADDRESS             S21101
         NI    LCBSTAT1,LCBOCNI+LCBOCWTN PRESERVE STOPLINE BITS@OY14092
         OI    LCBSTAT2,LCBNEGRP        TURN ON THE NEGATIVE     S21101
*                                         RESPONSE BIT           S21101
         OI    LCBSTAT1,LCBRECVN        SET LCB STATE            S21101
         LR    RPARM,RLCB               SET UP DISPATCHER PARM REG
         B     DSPPOST                  POST LCB TO ITSELF
         EJECT
*                                                                     *
*              COMPLETE SCB AND ERB INITIALIZATION                    *
*                                                                     *
BUILDSCB EQU   *
         MVI   LCBTTCIN+ONE,ONE                                 SA60792
         OI    QCBSTAT,QCBSEND          SET SEND MODE FLAG
         MVC   SCBSCHDR,QCBFFEFO        MOVE REL REC NO OF CURRENT
*                                       HEADER INTO SCB
         MVC   SCBSCSEG,QCBFFEFO        MOVE REL REC NO OF CURRENT
*                                       SEGMENT INTO SCB
         OI    SCBQTYPE,SCBBFMM         SET MID-MESSAGE FLAG
         STC   RTEMP,SCBPRI             PUT PRTY LEVEL QCB INDEX
*                                       INTO SCB
         OI    QCBSTAT,QCBBUFRD         TURN ON HEADER FLAG
         OI    QCBFLAG,QCBSDFFO
         XC    SCBFEFO(3),SCBFEFO
         STCM  RQCB,ADR,SCBDESTQ        PUT QCB ADDR IN SCB    @Z30X8IM
         MVI   SCBUNTCT,AVTEZERO        CLEAR UNIT COUNT           @03A
         SPACE 2
BUILDERB EQU   *
         LA    RTEMP,AVTDSIOB           GET ADDRESS OF DISK I/O
*                                       QCB FROM AVT
ACTIVATE EQU   *
         ST    RTEMP,LCBERB             SET QCB ADDR IN ERB
         NI    QCBELCHN+2,AVTEFF-QCBCNTEN TURN OFF BUFFER BUSY @OX17153
         NI    LCBSTAT1,LCBOCNI+LCBOCWTN PRESERVE STOPLINE BITS@OY14092
         OI    LCBSTAT1,LCBSENDN        SET LCB IN SEND STATE
         MVI   LCBERBPY,PRIINTRQ        INITIAL REQUEST PRIORITY
         MVC   LCBERBCT(1),DCBBUFOU     INITIAL BUFFER COUNT
         NI    LCBERBCT,X0F             CLEAR LOW-ORDER FOUR BITS
         XC    LCBRECAD,LCBRECAD
         XC    LCBRECOF,LCBRECOF
         LA    RPARM,LCBERB             PUT ERB ADDR IN PARM REG
*                                       FOR DISPATCHER
         B     DSPPOST                  POST ERB TO DISK I/O
         EJECT
DELAY    EQU   *
*                                                                     *
*        SINCE THE LAST OPERATION WAS A 'SEND', THE DESTINATION       *
*        QCB MUST BE PLACED IN THE TIME DELAY QUEUE FOR THE TIME      *
*        INTERVAL SPECIFIED IN THE TERMINAL ENTRY FOR THE LAST        *
*        SENT BLOCK.                                                  *
*                                                                     *
         MVC   SCBDCHDR(2),LCBTTCIN     SAVE TNT OFFSET
         SR    RPARM,RPARM              CLEAR REGISTER TO ZERO
         LH    RPARM,LCBTTCIN           TNT OFFSET OF TERMINAL
*                                       TO WHICH WE JUST SENT
         N     RPARM,AVTCLRHI           CLEAR HI-ORDER HALF
         LTR   RPARM,RPARM              IS TTCIN=0 ?           @OZ34395
         BZ    NEXTTEST                 YES                    @OZ34395
         L     RENTRY,AVTRNMPT          ADDR OF CONVERSION LOGIC
         BALR  RETURN,RENTRY            COMPUTE TERMINAL ENTRY ADDR
         LR    RTERM,RPARM              DSECT ADDRESSABILITY
         L     RQCB,TRMDESTQ-1          DESTINATION QCB ADDRESS
*
*              FIND BUFFERED TERMINAL TIME DELAY VALUE
*
         LA    RPARM,TRMOPNO            GET THE ADDRESS OF THE   S22025
*                                         FIRST DEVICE DEP FIELD S22025
         TM    TRMSTATE,TRMOPTFN        OPTION FIELDS SPECIFIED
         BZ    SCAN                     NO, BRANCH               S22025
         SPACE 2
COMPOPTF EQU   *
         SR    RETURN,RETURN            CLEAR REGISTER TO ZERO
         IC    RETURN,TRMOPNO           NUMBER OF OPTION FIELDS
         LA    RPARM,THREE(RETURN,RPARM) BUMP THE DEVICE DEP.    S22025
*                                         FIELD POINTER PAST THE S22025
*                                         OPTION OFFSETS         S22025
SCAN     EQU   *
         LH    RETURN,TRMDEVFL          GET THE DEVICE DEPENDENT S22025
*                                         FIELD FLAGS            S22025
         SLL   RETURN,FIFTEEN           SHIFT FLAGS TO THE LEFT  S22025
         LA    RENTRY,SIX               TEST 6 DEV DEP FLAGS     S22025
         SR    RTERM,RTERM              CLEAR A LENGTH REGISTER  S22025
SHIFT    EQU   *
         SLL   RETURN,ONE               TEST THE NEXT FLAG       S22025
         LTR   RETURN,RETURN            IS THIS FIELD PRESENT    S22025
         BNM   BUMP                     NO, BRANCH               S22025
         IC    RTERM,ZERO(RPARM)        GET THE LENGTH OF THE    S22025
*                                         CURRENT FIELD          S22025
         LA    RPARM,ONE(RTERM,RPARM)   BUMP TO THE NEXT OPTION  S22025
*                                         FIELD                  S22025
BUMP     EQU   *
         BCT   RENTRY,SHIFT            IS THIS THE BFDLY FIELD   S22025
*                                         IF NOT, CONTINUE SCAN  S22025
         SPACE 3
MOVETIME EQU   *
         MVC   QCBEOLDT(2),1(RPARM)     MOVE TIME DELAY INTO QCB
         MVI   QCBEOLDT+2,0             TIME DELAY INTERFACE INDEX
         MVI   QCBPRI,X'EC'             SET QCB PRIORITY
         LA    RTEMP,BTSTDQCB           BTS TIME DELAY INTERRUPT
         STCM  RTEMP,ADR,QCBELCHN       HANDLER QCB ADDRESS    @Z30X8IM
*                                                                    *
*        CHECK FOR SPECIAL CASE OF TIME DELAY OF 12 OR MORE HOURS    *
*                                                                    *
         CLC   QCBEOLDT(2),TWELVE       DELAY GREATER THAN 12 HOURS
         BL    LOWER                    BRANCH IF NO
         LH    RTEMP,QCBEOLDT           GET LARGE TIME DELAY
         N    RTEMP,AVTCLRHI            CLEAR HIGH-ORDER HALFWORD
         SH    RTEMP,TWELVE             DECREMENT BY 12 HOURS
         OI    QCBSTAT,QCBTIME          SET FLAG FOR TIME DELAY
         STH   RTEMP,QCBEOLDT           SET NEW DELAY VALUE
LOWER    EQU   *
         EJECT
*                                                                     *
*              REMOVE QCB FROM STCB CHAIN OF LCB                      *
*                                                                     *
NODECR   EQU   *
         TM    SCBQTYPE,SCBBFMM         IS THIS MIDDLE OF MSG    S21101
         BO    NODECR01                 YES, BRANCH              S21101
         TM    QCBSTAT,QCBTRMHO         IS TERMINAL HELD        SA52515
         BO    NEXTTEST                 BRANCH IF YES           SA52515
         NI    QCBSTAT,AVTEFF-QCBSEND   TURN OFF THE SEND BIT IN S21101
*                                         THE DESTINATION QCB    S21101
         SR    RTEMP,RTEMP              CLEAR REG TO ZERO      @OX12490
         IC    RTEMP,LCBUCBX            RLN-1                  @OX12490
         SLL   RTEMP,2                  MULTIPLE BY FOUR       @OX12490
         L     RILIST,DCBINVLI(RTEMP)   INVITATION LIST ADDRESS@OX12490
         SR    RTEMP,RTEMP              CLEAR REG TO ZERO      @OX12490
         IC    RTEMP,4(RILIST)          SUBTRACT ONE FROM COUNT@OX12490
         BCTR  RTEMP,0                  OF TERMINALS CURRENTLY @OX12490
         STC   RTEMP,4(RILIST)          SENDING                @OX12490
NODECR01 EQU   *                                                 S21101
         LR    RPARM,RQCB               PARM FOR TIME DELAY
         BAL   RETURN,SEARCH            FIND AND REMOVE STCB     A48267
         XC    13(3,RQCB),13(RQCB)
         L     RENTRY,AVTHG01           TIME DELAY LOGIC ADDRESS
         BALR  RETURN,RENTRY            POST QCB TO TIME DELAY
         LA    RSTCB,LCBSTCBA-1         RESTORE STCB ADDRESS
         SPACE 2
NEXTTEST EQU   *
*                                                                     *
*        IF THERE IS SOMETHING TO SEND, SEND IT.  IF NOT, AND         *
*        THERE IS AN ACTIVE ENTRY IN THE ILIST, TRY TO RECEIVE.       *
*        OTHERWISE, SET THE LINE FREE AND EXIT TO DISPATCHER.         *
*                                                                     *
         L     RDCB,LCBDCBPT            LINE GROUP DCB ADDRESS
         NI    LCBSTAT1,X'FF'-LCBSENDN  SET LCB NOT SENDING
         CLC   LCBRSLNK,AVTDELAD+1      ANOTHER QCB IN LCB CHAIN
         BNE   SENDTST                  BRANCH IF YES TO TRY TO
*                                       SEND ANOTHER BLOCK
         SR    RPARM,RPARM              CLEAR REGISTER TO ZERO
         IC    RPARM,LCBUCBX            RLN-1
         SLL   RPARM,2                  MULTIPLY BY FOUR
         L     RILIST,DCBINVLI(RPARM)   INVITATION LIST ADDRESS
         CLI   0(RILIST),0              ANY ACTIVE ENTRIES
         BNE   INVLTST                  BRANCH IF YES
         B     FREETST
         SPACE  2
SEARCH   EQU   *                                                 A48267
         LA    RSTCB,QCBSTVTO           ADDRESS OF SEND SCHEDULARA48267
*                                         TO BE REMOVED          A48267
         ST    RSTCB,AVTDOUBL                                    A48267
         LA    RSTCB,LCBRSKEY           ADDRESS OF RECEIVE SCH   A48267
SCHLOOP  EQU   *                                                 A48267
         CLC   AVTDOUBL+1(3),5(RSTCB)   IS THE NEXT SCHEDULAR    A48267
*        THE ONE TO BE REMOVED                                   A48267
         BE    DELINK                   BRANCH IF YES            A48267
         L     RSTCB,4(RSTCB)           NEXT SCHEDULAR           A48267
         B     SCHLOOP                  CONTINUE                 A48267
DELINK   EQU   *                                                 A48267
         L     RTEMP,4(RSTCB)           ADDRESS OF SCH TO REMOVE A48267
         MVC   5(3,RSTCB),5(RTEMP)      DELINK STCB FROM CHAIN   A48267
         BR    RETURN                   RETURN                   A48267
         EJECT
NOSEND   EQU   *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *  S22025
*                                                                     *
*        RECIEVE OPERATIONS ARE HALTED.  IF FLUSH CLOSE AND THERE     *
*        ARE MESSAGES TO SEND, SEND THEM UNTIL THE QUEUES ARE EMPTY.  *
*        IF QUICK CLOSE OR STOP LINE, CHECK FOR QCBSFOR WHICH THERE   *
*        IS A PARTIAL MESSAGE TO SEND.  IF NONE, POST LCB TO OPCTL.   *
*                                                                     *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *  S22025
         OI    LCBSTAT2,LCBSNDPR        SET A FLAG FOR LINE END  S22025
         CLC   LCBRSLNK,AVTDELAD+1      MESSAGE TO SEND
         BNE   TESTOPN                  BRANCH IF YES
PRESTOP  EQU   *
*                                                                     *
*        CHECK FOR QCB ON TIME DELAY QUEUE                            *
*                                                                     *
         L     RTEMP,AVTDELYB           DUMMY TIME QUEUE ELEMENT
         L     RPARM,AVTTIMQ            FIRST TIME QUEUE ELEMENT
         LA    RTEMP,0(0,RTEMP)         CLEAR HIGH-ORDER BYTE
TQLOOP   EQU   *
         LA    RPARM,0(0,RPARM)         CLEAR HIGH-ORDER BYTE
         CR    RTEMP,RPARM              END OF TIME QUEUE
         BE    STOPSEND                 BRANCH IF YES
         CLI   QCBEOLDT+2-IEDQQCB(RPARM),AVTEZERO ELEM A QCB
         BE    QCBTESTX                 BRANCH IF YES
TQNEXT   EQU   *
         L     RPARM,28(0,RPARM)        ADDR OF NEXT ELEM ON QUEUE
         B     TQLOOP                   CHECK NEXT ELEMENT       S22025
QCBTESTX EQU   *
         CLC   LCBDCBPT+1(3),QCBDCBAD-IEDQQCB(RPARM)             S21101
*                                       IS THIS QCB ASSOCIATED   S21101
*                                         WITH THIS LCB          S21101
         BNE   TQNEXT                   NO, CHECK NEXT ELEMENT   S21101
*                                         ON THE TIME QUEUE      S21101
RLNTEST  EQU   *
         IC    RENTRY,QCBRELLN-IEDQQCB(RPARM)  RLN
         BCTR  RENTRY,RZERO             RLN-1
         EX    RENTRY,TESTRLN           SAME RELATIVE LINE NUMBER
         BNE   TQNEXT                   BRANCH IF NO
         B     SETFREE                  EXIT IF QCB ON TIME DELAY Q
STOPSEND EQU   *
         OI    LCBSTAT2,LCBNEGRP        SET SWITCH               S21101
         L     RPARM,DCBDEBAD           GET DEB POINTER          S21101
         TM    DEBOPATB-DEBNMSUB(RPARM),OUTONLY       IS THIS AN S21101
*                                         OUTPUT ONLY LINE       S21101
         BO    POSTHK                   BRANCH IF YES            S21101
         SPACE
         LA    RTEMP,ILISTPRF(,RILIST)  POINT TO FIRST ENTRY IN  S21101
*                                         THE INVITATION LIST    S21101
         STCM  RTEMP,ADR,LCBINVPT       STORE ADDRESS OF FIRST @Z30X8IM
*                                         ILIST ENTRY          @Z30X8IM
         SPACE
LOOPEX   EQU   *                                                 S21101
         CLI   AVTEZERO(RTEMP),FE       IS THIS THE LAST ENTRY   S21101
         BE    POSTHK                   YES, BRANCH              S21101
         CLI   1(RTEMP),FE              END OF THE BSC LIST      S21101
         BE    POSTHK                   YES, BRANCH              S21101
         SPACE
ENTLOOP  EQU   *                                                 S21101
         BAL   RPQCB,GETQCBAD           GET THE QCB ADDRESS FOR  S21101
*                                         THIS ENTRY             S21101
         TM    QCBSTAT,QCBSEND          ARE WE CURRENTLY SENDING S22025
         BO    NEXTSEND                 YES, BRANCH              S22025
*                                                                S22025
         BAL   RPQCB,QCBRTNE            COMPUTE THE SCB ADDRESS  S22025
         TM    LCBSTAT1,LCBOCNI         REQUEST FOR STOPLINE?   SA56606
         BO    NEXTSEND                 BRANCH YES              SA56606
         TM    SCBQTYPE,SCBBFMM         MID MESSAGE ON RECEIVE?  S22025
         BO    GORCV                    YES, BRANCH              S22025
NEXTSEND EQU   *                                                 S22025
         SR    RPARM,RPARM              CLEAR A WORK REGISTER    S21101
         IC    RPARM,ILSTSIZE(,RILIST)  SIZE OF AN ENTRY         S21101
         AR    RTEMP,RPARM              ADDRESS OF NEXT ENTRY    S21101
*                                         IN INVITATION LIST     S21101
         B     LOOPEX                   SCAN NEXT ENTRY          S21101
         SPACE
POSTHK   EQU   *                                                 S21101
         TM    AVTBIT1,AVTDLAYN         IS THIS SYSTEM DELAY     S21101
         BNO   TSTSTOPL                 NO, BRANCH               S21101
         MVC   LCBQCBA(3),AVTHI+1       SET THE SYSTEM DELAY QCB S21101
*                                         ADDRESS                S21101
         B     SETPARM                  BRANCH TO POST LCB       S21101
TSTSTOPL EQU   *                                                 S21101
         TM    LCBSTAT1,LCBOCNI         STOPLINE REQUESTED
         BNO   POSTOPC                  BRANCH IF NO
         MVC   LCBQCBA(3),AVTHK+1       OPERATOR CONTROL QCB
SETPARM  EQU   *                                                 S21101
         LR    RPARM,RLCB               DISPATCHER PARAMETER
         B     DSPPOST                  POST LCB TO OPCTL
POSTOPC  EQU   *
         MVI   LCBSTAT1,LCBFREEN        SET LINE FREE
         SR    RPARM,RPARM              CLEAR REGISTER TO ZERO
         IC    RPARM,LCBUCBX            RLN-1
         SLL    RPARM,2                 MULTIPLY BY FOUR
         L     RILIST,DCBINVLI(RPARM)   INVITATION LIST ADDRESS
         MVI   4(RILIST),AVTEZERO       SET LINE NOT SENDING
         LA    RPARM,AVTOPCOB+4         OPERATOR CONTROL ECB ADDR
         POST  (1)
         B     DSPDISP                  EXIT TO DISPATCHER
         EJECT
TESTOPN  EQU   *
         TM    AVTBIT1,AVTCLOSN+AVTQUCKN FLUSH CLOSE
         BM    SENDTST                  BRANCH IF YES
*                                                                     *
*        SEE IF THERE IS A PARTIAL MESSAGE TO BE SENT.                *
*                                                                     *
         L     RSTCB,LCBRSLNK-1         BTS STCB ADDRESS
OPNLOOP  EQU   *                                                OX03968
         LA    RPARM,QCBSTVTO-IEDQQCB   STCB OFFSET INTO QCB
         LR    RQCB,RSTCB
         SR    RQCB,RPARM               COMPUTE QCB ADDRESS
         SR    RZERO,RZERO              CLEAR A WORK REGISTER    S21101
         IC    RZERO,QCBSCBOF           GET THE SCB INDEX        S21101
         BAL   RPQCB,QCBRTNE            COMPUTE THE SCB ADDRESS  S21101
         ST    RSCB,LCBSCBA-1           SET SCB ADDR IN LCB     SA68697
         SPACE
         TM    SCBQTYPE,SCBBFMM         MIDDLE OF MESSAGE
         BZ    NEXTQCB                  BRANCH IF NO            OX03968
         TM    QCBSTAT,QCBSEND          MID MSG ON OUTPUT       OX03968
         BO    SENDSEG                  BRANCH IF YES
NEXTQCB  EQU   *                                                OX03968
         L     RTEMP,STCBLINK-1         GET NEXT STCB ADDRESS   OX03968
         CLI   STCBPRIT(RTEMP),AVTEZERO IS THIS LAST STCB       OX03968
         BE    PRESTOP                  BRANCH IF YES TO CHECK  OX03968
*                                       TIME DELAY QUEUE FOR    OX03968
*                                       TERMINALS IN MIDDLE OF  OX03968
*                                       MESSAGE ON OUTPUT       OX03968
         LR    RSTCB,RTEMP              SAVE STCB ADDRESS       OX03968
         B     OPNLOOP                  AND LOOK AT NEXT STCB   OX03968
         SPACE 2                                               @XA08074
ABEND045 EQU   *                                               @XA08074
         LA    RENTRY,ABEND2            PASS SUBCODE FOR LOGCAL@XA08074
*                                        READ ERROR FOR REUS   @XA08074
         BAL   RETURN,AVTABEND          ABEND TCAM 045-2       @XA08074
         EJECT
BTSTDQCB DS    0F                       QCB TO WHICH TIME DELAY
         DC    C'BFTS'                  POSTS DEST QCB AT END OF
         DC    XL1'0A'                  TIME DELAY INTERVAL
         DC    C'TDQ'                   IDENTIFIER               S22025
         DC    A(*-4)                   STCB ADDRESS
*                                                                     *
*        THIS SUBTASK IS ACTIVATED BY THE TIME DELAY ROUTINE          *
*        WHEN THE TIME DELAY FOR A DESTINATION QCB HAS ELAPSED.       *
*        IT RETURNS THE QCB TO THE LCB CHAIN.  IF THE LINE IS         *
*        FREE, IT POSTS THE LCB TO ITSELF.  IF NOT, IT EXITS          *
*        TO THE DISPATCHER                                            *
*                                                                     *
         USING *,RENTRY                 SUBTASK ADDRESSABILITY
         LR    RQCB,RPARM               DEST QCB ADDRESS
*                                                                     *
*                   FIND THE LCB FOR THIS QCB                         *
*                                                                     *
         L     RDCB,QCBDCBAD-1          DCB ADDRESS
         L     RPARM,DCBDEBAD           DEB ADDRESS
         BAL   RPQCB,LCBFIND            FIND QCB'S LCB
         BAL   RPQCB,QCBRTNE            COMPUTE THE SCB ADDRESS  S21101
         SPACE
         LA    RPARM,QCBSTVTO           ADDR OF BTS SEND STCB
         LR    RDCB,RQCB                SAVE REG               @OX12490
         LA    RQCB,LCBRSLNK-1          ADDRESS OF STCB CHAIN
         BAL   RETURN,DSPPRIOR          INSERT STCB INTO LCB CHAIN
         LR    RQCB,RDCB                RESTORE REG            @OX12490
         L     RDCB,LCBDCBPT            DCB ADDRESS
         TM    DCBCPRI,EQPRI            EQUAL PRIORITY
         BO    NOSETX                   BRANCH IF YES
         OI    LCBSTAT2,LCBSNDPR        TELL LINE END THAT IT IS
*                                       SEND PRIORITY AND THAT THERE
*                                       IS SOMETHING TO SEND
NOSETX   EQU   *
         SR    RPARM,RPARM              CLEAR REG TO ZERO
         IC    RPARM,LCBUCBX
         SLL   RPARM,2
         L     RILIST,DCBINVLI(RPARM)   INVITATION LIST ADDRESS
         TM    QCBSTAT,QCBSEND          ARE WE SENDING         @OX12490
         BO    NOBUMP                   BRANCH IF YES          @OX12490
         IC    RTEMP,4(RILIST)                                 @OX12490
         LA    RTEMP,1(0,RTEMP)                                @OX12490
         STC   RTEMP,4(RILIST)                                 @OX12490
NOBUMP   EQU   *
         CLI   LCBSTAT1,AVTEZERO        LINE STOPPED
         BE    DSPDISP                  BRANCH IF YES
         TM    LCBSTAT1,LCBFREEN        IS THE LINE FREE
         BNO   AUTOTST                  BRANCH IF NO
         CLI   LCBPRI,PRILCBDL          SYSTEM INTERVAL IN EFFECT
         BE    DSPDISP                  BRANCH IF YES
         ST    RLCB,LCBQCBA-1           PREPARE TO POST LCB TO LCB
         NI    LCBSTAT1,LCBOCNI+LCBOCWTN PRESERVE STOPLINE BITS@OY14092
         OI    LCBSTAT1,LCBRECVN        SET RECEIVE STATUS IN LCB
         LR    RPARM,RLCB               DISPATCHER PARAMETER
         B     DSPPOST                  POST LCB TO ITSELF
AUTOTST  EQU   *
         CLI   LCBCPA+24,POLL           LINE IN AUTOPOLL
         BNE   DSPDISP                  BRANCH IF NO
         ST    RLCB,AVTSAVE2            SAVE LCB POINTER         Y05331
         L     RPARM,AVTSAVTP           POINTER TO SECONDARY AVT Y05331
         USING IEDNSVTD,RPARM                                    Y05331
         TM    SAVTDIAF,SAVTVIRT        IN VM ENVIRONMENT ?      Y05331
         BNO   NOVIRT                    NO,BRANCH               Y05331
         ST    RENTRY,AVTSAVE2+28       SAVE R15                 Y05331
         LA    RZERO,LCBCPA+56          TIC ADDRESS              Y05331
         L     RENTRY,SAVTDIAG          VIRTUAL DIAGNOSE ROUTINE Y05331
         DROP  RPARM                                             Y05331
         MVI   LCBCPA+56,CCWNOP         NOP VIRTUAL TIC          Y05331
         BAL   RETURN,4(,RENTRY)        NOP REAL TIC,STOP AUTOPOLY05331
         L     RENTRY,AVTSAVE2+28       SARESTORE R15            Y05331
         NI    3(RILIST),AUTOPOLF       STOP AUTOPOLL            Y05331
         B     DSPDISP                  EXIT TO DISPATCHER       Y05331
         SPACE
NOVIRT   EQU   *                        NON VIRTUAL ENVIRONMENT  Y05331
         MVI   LCBCPA+56,CCWNOP         STOP AUTOPOLL
         SPACE 1                                                SA61794
         B     DSPDISP                  EXIT TO DISPATCHER
         EJECT
TAG      EQU   *
*                                                                     *
*        THIS LOGIC IS ACTIVATED BY DESTINATION SCHEDULER WHEN        *
*        EOM IS POSTED TO DESTINATION QCB. (RQCB IS SET UP)           *
*                                                                     *
         USING *,RENTRY                 ESTABLISH ADDRESSABILITY
         L     RDCB,QCBDCBAD-1          LINE GROUP DCB
         TM    DCBOFLGS,OPEN            IS THIS DCB OPEN
         BCR   8,RETURN                 RETURN TO DEST SCHED IF NO
         L     RPARM,DCBDEBAD           DEB ADDRESS
         TM    DEBOPATB-DEBNMSUB(RPARM),OUTPUT   OPEN FOR OUTPUT
         BCR   8,RETURN                 RETURN TO DEST SCHED IF NO
*                                                                     *
*                   FIND THE LCB FOR THIS QCB                         *
*                                                                     *
         LA    RPQCB,ENDFIND
LCBFIND  EQU   *
         SR    RTEMP,RTEMP              CLEAR REGISTER TO ZERO
         IC    RTEMP,DEBNMEXT-DEBNMSUB(0,RPARM) NUMBER OF LCBS
         SR    RPARM,RPARM              CLEAR REGISTER TO ZERO
         IC    RPARM,QCBRELLN           QCB'S STARTING RLN
         LR    RLCB,RPARM               SAVE THIS RLN FOR LATER
         BCTR  RLCB,RZERO               RLN MINUS 1
         SR    RTEMP,RLCB               COUNT OF LCB'S TO SCAN
         BCR   13,RETURN                RETURN TO DEST SCHED IF RLN
*                                       LESS THAN DEBNMEXT
         SR    RTEMP,RTEMP              CLEAR REGISTER TO ZERO
         IC    RTEMP,DCBEIOBX           LCB SIZE
         MR    RPARM-1,RTEMP            STARTING LCB OFFSET
         AL    RPARM,DCBIOBAD           POINT TO IOB OF LCB
         LA    RTEMP,LCBFLAG1-IEDQLCB
         SR    RPARM,RTEMP              ADJUST TO START OF LCB
         LR    RLCB,RPARM               LCB ADDRESS
         LR    RTEMP,RETURN             SAVE RETURN ADDRESS OF
*                                       DESTINATION SCHEDULER
         BR    RPQCB                    RETURN TO CALLER
ENDFIND  EQU   *
         SPACE 2
         TM    LCBSTAT1,LCBFREEN        IS THE LINE FREE NOW
         BNO   LINKQCB                  BRANCH IF NO
         CLI   LCBPRI,PRILCBDL          SYSTEM INTERVAL IN EFFECT
         BE    LINKQCB                  BRANCH IF YES
         NI    LCBSTAT1,LCBOCNI+LCBOCWTN PRESERVE STOPLINE BITS@OY14092
         OI    LCBSTAT1,LCBRECVN        SET RECEIVE STATUS IN LCB
         ST    RLCB,LCBQCBA-1           PREPARE TO POST LCB TO LCB
         LR    RZERO,RQCB               SAVE QCB ADDRESS
         BAL   RETURN,DSPPOSTR          POST LCB TO ITSELF
         LR    RQCB,RZERO               RESTORE QCB ADDRESS
         EJECT
LINKQCB  EQU   *
*                                                                     *
*              PUT QCB INTO LCB'S CHAIN OF WAITING QCB'S              *
*                                                                     *
         LR    RSTCB,RLCB               ADDRESS OF QCB TO WHICH
*                                       STCB IS TO BE LINKED
         L     RDCB,LCBDCBPT            DCB ADDRESS
         TM    DCBCPRI,EQPRI            EQUAL PRIORITY
         BO    NOSET                    BRANCH IF YES
         OI    LCBSTAT2,LCBSNDPR        TELL LINE END THAT IT IS
*                                       SEND PRIORITY AND THAT THERE
*                                       IS SOMETHING TO SEND
NOSET    EQU   *
         BAL   RETURN,DSPUNAVR          LINK QCB TO LCB
         SPACE 2
*                                                                     *
*        IF LINE IS BEING AUTOPOLLED, STOP AUTOPOLL AND EXIT          *
*        TO DESTINATION SCHEDULER.  IF NO, JUST EXIT TO DEST SCHED    *
*                                                                     *
         CLI   LCBCPA+24,POLL           LINE IN AUTOPOLL
         BNE   OUT                      BRANCH IF NO
         ST    RLCB,AVTSAVE2            SAVE LCB POINTER         Y05331
         L     RPARM,AVTSAVTP           POINTER TO SECONDARY AVT Y05331
         USING IEDNSVTD,RPARM                                    Y05331
         TM    SAVTDIAF,SAVTVIRT        IN VM ENVIRONMENT ?      Y05331
         BNO   NOVIRT2                   NO,  BRANCH             Y05331
         LA    RZERO,LCBCPA+32          TIC ADDRESS              Y05331
         ST    RENTRY,AVTSAVE2+28       SAVE R15                 Y05331
         L     RENTRY,SAVTDIAG          VIRTUAL DIAGNOSE ROUTINE Y05331
         BAL   RETURN,4(,RENTRY)        NOP REAL TIC-STOP AUTOPOLY05331
         LA    RZERO,LCBCPA+56          TIC ADDRESS           Y05331
         L     RPARM,AVTSAVTP           POINTER TO SECONDARY AVT Y05331
         L     RENTRY,SAVTDIAG          VIRTUAL DIAGNOSE ROUTINE Y05331
         DROP  RPARM                                             Y05331
         MVI   LCBCPA+56,CCWNOP         NOP VIRTUAL TIC          Y05331
         BAL   RETURN,4(,RENTRY)        NOP REAL TIC,STOP AUTOPOLY05331
         L     RENTRY,AVTSAVE2+28       SARESTORE R15            Y05331
         B     OUT                                               Y05331
         SPACE
NOVIRT2  EQU   *                        NON VIRTUAL ENVIRONMENT  Y05331
         MVI   LCBCPA+56,CCWNOP           THE POLLS
         SPACE 2
OUT      EQU   *
         LR    RETURN,RTEMP             RESTORE RETURN ADDRESS
         SR    RTEMP,RTEMP              CLEAR REG TO ZERO
         IC    RTEMP,LCBUCBX            RLN-1
         SLL   RTEMP,2                  MULTIPLE BY FOUR
         L     RILIST,DCBINVLI(RTEMP)   INVITATION LIST ADDRESS
         SR    RTEMP,RTEMP              CLEAR REG TO ZERO
         IC    RTEMP,4(RILIST)
         LA    RTEMP,1(0,RTEMP)
         STC   RTEMP,4(RILIST)
         BR    RETURN                   RETURN TO DEST SCHEDULER
         EJECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *  S21101
*                                                             *  S21101
*        THIS SUBROUTINE WILL COMPUTE THE SCB ADDRESS         *  S21101
*                  FOR ANY DESTINATION QCB                    *  S21101
*                                                             *  S21101
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *  S21101
QCBRTNE  EQU   *                                                 S21101
         SR    RZERO,RZERO              CLEAR A WORK REGISTER    S22025
         SR    RSCB,RSCB                CLEAR ANOTHER            S21101
         IC    RSCB,QCBSCBOF            GET THE INDEX TO THE     S21101
*                                         RIGHT SCB              S21101
         IC    RZERO,AVTSCBSZ           SIZE OF AN SCB           S22025
         MR    RDCB,RZERO               COMPUTE THE OFFSET TO    S22025
*                                         THE SCB                S21101
         L     RDCB,LCBDCBPT            RESTORE THE DCB ADDRESS  S21101
         AL    RSCB,LCBSCBDA-1          ADD THE OFFSET TO THE    S21101
*                                         ABSOLUTE SCB ADDRESS   S21101
         BR    RPQCB                    RETURN TO THE CALLER     S21101
         EJECT
*                                                                     *
*      S Y M B O L I C    C O N S T A N T S    A N D    E Q U A T E S *
*                                                                     *
         SPACE 2
HONE     DC    H'1'                     HALFWORD OF ONE          S22025
TWELVE   DC    AL2(43200)               TWELVE-HOUR TIME DELAY
TESTRLN  DS    0H                       INSURE HALF WORD BOUNDRY S22025
         CLI   LCBUCBX,AVTEZERO         COMPARE RLN'S
AUTOPOLF EQU   X'FE'
AUTO     EQU   X'01'
STRTAUTO EQU   X'01'
EQPRI    EQU   X'02'                    DCB PRIORITY BYTE MASK
FE       EQU   X'FE'                    END-OF-ILIST INDICATOR
EOL      EQU   0                        OFFSET OF END-OF-ILIST
*                                       INDICATOR IN ILIST ENTRY
X0F      EQU   X'0F'                    LCBERBCT BIT FLIPPER
OPEN     EQU   X'10'                    OPENED DCB FLAG
OUTPUT   EQU   X'01'                    LINE OPEN FOR OUTPUT
*                                       LINE GROUP OPEN FOR OUTPUT
POLL     EQU   X'09'                    POLL COMMAND
CCWNOP   EQU   X'03'                    NO-OP COMMAND
ILISTPRF EQU   8                        SIZE OF ILIST CONTROL AREA
ZERO     EQU   0                                                 S22025
ONE      EQU   1
TWO      EQU   2                        CONSTANT                SA56606
ILSTSIZE EQU   2                        LENGTH OF AN ILIST ENTRY
THREE    EQU   3
STCBPRIT EQU   4                                                 S21101
FOUR     EQU   4                                                 S22025
SIX      EQU   6
EIGHT    EQU   8                                                 S22025
FIFTEEN  EQU   15
OUTONLY  EQU   X'03'                    LINE OPEN FOR OUTPUT ONLY
LCBDLY   EQU   X'02'
ABEND2   EQU   2                        045 ABEND SUBCODE      @XA08074
ADR      EQU   7                        MASK FOR ADDRESS STORE @Z30X8IM
         EJECT
         TPRIOR
         EJECT
         TAVTD 2
         EJECT
         TQCBD
         EJECT
         TLCBD
         EJECT
         TSCBD
         TDEBD
         DCBD  DSORG=TX
         TDISPD
         TTRMD
         TRECBD
         TSTCBD
         TPRFD
         END
