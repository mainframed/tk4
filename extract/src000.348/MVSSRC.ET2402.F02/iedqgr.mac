         TITLE 'IEDQGR - QRESET SERVICE ROUTINE'
IEDQGR   CSECT
*A130000,675300-677100,692000-694000,895000                      X02004
*D264000-273000,288000-663000,678000-684000,693000,777000-801000 X02004
*D864000-867000,897000-911000,921000-930000,936000-942000        X02004
*C690000,870000-894000                                           X02004
*A024000,138000,147000,150000,345000,831000                      Y02027
*C075000-110000,138000,147000,150000,156000,231000,240000        Y02027
*C315000-345000,675300-765300,831000,880000-890000,933000        Y02027
*D111000-116100,126000-130000,159000-168000,234000,243000        Y02027
*D261000,813000,837000-852000,874000-876000                      Y02027
***********************************************************************
* STATUS --                                                           *
*    CHANGE LEVEL 0                                                   *
*                                                                     *
* FUNCTION --                                                         *
*   THIS ROUTINE SETS UP TO GIVE CONTROL TO MCP-QRESET SUBTASK.       *
*   THE DCB ADDRESS,MAXIMUM NUMBER, AND USER FIELD ARE ACCESSED.      *
*   SVC 102 IS ISSUED WITH REGISTER 1 POINTING TO PARAMETER LIST      *
*   AND ECB ADDRESS IN THIS MODULE.                                   *
*                                                                     *
* ENTRY POINT --                                                      *
*         IEDQGR                                                      *
*                                                                     *
* INPUT --                                                            *
*    REG 1 CONTAINS A POINTER TO AN OPENED APPLICATION PROGRAM DCB    *
*    REG 2 CONTAINS A POINTER TO A FIELD WITH THE FOLLOWING FORMAT -  *
*    *  TERMINAL   *SEQUENCE*                                         *
*    *   NAME      * NUMBER *                                         *
*    ************************                                         *
*    0             7        9 BYTES                                   *
*                                                                     *
* OUTPUT --                                                           *
*   WHEN SVC 102 IS ISSUED REGISTER 1 POINTS TO PARAMETER LIST        *
*                                                                     *
*         ******************                                          *
*         *X'A4'* ECB ADDR *                                          *
*         ******************                                          *
*         * ZEROED ECB     *                                          *
*         ******************                                          *
*                                                                     *
*                                                                     *
* EXTERNAL REFERENCES --                                              *
*         IGC102 - AQCTL SVC                                          *
*                                                                     *
* EXITS,NORMAL --                                                     *
*    REG 15 CONTAINS ONE OF THE FOLLOWING RETURN CODES -              *
*    CODE      MEANING                                                *
*    X'00'     QRESET HAS BEEN EXECUTED SUCCESSFULLY                  *
*                                                                     *
* EXITS,ERROR --                                                      *
*    REG 15 CONTAINS ONE OF THE FOLLOWING RETURN CODES -              *
*    CODE      MEANING                                                *
*    X'04'     QRESET HAS BEEN UNABLE TO FIND ONE OR MORE OF THE      *
*              REQUESTED SEQUENCE NUMBERS.  REG 2 WILL CONTAIN THE    *
*              COUNT OF MESSAGES SUCCESSFULLY PROCESSED.              *
*    X'08'     REUSABLE DISK REORGANIZATION IS IN PROGRESS.  REG 2    *
*              WILL CONTAIN THE COUNT OF MESSAGES SUCCESSFULLY PRO-   *
*              CESSED BEFORE THE START OF THE REORGANIZATION.         *
*    X'0C'     REQUESTED SEQUENCE NUMBER IN ERROR                     *
*    X'10'     QRESET NOT ALLOWED FROM REQUESTING APPLICATION PROGRAM *
*    X'14'     INVALID TERMINAL NAME OR TERMINAL IS HELD              *
*    X'18'     AN ERROR EXISTS REGARDING THE TYPE OF QUEUING OR THERE *
*              ARE NO MESSAGES QUEUED FOR THE SPECIFIED TERMINAL      *
*    X'1C'     TERMINATE FUNCTION TO AVOID  POSSIBLE LOGICAL READ ERR *
*              REG 2 WILL CONTAIN THE COUNT OF MESSAGES MARKED UNSER- *
*              VICED.  HOWEVER, NO MESSAGES ARE SCHEDULED TO BE RESENT*
*                                                                     *
* TABLES/WORKAREAS --                                                 *
*   DCB                                                               *
*                                                                     *
* ATTRIBUTES --                                                       *
*    SERIALLY REUSABLE,ENABLED,PROBLEM PROGRAM MODE                   *
*                                                                     *
* CHARACTER CODE DEPENDENCY --                                        *
*    THE OPERATION OF THIS MODULE DEPENDS UPON AN INTERNAL REPRESENTA-*
*    TION OF AN EXTERNAL CHARACTER SET.  THE CODING HAS BEEN ARRANGED *
*    SO THAT REDEFINITION OF 'CHARACTER' CONSTANTS, BY REASSEMBLY,    *
*    WILL RESULT IN A CORRECT MODULE FOR THE NEW DEFINITIONS.         *
***********************************************************************
         SPACE 5
**********************************************************************
*                            REGISTER  EQUATES                        *
**********************************************************************
RZERO    EQU   0
RONE     EQU   1
RPARM2   EQU   2
RPEWA    EQU   3
RWORKA   EQU   4                        PARAMETER POINTER
RWORKB   EQU   5                        PARAMETER POINTER
RMAX     EQU   6                        MAX NUMBER SPECIFIED     Y02027
RSEQ     EQU   8                        SEQUENCE NUMBER REGISTER
RDCB     EQU   10                       DCB BASE                 Y02027
RDISP    EQU   11                       DISPATCHER BASE
RBASE    EQU   12                       MODULE BASE REGISTER
RSAVE    EQU   13                       SAVE AREA POINTER
R14      EQU   14
R15      EQU   15
***********************************************************************
         SPACE 5
         SPACE 2
         SAVE  (14,12)                  SAVE CALLLER'S REGISTERS
         LR    RBASE,R15                SET UP BASE REGISTER
         USING IEDQGR,RBASE             ESTABLISH ADDRESSABILITY
IEDQGR   IEDHJN  BEGIN
*                                                                Y02027
         XC    GRECB(FOUR),GRECB        CLEAR ECB                Y02027
         L     RDCB,ZERO(RONE)          GET DCB ADDRESS AND      Y02027
         L     RMAX,FOUR(RONE)          GET MAXIMUM NUMBER FROM  Y02027
*                                       PARAMETER REGISTER       Y02027
         LA    R15,SEQERR               SET ERROR CODE           Y02027
         CLC   SEQOFF(TWO,RPARM2),GRECB SEQUENCE NO. ZERO        Y02027
         BE    LEAVE                    BRANCH IF YES, RC='0C'   Y02027
         SPACE 2
**********************************************************************
*        BUILD AQCTL LIST TO POST SPECIAL ELEMENT TO IEDQGQ           *
**********************************************************************
         LA    RONE,QRPARM              PARAMETER LIST ADDRESS   Y02027
         AQCTL
*                                                                Y02027
*              RETURN FROM IEDQGQ VIA IEDQEB - REG 0 = # OF MSGS Y02027
*                                              REG 15=RET.CODE   Y02027
*                                                                Y02027
         LTR   RZERO,RZERO              NEED TO SET UP REG 2     Y02027
         BZ    LEAVE                    BRANCH IF NO             Y02027
         ST    RZERO,X1C(RSAVE)         STORE IN USERS REG 2     Y02027
*                                       NUMBER OF UNSERVICED MSG Y02027
LEAVE    EQU   *
         RETURN  (14,12),RC=(15)
         SPACE 5
**********************************************************************
*                  EQUATES AND CONSTANTS
**********************************************************************
         DS    0F                       PARAMETER LIST           Y02027
QRPARM   DC    X'A4'                    QRESET CODE FOR SUC 102  Y02027
         DC    AL3(GRECB)               ECB ADDRESS              Y02027
GRECB    DC    F'0'                     ECB FOR POST FROM IEDQGQ
X1C      EQU   X'1C'                    OFFSET FROM USER'S SAVE AREA
*                                       TO HIS REGISTER 2
ZERO     EQU   0                        OFFSET VALUES FOR INST USX02004
ONE      EQU   1                        *                        X02004
TWO      EQU   2                        *                        X02004
FOUR     EQU   4                        *                        X02004
SEQERR   EQU   12                       RETURN CODE              Y02027
SEQOFF   EQU   8                        OFFSET TO SEQ. NO. PARM  Y02027
**********************************************************************
*                          DESECTS                                    *
**********************************************************************
         END
