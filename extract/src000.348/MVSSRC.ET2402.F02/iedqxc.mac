 TITLE 'IEDQXC - TCAM MESSAGE QUEUE PRINT - PRIVATE MACROS            '
         MACRO
&LABEL   $PRNT &LOC,&CNT,&PRE,&POST
         LCLA  &LEN
         LCLB  &ENDIT
         LCLC  &CARR
&ENDIT   SETB  (0)
&CARR    SETC  '&PRE'
&LEN     SETA  K'&PRE
         AIF   ('&LABEL' EQ '').NOLAB
&LABEL   DS    0H                       DEFINE ENTRY LABEL
.NOLAB   AIF   ('&CARR' EQ 'SP1' OR '&CARR' EQ '').CKLOC
         AIF   (&LEN LT 3).CTLERR
         AIF   ('&CARR'(1,2) EQ 'SP' OR '&CARR'(1,2) EQ 'SK').CTL1
.CTLERR  MNOTE 2,'CARRIAGE CONTROL INVALID, &CARR IGNORED.'
         AGO   .CKLOC
.CTL1    AIF   ('&CARR'(1,2) EQ 'SP').CKSPCE
         MVI   OUTAREA,C'1'             INDICATE PAGE EJECT
         AGO   .CKLOC
.CKSPCE  AIF   (&LEN NE 3).CTLERR
         AIF   ('&CARR'(3,1) NE '2').CTL2
         MVI   OUTAREA,C'0'             INDICATE DOUBLE SPACE
         AGO   .CKLOC
.CTL2    AIF   ('&CARR'(3,1) NE '3').CTLERR
         MVI   OUTAREA,C'-'             INDICATE TRIPLE SPACE
.CKLOC   AIF   (&ENDIT).MEND
         AIF   ('&LOC' EQ '').CALLSEQ
         AIF   ('&CNT' EQ '').ONE20
         MVC   OUTAREA+1(&CNT),&LOC     MOVE DATA TO OUTPUT BUFFER
         AGO   .CALLSEQ
.ONE20   ANOP
         MVC   OUTAREA+1(120),&LOC      MOVE DATA TO OUTPUT BUFFER
.CALLSEQ ANOP
         BAL   14,PUT                   OUTPUT BUFFER
         AIF   ('&POST' EQ '').MEND
&CARR    SETC  '&POST'
&ENDIT   SETB  1
&LEN     SETA  K'&POST
         AGO   .NOLAB
.MEND    ANOP
         MEND
         EJECT
 TITLE 'IEDQXC - TCAM MESSAGE QUEUE PRINT - MODULE PROSE              '
IEDQXC   CSECT
*  CHANGE ACTIVITY AS FOLLOWS:
******************** MICROFICHE FLAGS *********************** SUPT CODE
*A155000,197000,474000,719012,725000                            SA50211
*C289000                                                         S21101
*D185000-186000,421000,427000-430000                             S21101
*A187000,204000,205000,215700,420000,422000,426000,439000,603000 S21101
*A719000,720000,765000                                           S21101
*A511000,516000                                                  A42375
*C515000,519000,530000,659000                                    A42375
*D513000-514000,517000,532000-533000                             A42375
*C242000,333000                                                  A42361
*A131000,187000,187600                                           A50187
*A439600                                                        SA56902
*A196500,180300-180600,422700-516120,547200-547600,626010-626640 S21903
*A697200-697800,722500,725200,792050-792900                      S21903
*D420016-420046,422700,603999-604266                             S21903
*C767000                                                         S21903
*A223500,317500,603999-604449                                   SA51078
*A176001,283001-283006,308001-308020,353500,604534-604537       SA59967
*A633001,689001,722001-722003                                   SA59967
*C178000,184000,215000-215300,474540,604059,606400,608800       SA59967
*C611200,613600,622000-622015,626200,695000                     SA59967
*A193300-193600                                                 SA61738
*D197300-197600                                                 SA61738
*C604089                                                        SA66943
*C530000,535000,537000,538000,658400,659000,660000-664000       SA69651
*A362000                                                       @XA05296
*D362090,362900                                                @XA08982
*C516600-543000,516200                                         @XA08072
*A116000,626640,630000                                         @XA08072
*A201000,204000,205700,215007,216400,217100,524500,539300      @OY12272
*A544000,547600,626840,633001,664000,719822,725200,765500      @OY12272
*D229000,528000                                                @OY12272
*C067000,074000                                                @OY12272
*D537600,A539300,A539500                                       @OX13503
*C242000,C333000                                               @OY19630
*572000,574000,577000                                          @OZ27790
*D592000,601000                                                @OZ27790
*D539360                                                       @OY19631
* $01=OZ40750  ETC2202  79.10.20 047252:                           @01A
* $02=OZ40789  ETC2202  79.10.20 047252:                           @02A
* $11=OZ46500  ETC2302  80.05.22 076195:                           @11A
* $21=OZ50497  ETC2402  82.01.23 937763: EXPAND REUS QUEUE SIZE    @21A
         SPACE 3
***********************************************************************
*                                                                     *
* $MOD(IEDQXC) COMP(Y@) PROD(TCAM):                                @11C
*                                                                     *
*DESCRIPTIVE-NAME=TCAM MESSAGE QUEUE PRINT ROUTINE                    *
*                                                                     *
*COPYRIGHT='NONE'                                                     *
*                                                                     *
*  STATUS:  CHANGE LEVEL 5                                            *
*                                                                     *
*FUNCTION:                                                            *
*   IEDQXC RUNS AS A JOB STEP IN THE BATCH ENVIRONMENT UNDER OS/360   *
*   AS A UTILITY PROGRAM, INDEPENDENT OF TCAM. THE MESSAGE QUEUE      *
*   DATASET PRINTOUT CAN BE DIRECTED TO A PRINTER, TAPE, OR DISK      *
*   FOR LATER PRINTOUT.                                               *
*   UPON ENTRY THE PARM FIELD OF THE EXEC CARD IS CHECKED FOR VALIDITY*
*   AND FOR THE TYPE OF OUTPUT DESIRED.                               *
*   THE TYPE OF DEVICE ON WHICH THE QUEUE RESIDES IS DETERMINED AND   *
*   THE DATASET IS OPENED.THE OUTPUT DATASET IS OPENED AND A HEADER   *
*   LINE IS WRITTEN DENOTING THE MEANING OF THE SPECIAL CHARACTERS.   *
*   NEXT THE PROPER QUEUE IS DETERMINED, AND THE NEXT HEADER LINE IS  *
*   WRITTEN.  AFTER THE RECORD IS READ THE PREFIX IS CHECKED AND      *
*   FORMATTED BEFORE PRINTING.                                        *
*   THE PROGRAM USES THE HEADER PREFIX TO CHAIN EACH MESSAGE SO THAT  *
*   THE MESSAGE WILL BE PRINTED CONTIGUOUSLY, EVEN THOUGH IT MAY BE   *
*   SPREAD OVER MORE THAN ONE VOLUME.                                 *
*                                                                     *
*ENTRY POINTS  IEDQXC IS THE ONLY ENTRY POINT                         *
*                                                                     *
*INPUT                                                                *
* 1.  INPUT PARAMETERS FROM EXEC CARD.                                *
*                                                                     *
*     Q=DMP                                                   @OY12272*
*     Q=XXXX,DMP                                              @OY12272*
*     Q=XXXX,ALL(,DU/,DS)(,FMT)                               @OY12272*
*     Q=XXXX,AAAA,BBBB,...(,DU/,DS)(,FMT)                     @OY12272*
*                                                             @OY12272*
*       WHERE - DMP= DUMP MESSAGES SEQUENTIALLY.              @OY12272*
*               ALL= LIST NUMBER OF QUEUES SPECIFIED IN XXXX  @OY12272*
*               XXXX= NUMBER OF LOGICAL QUEUES ON MESSAGE     @OY12272*
*                     QUEUE DATA SET.                         @OY12272*
*               AAAA,BBBB,...= A SERIES OF 1- TO 4-DIGIT      @OY12272*
*                     QUEUE NUMBERS, UP TO 5.                 @OY12272*
*               DU=  DUMP ONLY UNSERVICED MESSAGES IN         @OY12272*
*                    SPECIFIED QUEUES.                        @OY12272*
*               DS=  DUMP ONLY SERVICED MESSAGES IN           @OY12272*
*                    SPECIFIED QUEUES.                        @OY12272*
*               FMT= FORMAT DATFLAGS BYTE IN DATA FIELD.      @OY12272*
*                                                             @OY12272*
*       NOTE - THE PARENTHESES AROUND DU, DS, AND FMT         @OY12272*
*              INDICATE THAT THESE OPERANDS ARE OPTIONAL.     @OY12272*
*                                                             @OY12272*
* 2.  MESSAGE QUEUE DATA SET
*                                                                     *
*OUTPUT                                                               *
*   CONSISTS OF PRINTED,FORMATTED RECORDS                             *
*                                                                     *
*EXTERNAL ROUTINES  NONE                                              *
*                                                                     *
*EXITS-NORMAL WHEN THE PARMETERS FROM EXEC CARD ARE FINISHED
*                                                                     *
*EXITS-ERROR BR 14 AFTER WRITING APPROPRIATE MESSAGE ON CONSOLE       *
*          WHICH EXPLAINS THE REASON FOR TERMINATION                  *
*                                                                     *
*MESSAGES-                                                            *
*       IED119I UNABLE TO OPEN ''DDNAME''.-                           *
*               SYSTEM WAS NOT ABLE TO OPEN 'DDNAME' DATA SET.        *
*       IED122I INVALID MESSAGE CHAIN-                                *
*               CHAINING SEQUENCE ON MSG QUEUE DATA SET IS INVALID.   *
*       IED123I INVALID PARAMETERS-                                   *
*               ERROR IN EXEC CARD PARAMETERS.                        *
*       IED124I QUEUE HAS BEEN WRAPPED-                               *
*               IEDQXC CANNOT CONTINUE DUE TO A WRAPPED MESSAGE QUEUE.*
*TABLES AND WORK AREAS                                                *
*   HEADER PREFIX EXPANDED FROM 'TPRFD' MACRO                         *
*                                                                     *
*ATTRIBUTES  SERIALLY REUSEABLE  RUNS AS PROBLEM PROGRAM              *
*                                                                     *
*NOTE--THERE ARE NO REGISTER DEPENDENCIES FROM OTHER PROGRAMS.        *
*                                                                     *
***********************************************************************
R0       EQU   0
R1       EQU   1                   INPUT- EXEC PARM ADDR
RWRK1    EQU   1                   WORK REG
R2       EQU   2
RWRK2    EQU   2                   WLRK REG 2
R3       EQU   3
RWRK3    EQU   3                   WORKREG 3
RIR1     EQU   4                   INTERNAL RETURN REG 1
R5       EQU   5
RDTA     EQU   6                   ADDR OF DATA BLK BEING PROCESSED
RREC     EQU   7                   ABSOLUTE REC NUMBER REG
RPRF     EQU   8                   BASE REG FOR BFR PREFIX
RBASE1   EQU   9                        BASE REGISTER # 1
RBASE2   EQU   10                       BASE REGISTER # 2
RTOTNO   EQU   11                  USED IN IEDQFD
R13      EQU   13                                              @XA08072
R14      EQU   14
R15      EQU   15
 TITLE 'IEDQXC - TCAM MESSAGE QUEUE PRINT - INITIALIZATION            '
         USING PRFSUNIT,RPRF
         USING IEDQXC,RBASE1,RBASE2
         STM   14,12,12(13)
         LR    RBASE1,R15               SET UP BASE REGISTER
         LA    RBASE2,4095(,RBASE1)     INITIALIZE SECONDARY BASE
         LA    RBASE2,1(,RBASE2)        RBASE2=RBASE1+4096
         ST    13,SAVE+4
         LA    12,SAVE
         ST    12,8(13)
         LR    13,12
         LR    R5,R1                    SAVE PARAMETER POINTER
IEDQXC   IEDHJN BYPASS                  ID AND DATE              S22025
         OPEN  (PUTDCB,OUTPUT)
         TM    PUTDCB+48,X'10'     TEST FOR GOOD OPEN
         BNO   OPENERR             BRANCH IF NOT
         MVI   OUTBUF,X'EF'        CLEAR
         MVC   OUTBUF+1(119),OUTBUF     OUTPUT BFR
         EJECT
***********************************************************************
***                                                                 ***
***            INITIALIZE DCB'S & OPEN                              ***
***                                                                 ***
***********************************************************************
       DEVTYPE QDDNAME,INFO,DEVTAB      GET DEVICE CHARACTERISTICS
         LTR   R15,R15                  DID WE GET THE DEVTYPE
         BZ    *+8                      IF YES, READ THE JFCB
         BAL   R5,OPERR1                GO ISSUE OPEN FAIL MESSAGE
        RDJFCB RDDCB1                   READ JFCB FOR MESSAGE QUEUE
        OBTAIN QUEDSCB                  GET FORMAT 1 DSCB FROM VTOC
         LTR   R15,R15                  DID WE READ THE DSCB
         BZ    *+8                      IF YES, CONTINUE PROCESSING
         BAL   R5,OPERR1                GO ISSUE UNABLE TO OPEN MSG
         OPEN  RDDCB1,TYPE=J
         OPEN  MF=(E,DCBLIST+4)         OPEN REST OF MESSAGE QUEUE
         LR    R1,R5                    RESTORE PARAMETER POINTER
         BAL   RIR1,QFORMAT             GO CHECK PARAMETER LIST
RETURN   EQU   *                                                SA50211
         LH    R1,INFO+10          GET TRKS/CYL
         ST    R1,TRCYL
         M     R1-1,CYLVOL         TRK/CYL * CYL/VOL
         M     R1-1,NOVOL           * # VOLS
         LR    R2,R1               MOVE TO R2 FOR DIVIDE
         L     R1,TOTNO
         DR    R1-1,R2             TOTNO/TRCYL*CYLVOL*NOVOL
         M     R1-1,NOVOL          RCTRK*NOVOL
         ST    R1,VOLRT
         LA    RWRK1,0             CLEAR REG
         L     RWRK2,DCB
         IC    RWRK1,16(RWRK2)
         LA    RWRK1,6(RWRK1)      ADD DATA SIZE
         ST    RWRK1,UNIT          SAVE UNIT SIZE
         OI    RR1BIT,ON                SET SO RRN WILL NOT        @21A
*                                        BE CONVERTED              @21A
         LA    RREC,ONEE                SET TO READ FIRST RCD      @21A
         BAL   RIR1,FEDQFP              GO READ FIRST RECORD       @21A
         NI    RR1BIT,OFF               RESET SWITCH               @21A
         L     RIR1,UNIT                GET LENGTH OF RECORD       @21A
         S     RIR1,SIX                 BACK UP TO DATA FIELD      @21A
         AR    RIR1,RPRF                GET POINTER TO DATA        @21A
         USING IEDQDATA,RIR1            SET IEDQDATA BASE          @21A
         TM    DATFLAGS,DATNPRFX        DOES UNIT HAVE PREFIX      @21A
         BZ    CKZERO                   YES, CHECK FOR ZERO        @21A
*                                        RECORD                    @21A
         TM    DATFEFO+TWOO,CPBREUS     IS THIS A REUS QUEUE       @21A
         BO    SETREUS                  YES, SET REUS SWITCH       @21A
         B     NONREUS                  NO, NONREUSABLE QUEUE      @21A
CKZERO   EQU   *                                                   @21A
         OC    PRFSUNIT(30),PRFSUNIT    IS IT FORMATTED RCD        @21A
         BNZ   CKCRCD                   NO, CHECK RECORD ADDR      @21A
*                                        IN PRFCRCD                @21A
         TM    DATFLAGS,DATIFEFO        IS IT REUS QUEUE           @21A
* FIRST RECORD ON REUSABLE QUEUE WILL HAVE DATIFEFO SET ON         @21A
         BO    SETREUS                  YES, SET REUS SWITCH       @21A
         B     NONREUS                  NO, NONREUSABLE QUEUE      @21A
CKCRCD   EQU   *                                                   @21A
         TM    PRFCRCD+TWOO,CPBREUS     IS IT REUS NUMBER          @21A
         BZ    NONREUS                  NO, NONREUSABLE QUEUE      @21A
SETREUS  EQU   *                                                   @21A
         OI    CTRL,REUS                SET REUS QUEUE SWITCH      @21A
NONREUS  EQU   *                                                   @21A
         DROP  RIR1                     RELEASE BASE REG           @21A
PRINT    EQU   *                                                 S21903
         $PRNT SPECIAL,50,SKIP,SP3 NOTE SPECIAL CHARACTERS
 TITLE 'IEDQXC - TCAM MESSAGE QUEUE PRINT - FORMAT ROUTINES           '
***********************************************************************
***            FIND NEXT QUEUE TO BE PRINTED                        ***
***                                                                 ***
***********************************************************************
GETNEXT  L     RWRK1,NEXTQUE       GET ADDR NEXT QUEUE
         NI    CTRL,REUS+ALLQUES+DUMP   TURN OFF BITS           SA59967
         OC    0(4,RWRK1),0(RWRK1) IS THIS END OF REQUESTS      PTR1898
         BZ    ENDED                    YES,EXIT                SA59967
         L     RREC,QUESTART       GET ADDR
         LA    RREC,1(RREC)        NEXT QUEUE
         TM    SCANBYTE,ON              IS PARTIAL DUMP REQ      S21903
         BO    WTORTNE                  BRANCH IF YES            S21903
         TM    CTRL,DUMP           DUMP REQUESTED ?
         BO    DUMPQUE             YES
         C     RREC,NUMQUES        ALL QUEUES CHECKED ?
         BH    ENDED                    YES                     SA59967
GETALL   ST    RREC,QUESTART       QUEUE START ADDR
         TM    CTRL,ALLQUES             'ALL' SPECIFIED ?        A50187
         BO    ALLQ                     YES, BRANCH              A50187
         MVC   RR1RECD,0(RWRK1)         INITIALIZE TO ABSOLUTE   A50187
*                                       RCD # OF QUE TO BE DMPED A50187
         B     GETEND                                            A50187
ALLQ     EQU   *                                                 A50187
         ST    RREC,RR1RECD             INITIALIZE THE ABSOLUTE  S21101
*                                         RECORD NUMBER          S21101
GETEND   EQU   *                                                 A50187
         EJECT
***********************************************************************
***            WRITE 'FIRST MSG IN QUEUE'                           ***
***                                                                 ***
***********************************************************************
         MVI   OUTAREA,C'1'        INDICATE NEW PAGE
         OI    FRSTRCD,ON               INDICATE 1ST RECORD FOR SA61738
*                                       AN INDIVIDUAL QUEUE DMP SA61738
         TM    CTRL,ALLQUES        IS 'ALL' SPECIFIED
         BO    USESTART            YES
         L     RWRK1,NEXTQUE       GET NEXT QUEUE
         L     RWRK1,0(RWRK1)
         BC    15,CONV
USESTART L     RWRK1,QUESTART      GET START QUEUE
CONV     CVD   RWRK1,WORKDBL       CONVERT
         LR    RREC,RWRK1          INITIALIZE RREC
         TM    CTRL2,DUSW+DSSW          DUMP ONLY SERV OR UNSRV@OY12272
         BNZ   DSDU                     YES                    @OY12272
         UNPK  FRSTMSG+35(6),WORKDBL+4(4)
         OI    FRSTMSG+40,X'F0'
         $PRNT FRSTMSG,50               PRINT FIRST RECORD MESSAGE
RDREC    EQU   *                                               @OY12272
         OI    RR1BIT,ON                INDICATE THAT THE RECORD S21101
*                                         NUMBER IS TO BE        S21101
*                                         TREATED AS A DISK      S21101
*                                         RECORD NUMBER          S21101
         BAL   RIR1,FEDQFP         GO READ
         NI    RR1BIT,OFF               TURN OFF THE FIRST-      S21101
*                                         RECORD-IN-THE-QUEUE    S21101
*                                         BIT                    S21101
         B     WRFSTHDR                 GO WRITE FIRST HEADER  @OY12272
         SPACE 2                                               @OY12272
DSDU     EQU   *                                               @OY12272
         UNPK  DSDUMSG+FORTYONE(SIXX),WORKDBL+FOURR(FOURR)     @OY12272
         OI    DSDUMSG+FORTYSIX,DIGIT                          @OY12272
         TM    CTRL2,DUSW               DUMP UNSERV MSG?       @OY12272
         MVC   DSDUMSG+ELEVEN(TWOO),DUMSG   ASSUME YES         @OY12272
         BO    PRTMSG                   PRINT MESSAGE          @OY12272
         MVC   DSDUMSG+ELEVEN(TWOO),DSMSG  PRINT SERV MSG      @OY12272
PRTMSG   EQU   *                                               @OY12272
         $PRNT DSDUMSG,58                                      @OY12272
         B     RDREC                    GO READ RECORD         @OY12272
         EJECT
***********************************************************************
***            FORMAT & WRITE FIRST HEADER PREFIX                   ***
***********************************************************************
WRFSTHDR OC    PRFSUNIT(30),PRFSUNIT IS THIS END OF QUEUE       PTR1898
         BZ    EMPTYFST            IF YES, GO INDICATE END      PTR1898
         TM    PRFSTAT1,PRFNHDRN        HEADER                  SA59967
         BO    CHAINER1                 NO,ERROR                SA59967
         SR    RIR1,RIR1                CLEAR                   SA59967
         L     RIR1,UNIT                LENGTH OF UNIT          SA59967
         S     RIR1,SIX                 DECREMENT KEYLENGTH     SA59967
         AR    RIR1,RPRF                POINT TO DATA FLAGS     SA59967
         TM    ZERO(RIR1),ALLQUES       XTRA                    SA59967
         BO    CHAINER1                 YES,ERROR               SA59967
         MVC   NHDR+ONEE(THREE),PRFNHDR  NEXT HEADER           @OY12272
         TM    CTRL2,DUSW               DUMP UNSERV ONLY?      @OY12272
         BNO   HDR1                     NO                     @OY12272
         TM    ZERO(RIR1),DATSENT+DATCNCLD  MSG SERVICED OR        @01C
*                                           CANCELLED              @01A
         BNZ   MSGCHAIN                 YES, DO NOT PRINT          @01C
         SPACE 1
HDR1     EQU   *                                               @OY12272
         TM    CTRL2,DSSW               DUMP SERV ONLY?        @OY12272
         BNO   HDR2                     NO                     @OY12272
         TM    ZERO(RIR1),DATSENT       MSG SERVICED?          @OY12272
         BNO   MSGCHAIN                 NO, DO NOT PRINT       @OY12272
         SPACE 1
HDR2     EQU   *                                               @OY12272
         MVC   FORMATM(3),HDR+3    INDICATE HEADER              PTR1898
         MVC   RECORD(4),PRFCRCD-1      MOVE THE TCAM RECORD     S21101
*                                         NUMBER TO A FULL WORD  S21101
         UNPK  HEXRCD(7),RECORD+1(4)    UNPACK THE NUMBER        S21101
         TR    HEXRCD(6),HEXTABLE       TRANSLATE THE RECORD     S21101
*                                         NUMBER
         MVC   FORMATM+3(6),HEXRCD GET HEX RECORD NUMBER        PTR1898
         TM    CTRL2,FMTSW              FORMAT DATFLAG FIELD   @OY12272
         BZ    HDR7                     NO                     @OY12272
         MVC   OUTBUF(ELEVEN),FORMATM   GET HEX RECORD FOR FMT @OY12272
         LA    RWRK1,OUTBUF             GET ADDR OF OUTPUT AREA@OY12272
         LA    RWRK1,ELEVEN(RWRK1)      INCREMENT              @OY12272
         MVC   ZERO(THREE,RWRK1),DSDUMSG  GET 3 ASTERISKS      @OY12272
         LA    RWRK1,FOURR(RWRK1)       INCREMENT              @OY12272
         TM    ZERO(RIR1),DATSENT       MSG MARKED SERVICED?   @OY12272
         BNO   HDR3                     NO                     @OY12272
         MVC   ZERO(NINE,RWRK1),SERV    GET SERVICE MSG        @OY12272
         LA    RWRK1,NINE(RWRK1)        INCREMENT POINTER      @OY12272
HDR3     EQU   *                                               @OY12272
         TM    ZERO(RIR1),DATCNCLD      MSG CANCELED?          @OY12272
         BNO   HDR4                     NO                     @OY12272
         MVC   ZERO(NINE,RWRK1),CANCLD  GET CANCELED MSG       @OY12272
         LA    RWRK1,NINE(,RWRK1)       INCREMENT POINTER      @OY12272
HDR4     EQU   *                                               @OY12272
         TM    ZERO(RIR1),DATIFEFO      INIT MSG ON PRI QCB    @OY12272
         BNO   HDR5                     NO                     @OY12272
         MVC   ZERO(NINE,RWRK1),INITFEFO  GET INIT FEFO MSG    @OY12272
         LA    RWRK1,NINE(,RWRK1)       INCREMENT POINTER      @OY12272
HDR5     EQU   *                                               @OY12272
         TM    ZERO(RIR1),DATINITL      MSG LOST?              @OY12272
         BNO   HDR6                     NO                     @OY12272
         MVC   ZERO(TWELVE,RWRK1),MSGLOST  INITIATE MSG LOST   @OY12272
         LA    RWRK1,TWELVE(RWRK1)      INCREMENT POINTER      @OY12272
HDR6     EQU   *                                               @OY12272
         MVC   ZERO(THREE,RWRK1),DSDUMSG  PUT IN LAST 3 *'S    @OY12272
         $PRNT OUTBUF,60,SP3                                   @OY12272
         MVI   FORMATM,BLNK             BLANK OUT AREA         @OY12272
         MVC   FORMATM+ONEE(TEN),FORMATM                       @OY12272
         $PRNT FORMATM,93                                      @OY12272
         B     HDR8                                            @OY12272
HDR7     EQU   *                                               @OY12272
         $PRNT FORMATM,93,SP3      INDICATE FIELD DEFINITION    PTR1898
HDR8     EQU   *                                               @OY12272
         TM    PRFSTAT1,PRFNLSTN   ONLY ONE BUFFER ?
         BO    SAVENTXT            NO
         XC    NTXT(4),NTXT        IF YES, CLEAR POINTER        PTR1898
         B     SAVEXTRA
SAVENTXT MVC   NTXT+1(3),PRFNTXT   SAVE PREFIX
SAVEXTRA MVC   XTRA+1(3),PRFXTRA   CHAIN
         BAL   RIR1,SETCNT              GO SET UNIT CNT         SA51078
         MVC   XTRA(1),PRFNBUNT    (SAVE NO OF UNITS)
         SR    RWRK1,RWRK1         CLEAR RWRK1
         IC    RWRK1,XTRA          ADJUST EXTRA FOR
         S     RWRK1,ONE           NUMBER OF TIMES
         STC   RWRK1,XTRA          THRU UNCHAIN
         TM    PRFSTAT1,PRFNHDRN   IS THIS FIRST HEADER         PTR1898
         BNZ   WRAPERR             NO- ERROR
         UNPK  OUTBUF+11(9),PRFNBUNT(5) FORMAT 1ST HEADER
         MVC   OUTBUF+19(2),BLANK
         UNPK  OUTBUF+21(5),PRFSRCE(3)
         MVC   OUTBUF+25(2),BLANK
         UNPK  OUTBUF+27(5),PRFSIZE(3)
         MVC   OUTBUF+31(2),BLANK
         UNPK  OUTBUF+33(3),PRFSTAT1(2)
         MVC   OUTBUF+35(2),BLANK
         UNPK  OUTBUF+37(7),PRFXTRA(4)
         MVC   OUTBUF+43(2),BLANK
         UNPK  OUTBUF+45(5),PRFSCAN(3)  SCAN POINTER TO AREA   @OY19630
         MVC   OUTBUF+49(2),BLANK
         UNPK  OUTBUF+51(7),PRFNTXT(4)
         MVC   OUTBUF+57(2),BLANK
         UNPK  OUTBUF+59(7),PRFCRCD(4)
         MVC   OUTBUF+65(2),BLANK
         UNPK  OUTBUF+67(7),PRFNHDR(4)
         MVC   OUTBUF+73(2),BLANK
         UNPK  OUTBUF+75(7),PRFHQBCK(4)
         MVC   OUTBUF+81(2),BLANK
         UNPK  OUTBUF+83(5),PRFISEQ(3)
         MVC   OUTBUF+87(2),BLANK
         UNPK  OUTBUF+89(5),PRFDEST(3)
         MVC   OUTBUF+93(10),BLANK
         MVC   OUTBUF+103(10),BLANK
         MVC   OUTBUF+113(6),BLANK
         TR    OUTBUF(120),HEXTABLE XLATE TO PRINTABLE HEX      PTR1898
         $PRNT OUTBUF                   PRINT FILLED BUFFER
         LA    RDTA,PRFDEST+2
         BAL   RIR1,FMTDATA
         B     UNTCHAIN
EMPTYFST MVC   OUTBUF+3(6),HEXRCD  INDICATE RECORD NUMBER       PTR1898
         MVC   OUTBUF+11(20),BLNKMSG
         $PRNT OUTBUF                   PRINT FILLED BUFFER
         TM    CTRL,ALLQUES        ALL SPECIFIED ?
         BO    GETNEXT             YES
         L     RWRK1,NEXTQUE       GET NEXT QUEUE
         LA    RWRK1,4(RWRK1)      BUMP TO NEXT REQ
         ST    RWRK1,NEXTQUE       SAVE REQ &
         B     GETNEXT
         EJECT
***********************************************************************
*                                                                     *
*              UNIT CHAIN   FIND NEXT RECORD UNIT                     *
*                                                                     *
***********************************************************************
UNTCHAIN L     RREC,XTRA           GET REC NO OF NEXT UNIT
         LA    RWRK1,0
         IC    RWRK1,XTRA
         LTR   RWRK1,RWRK1         NOW ZERO
         BZ    BFRCHAIN            YES
         BAL   RIR1,FEDQFP         GO READ
         SR    RIR1,RIR1                CLEAR                   SA59967
         L     RIR1,UNIT                POINT TO END OF UNIT    SA59967
         S     RIR1,SIX                 POINT TO DATA FLAGS     SA59967
         AR    RIR1,RPRF           ADD UNIT PTR                 SA59967
         TM    ZERO(RIR1),ALLQUES       XTRA                    SA59967
         BNO   CHAINER2                 NO,ERROR                SA59967
         MVC   OUTBUF+3(6),HEXRCD  INDICATE RECORD NUMBER       PTR1898
         LR    RDTA,RPRF           SET ADDR OF DATA
         BAL   RIR1,FMTDATA        GO FORMAT + WRITE DATA
         L     RREC,XTRA
         LA    RWRK1,TWOO               SET INCREMENT OF TWO       @21C
         TM    CTRL,REUS                IS IT REUSABLE QUEUE       @21C
         BO    ADDTWO1                  YES, ADD 2 FOR REUS        @21C
         LA    RWRK1,FOURR              NONREUS QUEUE, ADD 4       @21C
ADDTWO1  EQU   *                                                   @21C
         AR    RREC,RWRK1               BUMP TO NEXT UNIT ADDR     @21C
         LA    RWRK1,0             DECREMENT
         IC    RWRK1,XTRA          UNIT COUNT
         S     RWRK1,ONE
         ST    RREC,XTRA           SAVE
         STC   RWRK1,XTRA          SAVE UNIT COUNT
         B     UNTCHAIN
         EJECT
***********************************************************************
*                                                                     *
*              FIND NEXT BUFFER IN THE MESSAGE                        *
*                                                                     *
***********************************************************************
BFRCHAIN L     RREC,NTXT           GET ADDR NEXT BFR
         LTR   RREC,RREC           END OF BFR CHAIN ?
         BZ    MSGCHAIN            YES- GET  NEXT MSG
         BAL   RIR1,FEDQFP         GO READ
         OC    PRFSUNIT(22),PRFSUNIT IS THIS END OF BFR CHAIN   PTR1898
         BZ    NXTMSG              IF YES, GO GET NEXT HEADER   PTR1898
         TM    PRFSTAT1,PRFNHDRN        HEADER                  SA59967
         BZ    CHAINER2                 YES,ERROR               SA59967
         SR    RIR1,RIR1                CLEAR                   SA59967
         L     RIR1,UNIT                POINT TO END OF UNIT    SA59967
         S     RIR1,SIX                 POINT TO DATA FLAGS     SA59967
         AR    RIR1,RPRF           ADD UNIT PTR                 SA59967
         TM    ZERO(RIR1),ALLQUES       XTRA                    SA59967
         BO    CHAINER2                 YES ,ERROR              SA59967
         L     RIR1,PRFCHDR             HDR POINTER             SA59967
         SRL   RIR1,8                   MOVE OUT STAT FIELD     SA59967
         LA    RIR1,ZERO(RIR1)          CLEAR BYTE              SA59967
         TM    CTRL,SUBSTEXT            SUBSEQUENT TEXT RECORD  SA59967
         BZ    SKIPLCB                  NO,DONT COMPARE LCB     SA59967
         C     RIR1,RDSAVE+52                                   SA59967
         BNE   CHAINER2                 NO,ERROR                SA59967
         B     DONTSET                  CONTINUE PROCESSING RCD SA59967
SKIPLCB  EQU   *                                                SA59967
         ST    RIR1,RDSAVE+52           STORE LCB FOR NEXT PASS SA59967
         OI    CTRL,SUBSTEXT            SUBSEQUENT TEXT RECORD  SA59967
DONTSET  EQU   *                                                SA59967
         MVC   FORMATM(3),BFR+3    INDICATE BUFFER              PTR1898
         MVC   FORMATM+3(6),HEXRCD GET HEX RECORD NUMBER        PTR1898
         $PRNT FORMATM,73,SP2      PRINT PARTIAL HEADER         PTR1898
         TM    PRFSTAT1,PRFNLSTN   ONLY ONE BUFFER ?
         BO    SVNTXT              NO
         XC    NTXT(4),NTXT        IF YES, CLEAR WORK POINTER   PTR1898
         B     SVXTRA
SVNTXT   MVC   NTXT+1(3),PRFNTXT   SAVE PREFIX
SVXTRA   MVC   XTRA+1(3),PRFXTRA    CHAIN POINTERS
         BAL   RIR1,SETCNT              GO SET UNIT CNT         SA51078
         MVC   XTRA(1),PRFNBUNT    (SAVE NO OF UNITS)
         SR    RWRK1,RWRK1         CLEAR RWRK1
         IC    RWRK1,XTRA          ADJUST EXTRA FOR
         S     RWRK1,ONE           NUMBER OF TIMES
         STC   RWRK1,XTRA          THRU UNCHAIN
         UNPK  OUTBUF+11(9),PRFNBUNT(5) FORMAT BUFFER HEADER
         MVC   OUTBUF+19(2),BLANK
         UNPK  OUTBUF+21(5),PRFSRCE(3)
         MVC   OUTBUF+25(2),BLANK
         UNPK  OUTBUF+27(5),PRFSIZE(3)
         MVC   OUTBUF+31(2),BLANK
         UNPK  OUTBUF+33(3),PRFSTAT1(2)
         MVC   OUTBUF+35(2),BLANK
         UNPK  OUTBUF+37(7),PRFXTRA(4)
         MVC   OUTBUF+43(2),BLANK
         UNPK  OUTBUF+45(5),PRFSCAN(3)  SCAN POINTER TO AREA   @OY19630
         MVC   OUTBUF+49(2),BLANK
         UNPK  OUTBUF+51(7),PRFNTXT(4)
         MVC   OUTBUF+57(2),BLANK
         UNPK  OUTBUF+59(7),PRFCRCD(4)
         MVC   OUTBUF+65(2),BLANK
         UNPK  OUTBUF+67(7),PRFCHDR(4)
         MVC   OUTBUF+73(2),BLANK
*
*              OUTBUF  HAS  75  BYTES  OF  DATA
*
         TR    OUTBUF(75),HEXTABLE XLATE TO PRINTABLE HEX       PTR1898
         $PRNT OUTBUF                   PRINT FILLED BUFFER
         LA    RDTA,PRFCHDR+3
         BAL   RIR1,FMTDATA        GO FORMAT AND WRITE DATA
         B     UNTCHAIN
NXTMSG   MVC   OUTBUF+3(6),HEXRCD  MOVE RECORD NUMBER TO PRINT  PTR1898
         MVC   OUTBUF+11(20),BLNKMSG
         $PRNT OUTBUF                   PRINT FILLED BUFFER
MSGCHAIN L     RREC,NHDR           GET ADDR NEXT HDR
         NI    CTRL,REUS+ALLQUES+DUMP   INDICATE NO SUBSEQUENTTXSA59967
         BAL   RIR1,FEDQFP         GO READ
         B     WRFSTHDR            GO WRITE IT
         EJECT
***********************************************************************
*              FORMAT AND WRITE OUT RECORD DATA                       *
*                                                                     *
***********************************************************************
FMTDATA  A     RPRF,UNIT           GET ADDR END OF BFR
         ST    RIR1,WORKDBL        SAVE RETURN REG
DATALOOP LR    RWRK1,RPRF
         SR    RWRK1,RDTA          AMOUNT OF BFR LEFT
         C     RWRK1,BLOCK         LESS THAN 32
         BL    FMTLAST             YES- USE RWRK1 FOR MOVE
         LA    RWRK1,32            SET DATA COUNT TO 32
         MVI   OUTBUF+86,C'*'      SET EBCIDIC INDICATOR
         MVC   OUTBUF+87(32),0(RDTA)    MOVE DATA TO EBCIDIC
         MVI   OUTBUF+119,C'*'     AREA & SET END INDICATOR
         LA    RDTA,32(RDTA)
XLATE    ST    RWRK1,WORKDBL+4     SAVE ADJUSTED DATA COUNT
         LR    RWRK2,RWRK1         GET DATA COUNT
         AR    RWRK2,RWRK2         DOUBLE FOR UNPK
         S     RWRK2,ONE           BUMP FOR UNPK
UNPKLOOP IC    RWRK3,OUTBUF+86(RWRK1)   GET CHAR
         STC   RWRK3,XWK(RWRK2)    STORE RIGHT HALF
         BCTR  RWRK2,0             DECREMENT WORK COUNT
         SRL   RWRK3,4             SHIFT TO LEFT HALF
         STC   RWRK3,XWK(RWRK2)    STORE LEFT HALF
         BCTR  RWRK2,0             DECREMENT WORK COUNT
         BCT   RWRK1,UNPKLOOP      DECREMENT DATA COUNT, DO SOME MORE
         L     RWRK1,WORKDBL+4     RESTORE DATA COUNT
         AR    RWRK1,RWRK1         DOUBLE FOR ADDING ZONES
         S     RWRK1,TWO           ADJUST FOR MOVE ZONES
         OI    XWK,X'F0'           MOVE ZONE BITS
         EX    RWRK1,MOVEZON       INTO WORK AREA
         MVC   OUTBUF+11(8),XWK    MOVE CHARS TO OUTPUT AREA
         MVC   OUTBUF+20(8),XWK+8
         MVC   OUTBUF+29(8),XWK+16
         MVC   OUTBUF+38(8),XWK+24
         MVC   OUTBUF+48(8),XWK+32
         MVC   OUTBUF+57(8),XWK+40
         MVC   OUTBUF+66(8),XWK+48
         MVC   OUTBUF+75(8),XWK+56
         TR    OUTBUF+11(75),HEXTABLE XLATE TO PRINTABLE HEX    PTR1898
         TR    OUTBUF+87(32),EBCTABLE
         $PRNT OUTBUF                   PRINT FILLED BUFFER
         B     DATALOOP
FMTLAST  LTR   RWRK1,RWRK1         FINISHED
         BZ    DONEDATA            YES
         MVI   OUTBUF+87,C' '      BLANK OUT
         MVC   OUTBUF+88(31),OUTBUF+87  AREA
         MVI   XWK,X'EF'           CLEAR WORK AREA
         MVC   XWK+1(63),XWK       TO X'EF' BLANKS
         LR    RWRK2,RWRK1         SUB ONE
         S     RWRK2,ONE           FOR MVC
         EX    RWRK2,LASTDATA      MOVE LAST DATA
         MVI   OUTBUF+86,C'*'      SET BEGIN +
         MVI   OUTBUF+119,C'*'     END INDICATOR
         LR    RPRF,RDTA           SET DATA=END
         B     XLATE
DONEDATA L     RIR1,WORKDBL
         BR    RIR1
         EJECT
***********************************************************************
*                                                                     *
*              DUMP MSGQUE SEQUENTIALY                                *
*                                                                     *
***********************************************************************
DUMPQUE  EQU   *                                                 S21101
         OI    RR1BIT,ON                INDICATE THE ABSOLUTE    S21101
*                                         RECORD NUMBER IS TO BE S21101
*                                         USED, NOT THE TCAM     S21101
*                                         RECORD NUMBER          S21101
         ST    RREC,RR1RECD             SAVE THE FIRST RECORD    S21101
*                                         NUMBER                 S21101
DUMPLOOP BAL   RIR1,FEDQFP         GO READ
         OC    PRFSUNIT(30),PRFSUNIT    IS THIS A ZERO RECORD    S21101
         BZ    BALINCR                  YES, BRANCH              S21101
         L     RREC,UNIT                GET UNIT SIZE            S21903
         SH    RREC,DATSIZE             DECR BY DATA SIZE        S21903
         AR    RREC,RPRF                POINT TO DATA FIELD      S21903
         USING IEDQDATA,RREC            DATA FIELD BASE          S21903
         MVC   RECORD(4),PRFCRCD-1      SET RCD NO THIS RCD      S21903
         TM    DATFLAGS,DATNPRFX        DOES RCD HAVE PREFIX     S21903
         BZ    YESPRFX1                 YES-CHECK FOR ZERO REC     @11C
         MVC   RECORD(4),DATFEFO-1      SET RCD NO THIS RCD      S21903
         B     YESPRFX                  PROCESS RECORD             @11A
YESPRFX1 EQU   *                                                   @11A
         OC    PRFSUNIT(30),PRFSUNIT    IS THIS A ZERO RECORD      @11A
         BZ    BALINCR                  YES-BRANCH                 @11A
         DROP  RREC
YESPRFX  EQU   *                                                 S21903
         UNPK  HEXRCD(7),RECORD+1(4)    UNPACK THE NO            S21903
         TR    HEXRCD(6),HEXTABLE       TRANSLATE FOR PRINT      S21903
         TM    SCANBYTE,ON              IS SCAN TO BE DONE       S21903
         BZ    MOVERECD                 BRANCH IF NO             S21903
         CLC   RECORD+1(3),PACKFLD1     IS THIS RECORD INCLUDED  S21903
*                                       IN THE RANGE             S21903
         BL    BALINCR                  BRANCH IF NO             S21903
         CLC   RECORD+1(3),PACKFLD2     DOES THIS RECORD EXCEED  S21903
*                                       THE SPECIFIED RANGE      S21903
         BH    WTORTNE2                 BRANCH IF YES            S21903
MOVERECD EQU   *                                                 S21903
         MVC   OUTBUF+3(6),HEXRCD  INDICATE RECORD NUMBER       PTR1898
         MVI   OUTAREA,C'0'        INDICATE DOUBLE SPACE        PTR1898
         LR    RDTA,RPRF           SET ADDR OF DATA
         BAL   RIR1,FMTDATA        GO FORMAT DATA + WRITE
BALINCR  EQU   *                                                 S21101
         BAL   RIR1,INCRECNT            INCREMENT THE COUNT      S21101
         SPACE 2
         L     RREC,RECORD              GET THE PREVIOUS TCAM    S21101
*                                         RECORD NUMBER          S21101
         LA    RIR1,TWOO                SET INCREMENT OF TWO       @21C
         TM    CTRL,REUS                IS IT REUSABLE QUEUE       @21C
         BO    ADDTWO2                  YES, ADD 2 FOR REUS        @21C
         LA    RIR1,FOURR               NONREUS QUEUE, ADD 4       @21C
ADDTWO2  EQU   *                                                   @21C
         AR    RREC,RIR1                BUMP TO NEXT UNIT ADDR     @21C
         B     DUMPLOOP            LOOP
 TITLE 'IEDQXC - TCAM MESSAGE QUEUE PRINT - READ MESSAGE QUEUE ROUTINE'
***********************************************************************
***            COMPUTE MBBCCHHR USING IEDQFP RTNE                   ***
***            FROM IGG019RC. THEN READ THE RECORD.                 ***
***                                                                 ***
***********************************************************************
FEDQFP   STM   0,12,RDSAVE
         ST    RREC,RECORD
         L     RREC,RR1RECD             RESET THE ABSOLUTE       S21101
*                                         RECORD NUMBER          S21101
         TM    RR1BIT,ON                IS THE FIRST-RECORD-IN-  S21101
*                                         THE-QUEUE BIT ON       S21101
         BO    UNPACKIT                 YES, DO NOT ADJUST THE   S21101
*                                         RECORD NUMBER          S21101
         L     RREC,RECORD              GET THE TCAM RECORD      S21101
*                                        NUMBER OF NEXT UNIT       @21C
         TM    CTRL,REUS                IS IT REUSABLE QUEUE       @21C
         BZ    NREUS                    NO, NONREUSABLE            @21C
         LA    RREC,ONEE(,RREC)         BUMP NUMBER BY ONE         @21C
         SRL   RREC,ONEE                DIVIDE BY TWO              @21C
         B     UNPACKIT                 SKIP NONREUS CODE          @21C
NREUS    EQU   *                                                   @21C
         LA    RREC,THREE(,RREC)        BUMP THE RECORD NUMBER   S21101
*                                         BY THREE               S21101
         SRL   RREC,FOUR                DIVIDE BY FOUR           S21101
UNPACKIT EQU   *                                                 S21101
         UNPK  HEXRCD(7),RECORD+1(4) GET REC NUMBER TO HEX      PTR1898
         TR    HEXRCD(6),HEXTABLE  TRANSLATE TO PRINTABLE HEX   PTR1898
         LA    R1,0(RREC)          CLEAR HI ORD BYTE
         SR    R0,R0               CLEAR EVEN REG
         L     RTOTNO,TOTNO        LOAD TOTAL # REC
         DR    R1-1,RTOTNO         REC#/TOTAL REC
         SRDA  R0,32               REMAINDER TO ODD RED
         D     R1-1,VOLRT               REL REC/(#VOLS * REC/TRK)
         LR    R3,R1               GET REL TRK
         SR    R2,R2               CLEAR EVEN REG
         D     R3-1,TRCYL               DIVIDE BY #TRK/CYL
         SRDA  R0,32               CLEAR EVEN REG
         D     R1-1,RCTRK               REL REC/REC PER TRACK
         XR    R1,R0
         XR    R0,R1
         XR    R1,R0
         SPACE  3
***            R0=M, BB=00, R2=HH, R1=R, R3= REL CYL
         SPACE  3
         LR    RTOTNO,R0           GET PROPER DCB
         SLL   RTOTNO,2                 MULTIPLY BY 4
         L     RTOTNO,DCB(RTOTNO)
         TM    48(RTOTNO),X'10'
         BZ    CHAINERR
         XC    6(2,RTOTNO),6(RTOTNO) ZERO BB                    PTR1898
         STC   R2,11(RTOTNO)       STORE
         SRL   R2,8                HH
         STC   R2,10(RTOTNO)
         STC   R1,12(RTOTNO)       STORE R
         L     R2,44(RTOTNO)       GET DEB ADDRESS
         LA    R2,32(R2)           GET DEB EXTENT
         AH    R3,6(R2)            GET ACTUAL CYL ADDR
         STC   R3,9(RTOTNO)        STORE
         SRL   R3,8                CC
         STC   R3,8(RTOTNO)
         NOTE  (RTOTNO)                 ISSUE NOTE TO LOCATE       @02A
*                                       BLOCK                      @02A
         ST    R1,POINTARG              STORE ARG IN TTRZ          @02A
         MVI   Z,ONEE                   SET SEARCH FOR NEXT        @02A
         LA    R0,POINTARG              SET PARAMETER LIST         @02A
         POINT (RTOTNO),(R0)            ISSUE POINT TO POSITION    @02A
*                                       BLOCK                      @02A
         READ  RDECB,SF,(RTOTNO),INBUF,'S'
         CHECK  RDECB
         TM    FRSTRCD,ON               FIRST RCD FOR INDIVDUAL SA50211
*                                       QUEUE DUMP?             SA50211
         BNO   NOCHECK                  NO,BYPASS LOGICAL READ  SA50211
*                                       ERROR CHECK             SA50211
         NI    FRSTRCD,ZERO             TURN SWITCH OFF         SA50211
         OC    INBUF(30),INBUF          END OF QUEUE?           SA50211
         BZ    NOCHECK                  YES,DON'T CHECK FOR     SA50211
*                                       LOGICAL READ ERROR      SA50211
         SR    R2,R2                    CLEAR REG TWO           SA50211
         L     R3,INBUF+16              LOAD RCD # IN BUFFER    SA50211
         N     R3,X0FFF                 CLEAR HIGH ORDER BYTE   SA50211
         LA    R1,TWOO                  SET TO DIVIDE BY TWO       @21C
         LA    R3,ONEE(,R3)             INCREMENT NUMBER BY 1      @21C
         TM    CTRL,REUS                IS IT REUSABLE QUEUE       @21C
         BO    REUSQ                    YES, DIVIDE BY TWO         @21C
         LA    R1,FOURR                 SET TO DIVIDE BY FOUR      @21C
         LA    R3,TWOO(,R3)             INCREMENT BY TWO MORE      @21C
REUSQ    EQU   *                                                   @21C
         DR    R2,R1                    DIVIDE REC # BY FOUR    SA50211
         CL    R3,RR1RECD               RCD # SAME AS REQUESTD? SA50211
         BE    NOCHECK                  YES,NO LOGICAL READ ERR SA50211
         LTR   R2,R2                    IS REMAINDER ZERO?      SA50211
         BZ    NOCHECK                  YES,NON-REUSABLE QUEUES SA50211
WTOR1    EQU   *                                                SA50211
         WTOR  'IED150D TCAM REUSABLE Q WRAPPED REPLY D TO DUMP ENTIRE *
               MSG DATA SET OR C TO CANCEL',RPLY,1,ECBAD
         LR    R2,R1                                            SA50211
         DOM   MSG=(2)                                          SA50211
         WAIT  ECB=ECBAD                                        SA50211
         XC    ECBAD,ECBAD              CLEAR ECB               SA50211
         CLI   RPLY,C                   CANCEL REQUESTED?       SA50211
         BE    ENDED                    YES,END XC              SA59967
         CLI   RPLY,D                   DUMP REQUESTED?         SA50211
         BNE   WTOR1                    REISSUE WTOR            SA50211
         OI    CTRL,DUMP                SET DUMP REQUEST FLAG   SA50211
         XC    QUESTART,QUESTART        CLEAR TO DUMP 1ST RCD   SA50211
         B     RETURN                   START DUMP OF ENTIRE Q  SA50211
NOCHECK  EQU   *                                                SA50211
         LM    0,12,RDSAVE
         LA    RPRF,INBUF          GET ADDR BUFFER
         BR    RIR1                RETURN TO CALLING RTNE
 TITLE 'IEDQXC - TCAM MESSAGE QUEUE PRINT - PARAMETER SCAN ROUTINE    '
***********************************************************************
***            GET PARM VALUES & INITIALIZE MSGQUEUE VALUES         ***
****                     Q=NNN,AAA,BBB,CCC,DDD,EEE                 ****
***                                                                 ***
***********************************************************************
QFORMAT  LH    R3,FMT1DSCB+52           GET NUM OF CYLS IN EXTENT
         ST    R3,CYLVOL                SAVE FOR LATER USE
         LH    R3,FMT1DSCB+54           GET TT OF LAST RECORD
         LA    R3,1(,R3)                ADD 1 TO GET TOTAL NUM TRACKS
         SR    R14,R14                  CLEAR IC REG
         IC    R14,FMT1DSCB+56          GET R OF LAST RECORD
         ST    R14,RCTRK                SAVE FOR GET REC ALGORITHM
         MR    R3-1,R14                 CALC TOTAL NUM RECORDS IN EXT
         ST    R3,TOTNO                 PLACE VALUE INTO SAVE AREA
         LA    R14,DCBLIST              GET ADDR OF DCB LIST
         SR    R2,R2                    INITIALIZE VOL COUNT TO ZERO
VOLLOOP  L     R3,0(,R14)               GET ADDR OF DCB TO TEST
         TM    48(R3),X'10'             IS DCB OPEN
         BZ    *+8                      IF NO, DON'T ADD TO COUNT
         LA    R2,1(,R2)                ADD ONE TO VOLUME COUNT
         LA    R14,4(,R14)              BUMP TO GET NEXT DCB
         LTR   R3,R3                    WAS THIS THE LAST DCB TO TEST
         BP    VOLLOOP                  IF NO, LOOP UNTIL DONE
         ST    R2,NOVOL                 SAVE VOLUME COUNT
         LR    R3,R2                    SHIFT VOL COUNT FOR MULTIPLY
         M     R3-1,TOTNO               VOLS * (REC/VOL) = TOTAL RECDS
         ST    R3,TOTNO                 SAVE FOR READ ROUTINE
         L     RWRK1,0(,R1)             GET PARM POINTER
         CLI   1(RWRK1),X'00'           ARE NO PARAMETERS SPECIFIED
         BE    SETDMP                   IF YES, GIVE SEQUENTIAL DUMP
         CLI   1(RWRK1),PARMLNG         IS THE PARM LENGTH OK
         BH    PARMERR             TOO LARGE
         LH    RWRK2,0(RWRK1)      GET LENGTH
         BCTR  RWRK2,0                  DECREMENT LENGTH BY 1    A42375
         EX    RWRK2,GETPARM       MOVE PARM TO WORKAREA
         CLC   WKNUMQE,QEQUAL           CHECK FORMAT             A42375
         BNE   PARMERR
         CLC   WKNUMX(4),PART           CHECK FOR Q=PART         S21903
         BE    SETPART                  BRANCH IF YES            S21903
         CLC   WKNUMX(3),DMP            CHECK FOR Q=DMP        @XA08072
         BE    SETDMP                   IF YES, SET DUMP REQUEST A42375
         LA    RWRK1,WKNUMX             GET THE ADDR OF THE    @XA08072
*                                       FIRST PARAMETER        @XA08072
         LA    RWRK3,FOURR              SET LOOP COUNT         @XA08072
         LR    RWRK2,RWRK1              LOAD INDEX REGISTER    @XA08072
PARMLP1  EQU   *                                               @XA08072
         TM    0(RWRK2),DIGIT           IS THIS A DECIMAL DIGIT@XA08072
         BNO   PARMERR                  NO, GO PRINT ERROR     @XA08072
         LA    RWRK2,ONEE(RWRK2)        INCREMENT INDEX        @XA08072
         CLI   0(RWRK2),COMMA           IS THIS A COMMA        @XA08072
         BE    OUT1                     YES, PACK DIGITS       @XA08072
         BCT   RWRK3,PARMLP1            LOOP 4 TIMES           @XA08072
         B     PARMERR                  NUM QUEUES SPECIFIED   @XA08072
*                                       AS MORE THAN 4 DIGITS  @XA08072
OUT1     EQU   *                                               @XA08072
         SR    RWRK2,RWRK1              GET THE NR. OF DIGITS  @XA08072
         BCTR  RWRK2,0                  SUBTRACT 1 FROM LENGTH @XA08072
         EX    RWRK2,PACKNUM            PACK NR. OF QUEUES     @XA08072
         AR    RWRK1,RWRK2              POINT TO NEXT PARM-2   @XA08072
         CVB   RWRK2,WORKDBL            CONVERT NR QUEUES TO   @XA08072
         ST    RWRK2,NUMQUES            HEX AND STORE          @XA08072
         LA    RWRK1,TWOO(RWRK1)        INDEX PAST COMMA       @XA08072
         ST    R13,SAVE13               SAVE REG 13            @OY12272
         CLC   0(THREE,RWRK1),ALL       ALL QUEUES REQUESTED   @XA08072
         BE    SETALL                   YES                    @XA08072
         CLC   0(THREE,RWRK1),DMP       IS QUEDUMP REQUESTED?  @XA08072
         BE    SETDMP                   YES                    @XA08072
         LA    RWRK3,QREQ1              GET THE ADDR OF FIRST  @XA08072
*                                       STORAGE AREA FOR QUEUE @XA08072
*                                       REQUEST                @XA08072
         LA    RREC,FIVE                SET LOOP COUNT OF 5    @XA08072
PARMLP2  EQU   *                                               @XA08072
         LA    R13,FIVE                 SET LOOP COUNT OF 5    @XA08072
         LR    RWRK2,RWRK1              LOAD INDEX REGISTER    @XA08072
PARMLP3  EQU   *                                               @XA08072
         TM    0(RWRK2),DIGIT           IS THIS A DECIMAL DIGIT@XA08072
         BNO   OUT2                     NO, CHECK FOR COMMA    @XA08072
         LA    RWRK2,ONEE(RWRK2)        INCREMENT BY ONE       @XA08072
         BCT   R13,PARMLP3              CHECK NEXT DIGIT       @XA08072
         B     PARMERR                  TOO MANY DIGITS IN PARM@XA08072
OUT2     EQU   *                                               @XA08072
         CLI   0(RWRK2),COMMA           IS THIS A COMMA        @XA08072
         BE    SKIP3                    YES, SKIP TO END       @XA08072
         CLI   0(RWRK2),ZERO            BLANK ?                @XA08072
         BNE   PARMERR                  NO, PARAMETER ERROR    @XA08072
         LA    RREC,ONEE                SET REGISTER TO EXIT   @XA08072
SKIP3    EQU   *                                               @XA08072
         SR    RWRK2,RWRK1              GET NR. OF DIGITS      @XA08072
         LTR   RWRK2,RWRK2              WERE THERE ANY DIGITS  @XA08072
*                                       AFTER COMMA            @XA08072
         BZ    PARMERR                  NO, PARAMETER ERROR    @XA08072
         BCTR  RWRK2,0                  SUBTRACT 1 FROM LENGTH @XA08072
         EX    RWRK2,PACKNUM            PACK QUEUE NUMBER      @XA08072
         AR    RWRK1,RWRK2              POINT TO NEXT PARM-2   @XA08072
         LA    RWRK1,TWOO(RWRK1)        INDEX PAST COMMA       @XA08072
         CVB   RWRK2,WORKDBL            CONVERT NR. QUEUES TO  @XA08072
         ST    RWRK2,0(RWRK3)           HEX AND STORE IN THE   @XA08072
*                                       AREA FOR QUEUE REQUEST @XA08072
         LA    RWRK3,FOURR(RWRK3)       UPDATE REQUEST ADDR    @XA08072
         CLI   ONEE(RWRK1),ZERO         END OF FIELD           @OX13503
         BE    FINISHED                 YES                    @OX13503
         TM    ZERO(RWRK1),DIGIT        IS THIS IS DIGIT?      @OY12272
         BNO   CHKSERV                  NO                     @OY12272
FINISHED EQU   *                                               @OX13503
         BCT   RREC,PARMLP2             PROCESS NEXT QUEUE REQU@XA08072
         L     R13,SAVE13               RESTORE REG 13         @XA08072
         BR    RIR1                     RETURN                 @XA08072
SETALL   OI    CTRL,ALLQUES        SET ALL
         LA    RWRK1,THREE(,RWRK1)      INCREMENT PAST ALL     @OY12272
         CLI   ZERO(RWRK1),COMMA        IS THERE A COMMA?      @OY12272
         LA    RWRK1,ONEE(,RWRK1)       INCREMENT PAST COMMA   @OY12272
         BE    CHKSERV                  YES                    @OY12272
         BR    RIR1
SETDMP   OI    CTRL,DUMP           SET DUMP REQ
         BR    RIR1
SETPART  EQU   *                                                 S21903
         OI    SCANBYTE,ON              SET PARTIAL DUMP REQ     S21903
         BR    RIR1                     RETURN                   S21903
         SPACE 2
CHKSERV  EQU   *                                               @OY12272
         L     R13,SAVE13               RESTORE REG 13         @OY12272
         CLC   ZERO(TWOO,RWRK1),DS      IS THIS DUMP SERV?     @OY12272
         BNE   CHKUNSRV                 NO, CHK DUMP UNSERV    @OY12272
         OI    CTRL2,DSSW               SET DUMP SERV SWITCH   @OY12272
         LA    RWRK1,TWOO(RWRK1)        INCREMENT TO NEXT CHAR @OY12272
         CLI   ZERO(RWRK1),COMMA        IS THERE A COMMA?      @OY12272
         BE    CHKFMTC                  YES                    @OY12272
         CLI   ZERO(RWRK1),ZERO         BLANK?                 @OY12272
         BNE   PARMERR                  NO                     @OY12272
         BR    RIR1                     RETURN                 @OY12272
         SPACE 2
CHKUNSRV EQU   *                                               @OY12272
         CLC   ZERO(TWOO,RWRK1),DU      IS THIS DUMP UNSERV?   @OY12272
         BNE   CHKFMT                   NO                     @OY12272
         OI    CTRL2,DUSW               SET DUMP UNSERV SW     @OY12272
         LA    RWRK1,TWOO(,RWRK1)       INCREMENT TO NEXT CHAR @OY12272
         CLI   ZERO(RWRK1),COMMA        IS THERE A COMMA?      @OY12272
         BE    CHKFMTC                  YES                    @OY12272
         CLI   ZERO(RWRK1),ZERO         BLANK?                 @OY12272
         BNE   PARMERR                  NO                     @OY12272
         BR    RIR1                     RETURN                 @OY12272
         SPACE 2
CHKFMTC  EQU   *                                               @OY12272
         LA    RWRK1,ONEE(,RWRK1)       INCREMENT TO NEXT CHAR @OY12272
         SPACE 1
CHKFMT   EQU   *                                               @OY12272
         CLC   ZERO(THREE,RWRK1),FMT    FORMAT DATFLAGS FIELD? @OY12272
         BNE   PARMERR                  NO                     @OY12272
         OI    CTRL2,FMTSW              SET FORMAT SWITCH      @OY12272
         LA    RWRK1,THREE(,RWRK1)      INCREMENT TO NEXT CHAR @OY12272
         CLI   ZERO(RWRK1),ZERO         BLANK?                 @OY12272
         BNE   PARMERR                  NO                     @OY12272
         BR    RIR1                     RETURN                 @OY12272
 TITLE 'IEDQXC - TCAM MESSAGE QUEUE PRINT - SYSPRINT OUTPUT ROUTINE   '
***********************************************************************
*                                                                     *
*                       PUT TO SYSPRINT ROUTINE                       *
*                                                                     *
*        THIS ROUTINE OUTPUTS TO THE 'SYSPRINT' DATA SET.  THE BUFFER *
*        AREA 'OUTAREA' IS CLEARED TO BLANKS AND THE LINE COUNTER IS  *
*        DECREMENTED AFTER PRINTING.  WHEN THE LINE COUNTER REACHES   *
*        ZERO, THE COUNTER WILL BE RE-INITIALIZED.  THE NEXT PASS     *
*        THROUGH THE ROUTINE WILL CAUSE A PAGE SKIP.  IF THE USER     *
*        PLACES A X'F1' AT 'OUTAREA', THE LINE COUNTER WILL BE RE-    *
*        INITIALIZED AND A PAGE SKIP WILL OCCUR.  WHEN THE PRINT      *
*        ROUTINE SENSES THAT A PAGE SKIP WILL OCCUR ON THE NEXT       *
*        PRINT OPERATION, REG 15 WILL BE SET TO 4, OTHERWISE IT WILL  *
*        BE SET TO ZERO...INDICATING NO AUTOMATIC PAGE SKIPPING.      *
*        RETURN TO THE USER IS DONE VIA R14.  THE AREA 'OUTBUF'       *
*        MUST ALREADY CONTAIN THE DATA TO BE PRINTED, PRIOR TO THE    *
*        INVOCATION OF THIS ROUTINE.                                  *
*                                                                     *
*        ALL REGISTERS ARE TRANSPARENT, EXCEPT REGS 0, 1, AND 15.     *
*                                                                     *
***********************************************************************
*
PUT      ST    R14,PUTSAVE              SAVE USERS RETURN REG
         LH    R14,LINECTR              LOAD CURRENT COUNT     @OZ27790
         CLC   LINECTR,LINECNT          SKIP TO NEW PAGE       @OZ27790
         BE    EJECT                    BRANCH YES             @OZ27790
         CLI   OUTAREA,C'-'             TRIPLE SPACE SPECIFIED @OZ27790
         BNE   DOUB                     BRANCH NO              @OZ27790
         BCT   R14,TRIP                 REDUCE AND BRANCH      @OZ27790
         B     EJECT                    CONTINUE               @OZ27790
DOUB     EQU   *                                               @OZ27790
         CLI   OUTAREA,C'0'             DOUBLE SPACE SPECIFIED @OZ27790
         BNE   CEJECT                   BRANCH NO              @OZ27790
TRIP     EQU   *
         BCT   R14,SING                 REDUCE AND BRANCH      @OZ27790
EJECT    EQU   *                                               @OZ27790
         MVI   OUTAREA,X'F1'            ELSE, SET CARRIAGE CTL TO SKIP
CEJECT   EQU   *                                               @OZ27790
         CLI   OUTAREA,X'F1'            DID USER SPECIFY A SKIP
         BNE   NEJECT                   BRANCH NO EJECT        @OZ27790
         MVC   LINECTR,LINECNT          SET COUNTER TO MAX LINES
         B     NEJECT                                          @OZ27790
SING     EQU   *                                               @OZ27790
         STH   R14,LINECTR              STORE CURRENT COUNT    @OZ27790
NEJECT   EQU   *                                               @OZ27790
         TR    OUTAREA+1(120),PRINTABL  INSURE NO UNPRINTABLES
         PUT   PUTDCB,OUTAREA           PRINT THE LINE
         SR    R15,R15                  SET RETURN CODE EQ ZERO
         LH    R14,LINECTR              GET LINE COUNTER
         BCT   R14,PUTEXIT              DECREMENT COUNTER      @OZ27790
         LH    R14,LINECNT              RE-INITIALIZE LINE COUNTER
PUTEXIT  STH   R14,LINECTR              UPDATE COUNTER FIELD
         MVI   OUTAREA,X'40'            CLEAR OUTPUT BUFFER
         MVC   OUTAREA+1(120),OUTAREA        TO BLANKS
         MVI   OUTBUF,X'EF'        CLEAR
         MVC   OUTBUF+1(119),OUTBUF BUFFER
         L     R14,PUTSAVE              GET USERS RETURN ADDRESS
         BR    R14                      RETURN TO USER
LINECNT  DC    H'60'                    MAX LINES PER PAGE
LINECTR  DC    H'60'                    ACTIVE LINE COUNTER
         EJECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *  S21101
*                                                             *  S21101
*        THIS SUBROUTINE INCREMENTS THE ABSOLUTE              *  S21101
*        RECORD COUNT AND CHECKS TO SEE IF THE                *  S21101
*        TOTAL RECORD COUNT HAS BEEN EXCEEDED                 *  S21101
*                                                             *  S21101
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *  S21101
INCRECNT EQU   *                                                 S21101
         L     RREC,RR1RECD             GET THE LATEST ABSOLUTE  S21101
*                                         RECORD NUMBER          S21101
         LA    RREC,1(,RREC)            INCREMENT IT             S21101
         ST    RREC,RR1RECD             STORE IT BACK            S21101
         CLC   RR1RECD(4),TOTNO         IS THIS THE END OF THE   S21101
*                                         DATA SET               S21101
         BCR   13,RIR1                  BRANCH IF NO             S21903
         TM    SCANBYTE,ON              HAS SCAN BEEN DONE       S21903
         BZ    ENDED                    YES,BRANCH              SA59967
         L     RREC,RECORD              PICK UP RECORD PROCESSEDSA66943
         LA    RIR1,TWOO                SET INCREMENT OF TWO       @21C
         TM    CTRL,REUS                IS IT REUSABLE QUEUE       @21C
         BO    ADDTWO3                  YES, ADD 2 FOR REUS        @21C
         LA    RIR1,FOURR               NONREUS QUEUE, ADD 4       @21C
ADDTWO3  EQU   *                                                   @21C
         AR    RREC,RIR1                BUMP TO NEXT UNIT ADDR     @21C
         ST    RREC,WORKDBL             SAVE RECORD NUMBER      SA66943
         CLC   WORKDBL+1(3),PACKFLD2    HAVE WE PASSED RANGE    SA66943
         BH    WTORTNE2                 YES GO PRINT MESSAGE    SA66943
         B     DUMPLOOP                 NO DUMP NEXT RECORD     SA66943
SETCNT   EQU   *                                                SA51078
         L     RWRK1,UNIT               GET UNIT SIZE           SA51078
         SH    RWRK1,DATSIZE            DECR FOR DATA FIELD     SA51078
         LH    R15,PRFSIZE              GET DATA SIZE           SA51078
         SR    R14,R14                  CLEAR FOR DIVIDE        SA51078
         DR    R14,RWRK1                GET UNITS PER BUF       SA51078
         LTR   R14,R14                  ANY REMAINDER           SA51078
         BZ    HAVECNT                  BRANCH IF NO            SA51078
         LA    R15,1(R15)               INCR UNIT CNT           SA51078
HAVECNT  EQU   *                                                SA51078
         STC   R15,PRFNBUNT             SET UNIT COUNT          SA51078
         BR    RIR1                     RETURN                  SA51078
 TITLE 'IEDQXC - TCAM MESSAGE QUEUE PRINT - WTO ERROR MESSAGES        '
CHAINER1 OI    CTRL,NXTQUE              INDICATES NEXT QUEUE    SA59967
         B     FINISH                   PRINT OUT INVALID MSG   SA59967
CHAINER2 OI    CTRL,NXTHDR              INDICATE NEXT HEADER    SA59967
         B     FINISH                   PRINT OUT INVALID MSG   SA59967
CHAINERR WTO   'IED122I INVALID MESSAGE CHAIN.',                       C
               ROUTCDE=11,DESC=7
         B     ENDED                    GO TERMINATE JOB        SA59967
PARMERR  WTO   'IED123I INVALID PARAMETERS.',                          C
               ROUTCDE=11,DESC=7
         B     ENDED                    GO TERMINATE JOB        SA59967
WRAPERR  WTO   'IED124I QUEUE HAS BEEN WRAPPED.',                      C
               ROUTCDE=11,DESC=7
         B     ENDED                    GO TERMINATE JOB        SA59967
OPENERR  WTO   'IED119I UNABLE TO OPEN ''SYSPRINT''.',                 C
               ROUTCDE=11,DESC=7
         B     ENDED                    GO TERMINATE JOB        SA59967
OPERR1   WTO   'IED119I UNABLE TO OPEN ''DISKQ01''.',                  C
               ROUTCDE=11,DESC=7
 TITLE 'IEDQXC - TCAM MESSAGE QUEUE PRINT - RETURN TO SUPERVISOR      '
***********************************************************************
*                                                                     *
*              ENTIRE QUEUE DUMPED.    EXIT                           *
*                                                                     *
***********************************************************************
FINISH   EQU   *                                                SA59967
         MVC   OUTBUF+3(6),HEXRCD       INSERT RECORD NUMBER    SA59967
         MVC   OUTBUF+11(39),INVALRCD   INSERT ERROR MESSAGE    SA59967
         $PRNT OUTBUF                   PRINT FILLED BUFFER     SA59967
         TM    CTRL,NXTHDR              GO TO NEXT HEADER?      SA59967
         BO    MSGCHAIN                 READ NEXT HEADER        SA59967
         TM    CTRL,NXTQUE              GO TO NEXT QUEUE?       SA59967
         BZ    ENDED                                            SA59967
         TM    CTRL,ALLQUES             ALL SPECIFIED?          SA59967
         BO    GETNEXT                  YES                     SA59967
         L     RWRK1,NEXTQUE            GET NEXT QUEUE          SA59967
         LA    RWRK1,4(RWRK1)           BUMP TO NEXT REQ        SA59967
         ST    RWRK1,NEXTQUE            SAVE REQ                SA59967
         B     GETNEXT                  GO GET ANOTHER QUEUE    SA59967
ENDED    EQU   *                                                SA59967
         L     13,SAVE+4                LOAD SAVE AREA BASE    @OX21557
         CLOSE MF=(E,DCBLIST)           CLOSE QUEUE DATASETS   @OX21557
         CLOSE (PUTDCB)                 CLOSE OUTPUT DATASET   @OX21557
         LM    14,12,12(13)
         LA    15,0
         BR    14
 TITLE 'IEDQXC - TCAM MESSAGE QUEUE PRINT - CONSTANTS AND WORK AREAS  '
*******                                            ************  S21903
*                                                             *  S21903
*        THIS ROUTINE WILL ENABLE A USER TO SPECIFY A         *  S21903
*        RANGE OF RECORD NUMBERS TO BE PRINTED, THUS          *  S21903
*        ELIMINATING THE NEED TO DUMP THE ENTIRE              *  S21903
*        MESSAGE QUEUE IN ORDER TO LOOK AT A FEW              *  S21903
*        CHOSEN RECORDS                                       *  S21903
*                                                             *  S21903
***************************************************************  S21903
WTORTNE  EQU   *                                                 S21903
         XC    WTORECB,WTORECB          CLEAR THE WTOR ECB       S21903
         XC    ANSWER(13),ANSWER        CLEAR WORK AREA          S21903
         WTOR  'IED426D SPECIFY TCAM RECORDS TO BE DUMPED,ALL,OR END', X
               ANSWER,13,WTORECB                                 S21903
         WAIT  ECB=WTORECB              WAIT FOR REPLY           S21903
         OI    CTRL,DUMP                ASSUME SEQ DUMP          S21903
         NI    SCANBYTE,255-ON          RESET PART DUMP          S21903
         OC    ANSWER(3),UPCSE          SET LC TO UC             S21903
         CLC   ANSWER(3),END            IS JOB TO BE TERMINATED  S21903
         BE    ENDED                    BRANCH IF YES           SA59967
         CLC   ANSWER(3),ALL            IS SEQ DUMP REQ          S21903
         BE    PRINT                    BRANCH IF YES            S21903
         OI    SCANBYTE,ON              INDICATE PART DUMP       S21903
         SPACE 1                                                 S21903
TRANSLAT EQU   *                                                 S21903
         TR    ANSWER(6),TTABLE         TRANSLATE REG RCD NO     S21903
         TR    ANSWER+7(6),TTABLE       TRANSLATE END RCD NO     S21903
         PACK  PACKFLD1(4),ANSWER(7)    PACK THE FIRST NO        S21903
         PACK  PACKFLD2(4),ANSWER+7(7)  PACK THE ENDING NO       S21903
         L     RREC,PACKFLD1            GET START RRN            S21903
         SRL   RREC,8                   SHIFT TO LOW ORDER BYTE  S21903
         ST    RREC,RECORD              SET FIRST RCD TO READ    S21903
         LA    RREC,0(RREC)             CLEAR HI BYTE            S21903
         LA    R1,TWOO                  SET TO DIVIDE BY TWO       @21C
         LA    RREC,ONEE(,RREC)         INCREMENT NUMBER BY 1      @21C
         TM    CTRL,REUS                IS IT REUSABLE QUEUE       @21C
         BO    REUSQUE                  YES, DIVIDE BY TWO         @21C
         LA    R1,FOURR                 SET TO DIVIDE BY FOUR      @21C
         LA    RREC,TWOO(,RREC)         INCREMENT BY TWO MORE      @21C
REUSQUE  EQU   *                                                   @21C
         SR    RDTA,RDTA                CLEAR REG                S21903
         DR    RDTA,R1                  GET ABSOLUTE REC NO      S21903
         ST    RREC,RR1RECD             SET START RCD NO         S21903
         L     RREC,RECORD              GET RECORD NO            S21903
         OI    RR1BIT,ON                USE ABSOLUTE RRN         S21903
         B     DUMPLOOP                                          S21903
         SPACE 2                                                 S21903
WTORTNE2 EQU   *                                                 S21903
         SPACE 2                                                 S21903
         WTO   'IED427I TCAM RECORDS DUMPED'                     S21903
         OI    SCANBYTE,ON              INDICATE PART DUMP       S21903
         B     PRINT                    GO FORMAT OUTPUT AREA    S21903
END      DC    CL3'END'                 END                      S21903
UPCSE    DC    3CL1' '                  BLANKS                   S21903
TTABLE DS      0X                       TRANSLATE TABLE          S21903
         DC    XL129'00'                NULL VALUES              S21903
         DC    X'FAFBFCFDFEFF'          LOWER CASE A-F           S21903
         DC    XL58'00'                 NULL                     S21903
         DC    X'FAFBFCFDFEFF'          A-F                      S21903
         DC    XL41'00'                 C7-EF                    S21903
         DC    X'F0F1F2F3F4F5F6F7F8F9'  0-9                      S21903
         SPACE 2                                                 S21903
PACKFLD1 DC    F'00'                    FIRST RRN                S21903
PACKFLD2 DC    F'00'                    SECOND RRN               S21903
WTORECB  DC    F'00'                    ECB FOR WTOR             S21903
ANSWER   DC    XL13'00'                 REPLY                    S21903
         DC    XL1'00'                  RESERCED                 S21903
SCANBYTE DC    XL1'00'                  FLAG BYTE                S21903
FIRSTREC EQU   1                        FIRST RRN                S21903
SAVE13   DS    F                        REG 13 SAVE AREA       @XA08072
POINTARG DS    0F                                                  @02A
TTR      DS    XL3                      SAVE AREA FOR TTR          @02A
Z        DS    XL1                      Z PARAMETER FOR POINT      @02A
COMMA    EQU   C','                                            @XA08072
ONEE     EQU   1                                               @XA08072
TWOO     EQU   2                                               @XA08072
FOURR    EQU   4                                               @XA08072
SIXX     EQU   6                        CONSTANT               @OY12272
NINE     EQU   9                        CONSTANT               @OY12272
TEN      EQU   10                       CONSTANT               @OY12272
ELEVEN   EQU   11                       CONSTANT               @OY12272
TWELVE   EQU   12                       CONSTANT               @OY12272
FORTYONE EQU   41                       CONSTANT               @OY12272
FORTYSIX EQU   46                       CONSTANT               @OY12272
DIGIT    EQU   X'F0'                                           @XA08072
FIVE     EQU   5                                               @XA08072
**********************   CONSTANT AREA  ************************
LASTDATA MVC   OUTBUF+87(0),0(RDTA)
GETPARM  MVC   PARMWORK(0),2(RWRK1) MOVE PARMLIST TO WORK
MOVEZON  MVZ   XWK+1(0),XWK        MOVE IN ZONES
PACKNUM  PACK  WORKDBL(8),0(0,RWRK1)     PACK THE DIGITS AFTER @XA08072
*                                        LAST COMMA            @XA08072
BLNKMSG  DC    C'HEADER IS ALL ZEROES'
FRSTMSG  DC    CL50'********** FIRST MESSAGE FOR QUEUE XXXXXX *******'
SPECIAL  DC    CL50'*** SPECIAL CHARACTERS- EOT= *, EOB= %, EOA= # **'
INVALRCD DC    C'*****RECORD READ IS INCORRECT TYPE*****'       SA59967
DSDUMSG  DC    CL58'********** XXSERVICED MESSAGES FOR QUEUE XXXXXX ***C
               *****'                                          @OY12272
DSMSG    DC    X'4040'                  TO MAKE MSG SAY SERV   @OY12272
DUMSG    DC    CL2'UN'                  TO MAKE MSG SAY UNSERV @OY12272
SERV     DC    CL9'SERVICED '           MSG SERVICED           @OY12272
CANCLD   DC    CL9'CANCELED '           MSG CANCELED           @OY12272
INITFEFO DC    CL9'INITFEFO '           INIT FEFO MSG          @OY12272
MSGLOST  DC    CL12'INITMSGLOST '       INITIATE MSG LOST      @OY12272
FORMATM  DC    11C' '                   FIELD DEFINITION FOR MESSAGES
         DC    CL8'NT LCB'
         DC    CL2' '
         DC    CL4'SRCE'
         DC    CL2' '
         DC    CL4'SIZE'
         DC    CL2' '
         DC    CL2'ST'
         DC    CL2' '
         DC    CL6'NXTREC'
         DC    CL2' '
         DC    CL4'SCAN'
         DC    CL2' '
BFR      DC    CL6'NXTTXT'                                      PTR1898
         DC    CL2' '
         DC    CL6'FSTREC'
         DC    CL2' '
HDR      DC    CL6'NXTHDR'                                      PTR1898
         DC    CL2' '
         DC    CL6' QBACK'
         DC    CL2' '
         DC    CL4'SEQO'
         DC    CL2' '
         DC    CL4'DEST'
PARMWORK DS    0X
WKNUMQS  DS    0XL7                     TOTAL NO OF QUEUES      SA69651
WKNUMQE  DC    XL2'0'                   1ST 2 BYTES ARE Q=       A42375
WKNUMX   DC    XL4'0'                   DMP OR NO OF QUEUES     SA69651
WKNUMC   DC    XL1'0'                   DELIMETER(COMMA)         A42375
WKREQ1   DC    5X'00'        1111,      FIRST QUEUE REQUEST/ALL SA69651
WKREQ2   DC    5X'00'        2222,      SECOND QUEUE REQUEST    SA69651
WKREQ3   DC    5X'00'        3333,      THIRD QUEUE REQUEST     SA69651
WKREQ4   DC    5X'00'        4444,      FOURTH QUEUE REQUEST    SA69651
WKREQ5   DC    4X'00'        5555,      FIFTH QUEUE REQUEST     SA69651
WKDFMT   DC    7X'00'                   DU,DS,FMT              @OY12272
ENDPRMWK DC    C','                END OF PARM WORK AREA
PARMLNG  EQU   (ENDPRMWK-PARMWORK) MAX LNGTH PARM FIELD (52)
CYLVOL   DC    F'0'
NOVOL    DC    F'0'
UNIT     DC    F'0'
TOTNO    DC    F'0'
QREQ1    DC    F'1'                SET TO ONE FOR 'ALL' OR 'DMP'
QREQ2    DC    F'0'
QREQ3    DC    F'0'
QREQ4    DC    F'0'
QREQ5    DC    F'0'
ENDREQ   DC    F'0'                MARKS END OF REQ
TRCYL    DC    F'0'                TRKS PER CYL
VOLRT    DC    F'0'                # VOLS * REC/TRK
RCTRK    DC    F'0'                # REC/TRK
XTRA     DC    F'0'                BUFR PREFIX
NTXT     DC    F'0'                LINKAGE
NHDR     DC    F'0'                SAVE AREA
RECORD   DC    F'0'                ADDR REC BEING PROCESSED
NUMQUES  DC    F'0'                NUMBER OF QUEUES ON DATASET
QUESTART DC    F'0'                BEGIN ADDR THIS QUEUE
NEXTQUE  DC    A(QREQ1)            ADDR OF NEXT QUE REQUEST
BLOCK    DC    F'32'
ONE      DC    F'1'
TWO      DC    F'2'
SIX      DC    F'6'                                             SA59967
XWK      DC    64X'EF'             UNPK WORK AREA
HEXRCD   DC    CL8' '                                           PTR1898
OUTBUF   DC    120C' '             OUTPUT BUFFER AREA
INFO     DC    5F'0'               WORK AREA FOR DEVTYPE MACRO
WORKDBL  DC    D'0'
RDSAVE   DC    14F'0'                   SAVE AREA FOR IEDQFP    SA59967
SAVE     DC    18F'0'
PUTSAVE  DS    F                        REG 14 SAVE AREA FOR PRINT RTNE
EXTLST   DS    0F                       DCB EXIT LST             S21903
         DC    X'85'                    END OF LIST,DCBEXIT      S21903
         DC    AL3(IEDQXC01)            ADDR OF EXIT RTN         S21903
DATSIZE  DC    H'6'                     SIZE OF DATA FIELD       S21903
QDDNAME  DC    CL8'DISKQ01'             MESSAGE Q DDNAME FOR DEVTYPE
DCB      DS    0F
DCBLIST  OPEN  (RDDCB1,,RDDCB2,,RDDCB3,,RDDCB4,,RDDCB5,,RDDCB6,,RDDCB7,C
               ,RDDCB8,,RDDCB9,,RDDCB10,,RDDCB11,,RDDCB12,,RDDCB13,,   C
               RDDCB14,,RDDCB15,,RDDCB16),MF=L
OUTAREA  DC    C'1',CL120' '            OUTPUT PRINT BUFFER
PRINTABL DC    CL16'                '   X 00 - 0F
         DC    CL16'                '   X 10 - 1F
         DC    CL16'                '   X 20 - 2F
         DC    CL16'                '   X 30 - 3F
         DC    CL16'           .    '   X 40 - 4F
         DC    CL16'           $*   '   X 50 - 5F
         DC    CL16'-/         ,%   '   X 60 - 6F
         DC    CL16'           #@''= '  X 70 - 7F
         DC    CL16'                '   X 80 - 8F
         DC    CL16'                '   X 90 - 9F
         DC    CL16'                '   X A0 - AF
         DC    CL16'                '   X B0 - BF
         DC    CL16' ABCDEFGHI      '   X C0 - CF
         DC    CL16' JKLMNOPQR      '   X D0 - DF
         DC    CL16'  STUVWXYZ      '   X E0 - EF
         DC    CL16'0123456789      '   X F0 - FF
RR1BIT   DC    X'00'                    FIRST TIME SWITCH        S21101
ON       EQU   X'01'                    SWITCH ON                S21101
OFF      EQU   X'FE'                    SWITCH OFF               S21101
FOUR     EQU   2                                                 S21101
THREE    EQU   3                                                 S21101
NOTUSED  EQU   X'03'                                             S21101
ECBAD    DC    F'0'                     ECB FOR WTOR            SA50211
X0FFF    DC    X'00FFFFFF'                                      SA50211
FRSTRCD  DC    X'00'                    FIRST RCD FOR INDIVDUAL SA50211
*                                       QUEUE DUMP SWITCH       SA50211
RPLY     DC    X'00'                    REPLY AREA FOR WTOR     SA50211
ZERO     EQU   X'00'                                            SA50211
PHOUR    EQU   4                        EQUATE FOR 4            SA50211
C        EQU   X'C3'                                            SA50211
D        EQU   X'C4'                                            SA50211
BLNK     EQU   X'40'                    BLANK                  @OY12272
CTRL2    DC    X'00'                    CONTROL BYTE           @OY12272
FMTSW    EQU   X'01'                    SW FOR FORMAT DATFLAGS @OY12272
DUSW     EQU   X'02'                    DUMP UNSERV MSGS ONLY  @OY12272
DSSW     EQU   X'04'                    DUMP SERV MSGS ONLY    @OY12272
CTRL     DC    X'00'               INTERNAL CONTROL BYTE
         DS    0F                                                S21101
RR1RECD  DC    F'0'                     ABSOLUTE RECORD NUMBER   S21101
REUS     EQU   X'02'                    REUSABLE QUEUEING SWITCH S21101
ALLQUES  EQU   X'80'               ALL QUEUES REQUESTED
DUMP     EQU   X'40'               DUMP ALL RECORDS SEQUENTIALLY
SUBSTEXT EQU   X'01'                    SUBSEQUENT TEXT RECORD  SA59967
NXTQUE   EQU   X'10'                    NEXT QUEUE TO BE READ   SA59967
NXTHDR   EQU   X'08'                    NEXT HEADER TO BE READ  SA59967
DEFAULT  EQU   1331                     DEFAULT BLKSIZE          S21903
BLANK    DC    10X'EF'             BLANK CHARACTER
QEQUAL   DC    CL2'Q='                  USED TO CHECK FORMAT
DMP      DC    CL3'DMP'
ALL      DC    CL3'ALL'
PART     DC    CL4'PART'                Q=PART                   S21903
FMT      DC    CL3'FMT'                 FORMAT                 @OY12272
DU       DC    CL2'DU'                  DUMP UNSERVICED        @OY12272
DS       DC    CL2'DS'                  DUMP SERVICED          @OY12272
         DS    0F                       BDY ALIGNMENT           SA50211
INBUF    DC    270X'00'            INPUT BUFFER
         ORG   *-X'EF'
HEXTABLE DS    0X
         ORG   *+X'EF'
         DC    X'40F0F1F2F3F4F5F6F7F8F9'
         DC    X'C1C2C3C4C5C6'
EBCTABLE DS    0X                  HEX TO EBCDIC TABLE
         DC    38C'.'
         DC    CL1'%'              EOB CHAR
         DC    16C'.'
         DC    CL1'*'              EOT CHAR
         DC    8C'.'
         DC    C' '
         DC    12C'.'
         DC    CL4'(+&&'
         DC    9C'.'
         DC    CL8'!$*);^-/'
         DC    9C'.'
         DC    CL1','
         DC    3C'.'
         DC    CL1'?'
         DC    10C'.'
         DC    CL2':#'             COLAN,POUND SIGN & EOA CHAR
         DC    CL1'.'
         DC    CL2'''='
         DC    2C'.'
         DC    9C'$'
         DC    7C'.'
         DC    9C'$'
         DC    8C'.'
         DC    8C'$'
         DC    23C'.'
         DC    CL9'ABCDEFGHI'
         DC    7C'.'
         DC    CL9'JKLMNOPQR'
         DC    8C'.'
         DC    CL8'STUVWXYZ'
         DC    6C'.'
         DC    CL10'0123456789'
         DC    6C'.'
DATE     DC    C'1208'                 07/27/71                  S21101
         EJECT
PUTDCB   DCB   DSORG=PS,MACRF=PM,RECFM=FBA,                            C
               LRECL=121,EXLST=EXTLST,                                 X
               DDNAME=SYSPRINT
JFCBADDR DS    0F                       ADDRESS OF JFCB FOR DISKQ01
         DC    X'87',AL3(JFCB)
QUEDSCB CAMLST SEARCH,JFCB,JFCB+118,FMT1DSCB
JFCB     DC    XL176'00'                READ IN AREA FOR JFCB
FMT1DSCB DC    35XL10'00'               READ IN AREA FOR FMT1 DSCB
         TPRFD
 TITLE 'IEDQXC - TCAM MESSAGE QUEUE PRINT - MESSAGE QUEUE DCBS        '
IEDQXC   CSECT
RDDCB1   DCB   BUFNO=1,DDNAME=DISKQ01,MACRF=RP,RECFM=F,            @01CC
               DSORG=PS,EXLST=JFCBADDR                             @01C
RDDCB2   DCB   BUFNO=1,DDNAME=DISKQ02,MACRF=RP,RECFM=F,            @01CC
               DSORG=PS                                            @01A
RDDCB3   DCB   BUFNO=1,DDNAME=DISKQ03,MACRF=RP,RECFM=F,            @01CC
               DSORG=PS                                            @01A
RDDCB4   DCB   BUFNO=1,DDNAME=DISKQ04,MACRF=RP,RECFM=F,            @01CC
               DSORG=PS                                            @01A
RDDCB5   DCB   BUFNO=1,DDNAME=DISKQ05,MACRF=RP,RECFM=F,            @01CC
               DSORG=PS                                            @01C
RDDCB6   DCB   BUFNO=1,DDNAME=DISKQ06,MACRF=RP,RECFM=F,            @01CC
               DSORG=PS                                            @01A
RDDCB7   DCB   BUFNO=1,DDNAME=DISKQ07,MACRF=RP,RECFM=F,            @01CC
               DSORG=PS                                            @01A
RDDCB8   DCB   BUFNO=1,DDNAME=DISKQ08,MACRF=RP,RECFM=F,            @02CC
               DSORG=PS                                            @02A
RDDCB9   DCB   BUFNO=1,DDNAME=DISKQ09,MACRF=RP,RECFM=F,            @02CC
               DSORG=PS                                            @02A
RDDCB10  DCB   BUFNO=1,DDNAME=DISKQ10,MACRF=RP,RECFM=F,            @02CC
               DSORG=PS                                            @02A
RDDCB11  DCB   BUFNO=1,DDNAME=DISKQ11,MACRF=RP,RECFM=F,            @02CC
               DSORG=PS                                            @02A
RDDCB12  DCB   BUFNO=1,DDNAME=DISKQ12,MACRF=RP,RECFM=F,            @02CC
               DSORG=PS                                            @02A
RDDCB13  DCB   BUFNO=1,DDNAME=DISKQ13,MACRF=RP,RECFM=F,            @02CC
               DSORG=PS                                            @02A
RDDCB14  DCB   BUFNO=1,DDNAME=DISKQ14,MACRF=RP,RECFM=F,            @02CC
               DSORG=PS                                            @02A
RDDCB15  DCB   BUFNO=1,DDNAME=DISKQ15,MACRF=RP,RECFM=F,            @02CC
               DSORG=PS                                            @02A
RDDCB16  DCB   BUFNO=1,DDNAME=DISKQ16,MACRF=RP,RECFM=F,            @01CC
               DSORG=PS                                            @01A
IEDQXC01 CSECT
         USING IEDQXC01,R15             BASE                     S21903
         USING IHADCB,R1                DCB                      S21903
         LH    RREC,DCBBLKSI            GET BLKSIZE              S21903
         LTR   RREC,RREC                BLKSIZE SPEC             S21903
         BZ    STDEFALT                 BRANCH IF NO             S21903
         SR    RDTA,RDTA                CLEAR REG                S21903
         LH    R2,DCBLRECL              GET RECORD LENGTH        S21903
         DR    RDTA,R2                  IS BLKSI MULT OF LRECL   S21903
         LTR   RDTA,RDTA                ANY REMAINDER            S21903
         BZ    BLKSIOK                  BRANCH IF NO             S21903
STDEFALT EQU   *                                                 S21903
         LA    RREC,DEFAULT             GET DEFAULT VALUE        S21903
         STH   RREC,DCBBLKSI            SET DEFAULT VALUE        S21903
BLKSIOK  EQU   *                                                 S21903
         RETURN
         EJECT ,                                                   @21A
         TCPBD ,                                                   @21A
         EJECT ,                                                   @21A
         DCBD  DSORG=PS
         EJECT ,                                                   @21A
         TDATAD
         END
