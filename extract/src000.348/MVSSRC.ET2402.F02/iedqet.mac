ET01     TITLE '''IEDQET'' - OPER. CTL./APPL. PROG. INTERFACE'
IEDQET   CSECT
*C  THIS MODULE HAS BEEN COMPLETELY REWRITTEN FOR TCAM LEVEL 6   Y02027
*        THIS MODULE HAS BEEN REWRITTEN FOR FETCH PROTECT        Y01004
*D856000                                                         S21903
*C288500,428000,436000,499000,536000,680000,732000,736000,740000 S21903
*A548000                                                        SA55391
*A322000                                                       @ZA02088
*D380000                                                       @ZA02088
         SPACE 4
***********************************************************************
*                                                                     *
*TITLE -- 'IEDQET' - OPERATOR CONTROL/APPL PROG INTERFACE ROUTINE     *
*                                                                     *
*  MODULE NAME = IEDQET                                               *
*                                                                     *
*  DESCRIPTIVE NAME = OPERATOR CONTROL/APPL PROG INTERFACE ROUTINE    *
*                                                                     *
*  COPYRIGHT = NONE                                                   *
*                                                                     *
*  STATUS = CHANGE LEVEL 6                                            *
*                                                                     *
*FUNCTION -- THIS MODULE ALLOWS THE USER TO PERFORM A SUBSET OF TCAM  *
*  OPERATOR CONTROL FUNCTIONS (ICHNG, MRELEASE, MCPCLOSE).  THIS      *
*  MODULE INITIALIZES A PARAMETER LIST FOR INPUT TO SVC 102.  SVC 102 *
*  SERVES AS AN INTERFACE TO THE MCP.  THE FUNCTIONAL PROCESSING OF   *
*  EACH FUNCTION IS ACTUALLY PROCESSED IN THE MCP BY IGC0A10D.
*  EACH FUNCTION IS ACTUALLY PROCESSED IN THE MCP BY THE APPROPRIATE  *
*  HANDLER.                                                           *
*                                                                     *
*NOTES -- SEE BELOW                                                   *
*                                                                     *
*  DEPENDENCIES = AN INTERNAL REPRESENTATION OF THE EXTERNAL CHARAC-  *
*  TER SET WHICH IS EQUIVALENT TO THE ONE USED AT ASSEMBLY TIME.  THE *
*  CODING HAS BEEN ARRANGED SO THAT REDEFINITION OF CHARACTER CONS-   *
*  TANTS, BY REASSEMBLY, WILL RESULT IN A CORRECT MODULE FOR THE NEW  *
*  DEFINITIONS.                                                       *
*                                                                     *
*  RESTRICTIONS = NONE                                                *
*                                                                     *
*  REGISTER CONVENTIONS --                                            *
*             RPARM  - PARAMETER REG                                  *
*             RTCB   - TCB BASE REG                                   *
*             RDEB   - DEB BASE REG                                   *
*             RSAVE  - SAVE AREA PTR                                  *
*             RENTRY - LINKAGE REG                                    *
*                                                                     *
*ATTRIBUTES -- SERIALLY REUSABLE, PROBLEM PROGRAM MODE                *
*                                                                     *
*ENTRY POINT -- IEDQET                                                *
*                                                                     *
*INPUT -- REG 0  PASSWORD ADDRESS OR BINARY ZERO                      *
*         REG 1  COMMAND INPUT BLOCK ADDRESS                          *
*         REG 13 CALLER'S SAVE AREA PTR                               *
*         REG 14 RETURN ADDRESS                                       *
*         REG 15 ENTRY ADDRESS                                        *
*                                                                     *
*OUTPUT -- REG 15 RETURN CODE                                         *
*                                                                     *
*EXIT -- NORMAL = RETURN TO CALLER                                    *
*                                                                     *
*EXIT -- ERROR = NONE                                                 *
*                                                                     *
*EXTERNAL REFERENCES --                                               *
*  ROUTINES = SVC 102                                                 *
*                                                                     *
*  DATA AREAS = COMMAND INPUT BLOCK                                   *
*                                                                     *
*  CONTROL BLOCKS = NONE                                              *
*                                                                     *
*  MACROS = RETURN                                                    *
*                                                                     *
*CHANGE ACTIVITY -- SEE ABOVE                                         *
*                                                                     *
*************************END OF SPECIFICATIONS*************************
         EJECT
RZERO    EQU   0                        PASSWORD ADDRESS
RPARM    EQU   1                        PARAMETER REG.
RWORK    EQU   2                        GENERAL WORK REGISTER    Y02027
RDEB     EQU   7                        DEB BASE REGISTER        Y02027
RBASE    EQU   12                       BASE REGISTER            Y02027
RSAVE    EQU   13                       SAVE AREA REGISTER       Y02027
RETURN   EQU   14                       RETURN REG
RENTRY   EQU   15                       RENTRY
         SPACE 2
         USING IEDQET,RBASE             CSECT ADDRESSABILITY     Y02027
         USING IEDQCIB,RPARM            CIB ADDRESSABILITY       Y02027
         SPACE 2                                               @ZA02088
         STM   RETURN,RBASE,TWLVE(RSAVE)  SAVE CALLER'S REGS   @ZA02088
         LR    RBASE,RENTRY             SET BASE                 Y02027
         SPACE 2
IEDQET   IEDHJN ENTER                                            Y01004
         ST    RSAVE,MYSAVE+FOUR        SAVE CALLER'S SAVE PTR   Y02027
         LA    RSAVE,MYSAVE             SET NEW SAVE PTR         Y02027
         ST    RZERO,APPASWD            SAVE PASSWORD PTR        Y02027
         CLI   CIBVERB,ICHNG            ICHNG FUNCTION           Y02027
         BH    RELEASE                  BR IF MRELEASE           Y02027
         BL    CLOSE                    BR IF MCPCLOSE           Y02027
*                                                              * Y02027
*             BUILD ICHNG PARAMETER LIST FOR SVC AQCTL         * Y02027
*                                                              * Y02027
         SPACE 1
         L     RWORK,CIBICHNG           GET WORKAREA ADDRESS OR  Y02027
*                                       ACT/DEACT CODE           Y02027
         ST    RWORK,APUSWKA            SET UP IN PARM LIST      Y02027
         LA    RWORK,CIBTNAME           GET PTR TO DDNAME        Y02027
         ST    RWORK,APNAME             SET IN PARM LIST         Y02027
         MVC   APNAME(ONE),CIBLRN       SET RLN IN PARM LIST     Y02027
         MVI   APCOMCD,ICHNG            TURN ON ICHNG FLAG       Y02027
         LA    RZERO,TWO                FUNCTION INDX FOR SVC102 Y02027
         B     SETUP                    GO TO ISSUE SVC AQCTL    Y02027
         SPACE 1
CLOSE    EQU   *                                                 Y02027
         MVC   APCOMCD(ONE),CIBVERB     MOVE COMMAND CD TO LIST  Y02027
         LA    RZERO,SIX                FUNCTION INDX FOR SVC102 Y02027
         B     SETUP                    ISSUE AQCTL              Y02027
         SPACE 2
RELEASE  EQU   *                                                 Y02027
         LA    RWORK,CIBTNAME           GET PTR TO TERM NAME     Y02027
         ST    RWORK,APNAME             AND STORE IN PARM LIST   Y02027
         MVI   APCOMCD,MRELEASE         TURN ON COMMAND CD FLAG  Y02027
         LA    RZERO,FIVE               SET INDEX REG FOR FUNCTN Y02027
*                                       LOCATE IN SVC AQCTL      Y02027
         SPACE 2
SETUP    EQU   *                                                 Y02027
         OI    APCOMCD,CHKPASWD         TURN ON FLAG = CHECK     Y02027
*                                       FOR PASSWORD             Y02027
         LA    RPARM,APPARM             SET PARM REGISTER        Y02027
         AQCTL                          ISSUE SVC                Y02027
         LA    RWORK,NOTCAM             SET REG FOR COMPARE      Y02027
         CR    RENTRY,RWORK             IS TCAM ACTIVE           Y02027
         BNE   EXIT                     BRANCH IF YES            Y02027
         LA    RENTRY,NOPEN             MPP EXPECTS ERR RETURN   Y02027
*                                       CODE = X'0C'             Y02027
         SPACE 2
EXIT     EQU   *                                                 Y02027
         L     RSAVE,MYSAVE+FOUR        RESTORE USER'S SAVE PTR  Y02027
         RETURN (14,12),T,RC=(15)       RETURN TO USER
         SPACE 2
         EJECT
*************************************************************** Y02027
*                          CONSTANTS AND EQUATES               * Y02027
*************************************************************** Y02027
MYSAVE   DC    18F'0'                   SAVEAREA                 Y02027
CHKPASWD EQU   X'20'                    FLAG = CHECK PASSWORD    Y02027
ICHNG    EQU   X'49'                    ICHNG COMMAND CODE       Y02027
MRELEASE EQU   X'87'                    MRELEASE FUNCTION CODE   Y02027
NOPEN    EQU   12                       CODE = NO OPEN TCAM DCB  Y02027
*                                       AND FOR TCAM NOT ACTIVE  Y02027
NOTCAM   EQU   X'80'                    ERROR CODE RETURNED BY   Y02027
*                                       SVC 102 IF TCAM NOT UP   Y02027
ZERO     EQU   0                        DISPLACEMENT VALUE       Y02027
ONE      EQU   1                        DISPLACEMENT             Y02027
TWO      EQU   2                        ICHNG FUNCTION INDX      Y02027
FOUR     EQU   4                        DISPLACEMENT VALUE       Y02027
FIVE     EQU   5                                                 Y02027
SIX      EQU   6                        MRELEASE FUNCTION CODE   Y02027
TWLVE    EQU   12                       DISPLACEMENT VALUE       Y02027
         SPACE 5
**************************************************************** Y02027
*              MAP OF INPUT DATA PROVIDED TO SVC 102           * Y02027
**************************************************************** Y02027
APCIBMAP DS    0F                                                Y02027
APPASWD  DC    AL4(0)                   POINTER TO PASSWORD      Y02027
APECB    DC    F'0'                     ECB FOR WAIT IN SVC 102  Y02027
*                                       AND SUBSEQUENT XMPOST    Y02027
APPCBAD  DC    AL4(0)                   PCB POINTER              Y02027
APCOMCD  DS    0XL1                     COMMAND VERB CODE        Y02027
APUSWKA  DC    AL4(0)                   PTR TO USER WORK AREA    Y02027
APNAME   DC    AL4(0)                   PTR TO TERM OR DD NAME   Y02027
APPARM   DS    0F                       PARAMETER FOR SVC 102    Y02027
         DC    X'84'                    EOL X'80' + FUNCTION     Y02027
*                                       CODE X'04'               Y02027
         DC    AL3(APCIBMAP)            PTR TO INPUT DATA        Y02027
         SPACE 1
**************************************************************** Y02027
         SPACE 2
IEDQCIB  DSECT                          COMMAND INPUT BUFFER-CIB Y02027
CIBQCBA  DS    F                        OPERATOR CONTROL QCB     Y02027
CIBPRI   DS    FL1                      CIB PRIORITY             Y02027
CIBLINK  DS    XL3                      LINK FIELD               Y02027
CIBVERB  DS    FL1                      COMMAND CODE             Y02027
CIBLEN   DS    XL1                      LENGTH OF CIB            Y02027
CIBLRN   DS    XL1                      REL LINE NUMBER          Y02027
CIBRC    DS    XL1                      COMPLETION CODE          Y02027
CIBECBA  DS    F                        ECB                      Y02027
CIBTNAME DS    XL8                      TERM OR DD NAME          Y02027
CIBICHNG DS    F                        ICHNG WORK AREA          Y02027
CIBEND   DS    0XL1                     END OF CIB               Y02027
CIBSIZE  EQU   CIBEND-IEDQCIB           LENGTH OF CIB            Y02027
         SPACE 2
         TTCBD                                                   Y02027
         SPACE 2
         TDEBAPD                                                 ×02027
         SPACE 2
         END
