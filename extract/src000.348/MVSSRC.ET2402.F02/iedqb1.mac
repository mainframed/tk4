         TITLE 'IEDQB1 - MCOUNT ROUTINE'
IEDQB1   CSECT
         SPACE 3
*  CHANGE ACTIVITY AS FOLLOWS:                                        *
******************** MICROFICHE FLAGS *********************** SUPT CODE
*        THIS MODULE HAS BEEN REWRITTEN FOR FETCH PROTECT        Y01004
*
         SPACE 3
***********************************************************************
*                                                                     *
*  TITLE: IEDQB1 - MCOUNT ROUTINE                                     *
*                                                                     *
*  MODULE NAME = IEDQB1                                               *
*                                                                     *
*  DESCRIPTIVE NAME = MCOUNT ROUTINE                                  *
*                                                                     *
*  COPYRIGHT = 'NONE'                                                 *
*                                                                     *
*  STATUS:  CHANGE LEVEL 5                                            *
*                                                                     *
* FUNCTION -- GETS THE MESSAGE COUNT FROM THE QCB AND ADJUSTS IT FOR  *
*    THE MESSAGE CURRENTLY BEING PROCESSED AND FOR ANY MESSAGE        *
*    PROCESSED BUT NOT MARKED SERVICED.                               *
*                                                                     *
* ENTRY POINT -- 'IEDQB1'                                             *
*    CALLED BY MCOUNT MACRO                                           *
*                                                                     *
* INPUT -- REGISTER 1 ADDRESS OF DCB                                  *
*                                                                     *
* OUTPUT -- REGISTERS:                                                *
*    REGISTER 1 - HAS MESSAGE COUNT                                   *
*    REGISTER 15 - RETURN CODES:                                      *
*         X'0' - GOOD RETURN                                          *
*         X'4' - TCAM NOT ACTIVE                                      *
*         X'8' - NO TCAM DEB FOUND                                    *
*         X'0C'-NO DESTINATION QCB FOR ENTRY                          *
*                                                                     *
* EXTERNAL REFERENCES --                                              *
*    AVT - ADDRESS VECTOR TABLE                                       *
*    QCB - QUEUE CONTROL BLOCK                                        *
*                                                                     *
* EXIT,NORMAL -- RETURN TO CALLER VIA A BRANCH R14 WITH REGISTERS SET *
*    AS INDICATED.                                                    *
*                                                                     *
* EXIT,ERROR -- N/A                                                   *
*                                                                     *
* TABLE/WORK AREAS -- ACCESS METHOD WORK AREA                         *
*                                                                     *
* ATTRIBUTES -- SERIALLY REUSABLE, ENABLED                            *
*                                                                     *
* CHARACTER CODE DEPENDENCY -- THE OPERATION OF THIS MODULE DOES NOT  *
*    DEPEND UPON AN INTERNAL REPRESENTATION OF THE EXTERNAL CHARACTER *
*    SET.                                                             *
*                                                                     *
* NOTES -- N/A                                                        *
*                                                                     *
***********************************************************************
         EJECT
RZERO    EQU   0                                                 Y01004
RPARM    EQU   1                                                 Y01004
RDEB     EQU   2                        DEB BASE                 Y02027
RAPCIB   EQU   3                        APCIB BASE               Y02027
RBASE    EQU   12                       BASE REGISTER
RSAVE    EQU   13                       SAVE AREA ADDRESS
RETURN   EQU   14                       RETURN REGISTER
RENTRY   EQU   15                       BRANCH REGISTER
         EJECT
         STM   RDEB,RBASE,TWNTY8(RSAVE) SAVE REGISTERS           Y02027
         BALR  RBASE,0                  SET UP BASE
         USING *,RBASE
         USING IHADCB,RPARM             DCB ADDRESSABILITY       Y02027
         USING IEDQDEB,RDEB             DEB ADDRESSABILITY       Y02027
         SPACE
IEDQB1   IEDHJN IEDQB101                MODULE ID
         ST    RSAVE,SAVEAREA+FOUR      USER SAVEAREA ADDR       Y01004
         LA    RENTRY,RC8               SET POSS RETURN CODE
         L     RDEB,DCBDEBAD            GET DEB
         TM    DEBTAMID,TCAM            TCAM DEB
         BNO   EXIT                     NO BRANCH
         SPACE 1
         LA    RENTRY,RC12              SET POSS RETURN CODE
         TM    DCBMACF2,DCBMRPUT+DCBMRWRT OUTPUT DCB             Y02027
         BNZ   EXIT                     YES, BRANCH - ERROR      Y02027
         SPACE 1
         SR    RZERO,RZERO              CLEAR REGISTER           Y02027
         ICM   RZERO,ADD,DEBPEWA        GET PEWA PTR             Y02027
         ST    RZERO,APNAME             SET PTR IN PARM LIST     Y02027
         DROP  RPARM                    FREE REGISTER            Y02027
         LA    RPARM,APPARM             SET PARM REG             Y02027
         MVI   APPCOMCD,MCNTCD          SET MCOUNT CODE          Y02027
         LA    RZERO,FOUR               SET FUNCTION CODE REG    Y02027
         SPACE 1
         AQCTL                          ISSUE SVC                Y02027
         SPACE 1
         LA    RDEB,RC80                CHECK FOR ERROR RET CD   Y02027
         CR    RDEB,RENTRY              IS TCAM ACTIVE           Y02027
         BNE   EXIT                     BRANCH IF YES - GOOD     Y02027
*                                       COMPLETION               Y02027
         LA    RENTRY,RC4               MPP EXPECTS RET CD = 4   Y02027
EXIT     EQU   *                                                 Y02027
         L     RSAVE,SAVEAREA+FOUR      RESTORE SAVE AREA PTR    Y02027
         RETURN (2,12),T,RC=(15)        RETURN TO CALLER         Y02027
         SPACE 2
         EJECT
**************************************************************** Y02027
*        C O N S T A N T S  AND  E Q U A T E S                 * Y02027
**************************************************************** Y02027
         SPACE 2
SAVEAREA DS    18F                      SAVE AREA                Y02027
ADD      EQU   7                        ADDRESS MASK             Y02027
FOUR     EQU   4                        OFFSET VALUE             Y02027
RC4      EQU   4                        RETURN CODE 4            Y02027
RC8      EQU   8                        RETURN CODE = 8          Y02027
RC12     EQU   12                       RETURN CODE = 12         Y02027
OPEN     EQU   X'10'                    DCB OPEN FLAG            Y02027
TCAM     EQU   X'C0'                    TCAM DEB ID              Y02027
TWNTY8   EQU   28                       OFFSET VALUE             Y02027
RC80     EQU   X'80'                    R C SET BY SVC 102 IF    Y02027
*                                       TCAM NOT ACTIVE          Y02027
MCNTCD   EQU   6                        MCOUNT COMMAND CODE      Y02027
         SPACE 1
**************************************************************** Y02027
*              MAP OF INPUT DATA PROVIDED TO SVC 102           * Y02027
**************************************************************** Y02027
         DS    0F                       FORCE ALIGNMENT          Y02027
APCIBMAP EQU   *                                                 Y02027
APPASWD  DC    AL4(0)                   POINTER TO PASSWORD      Y02027
APECB    DC    F'0'                     ECB FOR WAIT IN SVC 102  Y02027
*                                       AND SUBSEQUENT XMPOST    Y02027
APPCBAD  DC    AL4(0)                   PCB POINTER              Y02027
APPCOMCD DS    0XL1                     COMMAND VERB CODE        Y02027
APUSWKA  DC    AL4(0)                   PTR TO USER WORK AREA    Y02027
APNAME   DC    AL4(0)                   PTR TO TERMINAL NAME     Y02027
APPARM   DS    0F                       PARAMETER FOR SVC 102    Y02027
         DC    X'84'                    EOL X'80' + FUNCTION     Y02027
*                                       CODE X'04'               Y02027
         DC    AL3(APCIBMAP)            PTR TO INPUT DATA        Y02027
         SPACE 1
**************************************************************** Y02027
         SPACE 1
         TDEBAPD                                                 Y02027
         SPACE 1
         DCBD DSORG=PS                                           Y02027
         END
