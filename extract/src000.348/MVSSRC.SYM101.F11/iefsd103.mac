         TITLE 'IEFSD103, INITIATOR ATTACH INTERFACE'
IEFSD103 CSECT
*        SEE CHANGE LEVEL IN PROLOG FOR FLAGGING INFORMATION.
         BALR  R12,0
*                                                                     *
***********************************************************************
*                                                                     *
*    MODULE NAME = IEFSD103
*
*    DESCRIPTIVE NAME = ATTACH INTERFACE ROUTINE
*
*    COPYRIGHT = NONE
*
*    STATUS = VS/2 RELEASE 2
*
*    FUNCTION = INITIALIZE THE ATTACH PARAMETER LIST
*
*    OPERATION =
*      1)  MOVE THE EXPANSION OF THE ATTACH MACRO (LIST FORM) TO
*      THE STORAGE AREA GOTTEN FOR THE ATTACH LIST BY IEFSD162.
*
*      2) NEXT THE ENTRY POINT OF THE PROGRAM NAME
*         IS PUT IN THE ATTACH LIST. THE ADDRESS OF THE
*         ATTACH ECB AND FETCHLIB DCB ARE ALSO PUT IN
*         THE LIST.
*
*      3) NOW IT CHECKS FOR A JOBLIB DCB, IF THERE
*         IS, ATECBFLG HAS BIT X'02' SET ON TO INDICATE
*         TASKLIB TO ATTACH. THEN THE JOBLIB DCB POINTER
*         IS PUT IN THE ATTACH LIST.
*
*      4)  IF THE LCT INDICATES THAT THE 'DON'T SHARE SWA'       Y02621
*      OPTION IS NOT TO BE USED (IE., FOR THE MASTER SCHEDULER)  Y02621
*      CONTINUE WITH STEP 5. OTHERWISE, INDICATE IN THE ATTACH   Y02621
*      LIST WHICH SWA SUBPOOL IS TO BE INDICATED AS 'DON'T       Y02621
*      SHARE.' (THE JSCB INDICATES WHICH SUBPOOL IS NOT TO BE    Y02621
*      SHARED.)                                                  Y02621
*
*      5)  THEN THE ASCB PRIORITY MUST BE CACLULATED.            Y02669
*      (IF DPRTY IS SPECIFIED ON THE EXEC STATEMENT, IT IS OF    Y02669
*      THE FORM 'DPRTY=(A,B).)                                   Y02669
*
*         IF DPRTY WAS SPECIFIED, THE MEMORY PRIORITY WILL       Y02669
*         BE (16*A) + B. IF DPRTY WAS NOT SPECIFIED, THE MEMORY  Y02669
*         PRIORITY WILL BE THE APG PRIORITY FROM THE CVT. THE    Y02669
*         MEMORY PRIORITY IS SAVED FOR IEFSD263 IN IEFPARAM.     Y02669
*
*         TCB LIMIT AND DISPATCHING PRIORITIES NEED NOT          Y02669
*         BE ADJUSTED WHEN THE PROBLEM PROGRAM IS ATTACHED.      Y02669
*         ALTHOUGH THE NUMERICAL PRIORITIES WILL BE THE SAME,    Y02669
*         THE SUBTASK                                            Y02669
*         ATTACHED BY IEFSD263 WILL ALWAYS HAVE A POSITION ON    Y02669
*         THAT MEMORY'S TCB QUEUE JUST BELOW ITS MOTHER TCB.     Y02669
*
*      6) SAVE THE PERFORMANCE GROUP NUMBER FOR IEFSD263 IN      Y02655
*      IEFPARAM.                                                 Y02655
*
*    NOTES
*
*        CHARACTER CODE DEPENDENCIES = NONE
*
*        DEPENDENCIES = ATTACH PARAMETER LIST
*
*        RESTRICTIONS = NONE
*
*        REGISTER CONVENTIONS = STANDARD
*
*        PATCH LABEL = PATCH
*
*    MODULE TYPE = PROCEDURE
*
*        PROCESSOR = ASSEMBLER
*
*        MODULE SIZE = 1000 BYTES
*
*        ATTRIBUTES = PAGED LPA, ZERO PSW PROTECT KEY,
*                     REENTRANT, REFRESHABLE, SUPERVISOR STATE
*
*    ENTRY POINT = IEFSD103
*
*        PURPOSE = INITIALIZE THE ATTACH PARAMETER LIST
*
*        LINKAGE = FROM IEFSD162, VIA BRANCH
*
*        INPUT DATA = REG 1 POINTS TO LCT
*                     LCTQDRTY = ADDRESS OF THE CSCB
*                     LCTSCTAD = ADDRESS OF THE SCT
*                     LCTJCTAD = ADDRESS OF THE JCT
*
*        REGISTERS SAVED = NONE
*
*        REGISTERS CONTENTS DURING PROCESSING =
*        REGISTER 5 POINTS TO LCT
*        REGISTER 10 POINTS TO ATTACH LIST
*
*        REGISTERS RESTORED = NONE
*
*    EXIT NORMAL = TO IEFSD263 VIA BRANCH
*
*        CONDITIONS = ALWAYS
*
*        OUTPUT DATA = ATTACH PARAMETER LIST IS FILLED IN.
*        (POINTED TO BY IEFPARAM.)
*        IEFPARAM CONTAINS MEMORY DISPATCHING PRIORITY FOR STEP  Y02669
*        AND PERFORMANCE GROUP NUMBER.                           Y02655
*
*        RETURN CODE = NONE
*
*    EXIT ERROR = NONE
*
*    EXTERNAL REFERENCES
*
*        ROUTINES = NONE
*
*        DATA AREAS = NONE
*
*        CONTROL BLOCKS = LCT, JCT, SCT, JSCB, TCB, CVT          Y02621
*
*        TABLES = ATTCHLST - ATTACH PARAMETER LIST, IEFPARAM
*
*        MACROS = FREEMAIN, ATTACH (LIST FORM)
*
*        ENQUEUE RESOURCES = NONE
*
*        CHANGE LEVEL = SEE MODID MACRO AT BEGINNING OF MODULE
*        CODE HAS BEEN ADDED FOR THE FOLLOWING SUPPORT CODES:
*                                                                Y02621
*                                                                Y02655
*                                                                Y02669
*
*    MESSAGES = NONE
*
***********************************************************************
         EJECT
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         USING *,12
         USING IEFLOT,R5                                           I272
         USING IEFPARAM,R9                                         I270
         USING ATTCHLST,R10                                        I270
         USING IEFJCT,R6
         USING IEFSCT,R4                                          20032
         SPACE
IEFSD103 MODID                                                   Y01886
         SPACE
         LR    R9,R1               SAVE INPUT PARM ADDRESS        20032
         L     R5,IEFLCTAD        ADDRESS OF LCT                  20032
         L     R4,LCTSCTAD        ADDRESSABILITY FOR THE SCT       I270
         L     R6,LCTJCTAD        ADDRESSABILITY FOR THE JCT       I270
*****************************************************************
*
*        INITIALIZE ATTACH PARAMETER LIST
*
*****************************************************************
         L     R10,IEFREMLS       EST ADDR OF ATTACH LIST          I270
         LA    R10,X0(R10)        CLEAR HIGH ORDER BYTE            I270
         MVC   ATTCHLST(ATLSTSIZ),ATTLIST MOVE LIST                I270
         MVC   ATPRGNM(X8),SCTPGMNM PROGRAM NAME FROM SCT          I270
         LA    R8,ATPRGNM         ADDRESS OF PROGRAM NAME          I270
         ST    R8,ATPGNADA        STORE ENTRY ADDRESS              I270
         LA    R7,IEFAECB         ADDRESS OF ECB                   I270
         L     R8,IEFDCBAD        ADDRESS OF DCB                   I270
         LA    R8,X0(R8)          CLEAR HIGH ORDER BYTE            I270
         ST    R7,ATECBADR        ECB ADDRESS IN ATTACH LIST       I270
         ST    R8,ATDCBADA        DCB ADDRESS IN ATTACH LIST       I270
         L     R1,LCTJOBLB        TASKLIB DCB POINTER             20021
         LA    R1,D0(R1)           CLEAR HIGH-ORDER BYTE          20021
         LTR   R1,R1              IS THERE A JOBLIB DCB           20030
         BZ    NOJOBLB            BRANCH NO JOBLIB                20030
         OI    ATECBFLG,ATTSKLB   INDICATE TASKLIB TO ATTACH      20030
         ST    R1,ATTASKLB        STORE IN ATTACH LIST            20021
NOJOBLB  EQU   *                                                  20030
         MVC   ATJSCBA(X3),LCTJSCB+X1 PUT JSCB ADDR IN ATTLST
         TM    LCTOPSW1,LCTDPSWA   CHECK WHETHER 'DON'T SHARE    Y02621
*                                  SWA' OPTION IS TO BE BYPASSED Y02621
         BO    NOSHRSWA            DON'T SET OPTION (PROBABLY    Y02621
*                                  MASTER SCHEDULER)             Y02621
         L     R11,LCTTCBAD                                      Y02621
         USING TCB,R11                                           Y02621
         L     R11,TCBJSCB         POINT TO CURRENT JSCB         Y02621
         USING IEZJSCB,R11                                       Y02621
         MVC   ATTNSHLV+3(L'JSCBSWSP),JSCBSWSP  MOVE SWA SUBPOOL Y02621
*                                  NUMBER NOT TO BE SHARED       Y02621
NOSHRSWA EQU   *                                                 Y02621
*****************************************************************
*
*        CALCULATE AND SAVE MEMORY PRIORITY
*
*****************************************************************
         TM    SCTSDP,NOPRTY       WAS DEFAULT DPRTY            YM03629
*                                  SUBSTITUED BY C/I            YM03629
         BO    SETAPG              YES, GO PICK UP CORRECT      YM03629
*                                  DEFAULT                      YM03629
         LA    R0,HEX0F            LOAD MASK                     Y02669
         IC    R1,SCTSDP           LOAD PRIMARY PRIORITY         Y02669
         NR    R1,R0               CLEAR ALL BUT LAST 4 BITS     Y02669
         SLL   R1,M16              MULTIPLY BY 16                Y02669
         IC    R0,SCTSDP+1         LOAD SECONDARY PRIORITY       Y02669
         AR    R1,R0               ADD 2 VALUES TO GET 8-BIT     Y02669
*                                  PRIORITY                      Y02669
         B     SKIPAPG             SKIP ELSE CLAUSE             YM03629
SETAPG   EQU   *                                                YM03629
         L     R11,CVTPTR                                        Y02669
         USING CVTMAP,R11                                        Y02669
         IC    R1,CVTAPG           GET APG PRIORITY VALUE IF     Y02669
*                                  STEP PRIORITY WAS NOT GIVEN   Y02669
SKIPAPG  EQU   *                                                 Y02669
         STC   R1,PARNMEMP         SAVE MEMORY PRIORITY FOR      Y02669
*                                  IEFSD263                      Y02669
         MVC   PARPRFMF,SCTPRFMF   SAVE PERFORMANCE GROUP FOR    Y02669
*                                  IEFSD263                      Y02669
         LR    R1,R9                    RESTORE PARAMETER LIST
         L     R15,VCON263                                       Y02652
         BR    R15                 EXIT TO IEFSD263              Y02652
         EJECT
ATTLIST  ATTACH JSTCB=YES,SVAREA=YES,SZERO=YES,                        X
               KEY=PROP,SM=PROB,JSCB=0,SF=L,DISP=NO              Y01022
         SPACE 3
VCON263  DC    V(IEFSD263)         EXIT POINT                    Y02652
PATCH    DC    6F'0'               PATCH AREA                    Y02652
         EJECT
X0       EQU   0                                                   I272
X1       EQU   1                                                   I272
X3       EQU   3                                                   I272
X8       EQU   8                                                   I272
TCORE    EQU   253                                                 I272
HEX00    EQU   X'00'                                               I270
HEX0F    EQU   X'0F'                                               I241
D0       EQU   0                   DISPLACEMENT                   20032
M16      EQU   4                   USED TO MULTIPLY BY 16        Y02669
NOPRTY   EQU   X'10'               SET BY C/I TO INDICATE       YM03629
*                                  DEFAULT DPRTY                YM03629
         EJECT                                                     I272
         IEFZB630
         SPACE 4
         IEZATTCH                                                  I270
         SPACE 4
IEFLOT   DSECT
         IEFALLCT
         SPACE 4
IEFJCT   DSECT
         IEFAJCTB
         SPACE 4
IEFSCT   DSECT
         IEFASCTB
         SPACE 4
         CVT   DSECT=YES
         SPACE 4
         IKJTCB
         SPACE 4
         IEZJSCB                                                   I272
         END
