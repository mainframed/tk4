BB       TITLE '''IEDQBB'' CHECKPOINT REQUEST'
IEDQBB   CSECT
*C117000,315000,342000,369000,486000,504000,540000,558000,577000 S22025
*C603000-639000,667000-737000,772000-800000                      S22025
*A588000,591000,648000,820000-825000                             S22025
*C697000,821000                                                @Y17XAML
***********************************************************************
*TITLE -- 'IEDQBB' CHECKPOINT REQUEST ROUTINE                         *
*                                                                     *
*                                                                     *
*STATUS -- VERSION 10.0                                               *
*                                                                     *
*FUNCTION -- THIS ROUTINE SETS THE CHECKPOINT REQUEST FLAG IN THE SCB *
*   IF CHECKPOINT IS IN THE SYSTEM. IF ENTRY WAS FROM IEDQBD, THE     *
*   ERB IS TPOSTED TO IEDQBD.                                         *
*                                                                     *
*ENTRY POINT -- IEDQBB                                           S22025
*                                                                     *
*   CALLING SEQUENCE -                                                *
*                  L     R12,AVTMSGS-1                                *
*                  IC    RX,INDEX                                     *
*                  L     R12,0(RX,R12)                                *
*                  BR    R12                                          *
*                                                                     *
*INPUT -- ENTRY IS FROM 'IEDQUI' USER INTERFACE ROUTINE, OR           *
*   'IEDQBD' BUFFER DISPOSITION SUBTASK.
*         PARAMETERS ARE PASSED IMPLICITLY IN REGISTERS AS FOLLOWS:   *
*                  R0    ZERO (0) IF ENTRY FROM IEDQBD - ELSE NON ZERO*
*                  R3    ADDRESS OF STATION CONTROL BLOCK (SCB)       *
*                  R4    ADDRESS OF LINE CONTROL BLOCK (LCB)          *
*                  R11   BASE FOR TCAM DISPATCHER                     *
*                  R12   BASE FOR THIS ROUTINE                        *
*                  R13   ADDRESS OF AVTSAVE2                          *
*                                                                     *
*OUTPUT -- THE CHECKPOINT REQUEST BIT IS SET IN THE SCB.              *
*                                                                     *
*EXTERNAL ROUTINES -- NONE                                            *
*                                                                     *
*EXITS-NORMAL -- 'IEDQUI+4' WITH R15=0 IF ENTRY WAS FROM IEDQUI  S22025
*                 DISPATCHER AT ENTRY POINT DSPPOST IF FROM IEDQBD    *
*                                                                     *
*EXITS-ERROR -- 'IEDQUI+4' WITH R15=4, IF ENTRY WAS FROM IEDQUI  S22025
*                                                                     *
*TABLES/WORKAREAS --                                                  *
*   DSECTS USED - LCB,SCB,AVT,DISP,PRF                           S22025
*                                                                     *
*ATTRIBUTES -- REENTRANT, REFRESHABLE, ENABLED, RESIDENT, PROBLEM     *
*              PROGRAM MODE.                                          *
*                                                                     *
*NOTES -- THE OPERATION OF THIS MODULE DOES NOT DEPEND UPON A         *
*   PARTICULAR INTERNAL REPRESENTATION OF THE EXTERNAL CHARACTER SET  *
*                                                                     *
***********************************************************************
*
*
*              REGISTERS
*
R0       EQU   0                        PARAMETER REGISTER       S22025
R1       EQU   1                        PASSED ELEMENT (LCB)
R2       EQU   2                        ADDRESS OF VCON LIST     S22025
RSCB     EQU   3                        SCB ADDRESS
RLCB     EQU   4                        LCB ADDRESS
RPREFIX  EQU   6                        BUFFER ADDRESS IF FROM MH
R9       EQU   9                        AVT BASE                 S22025
R11      EQU   11                       ADDRESS OF DISPATCHER
R12      EQU   12                       BASE REGISTER            S22025
R13      EQU   13                       ADDRESS OF AVTSAVE2
R14      EQU   14                   REG14 FOR BAL TO DSPCHAIN  @Y17XACX
R15      EQU   15                       RETURN CODE              S22025
FOUR     EQU   4                        USED AS OFFSET           S22025
SEVEN    EQU   7                        NON-ZERO CONDITION       S22025
         EJECT
         USING *,R12                    BASE                     S22025
         USING IEDQDISP,R11             DISPATCHER BASE          S22025
         USING IEDQLCB,RLCB             LCB BASE                 S22025
         USING IEDQSCB,RSCB             SCB BASE                 S22025
         USING IEDQPRF,RPREFIX          PREFIX BASE              S22025
         LA    R15,AVTECD4              SET BAD RETURN
         LTR   R0,R0                    IS MH IN CONTROL         S22025
         BZ    BDEXIT                   BRANCH IF ENTRY FROM BD  S22025
         USING IEDQAVTD,R9                                       S22025
         TM    AVTCKFLG,AVTCKTAC        IS CHECKPOINT IN SYSTEM@Y17XAML
         BZ    MHCONT                   BRANCH NO                S22025
         OI    SCBSTATE,SCBCKPT         SET CHECK-POINT FLAG     S22025
         SR    R15,R15                  SET GOOD RETURN CODE     S22025
MHCONT   EQU   *                                                 S22025
         LH    R0,PRFSIZE               SIZE OF BUFFER
         LTR   R0,R0                    IS SIZE = 0
         BC    SEVEN,EXIT               RETURN IF NOT ZERO       S22025
         NI    SCBSTATE,X'FF'-SCBCKPT   SET CHECK-POINT REQUEST  S22025
EXIT     EQU   *                                                 S22025
         L     R12,AVTUI                GET RETURN INTERFACE ADDRS22025
         B     FOUR(R12)                BRANCH TO RETURN ROUTINE S22025
*
BDEXIT   EQU   *
         USING AVTSAVE2,R13                                      S22025
         TM    AVTCKFLG,AVTCKTAC        IS CHECKPOINT IN SYSTEM@Y17XAML
         BZ    BDCONT                   BRANCH NO                S22025
         OI    SCBSTATE,SCBCKPT         SET CHECK-POINT FLAG     S22025
         SR    R15,R15                  SET GOOD RETURN CODE     S22025
BDCONT   EQU   *                                                 S22025
         L     R2,AVTMSGS-1             ADDRESS OF VCON LIST
         MVC   LCBERBQB,1(R2)           ADDRESS OF IEDQBD TO QCB @
         ST    R1,LCBERBLK-1            SET LINK FIELD
         MVI   LCBERBPY,PRIRCQCB        SET PRIORITY
         LA    R1,LCBERB                ERB ADDRESS TO POST TO IEDQBD
         BAL   R14,DSPCHAIN             EXIT TO DISPATCHER     @Y17XACX
*
         EJECT
         TPRFD
         TDISPD
         TPRIOR
         TLCBD
         TSCBD
         TAVTD
         END
