AG01     TITLE '''IEDQAG'' -- MESSAGE LIMIT ROUTINE'
IEDQAG   CSECT
         SPACE 3
*  CHANGE ACTIVITY AS FOLLOWS
******************** MICROFICHE FLAGS *********************** SUPT CODE
*A444000,468000,504000,512000,772000,800000                     SA55944
*C512000,528000-540000,676000,704000                            SA55944
*D496000                                                        SA55944
*C532000                                                         A42405
*A588000,856000                                                  A42405
*A648000                                                        SA59976
*D480000-488000,506000,592000-792000,144000-180000              SA59976
*C384000                                                         S22024
*A644000,858000                                                  S22024
*A644730-644760                                                  Y06327
*C654000                                                       @OS78326
*
*C000000-999999                                                @Y17XAMJ
         SPACE 3
***********************************************************************
*                                                                     *
*TITLE 'IEDQAG' MESSAGE LIMIT ROUTINE                                 *
*                                                                     *
*  MODULE NAME = IEDQAG                                               *
*                                                                     *
*  DESCRIPTIVE NAME = MESSAGE LIMIT ROUTINE                           *
*                                                                     *
*  COPYRIGHT = 'NONE'                                                 *
*                                                                     *
*  STATUS:  CHANGE LEVEL 10                                    @Y17XAMJ
*                                                                     *
*                                                                     *
*FUNCTION -- THE PURPOSE OF THIS ROUTINE IS TO LIMIT THE NUMBER OF    *
*   MESSAGES TO OR FROM A TERMINAL DURING A SINGLE TRANSMISSION       *
*   SEQUENCE.                                                         *
*                                                                     *
*ENTRY POINTS --                                                      *
*        'IEDQAG' TO LIMIT NUMBER OF MESSAGES SENT OR RECEIVED IN     *
*   A TRANSMISSION SEQUENCE.  CALLING SEQUENCE IS                     *
*                             L    15,IEDMSGS-1                       *
*                             L    15,AGOFF(,15)                      *
*                             BALR 14,15                              *
*   WHERE AGOFF IS A MACRO-GENERATED OFFSET INTO IEDQMSGS OF THE      *
*   ADDRESS OF IEDQAG.                                                *
*                                                                     *
*INPUT -- REGISTER 1 - ADDRESS OF A TWO-BYTE PARAMETER LIST.  THE     *
*   FORMAT OF THE PARAMETER LIST IS:                                  *
*                                                                     *
*        *****************                                            *
*        * (UN-  * LIMIT *                                            *
*        * USED) * VALUE *                                            *
*        *       *       *                                            *
*        *****************                                            *
*                                                                     *
*   REGISTER 13 - ADDRESS OF THE SECOND SAVE-AREA IN THE AVT (AND     *
*   THEREFORE THE ADDRESS OF THE AVT).                                *
*                                                                     *
*   REGISTER 14 - RETURN ADDRESS.                                     *
*                                                                     *
*   REGISTER 15 - ENTRY POINT ADDRESS.                                *
*                                                                     *
*OUTPUT -- SCBMLMTN = 0 LIMIT NOT REACHED                      @YM07737
*          SCBMLMTN = 1 LIMIT REACHED                          @YM07737
*                                                                     *
*EXTERNAL ROUTINES --                                                 *
*        'IEDQTNT' TO CONVERT DESTINATION TERMINAL OFFSET TO AN       *
*   ADDRESS.                                                          *
*                                                                     *
*EXITS-NORMAL -- RETURN TO THE MH.                             @YM07737
*                                                                     *
*EXITS-ERROR -- N/A.                                                  *
*                                                                     *
*TABLES/WORK AREAS -- DSECTS OF THE AVT, BUFFER PREFIX, LCB,   @YM07737
*   QCB,SCB, AND TRM.                                           S22024*
*                                                                     *
*ATTRIBUTES -- SERIALLY REUSABLE,REFRESHABLE, ENABLED, RESIDENT,      *
*   PROBLEM PROGRAM MODE.                                             *
*                                                                     *
*NOTES -- THE OPERATION OF THIS MODULE DOES NOT DEPEND UPON AN        *
*   INTERNAL REPRESENTATION OF THE EXTERNAL CHARACTER SET.            *
*                                                                     *
***********************************************************************
*
         EJECT
********* REGISTER EQUATES *********
         SPACE
R0       EQU   0                        WORK REGISTER
RPARM    EQU   1                        PARM REG & WORK REG    @YM07737
RSCB     EQU   3                        SCB BASE REG           @YM07737
RLCB     EQU   4                        LCB/PLCB BASE REG      @YM07737
RPRF     EQU   6                        BUFFER BASE REG        @YM07737
RTRM     EQU   8                        TRM BASE REG           @YM07737
RBASE    EQU   12                       MODULE BASE REGISTER   @YM07737
RAVT     EQU   13                       AVT BASE REGISTER      @YM07737
R14      EQU   14                       RETURN REGISTER
R15      EQU   15                       LINK REG & WORK REG
         SPACE
********* OTHER EQUATES *********
ZERO     EQU   0                                                SA55944
ONE      EQU   1                        INCREMENT VALUE OF ONE
         EJECT
         USING IEDQSCB,RSCB             SCB ADDRESSABILITY     @YM07737
         USING IEDQLCB,RLCB             LCB ADDRESSABILITY     @YM07737
         USING IEDQPRF,RPRF             BFR ADDRESSABILITY     @YM07737
         USING IEDNTRM,RTRM             TRM ADDRESSABILITY     @YM07737
         USING IEDQAG01,RBASE           MODULE ADDRESSABILITY  @YM07737
         USING AVTSAVE2,RAVT            AVT ADDRESSABILITY     @YM07737
         SPACE
IEDQAG01 EQU   *
         STM   R14,RBASE,AVTSAVE2       SAVE REGISTERS         @YM07737
         LR    RBASE,R15                LOAD BASE REGISTER     @YM07737
IEDQAG   IEDHJN MSGLIMIT,HJN                                    SA55944
         SPACE
         MVC   AVTPARM(ONE),ONE(RPARM)  GET LIMIT FROM PARM    @YM07737
*                                       LIST                   @YM07737
         L     RPRF,AVTADBUF            SET BFR BASE REG       @YM07737
         LH    R0,PRFSIZE               PICK UP SIZE FIELD
         LTR   R0,R0                    IS IT ZERO
         BZ    ZERORTN                  YES, RETURN IMMEDIATELY
         SPACE
         L     RLCB,PRFLCB-1            SET LCB/PLCB BASE REG  @YM07737
         CLI   LCBFLAG1,LCBPLCB         PLCB                   @YM07737
         BE    NCPCHECK                 BR YES                 @YM07737
         SPACE
EPCHECK  EQU   *                        EP MSGLIMIT CHECKS     @YM07737
         TM    LCBSTAT2,LCBDIAL         IS IT DIAL
         BO    ZERORTN                  YES, RETURN IMMEDIATELY
         CLI   LCBRSKEY,DSPBUFSC        BUFFERED TERMINAL        A42405
         BE    ZERORTN                  YES, RETURN IMMEDIATELY  A42405
         TM    LCBSTAT1,LCBSENDN        SEND OR RECEIVE SIDE
         BO    GETSCB                   BR IF SEND             @YM07737
         CLI   LCBRSKEY,DSPPUTSC        IS SOURCE APPL PGM     @YM07737
         BE    ZERORTN                  YES, RETURN IMMEDIATELY
         B     GETSCB                   GO GET SCB             @YM07737
         SPACE
NCPCHECK EQU   *                        NCP MSGLIMIT CHECKS  IT@YM07737
         TM    LCBSTAT5,LCBLUNIT        LU                     @YM07737
         BO    ZERORTN                  BR YES                 @YM07737
         TM    LCBTSOB,LCBTSBUF         TSO IN SESSION?          Y06327
         BO    ZERORTN                  BRANCH YES               Y06327
         TM    LCBSTAT1,LCBSENDN        IS LCB SENDING           S22024
         BO    GETSCB                   BRANCH ON YES          @YM07737
         TM    LCBSTAT2,LCBDIAL         DIAL TRM               @YM07737
         BO    ZERORTN                  BR YES                 @YM07737
         LH    RPARM,LCBTTCIN           GET TERMINAL TNT INDEX @YM07737
         N     RPARM,AVTCLRHI           CLEAR HIGH ORDER HALF  @YM07737
         ST    R14,AVTPARM3             SAVE REGISTER            S22024
         L     R15,AVTRNMPT             ADDRESS OF TNT CODE      S22024
         BALR  R14,R15                  LINK TO TNT ROUTINE      S22024
         L     R14,AVTPARM3             RESTORE REGISTER         S22024
         LA    RTRM,TRMPRFSZ            GET TRM PREFIX SIZE    @YM07737
         LNR   RTRM,RTRM                MAKE IT NEGATIVE       @YM07737
         AR    RTRM,RPARM               SET TRM BASE           @YM07737
         IC    R15,TRMRLMCT             PICK UP MESSAGE COUNT    S22024
         LA    R15,ONE(,R15)            ADD ONE                  S22024
         STC   R15,TRMRLMCT             STORE NEW COUNT          S22024
         SR    R15,R15                  SET RETURN CODE          S22024
         CLC   TRMRLMCT(ONE),AVTPARM    HAS LIMIT BEEN REACHED   S22024
         BL    ZERORTN                  BR NO                  @YM07737
         MVI   TRMRLMCT,AVTEZERO        RESET MESSAGE COUNT      S22024
         L     RSCB,LCBSCBA-1           GET SCB ADDRESS        @YM07737
         OI    SCBBSCFM,SCBMLMTN        INDICATE LIMIT REACHED   S22024
         B     ZERORTN                  RETURN TO MH             S22024
GETSCB   EQU   *                                               @YM07737
         L     RSCB,LCBSCBA-1           GET SCB ADDRESS        @YM07737
         TM    SCBSTATE,SCBLCK1N+SCBMSGLN  LOCK MODE            SA59976
         BM    ZERORTN                  RETURN, IF LOCK EXTEND @OS78326
         IC    R15,SCBSNDCT             PICK UP MESSAGE COUNT
         LA    R15,ONE(,R15)            ADD ONE
         STC   R15,SCBSNDCT             STORE IT BACK
         SR    R15,R15                  SET RTN CODE IF BELOW LIMIT
         CLC   SCBSNDCT(ONE),AVTPARM    TEST IF LIMIT REACHED
         BL    ZERORTN                  BR IF NOT REACHED      @YM07737
         SPACE
         OI    SCBBSCFM,SCBMLMTN        YES, INDICATE IN SCB
         MVI   SCBSNDCT,AVTEZERO        RESET MSG COUNT TO ZERO
         SPACE 3
ZERORTN  EQU   *
         LM    R14,RBASE,AVTSAVE2       RESTORE REGISTERS      @YM07737
         SR    R15,R15                  SET 'NOT EXECUTED' RTN CODE
         BR    R14                      RETURN TO MH
         EJECT
********* DSECTS *********
         TAVTD
         EJECT
         TDISPD
         EJECT
         TLCBD
         EJECT
         TPRFD
         EJECT
         TSCBD
         EJECT
         TTRMD ,                                                 S22024
         END
