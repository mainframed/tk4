         TITLE 'IEDAYX - TSO LINKOR ROUTINE'
IEDAYX   CSECT
*A330000                                                         S22029
*C370000-390000                                                  S22029
*C115800-315800                                                  S21903
*D500000                                                         S21903
***********************************************************************
*TITLE  'IEDAYX'  TSO INMSG/OUTMSG LINKER                             *
*                                                                     *
*STATUS -- CHANGE LEVEL 20.0                                          *
*                                                                     *
*FUNCTION -- THE TSO INMSG/OUTMSG LINKER PROVIDES LINKAGE TO THE      *
*   ATTEN AND HANGUP ROUTINES (IEDQATTN AND IEDAYH, RESPECTIVELY).    *
*   IF LINKAGE IS TO HANGUP, THE ROUTINE ADDRESS IS GOTTEN FROM THE   *
*   MACRO EXPANSION PASSED TO THIS ROUTINE.  FOR LINKAGE TO ATTEN,    *
*   THE ROUTINE ADDRESS IS GOTTEN FROM THE STARTMH MACRO POINTED TO   *
*   IN THE DCB.  BEFORE BRANCHING TO THE ROUTINE, THE ERB PRIORITY    *
*   IS SET, AND THE ERB IS POSTED TO BUFFER DISPOSITION.              *
*                                                                     *
*ENTRY POINT -- IEDAYX - TO PROVIDE LINKAGE TO THE ATTEN OR HANGUP    *
*   ROUTINE WHEN ATTEN OR HANGUP MACRO INSTRUCTIONS ARE CODED IN THE  *
*   INMSG OR OUTMSG SUBGROUPS.                                        *
*   CALLING SEQUENCE          L    R12,AVTMSGS+OFFSET                 *
*                             BR   R12                                *
*                                                                     *
*INPUT -- IEDAYX IS CALLED BY BUFFER DISPOSITION WHEN ATTEN OR        *
*   HANGUP MACRO INSTRUCTIONS ARE CODED IN THE INMSG OR OUTMSG        *
*   SUBGROUPS, WITH THE FOLLOWING REGISTERS SET.                      *
*   R1 HAS THE ADDRESS OF A LIST OF THINGS TO BE POSTED, OR ZERO      *
*   R3 HAS THE SCB ADDRESS                                            *
*   R4 HAS THE LCB ADDRESS                                            *
*   R8 HAS THE ADDRESS OF THE MACRO EXPANSION                         *
*   R11 HAS THE DISPATCHER ADDRESS                                    *
*   R12 HAS THE ENTRY POINT ADDRESS                                   *
*   R13 HAS THE ADDRESS OF AVTSAVE2                                   *
*                                                                     *
*   FORMAT OF MACRO EXPANSION:                                        *
*   BYTE 1 -- X'02' = RECALL NECESSARY                                *
*             X'01' = UNCONDITIONAL EXECUTION                         *
*   BYTE 2 -- LENGTH OF MACRO EXPANSION                               *
*             X'01' = AND FUNCTION, AS OPPOSED TO OR FUNCTION         *
*   BYTE 3 -- X'02' = ADDRESS OF ROUTINE IN MACRO EXPANSION           *
*             X'01' = ADDRESS OF ROUTINE IN STARTMH MACRO             *
*   BYTES 4-8 -- MASK, IF CONDITIONAL EXECUTION                       *
*                                                                     *
*OUTPUT -- THE ERB IS POSTED TO BUFFER DISPOSITION, AND REGISTER 12   *
*   CONTAINS THE ENTRY POINT ADDRESS IN THE ATTEN OR HANGUP ROUTINE.  *
*                                                                     *
*EXTERNAL ROUTINES -- NONE                                            *
*                                                                     *
*EXITS-NORMAL -- TO THE ATTEN ROUTINE OR THE HANGUP ROUTINE           *
*                                                                     *
*EXITS-ERROR -- NONE                                                  *
*                                                                     *
*TABLES/WORKAREAS -- AVT, LCB, DCB.                                   *
*                                                                     *
*ATTRIBUTES -- SERIALLY REUSABLE, REFRESHABLE, ENABLED, RESIDENT,     *
*   PROBLEM PROGRAM MODE                                              *
*                                                                     *
*NOTES -- THE OPERATION OF THIS MODULE DOES NOT DEPEND UPON A         *
*   PARTICULAR INTERNAL REPRESENTATION OF THE EXTERNAL CHARACTER      *
*   SET.                                                              *
*                                                                     *
***********************************************************************
R1       EQU   1                        REGISTER EQUATE          S21903
RLCB     EQU   4                        REGISTER EQUATE          S21903
R7       EQU   7                        REGISTER EQUATE          S21903
R8       EQU   8                        REGISTER EQUATE          S21903
RDCB     EQU   10                       REGISTER EQUATE          S21903
R12      EQU   12                       REGISTER EQUATE          S21903
R13      EQU   13                       REGISTER EQUATE          S21903
R14      EQU   14                       REGISTER EQUATE          S21903
X01      EQU   1                        CONSTANT                 S21903
         USING IEDQLCB,RLCB             BASE FOR LCB             S21903
         USING AVTSAVE2,R13             BASE FOR SAVE            S21903
         USING IHADCB,RDCB              BASE FOR DCB             S21903
         USING *,R12                    PROGRAM BASE             S21903
IEDAYX   IEDHJN  AYX000
         L     R7,4(,R8)                ASSUME HANGUP RTN AND NO MASK
         TM    0(R8),X01                IS THE MASK PRESENT?
         BO    AYX050                   BRANCH ON NO
         L     R7,8(,R8)               ADDRESS OF ATTENTION RTN  S22029
AYX050   EQU   *
         ST    R1,LCBERBLK-1            LINK ELEMENTS
         MVI   LCBERBPY,PRIRCQCB        PRIORITY
         L     R14,AVTMSGS-1            ACCESS MH MACRO RTN ADDRESSES
         L     R14,0(,R14)              ADDRESS OF BUFFER DISPOSITION
         IC    R12,LCBERBKY             SAVE ERB KEY
         ST    R14,LCBERBQB-1           POST TO BUFFER DISPOSITION
         STC   R12,LCBERBKY             RESTORE ERB KEY
         LR    R12,R7                   SET ROUTINE BASE ADDRESS
         BR    R12                      BRANCH TO ROUTINE
         TAVTD
         TLCBD
         TPRIOR
         DCBD  DSORG=TX
         END
