         TITLE 'IEDQB7 - MULTIPLE ROUTE CHECK ROUTINE'
IEDQB7   CSECT
         SPACE
         ENTRY IEDQB701
******************** MICROFICHE FLAGS *********************** SUPT CODE
*C588000                                                       @YA05454
*C420000,828000                                                @G36XRAW
***********************************************************************
*                                                                     *
* MODULE NAME = IEDQB7                                                *
*                                                                     *
* DESCRIPTIVE NAME = MULTIPLE ROUTE CHECK ROUTINE                     *
*                                                                     *
* COPYRIGHT = 'NONE'                                                  *
*                                                                     *
* STATUS -- CHANGE LEVEL 0                                            *
*                                                                     *
* FUNCTION -- THIS ROUTINE PROVIDES A TECHNIQUE TO ALERT THE USER     *
*    THAT A MULTIROUTE FUNCTION IS IN PROGRESS.  THIS MODULE GAINS    *
*    CONTROL WHEN A MRCHECK MACRO IS CODED IN THE INMSG SUBGROUP OF   *
*    THE MH.  THIS MODULE EXECUTES ONLY IF 1) CHECKPOINT IS IN THE    *
*    SYSTEM, AND 2) EITHER A MULTIROUTE OR DLIST FUNCTION HAS BEEN    *
*    REQUESTED.  THE USER MUST HAVE SPECIFIED AN OPTION FIELD FOR THE *
*    SOURCE TERMINAL.  UPON ENTRY TO MODULE FROM IEDQBD, THE PREVIOUS *
*    TWO CONDITIONS ARE CHECKED.  IF THE EOM BUFFER HAS ALREADY BEEN  *
*    QUEUED, THE OPTION FIELD IS FLAGGED WITH THE INPUT SEQUENCE NUMBER
*    AND A X'02' INDICATING A RECEIVE OPERATION.  AN INCIDENT         *
*    CHECKPOINT IS TAKEN OF THE OPTION FIELDS FOR CURRENT TERMINAL    *
*    ENTRY.  IF THE EOM BUFFER HAS NOT BEEN QUEUED THEN THE POST LIST *
*    IS POSTED TO AN INTERNAL QCB.  WHEN THE LCB IS POSTED BACK TO    *
*    AN INTERNAL QCB.  WHEN THE LCB IS POSTED BACK TO THIS MODULE     *
*    FROM CHECKPOINT, TWO FLAGS, SCBCKPT AND SCBPRER ARE TURNED.      *
*    SCBCKPT IS SET TO INDICATE TO BD THAT A CHECKPOINT IS TO BE TAKEN*
*    AFTER MULTIROUTE OR DLIST HAVE COMPLETED.  SCBPRER IS SET TO     *
*    INDICATE THAT THE MRCHECK FUNCTION IS EFFECT AND THAT THE        *
*    MULTIROUTE AND DLIST MODULES SHOULD CLEAR OUT THE OPTION FIELD   *
*    BEFORE THE CHECKPOINT IS TAKEN.                                  *
*                                                                     *
* ENTRY POINTS --                                                     *
*    IEDQB7 TO PROCESS A BUFFER FROM IEDQBD                           *
*    IEDQB701 TO PROCESS ELEMENTS                                     *
*                                                                     *
* INPUT --                                                            *
*    REGISTER 1 - ADDRESS OF A POST LIST                              *
*                 LCB FROM CHECKPOINT                                 *
*                 ERB WHEN WAITING FOR EOM BUFFER TO BE QUEUED        *
*    REGISTER 11 - ADDRESS OF DISPATCHER                              *
*    REGISTER 13 - ADDRESS OF AVTSAVE2                                *
*                                                                     *
* OUTPUT --                                                           *
*    OPTION FIELDS CHECKPOINTED AND SCBCKPT FLAG SET FOR ANOTHER      *
*    CHECKPOINT.                                                      *
*                                                                     *
* EXTERNAL REFERENCES -- NONE                                         *
*                                                                     *
* EXITS,NORMAL -- DISPATCHER AT ENTRY POINT DSPCHAIN                  *
*                                                                     *
* EXITS,ERROR -- NONE                                                 *
*                                                                     *
* TABLES/WORKAREAS -- AVT, LCB, SCB, TERMINAL TABLE, TERMNAME TABLE   *
*                                                                     *
* ATTRIBUTES -- REENTRANT, REFRESHABLE, ENABLED, RESIDENT, PROBLEM    *
*    PROGRAM MODE.                                                    *
*                                                                     *
* NOTES -- THE OPERATION OF THIS MODULE DOES NOT DEPEND UPON A        *
*    PARTICULAR INTERNAL REPRESENTATION OF THE EXTERNAL CHARACTER SET.*
*                                                                     *
***********************************************************************
         EJECT
************************* REGISTER EQUATES ****************************
         SPACE
R0       EQU   0
R1       EQU   1                        PARAMETER REG
R2       EQU   2
RSCB     EQU   3                        ADDRESS OF SCB
RLCB     EQU   4                        ADDRESS OF LCB
RTERM    EQU   5                        ADDRESS OF TERMINAL ENTRY
RPREFIX  EQU   6                        ADDRESS OF BUFFER
R7       EQU   7                        WORK REGISTER
R8       EQU   8                        WORK REGISTER
R9       EQU   9                        WORK REG
R10      EQU   10                       WORK REGISTER
R11      EQU   11                       ADDRESS OF DISPATCHER
R12      EQU   12                       BASE REGISTER
R13      EQU   13
R14      EQU   14
R15      EQU   15
         SPACE 3
************************* OTHER EQUATES *******************************
         SPACE
THREE    EQU   3
X08      EQU   X'08'
FOUR     EQU   4
TWO      EQU   2
X02      EQU   X'02'
ONE      EQU   1
         SPACE
         USING AVTSAVE2,R13
         USING IEDQDISP,R11             DISPATCHER BASE
         USING IEDQLCB,RLCB             LCB BASE
         USING IEDQSCB,RSCB             SCB BASE
         USING IEDQPRF,RPREFIX          PREFIX BASE
         SPACE
         USING *,R12
         SPACE
IEDQB7   IEDHJN QB7VTO
         ST    R1,LCBERBLK-ONE          POST LIST SET UP
         TM    AVTCKFLG,AVTCKTAC        IS CHECKPOINT ACTIVE   @G36XRAW
         BZ    RETURN1                  NO
         SPACE
CONTMH   EQU   *
         SR    R7,R7                    CLEAR REGISTER
         L     RSCB,LCBSCBA-ONE         SET UP LCB
         C     R7,SCBMRFSD              IS MULTIROUTE OR DLIST
         BE    RETURN1                  NO
         SPACE
CKPSTLST EQU   *
         LR    RPREFIX,R1               SET UP REG 6
         LH    R1,LCBTTCIN              PICK UP SOURCE OFFSET
         N     R1,AVTCLRHI              CLEAR HIGH BYTES
         BZ    RETURN1                  NO SOURCE
         L     R15,AVTRNMPT             ADDR OF TNT CODE
         BALR  R14,R15                  LINK TO TNT CODE
         SPACE
         LR    RTERM,R1                 SET UP TERMINAL ENTRY
         USING IEDQTRM,RTERM
         SR    R8,R8                    CLEAR REG
         CLI   TRMOPNO,AVTEZERO         ANY OPTIONS SPECIFIED
         BE    RETURN1                  NO
         SPACE
         L     R7,AVTOPTPT              ADDR OF OPTION TABLE
         LR    R10,R8                   INDEX TO OPTION TABLE
         IC    R10,TRMOPT(R10)          OFFSET
         IC    R8,AVTHFF+ONE            TO COMPARE WITH OFFSET
         CR    R10,R8                   IS FIRST OPTION FIELD SPECIFIED
         BE    RETURN1                  NO
         SPACE
         L     R9,FOUR(R7)              OPTION FIELD LOOKUP
         CLI   AVTEZERO(R9),X02         IS OPTION FIELD 3 BYTES LONG
         BNE   RETURN1                  NO
         SPACE
         TM    SCBSTATE,SCBSEQIN        HAS SEQUENCE IN BEEN EXECUTED
         BZ    RETURN1                  NO
         SPACE
         LH    R9,TRMINSEQ              INPUT SEQUENCE NO
         BCTR  R9,AVTEZERO              DECREMENT SEQ NO
         LTR   R9,R9                    IS IT ZERO
         BZ    RETURN1                  YES
         SPACE
         LH    R7,TRMOPTBL              GET ENTRY OFFSET       @YA05454
         N     R7,AVTCLRHI              CLEAR HI HALF          @YA05454
         A     R7,AVTOPTPT              ADD OPT TBL ADDR       @YA05454
         AR    R7,R10                   ADDR OF OPTION FIELD
         STH   R9,AVTDOUBL              STORE IN TEMPORARY AREA
         MVC   AVTEZERO(TWO,R7),AVTDOUBL PUT SEQ NO IN OPTION FIELD
         MVI   X02(R7),X02              SET RECEIVE STATUS
         LTR   RPREFIX,RPREFIX          ANY ELEMENTS IN LIST
         BZ    BFRTHERE                 NO
         SPACE
POSTLIST EQU   *
         LA    R2,IEDQB701              SET QCB ADDRESS FOR 2ND ENTRY
         B     COMM1                    POST TO DISPATCHER
         SPACE
CKNEXT   EQU   *
SETLIST  EQU   *
BFRTHERE EQU   *
         OI    SCBSTATE,SCBCKPT         CHECKPOINT THIS
         OI    SCBSTATE,SCBPRER         OPTION FIELD FIRST ONE
         MVC   LCBRCQCB,LCBQCBA         SAVE QCB ADDRESS
         LA    R8,IEDQB701              QCB ADDRESS
         ST    R8,LCBQCBA-ONE           STORE IN LCB
         MVI   LCBPRI,PRIDESTQ          SET UP LCB PRIORITY FOR POST
*                                       BACK FROM CHECKPOINT
         LA    R2,AVTCKPTB              CHECKPOINT QCB
         MVI   LCBERBPY,PRICKPLN        CHECKPOINT PRIORITY
         B     COMM2                    POST TO DISPATCHER
         EJECT
         DS    0F
         ORG   *-8
IEDQB701 EQU   *
         ORG
         DC    AL1(DSPMCPL4)            SUBTASK ENTRY CODE
         DC    AL3(*-1)                 STCB ADDRESS
         SPACE 3
         USING *,R15
         L     R12,BASE                 SET UP BASE REGISTER
         DROP  R15
         USING IEDQB7,R12
         LR    RLCB,R1                  SET UP LCB POINTER
         SR    R1,R1                    CLEAR REGISTER
         CLI   LCBPRI,PRIRCQCB          PRIORITY FOR ERB
         BNE   CKLCB                    LCB BACK FROM CHECKPOINT
         SPACE
         LA    RPREFIX,LCBERB-IEDQLCB   ERB OFFSET
         SR    RLCB,RPREFIX             BACK UP TO LCB
         B     QB7VTO                   START AGAIN
         SPACE
CKLCB    EQU   *
RETURN   EQU   *
         ST    R1,LCBERBLK-ONE          CLEAR LINK FIELD
         L     RSCB,LCBSCBA-ONE         SET UP SCB REGISTER
         MVC   LCBQCBA,LCBRCQCB         RESTORE LCB FIELD
         OI    SCBSTATE,SCBCKPT         SET CHECKPOINT FLAG FOR BD
RETURN1  EQU   *
         L     R2,AVTMSGS-ONE
         L     R2,AVTEZERO(R2)          SET TO POST TO BD
COMM1    EQU   *
         MVI   LCBERBPY,PRIRCQCB        SET PRIORITY
COMM2    EQU   *
         ST    R2,LCBERB                SET UP QCB
         LA    R1,LCBERB                ERB ADDRESS TO POST TO BD
         BAL   R14,DSPCHAIN             EXIT TO DISPATCHER     @G36XRAW
         EJECT
BASE     DC    A(IEDQB7)                BASE FOR MODULE
         EJECT
         TAVTD
         EJECT
         TDISPD
         EJECT
         DCBD  DSORG=TX
         EJECT
         TLCBD
         EJECT
         TPRFD
         EJECT
         TPRIOR
         EJECT
         TSCBD
         TTRMD
         EJECT
         END
