DQBY TITLE '''IEDQBY''   LOG MESSAGE'
IEDQBY   CSECT
*A740000                                                         A42407
*C860000                                                         A42407
*A750000,830000                                                  A51765
*D800000                                                         A51765
***********************************************************************
*                                                                     *
*TITLE 'IEDQBY' LOG MESSAGE                                           *
*                                                                     *
*STATUS -- CHANGE LEVEL 0                                             *
*                                                                     *
*FUNCTION -- LOG MESSAGE WILL POST A RECALLED HEADER TO THE           *
*   DESTINATION QCB SPECIFIED BY THE USED FOR LOGGING THIS MESSAGE    *
*   THIS POST WILL ACTIVATE THE LOG SCHEDULER WHO WILL LOG THE        *
*   MESSAGE.                                                          *
*                                                                     *
*ENTRY POINTS -- 'IEDQBY×, TO LOG A MESSAGE                           *
*   CALLING SEQUENCE -       LA R12,IEDQBY                            *
*                            BR  R12                                  *
*                                                                     *
*INPUT -- IEDQBY IS CALLED BY BUFFER DISPOSITION AS A RESULT OF THE   *
*   LOG MACRO BEING SPECIFIED IN THE INMSG OR OUTMSG SUBGROUP         *
*   R8 - THE ADDRESS OF THE MACRO PAR LIST                            *
*   R6 - THE ADDRESS OF THE RECALLED HEADER                           *
*   R1 - THE ADDRESS OF THE FIRST OF A CHAIN OF ELEMENTS              *
*        TO BE POSTED OR 0 IF THERE ARE NONE.                         *
*   R11-  THE DISPATCHER BASE                                         *
*   R12- THE ENTRY POINT ADDRESS                                      *
*   THE MACRO PARLIST HAS THE ADDRESS FO THE TERMINAL TABLE ENTRY
*   FOR THE LOGTYPE MACRO.                                            *
*                                                                     *
*OUTPUT -- THE RECALLED HEADER WILL EBE POSTED TO THE DESTINATION     *
*   QCB SPECIFIED IN THE MACRO.                                       *
*                                                                     *
*EXTERNAL ROUTINES -- DISPATCHER TO POST A CHAIN OF ELEMENTS.         *
*                                                                     *
*EXITS-NORMAL -- EXIT IS TO DSPCHAIN TO POST A CHAIN OF ELEMENTS,     *
*   THE FIRST OF WHICH WILL BE THE RECALLED HEADER.                   *
*                                                                     *
*EXITS-ERROR  --  NONE                                                *
*                                                                     *
*TABLES/WORK AREAS -- TDISPD, TSCBD, TPRFD                            *
*                                                                     *
*                        REGISTER EQUATES                             *
*                                                                     *
*                         USING STATEMENTS                            *
*                                                                     *
*ATTRIBUTES -- REUSABLE,REFRESHABLE, RESIDENT, ENABLED                *
*                                                                     *
*NOTES -- THE OPERATION OF THIS MADULE DOES NOT DEPEND UPON A         *
*   PARTICULAR INTERNAL REPRESENTATION OF THE EXTERNAL CHARACTER      *
*   SET.                                                              *
*                                                                     *
***********************************************************************
*NOTES -- THE DCB OPENED MUST BE A BSAM DCB.                          *
*                                                                     *
R1       EQU   1                        PARAMETER COR CHAIN
RSCB     EQU   3                        SCB ADDRESS
RLCB     EQU   4
RPREFIX  EQU   6                        BUFFER ADDRESS
RQCB     EQU   7                        DEST Q ADDR
R8       EQU   8                        ADDRESS OF PARM LIST
RDISP    EQU   11                       DISPATCHER ADDR
RBASE    EQU   12                       ENTRY POINT ADDRESS
R14      EQU   14                       RETURN REGISTER        @Y17XAMX
         USING IEDQDISP,RDISP
         USING IEDQSCB,RSCB
         USING IEDQPRF,RPREFIX
         USING IEDQQCB,RQCB
         USING IEDQLCB,RLCB
         USING *,RBASE
IEDQBY   IEDHJN IDPASS                  ID AND DATE              S22025
         L     RQCB,4(R8)               SECOND WORD OF THE PARLASST
*                                       CONTAINS THE RTTABLE ADDR
         L     RQCB,0(RQCB)             ADDR OF DEST QCB
         ST    R1,PRFLINK-1             LINK BFR INTO CHAIN OF ELEMENTS
*                                       TO POST
         MVI   PRFPRI,PRIDESTQ          SET PRTY
         NI    PRFSTAT1,PRFERMGF        RESET ERRMSG FLAG        A42407
         LR    R1,RPREFIX               SET FIRST IN CHAIN ADDR
         TM    LCBSTAT1,LCBINITN+LCBSENDN SND INIT MODE MSG?     A51765
         BNO   NOTINIT                  BR NO                    A51765
         TM    SCBERR4,SCBSLCTN+SCBTXTTN ID LINE ERR OCCUR?      A51765
         BZ    NOTINIT                  BR NO                    A51765
         L     RQCB,LCBRCQCB            SET TO POST BFR TO BD AS A51765
*                                       IF THE DUPLICATE HDR HAS A51765
*                                       BEEN PROCESSED           A51765
         B     INITIATE                 GO SET INITIATE          A51765
NOTINIT  EQU   *                                                 A51765
         IC    R8,SCBDESTQ-1            SAVE HI BYTE
         ST    RQCB,SCBDESTQ-1          INITIALIZE SCB DESTQ
         STC   R8,SCBDESTQ-1            RESTORE HI BYTE
         MVI     SCBPRI,X'00'           SET PRTY QCB T/ 0
         L     RLCB,QCBDCBAD-1          GET LCB ADDR
         LH    RLCB,LCBTTCIN            OFFSET INTO TNT
         STH   RLCB,PRFDEST             INTO BFR PREFIX
INITIATE EQU   *                                                 A51765
         ST    RQCB,PRFQCBA-1           SET QCB ADDR             A51765
         BAL   R14,DSPCHAIN             EXIT                   @Y17XAMX
         TPRIOR
         TSCBD
         TLCBD
         TQCBD
         TDISPD
         TPRFD
         END
