AN01     TITLE '''IEDQAN'' -- MULTIPLE INSERT/REMOVE ROUTINE'
IEDQAN   CSECT
         SPACE 3
*   CHANGE ACTIVITY AS FOLLOWS
******************** MICROFICHE FLAGS *********************** SUPT CODE
*A123600,240140,527000                                          SA54948
*A192000                                                        SA53607
*A124100-124200,240000                                           S22025
*A065600,0786000,089090-089900,121000,123600,124100-124700       S22025
*A147200-147800,270100-270500,290002-290980,291020-291980        S22025
*A292020-292740,331200-331800,437900-435100,436900-448700,450300 S22025
*A454500,470100-470700,506070-506910                             S22025
*C207000,241260,245000-246000,272000-289000,291000-292000,310000 S22025
*C436000,003006.003807                                           S22025
*D120000-123200,130000-139000,206000,208000-209000,242000        S22025
*D293000-297000,311000,332000,395000,428000-435000,437000-448000 S22025
*D450000,462000,464000,5040000,514000-523000                     S22025
*D127200-128200,241190-244000                                   SA58444
*C126000                                                        SA58444
*A292680                                                        SA68683
*D292720                                                        SA68683
*D291020-291040                                                @SA70224
*C089720,089810,291660,292380                                  @SA70224
*C292392                                                       @SA74031
         SPACE
***********************************************************************
*                                                                     *
* MODULE NAME = IEDQAN                                                *
*                                                                     *
*  DESCRIPTIVE NAME = MULTIPLE INSERT REMOVE                          *
*                                                                     *
*  COPYRIGHT = 'NONE'                                                 *
*                                                                     *
*  STATUS:  CHANGE LEVEL 5                                            *
*                                                                     *
*FUNCTION -- PERFORMS INSERTIONS, DELETIONS AND REPLACEMENTS OF       *
*   DATA AT LOCATIONS IN THE MESSAGE SPECIFIED BY THE APPEARANCE OF   *
*   CHARACTER STRINGS.                                                *
*                                                                     *
*   THE HEART OF THE MULTIPLE INSERT/REMOVE ROUTINE IS A TRANSLA-     *
*   TION TABLE BUILT BY THE ROUTINE FROM THE INITIAL LETTER OF EACH   *
*   CHARACTER STRING AND USED BY A TRANSLATION-AND-TEST INSTRUCTION   *
*   WHICH IS PERFORMED ON ALL DATA IN THE BUFFER.                     *
*                                                                     *
*   ON ENTRY, THE ROUTINE CLEARS THE TRANSLATION TABLE AND BUILDS     *
*   IT FROM THE CHARACTER STRINGS SPECIFIED IN THE PARAMETER LIST.    *
*   AT AN OFFSET INTO THE TABLE EQUAL TO THE INITIAL LETTER OF A      *
*   CHARACTER STRING IS SET THE OFFSET FROM THE START OF THE PARA-    *
*   METER LIST TO THE SUBPARAMETER LIST WHOSE FUNCTION IS GOVERNED    *
*   BY THAT CHARACTER STRING.                                         *
*                                                                     *
*   IN THE PREFIX OF THE BUFFER ARE SAVED THE OFFSET OF THE BYTE OF   *
*   DATA (THE DATA OFFSET) AND THE NUMBER OF LOGICALLY EMPTY BYTES    *
*   AVAILABLE FOR INSERTION (THE INSERT OFFSET). THE INITIAL INSERT   *
*   OFFSET IS ZERO. THE MAIN LOOP IS THEN ENTERED.                    *
*                                                                     *
*   MAIN LOOP (TESTEOS) --                                            *
*   THE DATA OFFSET IS COMPARED WITH THE TOTAL SIZE OF DATA IN THE    *
*   BUFFER (PRFSIZE). IF THE DATA OFFSET IS NOT LOW, EXIT IS MADE     *
*   TO NORMAL COMPLETION, WHERE PRFSIZE IS ADJUSTED TO DECREMENT      *
*   ANY LEFTOVER LOGICALLY EMPTY BYTES AT THE END AND RETURN IS       *
*   MADE TO THE MESSAGE HANDLER VIA THE RETURN INTERFACE ROUTINE      *
*   (IEDQUI).                                                         *
*                                                                     *
*   IF THE DATA OFFSET IS LOW, IT IS PASSED TO THE ADDRESS FINDER     *
*   ROUTINE (ENTERLOP), WHICH RETURNS THE DATA ADDRESS AND THE        *
*   ADDRESS OF THE END OF THE UNIT IN WHICH THE ADDRESS IS LOCATED.   *
*   THE TRANSLATE-AND-TEST INSTRUCTION IS THEN PERFORMED STARTING     *
*   AT THE DATA ADDRESS FOR THE LENGTH OF THE UNIT OR,  IF THE UNIT   *
*   IS THE LAST UNIT IN THE BUFFER, FOR THE LENGTH OF DATA IN THE     *
*   UNIT.                                                             *
*                                                                     *
*   IF THE UNIT TRANSLATES ALL ZEROES,  A LINK IS MADE TO A TEST-     *
*   SHIFT SUBROUTINE. THIS SUBROUTINE TESTS IF THERE ARE ANY          *
*   LOGICALLY EMPTY BYTES PRECEDING THE DATA JUST TRANSLATED. IF      *
*   NOT, THE SUBROUTINE RETURNS IMMEDIATELY. OTHERWISE THE SUB-       *
*   ROUTINE LINKS TO THE INSERT DATA ROUTINE (IEDQAF) WHICH SHIFTS    *
*   THE DATA JUST TRANSLATED LEFTWARDS IN THE BUFFER, OVERLAYING      *
*   THE LOGICALLY EMPTY BYTES AND EFFECTIVELY MOVING THE LOGICALLY    *
*   EMPTY AREA TO THE RIGHT-HAND END OF THE UNIT.                     *
*                                                                     *
*   ON RETURN FROM THIS SUBROUTINE, THE MAIN LOOP INCREMENTS THE      *
*   DATA OFFSET BY THE LENGTH OF DATA JUST TRANSLATED AND REENTERS    *
*   THE MAIN LOOP TO TRANSLATE THE NEXT UNIT.                         *
*                                                                     *
*   IF THE TRANSLATE-AND-TEST RESULTS IN A 'HIT' (A BYTE IN THE       *
*   BUFFER TRANSLATES NON-ZERO), A BRANCH IS TAKEN TO A HIT ANAL-     *
*   YSIS SUBROUTINE.                                                  *
*                                                                     *
*   HIT ANALYSIS SUBROUTINE (NORMHIT) --                              *
*   THE SUBROUTINE DETERMINES IF THE HIT OCCURRED ON THE FIRST        *
*   BYTE TRANSLATED AND IF NOT LINKS TO THE TEST-SHIFT SUBROUTINE     *
*   TO SHIFT THE BYTES JUST TRANSLATED LEFTWARDS IN THE BUFFER,       *
*   THUS MOVING THE LOGICALLY EMPTY AREA RIGHTWARDS UP TO THE HIT     *
*   BYTE. ON RETURN THE DATA OFFSET IS INCREMENTED BY THE LENGTH      *
*   OF DATA JUST TRANSLATED.                                          *
*                                                                     *
*   THE SUBPARAMETER LIST INDICATED BY THE OFFSET TO WHICH THE HIT    *
*   BYTE TRANSLATED IS LOCATED. FROM INFORMATION IN THIS SUBPARA-     *
*   METER LIST IS BUILT A PARAMETER LIST FOR THE SCAN ROUTINE         *
*   (IEDQAL). THE OFFSET OF THE HIT IS PASSED TO THE SCAN ROUTINE     *
*   TO DETERMINE IF THE HIT BYTE IS THE FIRST BYTE OF  A  CHARACTER   *
*   STRING EQUAL TO THE STRING GOVERNING THE FUNCTION SPECIFIED BY    *
*   THE SUBPARAMETER LIST.                                            *
*                                                                     *
*   IF THE STRINGS ARE NOT EQUAL, THE DATA OFFSET IS INCREMENTED      *
*   PAST THIS SPURIOUS HIT BYTE AND MAIN LOOP IS RE-ENTERED. IF       *
*   THERE ARE INSUFFICIENT CHARACTERS REMAINING IN THE BUFFER TO      *
*   TELL IF THE STRINGS ARE EQUAL, A CHECK IS MADE TO SEE IF THE      *
*   FUNCTION IS BEING PERFORMED IN AN INBLOCK SUBGROUP. IF THE        *
*   FUNCTION IS IN INBLOCK AND THIS IS NOT THE LAST BUFFER, CONTROL   *
*   IS GIVEN TO THE CROSS-BUFFER ROUTINE (BNLINK). IF THE FUNCTION    *
*   IS NOT IN INBLOCK, OR THIS IS THE LAST BUFFER, THE CORRESPONDING  *
*   BYTE IN THE TRANSLATE TABLE IS CLEARED. THE DATA OFFSET IS        *
*   INCREMENTED AND THE MAIN LOOP IS RE-ENTERED.                      *
*                                                                     *
*   IF THE STRINGS ARE EQUAL, THE STRING IN THE BUFFER IS SHIFTED     *
*   LEFTWARDS IN THE BUFFER VIA A LINK TO THE TEST-SHIFT SUBROU-      *
*   TINE. THE SHIFT IS NOT PERFORMED, HOWEVER, IF THE FUNCTION        *
*   SPECIFIED IS A REMOVE FUNCTION WHICH INCLUDES REMOVAL OF THE      *
*   STRING ITSELF. IN THIS CASE THE INSERT OFFSET IS INCREMENTED      *
*   BY THE LENGTH OF THE STRING INSTEAD.                              *
*                                                                     *
*   IF THE FUNCTION SPECIFIED IS AN INSERT FUNCTION, THE INSERT       *
*   SUBROUTINE IS ENTERED. IF A REMOVE FUNCTION, THE REMOVE SUB-      *
*   ROUTINE IS ENTERED.                                               *
*                                                                     *
*   CROSS-BUFFER SUBROUTINE --                                        *
*   THIS SUBROUTINE DELETES DATA FROM A BUFFER AND PUTS THE DELETED   *
*   DATA ON THE HOLD QUEUE SO THAT IT WILL BE INSERTED INTO THE       *
*   NEXT BUFFER OF THE CURRENT MESSAGE. THIS SUBROUTINE LINKS TO      *
*   THE UNIT REQUEST ROUTINE (IEDQAO) AND THE SHIFT DATA ROUTINE      *
*   (IEDQAF) TO GET AN ADDITIONAL UNIT AND SHIFT INTO IT ALL OF       *
*   THE DATA IN A SINGLE UNIT, FOLLOWING AND INCLUDING THE 'FROM'     *
*   CHARACTER STRING. A TEXT PREFIX IS BUILT IN THE NEW UNIT AND      *
*   IT PLUS SUBSEQUENT UNITS FORM A BUFFER THAT IS PLACED ON THE      *
*   HOLD QUEUE. THE ORIGINAL BUFFER, WITH PRFSIZE DECREMENTED, IS     *
*   RETURNED FOR FURTHER MH PROCESSING.                               *
*                                                                     *
*   INSERT SUBROUTINE --                                              *
*   FROM INFORMATION IN THE SUBPARAMETER LIST, PARAMETERS ARE BUILT   *
*   FOR THE USER REQUEST INTERFACE AND A LINK IS MADE TO IT. THIS     *
*   INTERFACE ACCESSES ANOTHER UNIT IF NEEDED FOR SPACE TO INSERT     *
*   AND LINKS TO THE INSERT DATA ROUTINE. THIS ROUTINE INSERTS THE    *
*   DATA SPECIFIED, ADJUSTS THE DATA AND INSERT OFFSETS ACCORDINGLY   *
*   AND RETURNS TO THE MULTIPLE INSERT/REMOVE ROUTINE.  THE MAIN      *
*   LOOP IS THEN RE-ENTERED.                                          *
*   IF THE UNIT REQUEST INTERFACE FINDS NO EMPTY UNITS AVAILABLE,     *
*   IT RETURNS IMMEDIATELY WITH X'04' IN REGISTER 15.  THE ROUTINE    *
*   DISCONTINUES THE TRANSLATE-AND-TEST AND RETURNS IMMEDIATELY TO    *
*   THE USER WITH THIS RETURN CODE.                                   *
*                                                                     *
*   REMOVE SUBROUTINE --                                              *
*   THE SUBROUTINE DETERMINES WHAT THE 'TO' DELIMITER IS FOR THIS     *
*   REMOVE FUNCTION. (THE 'FROM' DELIMITER IS THE CHARACTER STRING    *
*   ALREADY FOUND.)                                                   *
*                                                                     *
*   IF THE 'TO' DELIMITER IS A CHARACTER STRING, THE SUBROUTINE       *
*   LINKS TO THE BUFFER SCAN ROUTINE (IEDQAX) TO SCAN PAST 'FROM' FOR *
*   DELIMITING CHARACTER STRING. ON RETURN, IF THE STRING WAS NOT     *
*   FOUND, THE FUNCTION CANNOT BE PERFORMED IN THIS BUFFER. THE       *
*   ROUTINE CHECKS TO SEE IF THE FUNCTION MAY CROSS BUFFERS (THIS     *
*   FUNCTION IS IN INBLOCK AND THIS IS NOT THE LAST BUFFER). IF       *
*   ACROSS BUFFERS, CONTROL IS GIVEN TO BNLINK. OTHERWISE THE         *
*   APPROPRIATE BYTE IN THE TRANSLATE TABLE IS CLEARED TO ZERO,       *
*   THE DATA OFFSET IS INCREMENTED PAST THE HIT BYTE AND RE-ENTRY     *
*   IS MADE TO THE MAIN LOOP.                                         *
*                                                                     *
*   IF THE STRING WAS FOUND, THE OFFSET TO ITS LAST BYTE IS           *
*   RETURNED. THE SUBROUTINE DETERMINES IF THE 'TO' STRING IS TO      *
*   BE ITSELF REMOVED.  IF NOT, THE OFFSET IS DECREMENTED BY THE      *
*   LENGTH OF THE 'TO' STRING; OTHERWISE IT IS NOT CHANGED.  THIS     *
*   OFFSET NOW BECOMES THE NEW DATA OFFSET,  AND THE INSERT OFFSET    *
*   IS INCREMENTED BY THE LENGTH OF DATA LOGICALLY REMOVED.  IF THE   *
*   REMOVE FUNCTION SPECIFIED IS A CONTRACT, THE MAIN LOOP IS         *
*   RE-ENTERED.                                                       *
*                                                                     *
*   IF THE REMOVE FUNCTION IS A REPLACE,  THE ROUTINE ENTERS THE      *
*   INSERT SUBROUTINE. IF THE REPLACE DATA IS A DELIMITER CHARAC-     *
*   TER, HOWEVER, THE ROUTINE MUST FIND THE TERMINAL TABLE ENTRY      *
*   FOR THE DESTINATION AND EXTRACT THE CONFIGURATION OF THE          *
*   DELIMITER FROM IT.  THE DELIMITER IS SET INTO THE SUBPARAMETER    *
*   LIST AND ENTRY IS THEN MADE TO THE INSERT SUBROUTINE.             *
*                                                                     *
*   IF THE 'TO' DELIMITER IS AN EXTENT,  THE EXTENT IS PICKED UP      *
*   FROM THE SUBPARAMETER LIST AND ADDED TO THE DATA OFFSET.  THE     *
*   RESULT IS THE NEW DATA OFFSET,  AND THE INSERT OFFSET IS INCRE-   *
*   MENTED BY THE LENGTH OF DATA LOGICALLY REMOVED.  IF THE REMOVE    *
*   FUNCTION IS A CONTRACT, THE MAIN LOOP IS RE-ENTERED.  OTHERWISE   *
*   PROCESSING PROCEEDS IN THE SAME MANNER AS FOR A REPLACE FUNC-     *
*   TION WITH A CHARACTER STRING AS THE 'TO' DELIMITER.               *
*                                                                     *
*ENTRY POINTS --                                                      *
*       'IEDQAN01' TO PERFORM MULTIPLE INSERT/REMOVE FUNCTIONS.       *
*   CALLING SEQUENCE FROM USER INTERFACE IS:                          *
*                                                                     *
*        L     R12,AVTMSGS-1            GET ADDR OF VCON TABLE        *
*        IC    R15,AVTEZERO(,R1)        GET INDEX TO ROUTINE ADDR     *
*        L     R12,AVTEZERO(R12,R15)    GET ROUTINE ADDRESS           *
*        BR    R12                      EXIT TO ROUTINE               *
*                                                                     *
*INPUT --                                                             *
*   REGISTER 1 - THE ADDRESS OF A MACRO-GENERATED PARAMETER LIST.     *
*   PARAMETER LIST FORMAT IS:                                         *
*                                                                     *
*        *********************************                            *
*        * INDEX * PARAM * INDEX * INDEX *
*        *  TO   * LIST  *  TO   *  TO   *
*        *   AN  *  LNG  *   AF  *   AO  *                            *
*        *********************************                            *
*        * (UN-  * BLANK *  NO.  * (UN-  *                            *
*        * USED) *  CHAR *   OF  * USED) *
*        *       *       *ENTRIES*       *                            *
*        *********************************                            *
*        * (UN-  *      ADDRESS OF       *                            *
*        * USED) *      CHARACTERS       *                            *
*        *       *        TABLE          *                            *
*        ************************************                         *
*        *       *       *     DATA      *                            *
*        *  KEY  *STATUS *  DESCRIPTION  *                            *
*        *       *       *               *   FIRST                    *
*        *********************************     SUB-PARAMETER          *
*        *      FROM     *      TO       *       LIST                 *
*        *   DELIMITER   *   DELIMITER   *                            *
*        *  DESCRIPTION  *  DESCRIPTION  *                            *
*        ************************************                         *
*        *                               *                            *
*        *                               *                            *
*                                                                     *
*                                                                     *
*                                                                     *
*        *                               *                            *
*        *                               *                            *
*        *********************************                            *
*                                                                     *
*        DATA=CHARACTERS                                              *
*          × DATA=IDLES COUNT                                         *
*          ×   × DATA=CONTRACT                                        *
*          ×   ×   × TO=CHAR STRING                                   *
*          ×   ×   ×   × TO=OFFSET                                    *
*          ×   ×   ×   ×   × TO=EXTENT                                *
*          ×   ×   ×   ×   ×   × INCLUSIVE FROM                       *
*          ×   ×   ×   ×   ×   ×   × INCLUSIVE TO                     *
*          ×   ×   ×   ×   ×   ×   ×   ×          BIT ON              *
*        *********************************                            *
*        *   *   *   *   *   *   *   *   * SUB-PARAMETER              *
*        * 0 * 1 * 2 * 3 * 4 * 5 * 6 * 7 *  STATUS BYTE               *
*        *   *   *   *   *   *   *   *   *   FORMAT                   *
*        *********************************                            *
*                                  ×   ×          BIT OFF             *
*                                  × EXCLUSIVE TO                     *
*                                EXCLUSIVE FROM                       *
*                                                                     *
*   REGISTER 3 - THE ADDRESS OF THE SCB                               *
*                                                                     *
*   REGISTER 4 - THE ADDRESS OF THE LCB                               *
*                                                                     *
*   REGISTER 6 - THE ADDRESS OF THE BUFFER                            *
*                                                                     *
*   REGISTER 9 - THE ADDRESS OF THE AVT                               *
*                                                                     *
*   REGISTER 12 - THE ENTRY POINT ADDRESS AND BASE REGISTER FOR       *
*   THIS ROUTINE.                                                     *
*                                                                     *
*OUTPUT --                                                            *
*   REGISTER 15 - A RETURN CODE OF X'00' IF ALL FUNCTIONS REQUESTED   *
*   ARE PERFORMED OR X'04' IF EMPTY UNITS NEEDED FOR DATA INSERTION   *
*   WERE NOT AVAILABLE.                                               *
*                                                                     *
*EXTERNAL REFERENCES --                                               *
*   'IEDQAF' - INSERT DATA ROUTINE                                    *
*                                                                     *
*   'IEDQAL' - SCAN AT OFFSET ROUTINE                                 *
*                                                                     *
*   'IEDQAX' - SCAN BUFFER ROUTINE                                    *
*                                                                     *
*   'IEDQAO' - UNIT REQUEST INTERFACE                                 *
*                                                                     *
*   AVT - ADDRESS VECTOR TABLE                                        *
*                                                                     *
*   SCB - STATION CONTROL BLOCK                                       *
*                                                                     *
*   LCB - LINE CONTROL BLOCK                                          *
*                                                                     *
*   HOLD QUEUE                                                        *
*                                                                     *
*   BUFFER CURRENTLY BEING PROCESSED                                  *
*                                                                     *
*EXITS, NORMAL -- ALL FUNCTIONS HAVE BEEN PERFORMED.  REGISTER 15     *
*   CONTAINS A RETURN CODE OF X'00'.                                  *
*                                                                     *
*EXITS, ERROR -- NOT ALL FUNCTIONS HAVE BEEN PERFORMED BECAUSE        *
*   EMPTY UNITS NEEDED FOR DATA INSERTION WERE NOT AVAILABLE.         *
*   REGISTER 15 CONTAINS A RETURN CODE OF X'04'.                      *
*                                                                     *
*TABLES/WORK AREAS --                                                 *
*                                                                     *
*   TRANSLATE TABLE FOR EXECUTED TRANSLATE-AND-TEST INSTRUCTION       *
*                                                                     *
*ATTRIBUTES -- SERIALLY REUSABLE, REFRESHABLE, ENABLED, RESIDENT,     *
*   PROBLEM PROGRAM MODE.                                             *
*                                                                     *
*NOTE -- THE OPERATION OF THIS MODULE DEPENDS UPON AN INTERNAL        *
*   REPRESENTATION OF THE EXTERNAL CHARACTER SET WHICH IS             *
*   EQUIVALENT TO THE ONE USED AT ASSEMBLY TIME.  THE CODING HAS      *
*   BEEN ARRANGED SO THAT REDEFINITION OF ''CHARACTER'' CONSTANTS,    *
*   BY REASSEMBLY, WILL RESULT IN A CORRECT MODULE FOR THE NEW        *
*   DEFINITIONS.                                                      *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
         EJECT
********* REGISTER EQUATES *********
         SPACE
R0       EQU   0                        WORK REGISTER
         SPACE
R1       EQU   1                        PARAMETER LIST ADDRESS
         SPACE
RWORK2   EQU   2                        WORK REGISTER
         SPACE
RSCB     EQU   3                        SCB ADDRESS
         SPACE
RLCB4    EQU   4                        LCB ADDRESS
RSTRING4 EQU   4                        CHARACTER STRING ADDRESS
         SPACE
RDATAD5  EQU   5                        DATA ADDRESS
         SPACE 1                                                 S22025
         SPACE 1                                                 S22025
RSCAN    EQU   5                        SCAN POINTER OFFSET      S22025
         SPACE
RPREFIX  EQU   6                        BUFFER ADDRESS
         SPACE
RPARM7   EQU   7                        PARAMETER LIST ADDRESS
         SPACE
RWORK8   EQU   8                        WORK REGISTER
         SPACE
RAVT     EQU   9                        AVT ADDRESS
         SPACE
RHIT10   EQU   10                       SUB-PARAMETER LIST ADDRESS
         SPACE
RCT11    EQU   11                       SUB-PARAM COUNT REGISTER
REX11    EQU   11                       EXECUTE REGISTER
         SPACE 1                                                 S22025
         SPACE 1                                                 S22025
REOUAD   EQU   11                       CURRENT EOU ADDR         S22025
         SPACE
RBASE    EQU   12                       BASE REGISTER
         SPACE
R13      EQU   13                       SAVE AREA ADDRESS
R14      EQU   14                       RETURN REGISTER
R15      EQU   15                       LINK REGISTER
         SPACE
********* OTHER EQUATES *********
         SPACE
ONE      EQU   1                        INCREMENT VALUE OF ONE
FOUR     EQU   4                        MOVE LENGTH OF FOUR
SHIFT8   EQU   8                        SHIFT LENGTH OF 1 BYTE
EIGHT    EQU   8                        TO POINT TO AN PARM LIST S22025
FIVE     EQU   5                        OFFSET TO BUFFER ADDR    S22025
THREE    EQU   3                        COMPARE LENGTH           S22025
LCBSCB   EQU   13                       LCB ADDR OFFSET IN PREFIXS22025
INBLK    EQU   X'02'                    TO SPECIFY INBLOCK       S22025
BLNKCHAR EQU   5                        OFFSET TO BLANK          S22025
TFOUR    EQU   24                       SHIFT LENGTH             S22025
MVOFF    EQU   12                       MOVE OFFSET            @SA70224
LEN      EQU   23                       MOVE LENGTH            @SA70224
TWO      EQU   2                        OFFSET                   S22025
BFREDIT  EQU   X'40'                    DATA MOVEMENT FLAG       S22025
LOWEQ    EQU   12                       LOW/EQUAL CONDITION      S22025
         SPACE
IDLEFLAG EQU   X'01'                    INSERT CHARACTERS = IDLES
SHIFFLAG EQU   X'01'                    MULTIPLE SHIFT REQUEST FLAG
SHIFUNIT EQU   X'02'                    ONE-UNIT SHIFT REQUEST
NOBLANKN EQU   X'02'                    'NO BLANK' FLAG
         SPACE
CCZERO   EQU   8                        CONDITION CODE OF ZERO
CCEQUAL  EQU   8                        CONDITION CODE OF EQUAL
         SPACE
DATACHAR EQU   X'80'                    DATA = CHARACTER STRING
DATAIDCT EQU   X'40'                    DATA = COUNT OF IDLES
DATACONT EQU   X'20'                    DATA = CONTRACT
TOCHAR   EQU   X'10'                    TO DELIMITER = CHAR STRING
TOOFFS   EQU   X'08'                    TO DELIMITER = OFFSET
TOEXTN   EQU   X'04'                    TO DELIMITER = EXTENT
FROMINCL EQU   X'02'                    FROM STRING INCL IN REMOVE
TOINCL   EQU   X'01'                    TO STRING INCL IN REMOVE
         SPACE
ADA      EQU   X'10'                    RCV SCHED KEY VALUES
VAN      EQU   X'12'                      FOR APPLICATION PROGRAM
         EJECT
         USING IEDQAVTD,RAVT            AVT ADDRESSABILITY       S22025
         USING IEDQLCB,RLCB4            LCB ADDRESSABILITY       S22025
         USING IEDQPARM,RPARM7          PARMLIST ADDRESSABILTY   S22025
         USING IEDQPRF,RPREFIX          BUFFER   ADDRESSABILTY   S22025
         USING IEDQSCB,RSCB             SCB ADDRESSABILITY       S22025
         USING IEDQSBPM,RHIT10          SUB-PARMLIST ADDR'BILITY S22025
         USING IEDQAN,RBASE             MODULE ADDRESSABILITY    S22025
         SPACE 1                                                 S22025
IEDQAN01 EQU   *                        ENTRY POINT              S22025
IEDQAN   IEDHJN MULTINRE                                         S22025
         L     RPREFIX,AVTADBUF         GET CURRENT BUFFER ADDR  S22025
         MVC   KEYSAVE(2),PRFKEY        SAVE LOCAL OPCODE BYTE  SA54948
         SR    RCT11,RCT11              CLEAR FOR COMPARE
         TM    LCBSTAT1,LCBSENDN        IS IT SEND               S22025
         BO    ITSEND                   YES                      S22025
         STH   RCT11,PRFXTRA+1          INITIALIZE COUNT FIELD   S22025
ITSEND   EQU   *                                                 S22025
         CH    RCT11,PRFSIZE            IS THIS A ZERO-LENGTH BFR
         BE    TESTINCL                 YES, RETURN IMMEDIATELY SA58444
         SPACE
         LR    RPARM7,R1                SET PARAMETER REGISTER
         IC    R0,PARMBLNK              PICK UP BLANK CHARACTER
         SPACE
         SPACE
CLEARTAB EQU   *
         XC    TRANTAB(256),TRANTAB     CLEAR TRANSLATE TABLE
         L     RWORK2,PARMCHAR          GET CHARACTERS TABLE ADDR
         IC    RCT11,PARMENTS           GET NO. OF SUB-PARAMS
         LA    RHIT10,PARMFIRS          POINT TO FIRST SUB-PARAM
         SPACE
TABLOOP  EQU   *
         TM    LCBSTAT1,LCBSENDN        SEND                     S22025
         BO    DELM                     YES                      S22025
         MVC   PRFXTRA(1),SBPMSTAT      GET STATUS BYTE          S22025
DELM     EQU   *                                                 S22025
         TM    SBPMSTAT,DATACHAR+DATAIDCT IS DATA DELIMITER
         BO    DELIMIT                  YES, GO VERIFY
         SPACE
SETTRANS EQU   *                                              N
         IC    R0,SBPMKEY               GET INDEX
         LH    R14,SBPMFROM             GET CHAR STRING OFFSET
         N     R14,AVTCLRHI
         SR    R15,R15                  (CLEAR FOR INSERT)
         IC    R15,ONE(R14,RWORK2)      GET 1ST CHAR IN STRING
         STC   R0,TRANTAB(R15)          SET TRANSLATE TABLE BYTE
         B     NEXTSBPM                 GO BUMP TO NEXT SUB-PARM
         SPACE
DELIMIT  EQU   *
         TM    LCBSTAT1,LCBSENDN        SEND SIDE OF MH
         BNO   NEXTSBPM                 NO, BYPASS THIS ENTRY
         SPACE
         CLI   LCBRSKEY,ADA             TEST RCV SCHED KEY VALUE
         BE    LINKTNT                  IT IS APPL PGM, PROCEED
         SPACE
         CLI   LCBRSKEY,VAN             TEST RCV SCHED KEY VALUE
         BNE   NEXTSBPM                 IT ISN'T, BYPASS THIS ENTRY
         SPACE
LINKTNT  EQU   *
         LH    R1,LCBTTCIN              GET DESTINATION OFFSET
         N     R1,AVTCLRHI
         L     R15,AVTRNMPT             GET ADDR OF TNT CODE
         BALR  R14,R15                  LINK TO GET TERMINAL ADDR
         SPACE
         USING IEDQTRM,R1
         IC    R1,TRMCHCIN              GET DELIMITER CHARACTER
         STC   R1,SBPMDATA+1            PUT IT IN SUB-PARM LIST
         MVI   SBPMDATA,ONE             PUT IN COUNT OF ONE
         B     SETTRANS                 GO SET KEY IN TABLE
         SPACE
NEXTSBPM EQU   *
         LA    RHIT10,SBPMNEXT          POINT TO NEXT SUB-PARAM
         BCT   RCT11,TABLOOP            LOOP TILL OUT OF SUB-PARAMS
         SPACE
         SR    RDATAD5,RDATAD5          (CLEAR FOR INSERT)
         IC    RDATAD5,LCBISZE          PICK UP NO. OF RESERVES
         LA    RDATAD5,AVTTXTSZ+1(,RDATAD5) ADD SIZE OF TEXT PREFIX
         TM    PRFSTAT1,PRFNHDRN        IS IT HEADER PREFIX
         BO    SETINIT                  NO, GO SET
         SPACE
         LA    RDATAD5,AVTHDRSZ-AVTTXTSZ(,RDATAD5) YES, ADD EXTRA
         TM    LCBSTAT1,LCBRECVN       RECEIVE OPERATION        SA53607
         BZ    SETINIT                 NO, PROCEED              SA53607
         TM    SCBBSCFM,SCBDATEN       DATE/TIME REQUESTED      SA53607
         BZ    SETINIT                 NO, PROCEED              SA53607
         LA    RDATAD5,EIGHT(RDATAD5)  YES, ADD FOR DATE/DIME   SA53607
         SPACE
SETINIT  EQU   *
         ST    RDATAD5,PRFINS           SET INITIAL OFFSETS
*        B     TESTEOS                  ENTER TRT LOOP
         EJECT
TESTEOS  EQU   *
         LH    RWORK8,PRFSIZE           GET TOTAL DATA SIZE
         N     RWORK8,AVTCLRHI
         LA    RWORK8,ONE(,RWORK8)      GET OFFSET TO LAST DATA
*                                         BYTE + 1
         SR    RWORK8,RDATAD5           SUBTRACT NEW DATA OFFSET
         BZ    NORMCOMP                 ALL DATA PROCESSED, RETURN
         SPACE
         BAL   R14,ENTERLOP             GET ADDRESS OF ITEM      S22025
         SPACE
         SR    REX11,RDATAD5            GET DATA LEFT IN UNIT
         CR    RWORK8,REX11             IS IT LESS THAN TOTAL LEFT
         BNL   TRANTEST                 NO, DO TRT ON WHOLE UNIT
         SPACE
         LR    REX11,RWORK8             YES, DO TRT ON DATA LEFT
         SPACE
TRANTEST EQU   *
         SR    RWORK2,RWORK2            CLEAR FOR HIT INSERT
         BCTR  REX11,AVTEZERO
         EX    REX11,TRTX               TRANSLATE-AND-TEST
         LA    REX11,ONE(,REX11)
         BNE   NORMHIT                  HIT, GO PROCESS
         SPACE
         BAL   R14,TESTSHIF             GO SHIFT DATA IF NEEDED
         L     R1,PRFLCB-1              GET LCB ADDRESS          S22025
         TM    LCBSTAT1-IEDQLCB(R1),LCBSENDN   SENDING           S22025
         BO    SEND1                    YES, NO EOB UPDATE       S22025
         MVC   PRFXTRA(3),AVTFZERO      CLEAR  FOR AF            S22025
SEND1    EQU   *                                                 S22025
         SPACE
TRUNOUT  EQU   *
         LH    RDATAD5,PRFDAT           GET CURRENT DATA OFFSET
         N     RDATAD5,AVTCLRHI
         AR    RDATAD5,REX11            ADD TRT LENGTH TO IT
         STH   RDATAD5,PRFDAT           STORE IT BACK
         B     TESTEOS                  RE-ENTER TRT LOOP
         SPACE
NORMCOMP EQU   *
         SR    R15,R15                  SET GOOD RETURN CODE
         SPACE
FINALSZE EQU   *
         LH    R0,PRFSIZE               GET OLD DATA SIZE
         N     R0,AVTCLRHI
         SH    R0,PRFINS                SUBTRACT LEFT-OVER BYTES
         STH   R0,PRFSIZE               SET FINAL DATA SIZE
         SPACE 1
CHKSIZE  EQU   *                                                 S22025
         SR    R1,R1                    (CLEAR FOR INSERT)
         L     RLCB4,PRFLCB-1           RELOAD LCB ADDRESS
         MVC   PRFKEY(2),KEYSAVE        RSTR LOCAL OPCODE BYTE  SA54948
         IC    R1,LCBISZE               PICK UP NO. OF RESERVES
         LA    R1,AVTTXTSZ+1(,R1)       ADD LNG OF TEXT PFX+1
         TM    PRFSTAT1,PRFNHDRN        IS THIS A HEADER BUFFER
         BO    TESTNONE                 NO, TEXT, SIZE IS RIGHT
         SPACE
         LA    R1,AVTHDRSZ-AVTTXTSZ(,R1) YES, ADD EXTRA
         SPACE
TESTNONE EQU   *
         CR    R1,R0                    AT LEAST ONE DATA BYTE?
         BNH   TESTINCL                 YES, BRANCH
         SPACE
         STH   R1,PRFSIZE               INDICATE ONE DATA BYTE
         SPACE
TESTINCL EQU   *
         L     RBASE,AVTUI              GET RET INTERFACE ADDR   S22025
         B     FOUR(RBASE)              BRANCH TO RETURN ROUTINE S22025
         EJECT
NORMHIT  EQU   *
         LA    R1,AVTEZERO(,R1)         CLEAR HIGH-ORDER BYTE
         SR    R1,RDATAD5               GET NO. TRANS BYTES - 1
         BZ    SETOFF                   HIT ON FIRST, DON'T SHIFT
         SPACE
         LR    REX11,R1                 SET SHIFT LENGTH
         BAL   R14,TESTSHIF             GO SHIFT DATA IF NEEDED
         L     R1,PRFLCB-1              GET LCB ADDRESS          S22025
         TM    LCBSTAT1-IEDQLCB(R1),LCBSENDN   SENDING           S22025
         BO    SEND                     YES, NO EOB UPDATE       S22025
         MVC   PRFXTRA(3),AVTFZERO      CLEAR  FOR AF            S22025
         OI    PRFTQBCK+TWO,BFREDIT     INDICATE DATA MOVEMENT   S22025
SEND     EQU   *                                                 S22025
         SPACE
         LH    RDATAD5,PRFDAT           GET OLD DATA OFFSET
         LA    RDATAD5,AVTEZERO(REX11,RDATAD5) ADD SHIFT LENGTH
         STH   RDATAD5,PRFDAT           STORE IT BACK
         B     FROMCHAR                 GO SCAN FOR 'FROM' CHARS
         SPACE
SETOFF   EQU   *
         LH    RDATAD5,PRFDAT           RESET DATA OFFSET
         N     RDATAD5,AVTCLRHI
         SPACE
FROMCHAR EQU   *
         LA    RHIT10,AVTEZERO(RWORK2,RPARM7) GET SUB-PARAM LIST
         L     RSTRING4,PARMCHAR        GET CHAR TABLE ADDRESS
         LH    RWORK8,SBPMFROM          GET OFFSET TO FROM CHARS
         N     RWORK8,AVTCLRHI
         IC    R0,AVTEZERO(RWORK8,RSTRING4) GET LENGTH OF STRING
         LA    RSTRING4,ONE(RWORK8,RSTRING4) POINT TO 1ST CHAR
         LR    RWORK8,R0                SAVE LENGTH OF STRING    S22025
         TM    AVTEZERO(RPARM7),NOBLANKN CHECK FOR BLANKS        S22025
         BO    NOTBLK                   NO                       S22025
         SR    RWORK2,RWORK2            YES                      S22025
         IC    RWORK2,BLNKCHAR(RPARM7)  OBTAIN BLANK CHAR        S22025
         B     SCANLINK                 KEEP IT                  S22025
*                                                                S22025
NOTBLK   EQU   *                                                 S22025
         LNR   RWORK2,RWORK2            INDICATE NO BLANKS       S22025
SCANLINK EQU   *                                                 S22025
         LR    R0,RSTRING4              ADDR OF CHARACTERS       S22025
         LH    R1,PRFDAT                OFFSET IN BUFFER         S22025
         N     R1,AVTCLRHI              CLEAR HI HALF OF REG     S22025
         BCTR  R1,R0                    DECREMENT BY ONE         S22025
         STH   R1,PRFKEY                SAVE OFFSET TO AT STRING S22025
         SLL   RWORK8,TFOUR             TO PUT COUNT IN HI BYTE  S22025
         AR    R1,RWORK8                LENGTH AND BUF OFF IN R1 S22025
         SRL   RWORK8,TFOUR             TO PUT COUNT IN LO BYTE  S22025
         L     R15,AVTAL                GET ADDR OF SCAN ROUTINE S22025
         BALR  R14,R15                  LINK TO SCAN ROUTINE     S22025
         TM    PARMAO,INBLK             IS IT INBLK              S22025
         BZ    SKPINBLK                 NO                       S22025
         LH    R1,PARMAF                GET AO AND AF INDEX      S22025
         STH   R1,AVTPARM+2             STORE IN AVTPARM         S22025
SKPINBLK EQU   *                        CHECK FOUND              S22025
         LTR   R15,R15                  HOW MANY CHARS FOUND     S22025
         BZ    THROBACK                 NO CHAR FOUND            S22025
         L     R1,PRFLCB-1              GET LCB ADDRESS          S22025
         TM    LCBSTAT1-IEDQLCB(R1),LCBSENDN SEND OPERATION      S22025
         BO    PARML                    YES, DON'T UPDATE EOBOFF S22025
         LR    R1,R0                    GET OFFSET AFTER FROM    S22025
         LA    R1,ONE(,R1)              ADD ONE                  S22025
         SH    R1,PRFDAT                SUBTRACT OFFSET TO 'FROM'S22025
         AH    R1,PRFXTRA+1             ADD TO TOTAL             S22025
         STH   R1,PRFXTRA+1             SAVE LENGTH TO DELETE    S22025
         MVC   PRFXTRA(1),SBPMSTAT      SET STATUS               S22025
PARML    EQU   *                                                 S22025
         CR    R15,RWORK8               WAS CHAR STRING FOUND    S22025
         BE    FOUNDEQU                 YES                      S22025
         TM    PARMAO,INBLK             IS INBLOCK SUBGROUP USED S22025
         BZ    THROBACK                 NO                       S22025
         TM    PRFSTAT1,PRFNLSTN        LAST                     S22025
         BNO   THROBACK                 YES                      S22025
BNLINK   EQU   *                                                 S22025
         LH    RWORK8,PRFKEY            GET DATA OFFSET          S22025
BNLINK1  EQU   *                                                 S22025
         LA    RPARM7,AVTEZERO(RPARM7)                           S22025
         LA    RWORK2,FOUR              TO ACCESS HOLD Q         S22025
         SR    RPARM7,RWORK2            GET ADDR OF HOLD Q       S22025
         MVC   PRFKEY+2(2),PRFINS       SAVE THE DATA OFFSET     S22025
         CLC   AVTADBUF+1(3),SCBDEOB+1  LAST GOOD EOB IN BUFFER  S22025
         BNE   BNLINK2                  NO, PUT DATA ON HOLD Q   S22025
         SPACE 1                                                 S22025
         LR    RWORK2,RWORK8            GET DATA OFFSET          S22025
         SH    RWORK2,PRFINS            SUBTRACT LOGICAL GARBAGE S22025
         CH    RWORK2,SCBEOB            EOB IN DATA TO HOLD      S22025
         BNH   THROBACK                 YES, DON'T HOLD DATA     S22025
BNLINK2  EQU   *                                                 S22025
         LA    RWORK8,ONE(RWORK8)       INCREMENT BY ONE         S22025
         ST    RWORK8,PRFINS            SET DATOFF,ZERO INSOFF   S22025
         IC    R15,AVTPARM+3            GET AO INDEX             S22025
         STC   R15,AVTPARM              SET INDEX FOR AO         S22025
         OI    AVTPARM,ONE              INDICATE DIRECT RETURN   S22025
         MVI   AVTPARM3,AVTEFF          INDICATE UNIT NEEDED     S22025
         MVI   AVTPARM+1,AVTEZERO       INDICATE DATA=CHARS      S22025
         LA    R1,AVTPARM               LINK TO                  S22025
         L     R15,AVTUI                AO TO GET                S22025
         BALR  R14,R15                  UNIT                     S22025
         LTR   R15,R15                  OUT OF UNITS             S22025
         BNZ   NOUNIT                   YES, GET OUT             S22025
         LH    RWORK8,PRFDAT            OBTAIN DATA OFFSET       S22025
         N     RWORK8,AVTCLRHI          CLEAR HALF               S22025
         BCTR  RWORK8,AVTEZERO          POINT TO LAST CHAR       S22025
         LH    RDATAD5,PRFSIZE          GET PREFIX SIZE          S22025
         N     RDATAD5,AVTCLRHI         CLEAR HALF               S22025
         SR    RDATAD5,RWORK8           GET AT CHAR LENGTH       S22025
         STH   RDATAD5,PRFKEY           SAVE AT CHAR LENGTH      S22025
         SH    RWORK8,PRFINS            FINAL SIZE               S22025
         LR    REX11,RWORK8             SAVE TRUE SIZE           S22025
         SH    RWORK8,PRFKEY+2          SUBTRACT THE DATA OFFSET S22025
         STH   RWORK8,PRFSIZE           SET SIZE                 S22025
         LR    RWORK8,REX11             RESET TRUE SIZE          S22025
UNITSCAN EQU   *                        TO FIND THE UNIT         S22025
         CH    RWORK8,AVTKEYLE          END OF DATA IN THIS UNIT S22025
         BNH   QMANIP                   YES                      S22025
         SH    RWORK8,AVTKEYLE          OBTAIN NEXT              S22025
         B     UNITSCAN                 UNIT                     S22025
*                                                                S22025
QMANIP   EQU   *                                                 S22025
         L     RWORK2,PRFLCB-1          LCB ADDRESS              S22025
         L     RWORK2,LCBDCBPT-IEDQLCB(,RWORK2)  DCB ADDRESS     S22025
         SR    R0,R0                    INITIALIZE REGISTER      S22025
         IC    R0,DCBRESER+1-IHADCB(,RWORK2) NO OF TEXT IDLES    S22025
         LR    RWORK2,R0                SAVE NUMBER OF IDLES     S22025
         LA    RWORK2,AVTTXTSZ(,RWORK2) TEXT PREFIX SIZE         S22025
GETUNIT  EQU   *                                                 S22025
         SR    R0,R0                    CLEAR                    S22025
         STH   R0,PRFINS                ZERO SPACE AVAILABLE     S22025
         LA    R1,AVTPARM               LINK TO                  S22025
         L     R15,AVTUI                AO TO GET                S22025
         BALR  R14,R15                  UNIT                     S22025
         LTR   R15,R15                  OUT OF UNITS             S22025
         BNZ   NOUNIT                   YES, GET OUT             S22025
         LH    R15,PRFSIZE              GET SIZE OF UNIT         S22025
         N     R15,AVTCLRHI             CLEAR                    S22025
         SH    R15,AVTKEYLE             GET CORRECT SIZE         S22025
         STH   R15,PRFSIZE              STORE                    S22025
         AH    RWORK8,AVTKEYLE          ADJUST DATA OFFSET       S22025
BLDPRF   EQU   *                                                 S22025
         L     RLCB4,PRFLCB-1           GET LCB ADDRESS          S22025
         SR    R15,R15                  CLEAR                    S22025
         IC    R15,PRFNBUNT             GET TOTAL NUMBER OF UNITSS22025
         SR    R0,R0                    CLEAR                    S22025
         LR    R1,REX11                 GET TRUE SIZE            S22025
         N     R1,AVTCLRHI              CLEAR FOR DIVIDE         S22025
         LH    R14,AVTKEYLE             GET UNIT SIZE            S22025
         DR    R0,R14                   GET NUMBER OF FULL UNITS S22025
         LTR   R0,R0                    ANY PARTIALLY FILLED     S22025
         BZ    SETUNITS                 NO                       S22025
         LA    R1,ONE(,R1)              ADD ONE                  S22025
SETUNITS EQU   *                        PUT NUMBER OF UNITS IN   S22025
         STC   R1,PRFNBUNT              IN ORIGINAL BUFFER       S22025
         SR    R15,R1                   GET NUMBER IN NEW        S22025
         LR    RDATAD5,RPREFIX          GET FIRST UNIT           S22025
BLOOP    EQU   *                                                 S22025
         L     RDATAD5,PRFTIC-IEDQPRF(,RDATAD5)  GET UNIT        S22025
         BCT   R1,BLOOP                 FIND CORRECT UNIT        S22025
         MVC   MVOFF(LEN,RDATAD5),MVOFF(RPREFIX) MOVE PREFIX     S22025
         L     RLCB4,AVTADBUF           SAVE OLD BUF ADDRESS     S22025
         ST    RDATAD5,AVTADBUF         SET UP NEW BUF ADDRESS   S22025
         LR    RPREFIX,RDATAD5          SET BASE TO NEW BUFFER   S22025
         STC   R15,PRFNBUNT             SET NO UNITS NEW BUF     S22025
         LA    R15,ONE(,RWORK8)         DATA OFFSET PLUS ONE     S22025
         STH   R15,PRFDAT               DATA OFFSET              S22025
         BCTR  R15,R0                   SUBT ONE FROM OFFSET     S22025
         SR    R15,RWORK2               CALCULATE AVAILABLE SPACES22025
         STH   R15,PRFINS               SPACE AVAILABLE          S22025
         OI    PRFSTAT1,PRFNHDRN        INSURE TEXT BUFFER       S22025
         OI    PRFTQBCK+TWO,BFREDIT     INDICATE DATA MOVEMENT   S22025
         STH   RWORK2,PRFSCAN           SCAN PAST PRFIX AND RES  S22025
         LH    RWORK2,PRFKEY-IEDQPRF(,RLCB4) GET AT CHAR LENGTH  S22025
         AR    RWORK8,RWORK2            GET SIZE                 S22025
         STH   RWORK8,PRFSIZE           STORE SIZE               S22025
         LR    RWORK8,RWORK2            SET UP REG 8 FOR AF      S22025
         IC    R15,AVTPARM+2            AF INDEX                 S22025
         LA    R15,ONE(R15)             SHIFT DATA FUNCTION      S22025
         STC   R15,AVTPARM              PUT INDEX IN PARM LIST CES22025
         MVI   AVTPARM+1,AVTEZERO       INDICATE DATA IA CHAR    S22025
         LA    R1,AVTPARM               POINT TO PARAMETER LISTCES22025
         L     R15,AVTUI                GET INTERFACE ROUTINE    S22025
         BALR  R14,R15                  GO TO AF                 S22025
         LH    RWORK2,PRFSIZE           GET SIZE SPACE AVAIL     S22025
         SH    RWORK2,PRFINS            SUBTRACT SPACE AVAILABLE S22025
         STH   RWORK2,PRFSIZE           STORE CORRECT SIZE       S22025
         LH    R14,AVTHA2               GET TIC ADDR FOR LAST UNIS22025
         SR    R15,R15                  CLEAR                    S22025
         IC    R15,PRFNBUNT             NO UNITS IN NEW BUF      S22025
         B     GETLAST                  CHECK FOR LAST           S22025
         SPACE 1                                                 S22025
NOUNIT   EQU   *                                                 S22025
         MVC   PRFINS(2),PRFKEY+2       RESET SPACE AVAILABLE    S22025
         LH    RWORK8,PRFSIZE           GET SIZE                 S22025
         N     RWORK8,AVTCLRHI          CLEAR                    S22025
         LA    RWORK8,ONE(,RWORK8)      BUMP ONE                 S22025
         SH    RWORK8,PRFDAT            GET MOVE LENGTH          S22025
         LA    RPARM7,FOUR(,RPARM7)     GET  PARM LIST           S22025
         L     RLCB4,PRFLCB-1           RESET LCB BASE           S22025
         LA    R14,FINALSZE             SET RETURN POINT         S22025
         B     CLEARUP                  CLOSE BUFFER             S22025
ONEUNIT  EQU   *                                                 S22025
         L     RDATAD5,PRFTIC-IEDQPRF(,RDATAD5) GET NEXT         S22025
GETLAST  EQU   *                        FIND LAST UNIT           S22025
         BCT   R15,ONEUNIT              FOUND                    S22025
         ST    R14,PRFTIC-IEDQPRF(,RDATAD5)  INVALID TIC ADDR    S22025
         MVI   PRFTIC-IEDQPRF(RDATAD5),EIGHT SET TIC FLAG        S22025
         LR    RDATAD5,RPREFIX          POINT TO FIRST UNIT      S22025
         L     R14,PRFLCB-ONE-IEDQPRF(RDATAD5) SET UP LCB ADDR @SA70224
         MVC   LCBSCB(3,RDATAD5),LCBSCBA-IEDQLCB(R14)  SCB ADDR@SA74031
         LR    RPREFIX,RLCB4            RESTORE ORIGINAL BUFFER  S22025
         ST    RPREFIX,AVTADBUF         IN AVT                   S22025
         NC    ONE(THREE,RPARM7),ONE(RPARM7)  IS Q EMPTY         S22025
         BZ    NOBUFS                   YES                      S22025
         L     RWORK8,AVTEZERO(RPARM7)  NO GET NEXT BUFFER ADDR  S22025
LOOP1    EQU   *                                                 S22025
         NC    FIVE(THREE,RWORK8),FIVE(RWORK8) IS THIS LAST ON Q S22025
         BZ    QLINK                    YES LINK IN NEW BUF      S22025
         L     RWORK8,FOUR(RWORK8)      NO GET NEXT BUF ADDR     S22025
         B     LOOP1                    GET NEXT BUF ADDR        S22025
QLINK    EQU   *                                                 S22025
         ST    RDATAD5,FOUR(RWORK8)     LINK BUF IN Q            S22025
         B     LASTBUF                  INDICATE LAST BUFFER     S22025
NOBUFS   EQU   *                                                 S22025
         ST    RDATAD5,AVTEZERO(RPARM7) PUT BUFFER ON Q          S22025
LASTBUF  EQU   *                                                SA68683
         MVC   FIVE(THREE,RDATAD5),AVTFZERO  CLEAR LINK FIELD    S22025
         LH    R0,PRFSIZE               GET PRFISIZE AND CHECK   S22025
         B     CHKSIZE                  FOR NO DATA LEFT IN BUFF S22025
         EJECT
CLEAR    EQU   *
         SR    R15,R15                  CLEAR 'FROM'
         LR    R0,R15                     BYTE FROM
         IC    R15,AVTDOUBL                 TRANSLATE
         STC   R0,TRANTAB(R15)                TABLE
         SPACE
THROBACK EQU   *
         LH    RWORK8,PRFDAT            GET OLD DATA OFFSET
         N     RWORK8,AVTCLRHI
         LR    RDATAD5,RWORK8           SET INPUT FOR IEDQAL
         SH    RDATAD5,PRFINS           GET TRUE INSERT OFFSET
         BAL   R14,ENTERLOP             GET ADDRESS OF ITEM      S22025
         SPACE
         IC    R15,AVTDOUBL             MOVE HIT BYTE
         STC   R15,AVTEZERO(,RDATAD5)     BACK TO INSERT POINT
         SPACE
PASSNORM EQU   *
         LA    RDATAD5,ONE(,RWORK8)     BUMP PAST HIT BYTE
         STH   RDATAD5,PRFDAT           STORE IT BACK
         B     TESTEOS                  RE-ENTER TRT LOOP
         SPACE 3
CLERFROM EQU   *
         LH    RWORK8,SBPMFROM          GET OFFSET TO FROM STRING
         N     RWORK8,AVTCLRHI
         L     RSTRING4,PARMCHAR        GET CHARS TABLE ADDRESS
         SR    R15,R15
         IC    R15,ONE(RWORK8,RSTRING4) PICK UP 1ST BYTE FROM
*                                         STRING
         STC   R15,AVTDOUBL             SAVE IT IN AVT WORK AREA
         B     CLEAR                    GO CLEAR 'FROM' BYTE
         EJECT
FOUNDEQU EQU   *
         LR    RWORK8,R0                NEXT CHAR FOR SCAN       S22025
         LA    RWORK8,ONE(,RWORK8)      BUMP OFFSET              S22025
         L     RWORK2,PARMCHAR          RESTORE ADDR OF CHAR TABLS22025
         SPACE
TESTREM  EQU   *
         TM    SBPMSTAT,TOCHAR+TOOFFS+TOEXTN REMOVE FUNCTION
         BNZ   XREMOVE                  YES, GO PROCESS
         SPACE
         LA    R14,INSERT               SET RETURN TO INSERT
         SPACE
TSTFINCL EQU   *
         LH    RDATAD5,PRFDAT           GET OFFSET TO START
         N     RDATAD5,AVTCLRHI           OF FROM STRING
         SR    RWORK8,RDATAD5           GET LNG OF FROM STRING
         SPACE
         TM    SBPMSTAT,FROMINCL        FROM STRING TO BE REMOVED
         BO    FROMREM                  YES, GO UPDATE OFFSETS
         SPACE
         CLI   PRFINS+1,AVTEZERO        ANY EMPTY BYTES
         BE    UPDATOFF                 NO, NO SHIFT NEEDED, BRANCH
         SPACE 1                                                 S22025
CLEARUP  EQU   *                                                 S22025
         SPACE
         IC    R1,PARMAF                YES, GET IEDQAF INDEX
         LA    R1,SHIFFLAG(,R1)         REQUEST MULTI-UNIT SHIFT
         B     DOUBSHIF                 GO SHIFT STRING AND RETURN
         SPACE
FROMREM  EQU   *
         LR    R0,RWORK8                UPDATE INSERT
         AH    R0,PRFINS                  OFFSET BY
         STH   R0,PRFINS                  STRING LENGTH
         SPACE
UPDATOFF EQU   *
         LA    RDATAD5,AVTEZERO(RWORK8,RDATAD5) UPDATE DATA OFFSET
         STH   RDATAD5,PRFDAT             BY LENGTH OF FROM STRING
         BR    R14                      RETURN TO CALLER
         EJECT
INSERT   EQU   *
         TM    SBPMSTAT,DATAIDCT        IS DATA IDLES
         BO    INSIDLE                  YES, GO INSERT IDLES
         SPACE
INSDATA  EQU   *
         L     RSTRING4,PARMCHAR        NO, GET CHAR TABLE ADDRESS
         LH    RDATAD5,SBPMDATA         GET OFFSET TO DATA CHARS
         N     RDATAD5,AVTCLRHI
         IC    REX11,AVTEZERO(RDATAD5,RSTRING4) GET LENGTH OF DATA
         LA    RSTRING4,ONE(RDATAD5,RSTRING4) GET ADDRESS OF DATA
         ST    RSTRING4,AVTPARM3        SET IN PARAMETER AREA
         STC   REX11,AVTPARM3           SET LNG IN PARAMETER AREA
         SR    R0,R0                    SET DATA TYPE = CHARS
         B     INSERT3                  GO BUILD AO PARAM LIST
         SPACE
INSIDLE  EQU   *
         LH    R0,SBPMDATA              PICK UP IDLES CT & CHAR
         STH   R0,AVTPARM3              PUT IN PARAMETER AREA
         LA    R0,IDLEFLAG              SET DATA TYPE = IDLES
         SPACE
INSERT3  EQU   *
         STC   R0,AVTPARM+1             SET DATA TYPE
         IC    R0,PARMAO                PICK UP IEDQAO INDEX
         STC   R0,AVTPARM               SET IN PARM LIST
         IC    R0,PARMAF                PICK UP IEDQAF INDEX
         STC   R0,AVTPARM+2             SET IN PARM LIST
         LA    R1,AVTPARM               POINT TO IEDQAO'S PARM LIST
         L     R15,AVTUI                GET USER INTERFACE ADDRESS
         BALR  R14,R15                  LINK TO INSERT DATA
         SPACE
         LH    RDATAD5,PRFDAT           REINITIALIZE
         N     RDATAD5,AVTCLRHI           DATA OFFSET REGISTER
         L     R1,PRFLCB-1              GET LCB ADDRESS          S22025
         TM    LCBSTAT1-IEDQLCB(R1),LCBSENDN SEND OPERATION      S22025
         BO    PARMM                    YES, DON'T UPDATE EOBOFF S22025
         MVC   PRFXTRA(3),AVTFZERO      CLEAR FOR SCBEOB UPDTE   S22025
         OI    PRFTQBCK+TWO,BFREDIT     INDICATE DATA MOVEMENT   S22025
PARMM    EQU   *                                                 S22025
         LTR   R15,R15                  TEST RETURN
         BZ    TESTEOS                  GOOD, RE-ENTER TRT LOOP
         SPACE
NOMORE   EQU   *
         CLI   PRFINS+1,AVTEZERO        ANY LOGICAL GARBAGE LEFT
         BE    FINALSZE                 NO, BYPASS CLOSEUP
         SPACE
         LH    RWORK8,PRFSIZE           PICK UP TOTAL DATA SIZE
         N     RWORK8,AVTCLRHI
         LA    RWORK8,ONE(,RWORK8)      POINT BEYOND DATA
         SR    RWORK8,RDATAD5           SET LIMIT REG = RESIDUAL CT
         SPACE
         IC    R1,PARMAF                PICK UP IEDQAF INDEX
         LA    R1,SHIFFLAG(,R1)         REQUEST MULTIPLE SHIFT
         BAL   R14,DOUBSHIF             GO CLOSE UP BUFFER
         SPACE
         LA    R15,AVTECD4              SET ERROR RETURN CODE
         B     FINALSZE                 GO SET FINAL SIZE
         EJECT
XREMOVE  EQU   *
         STH   RWORK8,PRFKEY+2          SAVE START OF DELETE DATA
         TM    SBPMSTAT,TOCHAR          IS TO A CHARACTER STRING
         BNO   XREMOVE2                 NO, TEST AGAIN
*                                                                S22025
         LH    RSTRING4,SBPMTO          GET OFFSET TO TO CHARS
         N     RSTRING4,AVTCLRHI
         L     RWORK8,PARMCHAR          GET CHARS TABLE OFFSET
         LA    RSTRING4,AVTEZERO(RSTRING4,RWORK8) GET TO CHARS ADDR
         TM    AVTEZERO(RPARM7),NOBLANKN  CHECK FOR BLANKS       S22025
         BO    FIXBLNK                  NO                       S22025
         SR    RWORK2,RWORK2            CLEAR REGISTER           S22025
         IC    RWORK2,BLNKCHAR(RPARM7)  GET BLANK CHAR           S22025
         B     LINKSCAN                 KEEP IT POSITIVE         S22025
FIXBLNK  EQU   *                                                 S22025
         LNR   RWORK2,RWORK2            INDICATE NO BLANKS       S22025
LINKSCAN EQU   *                                                 S22025
         IC    RWORK8,AVTEZERO(RSTRING4) GET LENGTH OF STRING    S22025
         LA    R1,ONE(,RSTRING4)        GET ADDR OF CHAR STRING  S22025
         SLL   RWORK8,TFOUR             TO PUT COUNT IN HI BYTE  S22025
         AR    R0,RWORK8                SET UP REG FOR AX        S22025
         SRL   RWORK8,TFOUR             SET REG FOR COMPARE      S22025
         L     R15,AVTAX                GET ADDR OF SCAN ROUTINE S22025
         BALR  R14,R15                  LINK TO SCAN ROUTINE     S22025
         SPACE
         CR    R15,RWORK8               WAS CHAR STRING FOUND    S22025
         BE    TOFOUND                  YES                      S22025
         TM    PARMAO,INBLK             IN INBLOCK GROUP         S22025
         BZ    CLERFROM                 NO CHAR STRING FOUND     S22025
         TM    PRFSTAT1,PRFNLSTN        LAST                     S22025
         BNO   CLERFROM                 YES                      S22025
         B     BNLINK                   PUT ON HOLD QUEUE        S22025
TOFOUND  EQU   *                                                 S22025
         SPACE
         LH    RWORK8,PRFKEY+2          GET OFFSET TO START OF
         N     RWORK8,AVTCLRHI           DELETE DATA
         LR    R15,R0                   FOUND,SAVE OFFSET TO     S22025
         STH   R15,PRFKEY+2               RESTART OF DATA
         BAL   R14,TSTFINCL             GO HANDLE FROM STRING
         SPACE
         LH    R15,PRFKEY+2             RESTORE OFFSET TO
         LA    R15,ONE(,R15)            FOR DATA OFFSET          S22025
         N     R15,AVTCLRHI               RESTART OF DATA
*        B     XREMFIND                 PROCEED
         EJECT
XREMFIND EQU   *
         TM    SBPMSTAT,TOINCL          IS TO INCLUDED IN FUNC
         BO    UPINSOFF                 YES, R15 IS CORRECT AS IS
         SPACE
         LA    R15,ONE(,RWORK2)         POINT TO START           S22025
         SPACE
UPINSOFF EQU   *
         LR    RDATAD5,R15              SET NEW DATA OFFSET
         LH    R0,PRFDAT                GET OLD DATA OFFSET
         N     R0,AVTCLRHI
         SR    R15,R0                   GET LENGTH OF NEW GARBAGE
         L     R1,PRFLCB-1              GET ADDRESS OF LCB       S22025
         TM    LCBSTAT1-IEDQLCB(R1),LCBSENDN INPUT OPERATION     S22025
         BO    NOUPDATE                 YES                      S22025
         LR    R0,R15                   OBTAIN REMOVE LENGTH     S22025
         AH    R0,PRFXTRA+1             INCREMENT COUNT          S22025
         STH   R0,PRFXTRA+1             STORE COUNT              S22025
NOUPDATE EQU   *
         AH    R15,PRFINS               ADD TO NO. AVAIL BYTES
         STH   R15,PRFINS               STORE IT BACK
         STH   RDATAD5,PRFDAT           STORE NEW DATA OFFSET
         SPACE
         TM    SBPMSTAT,DATACONT        IS FUNCTION CONTRACT
         BO    TESTEOS                  YES, RE-ENTER TRT LOOP
         B     INSERT                   NO, GO INSERT
         SPACE
XREMOVE2 EQU   *
         BAL   R14,TSTFINCL             GO HANDLE FROM STRING
         SPACE
         LH    R14,PRFDAT               PICK UP DATA OFFSET
         N     R14,AVTCLRHI
         LH    R15,SBPMTO               PICK UP EXTENT
         N     R15,AVTCLRHI
         AR    R15,R14                  ADD TO GET TO OFFSET
         LH    R14,PRFSIZE              PICK UP
         N     R14,AVTCLRHI               TOTAL SIZE
         CR    R15,R14                  IS NEW OFFSET BEYOND END
         BNH   UPINSOFF                 NO, GO COMPUTE NEW PRFINS
         SPACE
         LA    R15,ONE(,R14)            YES, MAKE IT COME OUT EVEN
         B     UPINSOFF                 GO COMPUTE NEW PRFINS
         EJECT
TESTSHIF EQU   *
         LH    R0,PRFINS                PICK UP NO. AVAIL BYTES
         LTR   R0,R0                    ARE THERE ANY
         BCR   CCZERO,R14               NO, NO SHIFT NEEDED, RETURN
         SPACE
         ST    RDATAD5,AVTPARM3         SET INSERT ADDRESS
         STC   REX11,AVTPARM3           SET INSERT LENGTH
         MVI   AVTPARM+1,AVTEZERO       SET DATA TYPE = CHARS
         SPACE
         IC    R1,PARMAF                PICK UP IEDQAF OFFSET
         LA    R1,SHIFUNIT(,R1)         REQUEST ONE-UNIT SHIFT
         SPACE
DOUBSHIF EQU   *
         STC   R1,AVTPARM               SET IN PARAMETER LIST
         LA    R1,AVTPARM               GET PARAM LIST ADDR
         L     R15,AVTUI                GET USER INTERFACE ADDRESS
         BR    R15                      LINK TO INSERT OR SHIFT
ENTERLOP EQU   *                                                 S22025
         LH    REOUAD,AVTKEYLE          GET KEY LENGTH           S22025
         LR    RWORK2,RPREFIX           LOAD BUFFER ADDR         S22025
         B     FINDUNIT                 BR TO GET UNIT ADDR      S22025
ADDRLOOP EQU   *                                                 S22025
         L     RWORK2,PRFTIC-IEDQPRF(RWORK2) PT TO NEXT UNIT     S22025
         SR    RSCAN,REOUAD             SUBT NO OF BYTES PASSED  S22025
FINDUNIT EQU   *                                                 S22025
         CR    RSCAN,REOUAD             IS ITEM IN THIS UNIT     S22025
         BH    ADDRLOOP                 NO, TEST NEXT UNIT       S22025
         LA    RSCAN,AVTUMALN-1(RSCAN,RWORK2) YES, UNIT ADDR     S22025
         LA    REOUAD,AVTUMALN(RWORK2,REOUAD) END OF UNIT ADDR   S22025
         BR    R14                      RETURN TO CALLER         S22025
         SPACE
********* EXECUTED INSTRUCTIONS *********
         SPACE
CLCX     CLC   AVTDOUBL(ONE),ONE(RSTRING4)
         SPACE
TRTX     TRT   AVTEZERO(ONE,RDATAD5),TRANTAB
         SPACE
         SPACE
         SPACE
         SPACE
         SPACE
********* TRANSLATE TABLE *********
         SPACE
TRANTAB  DC    256X'00'
KEYSAVE  DC    H'0'                     OPCODE SAVEAREA         SA54948
         EJECT
********* DSECTS *********
         SPACE
IEDQPARM DSECT
         SPACE
PARMAN   DS    XL1                      IEDQAN INDEX
PARMLEN  DS    XL1                      PARAMETER LIST LENGTH
PARMAF   DS    XL1                      IEDQAF INDEX
PARMAO   DS    XL1                      IEDQAO INDEX
PARMAJ   DS    XL1                      IEDQAJ INDEX
PARMBLNK DS    XL1                      BLANK CHARACTER
PARMENTS DS    XL1                      NUMBER OF ENTRIES
         DS    XL1                      (UNUSED)
PARMCHAR DS    AL4                      CHARACTERS TABLE ADDRESS
PARMFIRS DS    0XL1                     FIRST SUB-PARAMETER LIST
         SPACE 3
IEDQSBPM DSECT
         SPACE
SBPMKEY  DS    XL1                      KEY (OFFSET) OF THIS LIST
SBPMSTAT DS    XL1                      STATUS BYTE
SBPMDATA DS    H                        DATA DESCRIPTOR
SBPMFROM DS    H                        'FROM' DELIMITER DESCRIPTOR
SBPMTO   DS    H                        'TO' DELIMITER DESCRIPTOR
SBPMNEXT DS    0XL1                     NEXT SUB-PARAMETER LIST
         EJECT
         TAVTD
         EJECT
         TLCBD
         EJECT
         TPRFD
         SPACE
IEDQPRF  DSECT                                                 @Y17XAMZ
         ORG   IEDQPRF
PRFTINS  DS    H                        TEMPORARY INSERT OFFSET
PRFTDAT  DS    H                        TEMPORARY DATA OFFSET
PRFINS   DS    H                        INSERT OFFSET
PRFDAT   DS    H                        DATA OFFSET
         EJECT
         TSCBD
         EJECT
         TTRMD
         DCBD  DSORG=TX                                          S22025
         END
