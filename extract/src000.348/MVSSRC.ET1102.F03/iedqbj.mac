*CHANGE ACTIVITY=AS FOLLOWS:
******************** MICROFICHE FLAGS *********************** SUPT CODE
*A000000-999999                                                @XA09783
***********************************************************************
*                                                                     *
* MODULE NAME = IEDQBJ                                                *
*                                                                     *
* COPYRIGHT = 'NONE'                                                  *
*                                                                     *
* STATUS:  CHANGE LEVEL 5                                             *
*                                                                     *
* FUNCTION --                                                         *
*   THIS ROUTINE EVALUATES THE COUNT= EXPRESSION AND SETS A           *
*   RETURN CODE BASED ON THE COMPARISON.  UNLESS UPDATE=NO IS         *
*   SPECIFIED, THE COUNT= OPFIELD IS UPDATED BEFORE THE COMPARISON    *
*   IS MADE.  IF CLEAR=YES IS SPECIFIED THEN THE OPFIELD IS CLEARED   *
*   UPON A SUCCESSFUL COMPARISON.                                     *
* ENTRY POINTS --                                                     *
*   IEDQBJ                                                            *
*                                                                     *
* INPUT --                                                            *
*   REGISTER 1 - TERMINAL ENTRY ADDRESS                               *
*   REGISTER 2 - IEDQBJ PARAMETER LIST                                *
*   REGISTER 3 - SCB ADDRESS                                          *
*   REGISTER 4 - LCB ADDRESS                                          *
*   REGISTER 8 - CALLER'S PARAMETER LIST                              *
*   REGISTER 11 - DISPATCHER'S BASE ADDRESS                           *
*   REGISTER 12 - CALLING ROUTINE'S BASE ADDRESS                      *
*   REGISTER 13 - AVTSAVE2                                            *
*   REGISTER 14 - RETURN REGISTER                                     *
*   REIGSTER 15 - BASE REGISTER                                       *
*                                                                     *
* OUTPUT --                                                           *
*   REGISTER 2 - IEDQBJ PARAMETER LIST                                *
*   REGISTER 3 - SCB ADDRESS                                          *
*   REGISTER 4 - LCB ADDRESS                                          *
*   REGISTER 7 - TERMINAL ENTRY ADDRESS                               *
*   REGISTER 8 - CALLER'S PARAMETER LIST                              *
*   REGISTER 11 - DISPATCHER'S BASE ADDRESS                           *
*   REGISTER 12 - CALLING ROUTINE BASE                                *
*   REGISTER 13 - AVTSAVE2                                            *
*   REGISTER 15 - RETURN CODE                                         *
*                                                                     *
* EXTERNAL REFERENCES --                                              *
*   NONE                                                              *
*                                                                     *
* EXITS, NORMAL --                                                    *
*   CALLING ROUTINE WITH ZERO RETURN CODE.                            *
*                                                                     *
* EXITS, ERROR --                                                     *
*   CALLING ROUTINE WITH NONZERO RETURN CODE                          *
*                                                                     *
* TABLES/WORK AREAS --                                                *
*   INPCIND, TERMINAL ENTRY, TCAM AVT, OPTION FIELDS                  *
*                                                                     *
* ATTRIBUTES --                                                       *
*   REUSABLE, REFRESHABLE, ENABLED                                    *
*                                                                     *
* CHARACTER CODE DEPENDENCY --                                        *
*   NONE.                                                             *
*                                                                     *
*                                                                     *
***********************************************************************
         EJECT
IEDQBJ   CSECT
         TITLE 'IEDQBJ COUNT= EXPRESSION EVALUATION ROUTINE'
R0       EQU   0                        WORK REG
R1       EQU   1                        TERMINAL ENTRY ADDRESS
RPARM    EQU   2                        IEDQBJ PARAMETER LIST
RSCB     EQU   3                        SCB ADDRESS
RLCB     EQU   4                        LCB ADDRESS
R5       EQU   5
R6       EQU   6
R7       EQU   7                        SUBROUTINE LINK/WORK REG
R8       EQU   8                        CALLERS PARM LIST
R9       EQU   9                        WORK REG
RA       EQU   10                       WORK REG
RDISP    EQU   11
R12      EQU   12                       CALLING ROUTINE BASE
RAVT     EQU   13                       AVTSAVE2
RETURN   EQU   14                       RETURN REGISTER
R15      EQU   15                       BASE REGISTER
ONE      EQU   1                        ONE
FOUR     EQU   4                        FOUR
EIGHT    EQU   8                        EIGHT
THREE    EQU   3                        THREE
ZERO     EQU   0                        ZILCH
         USING IEDQTRM,R1
         USING INPCIND,RPARM
         USING AVTSAVE2,RAVT
         USING *,R15
         SPACE
IEDQBJ   IEDHJN  BEGIN
         SPACE 2
         TM    TRMSTATE,TRMOPTFN        OPTION FIELDS DEFINED
         BNO   NOOPT                    BR IF NO OPTION FIELD
         SR    R9,R9                    CLEAR REG
         IC    R9,INPOPT2               GET OPTION 2
         LTR   R9,R9                    IS OPTION 2 SPECIFIED
         BNZ   NOTINT                   B YES , NOT INTEGER
         MVC   AVTDOUBL+FOUR(FOUR),INPINTG  MOVE INTEGER
         B     NEXT
         SPACE
NOTINT   EQU   *
         BAL   R7,LOCSUB                FIND OPTION FIELD
         MVC   AVTDOUBL+4(FOUR),AVTDOUBL  MOVE OPTION 2
         SPACE
NEXT     EQU   *
         SR    R9,R9
         IC    R9,INPOPT                OPTION 1
         BAL   R7,LOCSUB                FIND OPTION FIELD
         TM    INPCC,INPUPDAT+INPUPADD  UPDATE= SPECIFIED
         BZ    COMP                     NO UPDATE
         LA    R0,ONE                   ASSUME ADD
         BO    ADD                      ADD 1
         LCR   R0,R0                    THIS IS TO SUBR 1
         SPACE
ADD      EQU   *
         A     R0,AVTDOUBL              ADD(OR SUBR) ONE FROM OPTION
         ST    R0,AVTDOUBL              SAVE IN FIELD
         EX    RA,MVC2                  DATA BACK TO OPFIELD
         SPACE
COMP     EQU   *
         L     R0,AVTDOUBL              GET FIRST OPERAND
         IC    R7,INPCC                 CONDITION CODE + FLAGS
         SLL   R7,FOUR                  SHIFT OUT FLAGS
         C     R0,AVTDOUBL+4            COMPARE OPFIELDS (OR INTGR)
         EX    R7,BC                    USE OPERATOR SPECIFIED
         LA    R15,FOUR                 BAD RETURN - NOEXEC
         BR    RETURN                   RETURN TO CALLER
         SPACE
NOOPT    EQU   *
         LA    R15,EIGHT                SET ERROR IN OPFIELD
         BR    RETURN                   RETURN
         SPACE
GOOD     EQU   *
         TM    INPCC,INPNOCLR           IS CLEAR= YES SPECIFIED
         BO    GOOD1                    BRANCH NO - FUNCTION COMPLETE
         EX    RA,XC                    CLEAR OPTION FIELD 1
GOOD1    EQU   *
         SR    R15,R15                  SET ZERO RETURN CODE
         LR    R7,R1                    RESTORE R7 FOR IEDQBI
         BR    RETURN                   RETURN - COMPLETE
LOCSUB   EQU   *
         EX    R9,CKMXOPT               THIS MANY OPTIONS DEFINED
         BL    NOOPT                      BRANCH NO
         SPACE
         LR    R0,R9                    SAVE OPTION FIELD NUMBER
         IC    R9,TRMOPT-1(R9)          OFFSET TO OPTION DATA
         EX    R9,CKNOOPT               CHECK IF OPDATA OMITTED
         BE    NOOPT                      BRANCH NO OPTION FIELD
         SPACE
         AL    R9,AVTOPTPT              ADD TABLE START
         LH    RA,TRMOPTBL              OFFSET TO DATA FOR THIS TERM
         N     RA,AVTCLRHI              ACCOUNT FOR 32K+
         ALR   R9,RA                    R9,POINTS TO OPTION DATA
         SPACE
         BCTR  R0,0                     DECREMENT FOR INDEX
         LR    RA,R0                    COPY
         ALR   RA,RA                    DOUBLE
         SLL   R0,THREE                 TIMES 8
         ALR   R0,RA                    R0 = TIMES 10
         L     RA,AVTOPTPT              OPTION TABLE ADDRESS
         L     RA,FOUR(,RA)             OPTION CHARACTERISTICS
         ALR   RA,R0                    FOR THIS OPTION FIELD
         SR    R5,R5                    CLEAR REG
         IC    R5,0(RA)                 GET LENGTH OF OPTION FIELD
         LR    RA,R5                    GET LENGTH IN REG 10
         EX    RA,CLIOPL                IS LENGTH LE 3
         BL    NOOPT                    BRANCH NO  - ERROR
         XC    AVTDOUBL(3),AVTDOUBL     CLEAR HIGH ORDER
         LA    R5,AVTDOUBL              SET TO ALIGN
         SR    R5,RA                      OPDATA IN DOUBL
         EX    RA,MVC1                  MOVE OPDATA TO DOUBL
         BR    R7                       RETURN TO CALLER
*        EXECUTE INSTRUCTIONS
BC       BC    ZERO,GOOD                USE OPERATOR SPECIFIED
*                                          IN COUNT(2)
MVC1     MVC   THREE(ZERO,R5),ZERO(R9)  DATA TO DOUBL
MVC2     MVC   ZERO(ZERO,R9),THREE(R5)  DOUBL TO DATA
XC       XC    ZERO(,R9),ZERO(R9)       CLEAR OPTION FIELD ONE
CKMXOPT  CLI   TRMOPNO,0                NUMBER OF OPTION FIELDS
CKNOOPT  CLI   AVTHFF,0                 OPTION FIELD OMITTED
CLIOPL   CLI   AVTHA2+1,0               COMPARE OPTION FIELD LENGTH
         EJECT
INPARM   DSECT
INPIND   DS    AL1                      INDEX TO IEDQBI
INPMSK   EQU   X'01'                    MASK NOT SPECIFIED
INPLEN   DS    AL1                      LENGTH OF PARM LIST
INPFLAG  DS    AL1                      FLAG BYTE
INPCOUNT EQU   X'80'                    COUNT= SPECIFIED
INPINVL  EQU   X'20'                    INTVL= SPECIFIED
INPLINE  EQU   X'10'                    TARGET= LINE
INPVARY  EQU   X'08'                    VARYOFF MACRO CALL
INPMASK  DS    XL5                      ERROR MASK
INPCIND  DS    AL1                      INDEX TO IEDQBJ
INPCC    DS    XL1                      CONDITION CODE + FLAGS
INPNOCLR EQU   X'40'                    CLEAR=NO
INPUPDAT EQU   X'20'                    UPDATE= SPECIFIED
INPUPADD EQU   X'10'                    UPDATE=ADD
INPCNINT EQU   X'80'                    COUNT(3)= INTEGER
INPOPT   DS    AL1                      INDEX TO OPFIELD 1
INPOPT2  DS    AL1                      INDEX TO OPFIELD 2 OR ZERO
INPINTG  DS    XL4                      INTEGER FOR COUNT(3)
INPINTV  DS    XL2                      INTERVAL
         EJECT
         TTRMD
         EJECT
         TAVTD
         END
