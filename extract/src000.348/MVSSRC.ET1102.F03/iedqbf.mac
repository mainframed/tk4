BF       TITLE ''' IEDQBF ''  UNLOCK ROUTINE'
IEDQBF   CSECT
         SPACE 3                                                SA63983
*  CHANGE ACTIVITY AS FOLLOWS
********************MICROFICHE FLAGS ************************ SUPT CODE
*A210000,310000                                                 SA51090
*A470000,485000,490000,545000,556000,650000                     SA51090
*C200000,360000                                                 SA51090
*C580000,605000,638600                                          SA51090
*D636000-637000,662000                                          SA51090
*A556000                                                         S22025
*C200000,310000,330000,560000-570000,640000                      S22025
*A558400                                                        SA63983
*C 559600                                                       SA63983
*A558800,639080,690000,515000                                  @OZ09283
*A559040                                                       @OZ27201
*C215000,517000,557640                                         @OZ27201
         SPACE 3                                                SA63983
***********************************************************************
*                                                                     *
*TITLE -- 'IEDQBF' UNLOCK TERMINAL ROUTINE                            *
*                                                                     *
*  MODULE NAME = IEDQBF                                               *
*                                                                     *
*  DESCRIPTIVE NAME = UNLOCK TERMINAL ROUTINE                         *
*                                                                     *
*  COPYRIGHT = 'NONE'                                                 *
*                                                                     *
*  STATUS CHANGE LEVEL 5                                              *
*                                                                     *
*FUNCTION -- THIS ROUTINE UNLOCKS THE CURRENTLY CONNECTED TERMINAL    *
*   IF IT HAS BEEN PREVIOUSLY LOCKED.                                 *
*                                                                     *
*ENTRY POINT -- IEDQBF                                                *
*                                                                     *
*   CALLING SEQUENCE -                                                *
*                  L     R12,AVTMSGS-1                                *
*                  IC    RX,INDEX                                     *
*                  L     R12,0(RX,R12)                                *
*                  BR    R12                                          *
*                                                                     *
*INPUT -- ENTRY IS FROM 'IEDQUI' USER INTERFACE FOUTINE         SA51090
*         OR 'IEDQBD' BUFFER DISPOSITION                        SA51090
*         PARAMETERS ARE PASSED IMPLICITLY IN REGISTERS AS FOLLOWS:   *
*                  R0    0 IF FROM IEDQBD                      @OZ27201
*                  R3    ADDRESS OF STATION CONTROL BLOCK (SCB)       *
*                  R4    ADDRESS OF LINE CONTROL BLOCK (LCB)          *
*                  R9    AVT ADDRESS                                  *
*                        (WHEN FROM IEDQUI)                    @ZM47875
*                  R12   BASE FOR THIS ROUTINE                        *
*                                                                     *
*OUTPUT -- THE LOCK BITS IN THE SCB ARE TURNED OFF IF THEY WERE ON.   *
*                                                                     *
*EXTERNAL ROUTINES -- NONE                                            *
*                                                                     *
*EXITS-NORMAL -- 'IEDQUI+4' WITH R15 =0                          S22025
*             -- 'DSPPOST' TO IEDQBD WITH R15=0                 SA51090
*                                                                     *
*EXIT-ERROR -- 'IEDQUI+4' WITH R15 = 4                           S22025
*                                                                     *
*TABLES/WORKAREAS --                                                  *
*DSECTS USED -- SCB,AVT,PRIOP,DISP,LCB,QCB                      SA51090
*                                                                     *
*ATTRIBUTES -- REUSABLE,REENTERANT,ENABLED,RESIDENT,PROBLEM           *
*              PROGRAM MODE.                                          *
*                                                                     *
*NOTES -- THE OPERATION OF THIS MODULE DOES NOT DEPEND UPON A         *
*   PARTICULAR INTERNAL REPRESENTATION OF THE EXTERNAL CHARACTER SET  *
***********************************************************************
         EJECT
*              REGISTERS
*
R0       EQU   0                        ENTRY INDICATOR         SA51090
R1       EQU   1                        DISPATCHER PARM LIST    SA51090
RSCB     EQU   3                        SCB ADDRESS
RLCB     EQU   4                        LCB ADDRESS
R7       EQU   7                        ADDRESS OF QCB FOR BD   SA51090
R8       EQU   8                        ADDRESS OF UNLOCK PARMS SA51090
R9       EQU   9                        AVT ADDRESS
R11      EQU   11                       DISPATCHER BASE ADDRESS SA51090
R12      EQU   12                       BASE
R14      EQU   14                       RETURN ADDRESS
R15      EQU   15                       BASE - RETURN CODE
FOUR     EQU   4                        OFFSET FOR RETURN
R6       EQU   6                        SAVT ADDR              @OZ09283
R10      EQU   10                       TTE ADDR AND SIBADDR   @OZ09283
ADDR     EQU   7                        MASK FOR ICM           @OZ09283
CVTPT    EQU   16                       CVT PTR ADDR           @OZ27201
         EJECT
         USING IEDQSCB,RSCB
         USING IEDQLCB,RLCB
         USING IEDQDISP,R11                                     SA51090
         USING *,R12
         SPACE 3
IEDQBF   IEDHJN START                                            S22025
         LTR   R0,R0                    ENTRY FROM UI OR BD     SA51090
         BNZ   UIUNLOCK                 BRANCH IF ENTRY FROM UI SA51090
         SPACE 2
* ENTRY FROM IEDQBD                                             SA51090
         L     R9,CVTPT                 GET CVT ADDR           @OZ27201
         USING CVT,R9                                          @ZM47875
         L     R9,CVTAQAVT              GET TCX ADDR           @ZM47875
         DROP  R9                                              @ZM47875
         USING IEDQTCX,R9                                      @ZM47875
         L     R9,TCXAVT                GET AVT ADDR           @ZM47875
         DROP  R9                                              @ZM47875
         USING IEDQAVTD,R9                                     @ZM47875
         LPR   R7,R7                    INSURE POSITIVE ADDRESS SA51090
         ST    R7,LCBERBQB-ONE          SET TO POST ERB TO BD   SA51090
         ST    R1,LCBERBLK-1             ADD TO POST LIST       SA63983
         MVI   LCBERBPY,PRIRCQCB        SET PRIORITY            SA51090
         SR    R10,R10                  INITIALIZE TTE ADDR    @OZ09283
         L     R15,AVTRNMPT             LOAD TTCIN AND         @OZ09283
         LH    R1,LCBTTCIN              BRANCH TO GET          @OZ09283
         N     R1,AVTCLRHI              TTE ADDRESS            @OZ09283
         BZ    NOTTCIN                  IF NO TTCIN BRANCH     @OZ09283
         BALR  R14,R15                                         @OZ09283
         LR    R10,R1                   TTE ADDRESS            @OZ27201
         LA    R15,TRMPRFSZ             BACKUP TO NEG PREFIX   @OZ09283
         SR    R10,R15                  OF TTE                 @OZ09283
NOTTCIN  EQU   *                                               @OZ09283
         LA    R1,LCBERB                SET PARM LIST FOR DISP  SA51090
         LA    R14,DSPCHAIN-FOUR        SET EXIT = DISPATCHER   SA63983
         B     UNLOCK                   UNLOCK TERMINAL         SA51090
         SPACE 2
* ENTRY FROM IEDQUI                                             SA51090
UIUNLOCK EQU   *                                                SA51090
         LA    R15,AVTECD4              SET A RETURN CODE        S22025
         L     R14,AVTUI                LOAD RETURN ROUTINE      S22025
         SPACE 2
UNLOCK   EQU   *                                                SA51090
         TM    SCBSTATE,SCBLCK1N+SCBMSGLN  IS TERMINAL LOCKED
         BZ    RETURN                   RETURN IF NOT          @Y17XAMX
*
         NI    SCBSTATE,SCBMSGLF-SCBLCK1N  UNLOCK TERMINAL
         XC    LCBINSRC,LCBINSRC        CLEAR DESTINATION AND FLAGS
         SR    R15,R15                  SET GOOD RETURN
         LTR   R0,R0                    ENTRY FROM UI OR BD     SA51090
         BNZ   RETURN                   RETURN IF FROM UI      @Y17XAMX
         EJECT
         TM    DISABLE(R8),ONE          DISABLE REQUESTED       SA51090
         BZ    RETURN                   RETURN IF NOT          @Y17XAMX
         TM    LCBSTAT2,LCBDIAL         IS THIS A DIAL LINE     SA51090
         BZ    RETURN                   RETURN IF NOT          @Y17XAMX
         OI    LCBSTAT2,LCBNEGRP        IF SO, SET UP LCB SO    SA51090
         MVI   LCBTSTSW,AVTEFF           DISABLE WILL BE DONE   SA51090
         LTR   R10,R10                   NO TTE ADDR           @OZ09283
         BZ    FOUR(R14)                 THEN BRANCH           @OZ09283
         USING IEDNTRM,R10                                    @OZ09283
         TM    TRMSTATE,TRMPREF          IF NOT NCP TTE        @OZ09283
         BNO   FOUR(R14)                 BRANCH                @OZ09283
         SR    R10,R10                   CLEAR FOR ICM         @OZ09283
         ICM   R10,ADDR,TRMSIBPT         GET SIB POINTER       @OZ09283
         DROP  R10                                             @OZ09283
         USING IEDSIBD,R10                                     @OZ09283
         LTR   R10,R10                   IF NO SIB THEN        @OZ09283
         BZ    FOUR(R14)                 BRANCH                @OZ09283
         L     R6,AVTSAVTP               OF NO LU TO LU SESSION@OZ09283
         USING IEDNSVTD,R6               BRANCH                @OZ09283
         CLC   SAVTSCPT,SIBINDEX                               @OZ09283
         BE    FOUR(R14)                                       @OZ09283
         OI    SIBSESSN,SIBLGOFF        SET LOGOFF BIT       @OZ09283
         DROP  R10                                             @OZ09283
         DROP  R6                                              @OZ09283
RETURN   LA    R14,FOUR(R14)   LOAD  BRANCH TO RETURN ROUTINE  @Y17XAMX
         BALR  R14,R14               BRANCH TO RETURN ROUTINE  @Y17XAMX
         EJECT
ONE      EQU   1                        CONSTANT ONE            SA51090
DISABLE  EQU   2                        OFFSET TO DISABLE FLAG  SA51090
ZERO     EQU   0                        ZERO                   @OZ09283
         SPACE 5
         TPRIOR                                                 SA51090
         TDISPD                                                 SA51090
         TLCBD
         TQCBD
         TAVTD
         EJECT
         TSCBD
         EJECT                                                 @OZ09283
         TSIBD                                                 @OZ09283
         EJECT                                                 @OZ09283
         TTRMD                                                 @OZ09283
         EJECT
         CVT   DSECT=YES
         EJECT
         TTCXD
         END
