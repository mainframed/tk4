         TITLE 'DQDA     ''IEDQDA''    -  VIRTUAL DIAGNOSE ROUTINE'
IEDQDA   CSECT
         SPACE
*CHANGE-ACTIVITY = AS FOLLOWS:
*A000000-999999                                                  Y05331
         SPACE 5
***********************************************************************
*                                                                     *
*TITLE -- 'IEDQDA'    VIRTUAL DIAGNOSE ROUTINE                        *
*                                                                     *
*  MODULE NAME = IEDQDA                                               *
*                                                                     *
*  DESCRIPTIVE NAME = VIRTUAL DIAGNOSE ROUTINE                        *
*                                                                     *
*  COPYRIGHT = 'NONE'                                                 *
*                                                                     *
*  STATUS:  CHANGE LEVEL 6
*                                                                     *
*FUNCTION -- IF IN A VIRTUAL MACHINE--I.E. UNDER CONTROL OF VM/370,   *
*   OTHER TCAM MODULES WILL ENTER THIS MODULE IMMEDIATELY AFTER       *
*   DYNAMICALLY ALTERING A CHANNEL PROGRAM--I.E. AFTER CHANGING A     *
*   CHANNNEL PROGRAM BETWEEN TIME O SIO AND TIME OF CHANNEL END/DECICE*
*   END.                                                              *
*                                                                     *
*            THIS ROUTINE WILL GET INTO SUPERVISOR STATE IF NOT       *
*   ALREADY IN SUCH A STATE. IT WILL THEN CALCULATE THE DEVICE ADDRESS*
*   UPON WHICH THE MODIFIED CHANNEL PROGRAM APPLIES AND WILL ISSUE    *
*   A SPECIAL VM/370 RECOGNIZABLE' DIAGNOSE INSTRUCTION--TELLING      *
*   VM/370 TO MAKE SAME CHANGE TO THE REAL CHANNEL COMMAND WORD       *
*   CORRESPONDING TO DESIGNATED VIRTUAL CHANNEL COMMAND WORD.         *
*                                                                     *
*ENTRY POINTS: SUPVST---ENTERED HERE WHEN CALLING ROUTINE IS ALREADY  *
*                     IN SUPERVISORY STATE. CALLING SEQUENCE:         *
*                                             L    R15,SAVTDIAG       *
*                                             BAL  R14,8(,R15)        *
*                                                                     *
*              NOSUPVST---ENTERED HERE WHEN CALLING ROUTINE IS NOT    *
*                     IN SUPERVISORY STATE. CALLING SEQUENCE:         *
*                                             L    R15,SAVTDIAG       *
*                                             BAL  R14,12(,R15)       *
*INPUT --CHANNEL COMMAND WORD JUST MODIFIED VIRTUAL ADDRESS IN R0     *
*        CURRENT LCB ADDRESS IN AVTSAVE2                              *
*                                                                     *
*OUTPUT--REAL CHANNEL COMMAND WORD MODIFIED ACCORDINGLY
*                                                                     *
*EXTERNAL ROUTINES -- IEAVMODE - MODESET SVC - TO GET INTO SUPERVISORY
*   MODE, IN ORDER TO ISSUE LRA AND DIAGNOSE INSTRUCTIONS.            *
*                                                                     *
*EXITS-NORMAL -- TO THE CALLING ROUTINE VIA BR 14                     *
*                                                                     *
* EXITS-ERROR -- NONE                                                 *
*                                                                     *
*TABLES/WORK AREAS -- AVT,SAVT,LCB,DCB,DEB,UCB                        *
*                                                                     *
*ATTRIBUTES -- REUSABLE,NON-RESIDENT,NON-RE-ENTRANT-
*                                                                     *
*NOTES -- THE OPERATION OF THIS MODULE DOES NOT DEPEND UPON A         *
*         PARTICULAR INTERNAL REPRESENTATION OF THE EXTERNAL          *
*         CHARACTER SET.                                              *
*       -- MODULE MUST BE LESS THAN 257 BYTES LONG.                   *
*                                                                     *
***********************************************************************
*                                                                     *
         EJECT
*        REGISTER EQUATES                                             *
***********************************************************************
*                                                                     *
R0       EQU   0                   VIRTUAL CCW ADDRESS
R1       EQU   1                        PARAMETER POINTER AN DWORK
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
RAVT     EQU   6
TWELVE   EQU   12
R14      EQU   14                       RETURN ADDRESS
R15      EQU   15                       ENTRY ADDRESS
CVTADDR  EQU   16
         EJECT
         USING *,R15
BEGIN    B     SUPVST                   IN SUPERVISOR STATE,BRANCH
         B     NOSUPVST                 NOT IN SUPERVISOR STATE,BRANCH
SAVEAREA DC    14F'0'                   REG SAVE AREA AND PATCH AREA.
IEDQDA   IEDHJN
         DS    0H
         EJECT
NOSUPVST EQU   *                        NON SUPERVISORY STATE
         STM   R14,R2,SAVEAREA+20       SAVE REGISTERS           MAINT
         LR    R2,R15                   GET INTO SUPERVISOR STATEMAINT
         MODESET MODE=SUP
         LR    R15,R2                   RESTORE BASE             MAINT
         L     R0,SAVEAREA+28           RESTORE CCW ADDR
         BAL   R14,SUPVST               FIND DEVICE ADDR AND ISSUE DIAG
*                                       SET BACK TO PROBLEM MODE
         LR    R2,R15                   SAVE BASE                MAINT
         MODESET MODE=PROB
         LR    R15,R2                   RESTORE BASE             MAINT
         LM    R14,R2,SAVEAREA+20       RESTORE REGISTERS        MAINT
         BR    R14                      WHEN RETURN MADE HERE,
*                                       ALL IS DONE.
         EJECT
KEY0DA   EQU   *                        DUMMY LABEL
         SPACE 2
SUPVST   EQU   *                        IN SUPERVISORY STATE.
         STM   R2,RAVT,SAVEAREA         SAVE REGISTERS 2-6
         L     RAVT,CVTADDR             SETTING UP R6
         L     RAVT,AVTCVTPT(,RAVT)          AS AVT
         L     RAVT,0(,RAVT)                     POINTER
         USING IEDQAVTD,RAVT
         L     R4,AVTSAVE2              CURRENT LCB ADDRESS
         USING IEDQLCB,R4               LCB BASE
         USING IHADCB,R3                DCB BASE
         USING IEDQUCB,R5               UCB BASE
         SPACE
* ROUTINE TO FIND THE DEVICE ADDRESS
         SPACE
         L     R3,LCBDCBPT              DCB ADDRESS
         L     R3,DCBDEBAD              DEB ADDRESS
         LA    R5,0                     CLEAR REGISTER
         IC    R5,LCBUCBX               PICK UP UCB INDEX
         SLL   R5,2                     MULTIPLY BY 4
         L     R5,DEBUCBAD-DEBNMSUB(R5,R3)  UCB ADDRESS
         LH    R5,UCBCHA                FLAGS,DEVICE ADDRESS
         SLL   R5,20                    SHIFTING OUT
         SRL   R5,20                                 FLAGS
         SPACE
*  ROUTINE TO ACTUALLY ISSUE THE DIAGNOSE INSTRUCTION
         SPACE
*                                       R5 NOW HAS DEVICE ADDRESS
         LR    R3,R0                    CAN'T USE ZERO AS A BASE
*                                       R3 NOW CONTAINS VIRTUAL
*                                       ADDRESS OF CCW MODIFIED
         LRA   R2,0(0,R3)               REAL ADDRESS OF MODIFIED
*                                       CHANNEL COMMAND WORD.
         LR    R4,R15                   SAVE BASE
         DC    X'83250028'              DIAGNOSE INSTRUCTION
         LR    R15,R4                   RESTORE BASE
         LM    R2,R5,SAVEAREA           RESTORE REGISTERS
         BR    R14                      RETURN TO CALLING ROUTINE
         EJECT
IEDQUCB  DSECT
         IEFUCBOB
         TAVTD
         TDEBD
         DCBD  DSORG=TX
         TLCBD
         END
