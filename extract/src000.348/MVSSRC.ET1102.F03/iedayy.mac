         TITLE 'IEDAYY TSO TIME DELAY REMOVAL RTN'
IEDAYY   CSECT
         SPACE 3
*  CHANGE ACTIVITY AS FOLLOWS
******************** MICROFICHE FLAGS *********************** SUPT CODE
*A550000                                                        SA44022
*C280000                                                         S21008
*C270000-290000                                                  S21903
*C430000,440000                                                 SA67755
*C430000-440000                                                @OZ07823
*D640000                                                       @OZ07823
***********************************************************************
*TITLE  'IEDAYY'  ASYNCHRONOUS TIME DELAY REMOVAL ROUTINE             *
*                                                                     *
*  MODULE NAME = IEDAYY                                               *
*                                                                     *
*  DESCRIPTIVE NAME = ASYNCHRONOUS TIME DELAY REMOVAL ROUTINE         *
*                                                                     *
*  COPYRIGHT = 'NONE'                                                 *
*                                                                     *
*  STATUS:  CHANGE LEVEL 5                                            *
*                                                                     *
*FUNCTION -- THE ASYNCHRONOUS TIME DELAY REMOVAL ROUTINE REMOVES      *
*   QCB'S FROM THE TIME DELAY QUEUE WHEN A SEND OR RECEIVE OPERATION  *
*   IS TO BE INITIATED.  THE ROUTINE FIRST CHECKS TO SEE IF IT HAS    *
*   BEEN ACTIVATED (POSTED TO THE READY QUEUE) BY A TIOC MODULE.  IF  *
*   NOT, IT RETURNS CONTROL TO THE DISPATCHER TO DISPATCH THE NEXT    *
*   SUBTASK.  IF IT HAS BEEN ACTIVATED, IT DEACTIVATES ITSELF (MARKS  *
*   ITS QCB NOT POSTED) AND GETS THE ADDRESSES OF THE PREVIOUS AND    *
*   CURRENT ELEMENTS ON THE TIME DELAY QUEUE.  IT THEN SCANS THE      *
*   TIME DELAY QUEUE LOOKING FOR A TIME-SHARING QCB WITH BOTH THE     *
*   WRITE BREAK FLAG AND THE TPUT REQUEST FLAG ON.  EACH TIME IT      *
*   FINDS ONE, IT UPDATES THE LINK FIELD OF ELEMENTS ON THE TIME      *
*   DELAY QUEUE, SETS THE TIME DELAY FLAG IN THE QCB TO INDICATE      *
*   THAT IT IS NO LONGER IN THE TIME DELAY QUEUE, POSTS THE QCB TO    *
*   ITSELF TO INITIATE A SEND OPERATION, LOADS THE POST REGISTER      *
*   (R1) WITH THE ADDRESS OF THE QCB TO BE POSTED TO THE READY QUEUE, *
*   AND LINKS TO THE DISPATCHER POST FUNCTION TO POST THE QCB.  UPON  *
*   RETURN FROM THE DISPATCHER, THIS ROUTINE CONTINUES ITS SCAN OF    *
*   THE TIME DELAY QUEUE, LOOKING FOR THE NEXT QCB TO BE REMOVED.     *
*   WHEN THE ENTIRE QUEUE HAS BEEN SEARCHED, AND ALL APPLICABLE QCB'S *
*   REMOVED, THIS ROUTINE BRANCHES BACK TO THE BEGINNING TO SEE IF    *
*   ANOTHER INTERRUPT HAS OCCURRED TO ACTIVATE IT AGAIN.  IF NOT, IT  *
*   RETURNS TO THE DISPATCHER.  IF IT HAS, THE ENTIRE PROCEDURE IS    *
*   REPEATED.                                                         *
*                                                                     *
*ENTRY POINT -- IEDAYY - TO REMOVE QCB'S FROM THE TIME DELAY QUEUE,   *
*   WHEN POSTED ASYNCHRONOUSLY FROM TIOC MODULES.                     *
*   CALLING SEQUENCE          L    R15,TSIDYQCB                       *
*                             BR   R15                                *
*                                                                     *
*INPUT -- IEDAYY IS ENTERED FROM THE DISPATCHER, AFTER BEING POSTED   *
*   ASYNCHRONOUSLY TO THE READY QUEUE BY A TIOC MODULE, WITH THE      *
*   FOLLOWING REGISTERS SET.                                          *
*   R11 HAS THE DISPATCHER ADDRESS                                    *
*   R13 HAS THE ADDRESS OF AVTSAVE2                                   *
*   R15 HAS THE ENTRY POINT ADDRESS                                   *
*                                                                     *
*OUTPUT -- IEDAYY LINKS TO THE DISPATCHER POST FUNCTION WITH THE      *
*   FOLLOWING REGISTERS SET.                                          *
*   R1 HAS THE ADDRESS OF THE QCB TO BE POSTED TO THE READY QUEUE     *
*   R14 HAS THE RETURN ADDRESS IN IEDAYY                              *
*                                                                     *
*EXTERNAL ROUTINES -- DISPATCHER POST FUNCTION - POSTS THE QCB(S) TO  *
*   THE READY QUEUE BY PRIORITY.                                      *
*                                                                     *
*EXITS-NORMAL -- TO THE DISPATCHER DISPATCH FUNCTION                  *
*                                                                     *
*EXITS-ERROR  -- NONE                                                 *
*                                                                     *
*TABLES/WORKAREAS -- AVT, QCB, TSID.                                  *
*                                                                     *
*ATTRIBUTES -- SERIALLY REUSABLE, REFRESHABLE, ENABLED, PROBLEM       *
*   PROGRAM MODE                                                      *
*                                                                     *
*NOTES -- THE OPERATION OF THIS MODULE DOES NOT DEPEND UPON A         *
*   PARTICULAR INTERNAL REPRESENTATION OF THE EXTERNAL CHARACTER      *
*   SET.                                                              *
*                                                                     *
***********************************************************************
RECB     EQU   1                       ADDR OF QCB TO POST TO RDY Q.
RWORK    EQU   3                       WORK REGISTER
RELE     EQU   4                       ADDR OF PREVIOUS ELEMENT ON
*                                      TIME DELAY QUEUE
RQCB     EQU   5                       ADDR OF CURRENT ELEMENT ON
*                                      TIME DELAY QUEUE
RTSINPUT EQU   6                       ADDR OF TSINPUT QCB
RDISP    EQU   11                      BASE ADDR OF DISPATCHER
RBASE    EQU   12                      BASE ADDR OF IEDAYY
RAVT     EQU   13                      ADDR OF AVTSAVE2 IN AVT
RLINK    EQU   14                      LINKAGE REGISTER TO DSPPOSTR
R15      EQU   15                      ADDR OF ENTRY POINT TO IEDAYY
         USING AYY010,RBASE
         USING IEDQTSI,RTSINPUT
         USING AVTSAVE2,RAVT
         USING IEDQDISP,RDISP
         USING IEDQQCB,RQCB
QCBMASK  EQU   X'FF'                   MASK TO TEST IF CURRENT ELEMENT
*                                      ON TIME DELAY QUEU IS A QCB
ELEINDEX EQU   X'12'                   ELEMENT TYPE IDENTIFIER
ELELINK  EQU   X'1D'                   LINK FKELD OF ELEMENTS ON TIME Q
EQUAL    EQU   8
         DC    AL1(DSPMCPL2),AL1(0)    IEDAYY STCB
         USING *,R15
IEDAYY   IEDHJN AYY000                  MODULE NAME AND DATE     S21903
         DROP  R15
         BALR  RBASE,0
AYY010   EQU   *
         L     RTSINPUT,AVTTSOPT       OBTAIN ADDR OF TSINPUT QCB
         TM    TSIDYQFG,TSIDYPOS       DELAY REMOVAL RTN ACTIVATED?
         BZ    DSPDISP                 NO. RETURN TO DISPATCHER.
         NI    TSIDYQFG,255-TSIDYPOS   RESET POSTED FLAG
AYY020   EQU   *
         LA    RELE,AVTDELYB           START WITH TIME DELAY QCB
AYY025   EQU   *
         L     RQCB,ELELINK-1(RELE)    GET NEXT ELEMENT ON DELAY QUEUE
         LA    RQCB,0(,RQCB)           CLEAR HI-ORDER BYTE
         C     RQCB,AVTDELYB           HAVE WE SEARCHED ENTIRE QUEUE?
         BE    AYY010                  YES. CHECK FOR POST.
         CLI   QCBRETCT,AVTEZERO        ELEMENT A QCB          @OZ07823
         BE    AYY040                   BRANCH YES             @OZ07823
*                                      OTHERWISE, SCAN TO NEXT ELEMENT
AYY030   EQU   *
         LR    RELE,RQCB               UPDATE PREVIOUS ELEMENT POINTER
         B     AYY025
AYY040   EQU   *
         TM    QCBFLAG,QCBTSSES        IS THIS A TIME-SHARING QCB?
         BZ    AYY030                  NO. SCAN TO NEXT ELEMENT.
         TM    QCBTSOF1,QCBWRBRK+QCBTPUT+QCBTGET  Q HAS A SEND OR 21008
*                                       RECEIVE OPERATION BEEN    21008
*                                       REQUESTED FOR THIS QCB    21008
         BZ    AYY030                  NO. SCAN TO NEXT ELEMENT.
         LR    RECB,RQCB                LOAD PARAMETER REGISTER WITH
*                                       QCB ADDRESS FOR TIME DELAY RTN
*                                       AND DISPATCHER POST RTN
         L     R15,AVTHG02              ADDR OF TIME DELAY REMOVAL RTN
         BALR  RLINK,R15                GO REMOVE ELEMENT FROM DELAY Q
         NI    QCBTSOF1,X'FF'-QCBDELAY     TURN OFF DELAY BIT    A44022
         OI    QCBTSOF2,QCBPOSTO        INDICATE QCB POSTED
         IC    RWORK,QCBDSFLG          POST QCB TO ITSELF IN ORDER TO
         ST    RQCB,QCBELCHN-1         INITIATE A SEND OPERATION.
         STC   RWORK,QCBDSFLG          RESTORE QCB FLAG BYTE
         BAL   RLINK,DSPPOSTR          GO POST ELEMENT TO READY QUEUE
         B     AYY020
         TAVTD
         TDISPD
         TQCBD
         TTSID
         END
