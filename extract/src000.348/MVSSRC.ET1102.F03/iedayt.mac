         TITLE 'IEDAYT - ABEND INTERFACE SUBTASK.'
IEDAYT   CSECT
*C007890,060000                                                  S22029
*D007960-008100,008940-009290,040000-052000,636000-708000        S22029
*C006000,240000,328000,420000,488000,724000-784000,800000-824000,S21903
*C844000                                                         S21903
***********************************************************************
*TITLE  'IEDAYT'  ABEND INTERFACE SUBTASK                             *
*                                                                     *
*STATUS -- CHANGE LEVEL 20.0                                          *
*                                                                     *
*FUNCTION -- THE ABEND INTERFACE SUBTASK INFORMS TSO WHEN TCAM        *
*   ABENDS, WHEN AN ATTACHED TCAM SUBTASK ABENDS, OR WHEN, IN A       *
*   MIXED TSO/TCAM ENVIRONMENT, THE EXCP DRIVER ABENDS BECAUSE NO     *
*   SPACE IS AVAILABLE ON A NON-REUSABLE DISK QUEUE AND A FLUSH       *
*   CLOSEDOWN HAS BEEN INITIATED.                                     *
*      WHEN TCAM ABENDS, THIS SUBTASK ISSUES THE TCABEND MACRO IN-    *
*   STRUCTION, WHICH SETS OFF THE TCAM READY BIT IN THE CVT.  IF THE  *
*   CVTTSRDY BIT IN CVTTSFLG IS ON, INDICATING THAT TSO IS ACTIVE,    *
*   THE TCABEND MACRO INSTRUCTION ALSO INDICATES THAT THE TIME        *
*   SHARING CONTROLLER TASK IS TO STOP BY SETTING ON THE TSCASTOP BIT *
*   IN THE TIME SHARING CVT, AND BY POSTING THE TIME SHARING CON-     *
*   TROLLER'S ECB IN TSECBTAB COMPLETE.                               *
*      WHEN AN ATTACHED TCAM SUBTASK ABENDS, THE ABEND INTERFACE      *
*   SUBTASK GETS ALL ELEMENTS THAT ARE POSTED TO THE ABENDING SUBTASK *
*   FROM THE READY QUEUE AND THE SUBTASK'S QCB AND DISPOSES OF THEM.  *
*   IF THE ABENDING SUBTASK IS EITHER CHECKPOINT OR ON-LINE TEST,     *
*   THIS SUBTASK ALSO CLEARS THE QCB STCB LINK ADDRESS, SETS THE      *
*   QCB'S ELEMENT CHAIN TO POINT TO THE DUMMY LAST ELEMENT ADDRESS IN *
*   THE AVT, AND RESETS THE QCB FLAG INDICATING THAT IT IS A QCB.     *
*   THESE FUNCTIONS ARE PERFORMED TO INDICATE THAT THE ABENDING SUB-  *
*   TASK NO LONGER EXISTS.  IF THE ABENDING SUBTASK IS OPERATOR CON-  *
*   TROL, THE ABEND INTERFACE SUBTASK                                 *
*   ISSUES THE AQCTL SVC (SVC 102) TO POST THE ABEND ROUTINE'S ECB TO *
*   THE READY QUEUE.  IT ALSO ISSUES THE TCABEND MACRO INSTRUCTION,   *
*   WHICH PERFORMS THE SAME FUNCTIONS AS WHEN TCAM ABENDS.            *
*                                                                     *
*ENTRY POINTS -- IEDAYT0 - ON RECEIVING CONTROL WHEN TCAM ABENDS.     *
*   CALLING SEQUENCE          L    R15,'STAE EXIT ADDRESS'            *
*                             BALR R14,R15                            *
*   IEDAYT1 - ON RECEIVING CONTROL WHEN AN ATTACHED TCAM SUBTASK      *
*   ABENDS.                                                           *
*   CALLING SEQUENCE          L    R15,'STAE EXIT ADDRESS'            *
*                             BALR R14,R15                            *
*                                                                     *
*INPUT -- IEDAYT, WHEN SPECIFIED AS THE ABNORMAL EXIT ROUTINE ON A    *
*   STAE MACRO INSTRUCTION, IS CALLED BY THE ABEND/STAE INTERFACE     *
*   ROUTINE OF THE OS SUPERVISOR WHEN TCAM, AN ATTACHED TCAM SUBTASK, *
*   OR, IN A MIXED TSO/TCAM ENVIRONMENT, THE EXCP DRIVER ABENDS.  AT  *
*   ENTRY THE FOLLOWING REGISTERS ARE SET.                            *
*   R1 HAS THE ADDRESS OF THE 104-BYTE STAE WORK AREA, WHICH CONTAINS *
*      THE ENTRY POINT ADDRESS IN THE ABENDING PROGRAM.               *
*   R13 HAS THE ADDRESS OF A SUPERVISOR-PROVIDED REGISTER SAVE AREA.  *
*   R14 HAS THE RETURN ADDRESS (AN SVC 3 INSTRUCTION).                *
*   R15 HAS THE ENTRY POINT ADDRESS.                                  *
*                                                                     *
*OUTPUT -- ON LINKING TO THE AQCTL SVC 102 ROUTINE, REGISTER 1 CON-   *
*   TAINS THE ADDRESS OF THE AQCTL PARAMETER LIST.  ON RETURNING TO   *
*   THE CALLING ROUTINE, REGISTER 15 CONTAINS THE RETURN CODE.        *
*                                                                     *
*EXTERNAL ROUTINES -- THE TCABEND (SVC 94) ROUTINE - TO SET OFF THE   *
*   TCAM READY BIT AND STOP THE TIME SHARING CONTROLLER.              *
*   IGC102 - TO POST THE ABEND ROUTINE'S ECB TO THE READY QUEUE.      *
*                                                                     *
*EXITS-NORMAL -- TO THE ABEND/STAE INTERFACE ROUTINE AT THE SVC 3     *
*   INSTRUCTION FOLLOWING THE STAE MACRO INSTRUCTION NAMING IEDAYT    *
*   AS ABNORMAL EXIT ROUTINE.                                         *
*                                                                     *
*EXITS-ERROR -- NONE                                                  *
*                                                                     *
*TABLES/WORKAREAS -- AVT, QCB, CVT, TSID, STAE WORK AREA.             *
*                                                                     *
*ATTRIBUTES -- SERIALLY REUSABLE, ENABLED, PROBLEM PROGRAM MODE.      *
*                                                                     *
*NOTES -- THE OPERATION OF THIS MODULE DOES NOT DEPEND UPON A         *
*   PARTICULAR INTERNAL REPRESENTATION OF THE EXTERNAL CHARACTER      *
*   SET.                                                              *
*                                                                     *
***********************************************************************
         SPACE 2
         USING IEDQAVTD,RAVT
         USING IEDQQCB,RQCB
         USING *,REG15
         B     IEDAYT0                  ENTRY POINT FOR STAE EXITS
         USING *,REG15
         B     IEDAYT1                  ENTRY POINT FOR STAI EXITS
         DROP  REG15                    DROP REGISTER 15 AS BASE
IEDAYT   IEDHJN
         SPACE 2
*  THE FOLLOWING ROUTINE IS ENTERED WHEN TCAM ABENDS.
         SPACE 2
         USING IEDAYT,REG15
         DS    0H
IEDAYT0  EQU   *
         TCABEND
         SPACE 2
         SR    REG15,REG15              CLEAR RETRUN CODE REGISTER
         BR    RETURN                   RETURN
         SPACE 2
*  THE FOLLOWING ROUTINE IS ENTERED WHEN AN ATTACHED SUBTASK ABENDS.
         SPACE 2
         DS    0H
IEDAYT1  EQU   *
         BALR  RBAS,0                   LOAD BASE REGISTER
         USING *,RBAS                   SET UP ADDRESSABILITY
         STC   REG0,NTRYCODE            STORE ENTRY CODE FOR TEST
         CLI   NTRYCODE,CODEX0C         IS CODE X'0C'
         BE    AYTOUT                   BRANCH ON YES
         L     RCVT,CVTPTR              ADDRESS OF CVT
         L     RAVT,AVTCVTPT(RCVT)      ADDRESS OF AVT ADDRESS
         L     RAVT,AVTEZERO(RAVT)      ADDRESS OF AVT
         L     RDSP,AVTEA               GET ADDRESS OF TCAM DISPATCHER
         USING IEDQDISP,RDSP
         L     RPRG,96(REG1)            ADDRESS OF ABENDING ROUTINE
         LA    RPRG,0(RPRG)             CLEAR HIGH ORDER BYTE
         L     RVCN,AVTCKPTB+8          ADDRESS OF CHECK POINT ROUTINE
         LA    RVCN,0(RVCN)             CLEAR HIGH ORDER BYTE
         CR    RPRG,RVCN                IS CHECK POINT ABENDING
         BE    AYT101                   YES, BRANCH
         L     RVCN,AVTOPCOB+8          ADDRESS OF OPER CONTROL ROUT
         LA    RVCN,0(RVCN)             CLEAR HIGH ORDER BYTE
         CR    RPRG,RVCN                IS OPER CONTROL ABENDING
         BE    AYT102                   YES, BRANCH
         L     RVCN,AVTOLTQB+8          ADDRESS OF ONLINE TEST ROUTINE
         LA    RVCN,0(RVCN)             CLEAR HIGH ORDER BYTE
         CR    RPRG,RVCN                IS ONLINE TEST ABENDING
         BE    AYT103                   YES, BRANCH
         B     AYTOUT                   NONE OF ABOVE, RETURN
         SPACE 2
*  THE CHECKPOINT SUBTASK IS ABENDING
         SPACE 2
AYT101   LA    RQCB,AVTCKPTB            ADDRESS OF CHECK POINT QCB
         BAL   RLNK,AYT104              BRANCH TO CHECK QCB      S21903
         SPACE 2
*  LCB'S, BUFFERS, AND SPECIAL ELEMENTS MAY BE POSTED TO CHECK POINT
         SPACE 2
AYT141   CLI   0(RELE),ZERO             IS FIRST BYTE ZERO
         BE    AYT111                   YES, ELEMENT IS LCB
         TM    8(RELE),CCWTIC           IS TIC COMMAND PRESENT
         BO    AYT121                   YES, ELEMENT IS BUFFER
*  OTHERWISE IT IS SPECIAL ELEMENT AND IS IGNORED
AYT131   L     RELE,4(RELE)             GET ADDRESS NEXT ELEMENT
         LA    RELE,0(RELE)             CLEAR HIGH ORDER BYTE
         LTR   RELE,RELE                IS ELEMENT LINK ZERO
         BZ    AYTOUT                   YES, RETURN
         B     AYT141                   BRANCH TO CHECK ELEMENT
AYT111   BAL   RLNK,AYT201              DISPOSE OF LCB
         B     AYT131                   CHECK NEXT ELEMENT
AYT121   BAL   RLNK,AYT301              DISPOSE OF BUFFER
         B     AYT131                   CHECK NEXT ELEMENT
         SPACE 2
*  THE OPERATOR CONTROL SUBTASK IS ABENDING
         SPACE 2
AYT102   LA    RQCB,AVTOPCOB            ADDRESS OF OPER CONTROL QCB
         BAL   RLNK,AYT104              BRANCH TO CHECK QCB      S21903
         SPACE 2
*  LCB'S, BUFFERS, AND SPECIAL ELEMENTS MAY BE POSTED TO OPERATOR
*  CONTROL
         SPACE 2
AYT151   CLI   4(RELE),LCBPRI           IS PRIORITY X'E8'
         BE    AYT161                   YES, ELEMENT IS LCB
         TM    8(RELE),CCWTIC           IS TIC COMMAND PRESENT
         BO    AYT171                   YES, ELEMENT IS BUFFER
*  OTHERWISE IT IS SPECIAL ELEMENT AND IS IGNORED
AYT181   L     RELE,4(RELE)             GET ADDRESS NEXT ELEMENT
         LA    RELE,0(RELE)             CLEAR HIGH ORDER BYTE
         LTR   RELE,RELE                IS ELEMENT LINK ZERO
         BZ    AYTOUT                   YES, RETURN
         B     AYT151                   BRANCH TO CHECK ELEMENT
AYT161   BAL   RLNK,AYT201              DISPOSE OF LCB
         B     AYT181                   CHECK NEXT ELEMENT
AYT171   BAL   RLNK,AYT301              DISPOSE OF BUFFER
         B     AYT181                   CHECK NEXT ELEMENT
         SPACE 2
*  THE ONLINE TEST SUBTASK IS ABENDING
         SPACE 2
AYT103   LA    RQCB,AVTOLTQB            ADDRESS OF ONLINE TEST QCB
         BAL   RLNK,AYT104              BRANCH TO CHECK QCB      S21903
         SPACE 2
*  BUFFERS ONLY ARE POSTED TO ONLINE TEST
         SPACE 2
AYT191   BAL   RLNK,AYT301              DISPOSE OF BUFFER
         L     RELE,4(RELE)             GET ADDRESS NEXT ELEMENT
         LA    RELE,0(RELE)             CLEAR HIGH ORDER BYTE
         LTR   RELE,RELE                IS ELEMENT LINK ZERO
         BNZ   AYT191                   CHECK NEXT ELEMENT
         L     RWRK,AVTTSOPT            ADDRESS OF TSINPUT MACRO
         USING IEDQTSI,RWRK
         XC    TSIABEND,TSIABEND        CLEAR ECB ADDRESS      @ZM
         LA    RWRK,TSIABEND            ADDRESS OF ABEND ROUTINE ECB
         ST    RWRK,AQCTLIST            ECB ADDRESS FOR AQCTL
         MVI   AQCTLIST,AQCTLPST        INDICATE POST TO AQCTL
         LA    REG1,AQCTLIST            ADDRESS OF PARAMETER LIST
         AQCTL                          POST ABEND ROUTINE ECB
         TCABEND
         B     AYTOUT                   BRANCH TO EXIT           S21903
AYT104   ST    RQCB,QCBSTCHN-1          STCB ADDRESS POINTS TO QCB
         XC    QCBLINK-1(4),QCBLINK-1   CLEAR QCB STCB LINK ADDRESS
         LA    RWRK,AVTDELEM            ADDRESS OF DUMMY ELEMENT
         L     RELE,QCBELCHN-1          ADDRESS OF FIRST ELEMENT
*                                       IN CHAIN
         LA    RELE,0(RELE)             CLEAR HIGH ORDER BYTE
         CR    RWRK,RELE                IS ELEMENT DUMMY ELEMENT
         BE    0(RLNK)                  YES, RETURN
         ST    RWRK,QCBELCHN-1          ELEMENT CHAIN POINTS TO
*                                       DUMMY ELEMENT
         MVI   QCBDSFLG,QCBFQCB         RESTORE QCBDSFLG
         BR    RLNK                     RETURN
         SPACE 2
*  DISPOSE OF LCB'S
         SPACE 2
AYT201   LR    RHLD,RLNK                SAVE RETURN ADDRESS
         ST    RELE,0(RELE)             LCB POSTED TO ITSELF
         MVI   4(RELE),PRIORITY         SET POST PRIORITY
         LR    REG1,RELE                ADDRESS OF ELEMENT TO BE POSTED
         BAL   RLNK,DSPPOSTR            LINK TO DISPATCHER
         LR    RLNK,RHLD                RESTORE RETURN REG
         BR    RLNK                     RETURN
         SPACE 2
*  DISPOSE OF BUFFERS
         SPACE 2
AYT301   LR    RHLD,RLNK                SAVE RETURN REG
         LA    RWRK,AVTBFRTB            ADDRESS OF BUFFER RETURN
         ST    RWRK,0(RELE)             BUFFER POSTED TO BUFFER RETURN
         MVI   4(RELE),PRIORITY         SET POST PRIORITY
         LR    REG1,RELE                ADDRESS OF ELEMENT TO BE POSTED
         BAL   RLNK,DSPPOSTR            LINK TO DISPATCHER
         LR    RLNK,RHLD                RESTORE RETURN REG
         BR    RLNK                     RETURN
AYTOUT   SR    REG15,REG15              CLEAR RETURN CODE REGISTER
         BR    RETURN                   RETURN
         SPACE 2
         SPACE 2
*  REGISTER EQUATES.
         SPACE 2
REG0     EQU   0                        REGISTER EQUATE          S21903
REG1     EQU   1                        REGISTER EQUATE          S21903
RCVT     EQU   2                        REGISTER EQUATE          S21903
RAVT     EQU   3                        REGISTER EQUATE          S21903
RPRG     EQU   4                        REGISTER EQUATE          S21903
RVCN     EQU   5                        REGISTER EQUATE          S21903
RQCB     EQU   6                        REGISTER EQUATE          S21903
RWRK     EQU   7                        REGISTER EQUATE          S21903
RELE     EQU   8                        REGISTER EQUATE          S21903
RHLD     EQU   9                        REGISTER EQUATE          S21903
RLNK     EQU   10                       REGISTER EQUATE          S21903
RDSP     EQU   11                       REGISTER EQUATE          S21903
RBAS     EQU   12                       REGISTER EQUATE          S21903
RSAV     EQU   13                       REGISTER EQUATE          S21903
RETURN   EQU   14                       REGISTER EQUATE          S21903
REG15    EQU   15                       REGISTER EQUATE          S21903
         SPACE 2
*  OTHER EQUATES.
         SPACE 2
ZERO     EQU   X'00'                    CONSTANT EQUATE          S21903
AQCTLPST EQU   X'02'                    FLAG EQUATE              S21903
CCWTIC   EQU   X'08'                    CCWTIC EQUATE            S21903
CODEX0C  EQU   X'0C'                    FLAG EQUATE              S21903
PRIORITY EQU   X'E4'                    PRIORITY EQUATE          S21903
LCBPRI   EQU   X'E8'                    PRIORITY EQUATE          S21903
CVTPTR   EQU   16                       CVT EQUATE               S21903
         SPACE 2
*  CONSTANTS.
         SPACE 2
TSIABEND DC    F'0'                    ECB                     @ZM
AQCTLIST DC    A(0),XL1'80',AL3(0)      PARAMETER LIST FOR AQCTL
NTRYCODE DC    X'00'                    ENTRY CODE STORAGE       S21903
         TAVTD
         TQCBD
         TTSID
         TDISPD
         END
