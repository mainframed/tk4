AQ01     TITLE '''IEDQAQ'' - OPERATOR CONTROL INTERFACE ROUTINE'
IEDQAQ   CSECT
*C606000                                                         S22029
*C060000-072000,396000,804000,810000                             S99240
*A274000,278000,399000,434000,436000,770000,772000,804100-805700 S99240
*A805620                                                       @ZA02630
*A561000,799000,802800                                         @OZ28568
*C801520                                                       @OZ28568
***********************************************************************
*                                                                     *
* STATUS -- CHANGE LEVEL 5                                       S22024
*                                                                     *
*FUNCTION -- COMPARES THE CHARACTERS PASSED IN THE AVT WORK AREA      *
*   (AVTDOUBL) WITH THE OPERATOR CONTROL CHARACTERS (AVTCTLCH).  ON   *
*   UNEQUAL COMPARE,  RETURN IS MADE TO THE CALLER THROUGH RETURN     *
*   INTERFACE.  ON EQUAL COMPARE, THE DQCB FIELD IN THE SCB IS   S99240
*   SET TO POINT TO THE OPERATOR CONTROL QCB.  THE BUFFER IS     S99240
*   THEN TPOSTED TO BUFFER DISPOSITION.  EXIT IS MADE TO THE     S99240
*   DISPATCHER AT DSPPOST.                                       S99240
*                                                                     *
*ENTRY POINTS --                                                      *
*        'IEDQAQ' TO TEST FOR OPERATOR CONTROL CHARACTERS AND         *
*   CONDITIONALLY RETURN TO THE CALLER OR POST THE BUFFER.  CALLING   *
*   SEQUENCE FROM USER INTERFACE IS:                                  *
*                                                                     *
*        L     R12,AVTMSGS-1            GET ADDR OF VCON TABLE        *
*        IC    R15,AVTEZERO(,R1)        GET INDEX TO ROUTINE ADDR     *
*        L     R12,AVTEZERO(R12,R15)    GET ROUTINE ADDRESS           *
*        BR    R12                      EXIT TO ROUTINE               *
*                                                                     *
*INPUT --                                                             *
*   REGISTER 1 - THE ADDRESS OF A MACRO-GENERATED PARAMETER LIST.     *
*   PARAMETER LIST FORMAT IS:                                         *
*                                                                     *
*        *********************************                            *
*        *       * PARAM *               *                            *
*        * INDEX * LIST  *     UNUSED    *                            *
*        *       * LNGTH *               *                            *
*        *********************************                            *
*                                                                     *
*   REGISTER 6 - THE ADDRESS OF THE CURRENT BUFFER.                   *
*                                                                     *
*   REGISTER 12 - THE ENTRY POINT ADDRESS AND BASE REGISTER FOR       *
*   THIS ROUTINE.                                                     *
*                                                                     *
*   REGISTER 13 - ADDRESS OF THE CALLER'S SAVE AREA (ALWAYS           *
*   AVTSAVE2).                                                        *
*                                                                     *
*OUTPUT --                                                            *
*   REGISTER 1 - ON EXIT TO POST,  THE ADDRESS OF THE BUFFER.         *
*                                                                     *
*EXTERNAL REFERENCES --                                               *
*                                                                S99240
*   'IEDQLM' - RETURN INTERFACE ROUTINE FOR EXIT                 S99240
*                                                                     *
*   AVT - ADDRESS VECTOR TABLE.                                       *
*                                                                     *
*   BUFFER CURRENTLY BEING PROCESSED.                                 *
*                                                                     *
*EXITS,  NORMAL -- THE CHARACTERS IN THE BUFFER ARE NOT OPERATOR      *
*   CONTROL CHARACTERS.  (RETURN IS MADE TO THE CALLER.)              *
*                                                                     *
*   THE CHARACTERS IN THE BUFFER ARE OPERATOR CONTROL CHARACTERS.     *
*   REGISTER 1 CONTAINS THE ADDRESS OF THE BUFFER.  (EXIT IS MADE     *
*   TO THE DISPATCHER TO POST THE BUFFER.)                            *
*                                                                     *
*EXITS,  ERROR -- N/A.                                                *
*                                                                     *
*TABLES/WORK AREAS -- N/A.                                            *
*                                                                     *
*ATTRIBUTES -- SERIALLY REUSABLE, REFRESHABLE, ENABLED, RESIDENT,     *
*   PROBLEM PROGRAM MODE.                                             *
*                                                                     *
*CHARACTER CODE DEPENDENCY                                       S99240
*      -- THE OPERATION OF THIS MODULE DEPENDS UPON THE INTERNAL S99240
*   REPRESENTATION OF AN EXTERNAL CHARACTER SET WHICH IS              *
*   EQUIVALENT TO THE ONE USED AT ASSEMBLY TIME.  THE CODING HAS      *
*   BEEN ARRANGED SO THAT REDEFINITION OF ''CHARACTER'' CONSTANTS,    *
*   BY REASSEMBLY, WILL RESULT IN CORRECT MODULE FOR THE NEW          *
*   DEFINITIONS.                                                      *
*                                                                     *
*NOTES -- NONE.                                                  S99240
*                                                                S99240
***********************************************************************
         EJECT
********* REGISTER EQUATES *********
         SPACE
R0       EQU   0                        WORK REGISTER
         SPACE
R1       EQU   1                        WORK REGISTER
         SPACE
RDISP    EQU   11                       DISPATCHER ADDRESS
         SPACE
R13      EQU   13                       SAVE AREA ADDRESS
R14      EQU   14                       RETURN REGISTER
R15      EQU   15                       LINK REGISTER
         SPACE
********* OTHER EQUATES *********
         SPACE
EIGHT    EQU   8                        COMPARE LENGTH
         SPACE
ON       EQU   1                        CONDITION CODE: BIT ON
EITHER   EQU   7                        CONDITION CODE: NOT ZERO
UNEQ     EQU   7                        CONDITION CODE: UNEQUAL
AD       EQU   7                        MASK FOR ADDR STCM     @YM07657
ZERO     EQU   8                        CONDITION CODE: ZERO
CCWTIC   EQU   8                        TIC OP CODE              S22025
UNCOND   EQU   15                       UNCONDITIONAL MASK     @OZ28568
         EJECT
         USING AVTSAVE2,R13
         USING IEDQAQ01,R15
         SPACE
IEDQAQ01 EQU   *
IEDQAQ   IEDHJN OPCTLINT                ID AND DATE CARD         S22025
         STH   R1,AVTPARM3              SAVE SCAN POINTER
         L     R1,AVTADBUF              GET BUFFER ADDRESS FROM AVT
         USING IEDQPRF,R1
         TM    PRFSTAT1,PRFTSMSG+PRFNHDRN TSO OR TEXT            S22024
         BCR   EITHER,R14               YES, RETURN
         SPACE
         CLC   AVTCTLCH(EIGHT),AVTDOUBL IS COMPARE GOOD
         BCR   UNEQ,R14                 NO, RETURN
         SPACE
         L     R15,PRFLCB-1             GET LCB ADDR FROM BUFFER
         USING IEDQLCB,R15
         TM    LCBSTAT1,LCBOCNI         IS OP CTL ALREADY GOING
         BCR   ON,R14                   YES, RETURN
         SPACE
         L     R15,LCBSCBA-1            GET SCB ADDR FROM LCB
         USING IEDQSCB,R15
         TM    SCBSTATE,SCBLCK1N        IS TERMINAL IN LOCK MODE
         BCR   ON,R14                   YES, RETURN
         SPACE
         LH    R1,PRFSRCE               GET KEY TO SOURCE TERMINAL
         N     R1,AVTCLRHI              ISIT ZERO
         BCR   ZERO,R14                 YES, RETURN
         SPACE
         ST    R14,AVTPARM              SAVE RETURN REGISTER
         L     R15,AVTRNMPT             GET ADDRESS OF TNT CODE
         BALR  R14,R15                  LINK TO GET TERM ENTRY ADDR
         SPACE
         L     R14,AVTPARM              RESTORE RETURN REGISTER
         USING IEDQTRM,R1
         TM    TRMSTATE,TRMSCNYN        IS THIS AN OP CTL TERMINAL
         BCR   ZERO,R14                 NO, RETURN
         SPACE
*                   GO GET THE COMMAND                           S99240
*                                                                S99240
         L     R1,AVTADBUF              GET BUFFER ADDRESS FROM AVT
         USING IEDQPRF,R1
         LH    R0,AVTPARM3              SET SCAN POINTER
         STH   R0,PRFSCAN                 PAST OP CTL CHARS
         LA    R0,AVTOPCOB              YES, GET OP CTL QCB ADDR
         DROP  R15                                               S22024
         ST    RDISP,AVTPARM            SAVE REG 11            @OZ28568
         BALR  RDISP,0                  SET BASE                 S22024
         USING *,RDISP                                           S22024
         TM    PRFSTAT1,PRFNLSTN        LAST BUFFER              S22024
         BZ    LAST                     YES, BRANCH              S22024
         TM    PRFTIC,PRFEOMSG          MADE NOT LAST BY LMD     S22024
         BZ    BFRTN                    NO SET FOR BFR RETURN  @OZ28568
         L     RDISP,AVTPARM            RESET REG 11           @OZ28568
         BCR   UNCOND,R14               RETURN TO USER         @OZ28568
*
*        THE DESTINATION QCB IS SET TO BUFFER RETURN HERE SO THAT
*        ALL TEXT BUFFERS WILL GO TO BUFFER RETURN.  THIS
*        PREVENTS BUFFER DISPOSITION FORM ATTEMPTING RECALLS ON ERROR
*        SITUATIONS
*        THE BUFFER ITSELF IS POSTED TO OPERATOR CONTROL BELOW
*
BFRTN    EQU   *                                               @OZ28568
         LA    R0,AVTBFRTB              SET QCB ADDRESS TO       S22024
*                                       BUFFER RETURN            S22024
LAST     EQU   *                                                 S22024
         L     R15,PRFLCB-1             GET LCB ADDR FROM BUFFER S99240
         USING IEDQLCB,R15              SET R15 AS LCB BASE      S99240
         SPACE
         L     R15,LCBSCBA-1            GET SCB ADDR FROM LCB    S99240
         USING IEDQSCB,R15              SET R15 AS SCB BASE      S99240
         SPACE
         STCM  R0,AD,SCBDESTQ           SET DEST QCB TO OP CTL @YM07657
*                                       IF BFR IS LAST OR TO   @YM07657
*                                       BFR RTN IF BFR IS NOT  @YM07657
*                                       LAST.                  @YM07657
         LA    R0,AVTINOUT              GET ADDR OF DUMMY IN/OUT S99240
         STCM  R0,AD,SCBMACR            PLACE ADDRESS IN SCB   @YM07657
         SPACE
         L     R15,AVTMSGS-1            GET VCON TABLE ADDRESS   S99240
         L     R15,AVTEZERO(R15)        GET ADDRESS OF BD'S QCB  S99240
         MVI   PRFPRI,PRIDSPLB          SET LOW PRIORITY         S22024
         TM    PRFSTAT1,PRFNLSTN        LAST BFR                 S22024
         BZ    LASTBFR                  YES, BRANCH              S22024
         TM    PRFSTAT1,PRFITCPN        TEMP END OF MESSAGE    @ZA02630
         BO    LASTBFR                  YES,BRANCH             @ZA02630
         LA    R15,AVTOPCOB             SET OP CONTROL QCB       S22024
         MVI   PRFPRI,PRIDESTQ          SET PRIORITY FOR OP CNTL S22024
LASTBFR  EQU   *                                                 S22024
         DROP  RDISP                                             S22024
         ST    R15,PRFQCBA-1            PUT QCB ADDR IN BUFFER   S99240
         MVI   PRFTIC,CCWTIC            SET OP CODE IN TIC FIELD S22025
         L     RDISP,AVTEA              GET DISPATCHER ADDRESS
         USING IEDQDISP,RDISP
         BAL   R14,DSPPOST              EXIT TO POST BFR       @YM07657
         EJECT
********* DSECTS *********
         SPACE
         TAVTD
         EJECT
         TDISPD
         EJECT
         TLCBD
         EJECT
         TPRFD
         EJECT
         TPRIOR
         EJECT
         TSCBD
         EJECT
         TTRMD
         END
