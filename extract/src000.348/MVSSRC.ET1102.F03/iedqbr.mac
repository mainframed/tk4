         TITLE 'IEDQBR - COUNT MODULE FOR SETEOM'
IEDQBR   CSECT
*CHANGE ACTIVITY AS FOLLOWS:
******************** MICROFICHE FLAGS *********************** SUPT CODE
*A309000,476000-478000,501000,592000,660600-664800              SA59188
*A667000-669000,828300-832500,939000                            SA59188
*C660000                                                        SA59188
*A478000                                                       @XA05312
*C750000,756000                                                @XA05312
***********************************************************************
*                                                                     *
* MODULE NAME - IEDQBR                                                *
*                                                                     *
* DESCRIPTIVE NAME - COUNT MODULE FOR SETEOM                          *
*                                                                     *
* COPYRIGHT - NONE                                                    *
*                                                                     *
* TITLE - 'IEDQBR' COUNT MODULE                                       *
*                                                                     *
* STATUS - CHANGE LEVEL 001                                           *
*                                                                     *
* FUNCTION - COUNTS DATA BYTES WHEN BLOCKING OR DEBLOCKING ON COUNT   *
*    IN SETEOM                                                        *
*                                                                     *
* ENTRY POINT - 'IEDQBR'                                              *
*    CALLING SEQUENCE -                                               *
*         L    R15,AVTUI                                              *
*         BALR R14,R15                                                *
*                                                                     *
* INPUT -                                                             *
*    EXPECTED REGISTER CONTENTS                                       *
*         USING R4,LCB ADDR                                           *
*         USING R6,BUFFER DSECT                                       *
*         USING R9,AVTBASE                                            *
*         USING R12,BASE REGISTER                                     *
*    REGISTER 1 CONTAINS ADDRESS OF AVTPARM WHICH CONTAINS IEDQBR     *
*    INDEX IN FIRST BYTE AND ADDRESS OF SETEOM PARAMETER LIST         *
*    REGISTER 7 CONTAINS ADDRESS OF OPTION FIELD (OR ADDRESS OF FIELD *
*    IN PARAMETER LIST) USED TO COUNTS BYTES IN A MESSAGE             *
*    REGISTER 8 CONTAINS ADDRESS OF OPTION FIELD WHICH CONTAINS       *
*    MAXIMUM LENGTH OF A LOGICAL MESSAGE                              *
*                                                                     *
* OUTPUT - REGISTER 15 CONTAINS 0 IF EOM ON COUNT CONDITION NOT MET   *
*    WITH THIS BUFFER OR REGISTER 15 CONTAINS OFFSET INTO BUFFER OF   *
*    END OF LOGICAL MESSAGE IF EOM ON COUNT CONDITION MET             *
*                                                                     *
* EXTERNAL REFERENCES - N/A                                           *
*                                                                     *
* EXITS,NORMAL - BRANCHES BACK TO CALLING MODULE (IEDQBO) VIA UI      *
*                                                                     *
* EXITS,ERROR - N/A                                                   *
*                                                                     *
* TABLES/WORK AREAS -                                                 *
*    AVT - ADDRESS VECTOR TABLE                                       *
*    LCB - LINE CONTROL BLOCK                                         *
*    BUFFER BEING PROCESSED                                           *
*                                                                     *
* ATTRIBUTES - REUSABLE,REENTRANT                                     *
*                                                                     *
* CHARACTER CODE DEPENDENCY - THE OPERATION OF THIS MODULE DOES NOT   *
*    DEPEND UPON AN INTERNAL REPRESENTATION OF THE EXTERNAL CHARACTER *
*    SET                                                              *
*                                                                     *
* NOTES - N/A                                                         *
*                                                                     *
***********************************************************************
R1       EQU   1                        REGISTER
R2       EQU   2                        WORK REG
RSCB     EQU   3                        SCB ADDRESS             SA59188
RLCB4    EQU   4                        LCB ADDR
R5       EQU   5                        WORK REG
RPREFIX  EQU   6                        BUFFER ADDR
CTROPT   EQU   7                        REG- ADDR OF COUNTER OPT FIELD
*                                       PASSED FROM IEDQBO
MAXCT    EQU   8                        REG- ADDR OF MAX COUNT OPT
*                                       FIELD OR ADDR IN SETEOM PARM
*                                       LIST OF MAX COUNT
*                                       PASSED FROM IEDQBO
RAVT     EQU   9                        AVT ADDR
RPARM    EQU   10                       ADDR OF SETEOM PARM LIST
RBASE    EQU   12                       BASE REG
R14      EQU   14                       LINK REG
R15      EQU   15                       RETURN CODE REG
STATUS   EQU   2                        STATUS BYTE OFFSET
AEOFF    EQU   3                        OFFSET IN SETEOM PARM LIST OF
*                                       AE INDEX
PARMLN   EQU   4                        AE PARM LIST LNG
DISP     EQU   4                        DISPLACEMENT VALUE
RG15OFF  EQU   16                       REG 15 OFFSET FOR AE PARM LIST
MAXOPT   EQU   X'08'                    FLAG - MAX COUNT IN OPT FIELD
PROCESS  EQU   X'10'                    FLAG - PROCESS EQ YES
DEBLKBFR EQU   X'80'                    FLAG TO INDICATE DEBLOCKED BFR
*                                       (PRFTQBCK+2 IS FLAG BYTE)
INTEG    EQU   X'0C'                    MAX COUNT OFFSET IN PARM LIST
*                                       (WHEN INTEGER)
MAXOFF   EQU   X'0D'                    OFFSET TO MAX COUNT OPT FIELD
CTOPOFF  EQU   X'0E'                    OFFSET IN SETEOM PARM LIST OF
*                                       COUNTER OPT FIELD
LMDVTO   EQU   X'0A'                    INDICATES TO P=NO THAT  SA59188
*                                       P=YES HAS ALREADY EXEC'DSA59188
DISP3    EQU   3                        DISPLACEMENT OF 3      @XA05312
UNEX     EQU   X'01'                    UNIT EXCEPTION         @XA05312
         USING IEDQBR01,RBASE
         USING IEDQAVTD,RAVT
         USING IEDQPRF,RPREFIX
         USING IEDQLCB,RLCB4
         USING IEDQSCB,RSCB             SCB ADDRESSABILITY      SA59188
IEDQBR01 EQU   *
IEDQBR   IEDHJN IDPASS                  MODULE ID AND DATE
         L     RPARM,0(R1)              ADDR OF SETEOM PARM LIST
         SR    R5,R5                    CLEAR REG
         IC    R5,LCBISZE               NO. OF RESERVES
         TM    PRFSTAT1,PRFNHDRN        HDR BFR?
         BO    TXTPRE                   BR NO
         MVC   0(2,CTROPT),AVTFZERO     CLEAR COUNT FIELD       SA59188
         LA    R5,AVTHDRSZ(R5)          HDR PREFIX SIZE
         B     SKIP1                    BRANCH
TXTPRE   EQU   *
         LA    R5,AVTTXTSZ(R5)          TEXT PREFIX SIZE
SKIP1    EQU   *
         LH    R2,PRFSIZE               ASSUME NO TEXT ERROR    SA59188
         TM    SCBERR4,SCBTXTTN         TEXT ERROR              SA59188
         BZ    SKIP2                    BR NO                   SA59188
         SPACE
         CLC   AVTADBUF+1(3),SCBDEOB+1  EOB BUFFER              SA59188
         BNE   INIT1                    BR NO                   SA59188
         SPACE
         LH    R2,SCBEOB                GET AMOUNT OF GOOD DATA SA59188
SKIP2    EQU   *                                                SA59188
         N     R2,AVTCLRHI              CLEAR HIGH ORDER BYTES
         LTR   R2,R2                    ANY GOOD DATA IN BFR    SA59188
         BZ    INIT1                    BR NO                   SA59188
         SPACE
         SR    R2,R5                    CALCULATE AMOUNT OF ACTUAL DATA
*                                       IN BUFFER
         AH    R2,0(CTROPT)             ADD CURRENT COUNT
         CH    R2,0(MAXCT)              MAX COUNT REACHED?
         BL    NOMSG                    BR NO
         LH    R2,0(MAXCT)              CALCULATE NO.
         N     R2,AVTCLRHI              OF DATA BYTES
         SH    R2,0(CTROPT)             IN EOM BFR
         AR    R2,R5                    CALCULATE NEW PRFSIZE
         LR    R15,R2                   SET RETURN CODE(EOM CONDITION
*                                       MET); NEW PRFSIZE
         TM    STATUS(RPARM),PROCESS    PROCESS EQ YES?
         BO    NOSET                    BR YES
         TM    LCBCSW+DISP3,UNEX        EOT RECEIVED           @XA05312
         BO    NOSET                    BR YES                 @XA05312
         MVC   0(2,CTROPT),0(MAXCT)     SET MAX COUNT IN COUNTER FIELD
*                                       (EOM CONDITION MET; PROCESS EQ
*                                       NO ON A NOT LAST BFR; BFR WILL
*                                       BE PUT ON HOLD Q BY IEDQBO AND
*                                       COUNT DECREMENTED)
         B     EXIT                     BRANCH TO EXIT MODULE
NOSET    EQU   *
         MVC   0(2,CTROPT),AVTFZERO     INITIALIZE COUNT FIELD
         B     EXIT                     BRANCH TO EXIT MODULE
NOMSG    EQU   *
         TM    PRFSTAT1,PRFNLSTN        EOT BFR
         BO    INIT                     BR NO
         SPACE
         TM    SCBERR4,SCBTXTTN         TEXT ERROR              SA59188
         BO    INIT                     BR YES                  SA59188
         SPACE
         TM    STATUS(RPARM),PROCESS    PROCESS EQ YES          SA59188
         BO    TSTRESP                  BR YES                  SA59188
         SPACE
         L     R5,LCBSTCBA-1            GET STCB ADDR           SA59188
         CLI   AVTEZERO(R5),LMDVTO      PROC=YES ALREADY EXEC'D SA59188
         BE    INIT                     BR YES                  SA59188
         SPACE
TSTRESP  EQU   *                                                SA59188
         TM    LCBSTAT2,LCBRESP         WILL READ CONT BE DONE  SA59188
         BO    INIT1                    BR YES                  SA59188
         SPACE
         TM    STATUS(RPARM),PROCESS    PROCESS EQ YES
         BZ    INIT                     BR NO
         MVC   0(2,CTROPT),AVTFZERO     REINIT CTR FIELD CAUSE FORCEOM
*                                       CONDITION
         B     INIT1                    BRANCH TO SET RETURN CODE AND
*                                       EXIT
INIT     EQU   *
         STH   R2,0(CTROPT)             INCREMENT COUNT FIELD
INIT1    EQU   *
         SR    R15,R15                  SET RETURN CODE FOR EOM
*                                       CONDITION NOT MET
EXIT     EQU   *
         LR    R1,RPARM                 RETURN TO
         L     R14,AVTUI                CALLING MODULE (IEDQBO)
         B     DISP(R14)                THRU UI
         TAVTD
         TPRFD
         TLCBD
         DCBD  DSORG=TX
         TSCBD                                                  SA59188
         END
