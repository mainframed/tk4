IEDQFSC  CSECT
         TITLE 'FULLSCREEN MACRO FUNCTION ROUTINE'
***********************************************************************
*C039000,086000,089500,099000,102500                           @OZ35823
*A038500,053500,042500,091500,104500,109500,115500             @OZ35823
***********************************************************************
*                                                                     *
* MODULE NAME = IEDFSC                                                *
*                                                                     *
* DESCRIPTIVE NAME = TSO FULLSCREEN MACRO FUNCTION ROUTINE            *
*                                                                     *
* COPYRIGHT = NONE                                                    *
*                                                                     *
* STATUS = VS2 RELEASE 3.7                                            *
*                                                                     *
* FUNCTION = DETERMINES WHETHER FULLSCREEN MODE IS TO BE ENTERED      *
*            AND RESETS LINE COUNT AND CARRIAGE COUNT VALUES WHEN     *
*            SPECIFIED.                                               *
*                                                                     *
* NOTES = SEE BELOW                                                   *
*                                                                     *
*   DEPENDENCIES = NONE                                               *
*                                                                     *
*   RESTRICTIONS = NONE                                               *
*                                                                     *
*   REGISTER CONVENTIONS = TCAM STANDARD                              *
*                                                                     *
*   PATCH LABEL = FIXAWAY                                             *
*                                                                     *
* MODULE TYPE = MACRO FUNCTION ROUTINE                                *
*                                                                     *
*   PROCESSOR = ASSEMBLER F                                           *
*                                                                     *
*   MODULE SIZE = N/A                                                 *
*                                                                     *
*   ATTRIBUTES = SERIALLY REUSEABLE                                   *
*                                                                     *
* ENTRY POINT = IEDFSC                                                *
*                                                                     *
* INPUT = BUFFER FROM MESSAGE HANDLER                                 *
*                                                                     *
* OUTPUT = TO MESSAGE HANDLER                                         *
*                                                                     *
* EXIT-NORMAL = TO MESSAGE HANDLER VIA IEDQUI                         *
*                                                                     *
* EXIT-ERROR = TO MESSAGE HANDLER VIA IEDQUI                          *
*               RETURN CODES            MEANING                       *
*                 4            FULLSCREEN MODE NOT SWITCHED           *
*                 8            OPTION FIELD NOT FOUND                 *
*                 C            NOT A TSO MESSAGE                      *
*                10            INVALID TRIGGER OPTION CONTENTS        *
*                                                                     *
* EXTERNAL REFERENCES = IEDQAE - TO LOCATE OPTION FIELDS              *
*                                                                     *
*   DATA AREAS = TCAM BUFFER                                          *
*                                                                     *
*   CONTROL BLOCKS = AVT, DCT, LCB, QCB, TRM, PRF                     *
*                                                                     *
* TABLES = NONE                                                       *
*                                                                     *
* MACROS = IEDDCT - TO LOCATE DEVICE CHARACTERISTICS TABLE ENTRY      *
*                                                                     *
***********************************************************************
R0       EQU   0
R1       EQU   1
RPARM    EQU   2
R3       EQU   3
RLCB     EQU   4
R5       EQU   5
RPREFIX  EQU   6
RQCB     EQU   7
RTRM     EQU   8
RAVT     EQU   9
RDCT     EQU   10
R11      EQU   11
RBASE    EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
CONWCC   EQU   X'C0'                    WCC CONTROL BITS       @OZ35823
PA3KEY   EQU   X'6B'                    PA3KEY                 @OZ35823
PA1KEY   EQU   X'6C'                    PA1KEY                 @OZ35823
ESC      EQU   X'27'                    ESCAPE CHARACTER       @OZ35823
SYN      EQU   X'32'                    SYN CHARACTER          @OZ35823
SAWCC    EQU   X'C4'                    SOUND ALARM WCC        @OZ35823
LLWCC    EQU   X'40'                    LEAVE LOCKED WCC       @OZ35823
KIWCC    EQU   X'02'                    KEYBOARD INHIBIT       @OZ35823
SBA      EQU   X'11'                    SBA AID                @OZ35823
CCWSTX   EQU   X'02'                    STX CHARACTER          @OZ35823
         EJECT
         USING IEDQAVTD,RAVT
         USING IEDDCT,RDCT
         USING IEDQLCB,RLCB
         USING IEDQQCB,RQCB
         USING IEDQPRF,RPREFIX
         USING IEDQTRM,RTRM
         USING PARMLIST,RPARM
         USING IEDQFSO,R3                                      @OZ35823
         USING *,RBASE
IEDQFSC  IEDHJN CODE
         EJECT
         LR    RPARM,R1                 SAVE PARAMETER LIST
         TM    PRFSTAT1,PRFTSMSG        TSO MESSAGE
         BNO   ERRORC                   BRANCH NO
         LH    R1,LCBTTCIN              GET CONNECTED INDEX
         N     R1,AVTCLRHI              CLEAR HIGH ORDER BYTES
         LTR   R1,R1                    IS TTCIN ZERO
         BZ    ERRORC                   BRANCH ERROR
         L     R15,AVTRNMPT             GET TERMINAL
         BALR  R14,R15                  ENTRY ADDRESS
         LR    RTRM,R1                  SAVE TERMINAL ADDRESS
         L     RQCB,TRMDESTQ-1          LOAD QCB ADDRESS
         IEDDCT REG=RDCT,LEN=6
         TM    DCTBYTE1,DCT3270         3270 TERMINAL
         BNO   ERRORC                   BRANCH NO
         LA    R1,AEOPT                 SET PARM LIST ADDRESS
         L     R15,AVTUI                LOCATE IEDQFSCR OPTION
         BALR  R14,R15                  FIELD
         LTR   R15,R15                  TEST RETURN CODE
         BNZ   ERROR8                   BRANCH NO
         EJECT
         TM    FSFLAG,EXPFLS            EXPANDED FULLSCREEN    @OZ35823
         BNO   REGFUN                   BRANCH NO              @OZ35823
         TM    LCBSTAT1,LCBSENDN        SENDING                @OZ35823
         BO    OUTPUT                   BRANCH YES             @OZ35823
         TM    PRFSTAT1,PRFNHDRN        HEADER BUFFER          @OZ35823
         BO    ERROR0                   BRANCH NO              @OZ35823
         LH    R5,PRFSIZE               GET DATA SIZE          @OZ35823
         LA    R11,AVTHDRSZ             GET HEADER SIZE        @OZ35823
         SR    R5,R11                   ANY DATA PRESENT       @OZ35823
         BNP   ERROR0                   BRANCH NO              @OZ35823
         SR    R11,R11                  CLEAR WORK REGISTER    @OZ35823
         IC    R11,LCBISZE              GET IDLES COUNT        @OZ35823
         LA    R11,PRFSHDR(R11)         POINT TO DATA          @OZ35823
         CLI   0(R11),CCWSTX            STX CHARACTER          @OZ35823
         BE    ISKIP3                   BRANCH YES             @OZ35823
         CH    R5,AVTHA4                MINIMUM INPUT          @OZ35823
         BNH   ISKIP0                   YES, IS AID,CSR,CSR,NL @OZ35823
         CLI   3(R11),SBA               SBA CHARACTER          @OZ35823
         BE    ISKIP0                   BRANCH YES             @OZ35823
         B     ISKIP2                   SKIP CU DVC CHARACTERS @OZ35823
ISKIP3   EQU   *                                               @OZ35823
         LA    R11,1(,R11)              SKIP STX,ESC,CMD       @OZ35823
ISKIP2   EQU   *                                               @OZ35823
         LA    R11,1(,R11)              SKIP ESC CMD           @OZ35823
ISKIP1   EQU   *                                               @OZ35823
         LA    R11,1(,R11)              SKIP 1 BYTE            @OZ35823
ISKIP0   EQU   *                                               @OZ35823
         CLI   0(R11),PA1KEY            PA1 KEY                @OZ35823
         BNE   INNEWC                   BRANCH NO              @OZ35823
         NI    FSOFIELD,AVTEFF-(FSOFS+FSOBC+FSORD) RESET STATUS@OZ35823
         B     ERROR0                                          @OZ35823
INNEWC   EQU   *                                               @OZ35823
         TM    FSOFIELD,FSOBC           BROADCAST              @OZ35823
         BZ    INNEWE                   BRANCH NO              @OZ35823
         NI    FSOFIELD,AVTEFF-FSOBC    RESET BROADCAST        @OZ35823
         NI    QCBTSOF1,AVTEFF-QCBSATRD RESET SATRD            @OZ35823
         XI    FSOFIELD,FSORD           FLIP INTERCEPT FLAG    @OZ35823
         MVI   0(R11),PA3KEY            SET AID TO PA3         @OZ35823
         B     NORMEXP                                         @OZ35823
INNEWE   EQU   *                                               @OZ35823
         TM    FSOFIELD,FSOFS           PREVIOUS OUTPUT FS     @OZ35823
         BZ    ERROR0                   BRANCH NO              @OZ35823
         B     NORMEXP                  SET LINE AND CARRIAGE  @OZ35823
         EJECT
OUTPUT   EQU   *                                               @OZ35823
         TM    PRFSTAT1,PRFNHDRN        HEADER BUFFER          @OZ35823
         BO    OUTNEWX                  BRANCH NO              @OZ35823
         NI    FSOFIELD,AVTEFF-FSOLK    RESET KEYBOARD LOCK    @OZ35823
         LH    R5,PRFSIZE               GET DATA SIZE          @OZ35823
         LA    R11,AVTHDRSZ             GET PREFIX SIZE        @OZ35823
         SR    R5,R11                   ANY DATA               @OZ35823
         BNP   ERROR0                   BRANCH NO              @OZ35823
         SR    R11,R11                  CLEAR FOR INSERT       @OZ35823
         IC    R11,LCBISZE              GET IDLES COUNT        @OZ35823
         LA    R11,PRFSHDR(R11)         POINT TO DATA          @OZ35823
         CLI   0(R11),CCWSTX            STX CHARACTER          @OZ35823
         BE    OSKIP3                   BRANCH YES             @OZ35823
         CLI   0(R11),ESC               ESCAPE CHARACTER       @OZ35823
         BE    OSKIP2                   BRANCH YES             @OZ35823
         B     OSKIP0                   SKIP NO CHARACTERS     @OZ35823
OSKIP3   EQU   *                                               @OZ35823
         LA    R11,1(,R11)              SKIP STX ESC CMD       @OZ35823
OSKIP2   EQU   *                                               @OZ35823
         LA    R11,1(,R11)              SKIP ESC CMD           @OZ35823
OSKIP1   EQU   *                                               @OZ35823
         LA    R11,1(,R11)              SKIP 1 BYTE            @OZ35823
OSKIP0   EQU   *                                               @OZ35823
         CLC   1(4,R11),EXPFSID         EXPANDED FULLSCREEN MSG@OZ35823
         BNE   OUTNEWH                  BRANCH NO              @OZ35823
         OI    FSOFIELD,FSOFS           SET FULLSCREEN         @OZ35823
         MVC   QCBCARCT(1),CARRIER      SET CARRIAGE COUNT     @OZ35823
         MVC   QCBSATCT(1),LCOUNT       AND LINE COUNT         @OZ35823
         TM    FSOFIELD,FSORD+FSOBC     REDISPLAY OR BROADCAST @OZ35823
         BZ    ERROR0                   BRANCH NO              @OZ35823
         XI    FSOFIELD,FSORD           FLIP REDISPLAY FLAG    @OZ35823
         OI    FSOFIELD,FSOLK           SET KEYBOARD LOCK      @OZ35823
         TM    0(R11),KIWCC             KEYBOARD LOCKED        @OZ35823
         BZ    ERROR0                   BRANCH NO              @OZ35823
         NI    QCBTSOF2,AVTEFF-QCBDSSMI RESET KEYBOARD UNLOCK  @OZ35823
         NI    0(R11),AVTEFF-KIWCC      INHIBIT KEYBOARD       @OZ35823
         CLI   0(R11),CONWCC            WCC CONTROL OFF        @OZ35823
         BNE   ERROR0                   BRANCH NO              @OZ35823
         MVI   0(R11),AVTEFF-CONWCC+1   CLEAR BITS             @OZ35823
         B     ERROR0                   EXIT                   @OZ35823
OUTNEWH  EQU   *                                               @OZ35823
         CLC   1(4,R11),EXPLFSID        LAST FULLSCREEN MESSAGE@OZ35823
         BNE   OUTNEWJ                  BRANCH NO              @OZ35823
         NI    FSOFIELD,AVTEFF-(FSOFS+FSOBC+FSORD) RESET EXP FS@OZ35823
         MVI   QCBCARCT,AVTEZERO        CLEAR CARRIAGE COUNT   @OZ35823
         LH    R1,5(,R11)               COMPUTE VALUE          @OZ35823
         SLDL  R0,24                    TO WHICH LINE COUNT    @OZ35823
         SLL   R1,2                     IS TO BE RESET         @OZ35823
         SRDL  R0,6                     AS REQUESTED IN        @OZ35823
         SRL   R1,20                    SECOND SBA             @OZ35823
         SLR   R0,R0                    ADDRESS OF             @OZ35823
         LA    R15,80                   FULLSCREEN             @OZ35823
         DR    R0,R15                   MESSAGE                @OZ35823
         STC   R1,QCBSATCT              RESET COUNT            @OZ35823
         B     ERROR0                   EXIT                   @OZ35823
OUTNEWJ  EQU   *                                               @OZ35823
         TM    FSOFIELD,FSOFS           PREVIOUS MESSAGE FS    @OZ35823
         BNO   ERROR0                   BRANCH NO              @OZ35823
         NI    FSOFIELD,AVTEFF-FSOFS    RESET FULLSCREEN       @OZ35823
         OI    FSOFIELD,FSOBC           BROADCAST              @OZ35823
         MVI   QCBSATCT,AVTEFF          SET NEXT TO LAST LINE  @OZ35823
         MVI   QCBCARCT,AVTEZERO        START                  @OZ35823
         OI    QCBTSOF1,QCBSATRD        SET SIMATTN READ REQ   @OZ35823
         OI    0(R11),SAWCC             SET SOUND ALARM WCC    @OZ35823
         B     ERROR0                   EXIT                   @OZ35823
OUTNEWX  EQU   *                                               @OZ35823
         TM    FSOFIELD,FSOLK           KEYBOARD TO BE LOCKED  @OZ35823
         BZ    ERROR0                   BRANCH NO              @OZ35823
         TM    PRFSTAT1,PRFNLSTN        LAST BUFFER            @OZ35823
         BO    ERROR0                   BRANCH NO              @OZ35823
         CLI   LCBFLAG1,LCBPLCB         3705 TERMINAL          @OZ35823
         BE    NOXTERM                  BRANCH NO              @OZ35823
         LA    RPREFIX,1(,RPREFIX)      INCREMENT LOOK ADDRESS @OZ35823
NOXTERM  EQU   *                                               @OZ35823
         CLC   PRFSTXT(3),ULSEQ         UNLOCK SEQUENCE        @OZ35823
         BNE   ERROR0                   BRANCH NO              @OZ35823
         NI    QCBTSOF2,AVTEFF-QCBDSSMI RESET KEYBOARD UNLOCK  @OZ35823
         MVI   PRFSTXT+2,AVTEFF-CONWCC+1 LEAVE KEYBOARD LOCKED @OZ35823
         B     ERROR0                   EXIT                   @OZ35823
         EJECT
REGFUN   EQU   *                                               @OZ35823
         LA    R11,AEPARMS              LOAD ADDRESS PARMLIST
MACLC    EQU   *
         TM    FSFLAG,LCOPT             LCOUNT IN OPTION FIELD
         BNO   MACCA                    BRANCH NO
         LR    R1,R11                   SET PARMLIST REGISTER
         LA    R11,4(,R11)              LOCATE
         L     R15,AVTUI                LCOUNT
         BALR  R14,R15                  OPTION FIELD
         LTR   R15,R15                  CARRIER OPTION PRESENT
         BNZ   ERROR8                   BRANCH NO
         MVC   LCOUNT(1),0(R5)          SET CARRIER
MACCA    EQU   *
         TM    FSFLAG,CAOPT             CARRIER IN OPTION FIELD
         BNO   MACTRIG                  BRANCH NO
         LR    R1,R11                   SET PARMLIST REGISTER
         LA    R11,4(,R11)              LOCATE
         L     R15,AVTUI                CARRIER
         BALR  R14,R15                  OPTION FIELD
         LTR   R15,R15                  TRIGGER OPTION PRESENT
         BNZ   ERROR8                   BRANCH NO
         MVC   CARRIER(1),0(R5)         SET CARRIER IN PARMLIST
         EJECT
MACTRIG  EQU   *
         TM    LCBSTAT1,LCBSENDN        SEND OPERATION
         BO    LOCTRIG                  BRANCH YES
         TM    PRFSTAT1,PRFNHDRN        HEADER BUFFER
         BO    ERROR4                   BRANCH NO
LOCTRIG  EQU   *
         TM    FSFLAG,CHARPRES          CONCHARS SPECIFIED
         BO    SETFULL                  BRANCH YES
         TM    FSFLAG,TROPT             TRIGGER IN OPTION FIELD
         BNO   MACALL                   BRANCH NO
         LR    R1,R11                   SET PARMLIST REGISTER
         L     R15,AVTUI                LOCATE TRIGGER
         BALR  R14,R15                  OPTION FIELD
         LTR   R15,R15                  TRIGGER OPTION PRESENT
         BNZ   ERROR8                   BRANCH NO
         MVC   TRIGGER(1),0(R5)         SET TRIGGER
MACALL   EQU   *
         CLI   TRIGGER,AVTEZERO         TERMINATE FULLSCREEN
         BE    OFF                      BRANCH YES
         CLI   TRIGGER,AVTEFF           ENTER FULLSCREEN
         BE    ON                       BRANCH YES
         TM    LCBSTAT1,LCBSENDN        SEND OPERATION
         BO    ERROR10                  BRANCH YES
         SR    R5,R5                    CLEAR INDEX REGISTER
         IC    R5,LCBISZE               GET IDLES COUNT
         LA    R5,AVTHDRSZ+12(R5,RPREFIX) SET DATA START
         TM    DCTBYTE2,DCTLOCAL        3270 LOCAL
         BZ    REMOTE                   BRANCH REMOTE
         CLC   0(1,R5),TRIGGER          LOCAL FULLSCREEN MODE
         BE    SETFULL                  BRANCH YES
         B     ERROR4                   BRANCH NOT FOUND
REMOTE   EQU   *
         TM    TRMDEVFL+1,TRMRNTRM      3705 TERMINAL
         BO    NCPTERM                  BRANCH YES
         CLC   3(1,R5),TRIGGER          TRIGGER FOUND
         BE    SETFULL                  BRANCH YES
         B     ERROR4                   BRANCH NOT FOUND
NCPTERM  EQU   *
         CLC   2(1,R5),TRIGGER          TRIGGER FOUND
         BNE   ERROR4                   BRANCH NOT FOUND
         EJECT
SETFULL  EQU   *
         XI    FSOFIELD,FSOFULL         FLIP FULLSCREEN MODE   @OZ35823
         SR    R15,R15                  SET GOOD RETURN CODE
         B     NORMAL                   PERFORM FULLSCREEN
ERROR4   EQU   *
         LA    R15,4                    SET MODE NOT SWITCHED
NORMAL   EQU   *
         L     RBASE,AVTUI              LOAD RETURN ADDRESS
         TM    FSOFIELD,FSOFULL         ENTRY FOR FULLSCREEN   @OZ35823
         BZ    4(,RBASE)                BRANCH NO
         MVC   QCBCARCT(1),CARRIER      RESET CARRIAGE
         MVC   QCBSATCT(1),LCOUNT       COUNT AND LINE COUNT
         B     4(,RBASE)                ERROR EXIT
NORMEXP  EQU   *                                               @OZ35823
         MVC   QCBCARCT(1),CARRIER      SET CARRIAGE COUNT     @OZ35823
         MVC   QCBSATCT(1),LCOUNT       AND LINE COUNT         @OZ35823
ERROR0   EQU   *                                               @OZ35823
         L     RBASE,AVTUI              LOAD RETURN ADDRESS    @OZ35823
         SR    R15,R15                  SET NORMAL RETURN      @OZ35823
         B     4(,RBASE)                RETURN                 @OZ35823
ERROR8   EQU   *
         L     RBASE,AVTUI              LOAD RETURN ADDRESS
         LA    R15,8                    SET OPTION FIELD NOT FOUND
         B     4(,RBASE)                RETURN
ERRORC   EQU   *
         L     RBASE,AVTUI              LOAD RETURN ADDRESS
         LA    R15,12                   SET NOT TSO MESSAGE
         B     4(,RBASE)                RETURN
ERROR10  EQU   *
         L     RBASE,AVTUI              LOAD RETURN ADDRESS
         LA    R15,16                   SET BAD CONTENTS IEDQFSCR
         B     4(,RBASE)                RETURN
ON       EQU   *
         L     RBASE,AVTUI              LOAD RETURN ADDRESS
         OI    FSOFIELD,FSOFULL         SET FULLSCREEN MODE    @OZ35823
         MVC   QCBCARCT(1),CARRIER      RESET CARRIAGE COUNT
         MVC   QCBSATCT(1),LCOUNT       AND LINE COUNT
         SR    R15,R15                  ZERO RETURN CODE
         B     4(,RBASE)                RETURN
OFF      EQU   *
         L     RBASE,AVTUI              LOAD RETURN ADDRESS
         NI    FSOFIELD,AVTEFF-FSOFULL  RESET FULLSCREEN MODE  @OZ35823
         SR    R15,R15                  ZERO RETURN CODE
         B     4(,RBASE)                RETURN
         EJECT
FIXAWAY  DC    20F'0'                   PATCH AREA
EXPFSID  DC    X'115D7F11'              FULLSCREEN MSG ID      @OZ35823
EXPLFSID DC    X'115D7E11'              LAST FULLSCREEN MSG ID @OZ35823
ULSEQ    DC    X'27F1C2'                UNLOCK SEQUENCE        @OZ35823
         EJECT
PARMLIST DSECT
         DS    XL1                      INDEX TO IEDQNWMH
         DS    XL1                      PARAMETER LIST LENGTH
         DS    XL1                      INDEX TO IEDQFSC
FSFLAG   DS    XL1                      FLAGS
LCOPT    EQU   X'80'                    LCOUNT PRESENT
CAOPT    EQU   X'40'                    CARRIER PRESENT
TROPT    EQU   X'20'                    TRIGGER PRESENT
CHARPRES EQU   X'10'                    CONCHARS PRESENT
EXPFLS   EQU   X'08'                    EXPANDED FULLSCREEN    @OZ35823
LCOUNT   DS    XL1                      LINE COUNT
CARRIER  DS    XL1                      CARRIAGE COUNT
TRIGGER  DS    XL1                      TRIGGER CHARACTER
AEOPT    DS    XL1                      INDEX TO IEDQAE
         DS    XL1                      FOR FULLSCREEN OPTION
         DS    XL1                      OPTION OFFSET
         DS    XL1                      OUTPUT REGISTER
AEPARMS  DS    XL1                      INDEX TO IEDQAE
         DS    XL1
         DS    XL1                      OPTION OFFSET
         DS    XL1                      OUTPUT REGISTER
         DC    XL1'00'                  ALIGNMENT BYTE
         EJECT
IEDQFSO  DSECT                                                 @OZ35823
FSOFIELD DS    XL1                      FULLSCREEN OPTION BYTE @OZ35823
FSOLK    EQU   X'20'                    KEEP KEYBOARD LOCKED   @OZ35823
FSORD    EQU   X'08'                    REDISPLAY FLAG         @OZ35823
FSOBC    EQU   X'04'                    BROADCAST FLAG         @OZ35823
FSOFS    EQU   X'02'                    EXPANDED FULLSCREEN    @OZ35823
FSOFULL  EQU   X'01'                    FULLSCREEN MODE SWITCH @OZ35823
         EJECT
         EJECT
         TAVTD
         EJECT
         TDCTD
         EJECT
         TLCBD
         EJECT
         TQCBD
         EJECT
         TPRFD
         EJECT
         TTRMD
         END
