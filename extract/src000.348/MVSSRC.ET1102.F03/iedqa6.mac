A601     TITLE '''IEDQA6'' - LINE CONTROL INITIALIZATION ROUTINE'
IEDQA6   CSECT
*C580000                                                         S21903
*A696000                                                         A42378
*D711000                                                         A42378
*C222000                                                         A42378
*A237300-239100,248500,                                          S22026
*C255000                                                         S22026
*A124000,151000,352000,353000,690600-692700                      S22025
*C219000,687000,690000                                           S22025
*D220000-228000                                                  S22025
*A125000,351100-351700,353500                                    S99238
*A193000-194500,270300-272100,352000-352900                      S22024
*A433000,523000,559000-560000,604000,726000-754000               S22024
*C006060,785000                                                  S22024
*A270600,271920,801000                                           X03039
*C006070,271840,271880                                           X03039
*C271500,271840,271880,294000                                  @YA08433
*C271500,271840,271880                                         @YA10995
*A237000                                                       @OS76508
*A270600,270620                                                @Y17XAMG
*C270300,271929,270640                                         @Y17XAMG
*C336000                                                       @OY18008
*D270680-270820                                                @OY18118
***********************************************************************
*                                                                     *
*STATUS -- CHANGE LEVEL 8                                        X03039
*                                                                     *
*FUNCTION --                                                          *
*   INITIALIZES FIELDS IN THE SCB TO INDICATE THE LINE CONTROL        *
*   CHARACTERS TO BE INSERTED, SUBBLOCK SIZE AND BLOCK SIZE           *
*   OR THE NUMBER OF SUBBLOCKS PER BLOCK AND THE SUBBLOCK DELIMITER   *
*   (VARIABLE-LENGTH REBLOCKING).                                     *
*                                                                     *
*   THE KEY FOR THE DESTINATION IS ACCESSED FROM THE PREFIX           *
*   (PRFDEST) AND PASSED TO THE TNT CODE (AVTRNMPT) TO RETURN THE     *
*   ADDRESS OF THE TERMINAL TABLE ENTRY.                              *
*                                                                     *
*   FROM INFORMATION IN THE PARAMETER LIST,  THE ROUTINE INITIAL-     *
*   IZES THE FOLLOWING FIELDS: (1) SCBEOBSZ AND SCBEOBAC TO THE       *
*   INTERVAL BETWEEN EOBS,  IF EOB INSERTION IS REQUESTED, OR TO      *
*   THE INTERVAL BETWEEN ETBS,  IF ETB INSERTION IS REQUESTED; AND    *
*   (2) SCBITBSZ AND SCBITBAC TO THE INTERVAL BETWEEN ITBS,  IF ITB   *
*   INSERTION IS REQUESTED; OR (3) SCBBLKCT,SCBENDCR,AND SCBCRLEN FOR *
*   VARIABLE REBLOCKING.  THE INTERVAL IS SET FROM THE PARAMETER      *
*   LIST,  IF SPECIFIED THERE;  OTHERWISE IT IS ACCESSED FROM         *
*   FIELDS IN THE TERMINAL TABLE ENTRY.  IF EOBS/ETBS OR ITBS ARE     *
*   NOT TO BE INSERTED,  THE CORRESPONDING SCB FIELDS ARE SET TO      *
*   ZERO.  THE ROUTINE RETURNS A RETURN CODE OF FOUR AND DOES NOT     *
*   PERFORM THE FUNCTION IF THE USER REQUESTS INTERVALS NOT SPECI-    *
*   FIED EITHER IN THE MACRO OR IN THE TERMINAL TABLE ENTRY, OR       *
*   IF THE USER SPECIFIES LINE CONTROL INVALID FOR THE DESTINATION    *
*   (E.G., ITBS FOR A START/STOP TERMINAL).                           *
*                                                                     *
*   IF INITIALIZATION HAS BEEN PERFORMED SUCCESSFULLY,  THE MSGFORM   *
*   REQUEST BIT IN SET IN THE SCB (SCBMGFMN) AND CONTROL RETURNS TO   *
*   THE CALLER WITH A RETURN CODE OF ZERO.                            *
*                                                                     *
*ENTRY POINTS --                                                      *
*       'IEDQA601' TO INITIALIZE SCB FIELDS TO LINE CONTROL           *
*   INTERVALS.  CALLING SEQUENCE FROM USER INTERFACE IS:              *
*                                                                     *
*        L     R12,AVTMSGS-1            GET ADDR OF VCON TABLE        *
*        IC    R15,AVTEZERO(,R1)        GET INDEX TO ROUTINE ADDR     *
*        L     R12,AVTEZERO(R12,R15)    GET ROUTINE ADDRESS           *
*        BR    R12                      EXIT TO ROUTINE               *
*                                                                     *
*INPUT --                                                             *
*   REGISTER 1 - ADDRESS OF A MACRO-GENERATED PARAMETER LIST.         *
*   PARAMETER LIST FORMATS ARE:                                       *
*                                                                     *
*        *********************************   BLOCK AND                *
*        * INDEX *       *     BLOCK     *     SUBBLOCK SPECIFIED     *
*        *  TO   * X'05' *   INTERVAL    *                            *
*        *   A6  *       *               *                            *
*        *********************************                            *
*        *SUBBLCK*                                                    *
*        * INTER-*                                                    *
*        *  VAL  *                                                    *
*        *********                                                    *
*                                                                     *
*        *********************************   BLOCK ONLY               *
*        * INDEX *       *     BLOCK     *     SPECIFIED              *
*        *  TO   * X'04' *   INTERVAL    *                            *
*        *   A6  *       *               *                            *
*        *********************************                            *
*                                                                     *
*        *************************   SUBBLOCK ONLY                    *
*        * INDEX *       *SUBBLCK*     SPECIFIED                      *
*        *  TO   * X'03' * INTER-*                                    *
*        *   A6  *       *  VAL  *                                    *
*        *************************                                    *
*        *********************************   ENDCHAR AND              *
*        * INDEX *       *       *NUMBER *     COUNT SPECIFIED        *
*        *  TO   * X'04' *ENDCHAR*SUBBLKS*                            *
*        *   A6  *       *       *PER BLK*                            *
*        *********************************                            *
*                                                                     *
*                                                                     *
*        *****************   NEITHER                                  *
*        * INDEX *       *     SPECIFIED                              *
*        *  TO   * X'02' *                                            *
*        *   A6  *       *                                            *
*        *****************                                            *
*                                                                     *
*        A6 INDEX BYTE: X'01' ON - TRANSPARENT MODE                   *
*                       X'01' OFF - TEXT MODE                         *
*                       X'02' ON - ENDCHAR AND COUNT SPECIFIED
*                       X'03' ON - TRANSPARENT WITH NO PADDING   S99238
*                                                                     *
*   REGISTER 3 - ADDRESS OF SCB                                       *
*                                                                     *
*   REGISTER 6 - ADDRESS OF CURRENT BUFFER                            *
*                                                                     *
*   REGISTER 9 - ADDRESS OF THE AVT                                   *
*                                                                     *
*   REGISTER 12 - ENTRY POINT ADDRESS AND BASE FOR THIS ROUTINE       *
*                                                                     *
*OUTPUT --                                                            *
*   SCBEOBSZ AND SCBEOBAC - IF EOBS/ETBS ARE BEING INSERTED,  CON-    *
*   TAIN  THE INTERVAL BETWEEN EOBS/ETBS.  IF NOT,  CONTAIN ZERO.     *
*   SCBBLKCT,SCBENDCR,AND SCBCRLEN - IF VARIABLE-LENGTH REBLOCKING    *
*   IS TO BE PERFORMED.                                               *
*                                                                     *
*   SCBITBSZ AND SCBITBAC - IF ITBS ARE BEING INSERTED,  CONTAIN      *
*   THE INTERVAL BETWEEN ITBS.  IF NOT,  CONTAIN ZERO.                *
*                                                                     *
*   SCB MSGFORM REQUEST BIT (SCBMGFMN IN SCBSTATE) - TURNED ON        *
*   EXCEPT WHEN ERRORS DETECTED                                       *
*                                                                     *
*EXTERNAL REFERENCES --                                               *
*   'AVTRNMPT' - TNT CODE                                             *
*                                                                     *
*   AVT - ADDRESS VECTOR TABLE                                        *
*                                                                     *
*   SCB - STATION CONTROL BLOCK                                       *
*                                                                     *
*   BUFFER CURRENTLY BEING PROCESSED                                  *
*                                                                     *
*   TERMINAL TABLE ENTRY FOR THE DESTINATION                          *
*                                                                     *
*EXITS,  NORMAL --                                                    *
*   SCB FIELDS ARE INITIALIZED AS SPECIFIED.  REGISTER 15 CONTAINS    *
*   A RETURN CODE OF X'00'.                                           *
*                                                                     *
*EXITS,  ERROR --                                                     *
*   IF THE USER REQUESTS INTERVALS NOT SPECIFIED IN EITHER THE        *
*   MACRO OR IN THE TERMINAL TABLE ENTRY,  OR IF THE USER SPECIFIES   *
*   LINE CONTROL INVALID FOR THE DESTINATION,  REGISTER 15 CONTAINS   *
*   A RETURN CODE OF FOUR.                                            *
*                                                                     *
*TABLES/WORK AREAS -- N/A.                                            *
*                                                                     *
*ATTRIBUTES -- SERIALLY REUSABLE, REFRESHABLE, ENABLED, RESIDENT,     *
*   PROBLEM PROGRAM MODE.                                             *
*                                                                     *
*NOTES -- THE OPERATION OF THIS MODULE DOES NOT DEPEND UPON AN        *
*   INTERNAL REPRESENTATION OF THE EXTERNAL CHARACTER SET.            *
*                                                                     *
***********************************************************************
         EJECT
********* REGISTER EQUATES *********
         SPACE
R0       EQU   0                        WORK REGISTER
         SPACE
R1       EQU   1                        TERMINAL TABLE ENTRY ADDR
         SPACE
RPTR2    EQU   2                        POINTER TO DEV DEP AREA
         SPACE
RSCB3    EQU   3                        SCB ADDRESS
         SPACE
RLCB4    EQU   4                        LCB ADDRESS
         SPACE
RBLOCK5  EQU   5                        BLOCKING FACTOR
         SPACE
RPREFIX  EQU   6                        BUFFER ADDRESS
         SPACE
RSUBBLK7 EQU   7                        SUB-BLOCKING FACTOR
         SPACE
RPARM8   EQU   8                        PARAMETER LIST ADDRESS
         SPACE
RAVT9    EQU   9                        AVT ADDRESS
         SPACE
RRET10   EQU   10                       LOCAL RETURN REGISTER
         SPACE
RBASE    EQU   12                       BASE REGISTER
         SPACE
R13      EQU   13                       SAVE AREA ADDRESS
R14      EQU   14                       WORK REGISTER
R15      EQU   15                       WORK REGISTER
         SPACE
********* OTHER EQUATES *********
         SPACE
PARMINDX EQU   0                        INDEX BYTE
PARMLEN  EQU   1                        PARAMETER LIST LENGTH
PARMBLOK EQU   2                        BLOCKING FACTOR
         SPACE
PARMTRPN EQU   X'01'                    TRANSPARENT MODE
         SPACE
HEX01    EQU   X'01'                    SUBBLK SPECIFIED IN MACRO
HEX02    EQU   X'02'                    COUNT & ENDCHAR SPEC     S22025
NOPADTRP EQU   X'03'                    NO PADDING TRANSPARENT   S99238
HEX04    EQU   X'04'                    BLOCK SPECIFIED IN MACRO
         SPACE
TRMBLOKN EQU   X'10'                    BLOCK SPEC IN TERM ENTRY
TRMSUBKN EQU   X'08'                    SUBBLK SPEC IN TERM ENTRY
TRMTRPBN EQU   X'04'                    FLAG                     S22024
         SPACE
ONE      EQU   1                        INCREMENT VALUE OF ONE
TWO      EQU   2                        INCREMENT VALUE OF TWO
THREE    EQU   3                        INCREMENT VALUE OF THREE
FOUR     EQU   4                        OFFSET USED ON RETURN    S22025
         SPACE
SHIFT1   EQU   1                        SHIFT VALUE OF ONE
SHIFT2   EQU   2                        SHIFT VALUE OF TWO
SHIFT5   EQU   5                        SHIFT VALUE OF FIVE
SHIFT8   EQU   8                        SHIFT VALUE OF EIGHT
SHIFT29  EQU   29                       SHIFT VALUE OF 29
         SPACE
SCTEOB   EQU   18                       SCT INDEX BYTE TO EOB
SCTITB   EQU   19                       SCT INDEX BYTE TO ITB
SCTEOT   EQU   20                       SCT INDEX BYTE TO EOT
         SPACE
CHECKING EQU   X'10'                    FLAG IN DEVICE CHAR TABLE
         SPACE
ZERO     EQU   8                        CONDITION CODE: ZERO
         SPACE 1
BIT12    EQU   12                       NUMBER OF OPTION FLAGS   S22024
*                                       TO CHECK                 S22024
MODELCF  EQU   X'FD'                    FLAGS                    S22024
MODELCN  EQU   X'02'                    FLAGS                    S22024
BIT3     EQU   4                        VALUE4                   S22024
         EJECT
         USING IEDQAVTD,RAVT9
         USING IEDQLCB,RLCB4
         USING IEDQPRF,RPREFIX
         USING IEDQSCB,RSCB3
         USING IEDQA601,RBASE
         SPACE
IEDQA601 EQU   *
IEDQA6   IEDHJN LCTLINIT                                         S22025
         LH    R15,PRFSIZE              TEST FOR ZERO-
         LTR   R15,R15                    LENGTH BUFFER
         BZ    EXITOFF                  YES, EXIT IMMEDIATELY
         SR    R15,R15                  CLEAR RETURN CODE      @OS76508
         TM    SCBQTYPE,SCBCONC         CONCENTRATOR SCB ?       S22026
         BZ    NOCONC                   NO, CONTINUE             S22026
         L     RSCB3,LCBSCBDA-1         GET CONCENTRATOR SCB     S22026
         TM    SCBSTAT1,SCBCBGN         CONC MSG BEGIN?          S22026
         BZ    EXITOFF                  NO,EXIT IMMEDIATELY      S22026
         B     CONC                     GO FIND TERM ENTRY       S22026
NOCONC   EQU   *                                                 S22026
         SPACE
         TM    PRFSTAT1,PRFNHDRN        IS THIS A HEADER BUFFER
         BO    EXITOFF                  NO, EXIT IMMEDIATELY
         SPACE
         TM    LCBTSOB,LCB2741N         IS DEVICE IBM 2741
         BO    EXITOFF                  YES, EXIT IMMEDIATELY
CONC     EQU   *                                                 S22026
         SPACE
         LR    RPARM8,R1                SAVE PARAMETER LIST ADDRESS
         LH    R1,LCBTTCIN              GET DESTINATION OFFSET   S22026
         N     R1,AVTCLRHI
         L     R15,AVTRNMPT             GET ADDRESS OF TNT CODE
         BALR  R14,R15                  GET TERM ENTRY ADDRESS
         SR    RBLOCK5,RBLOCK5          CLEAR REG TO ZERO        S22024
         SR    RSUBBLK7,RSUBBLK7        CLEAR REG TO ZERO        S22024
         SPACE
         USING IEDQTRM,R1
         TM    TRMSTATE,TRMPREF         IS TERMINAL ON 3705    @Y17XAMG
         BNO   NO3705                   BRANCH ON NO             S22024
         LA    R14,TRMPRFSZ             PREFIX SIZE            @Y17XAMG
         LR    RPTR2,R1                 TRM ENTRY ADDRESS      @Y17XAMG
         SR    RPTR2,R14                PREFIX ADDRESS         @Y17XAMG
         CLI   TRMTYPE-IEDNTRM(RPTR2),TRMLUNT LOGICAL UNIT     @Y17XAMG
         BE    EXITOFF                  BR YES                 @Y17XAMG
*                                                                X03039
         LA    R14,BIT12                SET FOR BIT SEARCH       S22024
         BAL   RRET10,POINT             FIND DEVICE DEP FIELD    S22024
         NI    THREE(RPTR2),MODELCF     INDICATE LINE CONTROL  @YA10995
         TM    PARMINDX(RPARM8),HEX02   LC=IN SET ?              S22024
         BZ    CKCHANGE                 BRANCH ON NO           @YA10995
         TM    PARMLEN(RPARM8),HEX01    LC=IN SET ?              S22024
         BZ    CKCHANGE                 BRANCH ON NO           @YA10995
         OI    THREE(RPTR2),MODELCN     INDICATE LC SUPPLIED   @YA10995
CKCHANGE EQU   *                                                 X03039
*                                       DID MODE CHANGE          X03039
         CLC   ONE(TWO,RPTR2),THREE(RPTR2)                       X03039
         BE    TESTTRAN                 BR NO                    X03039
*                                                                X03039
         LA    RPTR2,TRMPRFSZ           GET TERMINAL PREFIX SIZE X03039
         SR    R1,RPTR2                 BACK UP TO TRM PREFIX    X03039
*                                       SET REQUEST MODE CHANGE  X03039
         OI    TRMPRE-IEDNTRM(R1),TRMCMODE                     @Y17XAMG
TESTTRAN EQU   *                                                 S22024
         SR    R15,R15                  SET RETURN CODE          S22024
         TM    PARMINDX(RPARM8),PARMTRPN TRANSPARENT MODE ?      S22024
         BZ    EXITOFF                  NO, EXIT                 S22024
         OI    SCBSTATE,SCBTRANP        SET TRANS IN SCB         S22024
         B     EXITOFF                  EXIT                     S22024
NO3705   EQU   *                                                 S22024
         SPACE
         L     R15,LCBDCBPT             GET DCB ADDRESS FROM LCB
         USING IHADCB,R15
         L     R15,DCBSCTAD-1           GET SCT ADDRESS FROM DCB
         CLI   SCTEOT(R15),AVTEZERO     IS INDEX TO EOT ZERO
         BNE   EOBCHK                   NO, CHECK EOB OFFSET   @YA08433
         SR    R15,R15                  SET RETURN CODE        @YA08433
         B     EXITOFF                  NO EOT, EXIT           @YA08433
         SPACE
EOBCHK   EQU   *                                               @YA08433
         SPACE
         CLI   SCTEOB(R15),AVTEZERO     IS INDEX TO EOB ZERO
         BNE   TESTCHEK                 NO, GO TEST FOR CHECKING
         SPACE
         TM    PARMINDX(RPARM8),PARMTRPN IS IT TRANSPARENT MODE
         BNO   SETFIELD                 NO, GO SET TO INSERT
*                                         EOT ONLY
         B     MACRBLOK                 YES, GO GET BLOCK SIZE
         SPACE
TESTCHEK EQU   *
         SR    R15,R15
         IC    R15,TRMCHCIN             PICK UP DEV CHAR TBL INDEX
         BCTR  R15,AVTEZERO             DECR TO MAKE TRUE INEDX
         MH    R15,AVTDCTLN              SHIFT TO MAKE OFFSET  @OY18008
         A     R15,AVTCSTCS             GET THIS TERM'S ENTRY ADDR
         TM    THREE(R15),CHECKING      CHECKING FOR THIS TERMINAL
         BNO   SETFIELD                 NO, INSERT EOT ONLY
         SPACE
MACRBLOK EQU   *
         NI    SCBBSCFM,AVTEFF-SCBNPDTR INITILIAZE PAD BIT       S99238
         TM    PARMINDX(RPARM8),NOPADTRP PADDING WANTED          S99238
         BNO   PAD                      NO, DON'T SET BIT        S99238
         OI    SCBBSCFM,SCBNPDTR        SET NO PADDING BIT       S99238
         B     MACBLOK                  CHECK BLOCK CODED ON MAC S99238
         SPACE
PAD      EQU   *                                                 S99238
         TM    PARMINDX(RPARM8),HEX02   ARE COUNT AND ENCHAR     S22024
*                                       SPECIFIED ?              S22024
         BO    SETSCBFL                 YES, SET SCB FIELDS      S22025
MACBLOK  EQU   *                                                 S99238
         TM    PARMLEN(RPARM8),HEX04    IS BLOCK SPECIFIED IN MACRO
         BNO   TESTBLOK                 NO, MUST GET FROM TRMENTRY
         SPACE
         LH    RBLOCK5,PARMBLOK(,RPARM8) YES, PICK IT UP
         TM    PARMINDX(RPARM8),PARMTRPN IS IT TRANSPARENT MODE
         BNO   TESTSUB                  NO, GO SEE IF SUBBLK SPEC
         SPACE 2
         SPACE
         TM    LCBSTAT2,LCBSYNC         IS THIS A BISYNC LINE
         BO    SETFLAG                  YES, GO SET FLAG
         SPACE
         B     SETFIELD                 NO, HANDLE AS NON-TRANSP
         SPACE
TESTBLOK EQU   *
         TM    PARMINDX(RPARM8),PARMTRPN IS IT TRANSPARENT MODE
         BNO   LINKBLOK                 NO, GO GET FROM TRMENTRY
         SPACE 2
         SPACE
         TM    LCBSTAT2,LCBSYNC         IS THIS A BISYNC LINE
         BNO   LINKBLOK                 NO, HANDLE AS NON-TRANSP
         SPACE
         TM    TRMDEVFL,TRMTRPBN        IS TRANSP BLOCK IN TRMENTRY
         BO    POINTRP                  YES, GO POINT TO IT
         SPACE 2
         SPACE
ERREXIT  EQU   *
         LA    R15,AVTECD4              NO, SET ERROR RETURN CODE
         B     EXITOFF                  RETURN
         SPACE
POINTRP  EQU   *
         LA    R14,BIT3                 SET FOR BIT SEARCH       S22024
         BAL   RRET10,POINT             POINT TO DEVICE DEP AREA
         SPACE
         TM    TRMDEVFL,TRMBLOKN        IS BLOCK SPECIFIED
         BNO   TESTBUMP                 NO, SEE IF SUBBLK IS
         SPACE
         LA    RPTR2,THREE(,RPTR2)      YES, BUMP PAST IT
         SPACE
TESTBUMP EQU   *
         TM    TRMDEVFL,TRMSUBKN        IS SUBBLK SPECIFIED
         BNO   ICTRPB                   NO, GO PICK UP TRP BLOCK
         SPACE
         LA    RPTR2,TWO(,RPTR2)        YES, BUMP PAST IT
         SPACE
ICTRPB   EQU   *
         IC    RBLOCK5,ONE(,RPTR2)      PICK UP FIRST BLOCK BYTE
         SLL   RBLOCK5,SHIFT8           SHIFT LEFT
         IC    RBLOCK5,TWO(,RPTR2)      PICK UP SECOND BLOCK BYTE
         B     SETFLAG                  GO SET FLAG
         SPACE
LINKBLOK EQU   *
         TM    TRMDEVFL,TRMBLOKN        IS BLOCK SPEC IN TERM ENTRY
         BO    GETBLOK                  YES, GO GET IT
         SPACE
         TM    LCBSTAT2,LCBSYNC         IS DESTINATION BISYNC
         BO    ERREXIT                  YES, ERROR, GO SET FLAG
         SPACE
         B     SETFIELD                 NO, GO SET ZEROES & EXIT
         SPACE
GETBLOK  EQU   *
         SPACE
         LA    R14,BIT3                 SET FOR BIT SEARCH       S22024
         BAL   RRET10,POINT             YES, POINT TO DEV DEP AREA
         SPACE
         IC    RBLOCK5,ONE(,RPTR2)      PICK UP FIRST BLOCK BYTE
         SLL   RBLOCK5,SHIFT8           SHIFT LEFT
         IC    RBLOCK5,TWO(,RPTR2)      PICK UP SECOND BLOCK BYTE
         SPACE
TESTSUB  EQU   *
         L     R15,LCBDCBPT             GET DCB ADDRESS FROM LCB
         L     R15,DCBSCTAD-1           GET SCT ADDRESS FROM DCB
         CLI   SCTITB(R15),AVTEZERO     IS INDEX TO ITB ZERO
         BE    SETFIELD                 YES, GO SET FIELDS
         SPACE
         TM    PARMLEN(RPARM8),HEX01    IS SUBBLK SPEC IN MACRO
         BNO   LINKSUBK                 NO, MUST GET FROM TRMENTRY
         SPACE
         IC    RSUBBLK7,PARMLEN(,RPARM8) YES, GET ITS OFFSET
         BCTR  RSUBBLK7,AVTEZERO        ADJUST
         IC    RSUBBLK7,AVTEZERO(RSUBBLK7,RPARM8) PICK IT UP
         B     SETFIELD                 GO SET SCB FIELDS
*
         SPACE
LINKSUBK EQU   *
         TM    TRMDEVFL,TRMSUBKN        IS SUBBLK SPEC IN TRMENTRY
         BNO   SETFIELD                 NO, NOT SPEC AT ALL
         SPACE
         LTR   R0,R0                    HAVE WE LINKED TO POINT YET
         BZ    GETSUBK                  YES, DON'T NEED TO AGAIN
         SPACE
         LA    R14,BIT3                 SET FOR BIT SEARCH       S22024
         BAL   RRET10,POINT             LINK TO POINT TO DEV DEP
         SPACE
GETSUBK  EQU   *
         TM    TRMDEVFL,TRMBLOKN        IS BLOCK IN TERM ENTRY
         BNO   ICSUBK                   NO, NOW AT SUBBLK
         SPACE
         LA    RPTR2,THREE(,RPTR2)      YES, BUMP TO SUBBLK
         SPACE
ICSUBK   EQU   *
         IC    RSUBBLK7,ONE(,RPTR2)     PICK UP SUBBLK BYTE
         B     SETFIELD                 SET UP SCB FIELDS        S22025
         SPACE 2
         SPACE
SETFLAG  EQU   *
         OI    SCBSTATE,SCBTRANP        YES, TURN ON BIT
         B     SETFDEOB                 SET EOB ONLY
         SPACE
SETFIELD EQU   *
         OI    SCBSTATE,SCBMGFMN        SET MSGFORM REQUEST BIT
         STC   RSUBBLK7,SCBITBSZ        SET SUBBLK IN SCB
         STC   RSUBBLK7,SCBITBAC        SET SUBBLK INITIAL OFFSET
         STH   RBLOCK5,SCBEOBAC         SET BLOCK INITIAL OFFSET
         SPACE
SETFDEOB EQU   *
         STH   RBLOCK5,SCBEOBSZ         SET BLOCK IN SCB
         SR    R15,R15                  SET GOOD RETURN CODE
         SPACE
EXITOFF  EQU   *
         L     RBASE,AVTUI              GET RET INTERFACE ADDR   S22025
         B     FOUR(RBASE)              BRANCH TO RETURN ROUTINE S22025
         SPACE
SETSCBFL EQU   *                                                 S22025
         OI    SCBSTATE,SCBMGFMN        SET MSGFORM REQ BIT      S22025
         STC   RSUBBLK7,SCBBCTR         ZERO BLK COUNTER         S22025
         LA    RSUBBLK7,ONE(RSUBBLK7)   SET LENGTH OF ONE        S22025
         STC   RSUBBLK7,SCBCRLEN        STORE IT IN SCB          S22025
         MVC   SCBENDCR,TWO(RPARM8)     SET ENDCHAR IN SCB       S22025
         MVC   SCBBLKCT,THREE(RPARM8)   SET BLK COUNT IN SCB     S22025
         B     SETFDEOB                 BRANCH FOR GOOD RETURN   S22025
         EJECT
POINT    EQU   *
         SR    R15,R15                  CLEAR FOR INSERT         A42378
         TM    TRMSTATE,TRMOPTFN        ARE THERE ANY OPT FIELDS
         LA    RPTR2,TRMOPNO            ASSUME NO
         BNO   SETFLAGS                 NO, NOW AT DEV DEP AREA
         SPACE
         IC    R15,TRMOPNO              PICK UP NO. OF OPT OFFSETS
         LA    RPTR2,TRMOPT(R15)        POINT TO DEV DEP AREA
         SPACE
SETFLAGS EQU   *
         LH    R15,TRMDEVFL             PICK UP DEVICE FLAGS     S22024
         N     R15,AVTCLRHI             CLEAR LEFT 2 BYTES       S22024
         LTR   R15,R15                  ANY FLAGS PRESENT        S22024
         BCR   ZERO,RRET10              BRANCH ON NO             S22024
         SLL   R15,15                   SHIFT TO HIORDER HALF    S22024
         SR    R0,R0                    CLEAR REGISTER           S22024
         SPACE 1
SHIFT    EQU   *                                                 S22024
         LTR   R15,R15                  IS THIS BIT ON           S22024
         BNM   CHKCOUNT                 BRANCH ON NO             S22024
         AH    R0,HONE                  COUNT OF BITS FOUND      S22024
CHKCOUNT EQU   *                                                 S22024
         SLL   R15,1                    SHIFT TO NEXT BIT        S22024
         BCT   R14,SHIFT                HAVE ALL BITS BEEN CHKD  S22024
         LTR   R0,R0                    ANY DEV DEP FIELDS       S22024
         BCR   ZERO,RRET10              BRANCH ON NO             S22024
         SPACE
BUMP     EQU   *
         IC    R15,AVTEZERO(,RPTR2)     GET LENGTH OF FIELD
         LA    RPTR2,ONE(R15,RPTR2)     BUMP PAST FIELD
         BCT   R0,BUMP                  LOOP TILL ALL FIELDS PASSED
         SPACE
         BR    RRET10                   RETURN TO (LOCAL) CALLER
         SPACE 2
HONE     DC    H'1'                     HEX ONE                  S22024
         EJECT
********* DSECTS *********
         SPACE
         TAVTD
         EJECT
         DCBD  DSORG=TX
         EJECT
DCT      DSECT                          DEVICE CHAR TBL DSECT    X03039
         DS    XL1                      RESERVED                 X03039
DCTSTAT1 DS    XL1                      FIRST STATUS BYTE        X03039
DCT3270  EQU   X'04'                    3270 FLAG                X03039
DCTSTAT2 DS    XL1                      SECOND STATUS BYTE       X03039
DCTLOCAL EQU   X'02'                    LOCAL FLAG               X03039
         EJECT
         TLCBD
         EJECT
         TPRFD
         EJECT
         TSCBD
         EJECT
         TTRMD
         SPACE
         END
