BP01     TITLE '''IEDQBP'' - TGOTO ROUTINE'
IEDQBP   CSECT
*A138600-140400,177600-179400,526000-527000                      S22029
*A530000-532000,700000-701000                                    S22029
*C648000,696000,702000,855000,879000                             S22029
*D531000,918000                                                  S22029
***********************************************************************
*                                                                     *
* STATUS - CHANGE LEVEL 0                                             *
*                                                                     *
* FUNCTION -                                                          *
*    TO PASS A BUFFER TO A SECOND MH FOR HANDLING WHEN LMD OR MID     *
*    BATCH IS USED ON CONCENTRATOR-ATTACHED TERMINALS.  BEFORE        *
*    ACTIVATING THE SECOND MH, LCBSCBA IN THE LCB MUST BE CHANGED     *
*    TO POINT TO THE LMD SCB NEEDED IN HANDLING THE SECOND LEVEL      *
*    OF MESSAGE PROCESSING.  ALSO, ALL OF THE APPROPRIATE FIELDS      *
*    FROM THE LINE SCB MUST BE FILLED INTO THE LMD SCB.               *
*                                                                     *
*    A BRANCH TO DSPBYPAS IS USED TO ACTIVATE THE DESIGNATED          *
*    STARTMH STCB.                                                    *
*                                                                     *
* ENTRY POINTS -                                                      *
*         'IEDQBP01' TO PASS A BUFFER TO A SECONDARY MH FOR A         *
*         SECOND LEVEL OF MESSAGE PROCESSING.                         *
*         ENTRY IS INITIATED BY THE TGOTO MACRO THROUGH IEDQUI.       *
*         THE CALLING SEQUENCE IN IEDQUI IS:                          *
*                                                                     *
*         L     R12,AVTMSGS-1            GET ADDR OF VCON TABLE       *
*         IC    R15,AVTEZERO(,R1)        GET INDEX TO ROUTINE ADDR    *
*         LA    R0,ANDLOW                SET UP MASK                  *
*         NR    R15,R0                   CLEAR INDEX FLAGS            *
*         L     R12,AVTEZERO(R12,R15)    GET ROUTINE ADDR             *
*         BR    R12                      EXIT TO ROUTINE              *
*                                                                     *
* INPUT -                                                             *
*    REGISTER 1 - ADDRESS OF A MACRO-GENERATED PARAMETER              *
*    LIST. PARAMETER CAN HAVE ONE OF TWO FORMATS.                     *
*                                                                     *
*    IF MH=C'NAME OF MH' IS SPECIFIED ON TGOTO MACRO,                 *
*    (STAT FLAG = X'00')                                              *
*                                                                     *
*    *********************************                                *
*    * INDEX * PARAM *       *  RE-  *                                *
*    *  TO   * LIST  * STAT  *SERVED *                                *
*    *  BP   * LNGTH *       *       *                                *
*    *********************************                                *
*    *          ADDR OF MH           *                                *
*    *       (OF STARTMH QCB)        *                                *
*    *                               *                                *
*    *********************************                                *
*    *       ADDR OF IEDAYR IF       *                           S22029
*    *       MIXED ENVIRONMENT       *                           S22029
*    *      ELSE ADDR OF IEDQAA      *                           S22029
*    *********************************                           S22029
*                                                                     *
*    IF MH=OPFIELD IS SPECIFIED ON TGOTO MACRO.                       *
*    (STAT FLAG = X'80')                                              *
*                                                                     *
*    *********************************                                *
*    * INDEX * PARAM *       *  RE-  *                                *
*    *  TO   * LIST  * STAT  *SERVED *                                *
*    *  BP   * LNGTH *       *       *                                *
*    *********************************                                *
*    * INDEX * PARAM *  OPT  *  RET  *                                *
*    *  TO   * LIST  *  FLD  *  REG  *                                *
*    *  AE   * LNGTH * OFFST * OFFST *                                *
*    *********************************                                *
*    *       ADDR OF IEDAYR IF       *                           S22029
*    *       MIXED ENVIRONMENT       *                           S22029
*    *      ELSE ADDR OF IEDQAA      *                           S22029
*    *********************************                           S22029
*                                                                     *
*    REGISTER 3 - ADDRESS OF THE SCB.                                 *
*                                                                     *
*    REGISTER 4 - ADDRESS OF THE LCB.                                 *
*                                                                     *
*    REGISTER 6 - ADDRESS OF THE BUFFER.                              *
*                                                                     *
*    REGISTER 9 - ADDRESS OF THE AVT.                                 *
*                                                                     *
*    REGISTER 12 - ENTRY POINT ADDRESS AND BASE REGISTER.             *
*                                                                     *
*    REGISTER 14 - RETURN REGISTER.                                   *
*                                                                     *
*    REGISTER 15 - LINK REGISTER AND RETURN CODE REGISTER.            *
*    AND OPFIELD ADDRESS UPON RETURN FROM AE.                         *
*                                                                     *
* OUTPUT -                                                            *
*    ON EXIT TO DSPBYPAS-                                             *
*                                                                     *
*    REGISTER 1 - ADDRESS OF THE BUFFER.                              *
*                                                                     *
*    REGISTER 3 - ADDRESS OF DESIGNATED STARTMH STCB.                 *
*                                                                     *
*    REGISTER 7 - ADDRESS OF DESIGNATED STARTMH QCB.                  *
*                                                                     *
*    REGISTER 11 - ADDRESS OF THE DISPATCHER.                         *
*                                                                     *
*    REGISTER 13 - ADDRESS OF AVTSAVE2.                               *
*                                                                     *
* EXTERNAL REFERENCES -                                               *
*         DSPBYPAS - TO ACTIVATE STARTMH STCB.                        *
*                                                                     *
*         'IEQDUI' - USER INTERFACE TO LOCATE AN OPTION FIELD VIA     *
*         IEDQAE, AND TO RETURN BACK TO THE FIRST MH.                 *
*                                                                     *
*         TERMINAL NAME TABLE CODE (AVTRNMPT).                        *
*                                                                     *
* EXITS,  NORMAL -                                                    *
*         BRANCH TO DSPBYPAS FOR ALL BUFFERS OF MESSAGES ENTERED BY   *
*         CONCENTRATOR-ATTACHED TERMINALS SPECIFYING EITHER LMD=YES   *
*         OR MB=YES ON THEIR TERMINAL MACROS.                         *
*                                                                     *
*         RETURN TO FIRST MH THROUGH USER INTERFACE WITH REGISTER 15  *
*         CONTAINING RETURN CODE OF:                                  *
*                                                                     *
*         X'00' FOR ZERO LENGTH BUFFERS.                              *
*                                                                     *
*         X'04' FOR ALL BUFFERS OF MESSAGES ENTERED BY TERMINALS NOT  *
*         ATTACHED TO A CONCENTRATOR, OR IF CONCENTRATOR-ATTACHED,    *
*         NOT SPECIFYING LMD=YES OR MB=YES ON THEIR TERMINAL MACROS.  *
*                                                                     *
* EXITS,  ERROR -                                                     *
*         RETURN TO FIRST MH THROUGH USER INTERFACE WITH REGISTER 15  *
*         CONTAINING RETURN CODE OF:                                  *
*                                                                     *
*         X'08' IF AN OPTION FIELD TO CONTAIN THE SECOND MH ADDRESS   *
*         FOR THIS TERMINAL HAS NOT BEEN INITIALIZED FOR THIS         *
*         TERMINAL.                                                   *
*                                                                     *
*         X'0C' IF THE ADDR SPECIFIED EITHER IN THE MACRO OR IN AN    *
*         OPTION FIELD IS NOT A VALID MH ADDRESS.                     *
*                                                                     *
* TABLES/WORK AREAS -                                                 *
*                                                                     *
*    AVT                                                              *
*    LCB                                                              *
*    SCB                                                              *
*    LMD SCB                                                          *
*    TERMINAL TABLE                                                   *
*    BUFFER PREFIX                                                    *
*                                                                     *
* ATTRIBUTES -                                                        *
*    SERIALLY REUSABLE, REFRESHABLE, ENABLED, RESIDENT,               *
*    PROBLEM PROGRAM MODE.                                            *
*                                                                     *
* CHARACTER CODE DEPENDENCY -                                         *
*    THE OPERATION OF THIS MODULE DOES NOT DEPEND UPON AN INTERNAL    *
*    REPRESENTATION OF THE EXTERNAL CHARACTER SET.                    *
*                                                                     *
* NOTES - NONE                                                        *
*                                                                     *
***********************************************************************
         EJECT
********* REGISTER EQUATES *********
         SPACE
R0       EQU   0                        WORK REGISTER
R1       EQU   1                        PARAMETER LIST ADDRESS
RDDF     EQU   2                        DEVICE DEPND'T FLAGS
RSCB     EQU   3                        SCB ADDRESS
RSTCB    EQU   3                        AA'S STCB ADDR FOR DSP
RLCB     EQU   4                        LCB ADDRESS
RPARM    EQU   5                        PARM ADDR SAVE REG
RPRF     EQU   6                        BUFFER ADDR
RQCB     EQU   7                        STARTMH QCB ADDR
RWK8     EQU   8                        WORK REGISTER
RAVT     EQU   9                        AVT ADDRESS
RWKA     EQU   10                       WORK REGISTER
RWKB     EQU   11                       WORK REGISTER
RBAS     EQU   12                       ROUTINE BASE REGISTER
R13      EQU   13                       AVT SAVEAREA REGISTER
R14      EQU   14                       RETURN ADDRESS
R15      EQU   15                       LINK AND RET CODE REG
         SPACE 3
********* OTHER EQUATES *********
         SPACE
ONE      EQU   1                        DISPL AND LENGTH
TWO      EQU   2                        LENGTH FOR MVC
PARMSTAT EQU   2                        STATUS OFFSET IN PARM
KEYOFF   EQU   2                        OFFSET TO LMD SCB KEY IN
*                                       DEVICE DEPENDENT FIELDS.
THREE    EQU   3                        DISPLACEMENT OF THREE
QCBADDR  EQU   4                        QCB ADDR OFFSET IN PARM
AELIST   EQU   4                        OFFSET TO AE'S PARMLIST
FOUR     EQU   4                        FOR LNG AND RETURN TO UI
NINE     EQU   9                        CNTR FOR LOOP THRU
*                                       DEV DEP FIELDS.
MHCODE   EQU   9                        OFFSET IN PARMLIST TO    S22029
*                                       ADDR OF AA OR AYR        S22029
TWELVE   EQU   12                       DISPL TO STCB
STMHCODE EQU   13                       OFFSET IN STARTMH QCB TO S22029
*                                       ADDR OF AYR OR AA CODE   S22029
SHIFT15  EQU   15                       TO SHIFT OUT HI HALF OF REG
OPFLD    EQU   X'80'                    QCB ADDR IN OPFIELD
CODEOFF  EQU   X'FE'                    TO TURN OFF CODE BIT
         EJECT
         USING IEDQSCB,RSCB             ADDRESSABILITY FOR SCB
         USING IEDQLCB,RLCB             ADDRESSABILITY FOR LCB
         USING IEDQPRF,RPRF             ADDRESSABILITY FOR PREFIX
         USING IEDQTRM,RWK8             ADDRESSABILITY FOR TRM ENT
         USING IEDQAVTD,RAVT            ADDRESSABILITY FOR AVT
         USING IEDQBP01,RBAS            ADDRESSABILITY FOR ROUTINE
         SPACE
IEDQBP01 EQU   *
IEDQBP   IEDHJN  SKIPID
         LH    R15,PRFSIZE              GET PRFSIZE
         LTR   R15,R15                  ZERO LENGTH BUFFER?
         BZ    RETURN                   YES RETURN
         SPACE
         LR    RPARM,R1                 SAVE PARM LIST ADDR
         LH    R1,LCBTTCIN              GET TRM ENTRY KEY
         LTR   R1,R1                    DIAL LINE WITH ZERO TTCIN
         BZ    RETURN4                  YES, RETURN TO 1ST MH
         SPACE
         N     R1,AVTCLRHI              CLEAR HI ORDER HALF
         L     R15,AVTRNMPT             GET TNT CODE
         BALR  R14,R15                  GO GET TRM ENT ADDR
         SPACE
         LR    RWK8,R1                  SAVE TRM ENTRY ADDR
*                                       ATTACHED TO CONC AND
*                                       LMD OR MB SPECIFIED?
         TM    TRMDEVFL+1,TRMCONC+TRMLMD
         BNO   RETURN4                  NO, GO RETURN TO 1ST MH
         SPACE
         L     RQCB,QCBADDR(,RPARM)     ASSUME QCB ADDR IN PARMLST
         TM    PARMSTAT(RPARM),OPFLD    QCB ADDR IN OPFIELD?
         BZ    MHTEST                   NO, REG ALREADY SET      S22029
*                                       GO INSURE IT IS AN MH.
*                                       YES, FALL THRU.
         SPACE
LOCOPT   EQU   *
         LA    R1,AELIST(,RPARM)        POINT TO AE'S PARMLIST
         L     R15,AVTUI                GET ADDR OF UI
         BALR  R14,R15                  GO LOCATE OPTION FIELD
         SPACE
         LTR   R15,R15                  OPFIELD FOUND?
         BZ    BADRET8                  NO, GO SET 08 RET CODE
         SPACE
*                                       YES, GET MH QCB ADDR
         MVC   AVTPARM+1(THREE),ONE(R15)
         L     RQCB,AVTPARM             FROM OPFIELD.
         SPACE
MHTEST   EQU   *                                                 S22029
         LA    R15,AVTECD12             ASSUME NOT AN MH.
*                                       IS SECOND STCB PTR AA'S, S22029
*                                       OR AYR'S IF MIXED ENVIRONS22029
         CLC   STMHCODE(THREE,RQCB),MHCODE(RPARM)                S22029
         BNE   RETURN                   NO, NOT AN MH, RETURN.
         SPACE
GETSCB   EQU   *
         SR    RWKB,RWKB                CLEAR WORK REGISTER
         LA    R1,TRMOPNO               PTR TO DEV FLD IF NO OPFLD
         TM    TRMSTATE,TRMOPTFN        OPTION FIELDS USED?
         BZ    NOOPT                    NO GO FIND DEV DEP FIELD
         SPACE
         IC    RWKB,TRMOPNO             GET NO. OF OPTION ENTRIES
         LA    R1,THREE(RWKB,R1)        BUMP DEV DEP FLD PTR
*                                       PAST OPTION OFFSETS.
NOOPT    EQU   *
         LH    RDDF,TRMDEVFL            GET DEV DEP FLD FLAGS
         SLL   RDDF,SHIFT15             SHIFT TO HI HALF OF REG
         LA    RWK8,NINE                SET CTR TO END AT THE
*                                       LMD SCB OFFSET FIELD.
LOOP     EQU   *
         SLL   RDDF,ONE                 SHIFT TO TEST FOR
*                                       PRESENCE OF FIELD.
         LTR   RDDF,RDDF                FIELD PRESENT?
         BNM   BCTIT                    NO, DON'T BUMP PTR.
         SPACE
         IC    RWKB,AVTEZERO(,R1)       GET LNG OF THIS FIELD
         LA    R1,ONE(RWKB,R1)          BUMP PTR TO NEXT FIELD
         SPACE
BCTIT    EQU   *
         BCT   RWK8,LOOP                REACHED LMD SCB OFFSET
*                                       FIELD? IF NOT, GO BACK.
         IC    RWKB,KEYOFF(,R1)         GET LMD SCB KEY
         IC    RWK8,AVTSCBSZ            GET SCB SIZE
         MR    RWKA,RWK8                MULT FOR OFFSET
         A     RWKB,LCBSCBDA-1          OFFSET+BGN=PTR TO SCB
         IC    R15,LCBMSGFM             SAVE FLAG BYTE
         ST    RWKB,LCBSCBA-1           SET LMD SCB PTR
         STC   R15,LCBMSGFM             RESTORE FLAG BYTE
*                                       SET STATE IN LMD SCB
         OC    SCBSTATE-IEDQSCB(ONE,RWKB),SCBSTATE
*                                       SET ERRST IN LMD SCB
         OC    SCBERRST-IEDQSCB(FOUR,RWKB),SCBERRST
*                                       LAST EOB BUFFER
         CLC   SCBDEOB+1(THREE),AVTADBUF+1
         BNE   TESTCODE                 NO, DON'T UPDATE
         SPACE
*                                       SET DEOB IN LMD SCB
         MVC   SCBDEOB-IEDQSCB(FOUR,RWKB),SCBDEOB
*                                       SET EOB IN LMD SCB
         MVC   SCBEOB-IEDQSCB(TWO,RWKB),SCBEOB
TESTCODE EQU   *
         TM    SCBSTATE,SCBCODE         CODE MAC EXEC'D ALREADY?
         BZ    CONTINU                  NO, BRANCH AROUND
         SPACE
*                                       TURN OFF BIT IN LMD SCB
         NI    SCBSTATE-IEDQSCB(RWKB),CODEOFF
         SPACE
CONTINU  EQU   *
         L     RSTCB,TWELVE(,RQCB)      PASS ADDR OF STARTMH     S22029
*                                       STCB TO DISPATCHER.
         MVI   PRFPRI,PRIMHBFR          SET PRIORITY IN BFR
         LR    R1,RPRF                  ADDR OF BUFFER TO DSP
         LA    R13,AVTSAVE2             SET R13 TO AVTSAVE2
         SPACE
         USING IEDQDISP,RWKB            ADDRESSABILITY FOR DSP
         SPACE
         L     RWKB,AVTEA               GET ADDR OF DISPATCHER
         B     DSPBYPAS                 BYPASS TO STARTMH        S22029
         SPACE
BADRET8  EQU   *
         LA    R15,AVTECD8              08 RETURN CODE
         B     RETURN                   GO RETURN TO CALLER
         SPACE
RETURN4  EQU   *
         LA    R15,AVTECD4              04 RETURN CODE
         SPACE
RETURN   EQU   *
         L     RBAS,AVTUI               GET ADDR OF UI
         B     FOUR(RBAS)               RETURN TO CALLER
         SPACE
         EJECT
         TAVTD
         EJECT
         TDISPD
         EJECT
         TLCBD
         EJECT
         TPRFD
         EJECT
         TPRIOR
         EJECT
         TSCBD
         EJECT
         TTRMD
         END
