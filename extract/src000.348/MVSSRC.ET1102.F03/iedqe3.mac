E301     TITLE '''IEDQE3'' - TCAM TCHNG SERVICE ROUTINE'
IEDQE3   CSECT
         SPACE 3
*  CHANGE ACTIVITY AS FOLLOWS
*********************MICROFICHE FLAGS*************************SUPT CODE
*C THIS MODULE HAS BEEN REWRITTEN FOR TCAM LEVEL 6               Y02027
*              THIS MODULE HAS BEEN REWRITTEN FOR FETCH PROTECT  Y01004
*C020000,297000-318000,537000,600000-660000,952900-982900        Y05330
*C478000,981000                                                 SA61775
         SPACE 4
***********************************************************************
*                                                                     *
* TITLE:  'IEDQE3' TCHNG SERVICE ROUTINE                              *
*                                                                     *
* STATUS: CHANGE LEVEL 6                                         Y02027
*                                                                     *
*  MODULE NAME = IEDQE3                                               *
*                                                                     *
*  DESCRIPTIVE NAME = TCHNG SERVICE ROUTINE                           *
*                                                                     *
*  COPYRIGHT = 'NONE'                                                 *
*                                                                     *
*                                                                     *
* FUNCTION: THIS MODULE BUILDS AND INITIALIZES A PARAMETER LIST FOR   *
*    SVC 102 (IEDQEB). THE ACTUAL PROCESSING FOR TCHNG IS DONE IN THE *
*    MCP BY IGC0A10D WHO GETS CONTROL VIA SVC 102'S TPOSTING THE OP   *
*    CONTROL QCB.                                                Y02027
* ENTRY POINT:                                                        *
*         IEDQE301                                                    *
* INPUT:  R1 -  THREE WORD PARAMETER LIST                             *
*         WORD 1 - ADDRESS OF TERMNAME                                *
*         WORD 2 - ADDRESS OF USER'S WORKAREA                         *
*         WORD 3 - ADDRESS OF PASSWORD (0 IF NO PASSWORD)             *
*         R13- SAVEAREA                                               *
*         R14- RETURN                                                 *
*         R15  ENTRY POINT                                            *
* OUTPUT:                                                             *
*         R15  COMPLETION CODE                                        *
* EXTERNAL ROUTINES:                                                  *
*         IGC0010D                                                    *
* EXIT:   COMPLETION CODE                                             *
*         0    NORMAL                                                 *
*         4    TERMNAME INVALID                                       *
*        0C    CALLER NOT A VALID TCAM APPLICATION PROGRAM            *
*        14    INVALID PASSWORD                                       *
*        18    AIB TOO SMALL TO HOLD DATA                             *
* TABLES/WORK AREAS:                                                  *
*         NONE                                                        *
* ATTRIBUTES:                                                         *
*         SERIALLY REUSABLE, PROBLEM PROGRAM MODE                     *
* NOTES:  THE OPERATION OF THIS MODULE DEPENDS UPON AN INTERNAL       *
*   REPRESENTATION OF THE EXTERNAL CHARACTER SET WHICH IS EQUIVALENT  *
*   TO THE ONE USED AT ASSEMBLY TIME.  THE CODING HAS BEEN ARRANGED   *
*   SO THAT REDEFINITION OF 'CHARACTER' CONSTANTS, BY REASSEMBLY,     *
*   WILL RESULT IN A CORRECT MODULE FOR THE NEW DEFINITIONS.          *
*                                                                     *
***********************************************************************
         SPACE 4
RZERO    EQU   0                        GENERAL WORK REGISTER
RPARM    EQU   1                        PARAMETER REGISTER
RWORK    EQU   2                        WORKAREA ADDRESS
RBASE    EQU   12                       CSECT BASE REGISTER
RSAVE    EQU   13                       SAVEAREA REGISTER
RETURN   EQU   14                       RETURN ADDRESS
RENTRY   EQU   15                       ENTRY POINT/RETURN CODE
         SPACE 2
IEDQE301 EQU   *                                                 S22025
         STM   RETURN,RBASE,12(RSAVE)   SAVE CALLERS REGISTERS   S22025
         BALR  RBASE,0                  ESTABLISH
         USING *,RBASE                           ADDRESSABILITY
IEDQE3   IEDHJN ENTER                                            S22025
         EJECT
         ST    RSAVE,SAVEAREA+FOUR      ADDRESS OF USER SAVE AREAY01004
         LA    RSAVE,SAVEAREA           ADDRESS OF MYSAVE        Y01004
         SPACE 2                                                 Y02027
**************************************************************** Y02027
*              BUILD PARAMETER LIST INPUT TO SVC 102           * Y02027
**************************************************************** Y02027
         SPACE 2                                                 Y02027
         MVC   APPASWD(FOUR),EIGHT(RPARM)    SET PASSWORD PTR
         MVC   APUSWKA(THREE),FIVE(RPARM)    SET USER WORKAR PTR
         MVC   APNAME(FOUR),ZERO(RPARM) SET TERM NAME PTR        Y02027
         MVI   APCOMCD,TCHNGCD          TURN ON TCHNG FLAG       Y02027
         SR    RZERO,RZERO              INDICATE TCHNG FUNCTION  Y02027
         LA    RPARM,APPARM             SET PARM REG FOR SVC 102 Y02027
         AQCTL                          ISSUE SVC                Y02027
         SPACE 2
         LA    RWORK,RC80               IF TCAM NOT UP, RET CD=80
         CR    RENTRY,RWORK             IS TCAM ACTIVE
         BNE   EXIT                     BR IF YES
         LA    RENTRY,NOTCAM            MPP EXPECTS RET CD = 08
         SPACE 1
EXIT     EQU   *
         L     RSAVE,SAVEAREA+FOUR      RESTORE CALLER'S SAVE    Y02027
*                                       AREA POINTER             Y02027
         RETURN (14,12),T,RC=(15)       RETURN TO USER
         EJECT
**************************************************************** Y02027
*        C O N S T A N T S   A N D   E Q U A T E S             * Y02027
**************************************************************** Y02027
         SPACE 2
SAVEAREA DC    18F'0'                   MYSAVE AREA              Y02027
NOTCAM   EQU   8                        TCAM NOT ACTIVE CODE     Y02027
ZERO     EQU   0                        OFFSET                   Y02027
FOUR     EQU   4                        OFFSET & MOVE LENGTH     Y02027
THREE    EQU   3                        MOVE LENGTH              Y02027
FIVE     EQU   5                        OFFSET                   Y02027
EIGHT    EQU   8                        OFFSET                   Y02027
RC80     EQU   X'80'                    CODE RETURNED BY SVC 102 Y02027
*                                       IF TCAM NOT ACTIVE       Y02027
TCHNGCD  EQU   X'20'                    TCHNG CODE               Y02027
         SPACE 2
**************************************************************** Y02027
*              MAP OF INPUT DATA PROVIDED TO SVC 102           * Y02027
**************************************************************** Y02027
         DS    0F                       FORCE ALIGNMENT          Y02027
APCIBMAP EQU   *                                                 Y02027
APPASWD  DC    AL4(0)                   POINTER TO PASSWORD      Y02027
APECB    DC    F'0'                     ECB FOR WAIT IN SVC 102  Y02027
*                                       AND SUBSEQUENT XMPOST    Y02027
APPCBAD  DC    AL4(0)                   PCB POINTER              Y02027
APCOMCD  DC    XL1'0'                   COMMAND VERB CODE        Y02027
APUSWKA  DC    AL3(0)                   PTR TO USER WORK AREA    Y02027
APNAME   DC    AL4(0)                   PTR TO TERMINAL NAME     Y02027
APPARM   DS    0F                       PARAMETER FOR SVC 102    Y02027
         DC    X'84'                    EOL X'80' + FUNCTION     Y02027
*                                       CODE X'04'               Y02027
         DC    AL3(APCIBMAP)            PTR TO INPUT DATA        Y02027
         SPACE 1
**************************************************************** Y02027
         EJECT
         SPACE 2                                                 Y02027
         END
