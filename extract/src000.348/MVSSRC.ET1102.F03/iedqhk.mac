HK01     TITLE '''IEDQHK'' - RESIDENT STOPLINE I/O HANDLER'
IEDQHK   CSECT
*A-000000-999999                                               @X31X8R0
         SPACE 3
*CHANGE-ACTIVITY = AS FOLLOWS:                                   S21903
******************** MICROFICHE FLAGS *********************** SUPT CODE
*C014000                                                       @Z30X8RB
*A246200,247300,569500-570300                                    S99228
*D569500-570505                                                  S99228
*A530000                                                         A47145
*C667000                                                         A47145
*A667000                                                         CLUP21
*C322000,326000,354200-357200,363000,374000-388000,398000-400000 CLUP21
*C430000-437000,456000,456800,462100,463200,480000,483998        CLUP21
*C502600-503200,527000-528000,536000-538000,542000-544000        CLUP21
*C550000-552000,568500-573000,578000-580000,584000-586000        CLUP21
*C592000-594000,598000-600000,604000,609000-612000,616000-618000 CLUP21
*C623000,634000-636000,642600,644000,653000,658000,660000        CLUP21
*D247300,416000,674000                                           CLUP21
*A480000                                                          M6561
*A021000,112400-113600,139000,172500-173500,293100-293400        S22025
*A326600-327200,641000,643600,658400-659600,671000               S22025
*A202000,212000,216000,292000,296000,570500,676000               S21101
*A266000,411000,570570                                           S22025
*C481960,667000                                                   M6561
*C020000,116000-118000,308000,310000-312000,396000,402200-404000 S22025
*C412200,456600,458600,459400,524000-546000,556000,558000-566000 S22025
*C570740-570755,570785,570800,658000,661000-662000               S22025
*C294000,308000,667000                                           S21101
*C308000,524000-546000,558000-566000,570740-570755,570785,5708000S22025
*D480500-481920                                                   M6561
*D204000-206000,571000-574000,672000-674000                      S21101
*D488000-512000,568500-569000                                    S22025
*A540000                                                          M6684
*A292800,642600,643200,659600,671000                            SA54930
*D412600-412800                                                 SA54929
*A004000                                                         S21903
*C010000,210000-212000,216000,224000,238000-244000,              S21903
*C247000-247200,247500-251500,256500-266000                      S21903
*A582100-583500                                                  Y05331
*A437000                                                        SA66918
*A656000                                                        SA67759
* A416000                                                      @SA70195
*A233000                                                       @SA70889
*C643300,643400                                                @SA70889
*A282000,482998,483986                                         @SA71514
*A372000,378000                                                @ZA02071
*C656900                                                       @ZA02055
*A295500                                                       @YA07691
*C376000                                                       @YA07691
*A474000                                                       @ZA05018
*A280000,643400,659900                                         @YA10232
*A474000                                                       @SA75467
*D480000                                                       @SA75467
*A563000,C564000                                               @OY11167
*A246000,404000,410000,689000                                  @OY13669
*C282000,348000,364000,410050                                  @OY14491
*C456000-470000                                                @OX14763
*A293400,671000,443000                                         @OX14763
*A328300                                                       @OY15473
*A215000,219000,220600-221200,295511-295518,312600-313200      @OY14092
*A323000,416030-416630,493998,671001-689060,410350             @OY14092
*C310000,370000-380200,474000-483598,659960-660950,410500      @OY14092
*D483986-483990                                                @OY14092
*A416540                                                       @OY16355
*C416030-416450                                                @OY16355
*D474300,474600                                                @OY16355
*A660270,660400,660420                                         @OY18823
*C660010-660020                                                @OY18823
*A283200,437000,671000                                         @OZ24311
*A479400,480000                                                @OY19040
*C660120,416540-416550                                         @OY19040
*D365000                                                       @OY19040
* DUMMY APAR                                                   @OZ27328
*A479400                                                       @OZ30095
*A399000                                                       @OZ28736
*C411000,437000                                                @OZ29091
*C416150                                                       @OZ28940
*A410350,416000,410680                                         @OY20036
         SPACE 3
***********************************************************************
*                                                                     *
*MODULE-NAME = IEDQHK                                            S21903
*                                                                S21903
*DESCRIPTIVE-NAME = RESIDENT STOPLINE I/O HANDLER                S21903
*                                                                S21903
*COPYRIGHTS = 'NONE'                                             S21903
*
*STATUS -- CHANGE LEVEL 8                                      @Z30X8RB
*                                                                     *
*FUNCTION -- THIS MODULE PROVIDES THE I/O HANDLING REQUIRED TO        *
*   EFFECT A STOPLINE FUNCTION AND TO DETERMINE IF A TRUE ATTEN- S22025
*   TION SIGNAL HAS BEEN RECEIVED FROM A 2741 OR 1050 TERMINAL.  S22025
*                                                                     *
*   WHEN A VARY OFFTP (C OR I), HALT, OR ICHANGE COMMAND IS           *
*   RECEIVED BY OPERATOR CONTROL, A REQUEST FOR STOPLINE IS POSTED    *
*   TO THIS MODULE.  IF THE REQUEST IS FOR AN I-TYPE VARY, THE LINE   *
*   IS TESTED TO SEE IF IT IS ALREADY FREE.  IF SO, LCBSTAT1 IS SET   *
*   TO ZERO TO INDICATE LINE STOPPED AND THE LCB IS POSTED TO THE     *
*   OPERATOR CONTROL QUEUE.  IF IT IS NOT ALREADY FREE, AN IOHALT     *
*   (SVC 33) IS ISSUED, LCBOCNI IS SET ON IN LCBSTAT1, THE ADDRESS    *
*   OF THE STOPLINE REQUEST ELEMENT IS SAVED IN LCBEOLTD, AND THE     *
*   ROUTINE EXITS TO THE DISPATCHER.                                  *
*                                                                     *
*   IF THE REQUEST IS FOR A C-TYPE VARY (ICHANGE IS ALWAYS A C-TYPE   *
*   VARY) THE LCB IS REMOVED FROM THE TIME DELAY QUEUE IF IT IS ON.   *
*   IF THE LINE IS DIAL AND LCBTSTSW IS NOT ON, A TEST-AND-SET IS     *
*   DONE AND THE ZERO BRANCH TAKEN TO HALT I/O CODE AS ABOVE.  THE    *
*   NON-ZERO BRANCH SKIPS THE IOHALT BUT SETS THE LCB FIELDS AND      *
*   TAKES THE EXIT AS ABOVE.  IF THE TEST-AND-SET SWITCH IS NOT ON,   *
*   OR IF THIS IS NOT A DIAL LINE, THE LINE IS TESTED TO SEE IF IT    *
*   IS ALREADY FREE.  IF SO, THE EXIT IS THE SAME AS THE I-TYPE       *
*   EXIT WHEN ALREADY FREE.                                           *
*                                                                     *
*   IF THE LINE IS AUTOPOLL, THE CHANNEL PROGRAM IS SET TO A NOP      *
*   AND THE BIT-SETTING CODE IS EXECUTED (AS ABOVE, AFTER IOHALT).    *
*   IF THE LINE IS BI-SYNC AND THERE IS NO PREPARE ON THE LINE,       *
*   LCB FIELDS ARE SET AS ABOVE AND THE ROUTINE EXITS TO THE          *
*   DISPATCHER.  IF A PREPARE IS UP, THE TEST-AND-SET IS DONE AS      *
*   FOR DIAL LINES WHEN THE TEST-AND-SET BIT IS NOT ON.  IF THE       *
*   LINE IS NOT BUSY, LCB FIELDS ARE SET AND EXIT TAKEN AS ABOVE.     *
*   IF THE LINE IS RECEIVING, THE ACTION IS THE SAME AS FOR NOT       *
*   BUSY.  OTHERWISE AN IOHALT IS ISSUED AND THE SAME EXIT CODE IS    *
*   EXECUTED.  THIS LOGIC IS REPEATED FOR EACH LCB TO BE STOPPED.     *
*                                                                     *
*   THE LCBS WILL BE RETURNED TO THIS SUBTASK WHEN THE SCHEDULERS     *
*   RECOGNIZE THE NON-IMMEDIATE BIT (LCBOCNI).  FOR NON-DIAL LINES,   *
*   LCBSTAT1 IS SET TO ZERO TO INDICATE LINE STOPPED AND CONTROL      *
*   EXITS TO THE DISPATCHER WITH A REQUEST TO POST THE LCB TO THE     *
*   OPERATOR CONTROL QUEUE.  IF THE LCB IS A DIAL LCB AND EXCP HAS    *
*   ALREADY BEEN DONE, THE EXCP FLAG IS TURNED OFF AND EXIT IS AS     *
*   FOR NON-DIAL LINES.                                               *
*                                                                     *
*   FOR A DIAL LINE ON WHICH EXCPVR HAS NOT BEEN DONE, THE EXCPVR     *
*   FLAG IS TURNED ON, A DISABLE IS SET IN THE CHANNEL PROGRAM AREA,  *
*   LCBTTCIN IS CLEARED, THE TEST-AND-SET SWITCH IS TURNED ON,        *
*   NEGATIVE RESPONSE TO POLLING IS SET, AND EXCPVR (SVC 114) IS      *
*   ISSUED.  THE ROUTINE GIVES UP CONTROL TO THE DISPATCHER.          *
*                                                                     *
*   WHEN AN LCB IS TPOSTED BY LINE END APPENDAGE, A PREPARE/HALT S22025
*   IO COMMAND SEQUENCE IS EXECUTED AND EXIT IS MADE TO THE      S22025
*   DISPATCHER.                                                  S22025
*                                                                     *
*ENTRY POINTS --                                                      *
*   IEDQHK01 - ENTERED THROUGH THE TCAM DISPATCHER.              S22025
*                                                                     *
*INPUT -- REGISTER 1 - FROM OPERATOR CONTROL, A STOPLINE REQUEST      *
*   ELEMENT FIVE WORDS LONG.  FIRST WORD IS ADDRESS OF IEDQHK,        *
*   SECOND HAS X'DC' PRIORITY IN FIRST BYTE, HIGH BYTE OF THIRD       *
*   WORD IS X'00' IF STOP SINGLE LINE, X'80' IF I-TYPE VARY, X'49'    *
*   IF ICHANGE, X'01' IF STOP A LINE GROUP, AND X'10' IF STOP ALL     *
*   LINES FOR CLOSEDOWN (HALT COMMAND).  IF NOT CLOSEDOWN, FOURTH     *
*   WORD IS DEB ADDRESS.  IF NOT CLOSEDOWN, THE FIFTH WORD IS DCB     *
*   ADDRESS FOR STOP LINE GROUP, LCB ADDRESS OTHERWISE.  FROM THE     *
*   SCHEDULERS, THE ADDRESS OF THE LCB.                               *
*   FROM LINE END, REGISTER 1 CONTAINS THE LCB ADDRESS.          S22025
*                                                                     *
*   REGISTER 13 - SAVE AREA ADDRESS.                                  *
*                                                                     *
*   REGISTER 15 - ENTRY POINT ADDRESS.                                *
*                                                                     *
*OUTPUT -- REGISTER 1 - ADDRESS OF ELEMENT TO BE RETURNED TO          *
*   OPERATOR CONTROL.                                                 *
*                                                                     *
*EXTERNAL ROUTINES -- IEDQHG02                                   S22025
*                     IEDQTNT                                    S22025
*                    AOS EXCPVR ROUTINE (SVC 114) - TO START A        *
*   CHANNEL PROGRAM.                                                  *
*                                                                     *
*EXITS-NORMAL -- STOPPED LCB IS POSTED TO OPERATOR CONTROL QCB.       *
*   EXIT IS TO THE TCAM DISPATCHER TO DO THE POST.                    *
*                                                                     *
*   THE LCB IS NOT FREE.  BITS ARE SET TO INDICATE IT IS TO BE        *
*   RETURNED BY THE SCHEDULERS WHEN I/O IS COMPLETE.  EXIT IS TO      *
*   THE DISPATCHER.                                                   *
*                                                                     *
*   FOR ATTENTION SIGNAL CHECKING EXIT IS TO LINE END APPENDAGE  S22025
*   VIA THE TCAM DISPATCHER.                                     S22025
*                                                                     *
*EXITS-ERROR -- N/A.                                                  *
*                                                                     *
*TABLES/WORK AREAS -- DSECTS OF THE TCAM DISPATCHER, THE AVT, LCB,    *
*   CCW, DCB, DEB, AND PRIORITY LEVELS.                               *
*                                                                     *
*ATTRIBUTES -- SERIALLY REUSABLE, REFRESHABLE, ENABLED, RESIDENT,     *
*   PROBLEM PROGRAM MODE.                                             *
*                                                                     *
*NOTES -- THE OPERATION OF THIS MODULE DEPENDS UPON AN INTERNAL       *
*   REPRESENTATION OF THE EXTERNAL CHARACTER SET.  THE CODING HAS     *
*   BEEN ARRANGED SO THAT REDEFINITION OF 'CHARACTER' CONSTANTS, BY   *
*   REASSEMBLY, WILL RESULT IN A CORRECT MODULE FOR THE NEW           *
*   DEFINITIONS.                                                      *
*                                                                     *
***********************************************************************
         EJECT
*        REGISTER EQUATES
R0       EQU   0                        REGISTER EQUATE          S21903
R1       EQU   1                        REGISTER EQUATE          S21903
RQCB     EQU   1                        QCB BASE REGISTER        S21101
RELE     EQU   2
R2       EQU   2                        REGISTER               @OY14092
R3       EQU   3                        REGISTER EQUATE          S21903
RINVLIST EQU   3                        INVITATION LIST REGISTER S21101
RLCB     EQU   4
R4       EQU   4                        REGISTER               @OY14092
RWORK    EQU   5
R5       EQU   5                        REGISTER               @OY14092
RWORK1   EQU   6                        REGISTER               @OY14092
RDEB     EQU   6
R7       EQU   7                        REGISTER EQUATE          S21903
RDCB     EQU   8
ROFF     EQU   9
R9       EQU   9                        REGISTER               @OY14092
RLINK    EQU   9                        REGISTER               @OY14092
RSCB     EQU   ROFF
LOCKI    EQU   X'10'                    LOCK INQUIRY           @SA70889
RINV     EQU   10                       INVITATION LIST ADDR
RDISP    EQU   11
RBASE    EQU   12                       BASE REGISTER            S21903
R13      EQU   13                       REGISTER EQUATE          S21903
R14      EQU   14                       REGISTER EQUATE          S21903
R15      EQU   15                       REGISTER EQUATE          S21903
*        OTHER EQUATES
X1E      EQU   X'1E'                    DEVICE CHECK BYTE      @OY13669
X08      EQU   X'08'                    HEX EIGHT                S99228
X1A      EQU   X'1A'
ZERO     EQU   0                        LENGTH CONSTANT          S21903
ONE      EQU   1                        LENGTH CONSTANT          S21903
TWO      EQU   2                        LENGTH CONSTANT          S21903
THREE    EQU   3                        THREE                    S99228
FOUR     EQU   4                        LENGTH CONSTANT          S21903
FIVE     EQU   5                        LENGTH CONSTANT          S21903
PRI      EQU   4
SEVEN    EQU   7                        LENGTH CONSTANT          S21903
EIGHT    EQU   8                        LENGTH CONSTANT          S21903
TWELVE   EQU   12                       LENGTH CONSTANT          S21903
SIXTEEN  EQU   16                       LENGTH CONSTANT          S21903
CNDZERO  EQU   8                                                 S22025
UCB      EQU   X'20'
HI       EQU   X'80'
BDFLAG   EQU   X'80'                    INMSG COMPLETE FLAG    @YA10232
DIS      EQU   X'01'                    DISABLE MASK           @OY14491
OFFHI    EQU   X'FF'-DIS                                       @OY14491
X40      EQU   X'40'               INTERNAL STOPLN INDICATOR   @SA71514
XBF      EQU   X'BF'               RESET INT. STOPLN MASK      @SA71514
XDF      EQU   X'DF'                    TURN OFF HK PROC BIT   @OZ24311
PRISTOP  EQU   X'E8'                    USED TO RETURN A STOPPED
*                                         LINE TO OP CTL
*                                         USED BY IEDQHK
*                                         TESTED BY OPERATOR CONTROL
ENDILIST EQU   X'FE'                    END OF INVITATION LIST   S21101
REG2     EQU   2
LOCKR    EQU   X'20'                   LOCK RESPONSE FROM PUT S SA54930
XTEN     EQU   X'10'                                             S22025
OPNCHK   EQU   X'AC'                   FLAG THAT A PREPARE IS UP S22025
UNITOFF  EQU   24                       OFFSET TO EXTRA UNIT   @OX14763
STCBOFF  EQU   20                       OFFSET TO STCB IN UNIT @OX14763
OPCDE    EQU   12                       OFFSET TO OCOPTCDE     @OX14763
TERMOFF  EQU   16                       OFFSET TO TERM ENTRY   @OX14763
*                                        ADDRESS               @OX14763
AD       EQU   7                        MASK                   @OX14763
OCICHNGE EQU   X'49'                    ICHNGE COMMAND         @OX14763
SWITCH   EQU   13                       OFFSET TO OCSWITCH     @OX14763
STOPMM   EQU   X'20'                    STOP TERM MID-MSG FLAG @OX14763
         EJECT
OPTCAMLN EQU   X'2F'                    TO TEST FOR OPEN TCAM LINE
RLNDISP  EQU   X'66'                    DISP FROM OPCSAVE2 TO HEX
*                                       RLN IN OPCBITSW+1 FROM CV
CNTOFF   EQU   6                        OFFSET TO COUNT IN CCW @YA07691
ALL      EQU   15                       MASK                   @OY14092
AL3      EQU   3                        LENGTH OF 3            @OY14092
CBREAK   EQU   X'40'                    BREAK FEATURE          @OY14092
T4       EQU   24                       LENGTH OF 24           @OY14092
SCTEOT   EQU   0                        INDEX TO EOT OFFSET    @OY14092
DISCON   EQU   X'01'                    DISCONNECT NEEDED      @OY14092
SCTDLEOT EQU   X'10'                    INDEX TO DLEEOT OFFSET @OY14092
OFF11    EQU   11                       OFFSET 11              @OY14092
         EJECT
HIOCC    EQU   X'48'                    HALT I/O COMPLETION CODE Y02027
         SPACE
         USING IEDQDISP,RDISP
         USING IEDQQCB,RQCB             QCB DSECT BASE           S21101
         USING IEDQLCB,RLCB
         USING IEDQAVTD,R13
         USING IHADCB,RDCB
         USING IEDQSCB,RSCB
         USING DEBNMSUB,RDEB
*
IEDQHK   IEDHJN ,                                                S22025
         DC    AL1(DSPMCPL8)            STCB VTO               @OY14092
         DC    AL3(*-1)                 STCB ADDRESS             S22025
         DC    A(0)                     REQUEST QUEUE          @OY14092
         USING REQELE,RELE              REQUEST ADDRESS        @OY14092
IEDQHK01 EQU   *
         USING IEDQHK01,RBASE
         LR    RBASE,R15                BASE
         LR    RELE,R1                  COPY PARAMETER ADDRESS   CLUP21
         LR    RLCB,RELE                SET LCB ADDRESS        @OY14092
         CLI   PRI(RELE),PRIOPCTL       ELEMENT OR LCB
         BE    SRCHLCBS                 BRANCH IF ELEMENT        CLUP21
         CLI   PRI(RELE),PRILCBAT       REQUEST TO TEST FOR ATTN S22025
         BE    CHKTNT                   YES, BRANCH              S22025
*
         LR    RLCB,RELE                LCB - SET IN LCB REG
         ST    RLCB,LCBQCBA-1           SET TO POST LCB TO SELF@OY15473
         CLI   LCBPRI,X'DD'             REQUEST FROM STARTLINE @OY15473
         BE    EXCP1                    YES, ISSUE EXCP        @OY15473
         ST    R7,LCBQCBA-1             SET TO REPOST LCB TO HK@OY15473
         TM    LCBSTAT2,LCBDIAL         DIAL LINE              @OY13669
         BZ    ICHNG                    GO SET LCB STOPPED     @OY13669
         TM    LCBRSPRI,DIS             IS EXCPVR ALREADY DONE @OY14491
         BNO   EXCP                     NO
*
         L     R7,LCBSTCBA-1            GET FIRST STCB ADDRESS   CLUP21
         CLI   ZERO(R7),DSPSNDSC        IS IT SEND SCHEDULER     CLUP21
         BNE   ICHNG                    BRANCH IF NOT            CLUP21
         MVC   LCBSTCBA(3),FIVE(R7)     REMOVE SEND SCHED STCB   CLUP21
         MVC   FIVE(3,R7),ONE(R7)       SAVE IN DESTINATION QCB  CLUP21
         ST    R7,ZERO(R7)              CHAIN STCB TO ITSELF     CLUP21
         MVI   ZERO(R7),DSPSNDSC        SET TO SEND SCHEDULER    CLUP21
         SH    R7,HALF8                 BACK UP TO QCB           CLUP21
         SR    RWORK,RWORK              ZERO 'CURRENTLY CONNECTEDCLUP21
         STH   RWORK,LCBTTCIN           TERMINAL' INDICATOR      CLUP21
         LR    RWORK,RLCB               SAVE LCB ADDRESS         CLUP21
         L     R15,AVTDISP+4            GET SEND SCHEDULER ADDR  CLUP21
         SH    R15,AVTHA2               BACK UP TO OFFSET        CLUP21
         AH    R15,ZERO(R15)            COMPUTE ENTRY POINT      CLUP21
         BALR  R14,R15                  LINK TO SELECT NEW LINE  CLUP21
         LR    RLCB,RWORK               RESTORE LCB ADDRESS      CLUP21
         B     ICHNG                    CHECK FOR RETURN
*
EXCP     EQU   *
         MVI   LCBTPCD+10,ZERO          ZERO TP CODE FOR TSO     CLUP21
         OI    LCBRSPRI,DIS             TURN ON EXCP INDICATOR @OY14491
         MVC   LCBTTBIN,LCBTTCIN        SAVE CURRENTLY CONNECT @OY14092
         SR    RWORK,RWORK              CLEAR
         STH   RWORK,LCBTTCIN           CLEAR TTCIN
         XC    LCBCPA(T4),LCBCPA        CLEAR CCW AREA         @OY14092
         LA    R1,LCBCPA                POINT TO START         @OY14092
         L     RDCB,LCBDCBPT            GET DCB ADDR           @OY14092
         L     RDCB,DCBSCTAD-1          GET SCT ADDR           @OY14092
         TM    LCBSTAT2,LCBSYNC         BI-SYNC                @OY14092
         BZ    NOEOT                    NO, CONTINUE           @OY14092
         TM    LCBTPCD+OFF11,DISCON     DISCONNECT NEEDED      @OY14092
         BZ    NOEOT                    NO, CONTINUE           @OY14092
         NI    LCBTPCD+OFF11,AVTEFF-DISCON  RESET DISCONNECT   @OY14092
         LA    RWORK,SCTDLEOT           GET DLEEOT INDEX       @OY14092
         IC    RWORK,ZERO(RWORK,RDCB)   GET OFFSET TO COUNT    @OY14092
         LTR   RWORK,RWORK              CHARS SPECIFIED        @OY14092
         BZ    NOEOT                    NO, BRANCH             @OY14092
         LA    RWORK,ZERO(RWORK,RDCB)   GET ADDR OF COUNT      @OY14092
         USING IEDQCCW,R1               ADDRESSABILITY         @OY14092
         MVC   CCWCOUNT+1(ONE),ZERO(RWORK) SET COUNT           @OY14092
         LA    RWORK,ONE(,RWORK)        BUMP TO CHARS          @OY14092
         ST    RWORK,CCWADDR-1          SET DATA ADDR          @OY14092
         MVI   CCWOPCDE,CCWWRITE        SET WRITE COMMAND      @OY14092
         MVI   CCWFLAGS,CCWCC+CCWSLI    SET CHAINING           @OY14092
         LA    R1,EIGHT(,R1)            BUMP CCW POINTER       @OY14092
NOEOT    EQU   *                                               @OY14092
         ST    RLCB,CCWADDR-1           SET ADDR               @OY14092
         MVI   CCWOPCDE,CCWDISAB        DISABLE COMMAND        @OY14092
         MVI   CCWFLAGS,CCWCC+CCWSLI    SET CHAINING           @OY14092
         MVI   CCWCOUNT+1,ONE           SET DISABLE COUNT      @OY14092
         LA    R1,EIGHT(,R1)            BUMP TO NEXT CCW       @OY14092
         ST    RLCB,CCWADDR-1           SET ADDR               @OY14092
         MVI   CCWOPCDE,CCWNOP          SET NOOP COMMAND       @OY14092
         MVI   CCWCOUNT+1,ONE           SET NOOP COUNT         @OY14092
         MVI   LCBTPCD,TWO              SET FIRST TPCODE       @OY14092
         MVC   LCBTPCD+ONE(TWO),LCBTPCD SET OTHER TPCODES      @OY14092
         DROP  R1                                              @OY14092
         USING IEDQQCB,RQCB                                    @OY14092
         SPACE
         L     RWORK,AVTHK              STOPLINE QCB ADDRESS     CLUP21
         ST    RWORK,AVTEZERO(RLCB)     PUT IN QCB FLD OF LCB
         MVI   LCBTSTSW,AVTEFF          TURN ON TEST/SET SWITCH
         OI    LCBSTAT2,LCBNEGRP        SET NEG RESPONSE
EXCP1    LA    RWORK,LCBCPA             GET CHANNEL PROGRAM ADDR S22025
         ST    RWORK,LCBSTART-1         SET CHANNEL PROGRAM      CLUP21
*                                         START ADDRESS          CLUP21
         MVI   LCBINCAM,AVTEZERO        RESET RETRY COUNT      @OZ28736
         LA    R1,LCBFLAG1              IOB FOR IOS              CLUP21
         EXCPVR (1),SUBSYS                                       X01004
         TM    LCBTPCD+11,OPNCHK        RETURN FROM PREPARE ?    S22025
         BNO   DSPDISP                  NO EXIT                  S22025
         LA    R3,DSPDISP               GET RETURN ADDRESS       S22025
         L     RDCB,LCBDCBPT            GET DCB BASE             S22025
         L     RDEB,DCBDEBAD            GET DEB ADDRESS          S22025
         B     HIO1                     GO DO HALT I/O           S22025
*
LOOPUNIT EQU   *                                               @OY13669
         L     RWORK,AVTEZERO(RWORK)    NEXT ELEMENT           @OY13669
         B     CHKLCB                   COMPARE LCB            @OY13669
*                                                              @OY13669
ICHNG    EQU   *
         TM    LCBRSPRI,DIS             DISABLE DONE           @OY14491
         BO    EXCPDONE                 YES                    @OY13669
         L     RWORK,AVTOCGET           OPERATOR CONTROL AVT   @OY13669
         USING IEDQOPCD,RWORK                                  @OY13669
         L     RWORK,OPCWAITL           OP CTL STOPLINE LIST   @OY13669
         USING IEDQOPCE,RWORK                                  @OY13669
CHKLCB   EQU   *                                               @OY13669
         LTR   RWORK,RWORK              OPCE ADDRESS ZERO?     @OY20036
         BZ    FOUND                    YES                    @OY20036
         CLI   OCOPTCDE,OCVARY          VARY REQUEST           @OY14092
         BNE   GETREQ                   SKIP GROUP CHECK       @OY14092
         CLC   LCBDCBPT+1(THREE),OCMODNME+FIVE  SAME LINE GRP? @OY14092
         BNE   LOOPUNIT                 NO,LOOK AT NEXT        @OY14092
GETREQ   EQU   *                                               @OY14092
         L     R1,OCUNIT                PICK UP STOPLINE UNIT  @OY13669
         CLM   RLCB,SEVEN,SIXTEEN+ONE(R1) THIS LCB             @OY13669
         BE    FOUND                    YES,GO CHECK IT        @OY14092
         TM    OCFLAG,OCATTACH          ATTACHED REQUEST       @OY14092
         BZ    EXCPDONE                 NO, DON'T DISABLE      @OY14092
         L     RWORK,OCELEM             GET NEXT               @OY14092
         B     GETREQ                                          @OY14092
FOUND    EQU   *                                               @OY14092
         SPACE
         MVI   LCBSTAT1,LCBOCNI+LCBRECVN SET STATUS            @OY13669
         LTR   RWORK,RWORK              OPCE ADDRESS ZERO?     @OY20036
         BZ    CKLOCAL                  YES,CHECK FOR LOCAL    @OY20036
         TM    OCSWITCH,X40             LINE TO BE RESTARTED   @OY13669
         BO    EXCPDONE                 YES, DO NOT DISABLE    @OY13669
CKLOCAL  EQU   *                                               @OY20036
         CLI   LCBRSKEY,X1E             LOCAL DEVICE ?         @OY13669
         BNE   EXCP                     GO SET LCB STOPPED     @OY13669
EXCPDONE EQU   *                                               @OY13669
         NI    LCBRSPRI,OFFHI           RESET DISABLE          @OY13669
         SPACE
         MVI   LCBPRI,PRISTOP           SET LINE STOPPED PRTY  @OZ29091
         TM    LCBINSRC+2,TWO           LCB IN TIME DELAY        S22025
         BNO   FFREE                    NO-BR                    S22025
*                                                                S22025
         LR    R1,RLCB                  SET TO REMAVE LCB FROM   S22025
         L     R15,AVTHG02              TIME DELAY QUEUE         S22025
         BALR  R14,R15                  REMOVE LCB FROM QUEUE    S22025
*                                                                S22025
         TM    LCBSTAT2,LCBDIAL         IS THIS A DIAL LINE      S22025
         BO    FFREE                    YES-BYPASS               S22025
*                                                                S22025
         CLI   LCBRSKEY,X1A             BUFFERED TERMINAL        S22025
         BE    FFREE                    YES-BR                   S22025
*                                                                S22025
         LA    R7,LCBRSKEY              PUTIN THE RECIEVE        S22025
         LR    R1,R7                    SCHEDULER                S22025
         BAL   R14,DSPPRIOR             BACK ON QUEUE            S22025
*                                                                S22025
FFREE    EQU   *
         TM    AVTBIT1,AVTCLOSN         CLOSEDOWN              @OY16355
         BO    GETOFF                   YES, LOG USERS OFF     @OY16355
         L     RWORK,AVTOCGET           GET WORKAREA ADDRESS   @OY16355
         ICM   RWORK,AD,OPCWAITL+1-IEDQOPCD(RWORK) GET FIRST RE@OY16355
GRPCHK   EQU   *                                               @OY16355
         BZ    POSTLCB                  NONE THERE             @OY16355
         CLC   LCBDCBPT+1(THREE),OCMODNME+FIVE   SAME GROUP    @OZ28940
         BE    FOUNDGRP                 YES, BRANCH            @OY16355
         ICM   RWORK,AD,ONE(RWORK)      NO, GET NEXT           @OY16355
         B     GRPCHK                   GO CHECK NEXT          @OY16355
FOUNDGRP EQU   *                                               @OY16355
         L     RELE,OCUNIT              GET ADDR OF QHK REQ    @OY16355
         CLM   RLCB,AD,REQLCBA          CORRECT REQUEST        @OY16355
         BE    OFFQUE                   YES, GO CHECK OPTION   @OY16355
         TM    OCFLAG,OCATTACH          ANY MORE REQUESTS      @OY16355
         BZ    POSTLCB                  NO, BRANCH             @OY16355
         ICM   RWORK,AD,OCELEM+ONE      GET RELATED ONE        @OY16355
         B     FOUNDGRP                 GO CHECK               @OY16355
OFFQUE   EQU   *                                               @OY14092
         TM    REQOPT,LOGOFF            FORCE LOGOFF REQUESTED @OY14092
         BZ    POSTLCB                  NO, BRANCH TO OPCTL    @OY19040
GETOFF   EQU   *                                               @OY19040
         TM    AVTBIT1,AVTCLOSN         CLOSEDOWN              @OY19040
         BZ    ALRDYSET                 NO,ALREADY SET         @OY19040
         L     RWORK,AVTOCGET                                  @OY19040
         ICM   RWORK,AD,OPCWAITL+1-IEDQOPCD(RWORK)             @OY19040
*                                       TOP REQUEST            @OY19040
         L     RELE,OCUNIT              STOPLINE UNIT          @OY19040
ALRDYSET EQU   *                                               @OY19040
         LR    R1,RELE                  SET REGISTER FOR       @OY19040
*                                       INVSCAN                @OY19040
         CLI   REQPRI,AVTEZERO          HAS PRTY BEEN ZEROED   @OY19040
         BE    GOSCAN                   YES                    @OY19040
         SR    R1,R1                    SET REG FOR INVSCAN    @OY19040
GETNEXT  EQU   *                                               @OY19040
         TM    OCFLAG,OCATTACH          ANY MORE REQUESTS      @OY19040
         BZ    GOSCAN                   NO                     @OY19040
         ICM   RWORK,AD,OCELEM+ONE      GET RELATED ONE        @OY19040
         L     RWORK1,OCUNIT            GET ADDR OF HK REQ     @OY19040
         CLI   REQPRI-REQELE(RWORK1),AVTEZERO                  @OY19040
*                                     HAS PRIORITY BEEN ZEROED @OY19040
         BNE   GETNEXT                  GET NEXT ELEMENTS      @OY19040
         LR    R1,RWORK1                SET REG FOR INVSCAN    @OY19040
GOSCAN   EQU   *                                               @OY19040
         BAL   RLINK,INVSCAN            SCAN FOR SESSIONS      @OY14092
*        B     POSTLCB                  POST LCB               @OY14092
POSTLCB  EQU   *                                               @OY14092
         L     RDCB,LCBDCBPT            GET DCB ADDRESS        @SA70195
         SR    RWORK,RWORK              CLEAR FOR IC           @SA70195
         IC    RWORK,LCBUCBX            GET RLN - 1            @SA70195
         SLL   RWORK,2                  SHIFT RLN              @SA70195
         L     RWORK,DCBINVLI(RWORK)    GET INVLIST ADDRESS    @SA70195
         LA    RWORK,AVTECD8(RWORK)     POINT TO FIRST ENTRY   @SA70195
         ST    RWORK,AVTDOUBL           SAVE IN WORK AREA      @SA70195
         MVC   LCBINVPT,AVTDOUBL+1      SET CURRENT I LIST PTR @SA70195
         LA    RWORK,AVTOPCOB           GET OP CTL QCB ADDR
         LR    R1,RLCB
         ST    RWORK,LCBQCBA-1          SET QCB ADDRESS          CLUP21
         MVI   LCBSTAT1,AVTEZERO        SET LINE STOPPED       @OZ29091
         TM    AVTBIT1,AVTCLOSN         CLOSEDOWN              @OZ24311
         BNO   NOCLS                    NORMAL PROCESSING      @OZ24311
         L     RWORK,AVTOCGET           OPERATOR CTL AVT       @OZ24311
         USING IEDQOPCD,RWORK                                  @OZ24311
         L     RWORK,OPCWAITL           GET WAITL CHAIN        @OZ24311
         USING IEDQOPCE,RWORK                                  @OZ24311
         CLC   OCBACKUP(TWO),MODZ1      CLOSEDOWN OPCE         @OZ24311
         BNE   NOCLS                    STILL IN IGCZ110D      @OZ24311
         NI    OCSWITCH,XDF             TURN HK PROC SWTCH OFF @OZ24311
NOCLS    EQU   *                        NOT CLOSEDOWN          @OZ24311
         XC    LCBINSRC,LCBINSRC        CLEAR LCB OF LOCK       SA66918
         L     RSCB,LCBSCBA-1           SCB ADDRESS             SA66918
         NI    SCBSTATE,AVTEFF-(SCBLCK1N+SCBMSGLN) CLEAR LOCK   SA66918
         L     RWORK,LCBSTCBA-1         GET FIRST STCB         @OX14763
         CLC   AVTEZERO(FOUR,RWORK),STOPSTCB THIS MODULE       @OX14763
         BNE   DSPPOST                  NO, GO POST            @OX14763
         MVC   LCBSTCBA(THREE),FIVE(RWORK) DELINK STCB         @OX14763
         B     DSPPOST                  RETURN
*
SETBIT   EQU   *
         OI    LCBSTAT1,LCBOCNI         REQUEST LCB FROM SCHED   CLUP21
         L     R1,UNITOFF(RELE)         GET EXTRA UNIT FOR     @OX14763
*                                       BFRD STOP TERMINAL     @OX14763
         LTR   R1,R1                    IS ONE THERE           @OX14763
         BZ    DSPDISP                  NO, EXIT               @OX14763
         MVC   STCBOFF(ENDSTOP-STOPSTCB,R1),STOPSTCB INSERT    @OX14763
*                                       STOPLN STCB IN UNIT    @OX14763
         LA    R1,STCBOFF(R1)           GET STCB ADDR          @OX14763
         LA    R7,LCBSTCBA-1            SET TO INSERT STCB     @OX14763
         B     DSPPRIO                  GO INSERT IT           @OX14763
*
SRCHLCBS EQU   *                                               @OY14092
         L     RLCB,REQLCBA-ONE         SET LCB ADDRESS        @OY14092
         CLI   LCBSTAT1,AVTEZERO        IS LCB STOPPED         @OZ30095
         BE    DSPDISP                  YES, EXIT              @OZ30095
         LH    R9,LCBTTCIN              PICK UP TTCIN          @OY19040
         ST    R9,REQPRI                STORE IN STOPLINE ELE  @OY19040
         TM    REQOPT,CKTSO             TSO CHECK REQUESTED    @OY14092
         BZ    CONTINHK                 NO,CONTINUE            @OY14092
         LR    R1,RELE                  SET REG FOR INVSCAN    @OY19040
         BAL   RLINK,INVSCAN            SCAN INVLIST           @OY14092
         B     CONTINHK                 NO TSO FOUND           @OY14092
TSOERR   EQU   *                                               @OY14092
         SPACE
         LR    R1,RELE                  YES, PASS ELEM BACK TO CV
         LA    RWORK,AVTOPCOB           GET OP CTL QCB ADDR
         ST    RWORK,ZERO(RELE)         PUT OP CTL QCB ADDR IN ELEM
         MVI   FOUR(RELE),PRISTOP       SET E8 PRI FOR USE IN CV
         B     DSPPOST                  RETURN TO TELL CV THAT THIS
*                                       LINE NOT STOPPED DUE TO TSO
         SPACE
         SPACE
CONTINHK EQU   *
         L     RLCB,SIXTEEN(RELE)       PICK UP LCB ADDRESS      CLUP21
         L     RDEB,REQDEBA             GET DEB ADDR           @OY14092
TESTACT  EQU   *
         TM    LCBSTAT1,LCBFREEN        LINE ALREADY FREE
         BO    DIALCHK                  YES-BR                   S22025
         SPACE 2
         TM    EIGHT(RELE),HI           HALT LINE IMMEDIATE      S22025
         BO    HIO                      YES, BRANCH              S22025
         SPACE 2
         TM    LCBSTAT1,LCBSENDN+LCBRECVN  LCB MARKED AS SENDING S22025
*                                          AND RECEIVING         S22025
         BO    ICHNG                    YES, BRANCH              S22025
         SPACE 2
         TM    LCBSTAT2,LCBDIAL         IS THIS A DIAL LINE      S22025
         BNO   ACT                      NO-BR                    S22025
*                                                                S22025
         TM    AVTBIT1,AVTCLOSN         CLOSE DOWN IN PROGRESS   S22025
         BO    HIO                      YES-BR                   S22025
         TM    LCBTSTSW,HI                                       S22025
         BO    ACT                      YES-BR                   S22025
*                                                                S22025
TST      EQU   *
         TS    LCBTSTSW                 BEEN SET BEFORE          CLUP21
         BZ    HIO                      BRANCH IF NOT TO HALTIO  CLUP21
*
         B     SETBIT                   GO REQUEST LCB           S22025
DIALCHK  EQU   *                                                 S22025
         TM    LCBSTAT2,LCBDIAL         IS THIS A DIAL LINE      S22025
         BNO   SETFREE                  NO-BR                    S22025
         L     R14,LCBSCBA-1            GET DIAL SCB ADDR        S22025
         TM    SCBQTYPE-IEDQSCB(R14),SCBBFMM MIDDLE OF MSG       S22025
         BO    SETBIT                   YES-BR                   S22025
         MVI   LCBSTAT1,LCBOCNI+LCBRECVN TURN ON STOPLINE BIT  @OY11167
         B     EXCP                     SET LINE STOPPED       @OY11167
SETFREE  EQU   *
         SR    R15,R15                  CLEAR REGISTER           S99228
         IC    R15,LCBUCBX              PICK UP RLN              S99228
         SLL   R15,TWO                  MULTIPLY BY FOUR         S99228
         L     RDCB,LCBDCBPT            PICK UP DCB ADDR         S99228
         L     RINVLIST,DCBINVLI(R15)   PICK UP INVLIST ADDR     S99228
         CLI   LCBRSKEY,X1A             BUFFERED TERMINALS       S99228
         BE    QHK010                   YES, GO CHECK LINE STATUSS99228
         SR    R1,R1                    CLEAR REGISTER           S99228
         IC    R1,DCBEIOBX              INSERT LENGTH OF IOB     S99228
         SH    R1,AVTHA4                REDUCE LCB LENGTH BY 8   S99228
         SH    R1,AVTHA4                BYTES TO POINT TO EXTEN. S99228
         LA    R15,ZERO(R1,RLCB)        PICK LCB EXTEN. ADDR.    S99228
         USING IEDQLCBX,R15             SET ADDRESSABILITY       S99228
         TM    LCBXFLAG,LCBGPCTV+LCBERPND G.P. OR ERP ACTIVE     S99228
         BZ    ICHNG                    NO, SET LCB STOPPED      S99228
         TM    AVTBIT1,AVTCLOSN         IS CLOSEDOWN IN PROGRESS
         BO    ICHNG                    YES, SET LCB STOPPED
         B     SETBIT                   YES, GO REQUEST LCB      S99228
QHK010   EQU   *                                                 S99228
         L     RINVLIST,DCBINVLI(R15)   GET THE INVITATION LIST  S21101
*                                         ADDRESS FOR THIS LINE  S21101
         CLI   FOUR(RINVLIST),ZERO      ARE WE CURRENTLY SENDING S21101
*                                         TO ANY STATIONS ON     S21101
*                                         THIS LINE              S21101
         BNE   SETBIT                   YES, SET THE STOP LINE   S21101
*                                         REQUEST BIT AND WAIT   S21101
*                                         FOR A SCHEDULER TO     S21101
*                                         POST THE LCB TO IEDQHK S21101
         CLI   ONE(RINVLIST),ZERO       ARE WE CURRENTLY         S21101
*                                         RECEIVING FROM ANY     S21101
*                                         STATIONS ON THIS LINE  S21101
         BE    ICHNG                    NO, LINE CAN BE TAKEN    S21101
         LA    RDEB,ICHNG               SET RETURN ADDR          S22025
         EJECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *  S21101
*                                                             *  S21101
*        THIS SUBROUTINE WILL COMPUTE THE ADDRESS OF A        *  S21101
*        DESTINATION QCB BASED ON THE INVITATION LIST         *  S21101
*                        POINTER                              *  S21101
*                                                             *  S21101
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *  S21101
GETQCBAD EQU   *                                                 S21101
         LA    RWORK,EIGHT(RINVLIST)    GET THE ADDRESS OF THE   S21101
*                                         FIRST INVITATION LIST  S21101
*                                         ENTRY                  S21101
         SR    RDCB,RDCB                CLEAR A WORK REGISTER    S21101
         IC    RDCB,TWO(RINVLIST)       GET THE SIZE OF EACH     S21101
*                                         ILIST ENTRY            S21101
DECISIZE EQU   *                                                 S21101
         LR    R14,RDCB                 COPY THE SIZE
         BCTR  R14,0                    REDUCE THE SIZE BY 1     S21101
         IC    R14,ZERO(R14,RWORK)      PICK UP THE INDEX TO THE S21101
*                                         TNT OFFSET             S21101
         AR    R14,R14                  DOUBLE TO COMPUT THE     S21101
*                                         ILIST OFFSET           S21101
         LR    R15,RINVLIST             COPY THE ADDRESS OF THE  S21101
*                                         INVITATION LIST        S21101
         SR    R15,R14                  SUBTRACT THE INDEX FROM  S21101
*                                         THE BEGINNING ADDRESS  S21101
         LH    R1,ZERO(R15)             GET THE TNT OFFSET       S21101
         L     R15,AVTRNMPT             GET THE ADDRESS OF THE   S21101
*                                         TNT CONVERSION ROUTINE S21101
         BALR  R14,R15                  BRANCH AND LINK TO IT    S21101
         SPACE 3
         L     RQCB,ZERO(R1)            LOAD THE DESTINATION     S21101
*                                         QCB ADDRESS
         SR    R0,R0                    CLEAR                    S22025
         SR    R14,R14                  REGS FOR                 S22025
         SR    R15,R15                  INDEXING                 S22025
         IC    R14,QCBSCBOF             GET SCB OFFSET           S22025
         IC    R15,AVTSCBSZ             GET SCB SIZE             S22025
         MR    R14,R14                 SIZE  X OFFSST EQ INDEX   S22025
         L     R14,LCBSCBDA-1           SCB DIRECTORY POINTER    S22025
         LA    R15,ZERO(R14,R15)        GET SCB ADDR             S22025
         TM    SCBQTYPE-IEDQSCB(R15),SCBBFMM MIDDLE OF MSG       S22025
         BO    SETBIT                   YES RETURN               S22025
         SPACE 3
         AR    RWORK,RDCB               INCREMENT THE CURRENT    S21101
*                                         ILIST POINTER          S21101
         CLI   ZERO(RWORK),ENDILIST     IS THIS THE END OF THE   S21101
*                                         INVITATION LIST        S21101
         BCR   CNDZERO,RDEB             YES-BR                   S22025
         CLI   ONE(RWORK),ENDILIST      IS THIS THE END OF THE   S21101
*                                         INVITATION LIST        S21101
         BCR   CNDZERO,RDEB             YES-BR                   S22025
         B     DECISIZE                 COMPUTE NEXT QCB ADDRESS S21101
         EJECT
ACT      EQU   *
         CLI   LCBCPA+24,CCWPOLL        AUTOPOLL LINE            CLUP21
         BNE   BSC                      BRANCH IF NOT            CLUP21
*
         ST    RLCB,AVTSAVE2            LCB POINTER              Y05331
         L     R1,AVTSAVTP              SECONDARY AVT POINTER    Y05331
         USING IEDNSVTD,R1                                       Y05331
         TM    SAVTDIAF,SAVTVIRT        RUNNING UNDER VM  ?      Y05331
         BZ    NOVIRT                  * NO,BRANCH               Y05331
         L     R15,SAVTDIAG             VIRTUAL DIAGNOSE ROUTINE Y05331
         DROP  R1                                                Y05331
         LA    R0,LCBCPA+56             TIC ADDRESS              Y05331
         ST    R14,AVTSAVE2+24          R14 POSSIBLY NEDDS SAVINGY05331
         MVI    LCBCPA+56,CCWNOP        NOP VIRTUAL TIC          Y05331
         BAL   R14,4(,R15)              NOP CORRESPONDING REALTICY05331
         L     R14,AVTSAVE2+24     RESTORE R14                   Y05331
         B     SETBIT                   GO REQUEST LCB           Y05331
         SPACE
NOVIRT   EQU   *                                                 Y05331
         MVI   LCBCPA+56,CCWNOP         SET NOP IN CCW           CLUP21
         B     SETBIT                   GO REQUEST LCB           CLUP21
*
BSC      EQU   *
         TM    LCBSTAT2,LCBSYNC         BI-SYNCH LINE            CLUP21
         BNO   SENSE                    BRANCH IF NOT            CLUP21
*
         CLI   LCBCPA+16,CCWPREP        PREPARE ON THE LINE      CLUP21
         BNE   SETBIT                   BRANCH IF NOT            CLUP21
*
         B     TST                      RETURN TO TEST-AND-SET   CLUP21
*
SENSE    EQU   *
         L     RWORK,LCBCPA+24          ADDRESS THE SENSE BYTE   CLUP21
         CLI   LCBCPA+24,CCWSENSE       SENSE COMMAND            CLUP21
         BE    CLI                      BRANCH IF YES            CLUP21
*
         CLI   LCBCPA+32,CCWSENSE       WORLD TRADE              CLUP21
         BNE   SETBIT                   BRANCH IF NOT            CLUP21
*
         L     RWORK,LCBCPA+32          ADDRESS THE SENSE BYTE   CLUP21
*
CLI      EQU   *
         CLI   LCBCSWSV,AVTEFF          HAS DATA ALREADY BEEN    X01004
*                                       ENTERED                  X01004
         BNE   SETBIT                   NO, GO REQUEST LCB       CLUP21
*
HIO      EQU   *
         LA    R3,SETBIT                SET UP BRANCH AFTER EXCP S22025
         OI    LCBSTAT1,LCBOCNI         REQUEST LCB
         L     RSCB,LCBSCBA-1           POINT TO THE SCB         CLUP21
         TM    SCBSTATE,SCBLCK1N+SCBMSGLN LCB IN LOCK MODE      SA54930
         BZ    HIO1                    NO, PROCEED              SA54930
         NI    SCBSTATE,X'FF'-(SCBLCK1N+SCBMSGLN) TURN OFF LOCK
         TM    LCBINSRC+2,LOCKR+LOCKI   AFTER LOCK & BEFORE PUT@SA70889
         BNZ   LOCKCLR                  YES, BRANCH            @SA70889
CLRINSRC EQU   *                                               @YA10232
         XC    LCBINSRC(3),LCBINSRC    CLEAR LCB LOCK FLAGS     SA54930
HIO1     EQU   *                                                 S22025
         SR    RWORK,RWORK              CLEAR
         IC    RWORK,LCBUCBX            GET UCB INDEX
         SLL   RWORK,TWO                MULTIPLY BY FOUR
         L     R1,UCB(RDEB,RWORK)       GET UCB ADDR
         LA    R1,ZERO(R1)              CLEAR HIGH ORDER BYTE    CLUP21
         MVI   LCBECBCC,HIOCC           SET HALT I/O COMPLETION  Y02027
         IOHALT (1)                     HALT I/O ON LINE
*
         LTR   R15,R15                  RETURN CODE ZERO        SA67759
         BZR   R3                       YES, HIO COMPLETE       SA67759
         SR    R15,R15                  CLEAR FOR COMPARE      @ZA02055
         CLM   R15,15,LCBCSW+THREE      DID CHANNEL PROGRAM    @ZA02055
*                                        COMPLETE               SA67759
         BZ    HIO1                     NO, REISSUE HIO         SA67759
         BR    R3                       IF PROCESSING OP CONTROL S22025
*                                       BRANCH TO SET STOPLINE   S22025
*                                       BIT.  IF CHECKING FOR AN S22025
*                                       ATTENTION SIGNAL EXIT TO S22025
*                                       DSPDISP                  S22025
         SPACE 5
CHKTNT   EQU   *
         LR    RLCB,RELE                GET LCB BASE             S22025
ISSPRP   MVC   LCBCSWSV(7),LCBCSW       SAVE CSW                 S22025
         MVC   LCBCPA(8),PREPCCW        SET UP PREPARE CCW       S22025
         STCM  RLCB,7,LCBCPA+1          VALID FIXED CORE ADDR    Y01004
         MVI   LCBTPCD,XTEN             SET UP TP OP CODE        S22025
         MVI   LCBTPCD+11,OPNCHK        SET OPEN LINE CHECK      S22025
         B     EXCP1                    GO ISSUE PREPARE         S22025
LOCKCLR  EQU   *
         LH    R1,LCBTTCIN             TERMINAL INDEX           SA54930
         L     R15,AVTRNMPT            ADDRESS OF ENABLED SRCH  SA54930
         USING IEDQTNTD,R15            ADDRESSABILITY           SA54930
         BAL   R14,TNTCODE             GET TERMINAL ENTRY       SA54930
         DROP  R15                                              SA54930
         USING IEDQTRM,R1                                       SA54930
         L     R1,TRMDESTQ-1           GET QCB ADDRESS          SA54930
         USING IEDQQCB,R1                                       SA54930
         MVI   QCBLKRLN,ZERO           CLEAR LOCK REL LINE      SA54930
         TM    LCBINSRC+2,BDFLAG        HAS INMSG COMPLETED    @YA10232
         BO    CLRINSRC                 NO, ISSUE THE HIO      @YA10232
         XC    LCBINSRC(3),LCBINSRC    CLEAR LCB LOCK FLAGS     SA54930
         B     ICHNG
INVSCAN  EQU   *
         SR    R3,R3                    CLEAR FOR INDEX        @OY14092
         IC    R3,LCBUCBX               GET RLN                @OY14092
         SLL   R3,TWO                   MULTIPLY BY FOUR
         L     RDCB,LCBDCBPT            GET DCB ADDRESS        @OY18823
         L     RINV,DCBINVLI(R3)        GET CORRECT INVLIST    @OY14092
         LA    RWORK,SEVEN(,RINV)       POINT TO LIST -7       @OY14092
         SR    R3,R3                    CLEAR FOR COUNTER      @OY14092
         IC    R3,ONE(,RINV)            GET ACTIVE ENTRIES     @OY14092
         LTR   R3,R3                    ANY ACTIVE             @OY14092
         BZR   RLINK                    NO, RETURN TO CALLER   @OY14092
         L     R15,AVTRNMPT             GET LOOKUP RTN ADDR    @OY14092
         TM    LCBSTAT2,LCBDIAL         DIAL LINE              @OY14092
         BZ    NOTDIAL                  NO, CONTINUE           @OY14092
         LTR   R1,R1                    VALID STOPLINE ELEMENT @OY19040
*                                       FOUND                  @OY19040
         BZ    NOTDIAL                  NO USE INVLIST         @OY19040
         LH    R1,REQLINK+ONE-REQELE(R1) PICK UP TTCIN TO USE  @OY19040
         LTR   R1,R1                    ONE THERE              @OY14092
         LA    R3,ONE                   SET COUNT TO 1         @OY14092
         BH    GETENTRY                 BRANCH IF ONE THERE    @OY14092
         IC    R3,ONE(,RINV)            RESTORE TO ACT ENTRIES @OY14092
NOTDIAL  EQU   *                                               @OY14092
         SR    R7,R7                    CLEAR LENGTH           @OY14092
         IC    R7,TWO(,RINV)            GET ENTRY WIDTH        @OY14092
TSOLOOP  EQU   *                                               @OY14092
         AR    RWORK,R7                 BUMP TO NEXT           @OY14092
         SR    R0,R0                    CLEAR FOR INDEX        @OY14092
         IC    R0,ZERO(,RWORK)          GET INDEX              @OY14092
         SLL   R0,ONE                   DOUBLE INDEX           @OY14092
         LR    R1,RINV                  POINT TO LIST          @OY14092
         SR    R1,R0                    BACK UP TO OFFSET      @OY14092
         LH    R1,ZERO(,R1)             GET TNT OFFSET         @OY14092
         TM    AVTBIT1,AVTCLOSN         CLOSING DOWN           @OY18823
         BO    GETENTRY                 BRANCH IF YES          @OY18823
         LH    R0,REQTTCIN              GET SPECIFIED TERM     @OY14092
         LTR   R0,R0                    ONE SPECIFIED?         @OY14092
         BZ    GETENTRY                 NO, CONTINUE           @OY14092
         CR    R1,R0                    CORRECT ONE            @OY14092
         BNE   CONTLOOP                 NO, GET NEXT           @OY14092
         LA    R3,ONE                   SET LOOP COUNT         @OY14092
GETENTRY EQU   *                                               @OY14092
         BALR  R14,R15                  LOCATE TERMINAL ENTRY  @OY14092
         USING IEDQQCB,R1                                      @OY14092
         LR    RDEB,R1                  SAVE TERM ENTRY        @OY14092
         L     R1,TRMDESTQ-1-IEDQTRM(,R1) GET QCB ADDR         @OY14092
         TM    QCBFLAG,QCBTSSES         IN SESSION?            @OY14092
         BZ    CONTLOOP                 NO,CONTINUE            @OY14092
         TM    AVTBIT1,AVTCLOSN         CLOSING DOWN           @OY18823
         BO    FORCE                    BRANCH IF YES          @OY18823
         TM    REQOPT,LOGOFF            FORCE LOGOFF           @OY14092
         BZ    CKTTCIN                  NO,CONTINUE CHECKS     @OY14092
FORCE    EQU   *                                               @OY18823
         STM   R0,R15,REGSAVE           SAVE REGISTERS         @OY14092
         TM    QCBTSOF1,QCBDELAY        QCB IN TIME DELAY QUE  @OY14092
         BZ    CKTJID                   NO, CONTINUE           @OY14092
         L     R15,AVTHG02              ADDR OF DELAY REMOVER  @OY14092
         BALR  R14,R15                  REMOVE FROM TIME DELAY @OY14092
CKTJID   EQU   *                                               @OY14092
         NC    QCBTJID(TWO),QCBTJID     TJID ZERO              @OY14092
         BZ    NONTSO                   YES,ALREADY LOGGED OFF @OY14092
         L     RWORK,CVTPTR             GET CVT ADDR           @OY14092
         L     R2,CVTASVT-CVTMAP(,RWORK)  ASVT ADDRESS         @OY14092
         TM    AVTBIT3,AVTTSAB          HAS TSO ABENDED        @OY14092
         BO    NONTSO                   BRANCH YES             @OY14092
         SR    R5,R5                    CLEAR FOR ID           @OY14092
         ICM   R5,THREE,QCBTJID         GET TSO JOB ID         @OY14092
         BZ    NONTSO                   BRANCH NON THERE       @OY14092
         BCTR  R5,ZERO                  SUBTRACT ONE           @OY14092
         SLL   R5,TWO                   MULTIPLY BY FOUR       @OY14092
         L     R2,ASVTENTY-ASVT(R5,R2)  ASCB ADDR              @OY14092
         L     R9,ASCBTSB-ASCB(,R2)     TSB ADDR               @OY14092
         LR    R7,R1                    QCB ADDR               @OY14092
         LR    R1,R13                   SAVE AREA ADDR         @OY14092
         TM    AVTBIT3,AVTTSAB          TSO ABENDED            @OY14092
         BO    NONTSO                   YES, BRANCH            @OY14092
         QTIP  4,(1)                    CALL HANGUP            @OY14092
NONTSO   EQU   *                                               @OY14092
         LM    R0,R15,REGSAVE           RESTORE ALL REGS       @OY14092
         NI    QCBFLAG,AVTEFF-QCBTSSES-QCBREAD-QCBNOBRK        @OY14092
*                                       RESET STATUS           @OY14092
         NI    QCBDSFLG,AVTEFF-QCBALTMH RESET ALTERNATE MH     @OY14092
         MVI   QCBRETCT,AVTEZERO        CLEAR RETRY COUNT      @OY14092
         XC    QCBCARCT(THREE),QCBCARCT CLEAR CARR.CT & TJID   @OY14092
         XC    QCBSATCT(THREE),QCBSATCT CLEAR SIMATTN COUNT    @OY14092
         SR    R14,R14                  CLEAR REGISTER         @OY14092
         IC    R14,TRMCHCIN-IEDQTRM(,RDEB) GET INDEX           @OY14092
         BCTR  R14,ZERO                 SUBTRACT ONE           @OY14092
         SLL   R14,TWO                  MULTIPLY BY FOUR       @OY14092
         A     R14,AVTCSTCS             ADD TABLE POINTER      @OY14092
         TM    ONE(R14),CBREAK          BREAK SPECIFIED        @OY14092
         BO    SETBRK                   YES,BRANCH             @OY14092
         OI    QCBFLAG,QCBNOBRK         SET NO BREAK           @OY14092
SETBRK   EQU   *                                               @OY14092
         B     CONTLOOP                 CONTINUE               @OY14092
CKTTCIN  EQU   *                                               @OY14092
         NC    REQTTCIN(TWO),REQTTCIN   TERMINAL SPECIFIED     @OY14092
         BNZ   FOUR(RLINK)              YES, RETURN ERROR      @OY14092
         OI    LCBSTAT1,LCBOCWTN        INDICATE REQUEST WAIT'G@OY14092
         B     DSPDISP                  RETURN ERROR TO CALLER @OY14092
CONTLOOP EQU   *                                               @OY14092
         BCT   R3,TSOLOOP               CHECK NEST ENTRY       @OY14092
         BR    RLINK                    RETURN TO CALLER       @OY14092
         EJECT                                                 @OY14092
MINUS8   DC    H'-8'                    NEGATIVE EIGHT         @OY14092
REGSAVE  DC    16F'0'                   REGISTER SAVEAREA      @OY14092
HALF8    DC    H'8'                     TO BACK UP PTR TO QCB    S22025
PREPCCW  DC    X'06',AL3(*),X'200000BC' PREPARE CCW              S22025
MODZ1    DC    C'Z1'                    MOD ID TO MATCH        @OZ24311
STOPSTCB DS    0F                       FULL WORD ALIGNMENT    @OX14763
         DC    AL1(DSPMCPL8)            8-BYTE STCB            @OX14763
         DC    AL3(BFRDSTOP)            ROUTING ADDRESS        @OX14763
         DC    X'B0',AL3(0)             LINK FIELD AND PRI     @OX14763
         L     R15,AVTEZERO(R3)         GET ROUTINE ADDRESS    @OX14763
         BR    R15                      BRANCH TO IT           @OX14763
ENDSTOP  EQU   *                                               @OX14763
         USING *,R15                                           @OX14763
BFRDSTOP EQU   *                        SUBROUTINE FOR BFRD    @OX14763
*                                        STOP TERMINAL         @OX14763
         LR    RLCB,R1                  LCB ADDRESSABILITY     @OX14763
         LR    RELE,R3                  STCB ADDRESS           @OX14763
         SH    RELE,BACKUP              BACK UP TO UNIT        @OX14763
         ICM   R3,AD,FIVE(R3)           GET ADDR OF NEXT STCB  @OX14763
         LA    R1,ZERO(,R1)             CLEAR FOR COMPARE      @OX14763
         LA    R7,ZERO(,R7)             CLEAR FOR COMPARE      @OX14763
         CR    R1,R7                    LCB POSTED             @OX14763
         BNE   DSPBYPAS                 NO, BYPASS TO SCHEDULER@OX14763
         TM    LCBSTAT1,LCBSENDN        LCB MARKED AS SENDING  @OX14763
         BO    DSPBYPAS                 YES,EXIT               @OX14763
         CLI   OPCDE(RELE),OCICHNGE     ICHNGE COMMAND         @OX14763
         BE    POSTHK                   YES, POST LCB TO HK    @OX14763
*                                        FOR STOPLINE          @OX14763
         L     RWORK,TERMOFF(RELE)      TERM ENTRY ADDRESS     @OX14763
         USING IEDQTRM,RWORK            TERM ENTRY ADDR'BILITY @OX14763
         L     RWORK,TRMDESTQ-1         GET QCB ADDR           @OX14763
         USING IEDQQCB,RWORK            QCB ADDRESSABILITY     @OX14763
         TM    SWITCH(RELE),STOPMM      STOP TERM MID-MSG      @OX14763
         BZ    CHECKMM                  NO, CHECK MID-MSG      @OX14763
         TM    QCBSTAT,QCBSEND          TERMINAL SENDING       @OX14763
         BO    DSPBYPAS                 YES, EXIT              @OX14763
POSTHK   EQU   *                                               @OX14763
         MVC   LCBQCBA,AVTHK+1          SET TO POST TO HK      @OX14763
         MVI   LCBPRI,PRILNFRE          SET POST PRIORITY      @OX14763
         STCM  R3,AD,LCBSTCBA           REMOVE SPECIAL STCB    @OX14763
         B     DSPPOST                  POST LCB TO HK         @OX14763
CHECKMM  EQU   *                                               @OX14763
         SR    RDCB,RDCB                CLEAR FOR INDEX        @OX14763
         LR    RSCB,RDCB                CLEAR FOR INDEX        @OX14763
         IC    RDCB,QCBSCBOF            GET SCB INDEX          @OX14763
         IC    RSCB,AVTSCBSZ            GET SCB SIZE           @OX14763
         MR    RDCB,RDCB                OFFSET TO SCB          @OX14763
         AL    RSCB,LCBSCBDA-1          GET SCB ADDRESS        @OX14763
         TM    SCBQTYPE,SCBBFMM         MIDDLE OF MSG          @OX14763
         BO    DSPBYPAS                 YES, EXIT              @OX14763
         B     POSTHK                   POST LCB TO STOPLN     @OX14763
BACKUP   DC    AL2(STCBOFF)             OFFSET OF STCB IN UNIT @OX14763
         TTNTD
REQELE   DSECT                          REQUEST ELEMENT        @OY14092
REQQCB   DS    F .                      QCB ADDR               @OY14092
REQPRI   DS    XL1 .                    PRIORITY               @OY14092
REQLINK  DS    AL3 .                    LINK FIELD             @OY14092
REQOPT   DS    XL1 .                    OPTION BYTE            @OY14092
IMMED    EQU   X'80' .                  IMMEDIATE REQUEST      @OY14092
INTREQ   EQU   X'40' .                  INTERNAL REQUEST       @OY14092
CKTSO    EQU   X'20' .                  TSO CHECK REQUIRED     @OY14092
LOGOFF   EQU   X'10' .                  FORCE LOGOFF           @OY14092
REQCHAIN DS    AL3 .                    CHAIN OF RELATED REQ'S @OY14092
REQDEBA  DS    F .                      ADDRESS OF DEB         @OY14092
         DS    X .                                             @OY14092
REQLCBA  DS    AL3                      ADDRESS OF LCB         @OY14092
REQTTCIN DS    H .                      TERMINAL ENTRY INDEX   @OY14092
REQRLN   DS    XL1 .                    RELATIVE LINE NO       @OY14092
         SPACE 3
         TDISPD
         EJECT
         TSCBD
         TAVTD 2
         TLCBD
         TCCWD
         TPRIOR
         DCBD  DSORG=TX
         TDEBD
         TTRMD
         TQCBD
         CVT                                                   @OY14092
         IHAASCB                                               @OY14092
         IHAASVT                                               @OY14092
         TOPCED                                                @OY13669
         TOPCAVTD                                              @OY13669
         IEFUCBOB
         END
