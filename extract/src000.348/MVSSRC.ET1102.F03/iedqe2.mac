E2       TITLE '''IEDQE2'' - TCAM QCOPY SERVICE ROUTINE'
IEDQE2   CSECT
         SPACE 3
*  CHANGE ACTIVITY AS FOLLOWS:                                        *
******************** MICROFICHE FLAGS *********************** SUPT CODE
*C THIS MODULE HAS BEEN REWRITTEN FOR TCAM LEVEL 6               Y02027
*        THIS MODULE HAS BEEN REWRITTEN FOR FETCH PROTECT        X02004
          SPACE 4
***********************************************************************
*                                                                     *
*  TITLE: IEDQE2 - QCOPY SERVICE ROUTINE                              *
*                                                                     *
*  MODULE NAME = IEDQE2                                               *
*                                                                     *
*  DESCRIPTIVE NAME = QCOPY SERVICE ROUTINE                           *
*                                                                     *
*  COPYRIGHT = 'NONE'                                                 *
*                                                                     *
*  STATUS: CHANGE LEVEL 6                                        Y02027
*                                                                     *
*FUNCTION: THIS MODULE BUILDS AND PASSES A PARAMETER LIST TO SVC 102  *
*   THE ACTUAL PROCESSING FOR QCOPY IS DONE IN THE MCP BY MODULE      *
*   IGC0A10D WHO GETS CONTROL VIA SVC 102'S TPOSTING THE OP CONTROL   *
*   QCB.                                                              *
*                                                                     *
*ENTRY POINT:                                                         *
*       IEDQE201                                                 S22025
*INPUT:                                                               *
*    1 - ADDRESS OF THREE-WORD INPUT PARAMETER LIST:              99226
*      - WORD 1 - ADDRESS OF TERMINAL NAME                        99226
*      - WORD 2 - ADDRESS OF APPLICATION WORK AREA                99226
*      - WORD 3 - LIMIT OF QUEUE-ZERO IF NOT SPECIFIED            99226
*   13 - ADDRESS OF 18-WORD REGISTER SAVEAREA                         *
*   14 - RETURN ADDRESS                                               *
*   15 - ENTRY POINT ADDRESS                                          *
*OUTPUT: REGISTER 15 CONTAINS A COMPLETION CODE                       *
*EXTERNAL ROUTINES:                                                   *
*        IGC0010D - SVC 104  OPERATOR CONTROL                    X02004
*EXIT-NORMAL: COMPLETION CODE OF 0 FOR SUCCESSFUL COPY                *
*             COMPLETION CODE OF X'80' FOR END OF ITERATIVE PROC  99226
*EXITS-ERROR: COMPLETION CODE OF 4 FOR INVALID TERMNAME               *
*             COMPLETION CODE OF 8 FOR TCAM MCP NOT ACTIVE       S22025
*             COMPLETION CODE OF X'0C' FOR CALLER NOT A VALID TCAM APP*
*             COMPLETION CODE OF X'18' FOR AIB TOO SMALL TO HOLD DATA *
*             COMPLETION CODE OF X'20' FOR INVALID TERMINAL TYPE      *
*TABLES/WORK AREAS:                                                   *
*   NONE                                                              *
*ATTRIBUTES: SERIALLY REUSABLE, PROBLEM PROGRAM MODE                  *
*NOTES: THE OPERATION OF THIS MODULE DEPENDS UPON AN INTERNAL         *
*   REPRESENTATION OF THE EXTERNAL CHARACTER SET WHICH IS EQUIVALENT  *
*   TO THE ONE USED AT ASSEMBLY TIME.  THE CODING HAS BEEN ARRANGED   *
*   SO THAT REDEFINITION OF 'CHARACTER' CONSTANTS, BY REASSEMBLY,     *
*   WILL RESULT IN A CORRECT MODULE FOR THE NEW DEFINITIONS.          *
*                                                                     *
***********************************************************************
RZERO    EQU   0                        GENERAL WORK REG         Y02027
RWORK3   EQU   3                        WORK REGISTER            Y02027
RPARM    EQU   1                        PARAMETER REGISTER
RWORK    EQU   2                        WORKAREA ADDRESS
RBASE    EQU   12                       CSECT BASE REGISTER
RSAVE    EQU   13                       SAVEAREA REGISTER
RETURN   EQU   14                       RETURN ADDRESS
RENTRY   EQU   15                       ENTRY POINT/RETURN CODE
         SPACE 2
IEDQE201 EQU   *                                                 S22025
         STM   RETURN,RBASE,12(RSAVE)   SAVE CALLERS REGISTERS   S22025
         BALR  RBASE,0                  ESTABLISH
         USING *,RBASE                           ADDRESSABILITY
IEDQE2   IEDHJN ENTER                                            X02004
         ST    RSAVE,SAVEAREA+FOUR      USER SAVEAREA ADDRESS    X02004
         LA    RSAVE,SAVEAREA           SAVEAREA                 X02004
         SPACE 1
**************************************************************** Y02027
*              BUILD PARAMETER LIST INPUT TO SVC 102           * Y02027
**************************************************************** Y02027
         SPACE 1
         LM    RWORK,RWORK3,ZERO(RPARM) GET PTRS TO STATION NAME Y02027
*                                       AND USER WORK AREA       Y02027
         ST    RWORK,APNAME             SET POINTERS IN          Y02027
         ST    RWORK3,APUSWKA           PARMLIST                 Y02027
         L     RWORK,EIGHT(,RPARM)      GET 'LIMIT' IF ANY       Y02027
         ST    RWORK,APPASWD            AND STORE IN APCIB       Y02027
         MVI   APCOMCD,QCOPYCD          TURN ON FUNCTION CD FLAG Y02027
         LA    RZERO,ONE                FUNCTION INDX FOR SVC102 Y02027
         LA    RPARM,APPARM             SET PARM REG FOR SVC 102 Y02027
         AQCTL                          ISSUE SVC                Y02027
         SPACE 2
         LTR   RWORK,RENTRY             SAVE AND CHECK RET CODE  Y02027
         BZ    EXIT                     BRANCH IF GOOD           Y02027
         LA    RENTRY,NOTCAM            MPP EXPECTS RET CD = 08  Y02027
         CLM   RWORK,BY3,RC80           IS TCAM ACTIVE           Y02027
         BE    EXIT                     BRANCH IF NOT ACTIVE     Y02027
         LA    RENTRY,LIMTEND           SET POSS RET CODE FOR    Y02027
*                                       END OF QCOPY-LIMIT FUNC  Y02027
         CLM   RWORK,BY3,CODEF          END OF FUNCTION          Y02027
         BE    EXIT                     YES, BRANCH              Y02027
         LR    RENTRY,RWORK             RESTORE RETURN CODE REG  Y02027
         SPACE 1
EXIT     EQU   *                                                 Y02027
         L     RSAVE,SAVEAREA+FOUR      RESTORE USER'S SAVE PTR  Y02027
         RETURN (14,12),T,RC=(15)       RETURN TO USER           Y02027
         SPACE 2
         EJECT
**************************************************************** Y02027
*        C O N S T A N T S   A N D   E Q U A T E S             * Y02027
**************************************************************** Y02027
         SPACE 2
SAVEAREA DC    18F'0'                   MY SAVE AREA             Y02027
CODEF    DC    X'F'                     LIMIT END CODE PASSED BY Y02027
*                                       AQCTL                    Y02027
RC80     DC    X'80'                    CODE RETURNED BY SVC 102 Y02027
*                                       IF TCAM NOT ACTIVE       Y02027
BY3      EQU   1                        MASK FOR BYTE 3          Y02027
LIMTEND  EQU   X'80'                    RC = END QCOPY LIMIT     Y02027
NOTCAM   EQU   8                        TCAM NOT ACTIVE CODE     Y02027
NOPEN    EQU   12                       NO OPEN DCB CODE         Y02027
ZERO     EQU   0                        OFFSET                   Y02027
FOUR     EQU   4                        OFFSET & MOVE LENGTH     Y02027
ADD      EQU   7                        ADDRESS MASK             Y02027
ONE      EQU   1                        OFFSET                   Y02027
THREE    EQU   3                        MOVE LENGTH              Y02027
FIVE     EQU   5                        OFFSET                   Y02027
EIGHT    EQU   8                        OFFSET                   Y02027
QCOPYCD  EQU   X'40'                    FLAG FOR QCOPY           Y02027
         SPACE 2
**************************************************************** Y02027
*              MAP OF INPUT DATA PROVIDED TO SVC 102           * Y02027
**************************************************************** Y02027
         DS    0F                       FORCE ALIGNMENT          Y02027
APCIBMAP EQU   *                                                 Y02027
APPASWD  DC    AL4(0)                   POINTER TO PASSWORD      Y02027
APECB    DC    F'0'                     ECB FOR WAIT IN SVC 102  Y02027
*                                       AND SUBSEQUENT XMPOST    Y02027
APPCBAD  DC    AL4(0)                   PCB POINTER              Y02027
APCOMCD  DS    0XL1                     COMMAND CODE VERB        Y02027
APUSWKA  DC    AL4(0)                   PTR TO USER WORK AREA    Y02027
APNAME   DC    AL4(0)                   PTR TO TERMINAL NAME     Y02027
APPARM   DS    0F                       PARAMETER FOR SVC 102    Y02027
         DC    X'84'                    EOL X'80' + FUNCTION     Y02027
*                                       CODE X'04'               Y02027
         DC    AL3(APCIBMAP)            PTR TO INPUT DATA        Y02027
         SPACE 1
**************************************************************** Y02027
         EJECT
         TDEBAPD                                                 Y02027
         END
