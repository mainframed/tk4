BE01     TITLE '''IEDQBE'' - LOCK ROUTINE'
IEDQBE   CSECT
*C603000-625000                                                  S22026
*C297000,315000,634000,643000,693000,720000,743000,799000,812300 S22025
*C822300-826300                                                  S22025
*A594000                                                         S22025
*C611000                                                        SA57681
*A804720-804760,805950                                           S22024
*A799100,9000000                                               @SA71652
*A510000,806700                                                @OX12487
*A808100,840300                                                @OX13486
*C798980                                                       @OY13294
*A808200                                                       @OZ11170
***********************************************************************
*TITLE -- 'IEDQBE' LOCK TERMINAL ROUTINE                              *
*                                                                     *
*                                                                     *
*STATUS -- CHANGE LEVEL 5                                        S22024
*                                                                     *
*FUNCTION -- THIS ROUTINE LOCKS THE CURRENTLY CONNECTED TERMINAL TO   *
*   THE DESTINATION IF THE DESTINATION IS A PROCESS ENTRY. EXTENDED   *
*   LOCK IS SET IF REQUESTED BY THE LOCK MACRO.                       *
*                                                                     *
*ENTRY POINT -- IEDQBE                                                *
*                                                                     *
*   CALLING SEQUENCE -                                                *
*                  L     R12,AVTMSGS-1                                *
*                  IC    RX,INDEX                                     *
*                  L     R12,0(RX,R12)                                *
*                  BR    R12                                          *
*                                                                     *
*INPUT -- ENTRY IS ALWAYS FROM 'IEDQUI' USER INITERFACE ROUTINE       *
*         PARAMETERS ARE PASSED IMPLICITLY IN REGISTERS AS FOLLOWS:   *
*                  R1    PARAMETER LIST GENERATED BY LOCK MACRO       *
*                  R3    ADDRESS OF STATION CONTROL BLOCK (SCB)       *
*                  R4    ADDRESS OF LINE CONTROL BLOCK (LCB)          *
*                  R9    AVT ADDRESS                                  *
*                  R12   BASE FOR THIS ROUTINE                        *
*                                                                     *
*OUTPUT -- THE LOCK BIT(S) IN THE SCB IS(ARE) SET.                    *
*                                                                     *
*EXTERNAL ROUTINES -- NONE                                            *
*                                                                     *
*EXITS-NORMAL -- 'IEDQUI+4' WITH R15 = 0                         S22025
*                                                                     *
*EXITS-ERROR -- 'IEDQUI+4' WITH R15 = 4 OR 8                     S22025
*                                                                     *
*TABLES/WORKAREAS --                                                  *
*DSECTS USED -- AVT,LCB,QCB,SCB                                       *
*                                                                     *
*ATTRIBUTES -- REENTRANT, REFRESHABLE, ENABLED, RESIDENT, PROBLEM     *
*              PROGRAM MODE.                                          *
*                                                                     *
*NOTES -- THE OPERATION OF THIS MODULE DOES NOT DEPEND UPON A         *
*   PARTICULAR INTERNAL REPRESENTATION OF THE EXTERNAL CHARACTER SET  *
*                                                                     *
***********************************************************************
*
*        REGISTERS
*
R1       EQU   1                        ENTRY PARAMETER LIST
R2       EQU   2                        WORK REGISTER
RSCB     EQU   3                        SCB ADDRESS
RLCB     EQU   4                        LCB ADDRESS
R5       EQU   5                        WORK REGISTER
RPREFIX  EQU   6                        REG6                     S22024
R8       EQU   8                        ENTRY PARAMETER LIST
R9       EQU   9                        AVT ADDRESS
R10      EQU   10                       WORK REGISTER
R12      EQU   12                       BASE REGISTER
R13      EQU   13                       SAVE AREA              @OX12487
R14      EQU   14                       RETURN ADDRESS
R15      EQU   15                       RETURN CODE
FOUR     EQU   4                        RETURN OFFSET            S22025
         EJECT
         USING IEDQAVTD,R9
         USING IEDQPRF,RPREFIX
         USING IEDQSCB,RSCB
         USING IEDQLCB,RLCB
         USING *,R12
         SPACE 3
         LA    R15,AVTECD4              SET FOR BAD RETURN
IEDQBE  IEDHJN IEDQBE01
         LR    R2,R1                    SAVE REG                 S22026
         LH    R1,LCBTTCIN              GET SOURCE TERM INDEX    S22026
         N     R1,AVTCLRHI              CLEAR HI-ORDER BYTES     S22026
         LTR   R1,R1                    SOURCE FOUND             S22026
         BZ    RETURN                   BRANCH IF NO            SA57681
         L     R15,AVTRNMPT             TNT CODE                 S22026
         BALR  R14,R15                  LINK TO IT               S22026
         LA    R15,AVTECD4              RESTORE REG              S22026
         TM    TRMDEVFL+1-IEDQTRM(R1),TRMCONC                    S22026
*                                       SOURCE A CONC            S22026
         BO    RETURN                   BRANCH IF YES            S22026
         LR    R1,R2                    RESTORE REG              S22026
*
         CLI   LCBRSKEY,BFRD            IS SOURCE A BUFFERRED TERMI
         BZ    RETURN                   YES, RETURN TO CALLER    S22025
*
         AR    R15,R15                  SET RETURN CODE = 8
         OC    PRFSIZE,PRFSIZE          ZERO LENGTH BUFFER
         BZ    RETURN                   YES, RETURN TO CALLER    S22025
*
         L     R2,SCBDESTQ-1            DESTINATION QCB
         USING IEDQQCB,R2
         LA    R2,X0(,R2)               CLEAR HIGH ORDER BYTE
         LA    R5,AVTBFRTB              ADDRESS OF BUFFER RETURN QCB
         CLR   R2,R5                    IS THAT THE DESTINATION
         BE    RETURN                   YES, RETURN TO CALLER    S22025
*
         TM    QCBFLAG,QCBPROC          IS QCB FOR A PROCESSING PGM
         BZ    RETURN                   YES, RETURN TO CALLER    S22025
*
         LA    R15,AVTECD12             SET BAD RETURN
        TM    SCBSTATE,SCBLCK1N+SCBMSGLN  ALREADY LOCKED
         BNZ   RETURN                   YES, RETURN TO CALLER    S22025
*
         L     R15,AVTRNMPT             TNT ROUTINE ADDRESS
         LH    R2,PRFDEST               ASSUME HEADER BUFFER
         LR    R8,R1                    SAVE ADDRESS OF PARAMETER
         TM    PRFSTAT1,PRFNHDRN        IS THIS A HEADER BUFFER
         BZ    NOSRCH                   HEADER - NO SEARCH NECESSARY
*
         SR    R2,R2                    CLEAR TO SCAN TERMINAL TABLE
SEARCH   EQU   *
         LA    R2,X1(,R2)               FIRST/NEXT TNT OFFSET
NOSRCH   EQU   *
         LR    R1,R2                    PASS PARAMETER TO TNT
         BALR  R14,R15                  EXECUTE TNT TO FIND TERMINAL
*                                       ENTRY + QCB
*
         CLC   SCBDESTQ,X1(R1)          IS THIS THE ENTRY
         BNE   SEARCH                   NOT EQUAL - CONTINUE
*
         USING IEDQTRM,R1
         LA    R15,8                    SET UP RETURN CODE
         OC    TRMSTAT+1(2),TRMSTAT+1   IS PROCESS Q OPEN      @OY13294
         BZ    RETURN                   YES, RETURN TO CALLER    S22025
*
         L     R5,TRMSTAT               GET PEWA ADDRESS       @SA71652
         TM    PEWAFLG-IEDQPEWA(R5),PEWASTOP PROCESS ENTRY     @SA71652
*                                         DISABLED             @SA71652
         BO    RETURN                   BR YES, SKIP LOCK      @SA71652
         STC   R2,LCBINSRC+1            LOW ORDER BYTE OF OFFSET
         SRL   R2,8                     SHIFT OVER ONE BYTE
         STC   R2,LCBINSRC              SET HIGH ORDER BYTE
         OI    SCBSTATE,SCBLCK1N+SCBMSGLN   SET MESSAGE LOCK
         OI    PRFSTAT1,PRFLOCK         SET LOCK BIT IN BUFFER
         MVI   LCBINSRC+2,XD0           SET POST PENDING FLAGS
         L     R2,LCBDCBPT              DCB ADDRESS
         USING IHADCB,R2
         LA    RLCB,LCBFLAG1            BUMP TO IOB ADDRESS
         USING LCBFLAG1,RLCB
         TM    DCBDSORG,AVTE80          IS THIS AN LGB           S22024
         LA    R5,X1                    INDICATE RLN ONE         S22024
         BO    TRMQ3705                 IF SO, TAKE OFF          S22024
         SR    R1,R1                    CLEAR FOR IC
         LR    R5,R1                    CLEAR FOR COUNTER
         IC    R1,DCBEIOBX              SIZE OF AN LCB
         L     R10,DCBIOBAD             ADDRESS OF 1ST IOB-LCB SIZE
LCBLOOP  EQU   *
         BCTR  R5,0                     BUMP COUNTER BY ONR
         LA    R10,0(R1,R10)            POINT TO FIRST/NEXT IOB +
*                                       CLEAR HIGH ORDER BYTE
         CLR   R10,RLCB                 IS THIS THE SOURCE LCB
         BNE   LCBLOOP                  BRANCH NO - CONTINUE SEARCH
*
         LPR   R5,R5                    MAKE COUNT POSITIVE
TRMQ3705 EQU   *                                                 S22024
         LH    R1,LCBTTCIN              SOURCE TERMINAL INDEX
         N     R1,AVTCLRHI              CLEAR HIGH ORDER HALF
         L     R15,AVTRNMPT             TERMINAL NAME TABLE
         BALR  R14,R15                  LINK TO TNT
*
         SR    R15,R15                  SET GOOD RETURN
         L     R1,TRMDESTQ-1            ADDRESS OF SOURCE QCB
         STC   R5,QCBLKRLN-IEDQQCB(R1)  SET LOCK RELATIVE LINE #
         USING IEDQQCB,R1                                      @OX12487
         TM    QCBTSOF1,QCBDELAY        QCB IN TIME DELAY QUEUE@OX12487
         BNO   NOTIMEQ                  BRANCH IF NO           @OX12487
         LR    R5,R13                   SAVE REGISTER 13       @OX12487
         LA    R13,AVTSAVE3             GET SAVE AREA          @OX12487
         L     R15,AVTHG02              ADDRESS OF  TIME DELAY @OX12487
*                                        ROUTINE               @OX12487
         BALR  R14,R15                  REMOVE QCB             @OX12487
         LR    R13,R5                   RESTORE REGISTER 13    @OX12487
         MVC   QCBSLINK(THREE),QCBSTCHN  UPDATE STCB CHAIN     @OX13486
         LA    R5,QCBSTVTO              ADDRESS OF LINK FIELD  @OX13486
         IC    R14,QCBSTVTO             SET UP QCB             @OX13486
         ST    R5,QCBSTVTO              FOR SEND               @OX13486
         STC   R14,QCBSTVTO             OPERATION              @OX13486
NOTIMEQ  EQU   *                                               @OX12487
         XC    QCBLKRRN(THREE),QCBLKRRN CLEAR LINE NUMBER      @OZ11170
         TM    X1(R8),X1                EXTENDED LOCK REQUEST
         BNZ   RETURN                   YES, RETURN TO CALLER    S22025
*
         NI    SCBSTATE,SCBMSGLF        SET EXTENDED
RETURN   EQU   *                                                 S22025
         L     R14,AVTUI                LOAD RETURN ROUTINE      S22025
         B     FOUR(R14)                BRANCH TO RETURN ROUT    S22025
         EJECT
NONZ     EQU   7                        SEVEN                    S22024
EQUAL    EQU   8                        EIGHT                    S22024
ZERO     EQU   8                        EIGHT                    S22024
THREE    EQU   3                        CONSTANT               @OX13486
X0       EQU   0                        ZERO                     S22024
X1       EQU   1                        ONE                      S22024
XD0      EQU   X'D0'                    HEX D0                   S22024
BFRD     EQU   X'1A'                    BUFFERED TERMINAL SCHEDULER
         DCBD  DSORG=TX
         TTRMD
         TLCBD
         TSCBD
         TPRFD
         TQCBD
         TAVTD
         TPEWAD                                                @SA71652
         END
