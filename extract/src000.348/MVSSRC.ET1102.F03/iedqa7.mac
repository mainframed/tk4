A701     TITLE '''IEDQA7'' - COUNTER ROUTINE'
IEDQA7   CSECT
         SPACE
***********************************************************************
*STATUS -- CHANGE LEVEL 0                                             *
*                                                                     *
*FUNCTION --                                                          *
*   IF THE COUNTER MACRO WHICH GENERATED LINKAGE TO THE ROUTINE IS    *
*   IN AN INHDR OR OUTHDR SUBGROUP BUT THE BUFFER IS NOT A PHYSICAL   *
*   HEADER,  THE BUFFER IS NOT COUNTED.  RETURN IS MADE TO THE        *
*   CALLER WITH ZEROES IN REGISTER 15.                                *
*                                                                     *
*   THE ROUTINE DETERMINES IF THE CURRENT BUFFER IS A ZERO-LENGTH     *
*   BUFFER.  IF SO,  THE MESSAGE IS NOT COUNTED AND RETURN IS MADE    *
*   TO THE CALLER WITH X'FF' IN REGISTER 15.  OTHERWISE               *
*                                                                     *
*   THE ROUTINE LINKS THE LOCATE OPTION ROUTINE (IEDQAE) TO GET THE   *
*   ADDRESS OF THE OPTION FIELD.  IF THE OPTION FIELD CANNOT BE       *
*   FOUND,  THE MESSAGE IS NOT COUNTED AND RETURN IS MADE TO THE      *
*   CALLER WITH X'FF' IN REGISTER 15.  OTHERWISE                      *
*                                                                     *
*   THE COUNT IN THE OPTION IS INCREMENTED BY ONE.  RETURN IS MADE    *
*   TO THE CALLER WITH ZEROES IN REGISTER 15.                         *
*                                                                     *
*ENTRY POINT --                                                       *
*       'IEDQA701' TO COUNT A HEADER OR TEXT SEGMENT.  CALLING        *
*   SEQUENCE FROM THE MACRO EXPANSION IS:                             *
*                                                                     *
*        L     15,IEDMSGS-1             GET MH VCON TABLE ADDR        *
*        L     15,N(,15)                GET ADDR OF COUNTER RTN       *
*        BALR  1,15                     LINK TO COUNTER ROUTINE       *
*                                                                     *
*   WHERE 'N' IS THE INDEX TO THE ADDRESS OF THE COUNTER ROUTINE      *
*   IN THE MH VCON TABLE.                                             *
*                                                                     *
*INPUT --                                                             *
*   REGISTER 1 - THE ADDRESS OF A MACRO-GENERATED PARAMETER LIST      *
*   FOR THE LOCATE OPTION ROUTINE.                                    *
*                                                                     *
*OUTPUT --                                                            *
*   REGISTER 15 - RETURN CODE-  CONTAINS ZEROES IF SEGMENT IS         *
*   COUNTED.  CONTAINS X'FF' IF OPTION FIELD IS NOT FOUND OR IF       *
*   ZERO-LENGTH BUFFER.                                               *
*                                                                     *
*   OPTION FIELD - INCREMENTED BY ONE                                 *
*                                                                     *
*EXTERNAL REFERENCES -                                                *
*   'IEDQAE' - LOCATE OPTION ROUTINE                                  *
*                                                                     *
*   AVT - ADDRESS VECTOR TABLE                                        *
*                                                                     *
*   BUFFER CURRENTLY BEING PROCESSED                                  *
*                                                                     *
*   COUNTER OPTION FIELD                                              *
*                                                                     *
*EXITS,  NORMAL -- THE COUNTER OPTION FIELD IS INCREMENTED BY ONE.    *
*   REGISTER 15 CONTAINS A RETURN CODE OF ZERO.                       *
*                                                                     *
*EXITS,  ERROR --                                                     *
*   ZERO-LENGTH BUFFER.  THE COUNTER OPTION FIELD IS NOT CHANGED.     *
*   REGISTER 15 CONTAINS A RETURN CODE OF X'FF'.                      *
*                                                                     *
*   OPTION FIELD NOT FOUND.  REGISTER 15 CONTAINS A RETURN CODE OF    *
*   X'FF'.                                                            *
*                                                                     *
*TABLES/WORK AREAS -- W/A                                             *
*                                                                     *
*ATTRIBUTES -- REUSABLE,  REFRESHABLE, ENABLED,  RESIDENT, PROBLEM    *
*   PROGRAM MODE.                                                     *
*                                                                     *
*NOTES -- THE OPERATION OF THIS MODULE DOES NOT DEPEND UPON AN        *
*   INTERNAL REPRESENTATION OF THE EXTERNAL CHARACTER SET.            *
*                                                                     *
***********************************************************************
         EJECT
********* REGISTER EQUATES *********
         SPACE
R1       EQU   1                        WORK REGISTER
         SPACE
R13      EQU   13                       ADDRESS OF AVT
         SPACE
R14      EQU   14                       RETURN REGISTER
         SPACE
R15      EQU   15                       ADDRESS OF OPTION FIELD
         SPACE
********* OTHER EQUATES *********
         SPACE
ONE      EQU   1                        INCREMENT VALUE OF ONE
FOUR     EQU   4                        LENGTH OF IEDQAE PARAMETER
*                                         LIST
HEADER   EQU   X'01'                    INHDR/OUTHDR MACRO FLAG
         EJECT
         USING AVTSAVE2,R13
         USING IEDQA701,R15
         SPACE
IEDQA701 EQU   *
         B     COUNTER                  BRANCH AROUND ID
         DC    CL12'IEDQA7 11/23'
         SPACE
COUNTER  EQU   *
         LA    R14,FOUR(,R1)            ADD 4 TO GET RETURN ADDRESS
         ST    R14,AVTPARM              SAVE RETURN ADDRESS
         L     R14,AVTADBUF             GET ADDRESS OF BUFFER
         USING IEDQPRF,R14
         TM    PRFSTAT1,PRFCNCLN        IS THIS A RECALLED BUFFER
         BO    ZERORTN                  YES, DON'T COUNT
         SPACE
         TM    AVTEZERO(R1),HEADER      IS MACRO IN INHDR/OUTHDR
         BNO   NORMAL                   NO, BRANCH AND COUNT IT
         SPACE
         TM    PRFSTAT1,PRFNHDRN        YES, IS THIS HEADER BUFFER
         BO    ZERORTN                  IT ISN'T, DON'T COUNT, EXIT
         SPACE
NORMAL   EQU   *
         LH    R14,PRFSIZE              PICK UP PREFIX SIZE FIELD
         LTR   R14,R14                  IS THIS A ZERO-LENGTH BFR
         BZ    ERROR                    YES, ERROR, RETURN
         SPACE
         L     R15,AVTUI                GET USER INTERFACE ADDRESS
         BALR  R14,R15                  LINK TO LOCATE OPTION RTN
         SPACE
         BALR  R1,AVTEZERO              SET NEW BASE
         USING *,R1
         LTR   R15,R15                  WAS OPTION FIELD FOUND
         BZ    ERROR                    NO, ERROR, RETURN
         SPACE
         LH    R1,AVTEZERO(,R15)        GET COUNT IN OPTION FIELD
         LA    R1,ONE(,R1)              ADD ONE
         STH   R1,AVTEZERO(,R15)        STORE IT BACK
         SPACE
ZERORTN  EQU   *
         SR    R15,R15                  SET GOOD RETURN CODE
         L     R14,AVTPARM              GET RETURN ADDRESS
         BR    R14                      RETURN
         SPACE
ERROR    EQU   *
         L     R14,AVTPARM              GET RETURN ADDRESS
         LA    R15,AVTEFF               SET ERROR RETURN CODE
         BR    R14                      RETURN
         EJECT
********* DSECTS *********
         TAVTD
         EJECT
         TPRFD
         SPACE
         END
