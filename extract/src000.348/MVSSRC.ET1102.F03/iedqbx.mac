DQBX TITLE '''IEDQBX''   LOG SEGMENT'
IEDQBX   CSECT
*C210000,235000,535000-550000,557800,605000,690000,841000,847000 S22025
*C710000                                                        SA56630
*A590000,745000,800000                                         @SA72459
*A655000                                                       @XA11336
*                                                                     *
***********************************************************************
*                                                                     *
*TITLE 'IEDQBX'  LOG SEGMENT                                          *
*                                                                     *
*STATUS: CHANGE LEVEL 5                                               *
*                                                                     *
*FUNCTION -- THIS ROUTINE WILL WRITE A MESSAGE SEGMENT ONTO THE       *
*   LOGGING MEDIUM SPECIFIED BY THE USED.  THIS IS DONE AS IN LINE    *
*   CODE IN THE MH.  A WRITE WILL BE ISSUED FOR EACH UNIT OF THE      *
*   BUFFER, THEN A CHECK WILL BE ISSUED FOR EACH WRITE.  NP OTHER     *
*   PROCESSING WILL BE DONE BY TCAM WHILE THE LOG TAKES PLACE         *
*                                                                     *
*ENTRY POINTS -- 'IEDQBX', TO LOG A MESSAGE SEGMENT                   *
*   CALLING SEQUENCE  --  LA  R1,PARLIST                              *
*                         LA   R12,IEDQBX                             *
*                         BR   R12                                    *
*                                                                     *
*INPUT -- THE LOG MACRO WHEN ISSUED IN THE INHDR, OUTHDR, INBUF, OR   *
*   OUT BUF SUBGROUP, WILL EXPAND A 1 WORD PARAMETER LIST.  IT HAS IN *
*   THE HIGH ORDER BYTE AN OFFSET INTO IEDQMSGS TO THE VCON OF        *
*   IEDQBX WHICH WILL ALLOW IEDQUI TO CALL THE ROUTINE.  THE LOW      *
*   ORDER 3 BYTES WILL CONTAIN A 3 BYTE ADDRESS OF THE DCB OT BE      *
*   USED TO LOG THE SEGMENT.                                          *
*   IEDQUI WILL PASS THE PARAMETER LIST ADDRESS IN R1 AND THE         *
*   ADDRESS OF THE BUFFER TO BE WRITTEN IN R6.                        *
*                                                                     *
*OUTPUT -- IEDQBX WILL ATTEMPT TO LOG THE SEGMENT ON THE LOGGING      *
*   MEDIUM SPECIFIED IN THE DCB.  ON RETURN TO MH, R15 WILL           *
*   CONTAIN THE FOLLOWING RETURN CODES -                              *
*   / -- SUCCESSFULLY WRITTEN                                         *
*   4 -- DCB NOT OPEN                                                 *
*   IF AN ERROR OCCURRED IN WRITING THE BUFFER, BSAM WILL GIVE        *
*   CONTROL TO THE USER'S SYNAD EXIT IN THE DCB.  THIS EXIT MUST      *
*   RETURN TO THE INSTRUCTION FOLLOWING THE CHECK MACRO.              *
*                                                                     *
*EXTERNAL ROUTINES -- IEDQUI - LOG MACRO WILL BALR TO IEDQUI AND      *
*   UI WILL CALL IEDQBX.                                              *
*   IEDQUI+4 - IEDQBX WILL EXIT TO IEDQUI+4 TO RESTORE THE       S22025
*   USER'S REGISTERS AND RETURN TO THE NEXT INSTRUCTION.              *
*   BSAM WRITE - TO WRITE THE UNITS OF THE BUFFER                     *
*   BSAM CHECK - TO CHECK THE WRITES                                  *
*                                                                     *
*EXITS-NORMAL -- IEDQBX WILL EXIT TO IEDQUI+4 WHICH RETURNS TO   S22025
*   THE INSTRUCTION AFTER THE LOG MACRO, R15 WILL CONTAIN 0.          *
*                                                                     *
*EXITS-ERROR -- IF THE DCB IS NOT OPEN, IEDQBX WILL EXIT AS ABOVE     *
*   WITH R15 CONTAINING 4.                                            *
*   IF A WRITE WAS NOT SUCCESSFUL, THE CHECK MACRO WILL GO TO THE     *
*   SYNAD EXIT OF THE DCB.                                            *
*                                                                     *
*TABLES/WORK AREAS -- DCBD, AVTD                                      *
*   AVTFIELDS - AVTLM                                                 *
*                                                                     *
*ATTRIBUTES -- REUSABLE,REFRESHABLE, RESIDENT, ENABLED                *
*                                                                     *
*NOTES -- THE OPERATION OF THIS MADULE DOES NOT DEPEND UPON A         *
*   PARTICULAR INTERNAL REPRESENTATION OF THE EXTERNAL CHARACTER      *
*   SET.                                                              *
*                                                                     *
***********************************************************************
*                                                                     *
*NOTES -- THE DCB OPENED MUST BE A BSAM DCB.                          *
*   OPERANDS ARE --                                                   *
*   DSORG=PS                                                          *
*   BFALN=  OMITTED                                                   *
*   BLKSIZE=  (KEYLENGTH)  ALL UNITS WILL BE WRITTEN WITHOUT THE      *
*        12 BYTE UNIT MANAGEMENT AREA AND ALL ARE THE SAME SIZE       *
*   BUFCB=  OMITTED                                                   *
*   BUFL=  OMITTED                                                    *
*   DDNAME=  (USED OPTION)                                            *
*   NCP=  SHOULD BE THE MAXIMUM NUMBER OF UNITS THAT CAN APPEAR IN    *
*        A BUFFER.  THIS IS A USER OPTION - IT CAN BE ANYTHING.       *
*   RECFM=F                                                           *
*   MACRF=W                                                           *
*   SYNAD=  (USER SPECIFIED - SHOULD CONFORM TO STANDARDS FOR         *
*        SYNAD EXITS - ERRORS THAT MAY HAVE BEEN ENCOUNTERED WILL     *
*        IGNORED BY LOG.)                                             *
*                                                                     *
*   A GET MAIN WILL PROVIDE AS MANY CONTROL BLOCKS AS SPECIFIED IN    *
*   THE NCP OPERAND OF THE DCB.  THEY WILL BE IN A CONTIGUOUS         *
*   AREA.  THERE WILL BE A CONTROL AREA FOR EACH DCB USED FOR LOG.    *
*                                                                     *
*                                                                     *
*                         REGISTER EQUATES                            *
R0       EQU   0                        GET MAIN PAR LIST
RPARM    EQU   1                        ADDR OF THE PAR LIST PASSED
RDECB    EQU   2                        ADDR OF WRITE CNTRL BLK
RAREA    EQU   3                        ADDR OF AREA TO BE WRITTEN
R4       EQU   4
RNCP     EQU   5                        NO. OF WRITE CONTROL BLKS
RPREFIX  EQU   6                        ADDR OF BUFFER TO WRITE
RNB      EQU   7                        NO. OF UNITS IN BFR
RUNIT    EQU   8                        ADDR OF UNIT TO WRITE
RAVT     EQU   9                        ADDR OF AVT
RDCB     EQU   10                       DCB ADDRESS FRO LOG
R11      EQU   11
RBASE    EQU   12                       ENTRY POINT ADDRESS
R13      EQU   13
RETURN   EQU   14                       RETURN ADDRESS
R15      EQU   15
FOUR     EQU   4                        OFFSET USED ON RETURN    S22025
*
*                         USING STATMENTS                             *
         USING IEDQAVTD,RAVT            AVT  BASE                S22025
         USING IHADCB,RDCB              DCB  BASE                S22025
         USING IEDQPRF,RPREFIX          PREFIX BASE              S22025
         USING *,RBASE                  BASE FOR ROUTINE         S22025
*                                                                     *
IEDQBX01 EQU   *                                                 S22025
IEDQBX   IEDHJN ENTRY                                            S22025
         NC    PRFSIZE(2),PRFSIZE       ZERO LENGTH BFRS
         LA    R15,8                    SHOULD NOT BE LOGGED
         BZ    EXIT                      IF 0 EXIT WITH 8 RTN CODE
         L     RDCB,0(RPARM)            DCB ADDRESS
         TM   DCBOFLGS,OPEN             HAS THE DCB BEEN OPENED
         BO    OPENED                   BR YES
ERROR    EQU   *                                               @SA72459
         LA    R15,4                    SET RETURN CODE
         B     EXIT                     RETURN WITH 4 ERROR CODE S22025
OPENED   EQU   *
         TM    DCBEODAD+3,E01           HAS THE GETMAIN BEEN DONE
*              THE EODAD FIELD HAS A VALUE OF 1 IF NO EODAD IS
*              SPECIFIED.  THE EODAD EXIT WILL NEVER BE USED SINCE IT
*              APPLIES ONLY TO READ
         BNO   OK                       BR YES
         SR    RNCP,RNCP                CLEAR REG
         IC    RNCP,DCBNCP              NO. OF DECB'S NEEDED
         LTR   RNCP,RNCP                ANY WRITES REQD        @XA11336
         BZ    EXIT                     BR NO, EXIT            @XA11336
         LA    R4,DECBSZ                SIZE OF A DECB
         MR    R4,R4                    SIZE OF TOTAL REA IF R5
         LR    R0,RNCP                  SET FOR GETMAIN
         GETMAIN  R,LV=(0)
         IC    R0,DCBEODAD              SAVE HI ORDER BYTE
         ST    RPARM,DCBEODAD           ADDR OF DECBS IN DCB
         STC   R0,DCBEODAD              RESTORE HI-ORDER BYTE    S22025
OK       EQU   *
         SR    RNB,RNB                  CLEAR REGS
         SR    RNCP,RNCP
         EJECT
*              THE FOLLOWING CODE WILL DETERMINE THE NUMBER OF  SA56630
*              BUFFER UNITS THAT CONTAIN VALID DATA AND SHOULD  SA56630
*              BE LOGGED                                        SA56630
         XC    AVTDOUBL+4(4),AVTDOUBL+4      ZERO WORK AREA     SA56630
         MVC   AVTDOUBL+6(2),AVTKEYLE        MSG Q LENGTH       SA56630
         SR    RDECB,RDECB                   CLEAR WORK REG     SA56630
         LH    RAREA,PRFSIZE                 LOAD DATA LENGTH   SA56630
         N     RAREA,AVTCLRHI                CLEAR HI ORDR BYTE SA56630
         D     RDECB,AVTDOUBL+4              CALC NO UNITS      SA56630
         LR    RNB,RAREA                     NO. UNITS TO WRITE SA56630
         LTR   RDECB,RDECB                   EVEN MULTIPLE      SA56630
         BNP   NOADD                         BRANCH IF YES      SA56630
         LA    RNB,E01(RNB)                  INCREMENT COUNT    SA56630
NOADD    EQU   *
         EJECT
         LR    RUNIT,RPREFIX            ADDR FIRST UNIT OF BFR
WRITE1   EQU   *
         L     RDECB,DCBEODAD           ADDR FIRST DECB
WRITE    EQU   *
        LA    RNCP,1(RNCP)              ADD TO NO OF WRITES
         LR    RPARM,RDECB              ADDR OF DECB FOR WRITE
         LA    RAREA,AVTUMALN(RUNIT)    ADD 12 BYTES FR ADDR
         TM    DCBOFLGS,OPEN            DCB OPENED?            @SA72459
         BNO   ERROR                    BRANCH IF NO           @SA72459
         WRITE   (1),SF,(10),(3),MF=E
         BCT   RNB,NEXTUNIT             DECR NO. OF UNITS
         B    ALLBFR                    HERE IF ALL WRITTEN
NEXTUNIT EQU   *
         L     RUNIT,PRFTIC-IEDQPRF(RUNIT)
         LA    RDECB,DECBSZ(RDECB)      NEXT DECB ADDR
         EX    RNCP,CLI                 IS THIS ALL THE WRITES THAT
         BH    WRITE                    CAN BE DONE - BR YES
ALLBFR   EQU   *
         L     RDECB,DCBEODAD           ADDR FIRST DECB TO CHECK
CHECK    EQU   *
         TM    DCBOFLGS,OPEN            DCB OPENED?            @SA72459
         BNO   ERROR                    BRANCH IF NO           @SA72459
         CHECK   (2)
         LA    RDECB,DECBSZ(RDECB)      NEXT DECB ADDR
         BCT   RNCP,CHECK               DECRMENT NO. UNITS
         LTR   RNB,RNB                  ANY UNITS LEFT
         BNZ   WRITE1                   BR YES
         SR    R15,R15                  SET RETURN CODE
EXIT     EQU   *
         L     RBASE,AVTUI              GET RET INTERFACE ADDR   S22025
         B     FOUR(RBASE)              BRANCH TO RETURN ROUTINE S22025
OPEN     EQU   X'10'
E01      EQU   X'01'
DECBSZ   EQU   20
CLI      CLI   DCBNCP,X'00'             NO. OF WRITE CONTROL BLKSS22025
         TPRFD
         DCBD  DSORG=BS
         TAVTD
         END
