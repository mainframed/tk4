         TITLE 'IEDAYAA  -  TSO/TIOC ATTENTION ROUTINE'
IEDAYAA  CSECT
***********************************************************************
***********************************************************************
*                                                                     *
*                  RELEASE 3  VS/2  PTF
*
*
*
*  C 098650-098850                                              ZA24878
*  A 512900                                                     ZA12528
*  D 519700                                                     ZA12528
*0000087000,268200-269110,270910-270950                        @ZA08015
*0000280810-281700,509410-509460,519310                        @ZA08015
*         RELEASE 3 VS/2 CHANGES
*  ZA00968 - SEE SA70323                                        ZA00968
*A191300,191400                                                 ZA02235
*         RELEASE VS2/2 CHANGES                                 YS02019
*                                       9/25/1973               YM03889
*                                       1/23/1974               YM06727
*         RELEASE 21 DELETIONS                                        *
*0966727427                                                      A50694
*0966                                                            M0167
*0966073100,083100,092000-092200,092700,120600-121500,159300-    S21008
*0966160200,698400,701100-703800,707400-725600,727270,727354-    S21008
*0966727606,727669-841500                                        S21008
*0966000400,087000                                               M0891
*          RELEASE 20 DELETIONS                                       *
*                                                                 M4034
*                                                                M3835
*0000099780,100440-100500,100620-100680,175800,327600,330300,    TS1637
*0000600300,678600,680800,697500,719100,720900,724500-727200,    TS1637
*0000766800-782100,929700,934300,936900-937800,954000,958500-    TS1637
*0000999999                                                      TS1637
*0000621000,641900-684900                                        M0029
*0000                                                            TS0035
*                                                                 M2265
*                                                                M3658
*                                                                     *
*STATUS CHANGE LEVEL 003                                        YS02019
*                                                                     *
* FUNCTION -                                                          *
*                                                                     *
*    QTIP0000 - ENTERED FROM ATTN IEDAYA                              *
*                                                                     *
*         WHEN ATTENTION EXIT IS AVAILABLE -                    YS02019
*                                                                     *
*    * SET NEW ATTENTION COUNT IN TSB                           YS02019
*    * POST THE RCT TO SCHEDULE AN ATTENTION EXIT               YS02019
*    * CLEAR INPUT AND OUTPUT QUEUES EXCEPT ASID MESSAGES       YS02019
*    * RESET AUTOMATIC LINE NUMBERING BITS IN TSB               YS02019
*    * REMOVE USER AND SYSTEM LWAIT, USER AND SYSTEM OWAIT      YS02019
*                                                                     *
*         WHEN ATTENTION EXIT IS NOT AVAILABLE -                YS02019
*                                                                     *
*    * TURN ON TSBATTN IF USER IS IN OWAIT                      YS02019
*    * CLEAR INPUT AND OUTPUT QUEUES EXCEPT ASID MESSAGES       YS02019
*    * REMOVE USER AND SYSTEM LWAIT, USER AND SYSTEM OWAIT      YS02019
*                                                                     *
*    QTIP0030 - ENTERED FROM IEDAYOO FOR HANGUP                       *
*                                                                     *
*    1. CLEAR INPUT AND OUTPUT QUEUES INCLUDING TJID MESSAGES         *
*    2. REMOVE SYSTEM LWAIT, USER AND SYSTEM OWAIT                    *
*                                                                     *
*    QTIP0210 - ENTERED FROM IGG09401                           YS02019
*                                                                     *
*    1. CLEAR INPUT QUEUE                                             *
*    2. REMOVE SYSTEM LWAIT AND SYSTEM OWAIT                          *
*                                                                     *
*    QTIP0220 - ENTERED FROM IGG09401                           YS02019
*                                                                     *
*    1. CLEAR OUTPUT HEADER QUEUE EXCEPT TJID MESSAGES                *
*    2. REMOVE SYSTEM LWAIT, USER AND SYSTEM OWAIT                    *
*                                                                     *
* ENTRY POINTS -                                                      *
*                                                                     *
*         QTIP0000 - QTIP 0 ENTRY FROM IEDAYA                         *
*         QTIP0030 - BRANCH ENTRY FROM IEDAYOO DURING HANGUP          *
*         QTIP0210 - QTIP 21 ENTRY FROM IGG09401                YS02019
*         QTIP0220 - QTIP 22 ENTRY FROM IGG09401                YS02019
*                                                                     *
* INPUT -                                                             *
*                                                                     *
*         WHEN ENTERED FROM IEDAYA                              YS02019
*                                                                     *
*    REGISTER 3 - SCB                                           YS02019
*    REGISTER 4 - LCB                                                 *
*    REGISTER 5 - ASCB                                          YS02019
*    REGISTER 6 - TSB                                                 *
*    REGISTER 7 - QCB                                           YS02019
*    REGISTER 10 - TIOCRPT                                      YS02019
*                                                                     *
*         WHEN ENTERED FROM IEDAYOO                             YS02019
*                                                                     *
*    REGISTER  6 - TSB                                          YS02019
*    REGISTER 10 - TIOCRPT                                      YS02019
*                                                                     *
*         WHEN ENTERED FROM IGG09401                            YS02019
*                                                                     *
*    REGISTER 15 - TSB                                          YS02019
*                                                                     *
* OUTPUT - SAME AS INPUT                                              *
*                                                                     *
* EXTERNAL REFERENCES -                                               *
*         IEA0PT01 - BRANCH ENTRY TO POST ROUTINE                     *
*         TIOCSAVE - REGISTER SAVE AND WORK AREA IN TIOCRPT           *
*                                                                     *
* EXITS,NORMAL -                                                      *
*    SET RETURN CODE REG. 15 TO 0                                     *
*    BR 14 RETURN TO CALLER                                           *
*                                                                     *
* EXITS,ERROR - NONE                                                  *
*                                                                     *
* TABLES/WORKAREAS -                                                  *
*    TIOCSAVE - 72 BYTE REGISTER SAVE AND WORK AREA IN TIOCRPT  YS02019
*                                                                     *
* ATTRIBUTES - REUSABLE,ENABLED                                 YS02019
*                                                                     *
* CHARACTER CODE DEPENDENCIES - NONE                                  *
*                                                                     *
* NOTES - N/A                                                         *
*                                                                     *
***********************************************************************
***********************************************************************
         EJECT
* E Q U A T E S
R0       EQU   0                        PARM,WORK REG.
R1       EQU   1                        PARM,WORK REG.
R2       EQU   2                        ATTN COUNT AND TJID
R3       EQU   3                        SCB                     YS02019
R4       EQU   4                        LCB
R5       EQU   5                        ASCB                    YS02019
R6       EQU   6                        TSB
R7       EQU   7                        QCB
R8       EQU   8                        WORK REG AND RCB
R9       EQU   9                        WORK REG
R10      EQU   10                       TIOCRPT                 YS02019
R11      EQU   11                       WORK REG
R12      EQU   12                       BASE REG
R13      EQU   13                       SAVE AREA
R14      EQU   14                       RETURN TO CALLER
R15      EQU   15                       RETURN FROM INTERNAL SUBRTN
E0       EQU   0                        LENGTH/DISPLACEMENT CONSTANT
E1       EQU   1                        LENGTH/DISPLACEMENT CONSTANT
E3       EQU   3                        LENGTH/DISPLACEMENT CONSTANT
E4       EQU   4                        LENGTH/DISPLACEMENT CONSTANT
E5       EQU   5                        LENGTH/DISPLACEMENT CONSTANT
E16      EQU   16                       LENGTH/DISPLACEMENT     YS02019
E24      EQU   24                       LENGTH/DISPLACEMENT CONSTANT
OFF      EQU   X'FF'                    TO TURN OFF BITS
ON       EQU   4                        FOR TEST AND SET COND.
         SPACE 2
*
* DEFINE ENTRIES HERE
*
         ENTRY QTIP0000,QTIP0030,QTIP0210,QTIP0220               S21008
         USING IEDQSCB,R3                                        S21008
         USING ASCB,R5                                          YS02019
         USING TSB,R6
         USING TIOCRPT,R10              ADDRESSABILITY OF RPT   YS02019
         USING ATTNSAVE,R13                                      TS1637
         DC    CL8'IEDAYAA'             MODULE ID                S21008
         DC    X'6098'                  JULIAN DATE - 4/07/76  @ZA08015
*
* ENTRY 0
*
*
QTIP0000 DS    0H
         USING *,R12
         LA    R13,TIOCSAVE             GET RPT SAVE AREA       YS02019
         CLI   TSBSTAX,AVTEZERO         IS STAX EXIT AVAILABLE  YS02019
         BE    QTIPE2                   NO, DO ATTN IGNORED      S21008
*                                       PROCESSING
ATTNCT   EQU   *                                                YS02019
         CLC   TSBATTNC,TSBSTAX         IS ATTN LEVEL AVAILABLE YS02019
         BE    QTIPE2                   NO, IGNORE ATTENTION    YS02019
         L     R1,TSBATTNC              GET FULL WORD - BYTE 0  YS02019
*                                       HAS ATTNC, BYTE 1 HAS   YS02019
*                                       STAX                    YS02019
         LR    R2,R1                    PUT IT IN WORK REGISTER YS02019
         SRL   R2,E24                   RIGHT ADJUST ATTNC      YS02019
         TM    SCBERR3,SCBATTN          HARDWARE ATTN RECEIVED  YS02019
         BNO   CALCATCT                 NO, GO TO CALCULATE SIM YS02019
*                                       ATTENTION ACOUNT        YS02019
         LA    R2,E1(,R2)               INCREMENT ATTN CT BY 1  YS02019
         B     UPDATE                   GO TO UPDATE TSBATTNC   YS02019
         SPACE 1
CALCATCT EQU   *                                                YS02019
         SR    R0,R0                    CLEAR REGISTER          YS02019
         IC    R0,SCBSALEV              GET ATTN LEVEL ENTERED  YS02019
         SRL   R0,E4                    RIGHT ADJUST VALUE      YS02019
         AR    R2,R0                    OBTAIN NEW ATTN COUNT   YS02019
         LA    R0,E0(,R1)               GET STAX COUNT          YS02019
         SRL   R0,E16                   RIGHT ADJUST VALUE      YS02019
         CR    R2,R0                    IS NEW ATTN CT GT STAX  YS02019
         BNH   UPDATE                   NO, UPDATE TSBATTNC     YS02019
         LR    R2,R0                    OTHERWISE MAKE ATTN CT  YS02019
*                                       EQUAL TO STAX CT        YS02019
         SPACE 1
UPDATE   EQU   *                                                YS02019
         SLL   R2,E24                   PUT NEW ATTNC IN BYTE 0 YS02019
         LA    R0,E0(,R1)               CLEAR HIGH ORDER BYTE   YS02019
         OR    R2,R0                    MERGE IN BYTES 1-2-3    YS02019
         CS    R1,R2,TSBATTNC           STORE NEW ATTN COUNT    YS02019
         BNE   ATTNCT                   LOOP UNTIL IT'S DONE    YS02019
         SPACE 1
         NI    SCBERR3,OFF-SCBXPI-SCBXPD  RESET ERROR BITS      YS02019
         NI    TSBFLG4,OFF-TSBOWAIT-TSBIWAIT-TSBHOLD RESET HOLD ZA24878
*                       AND WAIT BITS...(SINCE RCT WILL REMOVE  ZA24878
*                                       IWAIT/OWAIT)            YS02019
*                                                               YS02019
* POST THE RCT TO SCHEDULE ATTENTION EXIT                       YS02019
*                                                               YS02019
         STM   R8,R14,SAVE8             SAVE REGISTERS          YS02019
         LR    R8,R13                   SAVE AREA POINTER       YS02019
         SR    R10,R10                  COMPLETION CODE IS ZERO YS02019
         LA    R11,ASCBECB              PASS THE ECB ADDRESS    YS02019
         O     R11,HIGHBIT              HI ORDER BIT ON FOR XM  YS02019
*                                       POST                    YS02019
         L     R12,CVTPTR               GET CVT ADDRESS         YS02019
         L     R15,CVT0PT01-CVT(,R12)   GET POST ROUTINE'S EPA  YS02019
         LA    R12,CVTBRET-CVT(,R12)    GET ADDRESS OF CVTBRET  YS02019
         LR    R13,R5                   ADDRESS OF USER'S ASCB  YS02019
         MODESET EXTKEY=SUPR            CHANGE INTO KEY 0       YS02019
         BALR  R14,R15                  GO TO POST RCT          YS02019
         MODESET EXTKEY=TCAM            CHANGE BACK TO TCAM KEY YS02019
         LR    R13,R8                   GET SAVE AREA POINTER   YS02019
         LM    R8,R14,SAVE8             RESTORE REGISTERS       YS02019
         SPACE
         MVI   CLRSW,CLRTRL+CLRATTN     INDICATE TRLR Q TO BE CLEARED
         BAL   R15,CLROUTQ              CLEAR OUTPUT QUEUES
         BAL   R15,CLRINQ               CLEAR INPUT QUEUES
         SPACE
         NI    TSBFLG2,OFF-TSBAUTON-TSBSTAUT   RESET AUTOLN BITS
         SPACE
         MVI   RMVSW,RMVULW+RMVSLW+RMVUOWT+RMVSOWT    REMOVE SYSTEM
         BAL   R15,RMVWAITS             TYPE WAITS
         SPACE
         B     RETURN                    RETURN TO CALLER
         SPACE 1
QTIPE2   EQU   *                                                 S21008
         NI    SCBERR3,OFF-SCBXPD-SCBATTN-SCBSATTN  RESET        S21008
         OI    SCBERR3,SCBXPI           ATTN IGNORED MESSAGE     S21008
         TM    TSBFLG4,TSBOWAIT         IS USER IN OWAIT        YS02019
         BNO   QENTRY2A                 NO-DON'T SET TSBATTN
         OI    TSBFLG3,TSBATTN          YES-SET TSBATTN
         SPACE 1
QENTRY2A EQU   *
         MVI   CLRSW,CLRTRL+CLRATTN+CLRIGN   SET CLEAR SWITCHES
         BAL   R15,CLROUTQ              CLEAR OUTPUT QUEUES
         BAL   R15,CLRINQ               CLEAR INPUT QUEUES
         SPACE
         MVI   RMVSW,RMVULW+RMVSLW+RMVUOWT+RMVSOWT    REMOVE SYSTEM
         BAL   R15,RMVWAITS                   TYPE WAITS
         SPACE
         B     RETURN                   RETURN TO CALLER
         DROP  R3,R5                    DROP SCB AND ASCB BASE YS02019
         SPACE 3
*
* ENTRY 3
*
QTIP0030 DS    0H
         LA    R13,TIOCSAVE             GET SAVE AREA ADDRESS   YS02019
         SPACE
         MVI   CLRSW,CLRTRL+CLRTJID+CLRHANG
         BAL   R15,CLROUTQ              CLEAR OUTPUT QUEUES
         BAL   R15,CLRINQ               CLEAR INPUT QUEUES
         SPACE
         MVI   RMVSW,RMVSLW+RMVUOWT+RMVSOWT    REMOVE SYSTEM
         BAL   R15,RMVWAITS                    TYPE WAITS
         SPACE
         B     RETURN                   RETURN TO CALLER
         SPACE 3
*
* ENTRY 21
*
QTIP0210 DS    0H
         BAL   R1,FINDSAVE              FIND THE RPT SAVEAREA   YS02019
         LR    R6,R15                   GET TSB ADDRESS         YS02019
         TM    TSBFLG4,TSBHUNG          DID USER HANG UP        YS02019
         BCR   1,R14                    YES, RETURN             YS02019
         SPACE 2
         TM    TSBFLG2,TSBBIPI          IS REPROMPT IN PROCESS  ZA02235
         BOR   R14                      YES, RETURN             ZA02235
         MVI   CLRSW,CLRTCLRQ           INDICATE CALL FROM TCLEARQ
         BAL   R15,CLRINQ               GO CLEAR INPUT Q
         SPACE 2
         MVI   RMVSW,RMVSLW+RMVSOWT            REMOVE SYSTEM
         BAL   R15,RMVWAITS                         REMOVE SYSTEM
         SPACE 2
         B     RETURN                   RETURN TO CALLER
         SPACE 3
*
* ENTRY 22
*
QTIP0220 DS    0H
         BAL   R1,FINDSAVE              FIND THE RPT SAVEAREA   YS02019
         LR    R6,R15                   GET TSB ADDRESS         YS02019
         TM    TSBFLG4,TSBHUNG          DID USER HANG UP        YS02019
         BCR   1,R14                    YES, RETURN             YS02019
         SPACE 2
         MVI   CLRSW,CLRTCLRQ           INDICATE CALL FROM TCLEARQ
         BAL   R15,CLROUTQ              GO CLEAR OUTPUT Q
         SPACE 2
         MVI   RMVSW,RMVSLW+RMVUOWT+RMVSOWT    REMOVE SYSTEM
         BAL   R15,RMVWAITS                   TYPE WAITS
         SPACE 2
RETURN   EQU   *
         SR    R15,R15                  PASS GOOD RETURN
         BR    R14                      RETURN TO CALLER
         SPACE 2                                                 TS1637
*
* THIS SUBROUTINE LOCATES THE SAVE AREA IN TIOCRPT AND RETURNS  YS02019
* THE ADDRESS IN REGISTER 13                                    YS02019
*
FINDSAVE EQU   *                                                 TS1637
         L     R10,CVTPTR               GET CVT ADDRESS         YS02019
         L     R10,CVTAQAVT-CVT(,R10)   GET TCS ADDRESS         YS02019
         L     R10,TCXRPT-IEDQTCX(,R10) GET RPT ADDRESS         YS02019
         LA    R13,TIOCSAVE             GET RPT SAVE AREA       YS02019
         BR    R1                       RETURN TO CALLER        YS02019
         EJECT
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
*  C L I N Q- CLEAR INPUT QUEUE
*
*            INPUT-  R6   TSB
*                    R10  TIOCRPT                               YS02019
*                    R15  RETURN
*            THE FOLLOWING REGISTERS ARE VOLATILE OVER THIS SUBR
*                    R0
*                    R1
*                    R2                                         YS02019
*                    R3                                         YS02019
*                    R8
*                    R9
*                    R11
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
CLRINQ   DS    0H
         USING IEDQQCB,R7               ADDRESSABILITY FOR QCB  YS02019
         ST    R15,SAVE15               SAVE RETURN ADDRESS     YS02019
         SR    R3,R3                    CLEAR LINE CT REGISTER  YS02019
         NI    TSBFLG2,OFF-(TSBBRKIN+TSBBIPI) END BREAKIN PROCESSING
         TM    CLRSW,CLRTCLRQ           CALL FROM TCLEARQ?      YM2814
         BO    CLINSPIT                 YES, DON'T RESET SPIT,  YM2814
*                                       OR IFLSH
         NI    TSBFLG1,OFF-TSBIFLSH     TURN OFF IFLSH (SET BY    M0029
*                                       TCLEARQ)                  M0029
         NI    TSBFLG3,OFF-TSBSPIT      TURN OFF STOP PROMPT BIT  M0029
CLINSPIT EQU   *                                                YM2814
*
* CHECK FOR ANY BUFFERS ON TRAILER Q
*
         OC    TSBITBFP,TSBITBFP        IS  TRLR Q EMPTY
         BZ    CLIHQ                    YES CLEAR H Q
*
* PREPARE TO CLEAR TRAILER Q
*
         L     R11,TSBITBFP-E1          TRAILR QUE PTR
         USING TIOCBUF,R11
         L     R1,TSBIBFP-E1            SAVE HDR Q PTR          YS02019
         XC    TSBITBFP,TSBITBFP        ZAP T Q
         XC    TSBIBFP,TSBIBFP          CLEAR INPUT HDR BFFR POINTER
         B     CLIXBUFF                 GO REMOVE BUFFER
*
* PREPARE TO CLEAR HEADER Q
*
CLIHQ    DS    0H
         OC    TSBIBFP,TSBIBFP          TEST IF INPUT Q EMPTY
         BZ    CLIEXITA                 YES,GO TO EXIT
         L     R11,TSBIBFP-E1           HEADER QUE PTR
         XC    TSBIBFP,TSBIBFP          CLEAR H Q PTR
         SPACE 2
CLIA     DS    0H
         L     R1,BUFFHEAD-E1           SAVE NEXT HEADER PTR    YS02019
         LA    R11,0(R11)               CLEAR FOR TEST
         LTR   R11,R11                  DID WE JUST PROCESS LAST MSG
         BZ    CLIEXIT                  YES,BEGIN EXIT PROCESSING
         TM    BUFFFL1,BUFFNLCR         HAS MSG BEEN COUNTED
*                                       BY TSINPUT
         BZ    CLIXBUFF                 IF NOT, DON'T COUNT
         LA    R3,E1(R3)                ADD 1 TO LINE COUNT     YS02019
         SPACE 2
*
* REMOVE BUFFER FROM INPUT QUE AND PUT ON FREE QUE IF          @ZA08015
* IT IS DETERMINED IT IS NOT ALSO ON OUTPUT QUE(BUFFIHOT).     @ZA08015
*  IF ON BOTH QUES, DROP BUFFER FROM INPUT QUE BUT DONT        @ZA08015
* PUT ON FREE QUE.                                             @ZA08015
*                                                              @ZA08015
CLIXBUFF DS    0H
         L     R8,BUFFTRLR-E1           SAVE NEXT TRLR PTR
         TM    BUFFFL1,BUFFIHOT         BUFFER ON BOTH QUES?   @ZA08015
         BNO   CLIXBUF1                 NO, PUT IT ON FREE QUE @ZA08015
         NI    BUFFFL1,OFF-BUFFIHOT     TURN OFF FLAG          @ZA08015
         B     CLISKIP                  GET NEXT TRAILER       @ZA08015
CLIXBUF1 EQU   *                                               @ZA08015
         LR    R2,R11                   BFR TO BE RETURNED      YS02019
         L     R15,AYQBRAD              BFR RETURN RTN'S EPA    YS02019
         BALR  R0,R15                   GO TO RETURN BFR        YS02019
         SPACE
*
* PREPARE NEXT TRAILER FOR REMOVAL
*                                                              @ZA08015
CLISKIP  EQU   *                                               @ZA08015
         LA    R11,0(R8)                GET TRAILER POINTER
         LTR   R11,R11                  WAS THIS THE LAST PART OF MSG
         BNZ   CLIXBUFF                 NO,CLEAR ANOTHER TRAILER
         SPACE
*
* PREPARE TO PROCESS NEXT MSG
*
         LR    R11,R1                   GET NEXT MSG HDR PRT    YS02019
         B     CLIA                     AND GO BACK
*
* ENTERED UPON COMPLETION OF CLEARING INPUT Q.
* LAST MSG JUST PROCESSED, DO EXIT PROCESSING
*
CLIEXIT  EQU   *
         MVI   TSBNIBF,E0               CLEAR TSBNIFB           YS02019
CLIEXITA EQU   *
         TM    CLRSW,CLRHANG            FROM HANGUP
         BO    CLIEND                   YES,BRANCH
         TM    CLRSW,CLRTCLRQ           CALLED BY TCLEARQ       YS02019
         BO    IFLSHON                  YES, SKIP BFR CHECKING  YS02019
         TM    QCBTSOF2,QCBBUFQ         IS BUFF ON HOLD Q
         BNO   CLIEXITB                 NO,BRANCH TO EXIT
         SPACE 1
IFLSHON  EQU   *                                                YS02019
         OI    TSBFLG1,TSBIFLSH         TELL TSINPUT TO FLSH TCAM BUFS
CLIEXITB EQU   *
         TM    CLRSW,CLRIGN+CLRTCLRQ    CALLED BY ATTN IGNORED  YM06727
*                                       OR TCLEARQ              YM06727
         BNZ   CLIEXITC                 YES,DON'T TURN OFF QCBREAD/TGET
         LM    R0,R1,TSBTPOST           GET TSBTPOST CONDITION  YS02019
RETRY1   EQU   *                                                YS02019
         LR    R8,R0                    .PUT TSBTPOST STATUS IN YS02019
         LR    R9,R1                    .WORK REGISTERS         YS02019
         O     R8,MASK1                 .TURN OFF QCBREAD IN    YS02019
         N     R8,MASK2                 .QCBFLAG                YS02019
         O     R9,MASK3                 .TURN OFF QCBTGET IN    YS02019
         N     R9,MASK4                 .QCBTSOF1               YS02019
         CDS   R0,R8,TSBTPOST           UPDATE TSBTPOST         YS02019
         BNE   RETRY1                   LOOP UNTIL IT'S DONE    YS02019
         BAL   R7,TPOSTAYP              GO TO TPOST AYP         YS02019
CLIEXITC EQU   *
         TM    TSBFLG2,TSBAULST+TSBAUTON     IS AUTO LN IN EFFECT
         BNO   CLIEND                    IF NOT, EXIT
         LTR   R3,R3                    ANY LINES DELETED       YS02019
         BZ    CLIEND                    IF NOT, EXIT
         L     R1,TSBAUTOS               CURRENT LINE COUNT
         L     R0,TSBAUTOI               LINE CNT INCR
         SPACE
CLILNDEC DS    0H
         SR    R1,R0                    DECR LN CNT
         BNP   CLIST0                   IF NOT PLUS, STORE ZERO
         BCT   R3,CLILNDEC              DECREMENT LD COUNT      YS02019
         ST    R1,TSBAUTOS              STORE NEW AUTO LN COUNT
         B     CLIEND                   SKIP NEXT INSTRUCTION
CLIST0   XC    TSBAUTOS,TSBAUTOS        ZERO LINE COUNT
CLIEND   DS    0H
         L     R15,SAVE15               GET RETURN ADDRESS      YS02019
         BR    R15                      RETURN
         DROP  R7                       DROP QCB BASE
         EJECT
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
* C L R O U T Q   -   CLEAR TSO OUTPUT QUEUES
*
*        INPUT: CLRSW    BIT 0:  0= CLEAR ALL NSGS EXCEPT TJID TYPE
*                                1= CLEAR ALL MSGS
*                        BIT 1:  0= DO NOT CLEAR TRAILER BUFFER
*                                1= CLEAR IT
*
*                        R4:  LCB FOR THIS LINE
*                        R6:  TSB
*                        R10: TIOCRPT                           YS02019
*
*        REGISTERS VOLATILE OVER THIS SUBROUTINE: R0,R1,R8,R11
*                                                 R2,R9         YS02019
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
CLROUTQ  DS    0H
         ST    R15,SAVE15               SAVE RETURN REGISTER
         SR    R15,R15                  ZAP R15
         ST    R15,SAVE11               ZERO OUT SAVE11         YM03889
         CLI   TSBNOBF,E0               ARE THERE ANY OUTPUT BUF
         BNZ   CLROQ1                   YES,BRANCH
         TM    CLRSW,CLRATTN+CLRHANG    FROM ATTN OR HANGUP
         BZ    CLOEXIT2                 NO,EXIT
         XC    TSBOTBFP(E3),TSBOTBFP    CLEAR OUTBUF POINTER
*                                       OF POSSIBLE REPROMPT MESSAGES
         B     CLOEXIT2                 GO TO EXIT
CLROQ1   EQU   *
         L     R9,TSBOBFP-E1            SAVE HQ PTR
         LA    R9,0(R9)                 CLEAR HIGH ORDER BYTE
         XC    TSBOBFP(E3),TSBOBFP      CLEAR TSB HQ PTR
         TM    CLRSW,CLRTRL             SHOULD TRAILR QUE BE CLEARED
         BZ    CLOHEAD                  NO, GO CLEAR HEADER Q
         TM    TSBSTAT,TSB3270          IS THIS A 3270          SA70323
         BNO   CLRNO327                 NO, CLEAR TRLR NORMALLY SA70323
         TM    CLRSW,CLRHANG            IS THIS HANGUP          SA70323
         BNO   CLOHEAD                  FOR 3270, CLEAR TRAILER SA70323
*                                       ONLY FOR HANGUP ENTRY   SA70323
CLRNO327 EQU   *                                                SA70323
         OC    TSBOTBFP(E3),TSBOTBFP    IS TRAILR QUE EMPTY
         BZ    CLOHEAD                  YES, CLEAR H Q
         TM    TSBFLG3,TSBTJMSG         TJID TYPE MESSAGE
         BZ    CLOTRL                   NO, GO CLEAR
         TM    CLRSW,CLRTJID            SHOULD TJID MSGS BE CLEARED
         BZ    CLOHEAD                  NO, GO CLEAR H Q
         SPACE 2
CLOTRL   DS    0H
         L     R11,TSBOTBFP-E1          GET 1ST TRLR PTR
         XC    TSBOTBFP(E3),TSBOTBFP    ZERO T Q PTR
         USING TIOCBUF,R11
         TM    CLRSW,CLRATTN+CLRHANG    FROM ATTN OR HANGUP
         BZ    CLOTRL2                  NO, DON'T TEST BIPI
         TM    TSBFLG2,TSBBIPI          IS BIPI ON (SET BY BREAKIN)
         BO    CLOHEAD                  DO NOT FREE TRLR QUE BFR
CLOTRL2  DS    0H
         LA    R8,0(R9)                 SAVE HQ PTR
         B     CLOXBUFF                 GO REMOVE BUFFER FROM Q
         SPACE 2
CLOHEAD  DS    0H
         LTR   R11,R9                   LAST HDR MSG
         BZ    CLOEXIT0                 YES,BRANCH
         SPACE 2
CLOBUMP  DS    0H
         L     R8,BUFFHEAD-E1           SAVE NEXT MSG PTR
         ST    R11,SAVE11               SAVE CURRENT BUFFER     YS02019
*                                       HEADER POINTER          YS02019
         TM    BUFFFL2,BUFFTJID         TEST IF TJID MESSAGE
         BNO   CLOXBUFF                 NO,GO RENAME BUFFER FROM Q
         TM    CLRSW,CLRTJID            SHOULD TJID MSGS BE CLRD
         BZ    CLORECHN                 NO,BRANCH
         SPACE
CLOXBUFF DS    0H
         L     R3,BUFFTRLR-E1           SAVE CURR BFR TRLR PTR  YS02019
         TM    BUFFFL1,BUFFIHOT         BUFFER ON BOTH QUES?   @ZA08015
         BNO   CLOXBUF1                 NO, PUT ON FREE QUE    @ZA08015
         NI    BUFFFL1,OFF-BUFFIHOT     TURN OFF FLAG          @ZA08015
         B     CLOSKIP                  GET NEXT TRAILER       @ZA08015
         SPACE 1                                               @ZA08015
CLOXBUF1 EQU   *                                               @ZA08015
         LR    R2,R11                   BFR TO BE RETURNED      YS02019
         L     R15,AYQBRAD              BFR RETURN RTN'S EPA    YS02019
         BALR  R0,R15                   GO TO RETURN BFR        YS02019
CLOSKIP  EQU   *                                                ZA12528
         SR    R1,R1                    CLEAR REGISTER          YS02019
         IC    R1,TSBNOBF               GET OUTPUT BUFFER CNT
         BCTR  R1,R0                    SUBTRACT 1
         STC   R1,TSBNOBF               UPDATE TSB CNT
*
* PREPARE TO FREE NEXT BUFFER
*
         LR    R11,R3                   GET TRLR POINTER        YS02019
         LA    R11,E0(R11)              CLEAR 1ST BYTE
         LTR   R11,R11                  LAST OF MSG CLEARED
         BNZ   CLOXBUFF                 NO, REMOVE NEXT BUFF
*
* GET NEXT MSG FOR REMOVAL
*
CLONEXT  DS    0H
         LA    R8,0(R8)                 CLEAR HI ORDER BYTE
         LTR   R11,R8                   LAST HDR MSG
         BZ    CLOEXIT0                 YES, EXIT
         B     CLOBUMP                  NO,CLEAR NEXT MSG
*
* RECHAIN TJID MSG
*
CLORECHN DS    0H
         XC    BUFFHEAD(E3),BUFFHEAD    ZAP HQ PTR
         OC    TSBOBFP(E3),TSBOBFP      IS QUE EMPTY
         BNZ   CLOFLAST                 NO, FIND LAST MSG
         IC    R1,TSBOBFP-E1            GET FLAG BYTE
         SLL   R1,E24                   LEFT ADJUST COUNT
         OR    R11,R1                   PUT CNT IN 1ST BYTE
         ST    R11,TSBOBFP-E1           UPDATE QUE PTR
         B     CLONEXT                  GO GET NEXT HDR MSG
*
* FIND LAST MSG
*
CLOFLAST DS    0H
         LR    R0,R11                   SAVE HEADER PTR
         L     R11,TSBOBFP-E1           GET 1ST MSG PTR
         SPACE
CLOA     DS    0H
         OC    BUFFHEAD(E3),BUFFHEAD    LAST MSG
         BZ    CLOB                     YES, CHAIN TO Q
         L     R11,BUFFHEAD-E1          NEXT MSG
         B     CLOA                     LOOK FOR LAST MSG
CLOB     DS    0H
         IC    R1,BUFFHEAD-E1           GET FLAG BYTE
         SLL   R1,E24                   LEFT ADJUST
         OR    R0,R1                    MERGE
         ST    R0,BUFFHEAD-E1           CHAIN MSG INTO QUE
         B     CLONEXT                  GO PROCESS NEXT MSG
         SPACE 2
CLOEXIT0 DS    0H
         OC    TSBOTBFP(E3),TSBOTBFP    TEST IF ANY TRAILER IN Q
         BZ    NOTRILR                  NO,SKIP SETTING BIT
         TM    TSBFLG3,TSBTJMSG         TEST IF ANY TJID MSG IN Q
         BO    NOTRILR                  YES,SKIP SETTING BIT
         TM    TSBFLG2,TSBBIPI          REPROMPT MSG ON OUT TRLR QUE
         BO    NOTRILR                  YES,BRANCH
         OI    TSBFLG1,TSBOFLSH         NO,TELL TSOUTPUT TO FLUSH TRLRQ
NOTRILR  EQU   *
         TM    CLRSW,CLRHANG            CALL FROM HANGUP        YS02019
         BO    CLOEXIT2                 YES, EXIT               YS02019
         L     R15,SAVE11               GET HEAD QUEUE PTR      YS02019
         LTR   R15,R15                  WAS HEADER QUE EMPTY
         BNZ   NOTRILR2                 NO,CHECK LAST MSG CLEARED
         TM    TSBFLG3,TSBTJMSG         TJID MSG ON OUT TRLR QUE
         BO    CLOEXIT2                 YES,DO NOT TURN OFF QCBPARTO
         B     NOTRILR3                 NO,TURN OFF QCBPARTO
NOTRILR2 EQU   *
         DROP  R11
         USING TIOCBUF,R15
         TM    BUFFFL2,BUFFTJID         TEST IF TJID MESSAGE
         BO    CLOEXIT2                 YES,GOTO RETURN
NOTRILR3 EQU   *
         L     R0,TSBF2M                GET TSBTPOST CONDITION  YS02019
RETRY2   EQU   *                                                YS02019
         LR    R1,R0                    .PUT TSBTPOST STATUS IN YS02019
*                                       .WORK REGISTERS         YS02019
         O     R1,MASK5                 .TURN OFF QCBPARTO      YS02019
         N     R1,MASK6                 .IN QCBTSOF1            YS02019
         CS    R0,R1,TSBF2M             UPDATE TSBTPOST         YS02019
         BNE   RETRY2                   LOOP UNTIL IT IS DONE   YS02019
         BAL   R7,TPOSTAYP              GO TO TPOST AYP         YS02019
         DROP  R15
CLOEXIT2 DS    0H
         L     R15,SAVE15               RESTORE RETURN REGISTER
         BR    R15                      RETURN TO CALLER
         EJECT
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
*  R M V W A I T S: REMOVE THE VARIOUS SYSTEM AND USER WAIT
*                   CONDITIONS. THE FLOW OF CONTROL THROUGH
*                   THIS ROUTINE IS CONTROLLED BY BIT SETTINGS
*                   IN RMVSW.
*
*                       BIT 0:     REMOVE USER LWAITS
*                           1:            SYSTEM LWAITS
*                           2:            WAITS ON OWAITS
*                           3:            TJID/TPUT WAITS
*                           4:            IWAITS
*                           5:            OWAITS
*
*
*                   REGISTER INPUTS:
*                        R4: LCB
*                        R6: TSB
*                        R10: TIOCRPT                           YS02019
*                        R13: TIOCSAVE                          YS02019
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                     *
RMVWAITS DS    0H
         STM   R14,R15,SAVE14           SAVE REGISTERS           TS1637
         USING TIOCRPT,R10                                      YS02019
         OC    TIOCNFBF,TIOCNFBF        ANY FREE BFRS AT ALL    YS02019
         BZ    RMVEXIT                  NO, GO TO EXIT          YS02019
         TM    RMVSW,RMVULW             DO WE WANT TO REMOVE USER
*                                       LWAITS
         BZ    RMVTSLW                  NO, MAKE MORE TESTS
         TM    TSBSTAT,TSBINUSE         IS USER STILL CONNECTED
         BZ    RMVTSLW                  NO,CHECK SYSTEM LWAIT
         TM    TSBSTAT,TSBLWAIT         IS USER IN LWAIT
         BZ    RMVTSLW                  NO, MAKE TEST FOR SYS LWAIT
*
* TRY TO REMOVE USER FROM LWAIT
*
         SR    R1,R1                    CLEAR WORK REG.
         IC    R1,TSBNIBF               NO. OF BUFRS ON INPUT Q
         CH    R1,TIOCRSTH              IS THRESHOLD EXCEEDED
         BH    RMVTSLW                  YES, SKIP FOLLOWING CODE
         NI    TSBSTAT,OFF-TSBLWAIT     TURN OFF LWAIT
*
         TM    TIOCFLG,TIOCSYLW         IS SYSTEM IN LWAIT
         BO    RMVTSLW                  YES, CHK FOR SYS LWAIT    M0029
*                                       REMOVAL                  M0029
         L     R0,TSBF2M                GET TSBTPOST CONDITION  YS02019
RETRY3   EQU   *                                                YS02019
         LR    R1,R0                    .PUT TSBTPOST IN        YS02019
*                                       .WORK REGISTERS         YS02019
         O     R1,MASK7                 .TURN OFF QCBNOBUF      YS02019
         N     R1,MASK8                 .IN QCBTSOF1            YS02019
         CS    R0,R1,TSBF2M             UPDATE TSBTPOST         YS02019
         BNE   RETRY3                   LOOP UNTIL IT IS DONE   YS02019
         BAL   R7,TPOSTAYP              GO TO INVOKE TPQ FOR    YS02019
*                                       TPOSTING TSB TO AYP     YS02019
         SPACE 2
*
* TEST TO SEE IF TCAM BUFFERS CAN BE REMOVED FROM THE WAIT QUEUE
* AND/OR IF THE SYSTEM CAN BE REMOVED FROM SYSTEM LWAIT
*
         SPACE 2
RMVTSLW  EQU   *
         TM    RMVSW,RMVSLW             DO WE WANT TO REMOVE SYS LWAIT
         BZ    RMVTUOWT                 NO,GO CHECK USER OWAIT
         TM    TIOCFLG,TIOCSYLW         NO,IS SYSTEM IN LWAIT
         BZ    RMVTUOWT                 NO,CHECK USER OWAIT
         SPACE 3                                                 M0167
POSTTSI  EQU   *
         L     R0,TSBTPOST              GET STATUS OF TSBTPOST  YS02019
RETRY4   EQU   *                                                YS02019
         LR    R1,R0                    PUT IN WORK REGISTER    YS02019
         O     R1,MASK9                 TURN ON TSBTPAYI FOR    YS02019
*                                       TPOSTING TSI TO AYI     YS02019
         CS    R0,R1,TSBTPOST           UPDATE TSBTPFLG         YS02019
         BNE   RETRY4                   LOOP UNTIL IT'S DONE    YS02019
         BAL   R7,TPOSTAYP              CALL AYP TO POST        YS02019
         SPACE 2
*
* THIS SECTION TESTS TO SEE IF USER OWAIT CAN BE REMOVED
*
         SPACE 2
RMVTUOWT EQU   *
         TM    RMVSW,RMVUOWT            DO WE WANT TO REMOVE USER OWAIT
         BZ    RMVTSOWT                 NO,CHECK SYSTEM OWAIT
         TM    TSBSTAT,TSBINUSE         IS USER STILL CONNECTED
         BZ    RMVTSOWT                 NO,CHECK SYSTEM OWAIT
         TM    TSBFLG4,TSBOWAIT         IS USER IN OWAIT        YS02019
         BZ    RMVTSOWT                 GO CHECK SYS OWAIT       S21008
*
* REGULAR TPUT IS IN OWAIT - TEST TO SEE IF IT CAN BE REMOVED
*
         SR    R0,R0                    CLEAR TEST REGISTER
         IC    R0,TSBNOBF               GET NO. BFR USER NOW HOLDING
         CH    R0,TIOCOWTH              IS THRESHOLD EXCEEDED
         BH    RMVTSOWT                 YES,CHECK SYSTEM OWAIT
*
* REMOVE USER FROM OWAIT
*
         STM   R8,R12,SAVE8             SAVE REGISTERS ACROSS   YS02019
*                                       CALL                     S21008
         LA    R8,0(,R6)                SET UP TSB ADDR FOR      S21008
*                                       SUB-RTN                  S21008
         IC    R9,TSBFLG2               SAVE TSBBIPI SETTING     S21008
         NI    TSBFLG2,OFF-TSBBIPI      REQUEST OWAIT REMOVAL    S21008
*
RMVUOWT1 EQU   *                                                 S21008
         L     R15,UOWRR                GET USER-OWAIT-REMOVAL   S21008
*                                       RTN ADD                  S21008
         L     R12,AYOOBASE             GET BASE ADDR OF RTN     S21008
         BALR  R4,R15                   GO TO OWAIT REMOVAL RTN  S21008
*                                       ADDR                     S21008
*
         STC   R9,TSBFLG2               RESTORE TSBBIPI SETTING  S21008
         LM    R8,R12,SAVE8             RESTORE REGISTERS       YS02019
         SPACE 2
*
* THIS SECTION TESTS FOR REMOVAL OF SYSTEM OWAIT
*
         SPACE 2
RMVTSOWT EQU   *
         TM    RMVSW,RMVSOWT            DO WE WANT TO REMOVE SYS OWAIT
         BZ    RMVEXIT                  NO,EXIT
         LR    R8,R6                    GET TSB ADDR FOR SUB-RTN S21008
         STM   R6,R7,SAVE6              SAVE REGISTERS          YS02019
         ST    R12,SAVE12               SAVE BASE ADDRESS       YS02019
         L     R15,SOWRR                GET SYS-OWAIT REMOVAL    S21008
*                                       RTN ADDR                 S21008
         L     R12,AYOOBASE             GET BASE ADDR OF RTN     S21008
         BALR  R1,R15                   GO REMOVE SYSTEM OWAIT   S21008
         L     R12,SAVE12               RESTORE BASE ADDRESS    YS02019
         STM   R6,R7,SAVE6              RESTORE REGISTERS       YS02019
         SPACE 2
         SPACE 5
*
* COMMON EXIT FOR RMVWAIT  ROUTINE
*
RMVEXIT  DS    0H
         LM    R14,R15,SAVE14           RESTORE REGISTERS        TS1637
         BR    R15                      RETURN
         SPACE 3
*                                                               YS02019
* GO TO TPQ FOR TPOSTING THE TSB TO AYP                         YS02019
*                                                               YS02019
*                                                               YS02019
* INTERFACE TO IEDAYTPQ                                         YS02019
*                                                               YS02019
*        R0  - RETURN ADDRESS                                   YS02019
*        R1  - ZERO, MEANS TPOST TSB TO IEDQYP                  YS02019
*        R2  - TSB ADDRESS                                      YS02019
*        R13 - ADDRESS OF 6-WORD SAVE AREA                      YS02019
*        R15 - ENTRY POINT ADDRESS                              YS02019
*                                                               YS02019
TPOSTAYP EQU   *                                                YS02019
         SR    R1,R1                    INDICATE TPOST TSB TO   YS02019
*                                       AYP                     YS02019
         LR    R2,R6                    PASS TSB ADDRESS        YS02019
         L     R15,TPOSTRTN             GET TPOST RTN'S EPA     YS02019
         BALR  R0,R15                   GO TO TPOST TSB TO AYP  YS02019
         BR    R7                       GO BACK TO CALLER       YS02019
         SPACE 3
         DS    0F                       FORCE WORD BOUNDARY     YS02019
MASK1    DC    AL1(0,0,QCBREAD,0)       MASK - INDICATING       YS02019
*                                       QCBREAD BIT POSITION    YS02019
MASK2    DC    AL1(255,255,255,255-QCBREAD)  MASK - TURN OFF    YS02019
*                                       QCBREAD                 YS02019
MASK3    DC    AL1(0,0,QCBTGET,0)       MASK - INDICATING       YS02019
*                                       QCBTGET BIT POSITION    YS02019
MASK4    DC    AL1(255,255,255,255-QCBTGET)  MASK - TURN OFF    YS02019
*                                       QCBTGET                 YS02019
MASK5    DC    AL1(0,0,QCBPARTO,0)      MASK - INDICATING       YS02019
*                                       QCBPARTO BIT POSITION   YS02019
MASK6    DC    AL1(255,255,255,255-QCBPARTO) MASK - TURN OFF    YS02019
*                                       QCBPARTO                YS02019
MASK7    DC    AL1(0,0,QCBNOBUF,0)      MASK - INDICATING       YS02019
*                                       QCBNOBUF BIT POSITION   YS02019
MASK8    DC    AL1(255,255,255,255-QCBNOBUF) MASK - TURN OFF    YS02019
*                                       QCBNOBUF                YS02019
MASK9    DC    AL1(TSBTPAYI,0,0,0)      MASK - TURN ON TSBTPAYI YS02019
         EJECT
*
* WORKAREAS,DSECTS
*
HIGHBIT  DS    0F                                                S21008
         DC    X'80000000'              HIGH ORDER BIT MASK      S21008
TPOSTRTN DC    V(IEDAYTPQ)              TPOST RTN'S EPA         YS02019
UOWRR    DC    V(IEDAYQIO)              USER OWAIT REMOVAL RTN  YS02019
SOWRR    DC    V(IEDAYQSO)              SYS OWAIT REMOVAL RTN   YS02019
AYOOBASE DC    V(QTIP0110)              IEDAYOO BASE ADDRESS     S21008
AYQBRAD  DC    V(IEDAYQBR)              BFR RETURN RTN'S EPA    YS02019
PATCHIT  DC    10F'0'                   PATCH AREA              YS02019
         SPACE 3
ATTNSAVE DSECT
SAVE0    DS    F                        RPT SAVE AREA WORD 0    YS02019
SAVE1    DS    F                        RPT SAVE AREA WORD 1    YS02019
SAVE2    DS    F                        RPT SAVE AREA WORD 2    YS02019
SAVE3    DS    F                        RPT SAVE AREA WORD 3    YS02019
SAVE4    DS    F                        RPT SAVE AREA WORD 4    YS02019
SAVE5    DS    F                        RPT SAVE AREA WORD 5    YS02019
SAVE6    DS    F                        RPT SAVE AREA WORD 6    YS02019
SAVE7    DS    F                        RPT SAVE AREA WORD 7    YS02019
SAVE8    DS    F                        RPT SAVE AREA WORD 8    YS02019
SAVE9    DS    F                        RPT SAVE AREA WORD 9    YS02019
SAVE10   DS    F                        RPT SAVE AREA WORD 10   YS02019
SAVE11   DS    F                        RPT SAVE AREA WORD 11   YS02019
SAVE12   DS    F                        RPT SAVE AREA WORD 12   YS02019
SAVE13   DS    F                        RPT SAVE AREA WORD 13   YS02019
SAVE14   DS    F                        RPT SAVE AREA WORD 14   YS02019
SAVE15   DS    F                        RPT SAVE AREA WORD 15   YS02019
PARMSW   DS    0XL3
CLRSW    DC    X'00'                    INPUT PARM TO CLROUTQ
CLRTRL   EQU   X'80'                    ON=CLEAR TRLR Q
CLRTJID  EQU   X'40'                    ON=CLEAR TJID TYPE MSGS
CLRATTN  EQU   X'20'                    ON=ATTN IS CLEARING Q'S
CLRHANG  EQU   X'10'                    ON=HANGUP IS CLEARING Q'S
CLRTCLRQ EQU   X'08'                    ON=TCLEARQ IS CLEARING Q'S
CLRIGN   EQU   X'04'                    ATTENTION IGNORED
RMVSW    DC    X'00'                    SWITCH FOR REMOVING WAITS
RMVULW   EQU   X'80'                    REMOVE USER LWAITS
RMVSLW   EQU   X'40'                    SYSTEM LWAITS
RMVUOWT  EQU   X'20'                    INDICATES REMOVE USER OWAIT
RMVSOWT  EQU   X'10'                    INDICATES REMOVE SYSTEM OWAIT
         EJECT
         IHAASCB                                                YS02019
         TAVTD
         IKJTIOCB
CVT      DSECT
         CVT
         TPRIOR
         TQCBD
         TSCBD
         IKJTCB
         EJECT
         TTCXD                                                  YS02019
         EJECT
         IKJTIOCP
         IKJTSB
         TTSID
         END
