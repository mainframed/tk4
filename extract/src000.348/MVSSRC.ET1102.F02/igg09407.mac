         TITLE 'IGG09407 - STCC ROUTINE'
IGG09407 CSECT
***********************************************************************
*                                                                     *
* STATUS -                                                            *
*    CHANGE LEVEL 01                                                  *
*                                                                     *
* FUNCTION -                                                          *
*    THIS ROUTINE IS TO SERVE THE MACRO 'STCC'. IT IS TO              *
*    CHANGE THE STCC BYTES IN TSB (TSBSTCC) TO REFLECT REQUEST OF THE *
*    MACRO.                                                           *
*                                                                     *
*    IF X'00' IS SPECIFIED AS LINE DELETE OR CHARACTER DELETE         *
*    CHARACTER, THE PREVIOUSLY USED CHARACTER IS RETAINED.  IN THIS   *
*    CASE, THE FORMER LINE DELETE CHARACTER IS PUT IN REG 0, THE      *
*    FORMER CHARACTER DELETE CHARACTER IS PUT IN REG 1.               *
*                                                                     *
*    IF X'FF' IS SPECIFIED, THE EDIT FUNCTION IS TERMINATED, AND      *
*    THE PROPER FIELD IS ZEROED OUT.                                  *
*                                                                     *
*    IF ATTENTION IS SPECIFIED FOR LINE DELETE, THE TSBATNLD BIT IN   *
*    TSBSTAT FIELD IS TURNED ON.                                      *
*                                                                     *
* ENTRY POINTS -                                                      *
*         IGG09407 - CONTROL IS RECEIVED FROM MODULE IGC0009D         *
*         VIA BRANCH ACCORDING ENTRY CODE 7, HODLING THE LOCAL        *
*         AND THE CMS LOCKS, AND IN SUPERVISOR KEY                    *
*                                                                     *
* INPUT -                                                             *
*    REGISTER 0 CONTAINS PARAMETER                                    *
**********************************************************************
* ENTRY CODE     *                 *                 *               *
**********************************************************************
*    REGISTER 1 CONTAINS PARAMETER                                    *
**********************************************************************
* FLAG           *                 * LINE DELETE     * CHAR DELETE   *
**********************************************************************
*    REGISTER 3 CONTAINS CVT ADDRESS                                  *
*    REGISTER 4 CONTAINS TCB ADDRESS                                  *
*    REGISTER 5 CONTAINS TSB ADDRESS                                  *
*    REGISTER 10 CONTAINS THE ADDRESS OF AN ARRAY OF CONSTANTS
*    REGISTER 12 CONTAINS THE BASE ADDRESS
*    REGISTER 14 CONTAINS THE RETURN ADDRESS
*                                                                     *
* OUTPUT -                                                            *
*    REGISTER 0 HAS THE FORMER LINE DELETE CHARACTER. X'FF' MEANS     *
*               THERE WAS NO FORMER LINE DELETE CHARACTER             *
*                                                                     *
*    REGISTER 1 HAS THE FORMER CHARACTER DELETE CHARACTER. X'FF' MEANS*
*               THERE WAS NO FORMER CHARACTER SELETE CHARACTER        *
*                                                                     *
*    REGISTER 15 HAS RETURN CODE:                                     *
*           0 - SUCCESSFUL                                            *
*           8 - CHARACTER REJECTED                                    *
*                                                                     *
* EXTERNAL REFERENCES -                                               *
*         NONE                                                        *
*                                                                     *
* EXITS, NORMAL -                                                     *
*         BR 14 WITH 0 IN REGISTER 15                                 *
*                                                                     *
* EXITS, ERROR -                                                      *
*         BR 14 WITH 4 OR 8 IN REGISTER 15                            *
*                                                                     *
* TABLES/WORK AREAS -                                                 *
*    CONTROL BLOCKS ARE DESCRIBED IN THE DSECTS AT END OF LISTING     *
*                                                                     *
* ATTRIBUTES -                                                        *
*    REENTRANT, REFRESHABLE, SUPERVISOR MODE, ENABLED                 *
*                                                                     *
* CHARACTER CODE DEPENDENCY -                                         *
*    THIS MODULE IS ASSEMBLED IN EBCDIC AND MUST BE REASSEMBLED IF    *
*    A DIFFERENT CHARACTER SET IS TO BE USED DURING EXECUTION         *
* NOTES -                                                             *
*    NONE                                                             *
*                                                                     *
***********************************************************************
*                                                                     *
***********************************************************************
         SPACE 3
********
******** REGISTER EQUATES
********
RPARM0   EQU   0                        PARAMETER REG 0
RWORK0   EQU   0                        WORK REGISTER
RPARM1   EQU   1                        PARAMETER REG 1
RWORK1   EQU   1                        WORK REGISTER
RWORK2   EQU   2                        WORK REGISTER
RWORK3   EQU   3                        WORK REGISTER
RTSB     EQU   5                        POINTS TO TSB           YS02019
RBASE    EQU   6                        BASE REGISTER           YS02019
RWORK4   EQU   8                        WORK REGISTER           YS02019
RCONST   EQU   10                       POINTS TO PROGRAM       YS02019
*                                       CONSTANTS               YS02019
R12      EQU   12                       INTERFACE REGISTER      YS02019
R13      EQU   13                       REGISTER 13
R14      EQU   14                       REGISTER 14
RLINK    EQU   14                       LINK REGISTER
R15      EQU   15                       REGISTER 15
RBRANCH  EQU   15                       BRANCH REGISTER
RCODE    EQU   15                       RETURN CODE REGISTER
         SPACE 2
********
******** DISPLACEMENT,LENGTH AND MASK EQUATES
********
BYTE1    EQU   8                        MASK FOR BYTE 1 IN REG.
BYTE2    EQU   4                        MASK FOR BYTE 2 IN REG.
BYTE3    EQU   2                        MASK FOR BYTE 3 IN REG.
BYTE4    EQU   1                        MASK FOR BYTE 4 IN REG.
BYTE34   EQU   3                        MASK FOR BYTES 3&4 IN REG.
BYTE234  EQU   7                        MASK FOR BYTES 2&3&4 IN REG.
D0       EQU   0                        DISPLACEMENT OF 0
D1       EQU   1                        DISPLACEMENT OF 1
D2       EQU   2                        DISPLACEMENT OF 2
D3       EQU   3                        DISPLACEMENT OF 3
D4       EQU   4                        DISPLACEMENT OF 4
D5       EQU   5                        DISPLACEMENT OF 5
D6       EQU   6                        DISPLACEMENT OF 6
D14      EQU   14                       DISPLACEMENT OF 14
L1       EQU   1                        LENGTH OF 1
L2       EQU   2                        LENGTH OF 2
L3       EQU   3                        LENGTH OF 3
L4       EQU   4                        LENGTH OF 4
L8       EQU   8                        LENGTH OF 8
L16      EQU   16                       LENGTH OF 16
L20      EQU   20                       LENGTH OF 20
L24      EQU   24                       LENGTH OF 24
L31      EQU   31                       LENGTH OF 31
L32      EQU   32                       LENGTH OF 32
RETCODE4 EQU   4                        RETURN CODE OF 4
RETCODE8 EQU   8                        RETURN CODE OF 8
RETCODEC EQU   12                       RETURN CODE OF 12
COMPLETE EQU   X'40'                    TO TEST POST COMPLETE BIT
CZERO    EQU   0                        TO TEST FOR ZERO
CONE     EQU   1                        CONSTANT OF 1
CTWO     EQU   2                        CONSTANT OF 2
CTHREE   EQU   3                        CONSTANT OF 3
CFIFTEEN EQU   15                       CONSTANT OF 15
CSIXTEEN EQU   16                       CONSTANT OF 16
CFOX     EQU   X'FF'                    TO TEST FOR 'FF'
BLANK    EQU   X'40'                    TO TEST FOR BLANK
OFF      EQU   X'FF'                    TO TURN BITS OFF
EXIT     EQU   3                        TO INVOKE EXIT,SVC 3
IDLE     EQU   X'17'                    TERM. CTL. CHAR. - IDLE
NL       EQU   X'15'                    TERM. CTL. CHAR. - NEW LINE
CR       EQU   X'0D'                    TERM. CTL. CHAR. - CARRIAGE RET
LF       EQU   X'25'                    TERM. CTL. CHAR. - LINE FEED
HEX18    EQU   X'18'                    TERM. CTL. CHAR. - TWX CHAR.
HEX20    EQU   X'20'                    TO TEST BIT 2 OF BYTE
         SPACE 3
********
******** ESTABLISH BASE REGISTER AND ADDRESSABILITY
********
         USING *,RBASE                  BASE ADDRESSABILITY
         LR    RBASE,R12                LOAD BASE REGISTER      YS02019
         B     MAIN                     BR AROUND ID AND DATE   YS02019
         DC    C'IGG09407'              MODULE ID               YS02019
         DC    X'3261'                  DATE - 09/18/73         YM03842
         USING TSB,RTSB                 TSB ADDRESSABILITY
         USING CONSTANT,RCONST          CONSTANT ADDRESSABILITY
********
******** MAIN FUNCTION OF ROUTINE - MODIFY LINE/CHAR DELETE CHARACTERS
********
MAIN     EQU   *                                                YS02019
         LR    RWORK4,RLINK             SAVE RETURN ADDRESS     YS02019
         MODESET EXTKEY=TCAM            GET TCAM'S KEY          YS02019
         SR    RCODE,RCODE              INITIALIZE RETURN CODE
         SR    RWORK0,RWORK0            CLEAR REGISTER
         SR    RWORK2,RWORK2            CLEAR REGISTER
         LR    RWORK3,RPARM1            PUT ENTRY PARMS IN WORK REG
         SR    RWORK1,RWORK1            CLEAR REGISTER
         IC    RWORK0,TSBLNDCC          GET OLD LINE DEL CHAR
         LTR   RWORK0,RWORK0            WAS IT ZERO
         BNZ   CHKOLDCD                 IF NOT, CHECK OLD CD CHAR
         LA    RWORK0,CFOX              YES,SET TO X'FF'
CHKOLDCD EQU   *
         IC    RWORK1,TSBCHDCC          GET OLD CHAR DEL CHAR
         LTR   RWORK1,RWORK1            WAS OLD CH DEL CHAR ZERO
         BNZ   STORE                    NO,STORE NEW ONES
         LA    RWORK1,CFOX              YES,SET TO X'FF'
STORE    EQU   *
         STH   RWORK3,TSBSTCC           STORE NEW LD/CD CHARS.   A111
********
******** CHECK ATTENTION
********
         TM    TSBSTAT,TSBATNLD         ATTN FOR LD ALREADY SET
         BZ    NOTON                    NO
         ICM   RWORK0,BYTE1,HIGHBIT     INDICATE ATTN LD IN EFFECT
NOTON    EQU   *
         SLDL  RWORK2,L1                GET FLAG BIT 0
         LTR   RWORK2,RWORK2            IS IT 1
         BZ    CHKLD1                   NO,1ST OP NOT SPECIF.    A111
         SR    RWORK2,RWORK2            CLEAR BIT
         SLDL  RWORK2,L1                GET FLAG BIT 1
         LTR   RWORK2,RWORK2            ATTN SPECIFIED
         BZ    NATN                     NO,NATN
ATTN     EQU   *
         TM    TSBTERM1,TSBCATTN        DOES DEVICE HAVE FEATUREYS02019
         BO    SETATTN                  YES,GO SET FOR LD
         LA    RCODE,RETCODE8           SET RETURN CODE OF 8
         B     CHKLD                    CHECK LINE DELETE
SETATTN  EQU   *
         OI    TSBSTAT,TSBATNLD         TURN ON ATTN FOR LD
         B     CHKLD                    GO CHECK LD
NATN     EQU   *
         NI    TSBSTAT,OFF-TSBATNLD     TURN OFF ATTN FOR LD
********
******** CHECK LINE DELETE CHARACTER
********
CHKLD    EQU   *
         SRL   RWORK3,L1                RESET FLAG BYTE          A111
CHKLD1   EQU   *                                                 A111
         SR    RWORK2,RWORK2            CLEAR BIT
         SLDL  RWORK2,L2                GET FLAG BIT 2           A111
         LTR   RWORK2,RWORK2            LD CHAR. SPECIFIED
         BP    CKLDZERO                 YES,TEST IT
LDZERO   EQU   *
         STC   RWORK0,TSBLNDCC          RESTORE OLD LD CHAR
         CLI   TSBLNDCC,CFOX            WAS OLD LD ZERO
         BNE   CHKCD                    NO,CHECK CD CHAR
         B     ZEROLDFD                 YES,RESET TO ZERO
CKLDZERO EQU   *
         CLI   TSBLNDCC,CZERO           IS NEW LD ZERO
         BNE   CKLDFOX                  NO,CHECK FOR X'FF'
         B     LDZERO                   NEW LD IS ZERO
CKLDFOX  EQU   *
         CLI   TSBLNDCC,CFOX            IS NEW LD FOX
         BE    ZEROLDFD                 YES,SET TO ZERO
CHKLDVLD EQU   *
         CLI   TSBLNDCC,CR              IS NEW LD CARRIAGE RETURN CHAR.
         BE    LDREJECT                 SET REJECT CODE
         CLI   TSBLNDCC,LF              WAS LINE FEED CHAR SPECIFIED
         BE    LDREJECT                 SET REJECT CODE
         CLI   TSBLNDCC,NL              WAS NEW LINE CHAR. SPECIFIED
         BE    LDREJECT                 SET REJECT CODE
         B     CHKCD                    GO CHECK CODE           YS02019
LDREJECT EQU   *
         LA    RCODE,RETCODE8           SET RETURN CODE OF 8
ZEROLDFD EQU   *
         MVI   TSBLNDCC,CZERO           ZERO FIELD
********
******** CHECK CHARACTER DELETE CHARACTER
********
CHKCD    EQU   *
         SR    RWORK2,RWORK2            CLEAR BIT
         SLDL  RWORK2,L1                GET FLAG BIT 3
         LTR   RWORK2,RWORK2            CHAR. DELTE SPECIFIED
         BNZ   CHKMORE                  YES
CDZERO   EQU   *
         STC   RWORK1,TSBCHDCC          RESTORE OLD CD CHAR
         CLI   TSBCHDCC,CFOX            WAS OLD CD CHAR ZERO
         BE    ZEROCDFD                 YES,GO RESET TO ZERO
         B     RETURN                   NO,RETURN TO CALLER
CHKMORE  EQU   *
         CLI   TSBCHDCC,CZERO           IS NEW CD ZERO
         BNE   CHKCDFOX                 NO,CHECK FOR X'FF'
         B     CDZERO                   NEW CD CHAR IS ZERO
CHKCDFOX EQU   *
         CLI   TSBCHDCC,CFOX            IS NEW CD CHAR. X'FF'
         BE    ZEROCDFD                 YES,RESET TO ZERO
CHKCDVLD EQU   *
         CLI   TSBCHDCC,CR              IS NEW CD CARRIAGE RETURN
         BE    CDREJECT                 YES,INVALID
         CLI   TSBCHDCC,NL              IS NEW CD NEW LINE CHAR.
         BE    CDREJECT                 YES,INVALID
         CLI   TSBCHDCC,LF              IS NEW CD LINE FEED CHAR.
         BE    CDREJECT                 YES,INVALID
         B     RETURN                   RETURN TO CALLER
CDREJECT EQU   *
         LA    RCODE,RETCODE8           SET RETURN CODE OF 8
ZEROCDFD EQU   *                                                 A111
         MVI   TSBCHDCC,CZERO           SET TSBCHDCC TO X'00'    A111
RETURN   EQU   *
         MODESET EXTKEY=SUPR            GET KEY FOR SETLOCKS    YS02019
***********************************************************************
*        RELEASE CMS AND LOCAL LOCKS                                  *
***********************************************************************
RELEASE  EQU   *                        LABEL FOR SETLOCK       YS02019
         SETLOCK RELEASE,TYPE=CMS,RELATED=(TSB,IGC0009D)        YS02019
         SETLOCK RELEASE,TYPE=LOCAL,RELATED=(TSB,IGC0009D)      YS02019
         LR    RLINK,RWORK4             RESTORE RETURN ADDR     YS02019
         BR    RLINK                    RETURN                  YS02019
         DC    9F'0'                    MAINTENANCE PATCH AREA  YS02019
         SPACE 3
CONSTANT DSECT                          DEFINES CONSTANTS IN 1ST CSECT
ZERO     DC    F'0'                     CONSTANT OF 0
NINES    DC    F'99999999'              CONSTANT OF 99,999,999
FIFTEEN  DC    AL1(15)                  CONSTANT OF 15
TWO      DC    XL1'02'                  CONSTANT OF 2
TEN      DC    H'10'                    CONSTANT OF 10
SIXTEEN  DC    AL1(16)                  CONSTANT OF 16
ENABLE   DC    X'FF'                    TO ENABLE INTERRUPTS
SIX      DC    H'6'                     CONSTANT OF 6
EIGHT    DC    AL1(8)                   CONSTANT OF 8
CP5041   DC    XL3'026D16'              BKSP/UNDERSCORE CHAR. PROMPT
HIGHBIT  DC    XL1'80'                  TO TEST HIGH ORDER BITS
CPTWX    DC    XL3'024B0D'              PERIOD/CAR.RETURN CHAR. PROMPT
TPUT1    DC    XL4'0E000000'            TPUT,HOLD,CONTROL
TPUT2    DC    XL4'0A000000'            TPUT,CONTROL
ONE      DC    AL1(1)                   CONSTANT OF ONE
XIDLE    DC    X'17'                    TPUT IDLE CHARACTER     YA00364
FOX      EQU   ENABLE                   CONSTANT OF 'FF'
DISABLE  EQU   ZERO                     TO DISABLE INTERRUPTS
         EJECT
         IHAPSA
         EJECT
         IKJTSB
         END
