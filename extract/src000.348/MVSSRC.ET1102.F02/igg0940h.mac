         TITLE 'IGG0940H - SECOND LOAD OF SVC 94 FOR TCAM/TSO'
IGG0940H CSECT
***********************************************************************
*                                                                     *
* MODULE NAME - IGG0940H - SVC 94, SECOND LOAD; ROUTER FOR TCAM/TSO   *
*                                                                     *
* STATUS - VERSION 000                                                *
*                                                                     *
* FUNCTION - THIS MODULE IS THE TCAM-SPECIFIC CODE FOR SVC 94. ITS    *
*    FUNCTION IS TO ROUTE CONTROL TO THE PROPER SUPPORT MODULE FOR    *
*    THE MACRO ISSUED.                                                *
*                                                                     *
* ENTRY POINTS -                                                      *
*         IGG0940H - CONTROL IS RECEIVED FROM IGC0009D IF TCAM/TSO    *
*                  IS ACTIVE.                                         *
*         POSTTSB - ROUTINE TO POST THE TSB TO IEDAYP                 *
*                  LOCAL AND CMS LOCKS MUST BE HELD, MUST BE IN       *
*                  TCAM'S KEY .                                       *
*                  R1 = ADDR OF MASK TO BE USED                       *
*                  R3 = ADDR OF CVT                                   *
*                  R14 = RETURN ADDR OF CALLER                        *
*                  R15 = ENTRY ADDRESS                                *
*                  R6 TO R9 ARE WORK REGISTERS                        *
*                                                                     *
* INPUT -  (WHEN ENTERED AT IGG0940H ENTRY POINT)                     *
*    REG 0 - ENTRY CODE IN HIGH ORDER BYTE, PARAMETERS IN REMAINDER   *
*    REG 1 - PARAMETERS                                               *
*    REG 3 - CVT                                                      *
*    REG 4 - TCB                                                      *
*    REG 5 - SVRB (UNUSED)                                            *
*    REG 12 - ENTRY POINT ADDRESS                                     *
*                                                                     *
* OUTPUT - RETURN CODE IN REGISTER 15                                 *
*                                                                     *
* EXTERNAL REFERENCES -                                               *
*         QTIP (IKJGGQT1) WITH ENTRY CODE 21 (CLEAR INPUT QUEUE) OR   *
*         ENTRY CODE 22 (CLEAR OUTPUT QUEUE)                          *
*                                                                     *
* EXITS - NORMAL                                                      *
*         BRANCH TO NEXT CSECT, LOCAL AND CMS LOCKS ARE HELD.         *
*    REG 0 - ENTRY CODE IN HIGH ORDER BYTE,PARAMETERS IN REMAINDER    *
*    REG 1 - PARAMETERS                                               *
*    REG 3 - CVT                                                      *
*    REG 4 - TCB                                                      *
*    REG 5 - TSB                                                      *
*    REG 10 - ADDR OF ARRAY OF CONSTANTS (FOR USE BY OTHER ROUTINES)  *
*    REG 14 - RETURN ADDRESS                                          *
*                                                                     *
* EXITS - ERROR                                                       *
*         BR 14 WITH RETURN CODE IN REGISTER 15                       *
*                                                                     *
* TABLES AND WORK AREAS - SEE DSECTS AT END OF MODULE                 *
*                                                                     *
* ATTRIBUTES - REENTRANT                                              *
*                                                                     *
* CHARACTER CODE DEPENDENCY - NONE                                    *
*                                                                     *
* NOTES - THIS MODULE CONTAINS THE TCAM-SPECIFIC CODE REMOVED IN THE  *
*         SPLIT OF IGC0009D                                           *
*                                                                     *
* CHANGE ACTIVITY -                                                   *
* A000000-999999                                               @ZZZZZZZ
*                                                                     *
***********************************************************************
         SPACE 3
********
******** REGISTER EQUATES
********
RPARM0   EQU   0                        PARAMETER REG 0
RWORK0   EQU   0                        WORK REGISTER
RPARM1   EQU   1                        PARAMETER REG 1
RWORK1   EQU   1                        WORK REGISTER
RWORK2   EQU   2                        WORK REGISTER
RPARM2   EQU   2                        PARAMETER REG 2 FOR OUTPUT
RCVT     EQU   3                        POINTS TO CVT
RTCB     EQU   4                        POINTS TO TCB
RTSB     EQU   5                        POINTS TO TSB
RWORK6   EQU   6                        WORK REGISTER
RTCX     EQU   6                        BASE FOR TCX
RTSI     EQU   7                        BASE FOR TSI
RWORK7   EQU   7                        WORK REGISTER
RWORK8   EQU   8                        WORK REGISTER
RWORK9   EQU   9                        WORK REGISTER
RWORK10  EQU   10                       WORK REGISTER
RCONST   EQU   10                       POINTS TO PROGRAM CONSTANTS
RWORK11  EQU   11                       WORK REGISTER
RBASE    EQU   12                       BASE REGISTER
R13      EQU   13                       REGISTER 13
R14      EQU   14                       REGISTER 14
RLINK    EQU   14                       LINK REGISTER
R15      EQU   15                       REGISTER 15
RBRANCH  EQU   15                       BRANCH REGISTER
RCODE    EQU   15                       RETURN CODE REGISTER
         SPACE 2
********
******** DISPLACEMENT,LENGTH AND MASK EQUATES
********
BYTE1    EQU   8                        MASK FOR BYTE 1 IN REG.
D2       EQU   2                        DISPLACEMENT OF 2
L2       EQU   2                        LENGTH OF 2
L24      EQU   24                       LENGTH OF 24
RETCODE4 EQU   4                        RETURN CODE OF 4
RETCODE8 EQU   8                        RETURN CODE OF 8
CZERO    EQU   0                        TO TEST FOR ZERO
CFOUR    EQU   4                        CONSTANT OF 4
CEIGHT   EQU   8                        CONSTANT OF 8
TWELVE   EQU   12                       CONSTANT OF 12
OFF      EQU   X'FF'                    TO TURN BITS OFF
QINEC    EQU   21                       QTIP ENTRY CODE/TCLEARQ/INPUT
QOUTEC   EQU   22                       QTIP ENTRY CODE/TCLEARQ/OUTPUT
         SPACE 3
********
******** DEFINE ALTERNATE ENTRY POINTS
********
         ENTRY CONST
         ENTRY POSTTSB
         SPACE 3
********
******** ESTABLISH BASE REGISTERS AND ADDRESSABILITY
********
         BALR  RBASE,RWORK0             ADDRESSABILITY
         USING *,RBASE                  FOR THIS CSECT
         USING CVT,RCVT                 CVT ADDRESSABILITY
         USING TCB,RTCB                 TCB ADDRESSABILITY
IGG0940H IEDHJN AFTERHJN,HJN
         LA    RCONST,CONST             BASE FOR CONSTANTS
*                                       (PASSED FOR OTHER MODULES)
*******
*******  CHECK ENTRY CODES
*******
         CLM   RPARM0,BYTE1,T20         HIGHEST ENTRY CODE
         BH    INVALID                  CODE IS INVALID
         SPACE 2
TCABTSE  EQU   *                        CHECK FOR TCABEND OR
*                                       TSEND
         CLM   RPARM0,BYTE1,XZERO       IS ENTRY CODE 0
         BE    BRANCH1                  YES, BRANCH TO MODULE
         CLM   RPARM0,BYTE1,T2          IS ENTRY CODE 2
         BE    BRANCH1                  YES BRANCH TO MODULE
         SPACE
***********************************************************************
*        GET POINTER TO TSB                                           *
***********************************************************************
BRANCH   EQU   *
         L     RWORK6,PSAAOLD-PSA       ADDRESS OF ASCB
         USING ASCB,RWORK6              ASCB ADDRESSABILITY
         L     RTSB,ASCBTSB             ADDRESS OF TSB
         DROP  RWORK6
         USING TSB,RTSB                 TSB IS ADDRESSABLE
********
******** BRANCH TO PROPER MODULE
********
         LR    RWORK2,RPARM0            GET ENTRY CODE
         SRL   RWORK2,L24               SHIFT TO LOW ORDER AND
         SLL   RWORK2,L2                MPY BY 4 (TABLE ENTRY
*                                       LENGTH)
BRNEXT   L     RWORK2,BRTBL(RWORK2)     GET BRANCH ADDRESS
BRNCH    LR    RBASE,RWORK2             SET UP NEW BASE REG
         BR    RBASE                    GO TO PROPER ROUTINE
         SPACE
***********************************************************************
*        FOR TSO/TCAM ENTRY CODES 0 AND 2, REG 5 (RRB) MUST           *
*        CONTAIN THE RB ADDRESS. THOSE CODES ARE HANDLED HERE         *
*        TO AVOID THE RESETTING OF REG 5 TO THE TSB ADDRESS           *
*        NEEDED BY THE OTHER TSO/TCAM ENTRY CODES.                    *
***********************************************************************
BRANCH1  EQU   *
         LR    RWORK2,RPARM0            GET ENTRY CODE
         SRL   RWORK2,L24               SHIFT TO LOW ORDER AND
         SLL   RWORK2,L2                MPY BY 4 (TABLE ENTRY LENGTH)
         L     RBASE,BRTBL(RWORK2)      GET BRANCH ADDRESS
         BR    RBASE                    GO TO PROPER ROUTINE
         SPACE 2
INVALID  EQU   *
         LA    RCODE,RETCODE4           INDICATE INVALID ENTRY CODE
         BR    R14                      RETURN TO CALLER
         SPACE
INVALID8 EQU   *
         LA    RCODE,RETCODE8           SET RETURN CODE 8 FOR
         BR    R14                                RETURN
         EJECT
***********************************************************************
*        POSTTSB ROUTINE - USED TO POST THE TSB TO IEDAYP WHO UPDATES *
*                          AND/OR DOES TPOSTING OF THE QCB AND/OR TSI *
*                          THE QCB IS IN TCAMS PRIVATE MEMORY AND NOT *
*                          DIRECTLY ADDRESSABLE                       *
*             INPUT = REGISTER 1  = ADDRESSS OF MASK                  *
*                     REGISTER 3  = ADDRESS OF CVT                    *
*                     REGISTER 5  = ADDRESS OF TSB TO BE POSTED       *
*                     REGISTER 6-9 = WORK REGISTERS                   *
***********************************************************************
POSTTSB  EQU   *
         USING *,RBRANCH                ADDRESSABILITY
         USING TSB,RTSB                 TSB ADDRESSABILITY
         USING CVT,RCVT                 CVT ADDRESSABILITY
         LM    RWORK6,RWORK7,TSBTPOST   GET CONTENTS OF TSBTPOST
*                                       FIELD FOR COMPARE
RETRY    EQU   *
         LR    RWORK8,RWORK6            COPY 1ST WORD IN REG8
         LR    RWORK9,RWORK7            COPY 2ND WORD IN REG9
         O     RWORK8,CZERO(,RWORK1)    OR WITH MASK
         N     RWORK8,CFOUR(,RWORK1)    AND IN VALUE
         O     RWORK9,CEIGHT(,RWORK1)   OR WITH MASK WORD 2
         N     RWORK9,TWELVE(,RWORK1)   AND WITH VALUE WORD 2
         CDS   RWORK6,RWORK8,TSBTPOST   COMPARE & SWAP TSBTPOST
         BNE   RETRY                    FIELDS NOT EQUAL,RETRY
         LTR   RWORK6,RWORK6            IS POST PENDING?
         BM    GOBACK                   YES,SKIP POSTING
         L     RTCX,CVTAQAVT            GET ADDR OF TCX
         USING IEDQTCX,RTCX             TCX ADDRESSABILITY
         L     RTSI,TCXTSI              GET ADDR OF TSI
         USING IEDQTSI,RTSI             TSI ADDRESSABILITY
         LA    RWORK8,TSITSAP-(QCBSTVTO-IEDQQCB) DUMMY QCB
*                                        STARTING ADDR
         ST    RWORK8,TSBRQCB           STORE IN QCB CHAIN
         LA    RWORK9,TSBRCB            ADDRESS OF RCB
         L     RWORK8,TCXREADY          GET ADDR OF READY QUEUE
AGAIN    EQU   *
         ST    RWORK8,TSBLINKA          STORE LINK ADDRESS
         MVI   TSBPRI,PRIDESTQ          STOR PRIORITY FIELD
         CS    RWORK8,RWORK9,TCXREADY   SWAP READY QUEUE ADDR
         BNE   AGAIN                    NOT EQUAL TRY AGAIN
         EJECT
***********************************************************************
*        XMPOST ROUTINE - USED TO WAKE UP TCAM DISPATCHER TO POST     *
*                         QCB. XMPOST IS DONE IN KEY ZERO WITH THE    *
*                         LOCAL AND CMS LOCKS HELD.                   *
*        REG 10 = ZERO                                                *
*        REG 11 = ADDR OF TCAM DISPATCHER'S ECB, HIGH ORDER BIT IS 1  *
*                 TO INDICATED XMPOST                                 *
*        REG 12 = ADDR OF CVTBRET FOR ERRET(ERROR RETURN)             *
*        REG 13 = ADDR OF TCAMS ASCB                                  *
*        REG 14 = RETURN ADDR                                         *
*        REG 15 = ADDR OF IEAOPT01 - XMPOST ENTRY POINT ROUTINE       *
*        REG 14 SAVED IN REG 1,REG11 SAVED IN REG 8,REG 10 SAVED IN   *
*        REG 6,REG 12 SAVED IN REG 7 AND REG 13 SAVED IN REG 9        *
***********************************************************************
         MODESET EXTKEY=SUPR            NEED SUPERVISOR KEY FOR XMPOST
         LR    RWORK1,R14               SAVE RETURN ADDR
         LR    RWORK7,RBASE             SAVE REG 12
         LA    RBASE,CVTBRET            E.P. IF POST FAILS
         LR    RWORK8,RWORK11           SAVE REG 11
         LR    RWORK9,R13               SAVE REG 13
         L     R13,TCXASCB              @ OF TCAM'S ASCB
         L     RWORK11,TCXAVT           ADDR OF AVT
         DROP  RTCX
         USING IEDQAVTD,RWORK11         ADDRESSABILITY TO AVT
         LA    RWORK11,AVTOSECB         @ OF TCAM'S ECB TO POST
         ICM   RWORK11,BYTE1,HIHBIT     HIGH ORDER BIT INDICATES
*                                       IT IS AN XMPOST
         DROP  RWORK11
         LR    RWORK6,RWORK10           SAVE REG 10
         SR    RWORK10,RWORK10          ZERO RWORK10 FOR XMPOST
         DROP  RBRANCH
         L     RBRANCH,CVT0PT01         @ OF E.P. FOR XMPOST
         BALR  R14,RBRANCH              BR TO XMPOST (IEAOPT01)
         LR    R14,RWORK1               .RESTORE
         LR    RWORK10,RWORK6           .ALL
         LR    RBASE,RWORK7             .REGISTERS
         LR    RWORK11,RWORK8           .PREVIOUSLY
         LR    R13,RWORK9               .SAVED
         MODESET EXTKEY=TCAM            RESTORE TCAM'S KEY
GOBACK   EQU   *
         BR    R14                      RETURN TO CALLER
         EJECT
PAT      DC    13F'0'                   MAINTENANCE PATCH AREA
         SPACE
         DS    0F
CONST    EQU   *
XZERO    DC    F'0'                     CONSTANT OF 0
XNINES   DC    F'99999999'              CONSTANT OF 99,999,999
XFIFTEEN DC    AL1(15)                  CONSTANT OF 15
XTWO     DC    XL1'02'                  CONSTANT OF 2
XTEN     DC    H'10'                    CONSTANT OF 10
XSIXTEEN DC    AL1(16)                  CONSTANT OF 16
XENABLE  DC    X'FF'                    TO ENABLE INTERRUPTS
XSIX     DC    H'6'                     CONSTANT OF 6
XEIGHT   DC    AL1(8)                   CONSTANT OF 8
XCP5041  DC    XL3'026D16'              BKSP/UNDERSCORE CHAR. PROMPT
XHIGHBIT DC    XL1'80'                  TO TEST HIGH ORDER BITS
XCPTWX   DC    XL3'024B0D'              PERIOD/CAR.RETURN CHAR. PROMPT
         DS    0F
XTPUT1   DC    XL4'0E000000'            TPUT,HOLD,CONTROL
XTPUT2   DC    XL4'0A000000'            TPUT,CONTROL
XONE     DC    AL1(1)                   CONSTANT OF 1
XIDLE    DC    X'17'                    TPUT IDLE CHARACTER
TPADDR   DC    A(POSTTSB)               TSB POSTING ROUTINE
TPMASK   DC    AL1(TSBPOSTO+TSBTPQCB,0,0,0) .MASK TO
         DC    F'-1'                    .CAUSE IEDAYP
         DC    AL1(0,0,QCBTPUT,QCBTPUT) .TO TPOST THE QCB
         DC    F'-1'                    .TO ITSELF
T2       DC    AL1(2)                   CONSTANT OF 2 FOR CLM
T3       DC    AL1(3)                   CONSTANT OF 3 FOR CLM
T11      DC    AL1(11)                  CONSTANT OF 11 FOR CLM
T15      DC    AL1(15)                  CONSTANT OF 15 FOR CLM
T16      DC    AL1(16)                  CONSTANT OF 16 FOR CLM
T17      DC    AL1(17)                  CONSTANT OF 17 FOR CLM
T20      DC    AL1(20)                  CONSTANT OF 20 FOR CLM
HIHBIT   DC    XL1'80'                  HIGH ORDER BIT SETTING
         EJECT
********
******** BRANCH TABLE FOR FUNCTION SUPPORT ROUTINES
********
         USING IGG0940H+D2,RBASE
         DS    0F                       FULLWORD ALIGNMENT
BRTBL    EQU   *
TCABEND  DC    V(IGG09400)              ENTRY CODE 0
TCLEARQ  DC    A(IGG09401)              ENTRY CODE 1
TSEND    DC    V(IGG09402)              ENTRY CODE 2
MSGS     DC    V(IGG09403)              ENTRY CODE 3
STBREAK  DC    V(IGG09404)              ENTRY CODE 4
STCOM    DC    V(IGG09405)              ENTRY CODE 5
STTIMEOU DC    V(IGG09406)              ENTRY CODE 6
STCC     DC    V(IGG09407)              ENTRY CODE 7
STATTN   DC    V(IGG09408)              ENTRY CODE 8
STAUTOLN DC    V(IGG09409)              ENTRY CODE 9
STSIZE   DC    V(IGG0940A)              ENTRY CODE 10
GTSIZE   DC    V(IGG0940B)              ENTRY CODE 11
STAUTOCP DC    V(IGG0940C)              ENTRY CODE 12
STAUTOPT DC    V(IGG0940D)              ENTRY CODE 13
RTAUTOPT DC    V(IGG0940E)              ENTRY CODE 14
ENTRY15  DC    A(INVALID)               ENTRY CODE 15,INVALID
STCLEAR  DC    V(IGG0940G)              ENTRY CODE 16
TSTGTTRM DC    V(IGG09411)              ENTRY CODE 17
TSTFSMOD DC    A(INVALID8)              ENTRY CODE 18,INVALID
TSTLINEN DC    A(INVALID8)              ENTRY CODE 19,INVALID
TSTTMPMD DC    V(IGG09414)              ENTRY CODE 20
         EJECT
***********************************************************************
* TCLEARQ - THE FUNCTION OF THIS SECTION IS TO PERFORM THE TCLEARQ    *
*           FUNCTION.  DEPENDING UPON THE PARAMETER PASSED, EITHER    *
*           INPUT OR OUTPUT BUFFER QUEUES ARE CLEARED.  FREED BUFFERS *
*           ARE REMOVED FROM THE TSB QUEUES AND RETURNED TO THE TIOC  *
*           FREE QUEUES .                                             *
***********************************************************************
IGG09401 CSECT
         USING CVT,RCVT                 CVT ADDRESSABILITY
         USING TCB,RTCB                 TCB ADDRESSABILITY
         USING TSB,RTSB                 TSB ADDRESSABILITY
         USING *,RBASE                  BASE ADDRESSABILITY
********
******** MAIN LINE TCLEARQ
********
         LR    RWORK2,RPARM1            SAVE PARM
         LTR   RPARM1,RPARM1            TEST ENTRY CODE
         BP    PARMBAD                  ONLY NEGATIVE OR ZERO VALID
         BZ    FREE                     GO PROCESS OUTPUT QUES
         SPACE 1
***********************************************************************
*        DISALLOW TERMINAL INPUT UNTIL NEXT TGET IN ORDER             *
*        TO SYNCHRONIZE WITH THE USER.                                *
***********************************************************************
         SPACE 1
INPUT    EQU   *
         MODESET EXTKEY=TCAM            GET TCAMS KEY FOR TSB
*                                       UPDATE
         NI    TSBFLG2,OFF-TSBBRKIN     DISALLOW BREAKIN IN PROGRESS
         OI    TSBSTAT,TSBLWAIT         LOCK KEYBOARD
         OI    TSBFLG1,TSBIFLSH         TELL TSINPUT TO FLUSH INPUT
         OI    TSBFLG3,TSBSPIT          STOP AUTOPROMPTING
         LR    RWORK10,RLINK            SAVE RETURN REGISTER
         L     RWORK1,M1                PASS ADDR OF MASK FLD
         L     RBRANCH,POST             ADDR OF POSTTSB ROUTINE
         BALR  RLINK,RBRANCH            GO POST TSB
         LR    RLINK,RWORK10            RESTORE RETURN REG
         EJECT
***********************************************************************
*        FREE CMS AND LOCAL LOCKS                                     *
***********************************************************************
         SPACE 1
FREE     EQU   *
         LR    RWORK7,RLINK             SAVE RETURN ADDR
         LR    RWORK10,RBASE            SAVE BASE
         MODESET EXTKEY=SUPR            RETURN TO SUPR KEY
         SETLOCK RELEASE,TYPE=CMS,      FREE CMS LOCK                  X
               RELATED=(TSB,IGC0009D(GETLKS))
         SETLOCK RELEASE,TYPE=LOCAL,    FREE LOCAL LOCK                X
               RELATED=(TSB,IGC0009D(GETLKS))
         LR    RBASE,RWORK10            RESTORE BASE
         LR    RLINK,RWORK7             RESTORE RETURN ADDR
         SPACE 1
*********
********* ISSUE QTIP SVC TO INVOKE MODULE IEDAYAA FOR QUEUE CLEARING
*********
         SPACE 1
         LR    R15,RTSB                 PUT TSB ADDR IN REG 15
         LTR   RWORK2,RWORK2            TEST ENTRY CODE
         BM    QTIPINPT                 ISSUE QTIP FOR INPUT
         SPACE
         QTIP  QOUTEC                   QTIP FOR OUTPUT
         B     LEAVE                    RETURN TO CALLER
         SPACE
QTIPINPT EQU   *
         QTIP QINEC                     QTIP FOR INPUT
LEAVE    EQU   *
         SR    RCODE,RCODE              SET RETURN CODE ZERO
         BR    R14                      RETURN TO CALLER
         SPACE
PARMBAD  EQU   *
         LA    RCODE,RETCODE4           INDICATE INVALID ENTRY CODE
         BR    R14                      RETURN TO CALLER
         EJECT
ZAP      DS    9F'0'                    MAINTENANCE PATCH AREA
         SPACE
POST     DC    A(POSTTSB)               POSTTSB ADDR
MASKD1   DC    AL1(TSBPOSTO+TSBTPQCB,0,0,0) .ALLOW
         DC    AL1(255,255,255,255)     .NO
         DC    AL1(0,0,QCBNOBUF+QCBTPUT,QCBTPUT)    .TERMINAL
         DC    AL1(255,255,255,255-QCBNOBUF) .INPUT
         SPACE
M1       DC    A(MASKD1)                ADDR OF MASKD1
         SPACE 4
CONSTANT DSECT
ZERO     DC    F'0'                     CONSTANT OF 0
NINES    DC    F'99999999'              CONSTANT OF 99,999,999
FIFTEEN  DC    AL1(15)                  CONSTANT OF 15
TWO      DC    XL1'02'                  CONSTANT OF 2
TEN      DC    H'10'                    CONSTANT OF 10
SIXTEEN  DC    AL1(16)                  CONSTANT OF 16
ENABLE   DC    X'FF'                    TO ENABLE INTERRUPTS
SIX      DC    H'6'                     CONSTANT OF 6
EIGHT    DC    AL1(8)                   CONSTANT OF 8
CP5041   DC    XL3'026D16'              BKSP/UNDERSCORE CHAR. PROMPT
HIGHBIT  DC    XL1'80'                  TO TEST HIGH ORDER BITS
CPTWX    DC    XL3'024B0D'              PERIOD/CAR.RETURN CHAR. PROMPT
TPUT1    DC    XL4'0E000000'            TPUT,HOLD,CONTROL
TPUT2    DC    XL4'0A000000'            TPUT,CONTROL
ONE      DC    AL1(1)                   CONSTANT OF ONE
IDLE     DC    X'17'                    TPUT IDLE CHARACTER
         EJECT
         IHAASCB
         EJECT
         TAVTD
         EJECT
CVT      DSECT
         CVT
         EJECT
         TPRIOR
         EJECT
         IHAPSA
         EJECT
         TQCBD
         EJECT
         IKJTCB
         EJECT
         TTCXD
         EJECT
         IKJTSB
         EJECT
         TTSID
         END
