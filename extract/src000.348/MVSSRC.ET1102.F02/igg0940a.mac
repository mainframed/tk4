         TITLE 'IGG0940A - STSIZE ROUTINE'
IGG0940A CSECT
         COPY  LCGASMS2                 LIBRARY CONTROL         YS02019
***********************************************************************
*                                                                M2650
*                                                                 M2696
*                                                                 M2697
*                                                                 M2689
*                                                                M5113
*                                                                     *
*                                                                M0590
*                                                                 M5113
*
*    RELEASE 23 DELETIONS/CHANGES                                     *
*    RELEASE 22 DELETIONS/CHANGES                                     *
*    RELEASE 21 DELETIONS/CHANGES                                     *
*                                                                     *
* STATUS - CHANGE LEVEL 002                                           *
*    CHANGE LEVEL 0                                                   *
*                                                                     *
* FUNCTION -                                                          *
*    THIS ROUTINE IS TO SERVE THE MACRO 'STSIZE', WHICH               *
*    ALLOW THE USER TO VARY THE PHYSICAL LINE SIZE OF HIS TERMINAL,   *
*    AND THE SCREEN LENGTH OF HIS DISPLAY STATION.  THIS ROUTINE      *
*    REPLACES THE ORIGINAL PHYSICAL LINE SIZE KEPT IN TSB (TSBLNSZ)   *
*    WITH USER SPECIFIED VALUE.  FOR DISPLAY STATION, IT ALSO REPLACES*
*    THE ORIGINAL SCREEN LENGTH KEPT IN TSB (TSBLNNO) WITH USER       *
*    SPECIFIED VALUE.                                                 *
*                                                                     *
* ENTRY POINTS -                                                      *
*         IGG0940A - CONTROL IS RECEIVED FROM MODULE IGC0009D VIA     *
*         BRANCH ACCORDING TO THE ENTRY CODE 10                       *
*         (HOLDING THE LOCAL AND THE CMS LOCKS IN SUPERVISOR KEY)     *
*                                                                     *
* INPUT -                                                             *
*    REGISTER 0 CONTAINS:                                             *
*    ********************************************************         *
*    * ENTRY CODE *            *            * SCREEN LENGTH *         *
*    ********************************************************         *
*    REGISTER 1 CONTAINS:                                             *
*    ********************************************************         *
*    *            *            *            *   LINE SIZE   *         *
*    ********************************************************         *
*    REGISTER 3 CONTAINS CVT ADDRESS                                  *
*    REGISTER 4 CONTAINS TCB ADDRESS                                  *
*    REGISTER 5 CONTAINS TSB ADDRESS                                  *
*    REGISTER 10 CONTAINS THE ADDRESS OF AN ARRAY OF CONSTANTS
*    REGISTER 12 CONTAINS THE BASE ADDRESS
*    REGISTER 14 CONTAINS THE RETURN ADDRESS
*                                                                     *
* OUTPUT -                                                            *
*    REGISTER 15 HAS RETURN CODE:                                     *
*           0 - SUCCESSFUL                                            *
*           4 - INVALID PARAMETER                                     *
*           8 - INCORRECT PARAMETER FOR TERMINAL                      *
*           C - DIMENSION NOT STANDARD (FOR DISPLAY STATION ONLY)     *
*                                                                     *
* EXTERNAL REFERENCES -                                               *
*         NONE                                                        *
*                                                                     *
* EXITS, NORMAL -                                                     *
*         BR 14 WITH 0 IN REGISTER 15                                 *
*                                                                     *
* EXITS, ERROR -                                                      *
*         BR 14 WITH 4, 8, 12 IN REGISTER 15                          *
*                                                                     *
* TABLES/WORK AREAS -                                                 *
*    CONTROL BLOCKS ARE DESCRIBED IN THE DSECTS AT END OF LISTING     *
*                                                                     *
* ATTRIBUTES -                                                        *
*    REENTRANT, REFRESHABLE, SUPERVISOR MODE, AND ENABLED             *
*                                                                     *
* CHARACTER CODE DEPENDENCY -                                         *
*    THIS MODULE IS NOT CHARACTER DEPENDENT                           *
*                                                                     *
*    NOTE -     THE STANDARD SCREEN DIMENSIONS ARE -                  *
*                                                                     *
*               FOR 3270 - 12X40, 24X80                               *
*                                                                     *
*               FOR 2260 - 12X80, 12X40, 6X40, 15X64                  *
*                                                                     *
***********************************************************************
         SPACE 3
********
******** EQUATES FOR REGISTER
********
RPARM0   EQU   0                        PARAMETER REG 0
RWORK0   EQU   0                        WORK REGISTER
RPARM1   EQU   1                        PARAMETER REG 1
RWORK1   EQU   1                        WORK REGISTER
RWORK2   EQU   2                        WORK REGISTER
RWORK3   EQU   3                        WORK REGISTER
RCVT     EQU   3                        POINTS TO CVT
RTCB     EQU   4                        POINTS TO TCB
RTSB     EQU   5                        POINTS TO TSB           YS02019
RBASE    EQU   12                       BASE REGISTER
RCONST   EQU   10                       POINTS TO PROGRAM       YS02019
*                                       CONSTANTS               YS02019
R13      EQU   13                       REGISTER 13
R14      EQU   14                       REGISTER 14
RLINK    EQU   14                       LINK REGISTER
R15      EQU   15                       REGISTER 15
RBRANCH  EQU   15                       BRANCH REGISTER
RCODE    EQU   15                       RETURN CODE REGISTER
         SPACE 2
********
******** DISPLACEMENT,LENGTH AND MASK EQUATES
********
BYTE1    EQU   8                        MASK FOR BYTE 1 IN REG.
BYTE2    EQU   4                        MASK FOR BYTE 2 IN REG.
BYTE3    EQU   2                        MASK FOR BYTE 3 IN REG.
BYTE4    EQU   1                        MASK FOR BYTE 4 IN REG.
BYTE34   EQU   3                        MASK FOR BYTES 3&4 IN REG.
BYTE234  EQU   7                        MASK FOR BYTES 2&3&4 IN REG.
D0       EQU   0                        DISPLACEMENT OF 0
D1       EQU   1                        DISPLACEMENT OF 1
D2       EQU   2                        DISPLACEMENT OF 2
D3       EQU   3                        DISPLACEMENT OF 3
D4       EQU   4                        DISPLACEMENT OF 4
D5       EQU   5                        DISPLACEMENT OF 5
D6       EQU   6                        DISPLACEMENT OF 6
D14      EQU   14                       DISPLACEMENT OF 14
L1       EQU   1                        LENGTH OF 1
L2       EQU   2                        LENGTH OF 2
L3       EQU   3                        LENGTH OF 3
L4       EQU   4                        LENGTH OF 4
L8       EQU   8                        LENGTH OF 8
L16      EQU   16                       LENGTH OF 16
L20      EQU   20                       LENGTH OF 20
L24      EQU   24                       LENGTH OF 24
L31      EQU   31                       LENGTH OF 31
L32      EQU   32                       LENGTH OF 32
RETCODE4 EQU   4                        RETURN CODE OF 4
RETCODE8 EQU   8                        RETURN CODE OF 8
RETCODEC EQU   12                       RETURN CODE OF 12
HIPRI    EQU   X'E4'                    TCAM POSTING PRIORITY
COMPLETE EQU   X'40'                    TO TEST POST COMPLETE BIT
CZERO    EQU   0                        TO TEST FOR ZERO
CONE     EQU   1                        CONSTANT OF 1
CTWO     EQU   2                        CONSTANT OF 2
CTHREE   EQU   3                        CONSTANT OF 3
CFIFTEEN EQU   15                       CONSTANT OF 15
CSIXTEEN EQU   16                       CONSTANT OF 16
CFOX     EQU   X'FF'                    TO TEST FOR 'FF'
BLANK    EQU   X'40'                    TO TEST FOR BLANK
OFF      EQU   X'FF'                    TO TURN BITS OFF
IDLE     EQU   X'17'                    TERM. CTL. CHAR. - IDLE
NL       EQU   X'15'                    TERM. CTL. CHAR. - NEW LINE
CR       EQU   X'0D'                    TERM. CTL. CHAR. - CARRIAGE RET
LF       EQU   X'25'                    TERM. CTL. CHAR. - LINE FEED
HEX18    EQU   X'18'                    TERM. CTL. CHAR. - TWX CHAR.
HEX20    EQU   X'20'                    TO TEST BIT 2 OF BYTE
LNSZ40   EQU   40                       LINE SIZE 40
LNSZ64   EQU   64                       LINE SIZE 64
LNSZ80   EQU   80                       LINE SIZE 80
LNNO6    EQU   6                        LINES PER SCREEN - 6
LNNO12   EQU   12                       LINES PER SCREEN - 12
LNNO15   EQU   15                       LINES PER SCREEN - 15
QINEC    EQU   21                       QTIP ENTRY CODE/TCLEARQ/INPUT
QOUTEC   EQU   22                       QTIP ENTRY CODE/TCLEARQ/OUTPUT
M8       EQU   8                        EIGHT
M16      EQU   16                       SIXTEEN
M12      EQU   12                       TWELVE
M15      EQU   15                       FIFTEEN
M40      EQU   40                       FORTY
M64      EQU   64                       SIXTY-FOUR
M6       EQU   6                        SIX
M24      EQU   24                       TWENTY-FOUR
M80      EQU   80                       EIGHTY
L80      EQU   80                       SAME AS ABOVE
         SPACE 3
********
******** ESTABLISH BASE REGISTER AND ADDRESSABILITY
********
         USING *,RBASE                  BASE REGISTER ADDRESSABILITY
         USING TSB,RTSB                 TSB ADDRESSABILITY
         B     VALIDITY                 BR AROUND ID AND DATE   YS02019
         DC    C'IGG0940A'              MODULE IDENTIFIER       YS02019
         DC    X'3123'                  DATE - 05/03/73         YS02019
********
******** VALIDITY CHECK PARAMETERS
********
VALIDITY EQU   *                                                YS02019
         LR    RWORK2,RPARM0            PUT PARM IN WORK REG
         SLL   RWORK2,L8                DROP ENTRY CODE
         SRL   RWORK2,L16               DROP PARAMETER
         LTR   RWORK2,RWORK2            TEST PARM
         BNZ   PARMBAD                  INVALID
         LR    RWORK2,RPARM1            PUT PARM IN WORK REG
         SRL   RWORK2,L8                DROP PARM
         LTR   RWORK2,RWORK2            TEST PARM
         BNZ   PARMBAD                  INVALID
********
******** PERFORM MAIN FUNCTION OF ROUTINE
********
         SR    RCODE,RCODE              CLEAR RETURN CODE REGISTER
         LTR   RPARM1,RPARM1            LINE SIZE SPECIFIED      S22028
         BZ    RC08                     NO - INVALIDS            S22028
         MODESET EXTKEY=TCAM            GET TCAM'S KEY          YS02019
         SLL   RPARM0,L8                DROP ENTRY CODE          Y01018
         SRL   RPARM0,L8                RESET REGISTER           Y01018
         TM    TSBSTAT,TSBDSPLY         IS THIS A DISPLAY
         BO    DISPLAY                  YES
NOTDSPLY EQU   *
         LTR   RPARM0,RPARM0            SCREEN LENGTH SPECIFIED  S22028
         BNZ   RC08                     YES,INVALID FOR NONDISPLAYS
         STC   RPARM1,TSBLNSZ           SET NEW LINE SIZE IN TSB
         B     RETRN                    RETURN TO SUPERVISOR    YS02019
DISPLAY  EQU   *
         LTR   RPARM0,RPARM0            SCREEN LENGTH SPECIFIED  S22028
         BZ    RC08                     NO PARM - INVALID
         STC   RPARM0,TSBLNNO           SET NUMBER OF LINES/SCREEN
         STC   RPARM1,TSBLNSZ           SET LINE SIZE
         SPACE 3
********
******** CHECK SCREEN DIMENSIONS
********
         CLI   TSBLNNO,LNNO12           LNNO SPECIFIED = 12      S22028
         BNE   CHK3270                  NO, CHK FOR 3270         S22028
         CLI   TSBLNSZ,M40              LNSZ SPECIFIED = 40      S22028
         BE    RETRN                    YES, RETURN             YS02019
         SPACE 1                                                 S22028
CHK3270  EQU   *                                                 S22028
         TM    TSBSTAT,TSB3270          TEST FOR 3270 TERMINAL   S22028
         BO    TERM3270                 BRANCH IF YES            S22028
TERM2260 EQU   *                                                 S22028
         CLI   TSBLNNO,M12              LNNO SPECIFIED = 12      S22028
         BNE   CKLNNO6                  NO, BRANCH               S22028
         CLI   TSBLNSZ,L80              LNSZ SPECIFIED = 80      S22028
         BE    RETRN                    YES, RETURN             YS02019
         SPACE 1                                                 S22028
CKLNNO6  EQU   *                                                 S22028
         CLI   TSBLNNO,M6               LNNO SPECIFIED 6         S22028
         BNE   CKLNNO15                 NO, BRANCH               S22028
         CLI   TSBLNSZ,M40              LNSZ SPECIFIED = 40      S22028
         BE    RETRN                    YES, RETURN             YS02019
         SPACE 1                                                 S22028
CKLNNO15 EQU   *                                                 S22028
         CLI   TSBLNNO,M15              LNNO SPECIFIED = 15      S22028
         BNE   RC0C                     NO, RETURN CODE = C      S22028
         CLI   TSBLNSZ,M64              LNSZ SPECIFIED = 64      S22028
         BNE   RC0C                     NO, RETURN CODE = C      S22028
         B     RETRN                    15X64 VALID FOR 2260    YS02019
         SPACE 1                                                 S22028
TERM3270 EQU   *                                                 S22028
         CLI   TSBLNNO,M24              LNNO SPECIFIED = 24      S22028
         BNE   RC0C                     NO, RETURN CODE = C      S22028
         CLI   TSBLNSZ,L80              LNSZ SPECIFIED = 80      S22028
         BNE   RC0C                     NO, RETURN CODE = C      S22028
         B     RETRN                    24X80 (VALID FOR 3270)  YS02019
         SPACE 3                                                 S22028
RC0C     EQU   *                                                 S22028
         LA    RCODE,RETCODEC           SET RETURN CODE TO C     S22028
         B     RETRN                    RETURN TO SUPERVISOR    YS02019
         SPACE 3
RC08     EQU   *
         LA    RCODE,RETCODE8           SET RETURN CODE TO 08
         B     RETRN                    RETURN TO SUPERVISOR    YS02019
         SPACE 1                                                 S22028
PARMBAD  EQU   *
         LA    RCODE,RETCODE4           INVALID PARAMETERS
RETRN    EQU   *
         MODESET EXTKEY=SUPR            CHANGE TO KEY 0         YS02019
         BR    RLINK                    RETURN TO CALLER        YS02019
         SPACE 3
         DC    9F'0'                    MAINTENANCE PATCH AREA  YS02019
         EJECT
         IKJTSB
         END
