         TITLE 'IGG09406 - STTIMEOU ROUTINE'
IGG09406 CSECT
         COPY  LCGASMS2
***********************************************************************
*                                                                     *
* STATUS -                                                            *
*    CHANGE LEVEL 01                                                  *
*                                                                     *
* FUNCTION -                                                          *
*    THIS ROUTINE IS TO SERVE THE MACRO 'STTIMEOU'. IT IS PART OF SVC *
*    94. IT SETS THE BIT CONDITION IN QCB TO REFLECT THE PRESENCE     *
*    OR ABSENCE OF TIMEOUT FEATURE OF THE TERMINAL.  THIS MACRO ONLY  *
*    APPLIES TO 1050 DIAL TERMINAL.  IF THE MACRO IS ISSUED FOR       *
*    TERMINALS OTHER THEN 1050 DIAL, THE FUNCTION IS IGNORED AND THE  *
*    INVALID TERMINAL CODE '08' IS RETURNED TO THE CALLER.            *
*    FOR 1050 DIAL TERMINALS,IF THE OPERAND SPECIFIES YES, (BIT 0 OF  *
*    FLAG BYTE IN REG 1 IS ON), THE QCBINHBN BIT IN QCB IS TURNED OFF.*
*    OTHERWISE, IF NO IS SPECIFIED, THE QCBINHBN BIT IS TURNED ON.    *
*                                                                     *
* ENTRY POINTS -                                                      *
*         IGG09406 - BEGINNING OF MODULE. CONTROL IS RECEIVED FROM    *
*         MODULE IGC0009D, FIRST LOAD OF SVC 94, ACCORDING TO ENTRY   *
*         CODE OF 6                                                   *
*                                                                     *
* INPUT -                                                             *
*    REGISTER 0 CONTAINS PARAMETER                                    *
*    REGISTER 1 CONTAINS PARAMETER                                    *
*    REGISTER 3 CONTAINS CVT ADDRESS                                  *
*    REGISTER 4 CONTAINS TCB ADDRESS                                  *
*    REGISTER 5 CONTAINS TSB ADDRESS                                  *
*    REGISTER 10 CONTAINS THE ADDRESS OF AN ARRAY OF CONSTANTS
*    REGISTER 12 CONTAINS THE BASE ADDRESS
*    REGISTER 14 CONTAINS THE RETURN ADDRESS
*                                                                     *
* OUTPUT -                                                            *
*    REGISTER 15 HAS RETURN CODE:                                     *
*           0 - SUCCESSFUL                                            *
*           4 - INVALID PARAMETER                                     *
*           8 - WRONG TERMINAL TYPE (NOT 1050)                        *
*                                                                     *
* EXTERNAL REFERENCES -                                               *
*         IEDQTNT - VIA BRANCH                                        *
*                                                                     *
* EXITS, NORMAL -                                                     *
*         BR 14 WITH 0 IN REGISTER 15                                 *
*                                                                     *
* EXITS, ERROR -                                                      *
*         BR 14 WITH 4 OR 8 IN REGISTER 15                            *
*                                                                     *
* TABLES/WORK AREAS -                                                 *
*    CONTROL BLOCKS ARE DESCRIBED IN THE DSECTS AT END OF LISTING     *
*    XCTL TABLE PRECEDES SAID DSECTS                                  *
*                                                                     *
* ATTRIBUTES -                                                        *
*    REENTRANT, REFRESHABLE, SUPERVISOR MODE, AND ENABLED             *
*                                                                     *
* CHARACTER CODE DEPENDENCY -                                         *
*    THIS MODULE IS NOT CHARACTER DEPENDENT                           *
* NOTES -                                                             *
*    NONE                                                             *
*                                                                     *
***********************************************************************
         SPACE 3                                                YS02019
********
******** REGISTER EQUATES
********
RPARM0   EQU   0                        PARAMETER REG 0         YS02019
RWORK0   EQU   0                        WORK REGISTER           YS02019
RPARM1   EQU   1                        PARAMETER REG 1         YS02019
RWORK1   EQU   1                        WORK REGISTER           YS02019
RWORK2   EQU   2                        WORK REGISTER           YS02019
RWORK3   EQU   3                        WORK REGISTER           YS02019
RCVT     EQU   3                        POINTS TO CVT           YS02019
RTCB     EQU   4                        POINTS TO TCB           YS02019
RTSB     EQU   5                        POINTS TO TSB           YS02019
RBASE    EQU   12                       BASE REGISTER           YS02019
RCONST   EQU   10                       POINTS TO PROGRAM       YS02019
*                                       CONSTANTS               YS02019
R13      EQU   13                       REGISTER 13             YS02019
R14      EQU   14                       REGISTER 14             YS02019
RLINK    EQU   14                       LINK REGISTER           YS02019
R15      EQU   15                       REGISTER 15             YS02019
RBRANCH  EQU   15                       BRANCH REGISTER         YS02019
RCODE    EQU   15                       RETURN CODE REGISTER    YS02019
         SPACE 2                                                YS02019
********
******** DISPLACEMENT,LENGTH AND MASK EQUATES
********
BYTE1    EQU   8                        MASK FOR BYTE 1 IN REG. YS02019
BYTE2    EQU   4                        MASK FOR BYTE 2 IN REG. YS02019
BYTE3    EQU   2                        MASK FOR BYTE 3 IN REG. YS02019
BYTE4    EQU   1                        MASK FOR BYTE 4 IN REG. YS02019
BYTE34   EQU   3                        MASK FOR BYTES 3&4 IN   YS02019
*                                       REG.                    YS02019
BYTE234  EQU   7                        MASK FOR BYTES 2&3&4 IN YS02019
*                                       REG.                    YS02019
D0       EQU   0                        DISPLACEMENT OF 0       YS02019
D1       EQU   1                        DISPLACEMENT OF 1       YS02019
D2       EQU   2                        DISPLACEMENT OF 2       YS02019
D3       EQU   3                        DISPLACEMENT OF 3       YS02019
D4       EQU   4                        DISPLACEMENT OF 4       YS02019
D5       EQU   5                        DISPLACEMENT OF 5       YS02019
D6       EQU   6                        DISPLACEMENT OF 6       YS02019
D14      EQU   14                       DISPLACEMENT OF 14      YS02019
L1       EQU   1                        LENGTH OF 1             YS02019
L2       EQU   2                        LENGTH OF 2             YS02019
L3       EQU   3                        LENGTH OF 3             YS02019
L4       EQU   4                        LENGTH OF 4             YS02019
L8       EQU   8                        LENGTH OF 8             YS02019
L16      EQU   16                       LENGTH OF 16            YS02019
L28      EQU   28                       LENGTH OF 28            YS02019
*                                       Y0101                   YS02019
L32      EQU   32                       LENGTH OF 32            YS02019
RETCODE4 EQU   4                        RETURN CODE OF 4        YS02019
RETCODE8 EQU   8                        RETURN CODE OF 8        YS02019
RETCODEC EQU   12                       RETURN CODE OF 12       YS02019
HIPRI    EQU   X'E4'                    TCAM POSTING PRIORITY   YS02019
COMPLETE EQU   X'40'                    TO TEST POST COMPLETE   YS02019
*                                       BIT                     YS02019
CZERO    EQU   0                        TO TEST FOR ZERO        YS02019
CONE     EQU   1                        CONSTANT OF 1           YS02019
CTWO     EQU   2                        CONSTANT OF 2           YS02019
CTHREE   EQU   3                        CONSTANT OF 3           YS02019
CFIFTEEN EQU   15                       CONSTANT OF 15          YS02019
CSIXTEEN EQU   16                       CONSTANT OF 16          YS02019
CFOX     EQU   X'FF'                    TO TEST FOR 'FF'        YS02019
BLANK    EQU   X'40'                    TO TEST FOR BLANK       YS02019
OFF      EQU   X'FF'                    TO TURN BITS OFF        YS02019
         SPACE 3                                                YS02019
********
******** ESTABLISH BASE REGISTER AND ADDRESSABILITY
********
         USING *,RBASE                  BASE ADDRESSABILITY     YS02019
         USING CVT,RCVT                 CVT ADDRESSABILITY      YS02019
         USING TSB,RTSB                 TSB ADDRESSABILITY      YS02019
         USING CONSTANT,RCONST          CONSTANT ADDRESSABILITY YS02019
         B     PASTID                   SKIP MODULE ID.         YS02019
         DC    C'IGG09406'              MODULE ID.              YS02019
         DC    X'3124'                  DATE - 05/04/73         YS02019
PASTID   EQU   *                                                YS02019
********
******** CHECK FOR VALID PARAMETER
********
         SLDL  RPARM0,L1                .CHECK                  YS02019
         LTR   RPARM1,RPARM1            .FOR                    YS02019
         BNZ   PARMBAD                  .VALID                  YS02019
         SRDL  RPARM0,L1                .PARAMETER IN RPARM1    YS02019
         LR    RWORK0,RLINK             SAVE RETURN ADDRESS     YS02019
         TM    TSBFLG4,TSBHUNG          HAS USER HUNG UP        YS02019
         BO    RETURN                   YES,RETURN TO CALLER    YS02019
********
******** TEST FOR 1050 TERMINAL
********
         TM    TSBTERM2,TSBC1050        IS TERMINAL A 1050      YS02019
         BO    TRMOK                    YES, CONTINUE           YS02019
         LA    RCODE,RETCODE8           SET INVALID TERMNL      YS02019
*                                       RETURN CODE             YS02019
         B     EXIT                     RETURN TO CALLER        YS02019
TRMOK    EQU   *                                                YS02019
         MODESET EXTKEY=TCAM            GET TCAM'S KEY          YS02019
         LTR   RPARM1,RPARM1            SET OR RESET TIMEOUT    YS02019
*                                       SUPPRESS                YS02019
         BNZ   TIMEOUT                  SET TIMEOUT             YS02019
         LA    RPARM1,MASKSET           GET MASKS TO SET        YS02019
         B     TPOST                    GO SET QCBINHBN         YS02019
TIMEOUT  EQU   *                                                YS02019
         LA    RPARM1,MASKRSET          GET MASKS TO RESET      YS02019
TPOST    EQU   *                                                YS02019
         L     R15,TPADDR               GET ENTRY POINT         YS02019
         BALR  R14,R15                  GO TPOST TSB            YS02019
RETURN   EQU   *                                                YS02019
         SR    RCODE,RCODE              SET GOOD RETURN CODE    YS02019
EXIT     EQU   *                                                YS02019
         MODESET EXTKEY=SUPR            GET KEY FOR SETLOCKS    YS02019
         LR    RLINK,RWORK0             RESTORE RETURN ADDR     YS02019
         BR    RLINK                    RETURN                  YS02019
PARMBAD  EQU   *                                                YS02019
         LA    RCODE,RETCODE4           SET BAD PARM RETURN CODEYS02019
         BR    RLINK                    RETURN TO SUPERVISOR    YS02019
         DS    0A                       FORCE ALIGNMENT         YS02019
MASKSET  DC    AL1(TSBPOSTO,0,0,0)      SET TSBPOSTO            YS02019
         DC    F'-1'                    NO RESETTING IN WORD 1  YS02019
         DC    AL1(QCBINHBN,QCBINHBN,0,0)  SET QCBINHBN         YS02019
         DC    F'-1'                    NO RESETTING IN WORD 2  YS02019
MASKRSET DC    AL1(TSBPOSTO,0,0,0)      SET TSBPOSTO            YS02019
         DC    F'-1'                    NO RESETTING IN WORD 1  YS02019
         DC    AL1(QCBINHBN,0,0,0)      ALTER QCBINHBN          YS02019
         DC    AL1(255,255-QCBINHBN,255,255)  RESET QCBINHBN    YS02019
TPADDR   DC    V(POSTTSB)               ADDR OF TPOSTING SUBRTN YS02019
PATCH    DC    10F'0'                   PATCH AREA              YS02019
CONSTANT DSECT                          DEFINES CONSTANTS IN 1STYS02019
*                                       CSECT                   YS02019
ZERO     DC    F'0'                     CONSTANT OF 0           YS02019
NINES    DC    F'99999999'              CONSTANT OF 99,999,999  YS02019
FIFTEEN  DC    AL1(15)                  CONSTANT OF 15          YS02019
TWO      DC    XL1'02'                  CONSTANT OF 2           YS02019
TEN      DC    H'10'                    CONSTANT OF 10          YS02019
SIXTEEN  DC    AL1(16)                  CONSTANT OF 16          YS02019
ENABLE   DC    X'FF'                    TO ENABLE INTERRUPTS    YS02019
SIX      DC    H'6'                     CONSTANT OF 6           YS02019
EIGHT    DC    AL1(8)                   CONSTANT OF 8           YS02019
CP5041   DC    XL3'026D16'              BKSP/UNDERSCORE CHAR.   YS02019
*                                       PROMPT                  YS02019
HIGHBIT  DC    XL1'80'                  TO TEST HIGH ORDER BITS YS02019
CPTWX    DC    XL3'024B0D'              PERIOD/CAR.RETURN CHAR. YS02019
*                                       PROMPT                  YS02019
TPUT1    DC    XL4'0E000000'            TPUT,HOLD,CONTROL       YS02019
TPUT2    DC    XL4'0A000000'            TPUT,CONTROL            YS02019
ONE      DC    AL1(1)                   CONSTANT OF ONE         YS02019
XIDLE    DC    X'17'                    TPUT IDLE CHARACTER     YA00364
FOX      EQU   ENABLE                   CONSTANT OF 'FF'        YS02019
DISABLE  EQU   ZERO                     TO DISABLE INTERRUPTS   YS02019
         EJECT
CVT      DSECT
         CVT
         EJECT
         IHAPSA
         EJECT
         TQCBD
         EJECT
         IKJTSB
         END
