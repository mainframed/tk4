         TITLE 'IGG09404 - STBREAK ROUTINE'
IGG09404 CSECT
         COPY  LCGASMS2
*                                                                M3267
*                                                                 M3810
*0000433000,558000,670400,733800-735000,792000-999999            TS0015
*                                                                M4283
*                                                                 M5466
***********************************************************************
*                                                                     *
*    VS2/2 DELETIONS/CHANGES                                          *
*                                                               YS02019
*                                                               YM01114
*    RELEASE 21 DELETIONS/CHANGES                                     *
*0891488000-510000,744000                                        A222
*0891736000                                                      S21008
*                                                                     *
* STATUS                                                              *
*    CHANGE LEVEL 002                                                 *
*                                                                     *
* FUNCTION -                                                          *
*    THIS ROUTINE IS TO SERVE THE MACRO 'STBREAK'. IT IS PART OF SVC  *
*    94. IT IS TO SET THE STATUS BITS IN QCB TO REFLECT THE PRESENCE/ *
*    ABSENCE OF A BREAK FEATURE ON THE TERMINAL.  IT ONLY APPLIES TO  *
*    TERMINAL TYPES 1050 OR 2741 DIAL.  IF THE MACRO 'STBREAK' IS     *
*    ISSUED FOR TERMINALS OTHER THAN THE ABOVE MENTIONED TYPES, THE   *
*    FUNCTION IS IGNORED.                                             *
*                                                                     *
*    FOR 2741 OR 1050 DIAL TERMINAL TYPES, IF OPERAND IS-             *
*                                                                     *
*    YES (BIT 0 OF FLAG BYTE IN REG 1 IS ON) - SET QCBNOBRK OFF       *
*                                                                     *
*    NO (BIT 0 OF FLAG BYTE IN REG 1 IS OFF) - SET QCBNOBRK ON        *
*                                                                     *
* ENTRY POINTS -                                                      *
*         IGG09404 - CONTROL IS RECEIVED FROM MODULE IGC0009D         *
*                                                                     *
* INPUT -                                                             *
*    REGISTER 0 CONTAINS PARAMETER                                    *
*    REGISTER 1 CONTAINS PARAMETER                                    *
*    REGISTER 3 CONTAINS CVT ADDRESS                                  *
*    REGISTER 4 CONTAINS TCB ADDRESS                                  *
*    REGISTER 5 CONTAINS TSB ADDRESS                            YS02019
*                                                                     *
* OUTPUT -                                                            *
*    REGISTER 15 HAS RETURN CODE:                                     *
*           0 - SUCCESSFUL                                            *
*           4 - INVALID PARAMETER                                     *
*           8 - WRONG TERMINAL TYPE                                   *
*                                                                     *
* EXTERNAL REFERENCES -                                               *
*         TPUT - VIA SVC CALL                                         *
*         IEDQTNT - VIA BRANCH                                        *
*                                                                     *
* EXITS, NORMAL -                                                     *
*         BR 14 WITH 0 IN REGISTER 15                           YS02019
*                                                                     *
* EXITS, ERROR -                                                      *
*         BR 14 WITH 4 OR 8 IN REGISTER 15                      YS02019
*                                                                     *
* TABLES/WORK AREAS -                                                 *
*    CONTROL BLOCKS ARE DESCRIBED IN THE DSECTS AT END OF LISTING     *
*                                                                     *
* ATTRIBUTES -                                                        *
*    REENTRANT, REFRESHABLE, SUPERVISOR MODE                          *
*                                                                     *
* CHARACTER CODE DEPENDENCY -                                         *
*    THIS MODULE IS NOT CHARACTER DEPENDENT                           *
* NOTES -                                                             *
*    YS02019 - VS/2 RELEASE 2 CHANGES                                 *
*                                                                     *
***********************************************************************
         SPACE 3
********
******** REGISTER EQUATES
********
RWORK0   EQU   0                        WORK REGISTER
RPARM0   EQU   0                        PARAMETER REGISTER
RWORK1   EQU   1                        WORK REGISTER
RPARM1   EQU   1                        PARAMETER REGISTER
RWORK2   EQU   2                        WORK REGISTER
RWORK3   EQU   3                        WORK REGISTER
RCVT     EQU   3                        POINTS TO CVT
RTCB     EQU   4                        POINTS TO TCB
RWORK4   EQU   4                        WORK REGISTER           YS02019
RTSB     EQU   5                        POINTS TO TSB
RWORK6   EQU   6                        WORK REGISTER           YS02019
RWORK7   EQU   7                        WORK REGISTER           YS02019
RWORK8   EQU   8                        WORK REGISTER           YS02019
RWORK9   EQU   9                        WORK REGISTER           YS02019
RCONST   EQU   10                       POINTS TO PROGRAM CONSTANTS
RWORK11  EQU   11                       WORK REGISTER           YS02019
RBASE    EQU   12                       BASE REGISTER
RWORK13  EQU   13                       WORK REGISTER           YS02019
R13      EQU   13                       REGISTER 13
R14      EQU   14                       REGISTER 14
RLINK    EQU   14                       LINK REGISTER
RTRNSTBL EQU   14                       POINTS TO TRANSLATE TABLES
R15      EQU   15                       REGISTER 15
RBRANCH  EQU   15                       BRANCH REGISTER
RCODE    EQU   15                       RETURN CODE REGISTER
         SPACE 2
********
******** DISPLACEMENT,LENGTH AND MASK EQUATES
********
BYTE1    EQU   8                        MASK FOR BYTE 1 IN REG.
BYTE2    EQU   4                        MASK FOR BYTE 2 IN REG.
BYTE3    EQU   2                        MASK FOR BYTE 3 IN REG.
BYTE4    EQU   1                        MASK FOR BYTE 4 IN REG.
BYTE34   EQU   3                        MASK FOR BYTES 3&4 IN REG.
BYTE234  EQU   7                        MASK FOR BYTES 2&3&4 IN REG.
D0       EQU   0                        DISPLACEMENT OF 0
D1       EQU   1                        DISPLACEMENT OF 1
D2       EQU   2                        DISPLACEMENT OF 2
D3       EQU   3                        DISPLACEMENT OF 3
D4       EQU   4                        DISPLACEMENT OF 4
D5       EQU   5                        DISPLACEMENT OF 5
D6       EQU   6                        DISPLACEMENT OF 6
D14      EQU   14                       DISPLACEMENT OF 14
L1       EQU   1                        LENGTH OF 1
L2       EQU   2                        LENGTH OF 2
L3       EQU   3                        LENGTH OF 3
L4       EQU   4                        LENGTH OF 4
L8       EQU   8                        LENGTH OF 8
L16      EQU   16                       LENGTH OF 16
L20      EQU   20                       LENGTH OF 20
L24      EQU   24                       LENGTH OF 24
L32      EQU   32                       LENGTH OF 32
RETCODE4 EQU   4                        RETURN CODE OF 4
RETCODE8 EQU   8                        RETURN CODE OF 8
RETCODEC EQU   12                       RETURN CODE OF 12
HIPRI    EQU   X'E4'                    TCAM POSTING PRIORITY
COMPLETE EQU   X'40'                    TO TEST POST COMPLETE BIT
CZERO    EQU   0                        TO TEST FOR ZERO
CONE     EQU   1                        CONSTANT OF 1
CTWO     EQU   2                        CONSTANT OF 2
CTHREE   EQU   3                        CONSTANT OF 3
CFOUR    EQU   4                        CONSTANT OF 4           YS02019
CFIFTEEN EQU   15                       CONSTANT OF 15
CSIXTEEN EQU   16                       CONSTANT OF 16
CFOX     EQU   X'FF'                    TO TEST FOR 'FF'
BLANK    EQU   X'40'                    TO TEST FOR BLANK
OFF      EQU   X'FF'                    TO TURN BITS OFF
IDLE     EQU   X'17'                    IDLE CHARACTER
         SPACE 3
********
******** ESTABLISH BASE REGISTER AND ADDRESSABILITY
********
         USING *,RBASE                  REGISTER ADDRESSABILITY
         USING CVT,RCVT                 CVT ADDRESSABILITY
         USING TSB,RTSB                 TSB ADDRESSABILITY
         USING CONSTANT,RCONST          CONSTANT ADDRESSABILITY
         B     PASTID                   SKIP MODULE ID.         YS02019
         DC    C'IGG09404'              MODULE ID.              YS02019
         DC    X'3183'                  DATE - 07/02/73         YM01114
PASTID   EQU   *                                                YS02019
********
******** TEST FOR VALID PARAMETER
********
         SLDL  RPARM0,L1                .TEST
         LTR   RPARM1,RPARM1            .FOR
         BNZ   PARMBAD                  .VALID
         SRDL  RPARM0,L1                .PARAMETER
         SPACE 1
         MODESET EXTKEY=TCAM            GET TCAMS KEY           YS02019
         TM    TSBFLG4,TSBHUNG          HAS USER HUNG UP        YS02019
         BO    RETURN                   YES,RETURN
********
******** TEST FOR 1050 OR 2741 TERMINAL
********
         TM    TSBTERM1,TSBC5041+TSBC2741 VALID 2741 OR 1050    YS02019
         BNZ   TRMOK                    VALID TERMINAL
         TM    TSBTERM2,TSBC1050        IS IT A 1050 TERMINAL   YS02019
         BO    TRMOK                    YES, VALID TERMINAL
WRNGTYPE EQU   *
         MODESET EXTKEY=SUPR            CHANGE TO KEY 0         YS02019
         LA    RCODE,RETCODE8           INDICATE INVALID TERMINAL TYPE
         BR    R14                      RETURN TO CALLER        YS02019
TRMOK    EQU   *
         LTR   RPARM1,RPARM1            REQUEST TO ACTIVATE REVERSE BRK
         BNZ   BREAK                    BRANCH IF YES
NOBREAK  EQU   *
         LR    RWORK2,RLINK             SAVE RETURN REGISTER    YS02019
         LA    RPARM1,MASK1             MASK ADDR FOR TPOST     YS02019
         L     RBRANCH,TPOST            ADDR OF POSTIT ROUTINE  YS02019
         BALR  RLINK,RBRANCH            TURN ON BIT             YS02019
*                                       IN QCB TO PREVENT INPUT YS02019
         LR    RLINK,RWORK2             RESTORE RETURN ADDRESS  YS02019
         OI    TSBSTAT,TSBLWAIT         LOCK KEYBOARD
         OI    TSBFLG3,TSBSPIT+TSBNOBRK NO AUTOPROMPTING BY     YM01114
*                                       TSOUTPUT AND BREAK      YM01114
*                                       FEATURE NOT ALLOWED     YM01114
         USING TCB,RTCB                 TCB ADDRESSABILITY
         L     RTCB,TCBRBP              LOCATE BUFFER
         USING RBSECT,RTCB              ADDRESSABILITY
FREE1    EQU   *                        FREE LOCKS FOR TPUT     YS02019
         MODESET EXTKEY=SUPR            NEED SUPR KEY FOR SETLK YS02019
         MVI   RBEXSAVE,IDLE            PUT IDLE CHAR IN BUFFER
         LR    RWORK6,RLINK             SAVE RETURN ADDR        YS02019
         LR    RWORK7,RBASE             SAVE BASE               YS02019
         SETLOCK RELEASE,TYPE=CMS,      FREE CMS LOCK           YS02019X
               RELATED=(TSB,IGC0009D(GETLKS))                   YS02019
         SETLOCK RELEASE,TYPE=LOCAL,    FREE LOCAL LOCK         YS02019X
               RELATED=(TSB,IGC0009D(GETLKS))                   YS02019
         LA    RWORK0,L1                .ISSUE
         LA    RWORK1,RBEXSAVE          .TPUT
         O     RWORK1,TPUT1             .TO
         TPUT  (1),(0),R                .SYNCHRONIZE THE LINE
GET2     EQU   *                        GET LOCKS AGAIN         YS02019
         SETLOCK OBTAIN,TYPE=LOCAL,MODE=UNCOND,                 YS02019X
               RELATED=(TSB,'SVC EXIT PROLOGUE')                YS02019
         SETLOCK OBTAIN,TYPE=CMS,MODE=UNCOND,                   YS02019X
               RELATED=(TSB,'SVC EXIT PROLOGUE')                YS02019
         LR    RLINK,RWORK6             RESTORE RETURN ADDR     YS02019
         LR    RBASE,RWORK7             RESTORE BASE ADDR       YS02019
         MODESET EXTKEY=TCAM            GET TCAMS KEY TO UPDTE  YS02019
*                                       THE TSB                 YS02019
         L     RWORK6,CVTAQAVT          GET ADDR OF TCX         YS02019
         LTR   RWORK6,RWORK6            IS TCAM STILL ACTIVE    YS02019
         BZ    RETURN                   NO, RETURN TO CALLER    YS02019
         L     RWORK6,TCXRPT-IEDQTCX(,RWORK6) GET TIOCRPT ADDR  YS02019
         LTR   RWORK6,RWORK6            IS TIME SHARING ACTIVE  YS02019
         BZ    RETURN                   NO, RETURN TO CALLER    YS02019
         TM    TSBFLG4,TSBHUNG          HAS USER HUNG UP        YS02019
         BO    RETURN                   YES,RETURN
         LR    RWORK2,RLINK             SAVE RETURN ADDR        YS02019
         LA    RPARM1,MASK2             MASK PARM FOR TPOST     YS02019
         L     RBRANCH,TPOST            ADDR OF POSTIT ROUTINE  YS02019
         BALR  RLINK,RBRANCH            MAKE A NOBREAK          YS02019
*                                       TERMINAL & ALLOW INPUT  YS02019
         LR    RLINK,RWORK2             RESTORE RETURN ADDR     YS02019
         NI    TSBSTAT,OFF-TSBLWAIT     UNLOCK KEYBOARD
         NI    TSBFLG3,OFF-TSBSPIT      PERMIT AUTOPROMPTING
         B     RETURN                   RETURN TO CALLER
BREAK    EQU   *
         NI    TSBFLG3,OFF-TSBNOBRK     USE OF BREAK FEATURE OK YM01114
         LR    RWORK2,RLINK             SAVE RETURN ADDR        YS02019
         LA    RPARM1,MASK3             PASS MASK ADDR          YS02019
         L     RBRANCH,TPOST            ADDR OF POSTIT ROUTINE  YS02019
         BALR  RLINK,RBRANCH            GO TO POSTTSB TO TPOST  YS02019
*                                       TO MAKE IT A BREAK TERM
         LR    RLINK,RWORK2             RESTORE RETURN REGISTER YS02019
RETURN   EQU   *
         MODESET EXTKEY=SUPR            RETURN TO SUPR KEY      YS02019
         SR    RCODE,RCODE              SET GOOD RETURN CODE
         BR    R14                      GO BACK TO CALLER       YS02019
PARMBAD  EQU   *
         LA    RCODE,RETCODE4           BAD PARAMETER RETURN CODE
         BR    R14                      GO BACK TO CALLER       YS02019
         DS    0A                       FORCE ALIGNMENT         YS02019
MASK1    DC    AL1(TSBPOSTO,0,0,0)      .ALLOW                  YS02019
         DC    AL1(255,255,255,255)     .NO                     YS02019
         DC    AL1(0,0,QCBNOBUF,QCBNOBUF) .INPUT                YS02019
         DC    AL1(255,255,255,255)     .TO TERMINAL            YS02019
MASK2    DC    AL1(TSBPOSTO,0,QCBNOBRK,QCBNOBRK) .ALLOW         YS02019
         DC    AL1(255,255,255,255)     .INPUT                  YS02019
         DC    AL1(0,0,QCBNOBUF,0)      .AND                    YS02019
         DC    AL1(255,255,255,255-QCBNOBUF) .NOBREAK           YS02019
MASK3    DC    AL1(TSBPOSTO,0,QCBNOBRK,0) .MAKE                 YS02019
         DC    AL1(255,255,255,255-QCBNOBRK) .IT                YS02019
         DC    AL1(0,0,0,0)             .A BREAK                YS02019
         DC    AL1(255,255,255,255)     .TERMINAL               YS02019
ZAP      DC    9F'0'                    PATCH AREA              YS02019
         SPACE 1
CONSTANT DSECT
ZERO     DC    F'0'                     CONSTANT OF 0
NINES    DC    F'99999999'              CONSTANT OF 99,999,999
FIFTEEN  DC    AL1(15)                  CONSTANT OF 15
TWO      DC    XL1'02'                  CONSTANT OF 2
TEN      DC    H'10'                    CONSTANT OF 10
SIXTEEN  DC    AL1(16)                  CONSTANT OF 16
SIX      DC    H'6'                     CONSTANT OF 6
EIGHT    DC    AL1(8)                   CONSTANT OF 8
CP5041   DC    XL3'026D16'              BKSP/UNDERSCORE CHAR. PROMPT
HIGHBIT  DC    XL1'80'                  TO TEST HIGH ORDER BITS
CPTWX    DC    XL3'024B0D'              PERIOD/CAR.RETURN CHAR. PROMPT
TPUT1    DC    XL4'0E000000'            TPUT,HOLD,CONTROL
TPUT2    DC    XL4'0A000000'            TPUT,CONTROL
ONE      DC    AL1(1)                   CONSTANT OF ONE
XIDLE    DC    X'17'                    TPUT IDLE CHARACTER     YA00364
TPOST    DS    F                        ADDR OF POSTTSB ROUTINE YS02019
TPMASK   DS    4F                       MASK TO TPOST QCB       YS02019
         EJECT
CVT      DSECT
         CVT     LIST=YES
         EJECT
         IKJRB
         EJECT
         TQCBD
         EJECT
         IKJTCB
         EJECT
         TTCXD                                                  YS02019
         EJECT
         IKJTSB  LIST=YES
         EJECT
         IHAPSA
         END
