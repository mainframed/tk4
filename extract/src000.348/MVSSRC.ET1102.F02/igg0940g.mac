         TITLE 'IGG0940G - STCLEAR ROUTINE'
IGG0940G CSECT
         COPY  LCGASMS2
*0000717000-721000                                               M0590
*0000468000-488000                                               TS0023
*                                                                M2651
*                                                                M4283
*                                                                 M5113
*                                                                 M5467
*
***********************************************************************
*    RELEASE 23 DELETIONS/CHANGES                                     *
*    RELEASE 22 DELETIONS/CHANGES                                     *
*    RELEASE 21 DELETIONS/CHANGES                                     *
*                                                                     *
* STATUS -                                                            *
*    CHANGE LEVEL 001                                                 *
*                                                                     *
* FUNCTION -                                                          *
*    THIS ROUTINE IS TO SERVE THE MACRO 'STCLEAR'. THE CHARACTER      *
*    STRING (1 TO 4 CHARACTERS) WHICH IS TO BE USED TO ERASE A        *
*    DISPLAY STATION SCREEN WILL BE STORED IN 'TSBERSDS'. ZEROS IN    *
*    THIS FIELD INDICATE THAT NO CHARACTER STRING WILL BE USED TO     *
*    ERASE THE SCREEN.                                                *
*                                                                     *
* ENTRY POINTS -                                                      *
*         IGG0940G - CONTROL IS RECEIVED FROM MODULE IGC0009D         *
*                    VIA BRANCH ACCORDING TO ENTRY CODE 16, HOLDING   *
*                    THE LOCAL AND THE CMS LOCKS, IN SUPERVISOR KEY   *
*                                                                     *
* INPUT -                                                             *
*    REGISTER 0 CONTAINS PARAMETER                                    *
*    REGISTER 1 CONTAINS PARAMETER                                    *
*    REGISTER 3 CONTAINS CVT ADDRESS                                  *
*    REGISTER 4 CONTAINS TCB ADDRESS                                  *
*    REGISTER 5 CONTAINS TSB ADDRESS                                  *
*    REGISTER 10 CONTAINS THE ADDRESS OF AN ARRAY OF CONSTANTS        *
*    REGISTER 12 CONTAINS THE BASE ADDRESS                            *
*    REGISTER 15 CONTAINS THE RETURN ADDRESS                          *
*                                                                     *
* OUTPUT -                                                            *
*    REGISTER 15 HAS RETURN CODE:                                     *
*           0 - SUCCESSFUL                                            *
*           4 - INVALID PARAMETER                                     *
*           8 - INVALID TERMINAL TYPE                                 *
*                                                                     *
* EXTERNAL REFERENCES -                                               *
*         NONE                                                        *
*                                                                     *
* EXITS, NORMAL -                                                     *
*         BR 14 WITH 0 IN REGISTER 15                                 *
*                                                                     *
* EXITS, ERROR -                                                      *
*         BR 14 WITH 4 OR 8 IN REGISTER 15                            *
*                                                                     *
* TABLES/WORK AREAS -                                                 *
*    CONTROL BLOCKS ARE DESCRIBED IN THE DSECTS AT END OF LISTING     *
*                                                                     *
* ATTRIBUTES -                                                        *
*    REENTRANT, REFRESHABLE, SUPERVISOR MODE, ENABLED                 *
*                                                                     *
* CHARACTER CODE DEPENDENCY -                                         *
*    THIS MODULE IS ASSEMBLED IN EBCDIC AND MUST BE REASSEMBLED IF    *
*    A DIFFERENT CHARACTER SET IS TO BE USED DURING EXECUTION         *
* NOTES -                                                             *
*         VS01537 - REFERENCED USER CORE IN TCAM'S KEY - 0C4          *
*                                                                     *
***********************************************************************
         SPACE 3
********
******** REGISTER EQUATES
RPARM0   EQU   0                        PARAMETER REG 0
RWORK0   EQU   0                        WORK REGISTER
RPARM1   EQU   1                        PARAMETER REG 1
RWORK1   EQU   1                        WORK REGISTER
RWORK2   EQU   2                        WORK REGISTER
RWORK3   EQU   3                        WORK REGISTER
RCVT     EQU   3                        POINTS TO CVT
RTCB     EQU   4                        POINTS TO TCB
RUCB     EQU   4                        POINTS TO UCB
RTSB     EQU   5                        POINTS TO TSB           YS02019
RCONST   EQU   10                       POINTS TO PROGRAM       YS02019
*                                       CONSTANTS               YS02019
RBASE    EQU   12                       BASE REGISTER           YS02019
R13      EQU   13                       REGISTER 13
R14      EQU   14                       REGISTER 14
RLINK    EQU   14                       LINK REGISTER
R15      EQU   15                       REGISTER 15
RBRANCH  EQU   15                       BRANCH REGISTER
RCODE    EQU   15                       RETURN CODE REGISTER
         SPACE 2
********
******** DISPLACEMENT,LENGTH AND MASK EQUATES
********
BYTE1    EQU   8                        MASK FOR BYTE 1 IN REG.
BYTE2    EQU   4                        MASK FOR BYTE 2 IN REG.
BYTE3    EQU   2                        MASK FOR BYTE 3 IN REG.
BYTE4    EQU   1                        MASK FOR BYTE 4 IN REG.
BYTE34   EQU   3                        MASK FOR BYTES 3&4 IN REG.
BYTE234  EQU   7                        MASK FOR BYTES 2&3&4 IN REG.
D0       EQU   0                        DISPLACEMENT OF 0
D1       EQU   1                        DISPLACEMENT OF 1
D2       EQU   2                        DISPLACEMENT OF 2
D3       EQU   3                        DISPLACEMENT OF 3
D4       EQU   4                        DISPLACEMENT OF 4
D5       EQU   5                        DISPLACEMENT OF 5
D6       EQU   6                        DISPLACEMENT OF 6
D14      EQU   14                       DISPLACEMENT OF 14
L1       EQU   1                        LENGTH OF 1
L2       EQU   2                        LENGTH OF 2
L3       EQU   3                        LENGTH OF 3
L4       EQU   4                        LENGTH OF 4
L8       EQU   8                        LENGTH OF 8
L16      EQU   16                       LENGTH OF 16
L20      EQU   20                       LENGTH OF 20
L24      EQU   24                       LENGTH OF 24
L31      EQU   31                       LENGTH OF 31
L32      EQU   32                       LENGTH OF 32
RETCODE4 EQU   4                        RETURN CODE OF 4
RETCODE8 EQU   8                        RETURN CODE OF 8
RETCODEC EQU   12                       RETURN CODE OF 12
HIPRI    EQU   X'E4'                    TCAM POSTING PRIORITY
COMPLETE EQU   X'40'                    TO TEST POST COMPLETE BIT
CZERO    EQU   0                        TO TEST FOR ZERO
CONE     EQU   1                        CONSTANT OF 1
CTWO     EQU   2                        CONSTANT OF 2
CTHREE   EQU   3                        CONSTANT OF 3
CFIFTEEN EQU   15                       CONSTANT OF 15
CSIXTEEN EQU   16                       CONSTANT OF 16
CFOX     EQU   X'FF'                    TO TEST FOR 'FF'
BLANK    EQU   X'40'                    TO TEST FOR BLANK
OFF      EQU   X'FF'                    TO TURN BITS OFF
IDLE     EQU   X'17'                    TERM. CTL. CHAR. - IDLE
NL       EQU   X'15'                    TERM. CTL. CHAR. - NEW LINE
CR       EQU   X'0D'                    TERM. CTL. CHAR. - CARRIAGE RET
LF       EQU   X'25'                    TERM. CTL. CHAR. - LINE FEED
HEX18    EQU   X'18'                    TERM. CTL. CHAR. - TWX CHAR.
HEX20    EQU   X'20'                    TO TEST BIT 2 OF BYTE
LNSZ40   EQU   40                       LINE SIZE 40
LNSZ64   EQU   64                       LINE SIZE 64
LNSZ80   EQU   80                       LINE SIZE 80
LNNO6    EQU   6                        LINES PER SCREEN - 6
LNNO12   EQU   12                       LINES PER SCREEN - 12
LNNO15   EQU   15                       LINES PER SCREEN - 15
QINEC    EQU   21                       QTIP ENTRY CODE/TCLEARQ/INPUT
QOUTEC   EQU   22                       QTIP ENTRY CODE/TCLEARQ/OUTPUT
         SPACE 3
********
******** ESTABLISH BASE REGISTER AND ADDRESSABILITY
********
         USING *,RBASE                  BASE REGISTER ADDRESSABILITY
         USING TSB,RTSB                 TSB ADDRESSABILITY
         USING CONSTANT,RCONST          CONSTANT ADDRESSABILITY
         B     DISPLAY                  BRANCH AROUND MODULE ID YS02019
         DC    CL8'IGG0940G'            MODULE ID               YS02019
         DC    X'3124'                  DATE OF LAST UPDATE 5/04/73
********
******** PERFORM MAIN FUNCTION OF ROUTINE
********
DISPLAY  EQU   *
         TM    TSBSTAT,TSBDSPLY         IS THIS A DISPLAY TERMINAL
         BZ    BADTERM                  NO,INVALID TERMINAL TYPE
         LTR   RPARM1,RPARM1            STRING PARM SPECIFIED
         BNZ   CKSTRING                 YES,CHECK THE STRING
         XC    TSBERSDS,TSBERSDS        NO,CLEAR SCREEN ERASE FIELD
         B     RETURN                   RETURN TO CALLER
********
******** MOVE CHARACTER STRING TO TSB
********
CKSTRING EQU   *
         L     RPARM1,D0(,RPARM1)       GET STRING FOR STORE    YM01537
         MODESET EXTKEY=TCAM            GET TCAM'S KEY          YM01537
         ST    RPARM1,TSBERSDS          STORE STRING            YM01537
         LA    RPARM1,TSBERSDS          RESET PARM POINTER
********
******** LOCATE NECESSARY CONTROL BLOCKS
********
MAIN     EQU   *
         LA    RWORK3,TSBERSDS+D3       POINT TO LAST CHAR. IN STRING
         LA    RWORK2,L1                SET INDEX INCREMENT
COMPARE  EQU   *
         CLI   D0(RPARM1),BLANK         IS CHAR. A BLANK
         BE    RETURN                   YES,GO RETURN           YS02019
         CLI   D0(RPARM1),CR            IS IT A CARRIAGE RET. CHAR.
         BE    INVALID                  YES,INVALID CHARACTER
         CLI   D0(RPARM1),NL            IS IT A NEW LINE CHAR.
         BE    INVALID                  YES,INVALID CHARACTER
         CLI   D0(RPARM1),LF            IS IT A LINE FEED CHAR.
         BE    INVALID                  YES,INVALID CHARACTER
         BXLE  RPARM1,RWORK2,COMPARE    GO CHECK NEXT CHAR. IN STRING
********
******** SET RETURN CODE AND EXIT
********
RETURN   EQU   *
         MODESET    EXTKEY=SUPR         CHANGE TO KEY 0         YS02019
         SR    RCODE,RCODE              SET GOOD RETURN CODE
         BR    RLINK                    RETURN TO CALLER        YS02019
INVALID  EQU   *
         MODESET    EXTKEY=SUPR         CHANGE TO KEY 0         YS02019
         LA    RCODE,RETCODE4           SET RETURN CODE 4
         BR    RLINK                    RETURN TO CALLER        YS02019
BADTERM  EQU   *
         LA    RCODE,RETCODE8           SET RETURN CODE 8
         BR    RLINK                    RETURN TO CALLER        YS02019
         DC    9F'0'                    MAINTENANCE PATCH AREA  YS02019
         SPACE 3
CONSTANT DSECT                          DEFINES CONSTANTS IN 1ST CSECT
ZERO     DC    F'0'                     CONSTANT OF 0
NINES    DC    F'99999999'              CONSTANT OF 99,999,999
FIFTEEN  DC    AL1(15)                  CONSTANT OF 15
TWO      DC    XL1'02'                  CONSTANT OF 2
TEN      DC    H'10'                    CONSTANT OF 10
SIXTEEN  DC    AL1(16)                  CONSTANT OF 16
ENABLE   DC    X'FF'                    TO ENABLE INTERRUPTS
SIX      DC    H'6'                     CONSTANT OF 6
EIGHT    DC    AL1(8)                   CONSTANT OF 8
CP5041   DC    XL3'026D16'              BKSP/UNDERSCORE CHAR. PROMPT
HIGHBIT  DC    XL1'80'                  TO TEST HIGH ORDER BITS
CPTWX    DC    XL3'024B0D'              PERIOD/CAR.RETURN CHAR. PROMPT
TPUT1    DC    XL4'0E000000'            TPUT,HOLD,CONTROL
TPUT2    DC    XL4'0A000000'            TPUT,CONTROL
ONE      DC    AL1(1)                   CONSTANT OF ONE
XIDLE    DC    X'17'                    TPUT IDLE CHARACTER     YA00364
FOX      EQU   ENABLE                   CONSTANT OF 'FF'
DISABLE  EQU   ZERO                     TO DISABLE INTERRUPTS
         EJECT
         IKJTSB
         END
