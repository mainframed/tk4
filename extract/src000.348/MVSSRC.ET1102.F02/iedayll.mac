         TITLE 'IEDAYLL - TIOC LOGON QTIP'
IEDAYLL  CSECT
         ENTRY QTIP0100
         DC    C'IEDAYLL '              MODULE ID
         DC    X'7346'             JULIAN DATE-12/12/77        @Z37XBYT
         DS    0H
         SPACE 3
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                     *
*                                                                     *
*    NAME - IEDAYLL, TIOC LOGON'S SECTION OF QTIP SVC                 *
*                                                               ZA25074
*A270600,428360,429600,632000,812900                            ZA25074
*C254000,332500,428340,429700,438600,440000,550500,576000       ZA25074
*C588000,594500                                                 ZA25074
*D428380-428397                                                 ZA25074
*A546000                                                       @Z37XBYT
*                                                                     *
*    VS/2-2 ACTIVITY                                                  *
*                                       02/19/73                VS00022
*                                       07/02/73                YM01114
*    VS2-3.0 ACTIVITY                                                 *
*C006800,194500                         08/22/74                ZA00966
*                                                                     *
*              VS2 - 037   ACTIVITY                            @ZA12511
*                                                              @ZA12511
*                                                              @ZA12511
*  A 272XXX                                                     ZA12511
*                                                              @ZA12511
*    VS2-MVS SU ACTIVITY                                              *
* A 132500,134700,166500,260500-261500   07/15/76       G32TKKM ZA16608
* A 632221-632234,779640,779660,809100   07/15/76       G32TKKM ZA16608
* A 809200                               07/15/76       G32TKKM ZA16608
*                                                                     *
*    RELEASE 21 DELETIONS/CHANGES                                     *
*1374006800,439200,441000,443000,450000,478000,504000,530000,    S22028
*1374536000,546000,550000                                        S22028
*    RELEASE 20 DELETIONS/CHANGES                                     *
*                                                                TS1623
*                                                                 M2668
*                                                                 M2725
*                                                                 M4540
*0000006800,290000,330000                                        M5446
**************************************************************  ZA25074
* MODULE NAME = IEDAYII  (QTIP,TSO)                             ZA25074
*                                                               ZA25074
* DESCRIPTIVE NAME = TIOC LOGON ROUTINE IN QTIP SVC             ZA25074
*                                                               ZA25074
* STATUS = TCAM VERSION 10.0                                    ZA25074
*                                                                     *
* FUNCTION -                                                          *
*    PREPARE TSO CONTROL BLOCKS FOR A NEW USER                        *
*                                                                     *
* ENTRY POINTS -                                                      *
*         IEDAYLL, TO PERFORM LOGON PROCESSING THAT REQUIRES THE      *
*         LOCAL AND CMS LOCKS.                                        *
*                                                                     *
* INPUT -                                                             *
*    REGISTER  4 HAS LCB ADDRESS                                      *
*    REGISTER  6 HAS TCAM BUFFER ADDRESS                              *
*    REGISTER  7 HAS QCB ADDRESS                                      *
*    REGISTER  8 HAS TERMINAL ENTRY ADDRESS                           *
*    REGISTER 10 HAS TERMINAL CHARACTERISTICS TABLE ADDRESS           *
*    REGISTER 12 HAS BASE ADDRESS                                     *
*    REGISTER 14 HAS RETURN ADDRESS                                   *
*                                                                     *
* OUTPUT -                                                            *
*    REGISTER 15 CONTAINS RETURN CODE                                 *
*       0 - A TSB-ASCB PAIR HAS BEEN INITIALIZED                      *
*       4 - NO TSB FOUND, OR TS STOPPED - USER 'SICED'                *
*                                                                     *
* EXTERNAL REFERENCES -                                               *
*         POST, VIA BRANCH ENTRY AT CVT0PT01                          *
*         SYSTEM-INITIATED CANCEL VIA BRANCH                          *
*                                                                     *
* EXITS, NORMAL - BR 14, RETURN TO IEDAYL VIA IDAYQT                  *
*                                                                     *
* EXITS, ERROR  - NONE                                                *
*                                                                     *
* TABLES/WORK AREAS -                                                 *
*    CONTROL BLOCKS ARE DESCRIBED IN THE DSECTS AT END OF LISTING     *
*                                                                     *
* ATTRIBUTES -                                                        *
*    REENTRANT, REFRESHABLE, SUPERVISOR STATE, HOLDS TCAM'S KEY       *
*    AND KEY 0                                                        *
*                                                                     *
* CHARACTER CODE DEPENDENCY -                                         *
*    THE OPERATION OF THIS MODULE IS DEPENDENT  UPON CERTAIN INTERNAL *
*    REPRESENTATIONS OF ITS CHARACTER SET...IT IS ASSEMBLED IN EBCDIC *
*    AND MUST BE SPECIALLY MODIFIED IF RE-ASSEMBLY IN ANOTHER CHARAC- *
*    TER SET IS DESIRED.....THE FIELDS REQUIRE THIS TREATMENT BECAUSE *
*    THEY ARE REPRESENTED BY HEX VALUES AND ARE OTHERWISE UNPRINTABLE *
*    THESE FIELDS ARE - TWXLD, TWXCD, AND T4150CD                     *
*                                                                     *
* NOTES -                                                             *
*    REGISTER EQUATES START WITH 'R'                                  *
*    LENGTH AND DISPLACEMENT EQUATES START WITH 'E'                   *
*                                                                     *
*                                                                     *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         EJECT
********
******** REGISTERS
********
R0       EQU   0                        REGISTER 0              YS02019
RTERM    EQU   1                        TERMINAL ENTRY ADDR
RLNTH    EQU   1                   LENGTH OF TERM ID            ZA16608
RTCX     EQU   2                        TCX ADDRESS             YS02019
RAVT     EQU   2                   TCAM AVT ADDRESS             ZA16608
RASCB    EQU   3                        ASCB ADDRESS            YS02019
RLCB     EQU   4                        LCB ADDRESS
RUCB     EQU   5                        UCB ADDRESS
RBUF     EQU   6                        BUFFER ADDRESS
RWORKA   EQU   6                        WORK REGISTER
RQCB     EQU   7                        QCB ADDRESS
RWORKB   EQU   7                        WORK REGISTER
RRPT     EQU   8                        TIOCRPT ADDR AND TERMINAL ENTRY
*                                       ADDR AT ENTRY
RTSB     EQU   9                        TSB ADDRESS
RCH      EQU   10                       WORK REG & TCT ADDR AT ENTRY
RIS      EQU   11                       WORK REGISTER
RBASE    EQU   12                       BASE ADDRESS
R13      EQU   13                       TJID FOR POST ROUTINE
R14      EQU   14                       RETURN ADDRESS
R15      EQU   15                       POST ADDR & PASS TJID TO IEDAYL
RTCT     EQU   15                       TCT ADDRESS
RTNT     EQU   15                  TNT ENTRY ADDRESS            ZA16608
         SPACE 3
********
******** MASKS
********
E0       EQU   0                        GIVE OFFSET OF 0         Y06327
E1       EQU   1                        GIVES A VALUE OF 1
E2       EQU   2                        GIVES A VALUE OF 2
E3       EQU   3                        GIVES A VALUE OF 3
E4       EQU   4                        GIVES A VALUE OF 4
E16      EQU   16                       GIVES VALUE OF 16       ZA25074
TIMES4   EQU   2                        SHIFT VALUE FOR TIMES 4 YS02019
E100     EQU   100                      GIVES A VALUE OF 100
DEBEXSCL EQU   2                        COMM DEVICE DEB EXTENT SCALE
DEBSIZE  EQU   32                       SIZE OF BASIC SECTION OF DEB
NOFLAGS  EQU   X'0F'                    ZEROS UCB CHANNEL FLAGS ZA00966
MODEL    EQU   16                       OFFSET TO UCB MODEL FIELD
UNITTYPE EQU   19                       OFFSET TO UCB UNIT TYPE FIELD
TELE     EQU   X'50'                    TELEGRAPH ADAPTER (UCB)
C1050    EQU   X'80'                    1050 TERMINAL (TCT)      Y06327
TERMADPT EQU   X'10'                    ADAPTER FOR 1050 TERMINAL (UCB)
NON1050A EQU   X'E0'                    ADAPTER FOR NON-1050 TERM (UCB)
TWX      EQU   X'20'                    TWX TERMINAL (TCT)
C2260    EQU   X'02'                    2260 TERMINAL (TCT)
C3270    EQU   X'04'                    3270 TERMINAL (TCT)      S22028
C2741    EQU   X'08'                    2741 TERMINAL (TCT)
C5041    EQU   X'10'                    1050/2741 LINE (TCT)
TELELNSZ EQU   72                       LINE SIZE OF TELEGRAPH TERMINAL
REGLNSZ  EQU   120                      LINE SIZE OF NON-TELEGRAPH TERM
TWXLD    EQU   X'18'                    LD CHAR FOR TWX
TWXCD    EQU   X'6D'                    CD CHAR FOR TWX
T4150CD  EQU   X'16'                    CD CHAR FOR 2741 AND 1050
NODEFALT EQU   X'00'                    DENOTES NO LD/CD DEFAULT CHARS
TMMASK   EQU   X'80'                    TESTS TRMDEVFL BITS
NOMASK   EQU   X'00'                    NULL MASK FOR EX TM INSTR
BITPOS7  EQU   7                        POS OF SCRSIZE BIT IN TRMDEVFL
NOT      EQU   X'FF'                    TURNS OFF VARIOUS BITS
         SPACE 3
* * * * * * * * * * * * *  SET UP ADDRESSES * * * * * * * * * * * * * *
         SPACE 3
********
******** ESTABLISH ADDRESSABILITY
********
QTIP0100 EQU   *
         USING *,RBASE
         LR    RTERM,RRPT               TERM ENT ADDR PASSED IN RRPT
         USING IEDNTRM,RTERM                                    ZA25074
         L     RTCX,CVTPTR              GET PTR TO CVT          YS02019
         L     RTCX,CVTAQAVT-CVT(,RTCX) GET PTR TO TCX          YS02019
         USING IEDQTCX,RTCX                                     YS02019
         L     RRPT,TCXRPT              RPT PTR FROM TCX        YS02019
         USING TIOCRPT,RRPT
         L     RAVT,TCXAVT         GET ADDRESS OF AVT           ZA16608
         DROP  RTCX                TCX NO LONGER NEEDED         ZA16608
         USING IEDQAVTD,RAVT       AVT IS ADDRESSABLE           ZA16608
         USING IEDQLCB,RLCB             LCB ADDR PASSED BY IEDAYL
         USING IEDQPRF,RBUF             TCAM BFR ADDR PASSED BY IEDAYL
         USING IEDQQCB,RQCB             QCB ADDR PASSED BY AYL  YS02019
         LR    RTCT,RCH                 SAVE TCT ADDR PASSED BY IEDAYL
         SPACE 3                                                YS02019
********
******** FIND AN UNUSED TSB, AND USE IT.
********
         L     RTSB,TIOCTSB-E1          INITIAL POINTER         YS02019
         LA    RCH,TRMPRFSZ             GET SIZE OF TTE INDEX   ZA25074
         SR    RTERM,RCH                BACK UP TO TTE PREFIX   ZA25074
         SR    RCH,RCH                  FOR INSERTION           YS02019
         IC    RCH,TIOCTSBS             GET TSB SIZE            YS02019
         LR    R0,RCH                   SAVE SIZE               YS02019
         LH    RIS,TIOCNTSB             GET NO. OF TSB'S        YS02019
         BCTR  RIS,0                    SUBTRACT 1 FOR LAST TSB YS02019
         MR    RCH,RCH                  MULT BY SIZE, TO GET    YS02019
*                                       OFFSET OF LAST TSB      YS02019
         LR    RCH,R0                   RESTORE SIZE            YS02019
         AR    RIS,RTSB                 GET PTR TO LAST TSB     YS02019
* RCH= TSB SIZE, RIS= LAST TSB, RTSB= FIRST TSB AND CURRENT TSB YS02019
CHKTHIS  EQU   *                                                YS02019
         USING TSB,RTSB                                         YS02019
         L     RASCB,TSBASCB-E1         GET POINTER FOR TEST    YS02019
         LTR   RASCB,RASCB              CHECK FOR 0, WHICH      YS02019
*                                       MEANS TSB UNUSED        YS02019
         BNZ   NEXT                     IN USE,TRY AGAIN        YS02019
         TM    TSBTPFLG,TSBPOSTO        IS TPOST OUTSTANDING    YM03998
         BZ    EUREKA                   NO,ALLOCATE TSB         YS02019
NEXT     EQU   *                                                YS02019
         BXLE  RTSB,RCH,CHKTHIS         ADD SIZE TO CURRENT PTR YS02019
*                                       CHECK FOR END OF TABLE, YS02019
*                                       BRANCH IF NOT END       YS02019
         L     RCH,CVTPTR               GET CVT ADDRESS         YM03998
         USING CVT,RCH                  CVT ADDRESSABLE         YM03998
         L     RCH,CVTASVT              GET ASVT ADDRESS        YM03998
         DROP  RCH                      ADDRESSABILITY ENDS     YS03998
         USING ASVT,RCH                 ASVT ADDRESSABLE        YM03998
         LH    RIS,QCBTJID              GET ASID                YM03998
         SLL   RIS,E2                   GET OFFSET IN ASVT      YM03998
         L     RASCB,ASVTENTY-L'ASVTENTY(RIS)  GET ASCB ADDRESS YM03998
         MODESET EXTKEY=SUPR                                    ZA12511
         B     CALLSIC                  GO CANCEL IF NO TSB     YS02019
         DROP  RCH                      ASVT NOT ADDRESSABLE    YM03998
         SPACE 3                                                YS02019
********                                                        YS02019
******** AT THIS POINT, RTSB HAS THE TSB ADDRESS                YS02019
********                                                        YS02019
EUREKA   EQU   *                                                YS02019
         SPACE 3
********
******** GET UCB ADDRESS TO INITIALIZE TSB
********
         TM    TRMSTATE,TRMPREF         NCP ATTACHED TERM       ZA25074
         BO    NOUCB                    YES,DON'T WANT UCB       Y06327
         L     RCH,LCBDCBPT             GET DCB ADDR
         L     RIS,DCBDEBAD-IHADCB(0,RCH)    GET DEB ADDR
         SR    RCH,RCH                  ZERO REG
         IC    RCH,LCBUCBX              GET UCB INDEX
         SLL   RCH,DEBEXSCL             GET PROPER DEB EXTENT OFFSET
         LA    RCH,DEBSIZE(RCH,RIS)     POINT TO END OF BASIC DEB AND
*                                       ADD DEB EXTENT OFFSET VALUE
         L     RUCB,0(0,RCH)            GET UCB ADDR
         USING UCB,RUCB                 UCB ADDRESSABILITY       Y06327
NOUCB    EQU   *                                                 Y06327
         SPACE 3                                                YS02019
* * * * * * * * * * * * * * *  FIND ASCB * * * * * * * * * * * * * * *
         SPACE 3                                                YS02019
         LH    RASCB,QCBTJID            GET ASID                YS02019
         L     RIS,CVTPTR               ADDRESSABILITY ON CVT   YS02019
         USING CVT,RIS                                          YS02019
         SLA   RASCB,TIMES4             GET OFFSET OF ENTRY     YS02019
         L     RIS,CVTASVT              GET ADDRESS OF ASVT     YS02019
         DROP  RIS                                              YS02019
         USING ASVT,RIS                                         YS02019
         L     RASCB,ASVTENTY-L'ASVTENTY(RASCB) ASCB ADDR       YS02019
         USING ASCB,RASCB                                       YS02019
         DROP  RIS                                              YS02019
         SPACE 3                                                YS02019
* * * * * * * * * * * * *  TSB INITIALIZATION * * * * * * * * * * * * *
         SPACE 3
********
******** REFRESH TSB TO AN UNUSED STATE
********
         XC    TSB(TSBEND-TSB),TSB      ZERO WHOLE TSB
         SPACE 3                                                YS02019
********                                                        YS02019
******** PUT ASCB ADDRESS IN TSB, TSB ADDRESS IN ASCB           YS02019
********                                                        YS02019
         MODESET EXTKEY=SUPR            GET KEY OF ASCB         YS02019
         ST    RTSB,ASCBTSB             STORE TSB ADDR          YS02019
         MODESET EXTKEY=TCAM            GET TCAM'S KEY          YS02019
         ST    RASCB,TSBASCBA           STORE ASCB ADDR         YS02019
         SPACE 3
********                                                        YS02019
******** UPDATE TIOC USER COUNT                                 YS02019
********                                                        YS02019
         L     RCH,TIOCUSCT-E2          GET ENTIRE WORD         YS02019
UPCOUNT  EQU   *                                                YS02019
         LR    RIS,RCH                  COPY USER COUNT         YS02019
         LA    RIS,E1(,RIS)             ADD 1 (WIPES OUT FLG)   YS02019
         CS    RCH,RIS,TIOCUSCT-E2      STORE IF NOT CHANGED    YS02019
         BNE   UPCOUNT                  RECOMPUTE IF CHANGED    YS02019
         STCM  RCH,B'1000',TIOCFLG      RESTORE TIOCFLG         YS02019
         SPACE 3
********
******** TURN ON TSBINUSE
********
         OI    TSBSTAT,TSBINUSE         DENOTE TSB IS BEING USED
         LR    R0,RQCB                  SAVE QCB POINTER        YM01114
         SPACE 2
**************************************************************  ZA25074
*        FOR TERMINALS ATTACHED VIA A NCP THE TCAM NETWORK      ZA25074
*        ADDRESS CONVERT ROUTINE IS USED TO CONVERT THE         ZA25074
*        TERMINAL NAME TABLE (TNT) INDEX TO A NETWORK ADDRESS   ZA25074
*        FOR SAVING IN THE TSB LINE ADDRESS FIELD.              ZA25074
**************************************************************  ZA25074
         TM    TRMSTATE,TRMPREF         NCP ATTACHED TERM       ZA25074
         BZ    NORSID                   NO,DON'T USE RESRCE  ID  Y06327
         STM   R14,RTERM,E4(RTSB)       SAVE REGS USED BY       ZA25074
*                                       NETWORK ADDRESS         ZA25074
*                                       CONVERT ROUTINE         ZA25074
         LH    RTERM,LCBTTCIN           GET TNT INDEX           ZA25074
         L     R15,CVTPTR               GET CVT                 ZA25074
         USING CVT,R15                                          ZA25074
         L     R15,CVTAQAVT             GET TCX                 ZA25074
         DROP  R15                                              ZA25074
         USING IEDQTCX,R15                                      ZA25074
         L     R15,TCXAVT               GET AVT                 ZA25074
         DROP  R15                                              ZA25074
         USING IEDQAVTD,R15                                     ZA25074
         L     R15,AVTSAVTP             GET SAVT                ZA25074
         DROP  R15                                              ZA25074
         USING IEDNSVTD,R15             SAVT ADDRESSABILITY     ZA25074
         L     R15,SAVTTNTX             GET ADDRESS OF THE      ZA25074
*                                       NETWORK ADDRESS         ZA25074
*                                       CONVERT ROUTINE         ZA25074
         BALR  R14,R15                  GET NETWORK ADDRESS     ZA25074
         DROP  R15                                              ZA25074
         STH   R15,TSBLINE              PUT NETWORK ADDRESS IN  ZA25074
*                                       TSB                     ZA25074
         LM    R14,RTERM,E4(RTSB)       RESTORE SAVED REGS      ZA25074
         XC    E4(E16,RTSB),E4(RTSB)    REINIT TSB              ZA25074
         B     TEST3270                 CHECK FOR 3270           Y06327
NORSID   EQU   *                                                 Y06327
         MVC   TSBLINE,UCBCHAN          SAVE CHAN & UNIT ADDR   YS02019
         NI    TSBLINE,NOFLAGS          REMOVE CHAN FLGS IF ANY  Y06327
         SPACE 3
********
******** IF 3270, TURN ON TSB3270
********
TEST3270 EQU   *                                                 Y06327
         USING IEDDCT,RTCT              DCT ADDRESSABILITY      ZA25074
         TM    DCTBYTE1,DCT3270         3270 TERMINAL           ZA25074
         BZ    CHK2260                  NO - CHK FOR 2260        S22028
SET3270  EQU   *                                                 S22028
         OI    TSBSTAT,TSB3270          DENOTE A 3270 TERMINAL   S22028
         B     SETDSPLY                 GO FINISH 3270 INIT      S22028
         SPACE 3
********
******** IF 2260, TURN ON TSBDSPLY
********
CHK2260  EQU   *                                                 S22028
         TM    DCTBYTE3,DCTNOIDL        2260R TERMINAL          ZA25074
         BO    SETDSPLY                 YES - INIT TSB           S22028
*                                       ACCORDINGLY              S22028
         TM    DCTBYTE2,DCTLOCAL        2260 LOCAL TERMINAL     ZA25074
         BZ    NOTDSPLY                 NO - CHK FOR TELE TERM   S22028
SETDSPLY EQU   *                                                 S22028
         OI    TSBSTAT,TSBDSPLY         DENOTE A DISPLAY STATION
         SPACE 3
********
******** FOR DISPLAY TERMINAL, COUNT THE NUMBER OF TRMDEVFL BITS THAT
******** ARE ON
********
         LA    RIS,BITPOS7              GET NUMBER OF BITS TO CHECK
         LR    RCH,RIS                  GET NUMBER OF BITS TO CHECK
         LA    RWORKB,TMMASK            GET MASK FOR TM EX INSTR
         DROP  RQCB                     RWORKB SAME AS RQCB     YS02019
CHKBITS  EQU   *
         EX    RWORKB,TESTBITS          IS THIS TRMDEVFL BIT ON
         BO    SHIFTMSK                 YES - DON'T DECR NO. OF BITS ON
         BCTR  RIS,0                    DECR NUMBER OF BITS ON
SHIFTMSK EQU   *
         SRL   RWORKB,E1                SHIFT MASK RIGHT ONE
         BCT   RCH,CHKBITS              BRANCH IF ALL BITS NOT CHECKED
         SPACE 3
********
******** FOR DISPLAY TERMINAL, POINT PAST TERMINAL ENTRY OPTION FIELDS,
******** IF ANY
********
         LA    RWORKB,TRMOPNO           ADDR OF NUMBER OF OPTIONS
*                                       IF ANY; OTHERWISE, ADDR OF
*                                       FIRST DEVICE DEPENDENT FIELD
         SR    RCH,RCH                  ZERO REG FOR IC INSTR
         TM    TRMSTATE,TRMOPTFN        ANY OPTION FIELDS
         BZ    CHECKDDF                 NO - CHECK FOR DEVICE FIELDS
         IC    RCH,TRMOPNO              GET NO. & SIZE OF OPTIONS
         LA    RWORKB,E3(RCH,RWORKB)    BUMP PAST REST OF TERMINAL
*                                       ENTRY (3) AND ANY OPTIONS (RCH)
         SPACE 3
********
******** FOR DISPLAY TERMINAL, POINT PAST TERMINAL ENTRY DEVICE
******** DEPENDENT FIELDS, IF ANY
********
CHECKDDF EQU   *
         LTR   RIS,RIS                  ANY TRMDEVFL BITS ON
         BZ    GETINFO                  NO - RWORKB NOW POINTS TO
*                                       FIRST DEVICE DEPENDENT FIELD
BUMPDDF  EQU   *
         IC    RCH,0(0,RWORKB)          GET LNG OF DEVICE DEPENDENT FLD
         LA    RWORKB,E1(RCH,RWORKB)    BUMP PAST LENGTH FIELD (1) AND
*                                       DEVICE DEPENDENT FIELD (RCH)
         BCT   RIS,BUMPDDF              BRANCH IF MORE FIELDS TO SKIP
         SPACE 3
********
******** FOR DISPLAY TERMINAL, INIT TSBLNNO FROM TERMINAL ENTRY DEVICE
******** DEPENDENT FIELDS
********
GETINFO  EQU   *
         MVC   TSBLNNO,E1(RWORKB)       PUT NO. LINES INTO TSB   S22028
         SPACE 3
********
******** INITIALIZE TSBLNSZ ACCORDING TO TERMINAL TYPE
********
         MVC   TSBLNSZ,E2(RWORKB)       PUT LINE SIZE INTO TSB   S22028
         TM    DCTBYTE5,DCTLSCR         LARGE SCREEN SUPPORTED @Z37XBYT
         BNO   NULLSTCC                 NO, GO SET NO DEFAULT  @Z37XBYT
         MVC   TSBPRMR,E1(RWORKB)       PRIMARY ROW SIZE       @Z37XBYT
         MVC   TSBPRMC,E2(RWORKB)       PRIMARY COLUMN SIZE    @Z37XBYT
         MVC   TSBALTR,E3(RWORKB)       ALTERNATE ROW SIZE     @Z37XBYT
         MVC   TSBALTC,E4(RWORKB)       ALTERNATE COLUMN SIZE  @Z37XBYT
         B     NULLSTCC                 GO SET TSBSTCC TO NO DEFAULT
NOTDSPLY EQU   *                                                 S22028
         TM    DCTBYTE2,DCTTWX          TELE TERMINAL           ZA25074
         BO    SMALLSIZ                 YES - SET TSBLNSZ TO 72
         LA    RCH,REGLNSZ              GET SIZE OF 120
         B     SETLINE                  BR TO SPECIFY SIZE
SMALLSIZ EQU   *
         LA    RCH,TELELNSZ             GET SIZE OF 72
SETLINE  EQU   *
         STC   RCH,TSBLNSZ              SPECIFY LINE SIZE IN TSB
         SPACE 3
********
******** INITIALIZE TSBSTCC ACCORDING TO TERMINAL TYPE
********
         TM    DCTBYTE2,DCTTWX          TWX TERMINAL            ZA25074
         BZ    CHKREST                  NO - CHK FOR 2741 OR 1050
         MVI   TSBLNDCC,TWXLD           SET TWX LD DEFAULT
         MVI   TSBCHDCC,TWXCD           SET TWX CD DEFAULT
         B     SETASRCE                 CONTINUE TSB INIT
CHKREST  EQU   *
         TM    DCTBYTE1,DCT2741+DCT5041 2741 OR 1050 TERMINAL   ZA25074
         BNZ   SETBOTH                  YES - SET PROPER DEFAULT CHARS
CHK1050  EQU   *
         TM    DCTBYTE3,DCT1050         IS IT A 1050            ZA25074
         BNO   NULLSTCC                 NO - DON'T DEFAULT       Y06327
         OI    TSBTERM2,TSBC1050        TERMINAL IS 1050        YS02019
SETBOTH  EQU   *
         MVI   TSBCHDCC,T4150CD         SET 2741 AND 1050 CD DEFAULT
         OI    TSBSTAT,TSBATNLD         DENOTE ATTN FOR LD
         B     NULLLD                   INIT LD CHAR
NULLSTCC EQU   *
         MVI   TSBCHDCC,NODEFALT        DENOTE CD HAS NO DEFAULT
NULLLD   EQU   *
         MVI   TSBLNDCC,NODEFALT        DENOTE LD HAS NO DEFAULT
         SPACE 3
********
******** SET TERMINAL INDEX AND CHARACTERISTICS
********
SETASRCE EQU   *
         OC    TSBTERMC,E1(RTCT)        COPY TCT FIELDS         YS02019
         DROP  RTCT                                             ZA25074
         LH    RWORKB,PRFSRCE      GET TNT INDEX FROM BFR       ZA16608
         STH   RWORKB,TSBASRCE     PUT TNT INDEX IN TSB         ZA16608
         BCTR  RWORKB,R0           DECR INDEX FOR MULT.         ZA16608
         L     RTNT,AVTRNMPT       GET TNT ENTRY ADDRESS        ZA16608
*              TCT NO LONGER ADDRESSABLE                        ZA16608
         SR    RLNTH,RLNTH         ZERO THE REGISTER            ZA16608
         IC    RLNTH,X'28'(,RTNT)  GET ID LENGTH FROM TNT       ZA16608
         LA    RIS,E3              GET LENGTH OF ADDRESS        ZA16608
         AR    RIS,RLNTH           ADD ADR. FIELD TO LENGTH     ZA16608
         MR    RCH,RWORKB          MULT LNTH * OFFSET           ZA16608
         AR    RIS,RTNT            ADD START OF TNT             ZA16608
         MVC   TSBTRMID,BLANKS     BLANK OUT ID FIELD           ZA16608
         BCTR  RLNTH,R0            DECR TERM NAME LNTH          ZA16608
         EX    RLNTH,ANMVC         MOVE TERM ID TO TSB          ZA16608
         LR    RQCB,R0             RESTORE QCB POINTER          YM01114
         USING IEDQQCB,RQCB             QCB ADDRESSABLE         YM01114
         TM    QCBFLAG,QCBNOBRK         IS REVERSE BREAK        YM01114
*                                       FEATURE ALLOWED         YM01114
         BZ    POSTAY3                  YES, DON'T SET TSBNOBRK YM01114
         OI    TSBFLG3,TSBNOBRK         USE OF REVERSE BREAK    YM01114
*                                       FEATURE NOT ALLOWED     YM01114
         DROP  RQCB                     QCB NO LONG ADDRESSABLE YM01114
         SPACE 3                                                YS02019
********                                                        YS02019
******** GO TO POST ROUTINE, TO WAKE UP IEDAY3, WHICH COULD BE  YS02019
******** WAITING ON ASCBECB.                                    YS02019
********                                                        YS02019
POSTAY3  EQU   *                                                YM01114
         LA    RIS,ASCBECB                                      YS02019
         SR    RCH,RCH                  ZERO COMPLETION CODE    YS02019
         L     RWORKA,CVTPTR            LOAD CVT ADDRESS
         SPACE 2                                                YS02019
********
******** FIND MASTER SCHEDULER'S ASCB FOR POST ROUTINE
********
         USING CVT,RWORKA               ADDRESS CVT
         L     R13,CVTMSER              ADDRESS OF BASEA        YS02019
         L     R13,BAASCB-BASE(,R13)    ASCB OF MASTR SCHED     YS02019
         SPACE 2                                                YS02019
********
******** CONTINUE SETTING UP REGISTERS FOR POST
********
         O     RIS,HIGHON               HI BIT SIGNALS XM POST  YS02019
         LR    R0,RBASE                 SAVE BASE REGISTER      YS02019
         LA    RBASE,CVTBRET            'ERRET' ADDRESS (BR14)  YS02019
         L     R15,CVT0PT01             LOAD POST ROUTINE ADDRESS
         LR    RWORKB,R14               SAVE RETURN ADDR
         DROP  RWORKA
         MODESET EXTKEY=SUPR            GET KEY ZERO FOR POST
         BALR  R14,R15                  GO TO POST ROUTINE
         LR    RBASE,R0                 RESTORE BASE REGISTER   YS02019
         LR    R14,RWORKB               RESTORE RETURN ADDR     YM06501
         SPACE 3
********
******** CHECK FOR TS STOPPING OR ABENDING.  SIC THIS USER IF IT IS
********
         TM    TIOCFLG,TIOCSTOP+TIOCTSAB CK FOR TERMINATION     YS02019
         BNZ   CALLSIC                  CALL CANCEL ROUTINE     YS02019
         DROP  RBASE                                            YS02019
         SPACE 3                                                YS02019
********
******** RETURN TO TIOC LOGON (IEDAYL)
********
         SR    R15,R15                  SET 0 RETURN CODE       YS02019
         BR    R14                      RETURN TO IEDAYQT       YS02019
         SPACE 3                                                YS02019
* * * * * * * * * *  CALL SIC TO CANCEL THIS USER  * * * * * * * * * *
********                                                        YS02019
******** USE BRANCH ENTRY TO SYSTEM-INITIATED CANCEL            YS02019
********                                                        YS02019
CALLSIC  EQU   *                                                YS02019
         USING QTIP0100,RBASE                                   YS02019
         L     RWORKA,CVTPTR            CVT ADDRESSABILITY      YS02019
         LA    R13,TIOCSAVE             GET SAVE AREA POINTER   YS02019
         L     R0,POSTCODE              'POST CODE' FOR SIC     YS02019
         USING CVT,RWORKA               CVT ADDRESSABLE         YS02019
         L     R15,CVTSIC               SIC ROUTINE ENTRY POINT YS02019
         DROP  RWORKA                   ADDRESSABILITY ENDS     YS02019
         LR    RTERM,RASCB              ASCB ADDR FOR SIC       YS02019
         LR    RWORKB,R14               SAVE RETURN ADDR        YS02019
         BALR  R14,R15                  GO TO SIC               YS02019
         SPACE 3                                                YS02019
********                                                        YS02019
******** SET RETURN CODE TO 4 AND RETURN                        YS02019
********                                                        YS02019
         LR    R14,RWORKB               RESTORE RETURN ADDRESS  YS02019
         LA    R15,E4                   SET CODE                YS02019
         BR    R14                      RETURN                  YS02019
         SPACE 3                                                YS02019
********                                                        YS02019
******** CONSTANTS                                              YS02019
********                                                        YS02019
BLANKS   DS    0F                  8 BYTES OF BLANKS USED       ZA16608
         DC    C'        '         TO BLANK OUT TSBTRMID        ZA16608
HIGHON   DS    0F                                               YS02019
         DC    X'80000000'              HIGH BIT FOR XMPOST     YS02019
POSTCODE DS    0F                       COMPLETION CODE FOR     YS02019
         DC    X'00000622'              TERMINATED MEMORY       YS02019
TESTBITS TM    TRMDEVFL,NOMASK          COUNT THE BITS THAT ARE ON
ANMVC    MVC   TSBTRMID(0),X'52'(RIS)                           ZA16608
PATCH    DC    10F'0'                   PATCH AREA              YM03998
         SPACE 3
* * * * * * * * * * * *  CONTROL BLOCK DSECTS * * * * * * * * * * * * *
         SPACE 3
         IHAASCB
         EJECT
         IHAASVT                                                YS02019
         EJECT                                                YS02019
         TAVTD                                                  ZA16608
         EJECT                                                  ZA16608
         IEEBASEA                                               YS02019
         EJECT                                                YS02019
CVT      DSECT
         CVT
         EJECT                                                YS02019
         TDCTD                                                  ZA25074
         EJECT                                                  ZA25074
         TTCXD                                                  YS02019
         EJECT
         IKJTIOCP
         EJECT
         IKJTSB
         EJECT
         TLCBD
         EJECT
         TPRFD
         EJECT
         TQCBD
         EJECT
         DCBD  DSORG=TX
         EJECT
         TTRMD
         EJECT
UCB      DSECT                                                   Y06327
         IEFUCBOB LIST=YES                                       Y06327
         END
