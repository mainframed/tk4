2046     TITLE '''IGG02046'' - TCAM/SAM CLOSE EXECUTOR'
IGG02046 CSECT
         SPACE 3                                                SA62364
*CHANGE-ACTIVITY = AS FOLLOWS:                                  SA21903
**************************** MICROFICHE FLAGS *************** SUPT CODE
*C212000                                                        SA55923
*A094300                                                         S22025
*A246000,538000                                                 SA53606
*A094000,334000,491000,510600,511500                             A47157
*D522000                                                         A47157
*A073000,074000,076000,078000,080000,082000,084000,089000,106000 CLUP21
*A112000,149000,165000,192000,196000,239000,268000,314000,317000 CLUP21
*A321000,357000,361000,369000,372000,491000                      CLUP21
*C094000,108000,129300-129600,130900,157000,194000-195000,237000 CLUP21
*C256000,316000,319000,324000,352000,359000-360000,371000,433000 CLUP21
*D138000,141000-143000,153000-155000,169000-171000,188000,224700 CLUP21
*D241000-243000,271000,273000-274000,299000,325000-328000        CLUP21
*D346000-349000,384000-385000,445000-449000,470000,471400-475000 CLUP21
*D491000,494000,497000,505000,508000-509000,511000               CLUP21
*A221000,227000                                                  A44869
*D470000,471400,236000-238000                                    CLUP21
*C224000                                                         CLUP21
*A136000                                                         A41028
*C231000,471700                                                  A41028
*D134000                                                         A41028
*C212000,213000,215000,216000                                    S21903
*C161000,254000,307000,391000-394000,427000,434000,437000,454000,S21903
*C468000,469000,471000,511500,511800,516000,517000               S21903
*D203000,211000,217000,221000                                  S21903
*D353300-353600                                                 SA51076
*D324000                                                        SA50090
*A224500                                                        SA59162
*D291000                                                        SA59162
*C148000                                                        SA60335
*D165000-187000                                                 SA60335
*A203500                                                        SA62364
*C006400,012000-024000,054000,085000,116000-268650,294000        Y02027
*C297000,397000,398000,451000,477500,498000-500100,511100        Y02027
*A073700,084200,297300-299700,313020-313800,374000-386000        Y02027
*A399000-411000,438160-438880,447000,456060,470000-475000,480500 Y02027
*A523300-523600,537030-538800                                    Y02027
*D050000,055000-056000,073000,074000-074500,078500,080500,082500 Y02027
*D084500,089500,096000-099000,104000,303000-308000,397500        Y02027
*D412000-414000,451090-456000,456200-461750,465000,476000,479500 Y02027
*D484000,486000,489000,491000,491060,491120,491200,491400,491600 Y02027
*D503000-506000,509500,511200-517000,525000-536000               Y02027
*A230000                                                        SA66366
*A120300,397500,510000                                          SA66747
*A042400-042800,166200-166800,182500,186300-186600,268900-268930,X01004
*A288500,289200,295500,305400-305800,477500,479500,481500,       X01004
*A482200-482600,509700                                           X01004
*C166000,181000-182000,183000,185000-186000,279000-280000,       X01004
*C283000-284000,289000,296000-297000,305000,306000,309000-310000 X01004
*D043000,172000-173000,184000,189000,281000,291000,298000        X01004
*A042100-042300,121060-121800,129500,129700-129900,130100,153000,Y01004
*A201800,202600,206100-206200,218100-218200,220500-221000,221500,Y01004
*A224200,294500,397500,451100-451910,452100,453800,454400-454900,Y01004
*A455100-458200,483500,491020-491110                             Y01004
*C024000,042000,108500,120000,136000,148000,158000,201000,       Y01004
*C204000-206000,220000,227200-227800,294000,307000,312000-313000,Y01004
*C397000,451000,452000,454000,455000                             Y01004
*D025000,105000,129300,129600,130000,130600,135000,136500,       Y01004
*D149000-152000,156000-157000,159000-165000,202000,216000-217500,Y01004
*D221200-221400,221600-221800,235000-264000,314000-369000,       Y01004
*D374000-396000,399000-411000,453000,469300-471200               Y01004
*A459050-459150                                                  X02004
*D461600-461700                                                  X02004
*A174030-174150,153050                                         @ZA00234
*D186220,186280                                                @ZA00234
*A103000,140200,190500,475000,522000                           @ZA01439
*D171200                                                       @ZA01439
*D179500-180000,182500-183000                                  @ZA00234
*A153000                                                       @ZA00234
*C146000                                                       @ZA00234
*A184350,186500                                                @ZA01567
*A161000,173000,185200,502000,537550,538800                    @ZA02601
*C137000-140264,140600,145600-159000,168000-169000             @ZA02601
*C174030-174150,385000                                         @ZA02601
*C386000,438160-438880                                         @ZA02601
*D179000,184200-184390,187200,188500-190900                    @ZA02601
*D118000                                                       @ZA02601
*C385000                                                       @ZA02601
*D120000,116000,438160-438660                                  @ZA04027
*C156400-156600,386000-402000                                  @ZA04027
*A192000                                                       @ZA04027
*C151000                                                       @ZA05047
*D193400                                                       @OZ07799
*A438000                                                       @OZ07799
*C174000                                                       @OZ07798
*C148600-148800                                                @OZ07819
*A156400                                                       @OZ07819
*D161000,438300-438500,410000                                  @OZ07819
*C313180                                PECB DSECT CHG         @OZ07830
*A149600,150000,151600,152200,468000                           @OZ17630
*D140280-140840                                                @OZ19756
*A148000                                                       @OZ19756
*C008200,171400,171600                                         @Z40X9TG
*C313240-313260                                                @OY16374
*D313320-313340                                                @OY16374
*A411000                                                       @OZ18749
*D148600-148800,438100-438700                                  @OZ18749
*                                       PECB DSECT CHG         @OZ18744
***********************************************************************
*                                                                     *
*TITLE: 'IGG02046' TCAM APPLICATION PROGRAM CLOSE EXECUTOR            *
*                                                                     *
*DESCRIPTIVE NAME = TCAM APPL PROG CLOSE EXECUTOR                     *
*                                                                     *
*  DESCRIPTIVE NAME = TCAM APPLICATION PROGRAM CLOSE EXECUTOR ( LOAD 1
*                                                                     *
*  COPYRIGHT = 'NONE'                                                 *
*                                                                     *
*  STATUS CHANGE LEVEL 5                                              *
*                                                                     *
*FUNCTION: THIS MODULE DEACTIVATES A DATA TRANSFER COMMUNICATION LINK *
*   BETWEEN AN APPLICATION PROGRAM AND THE MCP.  IT EXECUTES IN       *
*   SUPERVISOR MODE AND, FOR THE MOST PART, IN KEY 5.                 *
*                                                                     *
*   IF CLOSE WAS ENTERED FOR ABEND, THE PEB IS INVALIDATED SO NO      *
*   MCP ROUTINE WILL POST BACK TO THE APPLICATION PROGRAM             *
*                                                                     *
*   IF THE MCP IS ACTIVE, OPEN SETS UP TO GIVE CONTROL TO THE         *
*   OPEN/CLOSE SUBTASK IN THE MCP.  ONCE PER CLOSE, CORE IS           *
*   GOTTEN FOR AN RCB. THE RCB IS INITIALIZE WITH INFORMATION         *
*   NEEDED BY IEDQEU.  FOR INPUT DCB, IF THE ERB IS BUSY, A WAIT      *
*   IS ISSUED. (WHEN ERB IS FREED,THE RCB WILL BE POSTED TO EU.)      *
*   AN SVC 102 IS ISSUED WITH POST CODE IN PARMLIST. IT IS            *
*   FOLLOWED BY A WAIT FOR IEDQEU.                                    *
*   UPON RETURN, ACCESS METHOD MODULES ARE DELETED AND CORE FOR       *
*   PECB AND LOCATE MODE WORK AREA IS FREED.                          *
*                                                                     *
*   ON A CLOSEDOWN, THE OPERATOR CONTROL ECB IS POSTED COMPLETE.      *
*                                                                     *
*   BEFORE THE EXECUTOR XCTLS TO THE NEXT ENTRY IN THE WHERE-TO       *
*   GO TABLE, THE CORE FOR THE RCB IS FREED.                          *
*                                                                     *
*ENTRY POINT:                                                         *
*        IGG02046                                                     *
*                                                                     *
*INPUT: REGISTERS 5,6,7 AND 8 CONTAIN THE FOLLOWING VALUES:           *
*   5 - ADDRESS OF FIRST ENTRY IN DCB PARAMETER LIST                  *
*   6 - ADDRESS OF BEGINNING OF WHERE-TO-GO TABLE                     *
*   7 - ADDRESS OF CURRENT ENTRY IN DCB PARAMETER LIST                *
*   8 - ADDRESS OF CURRENT ENTRY IN WHERE-TO-GO TABLE                 *
*                                                                     *
*OUTPUT: REGISTERS 5,6,7 AND 8 CONTAIN THE FOLLOWING VALUES:          *
*   5 - SEE 'INPUT'                                                   *
*   6 - SEE 'INPUT'                                                   *
*   7 - ADDRESS OF NEXT ENTRY IN DCB PARAMETER LIST                   *
*   8 - ADDRESS OF NEXT ENTRY IN WHERE-TO-GO TABLE                    *
*                                                                     *
*EXTERNAL ROUTINES: DEBCHK -- SVC 117                                 *
*                   DELETE -- SVC 9                                   *
*                   WAIT -- SVC 1                                     *
*                   XCTL -- SVC 7                                     *
*                   IEDQEB -- AQCTL SVC 102 ROUTINE -- TO TPOST A     *
*   SPECIAL ELEMENT TO THE OPEN/CLOSE SUBTASK.                        *
*                                                                     *
*EXITS-NORMAL: THIS MODULE XCTLS TO NEXT ENTRY IN WTG TABLE           *
*                                                                     *
*EXITS-ERROR                                                          *
*                                                                     *
*TABLES/WORK AREAS:                                                   *
*   CVT                                                               *
*   DCB                                                               *
*   TCB                                                               *
*   DEB                                                               *
*                                                                     *
*   OPEN/CLOSE WORK AREA                                              *
*                                                                     *
*ATTRIBUTES -- REUSABLE,REENTERANT.                                   *
*                                                                     *
*NOTES: THE OPERATION OF THIS MODULE DEPENDS UPON AN INTERNAL         *
*   REPRESENTATION OF THE EXTERNAL CHARACTER SET WHICH IS EQUIVALENT  *
*   TO THE ONE USED AT ASSEMBLY TIME.  THE CODING HAS BEEN ARRANGED   *
*   SO THAT REDEFINITION OF 'CHARACTER' CONSTANTS, BY REASSEMBLY,     *
*   WILL RESULT IN A CORRECT MODULE FOR THE NEW DEFINITIONS.          *
*                                                                     *
***********************************************************************
         EJECT
*                                                                     *
*              S Y M B O L I C    R E G I S T E R S                   *
*                                                                     *
RZERO    EQU   0                        GENERAL WORK REGISTER
RPARM    EQU   1
RCORE    EQU   2                        DCB WORK AREA.           Y02027
RPEWA    EQU   3                        PROCESS ENTRY WORKAREA
RPENTRY  EQU   4                        PROCESS ENTRY
RMACR    EQU   4                        ADDRESS OF APPROPRIATE BYTE
*                                       IN DCBMACR FIELD
RPAR     EQU   5                        ADDRESS OF FIRST ENTRY IN
*                                       DCB PARAMETER LIST
RWTG     EQU   6                        ADDRESS OF WHERE-TO-GO
*                                       (WTG) TABLE
RPARC    EQU   7                        ADDRESS OF CURRENT ENTRY
*                                       IN DCB PARAMETER LIST
RWTGC    EQU   8                        ADDRESS OF CURRENT ENTRY
*                                       IN WHERE-TO-GO TABLE
RTCB     EQU   9                        TCB BASE REG.            Y02027
RTCX     EQU   9                        TCX ADDR                 Y02027
RDCB     EQU   10                       DATA CONTROL BLOCK
RDEB     EQU   11                       DATA EXTENT BLOCK
RBASE    EQU   12                       CSECT BASE REGISTER
RWORK    EQU   13                       ACCESS METHOD WORKAREA
RETURN   EQU   14                       RETURN ADDRESS
RENTRY   EQU   15                       COMPLETION CODE REGISTER
         SPACE 3
         BALR  RBASE,RZERO              ESTABLISH
         USING *,RBASE                  ADDRESSIBILITY           CLUP21
IGG02046 IEDHJN BYPASS                  BRANCH AROUND ID AND DATES22025
         SPACE 3
         USING IHADCB,RDCB              DATA CONTROL BLOCK
         USING IEDQPEWA,RPEWA           PROCESS ENTRY WORKAREA
         USING IEDQDEB,RDEB             DATA EXTENT BLOCK
         USING FORCORE,RCORE            DATA CONTROL BLOCK WORKAREA
         USING IEDQTCB,RTCB             TASK CONTROL BLOCK
         USING PSA,RZERO                PSA                    @ZA01439
         EJECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *CLUP21
*                                                                     *
* THE FOLLOWING SECTION OF CODE IS EXECUTED FOR EACH DCB BEING   CLUP21
* CLOSED.  IT INITIALIZES CONTROL BLOCK BASE REGISTERS.          Y01004
*                                                                     *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *CLUP21
START    EQU   *                                                 Y02027
         L     RCORE,FOUR(,RWTGC)       ADDRESS OF OPEN WORKAREA.
         L     RDCB,DXUDCBAD            ADDR OF REAL DCB         Y02027
         DEBCHK  (RDCB),TYPE=VERIFY,AM=TCAMAP  VALIDATE DEB      Y02027
         LR    RDEB,RPARM               LOAD DEB BASE REG.       Y02027
         L     RDCB,ZERO(,RPARC)        LOAD COPIED DCB ADDR.    Y02027
         TM    DEBPCBAD-ONE,FLGU        IS RECFM DEFAULT VALUE.  Y02027
         BNO   NOCLEAR                  RECFM IS NOT DEFUALT.    Y02027
         MVI   DCBRECFM,NULL            SET RECFM TO UNSPECIFIED.Y02027
NOCLEAR  EQU   *                                                 Y02027
         MODESET  EXTKEY=ZERO
         XC    DXCCW1,DXCCW1            CLEAR ECB.               Y02027
         ICM   RPEWA,AD,DEBPEWA         LOAD PEWA ADDRESS      @ZA02601
         BZ    NOEU                     BRANCH, NO PEWA        @ZA02601
         EJECT                                                   Y02027
*****************************************************************Y02027
***** SET UP RCB FOR IEDQEU(MCP MEMORY) AND POST RCB TO          Y02027
******   DISABLED READY QUEUE.                                   Y02027
*****************************************************************Y02027
         L     RTCX,CVTPTR              LOAD CVT PTR             Y02027
         ICM   RTCX,ALL,CVTAQAVT-CVT(RTCX) IS MCP ACTIVE       @ZA02601
         BZ    NOEU                     NO, BYPASS POST TO MCP @ZA02601
         TM    TCXFLAG1-IEDQTCX(RTCX),TCXMCPCD MCP TERMINATING @ZA02601
         BO    NOEU                     YES, BYPASS POST TO MCP@ZA02601
         L     RENTRY,PSAAOLD           ADDRESS OLD ASCB PTR   @ZA02601
         CLC   ASCBASID-ASCB(TWO,RENTRY),TCXASID-IEDQTCX(RTCX) @ZA02601
*                                       IS APPL A SUBTASK      @ZA02601
*                                       OF MCP                 @ZA02601
         BNE   MCPOK                    BR, NOT IN SAME MEMORY @ZA02601
         L     RENTRY,TCXTCB-IEDQTCX(RTCX) GET MCP TCB ADDRESS @ZA02601
         TM    TCBFLGS-IEDQTCB(RENTRY),TERM IS MCP TERMINATING @ZA02601
         BM    NOEU                     YES, BYPASS POST TO MCP@ZA02601
MCPOK    EQU   *                                               @ZA02601
***************** IF APPL PROG IS ABENDING INVALIDATE          @OZ19756
********       PEB TO PREVENT CROSS MEMORY POST                @OZ19756
         L     RTCB,DEBTCBAD-ONE        LOAD TCB ADDR          @OZ19756
         TM    TCBFLGS,ABND             IS APPL PROG ABENDING  @OZ19756
         BNO   NOTABEND                 BR NO                  @OZ19756
         ICM   RPARM,AD,DEBPEBAD        GET PEB ADDR           @OZ19756
         BZ    NOEU                     BR IF NO PEB           @OZ19756
         MVI   PEBASCB-IEDQPEB(RPARM),FF INVALIDATE THE PEB    @OZ19756
NOTABEND EQU   *                                               @OZ19756
         L     RTCX,CVTPTR              GET CVT PTR            @OZ19756
         ICM   RTCX,ALL,CVTAQAVT-CVT(RTCX)  GET TCX PTR        @OZ19756
         BZ    NOEU                     MCP NOT ACTIVE IF 0    @OZ19756
         ICM   RZERO,AD,TCXOCQ-IEDQTCX+ONE(RTCX) GET QCB ADDR  @ZA02601
         BZ    NOEU                     NO INTERFACE FOR APPL  @ZA02601
         LA    RPARM,TCXAIBC-IEDQTCX(RTCX) TCX AIB PTR         @ZA02601
AIBGET   EQU   *                                               @ZA02601
         ICM   RPARM,AD,AIBCHAIN-IEDQAIB(RPARM) ANY AIB'S AVAIL@ZA02601
         BNZ   GOTAIBS                  YES, GET ONE           @ZA02601
         LH    RZERO,TCXSZAIB-IEDQTCX(RTCX)   GET SIZE OF AIB  @OZ17630
         CH    RZERO,FOURK              SIZE GREATER THAN 4K   @OZ17630
         BH    GETCORE                  YES, BRANCH            @OZ17630
         LA    RZERO,HPAGE              2K VALUE               @ZA02601
         AR    RZERO,RZERO              DOUBLE TO PAGE SIZE    @ZA02601
GETCORE  EQU   *                                               @OZ17630
         LR    RCORE,RPARC              SAVE REG ACROSS GETMAIN@ZA02601
         ICM   RMACR,AD,DEBTCBAD        TCB ADDR FOR GETMAIN   @ZA02601
         L     RPARC,PSAAOLD            ASCB ADDR FOR GETMAIN  @ZA02601
*                                       GETMAIN CSA KEY6       @ZA02601
         MODESET   EXTKEY=TCAM          GET IN KEY 6           @ZA05047
         GETMAIN RU,LV=(0),SP=CSA,BNDRY=PAGE                   @ZA05047
         MODESET   EXTKEY=ZERO          GO BACK TO KEY 0       @ZA05047
         LR    RPARC,RCORE              RESTORE RPARC          @ZA02601
         L     RCORE,FOUR(,RWTGC)       RESTORE ADDR OF WRKAREA@ZA02601
         LH    RETURN,TCXSZAIB-IEDQTCX(RTCX) SIZE OF AIB       @ZA02601
         CH    RETURN,FOURK             SIZE GREATER THAN 4K   @OZ17630
         BNH   SMALLAIB                 NO, BRANCH             @OZ17630
         LR    RENTRY,RPARM             GET ADDR OF ONLY AIB   @OZ17630
         B     CLRBYTE                  BRANCH TO CLEAR BYTE   @OZ17630
SMALLAIB EQU   *                                               @OZ17630
         LA    RENTRY,HPAGE             2K VALUE               @ZA02601
         LA    RENTRY,HPAGE(RENTRY,RPARM) ADDR OF END OF AIBS  @ZA02601
         SR    RENTRY,RETURN            ADDR OF LAST AIB       @ZA02601
CLRBYTE  EQU   *                                               @OZ17630
         LA    RZERO,ZERO(RPARM)        CLEAR HI ORDER BYTE    @ZA02601
         B     BXLE                     BRANCH TO INITIALIZE   @ZA02601
CHAINAIB EQU   *                                               @ZA02601
         ST    RZERO,AIBCHAIN-ONE-IEDQAIB(RPARM) STORE AIB PTR @ZA02601
         LR    RPARM,RZERO              SAVE CURRENT PTR       @ZA02601
BXLE     EQU   *                                               @ZA02601
         BXLE  RZERO,RETURN,CHAINAIB    LOOP THRU NEW CORE     @ZA02601
*                                       TILL ALL AIBS LINKED   @ZA02601
         LR    RENTRY,RPARM             LAST AIB PTR           @ZA02601
         N     RPARM,CLRLOW             START OF GOTTEN CORE   @ZA02601
         L     RZERO,TCXAIBC-IEDQTCX(RTCX) GET CURRENT AIB PTR @ZA02601
AIBLOOP  EQU   *                                               @ZA02601
         ST    RZERO,AIBCHAIN-ONE-IEDQAIB(RENTRY) LINK AIBS    @ZA02601
         CS    RZERO,RPARM,TCXAIBC-IEDQTCX(RTCX) CHAIN AIBS    @ZA02601
         BNE   AIBLOOP                  RETRY IF CS FAILED     @ZA02601
GOTAIBS  EQU   *                                               @ZA02601
         TS    AIBSTATE-IEDQAIB(RPARM)  ALLOCATE AIB           @ZA02601
         BNZ   AIBGET                   IN USE, RETRY          @ZA02601
         MVC   AIBECBA-IEDQAIB(FOUR,RPARM),CLOS SET ID FOR     @ZA02601
*                                       FAILURE                @ZA02601
         ST    RPARM,DXCCW12            SAVE AIB ADDR          @ZA04027
HAVEAIB  EQU   *                                               @OZ07819
         LA    RPARM,AIBKEY-IEDQAIB(RPARM)  BUMP TO RCB        @ZA04027
         XC    ZERO(RCBSIZE,RPARM),ZERO(RPARM) CLEAR RCB       @ZA02601
         USING IEDQRECB,RPARM           SET UP RCB BASE.         Y02027
         MVI   RECBKEY,ZERO             SET KEY FOR CLOSE.       Y02027
         MVI   RECBPRI,CLOSEPRI         SET CLOSE PRIORITY.      Y02027
         MVC   RECBQCBA(3),TCXOCQ-IEDQTCX+ONE(RTCB) MOVE QCB ADDRY02027
         MVC   RCBTCB+ONE(THREE),DEBTCBAD MOVE TCB ADDR TO RCB.  Y02027
         MVC   RCBDCBMA,DCBMACRF        MOVE MACRF TO RCB.       Y02027
         L     RENTRY,DEBTCBAD-ONE      APPL TCB ADDR          @ZA02601
         MVC   RCBTCBFL,TCBFLGS-IEDQTCB(RENTRY) MOVE TCB FLAGS @ZA02601
         LA    RZERO,DXCCW1             LOAD CLOSE ECB ADDR.     Y02027
         ST    RZERO,RCBPECB            STORE ECB ADDR IN RCB.   Y02027
         L     RPEWA,PSAAOLD            GET PTR TO CURRENT ASCB@Z40X9TG
         LA    RPEWA,ZERO(,RPEWA)       CLEAR HIGH BYTE        @Z40X9TG
         ST    RPEWA,RCBASCB            SET ASCB IN RCB        @Z40X9TG
         MVC   RCBASID+TWO(TWO),ASCBASID-ASCB(RPEWA) SET       @Z40X9TG
*                                       ASID                   @Z40X9TG
         ICM   RPEWA,AD,DEBPEWA         LOAD PEWA ADDR.          Y02027
         ST    RPEWA,RCBDSNM            PASS PEWA ADDR. IN RCB.  Y02027
         STM   RDCB,RENTRY,RCBSAVE      SAVE REGS OVER SETLOCKS@ZA02601
LOCALGET SETLOCK OBTAIN,TYPE=LOCAL,MODE=UNCOND,                @ZA02601X
               RELATED=(PEBPEWA,IEDQEU(LOCALREL),IEDQOT01(LOCALREL),IGGX
               02046(LOCALREL))                                @ZA02601
CMSGET   SETLOCK OBTAIN,TYPE=CMS,MODE=UNCOND,                  @ZA02601X
               RELATED=(PEBPEWA,IEDQEU(CMSREL),IEDQOT01(CMSREL),IGG0204X
               6(CMSREL))                                      @ZA02601
         LM    RDCB,RENTRY,RCBSAVE      RESTORE REGS           @ZA02601
         LA    RMACR,TCXPEWAC-IEDQTCX-PEWALINK+IEDQPEWA(RTCX)  @ZA02601
*                                       FIRST PEWA ADDRESS     @ZA02601
         LA    RPEWA,ZERO(RPEWA)        CLEAR HIGH BYTE        @ZA02601
LOOPPEWA EQU   *                                               @ZA02601
         ICM   RMACR,ALL,PEWALINK-IEDQPEWA(RMACR) GET PEWA     @ZA02601
         BZ    ENDPEWA                  END OF CHAIN           @ZA02601
         CR    RMACR,RPEWA              PEWA IN CHAIN          @ZA02601
         BNE   LOOPPEWA                 NO, CHECK NEXT PEWA    @ZA02601
ENDPEWA  EQU   *                                               @ZA02601
CMSREL   SETLOCK RELEASE,TYPE=CMS,                             @ZA02601X
               RELATED=(PEBPEWA,IEDQEU(CMSGET),IEDQOT01(CMSGET),IGG0204X
               6(CMSGET))                                      @ZA02601
LOCALREL SETLOCK RELEASE,TYPE=LOCAL,                           @ZA02601X
               RELATED=(PEBPEWA,IEDQEU(LOCALGET),IEDQOT01(LOCALGET),IGGX
               02046(LOCALGET))                                @ZA02601
         LM    RDCB,RENTRY,RCBSAVE      RETORE REGS            @ZA02601
         LTR   RMACR,RMACR              PEWA FOUND IN CHAIN    @ZA02601
         BZ    NOEU                     NO, BYPASS POST TO MCP @ZA02601
         ST    RPARM,PEWARCB            SAVE RCB ADDR IN PEWA  @OZ07798
         TM    PEWAFLG,CFLG             ALREADY REQUESTED CLOSE@ZA02601
         BO    NOEU                     YES, BYPASS POST TO MCP@ZA02601
         OI    PEWAFLG,CFLG             SET CLOSE FLAG         @ZA02601
         CLI   DCBMACRF,ZERO            TEST FOR OUTPUT.         Y02027
         BE    ERBFREED                 BRANCH IF OUTPUT.        Y02027
         TM    PEWAFLG,ERBBUSY          TEST FOR ERB BEING BUSY. Y02027
         BNO   ERBFREED                 BRANCH IF ERB NOT BUSY.  Y02027
         B     WAITX                    GO WAIT FOR EU.          Y02027
ERBFREED EQU   *                                                 Y02027
*              PUT RCB ON ASYCHRONOUS READY QUEUE TO ACTIVATE    Y02027
*              IEDQEU SUBTASK IN MCP                             Y02027
         L     RENTRY,TCXREADY-IEDQTCX(RTCX)  FIRST ELEMENT      Y02027
POSTLOOP STCM  RENTRY,AD,RECBLINK-IEDQRECB(RPARM) POINT NEW      Y02027
*                                       ELEMENT TO FIRST ELEMENT Y02027
         CS    RENTRY,RPARM,TCXREADY-IEDQTCX(RTCX) CURRENT       Y02027
*                                       ELEMENT UPDATED?         Y02027
         BNE   POSTLOOP                 BRANCH IF YES, TRY AGAIN Y02027
*                                       NO, ASYCN READY QUEUE    Y02027
*                                       UPDATED WITH ELEMENT     Y02027
*                                                                Y02027
         TS    RCBERR                   SET QEU POSTED         @ZA02601
*              POST MCP ECB COMPLETE                             Y02027
*                                                                Y02027
         L     RENTRY,TCXAVT-IEDQTCX(RTCX)  GET AVT ADDRESS      Y02027
         LA    RENTRY,AVTOSECB-IEDQAVTD(RENTRY) MCP-ECB ADDRESS  Y02027
         ST    RENTRY,DXCCW2            STORE IN PARAMETER LIST  Y02027
         L     RENTRY,CVTPTR            ADDRESS OF CVT           Y02027
         LA    RENTRY,CVTBRET-CVT(RENTRY) ERROR ADDRESS          Y02027
         ST    RENTRY,DXCCW2+EIGHT      AND STORE IN LIST        Y02027
         MVC   DXCCW2+FOUR(4),TCXASCB-IEDQTCX(RTCX) STORE ADDRESSY02027
*                                       OF ASCB IN LIST          Y02027
         SR    RZERO,RZERO              ZERO COMPLETION CODE     Y02027
         LA    RPARM,DXCCW2             ADDRESS OF LIST          Y02027
         POST  ,(0),MF=(E,(1))
WAITX    EQU   *                                                 Y02027
         MODESET   EXTKEY=ZERO                                 @ZA02601
         L     RTCB,DEBTCBAD-ONE  RELOAD TCB ADDR                Y02027
         WAIT  ECB=DXCCW1               WAIT ON EU               Y02027
         L     RPEWA,DXCCW12            AIB ADDR               @ZA04027
         LA    RPARM,AIBKEY-IEDQAIB     RCB OFFSET             @ZA04027
         AR    RPARM,RPEWA              BUMP TO RCB            @ZA04027
         MVC   DXCCW1+FIVE(3),RCBPEBAD   ASCB ADDR OF MCP      @ZA04027
         MVC   DXCCW1(1),RCBAVTBT       AVTBIT1 FROM EU        @ZA04027
         MVC   DXCCW1+ONE(3),RCBOPECB   MOVE ECB ADDR          @ZA04027
NOEU     EQU   *                        MCP ABENDING             Y02027
         EJECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *CLUP21
*                                                                     *
* DETERMINE LOAD MODULES TO BE DELETED AND DELETE THEM                *
*                                                                     *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *CLUP21
DELETE1  EQU   *                                                 Y02027
         NI    DEBDCBAD-ONE,FF-DEBID    CLEAR TCAM DEB I.D.      Y02027
         LA    RPEWA,DELE               RPEWA = ADDRESS OF SUB-  X01004
*                                       ROUTINE TO DELETE MODULES.
         CLI   DCBMACRF,OUT             IS THIS AN OUTPUT DCB
         BE    OUTDCB                   BRANCH IF YES
         LA    RMACR,DCBMACRF           ADDRESS OF INPUT DCB
*                                       INSTRUCTION REFERENCE
         MVI   MNAME+SEVEN,RG           GET/READ MODULE ID.
         BALR  RETURN,RPEWA             DELETE GET/READ ROUTINE. X01004
         TM    0(RMACR),READFLG         BSAM LEVEL
         BZ    POINT                    BRANCH IF NO
CHECK    EQU   *
         MVI   MNAME+SEVEN,RL           CHECK MODULE ID.
         XC    DCBCHECK+ONE(THREE),DCBCHECK+ONE  REMOVE CHECK    X01004
*                                       ROUTINE'S ENTRY POINT    X01004
*                                       ADDRESS FROM DCB.        X01004
         BALR  RETURN,RPEWA             DELETE CHECK ROUTINE     X02004
POINT    EQU   *
         TM    0(RMACR),POINTFLG        POINT(RETRIEVE) SPECIFIED
         BZ    FREEWA                   BRANCH IF NO             Y02027
         MVI   MNAME+SEVEN,RM           POINT MODULE ID.
         XC    DCBPOINT(FOUR),DCBPOINT  REMOVE POINT ROUTINE'S   X01004
*                                       ADDRESS FROM DCB.        X01004
         BALR  RETURN,RPEWA             DELETE POINT MODULE      Y02027
         B     FREEWA                   BRANCH TO FREE STORAGE   Y02027
*****************************************************************CLUP21
*                                                                     *
* DELETE SPECIFIED ACCESS METHOD MODULE                          CLUP21
*                                                                     *
*****************************************************************CLUP21
DELE     EQU   *
         DELETE EPLOC=MNAME                                      CLUP21
         BR RETURN                      RETURN TO CALLER         CLUP21
OUTDCB   EQU   *
         LA    RMACR,DCBMACRF+ONE       ADDRESS OF OUTPUT DCB.
*                                       INSTRUCTION REFERENCE
         MVI   MNAME+SEVEN,RI           PUT/WRITE MODULE ID.
         BALR  RETURN,RPEWA             DELETE PUT/WRITE ROUTINE.X01004
         TM    0(RMACR),WRITEFLG        BSAM DCB
         BNZ   CHECK                    BRANCH IF YES TO DELETE  Y01004
*                                       CHECK ROUTINE.           Y01004
*****************************************************************CLUP21
*                                                                CLUP21
* FREE DYNAMICALLY ALLOCATED MAIN STORAGE AND CLEAN UP THE DEB   CLUP21
*                                                                CLUP21
*****************************************************************CLUP21
FREEWA   EQU   *
         XC    DCBREAD+1(3),DCBREAD+1
         L     RZERO,DCBPECB            ADDRESS OF PECB          Y02027
         LA    RPENTRY,PECBSIZE         SIZE OF PECB             Y02027
         BAL   RETURN,BLDLIST           FREE ACCESS METHOD WORKAREA
         ICM   RZERO,AD,DEBLCMWA        GET LOCATE MODE W/A    @OY16374
         BZ    FREEDEB                  BR, NOT LOCATE MODE    @OY16374
         LH    RPENTRY,DEBSOWA          SIZE OF LOCATE MODE      Y02027
*                                       WORKAREA                 Y02027
         BAL   RETURN,BLDLIST           FREE LOCATE MODE WORKAREA
FREEDEB  EQU   *
         MVI   DEBTAMID,NULL                                     CLUP21
         XC    DEBLCMWA(3),DEBLCMWA
         XC    DEBPCBAD(3),DEBPCBAD     ZERO PCB PTR IN DEB      Y02027
         XC    DEBPEBAD(3),DEBPEBAD     ZERO PEB ADDR IN DEB     Y02027
         XC    DEBPEWA(3),DEBPEWA       ZERO PEWA ADDR IN DEB    Y02027
         B     EXITTST                  PREPARE TO EXIT
*****************************************************************CLUP21
*                                                                CLUP21
* THIS SUBROUTINE SETS UP A PARAMETER LIST TO BE USED FOR        CLUP21
*  DEALLOCATION OF MAIN STORAGE ALLOCATED AT OPEN TIME           CLUP21
*                                                                CLUP21
*****************************************************************CLUP21
BLDLIST  EQU   *
         LR    RPARM,RZERO              FREEMAIN ARB ADDRESS     Y02027
         LA    RPARM,0(0,RPARM)         CLEAR HIGH ORDER BYTE    Y02027
         LTR   RPARM,RPARM              AREA TO BE FREED         Y02027
         BCR   8,RETURN                 EXIT IF NO
         LA    RZERO,0(0,RPENTRY)       FREEMAIN WORKAREA SIZE   Y02027
         O     RZERO,POOL               SET SUBPOOL VALUE IN REG
         L     RTCB,DEBTCBAD-ONE        GET TCB ADDRESS          Y02027
         MODESET  EXTKEY=RBT234,WORKREG=4
         FREEMAIN R,LV=(0),A=(1)
         MODESET  EXTKEY=ZERO
         BR    RETURN                   RETURN TO CALLER
         EJECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *CLUP21
*                                                                     *
* SET UP TO TRANSFER CONTROL TO THE NEXT LOAD OF OPEN            CLUP21
*                                                                     *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *CLUP21
EXITTST  EQU   *
         MODESET  KEYADDR=DXUKEY,WORKREG=15                      Y02027
*   RDCB=COPIED DCB, RENTRY=REAL DCB, RWORK=LEN OF PREFIX        Y02027
         L     RENTRY,DXUDCBAD          LOAD REAL DCB ADDR       Y02027
         SR    RWORK,RWORK              ZERO REG                 Y02027
         IC    RWORK,DXUDCBPL+ONE       LEN OF NULL PREFIX       Y02027
         LA    RENTRY,ZERO(RWORK,RENTRY) PT PAST NULL PREFIX REALY02027
         AH    RDCB,DXUDCBPL            PT PAST NULL PREFIX COPY Y02027
         IC    RWORK,DXUDCBML+ONE       LEN OF DCB TO MOVE       Y02027
         BCTR  RWORK,ZERO               DEC FOR MOVE             Y02027
         EX    RWORK,DCBMOVE            MOVE COPIED DCB TO REAL  Y02027
         SH    RDCB,DXUDCBPL            POINT TO PREFIX          Y02027
         MODESET  EXTKEY=ZERO                                  @ZA02601
         TM    DXCCW1,AVTCLOSN          IS IT CLOSEDOWN        @ZA04027
         BNO   NOTCLSDN                 BR, NO                 @ZA04027
         MVI   DXCCW1,ZERO              RESET FLAGS            @ZA04027
         L     RENTRY,CVTPTR            LOAD CVT ADDR            Y02027
         LA    RENTRY,CVTBRET-CVT(,RENTRY) LOAD ADDR OF BR 14    Y02027
         ST    RENTRY,DXCCW2            STORE ERRET ADDR IN LIST Y02027
         SR    RZERO,RZERO              CLEAR RETURN CODE.       Y02027
         LA    RPARM,DXCCW1             LOAD ADDR OF PARM LIST   Y02027
         POST ,(0),MF=(E,(1))           POST OP CTL CCOMPETE.    Y02027
NOTCLSDN EQU   *                                                 Y02027
         XC    NULL(TWO,RWTGC),NULL(RWTGC) CLEAR ID FOR THIS DCB.Y02027
         ICM   RENTRY,ALL,DXCCW12       AIB TO FREE?           @OZ18749
         BZ    NOAIB                    BR NO                  @OZ18749
         MVI   AIBSTATE-IEDQAIB(RENTRY),AIBFREE  FREE AIB      @OZ18749
NOAIB    EQU   *                                               @OZ18749
LOOP     EQU   *
         LA    RWTGC,EIGHT(RWTGC)       BUMP TO NEXT ENTRY IN
*                                       WTG TABLE
         LA    RPARC,FOUR(RPARC)        BUMP TO NEXT ENTRY IN
*                                       DCB LIST
         CLC   NULL(TWO,RWTGC),MODID    ANOTHER TCAM DCB TO CLOSE
         BE    START                    BRANCH IF YES TO RESTART
*                                       THIS EXECUTOR
         CLC   NULL(TWO,RWTGC),OSID     END OF WTG TABLE
         BL      LOOP                   BRANCH IF NO
*
         CLC   NULL(TWO,RWTGC),OSIDH
         BH    LOOP                     CHECK NEXT ENTRY         S21903
         LR    RPARC,RPAR               RESET POINTER TO BEGINNING
*                                       OF DCB PARAMETER LIST
         LA    RWTGC,C32(RWTG)          RESET POINTER TO BEGINNING
*                                       OF WTG TABLE
LABEL    EQU   *
         CLI   0(RWTGC),NULL            THIS DCB PROCESSED       CLUP21
         BNE   XCTLRTN                  GO TO RETURN             S21903
         LA    RWTGC,EIGHT(,RWTGC)
         LA    RPARC,FOUR(,RPARC)
         B     LABEL                    GO TO CHECK NEXT DCB     S21903
XCTLRTN  EQU   *
         LA    RENTRY,DXCCW12           GET ADDRESS OF A WORKAREA
*                                       FOR USE BY XCTL SVC
         MVC   SIX(TWO,RWTG),NULL(RWTGC) MOVE EXECUTOR ID TO THE
*                                       PARAMETER LIST
         MVC   C14(THREE,RWTG),TWO(RWTGC) MOVE THE TTR TO THE
*                                       PARAMETER LIST
         MODESET   EXTKEY=DATAMGT       BACK TO KEY 5            Y02027
         XCTL  DE=(RWTG),SF=(E,(15))
DCBMOVE  MVC   ZERO(ZERO,RENTRY),ZERO(RDCB)  MOVE COPIED DCB     Y02027
*                                       TO REAL DCB              Y02027
         EJECT
*                                                                     *
*              C O N S T A N T S    A N D    E Q U A T E S            *
*                                                                     *
MODID    DC    C'46'                    ID OF THIS CLOSE EXECUTOR
OSID     DC    C'0B'                    ID OF O/S CLOSE EXECUTOR
OSIDH    DC    C'0G'                    CONSTANT                 S21903
FOURK    DC    H'4096'                  LENGTH OF A PAGE       @OZ17630
MNAME    DC    CL8'IGG019R '            CONSTANT                 S21903
KEY5     DC    X'50'                    KEY 5.                   Y02027
KEY6     DC    X'60'                    KEY 6.                   Y02027
SPCSANFP DC    AL1(241)                 USER KEY IN CSA,NOT FTCHPY02027
         DS    0F                       BOUNDARY ALINEMENT FOR
*                                       NEXT INSTRUCTION-POOL
POOL     DC    X'FB000000'              SUBPOOL 251              Y02027
TERM     EQU   X'C0'                    TCB TERMINATE FLAGS    @ZA01439
NULL     EQU   0
ONE      EQU   1                        DISPLACEMENT             Y02027
TWO      EQU   2
EIGHT    EQU   8
FOUR     EQU   4
NINE     EQU   9                        DISPLACEMENT             Y02027
C32      EQU   32
FIVE     EQU   5                                                 X01004
SIX      EQU   6
SEVEN    EQU   7                        3-BYTE ICM/STCM MASK.    X01004
ELEVEN   EQU   11                                                X01004
TWELVE   EQU   12                                                X01004
C14      EQU   14
ALL      EQU   15                       MASK TO ICM/STCM 4 BYTES Y01004
RG       EQU   C'G'
RL       EQU   C'L'
RM       EQU   C'M'
RI       EQU   C'I'
POINTFLG EQU   X'04'                    DCBMACR FLAG (POINT)
LOCMFLG  EQU   X'08'                    DCBMACR FLAG (LOCATE MODE)
OUT      EQU   X'00'                    DCBMACR FLAG(PUT OR WRITE)
MOVER    EQU   X'8C'                    AQCTL ACTION CODE FOR-   Y02027
*                                       POST TO DISABLED QUEUE.  Y02027
FLGU     EQU   X'80'                    RECFM NOT SPECIFIED      Y02027
FF       EQU   X'FF'                    ALL BITS ON.             Y02027
DEBID    EQU   X'0F'                    TCAM DEB IDENTIFIER.     Y02027
ZERO     EQU   0                        CONSTANT OF ZERO.        Y02027
AD       EQU   7                        ICM MASK FOR ADDRS.      Y02027
FREETYPE EQU   X'20'                    FREEMAIN TYPE INDICATOR. Y02027
ABND     EQU   X'80'                    TCB FLAG FOR ABEND
THREE    EQU   3
CSA      EQU   231                      COMMON SUBPOOL         @ZA02601
HPAGE    EQU   2048                     HALF PAGE SIZE         @ZA02601
KEY6X    EQU   6                        KEY 6                  @ZA02601
CLRLOW   DS    0F                       MASK USED TO           @ZA02601
         DC    X'FFFFF000'              CLEAR 3 LOW BYTES      @ZA02601
CLOS     DC    C'CLOS'                  CLOSE ID               @ZA02601
READFLG  EQU   X'20'
CLOSEPRI EQU   X'C0'                    CLOSE ELEMENT PRIORITY.  X01004
WRITEFLG EQU   READFLG
         EJECT
LAST     EQU   *                        END OF CODE/CONSTANTS    A47157
         DC    XL50'00'                           PATCH AREA.    Y02027
         EJECT
*                                                                     *
*                   D  S  E  C  T  S                                  *
*                                                                     *
         IHAPSA                                                  Y02027
          IHAASCB                                              @ZA01439
          EJECT
         DCBD  DSORG=PS                 DATA CONTROL BLOCK
DCBPECB  EQU   DCBIOBAD                                          Y02027
         TPECBD                                                  Y02027
         TAVTD                          ADDRESS VECTOR TABLE
         TTCBD                          TASK CONTROL BLOCK
FORCORE  DSECT
         IECDSECT
         TDEBAPD
         TPEWAD
         TRECBD
*****************************************************************Y02027
*****    RCB EXTENSION FOR TCAM APPLICATION OPEN/CLOSE.          Y02027
*****************************************************************Y02027
RCBPECB  DS    F                        APPLIC. PGM PECB ADDR.   Y02027
RCBASCB  DS    F                        APPLIC. PGM ASCB ADDR.   Y02027
RCBERRET DS    0F                       ERROR RTN ADDR           Y02027
RCBASID  DS    F                        APPLIC. PGM ASID ADDR.   Y02027
RCBTCB   DS    F                        APPLIC. PGM TCB ADDR.    Y02027
RCBDCBMA DS    H                        DCBMACRF FROM APPLIC.    Y02027
RCBTCBFL DS    0X                       APPLIC. TCB FLAGS.       Y02027
RCBDCBDS DS    H                        DCBDSORG FROM APPLIC.    Y02027
RCBDSNM  DS    CL8                      PROCESS QUEUE NAME.      Y02027
RCBDEBAD DS    F                        DEB ADDR FOR APPLIC. DCB.Y02027
RCBDCBBU DS    H                        DCBBUFL FROM APPLIC.     Y02027
RCBDCBLR DS    H                        DCBLRECL FROM APPLIC.    Y02027
RCBDCBOP DS    X                        DCBOPTCD FROM APPLIC.    Y02027
RCBDCBRF DS    X                        DCBRECFM FROM APPLICATIONY02027
RCBDCBF1 DS    X                        DCBFLAG1 FROM APPLICATIONY02027
RCBERR   DS    X                        ERROR INDICATOR          Y02027
RCBJOBNM DS    CL8                      JOBNAME                  Y02027
RCBSAVE  DS    18F                      SAVE AREA FOR IEDQEU   @ZA02601
RCBEND   DS    0X                       END OF DSECT.            Y02027
         ORG   RCBTCB
RCBDEBTA DS    H                        OFFSET IN T.T. FOR ENTRY.Y02027
RCBOPECB DS    0AL3                     OPCTL ECB ADDRESS        Y02027
RCBDEBPW DS    AL3                      ADDR. OF PEWA.           Y02027
RCBDEBPC DS    AL3                      ADDR. OF PCB.            Y02027
         DS    2F                       QNAME                    Y02027
RCBSQ    DS    0X                       SECURE QUEUE FLAG        Y02027
RCBAVTBT DS    X                        AVTBIT1 ON RETURN FROM EUY02027
RCBPEBAD DS    AL3                      ADDR OF PEB IN CSA.      Y02027
RCBSIZE  EQU   RCBEND-RECBKEY           SIZE OF DSECT.           Y02027
         TPRIOR
         IHARB                                                   Y02027
         TPEBD                                                   Y02027
         TTCXD                                                   Y02027
CVT      DSECT                                                   Y02027
         CVT                                                     Y02027
         EJECT                                                 @ZA02601
         TAIBD                                                 @ZA02601
         END
