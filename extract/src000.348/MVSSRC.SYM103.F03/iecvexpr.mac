EXPR   TITLE 'IECVEXPR EXCP PURGE, RESTORE AND FRR ROUTINES - PROLOGUE'
IECVEXPR CSECT
***********************************************************************
*                                                                     *
* MODULE NAME = IECVEXPR                                              *
*                                                                     *
* DESCRIPTIVE NAME = EXCP PURGE RESTORE AND FRR ROUTINES              *
*                                                                     *
* COPYRIGHT = NONE                                                    *
*                                                                     *
* STATUS --                                                           *
*    CHANGE LEVEL IS 0                                                *
*                                                                     *
* FUNCTION --                                                         *
*    CONTAINED IN THIS MODULE ARE THREE SUBROUTINES. THEY ARE THE     *
*    PURGE SUBROUTINE, THE RESTORE SUBROUTINE AND THE FUNCTIONAL      *
*    RECOVERY ROUTINE FOR EXCP. EACH ROUTINE IS DESCRIBED IN DETAIL   *
*    IN THE HEADER FOR THE SPECIFIC SUBROUTINE.                       *
*    THE ROUTINE ALSO BUILDS A RESTORE CHAIN ON PURGE OPERATIONS      *
*                                                                     *
* NOTES = NONE                                                 @YM07015
*                                                              @YM07015
*    DEPENDENCIES = NONE                                       @YM07015
*                                                              @YM07015
*    RESTRICTIONS = NONE                                       @YM07015
*                                                              @YM07015
*    REGISTER CONVENTIONS = SEE REGISTER EQUATES BELOW         @YM07015
*                                                              @YM07015
*    PATCH-LABEL = PATCH                                       @YM07015
*                                                                     *
* MODULE TYPE = RESOURCE RECOVERY                                     *
*                                                                     *
*    PROCESSOR = ASSEMBLER                                            *
*                                                                     *
*    MODULE SIZE = HEX9C4 BYTES                                @YM07015
*                                                                     *
*    ATTRIBUTES =                                                     *
*      REENTRANT, ENABLED, LOCAL LOCK, SUPERVISOR STATE, KEY 0        *
*                                                                     *
* ENTRY POINTS =                                                      *
*         IECVXPUR - PURGE SUBROUTINE VIA BRANCH FROM SVC 16 OR 33.   *
*         IECVXRES - RESTORE SUBROUTINE VIA BRANCH FROM SVC 17.       *
*         IECVXFRR - FUNCTIONAL RECOVERY ROUTINE VIA BRANCH FROM RTM. *
*         IECVRCHN - RESTORE CHAIN BUILDER SUBROUTINE                 *
*                                                              @YM07015
*    PURPOSE =  IECVXPUR, ENTERED BY IOS NON-RESIDENT PURGE    @YM07015
*               MODULE FOR EITHER A HALT OR QUIESCE OPERATION. @YM07015
*               IECVXRES, ENTERED BY IOS RESTORE MODULE TO     @YM07015
*               RESTORE EACH IOB IN THE RESTORE CHAIN BY       @YM07015
*               ISSUING SVC'S 0,92, OR 114.                    @YM07015
*               IECVXFRR, ENTERED BY R/TM TO HANDLE PROCESS-   @YM07015
*               ING OF AN EXCP ABEND, A PROGRAM CHECK IN AN    @YM07015
*               APPENDAGE OR A PROGRAM CHECK IN AN EXCP PRO-   @YM07015
*               CEDURE.                                        @YM07015
*               IECVRCHN, ENTERED BY IECVXPUR (PURGE) AND EXCP @YM07015
*               TO CREATE OR EXTEND A CHAIN OF IOB'S THAT RE-  @YM07015
*               PRESENT QUIESCED I/O REQUESTS. ENTERED BY EXCP @YM07015
*               OR AN ABE APPENDAGE IF EITHER WANTS AN IOB TO  @YM07015
*               BE ADDED TO THE CHAIN OF IOBS.                 @YM07015
*                                                              @YM07015
*    LINKAGE = NORMAL ENTRY IS VIA BRANCH AND LINK             @YM07015
*                                                                     *
*    INPUT =                                                          *
*    PURGE AND RESTORE --                                             *
*         REGISTER 1 - POINTER TO IPIB ( OR RESTORE PARAMETERS).      *
*         REGISTER 13 - POINTER TO 16 WORD SAVE AREA.                 *
*         REGISTER 14 - RETURN ADDRESS.                               *
*         REGISTER 15 - ENTRY POINT ADDRESS.                          *
*    FRR SUBROUTINE --                                                *
*         REGISTER 0 - 200 BYTE WORK AREA ADDRESS                     *
*         REGISTER 1 - SDWA POINTER                                   *
*         REGISTER 14 - RETURN ADDRESS                                *
*         REGISTER 15 - ENTRY POINT ADDRESS (IECVXFRR)                *
*                                                                     *
*    OUTPUT =                                                         *
*    PURGE SUBROUTINE --                                              *
*         FOR QUIESCE OPTION, A LIST OF IOBS IS PASSED TO BE PLACED   *
*         ON THE PIRL. THE QUIESCE COUNT REFLECTS TOTAL OF            *
*         OUTSTANDING REQUESTS.                                       *
*    RESTORE SUBROUTINE --                                            *
*         SVCS HAVE BEEN ISSUED FOR EACH REQUEST TO BE RESTORED.      *
*    FRR SUBROUTINE --                                                *
*         AN ABEND CODE IS ASSIGNED AND CONTROL BLOCKS ARE            *
*         COPIED TO SUBPOOL 230 FOR PROBLEM DETERMINATION.            *
*                                                                     *
*    EXITS-NORMAL =                                                   *
*    BRANCH BACK VIA REGISTER 14, ALL REGISTERS RESTORED              *
*                                                                     *
*    EXITS-ERROR =                                                    *
*    BRANCH BACK VIA REGISTER 14, ALL REGISTERS RESTORED              *
*                                                                     *
* EXTERNAL REFERENCES =                                               *
*                                                              @YM07015
*    ROUTINES =                                                @YM07015
*       IECVTCCW - CCW TRANSLATOR                              @YM07015
*       IEAPSIB  - PAGE FIX/FREE ROUTINE OF RSM                @YM07015
*       IEAOPT01 - POST ROUTINE                                @YM07015
*       IEAOPT02 - POST ROUTINE                                @YM07015
*       IECVSMGR - STORAGE MANAGER OF IOS                      @YM07015
*                                                              @YM07015
*    DATA-AREAS = SEE TABLES BELOW                             @YM07015
*                                                              @YM07015
*    CONTROL-BLOCKS = SEE TABLES BELOW                         @YM07015
*                                                                     *
* TABLES =                                                            *
*         IOB - INPUT OUTPUT BLOCK - DESCRIBES THE SPECIFIC I/O       *
*               EVENT.                                                *
*         ECB - EVENT CONTROL BLOCK - POSTED AS PURGED.               *
*         DCB - DATA CONTROL BLOCK - DESCRIBES THE DATA SET           *
*         DEB - DATA EXTENT BLOCK - CONTAINS THE RELATED REQUEST QUEUE*
*         TCB - TASK CONTROL BLOCK - DESCRIBES THE REQUESTOR          *
*         IOSB - IOS BLOCK - DESCRIBES A REQUEST TO IOS               *
*         IPIB - IOS PURGE INTERFACE BLOCK - DESCRIPES PURGE REQUEST  *
*         PIRL - PURGE I/O RESTORE LIST                               *
*         TCCW - TRANSLATION CONTROL BLOCK - DESCRIBES REAL CHANNEL   *
*                PROGRAM.                                             *
*                                                                     *
* MACROS = SETLOCK,GETMAIN,FREEMAINMODESET,SETRP                      *
*                                                                     *
* CHANGE ACTIVITY =                                                   *
*    NEW MODULE FOR VS2/2                                             *
*                                                                     *
***********************************************************************
         TITLE 'IECVEXPR EXCP PURGE, RESTORE AND FRR ROUTINES - DSECTS'
         IEZIOB
         EJECT
         IEZDEB
         EJECT
         IKJTCB
         EJECT
         IEZJSCB
         EJECT
         IHARB
         EJECT
         CVT   DSECT=YES
         EJECT
         IHAPSA                                                @ZA12704
         EJECT                                                 @ZA12704
         IHAASCB                                               @ZA12704
         EJECT                                                 @ZA12704
         IHAASXB                                               @ZA12704
         EJECT                                                 @ZA12704
         IHASDWA                                               @ZA12704
         EJECT                                                 @ZA12704
         DCBD  DSORG=PS                                        @ZA12704
         EJECT                                                 @ZA12704
         SPACE 2                                               @ZA12704
         IHASRB                                                @ZA12704
         EJECT                                                 @ZA12704
         IECDIOSB                                              @ZA12704
         EJECT                                                 @ZA12704
         IECDIOCM                                              @ZA12704
         EJECT                                                 @ZA12704
         IECDBEB                                               @ZA12704
         EJECT                                                 @ZA12704
         IECDEPCB                                              @ZA12704
         EJECT                                                 @ZA12704
         IECDIPIB                                              @ZA12704
         EJECT                                                 @ZA12704
         IECDPIRL                                              @ZA12704
         EJECT                                                 @ZA12704
         IECDRQE                                               @ZA12704
         EJECT                                                 @ZA12704
         IECDTCCW                                              @ZA12704
         EJECT                                                 @ZA12704
         IECDXDBA                                              @ZA12704
         EJECT                                                 @ZA12704
         IHAFRRS                                               @ZA12704
         EJECT                                                 @ZA12704
         IECDXFRR                                              @ZA12704
         EJECT                                                 @ZA12704
         IECDSFRR                                              @ZA12704
         SPACE 2                                               @ZA12704
****     SAVE AREA -- EXPR FRR INTERFACE WITH SMGR       ****  @ZA12704
FRREXCPS DS    A                   EXCP FRR PARM AREA PTR      @ZA12704
FRRRRTM  DS    A                   RTM RETURN ADDRESS SAVE     @ZA12704
FRRBASE  DS    A                   EXPR BASE REGISTER SAVE     @ZA12704
         EJECT                                                 @ZA12704
         IHAQVPL                                               @ZA12704
         SPACE 4                                               @ZA12704
QVWORKS  DS    XL(QVPLWASZ)        WORK AREA FOR QVA           @ZA12704
QVSAVE   DS    18F                 SAVE AREA FOR CALLER        @ZA12704
QFRRSDWA DS    F                   SDWA BACK POINTER           @ZA12704
QFRRPARM DS    XL(FRRSLEN)         LENGTH OF SMGR FRR PARM     @ZA12704
******   THE PSEUDO FRR PARM AREA IS CONTAINED IN THE QVPL.    @ZA12704
******   FOLLOWING THE FRR PARM AREA, 3 WORDS ARE USED AS      @ZA12704
******   SAVE AREAS WHEN INTERFACING WITH SMGR.                @ZA12704
         EJECT                                                 @ZA12704
GTM      DSECT
***********************************************************************
*                                                                     *
* GETMAINED WORK AREA - IT DESCRIBES SAVED INFORMATION AND SAVE AREAS *
*                                                                     *
***********************************************************************
         SPACE 2
GTMSAVE  DS    F                   ADDRESS OF REG 13 SAVE AREA
GTMWORK  DS    X                   WORK AREA TO ISOLATE RB TYPE
         DS    AL3                 RESERVED
GTMAREA  DS    22F                 GETMAINED SAVE AREA AND FRR AREA
*                                  USED BY EXCP
GTMSVRA  DS    16F                 SAVE AREA BEFOR TERM RTN
         EJECT
RRQ      DSECT
***********************************************************************
*                                                                     *
*        RELATED REQUEST QUEUE DSECT - THIS QUEUE RESIDES IN LSQA,    *
*        SUBPOOL 254.                                                 *
*                                                                     *
***********************************************************************
         SPACE 2
RRQFIRST DS    A                   ADDRESS OF THE FIRST RQE ON QUEUE
RRQLAST  DS    A                   ADDRESS OF THE LAST RQE ON QUEUE
RRQBL    EQU   *-RRQ               LENGTH OF RRQ
         EJECT
***************************************************************@ZA06070
*                                                             *@ZA06070
*        SDWA VARIABLE AREA DSECT  - DESCRIBES SAVED          *@ZA06070
*        INFORMATION AND SAVE AREAS                           *@ZA06070
*                                                             *@ZA06070
***************************************************************@ZA06070
         SPACE 1
SDWA     DSECT                                                 @ZA06070
         ORG   SDWAVRA             SDWA VARIABLE WORK AREA     @ZA06070
SDWAVABO DS    H                   ORIGINAL ABEND CODE         @ZA06070
SDWAVABN DS    H                   ADJUSTED ABEND CODE         @ZA06070
SDWAVLHI DS    A                   HIGHEST LOCK HELD FROM PSA  @ZA06070
SDWAVFRR DS    (FRRXLEN)XL1        6 WORD FRR AREA             @ZA06070
SDWAVRQE DS    (RQEBL)XL1          ACTIVE RQE                  @ZA06070
SDWAVTOP DS    XL1                 TCCW OPTION BYTE            @ZA06070
SDWAVFLG DS    XL1                 TCCW TRANSLATION FLAGS      @ZA06070
SDWAVIOC DS    XL1                 IOSB COMPLETION CODE        @ZA06070
         DS    XL1                 RESERVED                    @ZA06070
SDWAVASI DS    XL2                 FAILING ASID                @ZA06070
SDWAVLST EQU   *                   LAST ENTRY IN VARIABLE AREA @ZA06070
SDWAVLTH EQU   SDWAVLST-SDWAVRA    LENGTH OF SDWA AREA USED    @ZA06070
 TITLE 'IECVEXPR EXCP PURGE RESTORE AND FRR ROUTINES - GENERAL EQUATES'
***********************************************************************
*                                                                     *
*                   REGISTER DEFINITIONS                              *
*                                                                     *
***********************************************************************
         SPACE 2
REG0     EQU   0                   PARAMETER REGISTER ZERO
REG1     EQU   1                   PARAMETER REGISTER ONE
GTMREG   EQU   8                   FRR WORK AREA ADDRESS REG
BASREG   EQU   9                   BASE REG FOR SUBROUTINES
WKREGA   EQU   10                  UTILITY REGISTER
WKREGB   EQU   11                  UTILITY REGISTER
WKREGC   EQU   12                  UTILITY REGISTER
SAVREG   EQU   13                  SAVE AREA ADDRESS REGISTER
REG14    EQU   14                  RETURN REGISTER
REG15    EQU   15                  ENTRY POINT REGISTER
         SPACE 2
*        REGISTERS SPECIFICALLY FOR PURGE SUBROUTINE
         SPACE 2
IOBREG   EQU   2                   IOB ADDRESS REGISTER
DEBREG   EQU   3                   DEB ADDRESS REGISTER
RQEREG   EQU   4                   RQE ADDRESS REGISTER
RRQREG   EQU   5                   RRQ ADDRESS REGISTER
IOSBRG   EQU   6                   IOSB ADDRESS REGISTER
IPIBRG   EQU   7                   IPIB ADDRESS REGISTER
QUSREG   EQU   10                  QUIESCE LIST REGISTER
         SPACE 2
*        REGISTERS SPECIFICALLY FOR RESTORE
         SPACE 2
LSTREG   EQU   4                   TCB/IOB LIST ADDRESS REG
CHAINRG  EQU   5                   IOB CHAIN ADDRESS REG
TOPREG   EQU   6                   TOP OF TCB LIST ADDRESS SAVE
ASCBREG  EQU   7                   ASCB ADDRESS REGISTER
ECBREG   EQU   7                   ECB ADDRESS REGISTER        @YM08618
         SPACE 2
*        REGISTERS SPECIFICALLY FOR FRR ROUTINE
         SPACE 2
WKREG2   EQU   2                   UTILITY REGISTER
RTMRREG  EQU   3                   RTM RETURN ADDRESS SAVED
SDWAREG  EQU   5                   POINTER TO SDWA
WKREG6   EQU   6                   UTILITY REGISTER
WKREG7   EQU   7                   UTILITY REGISTER
FRRREG   EQU   12                  POINTER TO FRR PARMS
         EJECT
***********************************************************************
*                                                                     *
*                             EQUATES                                 *
*                                                                     *
***********************************************************************
         SPACE 2
ECBFLAG  EQU   0                   OFFSET TO FLAGS IN ECB
ECBPURGE EQU   X'48'               PURGED REQUEST INDICATOR
C0       EQU   0                   CONSTANT OF 0
C1       EQU   1                   CONSTANT OF 1
C2       EQU   2                   CONSTANT OF 2
C3       EQU   3                   CONSTANT OF 3
C4       EQU   4                   CONSTANT OF 4
C6       EQU   6                   CONSTANT OF 6               @ZA16175
C8       EQU   8                   CONSTANT OF 8
C10      EQU   10                  CONSTANT OF 10              @YM07354
C20      EQU   20                  CONSTANT OF 20              @YM06502
C24      EQU   24                  CONSTANT OF 24
C28      EQU   28                  CONSTANT OF 28
C48      EQU   48                  CONSTANT OF 48              @ZA15706
C64      EQU   64                  CONSTANT OF 64              @YM07285
RC16     EQU   16                  RETURN CODE OF 16
RC20     EQU   20                  RETURN CODE OF 20
RC24     EQU   24                  RETURN CODE OF 24
NOP      EQU   X'03'               NOP CCW COMMAND CODE
A0       EQU   160                 CONSTANT OF 160
EXCP     EQU   0                   SVC NUMBER FOR EXCP
EXCPVR   EQU   114                 SVC NUMBER FOR EXCPVR
RESTORE  EQU   92                  SVC NUMBER FOR MEMORY RESTORE
EXCPVRB  EQU   X'F4'               BYTE DEPICTING EXCPVR ENTRY
LORD3BYT EQU   7                   ADDRESS PORTION OF REG
HIORDBYT EQU   8                   HIGH ORDER BYTE OF REG
DEBTBENT EQU   4                   DEB TABLE ENTRY AND HEADER LENGTH
VSAM     EQU   X'01'               VSAM DEB INDICATOR            YM0122
VTAM     EQU   X'82'               VTAM DEB INDICATOR            YM0122
SUBSYS   EQU   X'81'               SUBSYSTEM DEB INDICATOR       YM0122
R13DISP  EQU   52                  DISPLACEMENT TO R13 IN SAVEAREA
FF       EQU   X'FF'               MASK OF ONES
TWO56    EQU   256                 LENGTH FOR CLEARING XDBA
XPRM1    EQU   B'1000'             MASK FOR SP-254             @Y30IPLD
XPRM3    EQU   B'0011'             MASK FOR ICM INSTRUCT.      @YM02855
XPRM7    EQU   B'0111'             MASK FOR ICM INSTRUCT.      @YM05531
ECBPOST  EQU   X'40'               FLAG TO TEST POST IN THE ECB@YM08618
CODE200  EQU   X'0200'             ABEND CODE 200
CODE700  EQU   X'0700'             ABEND CODE 700
CODEA00  EQU   X'0A00'             ABEND CODE A00
CODEB00  EQU   X'0B00'             ABEND CODE B00
CODEE00  EQU   X'0E00'             ABEND CODE E00              @ZA05976
DISPSW   EQU   X'06'               MASK FOR DISABLED PSW       @ZA05976
PPKEY    EQU   X'80'               USED TO CHECK PROTECT KEY.  @ZA26460
         TITLE 'IECVEXPR EXCP PURGE, RESTORE AND FRR ROUTINES - PURGE  *
               INTERFACE ROUTINE'
IECVEXPR CSECT
         ENTRY IECVXPUR,IECVXRES,IECVXFRR,IECVRCHN
         SPACE 4                                               @ZA08162
         DC    CL8'IECVEXPR'                                   @ZA08162
         DC    C'&SYSDATE'                                     @ZA08162
         EJECT
***********************************************************************
*                                                                     *
*                          PURGE SUBROUTINE                           *
*                                                                     *
*        THE PURGE SUBROUTINE ACCEPTS, FROM IOS PURGE, AN IPIB        *
*        WHICH DESCRIBES THE FUNCTIONS WHICH MUST BE PERFORMED. FROM  *
*        THIS BLOCK, THE SUBROUTINE DOES THE FOLLOWING:               *
*           A. DEB PURGE - HALT OPTION - IT FREES RESOURCES FOR EACH  *
*              REQUEST PASSED BY PURGE FROM IOS QUEUES, FOR EACH      *
*              REQUEST ON THE DEB RELATED REQUEST QUEUE (FOR THE 3525 *
*              THE REQUESTS ONLY FOR THIS DEB ARE REMOVED) AND FOR    *
*              EACH REQUEST ON THE AEQ. IF RB PURGE WAS SPECIVIED, THE*
*              RB CHAIN IS SEARCHED FOR IRBS WITH RQES ASSOCIATED WITH*
*              THE DEB. POSTING IS DONE IF REQUESTED.                 *
*           B. DEB PURGE - QUIESCE OPTION - CHAIN THE IOBS ON THE IPIB*
*              WHICH HAVE NOT BEEN IDENTIFIED TO IOS. AT THE SAME TIME*
*              A TABLE IS BUILT IN PROTECTED STORAGE WHICH CONTAINS THE
*              TCB ADDRESS, A POINTER TO ANOTHER TCB LIST AND A LIST OF
*              IOBS WHICH WERE REMOVED BY THE PURGE FOR THAT TCB.     *
*              FOR ALL BUT MEMORY PURGE, THE TCB ADDRESS IS ZERO.     *
*              THE QUIESCE COUNT IS ALSO UPDATED TO REFLECT OUTSTANDING
*              I/O REQUESTS.                                          *
*           C. TCB PURGE - SAME AS DEB PURGE FOR EACH DEB ON THE      *
*              TCB CHAIN.                                             *
*           D. MEMORY PURGE - HALT OPTION - BR 14, IOS PURGE RELEASES *
*              ALL STORAGE OWNED BY IOS. IN DOING THIS, STORAGE IS    *
*              RELEASED FOR EXCP SINCE IT USES THE SAME STORAGE POOLS *
*              AND MANAGER.                                           *
*           E. MEMORY PURGE - QUIESCE OPTION - SAME AS TCB PURGE FOR  *
*              EACH TCB IN THE MEMORY.                                *
*                                                                     *
*        INPUT ON ENTRY IS AS FOLLOWS;                                *
*                                                                     *
*              REGISTER 1  -  ADDRESS OF IPIB                         *
*              REGISTER 13 -  ADDRESS OF EIGHTEEN WORD SAVEAREA       *
*              REGISTER 14 -  RETURN ADDRESS (PURGE)                  *
*              REGISTER 15 -  ENTRY POINT ADDRESS                     *
*                                                                     *
***********************************************************************
         PRINT OFF
*/*IECVXPUR: CHART */
*/* HEADER
*/*                    EXCP PURGE SUBROUTINE
*/*                                                      PAGE # */
*/*IECVXPUR: E ENTRY FROM SVC 16 */
*/* P (+0,SVC16,+4,SVC33) ENTRY POINTS 0 AND 4 FOR SVC 16 AND SVC 33 */
*/*SVC16: P SAVE REGS, ESTABLISH ADDRESSABILITY */
*/* L GETMAIN: GET WORK AREA */
*/* P ZERO THE HEADER AREA */
*/* L SETLOCK: OBTAIN THE LOCAL LOCK */
*/* P ESTABLISH IPIB REGISTER */
*/* D (NO,PURG005,YES,) MEMORY PURGE */
*/* D (YES,PURG900,NO,) HALT MEMORY */
*/* P ZERO SEARCH ARGUMENT SINCE MEMORY */
*/* P (,PURG015) ZERO INCR INTO RQE FOR SEARCHED FLD */
*/*PURG005: P GET SEARCH ARGUMENT FROM IPIB */
*/* D (NO,PURG010,YES,) TCB PURGE REQUEST */
*/* P (,PURG015) CALCULATE OFFSET INTO RQE FOR TCB POINTER */
*/*PURG010: P CALCULATE OFFSET INTO RQE FOR DEB POINTER */
*/* D (NO,PURG900,YES,) DEB AN EXCP DEB */
*/*PURG015: P LOAD POINTER TO AEQ VIA ASCB AND ASXB */
*/* P GET FIRST RQE FROM AEQ */
*/*PURG020: D (YES,PURG060,NO,) AEQ EMPTY */
*/*PURG025: D (YES,PURG030,NO,) MEMORY PURGE */
*/* D (NO,PURG035,YES,) SEARCH ARG MATCH SEARCHED FIELD */
*/*PURG030: D (YES,PURG040,NO,) HALT OPTION */
*/* P INCREMENT QUIESCE COUNT BY ONE */
*/*PURG035: D (YES,PURG060,NO,) END OF AEQ CHAIN */
*/* P SAVE BACK LINK POINTER */
*/* P (,PURG020) POINT TO NEXT RQE */
*/*PURG040: D (NO,PURG045,YES,) FIRST RQE ON THE QUEUE */
*/* P MOVE CHAIN FIELD TO AEQ FIRST ENTRY POINTER */
*/* D (NO,PURG050,YES,) LAST ON QUEUE TOO */
*/* P (,PURG050) SHOW THE CHAIN AS EMPTY */
*/*PURG045: P MOVE CHAIN TO PREVIOUS RQE */
*/* D (NO,PURG050,YES,) THIS RQE LAST IN QUEUE */
*/* P STORE NEW END OF CHAIN */
*/*PURG050: D (NO,PURG055,YES,) POST OPTION SPECIFIED */
*/* S PURGPOST: POST THE ECB PURGED */
*/*PURG055: S (,PURG035) PURGFREE: FREE THE RQE */
*/*PURG060: D (NO,PURG113,YES,) RB PURGE SPECIFIED */
*/* D (YES,PURG065,NO,) MEMORY PURGE */
*/* D (YES,PURG070,NO,) TCB PURGE */
*/* P (,PURG080) LOAD DEB AND TCB ADDRESS */
*/*PURG065: P (,PURG075) LOAD POINTER TO FIRST TCB ON CHAIN */
*/*PURG070: P LOAD TCB POINTER FROM IPIB */
*/*PURG075: P ZERO SEARCH ARGUMENT REGISTER */
*/*PURG080: P POINT TO TOP RB, SAVE TCB ADDRESS */
*/*PURG085: D (YES,PURG110,NO,) END OF RB CHAIN */
*/* P ISOLATE THE RB TYPE FLAGS */
*/* D (NO,PURG095,YES,) IS THIS AN IRB */
*/* D (NO,PURG095,YES,) RQE ATTACHED TO IRB */
*/* P LOAD THE ADDRESS OF THE RQE FROM IRB */
*/* D (YES,PURG090,NO,) IS SEARCH ARGUMENT ZERO */
*/* D (NO,PURG095,YES,) RQE FOR THIS DEB */
*/*PURG090: D (YES,PURG100,NO,) HALT IO OPTION */
*/* P ADD ONE TO THE QUIESCE COUNT */
*/*PURG095: P (,PURG085) LOAD POINTER TO NEXT RB */
*/*PURG100: P STORE POINTER TO SVC 3 IN RB */
*/* P ZERO RQE FIELD IN IRB */
*/* D (NO,PURG105,YES,) POST OPTION REQUESTED */
*/* S PURGPOST: POST RQE AS PURGED */
*/*PURG105: S (,PURG095) PURGFREE: FREE THE RQE */
*/*PURG110: D (NO,PURG120,YES,) MEMORY PURGE */
*/* D (YES,PURG115,NO,) LAST TCB IN CHAIN */
*/* P (,PURG080) LOAD POINTER TO NEXT TCB */
*/*PURG113: D (NO,PURG120,YES,) MEMORY PURGE */
*/*PURG115: P (,PURG125) LOAD TOP TCB ON QUEUE */
*/*PURG120: D (NO,PURG130,YES,) TCB PURGE */
*/*PURG125: P LOAD POINTER TO DEB TABLE FROM JSTCB */
*/* P CALCULATE END OF DEB TABLE */
*/*PURG127: P INCREMENT BY AN ENTRY */
*/* D (YES,PURG170,NO,) END OF TABLE */
*/* D (YES,PURG127,NO,) DUMMY ENTRY */
*/* P LOAD POINTER TO DEB FROM TABLE */
*/* D (NO,PURG127,YES,) DEB AN EXCP DEB */
*/*PURG130: P LOAD POINTER TO DEB RRQ */
*/* D (NO,PURG140,YES,) IS THE FIELD ZERO */
*/*PURG135: D (DEB,PURG170,TCB,PURG127,MEM,) TYPE PURGE */
*/* P LOAD POINTER TO NEXT DEB ON CHAIN */
*/* D (NO,PURG130,YES,) END OF CHAIN */
*/* D (NO,PURG170,YES,) MEMORY PURGE */
*/* D (YES,PURG170,NO,) LAST TCB IN CHAIN */
*/* P (,PURG125) LOAD POINTER TO NEXT TCB */
*/*PURG140: P LOAD POINTER TO FIRST RQE IN QUEUE */
*/*PURG145: D (YES,PURG135,NO,) END OF CHAIN */
*/* D (NO,PURG147,YES,) TCB PURGE */
*/* D (NO,PURG148,YES,) TCB ADDRESSES MATCH */
*/*PURG147: D (NO,PURG150,YES,) IS THIS RQE SCHEDLD */
*/* P STORE IPIB PTR IN RQE */
*/*PURG148: P (,PURG145) INCREMENT TO NEXT RQE */
*/*PURG150: D (YES,PURG155,NO,) HALT PURGE */
*/* L IECVRCHN: PLACE ENTRY ON RESTORE CHAIN AND EPCB */
*/*PURG155: D (YES,PURG160,NO,) POST OPTION */
*/* P SET NO POST BIT IN RQE */
*/*PURG160: P SET 48 INTO COMPLETION CODE IN IOB */
*/* D (NO,PURG165,YES,) EXCPVR REQUEST */
*/* P SAVE INDICATION OF EXCPVR ACROSS FREE */
*/*PURG165: L IECVTERM: FREE RQE AND RELATED STORAGE */
*/* D (YES,PURG135,NO,PURG150) END OF RQE CHAIN */
*/*PURG170: D (NO,PURG900,YES,) HALT OPTION */
*/* P LOAD POINTER TO CHAIN OF SRBS,MAKE CHAIN IN IPIB ZEROS */
*/*PURG175: D (NO,PURG900,YES,) ANY IN CHAIN */
*/* P LOAD POINTER TO IOSB */
*/* D (YES,PURG180,NO,) IS THIS AN EXCP EXCPVR REQUEST */
*/* P STORE TO GIVE BACK TO NEXT DRIVER */
*/* P (,PURG175) LOAD FORWARD LINK POINTER */
*/*PURG180: P LOAD FORWARD LINK POINTER */
*/* P LOAD ADDRESS OF RQE */
*/*PURG185: L (,PURG175) IECVTERM: RELEASE STORAGE FOR THIS REQUEST */
*/*PURG900: L SETLOCK: RELEASE LOCAL LOCK */
*/* P FREE SAVE AREA CORE */
*/* P RESTORE REGISTERS FOR PURGE */
*/* R RETURN TO PURGE */
*/*PURGPOST: E POST INTERFACE ROUTINE */
*/* P INDICATE POST CODE OF 48 */
*/* P LOAD POINTER TO ECB TO BE POSTED */
*/* L POST: POST THE ECB COMPLETE */
*/* R RETURN TO MAIN FLOW */
*/*PURGFREE: E FREE RQE ROUTINE */
*/* P INITIALIZE POINTER TO RQE */
*/* L IECVX025: FREE THE RQE */
*/* P RESTORE ORIGINAL REGISTERS */
*/* R RETURN TO MAIN ROUTINE */
*/*SVC33: E SVC33 ENTRY POINT */
*/* P LOAD RQE ADR FROM IOSB */
*/* D (NO,HALT16,YES,) VIRTUAL REQUEST */
*/* D (NO,HALT20,YES,) REQUEST STARTABLE */
*/* P CALCULATE CCW ADDR FROM OFFSET IN IOB */
*/* L ((BAD,HALT24,GOOD,) IECVTCCW: EXECUTE SINGLE ADR XLATE FUNCTION
*/**/
*/* P SET THE REAL CCW TO A NOP */
*/* R RETURN TO SVC33 */
*/*HALT16: R RETURN WITH RC=16 */
*/*HALT20: R RETURN WITH RC=20 */
*/*HALT24: R RETURN WITH RC=24 */
*/*IECVXPUR: END */
         PRINT ON
         SPACE 2
***********************************************************************
*                                                                     *
* PURGE ENTRY - CHECKS IPIB FOR THE WORK TO BE DONE AND MANAGES THE   *
* PURGE FOR THE VARIOUS OPTIONS (IE. DEB, TCB OR MEMORY PURGE).       *
*                                                                     *
***********************************************************************
         SPACE 2
         USING FLC,REG0            ADDRESSABILITY TO FIXED LOW CORE
         USING IECVXPUR,REG15      ADDRESSABILITY FOR ENTRY
IECVXPUR B     SVC16               PURGE SVC ENTRY POINT
         B     SVC33               IO HALT ENTRY POINT
         DROP  REG15                                            @YM3145
         SPACE 2
SVC16    STM   REG0,REG15,C0(SAVREG) SAVE PURGES REGISTERS
         BALR  BASREG,0            ESTABLISH BASE FOR PURGE
PURBASE  EQU   *                   PURGE SUBROUTINE ADDRESSING  @YM3145
         USING *,BASREG            ADDRESSABILITY FOR PURGE
         LR    IPIBRG,REG1         PLACE IPIB IN ITS BASE REGISTER
         USING IPIB,IPIBRG         ADDRESSABILITY FOR IPIB
         SPACE 2
         GETMAIN R,LV=160,SP=254   GET CORE FOR WORK AREA      @ZA09412
         LR    GTMREG,REG1         SET UP BASE REG FOR AREA
         USING GTM,GTMREG          ADDRESSABILITY TO GTM WORK AREA
         XC    C0(C28,GTMREG),C0(GTMREG) ZERO THE PRIMARY AREA
         ST    SAVREG,GTMSAVE      SAVE SAVE AREA ADDRESS
PUR000   EQU   *                   OBTAIN THE LOCAL LOCK
         SETLOCK OBTAIN,TYPE=LOCAL,MODE=UNCOND,                  YM1301*
               RELATED=(IECVEXPR,PURG900)                     @Y30IPLB
         EJECT
***********************************************************************
*                                                                     *
*        THE FOLLOWING CODE LOOPS THROUGH THE ASYNCHRONOUS EXIT       *
*        QUEUE AND PURGES ACCORDING TO THE DIRECTIONS PASSED IN       *
*        THE IPIB BY PURGE.                                           *
*                                                                     *
***********************************************************************
         SPACE 2
         L     WKREGC,PSAAOLD      LOAD ASCB PTR TO GET TO ASXB@YM05193
         USING ASCB,WKREGC         ADDRESSABILITY TO ASCB      @YM05501
         L     WKREGC,ASCBASXB     LOAD ASXB PTR TO GET TO AEQ @YM05501
         USING ASXB,WKREGC         ADDRESSABILITY TO ASXB      @YM05501
         SPACE
         TM    IPIBOPT,IPIBMEM     IS THIS A MEMORY PURGE      @YM05193
         BZ    PURG005             NO, CHECK TCB OR DEB        @Y30IPLB
         TM    IPIBOPT,IPIBHALT    HALT OPTION FOR MEMORY      @YM05193
         BO    PURG900             RETURN TO PURGE-NOT HANDLING@YM05193
         SLR   REG0,REG0           ZERO SRCH ARGMNT FOR AEQ SRCH
         SLR   WKREGB,WKREGB       ZERO INCR INTO RQE FOR SEARCHED FLD
         B     PURG015             HANDLE AEQ PURGE
         SPACE
PURG005  TM    IPIBOPT,IPIBRBP     RB PURGE SPECIFIED          @Y30IPLB
         BZ    PURG113             NO, SKIP THIS SEC OF PURGE  @YM05501
         SPACE
         L     REG0,IPIBARG        LOAD POINTER TO ARGUMENT    @Y30IPLB
         TM    IPIBOPT,IPIBTASK    IS THIS A TCB PURGE
         BZ    PURG010             NO, MUST BE A DEB PURGE
         LA    WKREGB,RQETCB-RQE   CALCULATE OFFSET TO TCB IN RQE
         B     PURG017             HANDLE AEQ PURGE            @YM05193
         SPACE 2
PURG010  LA    WKREGB,RQEDEB-RQE   CALCULATE OFFSET TO DEB IN RQE
         CLI   IPIBARG,C0          HIGH ORDER BYTE SAYS EXCP     YM0122
         BE    PURG017             IS EXCP, CONTINUE           @YM05193
         CLI   IPIBARG,IOSXCPID    EXCP DRIVER ID                YM0122
         BNE   PURG900             NOT EXCP, BYPASS PURGE        YM0122
         ICM   REG0,HIORDBYT,ZEROS SET HIGH BYTE TO ZEROS        YM0122
         SPACE 2
PURG015  TM    IPIBOPT,IPIBRBP     RB PURGE SPECIFIED?         @YM05193
         BZ    PURG113             NO, SKIP THIS SECTION       @YM05193
         SPACE
PURG017  L     RQEREG,ASXBFRQE     LOAD FIRST RQE FROM AEQ     @YM05501
         USING RQE,RQEREG          ADDRESSABILITY TO RQE
PURG020  LTR   RQEREG,RQEREG       IS QUEUE EMPTY               @YM1743
         BZ    PURG060             YES, END OF AEQ PURGE        @YM1743
         SPACE
         LTR   REG0,REG0           DUMMY SEARCH ARGUMENT       @Y30IPLB
         BZ    PURG030             YES, SKIP CHECK OF ARGUMENT
         C     REG0,C0(RQEREG,WKREGB) SEARCH ARG MATCH SEARCH FIELD
         BNE   PURG035             NO, CONTINUE PURGE CHECK
         SPACE 2
*        HAVE NOW FOUND AN ELEMENT REQUIRING PURGE. NOW THE PROCESS
*        OF CHECKING WHAT TO DO AN DOING IT MUST HAPPEN.
         SPACE 1
PURG030  TM    IPIBOPT,IPIBHALT    WAS HALT OPTION REQUESTED
         BO    PURG040             YES, HANDLE THE HALT OPTION
         BAL   REG15,PURGCNT       PURGE QUIESCE-INCR IPIB CNT @ZA20559
PURG035  C     RQEREG,ASXBLRQE     END OF LIST
         BE    PURG060             YES, GO ON TO RB PURGE
         LR    IOBREG,RQEREG       SAVE BACK LINK POINTER IF HALT
         L     RQEREG,RQENRQE      LOAD POINTER TO NEXT RQE
         B     PURG020             LOOK AT THIS RQE
         SPACE 2
PURG040  C     RQEREG,ASXBFRQE     IS THIS THE FIRST RQE ON QUEUE
         BNE   PURG045             NO, USE BACK LINK IN IOBREG
         MVC   ASXBFRQE,RQENRQE    MOVE CHAIN FIELD TO AEQ FIRST
         LA    IOBREG,ASXBFRQE-(RQENRQE-RQE) POINT TO OFFSET INTO ASXB
         C     RQEREG,ASXBLRQE     IS THE RQE THE LAST ONE TOO
         BNE   PURG050             NO, DONE WITH DEQUEUE
         MVC   ASXBLRQE,ASXBFRQE   SHOW THE CHAIN AS EMPTY
         SLR   IOBREG,IOBREG       INSURE SCAN TERMINATION      @YM1743
         B     PURG050             DEQUEUE COMPLETE,NOW GET RID OF RQE
         SPACE 2
PURG045  MVC   RQENRQE-RQE(C4,IOBREG),RQENRQE MOVE THIS CHAIN TO BACK
*                                  LINK POINTER
         C     RQEREG,ASXBLRQE     IS THE RQE THE LAST ONE IN QUEUE
         BNE   PURG050             NO, DONE WITH DEQUEUE
         ST    IOBREG,ASXBLRQE     SHOW NEW END OF CHAIN
         SPACE 2
PURG050  TM    IPIBOPT,IPIBPOST    POST OPTION SPECIFIED
         BZ    PURG055             NO SKIP THE POST FUNCTION
         BAL   REG14,PURGPOST      POST THE ECB AS PURGED
PURG055  BAL   REG14,PURGFREE      FREE THE RQE ALSO
         LR    RQEREG,IOBREG       PLACE PREVIOUS RQE ADDR IN RQE
         B     PURG035             RETURN TO CHECK AGAIN
         EJECT
***********************************************************************
*                                                                     *
*        THIS SECTION CHECKS FOR RB PURGE AND , IF SPECIFIED          *
*        CHAINS THROUGH THE RBS FOR THE TCB SPECIFIED, OR ALL         *
*        TCBS FOR MEMORY PURGE, LOOKING FOR IRBS WHICH CONTAIN        *
*        RQES.                                                        *
*                                                                     *
***********************************************************************
         SPACE 2
PURG060  EQU   *                   RB PURGE ENTRY
         SLR   REG1,REG1           ZERO A REGISTER              @YM1875
         SLR   DEBREG,DEBREG       ZERO DEB ARGUMENT REG        @YM1875
         TM    IPIBOPT,IPIBMEM     IS THIS A MEMORY PURGE
         BO    PURG065             YES, SET POINTER TO TOP TCB
         TM    IPIBOPT,IPIBTASK    IS THIS A TCB PURGE
         BO    PURG070             YES, SET POINTER TO THIS TCB
         ICM   DEBREG,LORD3BYT,IPIBARG+C1  LOAD DEB REGISTER    @YM1875
*                                  WITH DEB BEING PURGED
         USING DEBBASIC,DEBREG     ADDRESSABILITY TO DEB
         ICM   REG1,LORD3BYT,DEBTCBAD+C1 LOAD POINTER TO TCB    @YM1875
         B     PURG080             BRANCH TO COMMON RB PURGE ROUTINE
         SPACE 2
PURG065  L   REG1,ASXBFTCB         LOAD POINTER TO FIRST
*                                  * TCB ON CHAIN
         B     PURG080             BRANCH TO ZERO ARGUMENT     @Y30IPLB
         SPACE 2
PURG070  ICM   REG1,LORD3BYT,IPIBARG+C1 LOAD TCB POINTER FROM   @YM1875
*                                  * IPIB
         USING TCB,REG1            ADDRESSABILITY TO TCB
         SPACE 2
PURG080  ICM   WKREGB,LORD3BYT,TCBRBP+C1  POINT TO TOP RB ON    @YM1875
*                                  * THE CHAIN                  @YM1875
         USING RBBASIC,WKREGB      ADDRESSABILITY
         SPACE 2
PURG085  CR    REG1,WKREGB         END OF RB CHAIN IS PTR TO TCB YM0122
         BE    PURG110             THIS WAS THE END OF THE CHAIN
         MVC   GTMWORK,RBSTAB1     ISOLATE THE RB TYPE FIELD
         NI    GTMWORK,RBFTP       ZERO BITS WHICH DO NOT APPLY
         CLI   GTMWORK,RBFTIRB     IS THIS AN IRB
         BNE   PURG095             NO, LOOK AT NEXT RB IN CHAIN
         TM    RBSTAB2,RBIQENR     TEST FOR IQE                  YM1099
         BNZ   PURG095             YES, LOOK AT NEXT RB IN CHAIN YM1099
         ICM   RQEREG,LORD3BYT,RBIQEA+C1  LOAD ADDRESS OF THE   @YM1875
*                                  * RQE FROM THE RB            @YM1875
         LTR   DEBREG,DEBREG       IS SEARCH ARGUMENT ZERO
         BZ    PURG090             YES,DONT CHECK THE DEB
         C     DEBREG,RQEDEB       IS RQE FOR THIS DEB
         BNE   PURG095             NO, LOOK AT NEXT RB IN CHAIN
PURG090  TM    IPIBOPT,IPIBHALT    IS THIS REQUEST FOR HALT PURGE
         BO    PURG100             YES, USE HALT LOGIC
         BAL   REG15,PURGCNT       PURGE QUIESCE-INCR IPIB CNT @ZA20559
PURG095  ICM   WKREGB,LORD3BYT,RBLINK+C1  LOAD POINTER TO       @YM1875
*                                  * PREVIOUS RB                @YM1875
         B     PURG085             CHECK THIS RB FOR IRB
         SPACE 2
PURG100  L     IOSBRG,CVTPTR       LOAD ADDRESS OF THE CVT
         USING CVT,IOSBRG          ADDRESSABILITY TO CVT
         LA    IOSBRG,CVTEXIT      POINT TO SVC 3 IN CVT
         ST    IOSBRG,RBOPSW+C4    STORE POINTER TO SVC 3 IN RB
         SLR   IOSBRG,IOSBRG       ZERO REGISTER FOR STORE     @Y30IPLB
         ST    IOSBRG,RBIQEA       ZERO RQE FIELD
         TM    IPIBOPT,IPIBPOST    POST REQUESTED
         BZ    PURG105             NO, SKIP POST
         BAL   REG14,PURGPOST      POST REQUESTED
PURG105  BAL   REG14,PURGFREE      FREE RQE
         B     PURG095             CONTINUE WITH PURGE ON RBS
         SPACE 2
PURG110  TM    IPIBOPT,IPIBMEM     IS THIS A MEMORY PURGE
         BZ    PURG120             NO, CONTINUE WITH DEB RRQ PURGE
         C     REG1,ASXBLTCB       IS THIS THE LAST TCB IN CHAIN
         BE    PURG115             YES, GO ON TO DEB RRQ PURGE
         ICM   REG1,LORD3BYT,TCBTCB+C1  LOAD NEXT TCB           @YM1875
*                                  * ON READY CHAIN             @YM1875
         B     PURG080             GET FIRST RB ON CHAIN
         EJECT
***********************************************************************
*                                                                     *
*        PURGE THE DEB RELATED REQUEST QUEUES. THERE MAY BE REQUESTS  *
*        WHICH HAVE BEEN IDENTIFIED BY THE SVC PURGE ROUTINE. THESE   *
*        REQUESTS SHOULD BE SKIPPED. ALL OTHER REQUESTS SHOULD BE     *
*        PURGED AND THE IOBS PLACED ON A RESTORE CHAIN IF QUIESCE.    *
*                                                                     *
***********************************************************************
         DROP  REG1                ADDRESSABILITY ON TCB
         USING TCB,QUSREG          CHANGES TO QUSREG
         SPACE 2
PURG113  L     DEBREG,IPIBARG      LOAD ARGUMENT FROM IPIB IN CASE
*                                  DEB PURGE
         TM    IPIBOPT,IPIBMEM     IS THIS A MEMORY PURGE
         BZ    PURG120             NO, MUST BE TCB OR DEB PURGE
PURG115  L     QUSREG,ASXBFTCB     LOAD TOP TCB ON THE QUEUE
         B     PURG123             GO TO CODE COMMON BETWEEN   @YM07696
*                                  TCB AND MEMORY
         SPACE 2
PURG120  EQU   *                   DETERMINE TYPE OF PURGE     @YM07367
         SPACE
         LA    REG14,PURG170       GET BRANCH @ IF DEB PURGE   @YM07367
         TM    IPIBOPT,IPIBTASK    IS THIS A TCB PURGE?        @YM07367
         BZ    PURG128             NO, CHECK FOR DEB LIST PURGE@YM07367
         SPACE
         L     QUSREG,IPIBARG      LOAD TCB POINTER FROM IPIB  @YM07696
PURG123  LA    REG14,PURG127       GET BRANCH @ FOR TCB PURGE  @YM07696
         SPACE
PURG125  L     RRQREG,TCBJSTCB     LOAD POINTER TO JOB STEP TCB
         L     RRQREG,TCBJSCB-TCB(RRQREG) LOAD POINTER TO JSCB
         LA    RRQREG,C0(RRQREG)   CLEAR HIGH BYTE              @YM1875
         LTR   RRQREG,RRQREG       WAS THERE A JSCB ASSOCIATED
         BZ    PURG138             NO,CANNOT PURGE NO DEB TABLE@YM06581
         USING IEZJSCB,RRQREG      TEMPORARY ADDRESSABILITY TO JSCB
         ICM   RRQREG,LORD3BYT,JSCBDBTB+C1  LOAD POINTER TO     @YM1875
*                                  * DEB TABLE                  @YM1875
         LTR   IOBREG,RRQREG       IS THERE A TABLE
         BZ    PURG138             NO TABLE THUS NO DEBS       @YM06581
*                                  CALCULATE THE ADDRESS OF
         AH    IOBREG,C0(RRQREG)   THE END OF THE TABLE
PURG127  LA    RRQREG,DEBTBENT(RRQREG) POINT TO NEXT ENTRY
         CR    RRQREG,IOBREG       END OF TABLE YET
         BNL   PURG138             YES, TEST ANY MORE TCB'S    @YM06581
         L     DEBREG,C0(RRQREG)   GET DEB PTR                  @YM1145
         LTR   DEBREG,DEBREG       DOES ADDR EXIST              @YM1145
         BZ    PURG138             NO, SKIP PROCESSING         @YM06581
         TM    C3(RRQREG),C1       DUMMY ENTRY
         BO    PURG127             YES, LOOK AT THE NEXT DEB ENTRY
         SPACE
PURG128  EQU   *                   START TESTS FOR SPECIAL DEBS@YM07367
         LA    WKREGB,DEBBASIC-DEBPREFX GET OFFSET TO THE PREFIX YM0122
         SLR   DEBREG,WKREGB       RESET DEBREG TO PREFIX      @Y30IPLB
         SPACE
*        NOTE - ON THE FOLLOWING TESTS FOR SPECIAL DEBS, IF A TCB
*               PURGE WAS REQUESTED, THE BRANCH IS TO PURG127. IF
*               A DEB PURGE WAS REQUESTED, THE BRANCH IS TO PURG170.
*                                                              @YM07367
         CLC   DEBAMTYP-DEBPREFX(C1,DEBREG),XPRVSAM  VSAM DEB? @YM02921
         BER   REG14               VSAM DEB, DO NOT LOOK AT IT @YM07367
         CLC   DEBAMTYP-DEBPREFX(C1,DEBREG),XPRSUBSY  HASP OR  @YM02921
*                                  ASP DEB?                    @YM02921
         BER   REG14               SUBSYSTEM DEB, DON'T PURGE  @YM07367
         CLC   DEBAMTYP-DEBPREFX(C1,DEBREG),XPRTCAMP TCAM DEB? @YM02921
         BER   REG14               TCAM DEB, DISREGARD IT      @YM07367
         ALR   DEBREG,WKREGB       RESTORE BASIC DEB ADDRESS   @Y30IPLB
         SPACE 2
         L     WKREGB,DEBRRQ       LOAD POINTER TO DEB RRQ     @Y30IPLB
         LA    WKREGB,C0(WKREGB)   ZERO HIGH BYTE
         LTR   WKREGB,WKREGB       IS REQUEST FIELD ZERO
         BNZ   PURG140             YES, NO REQUESTS TO HANDLE
PURG135  TM    IPIBOPT,IPIBTASK+IPIBMEM TCB × MEMORY PURGE REQ?@YM06581
         BNZ   PURG127             YES, LOOK AT NEXT DEB IN TABLE
         SPACE
PURG138  EQU   *                   DETERMINE IF ANY MORE TCB'S @YM06581
         TM    IPIBOPT,IPIBMEM     MEMORY PURGE
         BZ    PURG170             NO, FINISHED WITH THIS TCB
         C     QUSREG,ASXBLTCB     LAST TCB IN CHAIN
         BE    PURG170             YES,FINISHED WITH DEB RRQ PURGE
         ICM   QUSREG,LORD3BYT,TCBTCB+C1 LOAD PTR TO NEXT TCB   @YM1875
         B     PURG125             PURGE THIS TCB
         SPACE 2
         USING RRQ,WKREGB          ADDRESSABILITY FOR RRQ
PURG140  L     RQEREG,RRQFIRST     LOAD POINTER TO FIRST RQE IN QUEUE
PURG145  LTR   RQEREG,RQEREG       IS THIS THE END OF THE CHAIN
         BNP   PURG135             YES, LOOK FOR ANOTHER DEB   @ZA12696
         TM    IPIBOPT,IPIBTASK    TCB PURGE
         BZ    PURG147             NO, NO CHECKS NECESSARY
         C     QUSREG,RQETCB       TCB PURGING MATCH ONE IN RQE
         BNE   PURG148             NO, DO NOT PURGE THIS RQE
PURG147  TM    RQEFLAG,RQESRBS     IS THIS RQE SCHEDULED
         BZ    PURG152             NO, FOUND FIRST ONE TO HANDLE HERE
         TM    IPIBOPT,IPIBHALT    IS THIS HALT OPTION
         BO    PURG148             YES, DONT LOOK FOR IPIB PTR @ZA05520
************************************************************** @ZA16154
*        PURGE QUIESCE OF A RELATED RQE THAT HAS BEEN READIED* @ZA16154
*        FOR STARTIO.  NEED TO CHECK TO SEE IF BASIC IOS HAS * @ZA16154
*        INITIATED I/O REQUEST AND SET THE IOSIPIB FIELD     * @ZA16154
*        AND ACCORDINGLY INCREMENTED THE IPIB COUNT FIELD.   * @ZA16154
*        IF SO, NO NEED TO COUNT, BUT DO NEED TO SET         * @ZA16154
*        RQEIPIB FIELD SO ON FREEING RQE THE COUNT IS        * @ZA16154
*        DECREMENTED.                                        * @ZA16154
************************************************************** @ZA16154
         L     WKREGB,RQESRB       LOAD POINTER TO SRB         @ZA05520
         L     REG15,SRBPARM-SRBSECT(WKREGB) GET IOSB ADDR     @ZA20559
         L     WKREGB,IOSIPIB-IOSB(REG15)    GET IPIB POINTER  @ZA20559
         ST    IPIBRG,RQEIPIB      UPDATE RQE FOR CASE BASIC   @ZA16154
*                                  IOS SET IOSIPIB FLD IN IOSB @ZA16154
         LTR   WKREGB,WKREGB       IS POINTER NULL?            @ZA05520
         BNZ   PURG148             NO, DO NOT STORE IPIB ADDR  @ZA05520
         CS    WKREGB,IPIBRG,IOSIPIB-IOSB(REG15)  SET IPIB PTR @ZA20559
*                                                 IN IOSB.     @ZA16154
         BNE   PURG148             IF CS FAILS, THEN BASIC IOS @ZA16154
*                                  HAS SET IOSIPIB FIELD.      @ZA16154
*                                  THERE IS NO NEED TO         @ZA16154
*                                  INCREMENT IPIB COUNT        @ZA16154
         BAL   REG15,PURGCNT       PURGE QUIESCE-INCR IPIB CNT @ZA20559
PURG148  L     RQEREG,RQENRQE      LOAD POINTER TO NEXT RQE
         B     PURG145             LOOP BACK TO LOOK AT THIS RQE
         SPACE 2
PURG150  TM    IPIBOPT,IPIBTASK    TCB PURGE
         BZ    PURG152             NO, NO CHECKS NECESSARY
         C     QUSREG,RQETCB       TCB PURGING MATCH ONE IN RQE
         BNE   PURG148             NO,LOOK AT NEXT RQE
PURG152  TM    IPIBOPT,IPIBHALT    IS THIS A HALT PURGE
         BO    PURG155             YES, SKIP THE BUILD OF QUIESCE CHAIN
         LR    REG0,IPIBRG         IPIB IN PARM REG 0           @YM1141
         LR    REG1,RQEREG         RQE IN PARM REG 1            @YM1141
         LA    SAVREG,GTMSVRA      LOAD SAVE AREA ADDRESS       @YM3100
         BAL   REG14,IECVRCHN      BUILD RESTORE CHAIN          @YM1141
         SPACE 2
PURG155  TM    IPIBOPT,IPIBPOST    IS POST BIT ON
         BO    PURG160             YES, DO NOT SET 'DONT POST' BIT
         OI    RQEFLAG,RQENOPST    SET DONT POST FLAG
PURG160  LR    REG0,IOBREG         SAVE THE END OF DEB LIST ADR @YM3831
         L     IOBREG,RQEIOB       LOAD POINTER TO IOB          @YM1141
         USING IOBSTDRD,IOBREG     ADDRESSABILITY TO IOB        @YM1141
         MVI   IOBECBCC,ECBPURGE   SET PURGED COMPLETION CODE   @YM3831
         L     IOSBRG,RQENRQE      SAVE NEXT RQE ADDRESS       @Y30IPLB
         BAL   REG14,PURGINTF      GO RELEASE STORAGE FOR THIS REQUEST
         LR    IOBREG,REG0         RESTORE DEB LIST ADDRESS     @YM1141
         LTR   RQEREG,IOSBRG       IS NEXT RQE ADDRESS NEGATIVE..IE.
*                                  END OF CHAIN.
         BP    PURG150             NO, PURGE NEW RQE           @ZA20559
         B     PURG135             LOOK FOR ANOTHER DEB
         EJECT
************************************************************** @ZA16154
*  THIS ROUTINE INCREMENTS THE PURGE QUIESCE COUNT IN THE    * @ZA16154
*  IPIB TO INDICATE I/O OUTSTANDING AND SET THE IPIB POINTER * @ZA16154
*  IN THE RQE SO THAT IPIB COUNT IS DECREMENTED ON THE       * @ZA16154
*  COMPLETION OF THE I/O OPERATION AT TIME WHEN RQE IS FREED * @ZA16154
************************************************************** @ZA16154
PURGCNT  EQU   *                                               @ZA16154
         ST    REG1,GTMAREA        SAVE TWO REGISTERS FOR      @ZA16154
         ST    IOBREG,GTMAREA+C4   INCREMENTING COUNT.         @ZA16154
         ST    IPIBRG,RQEIPIB      STORE IPIB ADDR IN RQE      @ZA16154
         L     REG1,IPIBCNT        LOAD IPIB QUIESCE COUNT     @ZA16154
PURGINCR EQU   *                                               @ZA16154
         LA    IOBREG,C1(REG1)     INCREMENT QUIESCE COUNT     @ZA16154
         CS    REG1,IOBREG,IPIBCNT SUCCESSFUL STORE ??         @ZA16154
         BNE   PURGINCR            NO, RETRY OPERATION.        @ZA16154
*                                  REG1 HAS UPDATED IPIBCNT.   @ZA16154
         L     REG1,GTMAREA        RESTORE                     @ZA16154
         L     IOBREG,GTMAREA+C4   RESTORE                     @ZA16154
         BR    REG15               RETURN TO CALLER            @ZA20559
         EJECT                                                 @ZA16154
***********************************************************************
*                                                                     *
*        QUEUE SEARCHES ARE COMPLETE. THERE IS SOME WORK LEFT TO      *
*        DO WHICH IS UNIQUE TO THE TYPE OF PURGE. THAT IS, FOR HALT   *
*        THE REQUESTS IDENTIFIED BY SVC 16 MUST BE PURGED.            *
*                                                                     *
***********************************************************************
         SPACE 2
PURG170  TM    IPIBOPT,IPIBHALT    IS THIS A HALT OR A QUIESCE
         BZ    PURG900             A QUIESCE, DONE WITH ALL     @YM1141
         L     REG1,IPIBSRB        LOAD POINTER TO FIRST SRB TO
*                                  BE HALTED.
         SLR   REG0,REG0           ZERO REG FOR STORE          @Y30IPLB
         ST    REG0,IPIBSRB        INITIALIZE CHAIN TO ZERO
         USING SRBSECT,REG1        ADDRESSABILITY FOR SRB
PURG175  LTR   REG1,REG1           ANYTHING TO PURGE
         BZ    PURG900             NO, DONE WITH THE HALT PURGE @YM2205
         L     IOSBRG,SRBPARM      LOAD POINTER TO IOSB
         USING IOSB,IOSBRG         ADDRESSABILITY TO IOSB
         SPACE 2
         L     REG1,SRBFLNK        LOAD POINTER TO NEXT IN CHAIN
         L     RQEREG,IOSUSE       LOAD POINTER TO RQE
         L     IOBREG,RQEIOB       LOAD POINTER TO IOB
         L     REG14,IOSIPIB       LOAD POINTER TO IPIB        @YM05504
         LTR   REG14,REG14         IS POINTER NULL?            @YM05504
         BZ    PURG183             YES, SET COMP CODE-PURGE    @YM05504
         SPACE
         ST    REG14,RQEIPIB       NO, PUT IPIB PTR IN RQE     @YM05504
         SPACE
PURG183  EQU   *                                               @YM05504
         MVI   IOBECBCC,ECBPURGE   SET CODE TO PURGE
         TM    IPIBOPT,IPIBPOST    POST OPTION SPECIFIED
         BO    PURG185             YES, FREE CORE
         OI    RQEFLAG,RQENOPST    SET DO NOT POST FLAG
PURG185  BAL   REG14,PURGINTF      GO RELEASE STORAGE FOR THIS REQUEST
         B     PURG175             LOOK AT NEXT SRB
         EJECT
***********************************************************************
*                                                                     *
*        PURGE EXIT ROUTINE                                           *
*                                                                     *
***********************************************************************
         SPACE 2
PURG900  SETLOCK RELEASE,TYPE=LOCAL,RELATED=(IECVEXPR,PURG000)  @YM2205
         SPACE 2
         LR    REG1,GTMREG         LOAD ADDRESS OF GETMAINED SAVE
*                                  AREA
         L     IOBREG,GTMSAVE      LOAD PURGE SAVE AREA ADDRESS
         FREEMAIN R,A=(1),LV=160,SP=254 FREE THE 160B WORK AREA@ZA09412
         SPACE 2
         LM    REG0,REG15,C0(IOBREG) RESTORE REGISTERS
         BR    REG14               RETURN TO PURGE
         EJECT
***********************************************************************
*                                                                     *
*        PURGE SUPPORT SUBROUTINES TO FREE THE RQE AND TO POST THE    *
*        ECB AS PURGED.                                               *
*                                                                     *
***********************************************************************
         SPACE 2
PURGPOST EQU   *                   POST ROUTINE INTERFACE
         STM   REG0,REG15,GTMAREA  SAVE REGISTERS 0 THRU 15    @YM05503
         L     IOBREG,RQEIOB       LOAD IOB ADDRESS             @YM1392
         L     WKREGB,IOBECBPT     LOAD POINTER TO ECB
         LA    WKREGB,C0(WKREGB)   ZERO THE HIGH BYTE
         TM    RQEPRT,PPKEY        IS ECB IN PROBLEM PROGRAM   @ZA26460
*                                  KEY 8 - 15?                 @ZA26460
         BZ    PURGPST1            NO. DONT VALIDITY CHECK ECB.@ZA26460
         MODESET SWAPKEY,KEYADDR=RQEPRT,WORKREG=15 CHECK ECB.  @ZA26460
         NI    C0(WKREGB),FF       TOUCH ECB WHILE IN USERS KEY@ZA26460
         MODESET EXTKEY=ZERO                                   @ZA26460
PURGPST1 EQU *                                                 @ZA26460
         LA    WKREGA,ECBPURGE     LOAD THE CONDITION CODE      @YM1392
         SLL   WKREGA,C24          PLACE IN HIGH BYTE
         L     REG15,AOPT02        GET ADDR OF POST WITH VALCHK
         BALR  REG14,REG15         GO TO POST
         LM    REG0,REG15,GTMAREA  RESTORE REGISTERS           @YM05503
         BR    REG14               BRANCH ON SAVED LNKREG
         SPACE 2
PURGFREE EQU   *                   FREE THE RQE
         LR    IOSBRG,REG14        SAVE RETURN ADDRESS
         LR    RRQREG,REG1         SAVE ADDRESS IN R1
         LR    REG1,RQEREG         INITIALIZE PARAMETER REG WITH RQE
         LA    SAVREG,GTMAREA      LOAD POINTER TO SAVE AREA
         LR    RQEREG,BASREG       SAVE BASE                     YM1210
         L     REG15,AX025         LOAD POINTER TO FREE RQE ROUTINE
         BALR  REG14,REG15         RETURN RQE TO STORAGE MANAGER
         LR    BASREG,RQEREG       RESTORE BASE ADDRESS          YM1210
         LR    REG14,IOSBRG        RESTORE REGISTER 14
         LR    REG1,RRQREG         RESTORE REGISTER 1
         BR    REG14               RETURN TO PURGE MAIN LINE
         SPACE 2
PURGINTF STM   REG0,REG15,GTMSVRA  SAVE PURGE REGISTERS
         LA    SAVREG,GTMAREA      POINT TO EXCP STORAGE AREA
         LR    REG1,RQEREG         PLACE RQE IN PARM REG
         L     REG15,ATERMRTN      LOAD POINTER TO PURGE INTERFACE
         BALR  REG14,REG15         EXECUTE TERM RTN OF EXCP
         USING GTMAREA,SAVREG      ADDRESSABILITY TO SOMETHING
         LM    REG0,REG15,GTMSVRA  RESTORE REGISTERS
         BR    REG14               RETURN TO CALLING ROUTINE
         DROP  SAVREG              STOP TEMPORARY ADDRESSABILITY
         TITLE 'IECVEXPR EXCP PURGE RESTORE AND FRR ROUTINES - RESTORE X
               CHAIN BUILD SUBROUTINE'
***********************************************************************
*                                                                     *
*        THIS SUBROUTINE BUILDS A RESTORE CHAIN FOR QUIESCE PURGE     *
*        FROM THIS ROUTINE OR FROM RELATED REQUEST PURGE FROM         *
*        MAINLINE EXCP OR FROM AN ABNORMAL END APPENDAGE WHICH        *
*        WANTS TO ADD TO THE RESTORE CHAIN.                           *
*                                                                     *
*        INPUT -  REGISTER 0 CONTAINS IPIB POINTER OR ZEROS           *
*                                  IF NONZERO AND POSITIVE THE RQE IS *
*                                    TO BE QUEUED AT THE BOTTOM OF THE*
*                                    PIRL ENTRY Q                     *
*                                  IF NONZERO AND NEGATIVE THE RQE IS *
*                                    TO BE QUEUED AT THE TOP OF THE   *
*                                    PIRL ENTRY Q                     *
*                 REGISTER 1 CONTAINS RQE ADDRESS                     *
*                 REGISTER 13 CONTAINS SAVE AREA ADDRESS              *
*                 REGISTER 14 CONTAINS RETURN ADDRESS                 *
*                 REGISTER 15 CONTAIN ENTRY POINT ADDRESS             *
*                                                                     *
*        OUTPUT - NONE                                                *
*        EXIT - RETURN TO CALLER VIA REGISTER 14                      *
*                                                                     *
***********************************************************************
         PRINT OFF
*/*IECVRCHN: CHART */
*/* HEADER
*/*                                  EXCP RESTORE CHAIN BUILDER
*/*                                 PAGE # */
*/*IECVRCHN: E RESTORE CHAIN BUILDER */
*/* P SAVE REGISTERS, EXTABLISH ADDRESSABILITY */
*/* D (NO,RCHN005,YES,) IPIB PASSED IN R0 */
*/* P (,RCHN015) POINT TO RESTORE CHAIN ANCHOR IN PIRL */
*/*RCHN005: P GET PIRL FROM DEB IF ONE */
*/* D (YES,RCHN010,NO,) WAS PIRL ON DEB */
*/* L GETMAIN: GET PIRL IN SP 0 */
*/* P INITIALIZE NO DRIVERS AND ZERO PIRL */
*/* P STORE PIRL ADDRESS IN DEB */
*/*RCHN010: P CALCULATE EXCP ENTRY IN PIRL */
*/* D (YES,RCHN015,NO,) AN EPCB ON PIRL */
*/* S RCHNGTMN: GET AND INIT EPCB */
*/* P STORE ITS ADDRESS IN PIRL */
*/*RCHN012: P (,RCHN050) POINT TO IOB CHAIN FIELD USED BY RESTORE */
*/*RCHN015: P ZERO REG FOR COMPARE REG */
*/*RCHN020: D (YES,RCHN030,NO,) ANY ENTRIES LEFT IN EPCB */
*/* D (NO,RCHN025,YES,) ANOTHER ONE ON CHAIN */
*/* P (,RCHN020) LOAD PTR TO NEXT ON CHAIN */
*/*RCHN025: S RCHNGTMN: GET AND INITIALIZE EPCB */
*/* P STORE IT IN CHAIN OF PREVIOUS EPCB */
*/*RCHN030: D (NO,RCHN012,YES,) ANYTHING IN RESTORE CHAIN */
*/*RCHN035: D (YES,RCHN040,NO,) END  IOB OF CHAIN */
*/* P (,RCHN035) LOAD PTR TO NEXT IOB */
*/*RCHN040: P POINT TO CHAIN FIELD IN IOB */
*/*RCHN050: P CHAIN IOB TO END OF CHAIN, SET END INDC */
*/* P LOAD CURRENT EPCB ENTRY PTR */
*/* P MOVE IOB AND PROTECT KEY TO EPCB */
*/* D (YES,,NO,RCHN055) RESTORE TO ORIG TCB */
*/* P MOVE TCB ADDR TO EPCB */
*/*RCHN055: P INDICATE NEW CURRENT ENTRY AND NO LEFT */
*/* P RESTORE REGISTERS */
*/* R RETURN TO CALLER */
*/*RCHNGTMN: E GET AND INIT EPCB */
*/* L GETMAIN: GET AN EPCB IN USER STORAGE, KEY 0 */
*/* P ZERO ENTIRE BLOCK */
*/* P SET FIRST ENTRY AS CURRENT AND 20 AS COUNT */
*/* R RETURN TO CALLER */
*/*IECVRCHN: END */
         PRINT ON
         SPACE 2
IECVRCHN STM   REG0,REG15,C0(SAVREG) SAVE REGISTERS             @YM1141
         BALR  BASREG,C0           ESTABLISH BASE FOR ROUTINE   @YM1141
         USING *,BASREG            ADDRESSABILITY FOR ROUTINE   @YM1141
         SPACE 2
* FIND OR GET A PIRL AND LOCATE THE EXCP ENTRY IN IT            @YM1141
         SPACE 1
         LTR   IPIBRG,REG0         IPIB PASSED TO THIS ROUTINE  @YM1141
         BZ    RCHN005             NO, LOOK AT DEB FOR PIRL     @YM1141
         L     QUSREG,IPIBPIRL     POINT TO RESTORE CHN ANCHOR @YM05533
         B     RCHN010             GO FIND EXCP ENTRY          @YM05533
         SPACE 1
RCHN005  L     DEBREG,RQEDEB-RQE(REG1) LOAD PTR TO DEB FROM RQE @YM1141
         L     QUSREG,DEBUSRPG     LOAD POINTER TO PIRL,IF ONE  @YM1141
         LA    QUSREG,C0(QUSREG)   ZERO HIGH BYTE OF REGISTER   @YM1141
         LTR   QUSREG,QUSREG       IS THERE A PIRL ON DEB       @YM1141
         BZ    RCHN008             NO, HAVE TO GET A PIRL      @YM06544
         CLM   QUSREG,XPRM7,XPRENDFF NOTHING PURGED? BYPASS    @YM06544
         BNE   RCHN010             GETMAIN, DON'T GET PIRL     @YM06544
         SPACE
RCHN008  LR    WKREGB,DEBREG       NEED PIRL,SAVE DEB BEFORE   @YM06544
*                                  DOING THE GETMAIN
         LR    WKREGC,REG1         SAVE RQE ACROSS GETMAIN      @YM1141
*  GET LENGTH FOR PIRL PLUS 12 EXTRA BYTES FOR RESTORE TO      @ZA05813
*  FREEMAIN AS A VALIDITY CHECK ON THE PIRL                    @ZA05813
         LA    REG0,PIRENTL*(IOSXCPID+C1)+(PIRRSTR-PIRL+12)    @ZA05813
         ICM   REG0,XPRM1,XPR254  GET SP-254 IN HIGH ORD BYTE  @Y30IPLD
         L     RQEREG,RQETCB-RQE(WKREGC) INIT TCB IN REG4       @YM1141
         L     IPIBRG,PSAAOLD      LOAD ASCB ADDRESS IN REG7    @YM1141
*                                                              @ZM30487
         GETMAIN R,LV=(0),BRANCH=YES GET STORAGE FOR PIRL-SP254@YM1141
*                                  EXPECTING 252 CORE-KEY 0
         SPACE 2
         LR    QUSREG,REG1         PLACE PIRL IN ITS REG        @YM1141
         LR    DEBREG,WKREGB       RESTORE DEB REGISTER         @YM1141
         LR    REG1,WKREGC         RESTORE RQE TO PARM REG      @YM1141
         SLR   IPIBRG,IPIBRG       INDICATE NO IPIB PATH        @YM1141
         SPACE 1
         USING PIRL,QUSREG         ADDRESSABILITY FOR PIRL      @YM1141
         XC    PIRL(PIRENTL*(IOSXCPID+C1)+(PIRRSTR-PIRL)),PIRL  @YM1141
         MVI   PIRCNT,IOSXCPID+C1  SET NO OF DRIVERS IN PIRL    @YM1141
         STCM  QUSREG,LORD3BYT,DEBUSRPG+C1 SET PIRL IN DEB      @YM1141
         SPACE 1
* FIND OUT IF THERE IS AN EPCB, IF NONE, GET ONE AND INITIALIZE IT
         SPACE 1
RCHN010  LA    QUSREG,PIRRSTR+IOSXCPID*PIRENTL FIND EXCP ENTRY  @YM1141
         USING PIRRSTR,QUSREG      ADDRESSABILITY FOR ENTRY     @YM1141
         LR    RQEREG,REG1         SET UP BASE FOR RQE          @YM3095
         L     IOSBRG,PIRDVRU      LOAD PTR TO EPCB, IF ONE     @YM1141
         USING EPCB,IOSBRG         ADDRESSABILITY TO EPCB       @YM1141
         LTR   IOSBRG,IOSBRG       IS THERE AN EPCB             @YM1141
         BNZ   RCHN015             YES, GO SEE IF IT IS FULL    @YM1141
         BAL   WKREGC,RCHNGTMN     GET CORE AND INIT EPCB       @YM1141
         ST    IOSBRG,PIRDVRU      STORE EPCB PTR IN PIRL       @YM1141
RCHN012  LA    WKREGB,PIRRSTR      POINT TO IOB CHAIN FIELD     @YM1141
         SLR   REG0,REG0           CLEAR 0                      YM3829P
         BCTR  REG0,0              DECREMENT TO -1              YM3829P
         B     RCHN050             PLACE IOB ON RESTORE CHAIN   @YM1141
*                                  AND IN EPCB                  @YM1141
         SPACE 1
* LOCATE EPCB, GET ANOTHER IF NO ROOM IN CURRENT                @YM1141
RCHN015  SLR   REG0,REG0           ZERO REG FOR COMPARE         @YM1141
RCHN020  CLI   EPCBNENT,C0         ANY ENTRIES IN THIS EPCB     @YM1141
         BNE   RCHN030             YES, USE THIS EPCB           @YM1141
         C     REG0,EPCBCHN        IS THERE ANOTHER EPCB        @YM1141
         BE    RCHN025             NO, MUST GET ANOTHER         @YM1141
         L     IOSBRG,EPCBCHN      LOAD PTR TO NEXT BLOCK       @YM1141
         B     RCHN020             LOOK AT THIS BLOCK FOR ONE   @YM1141
         SPACE 2
RCHN025  LR    RRQREG,IOSBRG       SAVE CURRENT EPCB ADDRESS    @YM1141
         BAL   WKREGC,RCHNGTMN     GET AND INITIALIZE AN EPCB   @YM1141
         ST    IOSBRG,EPCBCHN-EPCB(RRQREG) STORE CHAIN IN PREV  @YM1141
         EJECT
* FIND END OF IOB CHAIN                                         @YM1141
         SPACE 1
RCHN030  L     IOBREG,PIRRSTR      LOAD RESTORE CHAIN ANCHOR    @YM1141
         CLM   IOBREG,XPRM7,XPRENDFF END OF RESTORE CHAIN?     @Y30IPLB
         BE    RCHN012             YES, AN IOB MUST HAVE BEEN  @Y30IPLB
*                                  REMOVED FROM THE LIST        @YM1141
         BCTR  REG0,0              DECREMENT TO -1              YM3829P
         LTR   IPIBRG,IPIBRG       IS THERE A REEXCP FROM TOP   YM3829P
*                                  TOP OF RRQ                   YM3829P
         BNM   RCHN035             NO - CHAIN ON BOTTOM         YM3829P
         LR    REG0,IOBREG         YES - LOAD FIRST AS CHAIN    YM3829P
*                                  INSERT                       YM3829P
         LA    WKREGB,PIRRSTR      LOAD PIRL ANCHOR AS PLACE TO YM3829P
*                                  TO Q FROM                    YM3829P
         B     RCHN050             GO ENQ IT ON TOP             YM3829P
RCHN035  CLM   REG0,LORD3BYT,IOBRSTRB END OF CHAIN              @YM1141
         BE    RCHN040             YES, GO SET POINTER          @YM1141
         L     IOBREG,IOBRESTR     LOAD POINTER TO NEXT IOB     @YM1141
         B     RCHN035             LOOK AT THIS ONE FOR THE END @YM1141
*                                  OF THE CHAIN                 @YM1141
         SPACE 1
RCHN040  LA    WKREGB,IOBRESTR     POINT TO CHAIN FIELD         @YM1141
         SPACE 1
* SET UP NEW ENTRY AND MAINTAIN COUNT AND PTRS IN EPCB          @YM1141
RCHN050  L     IOBREG,RQEIOB       LOAD CURRENT IOB ADDRESS     @YM1141
         STCM  IOBREG,LORD3BYT,C1(WKREGB) CHAIN IOB TO PREVIOUS @YM1141
         STCM  REG0,LORD3BYT,IOBRSTRB ZERO CHAIN FOR NEXT       @YM1141
         L     WKREGB,EPCBENT      LOAD PTR TO CURRENT ENTRY    @YM1141
         USING EPCBIOB,WKREGB      ADDRESSABILITY TO EPCB ENTRY @YM1141
         ST    IOBREG,EPCBIOB      STORE IOB IN ENTRY           @YM1141
         MVC   EPCBPKEY,RQEPRT     MOVE PROTECT KEY TO EPCB     @YM1141
         LTR   IPIBRG,IPIBRG       WAS AN IPIB PASSED           @YM1141
         BZ    RCHN055             NO FINISHED WITH ENTRY       @YM1141
         MVC   IPIBIO(C8),PIRRSTR  SAVE PTRS I/O REQUEST LIST  @YM05533
         TM    IPIBOPT,IPIBOTCB    RESTORE TO ORIGINATING TCB   @YM1141
         BZ    RCHN055             NO, FINISHED WITH ENTRY      @YM1141
         L     WKREGC,RQETCB       LOAD TCB ADDRESS FROM RQE    @YM1141
         ST    WKREGC,EPCBTCB      AND STORE IT IN EPCB         @YM1141
         SPACE
RCHN055  EQU   *                                               @YM05526
         TM    RQETYPE,RQE114     IS THIS AN EXCPVR REQUEST?   @YM05526
         BZ    RCHN060            NO, GET NEXT EPCB ENTRY      @YM05526
         SPACE
         MVI   EPCBTCB,EXCPVRB    YES, THEN SET EXCPVR INDICA- @YM05526
*                                 TOR IN HI-ORDER BYTE TCB PTR @YM05526
         SPACE
RCHN060  EQU   *                                               @YM05526
         LA    WKREGB,EPCBENTL(WKREGB) INCREMENT TO NEXT ENTRY  @YM1141
         IC    REG0,EPCBNENT       GET PREVIOUS NO OF ENTRIES  @YM05526
         ST    WKREGB,EPCBNENT     STORE NEW CURRENT ENTRY     @YM05526
         BCTR  REG0,REG0           DECREMENT BY ONE             @YM1141
         STC   REG0,EPCBNENT       STORE CURRENT NO OF ENTRIES  @YM1141
         LM    REG0,REG15,C0(SAVREG) RESTORE REGS               @YM1141
         BR    REG14               RETURN TO CALLER             @YM1141
         DROP  WKREGB                                           YM3112P
         EJECT
* ROUTINE WHICH GETS AND INITIALIZES THE EPCB                   @YM1141
         SPACE 2
* GET THE STORAGE FOR THE EPCB                                  @YM1141
RCHNGTMN LA    REG0,EPCBBL         LOAD THE LENGTH OF GETMAIN   @YM1141
         LR    WKREGB,RQEREG       SAVE RQE ADDRESS             @YM1141
         LR    IOBREG,IPIBRG       SAVE IPIB ADDRESS            @YM1141
         L     RQEREG,RQETCB       LOAD TCB PTR IN REG 4        @YM1141
         L     RQEREG,TCBJSTCB-TCB(RQEREG) ALLOCATE SP230 EPCB @ZA24443
*                                  FROM JOB STEP TCB           @ZA24443
         L     IPIBRG,PSAAOLD      LOAD ASCB PTR IN REG 7       @YM1141
         GETMAIN RU,LV=(0),SP=230,BRANCH=YES,KEY=0 GET EPCB    @ZA17779
         LR    IOSBRG,REG1         SET UP EPCB IN ITS BASE REG  @YM1141
         LR    IPIBRG,IOBREG       RESTORE IPIB TO ITS BASE     @YM1141
         SLR   REG0,REG0           ZERO COMPARE REG            @Y30IPLB
         SPACE 1
* INITIALIZE THE BLOCK                                          @YM1141
         SPACE 1
         XC    EPCB(EPCBBL),EPCB   ZERO ENTIRE EPCB             @YM1141
         ST    RQEREG,EPCBRTCB     SAVE CURRENT TCB ADDRESS    @YM06170
*                                  FOR USE IN FREEMAIN
         LR    RQEREG,WKREGB       RESTORE RQE TO ITS BASE     @YM08100
         LA    WKREGB,EPCBIOB      POINT TO FIRST ENTRY         @YM1141
         USING EPCBIOB,WKREGB                                   YM3112P
         ST    WKREGB,EPCBENT      STORE IN CURRENT ENTRY PTR   @YM1141
         MVI   EPCBNENT,EPCBNE     INITIALIZE THE NUMBER OF ENT @YM1141
         BR    WKREGC              RETURN TO MAIN ROUTINE       @YM1141
         DROP  WKREGB                                           YM3112P
       TITLE 'IECVEXPR EXCP PURGE RESTORE AND FRR ROUTINES - UCB PURGE'
***********************************************************************
*                                                                     *
*                        IO HALT SUBROUTINE                           *
*                                                                     *
*        THIS INTERFACE IS USED BY SVC 33 WHICH IS IO HALT TO CHANGE  *
*        A REAL CCW IN AN ACTIVE CHANNEL PROGRAM TO A NOP. (USED BY   *
*        BTAM)                                                        *
*                                                                     *
*        INPUT R0 - OFFSET TO CCW FROM IOB                            *
*              R2 - IOSB ADDRESS                                      *
*              R14 - RETURN ADDRESS                                   *
*              R15 - MODULE ADDRESSABILITY                            *
*              R6 - BASE FO SVC 33 - TO BE MAINTAINED                 *
*                                                                     *
*        OUTPUT R15 - RETURN CODE                                     *
*                   0 - FOUND AND MODIFIED CCW                        *
*                   16 - NOT A VIRTUAL REQUEST                        *
*                   20 - TRANSLATION IN PROGRESS                      *
*                   24 - CANNOT FIND REAL CCW                         *
*                                                                     *
***********************************************************************
SVC33    STM   REG0,REG14,C0(SAVREG) SAVE REGISTERS
         BALR  BASREG,C0           ESTABLISH BASE FOR ROUTINE
         USING *,BASREG            ADDRESSABILITY FOR ROUTINE
         L     RQEREG,IOSUSE-IOSB(IOBREG) GET RQE IN ITS BASE
         TM    RQETYPE,RQEVIRT     IS THIS A VIRTUAL REQUEST
         BZ    HALT16              NO, GIVE A RETURN CODE 16
         TM    RQEFLAG,RQESTBL     IS THE REQUEST STARTABLE
         BZ    HALT20              NO, GIVE A RETURN CODE 20
         L     IOBREG,RQEIOB       LOAD POINTER TO IOB
         LA    IOBREG,C0(IOBREG)   CLEAR HIGH BYTE
         ALR   REG0,IOBREG         ADD INCREMENT FROM REG0     @Y30IPLB
         L     REG1,RQETCCW        LOAD POINTER TO THE TCCW
         MVI   TCCWOPTN-TCCW(REG1),TCCWSATR SET SINGLE ADDR XLATION
         L     REG15,ACCWXLAT      LOAD POINTER TO TRANSLATOR
         BALR  REG14,REG15         TRANSLATE THE ADDRESS
         LTR   REG15,REG15         RETURN CODE ZERO
         BNZ   HALT24              NO, COULD NOT FIND CCW
         LR    REG1,REG0           SET A REGISTER TO REAL CCW PTR
         MVI   C4(REG1),C0         SET THE FLAGS TO ZERO
         MVI   C0(REG1),NOP        SET THE COMMAND TO A NOP
HALT00   LM    REG0,REG14,C0(SAVREG) RESTORE REGISTERS
         BR    REG14               RETURN TO SVC33
         SPACE 1
HALT16   LA    REG15,RC16          SET RETURN CODE 16
         B     HALT00              RETURN TO SVC33
         SPACE 1
HALT20   LA    REG15,RC20          SET RETURN CODE 20
         B     HALT00              RETURN TO SVC33
         SPACE 1
HALT24   LA    REG15,RC24          SET RETURN CODE 24
         B     HALT00              RETURN TO SVC 33
         TITLE 'IECVEXPR EXCP PURGE, RESTORE AND FRR ROUTINES - RESTOREX
               INTERFACE ROUTINE'
***********************************************************************
*                                                                     *
*        RESTORE LOOPS THROUGH THE RESTORE CHAIN ISSUING SVC 0'S      *
*        114'S OR 92'S ACCORDING TO THE INFORMATION KEPT IN THE       *
*        IOB TABLE. IN ALL CASES, THE I/O IS RE-ISSUED IN THE         *
*        KEY IN WHICH IT WAS ORIGINALLY ISSUED.                       *
*                                                                     *
*        INPUT REGISTER 1 POINTS TO TWO WORD AREA WHERE               *
*              WORD 1 - ADDRESS OF IOB RESTORE CHAIN                  *
*              WORD 2 - ADDRESS OF TCB/IOB RESTORE LIST               *
*                                                                     *
***********************************************************************
         PRINT OFF
*/*IECVXRES: CHART */
*/* HEADER
*/*                    EXCP RESTORE SUBROUTINE
*/*                                                      PAGE # */
*/*IECVXRES: E RESTORE RTN ENTRY */
*/* P SAVE REGS, ESTAB ADDRESSABILITY */
*/* P LOAD POINTERS TO IOB RESTORE CHAIN AND EPCB */
*/*REST000: D (YES,REST900,NO,) END OF IOB CHAIN */
*/* P LOAD POINTER TO NEXT IOB IN CHAIN */
*/*REST010: P POINT TO FIRST IOB IN EPCB */
*/*REST020: D (YES,REST030,NO,) IOB LOOKING FOR */
*/* P INCREMENT TO NEXT IOB IN LIST */
*/* D (YES,REST025,NO,) END OF EPCB BLOCK */
*/* D (YES,REST060,NO,REST020) END OF ENTRIES IN EPCB */
*/*REST025: D (YES,REST060,NO,REST010) LOAD AND TEST FOR NEXT EPCB */
*/*REST030: P LOAD PTR TO TCB IF ONE */
*/* P TRANSFER TO KEY OF ORIGINAL REQUESTOR */
*/* D (YES,REST040,NO,) EXCPVR REQUEST */
*/* P LOAD IOB ADDRESS INTO PARAMETER REGISTER */
*/* D (YES,REST050,NO,) TCB ASSOCIATED WITH IOB */
*/* L (,REST060) ISSUE SVC 0 */
*/*REST040: P LOAD PARM REG WITH IOB ADDR AND F4 IN HIGH BYTE */
*/* D (YES,REST050,NO,) TCB ASSOCIATED WITH IOB */
*/* L (,REST060) ISSUE SVC114 */
*/*REST050: L ISSUE SVC 92 */
*/*REST060: P POINT TO NEXT IOB ON CHAIN, RESTORE LIST ADDRESS */
*/* P (,REST000) SWAP BACK TO KEY ZERO */
*/*REST900: P SWAP TO KEY ZERO */
*/* P FREE CORE IN EPCB */
*/* P RESTORE REGISTERS */
*/* R RETURN TO RESTORE */
*/*IECVXRES: END */
         PRINT ON
         DROP  IOSBRG                                          @ZA08162
         SPACE 2
IECVXRES STM   REG0,REG15,C0(SAVREG) SAVE REGISTERS
         BALR  BASREG,C0           ESTABLISH BASE FOR ROUTINE
         USING *,BASREG            ADDRESSABILITY FOR ROUTINE
         LR    GTMREG,REG1         SAVE INPUT PARM             @YM05531
         L     IOBREG,C0(REG1)     LOAD POINTER TO FIRST IOB
         L     LSTREG,C4(REG1)     LOAD TCB/IOB LIST ADDRESS
         USING EPCB,LSTREG         ADDRESSABILITY TO EPCB
         LR    TOPREG,LSTREG       LOAD SAVE REGISTER FOR LIST
         SLR   WKREGC,WKREGC       INITIALIZE REG TO ZERO      @Y30IPLB
         CLM   IOBREG,XPRM7,XPREND00 END OF CHAIN?
         BE    REST903             YES, FREE WORK AREA         @YM08365
         SPACE
REST000  EQU   *                                               @YM05531
         CLM   IOBREG,XPRM7,XPRENDFF END OF IOB CHAIN?         @YM05531
         BE    REST900             YES, THEN END OF RESTORE    @YM05531
         SPACE
         L     CHAINRG,IOBRESTR    LOAD POINTER TO NEXT IOB IN CHAIN
         STCM  CHAINRG,XPRM7,C1(GTMREG) STORE PTR TO NEXT IOB @YM05531
REST010  LA    WKREGB,EPCBIOB      INCR TO FIRST IOB POINTER    @YM1141
REST020  CLM   IOBREG,LORD3BYT,C1(WKREGB) IS THIS THE CORRECT IOB
         BE    REST030             YES, ISSUE SVC FOR THIS IOB
         LA    WKREGB,EPCBENTL(WKREGB) INCREMENT TO NEXT ENTRY  @YM1141
         LA    REG0,EPCBBL(LSTREG) POINT TO TOP OF EPCB         @YM1141
         CR    REG0,WKREGB         END OF THIS EPCB             @YM1141
         BE    REST025             YES, SKIP TO NEXT            @YM1141
         C     WKREGC,C0(WKREGB)   END OF LIST
         BE    REST080             YES, IGNORE THE IOB         @YM08618
         B     REST020             HANDLE THIS ENTRY            @YM1141
         SPACE 2
REST025  L     LSTREG,EPCBCHN      LOAD CHAIN POINTER TO NEXT EPCB
         LTR   LSTREG,LSTREG       END OF CHAIN OF EPCB
         BZ    REST080             YES, IGNORE THE IOB         @ZA06999
***      LR    TOPREG,LSTREG       INST DELETED- TOPREG POINTS @ZA16153
*                                  TOP EPCB FOR FREEING EPCBS  @ZA16153
         B     REST010             RETURN TO CONTINUE SEARCH   @ZA06999
         SPACE
         SPACE 2
         USING EPCBIOB,WKREGB      ADDRESSABILITY TO ENTRY     @YM05521
REST030  L     REG0,EPCBTCB        LOAD POINTER TO TCB, IF ONE  @YM1141
         MODESET SWAPKEY,KEYADDR=EPCBPKEY,WORKREG=15 SWAP TO USER KEY
         SPACE
         L     WKREGA,IOBECBPT     GET ECB POINTER BEFORE SVC  @YM08618
         L     ECBREG,C0(WKREGA)   GET ACTUAL ECB IN CASE AL-  @YM08618
*                                  READY WAITED ON
         SPACE
         LTR   REG15,REG0          WAS THIS EXCPVR REQUEST?    @YM05530
         BM    REST040             YES, HANDLE ACCORDINGLY     @YM05530
         SPACE
         LR    REG1,IOBREG         PLACE IOBREG INTO PARM REG
         LA    REG15,C0(REG15)     ZERO HI-ORDER BYTE          @YM05530
         LTR   REG0,REG15          IS THERE ASSOCIATED TCB?    @YM05530
         BNZ   REST050             YES, ISSUE SVC 92-RESTORE   @YM05530
         SPACE
         SVC   EXCP                ISSUE THE SVC ZERO
         B     REST060             CONTINUE RESTORE
         SPACE 2
REST040  LR    REG1,IOBREG         LOAD PARAMETER REGISTER
         ICM   REG1,HIORDBYT,EPCBTCB GET F4 INTO HIGH BYTE     @YM05530
         LA    REG15,C0(REG15)     ZERO HI-ORDER BYTE          @YM05530
         LTR   REG0,REG15          IS THERE ASSOCIATED TCB?    @YM05530
         BNZ   REST050             YES, ISSUE SVC 92-RESTORE   @YM05530
         SPACE
         SVC   EXCPVR              ISSUE SVC 114
         B     REST060             INITIALIZE FOR NEXT ONE
         SPACE 2
REST050  SVC   RESTORE             ISSUE SVC 92
REST060  EQU   *                   RETURN FROM EXCP HERE -     @YM08618
*                                  GET THE LOCAL LOCK BEFORE
*                                  MANIPULATION OF ECB, IF
*                                  WAITED ON ALREADY
*                                  INSURE KEY ZERO BEFORE SETLOCK
         MODESET EXTKEY=ZERO                                   @YM08618
         SPACE
         LTR   ECBREG,ECBREG       WHAT WAS STATUS OF ECB?     @YM08618
         BC    10,REST080          NOT WAITED ON, GET NEXT IOB @YM08618
*                                  TO BE RESTORED
REST065  EQU   *                   SERIALIZE FOR MP ENVIRONMENT@YM08618
         SETLOCK OBTAIN,TYPE=LOCAL,MODE=UNCOND,REGS=USE,               *
               RELATED=(IECVEXPR,REST075)
         SPACE
REST070  CS    WKREGC,ECBREG,C0(WKREGA) RESTORE THE ECB IF IT  @YM08618
*                                  HAS ALREADY BEEN WAITED ON
         BE    REST075             WAIT DONE, ECB RESTORED--   @YM08618
         SPACE
         TM    C0(WKREGA),ECBPOST  HAS THIS ECB BEEN POSTED YET@YM08618
         BZ    REST070             NO, THEN RESTORE THE        @YM08618
*                                  ORIGINAL ECB
         LR    REG14,WKREGC        SAVE COMPLETION CODE IN     @YM08618
*                                  CASE 'CS' INSTRUCTION
*                                  DOESN'T WORK
REST072  EQU   *                                               @YM08618
         CS    WKREGC,ECBREG,C0(WKREGA) YES, RESTORE ORIGINAL  @YM08618
*                                  ECB AND INSURE ITS POSTED
         BNE   REST072             IF FAILURE, TRY AGAIN       @YM08618
         SPACE
         LA    WKREGB,C0(WKREGA)   ZERO HI-BYTE OF ECB PTR     @YM08618
         LR    WKREGA,REG14        GET THE COMPLETION CODE     @YM08618
         L     REG15,AOPT02        GET ADDRESS OF POST ROUTINE @YM08618
********                                                       ********
*        POST INTERFACE DEFINITION - REGS 0-9,12,13 TRANSPARENT       *
*                                    REG 10 = COMPLETION CODE         *
*                                    REG 11 = ECB                     *
*                                    REG 14 = RETURN ADDRESS          *
*                                    REG 15 = ENTRY POINT OF POST     *
********                                                       ********
         BALR  REG14,REG15         GO TO POST THE ECB          @YM08618
         SPACE 2
REST075  EQU   *                                               @YM08618
         SLR   WKREGC,WKREGC       INITIALIZE WKREG TO ZERO    @Y30IPLB
         SETLOCK RELEASE,TYPE=LOCAL,REGS=USE,                          *
               RELATED=(IECVEXPR,REST065)
         SPACE
REST080  LR    IOBREG,CHAINRG                                  @YM08618
         SPACE
         LR    LSTREG,TOPREG       RESTORE LST ADDRESS
         B     REST000             RETURN TO RESTORE NEXT IOB
         SPACE 2
REST900  EQU   *
         MODESET EXTKEY=ZERO                                   @YM05530
         SPACE 2
REST903  EQU   *                   GET THE LOCAL LOCK          @YM08365
         LR    REG15,SAVREG        SAVE R13,LOCK MGR DESTROYS  @YM08365
         SETLOCK OBTAIN,TYPE=LOCAL,MODE=UNCOND,                        *
               RELATED=(IECVEXPR,REST910)
         LR    SAVREG,REG15        RESTORE REG13 ON RETURN     @YM08365
         SPACE
REST905  EQU   *                   FREE ALL EPCBS
         SPACE
         LR    REG1,TOPREG         PUT @ OF THE BLOCK IN PARM REGISTER
         LA    REG1,C0(REG1)       INSURE HI-ORDER IS ZERO     @YM06170
         L     LSTREG,EPCBRTCB-EPCB(TOPREG) GET TCB ADDR       @YM06170
         L     WKREGA,EPCBCHN-EPCB(TOPREG) LOAD ADDR NEXT EPCB @YM06170
         LA    REG0,EPCBBL         GET LENGTH FOR FREEMAIN     @YM06170
         L     ASCBREG,PSAAOLD     GET CURRENT ASCB ADDRESS    @YM07549
         SPACE
         FREEMAIN RU,A=(1),LV=(0),SP=230,BRANCH=YES,KEY=0      @ZA17779
         SPACE
         LTR   TOPREG,WKREGA       DETERMINE IF ANOTHER BLOCK  @YM06170
         BNZ   REST905             IS ONE, GO FREE IT           @YM1141
         EJECT
REST910  EQU   *                   RELEASE THE LOCAL LOCK      @YM08365
         LR    REG15,SAVREG        SAVE R13,LOCK MGR DESTROYS  @YM08365
         SETLOCK RELEASE,TYPE=LOCAL,                                   *
               RELATED=(IECVEXPR,REST903)
         LR    SAVREG,REG15        RESTORE REG13 ON RETURN     @YM08365
         SPACE
         LM    REG0,REG15,C0(SAVREG) RESTORE REGISTERS
         SLR   REG15,REG15         ZERO OUT RETURN CODE REG    @YM05529
         BR    REG14               RETURN TO RESTORE
        TITLE 'IECVEXPR - IECVEXFRR - EXCP FUNCTIONAL RECOVERY ROUTINE'
         PRINT OFF
*/*IECVXFRR: CHART */
*/* HEADER
*/*                    EXCP FUNCTIONAL RECOVERY ROUTINE
*/*                                                      PAGE # */
*/*IECVXFRR: E EXCP FRR */
*/* P ESTABLISH ADDRESSABILITY */
*/* P ESTABLISH SDWA BASE REGISTER */
*/* P ESTABLISH FRR AREA REGISTER */
*/* D (NO,FRR015,YES,) PROGRAM CHECK */
*/* D (NO,FRR004,YES,) HAS RQE BEEN INIT */
*/* D (NO,FRR004,YES,) PAGE FAULT OUT OF XLATOR */
*/* P (,FRR010) SET ABEND CODE TO 800 ABEND */
*/*FRR004: D (YES,FRR015,NO,) PERCOLATED TO HERE */
*/* P SET CODE TO A00 ABEND */
*/* D (YES,FRR010,NO,) AN APPENDAGE ERROR */
*/* P SET CODE TO 200 ABEND */
*/* D (YES,FRR010,NO,) KEY CHECK FOUND ERROR */
*/*FRR005: P SET CODE TO B00 FOR ALL OTHERS */
*/*FRR010: L SETRP: SET THE COMPLETION CODE IN SDWA */
*/*FRR015: L GETMAIN: GET CORE FOR SDBA IN SP 0 */
*/* P CLEAR THE FIRST 512 BYTES OF THE AREA */
*/* P STORE THE AREA ADDRESS IN THE TCB */
*/* D (NO,FRR020,YES,) PROGRAM CHECK */
*/* P MOVE THE PSW AND EXTENDED INFORMATION TO THE XDBA */
*/*FRR020: P MOVE THE COMPLETION CODE FROM SDWA TO XDBA */
*/* P MOVE THE FLAG FROM THE FRR AREA TO XDBA, SET SIGHT ID */
*/* D (NO,FRR070,YES,) RQE OBTAINED YET */
*/* P MOVE ENTIRE RQE TO THE XDBA */
*/* D (NO,FRR050,YES,) SRB/IOSB OBTAINED YET */
*/* P SET THE POINTER TO THE ORIGINAL SRB AND MOVE IN THE SRB/IOSB */
*/* D (NO,FRR050,YES,) 800 OR 900 ABEND */
*/* P SET THE POINTER TO THE ORIGINAL TCCW AND MOVE IN THE TCCW */
*/* P SET AN INDEX INTO THE REMAINING AREA IN THE DEBUGGING AREA */
*/* D (YES,FRR040,NO,) 800 ABEND CONDITION */
*/* P LOAD POINTER TO THE FIRST BEB */
*/*FRR030: D (YES,FRR050,NO,) END OF CHAIN */
*/* P MOVE ORIGINAL BLOCK POINTER AND THE BLOCK TO THE XDBA */
*/* P (,FRR030) INCREMENT TO NEXT IN CHAIN AND NEXT AREA IN BLOCK */
*/*FRR040: P (,FRR030) LOAD POINTER TO THE FIRST FIX BLOCK */
*/*FRR050: P SET PERMANENT ERROR IN THE DCB */
*/* D (YES,FRR060,NO,) POST STATUS APPENDAGE PROC. */
*/* L (,FRR070) PURGINTF: FREE ALL STORAGE AREAS */
*/*FRR060: P SET POST CODE TO X'45' */
*/*FRR070: P MOVE REGISTERS AT TIME OF ERROR INTO XDBA */
*/* P SETFRR: DELETE THE RECURSION FRR */
*/* D (NO,FRR080,YES,) SRB ENTRANCE TO EXCP */
*/* P SETRP: FREE THE LOCAL LOCK */
*/*FRR080: R RETURN TO RTM */
         PRINT ON
***********************************************************************
*                                                                     *
*                       FUNCTIONAL RECOVERY ROUTINE                   *
*                                                                     *
*        EXCP FUNCTIONAL RECOVERY ROUTINE ACCOMPLISHES TWO PURPOSES.  *
*        THESE ARE SERVICEABILITY AND RELIABILITY. FOR SERVICABILITY, *
*        EXCP ISSUES A GETMAIN FOR STORAGE IN SUBPOOL ZERO. IT        *
*        PLACES THE POINTER TO THIS AREA IN THE TCB OF THE REQUEST.   *
*        THIS AREA CONTAINS INVORMATION REGARDING THE ERROR CONDITION *
*        SUCH AS REGISTERS, PSW, RQE, AND TRANSLATION BLOCKS.         *
*                                                                     *
*        FOR RELIABILITY, ALL EXCP RESOURCES ARE RECLAIMED IN ALL     *
*        CASES EXCEPT WHEN THE PCI, CHE OR ABE APPENDAGES HAD HAD     *
*        CONTROL. IN THE CASE OF THE APPENDAGES MENTIONED ABOVE,      *
*        THIS ROUTINE PERCOLATES TO POST STATUS FRR WHICH ENTERS      *
*        A DRIVER RESOURCE MANAGER - AS OF NOW, THE TERMINATION       *
*        ROUTINE.                                                     *
*                                                                     *
*        REGISTERS ON ENTRY ARE AS FOLLOWS;                           *
*                                                                     *
*        REGISTER  0  -  ADDRESS OF 200 BYTE WORK AREA                *
*        REGISTER  1  -  ADDRESS OF THE SYSTEM DIAGNOSTIC WORK AREA   *
*        REGISTER  14 -  RETURN ADDRESS TO RTM                        *
*        REGISTER  15 -  ENTRY POINT ADDRESS                          *
*                                                                     *
***********************************************************************
EXPRFRR  DC    CL8'IECVXFRR'       FRR EP NAME                 @ZA06070
         SPACE 2
         USING PURBASE,BASREG      MODULE ADDRESSABILITY        @YM3145
         USING SDWA,SDWAREG        SDWA ADDRESSABILITY
         USING RQE,RQEREG          RQE ADDRESSABILITY
         USING GTM,GTMREG          WORK AREA DSECT ADDRESSING   @YM3145
         USING FRREXCP,FRRREG      FRR PARM AREA ADDRESSABILITY@ZA12704
IECVXFRR LA    BASREG,IECVXFRR-PURBASE(,0) ADJUST BASE ADDRESS  @YM3145
         SLR   REG15,BASREG        SET BASE-PURGE SUBRTN BASE  @Y30IPLB
         LR    BASREG,REG15        TRANSFER BASE TO REGISTER 9  @YM3145
         LR    SDWAREG,REG1        SET UP BASE FOR SDWA
         LR    GTMREG,REG0         200 BYTE WORK AREA ADDRESS   @YM3145
         L     FRRREG,SDWAPARM     GET FRR PARM AREA
         L     RQEREG,FRRCRQE      GET CURRENT RQE POINTER      @YM2246
         LR    RTMRREG,REG14       SAVE RETURN ADDRESS          @YM3145
******** CHECK IF STORAGE MANAGER (IECVSMGR) WAS ACTIVE        @ZA24342
         TM    FRRTCNT,FRR40+FRR160  STORAGE MANAGER ACTIVE ?? @ZA24342
         BNZ   FRR100              YES MUST CALL SMGR FRR
         EJECT                                                 @ZA06070
         SLR   WKREG6,WKREG6       INIT WORKREG BEFORE ICM     @Y30IPLB
         ICM   WKREG6,XPRM3,SDWACMPC GET SYSTEM COMP. CODE     @YM06599
         SRL   WKREG6,C4           SAVE ONLY MEANINGFUL 12 BITS@YM06599
         STH   WKREG6,SDWAVABO     SAVE ORIGINAL ABEND CODE    @ZA06070
         LTR   RQEREG,RQEREG       IS THERE A RQE POINTER?     @YM06742
         BNP   FRR001              NO, CONTINUE ABEND TESTS    @YM06742
         SPACE
         TM    RQETYPE,RQEDIE      ABEND IN PCI APPENDAGE?     @YM05537
         BNO   FRR001              NO, THEN TEST PGM CHECK     @YM07285
         SPACE
*        NOTE -ERROR IN PCI APPENDAGE, REGISTERS ARE INVALID AND
*              THEREFORE WILL BE ZEROED OUT.
         SPACE
         OI    FRRFLAG,FRRPCI+FRRAACT INDIC ERROR IN ACTIVE    @YM07285
*                                  ACTIVE APPENDAGE            @YN07285
         LA    WKREGB,CODEA00      SET CORRECT ABEND CODE      @Y30IPLB
         XC    SDWAGRSV(C64),SDWAGRSV ZERO OUT REGS IN SDWA    @YM07285
         B     FRR010              USE A00 ABEND CODE          @YM05537
         SPACE
FRR001   EQU   *                                               @ZA06070
         LR    WKREGB,WKREG6       PRESET TO ORIGINAL ABEND    @ZA06070
         CH    WKREGB,ABEND028     SYSTEM ABEND CODE = 028 ??  @ZA16175
         BE    FRR002              YES, NEED CK FOR TCCW PG CK @ZA16175
         CH    WKREGB,ABEND171     SYSTEM ABEND CODE = 171 ??  @ZA16175
         BE    FRR002              YES, NEED CK FOR TCCW PG CK @ZA16175
         TM    SDWAERRA,SDWAPCHK   WAS ENTRY DUE TO PGM CHECK? @ZA06070
         BZ    FRR015              NO, GO GET WORK AREA
         LA    WKREGB,CODE700      SET ABEND CODE IN CASE      @OZ00085
*                                  OF PERCOLATION FROM ANOTHER FRR
         TM    SDWAERRC,SDWAPERC   WAS ANOTHER FRR IN CONTROL? @OZ00085
         BO    FRR010              YES, ISSUE 700 ABEND--      @OZ00085
FRR002   EQU   *                                               @ZA16175
         LA    WKREGB,CODE700      SET ABEND CODE TO 700.      @ZA16175
         LTR   RQEREG,RQEREG       ANY RQE YET?
         BNP   FRR004              NO, CANNOT BE IN TRANSLATOR  @YM3145
         TM    RQETYPE,RQEVAM      VIO RELATED REQ RECOVERY ?? @ZA06070
         BO    FRR010              YES, BYPASS TCCW CHECK      @ZA06070
         L     WKREG6,RQETCCW      PICKUP TCCW POINTER
         LTR   WKREG6,WKREG6       ANY TCCW?
         BZ    FRR004              NO, CONTINUE
         TM    TCCWMODB-TCCW(WKREG6),TCCWPGCK WAS IT A PAGE CHK ERROR?
         BZ    FRR004              NO
         MVC   TCCWSAVA+C2-TCCW(C2,WKREG6),SDWAVABO      MOVE  @ZA16175
*                                  ORIGINAL ABEND CODE TO TCCW @ZA16175
         ST    WKREG6,SDWASR11     SET TCCW PTR                @ZA16175
         MVC   SDWASR06,SDWASR15   SAVE ANY RETURN CODE        @ZA26258
         L     WKREGB,ACCWXFRR     GET CCW TRANSLATOR RETRY    @YM04960
*                                  * ROUTINE ADDRESS           @YM04960
*                                  * PARAMETERS                @YM04960
         SETRP RETADDR=(WKREGB),RETREGS=YES,RECORD=NO,RC=4     @YM04960
         LR    REG14,RTMRREG       RESTORE RETURN ADDRESS      @YM04960
         BR    REG14               RETURN TO RTM FOR RETRY     @YM04960
FRR004   EQU   *                   TEST FOR LOCATION OF ERROR  @OZ00085
         TM    SDWAERRC,SDWAPERC   WAS ANOTHER FRR IN CONTROL?
         BNZ   FRR010              LEAVE THE CODE AT 700        @YM3145
         LA    WKREGB,CODEA00      SET CODE TO A00             @Y30IPLB
         TM    FRRFLAG,FRRAACT     WAS ERROR IN ACTV APPENDAGE?@YM05505
         BNZ   FRR010              YES  USE A00 CODE           @YM04202
         SPACE
         LA    WKREGB,CODE200      SET CODE 200                @Y30IPLB
         ICM   WKREG6,XPRM3,SDWACMPC GET SYS COMPLETION CODE   @YM02855
         N     WKREG6,XPRZBITS     LEAVE HIGH 12 BITS TO TEST  @YM02855
         CH    WKREG6,XPR0C4       SYS COMP CODE = TO 0C4      @YM02855
         BNE   FRR005              NO , USE B00 COMP CODE      @YM02855
         SPACE
****     PROTECTION CHECK (0C4) OCCURRED IN IECVEXCP.          @ZA12706
*        IF 0C4 OCCURRED IN VALIDITY CHECK ROUTINES, ISSUE     @ZA12706
*        ABEND 200.                                            @ZA12706
         SPACE 1                                               @ZA12706
         L     WKREG6,SDWANXT1     ADDR NEXT INST TO BE EXEC   @ZA12706
         LA    WKREG6,C0(WKREG6)   ZERO HIGH ORDER BYTE        @ZA12706
         CL    WKREG6,EXPRVAL1     ERROR IN VAL CK ROUTINE ??  @ZA12706
         BL    FRR004A             NO,CK NEXT RANGE            @ZA12706
         CL    WKREG6,EXPRVAL2     ERROR IN VAL CK ROUTINE ??  @ZA12706
         BL    FRR010              YES, USE ABEND 200 CODE     @ZA12706
FRR004A  EQU   *                                               @ZA12706
         CL    WKREG6,EXPRVAL3     ERROR IN EXCP VAL CK ??     @ZA12706
         BL    FRR004B             NO, CK POST ECB VAL CK      @ZA12706
         CL    WKREG6,EXPRVAL4     ERROR IN EXCP VAL CK ??     @ZA12706
         BL    FRR010              YES, USE ABEND 200 CODE     @ZA12706
FRR004B  EQU   *                                               @ZA12706
         CL    WKREG6,EXPRVAL5     ERROR IN POST RTN ECB VAL?? @ZA12706
         BL    FRR005              NO, USE ABEND B00 CODE      @ZA12706
         CL    WKREG6,EXPRVAL6     ERROR IN POST RTN ECB VAL?? @ZA12706
         BL    FRR010              YES, USE ABEND 200 CODE     @ZA12706
         SPACE 2                                               @ZA12706
FRR005   EQU   *                                               @ZA27884
         LA    WKREGB,CODEB00          SET ABEND CODE TO B00.  @ZA27884
         CLC   SDWAVABO,ABEND171       WAS ORIGINAL ABEND 171? @ZA27884
         BE    FRR005B                 YES.                    @ZA27884
         CLC   SDWAVABO,ABEND028       WAS ORIGINAL ABEND 028? @ZA27884
         BNE   FRR010                  NO.                     @ZA27884
         SPACE 1                                               @ZA27884
FRR005B  EQU   *                                               @ZA27884
         LTR   RQEREG,RQEREG           AT THIS POINT THE RQE   @ZA27884
         BNP   FRR010                  AND TCCW SHOULD EXIST.  @ZA27884
         L     WKREG6,RQETCCW          CHECKING IS DONE TO     @ZA27884
         LTR   WKREG6,WKREG6           VERIFY THEIR EXISTANCE. @ZA27884
         BZ    FRR010                                          @ZA27884
         LH    WKREGB,ABEND800         SET ABEND CODE TO 800.  @ZA27884
         STH   WKREGB,TCCWSAVA+C2-TCCW(WKREG6)                 @ZA27884
         NI    RQEFLAG,FF-RQEFIXST     ASSUME 171 AND 028      @ZA27884
*                                      ABENDS ARE ONLY FROM    @ZA27884
*                                      NON-VIRTUAL EXCP        @ZA27884
*                                      REQUESTS.               @ZA27884
FRR010   EQU   *                                               @YM02855
         SPACE
*                                  SET RETURN PARAMETERS
         SPACE 1                                               @ZA06070
         SETRP COMPCOD=((WKREGB),SYSTEM),DUMP=YES,RECORD=YES,  @ZA15703*
               DUMPOPT=DUMPB00                                 @ZA15703
         SPACE 2                                               @ZA06070
* MOVE FOLLOWING DATA INTO THE SDWA VARIABLE DATA AREA         @ZA06070
FRR015   EQU   *                                               @ZA06070
         LTR   RQEREG,RQEREG       ANY RQE ??                  @ZA06070
         BNP   FRR015A                    NO                   @ZA12706
         MVC   SDWAVRQE(RQEBL),0(RQEREG)  YES, MOVE RQE AREA   @ZA06070
         TM    RQETYPE,RQEVIRT     VIRTUAL EXCP REQUEST ??     @ZA06070
         BZ    FRR015A              NO                         @ZA06070
         L     WKREG6,RQETCCW      YES                         @ZA06070
         LTR   WKREG6,WKREG6       TCCW BLOCK OBTAINED ??      @ZA06070
         BZ    FRR015A             NO, BYPASS                  @ZA06070
         MVC   SDWAVTOP,TCCWOPTN-TCCW(WKREG6) MOVE OPT BYTE    @ZA06070
         MVC   SDWAVFLG,TCCWMODB-TCCW(WKREG6) MOVE XLATE FLAGS @ZA06070
         CH    WKREGB,ABEND800                                 @ZA16175
         BNE   FRR015A                                         @ZA16175
         MVC   SDWAVABO,TCCWSAVA+C2-TCCW(WKREG6)               @ZA16175
         SPACE 1                                               @ZA06070
FRR015A  MVC   SDWAVLHI,PSAHLHI    MOVE HIGHEST LOCK HELD      @ZA06070
         MVC   SDWAVFRR(FRRXLEN),0(FRRREG)   MOVE FRR PARM     @ZA12704
         MVI   SDWAURAL,SDWAVLTH   SET LTH OF VAR AREA USED    @ZA06070
         STH   WKREGB,SDWAVABN     SAVE ABEND CODE             @ZA06070
         L     WKREG6,PSAAOLD      LOAD ASCB POINTER           @ZA06070
         MVC   SDWAVASI,ASCBASID-ASCB(WKREG6) MOVE ASID        @ZA06070
         OI    SDWADPVA,SDWAHEX    INDICATE TO DUMP IN HEX     @ZA06070
         MVC   SDWAMODN(RC16),XPRMODN    MOVE MOD ID AND PTF # @ZA12706
         MVC   SDWAREXN,EXPRFRR    FRR ROUTINE NAME            @ZA12706
         EJECT                                                 @ZA12706
         TM    SDWAEC1,DISPSW      IS PSW DISABLED?            @ZA05976
         BNO   FRR050              YES, DO NOT OBTAIN XDBA     @ZA05976
         SPACE
         LTR   RQEREG,RQEREG       DOES RQE EXIST?             @ZA06070
         BP    FRR017              BRANCH YES - GO GET TCB PTR @ZA05486
         CLI   FRRCRQE,FF          NO RQE, WAS TCB ADDR SAVED? @ZA05486
         BE    FRR018              BRANCH YES - GO GET IT      @ZA05486
         L     WKREG6,PSATOLD      NO TCB ADDR, USE CURRENT    @ZA05486
         B     FRR019              GO ON TO GETMAIN            @ZA05486
FRR017   L     WKREG6,RQETCB       GET TCB ADDRESS FOR GETMAIN @ZA05486
         B     FRR019              GO ON TO GETMAIN            @ZA05486
FRR018   LR    WKREG6,RQEREG       TCB @ SAVED INSTEAD OF RQE  @ZA05486
FRR019   LA    WKREG6,C0(WKREG6)   ZERO HI-ORDER BYTE FOR GETMN@ZA05486
         LR    RQEREG,WKREG6       TRANSFER TCB @ FOR GETMAIN  @ZA05486
*                                  WKREG6 MAINTAINED OVER GETMN@ZA05486
         L     WKREG7,PSAAOLD      GET ASCB ADDRESS FOR GETMAIN
         LR    WKREGA,RTMRREG      SAVE RTM RETURN ADDR REG     @YM3145
         SPACE 1                                               @ZA12706
         GETMAIN   RU,LV=XDBASIZE,SP=230,BRANCH=YES,KEY=0      @ZA15702
         SPACE 1                                               @ZA12706
         LR    RTMRREG,WKREGA      RESTORE RTM RETURN ADDR REG  @YM3145
         USING XDBA,REG1           ADDRESSABILITY FOR XDBA
         XC    XDBA(TWO56),XDBA    CLEAR FIRST 256 BYTES
         XC    XDBA+TWO56(TWO56),XDBA+TWO56  CLEAR NEXT 256
         MVC   XDBACC(C2),SDWAVABO STORE ORIG ABEND CODE       @ZA06070
         L     WKREGB,TCBEXCPD-TCB(WKREG6)  LOAD PREVIOUS PTR  @ZA15703
         ST    WKREGB,XDBACHAN              AND SAVE IN XDBA   @ZA15703
         ST    REG1,TCBEXCPD-TCB(WKREG6) SAVE DEBUG PTR IN TCB @YM04297
         SPACE 1                                               @ZA06070
         TM    SDWAERRD,SDWARPIV   REGS & PSW VALID FROM ERROR @YM02855
         BO    FRR022              NO, BYPASS THE SAVE         @YM02855
         SPACE
         MVC   XDBAPSW,SDWAEC1     YES, THEN SAVE PSW AND      @YM02855
*                                  REGISTERS - DEBUG AREA      @YM02855
         MVC   XDBARGSV(XDBARQE-XDBARGSV),SDWAGRSV             @YM02855
         EJECT                                                 @ZA12706
FRR022   EQU   *                                               @YM05512
         ICM   WKREGB,XPRM3,SDWACMPC GET SYS COMPLETION CODE   @YM05512
         SRL   WKREGB,C4           LEAVE 12 BITS-SYS CODE      @YM05512
         STH   WKREGB,XDBACOMP     SAVE IN EXCP DEBUG AREA     @YM05512
         MVC   XDBAFLAG,FRRFLAG    MOVE FRR FLAG
         L     RQEREG,FRRCRQE      GET POINTER TO RQE
         LTR   RQEREG,RQEREG       ANY RQE
         BNP   FRR070              NO                          @YM06923
         MVC   XDBARQE,0(RQEREG)   MOVE IN RQE
         MVC   XDBATRAN(C4),SDWATRAN MOVE TRANS. EXCEP ADDR    @ZA06070
         TM    RQETYPE,RQEVAM      VIO RELATED REQ RECOVERY    @YM06502
         BO    FRR038              YES, INSURE RQETCCW ZEROED  @YM06502
         SPACE 1                                               @ZA06070
* MOVE THE 160 BYTE BLOCKS TO THE DEBUGGING AREA IN FOLLOWING  @ZA06070
* ORDER (IF PRESENT): EWA, SRB/IOSB, TCCW, IDAL, FIX AND BEB.  @ZA06070
         SPACE 1                                               @ZA06070
         LA    WKREGA,XDBAENT      POINT TO 1ST 160B BLOCK     @ZA06070
         USING XDBAENT,WKREGA      ADDRESSABILITY TO 160B BLKS @ZA06070
         LA    WKREG7,XDBABLKS     LIMIT OF 160B BLOCKS        @ZA06070
         SPACE 1                                               @ZA06070
         CLI   FRRFCNT,C0          ANY 160B BLKS ON FREE CHAIN @ZA06070
         BE    FRR030              NO, MOVE INDIVIDUALLY       @ZA06070
         L     WKREGB,FRRSTRG      YES, SET START ADDR AND     @ZA06070
         B     FRR034A                  GO MOVE CHAIN TO XDBA  @ZA06070
         SPACE
*              MOVE INDIVIDUAL TYPE BLOCKS TO XDBA             @ZA06070
FRR030   EQU   *                                               @ZA06070
         L     WKREG6,RQESRB-RQE(,RQEREG)  SRB PTR IN RQE      @ZA06070
         LTR   WKREG6,WKREG6       SRB/IOSB BLOCK OBTAINED ??  @ZA06070
         BZ    FRR050              NO, NO 160B BLKS GOTTEN YET @ZA06070
         L     WKREGB,SRBSIZE+IOSERP-IOSB(,WKREG6) EWA BLOCK   @ZA06070
         BAL   REG14,FRR037        MOVE ANY EWA BLOCKS(S)      @ZA06070
         MVC   XDBAENT,C0(WKREG6)  MOVE IN SRB/IOSB BLOCK      @ZA06070
         SR    WKREGB,WKREGB       ZERO MOVE POINTER AND       @ZA06070
         BAL   REG14,FRR037A       GO ADJUST POINTERS          @ZA06070
         SPACE 2                                               @ZA06070
         L     WKREG6,RQETCCW      TCCW POINTER FROM RQE       @ZA06070
         LTR   WKREG6,WKREG6       TCCW POINTER ACTIVE ??      @ZA06070
         BZ    FRR050              NO, NO FUTHER BLOCKS        @ZA06070
         MVC   XDBAENT,C0(WKREG6)  MOVE TCCW TO XDBA BUFFER    @ZA06070
         BAL   REG14,FRR037A       GO ADJUST POINTERS          @ZA06070
         L     WKREGB,TCCWTCB-TCCW(,WKREG6)  LOAD TCB ADDR??   @ZA06070
         CLM   WKREGB,XPRM7,RQETCB+C1  IS 1ST WORD THE TCB ADR @ZA06070
         BNE   FRR034A             NO, THEN TCCW CHAINED       @ZA06070
         SPACE
         TM    RQETYPE,RQE114+RQE1TO1  EXCPVR OR V=R REQUEST   @ZA06070
         BNZ   FRR034B             YES, BYPASS                 @ZA06070
         SPACE 1                                               @ZA06070
FRR034   EQU   *                                               @ZA06070
         L     WKREGB,TCCWINDA-TCCW(,WKREG6) IDAL BLOCKS       @ZA06070
         BAL   REG14,FRR037                                    @ZA06070
         L     WKREGB,TCCWFIX-TCCW(,WKREG6)  FIX BLOCKS        @ZA06070
         BAL   REG14,FRR037                                    @ZA06070
         L     WKREGB,TCCWBEB-TCCW(,WKREG6) BEB BLOCKS         @ZA06070
FRR034A  EQU   *                                               @ZA06070
         BAL   REG14,FRR037                                    @ZA06070
FRR034B  EQU   *                                               @ZA06070
         LTR   WKREG7,WKREG7       ANY 160 BYTE FIELDS LEFT ?? @ZA06070
         BZ    FRR050              NO                          @ZA06070
         XC    0(XDBAEL,WKREGA),0(WKREGA)  YES, ZERO NXT FLD   @ZA06070
         B     FRR050              CONTINUE                    @ZA06070
         SPACE
FRR037   EQU   *                                               @ZA15704
         LA    WKREGB,C0(WKREGB)   INSURE HI-ORDER BYTE ZERO   @ZA15704
         LTR   WKREGB,WKREGB       END OF CHAIN ??             @ZA15704
         BZR   REG14               YES, RETURN                 @ZA06070
         MVC   XDBAENT,C0(WKREGB)  MOVE IN A BLOCK             @YM07354
         L     WKREGB,C0(,WKREGB)  GET POINTER TO NEXT BLOCK   @ZA06070
FRR037A  EQU   *                                               @ZA06070
         LA    WKREGA,XDBAEL(,WKREGA) INCREMENT XDBA POINTER   @ZA06070
         BCT   WKREG7,FRR037       LIMIT OF 160B REACHED?      @ZA06070
         B     FRR050              YES, NOW GO TEST ENTRY      @ZA06233
         SPACE
FRR038   EQU   *                                               @YM06502
         XC    RQETCCW(C20),RQETCCW INSURE WORKAREA ZEROED-VIO @YM06502
         SPACE
FRR050   EQU   *                                               @YM05515
         SPACE 2                                               @ZA06070
         TM    FRRFLAG,FRRBKE      BACK END ENTRY?             @ZA05976
         BO    FRR057              DETERMINE IF POST SCHEDULED @ZA06811
*                                  THE TERMINATION RTN
         TM    FRRFLAG,FRRFTE      FRONT END ENTRY?            @ZM05976
         BZ    FRR060              NO, SET IOS CODE            @ZM32371
         CLI   RQEFLAG3,RQEINIOS   FRONT END ENTRY, NOW IS     @ZM32371
         BE    FRR070              REQUEST IN IOS, BR IF YES   @ZM32371
         SPACE
         EJECT                                                 @ZA06070
FRR055   OI    RQEFLAG,RQENOPST    SET NO POST FLAG IN RQE     @YM05535
         BAL   REG14,PURGINTF      GO FREE ALL STORAGE AREAS
         B     FRR070              CODE ALREADY SET
         SPACE
FRR057   TM    FRRFLAG,FRRCHE+FRRABE BACKEND ENTRY VIA APPEND  @ZA06811
         BZ    FRR055              NO, CONTINUE NORMAL TERM END@ZA06811
         SPACE
         OI    RQEFLAG,RQENOPST    YES, PERCOLATE TO POST AND  @ZA06811
         B     FRR070              SCHEDULE TERM ROUTINE       @ZA06811
         SPACE
FRR060   L     WKREG2,RQESRB       POINT TO SRB
         L     WKREG2,SRBPARM-SRB(,WKREG2) POINT TO IOSB
         MVC   SDWAVIOC,IOSCOD-IOSB(WKREG2)  SAVE COMPL CODE.  @ZA06070
         MVI   IOSCOD-IOSB(WKREG2),IOSABNC SET IOS ABEND CODE  @YM05517
         SPACE
FRR070   EQU   *                                               @YM02855
         TM    FRRFLAG,FRRBKE      BACK END ENTRY?             @ZM05976
         LR    REG1,SDWAREG        SET REG FOR SETRP           @ZA06222
*                                  FREE THE LOCAL LOCK         @ZA06222
         BZ    FRR080              NO LOCK NOT HELD
         SPACE
         SETRP FRELOCK=LOCAL,DUMP=YES,RECORD=YES               @ZA06070
         SPACE
FRR080   EQU   *                                               @YM06597
*                            TEST FOR RECORDING ABEND          @ZA12706
         TM    SDWAEC1,DISPSW      DISABLED PSW  ??            @ZA06070
         BNO   FRR090              YES, RECORD ON LOGREC       @ZA06070
FRR081   EQU   *                                               @ZA06070
         LA    WKREGB,CODE700                                  @ZA06070
         CH    WKREGB,SDWAVABN     ABEND CODE LESS THAN 700??  @ZA06070
         BNH   FRR090              NO, IGNORE RECORDING        @ZA06070
FRR082   EQU   *                                               @ZA06070
         SPACE 1                                               @ZA06070
         SETRP RECORD=NO,DUMP=YES,DUMPOPT=DUMPX00              @ZA15702
         SPACE 2                                               @ZA06070
FRR090   SLR   REG15,REG15         CLEAR REG 15                @Y30IPLB
         LR    REG14,RTMRREG       RESTORE RETURN ADDRESS       @YM3145
         BR    REG14               RETURN TO CALLER
         EJECT
******** IECVSMGR (STORAGE MANAGER) -                          @ZA06070
********       . A 40 BYTE BLOCK GET OR FREE REQ ACTIVE.       @ZA06070
********       . A 160 BYTE BLOCK GET OR FREE REQ ACTIVE       @ZA24342
********       AFTER SETUP BY EXPR, RETURN IS MADE TO THE      @ZA06070
********       IECVSMGR FRR ROUTINE.  THE SMGR FRR ROUTINE     @ZA06070
********       WILL DETERMINE WHETHER TO RETRY OR PERCOLATE.   @ZA06070
********       THE SMGR FRR ROUTINE WILL RETURN TO THE EXPR    @ZA06070
********       FRR ROUTINE.  THE EXPR FRR ROUTINE WILL THEN    @ZA06070
********       ACT ON THE SMGR FRR ACTION.                     @ZA06070
********       THE 200 BYTE WORK AREA PASSED BY RTM IS USED    @ZA06070
********       FOR THE QVPL, FRR PARM AREA AND FOR EXPR        @ZA06070
********       REGISTER SAVE AREA.                             @ZA06070
         USING QVPL,GTMREG         WORK AREA ADDRESSABILTY
FRR100   EQU   *                   INTERFACE WITH IECVSMGR FRR
         LA    WKREG7,QFRRPARM     FRR PARM AREA ADDR FOR SMGR @ZA12704
         DROP  GTMREG
         USING GTM,GTMREG          RESET GTM DSECT ADDRESSABILITY
         USING FRRSMGR,WKREG7      DUMMY SMGR FRR              @ZA12704
         ST    FRRREG,FRREXCPS     SAVE ADR OF EXCP FRR AREA
         ST    BASREG,FRRBASE      SAVE BASE REGISTER
         ST    RTMRREG,FRRRRTM     RETURN ADR OF RTM
         ST    WKREG7,SDWAPARM     STORE NEW FRR PARM AREA
         L     WKREGA,FRRLLSA      GET LOCAL LOCK SAVE AREA
         SPACE 2                                               @ZA24342
         TM    FRRTCNT,FRR40       40 B BLOCK REQUEST ??       @ZA24342
         BO    FRR102              YES,                        @ZA24342
         MVC   FRRSVA(FRRSLEN),C48(WKREGA) NO, MOVE FROM LLSA. @ZA24342
         B     FRR106                                          @ZA24342
         SPACE 1                                               @ZA24342
FRR102   EQU   *                                               @ZA24342
         LM    SAVREG,REG14,R13DISP(WKREGA) GET ORIGINAL R13,R14
         STM   SAVREG,REG14,FRRSVA STORE SAVE AND RETURN REGS
         L     REG15,PSAAOLD       PUT ASID IN THE             @ZA16162
         LH    REG15,ASCBASID-ASCB(REG15) PSEUDO FRR FOR THE   @ZA16162
         STH   REG15,FRRASID       STORAGE MANAGER             @ZA16162
         MVI   FRRFLA,C0           INITIALIZE THE FLAG         @ZA16162
         MVI   FRRFLB,C0           BYTES.                      @ZA16162
         TM    FRRTCNT,FRRG40      GET FORTY REQUEST
         BNO   FRR105              NO -FREE REQUEST
         OI    FRRFLA,FRRG40       TURN ON SMGR FRR GET REQ
FRR105   OI    FRRFLA,FRRMED       TURN ON SMGR FRR FREE REQ
FRR106   EQU   *                                               @ZA24342
         LR    REG0,GTMREG         LOAD ADDRESS OF 200 BYTE WORKAREA
         L     REG15,SMGRFRR       PREPARE TO GO TO SMGR
         BALR  REG14,REG15         GO TO CORE MANAGER
         EJECT                                                 @ZA06070
* RETURN ENTRY FROM IECVSMGR - STORAGE MANAGER                *@ZA06070
         LR    WKREG7,LSTREG       RESTORE PTR-FRR (SMGR USED) @YM03281
         L     BASREG,FRRBASE      RESTORE BASE REGISTER
         L     REG14,FRRRRTM       RESTORE RETURN ADDRESS
         L     FRRREG,FRREXCPS     RESTORE POINTER TO EXCP FRR
         ST    FRRREG,SDWAPARM     RESTORE SDWAPARM TO ORIGINAL STATE
         NI    FRRTCNT,FF-FRRG40   TURN OFF 40 BYTE BLOCK REQUEST
         NI    FRRTCNT,FF-FRRG40-FRR160  RESET FLAGS.          @ZA24342
         LA    REG15,IECVXFRR      RESTORE R15
         SPACE 1                                               @ZA06070
         TM    SDWARCDE-SDWA(REG1),SDWARETY  RETRY REQUESTED ? @ZA15705
         BZ    IECVXFRR            NO-RETURN TO TOP OF FRR SUB @ZA15705
         SPACE 1
**************************************************************
*        THE 200 BYTE WA WILL BE FREED BY RTM BEFORE RETRY IS* @ZA15706
*        EXECUTED. SMGR RETRY LOGIC LOOKS AT THE FLAG BYTES. * @ZA15706
*        THE LAST 6 WORDS OF THE LLSA WILL BE TREATED AS THE * @ZA15706
*        SMGR FRR PARM AREA.  ONLY THE WORD CONTAINING THE   * @ZA15706
*        ASID AND FLAGS ARE MOVED (BYTES 56-59).  THE REST OF* @ZA15706
*        THIS PARM AREA MUST NOT BE ALTERED BY THIS ROUTINE. * @ZA15706
**************************************************************
         SPACE 1                                               @ZA15706
         L     WKREGA,FRRLLSA      LD LLSA PTR FROM REAL FRR   @ZA15706
         L     WKREGB,FRRASID      LOAD ASID AND FLAGS         @ZA15706
         LA    WKREG7,C48(WKREGA)  REPOINT SMGR FRR AREA PTR   @ZA15706
         ST    WKREGB,FRRASID      STORE ASID AND FLAGS        @ZA15706
         ST    WKREG7,SDWASR04-SDWA(REG1)  STORE NEW FRR PTR   @ZA15706
*                                          IN SDWA             @ZA15706
         SPACE 1                                               @ZA15706
         BR    REG14               RETURN TO RTM TO RETRY
         EJECT
ATERMRTN DC    V(IECVXTRM)         ADDRESS OF PURGE RTN
SMGRFRR  DC    V(IECVSMFR)         STORAGE MANAGER FRR ROUTINE
AX025    DC    V(IECVX025)         FREE RQE RTN
ACCWXLAT DC    V(IECVTCCW)         CCW TRANSLATOR ADDRESS
ACCWXFRR DC    V(IECVTCFR)         RECOVERY RTN IN IECVTCCW    @YM04960
AOPT02   DC    V(IEA0PT02)         POST WITH VALIDITY CHECK
XPRZBITS DC    X'0000FFF0'         BITS TO SAVE SYS COM CODE-N @YM02855
EXPRVAL1 DC    V(IECVEXP1)         START- EXCP VAL CK RTN      @ZA12706
EXPRVAL2 DC    V(IECVEXP2)           END- EXCP VAL CK RTN      @ZA12706
EXPRVAL3 DC    V(IECVEXP3)         START- EXCP VALIDITY CK     @ZA12706
EXPRVAL4 DC    V(IECVEXP4)           END- EXCP VALIDITY CK     @ZA12706
EXPRVAL5 DC    V(IECVEXP5)         START- EXCP POST ECB VAL CK @ZA11892
EXPRVAL6 DC    V(IECVEXP6)           END- EXCP POST ECB VAL CK @ZA11892
XPR0C4   DC    X'0C40'             USED TO TEST FOR COM CODE   @YM02855
ABEND028 DC    X'0028'             ABEND CODE 028              @ZA16175
ABEND171 DC    X'0171'             ABEND CODE 171              @ZA16175
ABEND800 DC    X'0800'             ABEND CODE 800
ZEROS    DC    X'00'               BYTE OF ZEROS
         SPACE 3
XPRVSAM  DEBCHK AM=VSAM,MF=L                                   @YM02921
XPRSUBSY DEBCHK AM=SUBSYS,MF=L                                 @YM02921
XPRTCAMP DEBCHK AM=TCAMAP,MF=L                                 @YM02921
         SPACE 2
DUMPB00  SNAP  PDATA=(PSW,REGS,ALLPA,SPLS),                    @ZA15702*
               SDATA=(NUC,SQA,LSQA,SWA,TRT),MF=L               @ZA15702
DUMPX00  SNAP  PDATA=(SPLS),SDATA=(TRT),MF=L                   @ZA15702
XPRENDFF DC    X'FFFFFF'           FF'S TO TEST END IOB CHAIN @YM05531
XPREND00 DC    X'000000'           00'S TO TEST END IOB CHAIN @YM05531
XPR254   DC    X'FE'               SP-254 FOR PIRL            @Y30IPLD
         CNOP  0,4                                             @ZA06070
XPRMODN  DC    C'IECVEXPR'         MODULE ID                   @ZA06070
         DC    CL8' UZ15531'       PTF NUMBER FOR 030/037      @ZA27884
PATCH    DC    4CL16'IECVEXPR PATCH'    ZAP AREA               @ZA08162
         END
