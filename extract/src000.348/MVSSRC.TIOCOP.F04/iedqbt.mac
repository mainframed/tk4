BT       TITLE '''IEDQBT'' - EOB/ETB ERROR RETRY MODULE'
IEDQBT   CSECT
         SPACE 3
*CHANGE ACTIVITY AS FOLLOWS
******************** MICROFICHE FLAGS *********************** SUPT CODE
*A191000                                                         S99228
*A105200-106500,247300-247600                                    S22029
*D100000-101000,188000-190000,278760-278840                      S22029
*C105000,106000,753000                                           S22029
*A119000,173000,185000,396000,542000,619000,679000,721000,756000 S22025
*A758000,761000                                                  S22025
*C091200,160000,167000,205000,360000-369000,372000-387000,386000 S22025
*C386000,398000-412000,425000,4362000,463000-464000,             S22025
*C468000-464000,468000-479000,483000,485000,513000-519000,       S22025
*C683400-689000,713000-714000,717000-719000                      S22025
*D326000-339000,384300,414000-416000,418000                      S22025
*D433200-433600,612000,617000,720000                             S22025
*A714200,726000                                                 SA55402
*D240000                                                        SA51793
*C109000,112000                                                 SA56631
*A119007-119091                                                  S22024
*A174200-174800,202100-202700,277300-277600,553070-553840        S22024
*A660300-660600,715030-715840                                    S22024
*C179200-181200,213000-222000,768000                             S22024
*A605000                                                        SA63639
*C308200                                                        SA63640
*D301300                                                        SA63640
*C682000                                                         X01004
*D721000                                                         X01004
*A 360000,379200,641000                                         SA63962
*D 637000                                                       SA63962
*A104000,105200                                                  Y06327
*D105300,105400                                                  A65901
*A105900                                                         A65901
*A105200                                                        0Y01869
*C106400,107000                                                 0Y01869
*      OY01869 IS LOGICAL DUP OF                                SA66591
*A557000                                                       @SA72449
*A715270                                                        SA68245
*C222000,553210,715780-715840                                   SA68245
*D146000,148000-152000                                          SA68678
*A145000,147000,273000,742000                                  @XA05300
*A553840,742000                                                @SA70192
*A426000                                                       @SA70682
*C252000-253000                                                @XA05300
*A683200                                                        SA69621
*D105930-105960                                                @YA09313
*A195000                                                       @XA09327
*A195300                                                       @XA09327
*A202400,553890,662000                                         @Y17XANG
*C470000                                                       @OS79713
*A742600                                                       @OY16454
*C702000                                                       @OY16454
*C427500,430000                                                @OY20146
*A257000,273360                                                @OZ31599
*C463000,466000                                                @0Z33243
         SPACE 3
***********************************************************************
*TITLE -- 'IEDQBT' EOB/ETB HANDLING SUBTASK                           *
*                                                                     *
*  MODULE NAME = IEDQBT                                               *
*                                                                     *
*  DESCRIPTIVE NAME = EOB/ETB ERROR RETRY MODULE                      *
*                                                                     *
*  COPYRIGHT = 'NONE'                                                 *
*                                                                     *
*STATUS -- CHANGE LEVEL 7                                        X03039
*                                                                     *
*FUNCTION -- THIS SUBTASK GAINS CONTROL WHEN A BUFFER IS TPOSTED TO   *
*  STARTMH AND THE STARTMH OPERANDS SPECIFY THAT SOME FORM OF EOB/ETB *
*  HANDLING IS TO BE DONE.  IF THE BUFFER IS NOT MARKED LAST, EXIT    *
*  IS MADE TO THE TCAM DISPATCHER AT DSPBYPAS TO PERFORM A BYPASS     *
*  FUNCTION TO MODULE IEDQAA.  IF THE BUFFER IS MARKED LAST,          *
*  INDICATING THAT AN EOB/ETB APPEARS IN THE BUFFER,  A CHECK IS      *
*  MADE FOR A TEXT ERROR.                                        X03039
*  IF AN ERROR HAS OCCURRED FOR A NON 3705 TERMINAL, RETRY IS    X03039
*  ATTEMPTED BY BACKING UP THE CCW ADDRESS IF A PREVIOS EOB/ETB       *
*  OCCURRED IN THE CURRENT BUFFER OR BY RECALLING A PREVIOUSLY        *
*  RECEIVED/SENT BUFFER.  IF NO ERROR OCCURRED, (FOR RECEIVE          *
*  OPERATIONS ONLY) A CHECK IS MADE FOR THE EOB/ETB OPTIONS           *
*  SELECTED.  IF A USER EXIT WAS SPECIFIED, A BRANCH AND LINK IS      *
*  DONE TO PASS CONTROL.  THE CONV= OPERAND IS THEN CHECKED TO        *
*  DETERMINE IF THE EOB/ETB JUST RECEIVED IS TO BE TREATED AS END     *
*  OF MESSAGE.  IF NOT, THE BUFFER IS TPOSTED TO CONTINUE        X03039
*  MESSAGE RECEPTION.  FOR SEND OPERATIONS, ONLY THE LAST BUFFER      *
*  OF A MESSAGE OR ONE WITH AN EOB/ETB ERROR WILL BE MARKED LAST.     *
*  IF A PERMANENT ERROR OCCURS, THE STOP/CONT OPTIONS ARE CHECKED AND *
*  THE MESSAGE CONTINUES OR IS ABORTED BASED ON THE OPTIONS.
*                                                                     *
*ENTRY POINT -- IEDQBT                                                *
*                                                                     *
*   CALLING SEQUENCE -                                                *
*                  L     R1,ELMAD                                     *
*                  L     R15,QCBSTCHN-1                               *
*                  LA    R15,4(R14)                                   *
*                  BR    R15                                          *
*                                                                     *
*INPUT -- ENTRY IS ALWAYS FROM THE TCAM DISPATCHER                    *
*         PARAMETERS ARE PASSED IMPLICITLY IN REGISTERS AS FOLLOWS:   *
*                  R1    ADDRESS OF THE PASSED ELEMENT -              *
*                        BUFFER OR ERB                                *
*                  R11   BASE FOR TCAM DISPATCHER                     *
*                  R13   ADDRESS OF AVTSAVE2                          *
*                  R15   BASE FOR THIS ROUTINE                        *
*                                                                     *
*OUTPUT -- IF NO ERROR OCCURRED, A USER EXIT MAY BE TAKEN, AN EOB/ETB *
*  RECEIVED MAY BE TREATED AS END OF MESSAGE OR MESSAGE RECEPTION IS  *
*  CONTINUED.  NON-PERMANENT ERRORS FOR NON 3705 TERMINALS ARE   X03039
*  RETRIED.  IF AN ERROR IS PERMANENT, THE MESSAGE IS EITHER     X03039
*  ABORTED OR CONTINUED AFTER THE POINT OF ERROR.                X03039
*                                                                     *
*EXTERNAL ROUTINES -- IEDQAE                                          *
*                                                                     *
*EXITS - NORMAL -- THE TCAM DISPATCHER AT ENTRY POINT DSPPOST OR      *
*  DSPBYPASS OR DSPCHAIN.                                        S22025
*                                                                     *
*EXITS-ERROR -- NONE                                                  *
*                                                                     *
*TABLES/WORKAREAS --                                                  *
*    DSECTS USED - LCB,SCB,AVT,DIS,PRF,PRI                            *
*                                                                     *
*ATTRIBUTES -- REUSEABLE, REFRESHABLE, ENABLED, RESIDENT, PROBLEM     *
*              PROGRAM MODE.                                          *
*                                                                     *
*NOTES -- THE OPERATION OF THIS MODULE DOES NOT DEPEND UPON A         *
*   PARTICULAR INTERNAL REPRESENTATION OF THE EXTERNAL CHARACTER SET  *
*                                                                     *
***********************************************************************
R0       EQU   0                        REG 0                    S22024
R1       EQU   1                        PASSED ELEMENT (BUFFER)
R2       EQU   2                        REG 2                    S22024
R3       EQU   3                        BASE REGISTER
RLCB     EQU   4                        LCB ADDRESS
R5       EQU   5                        REG 5                    S22024
RPREFIX  EQU   6                        REG 6                    S22024
R7       EQU   7                        STARTMH PARAMETER LIST (QCB)
RSCB     EQU   8                        SCB ADDRESS
R9       EQU   9                        REG 9                    S22024
R10      EQU   10                       REG 10                   S22024
R11      EQU   11                       DISPATCHER BASE
R12      EQU   12                       REG 12                   S22024
R13      EQU   13                       AVTSAVE2
R14      EQU   14                       REG 14                   S22024
R15      EQU   15                       REG 15                   S22024
         EJECT
         USING *,R3
         USING STARTMH,R7
         USING IEDQDISP,R11
         USING AVTSAVE2,R13
         USING IEDQSCB,RSCB                                     OY01869
*
         DC    AL1(DSPMCPL8)            SUBTASK ENTRY CODE
         DC    C'EDQBT'                 MODULE ID
         IEDHJN
         SPACE 3
         SR    R2,R2                    CLEAR REGISTER
         TM    PRFTIC-IEDQPRF(R1),X08   PASSED ELEMENT A BUFFER
         BZ    ERB                      BRANCH NO - ERB
*
         LR    RPREFIX,R1               BUFFER ADDRESS
         USING IEDQPRF,RPREFIX
*
         MVI   PRFQCBA+2,REGOF          SET RETURN REGISTER = 12
         L     RLCB,PRFLCB-1            LCB ADDRESS
         USING IEDQLCB,RLCB
         L     RSCB,LCBSCBA-1           GET SCB ADDRESS         0Y01869
         CLI   LCBFLAG1,AVTEZERO        IS THIS A PLCB?          Y06327
         BE    TSO3705                  BRANCH YES               Y36327
         TM    LCBTSOB,LCBTSBUF         IS A TIME SHARING        S22029
*                                       TERMINAL ON THIS LINE    S22029
         BO    EOBTOBD                  YES - BYPASS TO STARTMH  S22029
         CLC   SCBDESTQ,AVTTSOPT+1      IS DESTINATION          0Y01869
*                                        TSINPUT QCB            0Y01869
         BE    EOBTOBD                  YES - LOGON NOT         0Y01869
*                                        COMPLETED YET -        0Y01869
*                                        BYPASS TO STARTMH      0Y01869
TSO3705  EQU   *
         NC    LCBTTCIN(2),LCBTTCIN     TTCIN ZERO?              S22029
         BZ    MHOK                     BRANCH YES               S22029
         BAL   R14,FNDTRM               GET TERMINAL ENTRY       S22029
         LR    R15,R1                   SAVE TERMINAL ENTRY      S22029
         LR    R1,RPREFIX               RESTORE BUFFER ADDRESS   S22029
         L     R15,AVTEZERO(R15)        GET DEST QCB ADDRESS     S22029
         USING IEDQQCB,R15              DESTINATION QCB BASE     S22029
         TM    QCBDSFLG,QCBALTMH        IS TERMINAL CONNECTED TO S22029
*                                       AN ALTERNATE MH          S22029
         BZ    MHOK                     NO - USE OWNING MH QCB   S22029
         L     R7,STARTMH               GET ALTMH QCB ADDRESS    S22029
MHOK     EQU   *                                                0Y01869
         IC    R2,AEINDX                OFFSET TO AE             S22029
         STC   R2,PRFKEY                SET IN PARM LIST         S22029
         DROP  R15                                               S22029
*
         CLI   PRFPRI,PRIAPBFR          GET SCHEDULER           SA56631
         BE    EOBTAA                   BRANCH YES
*
         CLI   LCBRSKEY,DSPPUTSC        PUT SCHEDULER           SA56631
         BE    EOBTAA                   BRANCH YES
*
         TM    LCBCHAIN,LCBABRTN        ABORT SEQUENCE SET
         BO    EOBTAA                   BRANCH YES - END OF MESSAGE
*
         TM    LCBSTAT1,LCBSENDN        IS LINE SENDING
         BO    EOBSND                   BRANCH YES
*                                                                S22025
         LH    R5,PRFXTRA+1             EOB OFFSET               S22025
         LTR   R5,R5                    EOB IN THIS BUFFER       S22025
         BZ    TESTBFMM                 BRANCH IF NO             S22025
*                                                                S22025
         TM    PRFSTAT1,PRFNHDRN        TEXT BUFFER              S22025
         BZ    SETEOB                   BRANCH NO, IT'S A HEADER S22025
*                                                                S22025
         L     R9,LCBDCBPT              DCB POINTER              S22025
         IC    R2,DCBRESER+1-IHADCB(,R9) TEXT RESERVES           S22025
         LA    R2,AVTTXTSZ(,R2)         COVER CASE WHERE EOB IS  S22025
*                                         AT LOGICAL ZERO OFFSET S22025
         CH    R2,PRFXTRA+1             NO DATA IN BUFFER        S22025
         BNE   SETEOB                   BRANCH IF NO             S22025
*                                                                S22025
         SR    R5,R5                    INDICATE ZERO EOB OFFSET S22025
         B     SETEOB                   SET EOB OFFSET           S22025
*                                                                S22025
TESTBFMM EQU   *                                                 S22025
         TM    PRFSTAT1,PRFNHDRN        HEADER BUFFER            S22025
         BO    NOUPDATE                 BRANCH IF NOT HEADER     S22025
*                                                                S22025
         TM    SCBQTYPE,SCBBFMM         SET ZERO EOB OFFSET FOR  S22025
*                                         'HEADER' BUFFER IN MID S22025
*                                         MSG SITUATION          S22025
         BZ    NOUPDATE                 BRANCH NOT MID MSG       S22025
*                                                                S22025
SETEOB   EQU   *                                                 S22025
         STH   R5,SCBEOB                SET EOB OFFSET           S22025
         ST    RPREFIX,AVTDOUBL         SCRATCH SPACE            S22025
         MVC   SCBDEOB+1(3),AVTDOUBL+1  SET BUFFER ADDRESS FOR FAS22025
NOUPDATE EQU   *                                                 S22025
*
         TM    PRFSTAT1,PRFNLSTN        BUFFER MARKED LAST
         BO    EOBTAA                   BRANCH NO - WAIT FOR LAST
*
         TM    SCBERR1,SCBCUTFN         IS CUTOFF OPERATING
         BO    EOBTAA                   BRANCH YES - NO RETRY
*
         TM    LCBCSW+3,UNEX            EOT RECEIVED
         BO    EOBTAA                   BRANCH YES - NO OPTIONS
*
         TM    SCBERR4,SCBTXTTN         TEXT ERROR
         BO    EOBERR                     BRANCH YES
*
         TM    SCBERR4,SCBSLCTN+SCBCONNN  ANY OTHER ERRORS
         BNZ   EOBTAA                   BRANCH YES - EXIT
*
         TM    FLAGS,LOGICAL+LOGICOP    IS LOGICAL CHECKING REQUESTED
         BZ    EOBNOL                     BRANCH NO
*
         IC    R2,LOGOPT                OFFSET TO LOGICAL= OPTIONS
         BNO   STUPXT                   BRANCH IF OPTION FIELD NOT
*                                        SPECIFIED
         BAL   R10,LOCSUB               LOCATE OPTION FIELD
*
         B     EOBNOL                   BAD RETURN
*
         LA    R2,BUMPOPT(R2)           GET L/E ADDR OPT OFFSET@XA05300
STUPXT   EQU   *
         BAL   R10,LOCSUB1              LOC L/E ADDR OPT FIELD @XA05300
*                                                              @XA05300
         LTR   R15,R15                  OPTION FIELD FOUND     @XA05300
         BNZ   EOBNOL                   BR NO                  @XA05300
*
*              SET UP TO EXIT TO USER ROUTINE
         L     R15,0(R12)               USER EXIT ADDRESS
         LR    R1,R12                   PASS OPTION FIELD ADDRESS IN R1
         BALR  R14,R15                  BRANCH AND LINK TO USER
*
         CLI   0(R12),0                 IS SWITCH = 0
         BE    EOBNOL                   CONTINUE                 S22025
*                                                                S22025
         CLI   0(R12),4                 RVI REQUEST              S22025
         BNE   CKSTPCNT                 BRANCH IF NO             S22024
*                                                                S22025
         OI    LCBMSGFM,LCBEOT          RVI REQUEST              S22025
         OI    PRFSTAT1,PRFNLSTN        SET NOT LAST BUFFER      S22025
         B     ABORT                    CONTINUE PROCESSING      S22025
*
EOBNOL   EQU   *
         NI    LCBMSGFM,X'FF'-X'80'     INSURE ACK IS SENT
         TM    FLAGS,CONV+CONVOP        IS CONV= SPECIFIED
         BZ    NOCONV                     BRANCH NO
*
         BNO   CHKLMD                   BRANCH IF UNCONDITONAL   S22025
*
         IC    R2,CONVOPT               POINTER TO CONV= OPTIONS
         BAL   R10,LOCSUB               LOCATE OPTION FIELD
*
         B     NOCONV                   BAD RETURN
*
         B     CKBSC                    GOOD RETURN              S22025
CHKLMD   EQU   *                                                 S22025
         TM    AEINDX,LMD+LMDOP         LMD REQUIRED             S22025
         BZ    CKBSC                    NO-BR                    S22025
         BNO   EOBTAA                   UNCONDITOONAL            S22025
         IC    R2,LMDOPT                POINTER TO LMD OPTIONS   S22025
         BAL   R10,LOCSUB               LOCATE OPTIONS           S22025
         B     CKBSC                    BAD RETURN               S22025
         B     EOBTAA                   GOOD RETURN              S22025
CKBSC    EQU   *
         CLI   LCBFLAG1,AVTEZERO        IS THIS A PLCB           S22024
         BNE   CKBSC1                   NO, CHECK FOR BSC        S22024
         OI    LCBSTAT2,LCBRESP         SET RESPONSE OWED        S22024
CKBSC1   EQU   *                                                 S22024
         TM    LCBSTAT2,LCBSYNC         IS LINE BSC
         BZ    EOBTAA                   BRANCH NO - END OF MESSAGE
*
         TM    SCBBSCFM,RCVETX          ETX RECEIVED
         BO    EOBTAA                   BR YES
         CLI   LCBFLAG1,AVTEZERO        IS THIS A PLCB           S22024
         BNE   NOCONV                   NO, CONTINUE             S22024
         NI    LCBSTAT2,AVTEFF-LCBRESP  TURN OFF RESPONSE OWED   S22024
*
NOCONV   EQU   *
         TM    AEINDX,LMD+LMDOP         LMD REQUIEED             S22025
         BZ    NOLMD                    NO-BR                    S22025
         BNO   EOBTAA                   UNCONDITONAL             S22025
         IC    R2,LMDOPT                POINTER TO LMD OPTIONS   S22025
         BAL   R10,LOCSUB               LOCATE OPTIONS           S22025
         B     NOLMD                    BAD   RETURN             S22025
         B     EOBTAA                   GOOD  RETURN             S22025
         SPACE 2                                                 S22025
NOLMD    EQU   *                                                 S22025
*
*              CONTINUE RECEIVING MESSAGE
         TM    LCBCSW+3,UNEX            END OF MESSAGE RECEIVED
         BO    EOBTAA                     BRANCH YES
         CLI   LCBFLAG1,LCBPLCB         PLCB                   @YM09045
         BE    ACTKA                    BR YES                 @YM09045
*
         L     R2,LCBDCBPT              LOAD ADDR DCB            S99228
         USING IHADCB,R2                                         S99228
         SR    R1,R1                    CLEAR LENGTH REGISTER    S99228
         IC    R1,DCBEIOBX              INSERT LCB LENGTH        S99228
         SH    R1,AVTHA4                REDUCE LCB LENGTH BY     S99228
         SH    R1,AVTHA4                8 BYTES FOR ADDR LCB EXT S99228
         DROP  R2                                                S99228
         USING IEDQLCBX,R2                                       S99228
         LA    R2,ZERO(R1,RLCB)         LOAD LCB EXTENSION ADDR  S99228
         TM    LCBXFLAG,LCBGPCTV        GENERAL POLL IN PROGRESS?S99228
         DROP  R2                                                S22027
         BO    EOBTAA                  YES, POST BUFFERS TO MH   S22027
         SPACE
         SR    R1,R1                    INDICATE END OF POST LIST
         NI    LCBMSGFM,X'FF'-X'80'     SET ACK TO BE SENT
         ST    RPREFIX,AVTADBUF         SAVE BFR ADDRESS       @XA09327
         CLC   AVTADBUF+1(3),LCBRECAD   THIS THE EOB BFR       @XA09327
         BE    ACTKA                    YES,DO READ CONTINUE   @XA09327
         ST    R7,PRFQCBA-1                                    @XA09327
         LR    R1,RPREFIX               SAVE BFR ADDRESS       @XA09327
         XC    PRFLINK,PRFLINK          CLEAR FOR POST         @XA09327
         SR    R10,R10                  CLEAR FOR IC           @XA09327
         IC    R10,PRFNBUNT             GET NO OF UNITS        @XA09327
NXTONE   L     RPREFIX,PRFTIC           GET NEXT UNIT          @XA09327
         BCT   R10,NXTONE               LOOP TO GET BFR ADDR   @XA09327
         STCM  RPREFIX,NZ,LCBLSPCI      SET LSPCI BFR          @XA09327
         ST    RPREFIX,LCBCPA+12        SET CONTINUE BFR       @XA09327
         OI    PRFSTAT1-IEDQPRF(R1),PRFNLSTN SET NOT LAST      @XA09327
ACTKA    EQU   *
         NI    SCBERR4,X'FF'-SCBTXTTN   RESET TEXT ERROR
         OI    PRFSTAT1,PRFNLSTN        SET NOT LAST BUFFER
SETCONT  EQU   *
         OI    LCBSTAT1,LCBCONT         SET CONTINUE FLAG
         MVC   LCBTTBIN(2),LCBTTCIN     SET UP TO BE CONNECTED
         LA    R2,AVTACTIB              ADDRESS OF I/O GENERATOR
         CLI   LCBFLAG1,AVTEZERO       IS THIS A PLCB            S22024
         BNE   NOT3705                 NO TPOST TO ACTIVATE      S22024
         L     R2,AVTSAVTP              ADDRESS OF SECONDARY AVT S22024
         USING IEDNSVTD,R2                                       S22024
         L     R2,SAVTCNIR              CONTROL NIR QCB ADDR   @Y17XANG
         SR    R1,R1                    SET END OF POST LIST     S22024
NOT3705  EQU   *
         ST    R2,PRFQCBA-1             SET QCB ADDRESS
         ST    R1,PRFLINK-1             LINK ELEMENTS
         MVI   PRFPRI,PRIDKEOB          SET PRIOITY              S22025
         LR    R1,RPREFIX               POST BUFFER TO ACTIVATE
         BAL   R14,DSPCHAIN             EXIT TO DISPATCHER     @Y17XAMX
*
CKNORTY  EQU   *
         TM    LCBCHAIN,LCBNORTY        IS NO RETRY FLAG SET
CKSTPC   EQU   *
         BO    EXIT                   EXIT IF NO RETRY SITUATION
*
         MVI   LCBINCAM,ZERO            CLEAR RETRY COUNTER
CKSTPCNT EQU   *                                                S22024
         BAL   R9,STOPCONT              GO CHECK STOP/CONT      SA68245
         B     NOCONV                   CONTINUE MESSAGE RECEPTION
*
*                                       TREAT AS END OF MESSAGE
ABORT    EQU   *
         OI    LCBCHAIN,LCBABRTN        SET ABORT FLAG FOR KA
         SR    R1,R1                    SET END OF POST LOST
         B     SETCONT                  SET CONTINUE FLAG AND POST TO
*                                       IEDQKA
EXIT     EQU   *
         NI    LCBCHAIN,X'FF'-LCBNORTY  TURN OFF NO RETRY FLAG
EOBTAA   EQU   *
         CLI   LCBFLAG1,AVTEZERO        3705 LCB?
         BNE   OXPOWER                  BRANCH YES
         SLDL  R2,64                    CLEAR WORK REGISTERS
         L     R7,LCBDCBPT              LOAD DCB ADDRESS
         IC    R1,DCBRESER+1-IHADCB(,R7)  INSERT NUMBER RESERVES
         LA    R2,AVTTXTSZ(,R2)         SET TEXT PREFIX SIZE
         TM    PRFSTAT1,PRFNHDRN        HEADER BUFFER?
         BO    TEXBUF                   BRANCH NO
         LA    R2,AVTHDRSZ-AVTTXTSZ(,R2)  SET HEADER PREFIX SIZE
         IC    R1,DCBRESER-IHADCB(,R7)  GET HEADER RESERVES
TEXBUF   EQU   *
         LA    R2,ZERO(R1,R2)           SET SIZE PREFIX AND RESERVES
         STC   R1,LCBISZE               SET RESERVES COUNT IN LCB
         CH    R2,PRFSIZE               NULL BUFFER?
         BE    SETCONT                  BRANCH YES
OXPOWER  EQU   *
         TM    SCBSTATE,SCBPRER         HA2 A TEXT ERROR OCCURRED
         BZ    EOBTOBD                  BRANCH NO PREVIOUS ERROR
*
         OI    SCBERR4,SCBTXTTN         SET TEXT ERROR BIT
EOBTOBD  EQU   *
         L     R7,LCBDCBPT              DCB POINTER              S22029
         L     R7,DCBMH-1-IHADCB(,R7)   MH QCB POINTER           S22029
         CLI   LCBFLAG1,LCBPLCB         THIS A PLCB            @YM06085
         BNE   GOTMH                    BR NO, HAVE PROPER MH  @YM06085
         L     R7,LCBMHA-1              GET PROPER MH QCB      @YM06085
GOTMH    EQU   *                                               @YM06085
         LR    R1,RPREFIX               RESTORE ELEMENT (BUFFER)
         L     R3,MHROUT-1              ADDRESS OF STARTMH STCB
         B     DSPBYPAS                 BYPASS TO STARTMH SUBTASK
*
         EJECT
*        LOCATE OPTION FIELD SUBROUTINES                       @XA05300
         SPACE 2
LOCSUB   EQU   *
         IC    R5,OPTION(R2)            OPTION FIRLD POINTER
         STC   R5,PRFQCBA+1             SET IEDQAE PARAMETER LIST
         MVI   PRFQCBA,FOUR             SET UP PARMLIST LENGTH @OZ31599
         MVI   PRFQCBA+BUMPOPT,REGOF    SET UP RETURN REG=12   @OZ31599
         ST    RPREFIX,AVTADBUF         SET BUFFER ADDRESS FOR IEDQUI
         LR    R1,RPREFIX               RESTORE PARAMETER LIST
         L     R15,AVTUI                ADDRESS OF IEDQUI
         BALR  R14,R15                  BRANCH AND LINK TO LOCOPT
*
         LTR   R15,R15                  WAS FIELD FOUND
         BCR   7,R10                    BRANCH NO - BAD RETURN
*
         IC    R5,OPTION+1(R2)          SWITCH TO REGISTER FOR CLI
         EX    R5,CLOPT                 IS OPTION FIEL EQUAL TO SWITCH
         BCR   8,R10                    BRANCH ZERO - BAD RETURN
*
         B     4(R10)                   RETURN TO CALLER
*
CLOPT    TM    0(R12),0                 TEST OPTION FIELD BITS
*
*                                                              @XA05300
LOCSUB1  EQU   *                                               @XA05300
         IC    R5,OPTION(R2)            GET OPTION FIELD INDEX @XA05300
         STC   R5,PRFQCBA+ONE           PUT IT IN AE PARM LIST @XA05300
         MVI   PRFQCBA,FOUR             SET UP PARMLIST LENGTH @OZ31599
         MVI   PRFQCBA+BUMPOPT,REGOF    SET UP RETURN REG=12   @OZ31599
         ST    RPREFIX,AVTADBUF         SET BFR ADDR FOR UI    @XA05300
         LR    R1,RPREFIX               SET PARM LIST REGISTER @XA05300
         L     R15,AVTUI                ADDR OF UI             @XA05300
         BALR  R14,R15                  GO LOCATE OPTION FIELD @XA05300
*                                                              @XA05300
         BR    R10                      RETURN TO CALLER       @XA05300
         EJECT
*        EOB/ETB RETRY ON RECEIVE
         SPACE 3
EOBERR   EQU   *
         CLI   LCBFLAG1,AVTEZERO       IS THIS A PSEUDO LCB      S22024
         BE    CKSTPCNT                 YES, CHECK STOP/CONT    S22024
         LA    R10,RCVRTY               SET RETURN ADDRESS
         TM    LCBSTAT1,LCBINITN        RECEIVING INITIATE MODE
         BO    CKNORTY                  BRANCH YES - CHECK NO RETRY
*
*
CKRTRY   EQU   *
*
         TM    LCBCHAIN,LCBNORTY        NO RETRY FLAG SET
         BCR   1,R10                    BRANCH YES - END OF MESSAGE
*
         TM    LCBCSW+3,UNEX            UNIT EXCEPTION ERROR     A44885
         BCR   1,R10                    YES, BRANCH EOM          A44885
         CLI   LCBINCAM,MAX             HAS RETRY COUNT BEEN EXHAUSTED
         BCR   8,R10                    BRANCH IF RETRY EXHAUSTED
*
         IC    R9,LCBINCAM              PICK UP RETRY COUNT
         LA    R9,UNEX(,R9)             ADD ONE
         STC   R9,LCBINCAM              STORE BACK
         B     FOUR(R10)                RETURN TO CALLER
RCVRTY   EQU   *
         B     CKSTPC                   CHECK IF RETRY COUNT EXHAUSTED
*
         LA    R10,LCBERB               ERB ADDRESS
         TM    LCBERBST,LCBDLNKN        IS DLINK SWITCH SET
         BO    ERBFRE                   BRANCH NOT IN CHAIN
*
         LA    R9,AVTBFRTB-4            BUFFER RETURN QCB
*        RTN Q AND READY QUEUE.
*
NEXT     EQU   *
         LR    R12,R9                   SET TO NEXT ELEMENT
         L     R9,4(0,R12)              FIRST/NEXT ELEMENT IN CHAIN
         LA    R9,0(0,R9)               CLEAR HIGH ORDER BYTR
         CLR   R9,R10                   IS THIS THE ERB
         BE    REMOVERB                 BR IF ERB FOUND
         C     R9,AVTDELAD              IS THIS THE DUMMY END
*        ELEMENT - IF YES THIS IS END OF THIS CHAIN.
         BNE   NEXT                     BR NOT END OF CHAIN
         LA    R9,AVTREADY-4            ADDRESS OF READY Q FOR LOOP
         B     NEXT                     SEARCH READY QUEUE      SA63640
REMOVERB EQU   *
*
         MVC   5(3,R12),5(R9)           REMOVE ERB FROM CHAIN
*
         OI    LCBERBST,LCBDLNKN        SET ERB POSTABLE
ERBFRE   EQU   *
         OI    LCBMSGFM,X'80'           SET NAK BIT
         SR    R12,R12                  CLEAR ELEMENT LINK REG
         LA    R9,AVTBFRTB              ADDRESS OF BUFFER RETURN
         L     R10,SCBDESTQ-1           DESTINATION OF THIS MESSAGE
         LA    R10,ZERO(,R10)           CLEAR HIGH ORDER
         CLR   R9,R10                   DESTINATION = BUFFER RETURN
         BE    ABORT                    BRANCH YES - END OF MESSAGE
*
         TM    SCBERR3,SCBLOSTN         WAS MESSAGE LOST
         BO    ABORT                    BRANCH YES
*
         SR    R1,R1                    CLEAR FOR ERB LINK FIELD S22025
         IC    R9,LCBERBCT              PICK UP COUNT
         AH    R9,LCBERBCT              ADD OTHER ONE BYTE COUNTER
         STC   R9,LCBCPA+16             SAVE IN LCB
         LA    R9,D256                  CONSTANT FOR RECALL
         MVC   LCBCPA+17(7),SCBDNSEG    SAVE DNSEG AND CLSEG
         SPACE 2
*        FOR THE CASE OF CANCELLED MESSAGES AND THEY WILL NOT
*        BE RESET - THE BUFFER MUST BE ADDED TO THE PRESENT CHAIN
         SPACE 2
         MVI   SCBDEOB,ZERO             CLEAR INSERT OFFSET FOR RECALL
         NC    SCBDEOB+1(3),SCBDEOB+1   WAS THERE A PREVIOUS EOB
         BNZ   RECALL1                  BRANCH YES
*        RECALL THE HEADER BUFFER
*
         OI    LCBCHAIN,LCBNORTY        SET FLAG FOR RECALL ENTRY
         MVC   SCBDEOB+1(3),SCBCCHDR    CORE ADDRESS OF HEADER
         TM    SCBQTYPE,SCBREUS+SCBNREUS  IS MESSAGE VORE QUEUED
         BZ    RECALL1                  BRANCH YES
*
         MVC   SCBDEOB+1(3),SCBDCHDR    DISK ADDRESS OF HEADER
         B     RECALL1                  RECALL SEGMENT
*
*   RETURN HERE WITH RECALLED BUFFER
SETRCV   EQU   *
         L     R2,SCBDESTQ-1            DEST Q ADDR
         MVZ   SCBQTYPE,QCBDSFLG-IEDQQCB(R2)   RSTORE QTYPE
         TM    SCBQTYPE,SCBREUS+SCBNREUS  ANY DISK QUEUEING
         BZ    NOXI                    BRANCH NO
*
         XI    SCBQTYPE,SCBREUS+SCBNREUS   BITS ARE REVERSED
NOXI     EQU   *
         MVC   SCBDNSEG(7),LCBCPA+17    RESET DNSEGAND CLSEG     S22025
         NC    LCBERBCH(3),LCBERBCH     HAS THE ERB BEEN POSTED SA63962
*        SINCE THE RECALL WAS DONE                              SA63962
         BZ    RESETSCB                 BR YES - SCBDESTQ CAN   SA63962
*                                       CAN BE RESET NOW        SA63962
         NI    PRFSTAT1,X'FF'-PRFDUPLN  RESET DUPL              SA63962
         TM    PRFSTAT1,PRFNHDRN        IS THIS A HEADER BUFFER
         BO    HDROK                    BRANCH IF NOT A HEADER
         LR    R1,RPREFIX               FOR POSTING              S22025
         MVI   PRFPRI-IEDQPRF(R1),PRIDESTQ-1 SET PRIORITY SO THATS22025
*                                         A CANCEL BUFFER REACHESS22025
*                                         FA BEFORE RECALLED BUF S22025
*                                         GETS TO HM             S22025
         BAL   R14,POSTLIST             ADD TO POST LIST         S22025
         TM    LCBCHAIN,LCBNORTY        FIRST EOB                S22025
         BZ    PREVEOB                  BRANCH NO                S22025
*                                                                S22025
         MVI   PRFSTAT1-IEDQPRF(R1),PRFCNCLN+PRFNHDRN SET TO CANCS22025
         XC    LCBERBCH(3),LCBERBCH     CLEAR FOR FLAG          SA63962
         OI    LCBSTAT1,LCBRCLLN        SET RECALL FLAG TO      SA63962
*        CAUSE THE ERB TO FOLLOW THE SAME PATH AS WITH A        SA63962
*        RECALLED BUFFER                                        SA63962
         LA    R1,LCBERB                REPOST THE REB TO BT TO SA63962
*        ALLOW THE CANCELLED BFR TO BE Q'D BEFORE DESTQ IS RESETSA63962
         MVC   LCBERBLK(3),AVTPARM+ONE  ADD POST LIST TO ERB    SA63962
         BAL   R14,DSPCHAIN             EXIT TO DISPATCHER     @Y17XAMA
RESETSCB EQU   *                                                SA63962
*        THE ERB WILL RETURN HERE AFTER THE SECOND POST         SA63962
         TM    SCBSTATE,SCBLCK1N+SCBMSGLN  IS THIS IS EXTENDED  SA63962
*        LOCK, DO NOT RESET SCBDESTQ OR THE LOCK FLAGS          SA63962
         BM    NODESTQ                  BR IF EXTENDED LOCK     SA63962
*        THE SCB MUST BE SET UP TO LOOK LIKE A NEW TRANSMISSION SA63962
         NI    SCBSTATE,X'FF'-(SCBLCK1N+SCBMSGLN+SCBPRER)       SA63962
*        CLEAR ALL LOCK FLAGS AND RESET THE MULTI RTE CKPT      SA63962
         LA    R14,AVTBFRTB             ADDRESS OF BUFFER RTN   SA63962
         IC    R1,SCBDESTQ-ONE          SET DESTQ TO BFRTN      SA63962
         ST    R14,SCBDESTQ-ONE         FOR CASE OF INVALID     SA63962
         STC   R1,SCBDESTQ-ONE          FORWARD ON NEW HDR      SA63962
NODESTQ  EQU   *                                                SA63962
         XC    SCBMRFSD(L'SCBMRFSD+L'SCBDLPTR),SCBMRFSD         SA63962
*        CLEAR MULTI ROUTE AND DLIST INDICATORS                 SA63962
         XC    SCBERR1(2),SCBERR1       CLEAR LOGICAL ERRORS     S22025
         NI    SCBSTATE,X'FF'-SCBCODE   RESET CODE BIT TO PREVENTS22025
*                                         DOUBLE HDR TRANSLATE   S22025
         XC    SCBDEOB+1(3),SCBDEOB+1   INDICATE NO PREVIOUS EOB
*    DECREMENT SEQUENCE NUMBER
*                                       IF ONLY EOT RECEIVED
         TM    SCBSTATE,SCBSEQIN        WAS SEQUENCE IN UPDATED
         BZ    BLDCCW                   BRANCH NO                S22025
*
         NI    SCBSTATE,X'FF'-SCBSEQIN   RESET FLAG
         BAL   R14,FNDTRM               GET TERMINAL TABLE ENTRY
*
         LH    R14,TRMINSEQ-IEDQTRM(R1)  INPUT SEQUENCE #
         BCT   R14,NOTZRO               DECREMENT AND BRANCH IF NOT 0
*
         LH    R14,DC9999               RESET TO MAXIMUM
NOTZRO   EQU   *
         STH   R14,TRMINSEQ-IEDQTRM(R1)  RESET
         B     BLDCCW                   BUILD HEADER BUFFER      S22025
         EJECT
PREVEOB  EQU   *
*        LAST GOOD EOB WAS IN HEADER BUFFER.  WE WANT TO CANCEL  S22025
*  THE MESSAGE AND POST RECALLED BUFFER BACK TO DESTINATION QUEUES22025
*  WITH PRFSIZE ADJUSTED TO REFLECT CHECKED DATA.                S22025
         SPACE 2
         MVC   PRFSIZE-IEDQPRF(2,R1),SCBEOB       GET NEW SIZE   S22025
         SR    R14,R14                  CLEAR A SIZE REGISTER    S22025
         IC    R14,PRFSCAN-IEDQPRF(R1)  GET THE SCAN POINTER     S22025
         CH    R14,PRFSIZE-IEDQPRF(R1)  IS PRFSCAN GREATER THAN  S22025
*                                         PRFSIZE                S22025
         BNH   ERRBUF                   NO, BRANCH               S22025
         SPACE 2
         LH    R14,SCBEOB               GET THE EOB OFFSET       S22025
         LA    R14,ONE(R14)             INCREMENT BY 1           S22025
         STC   R14,PRFSCAN-IEDQPRF(R1)  RESET THE SCAN POINTER   S22025
ERRBUF   EQU   *                                                 S22025
         L     RPREFIX,LCBLSPCI-1       GET ERROR BUFFER         S22025
         BAL   R1,GETBUF                ADDRESS OF LAST UNIT OF  S22025
         L     R1,LCBLSPCI-1            BUFFER TO UPDATE LSPCI   S22025
         MVC   LCBLSPCI,PRFTIC+1        NEXT ASSIGNED BUFFER     S22025
         MVI   PRFSTAT1-IEDQPRF(R1),PRFCNCLN+PRFNHDRN SET STATUS S22025
*                                         TO CANCEL MSG          S22025
         MVI   PRFPRI-IEDQPRF(R1),PRIDESTQ SET PRIORITY          S22025
         BAL   R14,POSTLIST             ADD BUFFER TO CANCEL MSG S22025
         OC    LCBLSPCI(2),LCBLSPCI     DO WE HAVE BUFFER FOR    S22025
*                                         RETRY                  S22025
         BNZ   BLDCCW                   BRANCH IF YES            S22025
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *  S22025
*                                                             *  S22025
*        IT IS POSSIBLE THAT A BUFFER IS NOT AVAILABLE        *  S22025
*        FOR RETRY.  THE ERB COULD HAVE BEEN HUNG ON THE      *  S22025
*        BUFFER RETURN QUEUE WAITING FOR BUFFERS WHEN         *  S22025
*        THE ERROR BUFFER WAS POSTED TO MH.                   *  S22025
*                                                             *  S22025
*        THE FOLLOWING CODE WILL POST THE ERB TO REQUEST      *  S22025
*        AN ADDITIONAL BUFFER WHICH WILL BE USED TO RETRY     *  S22025
*                      THE ERROR BLOCK                        *  S22025
*                                                             *  S22025
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *  S22025
         L     R2,LCBDCBPT              GET THE DCB ADDRESS      S22025
         LA    R9,D256                  TO REQUEST ONE BUFFER    S22025
         IC    R9,DCBUNTCT-IHADCB(,R2)  GET THE NUMBER OF UNITS  S22025
*                                         PER BUFFER             S22025
         OI    LCBERBST,LCBPRCPG        SET THE ERB STATUS       S22025
         LA    R2,AVTBFREB              GET THE ADDRESS OF       S22025
*                                         BUFFER REQUEST         S22025
         MVI   LCBERBPY,PRIINTRQ        SET THE ERB PRIORITY     S22025
         MVC   LCBERBLK,AVTPARM+1       ADD POST LIST            S22025
         B     POSTERB                  POST ERB AND POST LIST   S22025
         SPACE 3                                                 S22025
GOTBUF   EQU   *                        WHEN BUFFER IS OBTAINED  S22025
         NI    LCBERBST,X'FF'-LCBPRCPG  RESET ERB STATE          S22025
         MVC   LCBLSPCI,LCBERBCH        SET BUFFER TO LCB CHAIN  S22025
         B     BLDCCW                   BUILD BUFFER FOR RETRY   S22025
         EJECT
HDROK    EQU   *
         MVI   AVTDOUBL,ZERO            CLEAR FOR LTR
*        TO FORCE THIS BFR TO BE ADDED TO THE END OF THIS MSG
         L     R9,SCBCCHDR-1            ADDR OF HDR
         TM    SCBQTYPE,SCBREUS+SCBNREUS  DISK QUEUEING OR BACKUP
         BZ    NXTBFR                   BRANCH NO
*
         MVC   SCBDNSEG,PRFCRCD         SET SCB TO LAST GOOD EOB
         TM    SCBQTYPE,SCBCOREQ        CORE QUEUEING
         BZ    POSTBACK                 BRANCH NO                S22025
*
         LA    R9,0(R9)                 CLEAR HI ORDER BYTE    @SA70682
         LTR   R9,R9                    CORE COPY LOST         @SA70682
         BZ    POSTBACK                 BRANCH YES             @OY20146
         TM    PRFKEY-IEDQPRF(R9),X10   IS CORE COPY LOST
         BO    POSTBACK                 BRANCH YES             @OY20146
         LA    RPREFIX,PRFCRCD-PRFCORE(RPREFIX)   FOR DISK BACKUP - SET
*        TO COMPARE THE PRFCRCD FIELD OF R6 INSTEAD OF PRFCORE
         LA    R10,PRFCRCD-PRFCORE      SET TO USE CRCD FOR CLC
         B     NXTBFRA                  NO UPDATE TO DEOB IF DISK
*        BACKUP - DEOB HAS THE CORRECT DISK ADDRESS
*
*
NXTBFR   EQU   *
         SR    R10,R10                  SET TO USE PRFCORE FOR CLC
NXTBFRA  EQU   *
         LA    R1,0(R10,R9)             SET FOR CORRECT CLC
         CLC   PRFCORE,PRFCORE-IEDQPRF(R1)  IS THIS = RECALL
         BE    NXTBFRB                  BR IF THIS IS THIS RECALL
         MVC   SCBCLSEG(3),PRFCORE-IEDQPRF(R9)   LAST BFR ADDR
         MVC   AVTDOUBL+1(3),PRFNTXT-IEDQPRF(R9)   TO FULL WORD
         L     R9,AVTDOUBL              ADDR OF NEXT BFR
         B     NXTBFRA                  REPEAT CHECK FOR RECALL  S22025
NXTBFRB  EQU   *
         L     R9,SCBCLSEG-1            ADDR OF LAST BFR IN Q
         L     RPREFIX,LCBERBCH-1       RESTORE BFR ADDR
         MVC   AVTDOUBL+1(3),PRFNTXT-IEDQPRF(R9)  NEXT NEXT ADDRESS
         L     R10,AVTDOUBL            FIRST UNIT TO BE FREED
         XC    PRFNTXT-IEDQPRF(3,R9),PRFNTXT-IEDQPRF(R9)  CLEAR NEXT
*                                       TEXT POINTER
         LR    R1,R10                   SET FOR POST
         SR    R5,R5
NXTB     EQU   *
         LR    R12,R10                  SAVE BUFFER ADDRESS
         SR    R9,R9
         IC    R9,PRFNBUNT-IEDQPRF(R10)  NUMBER OF UNITS
         AR    R5,R9                    KEEP TOTAL COUNT
         BAL   R14,NXTUNT               KEEP @ OF LAST UNIT IN BFR
*
         L     R10,PRFTIC-IEDQPRF(R10)  NEXT UNIT ADDRESS
NXTUNT   EQU   *
         BCTR  R9,R14                   GET NEXT UNIT
*
         MVC   AVTDOUBL+1(3),PRFNTXT-IEDQPRF(R12)  TO FULL WORD
         MVC   PRFTIC+1-IEDQPRF(3,R10),AVTDOUBL+1  CHAIN NEXT BUFFER
*                                       TO LAST UNIT OF PREVIOUS
         L     R10,AVTDOUBL             ADDRESS OF NEXT BUFFER
         LTR   R10,R10                  END OF CHAIN
         BNZ   NXTB                     BRANCH NO
*
         STC   R5,PRFNBUNT-IEDQPRF(R1)    SET NUMBRR OF UNITS
         L     R2,AVTCADDR              GET MSUNIT COUNT       @OZ33243
         SR    R2,R5                    SUBTRACT OUT AVAILABLE @OZ32243
         ST    R2,AVTCADDR              STORE UPDATED COUNT    @OZ33243
         LA    R2,AVTBFRTB              BUFFER RETURN QCB      @OZ33243
         MVI   PRFPRI-IEDQPRF(R1),PRIBFRTB  SE\ PRIORITY
         BAL   R14,POSTLIST             ADD TO POST LIST         S22025
*                                                                S22025
POSTBACK EQU   *                                                 S22025
         L     R1,LCBERBCH-1            RECALLED BUFFER          S22025
         MVI   PRFPRI-IEDQPRF(R1),PRIDESTQ SET PRIORITY          S22025
         L     R2,SCBDESTQ-1            DESTINATION QUEUE        S22025
         MVC   PRFSIZE-IEDQPRF(2,R1),SCBEOB SET SIZE TO REFLECT  S22025
*                                         CHECKED DATA           S22025
         SR    R14,R14                  CLEAR A SIZE REGISTER    S22025
         IC    R14,PRFSCAN-IEDQPRF(R1)  GET THE SCAN POINTER     S22025
         CH    R14,PRFSIZE-IEDQPRF(R1)  IS PRFSCAN GREATER THAN  S22025
*                                         PRFSIZE                S22025
         BNH   CLEAREG                  NO, BRANCH               S22025
         SPACE 2
         LH    R14,SCBEOB               GET THE EOB OFFSET       S22025
         LA    R14,ONE(R14)             INCREMENT BY 1           S22025
         STC   R14,PRFSCAN-IEDQPRF(R1)  RESET THE SCAN POINTER   S22025
CLEAREG  EQU   *                                                 S22025
         SR    R5,R5                    CLEAR FOR COMPARE        S22025
         CH    R5,SCBEOB                NO DATA IN BUFFER        S22025
         BE    BFRRTN                   YES, RETURN BUFFER     @OS79713
         L     RPREFIX,LCBDCBPT         DCB BASE               @OS79713
         TM    PRFSTAT1-IEDQPRF(R1),PRFNHDRN  HEADER BUFFER    @OS79713
         BNO   HDRBFR                   BRANCH YES             @OS79713
         IC    R5,DCBRESER+1-IHADCB(RPREFIX)  TEXT RESERVES    @OS79713
         LA    R5,AVTTXTSZ(,R5)         PLUS TEXT PREFIX SIZE  @OS79713
         B     COMPARE                  GO CHECK AGAINST EOB   @OS79713
HDRBFR   EQU   *                                               @OS79713
         IC    R5,DCBRESER-IHADCB(RPREFIX)  HEADER RESERVES    @OS79713
         LA    R5,AVTHDRSZ(,R5)         PLUS HEADER PREFIX SIZE@OS79713
COMPARE  EQU   *                                               @OS79713
         CH    R5,SCBEOB                LOGICALLY EMPTY BFR    @OS79713
         BL    POSTDEST                 BRANCH IF NOT TO POST  @OS79713
BFRRTN   EQU   *                                               @OS79713
*                                                                S22025
         LA    R2,AVTBFRTB              BUFFER RETURN            S22025
POSTDEST EQU   *                                                 S22025
         BAL   R14,POSTLIST             ADD TO POST LIST         S22025
*                                                                S22025
BLDCCW   EQU   *                                                 S22025
         L     RPREFIX,LCBLSPCI-1       GET RETRY BUFFER         S22025
         MVC   LCBERBCT(1),LCBCPA+16    RESET ORIGIANL BUFF CNT  S22025
         MVI   LCBERBCT+1,AVTEZERO      CLEAR SECOND BYTE        S22025
         L     R2,LCBDCBPT              DCB BASE                 S22025
         LH    R9,DCBBUFSI-IHADCB(R2)   BUFFER SIZE              S22025
         SR    R5,R5                    PRESET EOB OFFSET        S22025
         STH   R5,SCBEOB                SCBEOB                   S22025
         IC    R5,DCBRESER-IHADCB(R2)   ASSUME HEADER BUFFER     S22025
         STC   R5,LCBISZE               SET IDLES FOR HEADER     S22025
         LA    R12,AVTHDRSZ(R5)         HEADER PREFIX+ IDLES     S22025
         NI    PRFSTAT1,PRFNHDRF        SET TO HEADER BUFFER     S22025
         TM    LCBCHAIN,LCBNORTY        FIRST EOB IN BUFFER      S22025
         BO    ITSHDR                   YES, IT'S A HEADER       S22025
*                                                                S22025
         MVC   SCBDEOB+1(3),LCBLSPCI    SET NEW DEOB             S22025
         IC    R5,DCBRESER+1-IHADCB(R2) GET TEXT RESERVES        S22025
         LA    R12,AVTTXTSZ(R5)         TEXT PREFIX+IDLES        S22025
         OI    PRFSTAT1,PRFNHDRN        SET AS TEXT BUFFER       S22025
*                                                                S22025
ITSHDR   EQU   *                                                 S22025
         STH   R12,PRFSIZE              SET PREFIX SIZE          S22025
         IC    R5,PRFNBUNT              UNIT COUNT IN BUFFER     S22025
         MVI   PRFFLAGS,AVTEZERO        CLEAR CCW FLAGS          S22025
         TM    DCBPCI-IHADCB(R2),RCVPCI  PCI SPECIFIED           S22025
         BZ    NOPCI                    BRANCH IF NO             S22025
*                                                                S22025
         OI    PRFFLAGS,CCWPCI          SET CCW FLAG             S22025
NOPCI    EQU   *                                                 S22025
         LH    R2,DCBBUFSI-IHADCB(R2)   BUFFER SIXE
         N     R2,AVTCLRHI              CLEAR HIGH ORDER HALF
         LH    R1,AVTKEYLE              UNIT SIZE
         LH    R12,PRFSIZE              CCW OFFSET TO LAST EOB   S22025
         N     R12,AVTCLRHI             CLEAR HIGH ORDER HALF
         BAL   R9,BUMPUNT               CONTINUE SETTING UP BUFR S22025
*
         L     RPREFIX,PRFTIC           NEXT UNIT
         SR    R2,R1                    DECREMENT BUFFER SIZE
BUMPUNT  EQU   *
         BCTR  R5,0                     DECREMENT UNIT COUNT
         SR    R12,R1                   SUBTRACT UNIT SIZE
         BCR   11,R9                    BRANCH IF RESULT NEGATIVE
*
         AR    R12,R1                   ADD BACK UNIT SIZE
         LA    R14,PRFSUNIT(R12)        NEXT DATA BYTE
         ST    R14,PRFIOADR-1           SET CCW ADDRESS
         BAL   R10,CCWST1               COMMON CODE TO SET CCW'S
*
         LA    RPREFIX,0(,RPREFIX)      CLEAR FOR STORE
         ST    RPREFIX,LCBCPA+12        SET FOR I/O GEN
         LR    R14,R1                   CALCULATE COUNT
         SR    R14,R12
         STH   R14,PRFCOUNT             SET CCW COUNT
         LR    R9,RPREFIX               SAVE BUFFER ADDRESS
         LTR   R5,R5                    IS UNIT COUNT ZERO
         BNZ   NOTLAST                  BRANCH NO
*
         SR    R2,R12                   CALCULATE CORRECT COUNT
SETCCW   EQU   *
         STH   R2,PRFCOUNT              SET CORRECT COUNT
         MVC   LCBCPA+16(8),PRFOPCDE-IEDQPRF(R9)  MOVE CCW FOR I/O GEN
         L     RPREFIX,LCBLSPCI-1       ADDRESS OF START BUFFER
         XC    LCBERBCH,LCBERBCH        CLEAR ERBCH              S22025
         NI    LCBCHAIN,X'FF'-LCBNORTY  RESET FLAG               S22025
         L     R1,AVTPARM               POST LIST                S22025
         B     ACTKA                    POST BUFFER TO KA        S22025
         SPACE 5                                                 S22025
*
NOTLAST  EQU   *
         L     RPREFIX,PRFTIC           NEXT UNIT
         LA    R12,PRFSUNIT             FIRST DATA BYTE
         BAL   R10,CCWSET               SET UP CCW
*
         STH   R1,PRFCOUNT              SET COUNT
         SR    R2,R1                    DECTEMENT BUFSIZE
         BCT   R5,NOTLAST               LOOP TILL END OF BUFFER
*
         B     SETCCW                   TO COMMON CODE
*
GETBUF   EQU   *
         SR    R12,R12                  CLEAR FOR IC
         IC    R12,PRFNBUNT             NUMBER OF UNITS IN BUFFER
         BAL   R9,GNXT                  ENTER LOOP TO FIND ADDRESS OF
*                                       NEXT BUFFER
         L     RPREFIX,PRFTIC           NEXT UNIT ADDTESS
GNXT     EQU   *
         BCTR  R12,R9                   LOOP TILL NEXT BUFFER FOUND
*
         BR    R1                       RETURN
*
         SPACE 5                                                 S22025
POSTLIST EQU   *                                                 S22025
         ST    R2,PRFQCBA-1-IEDQPRF(R1) SET QCB ADDRESS          S22025
         MVC   PRFLINK-IEDQPRF(3,R1),AVTPARM+1  LINK PREVIOUS    S22025
*                                         ELEMENT INTO CURRENT   S22025
         ST    R1,AVTPARM               NEW PREVIOUS ELEMENT     S22025
         BR    R14                      RETURN                   S22025
         EJECT
*        EOB/ETB RETRY ON SEND OPERATIONS
         SPACE 3
EOBSND   EQU   *
         TM    SCBERR4,SCBTXTTN         TEXT ERROR SET ?
         BZ    EOBTAA                   BRANCH NO - PASS TO AA
*
         SR    R9,R9                    CLEAR FOR COUNT
         CH    R9,PRFSIZE               IS THIS THE ERROR BUFFER
         BNE   EOBTAA                   BRANCH NO - WAIT FOT IT
*
         CLI   LCBFLAG1,AVTEZERO        IS THIS A PSEUDO LCB     S22024
         BNE   SENDRTRY                 NO, GO RETRY IF POSSIBLE S22024
         BAL   R9,STOPCONT              YES, GO CHECK STOP/CONT SA68245
         B     CHKCONT                  CONT= SPECIFIED          S22024
*                                       STOP= SPECIFIED          S22024
         OI    LCBCHAIN,LCBABRTN        SET ABORT FLAG           S22024
         OI    LCBERBST,LCBERROR        FLAG TO STOP DISK READS  S22024
         B     EOBTAA                   GO POST BUFFER TO MH     S22024
         SPACE
CHKCONT  TM    LCBCSW+3,UNEX            IS THIS THE RESPONSE TO  S22024
*                                       THE LAST WRITE FOR THIS  S22024
*                                       MESSAGE                  S22024
         BZ    ACTKA                    NO, DO CONT= PROCESSING  S22024
         B     EOBTOBD                  YES, DO STOP= PROCESSING S22024
*                                                                S22024
SENDRTRY EQU   *                                                 S22024
         ST    R1,AVTDOUBL             @SAVE ELEMENT ADDRESS   @SA70192
         BAL   R14,FNDTRM               LOCATE TERMINAL ENTRY  @SA70192
         SR    R10,R10                  CLEAR WORK REGISTER    @SA70192
         IC    R10,TRMCHCIN-IEDQTRM(,R1) LOAD DCT INDEX        @SA70192
         BCTR  R10,0                    REDUCE FOR MULTIPLY    @SA70192
         MH    R10,AVTDCTLN             MULTIPLY BY DCT ENTRY  @Y17XANG
*                                       LENGTH.                @Y17XANG
         AL    R10,AVTCSTCS             ADD FRO ENTRY ADDRESS  @SA70192
         TM    3(R10),CCONTIN           CAN TERMINAL CONTINUE? @SA70192
         BZ    EOBTOBD                  BRANCH NO              @SA70192
         L     R1,AVTDOUBL              RELOAD ELEMENT ADDRESS @SA70192
         BAL   R10,CKRTRY               CHECK IF RETRY POSSIBLE
*
         B     EOBTOBD                  IF PERMANENT ERROR - POST
*                                       BUFFER TO IEDQBD
         NI    SCBERR1,AVTEFF-SCBNOBFN  RESET OUT OF BUFFERS   @SA72449
*
         LA    R10,AVTBFRTB             ADDRESS OF BUFFER RETURN QCB
CLRLNK   EQU   *
         XC    PRFLINK,PRFLINK          CLEAR LINK FIELD
         ST    R10,PRFQCBA-1            SET QCB ADDRESS
         MVI   PRFPRI,PRIBFRTB          SET PRIORITY
         IC    R9,PRFNBUNT              NUMBER OF UNITS IN BUFFER
         LR    R5,RPREFIX               SAVE BUFFER ADDRESS
         BAL   R14,LOOPB                ENTER LOOP - FIND LAST UNIT
*
         L     R5,PRFTIC-IEDQPRF(R5)    NEXT UNIT ADDRESS
LOOPB    EQU   *
         BCTR  R9,R14                   BRANCH IF NOT LAST UNIT
         TM    PRFTIC-IEDQPRF+3(R5),2   IS THERE A NEXT BUFFER
         BNZ   POSTBFR                  BRANCH IF NOT
*
         MVC   PRFLINK,PRFTIC-IEDQPRF+1(R5)  LINK BUFFERS BY LINK
*                                       FIELD
         L     RPREFIX,PRFTIC-IEDQPRF(R5)  NEXT BUFFER ADDRESS
         B     CLRLNK                   LOOP TILL END OF BUFFER CHAIN
*
*
POSTBFR  EQU   *
         TM    LCBERBST,LCBDLNKN+LCBEOMSG  IS ERB AVAILABLE
         BNZ   RECALL         ON ZEROES EXIT TO THE DISPATCHER @Y17XAMA
*                                       BRANCH ERB NOT AVAILABLE
         BAL   R14,DSPCHAIN                                    @Y17XAMA
*
*        RECALL FROM DISK OR CORE AND SEND THRU MH
*
RECALL   EQU   *
         L     R2,LCBDCBPT              DCB ADDRESS
         IC    R9,DCBBUFOU-IHADCB(R2)   BUFOUT
         N     R9,MASK                  CLEAR REST OF REG
         SLL   R9,X08                   SET TO STORE IN LCBERBCT
         NC    SCBDEOB+1(3),SCBDEOB+1   FIRST RETRY OF HEADER
         BNZ   CKQTYP                   BRANCH NO
*
         MVC   SCBDEOB+1(3),SCBSCHDR    SET FOR SEND RECALL
CKQTYP   EQU   *
         TM    SCBQTYPE,SCBCOREQ        IS MESSAGE CORE QUEUED
         BO    CORE                     BRANCH YES
*
         TM    SCBSTAT1,PRFNLSTN        WAS EOM RECALLED LAST TIME
         BO    RECALL1                  BRANCH YES
*
         CLC   SCBDEOB+1(3),SCBCRCD     IS CURRENT RECALL A UNIT
*                                       OF THE LAST BUFFER
         BNL   CORE                     BR IF SENDING, REAL EOM SA63639
         NC    SCBXTRA(3),SCBXTRA       ANY XTRAS ?             SA63639
         BZ    RSET                     BRANCH IF NO            SA63639
         CLC   SCBDEOB+1(3),SCBXTRA     SENDING FROM XTRAS ?    SA63639
         BNL   CORE                     BRANCH IF NO            SA63639
RSET     EQU   *                                                SA63639
*
         OI    SCBSTAT1,PRFNLSTN        RESET TO NOT LAST BUFFER
RECALL1  EQU   *
         XC    SCBNTXT,SCBNTXT          CLEAR FOR RECALL
         XC    SCBXTRA,SCBXTRA          CLEAR FOR RECALL
CORE     EQU   *
         OI    LCBSTAT1,LCBRCLLN        SET RECALL BIT
         MVI   LCBERBST,ZERO            CLEAR ERB STATE
         MVC   LCBLINK-1(4),SCBDEOB     SAVE DEOB ACCROSS RECALL
         LA    R2,AVTDSIOB              DISK I/O QCB
         ST    R1,LCBERBLK-1            LINK BUFFER TO ERB
         MVI   LCBERBPY,PRIRECAL        SET PRIORITY FOR RECALL
POSTERB  EQU   *                                                 S22025
         STH   R9,LCBERBCT              SET ERB COUNTS           S22025
         ST    R7,LCBRCQCB              SET RECALL ADDRESS       S22025
         XC    LCBERBCH(3),LCBERBCH     CLEAR FOR RECALL
         ST    R2,LCBERBQB-1            SET ERB QCB ADDRESS
         LA    R1,LCBERB                POST ERB TO DISK I/O
         BAL   R14,DSPCHAIN             EXIT TO DISPATCHER     @Y17XAMA
*
ERB      EQU   *
         LR    RLCB,R1                  ERB ADDRESS
         LA    R1,LCBERB-IEDQLCB        SIZE OF LCB BEFORE ERB
         SR    RLCB,R1                  LCB ADDRESS NOW IN RLCB
         L     RSCB,LCBSCBA-1           SCB ADDRESS
         SR    R1,R1                    SET TO CLEAR ERB LINK FIELD
         ST    R1,AVTPARM               CLEAR POST LIST          S22025
         TM    LCBERBST,LCBPRCPG        REQUEST FOR A BUFFER     S22025
         BO    GOTBUF                   BRANCH YES               S22025
*                                                                S22025
         TM    LCBSTAT1,LCBRCLLN        LCBSTATE SET TO RECALL
         BZ    RECALL                   BRANCH NO
*
         MVC   SCBUNTCT,SCBDEOB         RESTORE UNIT COUNT
         MVC   SCBDEOB,LCBLINK-1        RESTORE DEOB
         L     RPREFIX,LCBERBCH-1       ADDRESS OF RECALLED SEGMENT
         NI    LCBERBST,LCBEOMSG        RESET STATUS EXCEPT FOR EOM
         OI    LCBERBST,LCBDLNKN
         NI    LCBSTAT1,X'FF'-LCBRCLLN  RESET RECALL FLAG
         LA    R2,AVTINOUT              ADDRESS OF DUMMY IN/OUTEND
         IC    R5,SCBMBHEN-1            SAVE HIGH ORDER BYTE
         ST    R2,SCBMBHEN-1            PREVENT MULTI-BFR HDR
         STC   R5,SCBMBHEN-1            RESTORE
         TM    LCBSTAT1,LCBSENDN        SEND OPERATION
         BZ    SETRCV                   BRANCH NO
*
         NI    PRFSTAT1,X'FF'-PRFDUPLN  RESET DUPL FLAG         SA63962
         XC    LCBERBCH,LCBERBCH        CLEAR ERB CHAIN
         XC    LCBLSPCI(3),LCBLSPCI
         L     R2,LCBDCBPT              DCB ADDRESS
         IC    R9,DCBBUFOU-IHADCB(R2)   BUFOUT
         N     R9,MASK                  CLEAR REST OF REG
         LR    R5,R9                    CLEAR FOR IC
         IC    R5,DCBBUFMA-IHADCB(R2)   BUFMAX
         SR    R5,R9                    DIFFERENCE
         STC   R5,LCBERBCT              SET DISABLED COUNT
         OI    PRFSTAT1,PRFCNCLN        SET FLAG FOR GD
         BAL   R14,FNDTRM               LOCATE TERMINAL ENTRY
*
         SR    R2,R2                    CLEAR FOR INSERT
         CH    R2,SCBEOB                ZERO BYTES INTO BUFFER
         BNE   LVFLAG                   BRANCH NO - LEAVE FLAG
*
         NI    PRFSTAT1,X'FF'-PRFCNCLN  RESET FLAG FOR GD
LVFLAG   EQU   *
         CLI   LCBFLAG1,AVTEZERO       IS THIS A PSEUDO LCB      S22024
         BE    BFRPST                   YES, BRANCH              S22024
         IC    R2,TRMCHCIN-IEDQTRM(R1)  CHARACTERISTICS TABLE INDEX
         BCTR  R2,0                     RELATIVE TO ZERO
         MH    R2,AVTDCTLN              TIMES DCT ENTRY LEN    @Y17XANG
         AL    R2,AVTCSTCS              ADD START ADDRESS OF TABLE
         TM    3(R2),CKCN               DOES TERMINAL HAVE CONTINUE
*                                       AND CHECKING CAPABILITY ?
         BNO   BFRPST                   BRANCH NO - END OF MESSAGE
*
         MVI   LCBCPA+12,X20            SIMULATE CHAN PGM CK FOR GD
         OI    LCBCHAIN,LCBEXCP         SET EXCP REQUIRED
         TM    3(R2),IDL                DOES TERMINAL HAVE A WRITE
*                                       IDLE CAPABILITY ?
         BO    BFRPST                   BRANCH NO - EXCP WILL BE
*                                       DONE BY IEDQGD
*
         NI    LCBCHAIN,X'FF'-LCBEXCP   RESET EXCP REQUIRED
         LA    R2,LCBCPA                ADDRESS OF WRITE IDLE-TIC LOOP
         ST    R2,LCBSTART-1            SET START ADDRESS FOR IEDQKA
         ST    R2,LCBCPA+8              SET TIC OF TIC-IDLE LOOP
         MVI   LCBCPA+8,TIC             SET TIC OP CODE
         LA    R1,LCBFLAG1              IOB ADDRESS
         EXCPVR (1),SUBSYS
BFRPST   EQU   *
         LR    R1,RPREFIX               FIRST BUFFER TO POST
         CLI   LCBERBCT,AVTEZERO        BUFOUT EQUAL BUFMAX     SA69621
         BE    NOPOST                   YES, SKIP ERB POST      SA69621
         TM    LCBERBST,LCBEOMSG        END OF MESSAGE ?         S22025
         BO    NOPOST                   BRANCH IF YES            S22025
         NI    LCBERBST,LCBDLNKF        TURN OFF DELINK SWITCH   S22025
         LA    R1,AVTDSIOB              GET @ OF DISK I/O QCB    S22025
         ST    R1,LCBERBQB-1            STORE @ IN ERB           S22025
         LA    R1,LCBERB                GET ERB ADDRESS          S22025
         ST    RPREFIX,LCBERBLK-1       ADD BUFFER TO POST LIST  S22025
         MVI   LCBERBPY,PRIFSPCI        SET PRIORITY             S22025
NOPOST   EQU   *
         NI    SCBERR4,X'FF'-SCBTXTTN   RESET ERROR FLAG
         BALR  R14,0                    SET FOR LOOP
*
         ST    R7,PRFQCBA-1             SET QCB ADDRESS
         MVI   PRFPRI,PRIMHBFR          SET PRIORITY
         L     RPREFIX,PRFLINK-1        NEXT BUFFER IN CHAIN
         LA    RPREFIX,0(,RPREFIX)      CLEAR HIGH ORDER BYTE
         LTR   RPREFIX,RPREFIX          END OF CHAIN
         BNZ   ADDLABEL      ON ZEROES  POST BUFFERS TO MH FOR @Y17XAMA
*                                       PROCESSING
*                                       IEDQGD WILL CHAIN BUFFER INTO
*                                       THE WRITE IDLE - TIC LOOP
         BAL   R14,DSPCHAIN                                    @Y17XAMA
ADDLABEL EQU   *                                               @OY16454
         BCR   FIFTEEN,R14              CONTINUE WITH NEXT BFR @OY16454
*
FNDTRM   EQU   *
         LH    R1,LCBTTCIN              SOURCE
         N     R1,AVTCLRHI              CLEAR HIGH ORDER HALF
         L     R15,AVTRNMPT             TERMNAME TABLE
         BR    R15                      FIND TERMINAL ENTRY
*
CCWSET   EQU   *
         ST    R12,PRFIOADR-1           SET UP CCW
CCWST1   EQU   *
         MVI   PRFOPCDE,CCWREAD         SET COMMAND CODE         S22025
         OI    PRFFLAGS,CCWCD+CCWSLI    SET FLAGS                S22025
         MVI   PRFFLAGS+1,AVTEZERO      CLEAR UNUSED BYTE        S22025
         MVI   PRFTIC,TIC              SET TIC CODE             SA55402
         NC    PRFTIC+1(3),PRFTIC+1    LAST BUFFER?             SA55402
         BCR   NZ,R10                  NO, RETURN               SA55402
         MVI   PRFTIC+3,LSTTWO         YES, SET DUMMY TIC ADDR  SA55402
         BR    R10                      RETURN
         EJECT
STOPCONT EQU   *                                                 S22024
*        THIS SUBROUTINE IS ENTERED WHEN AN UNRECOVERABLE ERROR  S22024
*        HAS OCCURED.  THE ROUTINE TESTS TO SEE WHETHER 'STOP='  S22024
*        OR 'CONT=' PROCESSING IS SPECIFIED.  REGISTER R2 IS     S22024
*        USED AS THE RETURN REGISTER;  RETURN IS TO 0(R2) IF     S22024
*        'CONT=' IS SPECIFIED OR TO 4(R2) IS 'STOP=' IS          S22024
*        SPECIFIED.                                              S22024
*                                                                S22024
         SR    R2,R2                    OFFSET TO OPTION FIELD  SA68245
         TM    FLAGS,CONT               WAS CONT= SPECIFIED      S22024
         BZ    TSTSTOP                  NO, ASSUME STOP=         S22024
*                                                                S22024
TSTCONT  EQU   *                        'CONT=' PROCESSING       S22024
         TM    FLAGS,STCOOP             OPTION FIELD TO CHECK    S22024
         BZ    XCONT                    NO, UNCONDITIONAL CONT=  S22024
         BAL   R10,LOCSUB               GO CHECK OPTION FIELD    S22024
         B     XSTOP                    OPTION FIELD NOT= SWITCH S22024
         B     XCONT                    OPTION FIELD = SWITCH    S22024
*                                                                S22024
TSTSTOP  EQU   *                        'STOP=' PROCESSING       S22024
         TM    FLAGS,STCOOP             OPTION FIELD TO CHECK    S22024
         BZ    XSTOP                    NO, UNCONDITIONAL STOP=  S22024
         BAL   R10,LOCSUB               GO CHECK OPTION FIELD    S22024
         B     XCONT                    OPTION FIELD NOT= SWITCH S22024
*                                       OPTION FIELD = SWITCH    S22024
XSTOP    B     FOUR(R9)                 TAKE 'STOP=' RETURN     SA68245
*
XCONT    BR    R9                       TAKE 'CONT=' RETURN     SA68245
         EJECT
         DS    0F                       FORCE BOUNDARY ALIGNMENT S22025
MASK     DC    X'0000000F'              MASK TO CLEAR ALL BUT    S22025
*                                       LOW ORDER BYTE           S22025
DC9999   DC    H'9999'                  MAXIMUM SEQUENCE # COUNT S22025
ONE      EQU   1                        ONE                      S22024
TIMES4   EQU   2                        TWO                      S22024
X20      EQU   X'20'                    HEX 20                   S22024
CKCN     EQU   X'14'                    HEX 14                   S22024
IDL      EQU   X'02'                    HEX 02                   S22024
TIC      EQU   X'08'                    HEX 08                   S22024
LSTTWO   EQU   2                                                SA55402
NZ       EQU   7                                                SA55402
RD       EQU   2                        TWO                      S22024
X08      EQU   8                        EIGHT                    S22024
ZERO     EQU   0                        ZERO                     S22024
SIX      EQU   6                        SIX                      S22024
MAX      EQU   6                        SIX                      S22024
D256     EQU   256                      TWO56                    S22024
UNEX     EQU   1                        ONE                      S22024
STCB     EQU   12                       TWELVE                   S22024
SBVTO    EQU   0                        ZERO                     S22024
FOUR     EQU   4                        FOUR                     S22024
X10      EQU   X'10'                    HEX 10                   S22024
REGOF    EQU   X'0C'                    REG12 RETURN FROM QAE  @YM07659
RCVETX   EQU   X'20'                    ETX RECEIVED             S22024
PCI      EQU   X'08'                    HEX 08                   S22024
RCVPCI   EQU   X'22'                    HEX 22                   S22024
NONZRO   EQU   7                        SEVEN                    S22024
CCONTIN  EQU   X'04'                    TERMINAL CAPABLE OF    @SA70192
BUMPOPT  EQU   2                        BUMP TO L/E ADR OPT OFF@XA05300
FIFTEEN  EQU   15                       CONDITION CODE F       @OY16454
STARTMH  DSECT
         DS    F                        TSO ONLY FIELD
FLAGS    DS    BL1                      FLAG BYTE
LCIN     EQU   X'80'                    LINE CONTROL IN
CONT     EQU   X'40'                    CONT= SPECIFIED
STCOOP   EQU   X'20'                    CONT OR STOP SPECIFIED OPTION
CONV     EQU   X'10'                    CONV= SPECIFIE YES OR OPTION
CONVOP   EQU   X'08'                    CONV= SPECIFIED OPTION FIELD
LOGICAL  EQU   X'04'                    LOGICAL= SPECIFIED
LOGICOP  EQU   X'02'                   LOGICAL= SPECIFIED SECOND OPTION
ALTMHAD  DS    AL3                      TSO ONLY                 S22029
LENGTH   DS    XL1                      PARAMETER LIST LENGTH
STCBA    DS    AL3                      STCB ADDRESS
AEINDX   DS    AL1                      OFFSET TO LOCOPT IF REQUIRED
*
LMD      EQU   X'01'                    LMD SPECIFIED           S22025
LMDOP    EQU   X'02'                    LMD SPECIFIED IN OPTION S22025
MHROUT   DS    AL3                      ADDRESS OF STARTMH STCB
LOGOPT   DS    AL1                      POINTER TO LOGICAL= OPTION
LMDOPT   DS    0AL1                     OPTION FIELD INDEX       S22025
CONVOPT  DS    AL1                      POINTER TO CONV= OPTION
OPTION   DS    AL2                      START OF OPTION FIELD POINTERS
         EJECT
         TCCWD                                                   S22025
         EJECT
         TTRMD
         TLCBD
         TSCBD
         TDISPD
         TPRIOR
         TQCBD
         TAVTD SAVT=YES                                          S22024
         TPRFD
         DCBD  DSORG=TX
         END
