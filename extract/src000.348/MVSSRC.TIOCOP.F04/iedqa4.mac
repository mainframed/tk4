A401     TITLE '''IEDQA4'' - INCOMING MESSAGE DELIMITER ROUTINE'
IEDQA4   CSECT
*A053000,254000,258000,389000,602200-603600                      S22025
*C004304,005320,108000,114000,246000-252000,256000,260000,262000 S22025
*C274000-278000,386000                                           S22025
*D110000-113000,206000-220000,388000,390000                      S22025
*C 300000                                                        X01004
*A278222                                                        SA59523
*D 074000                                                        X01004
*A312000                                                         A42391
*A018000                                                       @Y17XAMG
*C512000                                                       @Y17XAMG
*A417000                                                       @Y17XAMQ
***********************************************************************
*                                                                     *
*STATUS -- CHANGE LEVEL 10                                       X03039
*                                                                     *
*FUNCTION -- INITIALIZES THE BUFFER AND SELECTS EXIT FROM THE         *
*   MESSAGE HANDLER,  VIA EITHER A BRANCH TO BUFFER ASSOCIATION,      *
*   A BRANCH TO THE TRANSPARENT CCW BUILDER,  OR BY A POST OF THE     *
*   BUFFER TO THE PROPER QCB.  IF THE MESSAGE SOURCE IS AN     @Y17XAMG
*   OUTBOARD (OF THE HOST) SNA LU A CHECK IS MADE TO SEE IF    @Y17XAMG
*   A NEGATIVE RESPONSE MUST BE SENT (REQUESTED BY A TCAM      @Y17XAMG
*   COMPONENT OR AS RESULT OF A USER IEDRESP MACRO) AND IF SO  @Y17XAMG
*   LINKAGE IS MADE TO IEDQRS TO SEND A RESPONSE.  THIS IS     @Y17XAMG
*   TRIGGERED BY LEBRSPIN BEING ^0.  IF NO BFRS AVAILABLE,     @Y17XAMG
*   IT WILL BE SENT WHEN QA4 PROCESSES THE NEXT RU; OR,        @Y17XAMG
*   IEDQBD PROCESSES EOM.                                      @Y17XAMG
*                                                                     *
*   THE 'NOT-LAST-BUFFER' BIT IN THE BUFFER PREFIX STATUS FIELD       *
*   (PRFSTATE) IS TESTED.  IF THIS IS THE LAST BUFFER,  THE           *
*   PARAMETER LIST ADDRESS IS STORED IN THE SCB INMSG/OUTMSG MACRO    *
*   FIELD (SCBMACR).                                                  *
*                                                                     *
*   IF THE BUFFER IS A DUPLICATE HEADER,  THE DUPLICATE HEADER BIT    *
*   IN THE PREFIX (PRFDUPLN IN PRFSTAT1) IS TURNED OFF AND CONTROL    *
*   PASSES TO INMSG OR OUTMSG PROCESSING.                             *
*                                                                     *
*   IF THE BUFFER IS A ZERO-LENGTH BUFFER,  CONTROL PASSES TO INMSG   *
*   OR OUTMSG PROCESSING.                                             *
*                                                                     *
*   IF THE SCAN POINTER IS BEYOND THE END OF THE BUFFER AND THIS IS   *
*   THE LAST BUFFER,  THE INCOMPLETE HEADER BIT IN THE SCB            *
*   (SCBHDRRN IN SCBERR1) IS TURNED ON.  CONTROL THEN PASSES TO       *
*   INMSG OR OUTMSG PROCESSING.                                       *
*                                                                     *
*   SELECTION OF INMSG OR OUTMSG PROCESSING IS DETERMINED BY THE      *
*   SETTING OF THE SEND BIT IN THE LCB (LCBSENDN IN LCBSTATE).        *
*                                                                     *
*   ENTRY FROM INMSG MACRO EXPANSION:                                 *
*   THE SCB DESTINATION QUEUE FIELD (SCBDESTQ) IS EXAMINED.  IF IT    *
*   IS ZEROES,  NO VALID DESTINATION HAS BEEN FOUND.  THE ADDRESS     *
*   OF THE BUFFER RETURN QCB,  WHICH IS IN THE AVT (AVTBFRTB),  IS    *
*   ACCESSED AND STORED IN SCBDESTQ.                                  *
*                                                                     *
*   IF THIS IS THE FIRST BUFFER OF A MESSAGE BEING MULTIPLY ROUTED,   *
*   THE ROUTINE LINKS TO THE BUFFER SCAN ROUTINE (IEDQAX) TO FIND THE *
*   UNIQUE CHARACTER X'DF',  WHICH FLAGS THE END OF THE FIRST         *
*   DESTINATION NAME,  IS FOUND.  THE UNIQUE CHARACTER IS THEN        *
*   REPLACED WITH THE REAL LAST CHARACTER OF THE NAME,  AND ITS       *
*   OFFSET IS SET INTO THE SCB MULTIPLE ROUTE FIRST SECONDARY         *
*   DESTINATION FIELD (SCBMRFSD).  IF THE UNIQUE CHARACTER IS NOT     *
*   FOUND,  SCBMRFSD IS CLEARED TO ZEROES.                            *
*                                                                     *
*   IF THIS IS THE LAST BUFFER,  OR IF THE LOGICAL EOM BIT IN THE     *
*   TIC FIELD OF THE BUFFER IS SET,  THE ADDRESS OF THE BUFFER        *
*   DISPOSITION QCB IS SET IN THE BUFFER.  OTHERWISE THE DESTINA-     *
*   TION QCB ADDRESS IN THE SCB (SCBDESTQ) IS SET IN THE BUFFER.      *
*                                                                     *
*   UNLESS THIS IS A TSO BUFFER,  THE NUMBER OF UNUSED RESERVE        *
*   CHARACTERS (FOUND IN LCBISZE) IS SET INTO THE SCAN POINTER        *
*   FIELD (PRFSCAN).  THE BUFFER IS POSTED TO THE QCB VIA AN EXIT     *
*   TO THE DISPATCHER AT THE LABEL DSPPOST.                           *
*                                                                     *
*   ENTRY FROM OUTMSG MACRO EXPANSION:                                *
*   IF THIS IS A ZERO-LENGTH BUFFER,  CONTROL PASSES TO INMSG         *
*   PROCESSING TO POST THE BUFFER TO THE BUFFER DISPOSITION QCB.      *
*                                                                     *
*   IF THERE ARE ANY EMPTY UNITS AT THE END OF THE BUFFER (I.E.,      *
*   UNITS NOT CONTAINING MESSAGE TEXT),  THEY ARE COUNTED AND THEN    *
*   DE-LINKED FROM THE BUFFER.  AFTER THE UNIT COUNTS (PRFNBUNT) IN   *
*   EACH STRING OF UNITS IS RESET,  THE STRING OF EMPTY UNITS IS      *
*   POSTED TO THE BUFFER RETURN QCB VIA A LINK TO THE DISPATCHER AT   *
*   THE LABEL DSPPOSTR.                                               *
*                                                                     *
*   IF THE DESTINATION IS A PROCESS QCB,  THE NUMBER OF UNUSED        *
*   RESERVE CHARACTERS IS SET IN PRFSCAN AND THE BUFFER IS POSTED     *
*   TO THE READ-AHEAD QCB VIA AN EXIT TO THE DISPATCHER AT THE        *
*   LABEL DSPPOST.                                                    *
*                                                                     *
*   TESTS ARE MADE TO DETERMINE IF AN UNNEEDED EOA STRING IN THE      *
*   BUFFER SHOULD BE LOGICALLY REMOVED.  IT SHOULD NOT BE REMOVED     *
*   IF (1) LINE CONTROL INSERTION HAS BEEN PERFORMED,  (2) IF THE     *
*   BUFFER IS A TSO BUFFER,  (3) IF THE DESTINATION IS A BINARY       *
*   SYNCHRONOUS (BISYNC) DEVICE,  (4) IF THE DESTINATION IS AN IBM    *
*   2260 REMOTE,  OR (5) IF THE DESTINATION IS AN IBM 2760 IN         *
*   TETE-A-TETE MODE.  THE STRING WILL NOT BE PRESENT IF (1) THE      *
*   BUFFER IS NOT A HEADER OR (2) THERE IS NO SPECIAL CHARACTERS      *
*   TABLE FOR THE DESTINATION.  ONLY IF NONE OF THESE CONDITIONS IS   *
*   PRESENT IS THE EOA STRING (IF PRESENT) LOGICALLY REMOVED.         *
*                                                                     *
*   THE CONFIGURATION FOR AN EOA STRING FOR THE DESTINATION IS        *
*   ACCESSED FROM THE SPECIAL CHARACTERS TABLE AND COMPARED WITH      *
*   THE FIRST DATA CHARACTERS IN THE BUFFER.  IF THE STRING IS        *
*   PRESENT,  IT IS LOGICALLY REMOVED BY INCREMENTING THE NUMBER OF   *
*   UNUSED RESERVE CHARACTERS BY THE LENGTH OF THE EOA STRING.        *
*                                                                     *
*   IF THE DESTINATION IS A 3705 TERMINAL, EXIT IS MADE TO     @Y17XAMG
*   THE DATA NIR GENERATOR, IEDNGD.                            @Y17XAMG
*                                                                     *
*   IF THE DESTINATION IS A BISYNC DEVICE IN TRANSPARENT MODE,        *
*   EXIT IS MADE TO THE TRANSPARENT CCW BUILDER (IEDQGT).  IF NOT,    *
*   EXIT IS MADE TO THE BUFFER ASSOCIATION ROUTINE (IEDQGD).          *
*                                                                     *
*ENTRY POINTS --                                                      *
*       'IEDQA401' TO INITIALIZE THE BUFFER AND SELECT EXIT FROM      *
*   THE MESSAGE HANDLER.  ENTRY IS EITHER FROM THE USER INTERFACE     *
*   ROUTINE OR BY A DIRECT BRANCH FROM THE LINE CONTROL INSERTION     *
*   ROUTINE (IEDQAK).  CALLING SEQUENCE FROM USER INTERFACE IS:       *
*                                                                     *
*        L     R12,AVTMSGS-1            GET ADDR OF VCON TABLE        *
*        IC    R15,AVTEZERO(,R1)        GET INDEX TO ROUTINE ADDR     *
*        L     R12,AVTEZERO(R12,R15)    GET ROUTINE ADDRESS           *
*        BR    R12                      EXIT TO ROUTINE               *
*                                                                     *
*   CALLING SEQUENCE FROM IEDQAK IS:                                  *
*                                                                     *
*        LA    R15,INDEXA4              SET IEDQA4 INDEX IN R15       *
*        L     RBASE,AVTMSGS-1          GET MH VCON TABLE ADDRESS     *
*        L     RBASE,AVTEZERO(R15,RBASE) GET ADDRESS OF IEDQA4        *
*        BR    RBASE                    ENTER INMSG/OUTMSG ROUTINE    *
*                                                                     *
*INPUT --                                                             *
*   REGISTER 1 - THE ADDRESS OF A MACRO-GENERATED PARAMETER LIST.     *
*   PARAMETER LIST FORMAT ON ENTRY FROM USER INTERFACE IS:            *
*                                                                     *
*        *****************                                            *
*        * INDEX *       *                                            *
*        *  TO   * X'02' *                                            *
*        *   A4  *       *                                            *
*        *****************                                            *
*                                                                     *
*   PARAMETER LIST FORMAT ON ENTRY FROM IEDQAK IS:                    *
*                                                                     *
*        *********************************                            *
*        * INDEX *       * INDEX * INDEX *                            *
*        *  TO   * X'04' *  TO   *  TO   *                            *
*        *   AK  *       *   AF  *   AO  *                            *
*        *********************************                            *
*                                                                     *
*   REGISTER 3 - THE ADDRESS OF THE SCB.                              *
*                                                                     *
*   REGISTER 4 - THE ADDRESS OF THE LCB                               *
*                                                                     *
*   REGISTER 6 - THE ADDRESS OF THE BUFFER.                           *
*                                                                     *
*   REGISTER 9 - THE ADDRESS OF THE AVT.                              *
*                                                                     *
*   REGISTER 12 - THE ENTRY POINT ADDRESS AND BASE REGISTER FOR       *
*   THIS ROUTINE.                                                     *
*                                                                     *
*OUTPUT --                                                            *
*   SCB INMSG/OUTMSG MACRO FIELD (SCBMACR) - IF LAST BUFFER,  THE     *
*   ADDRESS OF THE PARAMETER LIST.                                    *
*                                                                     *
*   SCB DESTINATION QCB FIELD (SCBDESTQ) - IF NO VALID DESTINATION    *
*   ON ENTRY FROM INMSG MACRO EXPANSION,  THE ADDRESS OF THE          *
*   BUFFER RETURN QCB.                                                *
*                                                                     *
*   BUFFER RECB QCB ADDRESS FIELD (PRFQCBA) - IF ENTRY FROM INMSG     *
*   MACRO EXPANSION AND NOT LAST BUFFER,  THE QCB ADDRESS FROM        *
*   SCBDESTQ.  IF ENTRY FROM INMSG EXPANSION AND LAST BUFFER,  THE    *
*   ADDRESS OF THE BUFFER DISPOSITION QCB.  IF ENTRY FROM OUTMSG      *
*   MACRO EXPANSION AND THE DESTINATION IS A PROCESSING PROGRAM,      *
*   THE ADDRESS OF THE READ-AHEAD QCB FOR THE PROCESSING PROGRAM.     *
*                                                                     *
*   BUFFER RECB PRIORITY BYTE (PRFPRI) - IF THE BUFFER IS BEING       *
*   POSTED TO THE QCB INDICATED IN PRFDESTQ,  THE DESTINATION-QCB     *
*   PRIORITY (PRIDESTQ).  IF THE BUFFER IS BEING POSTED TO THE        *
*   BUFFER DISPOSITION ROUTINE,  THE DISPOSITION-LAST-BUFFER          *
*   PRIORITY (PRIDSPLB).  IF THE BUFFER IS BEING POSTED TO A          *
*   PROCESSING PROGRAM,  THE APPLICATION-PROGRAM-BUFFER PRIORITY.     *
*                                                                     *
*   REGISTER 1 - ON A POSTING EXIT,  THE ADDRESS OF THE BUFFER.       *
*                                                                     *
*EXTERNAL REFERENCES --                                               *
*   'IEDQAX' - BUFFER SCAN ROUTINE.                              S22025
*                                                                     *
*   AVTRNMPT - TNT CODE                                               *
*                                                                     *
*   AVT - ADDRESS VECTOR TABLE.                                       *
*                                                                     *
*   BUFFER CURRENTLY BEING PROCESSED.                                 *
*                                                                     *
*   LCB - LINE CONTROL BLOCK                                          *
*                                                                     *
*   SCB - STATION CONTROL BLOCK FOR CURRENT STATION.                  *
*                                                                     *
*   DCB - DATA CONTROL BLOCK                                          *
*                                                                     *
*   SCT - SPECIAL CHARACTERS TABLE                                    *
*                                                                     *
*   TERMINAL TABLE.                                                   *
*                                                                     *
*   QCB - THE QUEUE CONTROL BLOCK.                                    *
*                                                                     *
*   PEWA - THE PROCESS ENTRY WORK AREA.                               *
*   IEDQRS - THE RESPONSE GENERATION ROUTINE.                  @Y17XAMG
*
*                                                                     *
*EXITS,  NORMAL --                                                    *
*   A BUFFER,  NOT THE LAST BUFFER,  IS PASSED FROM AN INMSG MACRO    *
*   EXPANSION.  A VALID DESTINATION IS INDICATED IN THE SCB DESTI-    *
*   NATION QCB FIELD (SCBDESTQ).  THE BUFFER RECB QCB ADDRESS FIELD   *
*   (PRFQCBA) CONTAINS THE ADDRESS OF THE QCB INDICATED IN            *
*   SCBDESTQ.  THE BUFFER RECB PRIORITY BYTE (PRFPRI) CONTAINS THE    *
*   DESTINATION-QUEUE PRIORITY (PRIDESTQ).  REGISTER 1 CONTAINS THE   *
*   ADDRESS OF THE BUFFER.  EXIT IS TO THE DISPATCHER AT ENTRY        *
*   POINT DSPPOST.                                                    *
*                                                                     *
*   A BUFFER,  NOT THE LAST BUFFER,  IS PASSED FROM AN INMSG MACRO    *
*   EXPANSION.  SCBDESTQ CONTAINED ZEROES UPON ENTRY,  INDICATING     *
*   VALID DESTINATION NOT FOUND;  ON EXIT,  SCBDESTQ CONTAINS THE     *
*   ADDRESS OF THE BUFFER RETURN QCB.  PRFQCBA ALSO CONTAINS THE      *
*   ADDRESS OF THE BUFFER RETURN QCB.  PRFPRI CONTAINS PRIDESTQ.      *
*   REGISTER 1 CONTAINS THE ADDRESS OF THE BUFFER.  EXIT IS TO THE    *
*   DISPATCHER AT ENTRY POINT DSPPOST.                                *
*                                                                     *
*   THE LAST BUFFER OF A MESSAGE IS PASSED FROM AN INMSG MACRO        *
*   EXPANSION.  THE SCB INMSG/OUTMSG MACRO FIELD (SCBMACR) CONTAINS   *
*   THE ADDRESS OF THE PARAMETER LIST.  A VALID DESTINATION IS        *
*   INDICATED IN SCBDESTQ.  PRFQCBA CONTAINS THE ADDRESS OF THE       *
*   BUFFER DISPOSITION QCB.  PRFPRI CONTAINS THE DISPOSITION-LAST-    *
*   BUFFER PRIORITY (PRIDSPLB).  REGISTER 1 CONTAINS THE ADDRESS      *
*   OF THE BUFFER.  EXIT IS TO THE DISPATCHER AT ENTRY POINT          *
*   DSPPOST.                                                          *
*                                                                     *
*   THE LAST BUFFER OF A MESSAGE IS PASSED FROM AN INMSG MACRO        *
*   EXPANSION.  SCBMACR CONTAINS THE ADDRESS OF THE PARAMETER LIST.   *
*   SCBDESTQ CONTAINED ZEROES UPON ENTRY,  INDICATING VALID DESTI-    *
*   NATION NOT FOUND;  ON EXIT,  SCBDESTQ CONTAINS THE ADDRESS OF     *
*   THE BUFFER RETURN QCB.  PRFQCBA CONTAINS THE ADDRESS OF THE       *
*   BUFFER DISPOSITION QCB.  PRFPRI CONTAINS PRIDSPLB.  REGISTER 1    *
*   CONTAINS THE ADDRESS OF THE BUFFER.  EXIT IS TO THE DISPATCHER    *
*   AT ENTRY POINT DSPPOST.                                           *
*                                                                     *
*   A BUFFER IS PASSED FROM AN OUTMSG MACRO EXPANSION OR FROM    X03039
*   THE LINE CONTROL INSERTION ROUTINE. THE DESTINATION IS A     X03039
*   3705 TERMINAL.  EXIT IS TO THE DATA NIR GENERATOR, IEDNGD. @Y17XAMG
*                                                                     *
*   A BUFFER IS PASSED FROM AN OUTMSG MACRO EXPANSION OR FROM THE     *
*   LINE CONTROL INSERTION ROUTINE.  THE DESTINATION IS EITHER NOT    *
*   A BISYNC DEVICE OR A BISYNC DEVICE NOT IN TRANSPARENT MODE.       *
*   EXIT IS TO THE BUFFER ASSOCIATION ROUTINE.                        *
*                                                                     *
*   A BUFFER IS PASSED FROM AN OUTMSG MACRO EXPANSION OR FROM THE     *
*   LINE CONTROL INSERTION ROUTINE.  THE DESTINATION IS A BISYNC      *
*   DEVICE IN TRANSPARENT MODE.  EXIT IS TO THE TRANSPARENT CCW       *
*   BUILDER.                                                          *
*                                                                     *
*   A BUFFER IS PASSED FROM AN OUTMSG MACRO EXPANSION OR FROM THE     *
*   LINE CONTROL INSERTION ROUTINE.  THE DESTINATION IS A PROCESS-    *
*   ING PROGRAM.  THE BUFFER IS POSTED TO THE READ-AHEAD QCB VIA      *
*   A BRANCH TO THE DISPATCHER AT THE LABEL DSPPOST.                  *
*                                                                     *
*EXITS,  ERROR -- N/A.                                                *
*                                                                     *
*TABLES/WORK AREAS -- N/A.                                            *
*                                                                     *
*ATTRIBUTES -- SERIALLY REUSABLE, REFRESHABLE, ENABLED, RESIDENT,     *
*   PROBLEM PROGRAM MODE.                                             *
*                                                                     *
*NOTES -- THE OPERATION OF THIS MODULE DOES NOT DEPEND UPON AN        *
*   INTERNAL REPRESENTATION OF THE EXTERNAL CHARACTER SET.            *
*                                                                     *
***********************************************************************
         EJECT
********* REGISTER EQUATES *********
         SPACE
R0       EQU   0                        WORK REGISTER
         SPACE
R1       EQU   1                        PROGRAM PARAMETER REGISTER
*
RWORK    EQU   2                        WORK REG                 S22024
*
RSCB     EQU   3                        BASE REGISTER FOR SCB
R3       EQU   3                        WORK REGISTER            S22024
*
RLCB     EQU   4                        BASE REGISTER FOR LCB
         SPACE
RSCAN5   EQU   5                        WORK REGISTER
         SPACE
RPREFIX  EQU   6                        BASE REGISTER FOR BUFFER
         SPACE
RSCT7    EQU   7                        SPECIAL CHARS TABLE ADDRESS
RSCANOFF EQU   7                        BUFFER STEP OFFSET REGISTER
RQCB     EQU   7                        QCB ADDRESS              S22024
         SPACE
RAVT     EQU   9                        BASE FOR AVT
         SPACE
RDISP    EQU   11                       ENTRY TO DISPATCHER
         SPACE
R12      EQU   12                       REG 12                   S22024
R13      EQU   13                       ADDRESS OF AVT           S22025
         SPACE
R14      EQU   14                       WORK REGISTER
R15      EQU   15                       ENTRY POINT AND RETURN CODE
*
********* OTHER EQUATES *********
         SPACE
ONE      EQU   1                        INCREMENT VALUE OF ONE
TWO      EQU   2                        COMPARE LNG FOR LAST UNIT
THREE    EQU   3                        LENGTH OF A 3-BYTE ADDRESS
         SPACE
F2260R   EQU   X'02'                    IBM 2260 REMOTE FLAG
F2760    EQU   X'01'                    IBM 2760 FLAG
UNIQUE   EQU   X'DF'                    MULTIPLE ROUTE BYTE
         SPACE
SCTEOA   EQU   1                        SCT INDEX BYTE TO EOA
         SPACE
ZERO     EQU   8                        CONDITION CODE: ZERO
         EJECT
         USING IEDQAVTD,RAVT
         USING IEDQDISP,RDISP
         USING IEDQSCB,RSCB             GIVE SCB A BASE
         USING IEDQLCB,RLCB             GIVE LCB A BASE
         USING IEDQPRF,RPREFIX          GIVE PREFIX A BASE
         USING IEDQA4,R12
         SPACE
IEDQA401 EQU   *
IEDQA4   IEDHJN INOUTMSG                ID AND DATE              S22025
         LA    R13,AVTSAVE2             SET R13 FOR TCAM         S22025
         TM    PRFSTAT1,PRFNLSTN        LAST BUFFER
         BO    TESTTSO                  NO, DON'T SAVE PARM ADDR
*                                       YES, SAVE IT
         SPACE
         IC    R15,SCBMBHEN-1           SAVE HI-ORDER BYTE
         ST    R1,SCBMBHEN-1            STORE PARAM LIST ADDR
         STC   R15,SCBMBHEN-1           RESTORE HI-ORDER BYTE
         SPACE
TESTTSO  EQU   *
         TM    LCBTSOB,LCBTSBUF         IS THIS TSO                 TSO
         BO    TSTENTRY                 YES, DON'T PASS SCAN PTR,   TSO
*                                         DON'T SET INCL HDR        TSO
         SPACE
         SR    R0,R0                    TEST IF ZERO-
         CH    R0,PRFSIZE                 LENGTH BUFFER
         BE    TSTENTRY                 YES, DON'T SET INCL HDR,
*                                         DON'T PASS SCAN POINTER
         SPACE
         CLC   PRFSCAN,PRFSIZE          SCAN PTR BEYOND END OF BFR
         BNH   PASSSCAN                 NO, DON'T SET INCL HDR
         SPACE
         TM    PRFSTAT1,PRFDUPLN+PRFNLSTN DUPL HDR OR NOT LAST BFR
         BNZ   PASSSCAN                 YES, DON'T SET INCL HDR
         SPACE
         OI    SCBERR1,SCBHDRRN         SET INCOMPLETE HEADER BIT
         SPACE
PASSSCAN EQU   *
         TM    PRFSTAT1,PRFNHDRN        IS THIS A TEXT BUFFER
         BO    TSTENTRY                 YES, DON'T PASS SCAN PTR
         SPACE
         LA    RWORK,AVTEFF             SET DEFAULT VALUE
         CLC   PRFSCAN,AVTCLRHI+1       SCAN HIGHER THAN 255
         BH    SETSCAN                  YES, PASS DEFAULT VALUE
         SPACE
         IC    RWORK,PRFSCAN+1          NO, PASS TRUE SCAN POINTER
         SPACE
SETSCAN  EQU   *
         STC   RWORK,PRFSCAN            PASS SCAN PTR IN BUFFER
         SPACE
TSTENTRY EQU   *
         NI    PRFSTAT1,PRFDUPLF        MAKE SURE DUPL HDR FLAG OFF
         TM    LCBSTATE,LCBSENDN        SENDING OR RECEIVING?
         BO    OUTMSG                   SENDING, OUTMSG PROCESSING
*        B     INMSG                    RECEIVE, INMSG PROCESSING
         EJECT
* THIS SECTION OF CODE PROVIDES FOR ENTRY FROM INMSG MACRO
         SPACE
INMSG    EQU   *
         NI    PRFSTAT1,PRFCNCLF        TURN OFF CANCEL BIT IF ON
         SPACE
TESTMLTR EQU   *
         TM    PRFSTAT1,PRFNHDRN        IS THIS A HEADER BUFFER
         BO    TESTPOST                 NO, BYPASS MULT ROUTE TEST
         SPACE
         LH    R15,SCBMRFSD             IS THERE MULT ROUTING
         LTR   R15,R15
         BZ    TESTPOST                 NO, PROCEED
         SPACE
         SR    RSCANOFF,RSCANOFF        YES, PICK UP NO. OF
         IC    RSCANOFF,LCBISZE           RESERVE CHARACTERS
         LA    RSCANOFF,AVTHDRSZ(,RSCANOFF) ADD PREFIX SIZE
         LR    R0,RSCANOFF              PASS SCAN PTR OFFSET     S22025
         LNR   RWORK,R12                INSURE REG 2 IS NEGATIVE S22025
         MVI   AVTPARM,X'01'            MOVE CHAR COUNT TO PARM  S22025
         MVI   AVTPARM+1,X'DF'          MOVE CHAR STRING TO PARM S22025
         LA    R1,AVTPARM               LOAD ADDR OF CHAR STRING S22025
         L     R15,AVTAX                LOAD SCAN ROUTINE ADDR   S22025
         BALR  R14,R15                  BRANCH TO SCAN ROUTINE   S22025
         LTR   R15,R15                  OUT OF BUFFER            S22025
         BZ    NOMULTR                  YES, CLEAR               S22025
         SPACE
         SPACE
         IC    R15,SCBMRFSD+1           REPLACE 'DF' WITH        S22025
         STC   R15,AVTEZERO(,R1)          SAVED CHARACTER        S22025
         STH   R0,SCBMRFSD              SAVE OFFSET IN SCB       S22025
         TM    SCBERR2,SCBFRWDN         FORWARD ERROR           SA59523
         BNO   TESTPOST                 BRANCH IF NO            SA59523
         SR    RWORK,RWORK              CLEAR OFFSET            SA59523
         SPACE
NOMULTR  EQU   *
         STH   RWORK,SCBMRFSD             TO ZEROES
         SPACE
TESTPOST EQU   *
*
*
*        THIS SECTION OF CODE DETERMINES IF A RESPONSE IS TO BE@Y17XAMQ
*        SENT TO AN LU ATTACHED THRU AN NCP.                   @Y17XAMQ
*                                                              @Y17XAMQ
         CLI   LCBFLAG1,0               IS THIS A PLCB         @Y17XAMQ
         BNE   NORSP                      BRANCH NO - LCB      @Y17XAMQ
         SPACE
         TM    LCBSTAT5,LCBLUNIT        LU CURRENTLY CONNECTED @Y17XAMQ
         BZ    NORSP                      BRANCH NO            @Y17XAMQ
         SPACE
         TM    LCBRSPFG,LCBRSRH         RESPONSE IN PLCB?      @YM07705
         BZ    NORSP                      BRANCH NO            @Y17XAMQ
         TM    LCBSTAT2,LCBRESP         RESPONSE OWED?         @YM04677
         BZ    NORSP                      BRANCH NO            @YM04677
         SPACE
         L     R15,AVTSAVTF             SAVT ADDRESS           @Y17XAMQ
         LR    R0,R12                   POSITIVE VALUE FOR RS  @YM07802
         L     R12,SAVTQRS-IEDNSVTD(,R15)  ADDRESS OF IEDQRS   @YM07802
         SLR   R1,R1                    ZER0 R1 AS FLAG TO RS  @Y17XAMQ
         BALR  R14,R12                    LINK TO SEND RESPONSE@Y17XAMQ
         LR    R12,R0                   RESTORE BASE REG       @Y17XAMB
         SPACE
NORSP    EQU   *                                               @Y17XAMQ
         TM    PRFSTAT1,PRFNLSTN        IS IT END OF MESSAGE
         BZ    POSTDISP                 YES - GO TO POST TO DISPOSIT
         SPACE
         TM    PRFSTAT1,PRFITCPN        TEST FOR LOGICAL EOM     X01004
         BO    POSTDISP                 ON, POST TO DISPOSITION
*
* THIS SECTION POSTS THE BUFFER, WHICH IS NOT THE LAST BUFFER OF A
* MESSAGE, TO THE QCB FOR ITS DESTINATION
*
         MVC   PRFRCB+1(THREE),SCBDESTQ
         L     R15,SCBDESTQ-1          GET DESTINATION           A42391
         LA    R15,AVTEZERO(,R15)      CLEAR HI ODER BYTE        A42391
         LA    RLCB,AVTBFRTB           ADDR OF BUF RTN QCB       A42391
         CR    RLCB,R15                BUF RTN THE DEST ?        A42391
         BNE   NORTN                   BRANCH NO                 A42391
         MVI   PRFPRI,PRILNFRE         POST WITH LOW PRIORITY    A42391
         B     SETRESER                GO PASS NO IDLES          A42391
NORTN    EQU   *
         MVI   PRFPRI,PRIDESTQ          SET PRIORITY
         TM    PRFSTAT1,PRFNHDRN        IS THIS A HEADER BUFFER
         BO    SETRESER                 NO, GO PASS NO. IDLES
         SPACE
         MVI   PRFPRI,PRIDESTQ+1        YES, SET HIGHER PRIORITY
         B     SETRESER                 GO PASS NO. REMAINING IDLES
*
POSTDISP EQU   *
*
* THIS SECTION POSTS THE LAST BUFFER OF A MESSAGE TO THE QCB FOR
* THE DISPOSITION ROUTINE
*
         L     RWORK,AVTMSGS-1          GET VCON TABLE ADDR
         MVC   PRFQCBA,ONE(RWORK)       PUT BUF DISP QCB IN PREF S22025
         MVI   PRFPRI,PRIDSPLB          SET PRIORITY
         SPACE
SETRESER EQU   *
         L     RLCB,PRFLCB-1            RESET LCB ADDRESS
         TM    LCBTSOB,LCBTSBUF         IS THIS A TSO BUFFER
         BO    EXIT                     YES, DON'T PASS IDLES
         SPACE
         B     MOVEIDL                  BRANCH TO PASS IDLES     S22025
         EJECT
* THIS SECTION OF CODE PROVIDES FOR ENTRY FROM OUTMSG MACRO
         SPACE
OUTMSG   EQU   *
         LH    RSCAN5,PRFSIZE           PICK UP LENGTH OF BUFFER
         N     RSCAN5,AVTCLRHI          IS IT ZERO-LENGTH BUFFER
         BZ    POSTDISP                 YES, POST IT TO BFR DISP
         SPACE
         LR    RWORK,RPREFIX            LOAD PREFIX ADDR         S22025
         BAL   R14,ENTERLOP             GET ADDR OF SCAN POINTER S22025
         SPACE
         CLC   PRFTIC+1-IEDQPRF(TWO,RWORK),AVTFZERO IS IT LAST UNIT
         BE    TESTPROC                 YES, NONE EMPTY, PROCEED
         SPACE
         LR    RSCAN5,RWORK             NO, SAVE LAST DATA UNIT
         SR    R1,R1                    CLEAR COUNT REG
         SPACE
NOTLAST  EQU   *
         LA    R1,ONE(,R1)              COUNT EMPTY UNIT
         L     RWORK,PRFTIC-IEDQPRF(,RWORK) GET EMPTY UNIT'S ADDR
         CLC   PRFTIC+1-IEDQPRF(TWO,RWORK),AVTFZERO IS IT LAST UNIT
         BNE   NOTLAST                  NO, COUNT IT & GET NEXT ONE
         SPACE
         LR    R0,R1                    YES, SAVE NO. EMPTY UNITS
         IC    R1,PRFNBUNT              RESET COUNT IN
         SR    R1,R0                      PREFIX TO NUMBER
         STC   R1,PRFNBUNT                OF UNITS WITH DATA
         SPACE
         L     R1,PRFTIC-IEDQPRF(,RSCAN5) GET ADDR FIRST EMPTY UNIT
         MVC   PRFTIC+1-IEDQPRF(THREE,RSCAN5),PRFTIC+1-IEDQPRF(RWORK)
*                                       INDICATE LAST DATA UNIT
*                                         IS LAST UNIT
         LA    RWORK,AVTBFRTB           SET BUFFER RETURN QCB ADDR
         ST    RWORK,PRFQCBA-1-IEDQPRF(,R1) IN FIRST EMPTY UNIT
         MVI   PRFPRI-IEDQPRF(R1),PRIBFRTB SET BUFFER PRIORITY
         STC   R0,PRFNBUNT-IEDQPRF(,R1) SET NUMBER OF EMPTY UNITS
         L     RDISP,AVTEA              GET ADDRESS OF DISPATCHER
         BAL   R14,DSPPOSTR             LINK TO POST EMPTY UNITS
         SPACE
         L     RSCB,LCBSCBA-1           RESTORE SCB ADDRESS
         SPACE
TESTPROC EQU   *
         L     R1,SCBDESTQ-1            GET QCB ADDRESS
         USING IEDQQCB,R1
         SPACE
         TM    QCBFLAG,QCBPROC          IS IT PROCESS QCB
         BO    POSTMPP                  YES, GO GET QCB ADDRESS
         SPACE
DOUBLEOA EQU   *
         LA    RLCB,0(RLCB)             CLEAR FOR COMPARE      @YM07040
         L     R1,AVTSAVTP              SECONDARY AVT          @YM07040
         USING IEDNSVTD,R1                                     @YM07040
         C     RLCB,SAVTSLCB            IS THIS SSCP PLCB      @YM07040
         BE    PLCB3705                 YES                    @YM07040
         TM    PRFSTAT1,PRFNHDRN        IS THIS A HEADER BUFFER
         BO    SELEXIT                  NO, NO DOUBLE EOA, GO ON
         SPACE
         TM    SCBSTATE,SCBMGFMN        WAS LINE CONTROL INSERTED
         BO    SELEXIT                  YES, NO DOUBLE EOA, GO ON
         SPACE
         TM    LCBSTAT2,LCBSYNC         IS DESTINATION BISYNC
         BO    SELEXIT                  YES, LEAVE STX IN BUFFER
         SPACE
         TM    LCBTSOB,LCBTSBUF         IS THIS A TSO BUFFER
         BO    SELEXIT                  YES, NO DOUBLE EOA, GO ON
         SPACE
         LH    R1,PRFDEST               GET DESTINATION KEY
         N     R1,AVTCLRHI              CLEAR TOP HALF OF REGISTER
         L     R15,AVTRNMPT             LINK TO GET ADDRESS OF
         BALR  R14,R15                    TERMINAL TABLE ENTRY
         SPACE
         USING IEDQTRM,R1
         SR    R15,R15                  (CLEAR FOR INSERT)
         IC    R15,TRMCHCIN             PICK UP DEV CHAR TBL INDEX
         BCTR  R15,AVTEZERO             DECR TO MAKE TRUE INDEX
         MH    R15,AVTDCTLN             MULT BY DCT ENTRY LEN  @Y17XAMG
         A     R15,AVTCSTCS             GET THIS TERM'S ENTRY ADDR
         TM    THREE(R15),F2260R        TEST IF 2260 REMOTE
         BO    SELEXIT                  YES, LEAVE STX IN BUFFER
         SPACE
         TM    THREE(R15),F2760         TEST IF IBM 2760
         BNO   ELIMEOA                  NO, GO ELIMINATE EOA
         SPACE
         TM    LCBSTAT2,LCBRESP         YES, TEST IF TETE-A-TETE
         BO    SELEXIT                  YES, LEAVE IN EOA
         SPACE
ELIMEOA  EQU   *
         L     RSCT7,LCBDCBPT           GET DCB ADDRESS FROM LCB
         USING IHADCB,RSCT7
         L     RSCT7,DCBSCTAD-1         GET SCT ADDRESS FROM DCB
         LA    RSCT7,AVTEZERO(,RSCT7)   CLEAR HIGH-ORDER BYTE
         LTR   RSCT7,RSCT7              IS THERE AN SCT
         BZ    SELEXIT                  NO, NO DOUBLE EOA, GO ON
         SPACE
         SR    R15,R15                  (CLEAR FOR INSERT)
         IC    R15,SCTEOA(,RSCT7)       GET EOA INDEX BYTE
         LA    RWORK,ONE(R15,RSCT7)     POINT TO EOA
         IC    R15,AVTEZERO(R15,RSCT7)  PICK UP LENGTH OF EOA
         LR    RSCT7,RWORK              RESET EOA ADDRESS
         SPACE
         LR    R14,R15                  (CLEAR FOR INSERT)
         IC    R14,LCBISZE              PICK UP RESERVE CHARS COUNT
         LA    RWORK,PRFSHDR(R14)       POINT TO START OF DATA
         BCTR  R15,AVTEZERO             (DECREMENT FOR EXECUTE)
         EX    R15,CLCEOA               IS EOA STRING IN BUFFER
         BNE   SELEXIT                  NO, NO DOUBLE EOA, GO ON
         SPACE
         LA    R15,ONE(R14,R15)         YES, BUMP RESERVE COUNT BY
         STC   R15,LCBISZE                EOA LENGTH AND STORE BACK
         SPACE
SELEXIT  EQU   *
         NI    LCBSTAT2,AVTEFF-LCBRESP  TURN OFF TETE-A-TETE FLAG
         CLI   LCBFLAG1,AVTEZERO        IS THIS A 3705 PLCB ?    S22024
         BE    PLCB3705                 BR YES                   X03039
         L     R15,AVTGD                NO, GET ADDR OF BFR ASSOC
         TM    LCBSTAT2,LCBSYNC         TEST IF BISYNC
         BCR   ZERO,R15                 NO, GO TO BFR ASSOCIATION
         SPACE
         TM    SCBSTATE,SCBTRANP        TEST IF TRANSPARENT
         BCR   ZERO,R15                 NO, GO TO BFR ASSOCIATION
         SPACE
         L     R15,AVTGT                ELSE GET TRANS CCW BUILDER
         BR    R15                        ADDR & GO TO IT
         SPACE 2
PLCB3705 EQU   *                                                 S22024
         L     RQCB,AVTSAVTP            SECONDARY AVT ADDRESS    S22024
*                                       DATA QCB ADDRESS       @Y17XAMZ
         L     RQCB,SAVTDNIR-IEDNSVTD(,RQCB)                   @Y17XAMZ
*                                       STCB ADDRESS             X03039
         L     R3,QCBSTCHN-ONE-IEDQQCB(,RQCB)                    X03039
         MVI   PRFPRI,PRIMHBFR          SET PRIORITY FOR MH      S22024
         LR    R1,RPREFIX               PARAMETER FOR POST       S22024
         L     RDISP,AVTEA              ADDR OF DISPATCHER       S22024
         B     DSPBYPAS                 BYPASS TO LCCW GEN'R     X03039
         SPACE 3
ADDRLOOP EQU   *                                                 S22025
         L    RWORK,PRFTIC-IEDQPRF(RWORK) POINT TO NEXT UNIT     S22025
         SH    RSCAN5,AVTKEYLE          SUBTRACT NO. BYTES PASSEDS22025
ENTERLOP EQU   *                                                 S22025
         CH    RSCAN5,AVTKEYLE          IS ITEM IN THIS UNIT     S22025
         BH    ADDRLOOP                 NO, TEST NEXT UNIT       S22025
         LA    RSCAN5,AVTUMALN-1(RSCAN5,RWORK) GET ADDR OF ITEM  S22025
         BR    R14                      RETURN TO CALLER         S22025
         EJECT
POSTMPP  EQU   *
         SPACE
         USING IEDQQCB,R1
         L     R1,QCBPREN               GET TERM TABLE ENTRY ADDR
         USING IEDQTRM,R1
         SPACE
         L     R1,TRMSTAT               GET ADDR OF PROCESS ENTRY
*                                         WORK AREA
         USING IEDQPEWA,R1
         SPACE
         LA    R1,PERAQCB               GET READ-AHEAD QCB ADDR
         ST    R1,PRFRCB                PUT IT IN THE RECB
         MVI   PRFPRI,PRIAPBFR          SET PRIORITY
MOVEIDL  EQU   *                                                 S22025
         IC    RWORK,LCBISZE            PICK UP RESERVE COUNT    S22025
         STC   RWORK,PRFSCAN+1          PUT IN SCAN PTR FIELD    S22025
         SPACE
EXIT     EQU   *
*
* THIS SECTION COMPLETES CONSTRUCTION OF THE POST PARAMETER LIST
* AND RETURNS CONTROL TO THE DISPATCHER
         SPACE
         LR    R1,RPREFIX               GET RECB ADDR IN R1
         L     RDISP,AVTEA              GET ADDRESS OF DISPATCHER
         BAL   R14,DSPPOST              EXIT TO POST RECB      @Y17XAMX
         SPACE
********* EXECUTED INSTRUCTION *********
         SPACE
CLCEOA   CLC   AVTEZERO(AVTEZERO,RWORK),AVTEZERO(RSCT7) COMPARE  S22024
         EJECT
********* DSECTS *********
         SPACE 3
         TAVTD
         SPACE
AVTGT    EQU   AVTA3                    TRANSPARENT CCW BUILDER
         EJECT
         DCBD  DSORG=TX
         EJECT
         TDISPD
         EJECT
         TLCBD
         EJECT
         TPEWAD
         EJECT
         TPRFD
         EJECT
         TPRIOR
         EJECT
         TQCBD
         EJECT
         TSCBD
         EJECT
         TTRMD
         END
