E1       TITLE '''IEDQE1'' - TCAM TCOPY SERVICE ROUTINE'
IEDQE1   CSECT
*C THIS MODULE HAS BEEN REWRITTEN FOR TCAM LEVEL 6               Y02027
*        THIS MODULE HAS BEEN REWRITTEN FOR FETCH PROTECT        Y01004
*C636000-639000                                                  Y05330
*A536000,654000                                                 SA61775
         SPACE 4
***********************************************************************
*                                                                     *
*  MODULE NAME = IEDQE1                                               *
*                                                                     *
*  DESCRIPTIVE NAME = TCAM TCOPY SERVICE ROUTINE                      *
*                                                                     *
*  COPYRIGHT = 'NONE'                                                 *
*                                                                     *
*  STATUS: CHANGE LEVEL 6                                             *
*                                                                     *
* FUNCTION: THIS MODULE BUILDS A PARAMETER LIST FOR INPUT TO SVC 102  *
*           THE ACTUAL PROCESSING FOR TCOPY IS DONE IN THE MCP BY     *
*           IGC0A10D WHO GAINS CONTROL VIA SVC 102'S TPOSTING THE OP  *
*           CONTROL QCB                                               *
* ENTRY POINT:                                                        *
*        IEDQE101                                                     *
* INPUT:                                                              *
*          R0 - ADDRESS OF TERMNAME                                   *
*          R1 - ADDRESS OF USER'S WORKAREA                            *
*          R13- ADDRESS OF USER SAVE AREA                             *
*          R14- RETURN ADDRESS                                        *
*          R15- ENTRY ADDRESS                                         *
* OUTPUT:                                                             *
*          R15- COMPLETION CODE                                       *
* EXTERNAL ROUTINES:                                                  *
*        SVC 102 (IEDQEB)                                             *
*                                                                     *
* EXIT: RETURN TO CALLER WITH RETURN CODE AS PASSED FROM SVC 102      *
*        COMPLETION CODES AS FOLLOWS:                                 *
*              0 = NORMAL                                             *
*              8 = TCAM MCP NOT ACTIVE                                *
*             12 = CALLER NOT A VALID TCAM APPLICATION PROGRAM        *
*             18 = AIB TOO SMALL TO ACCOMODATE DATA                   *
*             20 = INVALID TERMINAL NAME                              *
*                                                                     *
*        IEDQUI-SEARCHES TERMNAME TABLE                               *
* TABLES/WORK AREAS: NONE                                             *
*                                                                     *
* ATTRIBUTES - SERIALLY REUSABLE                                      *
*                                                                     *
*NOTES: THE OPERATION OF THIS MODULE DEPENDS UPON AN INTERNAL         *
*   REPRESENTATION OF THE EXTERNAL CHARACTER SET WHICH IS EQUIVALENT  *
*   TO THE ONE USED AT ASSEMBLY TIME.  THE CODING HAS BEEN ARRANGED   *
*   SO THAT REDEFINITION OF 'CHARACTER' CONSTANTS, BY REASSEMBLY,     *
*   WILL RESULT IN A CORRECT MODULE FOR THE NEW DEFINITIONS.          *
*                                                                     *
***********************************************************************
         EJECT
RZERO    EQU   0                        REG0                     S22024
RPARM    EQU   1                        PARAMETER REGISTER
RWORK    EQU   2                        WORK REGISTER            Y02027
RBASE    EQU   12                       CSECT BASE REGISTER
RSAVE    EQU   13                       SAVEAREA REGISTER
RETURN   EQU   14                       RETURN ADDRESS
RENTRY   EQU   15                       ENTRY POINT/RETURN CODE
         SPACE 4
IEDQE101 EQU   *
         STM   RETURN,RBASE,TWLV(RSAVE) SAVE CALLER'S REGS       Y02027
         BALR  RBASE,0                  ESTABLISH
         USING *,RBASE                           ADDRESSABILITY
IEDQE1   IEDHJN ENTER
         EJECT
         ST    RSAVE,SAVEAREA+FOUR      LINKAGE                  Y01004
         LA    RSAVE,SAVEAREA           CONVENTIONS              Y01004
         SPACE 2                                                 Y02027
**************************************************************** Y02027
*              BUILD PARAMETER LIST INPUT TO SVC 102           * Y02027
**************************************************************** Y02027
         SPACE 1
         STCM  RPARM,ADD,APUSWKA        SET USER WORK AREA PTR   Y02027
         ST    RZERO,APNAME             SET PTR TO TERMINAL NAME Y02027
         MVI   APCOMCD,TCOPYCD          TURN ON TCOPY FLAG       Y02027
         SR    RZERO,RZERO              INDICATE TCOPY FUNCTION  Y02027
         LA    RPARM,APPARM             SET PARM REG FOR SVC 102
         AQCTL                          ISSUE SVC                Y02027
         SPACE 2
         LA    RWORK,RC80               CHECK FOR ERROR RET CD   Y02027
         CR    RENTRY,RWORK             IS TCAM ACTIVE           Y02027
         BNE   EXIT                     BR IF YES                Y02027
         LA    RENTRY,NOTCAM            MPP EXPECTS RET CD = 08  Y02027
         SPACE 1
EXIT     EQU   *                                                 Y02027
         L     RSAVE,SAVEAREA+FOUR      RESTORE CALLER'S SAVE    Y02027
*                                       AREA POINTER             Y02027
         RETURN (14,12),T,RC=(15)       RETURN TO USER
         EJECT
**************************************************************** Y02027
*        C O N S T A N T S   A N D   E Q U A T E S             * Y02027
**************************************************************** Y02027
         SPACE 2
SAVEAREA DC    18F'0'                   MY SAVE AREA             Y02027
NOTCAM   EQU   8                        TCAM NOT ACTIVE CODE     Y02027
NOPEN    EQU   12                       NO OPEN DCB CODE         Y02027
ZERO     EQU   0                        OFFSET                   Y02027
FOUR     EQU   4                        OFFSET & MOVE LENGTH     Y02027
ADD      EQU   7                        ADDRESS MASK             Y02027
ONE      EQU   1                        OFFSET                   Y02027
TCOPYCD  EQU   X'80'                    TCOPY FLAG               Y02027
THREE    EQU   3                        MOVE LENGTH              Y02027
FIVE     EQU   5                        OFFSET                   Y02027
EIGHT    EQU   8                        OFFSET                   Y02027
RC80     EQU   X'80'                    CODE RETURNED BY SVC 102 Y02027
*                                       IF TCAM NOT ACTIVE       Y02027
TWLV     EQU   12                       OFFSET
         SPACE 2
         EJECT
**************************************************************** Y02027
*              MAP OF INPUT DATA PROVIDED TO SVC 102           * Y02027
**************************************************************** Y02027
         DS    0F                       FORCE ALIGNMENT          Y02027
APCIBMAP EQU   *                                                 Y02027
APPASWD  DC    AL4(0)                   POINTER TO PASSWORD      Y02027
APECB    DC    F'0'                     ECB FOR WAIT IN SVC 102  Y02027
*                                       AND SUBSEQUENT XMPOST    Y02027
APPCBAD  DC    AL4(0)                   PCB POINTER              Y02027
APCOMCD  DC    XL1'0'                   COMMAND VERB CODE        Y02027
APUSWKA  DC    AL3(0)                   PTR TO USER WORK AREA    Y02027
APNAME   DC    AL4(0)                   PTR TO TERMINAL NAME     Y02027
APPARM   DS    0F                       PARAMETER FOR SVC 102    Y02027
         DC    X'84'                    EOL X'80' + FUNCTION     Y02027
*                                       CODE X'04'               Y02027
         DC    AL3(APCIBMAP)            PTR TO INPUT DATA        Y02027
         SPACE 1
**************************************************************** Y02027
         SPACE 2                                                 S22024
PARMLST  DS    0F                       PARAMETER LIST FOR 3705  S22024
PQCBADR  DC    A(0)                     QCB ADDRESS              S22024
PPRI     DC    XL1'D0'                  POST PRIORITY            S22024
PLINK    DC    AL3(0)                   ELEMENT CHAIN POINTER    S22024
PVERB    DC    XL1'0'                   OP CONTROL VERB CODE     S22024
PENTCD   DC    XL1'0'                   ENTRY CODE FOR TCOPY     S22024
PANSCD   DC    XL1'0'                   SECONDARY ENTRY CODE     S22024
PNOPAS   DC    XL1'FF'                  NO PASSWORD              S22024
PECBA    DC    A(PECB)                  ECB ADDRESS              S22024
PTO      DC    A(0)                     DATA TARGET ADDRESS      S22024
PFROM    DC    A(0)                     DATA SOURCE ADDRESS      S22024
PTNTNDX  DC    H'0'                     TERMNAME INDEX           S22024
PRQTG    DC    H'0'                     REQUEST SEQUENCE NUMBER  S22024
PAQCTL   DC    A(PARMLST)               SOURCE                   S22024
PECB     DC    A(0)                     ECB                      S22024
         DC    A(0)                     LENGTH ADDRESS           S22024
         END
