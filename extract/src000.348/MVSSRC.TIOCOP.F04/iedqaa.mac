QAA1     TITLE '''IEDQAA'' -- STARTMH SUBTASK, TCAM-TSO MIXED' @Y17XAMF
IEDQAA   CSECT
**A-000000-999999                                              @X31X8M0
******************** MICROFICHE FLAGS *********************** SUPT CODE
**C862984                                                        Y01004
**A419000,430000,443000,447000,747000-XXXXXX,748000-XXXXXX       S99528
**A807000-XXXXXX                                                 S99528
**C694000                                                        S99528
**D687400-687460,708000-708300,710000-710500,714000-721000       S99528
**D753000-754000                                                 S99528
**A533500,897020-XXXXXX                                          S22029
**C427000,510000-510500,511000-511500,528000,618360,813000       S22029
**C824100,824500                                                 S22029
**D508000,531000,618030-618330,618390,816000,831000-860000       S22029
**A689000                                                        A44861
**C483000                                                        A44861
**A824000                                                         M6673
**A862000                                                        S21101
**C483000                                                         M6673
**C483000,839000                                                 A44907
**D841000                                                        A44907
**A858100                                                        A44907
**A441000,590070,618333,618400,630300,632000,687100,862980,      S22025
**A872200,926200                                                 S22025
**C556000,569000,610000,617000,632000,636000-644000,687000,      S22025
**C690000-691500,694000,697000-700000,706000,709000-710500,      S22025
**C714000-717000,721000,769000-775000                            S22025
**D578000-583000,662800-665000,718000-719000,725000-743000,      S22025
**D760000-766000,786000-787000,788000-805000,862500-862960,892000S22025
**A467000,680000,822000                                         SA52512
**C630600,872000                                                SA52512
**A872000                                                       SA56222
**C822000                                                       SA56222
**A868000,906000                                                SA54959
**C630600                                                       SA54959
**A565000,932000                                                SA52971
**C538000-541000                                                SA52971
**D543000-554000,562000-563000                                  SA52971
**C882000,887000-888000,895000,897520,902000                     A59509
**D886000,897580,897740                                          A59509
**A897940-897960                                                 A59509
**D862982,862994                                                SA59169
**A464000,642000,861000,863000,897200,936000                     S22024
**A467700-467900,758100-758800,807040-807880                     S22024
**C211000,710400,871000                                          S22024
**C824700                                                       SA61771
**A618354                                                       SA62383
**A467600,566000                                                SA60343
**747800,747880                                                 SA63640
**D747200,747240                                                SA63640
**A467000,907100                                                 Y05330
**C467000,565600,585000,590770,626000,674000,758000,807180,      Y05330
**C807450,807660,897600,906620                                   Y05330
**D451000-453000,453700-454000,459000-461000,508000,521000,      Y05330
**D526000,586000-588000,627000-628000,675000-677000,723000,      Y05330
**D750000,752000,807210-807240,807480-807510,811000,863500,      Y05330
**D897640,906640                                                 Y05330
**D683000-684000                                                 Y06330
*A807000                                                       @SA68025
*C807090                                                       @SA68025
*D807780-809000                                                @SA68025
**D537000-555000                                                SA66605
**A606000                                                       SA66605
**C693000,694000                                               @SA74025
**C704500,706000                                               @SA74025
**A776000                                                      @SA74025
**D868500                                                      @SA74879
*C618450                                                       @SA75454
*A868000                                                       @YA10671
*C867000                                                       @SA75471
**A507000                                                        X03039
**C009600,211600,453600,495000,512070-512910,909500            @Z30X8MJ
**C824800                                                      @ZA04049
*A565000                                                       @OS76313
*A467900,512770                                                @YA12287
*C512630                                                       @YA12287
*A906400,906520,912000                                         @OY12280
*A697000                                                       @OY12671
*A642000,912000                                                @OY13651
*C007000,009200,009600,011000,134000-135000                    @Y17XAMF
*A015000,027000,054000,068000,085000,096000,104000,118000      @Y17XAMF
*A133000,186000,189000,194000,411000,413000,427500,506000      @Y17XAMF
*A507400,908000,912000,912300,936600                           @Y17XAMF
*C876200-876400                                                @Y17XAMF
*A897100,897450                                                @OY15001
*A8718500,986400                                               @YA13265
*A889050                                                       @OZ24601
*A889055                                                       @OX14795
*A871900                                                       @OY15569
*A870950                                                       @OY18039
***********************************************************************
         EJECT
***********************************************************************
*TITLE  'IEDQAA'  STARTMH SUBTASK FOR TCAM AND TSO MH'S        @Y17XAMF
*                                                                     *
*  MODULE NAME = IEDQAA                                               *
*                                                                     *
*  DESCRIPTIVE NAME= STARTMH SUBTASK                           @Y17XAMF
*                                                                     *
*  COPYRIGHT = 'NONE'                                                 *
*                                                                     *
*  STATUS = CHANGE LEVEL 10                                    @Y17XAMF
*                                                                     *
*FUNCTION -- THE STARTMH SUBTASK PERFORMS INITIALIZATION FUNC- @Y17XAMF
*   TIONS FOR A BUFFER BEFORE SENDING IT THROUGH A TCAM OR TSO MES-   *
*   SAGE HANDLER.  THE EXACT FUNCTIONS VARY, DEPENDING UPON WHETHER   *
*   THE MESSAGE SOURCE IA A SNA LU, WHETHER                    @Y17XAMF
*   THE BUFFER IS A HEADER OR A TEXT BUFFER, AND WHETHER IT IS TO BE  *
*   PROCESSED BY THE INCOMING OR OUTGOING SIDE OF THE MH.             *
*                                                              @Y17XAMF
*      IF THE MESSAGE SOURCE OR DESTINATION IS THE SSCP        @Y17XAMF
*   MINIMAL PROCESSING  IS PERFORMED; ENTRY IS MADE DIRECTLY TO@Y17XAMF
*   THE PROPER MH SUBGROUP.                                    @Y17XAMF
*                                                              @Y17XAMF
*      THE FOLLOWING PROCESSING IS PERFORMED EXCLUSIVELY FOR   @Y17XAMF
*   MESSAGES INPUT FROM SNA LU'S:                              @Y17XAMF
*                                                              @Y17XAMF
*      -'RESPONSE REQUIRED' IS SET ON FOC(FIRST OF CHAIN):     @Y17XAMF
*        LCBRESP IS SET;                                       @Y17XAMF
*                                                              @Y17XAMF
*      - DEFAULT ROUTING IS ESTABLISHED: FOR LU*S IN SESSION   @Y17XAMF
*        WITH A TPROCESS ENTRY HAVING LU=YES MESSAGES WILL     @Y17XAMF
*        BE SENT THE TPROCESS QUEUE IF NO FORWARD IS ISSUED;   @Y17XAMF
*                                                              @Y17XAMF
*      - DFC COMMAND COMPATIBILITY MODE CHECKS ARE MADE TO     @Y17XAMF
*        SCREEN OUT COMMANDS NOT SUPPORTED BY THE MH;          @Y17XAMF
*                                                              @Y17XAMF
*      - IF CANCEL IS NOT SUPPORTED IN THE MH THE CANCEL       @Y17XAMF
*        RU IS HANDLED BY THE CHAIN MANAGER IN IEDRQIN         @Y17XAMF
*                                                              @Y17XAMF
*      THIS ROUTINE CHECKS FOR A RECALLED BUFFER. IF IT IS RE- @Y17XAMF
*   CALLED, NO INITIALIZATION FUNCTIONS ARE PERFORMED, AND ONLY THE   *
*   CHECK FOR TRANSLATION IS MADE.  IF THE BUFFER IS OUTGOING, AND    *
*   THE DESTINATION TERMINAL IS IN LOCK MODE, THE LCB COUNT OF OUT-   *
*   STANDING LOCK RESPONSES IS INCREMENTED, AND THE PREFIX LOCK BIT   *
*   IS SET OFF.  IF THE BUFFER, INCOMING OR OUTGOING, IS A TSO BUFFER,*
*   THE LCB RESERVE COUNT IS INITIALIZED TO ZERO.  THEN THIS ROUTINE  *
*   CHECKS TO SEE IF THE BUFFER IS HEADER OR TEXT.  FOR ALL HEADER    *
*   BUFFERS, THE SCB MULTIPLE BUFFER HEADER ENTRY IS SET TO ZERO.     *
*   NEXT, THIS ROUTINE CHECKS TO SEE IF THE BUFFER IS TO BE PROCESSED *
*   BY AN INCOMING OR OUTGOING MH.  PROCESSING IS THEN TAILORED FOR   *
*   EACH OF THE FOUR SITUATIONS.                                      *
*                                                              @Y17XAMF
*      FOR AN INCOMING HEADER BUFFER, THE PREFIX SOURCE FIELD IS      *
*   INITIALIZED FROM THE CURRENT TERMINAL INDEX IN THE LCB, AND THE   *
*   SCB PRIORITY AND CUTOFF COUNT ARE SET TO ZERO.  IF IT IS A TSO    *
*   BUFFER, THIS ROUTINE BRANCHES TO THE EXIT ROUTINE.  IF NOT, THE   *
*   PREFIX SCAN POINTER IS SET TO POINT TO THE LAST BYTE OF THE PRE-  *
*   FIX, OR TO THE LAST RESERVE CHARACTER, IF SPECIFIED.  IF THE      *
*   MESSAGE SOURCE WAS AN APPLICATION PROGRAM, THIS ROUTINE NOW       *
*   BRANCHES TO THE EXIT ROUTINE.  OTHERWISE, IT CHECKS TO SEE IF AN  *
*   EOA SEQUENCE WAS DEFINED IN THE SPECIAL CHARACTERS TABLE.  IF IT  *
*   WAS, IT LINKS TO THE SKIP FORWARD AND SCAN ROUTINE TO SEE IF THE  *
*   EOA SEQUENCE IS IN THE BUFFER.  IF IT IS, THE SCAN POINTER IS SET *
*   TO POINT BEYOND IT.  FOR 1030'S AND REMOTE 2260'S, THE SCAN       *
*   POINTER IS ALSO MOVED BEYOND THE ADDRESSING CHARACTER, WHICH FOL- *
*   LOWS THE EOA SEQUENCE FOR THESE DEVICES.  AT THIS POINT, IF THE   *
*   TERMINAL IS IN LOCK MODE, THIS ROUTINE BRANCHES TO THE EXIT ROU-  *
*   TINE.  OTHERWISE, THIS ROUTINE CHECKS TO SEE IF THE BUFFER CON-   *
*   TAINS AN ON-LINE TEST MESSAGE.  FOR BINARY SYNCHRONOUS LINES, A   *
*   BIT IN THE LCB IS CHECKED; FOR START/STOP LINES, THIS ROUTINE     *
*   LINKS TO THE SKIP FORWARD AND SCAN ROUTINE TO SEE IF AN ON-LINE   *
*   TEST SEQUENCE IS IN THE BUFFER.  IF THE BUFFER CONTAINS AN ON-    *
*   LINE TEST MESSAGE, BUT ON-LINE TEST IS NOT IN THE SYSTEM, THIS    *
*   ROUTINE SETS A BIT IN THE ERROR WORD AND BRANCHES TO THE EXIT     *
*   ROUTINE.  IF ON-LINE TEST IS IN THE SYSTEM, THIS ROUTINE SETS ON- *
*   LINE TEST PRIORITY IN THE BUFFER PREFIX AND EXITS TO THE DIS-     *
*   PATCHER POST FUNCTION TO POST THE BUFFER TO THE ON-LINE TEST QCB. *
*   IF THE BUFFER DOES NOT CONTAIN AN ON-LINE TEST MESSAGE, THIS      *
*   ROUTINE BRANCHES TO THE EXIT ROUTINE.                             *
*                                                              @Y17XAMF
*      FOR AN INCOMING TEXT BUFFER, THE PREFIX SOURCE FIELD IS INI-   *
*   TIALIZED AS FOR AN INCOMING HEADER BUFFER.  IF THE MH TO RECEIVE  *
*   THE BUFFER IS A TSO MH, THIS ROUTINE CHECKS TO SEE IF THE DES-    *
*   TINATION IS THE TSINPUT QCB.  IF IT IS, THIS ROUTINE BRANCHES     *
*   TO THE ROUTINE TO CHECK FOR TRANSLATION.  IF IT IS NOT, THIS      *
*   ROUTINE CHECKS FOR A LOGON EXIT.  IF A LOGON EXIT IS NOT PRESENT, *
*   IT ALSO BRANCHES TO THE TRANSLATION CHECK.  IF A LOGON EXIT IS    *
*   PRESENT, A NEW QCB AND MH ARE ESTABLISHED, AND THIS ROUTINE       *
*   BRANCHES TO THE NEW MH.  IF THE MH TO RECEIVE THE BUFFER IS NOT A *
*   TSO MH, THE LCB RESERVE COUNT IS INITIALIZED FROM THE COUNT       *
*   SPECIFIED IN THE DCB, AND THE SCAN POINTER IS SET TO POINT TO THE *
*   LAST BYTE OF THE TEXT PREFIX, OR TO THE LAST RESERVE CHARACTER,   *
*   IF SPECIFIED.  THEN THIS ROUTINE BRANCHES TO THE TRANSLATION      *
*   CHECK.                                                            *
*                                                              @Y17XAMF
*      FOR AN OUTGOING HEADER BUFFER, AN IMMEDIATE BRANCH TO THE EXIT *
*   ROUTINE IS TAKEN IF IT IS A TSO BUFFER.  OTHERWISE, A CHECK IS    *
*   MADE FOR A LOGON EXIT.  IF ONE IS PRESENT, THIS ROUTINE SETS UP   *
*   AND BRANCHES TO THE NEW MH, AS DESCRIBED ABOVE.  IF NO LOGON EXIT *
*   IS PRESENT, THIS ROUTINE PERFORMS FEFO UPDATING.  NORMALLY, THE   *
*   FEFO POINTER IN THE DESTINATION (PRIORITY) QCB IS UPDATED FROM    *
*   THE SCB FEFO POINTER, AND THE 'CURRENTLY SENDING' FLAG IN THE     *
*   MASTER QCB IS SET OFF.  HOWEVER, THERE ARE THREE SITUATIONS IN    *
*   WHICH THIS IS NOT DONE: (1) IF THE DESTINATION TERMINAL IS IN     *
*   LOCK MODE WITH AN APPLICATION PROGRAM, NO FEFO UPDATING CAN TAKE  *
*   PLACE UNTIL IT IS UNLOCKED.  (2) IF THE LINE IS IN INITIATE MODE, *
*   NO FEFO UPDATING CAN TAKE PLACE UNTIL THE LAST BUFFER OF THE INI- *
*   TIATE MESSAGE HAS BEEN PROCESSED THROUGH THE MH.  (3) IF A TEXT   *
*   TRANSFER ERROR GENERATED A ZERO LENGTH BUFFER, NO FEFO UPDATING   *
*   TAKES PLACE BECAUSE THE BUFFER WILL NOT BE SENT TO THE DESTINA-   *
*   TION.  IF THE DESTINATION TERMINAL IS IN BOTH LOCK AND EXTENDED   *
*   LOCK MODE, BOTH LOCK BITS IN THE SCB ARE SET OFF.                 *
*                                                              @Y17XAMF
*      AFTER ANY FEFO UPDATING IS COMPLETE, THE LCB RESERVE COUNT IS  *
*   SET TO ZERO FOR ZERO LENGTH BUFFERS, OR TO THE VALUE IN THE SCAN  *
*   POINTER FOR NON-ZERO LENGTH BUFFERS.  THE SCAN POINTER IS THEN    *
*   SET TO POINT TO THE LAST BYTE OF THE HEADER PREFIX, OR TO THE     *
*   LAST RESERVE CHARACTER, IF SPECIFIED.  IN ADDITION, FOR NON-ZERO  *
*   LENGTH BUFFERS, THIS ROUTINE LINKS TO THE TERMNAME TABLE CODE TO  *
*   GET THE TERMINAL ENTRY, SAVES THE CURRENT OUTPUT SEQUENCE NUMBER  *
*   IN THE SCB, AND INCREMENTS THE OUTPUT SEQUENCE NUMBER IN THE      *
*   TERMINAL ENTRY.  IF THE MAXIMUM OUTPUT SEQUENCE NUMBER IS EX-     *
*   CEEDED, IT IS RESET TO ONE.  FINALLY, THIS ROUTINE BRANCHES TO    *
*   THE EXIT ROUTINE.                                                 *
*                                                              @Y17XAMF
*      FOR AN OUTGOING TEXT BUFFER, AN IMMEDIATE BRANCH TO THE TRANS- *
*   LATION CHECK IS TAKEN IF IT IS A TSO BUFFER.  OTHERWISE A CHECK   *
*   IS MADE FOR A LOGON EXIT.  IF ONE IS PRESENT, THIS ROUTINE SETS   *
*   UP AND BRANCHES TO THE NEW MH, AS DESCRIBED ABOVE.  IF NO LOGON   *
*   EXIT IS PRESENT, THIS ROUTINE INITIALIZES THE LCB RESERVE COUNT   *
*   TO ZERO, SETS THE SCAN POINTER TO POINT TO THE LAST BYTE OF THE   *
*   TEXT PREFIX, AND BRANCHES TO THE TRANSLATION CHECK, WHICH IS DE-  *
*   SCRIBED NEXT.                                                     *
*                                                              @Y17XAMF
*      THE TSO STARTMH SUBTASK HANDLES BUFFER TRANSLATION FOR THE     *
*   FOLLOWING TYPES OF BUFFERS: (1) INCOMING TEXT BUFFERS DIRECTED TO *
*   A NON-TSO MH, (2) INCOMING TEXT BUFFERS DIRECTED TO A TSO MH WITH-*
*   OUT A LOGON EXIT, (3) OUTGOING TCAM (NON-TSO) HEADER BUFFERS, AND *
*   (4) ALL RECALLED BUFFERS.  THE TRANSLATION CHECK FIRST CHECKS FOR *
*   A ZERO LENGTH BUFFER.  A ZERO LENGTH BUFFER IS NOT TRANSLATED.    *
*   NEXT IT CHECKS THE SCB TO SEE IF TRANSLATION WAS REQUESTED.  IF   *
*   NOT, THE BUFFER IS NOT TRANSLATED.  IF IT WAS, THIS ROUTINE LINKS *
*   TO THE TRANSLATE BUFFER ROUTINE, VIA THE USER INTERFACE, TO TRANS-*
*   LATE THE BUFFER.  IF THE SCB MULTIPLE BUFFER HEADER ENTRY IS ZERO,*
*   THIS ROUTINE BRANCHES TO THE EXIT ROUTINE.  IF NOT, THE PRESENCE  *
*   OF A MULTIPLE BUFFER HEADER IS INDICATED BY SETTING A NEGATIVE    *
*   VALUE IN REGISTER 0, AND THE ADDRESS OF THE USER INTERFACE IS     *
*   LOADED INTO REGISTER 15, BEFORE BRANCHING TO THE EXIT ROUTINE.    *
*                                                              @Y17XAMF
*      THE EXIT ROUTINE OF THIS SUBTASK GETS THE ADDRESS OF THE FIRST *
*   INSTRUCTION IN THE MH AND SETS AN INCREMENT OF 4096 IN REGISTER 2 *
*   FOR MULTIPLE BASE REGISTER SUPPORT.  IF A MULTIPLE BUFFER HEADER  *
*   IS PRESENT, A CONDITION CODE OF 4 IS SET IN THE PSW.  OTHERWISE,  *
*   A CONDITION CODE OF EITHER 1 OR 8 IS SET IN THE PSW, DEPENDING ON *
*   WHETHER THE LINE IS SENDING OR RECEIVING, RESPECTIVELY.  THESE    *
*   CONDITION CODES WILL BE TESTED BY THE CODE GENERATED BY THE       *
*   STARTMH MACRO INSTRUCTION.  FINALLY, THIS ROUTINE EXITS TO THE    *
*   FIRST EXECUTABLE INSTRUCTION OF THE MH.                           *
*                                                                     *
*ENTRY POINT -- IEDQAA01 - TO PERFORM INITIALIZATION FOR A BUFFER     *
*   BEFORE SENDING IT THROUGH A TCAM OR TSO MESSAGE HANDLER           *
*   CALLING SEQUENCE          L    R15,STCBLINK-1                     *
*                             BR   R15                                *
*                                                                     *
*INPUT -- IEDQAA IS CALLED BY THE DISPATCHER WHEN A BUFFER     @Y17XAMF
*   IS TPOSTED TO THE STARTMH QCB.                             @Y17XAMF
*   AT ENTRY, THE FOLLOWING REGISTERS ARE SET.                        *
*   R1 HAS THE BUFFER ADDRESS, WHICH THIS ROUTINE SAVES IN THE AVT    *
*   R7 HAS THE ADDRESS OF THE STARTMH QCB                             *
*   R11 HAS THE DISPATCHER ADDRESS                                    *
*   R13 HAS THE ADDRESS OF AVTSAVE2                                   *
*   R15 HAS THE ENTRY POINT ADDRESS                                   *
*                                                                     *
*OUTPUT -- ON LINKING TO THE SKIP FORWARD AND SCAN ROUTINE (IEDQAI),  *
*   VIA THE USER INTERFACE, THE FOLLOWING REGISTERS ARE SET.          *
*   R1 HAS THE ADDRESS OF THE PARAMETER LIST, WHICH INCLUDES THE OFF- *
*   SET OF THE EOA SEQUENCE OR ON-LINE TEST SEQUENCE IN THE SPECIAL   *
*   CHARACTERS TABLE.                                                 *
*   R8 HAS THE ADDRESS OF THE SPECIAL CHARACTERS TABLE                *
*   R14 HAS THE RETURN ADDRESS IN IEDAYR                              *
*   R15 HAS THE ENTRY POINT ADDRESS IN IEDQUI                         *
*                                                                     *
*   ON LINKING TO THE TRANSLATE BUFFER ROUTINE (IEDQAW), VIA THE USER *
*   INTERFACE, THE FOLLOWING REGISTERS ARE SET.                       *
*   R1 HAS THE ADDRESS OF THE TRANSLATE PARAMETER LIST FROM THE SCB   *
*   R14 HAS THE RETURN ADDRESS IN IEDQAA                       @Y17XAMF
*   R15 HAS THE ENTRY POINT ADDRESS IN IEDQUI                         *
*                                                                     *
*   ON EXIT TO THE DISPATCHER POST FUNCTION, THE ELEMENT KEY IN THE   *
*   PREFIX IS SET TO INDICATE EITHER A BISYNC OR A START/STOP LINE,   *
*   THE DESTINATION QCB FIELD IN THE SCB CONTAINS THE ADDRESS OF THE  *
*   ON-LINE TEST QCB, THE LINE STATUS IS SET TO STOPPED, AND REGISTER *
*   1 CONTAINS THE ADDRESS OF THE BUFFER TO BE POSTED.                *
*                                                                     *
*   ON EXIT TO AN MH SPECIFIED AS A LOGON EXIT, THE FOLLOWING REGIS-  *
*   TERS ARE SET.                                                     *
*   R3 HAS THE SCB ADDRESS                                            *
*   R4 HAS THE LCB ADDRESS                                            *
*   R6 HAS THE BUFFER ADDRESS                                         *
*   R7 HAS THE ADDRESS OF THE NEW QCB                                 *
*   R15 HAS THE ENTRY POINT ADDRESS IN THE NEW MH                     *
*                                                                     *
*   ON EXIT TO THE MH TO PROCESS THE BUFFER, A CONDITION CODE OF 1,   *
*   4, OR 8 IS SET IN THE PSW, AND THE FOLLOWING REGISTERS ARE SET.   *
*   R0 HAS A NEGATIVE VALUE, IF A MULTIPLE BUFFER HEADER IS PRESENT,  *
*   OR ZERO                                                           *
*   R2 HAS AN INCREMENT OF 4096                                       *
*   R3 HAS THE SCB ADDRESS                                            *
*   R4 HAS THE LCB ADDRESS                                            *
*   R6 HAS THE BUFFER ADDRESS                                         *
*   R12 HAS THE ENTRY POINT ADDRESS IN THE MH                         *
*                                                                     *
*EXTERNAL ROUTINES -- IEDQAI - TO SCAN FOR AN EOA SEQUENCE OR ON-LINE *
*   TEST SEQUENCE IN THE BUFFER                                       *
*   IEDQTNT - TO GET THE TERMINAL ENTRY FOR THE DESTINATION OF AN     *
*   OUTGOING HEADER BUFFER                                            *
*   IEDQAW - TO TRANSLATE THE BUFFER                                  *
*                                                                     *
*EXITS-NORMAL -- TO THE MH SPECIFIED IN THE STARTMH QCB               *
*   TO THE MH SPECIFIED AS THE LOGON EXIT IN THE STARTMH QCB          *
*   TO THE DISPATCHER POST FUNCTION                                   *
*   TO THE SSCP MH.                                            @Y17XAMF
*                                                                     *
*EXITS-ERROR -- NONE                                                  *
*                                                                     *
*TABLES/WORKAREAS -- AVT, LCB, SCB, DCB, DEB, UCB, QCB, BUFFER PREFIX,*
*   TERMINAL ENTRY, SCT, TPRIOR, ERB, SIB, TSNSD               @Y17XAMF
*                                                                     *
*ATTRIBUTES -- SERIALLY REUSABLE, REFRESHABLE, ENABLED, RESIDENT,     *
*   PROBLEM PROGRAM MODE.                                             *
*                                                                     *
*NOTES -- THE OPERATION OF THIS MODULE DOES NOT DEPEND UPON A         *
*   PARTICULAR INTERNAL REPRESENTATION OF THE EXTERNAL CHARACTER SET. *
*                                                                     *
***********************************************************************
         EJECT
********* REGISTER EQUATES *********
         SPACE
R0       EQU   0                        WORK REGISTER
         SPACE
R1       EQU   1                        PARAMETER LIST
RTRM     EQU   1                        TTE ADDRESS            @Y17XAMF
         SPACE
RWORK2   EQU   2                        WORK REGISTER
RPREF    EQU   2                        NEG HEADER PREFIX ADDR @Y17XAMF
         SPACE
RSCB     EQU   3                        SCB ADDRESS
         SPACE
RLCB     EQU   4                        LCB ADDRESS
         SPACE
RUCB5    EQU   5                        ADDRESS OF UCB
RWORK5   EQU   5                        WORK REGISTER            S99528
         SPACE
RPREFIX  EQU   6                        BUFFER ADDRESS
         SPACE
RQCB7    EQU   7                        ADDRESS OF STARTMH QCB
         SPACE
RSCT8    EQU   8                        SPECIAL CHARS TABLE ADDRESS
RSAVT    EQU   8                        SAVT ADDRESS
         SPACE
R9       EQU   9                        WORK REGISTER               TSO
RTQCB    EQU   9                        ADDR. OF TERMINAL'S QCB. S22029
RSIB     EQU   9                        SIB ADDRESS            @Y17XAMF
         SPACE
RRET10   EQU   10                       F.E. RETURN REGISTER
RDCB     EQU   10                       DCB ADDRESS
RERB     EQU   10                       ERB ADDRESS              S99528
         SPACE
RDISP    EQU   11                       DISPATCHER ADDRESS
         SPACE
RBASE    EQU   12                       BASE OF ROUTINE
         SPACE
R13      EQU   13                       SAVE AREA
         SPACE
R14      EQU   14                       RETURN ADDRESS
         SPACE
R15      EQU   15                       ENTRY POINT
         SPACE
RAVT     EQU   9                                                 S22025
********* OTHER EQUATES *********
         SPACE
ZERO     EQU   0                        CONSTANT DISPLACEMENT    S99528
ONE      EQU   1                        BYTE OFFSET
TWO      EQU   2                        MOVE LENGTH FOR AN OFFSET
THREE    EQU   3                        MOVE LENGTH FOR AN ADDRESS
FOUR     EQU   4                        WORK OFFSET
FIVE     EQU   5                        CONSTANT DISPLACEMENT    S99528
CVTOFF   EQU   16                       OFFSET TO CVT            S22025
         SPACE
SCTEOA   EQU   1                        OFFSET TO EOA OFFSET
SCTOLT   EQU   13                       OFFSET TO OLT OFFSET
BSOLTLEN EQU   2                        LNG OF BISYNC OLT STRING
SSTOTLEN EQU   14                       OFFSET TO OLT STRING   @Z30X8MJ
POSTPEND EQU   X'D0'                    POST PENDING FLAGS
         SPACE
PARMLEN  EQU   8                        LENGTH OF STARTMH PARM LIST
         SPACE
TPDEVICE EQU   X'40'                    DEVICE CLASS = TP
IBM1030  EQU   X'21'                    DEVICE TYPE = IBM 1030
IBM2260R EQU   X'81'                    DEVICE TYPE = IBM 2260R
LGB      EQU   X'80'                    LINE GROUP BLOCK - 3705  S22024
         SPACE
LMDSTCB  EQU   X'0A'                    LMD FLAG                 S22025
         SPACE
MINUS    EQU   4                        CONDITION CODE: NEGATIVE
HLF      EQU   3                        MASK FOR ICM HALF WORD   Y05330
AD       EQU   7                        MASK FOR ICM/STCM ADDRESSY05330
SEVEN    EQU   7                        CONDITION CODE          SA52512
ALL      EQU   15                       MASK FOR ICM/STCM      @Y17XAMF
SCTEOT   EQU   20                       EOT INDEX OFFSET IN SCT SA52512
LASTNOFF EQU   X'04'                    FLAG SET BY RP WHEN     SA60343
*                                       SETTING A ZARO FEFO PRT SA60343
RNKEY    EQU   X'14'                    PRFKEY SETTING FOR TOTE  S22024
*                                         INDICATING MULTIPLE    S22024
*                                         BUFFER FROM 3705       S22024
C3270    EQU   X'04'                    3270 TERMINAL          @YA12287
CNOIDLES EQU   X'02'                    2260 TERMINAL          @YA12287
CLOCAL   EQU   X'02'                    LOCAL TERMINAL         @YA12287
CSWST1   EQU   3                        OFFSET TO CSW STATUS   @OY12280
ADDR     EQU   7                        MASK FOR STCM AND ICM  @Y17XAMF
UNEX     EQU   X'01'                    MASK FOR UNIT EXCEPTION@OY12280
STMHTSFG EQU   X'01'                    INDICATES TSO MH       @YM09043
         EJECT
         DC    AL1(DSPMCPL2)            STCB
         DC    AL1(0)
         SPACE
         USING IEDQAA01,R15
         USING AVTSAVE2,R13
         SPACE
IEDQAA01 EQU   *
IEDQAA   IEDHJN STARTMH .               MODULE ID AND DATE       S22025
         TM    AVTAFE30,AVTEFF          IS DUMP REQUESTED
         BZ    SETBASE                  NO, GO SET NEW BASE
         SPACE
         L     RWORK2,AVTAFE30          YES, GET ROUTINE ADDRESS
         BALR  RRET10,RWORK2            LINK TO IT
         SPACE
SETBASE  EQU   *
         DROP  R15
         LR    RBASE,R15                GET MODULE BASE ADDR   @Z30X8MJ
         USING IEDQAA01,RBASE
         ST    R1,AVTADBUF              SET BUFFER ADDRESS
         LR    RPREFIX,R1               SAVE BUFFER ADDRESS
         USING IEDQPRF,RPREFIX
         L     RLCB,PRFLCB-1            LCB ADDRESS
         USING IEDQLCB,RLCB             ESTABLISH ADDRESSABILITY
         L     RSCB,LCBSCBA-1           SCB ADDRESS
         USING IEDQSCB,RSCB             ESTABLISH ADDRESSABILITY
         L     RDCB,LCBDCBPT            GET DCB ADDRESS FROM LCB
         USING IHADCB,RDCB
         USING IEDQDISP,RDISP                                  @Y17XAMF
         SPACE
         LR    RPREF,RPREFIX            GET ADDRESSABILITY     @Y17XAMF
         SH    RPREF,PREFIX             TO NEGATIVE PREFIX     @Y17XAMF
         USING IEDPF1,RPREF                                    @Y17XAMF
         TM    STMHFLG0-STMHUBXT(RQCB7),STMHTSFG TSO MH?       @YM09043
         BZ    NOTSMH                   BRANCH IF NOT          @YM09043
         TM    LCBSTAT1,LCBRECVN        LINE RECEIVING         @YM09043
         BZ    NOTSMH                   NO, BRANCH             @YM09043
         LH    R1,LCBTTCIN              GET SOURCE TERM INDEX  @YM09043
         N     R1,AVTCLRHI              CLEAR HIGH ORDER HALF  @YM09043
         BZ    NOTSMH                   BRANCH IF ZERO INDEX   @YM09043
         L     R15,AVTRNMPT             TERMNAME CODE ADDRESS  @YM09043
         BALR  R14,R15                  GET TERM ENTRY ADDRESS @YM09043
         L     R1,TRMDESTQ-1-IEDQTRM(R1) GET QCB ADDRESS       @YM09043
         TM    QCBTSOF1-IEDQQCB(R1),QCBDELAY QCB ON TIME DELAY @YM09043
         BZ    NOTSMH                   NO, SKIP REMOVAL       @YM09043
         L     R15,AVTHG02              TIME DELAY REMOVAL     @YM09043
         BALR  R14,R15                  LINK TO REMOVE QCB     @YM09043
         TM    QCBFLAG-IEDQQCB(R1),QCBTSSES TSO SESSION?       @YM09043
         BO    NOTSMH                   BRANCH IF YES          @YM09043
         XC    QCBLKRRN-IEDQQCB(,R1),QCBLKRRN-IEDQQCB(R1)      @YM09043
*                                        CLEAR CARCT AND TJID  @YM09043
         MVC   QCBSLINK-IEDQQCB(THREE,R1),QCBSTCHN-IEDQQCB(R1) @YM09100
*                                        RESET SCHEDULER LINK  @YM09100
         LA    R14,QCBSTVTO-IEDQQCB(,R1) SEND SCHED VTO ADDR   @YM09100
         STCM  R14,AD,QCBSTCHN-IEDQQCB(R1) RESET SCHEDULER     @YM09100
NOTSMH   EQU   *                                               @YM09043
         CLI   LCBFLAG1,LCBPLCB         IS THIS A PLCB         @Y17XAMF
         BNE   CHKSEND                  BRANCH IF NO           @Y17XAMF
         STCM  RPREFIX,AD,LCBLSPCI      SET LCBLSPCI BUFFER    @YM08547
         TM    LCBSTAT5,LCBLUNIT        SNA RESOURCE           @Y17XAMF
         BZ    CLRPRFX                  BRANCH IF NO           @Y17XAMF
         L     RSAVT,AVTSAVTP           GET SAVT ADDR          @Y17XAMF
         USING IEDNSVTD,RSAVT                                  @Y17XAMF
         BAL   R14,PROCSSCP             PROCESS SSCP LOGIC     @Y17XAMF
         LTR   R15,R15                  SSCP SESSION           @Y17XAMF
         BNZ   EXIT                     BRANCH IF YES          @Y17XAMF
         BAL   R14,PROCSNA              PROCESS SNA LOGIC      @Y17XAMF
         LTR   R15,R15                  POST REQURIED          @Y17XAMF
         BZ    CHKSEND                  BRANCH IF NO           @Y17XAMF
         LR    R1,RPREFIX               SET POST REG           @Y17XAMF
         BAL   R14,DSPPOST              EXIT TO POST           @Y17XAMF
         EJECT
CLRPRFX  EQU   *
         TM    LCBSTAT1,LCBSENDN        SENDING                @Y17XAMF
         BZ    TSCHK                    BRANCH IF NO           @Y17XAMF
         CLI   PRFPRI,PRIMHBFR-1        ERROR BUFFER           @Y17XAMF
         BE    TSOCHK                   BRANCH IF YES          @Y17XAMF
         XC    PRF1RH(PRF1LEN),PRF1RH   CLEAR TOTAL PREFIX     @Y17XAMF
         DROP  RSAVT                                           @Y17XAMF
         DROP  RPREF                                           @Y17XAMF
CHKSEND  EQU   *                                               @Y17XAMF
         TM    LCBSTATE,LCBSENDN        SEND OPERATION           S22029
         BZ    TSCHK                    NO BRANCH                S22029
TSOCHK   EQU   *                                               @Y17XAMF
         L     R15,SCBDESTQ-1           GET DEST QCB ADDRESS     S22029
         TM    QCBFLAG-IEDQQCB(R15),QCBPROC IS THIS A PROCESS ENTS22029
         BO    TSO1                     YES, BRANCH              S22029
TSCHK    EQU   *                                                 S22029
         SR    R15,R15                  CLEAR REGISTER           S22029
         TM    LCBTSOB,LCBTSBUF         IS A TIME-SHARING        S22029
*                                       TERMINAL ON THIS LINE    S22029
         BZ    TSO1                     NO. CONTINUE MH FLOW     S22029
         OI    PRFSTAT1,PRFTSMSG        FLAG TIME-SHARING MSG    S22029
         SR    RWORK2,RWORK2            CLEAR SCAN REGISTER    @OY18039
         LA    RWORK2,AVTTXTSZ(,RWORK2) REGISTER               @OY18039
         TM    PRFSTAT1,PRFNHDRN        HEADER BUFFER          @OY18039
         BO    TEXTUNIT                 BRANCH TEXT BUFFER     @OY18039
         LA    RWORK2,AVTHDRSZ-AVTTXTSZ(,RWORK2) INCREASE AGAIN@OY18039
TEXTUNIT EQU   *                                               @OY18039
         STH   RWORK2,PRFSCAN           SET SCAN POINTER       @OY18039
         SPACE
         LH    R1,LCBTTCIN              GET TERMINAL OFFSET    @Z30X8MJ
         N     R1,AVTCLRHI              CLEAR HI-ORDER BYTE    @Z30X8MJ
         L     R15,AVTRNMPT             GET ADDR OF TNT ROUTINE@Z30X8MJ
         BALR  R14,R15                  BRANCH TO TNT LOOKUP   @Z30X8MJ
         USING IEDQTRM,R1               SET UP ADDRESSIBILITY  @Z30X8MJ
         TM    TRMSTATE,TRMPREF         IS THIS A 3705 TERMINAL@Z30X8MJ
         BNO   ERCHECK                  BRANCH NO              @YA12287
         TM    LCBSTATE,LCBSENDN        SEND OPERATION?        @Z30X8MJ
         BNO   TSO1                     NO, BRANCH             @Z30X8MJ
         B     TSOISIZE                 CLEAR IDLES COUNT      @YA12287
ERCHECK  EQU   *                                               @YA12287
         TM    LCBSTAT1,LCBSENDN        SENDING                @YA12287
         BNO   TSOISIZE                 BRANCH NO              @YA12287
         CLI   PRFPRI,PRIMHBFR-1        ERROR BUFFER           @YA12287
         BNE   TSOISIZE                 BRANCH NO              @YA12287
         TM    SCBERR3,SCBATTN+SCBSATTN ATTENTION INTERRUPT    @YA12287
         BNZ   TSOISIZE                 BRANCH YES             @YA12287
         L     R9,TRMDESTQ-1            QCB ADDRESS            @YA13265
         USING CVT,RWORK2                                      @YA13265
         L     RWORK2,CVTOFF            ADDRESS OF CVT         @YA13265
         L     RWORK2,CVTASVT           GET ASVT ADDRESS       @ZM46751
         DROP  RWORK2                                          @YA13265
         LA    RWORK2,ASVTENTY-ASVT(RWORK2) ASCB LIST ADDRESS  @ZM46751
         LH    R15,QCBTJID-IEDQQCB(R9)  GET ASID               @ZM46751
         BCTR  R15,0                    MAKE ZERO ORIGIN       @ZM46751
         SLL   R15,2                    GET OFFSET INTO LIST   @ZM46751
         L     RWORK2,AVTEZERO(R15,RWORK2) ASCB ADDRESS        @ZM46751
         L     RWORK2,ASCBTSB-ASCB(RWORK2) GET TSB ADDRESS     @ZM46751
         USING TSB,RWORK2               SET ADDRESSABILITY     @ZM46751
         TM    TSBFLG4,TSBHUNG          TERMINAL HAS HUNG UP   @ZM46751
         BO    TSOISIZE                 YES                    @ZM46751
         TM    TSBSTAT,TSBDISC          TERMINAL HAS LOGGED OFF@ZM46751
         BO    TSOISIZE                 YES                    @ZM46751
         TM    QCBTSOF1-IEDQQCB(R9),QCBDISC USER BEING         @OZ15624
         BO    TSOISIZE                 LOGGED OFF             @OZ15624
         DROP RWORK2                                           @YA13265
         OI    PRFSTAT1,PRFERMGN        SET ERROR FOR TSO      @YA12287
         NI    QCBTSOF1-IEDQQCB(R9),X'FF'-QCBSATRD             OY155693
*                                       RESET SIM ATTN READ    @ZM47773
         SR    R15,R15                  CLEAR INDEX REGISTER   @YA12287
         IC    R15,TRMCHCIN             GET DCT INDEX          @YA12287
         BCTR  R15,0                    REDUCE FOR MULTIPLY    @YA12287
         MH    R15,AVTDCTLN             MULTIPLY FOR ADDRESS   @Y176027
         A     R15,AVTCSTCS             GET ADDRESS DCT        @YA12287
         TM    1(R15),C3270             3270 DEVICE            @YA12287
         BO    TSOISIZE                 BRANCH YES             @YA12287
         TM    3(R15),CNOIDLES          2260 DEVICE            @YA12287
         BO    TSOISIZE                 BRANCH YES             @YA12287
         TM    2(R15),CLOCAL            LOCAL DEVICE           @YA12287
         BO    TSOISIZE                 BRANCH YES             @YA12287
         L     R15,TRMDESTQ-1           GET QCB ADDRESS        @YA12287
         MVI   QCBCARCT-IEDQQCB(R15),AVTEFF SET FOR CARRIAGE   @YA12287
*                                       RETURN BEFORE RETRY    @YA12287
TSOISIZE EQU   *                                               @Z30X8MJ
         SR    R15,R15                  CLEAR REGISTER 15      @Z30X8MJ
         STC   R15,LCBISZE              ZERO OUT IDLES COUNT        TSO
         TM    TRMSTATE,TRMPREF         NCP RESOURCE           @YM06159
         BNO   TSO1                     BR NO                  @YM06159
         TM    LCBSTAT5,LCBLUNIT        LOGICAL UNIT           @YM06159
         BNO   TSO1                     BR NO                  @YM06159
         SH    R1,TPREFIX               BACK UP TO TTE PREFIX  @YM06159
         USING IEDNTRM,R1               TTE PREFIX BASE        @YM06159
*                                       CHECK FOR SNA 3270     @YM06159
*                                       COMPATIBLE             @YM06159
         IEDDCT REG=R15,FLD=AVTPARM3,LEN=4                     @YM06159
         DROP  R1                                              @YM06159
         TM    DCTBYTE1-IEDDCT(R15),DCT3270 SNA 3270 COMPAT.   @YM06159
         BNO   TSO1                     BR NO                  @YM06159
         IC    R15,LCBISZE              GET IDLE COUNT         @YM06159
         LA    R15,1(,R15)              BUMP PAST ESCAPE CHAR  @YM06159
         STC   R15,LCBISZE              RESET LCBISZE          @YM06159
TSO1     EQU   *                                                    TSO
         SR    R15,R15                  CLEAR REGISTER           S22029
         TM    PRFSTAT1,PRFNHDRN        TEST IF HEADER OR TEXT
         BO    TEXT                     TEXT,  GO PROCESS
*                                       HEADER,  FALL THROUGH
         SPACE
         TM    LCBSTATE,LCBSENDN        TEST IF SEND OR RECEIVE
         BNO   HDRRCV                   RECEIVE, GO PROCESS
*                                       SEND, FALL THROUGH
         EJECT
         TM    PRFSTAT1,PRFTSMSG .      IS IT A TS MESSAGE       S22029
         BO    EXIT                     BRANCH ON YES TO EXIT       TSO
         SPACE
         SPACE
         TM    PRFSTAT1,PRFCNCLN        IS THIS A RECALLED BUFFER
         BO    COMPIDLS                 YES, DON'T UPDATE FEFO
         SPACE
         SR    R0,R0 .                  CLEAR FOR INSERT         S22025
         IC    R0,SCBPRI                GET INDEX TO PRIORITY QCB
         LA    R1,QCBPSIZE              GET SIZE OF PRIORITY QCB
         MR    R0,R0                    MULTIPLY TO GET OFFSET
         L     RWORK2,SCBDESTQ-1        GET ADDRESS OF MASTER QCB
         USING IEDQQCB,RWORK2
         LA    R1,QCBMSIZE(RWORK2,R1)   GET ADDR OF PRIORITY QCB
         USING IEDQPQCB,R1
         CLC   SCBSCHDR,QCBLKRRN        QUEUED ADDR = LOCK MSG @OS76313
         BNE   NORRN                    BRANCH NO              @OS76313
         XC    QCBLKRRN,QCBLKRRN        CLEAR LOCK MSG ADDR    @OS76313
NORRN    EQU   *                                               @OS76313
         LA    RWORK2,SCBSCHDR          ASSUME FEFO ADDR IN SCB SA52971
         TM    SCBSCHDR+L'SCBSCHDR-1,CPBQTYPE CORE COPY OF DISK SA52971
*                                         BACKUP                SA52971
         BNZ   MOVEFEFO                 NO, GOT IT              SA52971
         L     RWORK2,SCBSCHDR-1        POINT TO QUEUED HEADER  SA52971
         LA    RWORK2,PRFCRCD-IEDQPRF(,RWORK2) POINT TO DISK ADRSA52971
MOVEFEFO EQU   *                                                SA52971
         CLC   AVTEZERO(3,RWORK2),QCBFFEFO IS IT TOP ON QUEUE   SA52971
         BNE   COMPIDLS                 NO, DON'T RESET         SA52971
         ICM   RWORK2,AD,QCBINTFF       IS QUEUE HELD            Y05330
         BNZ   JUSTPVF                  YES, DON'T UPDATE PFEFO SA52971
         MVC   QCBPFEFO(3),QCBPREVF     SHUFFLE TO LAST SRVCD   SA52971
*                                         MESSAGE               SA52971
         MVC   QCBPREVF(3),QCBFFEFO     KEEP POINTER TO THIS MSGSA52971
JUSTPVF  EQU   *                                                SA52971
         MVC   QCBFFEFO(THREE),SCBFEFO  UPDATE FEFO POINTER
         TM    QCBFFEFO+L'QCBFFEFO-1,CPBQTYPE                   SA60343
*              ARE THE FLAGS FOR DISK QUEUEING ON               SA60343
         BNZ   COMPIDLS                 BR IS NOT SET           SA60343
         NI    QCBFFEFO+L'QCBFFEFO-1,X'FF'-LASTNOFF             SA60343
*              RESET THE 'DO NOT UPDATE' FLAG SET BY RP FOR     SA60343
*              FA WHEN REMOVING THE LAST FEFO MESSAGE           SA60343
         DROP  RWORK2                                            S22029
         SPACE
COMPIDLS EQU   *
         SR    RWORK2,RWORK2 .           SET IDLES CNT TO ZERO   S22025
         CH    RWORK2,PRFSIZE           ZERO-LENGTH BUFFER
         BE    STORIDLS                 YES, LEAVE IDLES CT ZERO
         SPACE
         IC    RWORK2,PRFSCAN+1   NO, GET IDLES CT FROM BFR
         SPACE
STORIDLS EQU   *
         STC   RWORK2,LCBISZE           INITIALIZE LCB RESERVE CT
         LA    RWORK2,AVTHDRSZ(,RWORK2) ADD SIZE OF HEADER PREFIX
         SPACE
         BAL   R14,TNTSUB               LOCATE TERMINAL ENTRY    Y05330
         SPACE
         USING IEDQTRM,R1
         TM    TRMSTATE,TRMPROC         INSURE THAT TTE ENTRY  @Y17XAMF
         BNO   NODATE                    IS A TPROCESS ENTRY   @Y17XAMF
         TM    TRMSTATE,TRMLOG                                 @Y17XAMF
         BO    NODATE                                          @Y17XAMF
         LA    R0,TRMTPIN               LOAD MASK FOR IEDQTL   @Y17XAMF
         L     R15,AVTDDFT              GET ADDR TO IEDQTL     @Y17XAMF
         BALR  R14,R15                  GO GET TPROCESS DEV DEP FIELD
*                                                              @Y17XAMF
         TM    PROFLAG-IEDPROC(R15),PRODATE IS DATE=YES SPECIFIED
*                                                              @Y17XAMF
         BNO   NODATE                   IF NOT, BRANCH         @Y17XAMF
         LA    RWORK2,PARMLEN(RWORK2)   BUMP SCAN FOR DATE/TIME  S22025
NODATE   EQU   *                                                 S22025
         STH   RWORK2,PRFSCAN           INITIALIZE SCAN          S22025
         TM    PRFSTAT1,PRFCNCLN        RECALLED                 S22025
         BO    EXIT                     YES, DON'T UPDATE SEQ OUTS22025
         ICM   RWORK2,HLF,SCBOSEQ       SEQ OUT UPDATED          Y05330
         BNZ   EXIT                     BRANCH IF YES            S22025
         LH    RWORK2,TRMOUTSQ          GET OUTPUT SEQUENCE NUMBER
         STH   RWORK2,SCBOSEQ           PASS IN SCB
         LA    RWORK2,ONE(,RWORK2)      INCREMENT BY ONE
         CH    RWORK2,CONMAX            IS MAXIMUM COUNT EXCEEDED
         BNH   UPCOUNT                  NO, PUT BACK UPDATED COUNT
         SPACE
         LA    RWORK2,ONE               NO, RESET COUNT TO ONE
         SPACE
UPCOUNT  EQU   *
         STH   RWORK2,TRMOUTSQ          STORE IT BACK
         B     EXIT                     RETURN TO MH
         EJECT
HDRRCV   EQU   *
         TM    PRFSTAT1,PRFCNCLN        IS THIS A RECALLED BUFFER
         BO    TESTRAN                  YES, GO TRANSLATE & EXIT
         SPACE
         SPACE
         LH    RWORK2,LCBTTCIN          INITIALIZE ORIGIN FIELD
         STH   RWORK2,PRFSRCE             IN PREFIX FROM LCB
         CLI   LCBFLAG1,AVTEZERO        IS THIS A PLCB         @Y17XAMF
         BNE   NOTNCP                   BRANCH IF NO           @Y17XAMF
         MVC   LCBISZE,PRFISIZE         SET UP RESERVE COUNT   @Y17XAMF
NOTNCP   EQU   *                                               @Y17XAMF
         SR    RWORK2,RWORK2 .          CLEAR FOR INSERT         S22025
         IC    RWORK2,LCBISZE           PICK UP RESERVE CT FROM LCB
         TM    SCBQTYPE,SCBBFMM .       IS THIS MID-OF-MSG       S22025
         BNO   NOTLMD .                 NO, CONTINUE AS HEADER   S22025
         MVC   SCBDEOB+1(3),AVTADBUF+1  SET DEOB FOR THE CASE OF S22025
*                                         A DE-BLOCKED HEADER    S22025
         NI    SCBQTYPE,X'FF'-SCBBFMM . TURN OFF MID-OF-MSG      S22025
         OI    PRFSTAT1,PRFNHDRN .      SET AS TEXT BUFFER       S22025
         OI    PRFTIC,PRFBFMM .         SET AS HDR CHGD TO TEXT  S22025
         LA    RWORK2,AVTHDRSZ-AVTTXTSZ(,RWORK2)   ADD TO ISIZE  S22025
         B     REALTEXT .               BRANCH TO TREAT AS TEXT  S22025
         SPACE 1                                                 S22025
NOTLMD   EQU   *                                                 S22025
         TM    PRFSTAT1,PRFITCPN        TEMPORARY END OF MSG    SA62383
         BNO   SKIPMM                   BRANCH IF NO            SA62383
         OI    SCBQTYPE,SCBBFMM         SET MIDDLE OF MESSAGE   SA62383
SKIPMM   EQU   *                                                SA62383
         LA    RWORK2,AVTHDRSZ(,RWORK2) ADD LNG OF HDR PREFIX
         ST    R15,SCBPRI .             CLEAR SCB PRIORITY       S22025
         CH    RWORK2,PRFSIZE           IS THERE DATA IN BUFFER?
         BL    SETSCPTR                 YES,GO SET SCAN PTR    @SA75454
         SPACE
         STH   R15,PRFSIZE              NO, SET SIZE TO ZERO
         SPACE
SETSCPTR EQU   *
         TM    PRFSTAT1,PRFTSMSG .     IS IT A TS MESSAGE        S22029
         BO    EXIT                     BRANCH ON YES TO EXIT
         SPACE
         STH   RWORK2,PRFSCAN           INITIALIZE PREFIX SCAN PTR
         STH   R15,PRFISEQ              INITIALIZE SEQ IN TO ZERO
         ICM   RSCT8,AD,DCBSCTAD        SPECIAL CHAR TBL         Y05330
         BZ    EXIT                     NO, SOURCE IS APPLICATION
*                                         PROGRAM,  RETURN TO MH
         CH    R15,PRFSIZE .             IS THIS A ZERO BUFFER   S22025
         BE    EOTCODE .                BRANCH IF YES           SA54959
EOTCODE1 EQU   *                                                SA54959
         SPACE
         L     R1,LCBSTCBA-1 .          LOAD ADDRESS OF STCB     S22025
         CLI   0(R1),LMDSTCB .          IS IT LMD STCB           S22025
         BE    EXIT .                   YES, RETURN              S22025
         SPACE
         LR     R1,RWORK2 .             SET PARAMETER FOR SCAN   S22025
         L     RAVT,AVTBASE             @AVT                   @Y17XANQ
         L     RAVT,CVTOFF              GET ADDRESS OF CVT       S22025
         L     RAVT,AVTCVTPT(,RAVT)     FIND POINTER TO AVT      S22025
         L     RAVT,AVTEZERO(,RAVT)     GET ADDRESS OF AVT       S22025
         CLI   SCTEOA(RSCT8),AVTEZERO   IS AN EOA SEQ DEFINED
         BE    TESTLOCK                 NO, PROCEED
         SPACE
         TM    LCBTSOB,LCB2741N         IS SOURCE AN IBM 2741
         BO    TESTLOCK                 YES, NO EOA, BRANCH
         SPACE
         LA     R15,SCTEOA .            GET EOA SEQUENCE OFFSET  S22025
         BAL   R14,LINKAL .             SCAN FOR EOA CHARACTER   S22025
         SPACE 1                                                 S22025
*   ON RETURN -- PRFDATOF WILL BE INCREMENTED IF EOA SEQUENCE    S22025
*            IS IN BUFFER                                        S22025
         SPACE 1                                                 S22025
         LR    R1,R0 .                  GET UPDATED SCAN OFFSET  S22025
         SR    RWORK5,RWORK5            ZERO REGISTER          @OY13651
         IC    RWORK5,LCBISZE           GET IDLE COUNT         @OY13651
         LA    R15,AVTHDRSZ(RWORK5,R15) GET BUFFER SIZE        @OY13651
         CH    R15,PRFSIZE              EOA ONLY BYTE IN BUF?  @OY13651
         BNE   NEOAONLY                 NO-CONTINUE            @OY13651
         SR    R15,R15                  SET UP TO CLEAR PRFSIZE@OY13651
         B     EOTCODE3                 CONT WITH 0-LNGTH BFR  @OY13651
NEOAONLY EQU   *                                               @OY13651
         TM    DCBDSORG,LGB             IS BLOCK A DCB OR LGB    S22024
         BO    TESTLOCK                 BRANCH IF LGB            S22024
         L     RUCB5,DCBDEBAD           GET ADDRESS OF DEB FROM DCB
         USING DEBNMSUB,RUCB5
         L     RUCB5,DEBUCBAD           GET ADDRESS OF UCB FROM DEB
         USING UCBOB,RUCB5
         CLI   UCBTBYT3,TPDEVICE        IS THIS A TP DEVICE
         BNE   TESTLOCK                 NO, GO TEST FOR LOCK
         SPACE
         IC    R15,UCBTBYT1             PICK UP MODEL BYTE
         STC   R15,AVTDOUBL             SET IT IN AVT WORK AREA
         MVZ   AVTDOUBL(ONE),UCBTBYT4   OVERLAY IT WITH ADAP BYTE
         CLI   AVTDOUBL,IBM1030         IS DEVICE AN IBM 1030
         BE    BUMPXTRA                 YES, BUMP SCAN ONE MORE
         SPACE
         CLI   AVTDOUBL,IBM2260R        IS DEVICE IBM 2260 REMOTE
         BNE   TESTLOCK                 NO, GO TEST FOR LOCK
         SPACE
BUMPXTRA EQU   *
         LA    R1,ONE(,R1) .            UPDATE SCAN OFFSET       S22025
TESTLOCK EQU   *
         TM    SCBSTATE,SCBLCK1N        TERMINAL IN LOCK MODE
         BNO   NOLOCK                  NO, BRANCH
         SPACE
         NI    LCBMSGFM,AVTEFF-LCBOLT   MAKE SURE OLT BIT IS OFF
         OI    PRFSTAT1,PRFLOCK         SET PREFIX LOCK BIT
         MVC   PRFDEST,LCBINSRC         GET INDEX IN TERMNAME TABLE
         MVI   LCBINSRC+2,POSTPEND      SET POST PENDING FLAGS
         BAL   R14,TNTSUB               LOCATE TERMINAL ENTRY    Y05330
         SPACE
         MVC   SCBDESTQ,TRMDESTQ        SET DEST QCB ADDRESS
         B     EXIT                     EXIT TO MH              S22025
         SPACE
LOCKTEST EQU   *                                                SA52512
         TM    SCBSTATE,SCBLCK1N .      LOCK MODE               SA52512
         BNO   EXIT .                   NO, BRANCH              SA52512
         MVI   PRFSIZE+ONE,PRFSHDR+ONE-PRFSUNIT SET PRFSIZE     SA52512
         BAL   R14,EOTADDR              COMPUTE EOT ADDRESS     SA52512
         MVC   PRFSHDR(ONE),ONE(R1)     INSERT EOT IN BUFFER    SA52512
         MVI   LCBISZE,AVTEZERO .       CLEAR IDLES COUNT       SA52512
         B     TESTLOCK .               FINISH INQUIRY HANDLING SA52512
EOTADDR  EQU   *                                                SA52512
         L     RSCT8,DCBSCTAD-1         SPEC CHAR TABLE         SA52512
         SR    R1,R1                    CLEAR INDEX REGISTER    SA52512
         IC    R1,SCTEOT(RSCT8)         GET EOT INDEX           SA52512
         LA    R1,AVTEZERO(R1,RSCT8)    EOT ENTRY ADDRESS       SA52512
         CLI   AVTEZERO(R1),TWO         ETX/EOT                 SA52512
         BCR   SEVEN,R14                NO, RETURN              SA52512
         LA    R1,ONE(,R1)              BUMP PAST ETX           SA52512
         BR    R14                      RETURN                  SA52512
         SPACE
NOLOCK   EQU   *
         SPACE
         TM    LCBMSGFM,LCBOLT .        IS THIS OLT MESSAGE    @YM07683
         BO    LINKERST .               YES, CHECK TOTE ACTIVE @YM07683
         TM    LCBSTAT2,LCBSYNC         IS THIS BISYNC LINE
         BNO   TOTESS .                 NO, CHECK FOR TOTE STRINGS22025
         B     EXIT                     NO OLT, GET OUT
         SPACE 1                                                 S22025
         SPACE
TOTESS   EQU   *
         CLI   SCTOLT(RSCT8),AVTEZERO   OLT SEQ SPECIFIED        A44861
         BE    EXIT                     BRANCH IF NO             S22025
         LA    R15,SCTOLT .             GET OLT SEQUENCE OFFSET  S22025
         BAL   R14,LINKAL .             LINK TO SCAN             S22025
         SPACE 1                                                 S22025
         LR     R1,R0 .                 GET OFFSET RETURNED      S22025
         SPACE
         LPR   RWORK2,RWORK2            ADDRESS OF OLT SEQ     @SA74025
         CLM   R15,ONE,0(RWORK2)        WAS FULL SEQ FOUND     @Y17XANQ
         BE    LINKERST                 YES,HOW ABOUT OLT      @SA74025
         SPACE
         TM    LCBTSOB,LCB2741N         IS IT IBM 2741
         BNO   EXIT .                   NO, NOT OLT, EXIT TO MH  S22025
         CLI   LCBFLAG1,AVTEZERO        IS THIS A PLCB?        @OY12671
         BE    EXIT                     YES,NO SECOND STRING   @OY12671
         SPACE 1                                                 S22025
         LA    RWORK2,SSTOTLEN(,RWORK2) POINT TO 2D OLT STRING   S22025
         BAL   R14,LINKAL2 .            SCAN FOR 2D TOTE STRING  S22025
         SPACE
         LPR   RWORK2,RWORK2            ADDRESS OF OLT SEQ     @SA74025
         CLM   R15,ONE,0(RWORK2)        WAS FULL SEQ FOUND     @Y17XANQ
         BNE   EXIT                     NO OLT,GET OUT         @SA74025
         SPACE
LINKERST EQU   *                                                 S22025
         CLI   AVTOLTST,AVTEZERO .      IS ON-LINE TEST IN SYSTEMS22025
         BE    OLTERROR .               NO, SET ERROR BIT        S22025
         TM    PRFSTAT1,PRFNLSTN  IS MESSAGE CONTAINED IN ONE    S99528
*                                   BUFFER?                      S99528
         BZ    SETOTT                   BRANCH YES BUFFER      @Y17XANQ
         OI    LCBMSGFM,LCBOLT          INSURE TOTE FLAG SET   @Y17XANQ
         SPACE
SETOTT   EQU   *
         LA    RWORK2,AVTOLTQB          GET ADDRESS OF OLT QCB
*                               QUEUE POINTER                    S99528
         MVI   PRFPRI,PRIONLT           SET ON-LINE TEST PRIORITY
         MVI   PRFKEY,FOUR              SET PRFKEY             @Y17XANQ
         STCM  RWORK2,AD,PRFQCBA        QCB @ TO ELEMENT       @Y17XANQ
         LR    R1,RPREFIX               BUFFER=ELEMENT         @Y17XANQ
         BAL   R14,DSPPOST              TPOST BFR TO TOTE      @Y17XANQ
         SPACE
OLTERROR EQU   *
         OI    SCBERR2,SCBOLTR          SET OLT ERROR BIT IN SCB
         B     EXIT .                   EXIT TO MH               S22025
         SPACE 2                                                 S22025
LINKAL   EQU   *                                                 S22025
         SR    RWORK2,RWORK2 .          CLEAR FOR INSERT         S22025
         IC    RWORK2,AVTEZERO(R15,RSCT8)  PICK UP SEQ OFFSET    S22025
         LA    RWORK2,0(RWORK2,RSCT8) . GET ADDR OF STRING       S22025
LINKAL2  EQU   *                                                 S22025
         LR    R0,RWORK2 .              SET FOR SCAN ROUTING     S22025
         LNR   RWORK2,RWORK2            DO NOT SKIP BLANKS     @SA74025
         L     R15,AVTAL                LOAD SCAN ROUTINE ADDR   S22025
         BR    R15 .                                             S22025
         EJECT
TEXT     EQU   *
         TM    LCBSTATE,LCBSENDN       SEND OR RECEIVE         @SA68025
         BO    TXTSEND                  SEND, NOT TOTE REQUEST @SA68025
         TM    LCBMSGFM,LCBOLT          IS THIS AN ONLINE TEST   S22024
*                                         TEXT BUFFER            S22024
         BNO   TXTRCV                   RECEIVES               @Y17XANQ
         TM    PRFSTAT1,PRFTSMSG        IS THIS A TSO MSG?     @OZ24601
         BNO   NOOLT                    NO                     @OZ24601
         NI    LCBMSGFM,AVTEFF-LCBOLT  TURN OFF ONLINE TEST BIT@OZ24601
         B     TXTRCV                   CONTINUE               @OZ24601
NOOLT    EQU   *                                               @OZ24601
         CLI   AVTOLTST,AVTEZERO        TOTE IN SYSTEM         @OX14795
         BNE   YESTOTE                  YES,CONTINUE           @0X14795
         TM    PRFSTAT1,PRFNLSTN        LAST                   @0X14795
         BO    TXTRCV                   NO,TREAT AS NORMAL     @OX14795
         NI    LCBMSGFM,AVTEFF-LCBOLT   TURN OFF OLT BIT       @OX14795
         B     TXTRCV                                          @OX14795
YESTOTE  EQU   *                        TOTE IN SYSTEM         @OX14795
         LA    RWORK2,AVTBFRTB          GET ADDRESS OF BUFFER    S22024
*                                         RETURN QCB             S22024
         STCM  RWORK2,AD,SCBDESTQ       SET QCB ADDRESS          Y05330
         MVI   PRFPRI,PRIBFRTB          PUT IN PRIORITY          S22024
         TM    PRFSTAT1,PRFNLSTN        IS THIS THE LAST BUFFER? S22024
         BO    BUFRET                   NO, GO POST BUFFER TO    S22024
*                                         BUFFER RETURN          S22024
         NI    LCBMSGFM,AVTEFF-LCBOLT   TURN OFF ONLINE TEST BIT S22024
         LA    RWORK2,AVTINOUT          GET ADDR OF DUMMY IN/OUT S22024
         STCM  RWORK2,AD,SCBMACR        SET DUMMY END            Y05330
         L     RWORK2,AVTMSGS-ONE       GET VCON TABLE ADDRESS   S22024
         L     RWORK2,AVTEZERO(RWORK2)  GET ADDRESS OF BD'S QCB  S22024
         MVI   PRFPRI,PRIDSPLB          SET LOW PRIORITY         S22024
BUFRET   EQU   *                                                 S22024
         STCM  RWORK2,AD,PRFQCBA        QCB ADDRESS TO ELEMENT   Y05330
         LR    R1,RPREFIX               PUT ADDR OF BUFFER IN R1 S22024
         BAL   R14,DSPPOST              EXIT TO POST BUFFER TO @Y17XAMF
*                                         QCB                    S22024
TXTSEND  EQU   *
         SPACE
         TM    PRFSTAT1,PRFTSMSG .      IS IT A TS MESSAGE       S22029
         BO    TESTRAN                  YES, GO TRANS AND EXIT      TSO
         SPACE
         SPACE
         MVI   LCBISZE,AVTEZERO         SET IDLES COUNT TO ZERO
         LA    RWORK2,AVTTXTSZ          GET SIZE OF TEXT PREFIX
         STH   RWORK2,PRFSCAN           INITIALIZE SCAN POINTER
         CH    R15,PRFSIZE              IS THIS A ZERO BFR      SA56222
         BE    EXIT                     IF 0 EXIT               SA56222
         B     TESTRAN1                 IF NOT 0 CONTINUE       SA56222
         SPACE
CHKLOCK  EQU   *                                                SA52512
         TM    SCBSTATE,SCBLCK1N .      LOCK MODE               SA52512
         BNO   EXIT .                   NO, BRANCH              SA52512
         MVI   PRFSIZE+ONE,PRFSTXT+ONE-PRFSUNIT SET PRFSIZE     SA52512
         BAL   R14,EOTADDR              COMPUTE EOT ADDRESS     SA52512
         MVC   PRFSTXT(ONE),ONE(R1)     INSERT EOT IN BUFFER    SA52512
         MVI   LCBISZE,AVTEZERO .       CLEAR IDLES COUNT       SA52512
         B     EXIT .                   END TEXT PROCESSING     SA52512
         SPACE
TXTRCV   EQU   *
         TM    LCBMSGFM,LCBOLT          IS THIS A TOTE REQUEST @Y17XANQ
         BO    SETOTT                   BRANCH YES             @Y17XANQ
         LH    R0,LCBTTCIN              INITIALIZE ORIGIN FIELD SA61771
         STH   R0,PRFSRCE                 IN PREFIX FROM LCB    SA61771
         SR    RWORK2,RWORK2            (CLEAR FOR INSERT)
         IC    RWORK2,DCBRESER+1        PICK UP NO. RESERVE CHARS
         TM    PRFSTAT1,PRFTSMSG .      IS IT A TS MESSAGE       S22029
         BNO   TSTEXIT                  NO, GO CHECK LOGON EXIT   M6673
         TM    DCBDSORG,AVTE80          IS THIS 3705 LGB         Y06327
         BNO   NOT3705                  BRANCH ON NO             Y06327
         STC   RWORK2,LCBISZE           SET NUMBER OF IDLES      Y06327
NOT3705  EQU   *                                                 Y06327
         SR    RWORK2,RWORK2            CLEAR WORK REGISTER       M6673
         IC    RWORK2,LCBISZE           GET NUMBER OF IDLES       M6673
         LA    RWORK2,AVTTXTSZ(,RWORK2) ADD TEST PREFIX LENGTH   S22029
         CH    RWORK2,PRFSIZE           CHECK BUFFER DATA LENGTH  M6673
         BNE   TESTRAN                  BR IF NOT EMPTY         SA61771
         B     EOTCODE3                 HANDLE ZERO LENGTH BFR @ZA04049
TSTEXIT  EQU   *                                                  M6673
         SPACE
         TM    PRFSTAT1,PRFCNCLN        IS THIS A RECALLED BUFFER
         BO    TESTRAN                  YES, DON'T CHANGE SCAN
         SPACE
REALTEXT EQU   *                                                 S21101
         TM    PRFSTAT1,PRFITCPN        TEMP END OF MSG          Y01004
         BZ    NOTEOM                   BRANCH NO                S22025
*                                                                S22025
         OI    SCBQTYPE,SCBBFMM         INDICATE MIDDLE  OF MSG  S22025
NOTEOM   EQU   *                                                 S22025
         STC   RWORK2,LCBISZE           SET COUNT IN LCB
         LA    RWORK2,AVTTXTSZ(,RWORK2) ADD SIZE OF TEXT PREFIX
         STH   RWORK2,PRFSCAN           INITIALIZE SCAN PTR
         CH    RWORK2,PRFSIZE
         BL    TESTRAN                                         @SA75471
         XC    PRFSIZE(2),PRFSIZE
         TM    PRFSTAT1,PRFNLSTN        EOM BUFFER             @YA10671
         BNO   EOTCODE3                 BRANCH YES             @YA10671
         SPACE
TESTRAN  EQU   *
         CLC   AVTFZERO(TWO),PRFSIZE    ZERO LENGTH BUFFER ?     S22024
         BE    CHKLOCK .                YES, BRANCH             SA52512
TESTRAN1 EQU   *                                                SA56222
         TM    LCBSTATE,LCBSENDN .      IS LINE SENDING          S22025
         BNO   EXIT .                   IF RECEIVING, INHDR      S22025
*                                         WILL MAKE TESTS        S22025
         SPACE
         TM    SCBSTATE,SCBCODE         IS TRANSLATION REQUESTED
         BNO   TESTMBH                  NO, GO TEST FOR MBH
         SPACE
         L     R1,SCBTRANS-1            YES, GET CODE PARM LIST
         L     R15,AVTUI                GET USER INTERFACE ADDRESS
         BALR  R14,R15                  LINK TO TRANSLATE BUFFER
         SPACE
TESTMBH  EQU   *
         L     RWORK2,SCBMBHEN-1        GET MBH PARM LIST ADDR   A59509
         CLC   AVTINOUT,AVTEZERO(RWORK2) DOES IT POINT TO '0100' A59509
         BE    EXIT                     IT DOES, NO MBH, EXIT
         SPACE
         LNR   RWORK5,RWORK2            SET NEG REGISTER AS FLAG A59509
         BCT   RWORK2,MBHEXIT           DECR TO CLEAR FLAG BIT   A59509
*                                         AND EXIT TO RE-ENTER RTN
         EJECT
         SPACE
EXIT     EQU   *
         SR    RWORK5,RWORK5            ZERO FLAG REG - NO MBH   A59509
         SPACE
MBHEXIT  EQU   *
         CLC   ONE(THREE,RQCB7),AVTFZERO ALTMH PRESENT           S22029
         BE    NOALTMH .                NO CONTINUE              S22029
         USING IEDQQCB,RTQCB                                     S22029
         SR    RTQCB,RTQCB                                       S22029
         CLI   LCBFLAG1,AVTEZERO        IS THIS 3705 PLCB        S22024
         BE    NOTDIAL                  BRANCH ON YES            S22024
         TM    LCBSTAT2,LCBDIAL .      IS IT A DIAL LINE         S22029
         BZ    NOTDIAL .                NO  BRANCH               S22029
         LH    R1,LCBLNENT .            OFFSET TO LINE ENTRY     S22029
         B     TERMENT .                TO GET TERM ENTRY        S22029
NOTDIAL  EQU   * .                                               S22029
         LH    R1,LCBTTCIN .            INDEX TO CURRENT TERM    S22029
TERMENT  EQU   * .                                               S22029
         N     R1,AVTCLRHI              CLEAR HIGH AND TEST      A59509
         BZ    NOALTMH                  NO.CANNOT LOCATE ALTMH   S22029
         L     R15,AVTRNMPT .           ADDRESS OF TERMNAME TBL  S22029
         BALR  R14,R15 .                GET TERMINAL ENTRY       S22029
         USING IEDQTRM,R1 .                                      S22029
         L     RTQCB,TRMDESTQ-1         GET DESTINATION ADDRESS  S22029
         TM    QCBDSFLG,QCBALTMH .      ALTERNATE MH             S22029
         BZ    NOALTMH .                NO CONTINUE              S22029
         L     RQCB7,AVTEZERO(,RQCB7)   GET ALTMH                S22029
NOALTMH  EQU   * .                                               S22029
         LR    R1,RWORK2                SET MBH PARAMETER IN     A59509
*                                        CORRECT REG FOR EXIT    A59509
         L     R15,AVTUI                GET USER INTERFACE ADDR  A59509
         LH    RWORK2,CONBASE           SET INCR VALUE IF MULT BASE
         SR    RBASE,RBASE              (CLEAR FOR INSERT)
         IC    RBASE,PARMLEN(,RQCB7)    PICK UP LNG OF PARM LIST
         LA    RBASE,AVTEZERO(RQCB7,RBASE) GET 1ST INSTRUCTION ADDR
         LTR   RWORK5,RWORK5            MBH RE-ENTRY NEEDED      A59509
         BCR   MINUS,RBASE              YES, PASS MINUS TO MH
         SPACE
         TM    LCBSTAT1,LCBSENDN        NO, TEST IF SENDING
         BR    RBASE                    ENTER MH, SELECT IN OR OUT
         EJECT
EOTCODE  EQU   *                                                SA54959
         TM    SCBERR4,SCBTXTTN .       TEXT TRANSFER ERROR     SA54959
         BNO   LOCKTEST .               BRANCH IF NO            SA54959
EOTCODE3 EQU   *                                                SA54959
         STH   R15,PRFSIZE .            SET ZERO LENGTH         SA54959
         L     R15,DCBSCTAD-1 .         SPECIAL CHAR TABLE      SA54959
         SR    R1,R1 .                  CLEAR INDEX REGISTER    SA54959
         MVI   LCBISZE,AVTEZERO .       CLEAR IDLES COUNT       SA54959
         IC    R1,SCTEOT(R15) .         GET EOT INDEX           SA54959
         LA    R1,AVTEZERO(R1,R15) .    EOT ENTRY ADDRESS       SA54959
         CLI   AVTEZERO(R1),TWO .       ETX/EOT                 SA54959
         BNE   EOTCODE2 .               BRANCH IF NO            SA54959
         LA    R1,ONE(,R1) .            BUMP PAST ETX           SA54959
EOTCODE2 EQU   *                                                SA54959
         SR    R15,R15 .                CLEAR WORK REGISTER     SA54959
         TM    PRFSTAT1,PRFNHDRN .      HEADER BUFFER           SA54959
         BNO   EOTCODE4 .               BRANCH IF YES           SA54959
         MVC   PRFSTXT(ONE),ONE(R1) .   INSERT EOT IN BFR       SA54959
         MVI   PRFSIZE+ONE,PRFSTXT+ONE-PRFSUNIT SET PRFSIZE     SA54959
         TM    SCBERR4,SCBTXTTN+SCBSLCTN+SCBCONNN              @OY15001
*                            TEXT,SELECTION OR CONNECT ERROR?  @OY15001
         BNZ   ERROR1                   YES SET UNIT EXCEPTION @OY15001
         TM    SCBERR1,SCBCUTFN         CUTOFF EXECUTED        @OY15001
         BNO   TESTRAN                  NO, CONTINUE           @OY15001
ERROR1   EQU   *                                               @OY15001
         OI    LCBCSW+CSWST1,UNEX       SET EOT RECEIVED       @OY12280
         B     TESTRAN .                CONTINUE PROCESSING     SA54959
EOTCODE4 EQU   *                                                SA54959
         TM    SCBSTATE,SCBLCK1N .      LOCK MODE               SA54959
         BNO   EOTCODE5 .               BRANCH IF NO            SA54959
         MVC   PRFSHDR(ONE),ONE(R1) .   INSERT EOT IN BFR       SA54959
         MVI   PRFSIZE+ONE,PRFSHDR+ONE-PRFSUNIT SET PRFSIZE     SA54959
         TM    SCBERR4,SCBTXTTN+SCBSLCTN+SCBCONNN              @OY15001
*                            TEXT,SELECTION OR CONNECT ERROR?  @OY15001
         BNZ   ERROR2                   YES,SET UNIT EXCEPTION @OY15001
         TM    SCBERR1,SCBCUTFN         CUTOFF EXECUTED        @OY15001
         BNO   EOTCODE1                 CONTINUE PROCESSING    @OY15001
ERROR2   EQU   *                                               @OY15001
         OI    LCBCSW+CSWST1,UNEX       SET EOT RECEIVED       @OY12280
         B     EOTCODE1 .               CONTINUE PROCESSING     SA54959
EOTCODE5 EQU   *                                                SA54959
         XC    SCBMRFSD(4),SCBMRFSD .   CLEAR MULTI ROUTE       SA54959
         LA    R1,AVTBFRTB .            BUFFER RETURN QUEUE     SA54959
         STCM  R1,AD,SCBDESTQ           BUFFER RETURN QCB        Y05330
         B     EXIT .                   SCRAM                   SA54959
         SPACE
TNTSUB   EQU   *                                                 Y05330
         LH    R1,PRFDEST               DESTINATION OFFSET       Y05330
         N     R1,AVTCLRHI              CLEAR HIGH ORDER HALF    Y05330
TNTSUB1  EQU   *                                                 Y05330
         L     R15,AVTRNMPT             ADDRESS OF TNT           Y05330
         BR    R15                      LINK TO TNT              Y05330
         EJECT
***************************************************************@Y17XAMF
*        SNA SSCP PROCESSING                                  *@Y17XAMF
***************************************************************@Y17XAMF
         SPACE 3
         USING IEDNSVTD,RSAVT                                  @Y17XAMF
PROCSSCP EQU   *
         SR    R15,R15                  SET NOT SSCP RETURN    @Y17XAMF
         CLC   PRFSRCE,SAVTSCPT         SOURCE THE SSCP        @Y17XAMF
         BE    PRSSCP                   BRANCH IF YES          @Y17XAMF
         CLC   PRFDEST,SAVTSCPT         DEST THE SSCP          @Y17XAMF
         BNE   SSCPRET                  BRANCH IF NO           @Y17XAMF
PRSSCP   EQU   *                                               @Y17XAMF
         SR    R1,R1                    CLEAR REG ONE          @Y17XAMF
         IC    R1,PRFISIZE              GET RESERVE COUNT      @Y17XAMF
         STC   R1,LCBISZE               SET LCB RESERVE COUNT  @Y17XAMF
         LA    R1,AVTHDRSZ(R1)          ADD HEADER SIZE        @Y17XAMF
         STH   R1,PRFSCAN               SET THE SCAN POINTER   @Y17XAMF
         LA    R1,AVTINOUT              END-OF-PARM-LIST ADDR  @YM05706
         STCM  R1,AD,SCBMACR            INITIALIZE MACRO PTR   @YM05706
         OI    SCBQTYPE,SCBNORCL        PREVENT RECALLS        @Y17XAMF
         TM    LCBSTAT1,LCBSENDN        SENDING?               @Y17XAMF
         BO    SETTWO                   BRANCH IF SO           @Y17XAMF
         OI    LCBSTAT2,LCBRESP         SET RESPONSE RQRD      @Y17XAMF
SETTWO   EQU   *                                               @Y17XAMF
         LA    R15,TWO                  SET SSCP EXECUTED      @Y17XAMF
SSCPRET  EQU   *                                               @Y17XAMF
         BR    R14                      RETURN                 @Y17XAMF
         EJECT
***************************************************************@Y17XAMF
*        SNA LU PROCESSING                                    *@Y17XAMF
***************************************************************@Y17XAMF
         SPACE 3
PROCSNA  EQU   *                                               @Y17XAMF
         ST    R14,SAV14                SAVE RETURN REG        @Y17XAMF
         USING IEDPF1,RPREF                                    @Y17XAMF
         TM    LCBSTAT1,LCBSENDN        SENDING                @Y17XAMF
         BZ    CKFMDATA                 BRANCH IF NO           @Y17XAMF
         CLI   PRFPRI,PRIMHBFR-1        ERROR RESPONSE BUFFER  @Y17XAMF
         BE    SNARET                   BRANCH IF YES          @Y17XAMF
         XC    PRF1RH(PRF1LEN),PRF1RH   CLEAR TOTAL PREFIX     @Y17XAMF
         B     SNARET                   BRANCH TO RETURN       @Y17XAMF
         DROP  RPREF                                           @Y17XAMF
CKFMDATA EQU   *                                               @Y17XAMF
         USING IEDRH,RPREF                                     @Y17XAMF
         TM    PRFSTAT1,PRFNHDRN        HEADER BUFFER          @YM08571
         BO    DONTSET                  BRANCH IF NOT HEADER   @YM08571
         TM    PRF1FLG1-IEDPF1(RPREF),PRF1LOGD  IS THIS BUFFER @YM08571
*                                       DATA FROM THE LOGON?   @YM08571
         BO    DONTSET                  BRANCH IF SO           @YM08571
         OI    LCBSTAT2,LCBRESP         SET RESPONSE RQRD      @YM08571
DONTSET  EQU   *                                               @YM08571
         TM    TRHBYTE0,TRHSC           FM DATA?               @YM08571
         BNZ   CHKDFC                   BRANCH IF NO           @YM08571
         TM    PRFSTAT1,PRFNHDRN        HEADER BUFFER          @YM08571
         BO    SNARET                   BRANCH IF NOT HEADER   @ZM47696
         LH    R1,PRFSRCE               GET SOURCE             @Y17XAMF
         N     R1,AVTCLRHI              CLEAR HI-ORDER BYTES   @Y17XAMF
         L     R15,AVTRNMPT             LOAD TNT ADDR          @Y17XAMF
         BALR  R14,R15                  CALL TNT ROUTINE       @Y17XAMF
         SH    RTRM,TPREFIX             ADDR NEG PREFIX        @Y17XAMF
         USING IEDNTRM,RTRM                                    @Y17XAMF
         SR    RSIB,RSIB                CLEAR SIB REG          @Y17XAMF
         ICM   RSIB,ADDR,TRMSIBPT       GET THE FIRST SIB      @Y17XAMF
         USING IEDSIBD,RSIB                                    @Y17XAMF
         BZ    SNARET                   BRANCH IF NO SIB       @Y17XAMF
CHKSIB   EQU   *                                               @Y17XAMF
         CLC   PRFDEST,SIBINDEX         CORRECT SIB            @Y17XAMF
         BNE   SNARET                   BRANCH IF NO           @Y17XAMF
         CLC   SIBINDEX,SAVTMHDX        INBOARD LU A TPROCESS  @Y17XAMF
         BNL   SNARET                   BRANCH IF NO           @YM06113
         DROP RTRM                                             @Y17XAMF
         LH    R1,SIBINDEX              GET TNT INDEX OF TPQ   @Y17XAMF
         N     R1,AVTCLRHI              CLEAR HI-ORDER BYTES   @Y17XAMF
         L     R15,AVTRNMPT             LOAD TNT ROUTINE ADDR  @Y17XAMF
         BALR  R14,R15                  CALL TNT ROUTINE       @Y17XAMF
         USING IEDQTRM,RTRM                                    @Y17XAMF
         MVC   SCBDESTQ,TRMDESTQ        SET DEST QUEUE ADDR    @Y17XAMF
         TM    AVTBIT3,AVTRECVN+AVTRFULN                       @Y17XAMF
*                                       ANY NO RECEIVE CONDX   @Y17XAMF
         BZ    SNARET                   BRANCH IF NO           @Y17XAMF
         DROP  RSIB                                            @Y17XAMF
         L     RTQCB,TRMDESTQ-1         LOAD QCB ADDR          @Y17XAMF
         USING IEDQQCB,RTQCB                                   @Y17XAMF
         TM    QCBDSFLG,QCBNREUS        NON-REUS QUEUEING      @Y17XAMF
         BO    SNARET                   BRANCH IF YES          @Y17XAMF
         TM    AVTBIT3,AVTRFULN         REUS QUEUE FULL        @Y17XAMF
         BZ    CHKCORE                  BRANCH IF NO           @Y17XAMF
         TM    QCBDSFLG,QCBREUS         REUS QUEUEING          @Y17XAMF
         BO    NOROUTE                  BRANCH IF YES          @Y17XAMF
CHKCORE  EQU   *                                               @Y17XAMF
         TM    AVTBIT3,AVTRECVN         CORE QUEUE FULL        @Y17XAMF
         BZ    SNARET                   BRANCH IF NO           @Y17XAMF
         TM    QCBDSFLG,QCBREUS+QCBNREUS                       @Y17XAMF
*                                       ANY DISK QUEUEING      @Y17XAMF
         BNZ   SNARET                   BRANCH IF YES          @Y17XAMF
NOROUTE  EQU   *                                               @Y17XAMF
         OI    SCBERR2,SCBFRWDN         INDIC FWD ERROR        @YM06894
         OI    SCBERR3,SCBTHRSN         INDIC THRESH ERR       @YM06894
         LA    R15,AVTBFRTB             LOAD ADDR OF BUFF RET  @Y17XAMF
         STCM  R15,ADDR,SCBDESTQ        RESET ROUTING TO BF RT @Y17XAMF
         DROP  RTQCB                                           @Y17XAMF
         LH    R1,AVTDLQX               GET DLQ TTCIN          @YM06894
         LTR   R1,R1                    IS THERE A DLQ?        @YM06894
         BZ    SNARET                   NO                     @YM06894
         L     R15,AVTRNMPT             GET TTE CONVERT PTR    @YM06894
         BALR  R14,R15                  GO CONVERT TO TTE ADDR @YM06894
         USING IEDQTRM,RTRM             ESTAB. ADDRESSABILITY  @YM06894
         MVC   SCBDESTQ,TRMDESTQ        SET DLQ AS DEST QCB    @YM06894
         B     SNARET                   BRANCH TO RETURN       @Y17XAMF
************************************************************** @Y17XAMF
*        DFC PROCESSING                                      * @Y17XAMF
************************************************************** @Y17XAMF
CHKDFC   EQU   *                                               @Y17XAMF
         TM    TRHBYTE0,TRHDFC          DFC     COMMAND        @Y17XAMF
         BZ    SNARET                   BRANCH IF NO           @Y17XAMF
         TM    TRHBYTE0,TRHSC           SC COMMAND             @Y17XAMF
         BO    SNARET                   BRANCH IF YES          @Y17XAMF
         LR    R9,RQCB7                 SET STATRMH QCB REG    @Y17XAMF
         SH    R9,SPREFIX               SET TO STARTMH QCB     @Y17XAMF
*                                       NEG PREFIX             @Y17XAMF
         TM    STMHFLG0-IEDNSTMH(R9),STMHDFC DFC SUPPORT       @Y17XAMF
         BZ    CHKPART                  BRANCH IF NO           @Y17XAMF
         CLC   LCBLUSNS,NOTSUPP         SUPPORTED COMMAND      @Y17XAMF
         BNE   SNARET                   BRANCH IF YES          @Y17XAMF
         BAL   R14,SETBD                CALL IEDQBD POST SET   @Y17XAMF
         B     POSTRET                  BRANCH TO RETURN       @Y17XAMF
CHKPART EQU    *                                               @Y17XAMF
         TM    STMHFLG0-IEDNSTMH(R9),STMHDFCP  PARTIAL DFCF    @Y17XAMF
         BO    PROCPART                 BRANCH IF YES          @Y17XAMF
         BAL   R14,SETBD                CALL IEDQBD POST ROUT  @Y17XAMF
         B     POSTRET                  BRANCH TO RETURN       @Y17XAMF
PROCPART EQU   *                                               @Y17XAMF
         SR    R9,R9                    CLEAR REG NINE         @Y17XAMF
         IC    R9,PRFISIZE              GET RESERVE COUNT      @Y17XAMF
         LA    R9,AVTHDRSZ+AVTUMALN(R9) ADD HEADER SIZE        @Y17XAMF
         AR    R9,RPREFIX               POINT TO FIRST CHAR    @Y17XAMF
         TM    TRHBYTE0,TRHSDI          EXCEPTION REQUEST      @Y17XAMF
         BZ    NOTEXR                   BRANCH IF NO           @Y17XAMF
         LA    R9,L'SNSSYSTM+L'SNSUSER(R9) POINT TO FIRST CHAR @Y17XAMF
NOTEXR   EQU   *                                               @Y17XAMF
         CLI   0(R9),CD1SIG             SIGNAL DFC CMD         @Y17XAMF
         BE    SNARET                   BRANCH IF YES          @Y17XAMF
         CLI   0(R9),CD1RTR             RTR DFC CMD            @Y17XAMF
         BE    SNARET                   BRANCH IF YES          @Y17XAMF
         CLI   0(R9),CD1LSTAT           LUSTAT DFC CMD         @Y17XAMF
         BE    SNARET                   BRANCH IF YES          @Y17XAMF
         BAL   R14,SETBD                CALL IEDQBD POST ROUT  @Y17XAMF
         B     POSTRET                  BRANCH TO RETURN       @Y17XAMF
SNARET   EQU   *                                               @Y17XAMF
         SR    R15,R15                  SET NO POST RETURN     @Y17XAMF
POSTRET  EQU   *                                               @Y17XAMF
         L     R14,SAV14                RESET RETURN REG       @Y17XAMF
         BR    R14                      RETURN                 @Y17XAMF
         SPACE 3
SETBD    EQU   *                                               @Y17XAMF
         MVC   LCBISZE,PRFISIZE         SET # IDLES            @YM07011
         L     R15,AVTMSGS-1            LOAD VCON LIST ADDR    @Y17XAMF
         L     R15,AVTEZERO(R15)        LOAD IEDQBD ADDR       @Y17XAMF
         STCM  R15,ADDR,PRFQCBA         SET QCB ADDR           @Y17XAMF
         MVI   PRFPRI,PRIDSPLB          SET IEDQBD PRIORITY    @Y17XAMF
         BR    R14                      RETURN                 @Y17XAMF
         DROP  RPREF                                           @Y17XAMF
         DROP  RSAVT                                           @Y17XAMF
         EJECT
         SPACE 3                                                 Y05330
********* CONSTANTS *********
         SPACE
SAV14    DS    F                        REGISTER SAVE          @Y17XAMF
         DS    0H                       FOLLOWING FIELDS MUST  @Y17XAMF
*                                        BE ON HALFWORD BDY    @Y17XAMF
PREFIX   DC    AL2(PRF1LEN)             LENGTH OF BUFFER PREFIX@Y17XAMF
TPREFIX  DC    AL2(TRMPRFSZ)            LENGTH OF TERMINAL PREF@Y17XAMF
SPREFIX  DC    AL2(STMHNLEN)            LENGTH OF STARTMH PREF @Y17XAMF
CONMAX   DC    H'9999'                  MAXIMUM SEQUENCE NUMBER
CONBASE  DC    H'4096'                  INCREMENT VALUE FOR
*                                         MULTIPLE BASE REGISTERS
NOTSUPP  DC    X'10030000'              FUNCTION NOT SUPPORTED @Y17XAMF
         EJECT
********* DSECTS *********
         SPACE
         TAVTD
         EJECT
         DCBD  DSORG=TX
         EJECT
         TDCTD                                                 @YM06159
         EJECT                                                 @YM06159
         TDEBD
         EJECT
         TDISPD
         EJECT
         TLCBD
         EJECT
         TPRFD
         EJECT
         TPRIOR
         EJECT
         TPROCD
         EJECT
         TQCBD
         EJECT
         TSCBD
CPBQTYPE EQU   X'03'                    RRN QUEUING MEDIA FLAGS SA52971
         EJECT
         TTRMD
         EJECT
         IEFUCBOB
         EJECT
         TLGBD ,                                                 S22024
         EJECT                                                 @Y17XAMF
         TCD1D                                                 @Y17XAMF
         EJECT                                                 @Y17XAMF
         TRHD                                                  @Y17XAMF
         EJECT                                                 @Y17XAMF
         TSIBD                                                 @Y17XAMF
         EJECT                                                 @Y17XAMF
         TSTMHND                                               @Y17XAMF
         EJECT
         TSNSD                                                 @Y17XAMF
         EJECT
         CVT    DSECT=YES
         EJECT
         IKJTSCVT
         EJECT
         IHAASVT                                               @ZM46751
         EJECT
         IHAASCB                                               @ZM46751
         EJECT
         IKJTSB LIST=YES
         END
