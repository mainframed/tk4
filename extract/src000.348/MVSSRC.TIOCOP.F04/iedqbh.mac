         TITLE 'IEDQBH -- CONCENTRATOR BUFFER DISPOSITION'
         SPACE 3
IEDQBH   CSECT
         SPACE 3
******************** MICROFICHE FLAGS *********************** SUPT CODE
*D536100-537500,604600-605200,689000                            SA52971
*A285540,287240,414000,623000,797000                             Y01004
*
*C284240,284480,306000,514000,518000,594000,632000,668000,708000 Y01004
*D285520,285600-285680,286640-287140,422000-426000,512000,516000,Y01004
         SPACE 3
*D520000-522000,592000,596000,622000,624000-626000,630000,634000,Y01004
*D706000,710000                                                  Y01004
***********************************************************************
*                                                                     *
* STATUS -- CHANGE LEVEL 0                                            *
*                                                                     *
* FUNCTION -- TO LOCATE THE PROPER SCB AND TO INTERFACE WITH IEDQBD   *
*    TO CONTROL THE EXECUTION OF OUTMSG FOR A CONCENTRATOR MH         *
*                                                                     *
* ENTRY POINTS --                                                     *
*         IEDQBH01                                                    *
*         IEDQBH02                                                    *
*                                                                     *
* INPUT --                                                            *
*    R1 ADDRESS OF A BUFFER, LCB OR ERB                               *
*    R3 ADDRESS OF STCB                                               *
*    R7 ADDRESS OF QCB                                                *
*    R11 DISPATCHER BASE                                              *
*    R13 ADDRESS OF AVTSAVE2                                          *
*    R15 ENTRY POINT ADDRESS                                          *
*                                                                     *
* OUTPUT --                                                           *
*    R1 ADDRESS OF A BUFFER, LCB OR ERB                               *
*    R3 ADDRESS OF STCB                                               *
*    R11 DISPATCHER BASE                                              @
*    R13 ADDRESS OF AVTSAVE2                                          *
*                                                                     *
* EXTERNAL REFERENCES --                                              *
*         NONE                                                        *
*                                                                     *
* EXITS,NORMAL --                                                     *
*         DSPPOST                                                     *
*         DSPBYPAS                                                    *
*                                                                     *
* EXITS,ERROR --                                                      *
*         NONE                                                        *
*                                                                     *
* TABLES/WORK AREAS --                                                *
*    AVT                                                              *
*    LCB                                                              *
*    QCB                                                              *
*    SCB                                                              *
*    TERMINAL ENTRY                                                   *
*    DRQ                                                              *
*    QCBE                                                             *
*                                                                     *
* ATTRIBUTES --                                                       *
*    RE-ENTRANT, REFRESHABLE, PROBLEM PROGRAM MODE                    *
*                                                                     *
* CHARACTER CODE DEPENDENCY --                                        *
*    THE OPERATION OF THIS MODULE DOES NOT DEPEND UPON A PARTICULAR   *
*    INTERNAL REPRESENTATION OF THE EXTERNAL CHARACTER SET.           *
*                                                                     *
* NOTES --                                                            *
*    STANDARD TCAM LINKAGES AND DEVIATIONS FROM STANDARDS.            *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*        SYMBOLIC REGISTERS                                           *
***********************************************************************
         SPACE 1
RZERO    EQU   0                        WORK REG
RPARM    EQU   1                        PARAMETER REG
RWORKA   EQU   2                        WORK REG
RQCBE    EQU   3                        QCBE
RSTCB    EQU   3                        STCB
RLCB     EQU   4                        LCB
RWORKB   EQU   5                        WORK REG
RDRQ     EQU   6                        DRQ
RQCB     EQU   7                        QCB
RWORKC   EQU   8                        WORK REG
RTERM    EQU   8                        TERMINAL ENTRY
RSCB     EQU   9                        SCB
RDCB     EQU   10                       DCB
RDISP    EQU   11                       DISPATCHER BASE
RBASE    EQU   12                       CSECT BASE
RAVT     EQU   13                       AUT BASE
RRETURN  EQU   14                       RETURN REG
RENTRY   EQU   15                       ENTRY POINT REG
         SPACE 1
***********************************************************************
*        ESTABLISH CSECT AND CONTROL BLOCK ADDRESSABILITY             *
***********************************************************************
         SPACE 1
         USING IEDQDISP,RDISP           DISP
         USING AVTSAVE2,RAVT            AVT
         USING IEDQQCB,RQCB             QCB
         USING IEDQSCB,RSCB             SCB
         USING IEDQLCB,RLCB             LCB
         USING IEDQTRM,RTERM            TERMINAL ENTRY
         USING IEDQDRQ,RDRQ             DRQ
         USING IEDQQCBE,RQCBE           QCBE REG
         SPACE 3
IEDQBH   IEDHJN
         DC    AL1(DSPMCPL4)            STCB
         DC    AL3(*-1)                 STCB ADDR
IEDQBH01 EQU   *
         USING *,RBASE
         LR    RBASE,RENTRY             SET BASE REG
         CLI   LCBPRI-IEDQLCB(RPARM),PRIFSPCI-1
*                                       ELEMENT A LCB
         BE    LCB                      BRANCH IF YES
         L     RLCB,PRFLCB-1-IEDQPRF(RPARM)
*                                       ASSUME BUFFER
         TM    PRFTIC-IEDQPRF(RPARM),CCWTIC
*                                       ELEMENT A BUFFER
         BO    BUFFER                   BRANCH IF YES
         SPACE 1
***********************************************************************
*        ELEMENT IS AN ERB                                            *
***********************************************************************
         SPACE 1
BDEXIT   EQU   *
         L     RQCB,IEDQBD              BD ADDR
BDEXIT2  EQU   *
         L     RSTCB,QCBSTCHN-1-IEDQQCB(RQCB)
*                                       STCB ADDR
         B     DSPBYPAS                 BYPASS TO BD
         SPACE 1
***********************************************************************
*        ELEMENT IS A LCB                                             *
***********************************************************************
         SPACE 1
LCB      EQU   *
         LR    RLCB,RPARM               SET LCB REG
         SPACE 1
***********************************************************************
*        ELEMENT IS A BUFFER                                          *
***********************************************************************
         SPACE 1
BUFFER   EQU   *
         TM    LCBSTAT1,LCBSENDN        LCB SENDING
         BNO   BDEXIT                   BRANCH IF NO
         L     RSCB,LCBSCBA-1           SCB ADDR
         TM    SCBQTYPE,SCBCONC         CONC
         BNO   BDEXIT                   BRANCH IF NO
         CLI   LCBPRI-IEDQLCB(RPARM),PRILCB
*                                       ELEMENT A LCB
         BNE   NOTLCB                   BRANCH IF NO
         L     RPARM,LCBLSPCI-1         BUFFER ADDR
NOTLCB   EQU   *
         TM    LCBERBST,LCBERROR        LINE ERROR
         BO    CHKERBWT                 BRANCH IF YES
         TM    PRFSTAT1-IEDQPRF(RPARM),PRFITCPN                  Y01004
*                                       LOGICAL MID-MESSAGE
         BZ    DRQLP                    BRANCH IF NO
         NI    PRFSTAT1-IEDQPRF(RPARM),X'FF'-PRFITCPN            Y01004
*                                       RESET LOGICAL MID-MESSAGE
         OI    LCBERBST,LCBERROR        SIMULATE ERROR
CHKERBWT EQU   *
         TM    LCBERBST,LCBDLNKN+LCBEOMSG
*                                       ERB AVAILABLE
         BNZ   DRQLP                    BRANCH IF YES
         LA    RWORKA,IEDQBH02          QCB ADDR
         ST    RWORKA,LCBRCQCB          SET ERB RETURN ADDR
         CLI   LCBERBPY,PRIDSPLB-1      ERB ON RDY Q
         BNE   DONTCHG                  BRANCH IF NO
         ST    RWORKA,LCBERB            SET QCB ADDR
DONTCHG  EQU   *
         STCM  RPARM,AD,LCBLSPCI        SAVE BUFFER ADDRESS      Y01004
         B     DSPDISP                  WAIT FOR ERB
DRQLP    EQU   *
         L     RQCB,SCBDESTQ-1          DEST QUEUE
         LR    RQCBE,RQCB               SET REG
         AH    RQCBE,QCBEXTO            QCBE ADDR
         L     RDRQ,QCBECONC-1          CONC TERM ADDR
         L     RDRQ,TRMDESTQ-1-IEDQTRM(RDRQ)
*                                       DRQ ADDR
         L     RWORKA,LCBSCBDA-1        LINE SCB ADDR
         STCM  RDRQ,AD,SCBDESTQ-IEDQSCB(RWORKA)  SAVE DRQ ADDR.  Y01004
         LR    RQCB,RDRQ                DRQ ADDR
         LA    RWORKA,DRQSLINK-IEDQDRQ-1
*                                       SET REG
         SR    RQCB,RWORKA              SET LOOP REG
DRQLPA   EQU   *
         L     RQCB,QCBSLINK-IEDQQCB-1(RQCB)
*                                       FIRST/NEXT STCB
         CLI   FOUR(RQCB),ZERO          LAST ENTRY
         BNE   QCBSET                   BRANCH IF NO
         ICM   RQCB,AD,LCBLSPCI         BUFFER TO FREE           Y01004
         BNZ   BUFFREE                  BRANCH IF YES
         OI    LCBSTAT1,LCBRCLLN        SET RECALL FOR BD/2
BUFFREE  EQU   *
         L     RQCB,IEDQBD2             BD/2 ADDR
         ST    RQCB,LCBQCBA-1           SET QCB ADDR
         XC    LCBLINK,LCBLINK          ZERO FIELD
         MVI   LCBPRI,PRIDESTQ-1        SET PRTY
         MVC   LCBSCBA,LCBSCBDA         SET SCB POINTER
         LR    RPARM,RLCB               SET REG FOR POST
         L     RSCB,LCBSCBDA-1          LINE SCB ADDR
         NI    SCBQTYPE,255-SCBCONC     RESET TEMP CONC FLAG
         B     BDEXIT2                  BYPASS TO BD/2
QCBSET   EQU   *
         LA    RZERO,DRQSTVTO-IEDQDRQ   STCB OFFSET
         SR    RQCB,RZERO               QCB ADDR
         LH    RQCBE,QCBEXTO            EXTENSION OFFSET
         AR    RQCBE,RQCB               QCBE ADDR
         TM    QCBEFLG,QCBEDATA+QCBEOMSG
*                                       OUTMSG POSSIBLE
         BZ    DRQLPA                   BRANCH IF NO
         L     RWORKA,LCBSCBDA-1        SCB ADDR
*        USING RWORKA  LINE SCB
         TM    SCBERR4-IEDQSCB(RWORKA),ERROR4
*                                       ANY ERRORS
         BZ    NOERRA                   BRANCH IF NO
         TM    QCBEFLG,QCBEDATA         DATA IN THIS MSG
         BO    EXOUTMSG                 BRANCH IF YES
NOERRA   EQU   *
         NI    QCBEFLG,255-QCBEDATA     RESET DATA IN MSG
         TM    QCBEFLG,QCBEOPL          LEVEL QUEUEING
         BZ    NOTLVLQ                  BRANCH IF NO
         SR    RWORKC,RWORKC            ZERO REG
         IC    RWORKC,QCBENPLV          NUMBER OF LEVELS
         LR    RWORKB,RQCBE             SET REG
         SR    RZERO,RZERO              ZERO REG
QCBELPA  EQU   *
         IC    RZERO,QCBELGTH-IEDQQCBE(RWORKB)
*                                       GET EXT LENGTH
         AR    RWORKB,RZERO             QCBE ADDR
         NI    QCBEFLG-IEDQQCBE(RWORKB),255-QCBEDATA
*                                       RESET DATA IN MSG
         BCT   RWORKC,QCBELPA           BRANCH IF MORE LEVELS
NOTLVLQ  EQU   *
         TM    QCBEFLG,QCBEOMSG         OUTMSG PENDING
         BZ    DRQLPA                   BRANCH IF NO
         TM    QCBEFLG,QCBESTAT         STATUS SPECIFIED
         BO    DRQLPA                   BRANCH IF YES
EXOUTMSG EQU   *
         STCM  RQCB,AD,DRQCURQ          SAVE LAST QCB            Y01004
         SR    RSCB,RSCB                ZERO REG
         SR    RWORKB,RWORKB            ZERO REG
         IC    RSCB,QCBSCBOF            SCB OFFSET
         IC    RWORKB,AVTSCBSZ          SCB SIZE
         MR    RSCB-1,RWORKB            COMPUTE
         ALR   RSCB,RWORKA              SCB ADDR
         OC    SCBERR1(FOUR),SCBERR1-IEDQSCB(RWORKA)
*                                       SET SCB
         MVC   SCBEOB,QCBEEOB           SET SAVED SCBEOB
         TM    QCBEFLG,QCBECNT          BYTE COUNT TERMINAL     SA52971
         BZ    CHKBUF                   BRANCH IF NO            SA52971
         MVC   SCBDEOB,QCBEDEOB         SET SAVED SCBDEOB
*        DROP  RWORKA  LINE SCB
CHKBUF   EQU   *
         LTR   RPARM,RPARM              GOT A BUFFER ALREADY
         BNZ   BUFFSET                  BRANCH IF YES
         L     RPARM,AVTBFREB           GET UNIT
         LA    RPARM,ZERO(RPARM)        ZERO HI-ORDER BYTE
         LTR   RPARM,RPARM              BUFFER AVAILABLE
         BNZ   DLNKBUF                  BRANCH IF YES
         LA    RWORKA,IEDQBH02          QCB ADDR FOR RETURN
         ST    RWORKA,LCBRCQCB          RETURN QUEUE
         MVI   LCBERBPY,PRIACTIV        POST PRIORITY
         LA    RWORKA,X'0101'           SET REG
         STH   RWORKA,LCBERBCT          SET COUNT TO ONE
         OI    LCBERBST,LCBPRCPG        INDICATE POST BACK
         LA    RWORKA,AVTBFREB          AVAILABLE BUFFER QUEUE
ERBPOST  EQU   *
         ST    RWORKA,LCBERB            QCB ADDR FOR POST
         XC    LCBERBLK,LCBERBLK        CLEAR LINK FIELD
         LA    RPARM,LCBERB             ADDR FOR POST
         BAL   RRETURN,DSPPOST          POST ERB               @Y17XAIX
DLNKBUF  EQU   *
         USING IEDQPRF,RPARM
         MVC   AVTBFREB+1(THREE),PRFLINK
*                                       DELINK BUFFER
         LH    RWORKA,AVTAVFCT          BUFFER COUNT
         N     RWORKA,AVTCLRHI          FORCE POSIFIVE
         BCTR  RWORKA,ZERO              SUB ONE
         STH   RWORKA,AVTAVFCT          RESTORE COUNT
         MVI   PRFNBUNT,ONE             SET ONE UNIT
         MVC   PRFTIC+ONE(THREE),BUFLINK
*                                       CLEAR LINK.
         MVI   PRFPRI,PRIMHBFR          SET PRIORITY
BUFFSET  EQU   *
         STCM  RPARM,AD,LCBLSPCI        SET BUFFER ADDRESS       Y01004
         STCM  RLCB,AD,PRFLCB           SET LCB ADDRESS          Y01004
         MVI   PRFSTAT1,ONE             SET TEXT LAST
         MVI   PRFTIC,CCWTIC            SET BUFFER FLAG
         NI    QCBEFLG,255-QCBEDATA-QCBEOMSG-QCBEHELD
*                                       RESET FLAGS
         LR    RWORKC,RQCBE             SAVE MAIN EXT
         TM    QCBEFLG,QCBEOPL          PRTY QUEUEING
         BZ    NOPRTY                   BRANCH IF NO
         SR    RZERO,RZERO              ZERO REG
         SR    RWORKB,RWORKB            ZERO REG
         SR    RWORKA,RWORKA            ZERO REG
         IC    RWORKB,QCBENPLV          NUMBER OF PRTY LEVLES
QCBELP   EQU   *
         AH    RZERO,HALFONE            ADD ONE
         CR    RZERO,RWORKB             LAST LEVEL
         BH    RESETSCB                 BRANCH IF YES
         IC    RWORKA,QCBELGTH          ENTRY LENGTH
         AR    RQCBE,RWORKA             ENTRY ADDR
         TM    SCBERR4,ERROR4           ANY ERRORS
         BZ    NOERRB                   BRANCH IF NO
         TM    QCBEFLG,QCBEDATA         DATA IN THIS MESSAGE
         BO    FOMSG                    BRANCH IF YES
NOERRB   EQU   *
         TM    QCBEFLG,QCBEOMSG         OUTMSG PENDING
         BZ    QCBELP                   BRANCH IF NO
         TM    QCBEFLG,QCBESTAT         STATUS SPECIFIED
         BO    QCBELP                   BRANCH IF YES
FOMSG    EQU   *
         TM    SCBERR4,SCBSLCTN         INITIAL SELECTION ERROR
         BO    SCBSET                   BRANCH IF YES
         MVC   SCBSCHDR,QCBEHDR         SET HDR ADDR
         MVC   SCBFEFO,QCBEFEFO         SET FEFO POINTER
         MVC   SCBPRI,QCBEPRI           SET PRTY LEVEL
         MVC   SCBOSEQ,QCBEOSEQ         SET SEQ NUMBER
         MVZ   SCBQTYPE,QCBEQTYP        SET QTYPE
         STCM  RQCB,AD,SCBDESTQ         SET DESTQ                Y01004
SCBSET   EQU   *
         NI    QCBEFLG,255-QCBEDATA-QCBEOMSG-QCBEHELD
*                                       RESET FLAGS
NOPRTY   EQU   *
         MVC   PRFNHDR,SCBSCHDR         SET HDR ADDR
         DROP  RPARM
         TM    SCBERR4,SCBSLCTN         INITIAL SELECTION ERROR
         BO    TTCINSET                 BRANCH IF YES
         MVC   LCBTTCIN,QCBETCIN        SET TTCIN
         MVC   SCBMACR,QCBEMACR-IEDQQCBE(RWORKC)
*                                       SET SCBMACR
TTCINSET EQU   *
         STCM  RSCB,AD,LCBSCBA          SET SCB ADDRESS          Y01004
         NI    LCBSTAT1,255-LCBRCLLN    RESET RECALL
         LA    RWORKA,IEDQBH02          QCB ADDR
         STCM  RWORKA,AD,LCBQCBA        SET RETURN               Y01004
         B     BDEXIT                   BYPASS TO BD
         DS    0F
         ORG   *-8                      ORG BACK TO CREATE DUMMY QCB
IEDQBH02 EQU   *
         ORG
         DC    AL1(DSPMCPL4)            STCB
         DC    AL3(*-1)                 STCB ADDR
         USING *,RENTRY
         L     RBASE,BHBASE             SET BASE REG
         DROP  RENTRY
         LR    RLCB,RPARM               SET LCB REG
         LR    RWORKB,RPARM             SAVE REG
         SR    RPARM,RPARM              INDICATE NO BUFFER
         CLI   LCBRSKEY-IEDQLCB(RWORKB),RSVTO
*                                       ELEMENT A LCB
         BNE   ERB                      BRANCH IF NO
         STCM  RPARM,AD,LCBLSPCI        INDICATE NO BUFFER       Y01004
         NI    LCBSTAT1,255-LCBRCLLN    RESET RECALL
         MVI   LCBERBST,ZERO            CLEAR ERB STATUS
         L     RSCB,LCBSCBA-1           SCB ADDR
         L     RWORKA,LCBSCBDA-1        LINE SCB ADDR
         L     RDRQ,SCBDESTQ-1-IEDQSCB(RWORKA)
*                                       RESTORE DRQ ADDR
         L     RQCB,DRQCURQ-1           LAST QCB SERVICED
         LR    RQCBE,RQCB               SET REG
         AH    RQCBE,QCBEXTO            QCBE ADDR
         TM    SCBQTYPE,SCBBFMM         HOLD LEVEL=BLOCK
         BO    DRQLPA                   BRANCH IF YES
         LA    RWORKA,AVTCKPTB          CHKPT QCB ADDR
         MVI   LCBERBPY,PRICKPLN        SET ERB PRIORITY
         TM    SCBSTATE,SCBCKPT         CHKPT REQUESTED
         BO    ERBPOST                  BRANCH IF YES
         MVI   SCBHBFNO,ZERO            CLEAR SCBHBFNO
         TM    QCBEFLG,QCBEOPL          LEVEL QUEUEING
         BO    CHKBUF                   BRANCH IF YES
RESETSCB EQU   *
         SR    RZERO,RZERO              ZERO REG
         ST    RZERO,SCBERRST           CLEAR ERROR BYTES
         ST    RZERO,SCBDEOB            CLEAR DEOB
         STH   RZERO,SCBEOB             CLEAR EOB
         ST    RZERO,SCBHBFNO           CLEAR HBFNO AND SCHDR   SA52971
         NI    SCBQTYPE,255-SCBNREUS-SCBREUS-SCBCOREQ-SCBNORCL-SCBBFMM
*                                       RESET QTYPE
         LA    RWORKA,AVTBFRTB          BUFFER RETURN
         STCM  RWORKA,AD,SCBDESTQ       SET TO BUFFER RETURN     Y01004
         B     DRQLPA                   BRANCH TO LOOP
         SPACE 1
***********************************************************************
*        ELEMENT IS A ERB                                             *
***********************************************************************
         SPACE 1
ERB      EQU   *
         LA    RWORKA,LCBERB-IEDQLCB     ERB OFFSET INTO LCB
         SR    RWORKB,RWORKA            COMPUTE LCB
         LR    RLCB,RWORKB              LCB
         L     RSCB,LCBSCBA-1           LOAD SCB ADDR
         TM    LCBERBST,LCBPRCPG        ERB FROM BUFFER REQUEST
         BO    BUFRET                   BRANCH IF YES
         OI    LCBERBST,LCBDLNKN        MARK ERB AVAILABLE
         L     RPARM,LCBLSPCI-1         RESTORE BUFFER ADDR
         B     DRQLP                    BRANCH TO PROCESS
BUFRET   EQU   *
         NI    LCBERBST,255-LCBPRCPG    RESET REQUEST BIT
         L     RPARM,LCBERBCH-1         BUFFER ADDR
         MVC   PRFTIC+ONE-IEDQPRF(THREE,RPARM),BUFLINK
*                                       CLEAR LINK
         L     RWORKA,LCBSCBDA-1        LINE SCB ADDR
         L     RDRQ,SCBDESTQ-1-IEDQSCB(RWORKA)
*                                       RESTORE DRQ ADDR
         L     RQCB,DRQCURQ-1           CURRENT QCB ADDR
         LR    RQCBE,RQCB               SET REG
         AH    RQCBE,QCBEXTO            QCBE ADDR
         B     EXOUTMSG                 BRANCH TO PROCESS
         SPACE 1
         EJECT
         SPACE 1
***********************************************************************
*        EQUATES                                                      *
***********************************************************************
         SPACE 1
RSVTO    EQU   X'0C'                    RCV VTO
CSVTO    EQU   X'20'                    CONC VTO
ZERO     EQU   0                        ZERO
ONE      EQU   1                        ONE
TWO      EQU   2                        TWO
THREE    EQU   3                        THREE
FOUR     EQU   4                        FOUR
FIVE     EQU   5                        FIVE
SIX      EQU   6                        SIX
SEVEN    EQU   7                        SEVEN
EIGHT    EQU   8                        EIGHT
AD       EQU   7                        MASK FOR ICM AND STCM    Y01004
         EJECT
***********************************************************************
*        CONSTANTS                                                    *
***********************************************************************
         SPACE 1
BHBASE   DC    A(IEDQBH01)              BASE ADDR OF BH
IEDQBD   DC    V(IEDQBD)                VCON FOR IEDQBD
IEDQBD2  DC    V(IEDQBD02)              BD/2 VCON
HALFONE  DC    H'1'                     ONE
BUFLINK  DC    X'000002'                BUFFER LINK
         EJECT
         SPACE 1
***********************************************************************
*        DSECTS                                                       *
***********************************************************************
         SPACE 1
         TPRIOR
         TAVTD
         TQCBD
         TLCBD
         TSCBD
ERROR4   EQU   SCBSLCTN+SCBCONNN+SCBTRMLN+SCBCTLUN+SCBCHANN+SCBUNDFN+SCX
               BTXTTN
         TDISPD
         TTRMD
         TPRFD
         TCCWD
         TDRQD
         TQCBED
         END
