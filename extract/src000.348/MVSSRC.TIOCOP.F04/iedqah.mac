AH01     TITLE '''IEDQAH'' - INPUT SEQUENCE VERIFICATION ROUTINE'
IEDQAH   CSECT
*A519000,703000-705000                                           S22025
*C042000,093000,123000                                           S22025
*C517200-518400,531000,633000,648000                             S22025
*D264000,516600-518400,519000,705000                             S22025
         SPACE
***********************************************************************
*                                                                     *
*STATUS -- CHANGE LEVEL 0                                             *
*                                                                     *
*FUNCTION -- VERIFIES AND UPDATES AN INPUT SEQUENCE NUMBER SPECI-     *
*   FIED BY THE USER IN THE BUFFER.                                   *
*                                                                     *
*   THE ROUTINE EXAMINES THE TERMINAL-CURRENTLY-CONNECTED FIELD IN    *
*   THE LCB (LCBTTCIN).  IF IT IS ZEROES,  THE ORIGIN IS UNKNOWN.     *
*   A RETURN CODE OF X'0C' IS SET IN REGISTER 15 AND RETURN IS MADE   *
*   TO THE CALLER VIA MH INTERFACE ROUTINE (IEDQUI).             S22025
*                                                                     *
*   IF IT IS NOT ZEROES,  LCBTTCIN IS PASSED TO THE TNT CODE          *
*   (AVTRNMPT),  WHICH RETURNS THE ADDRESS OF THE TERMINAL TABLE      *
*   ENTRY FOR THE ORIGIN.                                             *
*                                                                     *
*   THE EBCDIC CHARACTERS MAKING UP THE USER-SUPPLIED INPUT           *
*   SEQUENCE NUMBER ARE PASSED IN THE AVT WORK AREA (AVTDOUBL).       *
*   THE ROUTINE CONVERTS THIS NUMBER TO A BINARY NUMBER AND           *
*   COMPARES IT TO THE ANTICIPATED INPUT SEQUENCE NUMBER LOCATED      *
*   IN THE TERMINAL TABLE ENTRY (TRMINSEQ).  IF THE NUMBER PASSED     *
*   IS HIGHER,  THE 'SEQUENCE NUMBER HIGH' ERROR FLAG IS SET IN       *
*   THE SCB,  AND A RETURN CODE OF X'08' IS SET IN REGISTER 15.  IF   *
*   THE NUMBER PASSED IS LOWER,  THE 'SEQUENCE NUMBER LOW'  ERROR     *
*   FLAG IS SET IN THE SCB,  AND A RETURN CODE OF X'04' IS SET IN     *
*   REGISTER 15.  IN BOTH CASES,  THE SEQUENCE NUMBER IN THE          *
*   TERMINAL TABLE ENTRY REMAINS UNCHANGED AND RETURN IS MADE TO      *
*   THE CALLER VIA IEDQUI                                        S22025
*                                                                     *
*   IF THE PASSED NUMBER IS EQUAL TO THE NUMBER IN THE TERMINAL       *
*   TABLE ENTRY,  THE SEQUENCE-IN FLAG IN THE SCB STATUS FIELD IS     *
*   SET (SCBSEQIN IN SCBSTATE).  IF THE CURRENT BUFFER IS A HEADER    *
*   BUFFER,  THE NUMBER IS SET IN THE PREFIX INPUT SEQUENCE NUMBER    *
*   FIELD (PRFISEQ).  THE NUMBER IS THEN INCREMENTED BY ONE.  IF      *
*   THE RESULT IS OVER 9999 (THE MAXIMUM PERMITTED SEQUENCE NUMBER)   *
*   IT IS RESET TO ONE.  THE UPDATED NUMBER IS STORED BACK IN THE     *
*   TERMINAL TABLE ENTRY,  A RETURN CODE OF X'00' IS SET IN           *
*   REGISTER 15, AND RETURN IS MADE TO THE CALLER VIA IEDQUI.    S22025
*                                                                     *
*ENTRY POINTS --                                                      *
*       'IEDQAH01' TO VERIFY AND UPDATE THE INPUT SEQUENCE NUMBER.    *
*   CALLING SEQUENCE IS:                                              *
*                                                                     *
*        L     R12,AVTMSGS-1            GET ADDR OF VCON TABLE        *
*        IC    R15,AVTEZERO(,R1)        GET INDEX TO ROUTINE ADDR     *
*        L     R12,AVTEZERO(R12,R15)    GET ROUTINE ADDRESS           *
*        BR    R12                      EXIT TO ROUTINE               *
*                                                                     *
*INPUT --                                                             *
*   REGISTER 1 - THE ADDRESS OF A MACRO-GENERATED PARAMETER LIST.     *
*   PARAMETER LIST FORMAT IS:                                         *
*                                                                     *
*   *****************                                                 *
*   * INDEX * PARM  *                                                 *
*   *  TO   *  LIST *                                                 *
*   *   AH  *  LNG  *                                                 *
*   *****************                                                 *
*                                                                     *
*   REGISTER 3 - ADDRESS OF THE SCB.                                  *
*                                                                     *
*   REGISTER 4 - ADDRESS OF THE LCB.                                  *
*                                                                     *
*   REGISTER 6 - ADDRESS OF THE CURRENT BUFFER.                       *
*                                                                     *
*   REGISTER 9 - ADDRESS OF THE AVT.                                  *
*                                                                     *
*   REGISTER 12 - ENTRY POINT AND BASE REGISTER FOR THIS ROUTINE.     *
*                                                                     *
*   FIRST AVT PARAMETER AREA (AVTPARM) - THE NUMBER OF BYTES IN THE   *
*   USER-SUPPLIED INPUT SEQUENCE NUMBER.                              *
*                                                                     *
*   AVT WORK AREA (AVTDOUBL) - THE USER-SUPPLIED INPUT SEQUENCE       *
*   NUMBER.                                                           *
*                                                                     *
*OUTPUT --                                                            *
*   REGISTER 15 - RETURN CODE.  ITS VALUE IS X'00' ON SUCCESSFUL      *
*   COMPLETION,  X'04' IF SEQUENCE NUMBER LOW,  X'08' IF SEQUENCE     *
*   NUMBER HIGH,  AND X'0C' IF ORIGIN NOT SPECIFIED.                  *
*                                                                     *
*   PREFIX INPUT SEQUENCE NUMBER FIELD (PRFISEQ) - IF A HEADER        *
*   BUFFER,  INITIALIZED TO THE INPUT SEQUENCE NUMBER ON SUCCESSFUL   *
*   COMPLETION.                                                       *
*                                                                     *
*EXTERNAL REFERENCES --                                               *
*                                                                     *
*   TERMINAL NAME TABLE CODE (AVTRNMPT).                              *
*                                                                     *
*   AVT - ADDRESS VECTOR TABLE.                                       *
*                                                                     *
*   BUFFER CURRENTLY BEING PROCESSED.                                 *
*                                                                     *
*   LCB - LINE CONTROL BLOCK.                                         *
*                                                                     *
*   SCB - STATION CONTROL BLOCK.                                      *
*                                                                     *
*EXITS,  NORMAL -- SUCCESSFUL COMPLETION.  IF A HEADER BUFFER,  THE   *
*   PREFIX INPUT SEQUENCE NUMBER FIELD IS SET.  THE INPUT SEQUENCE    *
*   NUMBER FIELD IN THE TERMINAL TABLE ENTRY (TRMINSEQ) IS UPDATED    *
*   TO THE NEXT ANTICIPATED INPUT SEQUENCE NUMBER.  THE SEQUENCE-IN   *
*   FLAG (SCBSEQIN) IS SET IN THE SCB (SCBSTATE).  REGISTER 15 CON-   *
*   TAINS A RETURN CODE OF X'00'.                                     *
*                                                                     *
*EXITS,  ERROR -- ORIGIN IS NOT SPECIFIED IN THE LCB (LCBTTCIN).      *
*   REGISTER 15 CONTAINS A RETURN CODE OF X'0C'.                      *
*                                                                     *
*   USER-SUPPLIED INPUT SEQUENCE NUMBER IS LOW.  THE 'SEQUENCE-       *
*   NUMBER-LOW' ERROR FLAG IS SET IN THE SCB.  REGISTER 15 CONTAINS   *
*   A RETURN CODE OF X'04'.                                           *
*                                                                     *
*   USER-SUPPLIED INPUT SEQUENCE NUMBER IS HIGH.  THE 'SEQUENCE-      *
*   NUMBER-HIGH' ERROR FLAG IS SET IN THE SCB.  REGISTER 15           *
*   CONTAINS A RETURN CODE OF X'08'.                                  *
*                                                                     *
*TABLES/WORK AREAS -- N/A.                                            *
*                                                                     *
*ATTRIBUTES -- SERIALLY REUSABLE,  REFRESHABLE,  ENABLED,             *
*   RESIDENT,  PROBLEM PROGRAM MODE.                                  *
*                                                                     *
*NOTES -- THE OPERATION OF THIS MODULE DOES NOT DEPEND UPON AN        *
*   INTERNAL REPRESENTATION OF THE EXTERNAL CHARACTER SET.            *
*                                                                     *
***********************************************************************
         EJECT
********* REGISTER EQUATES *********
         SPACE
RDIG0    EQU   0                        DIGIT REGISTER
         SPACE
R1       EQU   1                        INITIAL PARAMETER LIST ADDR
*                                         AND TERMINAL ENTRY ADDR
         SPACE
RRET2    EQU   2                        EXIT REGISTER
         SPACE
RSCB3    EQU   3                        SCB ADDRESS
         SPACE
RLCB4    EQU   4                        LCB ADDRESS
         SPACE
RPREFIX  EQU   6                        BUFFER ADDRESS
         SPACE
RCLEAR7  EQU   7                        CLEAR MASK REGISTER
         SPACE
RAVT9    EQU   9                        AVT ADDRESS
         SPACE
RCT11    EQU   11                       COUNT REGISTER
         SPACE
RBASE12  EQU   12                       BASE REGISTER
         SPACE
RINDEX14 EQU   14                       INDEX TO SUBSEQUENT DIGITS
R14      EQU   14                       RETURN REGISTER
         SPACE
RNUM15   EQU   15                       TRUE SEQ IN NO. (BFR NO.)
R15      EQU   15                       RETURN CODE REGISTER
         SPACE
********* OTHER EQUATES *********
         SPACE
NOTZERO  EQU   7                        CONDITION CODE: NOT ZERO
CLERMASK EQU   X'0F'                    MASK TO CLEAR ZONE
FOUR     EQU   4                        MAX. NO. OF DIGITS IN SEQIN
ONE      EQU   1                        INCREMENT VALUE OF 1
         SPACE
ZERO     EQU   8                        CONDITION CODE: ZEROES
NUMZONE  EQU   X'F0'                    ZONE FOR A NUMBER
         EJECT
         USING IEDQAVTD,RAVT9
         USING IEDQLCB,RLCB4
         USING IEDQPRF,RPREFIX
         USING IEDQSCB,RSCB3
         USING IEDQAH,RBASE12
         SPACE
IEDQAH01 EQU   *
IEDQAH   IEDHJN SEQIN                   MODULE ID                S22025
         LH    R1,LCBTTCIN              PICK UP SOURCE KEY
         N     R1,AVTCLRHI              IS IT ZERO
         LA    R15,AVTECD12             ANTICIPATE ORIGIN NOT FOUND
         BC    ZERO,EXIT                BRANCH TO RETURN IF ZERO S22025
         SPACE
         L     R15,AVTRNMPT             GET TNT CODE ADDRESS
         BALR  R14,R15                  ELSE LINK TO GET TERM ENTRY
         SPACE
CONVERT  EQU   *
         USING IEDQTRM,R1
         SR    RNUM15,RNUM15            SET INITIAL VALUE
         LA    RCLEAR7,CLERMASK         SET CLEAR MASK
         LA    RINDEX14,AVTDOUBL        POINT TO 1ST SEQ IN DIGIT
         LA    RCT11,FOUR               GET MAX COUNT OF DIGITS
         CLI   AVTPARM+1,FOUR           ARE THERE FOUR OR MORE
         BNL   LOOPENT                  YES, FOUR IS RIGHT, BRANCH
         SPACE
         IC    RCT11,AVTPARM+1          NO, PICK UP NO. OF DIGITS
         B     LOOPENT                  ENTER LOOP
         SPACE
LOOP     EQU   *
         MH    RNUM15,H10               MULTIPLY NUMBER BY TEN
         LA    RINDEX14,ONE(,RINDEX14)  POINT TO NEXT DIGIT
         SPACE
LOOPENT  EQU   *
         TM    AVTEZERO(R14),NUMZONE    IS THIS A NUMBER
         BNO   SETLOW                   NOT A NUMBER, CONSIDER LOW
         SPACE
         IC    RDIG0,AVTEZERO(,RINDEX14) PICK UP DIGIT
         NR    RDIG0,RCLEAR7            CLEAR ZONE
         AR    RNUM15,RDIG0             ADD IT TO ACCUMULATED
         BCT   RCT11,LOOP               LOOP TILL ALL DIGITS GOTTEN
         SPACE
         CH    RNUM15,TRMINSEQ          TEST IF SEQ. NO. CORRECT
         BE    GOOD                     YES, BRANCH
         SPACE
         BL    SETLOW                   BFR NO. LOW, BRANCH
         SPACE
         OI    SCBERR1,SCBSEQHN         SET 'SEQ NO. HI' IN SCB
         LA    R15,AVTECD8              SET 'SEQ NO. HI' RTN CODE
         B     EXIT                     BRANCH TO RETURN         S22025
         SPACE
SETLOW   EQU   *
         OI    SCBERR1,SCBSEQLN         SET 'SEQ NO. LO' IN SCB
         LA    R15,AVTECD4              SET 'SEQ NO. LO' RTN CODE
         B     EXIT                     BRANCH TO RETURN         S22025
         SPACE
GOOD     EQU   *
         OI    SCBSTATE,SCBSEQIN        SET FLAG IN SCB STATUS
         TM    PRFSTAT1,PRFNHDRN        IS PREFIX A HDR PREFIX
         BO    UPCOUNT                  NO, CAN'T SET FIELD, BRANCH
         SPACE
         STH   RNUM15,PRFISEQ           YES, SET BFR NO. IN PREFIX
         SPACE
UPCOUNT  EQU   *
         LA    RNUM15,ONE(,RNUM15)      ADD ONE TO BFR NO.
         CH    RNUM15,H10K              MAX SEQ-IN NO. EXCEEDED
         BL    STORE                    NO, VALID AS IS
         SPACE
         LA    RNUM15,ONE               YES, RESET TO ONE
         SPACE
STORE    EQU   *
         STH   RNUM15,TRMINSEQ          SET AS NEXT EXPECTED NO.
         SR    R15,R15                  SET GOOD RETURN CODE
EXIT     EQU   *                                                 S22025
         L     RBASE12,AVTUI            GET ADDR OF RET ROUTINE  S22025
         B     FOUR(RBASE12)            RETURN TO CALLER         S22025
         SPACE 3
********* CONSTANTS *********
         SPACE
H10      DC    H'10'                    RADIX OF DECIMAL NUMBER
H10K     DC    H'10000'                 MAXIMUM SEQUENCE-IN NO.
         EJECT
********* DSECTS *********
         SPACE
         TAVTD
         EJECT
         TLCBD
         EJECT
         TPRFD
         EJECT
         TSCBD
         EJECT
         TTRMD
         SPACE
         END
