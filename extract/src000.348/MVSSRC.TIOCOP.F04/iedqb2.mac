         TITLE 'IEDQB2 -- TPDATE ROUTINE'
IEDQB2   CSECT                                                   S22025
*C THIS MODULE HAS BEEN REWRITTEN FOR TCAM LEVEL 6               Y02027
*****************************************************************S22025
*                                                                     *
* TITLE: 'IEDQB2 -- TPDATE ROUTINE'                                   *
*                                                                     *
* STATUS: CHANGE LEVEL 6                                         Y02027
*                                                                     *
* FUNCTION: THIS MODULE BUILDS A PARAMETER LIST FOR SVC 102.  THE     *
*         PROCESSING  FOR TPDATE IS HANDLED BY IGC0A10D IN THE MCP.   *
*         IGC0A10D GETS CONTROL VIA SVC 102'S TPOSTING THE OPERATOR   *
*         CONTROL QCB.                                                *
*                                                                     *
*                                                                     *
* ENTRY POINTS - IEDQB2                                               *
*                                                                     *
*                                                                     *
* INPUT - REGISTER 0 CONTAINS AN OPTION BYTE (X'04', INDICATION       *
*         THAT DELETE=YES WAS SPECIFIED, X'20',INDICATING THAT        *
*         DTSAREA WAS SPECIFIED, X'80', INDICATING THAT RECDEL=YES    *
*         WAS SPECIFIED) AND THE ADDRESS OF THE DCB.                  *
*                                                                     *
*                                                                     *
* OUTPUT: REGISTER 15 CONTAINS A RETURN CODE.                         *
*           X'00' SUCCESSFUL COMPLETION                               *
*           X'04' AVT NOT ACTIVE                                      *
*           X'08' THE DEB POINTED TO BY THE DCB IS NOT A TCAM DEB     *
*                                                                     *
*         REGISTER 1 CONTAINS THE RECORD DELIMITER                    *
*                                                                     *
* EXTERNAL REFERENCES - CVT,AVT,DEB,DCB,AMWA(ACCESS METHOD WORKAREA)  *
*                                                                     *
*                                                                     *
* EXITS,NORMAL - RETURN TO ADDRESS IN REGISTER 14.                    *
*                                                                     *
*                                                                     *
* EXITS,ERROR -  RETURN TO ADDRESS IN REGISTER 14.                    *
*                                                                     *
*                                                                     *
* TABLES/WORKAREAS - NONE                                             *
*                                                                     *
*                                                                     *
* ATTRIBUTES: SERIALLY REUSABLE, PROBLEM PROGRAM MODE                 *
*                                                                     *
*                                                                     *
* NOTES - NONE                                                        *
*                                                                     *
*                                                                     *
* CHARACTER CODE DEPENDENCY -- THE OPERATION OF THIS MODULE DOES NOT  *
*    DEPEND UPON AN INTERNAL REPRESENTATION OF THE EXTERNAL CHARACTER *
*    SET.                                                             *
*                                                                     *
***********************************************************************
         EJECT
RZERO    EQU   0                        PARAMETER REG            Y01004
RPARM1   EQU   1                        PARAMETER REGISTER
RWORK    EQU   2                        GENERAL WORK REGISTER    Y02027
RTCB     EQU   4                        TCB BASE REGISTER        Y02027
RDEB     EQU   7                        DEB BASE REGISTER        Y02027
RDCB     EQU   8                        DCB BASE REGISTER        Y02027
RBASE    EQU   12                       MODULE BASE REGISTER
RSAVE    EQU   13                       SAVEAREA BASE REGISTER
RETURN   EQU   14                       RETURN ADDRESS
RENTRY   EQU   15                       RETURN CODE REGISTER     Y02027
         SPACE 5
         USING IEDQB2,RBASE             MODULE ADDRESSABILITY    Y02027
         USING IEDQDEB,RDEB             DEB ADDRESSABILITY       Y02027
         USING IHADCB,RDCB              DCB ADDRESSABILITY       Y02027
         SPACE 3
         SAVE  (2,12)                   SAVE CALLER'S REGISTERS SA52997
         LR    RBASE,RENTRY             GET ENTRY ADDRESS
IEDQB2   IEDHJN   IDPASS                ID,DATE
         ST    RSAVE,SAVEAREA+FOUR      USER SAVE ADDRESS        Y01004
         LA    RSAVE,SAVEAREA           LINKAGE                  Y01004
         ST    RPARM1,APUSWKA           SAVE DTSAREA PTR AND     Y02027
         STCM  RZERO,EIGHT,APNAME       OPT BYTE IN PARM LIST    Y02027
         LR    RDCB,RZERO               GET DCB PTR PASSED BY    Y02027
*                                       USER                     Y02027
         LA    RDCB,ZERO(,RDCB)         CLEAR HIGH ORDER BYTE    Y02027
         SR    RENTRY,RENTRY            CLEAR REGISTER           Y02027
         L     RDEB,DCBDEBAD            GET DEB ADDRESS          Y02027
         TM    DEBTAMID,ID              TCAM DEB                 Y02027
         BNO   ERROR8                   NO, BRANCH               Y02027
         MVC   APNAME+ONE(3),DEBPEWA    SET PEWA PTR IN PARM LST Y02027
         SPACE 1
         MVI   APPCOMCD,TPDATECD        TURN ON TPDATE FLAG      Y02027
         LA    RZERO,THREE              SET FUNCTION CODE        Y02027
         LA    RPARM1,APPARM            SET PARM REG FOR SVC 102 Y02027
         AQCTL                          ISSUE SVC                Y02027
         SPACE 2
         LR    RPARM1,RENTRY            SAVE RETURN CODE REG     Y02027
         LA    RENTRY,ZERO(RENTRY)      CLEAR HIGH ORDER BYTE    Y02027
         LA    RWORK,RC80               CHECK FOR ERROR RET CD   Y02027
         CR    RWORK,RENTRY             IS TCAM ACTIVE           Y02027
         BNE   GETRECDL                 YES, BRANCH              Y02027
         LA    RENTRY,FOUR              APP PROG EXPECTS RET     Y02027
*                                       CODE  = 4                Y02027
         B     EXIT                     RETURN TO USER           Y02027
         SPACE 2
GETRECDL EQU   *                                                 Y02027
         SRL   RPARM1,TWENTY4           GET RECORD DELIMITER OR  Y02027
*                                       ZERO                     Y02027
         B     EXIT                     RETURN TO USER           Y02027
         SPACE 2
ERROR12  LA    RENTRY,FOUR              INIT RC REG FOR RC = 12  Y02027
ERROR8   LA    RENTRY,EIGHT(,RENTRY)    INIT FOR RC = 8          Y02027
         SPACE 1
EXIT     EQU   *                                                 Y02027
         L     RSAVE,SAVEAREA+FOUR      RESTORE USER'S SAVE REG  Y02027
         RETURN (2,12),T,RC=(15)        RETURN TO CALLER
         SPACE 2
         EJECT
**************************************************************** Y02027
*        C O N S T A N T S   A N D   E Q U A T E S             * Y02027
**************************************************************** Y02027
         SPACE 2
SAVEAREA DC    18F'0'                   B2'S SAVE AREA           Y02027
CVT      EQU   16                       CVT ADDRESS              Y02027
NOTCAM   EQU   8                        TCAM NOT ACTIVE CODE     Y02027
NOPEN    EQU   12                       NO OPEN DCB CODE         Y02027
ID       EQU   X'C0'                    TCAM DEB ID              Y02027
ZERO     EQU   0                        OFFSET                   Y02027
FOUR     EQU   4                        OFFSET & MOVE LENGTH     Y02027
TPDATECD EQU   X'07'                    TPDATE COMMAND CODE      Y02027
ADD      EQU   7                        ADDRESS MASK             Y02027
ONE      EQU   1                        OFFSET                   Y02027
THREE    EQU   3                        MOVE LENGTH              Y02027
FIVE     EQU   5                        OFFSET                   Y02027
EIGHT    EQU   8                        OFFSET                   Y02027
OPEN     EQU   X'10'                    MASK FOR OPENED DCB      Y02027
RC80     EQU   X'80'                    CODE RETURNED BY SVC 102 Y02027
*                                       IF TCAM NOT ACTIVE       Y02027
TWENTY4  EQU   24                       SHIFT VALUE              Y02027
TWLV     EQU   12                       OFFSET
         SPACE 2
**************************************************************** Y02027
*              MAP OF INPUT DATA PROVIDED TO SVC 102           * Y02027
**************************************************************** Y02027
         DS    0F                       FORCE ALIGNMENT          Y02027
APCIBMAP EQU   *                                                 Y02027
APPASWD  DC    AL4(0)                   POINTER TO PASSWORD      Y02027
APECB    DC    F'0'                     ECB FOR WAIT IN SVC 102  Y02027
*                                       AND SUBSEQUENT XMPOST    Y02027
APPCBAD  DC    AL4(0)                   PCB POINTER              Y02027
APPCOMCD DS    0XL1                     COMMAND VERB CODE        Y02027
APUSWKA  DC    AL4(0)                   PTR TO USER WORK AREA    Y02027
APNAME   DC    AL4(0)                   PTR TO TERMINAL NAME     Y02027
APPARM   DS    0F                       PARAMETER FOR SVC 102    Y02027
         DC    X'84'                    EOL X'80' + FUNCTION     Y02027
*                                       CODE X'04'               Y02027
         DC    AL3(APCIBMAP)            PTR TO INPUT DATA        Y02027
         SPACE 1
**************************************************************** Y02027
         EJECT
         TDEBAPD                                                 Y02027
         SPACE 2
         DCBD  DSORG=PS
         END
