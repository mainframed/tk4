E6       TITLE '''IEDQE6'' - TCAM PASSWORD SCRAMBLER'
IEDQE6   CSECT
******************** MICROFICHE FLAGS **********************  SUPT CODE
*CHANGE ACTIVITY AS FOLLOWS:
*C531000-801000,864000-873000                                  @SA74060
         SPACE 4
***********************************************************************
*                                                                     *
*TITLE: 'IEDQE9' PASSWORD SCRAMBLER                                   *
*FUNCTION: THIS ROUTINE CONVERTS AN 8-BYTE CHARACTER STRING TO A      *
*   SCRAMBLED PASSWORD.  CONVERSION IS ACCOMPLISHED VIA RANDOM        *
*   CHARACTER ARRANGEMENT AND TRANSLATION.                            *
*ENTRY POINT:                                                         *
*        IEDQE9                                                       *
*INPUT:                                                               *
*   1  - ADDRESS OF CHARACTER STRING TO BE SCRAMBLED                  *
*   13 - REGISTER SAVEAREA ADDRESS                                    *
*   14 - RETURN ADDRESS                                               *
*   15 - ENTRY POINT ADDRESS                                          *
*OUTPUT-                                                              *
*   0  - FIRST HALF OF SCRAMBLED PASSWORD                             *
*   1  - SECOND HALF OF SCRAMBLED PASSWORD                            *
*   2-15- SAME AS 'INPUT'                                             *
*EXTERNAL ROUTINES:                                                   *
*        NONE                                                         *
*EXITS-NORMAL:                                                        *
*        SEE 'OUTPUT'                                                 *
*EXITS-ERROR:                                                         *
*        NONE                                                         *
*TABLES/WORKAREAS:                                                    *
*        NONE                                                         *
*ATTRIBUTES:REENTRANT,REFRESHABLE,PROBLEM PROGRAM MODE                *
*NOTES: THE OPERATION OF THIS MODULE DEPENDS UPON AN INTERNAL         *
*   REPRESENTATION OF THE EXTERNAL CHARACTER SET WHICH IS EQUIVALENT  *
*   TO THE ONE USED AT ASSEMBLY TIME.  THE CODING HAS BEEN ARRANGED   *
*   SO THAT REDEFINITION OF 'CHARACTER' CONSTANTS, BY REASSEMBLY,     *
*   WILL RESULT IN A CORRECT MODULE FOR THE NEW DEFINITIONS.          *
***********************************************************************
         EJECT
         SPACE 2
*                                                                     *
*              S Y M B O L I C    R E G I S T E R S                   *
*                                                                     *
         SPACE 2
RZERO    EQU   0                        PARAMETER REGISTER
RPARM    EQU   1                        PARAMETER REGISTER
RNAME    EQU   2                        PASSWORD ADDRESS
RLOOP    EQU   3                        LOOP ITERATION COUNTER
RWORK    EQU   4                        SCRAMBLER WORKAREA
R5       EQU   5                        UNUSED
R6       EQU   6                        UNUSED
R7       EQU   7                        UNUSED
R8       EQU   8                        UNUSED
R9       EQU   9                        UNUSED
R10      EQU   10                       UNUSED
R11      EQU   11                       UNUSED
RBASE    EQU   12                       CSECT BASE REGISTER
RSAVE    EQU   13                       SAVEAREA ADDRESS
RETURN   EQU   14                       RETURN ADDRESS
RENTRY   EQU   15                       ENTRY POINT/COMPLETION CODE
         SPACE 2
         USING *,RENTRY                                        @SA74060
IEDQE6   IEDHJN                                                @SA74060
         DROP  RENTRY                                          @SA74060
         LR    RENTRY,RPARM             ADDRESS OF PASSWORD    @SA74060
         SR    RZERO,RZERO              CLEAR REGS             @SA74060
         SR    RPARM,RPARM               FOR PASSWORD          @SA74060
         LH    RZERO,2(RENTRY)           INSERT                @SA74060
         SLL   RZERO,NUM                 SCRAMBLED             @SA74060
         IC    RZERO,1(RENTRY)           PASSWORD              @SA74060
         SLL   RZERO,NUM                 IN                    @SA74060
         IC    RZERO,6(RENTRY)           REGISTERS             @SA74060
         IC    RPARM,7(RENTRY)           ZERO                  @SA74060
         SLL   RPARM,NUM                 AND                   @SA74060
         IC    RPARM,0(RENTRY)           ONE                   @SA74060
         SLL   RPARM,NUM                 FOR                   @SA74060
         IC    RPARM,4(RENTRY)           CALLING               @SA74060
         SLL   RPARM,NUM                 ROUTINE               @SA74060
         IC    RPARM,5(RENTRY)           USE                   @SA74060
         BALR  RENTRY,RZERO             ESTABLISH              @SA74060
         USING *,RENTRY                  ADDRESSIBILITY        @SA74060
         N     RZERO,NR                 TURN OFF 1 BITS        @SA74060
         N     RPARM,NR                 TURN OFF 1 BITS        @SA74060
         X     RZERO,XR                 FLIP BITS 4-7          @SA74060
         X     RPARM,XR                 FLIP BITS 4-7          @SA74060
         BR    RETURN                   RETURN TO CALLER
         EJECT
*                                                                     *
*                  E   Q   U   A   T   E   S                          *
*                                                                     *
         SPACE 2
ALINE    DS    0F                                              @SA74060
NR       DC    4X'BF'                   NON-BLANK MASK         @SA74060
XR       DC    4X'0F'                   BIT FLIPPER            @SA74060
NUM      EQU   8                        CHARS PER PASSWORD
         END
