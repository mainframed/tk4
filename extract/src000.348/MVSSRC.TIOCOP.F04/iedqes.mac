         TITLE '''IEDQES'' - TCAM RETRIEVE ROUTINE'
IEDQES   CSECT
*C000000,999999                                                  Y02027
*C166000                                                        SA52502
*A170000,390000,708000                                          SA52502
*D202600-203200                                                 SA52502
*A278000,601000                                                @ZA05004
*D330000                                                       @OZ09273
*A342000                                                       @OZ09273
*C378000-382000                                                @OS76979
*A390000                                                       @OZ14194
*A422000                                                       @OZ14195
***********************************************************************
* TITLE:  'IEDQES' TCAM RETRIEVE ROUTINE                              *
*                                                                     *
* FUNCTION: THIS MODULE VALIDITY CHECKS THE POINT REQUEST. IT         *
*    MAKES CHECKS FOR DISK QUEUEING, VALID TERM NAME, AND CORRECT     *
*    QUEUING. THE ECB IN THE PECB IS POSTED COMPLETE WITH RETURN      *
*    CODE AS COMPLETION CODE.                                         *
* ENTRY POINT:                                                        *
*        BEGIN - IEDQES CONTAINS A QCB-STCB AND IS ENTERED VIA THE    *
*                 DISPATCHER AS A RESULT OF IEDQEB PUTTING AN ELEMENT *
*                 ON READY QUEUE                                      *
* INPUT:                                                              *
*         1  = ELEMENT IN AIB                                         *
*         11 = DISPATCHER BASE                                        *
*         13 = AVT SAVE AREA                                          *
*         15 = ENTRY POINT                                            *
*                                                                     *
* OUTPUT  0  = RETURN CODE                                            *
*         1  = ECB ADDRESS                                            *
*                                                                     *
* EXTERNAL ROUTINES:                                                  *
*         IEDQA1                                                      *
*         POST - CROSS MEMORY                                         *
* EXITS-NORMAL:                                                       *
*         DSPDISP                                                     *
* EXITS -ERROR:                                                       *
*         RETURNS TO IEDQEB VIA XMPOST WITH COMPLETION CODE           *
*                                                                     *
*         X'08' - INVALID TERM NAME SPECIFIED                         *
*         X'0C' - NO DISK QUEUING                                     *
*                 I OR O SPECIFIED INCORRECTLY                        *
* TABLES/WORKAREAS                                                    *
*         QCB                                                         *
*         TERM                                                        *
*         PEWA                                                        *
*         AIB                                                         *
*         PCB                                                         *
*         PEB                                                         *
* ATTRIBUTES: REUSEABLE                                               *
* NOTES: THE OPERATION OF THIS MODULE DEPENDS UPON AN INTERNAL        *
*   REPRESENTATION OF THE EXTERNAL CHARACTER SET WHICH IS EQUIVALENT  *
*   TO THE ONE USED AT ASSEMBLY TIME.  THE CODING HAS BEEN ARRANGED   *
*   SO THAT REDEFINITION OF 'CHARACTER' CONSTANTS, BY REASSEMBLY,     *
*   WILL RESULT IN A CORRECT MODULE FOR THE NEW DEFINITIONS.          *
***********************************************************************
         EJECT
         SPACE 2
*                                                                     *
*   S  Y  M  B  O  L  I  C    R  E  G  I  S  T  E  R  S               *
*                                                                     *
         SPACE 2
RZERO    EQU   0                        GENERAL WORK REGISTER
RPARM    EQU   1                        PARAMETER REGISTER
RWORK    EQU   2                        GENERAL WORK REGISTER
RQCB     EQU   3                        BASE FOR QCB             Y02027
RPECB    EQU   4                        BASE FOR PECB            Y02027
RTERM    EQU   5                        BASE FOR TERM TABLE ENTRYY02027
RPEWA    EQU   6                        BASE FOR PEWA            Y02027
RTNT     EQU   7                        BASE FOR TERM NAME TABLE Y02027
RDISP    EQU   11                       ADDRESS OF DISPATCHER    Y02027
RBASE    EQU   12                       CSECT BASE REGISTER
RAVT     EQU   13                       TCAM ADDRESS VECTOR TABLEY02027
RETURN   EQU   14                       RETURN ADDRESS
RENTRY   EQU   15                       ENTRY POINT/RETURN CODE REG
         SPACE 2
IEDQES   IEDHJN
RSTCB    DC    X'06'                    DSPMCPL4 FOR STCB        Y02027
         DC    AL3(RSTCB)               STCB PTR                 Y02027
*                                                                Y02027
*              QCB FOR POINT - WORD 1 AND 2 NOT USED             Y02027
*                                                                Y02027
BEGIN    LR    RBASE,RENTRY             SET UP BASE REGISTER     Y02027
         USING BEGIN,RBASE              ESTABLISH ADDRESSABILITY Y02027
         USING IEDQDISP,RDISP           DISPATCHER ADDRESSABILITYY02027
         USING IEDQQCB,RQCB             QCB ADDRESSABILITY       Y02027
         USING IEDQTRM,RTERM            TERMINAL TABLE ENTRY     Y02027
         USING IEDQTNTD,RTNT            TERM NAME TABLE          Y02027
         USING AVTSAVE2,RAVT            AVT ADDRESSABILITY       Y02027
         USING IEDQPEWA,RPEWA           PEWA ADDRESSABILITY      Y02027
         EJECT
         SH    RPARM,AVTHA16            BACK UP FROM RCB TO      Y02027
*                                       BEGINNING OF AIB         Y02027
         USING IEDQAIB,RPARM            AIB ADDRESSABILITY       Y02027
         LA    RZERO,QERR               SET POSSIBLE ERROR CODE  Y02027
         TM    AVTBIT1,AVTDISKN         DISK QUEUEING SUPPORTED  Y02027
         BNO   XMPOST                   BRANCH IF NO TO EXIT     Y02027
*                                                                Y02027
*              CHECK FOR VALID TERM NAME                         Y02027
*                                                                Y02027
         LR    RWORK,RPARM              SAVE AIB ADDRESS THRU    Y02027
*                                       BRANCH TO BINARY SEARCH  Y02027
         LA    RPARM,AIBSTANM           LOAD ADDR OF TERMNAME    Y02027
*                                       IN REG 1                 Y02027
         LA    RZERO,EIGHT              LOAD MAXIMUM SIZE        Y02027
         LA    RENTRY,ADDR(RPARM)       BUMP TO END            @ZA05004
NAMELOOP EQU   *                                               @ZA05004
         CLI   ZERO(RENTRY),BLANK       END OF NAME            @ZA05004
         BNE   NAMEEND                  BRANCH IF YES          @ZA05004
         BCTR  RENTRY,0                 DECREMENT ADDRESS      @ZA05004
         BCT   RZERO,NAMELOOP           SEARCH AGAIN           @ZA05004
         LA    RZERO,EIGHT              SET RETURN CODE IF NO  @ZA05004
*                                        NAME EXISTS           @ZA05004
         B     XMPOST                   EXIT                   @ZA05004
NAMEEND  EQU   *                                               @ZA05004
         L     RENTRY,AVTMSGS-ONE       GET VCON TABLE ADDR      Y02027
         L     RENTRY,EIGHT(RENTRY)     LOAD ADDR OF IEDQA1      Y02027
         BAL   RETURN,FOUR(RENTRY)      BRANCH TO SEARCH ROUTINE Y02027
*                                       AT SECOND ENTRY POINT    Y02027
         LR    RPARM,RWORK              RESTORE AIB ADDRESS      Y02027
         LTR   RENTRY,RENTRY            VALID TERMNAME           Y02027
         BNZ   NAMEOK                   BRANCH IF YES            Y02027
         LA    RZERO,INVTERM            SET ERROR CODE           Y02027
         B     XMPOST                   BRANCH TO EXIT           Y02027
NAMEOK   EQU   *                                                 Y02027
         L     RPEWA,AIBRPEWA           GET PEWA ADDRESS         Y02027
         LR    RPARM,RENTRY             TERMNAME TABLE OFFSET    Y02027
         STH   RENTRY,PEWARTVE          TNT OFFSET FOR DEST      Y02027
         L     RENTRY,AVTRNMPT          OFFSET CONVERSION LOGIC  Y02027
         BALR  RETURN,RENTRY            GET ADDR OF TERM ENTRY   Y02027
         LA    RZERO,QERR               SET POSSIBLE ERROR CODE@OZ09273
         ST    RPARM,PEWARTVE+FOUR      AND STORE IN PEWA        Y02027
         LR    RTERM,RPARM              TT BASE                  Y02027
         LR    RPARM,RWORK              RESTORE AIB BASE         Y02027
         MVC   PEWARTVE+TWO(3),AIBINOUT MOVE TYPE AND SEQ. NO.   Y02027
*                                                                Y02027
*              TEST FOR CORRECT QUEUEING                         Y02027
*                                                                Y02027
         L     RQCB,TRMDESTQ-ONE        GET QCB ADDRESS          Y02027
         TM    TRMSTATE,TRMLIST         IS THIS A LIST         @OS76979
         BNZ   XMPOST                   BR IF LIST  RC='0C'    @OS76979
         TM    TRMSTATE,TRMPROC         SINGLE OR PROCESS ENTRY  Y02027
         BNO   DISKCK                   BR IF SINGLE           @OZ14194
         CLI   QCBDSFLG,QCBFQCB         PUT PROCESS ENTRY        Y02027
         BNE   PROCGET                  BRANCH IF NO             Y02027
         CLI   AIBINOUT,AIBIN           INPUT  SPECIFIED         Y02027
         BNE   XMPOST                   BRANCH IF NO RC=X'0C'    Y02027
         B     GOODOUT                  BRANCH IF YES            Y02027
PROCGET  EQU   *                                                 Y02027
         CLI   AIBINOUT,AIBOUT          OUTPUT SPECIFIED         Y02027
         BNE   XMPOST                   BRANCH IF NO RC=X'0C'    Y02027
DISKCK   EQU   *                                               @OZ14194
         CLI   AIBINOUT,AIBIN           INPUT SPEC             @OZ14195
         BE    GOODOUT                  YES, DON'T CHECK       @OZ14195
*                                       QUEUING IF INPUT SPEC  @OZ14195
         TM    QCBDSFLG,QCBDISK         CORE QUEUE ONLY          Y02027
         BZ    XMPOST                   BRANCH IF YES RC=X'0C'   Y02027
GOODOUT  EQU   *                                                 Y02027
         OI    PEWAOPTC,RTVFLG          TURN ON RETRIEVE MODE    Y02027
         SR    RZERO,RZERO              SET GOOD RETURN CODE     Y02027
*                                                                Y02027
*              CROSS MEMORY POST ECB TO RETURN TO                Y02027
*              POINT FUNCTION IN IEDQEB                          Y02027
*                                                                Y02027
XMPOST   EQU   *                                                 Y02027
         MVC   AIBCMPTR(4),AIBECBA      GET ECB ADDRESS          Y02027
         L     RTNT,AIBPCBAD            GET PCB ADDRESS          Y02027
         L     RTNT,PCBPEBAD-IEDQPCB(RTNT) GET PEB ADDRESS       Y02027
         LTR   RTNT,RTNT                TEST IF AP IS ACTIVE     Y02027
         BZ    DSPDISP                  BRANCH IF NO TO AVOID    Y02027
*                                       XMPOST                   Y02027
         L     RTNT,PEBASCB-IEDQPEB(RTNT) GET ASCB ADDRESS       Y02027
         LTR   RTNT,RTNT                TEST IF FLAG HAS         Y02027
*                                       INVALIDATED ASCB ADDR    Y02027
         BM    DSPDISP                  BRANCH IF NEGATIVE FLAG  Y02027
*                                       ON - AVOID XMPOST        Y02027
         ST    RTNT,AIBCMPTR+FOUR       ASCB AND STORE IN LIST   Y02027
         L     RTNT,CVTPTR              ADDRESS OF CVT           Y02027
         LA    RTNT,CVTBRET-CVTMAP(RTNT)  GET ERROR ADDRESS      Y02027
         ST    RTNT,AIBCMPTR+EIGHT      AND STORE IN LIST        Y02027
         LA    RPARM,AIBCMPTR           GET LIST ADDRESS         Y02027
         POST  ,(0),MF=(E,(1))
         B     DSPDISP                  BRANCH TO DISPATCHER     Y02027
FOUR     EQU   4                        REG 13 OFFSET            Y01004
ZERO     EQU   0                        ZERO DISP                Y01004
         EJECT
*                         E Q U A T E S                          Y02027
*                                                                Y02027
ONE      EQU   1                        DISPLACEMENT             Y02027
TWO      EQU   2                        DISPLACEMENT             Y02027
EIGHT    EQU   8                        DISPLACEMENT AND SIZE    Y02027
INVTERM  EQU   8                        INVALID TERMNAME         Y02027
QERR     EQU   X'0C'                    INVALID QUEUEING         Y02027
ADDR     EQU   7                        DISPLACEMENT           @ZA05004
BLANK    EQU   C' '                     BLANK TRMNAME CHARACTER@ZA05004
         EJECT
*                             D S E C T S                        Y02027
CVTMAP   CVT   DSECT=YES
         TQCBD                                                   Y02027
         TTNTD                                                   Y02027
         TAVTD                                                   Y02027
         TPEWAD                                                  Y02027
         TAIBD EXT=(POINT)                                       Y02027
         TPEBD                                                   Y02027
         TPCBD                                                   Y02027
         TTRMD                                                   Y02027
         TDISPD                                                  Y02027
         END
