HG   TITLE '''IEDQHG'' - TIME DELAY SUBTASK'
IEDQHG   CSECT
         SPACE 3
*  CHANGE ACTIVITY AS FOLLOWS
******************** MICROFICHE FLAGS *********************** SUPT CODE
*C226000                                                         A41021
*D339200-339800                                                  A41021
*C226000,421000                                                  S22025
*C225000                                                         S22025
*C225000                                                        S21903
*C410400-410600                                                 SA62950
*A248000                                                       @SA70674
*C248700                                                       @SA71082
*C246000-248000                                                @ZA03725
*A440400                                                       @ZA03725
*A275200,410600,588000                                         @XA11307
*D 410710-410870                                               @Y17XADZ
*D 402240                                                      @Y17XADZ
*C 402040 402080 402280 402480                                 @Y17XADZ
*A380000                                                       @OZ29220
         SPACE 3
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                     *
*TITLE   'IEDQHG' - TIME DELAY SUBTASK                                *
*                                                                     *
*  MODULE NAME = IEDQHG                                               *
*                                                                     *
*  DESCRIPTIVE NAME = TIME DELAY SUBTASK                              *
*                                                                     *
*  COPYRIGHT = 'NONE'                                                 *
*                                                                     *
*  STATUS -- CHANGE LEVEL 7                                      X03039
*                                                                     *
*FUNCTION -- THIS SUBTASK RECEIVES ELEMENTS, EACH REQUESTING          *
*   NOTIFICATION UPON COMPLETION OF A SPECIFIED TIME INTERVAL.        *
*   THESE ELEMENTS ARE MAINTAINED ON A QUEUE FROM WHICH THEY MAY      *
*   BE REMOVED UPON A SUBSEQUENT REQUEST.                             *
*                                                                     *
*ENTRY POINTS -- THERE ARE FOUR ENTRY POINTS -                        *
*        'IEDQHG' - THE SUBTASK IS ACTIVATED WHEN AN ELEMENT IS       *
*   POSTED TO THE TIME DELAY QCB, ORDERS THEM ON THE TIME DELAY       *
*   QUEUE, AND ISSUES STIMER MACRO TO REQUEST INTERRUPT WHEN THE      *
*   TIME INTERVAL ON THE FIRST ELEMENT HAS ELAPSED.  UPON RECEIVING   *
*   THE TIME ELAPSED INTERRUPT, THIS ROUTINE IS ALSO POSTED BY THE    *
*   STIMER EXIT ROUTINE TO REMOVE THE ELAPSED ELEMENT FROM            *
*   THE TIME QUEUE, POST THE ELEMENT BACK TO THE INDICATED QCB TO     *
*   SIGNIFY TO THE REQUESTOR THAT THE INTERVAL HAS ELAPSED, AND       *
*   ISSUES STIMER ON THE NEXT ELEMENT ON THE TIME DELAY QUEUE.  THIS  *
*   ENTRY IS USED BY ATTACHED TASKS.                                  *
*        'IEDQHG01' - TCAM SUBTASKS MAY ALSO HAVE ELEMENTS PUT ON     *
*   THE TIME DELAY QUEUE BY MEANS OF A 'BALR' TO THIS ENTRY.          *
*        'IEDQHG02' - TCAM SUBTASKS MAY REQUEST THE REMOVAL OF AN     *
*   ELEMENT BY MEANS OF A 'BALR' TO THIS ENTRY.                       *
*        'IEDQHG03' - ATTACHED TASKS MAY REQUEST THE REMOVAL OF AN    *
*   ELEMENT BY POSTING ANOTHER ELEMENT TO THIS SUBTASK.               *
*                                                                     *
*INPUT -- THE INPUT VARIES ACCORDING TO THE ENTRY POINT -             *
*   'IEDQHG' - A TIME DELAY REQUEST ELEMENT IS POSTED TO THE TIME     *
*   DELAY SUBTASK QCB.  THE REQUEST ELEMENT IS FORMATTED AS -         *
*                                                                     *
*        BYTE 0 - ADDRESS OF TIME DELAY QCB IN AVT          4 BYTES   *
*        BYTE 4 - PRIORITY                                  1 BYTE    *
*        BYTE 5 - LINK FIELD, USED BY DISPATCHER            3 BYTES   *
*        BYTE 8 - UNDEFINED                                 8 BYTES   *
*        BYTE 16- NUMBER OF SECONDS OF TIME INTERVAL WANTED 2 BYTES   *
*        BYTE 18- OFFSET INTO ELEMENT OF WORD WHOSE LOW               *
*          ORDER 3 BYTES IS THE ADDRESS OF THE QCB TO WHICH           *
*          THIS ELEMENT IS TO BE POSTED UPON COMPLETION OF            *
*          THE TIME DELAY INTERVAL.                         1 BYTE    *
*            X'00' - TIME DELAY REQUEST ELEMENT IS A QCB              *
*            X'14' - TIME DELAY REQUEST ELEMENT IS AN LCB             *
*            X'08' - OTHERS (SPECIAL ELEMENTS, BUFFERS, CHECKPOINT)   *
*        BYTE 19-UNDEFINED                                  4 BYTES   *
*        BYTE 23-FLAG BYTE, ONLY ONE BIT DEFINED, X'02'     1 BYTE    *
*            BIT ON- ELEMENT IS IN TIME DELAY Q                       *
*            BIT OFF- ELEMENT IS NOT IN QUEUE                         *
*          THIS BIT IS FLIPPED FOR ALL ELEMENTS ON TIME DELAY Q,      *
*          HOWEVER, IT MAY BE CHECKED FOR IN ONLY THE LCB OR QCB      *
*          AND CHECKPOINT WHERE THE BIT IS DEDICATED TO THIS          *
*          PURPOSE.  IN OTHER ELEMENTS, THIS FIELD HAS OTHER          *
*          DEFINITIONS.  USERS OF THESE OTHER ELEMENTS MUST TAKE      *
*          PRECAUTIONS TO PRESERVE THE STATUS OF THIS BIT IF          *
*          SUCH INFORMATION IS NEEDED TO BE KEPT AND THEN RESTORED    *
*          WHEN THE ELEMENT LEAVES THE TIME DELAY QUEUE.              *
*        BYTE 24- UNDEFINED                                 5 BYTES   *
*        BYTE 29- LINK FIELD TO CHAIN ELEMENTS ON TIME DELAY          *
*          QUEUE                                            3 BYTES   *
*   THIS ELEMENT IS ALSO TO HAVE A 3 BYTE ADDRESS OF THE QCB TO       *
*   WHICH THIS ELEMENT IS TO BE RETURNED UPON COMPLETION OF THE TIME  *
*   INTERVAL.  THE WORD CONTAINING THIS QCB ADDRESS IS LOCATED        *
*   ACCORDING TO TH SET VALUE OF BYTE 18, THE OFFSET, WHICH IS        *
*   UNIQUE DEPENDING ON THE TYPE OF ELEMENT BEING SENT TO TIME DELAY. *
*                                                                     *
*   NOTE THE PRESENCE OF TWO LINK FIELDS - DISPATCHER'S AND TIME      *
*   DELAY'S.  ONCE AN ELEMENT IS ON THE TIME DELAY Q, IT MAY ALSO     *
*   BE POSTED TO ANOTHER SUBTASK.                                     *
*                                                                     *
*   'IEDQHG01' - CALLED BY MEANS OF 'BALR' FROM TCAM SUBTASKS.        *
*   R14 - RETURN ADDRESS                                              *
*   R13 - ANY STANDARD SAVEAREA                                       *
*   R1  - THE ADDRESS OF A TIME DELAY REQUEST ELEMENT (DESCRIBED      *
*        ABOVE--FIRST TWO WORDS NOT NEEDED) TO BE ADDED TO TIME Q     *
*        ELEMENT NEEDS -                                              *
*          NUMBER OF SECONDS OF INTERVAL                              *
*          ADDRESS OF QCB TO BE POSTED WHEN INTERVAL HAS ELAPSED      *
*          OFFSET TO QCB ADDRESS WORD                                 *
*   R15 - ENTRY POINT OF 'IEDQHG01', WHOSE VCON IS IN AVT             *
*                                                                     *
*   TO REMOVE AN ELEMENT FROM THE TIME DELAY QUEUE WITHOUT WAITING    *
*   FOR THE INDICATED TIME INTERVAL, THERE ARE TWO ENTRY POINTS.      *
*                                                                     *
*   IF THE INDICATED ELEMENT IS NOT ON THE TIME QUEUE, NO ACTION.     *
*                                                                     *
*   'IEDQHG02' - CALLED BY MEANS OF A 'BALR' FROM TCAM SUBTASKS.      *
*   R14 - RETURN ADDRESS                                              *
*   R13 - ANY STANDARD SAVEAREA                                       *
*   R1  - ADDRESS OF THE ELEMENT TO BE REMOVED FROM TIME DELAY Q      *
*   R15 - ENTRY POINT OF 'IEDQHG02', WHOSE VCON IS IN AVT             *
*                                                                     *
*   'IEDQHG03' - A SUBTASK USED BY ATTACHED TASKS TO REMOVE AN        *
*   ELEMENT FROM THE TIME DELAY QUEUE.  A SPECIAL 'DELETE FROM TIME   *
*   Q ELEMENT' IS POSTED TO THE REMOVE ELEMENT FROM TIME Q QCB.       *
*                                                                     *
*   FORMAT OF 'DELETE FROM TIME QUEUE ELEMENT'                        *
*        BYTE 0 - UNDEFINED                                 1 BYTE    *
*        BYTE 1 - ADDRESS OF DELETE FROM TIME Q QCB (IN AVT)3BYTES    *
*        BYTE 4 - PRIORITY                                  1 BYTE    *
*        BYTE 5 - LINK, USED BY DISPATCHER                  3 BYTES   *
*        BYTE 8 - UNDEFINED                                 1 BYTE    *
*        BYTE 9 - ADDRESS OF ELEMENT TO BE REMOVED FROM     3 BYTES   *
*          TIME DELAY QUEUE, IF ELEMENT IS ON THAT Q                  *
*        BYTE 12- UNDEFINED                                 1 BYTE    *
*        BYTE 13- ADDRESS OF QCB TO WHOM THIS DELETE ELEMENT          *
*          IS TO BE POSTED AFTER REQUEST IS SERVICED        3 BYTES   *
*   (WHEN ELE IS AN LCB, THIS ADDRESS IS NOT NEEDED. SINCE IT IS      *
*   UNDERSTOOD AN LCB IS ALWAYS POSTED TO ITSELF.)                    *
*                                                                     *
*OUTPUT -- WHEN THE INDICATED TIME INTERVAL HAS ELAPSED, THE          *
*   TIME DELAY REQUEST ELEMENT IS POSTED TO THE QCB WHOSE ADDRESS     *
*   IS POINTED TO BY THE OFFSET BYTE OF THE ELEMENT.                  *
*                                                                     *
*   WHEN AN ELEMENT IS REQUESTED TO BE REMOVED FROM THE TIME QUEUE,   *
*   IT IS SEARCHED FOR ON THE QUEUE, AND IF FOUND, REMOVED.  IF       *
*   REQUEST WAS MADE TO 'IEDQHG03', THE DELETE REQUESTING ELEMENT     *
*   IS POSTED BACK TO THE INDICATED QCB.  IF 'IEDQHG02' WAS USED,     *
*   RETURN IS MADE TO ADDRESS IN REGISTER 14.                         *
*                                                                     *
*   WHEN EITHER OF THE 'BALR' ENTRIES ARE USED, ALL CALLER'S          *
*   REGISTERS ARE PRESERVED, USING SAVEAREA IN REGISTER 13.           *
*                                                                     *
*EXTERNAL ROUTINES -- SUPERVISOR CALLS ARE MADE WITH THREE MACROS -   *
*   'STIMER' - SVC 47, REQUEST TIME DELAY                             *
*   'TIME' - SVC 11, GET CURRENT TIME OF DAY                          *
*   'AQCTL' - SVC 102, TPOST TIME QCB TO ITSELF WHEN TIME ELAPSES     *
*                                                                     *
*EXITS-NORMAL -- BOTH SUBTASKS EXIT TO THE DISPATCHER, BOTH 'BALR'    *
*   ROUTINES RETURN TO THE ADDRESS IN R14, ALL REGISTERS INTACT.      *
*   NO RETURN CODE IS PASSED IN R15.                                  *
*                                                                     *
*EXITS-ERROR -- N/A                                                   *
*                                                                     *
*TABLES/WORK AREAS -- THE AVT HAS BOTH TIME DELAY QCBS, ENQUEUE       *
*   AND DELETE FUNCTIONS, THE AQCTL POST ELEMENT.  BOTH TIME DELAY    *
*   REQUEST AND DELETE REQUEST ELEMENTS MUST NOT BE STORAGE           *
*   PROTECTED FROM TCAM SUBTASKS.                                     *
*                                                                     *
*ATTRIBUTES -- NON-REFRESHABLE, REUSABLE, RESIDENT, PROBLEM           *
*   PROGRAM MODE.                                                     *
*                                                                     *
*NOTES -- THE 'STIMER' MACRO REQUIRES A USER EXIT TO RECEIVE          *
*   CONTROL UPON COMPLETION OF THE REQUESTED TIME INTERVAL.  THIS     *
*   ROUTINE IS ALSO PART OF THIS MODULE.  (SEE TAG 'TIMEEXIT').       *
*   THIS ROUTINE GETS CONTROL AS A RESULT OF A TIME INTERVAL INTER-   *
*   RUPT, USES AQCTL (SVC 102) TO POST THE TIME DELAY QCB TO ITSELF,  *
*   THEN RETURNS TO THE SUPERVISOR.  WHEN TIME DELAY SUBTASK RECEIVES *
*   ITS OWN QCB FROM THE DISPATCHER, HE KNOWS THIS IS NOT A REQUEST   *
*   TO ADD AN ELEMENT TO THE TIME QUEUE, BUT CHECKS THE QUEUE FOR     *
*   ELEMENTS WHOSE TIME HAS ELAPSED, IF ANY.                          *
*                                                                     *
*   MACROS USED IN THIS MODULE -                                      *
*        STIMER - SET INTERVAL TIMER                                  *
*        AQCTL - SVC 102, TPOST ELEMENT TO DISABLED READY QUEUE       *
*        RETURN - RESTORE REGISTERS AND EXIT TO CALLER                *
*        TIME - GET TIME OF DAY NOW                                   *
*   REMAINING MACROS DEFINE TCAM DSECTS REFERENCED BY THIS MODULE.    *
*        TDISPD                                                       *
*        TTCBD                                                        *
*        TLCBD                                                        *
*        TQCBD                                                        *
*        TAVTD                                                        *
*                                                                     *
*   THIS MODULE IS OPTIONAL.  A 12 BYTE DUMMY 'IEDQHG' IS             *
*   GENERATED BY INTRO MACRO IF USER CODES 'FEATURE=(,,NOTIMER)'.     *
*                                                                     *
*   THE OPERATION OF THIS MODULE DOES NOT DEPEND UPON A PARTICULAR    *
*   INTERNAL REPRESENTATION OF THE EXTERNAL CHARACTER SET.            *
*                                                                     *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         EJECT
*
R15      EQU   15                       ENTRY ADDRESS, TEMPORARY BASE
R14      EQU   14                       RETURN ADDRESS
R13      EQU   13                       SAVEAREA ADDRESS
RAVT     EQU   13                       AVT DSECT BASE
R12      EQU   12
RBASE    EQU   12                       PROGRAM BASE
R11      EQU   11                       DISPATCHER ENTRY ADDRESS
R10      EQU   10
REXIT    EQU   10                       SWITCH DETERMINES TYPE OF EXIT
*                                         A(DSPDISP)  - DISPATCH
*                                         A(RETURN)   - BR 14
*                                         A(POSTEXIT) - POST
R9       EQU   9                        UNUSED
R8       EQU   8                        HOLDS CALLER'S SAVEAREA ADDR
R7       EQU   7                        TIME INTERVAL - ODD
R6       EQU   6                        TIME INTERVAL - EVEN
RSPEC    EQU   6                        BASE OF SPECIAL ELEMENT
*                                         REQUESTING REMOVAL OF ELE
R5       EQU   5
RTIME    EQU   5                        TIME OF DAY
R4       EQU   4
RELE     EQU   4                        TIME DELAY ELEMENT
R3       EQU   3                        TIME DELAY ELEMENT ON Q
R2       EQU   2                        TEMP REG
R1       EQU   1                        INPUT PARAMETER
R0       EQU   0                        EVEN REG FOR MULT OR DIVIDE
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
         USING IEDQTCB,R2
         USING IEDQDISP,R11
         USING IEDQAVTD,RAVT
         USING SPECELE,RSPEC
         USING ELEMENT,RELE
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
*                       ENTRY IEDQHG
*
* SUBTASK PUTS ENTRY ON TIME DELAY QUEUE AND REQUEST TIME INTERRUPT
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
         DC    AL1(DSPMCPL2),AL1(0)     IEDQHG STCB
*
         USING *,R15                    TEMPORARY BASE
         BAL   RBASE,TAG                SKIP CONSTANTS AND
*                                         SET PROGRAM BASE
         DROP  R15                      FORGET TEMPORARY
         USING *,RBASE                  ESTABLISH ADDRESSABILITY
BASE     EQU   *
IEDQHG   IEDHJN
TAG      DS    0H
         BAL   REXIT,SET                PRESET REGS
*
         LA    REXIT,DSPDISP            REQUEST EXIT TO DISPATCHER
*
* R1 HAS ADDRESS OF ELEMENT TO BE ADDED TO TIME DELAY QUEUE
*
ADDNEW   EQU   *
         LA    RELE,0(,R1)              CLEAR HIGH BYTE
         LR    R15,RELE
         TIME  BIN                      GET TIME OF DAY INTO R0
*
         LR    R1,R0                    SHIFT TIME TO ODD REG
         M     R0,UNITSDAY              MULTIPLY BY UNITS/DAY
         D     R0,SECSX100              DIVIDE BY SEC/DAY * 100
         LR    RTIME,R1                 PRESERVE TIME OF DAY
         C     RELE,AVTDELYB            IS NEW ELE THE TIME DELAY
*                                         QCB ITSELF
         BNE   CKPOST                   NO, ADD ELE TO TIME Q  @ZA03725
         MVI   ELEPRI,AVTEZERO          ZERO PRIORITY          @ZA03725
         B     STIMER                   A TIMER INTERRUPT      @ZA03725
*                                       HAS OCCURRED           @ZA03725
         SPACE 2                                               @ZA03725
CKPOST   EQU   *                                               @ZA03725
         LA    R7,AVTCKELE              GET CHECKPOINT QCB ADDR@SA70674
         CR    R7,RELE                  IS CKPT BEING POSTED   @SA70674
         BNE   NOTCKP                   BRANCH IF NOT          @SA70674
         NI    AVTCKELF,AVTEFF-AVTCPIPN TURN OFF CHECKPOINT    @SA70674
*                                        IN PROGRESS           @SA70674
         LR    R14,REXIT                SET RETURN ADDRESS     @SA70674
         MVC   ELEPOST+1(3),ELESPEQ+1   SET POST ADDRESS       @SA70674
         TM    AVTCKELF,AVTCRTLN        HAS ANOTHER CHECKPOINT @SA70674
*                                        BEEN REQUESTED        @SA70674
         BO    POSTSUB                  YES,POST REQUEST FOR   @SA71082
*                                        A NEW CHECKPOINT      @SA70674
NOTCKP   EQU   *                                               @SA70674
         CLI   ELEINDEX,AVTEZERO        QCB BEING INSERTED     @YM09057
         BNE   GETINTVL                 NO, BRANCH             @YM09057
         MVC   ELEPRI+1(3),ELELINK      SAVE TSO INFORMATION   @YM09057
GETINTVL EQU   *                                               @YM09057
         LH    R7,ELETIME               GET NUMBER OF SECONDS OF TIME
*                                         INTERVAL FROM NEW ELEMENT
*                                         INTO ODD REG
         N     R7,AVTCLRHI              FORCE POSITIVE
         M     R6,UNITSDAY              MULTIPLY SEC * UNITS/DAY
         D     R6,SECPRDAY              DIVIDE BY SEC/DAY TO GET UNITS
         LTR   R6,R7                    IS INTERVAL ZERO
         BNZ   TIME                       NO, HAVE NON-ZERO INTERVAL
*                                         YES, INTERVAL IS ZERO
         LA    R6,DEFAULT               FORCE INTERVAL TO POSITIVE
TIME     EQU   *
*                                       TIME OF DAY IS IN R1
         STH   RTIME,AVTDELYB+(ELETIME-ELEMENT) SAVE TIME OF DAY IN
*                                         TIME QCB
         XI    AVTDELYB+(ELETIME-ELEMENT),AVTE80 ESTABLISH REFERENCE
*                                         TIME 6 HOURS OFF CURRENT
*                                         TIME OF DAY
         AR    R6,RTIME                 ADD TIME OF DAY TO INTERVAL
*                                         TO GET TIME OF INTERRUPT
         STH   R6,ELETIME               PUT INTERRUPT TIME BACK
*                                         INTO NEW ELEMENT
         CLI   ELEINDEX,ELELCB          IS ELE AN LCB
         BNE   NOTLCB                     NO, NOT AN LCB
*                                         YES, IT IS AN LCB
         CLI   LCBRSKEY-IEDQLCB(RELE),DSPBUFSC IS LCB FOR BUFFERED
         BE    NOTLCB                   YES, SKIP POST BUF LCB
*                                         NO, NOT BUFFERED
         CLI   LCBPRI-IEDQLCB(RELE),PUTDELAY  PUT DELAY LCB    @XA11307
         BE    NOTLCB                   YES, SKIP POST LCB     @XA11307
*                                        NO, NOT PUT DELAY     @XA11307
LEASED   EQU   *
         ST    RELE,ELEPOST             AN LCB IS TO BE POSTED
*                                         AS SOON AS RECEIVED BY
*                                         TIME DELAY
         BAL   R14,POSTSUB              POST LCB TO ITSELF
NOTLCB   EQU   *
         LA    R3,AVTDELYB              GET ADDRESS OF TIME QCB
*
TIMELOOP EQU   *
         LR    R6,R3                    R6 HAS ADDR OF EITHER TIME
*                                         QCB (IF FIRST PASS), OR
*                                         PREVIOUS ELE ON TIME Q
*                                       TIME QCB LOOKS LIKE TIME ELE
         L     R3,ELELINK-1-ELEMENT(R6) GET ADDRESS OF NEXT ENTRY
         CLC   ELETIME,AVTDELYB+(ELETIME-ELEMENT) COMPARE CURRENT
*                                         ENTRY WITH REFERENCE TIME
         BNH   TIMESKIP                 BRANCH IF IN LATTER PORTION
*                                       CONTINUE IF IN EARLY PORTION
         CLC   ELETIME-ELEMENT(,R3),AVTDELYB+(ELETIME-ELEMENT)
*                                       COMPARE NEXT ENTRY ON QUEUE
*                                         WITH REFERENCE TIME
         BNH   TIMEINS                  BRANCH IF IN LATTER PORTION
*                                       CONTINUE IF IN EARLY PORTION
         B     TIMEADD                  BOTH NEW ELE AND OLD ELE ON
*                                         Q ARE IN EARLY PORTION
TIMESKIP EQU   *                        NEW ELE IS IN LATTER PORTION
         CLC   ELETIME-ELEMENT(,R3),AVTDELYB+(ELETIME-ELEMENT)
*                                       COMPARE NEXT ENTRY IN Q WITH
*                                         REFERENCE TIME
         BH    TIMELOOP                 BRANCH IF ENTRY IS IN EARLIER
*                                         PORTION, CONTINUE IF IT IS
*                                         IN LATTER PORTION ALSO
TIMEADD  EQU   *                        BOTH ENTRIES ARE IN SAME
*                                         PORTION
         CLC   ELETIME,ELETIME-ELEMENT(R3) COMPARE CURRENT REQUEST
*                                         TO ENTRY IN Q
         BH    TIMELOOP                 BRANCH IF CURRENT REQUEST IS
*                                         LATER, CONTINUE IF CURRENT
*                                         REQUEST IS EARLIER
TIMEINS  EQU   *
         MVC   ELELINK,ELELINK-ELEMENT(R6) INSERT NEW ELEMENT AFTER
*                                         LAST ENTRY IN TIME QUEUE
         IC    R3,ELELINK-1-ELEMENT(R6) SAVE HIGH BYTE
         ST    RELE,ELELINK-1-ELEMENT(R6) PUT ON NEW ELEMENT
         STC   R3,ELELINK-1-ELEMENT(R6) RESTORE HIGH BYTE
         OI    ELEBYTE,ELEBIT           MARK NEW ELE AS BEING ON
*                                         TIME DELAY Q
STIMER   EQU   *
         L     RELE,AVTDELYB+(ELELINK-1-ELEMENT) GET ADDR OF FIRST
*                                         ELEMENT ON CHAIN
         CLC   AVTDELYB+1(3),AVTDELYB+(ELELINK-ELEMENT) IS TIME QCB
*                                         ONLY ELE ON TIME Q
         BCR   EQUAL,REXIT                YES, QUIT
*                                         NO, DO STIMER
         LH    R6,ELETIME               GET INTERRUPT TIME FOR FIRST
*                                         ELEMENT ON TIME Q
         N     R6,AVTCLRHI              FORCE POSITIVE
         SR    R6,RTIME                 SUBTRACT CURRENT TIME OF DAY
*                                         FROM TIME OF INTERRUPT
         SLL   R6,HALFWORD              SHIFT LOWER HALF TO UPPER HALF
         SRA   R6,HALFWORD              EXTEND SIGN BIT
         BC    ZERORNEG,RESUME          BRANCH IF TIME HAS ALREADY
*                                         ELAPSED FOR FIRST ELEMENT
*                                       CONTINUE IF TIME INTERVAL
*                                         HAS NOT ELAPSED AND DO STIMER
         LR    R7,R6                    MOVE INTERVAL TO ODD REG
         M     R6,SECSX100              MULTIPLY BY SECS/DAY * 100
         D     R6,UNITSDAY              DIVIDE BY UNITS/DAY
         ST    R7,AVTDELYB+12           SET INTERVAL INTO TIME QCB
         LA    R1,AVTDELYB+12             FOR STIMER
         LR    R15,RELE
         STIMER REAL,TIMEEXIT,BINTVL=(1) REQUEST TIMER INTERRUPT       *
                                          AT END OF TIME INTERVAL
*
         ST    RELE,STIMADDR            REMEMBER WHAT ELE GOT STIMER
         BR    REXIT                    END OF ROUTINE
*
RESUME   EQU   *                        TIME HAS ELAPSED FOR FIRST
*                                         ELEMENT ON TIME Q
         MVI   STIMADDR,AVTEFF          MARK TO INDICATE NO STIMER
*                                         STILL ACTIVE
         MVC   AVTDELYB+ELELINK-ELEMENT(3),ELELINK DELINK 1ST ELE
*                                         FROM TIME DELAY QCB
         CLI   ELEINDEX,AVTEZERO        IS ELEMENT A QCB
         BE    QCB                        YES, ELE IS A QCB
*                                         NO, NOT A QCB
         NI    ELEBYTE,X'FF'-ELEBIT     TURN OFF BIT-MARK ELE AS
*                                         NOT BEING ON TIME Q
         CLI   ELEINDEX,ELELCB          IS ELEMENT AN LCB
         BE    LCB                        YES, GOT LCB
*                                         NO, ELE IS BUF OR SPECIAL
         MVC   ELEPOST+1(3),ELESPEQ+1   THIRD WORD OF BUF (OR SPECIAL
*                                         ELE) HAS QCB ADDR TO
*                                         RECEIVE ELE AT END OF TIME
         LA    R1,0(,RELE)              CLEAR HIGH BYTE OF ELE ADDR
         LA    R15,AVTCKELE             GET CKPOINT ELE ADDR
         CR    R1,R15                   IS CKPT BEING POSTED
         BNE   POST                       NO, SOMETHING ELSE
*                                         YES, POSTING CKPOINT
         OI    AVTCKELF,AVTCRTLN        MARK CKPT ELE AS 'POSTED'
POST     EQU   *                        RETURN ELEMENT TO ITS QCB
         BAL   R14,POSTSUB              POST ELE TO ITS QCB
*
         B     STIMER                   GO CHECK NEXT ELEMENT ON
*                                         TIME QUEUE
QCB      EQU   *
         TM    QCBSTAT-IEDQQCB(RELE),QCBTIME QCB NEED 12 MORE HOUR
         BO    TWELVE                     YES, REPOST IT TO TIME Q
*                                         NO, READY TO LEAVE TIME Q
         MVI   QCBPRI-IEDQQCB(RELE),PRIHIFLG                   @OZ29220
*              SET E9 PRIORITY SO WILL                         @OZ29220
*              EXECUTE BEFORE ANY POSTS FROM QFA FOR SAME      @OZ29220
*              RESOURCE ALREADY ON READY QUEUE                 @OZ29220
         TM    QCBFLAG-IEDQQCB(RELE),QCBTSSES IS THIS A TSO QCB
         BNO   NOTTSO                     NO, NOTHING SPECIAL
*                                         YES, TSO NEEDS SPECIAL
         OI    QCBTSOF2-IEDQQCB(RELE),QCBPOSTO MARK QCB POSTED
         MVC   ELELINK(3),ELEPRI+1      TSO HAS SAVED DATA INTO
*                                         ELEPRI+1 FROM ELELINK
*                                         PRIOR TO PUTTING QCB
*                                         ON TIME DELAY. THIS
*                                         RESTORES TSO INFO
         NI    ELEBYTE,X'FF'-ELEBIT     MARK QCB AS NOT BEING ON
*                                         TIME DELAY Q
*
* TO AVOID DISABLED-ENABLED INTERFACE CONFUSION, THIS ELEBIT BIT
* MUST BE TURNED OFF AFTER THE QCBPOSTO BIT IS SET.
*
         B     POST                     GO POST QCB
*                                       TSO DOES NOT CHANGE THE
*                                         SCHEDULERS IN ITS QCBS
*
NOTTSO   EQU   *
         NI    ELEBYTE,X'FF'-ELEBIT     TURN OFF BIT-MARK ELE AS
*                                         NOT BEING ON TIME Q
         LA    RELE,0(,RELE)            CLEAR HIGH BYTE
         C     RELE,AVTHI               QCB COMING OFF TIME Q, IS IT
*                                         THE SYSTEM DELAY QCB
         BE    POST                       YES, SKIP MOVING SCHEDULERS
*                                           SINCE IT HAS NONE
*                                         NO, MUST BE DESTINATION QCB
         CLI   QCBSTVTO-IEDQQCB(RELE),DSPBUFSC IS QCB FOR BUF TERM
         BE    POST                       YES, SKIP MOVE SCHEDULER
*                                         NO, MOVE SCHEDULERS
         MVC   QCBSLINK-IEDQQCB(3,RELE),QCBSTCHN-IEDQQCB(RELE) MOVE
*                                       DESTINATION ASSIGNMENT DOWN
*                                         IN SUBTASK CHAIN
         LA    R14,QCBSTCHN-1-IEDQQCB(RELE) MOVE
         IC    R0,QCBSTCHN-1-IEDQQCB(RELE)  THE
         ST    R14,QCBSTCHN-1-IEDQQCB(RELE)   SCHEDULER
         STC   R0,QCBSTCHN-1-IEDQQCB(RELE)
         B     POST                     POST QCB               @YM07996
         SPACE
*
TWELVE   EQU   *
         NI    QCBSTAT-IEDQQCB(RELE),X'FF'-QCBTIME TURN OFF 12 HR REQ
         MVC   ELETIME(2),HOUR12        REQUEST 12 HOUR DELAY
         LR    R1,RELE                  PASS ADDR OF NEW ELE IN R1
         B     ADDNEW                   GO HANG QCB ONTO TIME Q
*
LCB      EQU   *
         TM    LCBSTAT2-IEDQLCB(RELE),LCBDIAL IS IT A DIAL LCB
         BO    LEASLCB                    YES, DIAL LCB, GO MOVE
*                                           SCHEDULER AS IF LEASED
*                                         NO, NOT DIAL
         CLI   LCBRSKEY-IEDQLCB(RELE),DSPBUFSC BUFFERED LINE    SA62950
         BE    NOMOVE                   YES, SKIP MOVE SCHEDULERSA62950
*                                       NO, LEASED LCB          SA62950
         CLI   LCBPRI-IEDQLCB(RELE),PUTDELAY  PUT DELAY LCB    @XA11307
         BE    NOMOVE                   YES, SKIP MOVE SCHEDULR@XA11307
LEASLCB  EQU   *
INSERTRS EQU   *                                                 X03039
         LA    R7,LCBRSKEY-IEDQLCB(RELE) LINK RECEIVE SCHEDULER
         LR    R1,R7                      INTO THE STCB CHAIN
         BAL   R14,DSPPRIOR             MOVE SCHEDULER
*
NOMOVE   EQU   *
         TM    LCBSTAT1-IEDQLCB(RELE),LCBFREEN IS LCB FREE
         BZ    STIMER                     IF NOT FREE, FORGET IT, AND
*                                           GO CK NEXT ELE ON TIME Q
         XI    LCBSTAT1-IEDQLCB(RELE),LCBRECVN+LCBFREEN          S22025
*                                       MARK LCB NOT POSTABLE    S22025
POSTLCB  EQU   *
         ST    RELE,ELEPOST               IF FREE, POST IT TO ITSELF
         B     POST                     GO POST LCB
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
*                       TIMER COMPLETION EXIT ROUTINE
*
TIMEEXIT EQU   *
         USING *,R15                    TEMPORARY BASE
         STM   R1,R2,HISREG1(R13)       SAVE STIMER OS REGS
         L     R1,CVTPTR                GET CVT ADDR
         L     R1,AVTCVTPT(,R1)         GET DISPATCHER ADDR FROM
*                                         TCAM WORD IN CVT
         LTR   R1,R1                    IS TCAM IN SYSTEM
         BZ    TIMERET                    NO, FORGET INTERRUPT
*                                         YES, CONTINUE
         L     R1,0(,R1)                GET AVT ADDR FROM DISPATCHER
         L     R2,AVTTCB-IEDQAVTD+AVTSVSIZ(,R1) GET TCB ADDR FROM AVT
         TM    TCBFLGS,TCBENDED         IS TCAM TASK SHUT DOWN
         BC    MIXEDONE,TIMERET           YES, FORGET INTERRUPT
*                                         NO, CONTINUE
         CLI   AVTDELYB-IEDQAVTD+QCBPRI-IEDQQCB+AVTSVSIZ(R1),PRITIME
*                                       IS QCB PRIORITY F0     @ZA03725
         BE    TIMERET                  YES, FORGET INTERRUPT  @ZA03725
         MVI   AVTDELYB-IEDQAVTD+QCBPRI-IEDQQCB+AVTSVSIZ(R1),PRITIME
*                                       SET PRIORITY TO TIME QCB
         XC    AVTDELYB-IEDQAVTD+QCBLINK-IEDQQCB+AVTSVSIZ(L'QCBLINK,R1)*
               ,AVTDELYB-IEDQAVTD+QCBLINK-IEDQQCB+AVTSVSIZ(R1)
*                                       CLEAR ELEMENT LINK FIELD
         LA    R1,AVTIMQPS-IEDQAVTD+AVTSVSIZ(,R1) PASS SPEC ELE TO HAVE
         AQCTL                          TIME QCB POSTED TO ITSELF
TIMERET  EQU   *
         LM    R1,R2,HISREG1(R13)       RESTORE OS REGS
         BR    R14                      RETURN TO SUPERVISOR
*
         DROP  R15                      FORGET TEMPORARY BASE
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
                        ENTRY IEDQHG01
*
* ADD ELEMENT TO TIME QUEUE, CALLED WITH 'BALR'
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
IEDQHG01 EQU   *
         USING *,R15                    TEMPORARY BASE
         STM   R14,R12,HISREG14(R13)    SAVE CALLER'S REGISTERS
         L     RBASE,ADBASE             SET ADDRESSABILITY
         DROP  R15
         BAL   REXIT,SET                PRESET REGS
*
         LA    REXIT,RETURN             REQUEST EXIT WITH RETURN
         B     ADDNEW                   GO ADD ELE TO TIME QUEUE
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
                        ENTRY IEDQHG03
*
* SUBTASK TO REMOVE ELEMENT FROM Q
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
IEDQHG03 EQU   *
         DC    AL1(DSPMCPL2),AL1(0)     IEDQHG03 STCB
*
         USING *,R15                    TEMPORARY BASE
         L     RBASE,ADBASE             ESTABLISH ADDRESSABILITY
         DROP  R15
         BAL   REXIT,SET                PRESET REGISTERS
*
         LA    REXIT,POSTEXIT           REQUEST EXIT TO DSPPOST
         LR    RSPEC,R1                 SET BASE OF REMOVE ELEMENT
*                                         REQUESTING ELEMENT
         L     R1,SPECELEP              GET ADDR OF ELEMENT TO BE
*                                         REMOVED
TAKEOFF  EQU   *
         LA    R1,0(,R1)                CLEAR HIGH BYTE
         LA    RELE,AVTDELYB            START WITH TIME QCB
*
LOOP     EQU   *
         L     R2,ELELINK-1             GET NEXT ELEMENT ON Q
         LA    R2,0(,R2)                CLEAR HIGH BYTE
         C     R2,AVTDELYB              IS ELE REALLY TIME QCB ITSELF
         BCR   EQUAL,REXIT                YES, DESIRED ELE IS NOT HERE
*                                         NO, IT IS A REAL ELEMENT
         CLR   R2,R1                    IS THIS THE DESIRED ELEMENT
         BE    REMOVEIT                   YES, TAKE IF OFF
*                                         NO, THIS IS NOT IT
         LR    RELE,R2                  BUMP TO NEXT ELE ON Q
         B     LOOP                     GO CHECK NEXT ELEMENT
*
REMOVEIT EQU   *
         MVC   ELELINK,ELELINK-ELEMENT(R2) REMOVE ELEMENT FROM Q
         MVC   ELELINK-ELEMENT(3,R2),ELEPRI+1-ELEMENT(R2) TSO INFO
         NI    ELEBYTE-ELEMENT(R2),X'FF'-ELEBIT MARK ELE AS NOT ON
*                                         TIME QUEUE
         C     R2,STIMADDR              IS ELE BEING REMOVED THE
*                                         ONE WITH ACTIVE STIMER
         BCR   NOTEQUAL,REXIT             NO, A DIFFERENT ONE, QUIT
*                                         YES, CONTINUE
         MVI   STIMADDR,AVTEFF          MARK TO INDICATE NO STIMER
*                                         STILL ACTIVE
         BR    REXIT                    QUIT
*
POSTEXIT EQU   *
         LR    R1,RSPEC                 PASS ELE TO BE POSTED
         MVC   SPECPOST+1-SPECELE(3,R1),SPECQCB+1-SPECELE(R1) SET
*                                         SPECIAL ELEMENT TO GO TO
*                                         SPECIFIED QCB
         BAL   R14,DSPPOST              EXIT TO DISPATCHER     @Y17XADX
*
RETURN   EQU   *
         LR    R13,R8                   RESTORE TO CALLER'S SAVEAREA
         LM    R14,R12,HISREG14(R13)    RESTORE CALLER'S REGISTERS
         BR    R14                      RETURN TO CALLER
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
                        ENTRY IEDQHG02
*
* ROUTINE REMOVES ELEMENT FROM TIME Q, CALLED WITH 'BALR'
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
IEDQHG02 EQU   *
         USING *,R15                    TEMPORARY BASE
         STM   R14,R12,HISREG14(R13)    SAVE CALLER'S REGISTERS
         L     RBASE,ADBASE             ESTABLISH ADDRESSABILITY
         DROP  R15
         BAL   REXIT,SET                PRESET REGS
*
         LA    REXIT,RETURN             REQUEST EXIT WITH RETURN
         B     TAKEOFF                  REMOVE ELEMENT
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
*                       SUBROUTINES
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
SET      EQU   *
         LR    R8,R13                   SAVE CALLER'S SAVEAREA
         L     RAVT,ADAVT               SET AVT DSECT BASE
         LA    R13,AVTSAVE2+AVTSVSIZ    SET R13 TO SAVE2 FOR DISPATCHER
         L     R11,AVTEA                SET DISPATCHER BASE
         BR    REXIT                    RETURN TO ENTRY POINT
*
POSTSUB  EQU   *
         LR    R1,RELE                  PASS ELE TO BE POSTED
         B     DSPPOSTR                 POST ELEMENT
*
*                                       RETURN ADDR IN R14 ALREADY
*                                         SET BY CALLER
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
*                       CONSTANTS
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
CVTPTR   EQU   16                       WORD HAVING CVT ADDRESS
HISREG14 EQU   12                       OFFSET INTO SAVEAREA OF R14
HISREG1  EQU   24                       OFFSET INTO SAVEAREA OF R1
DEFAULT  EQU   1                        SUBSTITUTED FOR ZERO INTERVAL
HALFWORD EQU   16                       SHIFT COUNT FOR HALF WORD
ZERORNEG EQU   12                       CONDITION CODE FOR ZERO OR
*                                         NEG FOLLOWING SRA INSTR
MIXEDONE EQU   7                        CONDITION CODE, MIXED OR ONES
NOTEQUAL EQU   7                        CONDITION CODE, NOT EQUAL
EQUAL    EQU   8                        CONDITION CODE, EQUAL
*
HOUR12   DC    AL2((60*60*12)-1)        SECONDS IN 12 HOURS
PUTDELAY EQU   X'D6'                    PUT TIME DELAY FLAG    @XA11307
ADAVT    DC    A(IEDQAVT)               SETS AVT DSECT BASE
         EXTRN IEDQAVT
ADBASE   DC    A(BASE)                  SETS RBASE FOR ADDRESSABILITY
STIMADDR DC    X'FF',AL3(0)             ADDRESS OF LAST ELE TO
*                                         HAVE STIMER DONE
*                                           FIRST BYTE ---
*                                             X'FF'-NO STIMER ACTIVE
*                                             X'00'-STIMER ACTIVE
SECPRDAY DC    F'86400'                 SECONDS/DAY
UNITSDAY DC    F'65536'                 UNITS PER DAY
SECSX100 DC    F'8640000'               SEC/DAY * 100
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
*                       DSECTS
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
SPECELE  DSECT                          ELEMENT REQUESTS REMOVAL OF
*                                         ELEMENT FROM TIME DELAY Q
SPECPOST DS    A                        ADDR OF QCB TO RECEIVE ELE
SPECLINK DS    A                        LINK FIELD
SPECELEP DS    A                        ADDR OF ELE TO BE REMOVED
SPECQCB  DS    A                        ADDR OF QCB TO RECEIVE THIS
*                                         ELE WHEN SERVICE COMPLETE
*
*       DECHEX *********************************************
*        0  0  * SPECPOST                                  *
*              *********************************************
*        4  4  * SPECLINK                                  *
*              *********************************************
*        8  8  * SPECELEP                                  *
*              *********************************************
*        12 C  * SPECQCB                                   *
*              *********************************************
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
ELEMENT  DSECT                          TIME DELAY REQUEST ELEMENT
ELEPOST  DS    A                        ADDR OF TIME DELAY QCB
ELEPRI   DS    A                        DISPATCHER LINK FIELD
ELESPEQ  DS    A                        IF ELE IS BUF OR SPECIAL ELE,
*                                         ADDR OF QCB TO BE RETURNED TO
         DS    A                        UNUSED BY TIME DELAY
ELETIME  DS    H                        INTERVAL IN SECONDS
ELEINDEX DS    X                        OFFSET TO QCB ADDRESS TO RE-
*                                         CEIVE ELE WHEN TIME ELAPSES
*                                       ALSO TYPE ELEMENT IDENTIFIER
*                                         X'00' - QCB
*                                         X'14' - LCB
*                                         X'08' - OTHERS
ELELCB   EQU   X'14'                    OFFSET INTO LCB OF ADDR OF QCB
         DS    4X                       UNDEFINED
ELEBYTE  DS    X                        FLAG BYTE, ONE BIT USED
ELEBIT   EQU   X'02'                    BIT ON, ELE ON TIME Q
*                                         BIT OFF, ELE NOT ON Q
*                                       BIT IS MEANINGFUL ON LCB
*                                         AND QCB ONLY
         DS    5X                       UNDEFINED
ELELINK  DS    AL3                      LINK FIELD OF ELEMENTS ON
*                                         TIME QUEUE
*
*       DECHEX *********************************************
*        0  0  * ELEPOST                                   *
*              *********************************************
*        4  4  * ELEPRI   * (LINK FIELD)                   *
*              *********************************************
*        8  8  * ELESPEQ                                   *
*              *********************************************
*        12 C  * (UNUSED)                                  *
*              *********************************************
*        16 10 * ELETIME             * ELEINDEX * (UNUSED) *
*              *********************************************
*        20 14 * (UNUSED)                       * ELEBYTE  *
*              *********************************************
*        24 18 * (UNUSED)                                  *
*              *********************************************
*        28 1C * (UNUSED) * ELELINK                        *
*              *********************************************
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         TDISPD
         TPRIOR
         TTCBD
TCBENDED EQU   X'F7'                    IF ANY ONE OF THESE BITS
*                                         IN TCBFLGS IS ON, TASK
*                                         HAS TERMINATED
         TLCBD
         TQCBD
         TAVTD 2
         EJECT
         TTRMD                                                   X03039
         END
