DQAR TITLE '''IEDQAR''   CANCEL MESSAGE'
IEDQAR   CSECT
*A569000,913600-926200                                           S22025
*C225000,252000,648000,819000,857000,926200                      S22025
*D558000-594000,711000-756000,909000,918000                      S22025
*C603000                                                        SA56320
*A914200,917200                                                 SA56320
***********************************************************************
*TITLE 'IEDQAR' CANCAL MESSAGE                                        *
*                                                                     *
*STATUS -- CHANGE LEVEL 0                                             *
*                                                                     *
*FUNCTION -- CANCEL MESSAGE WILL SET A FLAG FOR DESTINATION SCHEDULER *
*   AND DISK U/O TO ENABLE THEM TO CANCEL A MEESAGE.  IT WILL ALSO    *
*   STOP THE MULTIPLE ROUTING AND CHECKPOINT FUNCTIONS.               *
*                                                                     *
*ENTRY POINTS -- IEDQAR - TO CANCEL A MESSAGE.                        *
*   CALLING SEQUENCE -   LA   R12,IEDQAR                              *
*                        BR   R12                                     *
*                                                                     *
*INPUT -- IEDQAR IS CALLED BY BUFFER DISPOSITION WITH THE FOLLOWING   *
*   REGISTIRS INITIALIZER                                             *
*   R4 HAS THE LCB ADDRESS                                            *
*   R3 HAS THE SCB ADDRESS                                            *
*   R11 HAS THE DISPATCHER ADDRESS                                    *
*   R12 HAS THE ENTRY POINT ADDRESS                                   *
*   R13 HAS THE AVT SAVE2 ADDRESS                                     *
*   R1 HAS THE ADDRESS OF A LIST OF ELEMENTS TO BE POSTED             *
*                                                                     *
*EXTERNAL ROUTINES -- DISPATCHER POST FUNCTION - PLACES THE      S22025
*   CHAIN OF ELEMENTS ON THE READY QUEUE.                             *
*                                                                     *
*EXITS-NORMAL -- TO THE DISPATCHER AT POST                       S22025
*                                                                     *
*EXITS-ERROR  --  NONE                                                *
*                                                                     *
*TABLES/WORKAREAS -- AVT,LCB,SCB                                      *
*                                                                     *
*ATTRIBUTES -- REUSABLE, REFRESHABLE, ENABLED, RESIDENT               *
*                                                                     *
*NOTES -- THE OPERATION OF THIS MODULE DOES NOT DEPEND UPON A         *
*   PARTICULAR INTERNAL REPRESENTATION OF THE EXTERNAL CHARACTER      *
*   SET.                                                              *
*                                                                     *
***********************************************************************
         TPRIOR
*                                                                     *
*                  REGISTER EQUATES                                   *
*                                                                     *
R0       EQU   0
R1       EQU   1                        PARAMETER REG
R2       EQU   2                        WORK REGISTER            S22025
RSCB     EQU   3                        SCB ADDRESS
RLCB     EQU   4                        LCB ADDRESS
RPRF     EQU     5
RQCB     EQU   7                        QCB ADDRESS
RDSP     EQU   11                       DISPATCHER BASE
RBASE    EQU   12                       BASE REG
RAVT     EQU   13                       AVT SAVE2 BASE
R14      EQU   14
R15      EQU   15
         USING   IEDQLCB,RLCB
         USING  IEDQPRF,RPRF
         USING IEDQSCB,RSCB
         USING IEDQAVTD,RAVT
         USING IEDQDISP,RDSP
         USING *,RBASE
IEDQAR   IEDHJN TAG
         LA    RPRF,AVTEZERO(,R1)       ADDR OF POST LIST       SA56320
         LTR   RPRF,RPRF                WAS LIST PASSED         SA56320
         BZ    POSTERB                 BR IF NO                 SA56320
CHECK    EQU     *
         NC    PRFLINK(3),PRFLINK       LAST ELEM ON LIST
         BZ    HAVEBFR                  BR YES - THIS IS LAST BFR
         L      RPRF,PRFLINK-1          OF MSG - NO - GET NEXT ON LIST
         B     CHECK                    LOOP AND CHECK AGIAN     S22025
HAVEBFR  EQU  *
         OI    PRFSTAT1,PRFCNCLN
         NI    SCBSTATE,X'FF'-SCBCKPT   STOP CHECKPOINT
         ST    R0,SCBMRFSD              R0 HAS ZERO WHEN ENTERED,
*        THIS STORE WILL SOTP DIST. LIST AND MULT. ROUTE
         TM    SCBSTATE,SCBSEQIN        HAS SEQ NO BEEN DONE
         BNO   RETURNA                  BR NO
         NI    SCBSTATE,X'FF'-SCBSEQIN  RESET
         LH    R1,PRFSRCE               OFFSET TO SRCE TNT
         N     R1,AVTCLRHI              CLEAR HIGH HALF
         L     R15,AVTRNMPT             ADDR OF CODE
         BALR  R14,R15                  BRANCH TO TERM TABLE ROUTS22025
         USING  IEDQTRM,R1
         LH    R14,TRMINSEQ             INPUT SEQ NO
         BCT   R14,SETSEQ               DECREMENT SEQUENCE NO.   S22025
         LH    R14,DC9999               SET WRAPPED VALUE
SETSEQ   EQU   *
         STH   R14,TRMINSEQ             SET NEW VALUE
RETURNA  EQU   *
         TM    SCBSTATE,SCBLCK1N+SCBMSGLN   LOCK
         BZ    POSTBACK                 NO, BRANCH               S22025
         BM    EXTLOCK                  BR IF EXTENDED LOCK
         NI    SCBSTATE,AVTEFF-(SCBMSGLN+SCBLCK1N)
*       IF THIS IS MSG LOCK - RESET ALL LOCK FLAGS
EXTLOCK EQU   *
         MVI   LCBINSRC+2,AVTEZERO      RESET FLAGS FOR BD TO POST
         NI    LCBSTAT1,X'FF'-LCBRECVN  SET FOR SCH TO REPOLL
POSTBACK EQU   *                                                 S22025
         LR    R1,RPRF                  GET THE BUFFER ADDRESS   S22025
SETADDR  EQU   *                                                SA56320
         L     R2,AVTMSGS-1             GET VCON TABLE ADDRESS   S22025
         L     R2,0(,R2)                GET ADDRESS OF IEDQBD    S22025
         ST    R2,PRFQCBA-1             SET THE POST ADDRESS     S22025
         MVI   PRFPRI,PRIRCQCB          SET THE POST PRIORITY    S22025
         BAL   R14,DSPPOST              EXIT TO THE DISPATCHER @Y17XAMX
POSTERB  EQU   *                                                SA56320
         LA    R1,LCBERB                ERB ADDRESS             SA56320
         LR    RPRF,R1                  SET ERB ADDRESS         SA56320
         B     SETADDR                                          SA56320
DC9999   DC    H'9999'                  WRAP VALUE               S22025
         TTRMD
         TPRFD
         TDISPD
         TSCBD
         TLCBD
         TAVTD  2
         END
