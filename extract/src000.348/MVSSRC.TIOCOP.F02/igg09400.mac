         TITLE 'IGG09400 - TCABEND'
IGG09400 CSECT
***********************************************************************
*
*01* MODULE-NAME = IGG09400
*
*01* DESCRIPTIVE-NAME = TCABEND
*
*01* COPYRIGHT = NONE
*
*01* STATUS
*            -03/04/76                                         @ZA09622
*
*01* FUNCTION = NOTIFY THE SYSTEM THAT TCAM HAS ABNORMALLY TERMINATED
*               AND PREPARE TO STOP TIME SHARING.
*
*01* NOTES = THIS MODULE IS A COMPLETE REWRITE OF VS2/1        YS02019
*
*01* RESTRICTIONS = N/A
*
*02*   DEPENDENCIES = NONE
*
*02*   REGISTER-CONVENTIONS = SEE REGISTER EQUATES
*
*02*   PATCH-LABEL = ZAPAREA
*
*01* MODULE-TYPE = ASSEMBLER LANGUAGE CSECT
*
*02*   PROCESSOR = ASSEMXF-370R
*
*02*   MODULE-SIZE = SEE EXTERNAL SYMBOL DICTIONARY TYPE SD ID-01
*
*02*   ATTRIBUTES = REENTRANT,REUSABLE,ENABLED,SUPERVISOR STATE
*
*01* ENTRY-POINT = IGG09400 (HODLING THE LOCAL LOCK AND THE CMS LOCK,
*                  IN SUPERVISOR'S KEY)
*
*02*   PURPOSE = SEE FUNCTION
*
*02*   LINKAGE = FROM IGC0009D VIA BRANCH
*
*01* INPUT = REGISTER  0 CONTAINS ENTRY CODE IN HIGH ORDER BYTE
*            REGISTER  1 CONTAINS ZERO
*            REGISTER  3 CONTAINS CVT ADDRESS
*            REGISTER  4 CONTAINS TCB ADDRESS
*            REGISTER  5 CONTAINS SVRB ADDRESS
*            REGISTER 14 CONTAINS RETURN ADDRESS
*
*01* OUTPUT = REGISTER 15 CONTAINS RETURN CODE
*
*01* EXIT-NORMAL = RETURN TO CALLER
*
*01* EXIT-ERROR = NONE
*
*01* EXTERNAL-REFERENCES = IKJ409I (ENTRY IN IGG09403)
*
*02*   CONTROL-BLOCKS = CVT,TCB,TSB,TIOCRPT,TCX,AVT,BASEA,ASCB,ASVT
*
*01* TABLES = NONE
*
*01* MACROS = GETMAIN,FREEMAIN,SDUMP,SETLOCK,WTO,TSEND
*
*01* CHANGE-ACTIVITY
*           VS2-037 CHANGES
*
*
*
* A 129430     D 129410                                         ZA32570
* A 123000-1234600,129410                                       ZA25126
* D 115500,122600-122700                                        ZA25126
*           VS2-3.0 CHANGES
*D000640,014000-016000,017000,017500,018000,040092-079540       ZA01299
*D079560-084987,116500-119500,129100,129200,129424-129460       ZA01299
*D134000-134100                                                 ZA01299
*C024500,037960,040088,089900                                   ZA01299
*A040888-041588,121500,040608-040668,084992-085728              ZA01299
*A12290-122984                                                  ZA01299
*           VS2-3.0 SERVICE UPDATE
*0000024500,129350                                   UZ03908   @ZA09622
*0000040838-041388                                   UZ02380   @ZA06118
*
*
***********************************************************************
         EJECT
*****    REGISTER EQUATES
RPARM0   EQU   0                  PARM REG 0
RWRK0    EQU   0                  WORK REG
RPARM1   EQU   1                  PARM REG 1
RWRK1    EQU   1                  WORK REG
RWRK2    EQU   2                  WORK REG
RWRK3    EQU   3                  WORK REG
RCVT     EQU   3                  CVT ADDRESS
RWRK4    EQU   4                  WORK REG
RTCB     EQU   4                  TCB ADDRESS
RTIOC    EQU   5                  TIOCRPT ADDRESS
RWRK5    EQU   5                  WORK REG
RBASE    EQU   6                  BASE REG
RWRK7    EQU   7                  WORK REG
RRB      EQU   7                  RB ADDRESS
RWRK8    EQU   8                  WORK REG
RWRK9    EQU   9                  WORK REG
RWRK10   EQU   10                 WORK REG
RWRK11   EQU   11                 WORK REG
RWRK12   EQU   12                 WORK REG
RWRK13   EQU   13                 WORK REG
RSAVE    EQU   13                 SAVE AREA ADDRESS
RLINK    EQU   14                 RETURN ADDRESS
RBRANCH  EQU   15                 BRANCH ADDRESS
RCODE    EQU   15                 RETURN CODE
         SPACE 3
******   VALUE EQUATES                                         ******
E0       EQU   0                  VALUE OF 0
E1       EQU   1                  VALUE OF 1
BYTE1    EQU   8                  BYTE 1 OF WORD
USERKEY  EQU   X'80'              CALLERS KEY FOR RB TEST       YM01246
RCODE4   EQU   4                  RETURN CODE 4,INVALID CALL
         EJECT
***********************************************************************
*        SET UP ADDRESSABILITY AND MODULE ID                          *
***********************************************************************
         BALR  RBASE,E0           .BASE REGISTER
         USING *,RBASE            .ADDRESSABILITY
         USING CVT,RCVT           CVT ADDRESSABILITY
         LR    RWRK10,RLINK       SAVE RETURN ADDRESS
         B     VALIDATE           BRANCH PAST MODULE ID
         DC    CL8'IGG09400'      MODULE ID
         DC    X'6064'            DATE-  03/04/76              @ZA09622
         SPACE 1
***********************************************************************
*        SAVE PARAMETER IN SVRB EXTENDED SAVE AREA                    *
***********************************************************************
VALIDATE EQU   *
         USING RBBASIC,RWRK5      RB ADDRESSABLE
         ST    RPARM1,RBEXSAVE    SAVE PARMETER FOR LATER USE
         LR    RWRK2,RPARM1       SAVE  PARAMETER FOR IMMEDIATE USE
         L     RWRK7,RBLINK       CALLERS RB ADDRESS            YM01246
         DROP  RWRK5              RB ADDRESSABILITY ENDS
         SPACE 2
***********************************************************************
*        VERIFY CALLER                                                *
***********************************************************************
         USING RBBASIC,RWRK7            RB ADDRESSABILITY       YM01246
         TM    RBOPSW+E1,USERKEY        IS CALL MADE BY SYSTEM  YM01246
         DROP  RWRK7                    RB ADDRESSABILITY ENDS  YM01246
         BO    INVALID                  NO,RETURN CODE 4 & EXIT YM01246
         SPACE 2
***********************************************************************
*        GET TIOCRPT AND AVT TO INDICATE TIME SHARING HAS ABNORMALLY  *
*           TERMINATED                                                *
***********************************************************************
         L     RWRK7,CVTAQAVT     TCX ADDRESS
         L     RTIOC,TCXRPT-IEDQTCX(,RWRK7) TIOCRPT ADDRESS
         LTR   RTIOC,RTIOC        TIME SHARING RUNNING
         BZ    INVALID            NO,INVALID CALL
         USING TIOCRPT,RTIOC      TIOCRPT ADDRESSABILITY
         TM    TIOCFLG,TIOCTSAB   TIME SHARING ABENDED
         BO    INVALID            YES,INVALID CALL
         OI    TIOCFLG,TIOCTSAB   INDICATE TIME SHARING ABEND
         L     RWRK1,TCXASCB-IEDQTCX(,RWRK7) TCAM'S ASCB ADDR   YM02864
         C     RWRK1,PSAAOLD-PSA  RUNNING IN TCAM'S MEMORY?     YM02864
         BNE   DUMP               NO, CANNOT REFERENCE AVT      ZA01299
         L     RWRK7,TCXAVT-IEDQTCX(,RWRK7) AVT ADDRESS
         USING IEDQAVTD,RWRK7     AVT ADDRESSABILITY
         OI    AVTBIT3,AVTTSAB    TIME SHARING HAS ABENDED
         DROP  RWRK7              RELEASE ADDRESSABILITY ON AVT
         SPACE 2
***********************************************************************
*        TEST IF TIOC CONTROL BLOCKS SHOULD BE DUMPED                 *
***********************************************************************
         LTR   RWRK2,RWRK2        CHECK IF FSTOP
         BNZ   SCAN               YES, BYPASS DUMP              ZA01299
         SPACE 2
***********************************************************************
*        FREE CMS AND LOCAL LOCKS                                     *
***********************************************************************
DUMP     EQU   *                                                ZA01299
         SETLOCK RELEASE,TYPE=CMS,RELATED=(TIOCRPT,IGC0009D)    ZA01299
         SETLOCK RELEASE,TYPE=LOCAL,RELATED=(TIOCRPT,IGC0009D)  ZA01299
***********************************************************************
*        ISSUE SDUMP TO DUMP ALL TSO AND TCAM INFORMATION             *
***********************************************************************
         USING TCBRBP,RTCB                                     @ZA06118
         CLC   TCBCMPC,CANCELED         WAS TCAM CANCELED?     @ZA06118
         BNE   DUMPIT                   NO,TAKE THE DUMP       @ZA06118
         TM    TCBCMP,TCBCREQ           WAS A DUMP REQUESTED?  @ZA06118
         BZ    IKJMSG                   NO,JUST SEND MESSAGE   @ZA06118
         DROP  RTCB                                            @ZA06118
DUMPIT   LA    RWRK1,SDUMP              LOAD ADDR OF LIST FORM  ZA01299
         SDUMP MF=(E,(RWRK1))                                   ZA01299
         SPACE 2
***********************************************************************
*        INFORM OPERATOR TIME SHARING HAS ABNORMALLY TERMINATED       *
***********************************************************************
IKJMSG   L     RWRK1,MSG          PARAMETER FOR MESSAGE        @ZA06118
         WTO   MF=(E,(1))         MESSAGE IKJ409I
         SPACE 2
***********************************************************************
*        GET CMS AND LOCAL LOCKS                                      *
***********************************************************************
GETLOCK1 EQU   *                                               ZA01299
         SETLOCK OBTAIN,TYPE=LOCAL,MODE=UNCOND,                ZA01299 X
               RELATED=(TIOCRPT,IGG09400(DUMP))                ZA01299
         SETLOCK OBTAIN,TYPE=CMS,MODE=UNCOND,                  ZA01299 X
               RELATED=(TIOCRPT,IGG09400(DUMP))                ZA01299
***********************************************************************
*         WHEN LOCKS ARE FREED AND THEN RE-OBTAINED, THE TIOCRPT      *
*         ADDRESS MUST BE RE-VERIFIED TO INSURE TIME SHARING IS       *
*         STILL ACTIVE.                                               *
***********************************************************************
         L     RTIOC,CVTAQAVT     TCX ADDRESS                  ZA01299
         LTR   RTIOC,RTIOC        IS TCAM STILL AROUND         ZA01299
         BZ    INVALID            NO, EXIT                     ZA01299
         L     RTIOC,TCXRPT-IEDQTCX(,RTIOC)  TIOCRPT ADDRESS   ZA01299
         LTR   RTIOC,RTIOC        TIME SHARING STILL ACTIVE    ZA01299
         BNZ   SCAN               YES, CANCEL TSBS             ZA01299
         SR    RCODE,RCODE        ZERO RETURN CODE             ZA01299
         LR    RLINK,RWRK10       RETURN ADDRESS               ZA01299
         BR    RLINK              RETURN TO CALLER             ZA01299
***********************************************************************
*        SCAN ALL TSBS AND CANCEL THE ONES THAT DON'T HAVE THE TSBCANC*
*         BIT ON. USE SYSTEM INITIATED CANCEL.                        *
***********************************************************************
*          INPUT REGISTERS TO SIC                                     *
*                     0 = POST CODE (622 ABEND)                       *
*                     1 = ASCB                                        *
*                    13 = SAVE AREA ADDRESS                           *
*                    14 = RETURN ADDRESS                              *
*                    15 = ENTRY POINT                                 *
***********************************************************************
SCAN     EQU   *
         LR    RWRK4,RWRK10       SAVE RETURN ADDRESS           YM05366
         LH    RWRK8,TIOCUSCT     GET NO. ACTIVE USERS
         LTR   RWRK8,RWRK8        ANY USERS LOGGED ON
         BZ    CLEAN              NO, GO CHECK TSEND            ZA01299
         SR    RWRK8,RWRK8        CLEAR REG
         IC    RWRK8,TIOCTSBS     SIZE OF TSB
         LH    RWRK9,TIOCNTSB     NUMBER OF TSBS
         BCTR  RWRK9,E0           MINUS ONE
         MR    RWRK8,RWRK8        OFFSET TO LAST TSB
         L     RWRK7,TIOCTSB-E1   START OF TSBS
         LA    RWRK7,E0(,RWRK7)   CLEAR HI-BYTE FOR BXLE
         AR    RWRK9,RWRK7        ADDRESS OF LAST TSB
         SR    RWRK8,RWRK8        CLEAR REGISTER
         IC    RWRK8,TIOCTSBS     SIZE OF TSB
         L     RWRK0,POSTCODE     622 ABEND CODE
         L     RWRK2,CVTPTR       ADDRESS OF CVT
         L     RSAVE,CVTMSER-CVT(,RWRK2) ADDRESS OF BASEA
         L     RSAVE,BAASCB-BASE(,RSAVE) ASCB OF MASTER SCHEDULER
         USING TSB,RWRK7          TSB ADDRESSABILITY
         LR    RWRK2,RSAVE        SAVE MSTR SCHED ASCB ADDR     YM02864
NEXTTSB  EQU   *
         TM    TSBSTAT,TSBINUSE   TSB ACTIVE
         BNO   CHECKTSB           NO,CHECK NEXT TSB
         SR    RWRK1,RWRK1              CLEAR REGISTER          VS08528
         STH   RWRK1,TSBASRCE           ZERO TSBASRCE, SO AYP   VS08528
*                                       WON'T TOUCH THE QCB     VS08528
         TM    TSBFLG4,TSBCANC    USER CANCELLED
         BO    POSTAY8            YES, DON'T SIC AGAIN          YM02864
         L     RWRK1,TSBASCBA     ASCB ADDRESS
         L     RBRANCH,CVTPTR     CVT ADDRESS
         USING CVT,RBRANCH        CVT ADDRESSABILITY
         L     RBRANCH,CVTSIC     ENTRY POINT TO SIC
         DROP  RBRANCH
         LA    RSAVE,TIOCSAVE     SAVE AREA ADDRESS
         BALR  RLINK,RBRANCH      BRANCH TO SIC
         LR    RSAVE,RWRK2        RESTORE MSTR SCHED ASCB ADDR  YM02864
         SPACE 1
***********************************************************************
*        XMPOST THE TSBLECB SO THAT IEDAY8 CAN TERMINATE USER         *
***********************************************************************
*                                                                     *
*        INPUT REGISTERS TO XMPOST -                                  *
*                                                                     *
*              10 = ZERO (NO ERROR CODE)                              *
*              11 = ECB ADDRESS (HIGH ORDER BIT ON FOR XM)            *
*              12 = ADDRESS OF CVTBRET                                *
*              13 = MASTER SCHEDULER'S ASCB ADDRESS                   *
*              14 = RETURN ADDRESS                                    *
*              15 = ENTRY ADDRESS                                     *
***********************************************************************
POSTAY8  EQU   *                                                YM02864
         SR    RWRK10,RWRK10      CLEAR REG
         L     RWRK1,CVTPTR       CVT ADDRESS                   YM02864
         LA    RWRK12,CVTBRET-CVT(,RWRK1) ERROR RETURN ADDR     YM02864
         LA    RWRK11,TSBLECB     ECB ADDRESS TO POST
         ICM   RWRK11,BYTE1,XMPOST  INDICATE  CROSS MEMORY POST
         L     RBRANCH,CVT0PT01-CVT(,RWRK1) POST ENTRY POINT    YM02864
         BALR  RLINK,RBRANCH      GO TO POST
         LR    RSAVE,RWRK2        RESTORE MSTR SCHED ASCB ADDR  YM02864
CHECKTSB EQU   *
         BXLE  RWRK7,RWRK8,NEXTTSB PROCESS MORE TSBS
         SPACE 2
***********************************************************************
*        CHECK IF TSEND SHOULD BE ISSUED                              *
***********************************************************************
CLEAN    EQU   *                                                ZA01299
         L     RPARM1,PSATOLD-PSA GET CURRENT TCB ADDRESS
         L     RPARM1,TCBRBP-TCB(,RPARM1)  GET SVRB ADDRESS
         L     RPARM1,RBEXSAVE-RBBASIC(,RPARM1) GET CALLER'S PARM
         LTR   RPARM1,RPARM1      WAS CALL MADE BY FSTOP
         BNZ   RETURN             YES, DON'T ISSUE TSEND
         SPACE 2
***********************************************************************
*        FREE CMS AND LOCAL LOCKS BEFORE TSEND                        *
***********************************************************************
         SETLOCK RELEASE,TYPE=CMS,                              ZA01299X
               RELATED=(TIOC,IGG09400(GETLOCK1))                ZA01299
         SETLOCK RELEASE,TYPE=LOCAL,                            ZA01299X
               RELATED=(TIOC,IGG09400(GETLOCK1))                ZA01299
         SPACE 2
         L     RWRK7,CVTAQAVT     TCX ADDRESS                   ZA25126
         LTR   RWRK7,RWRK7        TCAM STILL RUNNING?           ZA25126
         BZ    INVALID            NO, INVALID NOW               ZA25126
         L     RTIOC,TCXRPT-IEDQTCX(,RWRK7) TIOCRPT ADDRESS     ZA25126
         LTR   RTIOC,RTIOC        TIME SHARING RUNNING?         ZA25126
         BZ    INVALID            NO,INVALID NOW                ZA25126
         USING TIOCRPT,RTIOC      TIOCRPT ADDRESSABILITY        ZA25126
         LH    RWRK8,TIOCUSCT     GET USER COUNT                ZA25126
         LTR   RWRK8,RWRK8        USER COUNT ZERO               ZA25126
         BZ    DOTSEND            YES, DONE DO TSEND            ZA25126
         STIMER WAIT,BINTVL=TIME  GIVE USERS TIME FOR CLEANUP   ZA25126
DOTSEND  EQU   *                                                ZA25126
         TSEND                    ,CLEAN UP TIOC CONTROL BLOCKS
RETURN   EQU   *
         SR    RCODE,RCODE        RETURN CODE ZERO
         LR    RLINK,RWRK4        RETURN ADDRESS
         BR    RLINK              RETURN TO CALLER
INVALID  EQU   *
         LR    RLINK,RWRK10       RETURN ADDRESS
         LA    RCODE,RCODE4       INVALID CALL
         BR    RLINK              RETURN TO CALLER
         EJECT
***********************************************************************
*        CONSTANTS AND MAPPING MACROS                                 *
***********************************************************************
MSG      DC    V(IKJ409I)         TIME SHARING TERMINATED
CANCELED DC    XL3'222000'        TCAM CANCELED                @ZA09622
POSTCODE DS    0F
         DC    XL4'00000622'      CANCEL ABEND CODE 622
TIME     DC    F'3000'            WAIT VALUE                    ZA32570
         DS    0F
XMPOST   DC    XL1'80'            CROSS MEMORY POST
SDUMP    SDUMP HDR='TIOC AND TCAM CONTROL BLOCKS AND BUFFERS',  ZA01299X
               SDATA=(RGN,TRT,CSA,NUC,LPA,SQA,ALLPSA),MF=L      ZA01299
ZAPAREA  DC    10F'0'             PATCH AREA
         EJECT
         IHAASCB
         EJECT
         IHAASVT
         EJECT
         TAVTD
         EJECT
         IEEBASEA
         EJECT
CVT      DSECT
         CVT
         EJECT
         IHAPSA
         EJECT
         IKJRB
         EJECT
         IKJTCB LIST=YES
         EJECT
         TTCXD
         EJECT
         IKJTIOCP
         EJECT
         IKJTSB
         END
