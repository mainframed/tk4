         TITLE 'IGG0940E - RTAUTOPT ROUTINE'
IGG0940E CSECT
         COPY  LCGASMS2
*0000760500-761700                                               M0590
*0000                                                            TS0007
*                                                                 M2669
*                                                                M4283
*                                                                M0047
*                                                                 M5113
*                                                                 M5467
***********************************************************************
*    RELEASE 23 DELETIONS/CHANGES                                     *
*    RELEASE 22 DELETIONS/CHANGES                                     *
*    RELEASE 21 DELETIONS/CHANGES                                     *
*0885563780-563810                                               M0108
*0885563600,634000                                               M0104
*0885569000                                                      M0101
*0885                                                            S21008
*                                                                     *
* STATUS -                                                            *
*    CHANGE LEVEL 001                                                 *
*                                                                     *
* FUNCTION -                                                          *
*    THIS ROUTINE IS TO SERVE THE MACRO 'RTAUTOPT'. IT IS TO          *
*    SET THE BIT CONDITION IN TSB (TSBAUTON) TO REFLECT THAT AUTOMATIC*
*    PROMPTING IS RESTARTED. WHEN THE USER REQUESTS RESTARTING OF     *
*    AUTOMATIC PROMPTING FACILITY, TSBAULST BIT AND TSBAUTOC BIT ARE  *
*    CHECKED TO SEE WHETHER THIS FACILITY HAS PREVIOUSLY BEEN STARTED.*
*                                                                     *
* ENTRY POINTS -                                                      *
*         IGG0940E - CONTROL IS RECEIVED FROM MODULE IGC0009D         *
*                    VIA BRANCH ACCORDING ENTRY CODE 14 HOLDING       *
*                    THE LOCAL AND THE CMS LOCKS, IN SUPERVISOR KEY   *
* INPUT -                                                             *
*    REGISTER 0 CONTAINS PARAMETER                                    *
*    REGISTER 1 CONTAINS PARAMETER                                    *
*    REGISTER 3 CONTAINS CVT ADDRESS                                  *
*    REGISTER 4 CONTAINS TCB ADDRESS                                  *
*    REGISTER 5 CONTAINS TSB ADDRESS                                  *
*    REGISTER 10 CONTAINS THE ADDRESS OF AN ARRAY OF CONSTANTS        *
*    REGISTER 12 CONTAINS THE BASE ADDRESS                            *
*    REGISTER 14 CONTAINS THE RETURN ADDRESS                          *
*                                                                     *
* OUTPUT -                                                            *
*    REGISTER 15 HAS RETURN CODE:                                     *
*           0 - SUCCESSFUL                                            *
*           4 - INVALID PARAMETER                                     *
*           8 - AUTOMATIC PROMPTING NOT PREVIOUSLY STARTED            *
*                                                                     *
* EXTERNAL REFERENCES -                                               *
*         NONE                                                        *
*                                                                     *
* EXITS, NORMAL -                                                     *
*         BR 14 WITH 0 IN REGISTER 15                                 *
*                                                                     *
* EXITS, ERROR -                                                      *
*         BR 14 WITH 4 OR 8 IN REGISTER 15                            *
*                                                                     *
* TABLES/WORK AREAS -                                                 *
*    CONTROL BLOCKS ARE DESCRIBED IN THE DSECTS AT END OF LISTING     *
*                                                                     *
* ATTRIBUTES -                                                        *
*    REENTRANT, REFRESHABLE, SUPERVISOR MODE, AND ENABLED             *
*                                                                     *
* CHARACTER CODE DEPENDENCY -                                         *
*    THIS MODULE IS NOT CHARACTER DEPENDENT                           *
* NOTES -                                                             *
*    NONE                                                             *
*                                                                     *
***********************************************************************
         SPACE 3
********
******** REGISTER EQUATES
********
RPARM0   EQU   0                        PARAMETER REG 0
RWORK0   EQU   0                        WORK REGISTER
RPARM1   EQU   1                        PARAMETER REG 1
RWORK1   EQU   1                        WORK REGISTER
RWORK2   EQU   2                        WORK REGISTER
RWORK3   EQU   3                        WORK REGISTER
RCVT     EQU   3                        POINTS TO CVT
RTCB     EQU   4                        POINTS TO TCB
RTSB     EQU   5                        POINTS TO TSB           YS02019
RCONST   EQU   10                       POINTS TO PROGRAM       YS02019
*                                       CONSTANTS               YS02019
RBASE    EQU   12                       BASE REGISTER           YS02019
R13      EQU   13                       REGISTER 13
R14      EQU   14                       REGISTER 14
RLINK    EQU   14                       LINK REGISTER
R15      EQU   15                       REGISTER 15
RBRANCH  EQU   15                       BRANCH REGISTER
RCODE    EQU   15                       RETURN CODE REGISTER
         SPACE 2
********
******** DISPLACEMENT,LENGTH AND MASK EQUATES
********
BYTE1    EQU   8                        MASK FOR BYTE 1 IN REG.
BYTE2    EQU   4                        MASK FOR BYTE 2 IN REG.
BYTE3    EQU   2                        MASK FOR BYTE 3 IN REG.
BYTE4    EQU   1                        MASK FOR BYTE 4 IN REG.
BYTE34   EQU   3                        MASK FOR BYTES 3&4 IN REG.
BYTE234  EQU   7                        MASK FOR BYTES 2&3&4 IN REG.
D0       EQU   0                        DISPLACEMENT OF 0
D1       EQU   1                        DISPLACEMENT OF 1
D2       EQU   2                        DISPLACEMENT OF 2
D3       EQU   3                        DISPLACEMENT OF 3
D4       EQU   4                        DISPLACEMENT OF 4
D5       EQU   5                        DISPLACEMENT OF 5
D6       EQU   6                        DISPLACEMENT OF 6
D14      EQU   14                       DISPLACEMENT OF 14
L1       EQU   1                        LENGTH OF 1
L2       EQU   2                        LENGTH OF 2
L3       EQU   3                        LENGTH OF 3
L4       EQU   4                        LENGTH OF 4
L8       EQU   8                        LENGTH OF 8
L16      EQU   16                       LENGTH OF 16
L24      EQU   24                       LENGTH OF 24
L32      EQU   32                       LENGTH OF 32
RETCODE4 EQU   4                        RETURN CODE OF 4
RETCODE8 EQU   8                        RETURN CODE OF 8
RETCODEC EQU   12                       RETURN CODE OF 12
HIPRI    EQU   X'E4'                    TCAM POSTING PRIORITY
COMPLETE EQU   X'40'                    TO TEST POST COMPLETE BIT
CZERO    EQU   0                        TO TEST FOR ZERO
CONE     EQU   1                        CONSTANT OF 1
CTWO     EQU   2                        CONSTANT OF 2
CTHREE   EQU   3                        CONSTANT OF 3
CFIFTEEN EQU   15                       CONSTANT OF 15
CSIXTEEN EQU   16                       CONSTANT OF 16
CFOX     EQU   X'FF'                    TO TEST FOR 'FF'
BLANK    EQU   X'40'                    TO TEST FOR BLANK
OFF      EQU   X'FF'                    TO TURN BITS OFF
IDLE     EQU   X'17'                    TERM. CTL. CHAR. - IDLE
NL       EQU   X'15'                    TERM. CTL. CHAR. - NEW LINE
CR       EQU   X'0D'                    TERM. CTL. CHAR. - CARRIAGE RET
LF       EQU   X'25'                    TERM. CTL. CHAR. - LINE FEED
HEX18    EQU   X'18'                    TERM. CTL. CHAR. - TWX CHAR.
HEX20    EQU   X'20'                    TO TEST BIT 2 OF BYTE
LNSZ40   EQU   40                       LINE SIZE 40
LNSZ64   EQU   64                       LINE SIZE 64
LNSZ80   EQU   80                       LINE SIZE 80
LNNO     EQU   6                        LINES PER SCREEN - 6
LNNO12   EQU   12                       LINES PER SCREEN - 12
LNNO15   EQU   15                       LINES PER SCREEN - 15
QINEC    EQU   21                       QTIP ENTRY CODE/TCLEARQ/INPUT
QOUTEC   EQU   22                       QTIP ENTRY CODE/TCLEARQ/OUTPUT
         SPACE 3
         SPACE 3
********
******** ESTABLISH BASE REGISTER AND ADDRESSABILITY
********
         USING *,RBASE                  BASE REGISTER ADDRESSABILITY
         USING TSB,RTSB                 TSB ADDRESSABILITY
         USING CVT,RCVT                 CVT ADDRESSABILITY
         USING CONSTANT,RCONST          CONSTANT ADDRESSABILITY
         B     VALIDATE                 BR AROUND ID AND DATE   YS02019
         DC    C'IGG0940E'              MODULE IDENTIFIER       YS02019
         DC    X'3304'                  DATE - 10/31/73         YA03021
********
******** CHECK FOR VALID PARAMETER
********
VALIDATE EQU   *                                                YS02019
         LTR   RPARM1,RPARM1            ANY PARM SPECIFIED
         BNZ   PARMBAD                  YES, PARM INVALID       YS02019
********
******** WAS AUTO LINE NUMBERING OR AUTO CHAR. PROMPT PREVIOUSLY ON
********
         TM    TSBFLG2,TSBAUTON         AUTO PROMPTING NOW ON    Y01018
         BO    RETURN                   YES, DON'T RESTART       Y01018
         TM    TSBFLG2,TSBAULST         WAS AUTO LINE NO. ON
         BNZ   PUTITOUT                 YES,HAS A PUT BEEN      YS02019
*                                       INITIATED               YS02019
CKAUTOCP EQU   *
         TM    TSBSTAT,TSBDSPLY         IS THIS A DISPLAY TERMINAL
         BO    RETURN                   YES, DO NOTHING
         TM    TSBFLG2,TSBAUTOC         WAS CHAR. PROMPT ON BEFORE
         BNZ   PUTITOUT                 YES,HAS A TPUT BEEN INITIATED
         LA    RCODE,RETCODE8           NO,SET ERROR RETURN CODE
         BR    RLINK                    RETURN TO CALLER        YS02019
PUTITOUT EQU   *
         MODESET EXTKEY=TCAM            GET TCAM'S KEY          YS02019
         OI    TSBFLG2,TSBAUTON+TSBSTAUT INDICATE AUTO PROMPTING
*                                       STARTED,THIS IS THE 1ST PROMPT
         NI    TSBFLG3,OFF-TSBSPIT      ALLOW PROMPTING         YA03021
         TM    TSBFLG3,TSBTPUT          IS TSOUTPUT SCHEDULED   YS02019
*                                       TO GET CONTROL
         BO    GOBACK                   YES, DON'T TPOST        YS02019
         OI    TSBFLG3,TSBTPUT          INDIC TPUT SCHEDULED    YS02019
*                                       YS0201                  YS02019
         LA    RPARM1,TPMASK            ADDR OF TPOSTING MASK   YS02019
         L     R15,TPADDR               ADDR OF TPOSTING RTN    YS02019
         LR    RPARM0,RLINK             SAVE RETURN ADDRESS     YS02019
         BALR  R14,R15                  GO TPOST TSB TO         YS02019
*                                       SCHEDULE TSOUTPUT
         LR    RLINK,RPARM0             RETURN RETURN ADDRESS   YS02019
GOBACK   EQU   *
         MODESET EXTKEY=SUPR            GET KEY FOR SETLOCK     YS02019
RETURN   EQU   *
         SR    RCODE,RCODE              SET ZERO RETURN CODE    YS02019
         BR    RLINK                    RETURN TO CALLER        YS02019
PARMBAD  EQU   *
         LA    RCODE,RETCODE4           INDICATE BAD PARAMETERS
         BR    RLINK                    RETURN TO CALLER        YS02019
PATCH    DC    9F'0'                    PATCH AREA              YS02019
         SPACE 3
CONSTANT DSECT                          DEFINES CONSTANTS IN 1ST CSECT
ZERO     DC    F'0'                     CONSTANT OF 0
NINES    DC    F'99999999'              CONSTANT OF 99,999,999
FIFTEEN  DC    AL1(15)                  CONSTANT OF 15
TWO      DC    XL1'02'                  CONSTANT OF 2
TEN      DC    H'10'                    CONSTANT OF 10
SIXTEEN  DC    AL1(16)                  CONSTANT OF 16
ENABLE   DC    X'FF'                    TO ENABLE INTERRUPTS
SIX      DC    H'6'                     CONSTANT OF 6
EIGHT    DC    AL1(8)                   CONSTANT OF 8
CP5041   DC    XL3'026D16'              BKSP/UNDERSCORE CHAR. PROMPT
HIGHBIT  DC    XL1'80'                  TO TEST HIGH ORDER BITS
CPTWX    DC    XL3'024B0D'              PERIOD/CAR.RETURN CHAR. PROMPT
TPUT1    DC    XL4'0E000000'            TPUT,HOLD,CONTROL
TPUT2    DC    XL4'0A000000'            TPUT,CONTROL
ONE      DC    AL1(1)                   CONSTANT OF ONE         YS02019
XIDLE    DC    X'17'                    TPUT IDLE CHARACTER     YA00364
TPADDR   DC    V(POSTTSB)               ADDR OF TPOSTING RTN    YS02019
TPMASK   DS    4F                       TPOST QCB TO ITSELF     YS02019
         EJECT
CVT      DSECT
         CVT
         EJECT
         IHAPSA
         EJECT
         IKJTSB
         END
