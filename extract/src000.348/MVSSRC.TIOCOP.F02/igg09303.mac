IGG09303 CSECT
         TITLE 'IGG09303 - SECOND LOAD OF TGET'
***********************************************************************
*                                                                     *
*                  RELEASE 2, VS/2 DELETIONS                          *
*                                                                     *
*D006000-022000,078600-086000,118000-128000,142000,166000       YS02019
*D174000,202000-204000,212000-224000,250000,254000              YS02019
*D302000-316400,586080-602000,662180-663840                     YS02019
*                                                               YS02019
*                                       05/30/73                YM01246
*                                       10/11/73                YM03998
*0795768860                                                      S22028
*                  VS/2  RELEASE 3.7 CHANGES                          *
*                                                                     *
*A 504000,525000                                                ZA28576
*C 482000,506000                                                ZA28576
*A280200,559500                                                 ZA16627
*
*
* STATUS -                                                            *
*    CHANGE LEVEL 003                                                 *
*                                                                     *
* FUNCTION -                                                          *
*                                                                     *
*    THE FUNCTION OF IGG09303 IS TO LEAVE ALL WAIT STATES THAT        *
*    THE FREEING OF TS BUFFERS BY THIS TGET WILL ALLOW.               *
*                                                                     *
* ENTRY POINTS -                                                      *
*                                                                     *
*         IGG09303 - ENTRY POINT FROM IGC0009C VIA BRANCH             *
*                                                                     *
* INPUT -                                                             *
*                                                                     *
*    AT TIME OF ENTRY THE FOLLOWING REGISTERS HAVE USABLE             *
*         3- CVT POINTER, 4- TCB POINTER, 5- SVRB POINTER,            *
*         8- ASCB POINTER, 9- TSB POINTER, 10- TIOCRPT POINTER,       *
*    ALSO AT TIME OF ENTRY, THE FIELDS 'XSAPRM0,' AND XSAPRM1,'       *
*    IN THE SVRB EXTENDED SAVE AREA, CONTAIN, RESPECTIVELY, THE       *
*    RETURN CODE FROM IGC0009C AND THE AMOUNT OT DATA MOVED.          *
*                                                                     *
* OUTPUT                                                              *
*                                                                     *
*    OUTPUT IS RETURN CODE IN REGISTER 15 AND COUNT OF DATA IN 1      *
*                                                                     *
* EXTERNAL REFERENCES                                                 *
*                                                                     *
*         CVT0PT01 - POST ROUTINE                                     *
*         SETLOCK                                                     *
*         STATUS                                                      *
*                                                                     *
* EXITS, NORMAL                                                       *
*                                                                     *
*    BRANCH BACK TO SUPERVISOR VIA REGISTER 14                        *
*                                                                     *
* EXITS,ERROR -                                                       *
*                                                                     *
*    THERE ARE NO ERROR EXITS FROM THIS ROUTINE                       *
*                                                                     *
* TABLES/WORK AREAS -                                                 *
*                                                                     *
*    THESE ARE DESCRIBED BY DSECTS AT THE END OF THE LISTING          *
*                                                                     *
* ATTRIBUTES -                                                        *
*                                                                     *
*    REENTRANT, REUSABLE, PRIVILEGED, KEY 0, HOLDS THE LOCAL AND      *
*    THE CMS LOCKS AT ENTRY                                           *
*                                                                     *
* CHARACTER CODE DEPENDENCY -                                         *
*                                                                     *
*    NONE                                                             *
*                                                                     *
* NOTES -                                                             *
*                                                                     *
*    NONE                                                             *
*                                                                     *
***********************************************************************
***
***                 REGISTER EQUATES
***
R0       EQU   0                        USED TO SPEED ASM
RP1      EQU   1                        PARM REG - LENGTH RETURN
RAVT     EQU   2                        POINTER TO AVT
RCVT     EQU   3                        POINTER TO CVT
RTCB     EQU   4                        POINTER TO TCB
RSVRB    EQU   5                        POINTER TO SVRB
RBASE    EQU   6                        BASE REGISTER
RTSI     EQU   7                        POINTER TO TSINPUT QCB
RASCB    EQU   8                        POINTER TO ASCB         YS02019
RTSB     EQU   9                        POINTER TO TSB
RRPT     EQU   10                       POINTER TO TIOCRPT
RECB     EQU   11                       ECB POINTER FOR POST
RTCX     EQU   11                       POINTER TO TCX          YS02019
RRCB     EQU   12                       POINTER TO RCB
RSAVE    EQU   13                       WORK REGISTER
RTJID    EQU   13                       TJID FOR POST
RETURN   EQU   14                       RETURN ADDRESS
RWRK     EQU   14                       WORK REGISTER
RWRK1    EQU   15                       WORK REGISTER
RGO      EQU   15                       GO TO ADDRESS
RCODE    EQU   15                       RETURN CODE REGISTER
         SPACE 2
***
***                 OTHER EQUATES
***
K0       EQU   0                        ZERO DISPLACEMENT
K1       EQU   1                        OFFSET AND CONSTANT
K3       EQU   3                        LENGTH OF ADDRESS
K4       EQU   4                        LENGTH OF WORD
K5       EQU   5                        OFFSET CONSTANT
NDISPBIT EQU   X'800'                   OWAIT NONDISP BIT       YS02019
STEPSD   EQU   8                        ENTRY CODE FOR 'STATUS' YS02019
TIMES4   EQU   2                        SHIFT CONSTANT          YS02019
OFF      EQU   X'FF'                    USED TO TURN BITS OFF
         SPACE 2
         BALR  RBASE,R0                 SET BASE REGISTER
         USING *,RBASE                  ADDRESSABILITY
         B     TGT0050                  BRANCH AROUND ID/DATE   YS02019
         DC    C'IGG09303'              MODULE ID               YS02019
         DC    X'3319'                  JULIAN DATE 11/15/73    YM05387
         USING TIOCRPT,RRPT
         USING IEDQAVTD,RAVT
         USING CVT,RCVT
         USING TCB,RTCB
         USING IEDQTSI,RTSI
         USING ASCB,RASCB                                       YS02019
         USING TSB,RTSB
         USING RBSECT,RSVRB                                      M0094
         SPACE 2
***********************************************************************
***                                                             YS02019
***            THIS MODULE IS BRANCH ENTERED FROM IGC0009C      YS02019
***            HOLDING BOTH THE CMS AND THE LOCAL LOCKS         YS02019
***                                                             YS02019
***********************************************************************
         SPACE 3                                                YS02019
TGT0050  EQU   *                                                YS02019
***
***                 DETERMINE WHETHER ANYTHING TO DO
***
         TM    CVTTCMFG,CVTTCRDY        IS TCAM STILL AROUND    YS02019
         BNO   TGT3000                  IF NOT, DO NOTHING      YS02019
         L     RWRK,CVTAQAVT            GET POINTER TO TCX      YS02019
         L     RWRK,TCXRPT-IEDQTCX(,RWRK) GET TIOCRPT POINTER   YS02019
         LTR   RWRK,RWRK                SEE IF TSO STILL UP     YS02019
         BZ    TGT3000                  NOT UP, DO NOTHING      YS02019
         NC    TIOCNFBF,TIOCNFBF        ANY FREE TS BUFFERS
         BZ    TGT3000                  NO, RETURN TO USER
         TM    TIOCFLG,TIOCTJBF+TIOCNOBF ANY TPUTS WAITING FOR BFRS
         BZ    TGT2000                  NO, GO CHECK LWAIT
         SPACE 2
***********************************************************************
*****                                                             *****
*****          WAKE UP ALL TPUTS WAITING FOR BUFERS               *****
*****                                                             *****
***********************************************************************
         SPACE 2
         LR    RRCB,RTSB                SAVE TSB REG            ZA16627
         L     RTSB,TIOCTSB-K1          START OF TSB TABLE
         LH    RSAVE,TIOCNTSB           NO. TSB'S IN TABLE      YS02019
         SR    RWRK1,RWRK1
         IC    RWRK1,TIOCTSBS           SIZE OF TSB'S
TGT0100  EQU   *
         TM    TSBFLG1,TSBTJBF          IS THIS TSB WAITED ON
         BNO   TGT0300                  IF NOT
         NI    TSBFLG1,OFF-TSBTJBF      TURN OFF NO BFR INDR
         SPACE 1
***
***                 GO TO POST ROUTINE TO ACTIVATE WAITING TASK
***
         SPACE 2
         STM   RRPT,RWRK1,XSAUBFRP      SAVE REGISTERS           M0094
         SR    RRPT,RRPT                COMPLETION CODE FOR POST
         LA    RECB,TSBECB              ECB POINTER
         LH    RAVT,TSBWTJID            ASID OF POSTED USER     YS02019
         SLA   RAVT,TIMES4              MULTIPLY BY FOUR        YS02019
         L     RGO,CVTASVT              GET ASVT POINTER        YS02019
         L     RSAVE,ASVTENTY-ASVT-L'ASVTENTY(RGO,RAVT)         YS02019
*                                       GET ASCB ADDRESS        YS02019
         O     RECB,TJIDN               TURN ON HIGH-ORDER BIT   S21A12
         L     RGO,CVT0PT01             POST ROUTINE ADDR
         LA    RRCB,CVTBRET             'ERRET' ADDRESS         YS02019
         SPACE 2
***********************************************************************
***                                                                 ***
***      CONTENTS OF REGISTERS TO BE USE BY POST ROUTINE            ***
***                                                                 ***
***      10- COMPLETION CODE (ZERO), 11- ADDRESS OF TSBECB WITH     ***
***      HIGH-ORDER BIT ON, 12- ERRET ADDR, 13- ASCB OF TARGET      ***
***      TASK, 14 - RETURN ADDRESS, 15 - ADDRESS OF POST ROUTINE    ***
***                                                                 ***
***********************************************************************
         SPACE 2                                                      *
         BALR  RETURN,RGO               GO TO POST ROUTINE
         SPACE 2
***
***                 UPON RETURN FROM POST
***
         LM    RRPT,RWRK1,XSAUBFRP      RESTORE REGISTERS        M0094
         SPACE
TGT0300  EQU   *
***
***            CHECK FOR NORMAL TPUT WAITING FOR BUFFERS
***
         TM    TSBSTAT,TSBNOBUF         IS THIS TSB IN OWAIT
         BNO   TGT1600                  IF NOT
         L     RASCB,TSBASCBA           GET ASCB ADDRESS        YS02019
         SPACE 2
***
***                 REMOVE OWAIT
***
         NI    TSBSTAT,OFF-TSBNOBUF     TURN OFF INDICATOR
         SPACE 3
***                GO TO 'STATUS' ROUTINE TO RESET NONDISPATCHABILITY
***                OF ALL TASKS IN THIS TSB'S STEP THAT ARE IN OWAIT
***
***                REGISTERS FOR 'STATUS' -
***
***            0 (R0) - HIGH-ORDER = ASID, LOW= ENTRY CODE (8)
***            1 (RP1)- HIGH BIT ON TO INDICATE 'RESET'
***            13 (RSAVE)-MASK OF SECONDARY BITS TO RESET       ZA28576
***            14,15- RETURN, ENTRY
         SPACE 3                                                YS02019
         LA    R0,STEPSD                ENTRY CODE              YS02019
         L     RP1,TJIDN                HIGH-ORDER BIT ONLY ON  YS02019
         ICM   R0,B'1100',ASCBASID      ASID OF TARGET STEP     YS02019
         L     RGO,CVTABEND             SECONDARY CVT POINTER   YS02019
         USING SCVTSECT,RGO                                     YS02019
         LR    RAVT,RSAVE               SAVE REG 13             ZA28576
         LA    RSAVE,NDISPBIT           BIT TO RESET            ZA28576
         L     RGO,SCVTSTAT             ADDRESS OF STATUS RTN   YS02019
         DROP  RGO                                              YS02019
         BALR  RETURN,RGO               GO TO STATUS ROUTINE    YS02019
         LR    RSAVE,RAVT               RESTORE REG 13 VALUE    ZA28576
         SPACE 2
TGT1600  EQU   *
***
***                 PREPARE TO LOOK AT NEXT TSB
***
         SR    RWRK1,RWRK1              ZERO REGISTER           YM03998
         IC    RWRK1,TIOCTSBS           RELOAD TSB SIZE (SAVED  YM03998
*                                       BEFORE WENT TO POST)    YM03998
         AR    RTSB,RWRK1               UPDATE TSB POINTER
         BCT   RSAVE,TGT0100            CHECK NEXT TSB, IF ANY
         SPACE 2
***
***                 TURN OFF SYSTEM WAIT INDICATORS
***
         LR    RTSB,RRCB                 RESTORE TSB POINTER    ZA16627
         NI    TIOCFLG,OFF-(TIOCNOBF+TIOCTJBF)
         SPACE 2
TGT2000  EQU   *
***********************************************************************
*****                                                             *****
*****               CAN HELD TCAM BUFFERS BE REMOVED FROM WAIT    *****
*****               QUEUE, AND CAN SYSTEM BE REMOVED FROM LWAIT   *****
*****                                                             *****
***********************************************************************
         SPACE 2
         TM    TIOCFLG,TIOCSYLW         IS SYSTEM IN LWAIT
         BNO   TGT3000                  NO, RETURN TO USER
         CLC   TIOCNFBF,TIOCUSLW        CAN SYS LWAIT BE REMOVED
         BL    TGT3000                  NO, RETURN TO USER
         SPACE 2
***
***                 TPOST THE TSB TO IEDAYP WITH TSBTPAYI ON,   YS02019
***                 TO INDICATE TSINPUT SHOULD BE POSTED.       YS02019
***                 ONLY TSINPUT MAY REMOVE SYSTEM LWAIT        YS02019
***                                                             YS02019
         L     RWRK,TSBTPFLG            GET OLD FLAGS           YS02019
TGT2050  EQU   *                                                YS02019
         LR    RWRK1,RWRK               COPY                    YS02019
         O     RWRK1,TPOSTBTS           SET BITS IN COPY        YS02019
         CS    RWRK,RWRK1,TSBTPFLG      SWAP NEW BITS FOR OLD   YS02019
         BNE   TGT2050                  TRY AGAIN IF CHANGED    YS02019
         LTR   RWRK,RWRK                WAS TSBPOSTO ON         YS02019
         BM    TGT3000                  YES, NEEDN'T TPOST, BE- YS02019
*                                       CAUSE ALREADY TPOSTED   YS02019
         SPACE 2                                                YS02019
***
***                 TPOST THE TSB TO IEDAYP
***
         L     RTCX,CVTAQAVT            GET TCX POINTER         YS02019
         USING IEDQTCX,RTCX                                     YS02019
         L     RTSI,TCXTSI              GET TSI POINTER         YS02019
         USING IEDQTSI,RTSI                                     YS02019
         LA    RWRK,TSITSAP-(QCBSTVTO-IEDQQCB)                  YS02019
*                                       ADDR OF AYP'S 'QCB'     YS02019
         ST    RWRK,TSBRQCB             QCB TO WHICH TSB IS     YS02019
*                                       BEING TPOSTED           YS02019
         L     RWRK,TCXREADY            CURRENT READY Q PTR     YS02019
TGT2100  EQU   *                                                YS02019
         ST    RWRK,TSBLINKA            LINK TSB TO NEXT ELEM   YS02019
         MVI   TSBPRI,PRIBFRTB          SET PRIORITY            YS02019
         LA    RWRK1,TSBRCB             ADDRESS OF NEW ELEMENT  YS02019
         CS    RWRK,RWRK1,TCXREADY      UPDATE READY Q ANCHOR   YS02019
         BNE   TGT2100                  AGAIN IF NO GOOD        YS02019
         SPACE 3                                                YS02019
***                                                             YS02019
***                 XM POST TCAM'S ECB                          YS02019
***                                                             YS02019
         SR    RRPT,RRPT                COMPLETION CODE IN 10   YS02019
         L     RAVT,TCXAVT              GET AVT POINTER         YS02019
*                   NOTE - AVT ISN'T REALLY ADDRESSABLE         YS02019
         L     RSAVE,TCXASCB            GET ASCB POINTER        YM03998
         DROP  RTCX                     TCX NOT ADDRESSABLE     YM03998
         LA    RECB,AVTOSECB            ADDRESS OF ECB          YS02019
         LA    RRCB,CVTBRET             'ERRET' ADDRESS         YS02019
         L     RGO,CVT0PT01             POST ROUTINE ADDRESS
         O     RECB,TJIDN               SHOW X-M REQUEST        YS02019
         BALR  RETURN,RGO               GO TO POST ROUTINE
         SPACE 2
TGT3000  EQU   *
         L     RP1,XSAPRM0              RESTORE DATA COUNT       M0094
         L     RCODE,XSAPRM1            RESTORE RETURN CODE      M0094
         TM    XSAFLAG,XSATCBFX         SHOULD WE TURN OFF TCBFX M0094
         BZ    TGT4000                  NO,SKIP
         NI    TCBFLGS1,OFF-TCBFX       ALLOW ASYNCHRONOUS EXITS
TGT4000  EQU   *
         NI    TCBTSFLG,OFF-TCBTIOTG    ATTN EXIT INDR
         SETLOCK RELEASE,TYPE=CMS,RELATED=(TSB,IGC0009C(TGT0350))
         SETLOCK RELEASE,TYPE=LOCAL,RELATED=(TSB,IGC0009C(TGT0350))
         L     RETURN,XSARETRG          RESTORE RETURN ADDRESS  YS02019
         BR    RETURN                   RETURN TO SUPERVISOR    YS02019
         SPACE 2
***
***                 CONSTANTS
***
KZERO    DC    F'00'                    ZERO COMPARATOR
TJIDN    DC    X'80000000'              FOR INTERREGION POST REQ S21A12
TPOSTBTS DC    AL1(TSBTPAYI+TSBPOSTO)   BITS FOR IEDAYP         YS02019
         DC    AL3(0)                   FILL OUT FULLWORD       YS02019
PATCHREA DC    10F'0'                   PATCH AREA               Y01018
         EJECT
         IHAASCB                                                YS02019
         EJECT                                                  YS02019
         IHAASVT                                                YS02019
         EJECT                                                  YS02019
         TAVTD                                                  YS02019
         EJECT                                                  YS02019
CVT      DSECT
         CVT
         EJECT
         IHAPSA                                                 YS02019
         EJECT                                                  YS02019
         TPRIOR                                                 YS02019
         EJECT                                                  YS02019
         TQCBD                                                  YS02019
         EJECT                                                  YS02019
         IKJRB
***
***      EXTENDED SAVE AREA.  THSES STATEMENTS SHOULD FOLLOW
***      THE MACRO 'IKJRB.'
***
         ORG   RBEXSAVE                 START OF SAVE AREA       M0094
XSAPRM0  DS    0F                       REG 0 AT ENTRY           M0094
XSAPRMTJ DS    H                        TJID PASSED              M0094
XSAPRMSZ DS    H                        SIZE PASSED              M0094
XSAPRM1  DS    0F                       REG 1 AT ENTRY           M0094
XSAOPTNS DS    X                        USER OPTIONS             M0094
MASIS    EQU   X'01'                    ASIS OPTION SPECIFIED    M0094
MCNTRL   EQU   X'02'                    1 MEANS CONTROL          M0094
*
*   BOTH OF THE ABOVE BITS OFF MEANS EDIT OPTION
*
MBREAK   EQU   X'04'                    BREAKIN SPECIFIED        M0094
MHOLD    EQU   X'08'                    1 MEANS HOLD             M0094
MTGET    EQU   X'80'                    1 MEANS TGET REQUESTED   M0094
MILLEGAL EQU   X'40'                    RESERVED BIT             M0094
MLOWP    EQU   X'20'                    0 HIGHPRI, 1 LOWPRI      M0094
MWAIT    EQU   X'10'                    1 MEANS NOWAIT           M0094
         DS    AL3                      REST OF REGISTER ONE     M0094
XSAWD3   DS    0F                       FOR SAVING REGISTERS     M0094
XSAFLAG  DS    X                        FLAG PASSED BY 9C TO 01  M0094
*                                       AND 02                   M0094
XSATCBFX EQU   X'20'                    TURN OFF TCBFX AT EXIT   M0094
         DS    AL3                      REST OF WORD             M0094
         ORG   XSAWD3+2                 INPUT ED, OUTPUT MOVE    M0094
XSAUBFRS DS    H                        AMT DATA LEFT TO MOVE    M0094
XSAUBFRP DS    0F                       LOC DATA LEFT TO MOVE    M0094
XSAESTAL DS    CL16                     LIST FOR ESTAE MACRO    YS02019
         ORG   XSAUBFRP+4               USED FOR SAVE AREA WHEN YS02019
*                                       NOT NEEDED BY ESTAE     YS02019
XSAWD5   DS    F                        FOR SAVING REGISTERS     M0094
XSAWD6   DS    F                        FOR SAVING REGISTERS     M0094
         ORG   XSAWD5                   USE WORDS 5-6 FOR NAME   M0094
XSAENQNM DS    CL8                      ENQ NAME FOR TJID TPUT   M0094
XSAWD7   DS    F                        FOR SAVING REGISTERS     M0094
XSAWD8   DS    F                        FOR SAVING REGISTERS     M0094
         ORG   XSAWD7                   USE WDS 7-8 FOR ENQ PRMS M0094
XSAENQAD DS    2F                       WORK AREA FOR ENQ/DEQ    M0094
XSAUSERP DS    0F                       PTR TO USER ID, IF PRES YS02019
XSAWD9   DS    F                        FOR SAVING REGISTERS     M0094
XSARETRG DS    0F                       RETURN ADDRESS          YS02019
XSAWD10  DS    F                        FOR SAVING REGISTERS     M0094
         DS    CL1                      RESERVED                YS02019
XSAUSERK DS    CL1                      SVC CALLER'S KEY        YS02019
XSARC    DS    H                        RETURN CODE SAVE AREA   YS02019
XSATJID  DS    H                        CALLER'S TJID            M0094
XSAFLAG2 DS    X                        FLAGS                   YM05387
XSAATR   EQU   X'80'                    NEXT CHAR IN TS BUFFER   S22028
*                                       IS A 3270 ATTRIBUTE BYTE
XSABFRAL EQU   X'40'                    TIOC BUFFER HAS BEEN    YM05387
*                                       ALLOCATED USED BY ESTAE YM05387
XSA15D   EQU   X'20'                    CHANGE COMPLETION CODE  YM05387
*                                       TO 15D IN ESTAE         YM05387
*                  NEXT 5 BITS UNUSED                           YM05387
XSAF     DS    X                        FLAGS USED BY TPUT. INI- M0094
*                                       TIALIZED FROM XSAFLAG.
HSW      EQU   X'80'                    HEADER BFR DONE          M0094
PSW      EQU   X'40'                    NEED TO TPOST QCB        M0094
*SATCBFX EQU   X'20'                    TURN OFF TCBFX AT EXIT
OSW      EQU   X'10'                    TSBOWIP TURNED ON BY US  M0094
*                                       (NOT SOME OTHER TPUT)
XSADMOVE EQU   X'08'                    DATA IS MOVING          YS02019
XSAAUTH  EQU   X'04'                    USER IS AUTHORIZED      YS02019
XSAIDENQ EQU   X'02'                    ASID TPUT ASID ENQUEUED YS02019
XSAPARTO EQU   X'01'                    QCBPARTO IS SET ON      YM01246
         EJECT
         IHASCVT
         EJECT
         IKJTCB                                                 YS02019
         EJECT                                                  YS02019
         TTCXD                                                  YS02019
         EJECT                                                  YS02019
         IKJTIOCP                                               YS02019
         EJECT                                                  YS02019
         IKJTSB                                                 YS02019
         EJECT
         TTSID
         END   ,                                                YS02019
