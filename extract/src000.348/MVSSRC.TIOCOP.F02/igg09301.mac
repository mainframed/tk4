         TITLE 'IGG09301 - SECOND LOAD OF TPUT'
IGG09301 CSECT
*                                                                     *
*            VS2 3.7
*  C 535560,535630                                              ZA16599
*  C 301842-301942,305877-305926,305942,305944,307000           ZA14769
*  C 308000-309000  D 313000-319000                             ZA14769
*0000227500,305886,305843                                      @ZA08262
*0000522354                                                    @ZA03182
*0000680520                                                    @ZA03945
*0000535010-535120                                             @ZA02244
*
*
*    RELEASE VS2-3.0 ACTIVITY                                         *
*D001760                                                        ZA02097
*C227500,238900                                                 ZA02097
*                                                                     *
*    RELEASE VS/2-2 ACTIVITY                                          *
*                                       02/19/73                VS00022
*                                       03/24/73                YM01564
*                                       05/30/73                YM01246
*                                       07/03/73                YM01114
*                                       07/16/73                YM01176
*                                       10/11/73                YM03998
*                                       11/15/73                YM05387
*                                       04/11/74                YM08378
*                                                                     *
*D500000                                                         YM3028
*D272800-294000,299700-300000                                    YM0895
*1301                                                            M0126
*                                                                S21A12
*1301182000-185000,193000-208000,227300-227600,229000,234000-    M0094
*1301235000,257000,271500-275000,293000,326000,335000,346750-    M0094
*1301346800,348600-349200,353000,378000-384000,418000,422000,    M0094
*1301449000,462000-462500,496000,500000,509000-512000,521000,    M0094
*1301529000,538000,544200-544300,545300-546300,548000,571000,    M0094
*1301577000-578000,581000,582000,587000,590000,605000,631000,    M0094
*1301674000,676300,722000                                        M0094
*1301349000,546000,706000-707000                                 S21008
*                                                                M2152
*0000455100-456000,547800                                        M0019
*0000158100,158700,382200                                        TS1635
*                                                                M0661
*                                                                M3391
*                                                                 M3807
*                                                                 M3830
*                                                                M4395
*                                                                 M5113
*                                                                 M4415
*0000530000                                                      M3830
*                                                                M6024
*1301722620                                                      S22028
*
* STATUS -                                                            *
*           VS2    3.0  SU                                     @ZA02244
*                                                                     *
* FUNCTION -                                                          *
*                                                                     *
*   PUT OUTPUT FROM USER'S BUFFER INTO TSO BUFFERS FOR LATER          *
*   TRANSFERENCE, VIA TCAM, TO A TERMINAL.                            *
*                                                                     *
*   PROVIDE LIMITED EDITING OPTIONS                                   *
*                                                                     *
*                                                                     *
* ENTRY POINTS -                                                      *
*                                                                     *
*   IGG09301 - ENTRY FROM FIRST LOAD OF TERMINAL I/O SVC              *
*              ROUTINES (IGC0009C) VIA BRANCH                         *
*                                                                     *
* INPUT -                                                             *
*                                                                     *
***********************************************************************
***                                                                 ***
***                 REGISTER CONTENTS UPON ENTRY                    ***
***      3 - CVT POINTER      8 - ASCB POINTER                      ***
***      4 - TCB POINTER      9 - TSB POINTER       12 - TJID       ***
***      5 - SVRB POINTER    10 - TIOCRPT POINTER                   ***
***                                                                 ***
***                                                                 ***
***********************************************************************
         SPACE 2
* OUTPUT -                                                            *
*                                                                     *
*   RETURN CODE IN REG15                                              *
*              00 - SUCCESSFUL                                        *
*              04 - NOWAIT SPECIFIED AND NO BFRS AVAILABLE            *
*              08 - ATTN OCCURED DURING SVC EXECUTION                 *
*              10   INVALID PARAMETERS SPECIFIED                      *
*              14 - USER HAS HUNG UP
*                                                                     *
* EXTERNAL REFERENCES -
*                                                                     *
*         OPTIMIZER (VIA SYSEVENT MACRO)                              *
*         SETLOCK                                                     *
*         POST (VIA BRANCH ENTRY AT CVTOPT01)                         *
*                                                                     *
* EXITS,NORMAL -                                                      *
*                                                                     *
*    BR 14, WITH RETURN CODES AS ABOVE                                *
*                                                                     *
* EXITS,ERROR -                                                       *
*                                                                     *
*   THERE ARE NO ERROR EXITS FROM THIS ROUTINE.                       *
*                                                                     *
* TABLES/WORKAREAS -                                                  *
*                                                                     *
*   ALL TABLES AND WORK AREAS ARE DESCRIBED BY                        *
*   DSECTS AT THE END OF THE LISTING .                                *
*                                                                     *
* ATTRIBUTES -                                                        *
*                                                                     *
*   REENTRANT, ENABLED, PRIVILEGED, LOCAL LOCK IS HELD AT ENTRANCE,   *
*   CMS LOCK IS OBTAINED EXPLICITLY, KEY ZERO.                        *
*                                                                     *
* CHARACTER CODE DEPENDENCY -                                         *
*                                                                     *
*    THE OPERATION OF THIS MODULE DEPENDS UPON THE FOLLOWING          *
*    PROPERTIES OF THE INTERNAL REPRESENTATION OF THE EXTERNAL        *
*    CHARACTER SET- BACKSPACE EQUAL TO X'16', BLANK EQUAL TO          *
*    X'40', NEW LINE EQUAL X'15'.                                     *
*    THE SPECIFIC INSTRUCTIONS WHICH REQUIRE MODIFICATION IF          *
*    THESE PROPERTIES OF THE CHARACTER SET ARE CHANGED MAY BE         *
*    IDENTIFIED BY - BLANK, BKSPCHR, AND NEWLINE, RESPECTIVELY.       *
*                                                                     *
* NOTES -                                                             *
*                                                                     *
*    NONE                                                             *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*****                                                             *****
*****               REGISTER EQUATES                              *****
*****                                                             *****
***********************************************************************
         SPACE
R0       EQU   0                        USED TO SPEED ASSEMBLY
RSAVE    EQU   0                        WORK REGISTER
RWRK2    EQU   1                        WORK REGISTER
RBUF     EQU   2                        POINTER TO TS BUFFER
RCVT     EQU   3                        POINTER TO CVT
RTCB     EQU   4                        POINTER TO TCB
RSVRB    EQU   5                        POINTER TO SVRB
RBASE    EQU   6                        BASE REGISTER
RTCX     EQU   7                        TCX POINTER             YS02019
RASCB    EQU   8                        ASCB POINTER            YS02019
RTSB     EQU   9                        POINTER TO TSB
RRPT     EQU   10                       POINTER TO TIOCRPT
RECB     EQU   11                       ADDR OF ECB TO BE POSTED
RTJID    EQU   12                       TJID
RAVT     EQU   13                       POINTER TO AVT
RETURN   EQU   14                       RETURN ADDRESS
RWRK     EQU   14                       WORK REGISTER
RWRK1    EQU   15                       WORK REGISTER
RCODE    EQU   15                       RETURN CODE
RGO      EQU   15                       BRANCH ADDRESS
         SPACE 2
***
***                 CONSTANTS
***
K0       EQU   0                        OFFSET
K1       EQU   1                        OFFSET AND SIZE
K2       EQU   2                        OFFSET AND SIZE
K3       EQU   3                        OFFSET AND SIZE
K4       EQU   4                        OFFSET AND SIZE
K5       EQU   5                        OFFSET
K8       EQU   8                        OFFSET
K16      EQU   16                       SHIFT CONSTANT
SDTCB    EQU   15                       ENTRY CODE FOR STATUS   YS02019
***
***                 RETURN CODES
***
RC04     EQU   X'04'                    NOWAIT AND NO BUFFERS
RC08     EQU   X'08'                    ATTENTION HIT
RC10     EQU   X'10'                    INVALID PARAMETER
RC14     EQU   X'14'                    DISCONNECTED
***
***                 MASKS
***
NDISPBIT EQU   X'400'                   MASK FOR 'STATUS'       YS02019
ECBWAIT  EQU   X'80'                    ECB WAITING
BYPASS   EQU   X'24'                    BYPASS CHAR            @ZA02244
OFF      EQU   X'FF'                    TO TURN OFF SELECTED BIT
ON       EQU  1                         FOR BCR INSTRUCTIONS    YS02019
***
***                 OTHER EQUATES
***
BKSPCHAR EQU   X'16'                    BACKSPACE CHARACTER
BLANK    EQU   X'40'                    BLANK CHARACTER
RGOTO    EQU   15                       ENTRY POINT
MOVECODE EQU   19                       QTIP ENTRY CODE FOR MOVE
SEVEN    EQU   7                        MASK USED IN STCM        Y01018
STEPSD   EQU   8                        ENTRY CODE FOR 'STATUS' YS02019
         EJECT
*
***********************************************************************
*
*        THIS MODULE IS BRANCH ENTERED FROM IGC0009C HOLDING THE
*        LOCAL LOCK
*
***********************************************************************
*
         BALR  RBASE,R0                 SET BASE REGISTER
         USING *,RBASE                  ADDRESSABILITY          YS02019
         SPACE 2
         USING CVT,RCVT
         USING TCB,RTCB
         USING ASCB,RASCB                                       YS02019
         USING TSB,RTSB                                         YS02019
         USING TIOCRPT,RRPT                                     YS02019
         USING RBSECT,RSVRB                                      M0094
         SPACE 3
***
***                INITIALIZE
***
         MVC   XSAF,XSAFLAG             INIT FLAGS               M0094
         B     TPT0050                  BRANCH AROUND ID         Y01018
         DC    C'IGG09301'              MODULE IDENTIFIER        Y01018
         DC    X'6007'                  DATE - 01/07/76        @ZA08262
TPT0050  EQU   *                                                 Y01018
         STH   RTJID,XSATJID            SAVE CALLER'S TJID       M0094
         SR    RBUF,RBUF
         LR    RCODE,RBUF               SET RETURN CODE TO ZERO
         CH    RBUF,XSAPRMSZ            IS USER SIZE ZERO        M0094
         BE    TPT9750                  RETURN TO CALLER
         STH   RBUF,XSAPRMTJ            ZERO DESTINATION TJID    M0094
*                                       SO GP DOESN'T SET BUFF-
*                                       TJID
         SPACE 2
         LH    RWRK1,XSAPRMSZ           GET USER SPECD SIZE      M0094
         L     RWRK,XSAPRM1             GET PTR TO USER AREA     M0094
         LA    RWRK,0(,RWRK)            CLEAR HI-ORDER BYTE      M0094
         OI    XSAF,XSADMOVE+XSAAUTH    SHOW DATA MOVEMENT,     YM05387
*                                       TELL GP NOT TO ADD '+'  YM05387
         TM    XSAOPTNS,MCNTRL+MASIS    OTHER THAN EDIT          M0094
         BNZ   TPT0390                  IF ASIS OR CONTROL      ZA02097
         SPACE 2
***
***                 REMOVE TRAILING BLANKS
***
         AR    RWRK,RWRK1               PTR TO END OF BFR + ONE
         SPACE
TPT0300  EQU   *
         BCTR  RWRK,R0                  ADJUST BUFFER POINTER
         CLI   K0(RWRK),BLANK           IS CHARACTER A BLANK
         BNE   TPT0350                  IF NON-BLANK
         BCT   RWRK1,TPT0300            SUBTRACT 1 FROM BUFFER  YS02019
*                                       SIZE, BRANCH IF NOT 0   YS02019
         B     TPT7500                  SIZE IS NOW 0, MSG MUST YS02019
*                                       HAVE BEEN ALL BALNKS    YS02019
         SPACE 2
TPT0350  EQU   *
         STH   RWRK1,XSAPRMSZ           STORE UPDATED SIZE       M0094
         SPACE 2                                                 Y01018
TPT0390  EQU   *                                                 Y01018
         STH   RWRK1,XSAUBFRS           STORE STARTING SIZE      Y01018
         L     RWRK1,XSAPRM1            GET STARTING ADDRESS    YS02019
         ST    RWRK1,XSAUBFRP           STORE IT                YS02019
         SPACE 2
***
***                 CHECK FOR OWAIT IN PROGRESS
***
*                                       SERIALIZE TSB CHANGES   YS02019
         SETLOCK OBTAIN,TYPE=CMS,MODE=UNCOND,                   YS02019*
               RELATED=(TSB,IGG09301(TPT9700))                  YS02019
         SPACE 1                                                YM01176
TPT0100  EQU   *                                                YM01176
         BAL   RBUF,TPT8400             GO CHECK TERMINAL STATUS
         TM    TSBFLG1,TSBOWIP          IS OWAIT IN PROGRESS
         BZ    TPT0200                  IF NOT
         SPACE 2
*****
*****               SET UP FOR OWAIT SUBROUTINE
*****
TPT0150  EQU   *
         TM    XSAOPTNS,MWAIT           WAS WAIT SPECIFIED (0)   M0094
         BO    TPT8000                  IF NOT
         OI    TSBFLG1,TSBWOWIP         WAITING ON OWAIT INDR
         BAL   RBUF,TPT6000             GO TO OWAIT SUBROUTINE
         SPACE 2
***
***                 UPON RETURN FROM OWAIT SUBROUTINE
***
         SPACE
         B     TPT0100                  CHECK AGAIN
         SPACE 2
TPT0200  EQU   *
         NI    TSBFLG1,OFF-TSBWOWIP     TURN OFF WAITING ON
*                                       OWAIT IN PROGRESS
         OI    TSBFLG1,TSBOWIP          INDICATE OWAIT
         ST    RTCB,TSBCTCB             STORE CURRENT TCB ADDRESS
         OI    XSAF,OSW                 INDICATE WE SET TSBOWIP  M0094
***
***                 CHECK FOR BFR ELIGIBILITY AND AVAIALABILITY
***
TPT0400  EQU   *
         TM    TSBFLG4,TSBHUNG          IS TERMINAL DISC'TED    YM01176
         BO    TPT0470                  YES GO ENTER OWAIT      YM01176
         BAL   RBUF,TPT8400             GO CHECK TERMINAL STAT  YS02019
         CLC   TIOCNFBF(K2),TIOCUSLW ANY FREE BFR?              ZA14769
         BL    TPT0425               NO, SET SYSTEM FLAG        ZA14769
         SPACE 3                                                SA60002
***
*** FOR FULL-SCREEN OPTION, ENTIRE MESSAGE MUST GO THROUGH, WITH
***  NO OWAIT.  THEREFORE, CALCULATE HOW MANY BUFFERS MESSAGE WILL
***  REQUIRE, AND WAIT UNTIL THAT MANY ARE AVAILABLE.
***
         TM    XSAOPTNS,MASIS+MCNTRL    FULL SCREEN OPTION      SA60002
         BNO   TPT0423                  NO, BYPASS CALCULATION  SA60002
         SR    RWRK,RWRK                CLEAR FOR DIVIDE        SA60002
         LH    RWRK1,XSAPRMSZ           GET LENGTH OF MSG       SA60002
         LA    RWRK1,BUFFHDLN-BUFFTRLN(,RWRK1) ADD EXTRA FOR    SA60002
*                                              HEADER PREFIX    SA60002
         LA    R0,BUFFTRLN              GET LENGTH OF TRLR PRF  SA60002
         SH    R0,TIOCBFSZ              WHAT'S LEFT OVER (COMP) SA60002
         LPR   R0,R0                    GET BUFSIZE-TRLR PREF   SA60002
***
*** R0 = AMOUNT OF ROOM IN EACH BUFFER FOR REAL DATA
*** R15 AND R15 = AMOUNT OF DATA THAT MUST GO INTO BUFFERS
***
         DR    RWRK,R0                  GET NO OF BFRS REQD     SA60002
         LA    RWRK1,1(,RWRK1)          ADD 1 FOR POSSIBLE      SA60002
*                                       REMAINDER               SA60002
         SPACE 3
***
*** RWRK1 NOW CONTAINS NUMBER OF BFRS MSG WILL REQUIRE.
*** MESSAGE CAN PROCEED IF
***     1.  RWRK1 LTE TIOCNFBF, AND
***     2.  RWRK1+TSBNOBF LTE TIOCAOMX
***
         CH    RWRK1,TIOCAOMX        EVER POSSIBLE TO GET BFRS? ZA14769
         BH    TPT6800               NO, INV. PARM              ZA14769
         LH    RWRK2,TIOCNFBF      GET NO. OF FREE BFR          ZA14769
         SH    RWRK2,TIOCUSLW      ALLOW RESERVE LEVEL          ZA14769
         CR    RWRK1,RWRK2         ENOUGH FREE BFRS?            ZA14769
         BH    TPT0427             NO, GHECK FLAG SETTING       ZA14769
         CLI   TSBNOBF,K0          ANY ON OUTPUT QUEUE?         ZA14769
         BE    TPT0500             NO, GO GET BUFFERS           ZA14769
         SR    R0,R0                    CLEAR                   SA60002
         IC    R0,TSBNOBF               CURRENT NO. OUTPUT BUFF SA60002
         AR    RWRK1,R0                 ADD TO NUMBER NEEDED    SA60002
         CH    RWRK1,TIOCAOMX      TOTAL OF BOTH LESS THEN MAX? ZA14769
         BH    TPT0450                  NO, GO WAIT OR RETURN  @ZA08262
         B     TPT0500             YES, GO GET BUFFER           ZA14769
***
*** SEE WHETHER ANY TPUT CAN PROCEED
***
TPT0423  EQU   *                                                SA60002
         CLC   TIOCAOMX+K1(K1),TSBNOBF  IS NO. ALLOWED GREATER
*                                       THAN NO. ALREADY ON Q
         BH    TPT0500             YES, GO GET BUFFER           ZA14769
TPT0425  EQU   *
         OI    TIOCFLG,TIOCNOBF    SET SYSTEM FLAG              ZA14769
TPT0427  EQU   *                                                ZA14769
         CLI   TSBNOBF,K0               ANY ON OUTPUT QUEUE?    YS02019
         BNE   TPT0450                  IF ANY
         SPACE 2
         OI    TSBSTAT,TSBNOBUF         TASK INDICATOR
         SPACE 2
TPT0450  EQU   *
***
***                 PREPARE TO GO TO OWAIT SUBROUTINE
***
         TM    XSAF,PSW                 DOES TCAM NEED TO BE     M0094
*                                       POSTED - IS PART OF
*                                       THIS MSG ON THE QUEUE
         BZ    TPT0470                  IF NOT
         SPACE 2
***
***                 CHECK NEXT CHAR TO BE SENT FOR BACKSPACE
***
         SPACE 2
         L     RWRK,XSAUBFRP            PTR TO NEXT CHARACTER    M0094
*   NOTE THAT, SINCE PSW IS ON, IEDAYGP (OUTPUT MOVE PORTION) HAS BEEN
*   ENTERED AT LEAST ONCE.  THEREFORE,XSAUBFRP WILL, BY THIS TIME,
*   POINT TO SOMEWHERE WITHIN THE USER'S BUFFER.
         CLI   K0(RWRK),BKSPCHAR        IS NEXT CHAR A BACKSPACE
         BNE   TPT0460                  IF NOT
         OI    TSBFLG3,TSBNBKSP         INDR TO OUTPUT EDIT SO
*                                       IT WON'T SEND CR AT END
         SPACE 2
TPT0460  EQU   *
***                                                             YS02019
***                 GO TO TPOST SUBROUTINE                      YS02019
***                                                             YS02019
         BAL   RBUF,TPT9900             GO TO TPOSTER           YS02019
         NI    XSAF,OFF-PSW             INDICATE TCAM POSTED     M0126
         B     TPT0490                  GO TO TSIP CALL
         SPACE 2
TPT0470  EQU   *
         TM    XSAOPTNS,MWAIT           WAS WAIT SPECIFIED (0)   M0094
         BO    TPT8000                  IF NOT, GO TO NOWAIT RETURN
         SPACE 2
TPT0490  EQU   *
         OI    TSBFLG4,TSBOWAIT         INDICATE REAL OWAIT     YS02019
         BAL   RBUF,TPT6000             GO TO OWAIT SUBROUTINE
         SPACE 2
         B     TPT0400                  RECHECK BFR ELEGIBILITY
         SPACE 2
TPT0500  EQU   *
***********************************************************************
***                                                                 ***
***            AT THIS POINT IT HAS BEEN DETERMINED THAT            ***
***            THIS TASK IS ELIGIBLE FOR AT LEAST ONE TS            ***
***            BUFFER AND THERE IS ONE FREE. IT IS NOW              ***
***            COMMITED TO PLACING THE ENTIRE MESSAGE               ***
***            ONTO THE OUTPUT QUEUE.                               ***
***                                                                 ***
***********************************************************************
         NI    XSAOPTNS,OFF-MWAIT       FORCE WAIT OPTION        M0094
         SPACE 2
***********************************************************************
***                                                                   *
***            QTIP WILL NOW BE CALLED TO DO THE FOLLOWING            *
***                                                                   *
***                 1- FIND THE PROPER PLACE ON THE OUTPUT            *
***                    QUEUE TO PUT THE NEXT BFR.                     *
***                                                                   *
***                 2- PLACE FIRST FREE BUFFER ONTO OUTPUT QUEUE.     *
***                                                                   *
***                 3- UPDATE PTRS & CNTS OF FREE AND OUTPUT QUEUES.  *
***                                                                   *
***                 4- MOVE EITHER ALL REMAINING DATA, OR AS MUCH AS  *
***                           BUFFER WILL HOLD WHICHEVER IS LESS.     *
***                                                                   *
***                 5- UPDATE USER BUFFER SIZE AND POINTER IN SVRB    *
***                    EXTENDED SAVE AREA (XSABSZ AND XSABPTR)        *
***                                                                   *
***********************************************************************
         SPACE 2
TPT1000  EQU   *
***
***                 GO TO QTIP TO GET A TS BFR AND MOVE DATA
***
         LA    RSAVE,MOVECODE           ENTRY CODE
         L     RGOTO,TIOCQTIP           QTIP BRANCH ADDRESS     YS02019
         LA    RAVT,TIOCSAVE            GET SAVE AREA FOR GP    YS02019
         BALR  RETURN,RGOTO             GO TO QTIP
         SPACE 2
***
***                 UPON RETURN FROM QTIP
***
         NI    XSAF,OFF-XSADMOVE        DATA MOVEMENT DONE      YS02019
         OI    XSAF,HSW+PSW             TURN ON TO INDICATE      M0094
*                                       THAT NEXT BFR IS TO BE
*                                       A TRLR AND TCAM NEEDS
*                                       TO BE POSTED
         CLC   XSAUBFRS,KZERO           HAS ALL OF DATA BEEN     M0094
*                                       MOVED INTO TS BUFFERS
         BE    TPT9000                  IF SO PREPARE TO RETURN
         TM    XSAOPTNS,MASIS+MCNTRL    FULL SCREEN OPTION      SA60002
         BO    TPT1000                  YES, BYPASS RECHECKING  SA60002
         B     TPT0400                  GO TO GET NEXT BUFFER
         EJECT
TPT6000  EQU   *
***********************************************************************
*****                                                             *****
*****               COMMON OWAIT SUBROUTINE                       *****
*****                                                             *****
***********************************************************************
         SPACE
***                                                             YS02019
***           INFORM SYSTEM RESOURCE MANAGER OF WAIT            YS02019
***                                                             YS02019
         LH    RWRK2,TIOCNFBF           NO, FREE BFRS FOR       YS02019
*                                       SRM STATISTICS          YS02019
         O     RWRK2,TSIPSIGN           SIGN BIT ON MEANS OWAIT YS02019
         LA    RAVT,TIOCSAVE            GET SYSEVENT SAVE AREA  YS02019
         SYSEVENT TERMWAIT,ASIDL=XSATJID,ENTRY=BRANCH           YS02019
         SPACE 2                                                YS02019
***
***                 FREE CMS LOCK AND SET UP RESUME ENVIRONMENT.
***                 'STATUS' WILL FREE LOCAL LOCK
***
         SETLOCK RELEASE,TYPE=CMS,RELATED=(TSB,IGG09301(TPT0100))
         STM   R0,RGOTO,TCBGRS          SAVE REGISTERS IN TCB - YS02019
*                                       DISPATCHER WILL RESTORE YS02019
         LA    RGOTO,TPT6500            GET RESUME ADDRESS      YS02019
         ST    RGOTO,RBOPSW+K4          STORE RESUME ADDRESS    YS02019
         SPACE 2                                                YS02019
***                                                             YS02019
***                 BRANCH TO 'STATUS' ROUTINE TO SET UP WAIT.  YS02019
***                 DISPATCHER WILL RETURN CONTROL TO TPT6500,  YS02019
***                 AFTER WHOEVER REMOVES OWAIT GOES TO         YS02019
***                 STATUS TO MAKE THIS TASK DISPATCHABLE.      YS02019
***                                                             YS02019
         L     RGOTO,CVTABEND           SECONDARY CVT POINTER   YS02019
         USING SCVTSECT,RGOTO                                   YS02019
         LA    R0,SDTCB                 SET ENTRY CODE          YS02019
         LA    RAVT,NDISPBIT            GET NONDISP MASK IN R13 YS02019
         L     RGOTO,SCVTSTAT           STATUS BRANCH ENTRY ADR YS02019
         DROP  RGOTO                                            YS02019
         LA    RWRK2,0(,RTCB)           TCB PTR FOR STATUS      YS02019
         BALR  RETURN,RGOTO             GO TO STATUS ROUTINE    YS02019
         SPACE 2
TPT6500  EQU   *
***
***                 UPON RETURN FROM OWAIT
***
         SPACE 2
         SETLOCK OBTAIN,TYPE=LOCAL,MODE=UNCOND,                 YS02019*
               RELATED=(TSB,IGG09301(TPT9800))                  YS02019
         SETLOCK OBTAIN,TYPE=CMS,MODE=UNCOND,                   YS02019*
               RELATED=(TSB,IGG09301(TPT9700))                  YS02019
         LH    RTJID,XSATJID            SETLOCK BLASTS RTJID    YS02019
         BR    RBUF                     RETURN TO MAINLINE
         EJECT
***
***                    INVALID PARAMETER RETURN
***
TPT6800  EQU   *                                                 Y06327
         LA    RCODE,RC10               SET INVALID RETURN CODE  YM0895
         B     TPT7250                  GO TO ERROR ROUTINE      YM0895
         SPACE 3                                                SA60002
TPT7000  EQU   *
***
***                 ATTENTION RETURN
***
         LA    RCODE,RC08               SET RETURN CODE
         SPACE 2
TPT7250  EQU   *                                                 YM0895
***
***            COMMON ERROR RETURN
***
         STH   RCODE,XSARC              SAVE RETURN CODE        YS02019
         TM    XSAF,OSW                 DID THIS TPUT REALLY DO  M0094
*                                       ANYTHING
         BZ    TPT9700                  NO, JUST RETURN
         B     TPT9300                  REMOVE WAITS IF POSSIBLE
         SPACE 3                                                YS02019
TPT7500  EQU   *
***
***                 NO DATA - JUST SEND NEW LINE CHARACTER
***
         LA    RWRK1,NEWLINE            POINT TO NL CHAR
         STCM  RWRK1,SEVEN,XSAPRM1+K1   STORE NEW BFR POINTER    Y01018
         OI    XSAOPTNS,MASIS           FORCE ASIS OPTION        M0094
         LA    RWRK1,K1                 INDICATE LENGTH OF 1
         B     TPT0350                  RETURN
         SPACE 4
TPT8000  EQU   *
***
***                 NOWAIT RETURN
***
         LA    RCODE,RC04               NOWAIT RETURN CODE
         STH   RCODE,XSARC              SAVE RETURN CODE       YS02019
         B     TPT9600                  GO TO EXIT
         SPACE
TPT8400  EQU   *
***
***                 SUBROUTINE TO CHECK TERMINAL STATUS
***
         TM    TSBFLG4,TSBHUNG          ID USER HUNG           @ZA03182
         BO    TPT8500                  YES, ABORT THIS TPUT    YS02019
         L     RRPT,CVTAQAVT            GET TCX POINTER         YS02019
         LTR   RRPT,RRPT                IS TCAM UP              YM01564
         BNM   TPT8500                  NO, ABORT THIS TPUT     YM01564
         USING IEDQTCX,RRPT                                     YS02019
         L     RRPT,TCXRPT              GET RPT PTR FOR TEST    YM01564
         DROP  RRPT                     DROP TCX BASE           YM01564
         LTR   RRPT,RRPT                IS TS STILL UP          YM01564
         BZ    TPT8500                  NO, ABORT THIS TPUT     YM01564
         USING TIOCRPT,RRPT             RPT ADDRESSABLE         YM01564
         TM    TCBTSFLG,TCBTIOTG        HAS ATTENTION BEEN HIT
         BO    TPT7000                  YES, GO TO ATTN RETURN
         TM    TSBFLG3,TSBATTN          HAS ATTEN BEEN HIT, IGNORED
         BO    TPT7000                  YES, GO TO ATTN RETURN
         BR    RBUF                     ALL IS WELL, RETURN     YM01564
         SPACE 2
TPT8500  EQU   *
***
***                 USER HUNG UP RETURN
***
         LA    RCODE,RC14
         STH   RCODE,XSARC              SAVE RETURN CODE        YS02019
         B     TPT7250                  GO TO RETURN            YS02019
         EJECT
TPT9000  EQU   *
***
***                 RETURN TO USER ROUTINE
***
*   TEST TO SEE IF LINE SHOULD BE MARKED FOR NO                @ZA02244
*   FLASHBACK                                                  @ZA02244
         TM    TSBSTAT,TSBDSPLY         DISPLAY SCREEN         @ZA02244
         BNO   NTDPSC                   NO                     @ZA02244
         NI    TSBFLG3,OFF-TSBNFLOP     TURN OFF FLAG          @ZA02244
         L     RWRK,XSAUBFRP            POINT TO BUFFER        @ZA02244
         BCTR  RWRK,R0                  DECEM BY 1             @ZA02244
         CLI   0(RWRK),BYPASS          BYPASS CHAR. ON?         ZA16599
         BNE   NTDPSC                  NO                       ZA16599
         OI    TSBFLG3,TSBNFLOP         TURN ON BIT            @ZA02244
*                                                              @ZA02244
NTDPSC   EQU   *                                               @ZA02244
         SPACE
         SR    RCODE,RCODE              SUCCESSFUL RETURN CODE
         STH   RCODE,XSARC              SAVE RETURN CODE        YS02019
         SPACE 2
***                                                             YS02019
***                 GO TO SUBROUTINE TO TPOST TSB               YS02019
***                                                             YS02019
         BAL   RBUF,TPT9900             GO DO TPOST             YS02019
         SPACE 2
***
***                 CHECK IF SMF IS UP
***
         L     RWRK,TCBTCT              TCT POINTER
         LA    RWRK,K0(R0,RWRK)         CLEAR FIRST BYTE
         LTR   RWRK,RWRK                IS SMF UP
         BZ    TPT9200                  IF NOT
         SPACE 2
***
***                 UPDATE LINE COUNT IF UP
***
         USING SMFTCT,RWRK
         L     RWRK2,TCTLOUT            OLD COUNT
         LA    RWRK2,K1(R0,RWRK2)       ADD ONE TO COUNT
         ST    RWRK2,TCTLOUT
         DROP  RWRK
         SPACE 2
TPT9200  EQU   *
         SPACE 2
***
***                 ENTER OWAIT FOR HOLD MESSAGE
***
         TM    XSAOPTNS,MHOLD           DID USER SPECIFY HOLD    M0094
         BZ    TPT9250                  IF NOT
         OI    TSBFLG4,TSBOWAIT+TSBHOLD INDICATE OWAIT          YS02019
         BAL   RBUF,TPT6000             GO TO OWAIT SUBROUTINE
*                   AFTER MESSAGE HAS BEEN PRINTED
         BAL   RBUF,TPT8400             GO CHECK TERMINAL STATUS
         LH    RCODE,XSARC              RESTORE RETURN CODE     YS02019
TPT9250  EQU   *
***                                                             YS02019
***        INFORM SYSTEM RESOURCE MANAGER OF SUCCESSFUL TPUT    YS02019
***                                                             YS02019
         LH    RWRK2,XSAPRMSZ           AMMOUNT OF DATA MOVED   YS02019
         O     RWRK2,TSIPSIGN           SIGN MEANS TPUT         YS02019
         LA    RAVT,TIOCSAVE            GET SYSEVENT SAVE AREA  YS02019
         SYSEVENT TGETTPUT,ASIDL=XSATJID,ENTRY=BRANCH           YS02019
         SPACE 2                                                YS02019
***                                                             YS02019
***                 POST TSBECB IF AN ASID TPUT IS WAITING      YS02019
***                                                             YS02019
TPT9300  EQU   *                                                YS02019
         TM    TSBFLG3,TSBAWOIP         ANYBODY WAITING         YS02019
         BNO   TPT9400                  NO, FORGET IT           YS02019
         NI    TSBFLG3,OFF-TSBAWOIP     SET WAIT INDICATOR OFF  YS02019
         SPACE 2                                                YS02019
***                                                             YS02019
***                 CROSS-MEMORY POST THE TSBECB                YS02019
***                                                             YS02019
         LA    RECB,TSBECB              ECB ADDRESS             YS02019
         LA    RTJID,CVTBRET            ERRET = NOP (BR 14)     YS02019
         LH    RAVT,TSBWTJID            ASID OF WAITING TASK    YS02019
         BCTR  RAVT,R0                  DECREMENT ASID BY 1     YS02019
         SLL   RAVT,K2                  ASID * 4                YS02019
         L     RRPT,CVTASVT             GET ASVT ADDR           YS02019
         DROP  RRPT                     NO LONGER HAS RPT ADDR  YS02019
         L     RAVT,ASVTENTY-ASVT(RAVT,RRPT) GET ASCB ADDR      YS02019
         SR    RRPT,RRPT                ZERO COMPLETION CODE    YS02019
         L     RGOTO,CVT0PT01           ENTRY POINT OF POST     YS02019
         O     RECB,TSIPSIGN            HI-ORDER BIT SIGNIFIES  YS02019
*                                       CORSS-MEMORY POST       YS02019
         SPACE 2                                                YS02019
***                                                             YS02019
***                 REGISTERS FOR POST -                        YS02019
***                                                             YS02019
***            10 (RRPT) = COMPLETION CODE                      YS02019
***            11 (RECB) = ECB ADDRESS, HI-ORDER BIT ON         YS02019
***            12 (RTJID) = 'ERRET' ADDRESS                     YS02019
***            13 (RAVT) = ASCB ADDRESS OF POSTED MEMORY        YS02019
***            14 (RETURN) = RETURN ADDRESS                     YS02019
***            15 (RGOTO) = ENTRY POINT OF POST                 YS02019
***                                                             YS02019
         BALR  RETURN,RGOTO             GO TO POST ROUTINE      YS02019
         SPACE 2
TPT9400  EQU   *
***
***                 CHECK FOR WAITING ON OWAIT TASKS
***
         TM    TSBFLG1,TSBWOWIP         ANY TASKS WAITING
         BZ    TPT9600                  IF NOT
         SPACE 2
***                                                             YS02019
***                 GO TO STATUS TO RESET ALL TASKS THAT MIGHT  YS02019
***                 HAVE BEEN WAITING FOR THE TSB               YS02019
***                                                             YS02019
***                 REGISTERS FOR 'STATUS' -                    YS02019
***                                                             YS02019
***            0 (R0) - HIGH-ORDER = ASID, LOW = ENTRY CODE (8) YS02019
***            1 (RWRK2) - HIGH-ORDER BIT ON, REST = 0          YS02019
***            13 (RAVT) - MASK OF SECONDARY BITS TO BE RESET   YS02019
***            14,15 (RETURN,RGOTO) - CONVENTIONAL              YS02019
***                                                             YS02019
         LA    R0,STEPSD                ENTRY CODE TO RESET ALL YS02019
*                                       TASKS IN STEP           YS02019
         L     RWRK2,TSIPSIGN           MEANS 'RESET', NO TCB   YS02019
*                                       POINTER MEANS ALL STEP  YS02019
         ICM   R0,B'1100',XSATJID       ASID OF CALLER          YS02019
         L     RGOTO,CVTABEND           PTR TO 2NDARY CVT       YS02019
         LA    RAVT,NDISPBIT            USE SAME MASK AS BEFORE YS02019
         USING SCVTSECT,RGOTO                                   YS02019
         L     RGOTO,SCVTSTAT           ENTRYPOINT OF STATUS    YS02019
         DROP  RGOTO                                            YS02019
         BALR  RETURN,RGOTO             GO TO STATUS ROUTINE    YS02019
TPT9600  EQU   *
         NI    TSBFLG1,OFF-(TSBOWIP+TSBWOWIP)
*                                       OTHER TPUTS CAN NOW PROCEED
         SPACE 2
TPT9700  EQU   *
***
***                 TURN OFF TCB ATTN INDR AND RETURN
***
         LH    RCODE,XSARC              RESTORE RETURN CODE     YS02019
         NI    TCBTSFLG,OFF-TCBTIOTG    ATTN HIT INDR
         NI    TSBFLG3,OFF-TSBATTN      TURN OFF ATTN HIT INDR
*                                       ALL DONE WITH CMS LOCK  YS02019
         SETLOCK RELEASE,TYPE=CMS,RELATED=(TSB,IGG09301(TPT0100))
         SPACE 1
TPT9750  EQU   *
***
***                TURN OFF TCBFX IF OFF AT ENTRY
***
         TM    XSAF,XSATCBFX            SHOULD WE TURN OFF TCBFX M0094
         BZ    TPT9800                  IF NOT,SKIP
         NI    TCBFLGS1,OFF-TCBFX       ALLOW ASYNCHRONOUS EXITS
TPT9800  EQU   *
         SETLOCK RELEASE,TYPE=LOCAL,RELATED=('SVC SLIH')        YS02019
         L     RETURN,XSARETRG          GET SUPERVISOR RET ADDR YS02019
         BR    RETURN                   RETURN                  YS02019
         EJECT
***********************************************************************
***                                                                 ***
***                 SUBROUTINE TO TPOST TSB TO IEDAYP               ***
***                                                                 ***
***********************************************************************
TPT9900  EQU   *                                                YS02019
         LM    R0,RWRK2,TSBTPOST        GET TPOSTING FIELD      YS02019
         ST    RBUF,XSAWD7              SAVE REGISTER 2         YM03998
TPT9907  EQU   *
         STM   R0,RWRK2,XSAWD5          MOVE TO XSA TO SET BITS YS02019
         LA    RGO,XSAWD5               ADDRESSABILITY          YS02019
         USING TSBTPOST,RGO             NEXT REFERENCES TO TSB  YS02019
*                                       TPOST & BEYOND WILL     YS02019
*                                       REALLY BE TO XSA        YS02019
***                                                             YS02019
***                 IF ALL OUTPUT IS ON QUEUE, TURN OFF         YS02019
***                 QCBPARTO. ELSE, TURN IT ON                  YS02019
***                                                             YS02019
         SR    RWRK,RWRK                                        YS02019
         CH    RWRK,XSAUBFRS            REMAINING SIZE ZERO     YS02019
         BNE   TPT9910                  NO, TURN ON PARTO       YS02019
         TM    XSAF,XSAPARTO            HAS PARTO BEEN SET      YM01246
         BZ    TPT9915                  NO, NO NEED TO RESET    YM01246
         NI    TSBF1V,OFF-QCBPARTO      YES, TURN OFF PARTO     YS02019
         OI    TSBF1M,QCBPARTO          ACTION BIT FOR AYP      YM01246
         NI    XSAF,OFF-XSAPARTO        RESET PARTO FLAG        YM01246
         B     TPT9915                  SKIP                    YS02019
TPT9910  EQU   *                                                YS02019
         TM    XSAF,XSAPARTO            HAS PARTO BEEN SET      YM01246
         BO    TPT9915                  YES, NO NEED TO SET     YM01246
         OI    TSBF1M,QCBPARTO          ACTION BIT FOR AYP      YM01246
         OI    TSBF1V,QCBPARTO          TURN ON PARTO           YS02019
         OI    XSAF,XSAPARTO            SET PARTO FLAG IN WA    YM01246
TPT9915  EQU   *                                                YS02019
         SPACE 2                                                YS02019
***                                                             YS02019
***                 TURN ON QCBTPUT.  IF BREAKIN REQUEST, AND   YS02019
***                 BREAKIN IS ALLOWED, TURN ON QCBWRBRK.       YS02019
***                                                             YS02019
         TM    XSAOPTNS,MBREAK          BREAKIN REQUEST         YM01176
         BO    TPT9917                  YES, SKIP TSBTPUT TEST  YM01176
         TM    TSBFLG3,TSBTPUT          ALREADY DOING A TPUT    YM01246
         BZ    TPT9917                  NO, MUST TPOST          YM01246
         TM    TSBF1M,QCBPARTO          MUST PARTO BE SET       YM01246
         BZ    TPT9930                  NO NEED TO TPOST        YM08378
TPT9917  EQU   *                                                YM01246
         OI    TSBF1V,QCBTPUT           SET TPUT VALUE          YS02019
         OI    TSBF1M,QCBTPUT           ACTION BIT FOR AYP      YS02019
         TM    XSAOPTNS,MBREAK          BREAKIN REQUEST         YS02019
         BNO   TPT9920                  NO, SKIP                YS02019
         TM    TSBFLG3,TSBNOBRK         BREAKIN ALLOWED         YS02019
         BO    TPT9920                  NO, SKIP                YM01114
         OI    TSBF1V,QCBWRBRK          SET VALUE BIT           YS02019
         OI    TSBF1M,QCBWRBRK          SET ACTION BIT          YS02019
TPT9920  EQU   *                                                YS02019
         OI    TSBTPFLG,TSBPOSTO+TSBTPQCB TELL TYP TO TPOST QCB YS02019
*                                         SHOW TPOST OUTSTAND'G YS02019
         SPACE 2                                                YS02019
***
***                 SET BITS IN REAL TSB, USING CDS.  IF TSBPOSTO
***                 WAS ON DURING SWAP, NO NEED TO DO ACTUAL
***                 TPOST
***
         DROP  RGO                      USE REAL TSB NOW        YS02019
         LM    RBUF,RCVT,XSAWD5         GET NEW BITS            YS02019
         DROP  RCVT                                             YS02019
         CDS   R0,RBUF,TSBTPOST         TRY TO RESET BITS       YS02019
         BNE   TPT9907                  IF CHANGED, DO IT AGAIN YS02019
         OI    TSBFLG3,TSBTPUT          SET INDR FOR FUTURE     YS03998
         L     RCVT,CVTPTR                                      YS02019
         USING CVT,RCVT                                         YS02019
         L     RTCX,CVTAQAVT            GET TCX ADDRESS         YS02019
         USING IEDQTCX,RTCX                                     YS02019
         LTR   R0,R0                    TEST TSBPOSTO (THIS TST YS02019
*                                       NEEDS TSBPOSTO EQU 8)   YS02019
         BM    TPT9930                  IF ON, DON'T TPOST.     YS02019
*                                       IEDAYP WILL PICK UP     YS02019
*                                       THESE FLAGS ANYWAY      YS02019
         SPACE 2                                                YS02019
***                                                             YS02019
***                 TPOST THE TSB TO IEDAYP
***
         L     RWRK,TCXTSI              GET TSINPUT QCB ADDRESS YS02019
         USING IEDQTSI,RWRK                                     YS02019
         LA    RWRK,TSITSAP-(QCBSTVTO-IEDQQCB)                  YS02019
*                                       ADDRESS OF IEDAYP'S     YS02019
*                                       'QCB'                   YS02019
         DROP  RWRK                                             YS02019
         ST    RWRK,TSBRQCB             QCB TO WHICH TSB WILL   YS02019
*                                       BE TPOSTED              YS02019
         L     RWRK,TCXREADY            GET CONTENTS OF READY Q YS02019
TPT9925  EQU   *                                                YS02019
         ST    RWRK,TSBLINKA            LINK TSB TO NEXT ELEM   YS02019
         MVI   TSBPRI,PRIDESTQ          SET PRIORITY            YS02019
         LA    RSAVE,TSBRCB             GET READY TO PUT ELEM   YS02019
*                                       ADDRESS INTO Q HEADER   YS02019
         CS    RWRK,RSAVE,TCXREADY      TRY TO ADD IT TO QUEUE  YS02019
         BNE   TPT9925                  NO GOOD, TRY AGAIN      YS02019
         SPACE 2                                                YS02019
***                                                             YS02019
***                 X-M POST TCAM                               YS02019
***                                                             YS02019
         SR    RRPT,RRPT                COMPLETION CODE         YS02019
         LR    RASCB,RTJID              SAVE TJID               YS02019
         LA    RTJID,CVTBRET            'ERRET' ADDRESS         YS02019
         L     RAVT,TCXASCB             LOAD ASCB OF TCAM       YS02019
         LR    R0,RTCX                  SAVE TCX POINTER        YS02019
         L     RTCX,TCXAVT              AVT POINTER             YS02019
         DROP  RTCX                                             YS02019
         USING IEDQAVTD,RTCX                                    YS02019
         LA    RECB,AVTOSECB            ECB POINTER ** NOTE     YS02019
*                                       THAT AVT IS NOT REALLY  YS02019
*                                       ADDRESSABLE             YS02019
         L     RGOTO,CVT0PT01           POST ROUTINE ADDRESS    YS02019
         O     RECB,TSIPSIGN            HI-ORDER BIT=XMPOST     YS02019
         BALR  RETURN,RGOTO             GO POST                 YS02019
         SPACE 2                                                YS02019
         LR    RTCX,R0                  RESTORE TCX PTR         YS02019
         USING IEDQTCX,RTCX                                     YS02019
         LR    RTJID,RASCB              RESTORE TJID            YS02019
         L     RASCB,TSBASCBA           RESTORE ASCB POINTER    YS02019
         L     RRPT,TCXRPT              RESTORE RPT POINTER     YS02019
         USING TIOCRPT,RRPT                                     YS02019
TPT9930  EQU   *                                                YS02019
         L     RCVT,CVTPTR              RESTORE CVTPTR         @ZA03945
         L     RBUF,XSAWD7              RESTORE REGISTER 2      YS02019
         BR    RBUF                     RETURN                  YS02019
         EJECT                                                  YS02019
***********************************************************************
*****                                                             *****
*****               CONSTANTS USED BY IGG09301                    *****
*****                                                             *****
***********************************************************************
         SPACE
NEWLINE  DC    X'15'                    NL CHAR TO SEND WHEN NO DATA
TPTPSW   DS    0F                       ALIGNMENT
         DC    X'070C0000'              1ST WD OF RESUME PSW -   Y01018
*                                       TRANS, ENABLE, EC, M CK  Y01018
FOUR     DC    F'4'                     CONSTANT FOR COMPARISON  YM0895
KZERO    DC    F'0'                     ZERO COMPARATOR
TSIPSIGN DC    X'80000000'              TPUT INDR FOR TSIP
PATCHREA DC    10F'0'                   PATCH AREA               Y01018
         EJECT
         IHAASCB                                                YS02019
         EJECT                                                  YS02019
         IKJTIOCB                                               SA60002
         EJECT                                                  SA60002
         IHAASVT                                                YS02019
         EJECT                                                  YS02019
         TAVTD                                                  YS02019
         EJECT                                                  YS02019
CVT      DSECT                                                  YS02019
         CVT                                                    YS02019
         EJECT                                                  YS02019
         TPRIOR                                                 YS02019
         EJECT                                                  YS02019
         IHAPSA                                                 YS02019
         EJECT                                                  YS02019
         TQCBD                                                  YS02019
         EJECT
         IKJRB
***
***      EXTENDED SAVE AREA.  THSES STATEMENTS SHOULD FOLLOW
***      THE MACRO 'IKJRB.'
***
         ORG   RBEXSAVE                 START OF SAVE AREA       M0094
XSAPRM0  DS    0F                       REG 0 AT ENTRY           M0094
XSAPRMTJ DS    H                        TJID PASSED              M0094
XSAPRMSZ DS    H                        SIZE PASSED              M0094
XSAPRM1  DS    0F                       REG 1 AT ENTRY           M0094
XSAOPTNS DS    X                        USER OPTIONS             M0094
MASIS    EQU   X'01'                    ASIS OPTION SPECIFIED    M0094
MCNTRL   EQU   X'02'                    1 MEANS CONTROL          M0094
*
*   BOTH OF THE ABOVE BITS OFF MEANS EDIT OPTION
*
MBREAK   EQU   X'04'                    BREAKIN SPECIFIED        M0094
MHOLD    EQU   X'08'                    1 MEANS HOLD             M0094
MTGET    EQU   X'80'                    1 MEANS TGET REQUESTED   M0094
MUSERP   EQU   X'40'                    REG 15 PTS TO USERID    YS02019
MLOWP    EQU   X'20'                    0 HIGHPRI, 1 LOWPRI      M0094
MWAIT    EQU   X'10'                    1 MEANS NOWAIT           M0094
         DS    AL3                      REST OF REGISTER ONE     M0094
XSAWD3   DS    0F                       FOR SAVING REGISTERS     M0094
XSAFLAG  DS    X                        FLAG PASSED BY 9C TO 01  M0094
*                                       AND 02                   M0094
XSATCBFX EQU   X'20'                    TURN OFF TCBFX AT EXIT   M0094
         DS    AL3                      REST OF WORD             M0094
         ORG   XSAWD3+2                 INPUT ED, OUTPUT MOVE    M0094
XSAUBFRS DS    H                        AMT DATA LEFT TO MOVE    M0094
XSAUBFRP DS    0F                       LOC DATA LEFT TO MOVE    M0094
XSAESTAL DS    CL16                     LIST FOR ESTAE MACRO    YS02019
         ORG   XSAUBFRP+4               USED FOR SAVE AREA WHEN YS02019
*                                       NOT NEEDED BY ESTAE     YS02019
XSAWD5   DS    F                        FOR SAVING REGISTERS     M0094
XSAWD6   DS    F                        FOR SAVING REGISTERS     M0094
         ORG   XSAWD5                   USE WORDS 5-6 FOR NAME   M0094
XSAENQNM DS    CL8                      ENQ NAME FOR TJID TPUT   M0094
XSAWD7   DS    F                        FOR SAVING REGISTERS     M0094
XSAWD8   DS    F                        FOR SAVING REGISTERS     M0094
         ORG   XSAWD7                   USE WDS 7-8 FOR ENQ PRMS M0094
XSAENQAD DS    2F                       WORK AREA FOR ENQ/DEQ    M0094
XSAUSERP DS    0F                       PTR TO USERID           YS02019
XSAWD9   DS    F                        FOR SAVING REGISTERS     M0094
XSARETRG DS    0F                       FOR SAVING RETURN ADDR  YS02019
XSAWD10  DS    F                        FOR SAVING REGISTERS     M0094
         DS    CL1                      RESERVED                YS02019
XSAUSERK DS    CL1                      KEY OF TGET CALLER      YS02019
XSARC    DS    H                        RETURN CODE SAVE AREA   YS02019
XSATJID  DS    H                        CALLER'S TJID            M0094
XSAFLAG2 DS    X                        FLAGS                   YM05387
XSAATR   EQU   X'80'                    NEXT CHAR IN TS BUFFER   S22028
*                                       IS A 3270 ATTRIBUTE BYTE
XSABFRAL EQU   X'40'                    TIOC BUFFER HAS BEEN    YM05387
*                                       ALLOCATED               YM05387
XSA15D   EQU   X'20'                    CHANGE COMPLETION CODE  YM05387
*                                       TO 15D                  YM05387
*                  NEXT 5 BITS UNUSED                           YM05387
XSAF     DS    X                        FLAGS USED BY TPUT. INI- M0094
*                                       TIALIZED FROM XSAFLAG.
HSW      EQU   X'80'                    HEADER BFR DONE          M0094
PSW      EQU   X'40'                    NEED TO TPOST QCB        M0094
*SATCBFX EQU   X'20'                    TURN OFF TCBFX AT EXIT
OSW      EQU   X'10'                    TSBOWIP TURNED ON BY US  M0094
XSADMOVE EQU   X'08'                    DATA MOVEMENT GOES ON   YS02019
XSAAUTH  EQU   X'04'                    USER IS AUTHORIZED      YS02019
*                                       (NOT SOME OTHER TPUT)   YS02019
XSAUDEBQ EQU   X'02'                    ASID TPUT ASID ENQUEUED YS02019
XSAPARTO EQU   X'01'                    QCBPARTO IS SET ON      YM01246
         EJECT                                                  YS02019
         IHASCVT                                                YS02019
         EJECT                                                  YS02019
         IKJTCB                                                 YS02019
         EJECT                                                  YS02019
         IEFTCT                                                 YS02019
         EJECT                                                  YS02019
         TTCXD                                                  YS02019
         EJECT                                                  YS02019
         IKJTIOCP                                               YS02019
         EJECT                                                  YS02019
         IKJTSB                                                 YS02019
         EJECT                                                  YS02019
         TTSID                                                  YS02019
         EJECT                                                  YS02019
         END   IGG09301
