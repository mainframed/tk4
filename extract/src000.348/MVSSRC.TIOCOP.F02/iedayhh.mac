         TITLE 'IEDAYHH - HANG UP INITIALIZATION FOR TSO REGION'
IEDAYHH  CSECT
         COPY  LCGASMS2                                        ,YS02019
         ENTRY QTIP0040
         DC    C'IEDAYHH '              MODULE ID
         DC    X'4098'                  DATE  4/8/74            VS08528
         DS    0H
         SPACE 3
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                     *
*                                                                     *
*    NAME - IEDAYHH, TIOC HANG UP'S SECTION OF QTIP SVC               *
*                                                                     *
*    RELEASE 21 DELETIONS/CHANGES                                     *
*    RELEASE 20 DELETIONS/CHANGES                                     *
*                                                                TS1623
*                                                                TS1628
*                                                                 M3846
*                                                                 M0032
*                                                                     *
* STATUS -                                                            *
*    CHANGE LEVEL 0                                                   *
*                                                                     *
* FUNCTION -                                                          *
*    CALL SYSTEM INITIATED CANCEL (SIC) ROUTINE TO REMOVE THE         *
*    USER FROM THE SYSTEM                                             *
*                                                                     *
* ENTRY POINTS -                                                      *
*         QTIP0040, TO PERFORM HANG UP PROCESSING                     *
*                                                                     *
* INPUT -                                                             *
*    REGISTER  2 HAS ASCB ADDRESS                                     *
*    REGISTER  7 HAS QCB ADDRESS                                      *
*    REGISTER  9 HAS TSB ADDRESS                                      *
*    REGISTER 12 HAS ENTRY POINT ADDRESS                              *
*    REGISTER 13 HAS AVTSAVE2 ADDRESS                                 *
*    REGISTER 14 HAS RETURN ADDRESS                                   *
*                                                                     *
* OUTPUT -                                                            *
*    NONE                                                             *
*                                                                     *
* EXTERNAL REFERENCES -                                               *
*         SIC, VIA BRANCH ENTRY AT CVTSIC                             *
*         QTIP 28, VIA BRANCH ENTRY AT QTIP28AD                       *
*         QTIP 14, VIA BRANCH ENTRY AT QTIP14AD                       *
*                                                                     *
* EXITS, NORMAL -                                                     *
*         BR 14, RETURN TO HANG UP (IEDAYH)                           *
*                                                                     *
* EXITS, ERROR -                                                      *
*         NONE                                                        *
*                                                                     *
* TABLES/WORK AREAS -                                                 *
*    CONTROL BLOCKS ARE DESCRIBED IN THE DSECTS AT END OF LISTING     *
*                                                                     *
* ATTRIBUTES -                                                        *
*    REENTRANT, REFRESHABLE, SUPERVISOR STATE                         *
*                                                                     *
* CHARACTER CODE DEPENDENCY -                                         *
*    THIS MODULE IS ASSEMBLED IN EBCDIC AND MUST BE RE-ASSEMBLED IF A *
*    DIFFERENT CHARACTER SET IS TO BE USED DURING EXECUTION           *
*                                                                     *
* NOTES -                                                             *
*    NONE                                                             *
*                                                                     *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         EJECT
********
******** REGISTER EQUATES
********
R0       EQU   0                        WORK REGISTER           YS02019
R1       EQU   1                        WORK REGISTER           YS02019
RASCB    EQU   2                        ASCB ADDR PASSED BY AYH YS02019
RHOLDRET EQU   3                        SAVES RET ADDR TO HANG UP
ROTTEN   EQU   4                        WORK REGISTER
ROB      EQU   5                        WORK REGISTER
RTIOC    EQU   6                        TIOCRPT ADDR            YS02019
RQCB     EQU   7                        QCB ADDRESS             YS02019
RWORK8   EQU   8                        WORK REGISTER           YS02019
RTSB     EQU   9                        TSB ADDRESS             YS02019
RAH      EQU   10                       WORK REGISTER
RECB     EQU   11                       ECB ADDR
RBASE    EQU   12                       ENTRY AND BASE ADDRESS
RSAVE    EQU   13                       AVT SAVE AREA ADDR AT ENTRY AND
*                                       SAVE AREA ADDR FOR SIL RTN
RETURN   EQU   14                       RETURN ADDRESS
RGO      EQU   15                       HOLDS ENTRY ADDRESSES
         SPACE 3
********
******** MASKS
********
SAVESIZE EQU   72                       SIZE OF AVT SAVEAREAS
NOT      EQU   X'FF'                    ZEROS VARIOUS BITS
POSTBIT  EQU   X'40'                    POST BIT                YS02019
ONE      EQU   1                        CONSTANT OF 1           YS02019
         SPACE 3
********
******** ESTABLISH ADDRESSABILITY AND INITIALIZE REGISTERS
********
QTIP0040 EQU   *
         USING *,RBASE
         USING TSB,RTSB                 TSB ADDRESSABILITY      YS02019
         USING ASCB,RASCB               ASCB ADDRESSABILITY     YS02019
         USING IEDQQCB,RQCB             QCB ADDRESSABILITY      YS02019
         LR    RHOLDRET,RETURN          SAVE HANG UP RETURN ADDR
         SPACE 3
**********************************************************************
*        CHECK IF TSB HAS BEEN THRU LOGOFF                           *
**********************************************************************
         TM    TSBSTAT,TSBDISC          LOGOFF OCCURED          YS02019
         BNO   HANGUP                   NO, BR TO SET TSBHUNG   YS02019
         SPACE 3
**********************************************************************
*        EXIT QTIP 14 TO CLEAN UP TSO CONTROL BLOCKS                 *
**********************************************************************
         LR    RWORK8,RTSB              QTIP14 NEEDS TSB IN R8  YS02019
         LR    ROTTEN,RBASE             SAVE BASE ADDRESS       YS02019
         L     RGO,QTIP14AD             QTIP14 ENTRY ADDRESS    YS02019
         L     RBASE,QTIP11AD           QTIP14 BASE ADDRESS     YS02019
         BR    RGO                      BRANCH TO QTIP14        YS02019
         SPACE 3
***********************************************************************
*        INDICATE TERMINAL HAS HUNG UP                                *
***********************************************************************
HANGUP   EQU   *                                                YS02019
         OI    TSBFLG4,TSBHUNG          INDICATE A HANGUP       YS02019
         SR    ROTTEN,ROTTEN            CLEAR REGISTER          VS08528
         STH   ROTTEN,TSBASRCE          ZERO TSBASRCE SO AYP    VS08528
*                                       WON'T TOUCH THE QCB     VS08528
         SPACE 3
**********************************************************************
*       GO TO QTIP ENTRY 28 TO RETURN HELD TCAM BUFFERS FOR THIS USER*
*       INPUT REGISTERS ARE 7 (QCB), 9 (TSB), AND 13 (AVTSAVE2)      *
***********************************************************************
         LR    ROTTEN,RBASE             PRESERVE BASE ADDR
         L     RGO,QTIP28AD             GET QTIP 28 ENTRY ADDR
         L     RBASE,QTIP5AD            GET QTIP 28 BASE ADDR
         BALR  RETURN,RGO               GO DO YOUR THING
         LR    RBASE,ROTTEN             RESTORE BASE ADDR
         SPACE 3
**********************************************************************
*         CHECK IF A LINE DISCONNECT RECOVERY PERIOD WAS SPECIFIED    *
**********************************************************************
         L     ROB,CVTPTR               CVT ADDRESS             YS02019
         L     RTIOC,CVTAQAVT-CVT(,ROB) GET TCX ADDRESS         YS02019
         L     RTIOC,TCXRPT-IEDQTCX(,RTIOC) GET TIOCRPT ADDRESS YS02019
         USING TIOCRPT,RTIOC            TIOCRPT ADDRESSABILITY  YS02019
         LH    RAH,TIOCRCLM             GET RECONNECT LIMIT     YS02019
         CH    RAH,ZERO                 IS RECONNECT 0 MINUTES  YS02019
         BE    SIC                      YES, BR TO FORCE LOGOFF YS02019
         SPACE 3
***********************************************************************
*        LINE DISCONNECT RECOVERY - SEE IF RECONNECT PERIOD WAS       *
*    ALREADY ENTER                                                    *
***********************************************************************
*                                       .STORE # OF MIN. BEFORE YS02019
         ST    RAH,TSBMINL              .USER WILL DISCONNECT   YS02019
         TM    TIOCTECB,POSTBIT         RECON PERIOD STARTED    YM01176
         BO    INCR                     YES,BR & INCR TSBMINL   YS02019
         SPACE 3
***********************************************************************
*        POST TIOCTECB SO A RECONNECT PERIOD CAN BEGIN                *
*           ENTRY TO POST                                             *
*             R10 = COMPLETION CODE                                   *
*             R11 = ECB ADDRESS                                       *
*             R12 = ERROR RETURN ADDRESS                              *
*             R13 = ZERO ASCB ADDRESS                                 *
*             R14 = RETURN ADDRESS                                    *
*             R15 = CVTOPT01                                          *
***********************************************************************
         LA    RECB,TIOCTECB            GET ECB ADDR
         SR    RAH,RAH                  ZERO COMPLETION CODE
         LR    ROB,RBASE                SAVE BASE REGISTER
         L     ROTTEN,CVTPTR            GET ADDR OF CVT
         USING CVT,ROTTEN               CVT ADDRESSABILITY      YS02019
         LA    RBASE,CVTBRET            NO ERROR RETURN ADDR    YS02019
         L     RGO,CVT0PT01             GET ADDR OF POST ROUTINE
         LR    ROTTEN,RSAVE             PRESERVE SAVEAREA ADDR
         SR    RSAVE,RSAVE              NO ASCB ADDR            YS02019
         MODESET EXTKEY=SUPR            NEED SUPERVISOR KEY FOR POST
         BALR  RETURN,RGO               GO POST LOGOFF
         MODESET EXTKEY=TCAM            BACK TO TCAMS KEY
         LR    RBASE,ROB                RESTORE BASE REGISTER
         LR    RSAVE,ROTTEN             RESTORE SAVEAREA ADDR
         B     FINISH                   RETURN TO CALLER        YS02019
         SPACE 3
***********************************************************************
*        RECONNECT PERIOD HAS STARTED, ADD 1 MINUTE TO SYNCHRONIZE    *
***********************************************************************
INCR     EQU   *                                                YS02019
         L     RAH,TSBMINL              .UPDATE DISCONNECT      YS02019
         LA    RAH,ONE(,RAH)            .TIME BY ONE            YS02019
         ST    RAH,TSBMINL              .MINUTE                 YS02019
         B     FINISH                   RETURN TO CALLER        YS02019
         SPACE 3
***********************************************************************
*        SYSTEM INITIATED CANCEL (SIC) - USED TO CANCEL A TERMINAL    *
*              IF HANGUP OCCURS OR IF UNRECOVERABLE I/O ERRORS OCCUR  *
*         ENTRY TO SIC                                                *
*           R0 = POST CODE (622 ABEND)                                *
*           R1 = ASCB                                                 *
*           R13= SAVEAREA ADDRESS                                     *
*           R14= RETURN ADDRESS                                       *
*           R15= ENTRY POINT                                          *
***********************************************************************
SIC      EQU   *                                                YS02019
         L     R1,TSBASCBA              ASCB OF USER TO SIC     YS02019
         L     R0,SICODE                622 ABEND CODE          YS02019
         L     ROB,CVTPTR               ADDR OF CVT             YS02019
         USING CVT,ROB                  CVT ADDRESSABILITY      YS02019
         L     RGO,CVTSIC               ENTRY TO SIC            YS02019
         LA    RSAVE,SAVESIZE(,RSAVE)   NEXT USABLE SAVEAREA    YS02019
         MODESET EXTKEY=SUPR            KEY ZERO FOR SIC        YS02019
         BALR  RETURN,RGO               CALL SIC                YS02019
         SPACE 3
**********************************************************************
*        RETURN TO HANG UP, IEDAYH                                    *
***********************************************************************
FINISH   EQU   *
         LR    RETURN,RHOLDRET          RESTORE RETURN ADDR
         BR    RETURN                   GO BACK
         SPACE 3
***********************************************************************
*        CONSTANTS                                                    *
***********************************************************************
QTIP5AD  DC    V(QTIP0050)              BASE ADDR FOR QTIP ENTRY 28
QTIP28AD DC    V(QTIP0280)              ENTRY ADDR FOR QTIP ENTRY 28
QTIP11AD DC    V(QTIP0110)              BASE ADDR FOR QTIP 14   YS02019
QTIP14AD DC    V(QTIP0140)              ENTRY ADDR FOR QTIP 14  YS02019
SICODE   DS    0F                                               YS02019
         DC    XL4'00000622'            SIC ABEND CODE          YS02019
ZERO     DC    H'0'                     ZERO CONSTANT           YS02019
KSAVESIZ DC    H'72'                    SIZE OF AVT SAVEAREA
PATCH    DC    10F'0'                   PATCH AREA              YS02019
         EJECT
* * * * * * * * * * * *  CONTROL BLOCK DSECTS * * * * * * * * * * * * *
         SPACE 3
CVT      DSECT
         CVT
         EJECT
         IKJTSB
         EJECT
         IKJTIOCP
         EJECT
         IHAASCB
         EJECT
         TTCXD
         EJECT
         TQCBD
         EJECT
         TAVTD
         END
