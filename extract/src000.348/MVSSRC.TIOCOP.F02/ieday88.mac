         TITLE 'IEDAY88 - TIOC LOGOFF/TERMINATION QTIP'
IEDAY88  CSECT
**********************************************************************
*
*01 MODULE-NAME = IEDAY88
*
*01 DESCRIPTIVE-NAME = TIOC LOGOFF/TERMINATION QTIP
*
*01 COPYRIGHT = NONE
*
*01 STATUS = 01
*
*01 FUNCTION = PERFORM TERMINATION CLEANUP (QTIP 23). TRY TO
*               RECONNECT A DISCONNECTED USER WHO HAS LOGGED BACK IN
*               (QTIP 1). LOG OFF DISCONNECTED USERS WHOSE RECONNECT
*               PERIOD HAS EXPIRED (QTIP 27). TEST WHETHER USER HAS
*               HUNG UP.
*
*01 NOTES = N/A
*
*02   DEPENDENCIES = N/A
*
*02   RESTRICTIONS = N/A
*
*02   REGISTER-CONVENTIONS = SEE REGISTER EQUATES
*
*02   PATCH-LABEL = PATCH
*
*01 MODULE-TYPE = ASSEMBLER LANGUAGE CSECT
*
*02   PROCESSOR = ASSEMXF-370R
*
*02   MODULE-SIZE = SEE EXTERNAL SYMBOL DICTIONARY TYPE SD ID 01
*
*02   ATTRIBUTES = REENTRANT,REUSABLE ENABLED,SUPERVISOR STATE
*
*01 ENTRY-POINT = QTIP0230,QTIP0010,QTIP0270
*
*02   PURPOSE = SEE FUNCTION
*
*02   LINKAGE = QTIP0230 FROM IEDAY8 (VIA IEDAYQT)
*                QTIP0270 FROM IEDAY8 (VIA IEDAYQT)
*                QTIP0010 FROM LOGON PROMPTER (VIA IEDAYQT)
*
*01 INPUT = REGISTER 12 CONTAINS ENTRY POINT
*            REGISTER 14 CONTAINS RETURN ADDRESS
*             (QTIP0010)
*            REGISTER 15 CONTAINS ADDRESS OF TWO WORD PARAMETER LIST.
*                        WORD 1 POINTS TO THE USER ID.
*                        WORD 2 POINTS TO THE PASSWORD SUPPLIED BY USER
*             (QTIP0230)
*            REGISTER 15 CONTAINS ADDRESS OF RMPL
*             (QTIP0270)
*            REGISTER 15 CONTAINS ADDRESS OF TIOCRPT
*
*01 OUTPUT =
*            QTIP0010
*               REGISTER 15 = 0  -  SUCCESSFUL
*                           = 4  -  PASSWORD DOES NOT MATCH
*                           = 8  -  USERID UNAVAILABLE(SEE REGISTER 0)
*               REGISTER  0 = 0  -  USERID NOT FOUND
*                           = 4  -  USERID IN USE
*            QTIP0230
*               REGISTER 15 = 4  -  TELL IEDAY8 TO WAIT FOR TSOUTPUT
*                                   TO FINISH
*            QTIP0270
*               REGISTER 15 = 0  -  TELL IEDAY802 TO ISSUE ANOTHER
*                                   1 MINUTE STIMER
*                           = 4  -  TELL IEDAY802 TO ISSUE WAIT ON
*                                   TIOCTECB
*
*01 EXIT-NORMAL = TO IEDAYQT
*
*01 EXIT-ERROR = NONE
*
*01 EXTERNAL-REFERENCES
*
*02   ROUTINES = QTIP0140 IN CSECT IEDAYOO IN LOAD MODULE SVC 101
*
*
*02   DATA-AREAS = N/A
*
*02   CONTROL-BLOCKS = CVT,TSB,ASCB,QCB,RMPL,TCX,TIOCRPT,TSI
*
*01 TABLES = N/A
*
*01 MACROS = NONE
*
*01 CHANGE-ACTIVITY
*        VS2-3.0 CHANGES
*A756974-756979                                                 ZA01520
*D021000                                                        ZA01520
*C182000                                                        ZA01520
*        VS2-030 SERVICE UPDATE
*0000754794-754795                                             @ZA07703
*
*
**********************************************************************
         EJECT
         ENTRY QTIP0010
         ENTRY QTIP0230
         ENTRY QTIP0270
*
*   REGISTER EQUATES
*
RWRK0    EQU   0                        WORK REGISTER
RWRK1    EQU   1                        WORK REGISTER
RWRK2    EQU   2                        WORK REGISTER
RCVT     EQU   3                        CVT POINTER
RWRK3    EQU   3                        WORK REGISTER
RTCX     EQU   4                        TCX ADDRESS
RTCB     EQU   4                        TCB POINTER
RWRK4    EQU   4                        WORK REGISTER
RWRK5    EQU   5                        WORK REGISTER
RASCB    EQU   5                        ASCB POINTER
RWRK6    EQU   6                        WORK REGISTER
RWRK7    EQU   7                        WORK REGISTER
RTIOC    EQU   7                        TIOC POINTER
RTSB     EQU   8                        TSB POINTER
RWRK8    EQU   8                        WORK REGISTER
RWRK9    EQU   9                        WORK REGISTER
RCSCB    EQU   9                        CSCB ADDRESS
RWRK10   EQU   10                       WORK REGISTER
RWRK11   EQU   11                       WORK REGISTER
RBASE    EQU   12                       BASE REGISTER
RSAVE    EQU   13                       SAVE AREA POINTER
RLINK    EQU   14                       LINK REGISTER
RRMPL    EQU   15                       RMPL POINTER
REPA     EQU   15                       ENTRY POINT ADDR
RCODE    EQU   15                       RETURN CODE REGISTER
         SPACE 3
*
*        VALUE EQUATES
*
E0       EQU   0                        ZERO
E1       EQU   1                        ONE
E2       EQU   2                        TWO
E4       EQU   4                        FOUR
E8       EQU   8                        EIGHT
E12      EQU   12                       OFFSET OF 12
ALL      EQU   X'FF'                    FLAG FOR NI
WAITCODE EQU   4                        IEDAY8 SHOULD WAIT FOR TCAM
         SPACE 3
*
*   ADDRESSABILITY
*
         DC    C'IEDAY88 '              MODULE I.D.
         DC    X'6036'                  DATE - 01/05/76         ZA07703
QTIP0230 EQU   *
         USING *,RBASE                  MODULE ADDRESSABLE
         USING RMPL,RRMPL               RMPL ADDRESSABLE
         L     RASCB,RMPLASCB           GET ASCB ADDR
         USING ASCB,RASCB               ASCB ADDRESSABLE
         L     RTSB,ASCBTSB             GET TSB ADDR
         USING TSB,RTSB                 TSB ADDRESSABLE
         L     RCVT,CVTPTR              GET CVT ADDR
         USING CVT,RCVT                 CVT ADDRESSABLE
         L     RTCX,CVTAQAVT            GET TCX ADDR
         USING IEDQTCX,RTCX             TCX ADDRESSABLE
         L     RTIOC,TCXRPT             GET TIOCRPT ADDR
         USING TIOCRPT,RTIOC            TIOCRPT ADDRESSABLE
*
*   IF THE USER HAS HUNG UP, EXIT TO QTIP 14 (IN IEDAYOO) TO
*   CLEAN UP THE TS CONTROL BLOCKS
*
         TM    TSBFLG4,TSBHUNG          HAS USER HUNG UP
         BNO   NOTHUNG                  NO, DON'T CLEANUP YET
*
*   REGISTERS AT ENTRY TO QTIP 14
*
*        08 - TSB ADDR
*        12 - BASE ADDR OF IEDAYOO
*        14 - RETURN ADDR
*        15 - ADDR OF QTIP 14
*
         L     REPA,QTIP14AD            GET EPA OF QTIP 14
         DROP  RRMPL                    15 NOT RMPL BASE
         L     RBASE,AYOOBASE           GET BASE ADDR FOR AYOO
         DROP  RBASE                    BASE REGISTER ALTERED
         BR    REPA                     EXIT TO QTIP 14
*
         USING QTIP0230,RBASE           REESTABLISH BASE ADDR
         USING RMPL,RRMPL               RMPL
*
*   TERMINAL IS STILL CONNECTED.  CAUSE TSOUTPUT TO PERFORM
*   CONTROL BLOCK CLEANUP FOR THIS USER AFTER CURRENT OUTPUT
*   MESSAGES HAVE BEEN SENT.
*
NOTHUNG  EQU   *
         OI    TSBSTAT,TSBDISC          FLAG TERM AS LOGGING OFF
         TM    RMPLFLG1,RMPLTYPE        IS THIS OUT-OF-CORE ABND
         BNO   TPOST                    NO, DON'T SET FLAG
         OI    TSBFLG4,TSBOCAB          HAVE IEDAYO SEND MSG
*
TPOST    EQU   *
         LM    RWRK0,RWRK1,TSBTPOST     GET TPOST FLAGS
RESET    EQU   *
         LR    RWRK10,RWRK0             COPY TPOST FLAGS
         LR    RWRK11,RWRK1             COPY TPOST FLAGS
         O     RWRK11,DISCMASK          REQUEST QCB BIT SETTINGS
         O     RWRK10,TQCBMASK          REQUEST QCB TPOST
         CDS   RWRK0,RWRK10,TSBTPOST    UPDATE TPOST FLAGS
         BNE   RESET                    RETRY UNTIL SUCCESSFUL
*
*   CALL IEDAYTPQ TO TPOST THE TSB TO IEDAYP.
*   IEDAYP WILL SET QCBWRBRK, QCBTPUT, AND QCBDISC AND WILL
*   TPOST THE QCB TO ITSELF.
*   THIS WILL CAUSE TSOUTPUT TO GAIN CONTROL.
*   TSOUTPUT WILL THEN PERFORM LOGOFF CLEANUP PROCESSING
*   AFTER ALL OUTPUT MESSAGES HAVE BEEN SENT TO THE TERMINAL.
*
*   REGISTERS FOR IEDAYTPQ--
*
*        00 -- RETURN ADDR.
*        01 -- ZERO, INDICATING REQUEST TO TPOST TSB TO IEDQYP
*        02 -- TSB ADDR
*        13 -- SIX WORD SAVE AREA
*        15 -- EPA
*
         LA    RSAVE,TIOCSAVE           GET ADDR OF SAVE AREA
         SR    RWRK1,RWRK1              REQUEST TPOST OF TSB
         LR    RWRK2,RTSB               GET TSB ADDR
         L     REPA,TPQEPA              GET ADDR OF IEDAYTPQ
         BALR  RWRK0,REPA               GO TPOST
*
         LA    REPA,WAITCODE            TELL IEDAY8 TO WAIT
*                                       FOR TSOUTPUT TO FINISH
         BR    RLINK                    RETURN
*
         EJECT
***********************************************************************
*        QTIP0010 IS ENTERED FROM THE LOGON PROMPTER(VIA IEDAYQT). IT *
*      IS USED TO RECONNECT A DISCONNECTED USER WHO HAS RELOGGED ON   *
*      WITH THE RECONNECT OPERAND. IT WILL SCAN THE TSB/ASCB/CSCB     *
*      CHAINS FOR A MATCHING USERID AND PASSWORD, PLUS CHECK IF THE   *
*      TSBHUNG BIT IS ON INDICATING THIS IS THE "OLD TSB". IT THEN    *
*      WILL REMOVE ANY WAITS VIA IEDAYQIO, MERGE THE TERMINAL         *
*      DEPENDENT FIELDS FROM THE "CURRENT OR NEW TSB" INTO THE "OLD   *
*      TSB", TURN ON TSBNEWID IN THE 'OLD TSB' AND POST IT TO IEDAYP. *
*         INPUT - REGISTER 4  =TCB ADDRESS                            *
*                 REGISTER 15 =ADDRESS OF TWO WORD PARAMETER          *
*                   WORD 1=USERID ADDRESS                             *
*                   WORD 2=PASSWORD ADDRESS                           *
*      NOTE - 'NEW TSB' IS THE TSB THE USER CURRENTLY HAS WHEN HE HAS *
*                       LOGGED ON WITH RECONNECT.                     *
*             'OLD TSB' IS THE TSB QTIP0010 IS SCANNING FOR, THE ONE  *
*                       HE WAS DISCONNECTED FROM.                     *
***********************************************************************
QTIP0010 EQU   *
         USING CSCB,RCSCB               CSCB ADDRESSABILITY
         MODESET EXTKEY=SUPR            KEY ZERO TO AVOID FETCH YM02856
*                                       PROTECTION CHK ON PARMS YM02856
****     FIND CURRENT TSB OF USER RECONNECTING                     ****
         L     RCVT,PSAAOLD-PSA         GET CURRENT ASCB ADDR
         L     RWRK1,ASCBTSB-ASCB(,RCVT) 'NEW TSB'
         SPACE 1
****     GET USERID AND PASSWORD ADDRESSES                         ****
         L     RWRK10,E0(,REPA)         USERID ADDRESS
         L     RWRK11,E4(,REPA)         PASSWORD ADDRESS
         SPACE 1
****     SET UP FOR TSB/ASCB/CSCB SCAN                             ****
         L     RCVT,CVTPTR              CVT ADDRESS
         L     RCVT,CVTAQAVT-CVT(,RCVT) TCX ADDRESS
         L     RTIOC,TCXRPT-IEDQTCX(,RCVT)  TIOCRPT ADDRESS
         USING TIOCRPT,RTIOC            TIOCRPT ADDRESSABILITY
         SR    RWRK2,RWRK2              CLEAR WORK REGISTER
         IC    RWRK2,TIOCTSBS           SIZE OF TSB
         LH    RWRK3,TIOCNTSB           NUMBER OF TSBS
         BCTR  RWRK3,E0                 DECREMENT BY ONE
         MR    RWRK2,RWRK2              OFFSET TO LAST TSB
         L     RTSB,TIOCTSB-E1          START OF TSBS
         USING TSB,RTSB                 TSB ADDRESSABILITY
         LA    RTSB,E0(,RTSB)           ZERO HIGH ORDER BYTE
         AR    RWRK3,RTSB               ADDR OF LAST TSB
         SR    RWRK2,RWRK2              ZERO WORK REGISTER
         IC    RWRK2,TIOCTSBS           SIZE OF TSB
         LA    RSAVE,TIOCSAVE           SAVE AREA FOR TPOST
         SPACE 2
****     SCAN FOR 'OLD TSB'
SCAN     EQU   *
         CR    RTSB,RWRK1               IS THIS 'NEW TSB'
         BE    BUMP                     YES, POINT TO NEXT TSB
         L     RASCB,TSBASCBA           ASCB OF TSB BEING SCAND
         LTR   RASCB,RASCB              VALID ASCB ADDRESS      YM04678
         BZ    BUMP                     NO,CHECK NEXT ONE       YM04678
         L     RCSCB,ASCBCSCB-ASCB(,RASCB) CSCB CONTAINS USERID
         USING CSCB,RCSCB               CSCB ADDRESSABILITY
         CLC   CHKEY,E0(RWRK10)         USERIDS MATCH
         DROP  RCSCB                    DROP CSCB BASE REG
         BE    HUNG                     YES, CHECK FURTHER
BUMP     EQU   *
         BXLE  RTSB,RWRK2,SCAN          POINT TO NEXT TSB & CHK
****     NO MATCHING USERID, ERROR, RETURN                         ****
         LA    RCODE,E8                 RETURN CODE = 8
         LA    RWRK0,E0                 USERID NOT FOUND
         BR    RLINK                    RETURN TO CALLER
         SPACE 1
HUNG     EQU   *
         TM    TSBFLG4,TSBHUNG          IS THIS THE OLD TSB
         BNO   ERROR1                   NO,ERROR
         CLC   TSBPSWD,E0(RWRK11)       PASSWORDS MATCH
         BNE   ERROR2                   NO, ERROR
         SPACE 1
         MODESET EXTKEY=TCAM            CHANGE TSBS IN KEY 6    YM02856
****     START RECONNECT PROCESS HERE                              ****
         OI    TSBFLG4-TSB(RWRK1),TSBHUNG SET TSBHUNG IN NEW TSB
         NI    TSBFLG4,ALL-TSBHUNG      TURN OFF TSBHUNG IN OLD
         SPACE 1
****     CHECK IF OLD TSB HAS ANY IWAIT OR OWAIT OUTSTANDING       ****
         TM    TSBFLG4,TSBIWAIT+TSBOWAIT ANY WAITS IN PROGRESS
         BZ    TERM                     NO,CONTINUE RECONNECT
         SPACE 1
***********************************************************************
*        BRANCH TO IEDAYQIO TO REMOVE USER IWAIT OR OWAIT             *
*          INPUT TO IEDAYQIO = REGS 15 - 2 WORK REGS                  *
*                              REG  4      RETURN ADDRESS             *
*                              REG  8      TSB ADDRESS                *
*                              REG  12     BASE ADDRESS               *
*          NOTE: REG 8 IS RTSB WHICH CONTAINS 'OLD TSB'               *
***********************************************************************
         LR    RWRK3,RWRK1              SAVE 'NEW TSB ADDRESS
         LR    RWRK7,RBASE              SAVE BASE ADDRESS
         L     REPA,QIO                 ENTRY POINT TO IEDAYQIO
         L     RBASE,QIOBASE            BASE ADDR FOR IEDAYQIO
         DROP  RBASE                    DROP BASE ADDRESSABILITY
         BALR  RWRK4,REPA               BR TO IEDAYQIO
         XI    TSBFLG2,TSBBIPI          INDICATE TO REMOVE OTHER WAIT
         USING QTIP0230,RWRK7           RESET ADDRESSABILITY
         L     REPA,QIO                 ENTRY POINT TO IEDAYQIO
         BALR  RWRK4,REPA               BR TO IEDAYQIO
         DROP  RWRK7                    USE NORMAL BASE REG
         USING QTIP0230,RBASE           REESTABLISH ADDRESSABILITY
         XI    TSBFLG2,TSBBIPI          RESTORE ORIGINAL STATE
         LR    RBASE,RWRK7              RESTORE BASE ADDR
         LR    RWRK1,RWRK3              RESTORE 'NEW TSB' ADDRESS
         SPACE 1
TERM     EQU   *
****     MERGE TERMINAL DEPENDENT FIELDS FROM 'NEW TSB' TO 'OLD    ****
****       TSB' IN CASE USER HAS RECONNECTED ON A DIFFERENT TYPE   ****
****       TERMINAL. old tsb has addressability.                   ****
         TM    TSBSTAT-TSB(RWRK1),TSBDSPLY   NEW TSB A DISPLAY
         BZ    OFFDSPLY                 NO,INDICATE NO DISPLAY
         OI    TSBSTAT,TSBDSPLY         INDICATE A DISPLAY
         B     T3270                    TEST FOR 3270
OFFDSPLY EQU   *
         NI    TSBSTAT,ALL-TSBDSPLY     TURN OFF DISPLAY BIT
T3270    EQU   *
         TM    TSBSTAT-TSB(RWRK1),TSB3270  NEW TSB A 3270
         BZ    NO3270                   NO,SHOW NO 3270
         OI    TSBSTAT,TSB3270          INDICATE 3270 TERM
         B     LINSZE                   MOVE IN LINE SIZE
NO3270   EQU   *
         NI    TSBSTAT,ALL-TSB3270      TURN OFF 3270 BIT
LINSZE   EQU   *
         MVC   TSBLNSZ(E8),TSBLNSZ-TSB(RWRK1)  PHYSICAL LINE   @ZA07703
*                           SIZE AND OUTPUT BUFFER QUEUES      @ZA07703
         TM    TSBFLG3-TSB(RWRK1),TSBNOBRK BREAK FEATURE ALLOWD
         BZ    BRK                      YES,TURN BIT OFF
         OI    TSBFLG3,TSBNOBRK         BREAK NOT ALLOWED
         B     TERMCHAR                 COPY TERMINAL CHAR.
BRK      EQU   *
         NI    TSBFLG3,ALL-TSBNOBRK     INDICATE BREAK ALLOWED
TERMCHAR EQU   *
         MVC   TSBTERMC(E2),TSBTERMC-TSB(RWRK1) TERMINAL CHARS.
         MVC   TSBLNNO(E1),TSBLNNO-TSB(RWRK1)  # LINES ON SCREEN
         MVC   TSBASRCE(E2),TSBASRCE-TSB(RWRK1) TCAM TERM INDEX
         MVC   TSBLINE(E2),TSBLINE-TSB(RWRK1)  LINE ADDR OR 3705 ID
         SPACE 2
***********************************************************************
*        TURN ON TSBNEWID TO INDICATE TO IEDAYP THAT THE NEW QCBTJID  *
*           FIELD MUST BE UPDATED WITH THE OLD ASID(TJID).            *
***********************************************************************
         LM    RWRK4,RWRK5,TSBTPOST     CONTENTS OF TSBTPOST
RETRY    EQU   *
         LR    RWRK6,RWRK4              1ST WORD OF TSBTPOST
         LR    RWRK7,RWRK5              2ND WORD OF TSBTPOST
         O     RWRK6,TPMSK              TELL IEDAYP TO UPDATE QCBTJID
         TM    TSBFLG3,TSBTPUT          WAS A TPUT IN PROGRESS
         BZ    SWAP                     NO
         O     RWRK6,TQCBMASK           TPOST QCB
         O     RWRK7,TPQCBMSK           TURN ON QCBTPUT
SWAP     EQU   *
         CDS   RWRK4,RWRK6,TSBTPOST     STORE IN NEW TSBPOST
         BNE   RETRY                    FIELDS NOT EQUAL RETRY
         SR    RWRK1,RWRK1              INDICATE TSB TO TPOST
         ST    RWRK1,TSBMINL            ZERO RECONNECT LIMIT
         LTR   RWRK4,RWRK4              IS POST OUTSTANDING
         BM    RETURN                   YES,RETURN TO CALLER
         SPACE 2
***********************************************************************
*        CALL IEDAYTPQ TO TPOST THE TSB TO IEDAYP                     *
*          INPUT REGISTERS = REG  0 - RETURN ADDRESS                  *
*                            REG  1 - ZERO,INDICATING TPOST TSB TO AYP*
*                            REG  2 - TSB ADDRESS                     *
*                            REG 13 - SIX WORD SAVE AREA              *
*                            REG 15 - ENTRY POINT                     *
***********************************************************************
         LR    RWRK2,RTSB               ADDRESS OF TSB
         L     REPA,TPQEPA              ENTRY POINT ADDR
         BALR  RWRK0,REPA               TPOST TSB VIA IEDAYQTP
RETURN   EQU   *
         LA    RCODE,E0                 RETURN CODE ZERO
         BR    RLINK                    RETURN TO CALLER
         SPACE 1
ERROR1   EQU   *
         LA    RCODE,E8                 USERID UNAVAILABLE
         LA    RWRK0,E4                 USERID IN USE
         BR    RLINK                    RETURN TO CALLER
         SPACE 1
ERROR2   EQU   *
         LA    RCODE,E4                 PASSWORD UNEQUAL
         BR    RLINK                    RETURN TO CALLER
         EJECT
***********************************************************************
*        QTIP0270 IS USED TO CANCEL A DISCONNECTED USER WHOSE         *
*         RECONNECT PERIOD HAS EXPIRED,OR DECREMENT BY 1 MINUTE THE   *
*         TIME THAT IS LEFT.                                          *
*          INPUT: REGISTER 15 = TIOCRPT ADDRESS                       *
***********************************************************************
QTIP0270 EQU   *
         SR    RWRK10,RWRK10            ZERO WORK REGISTER
         LR    RTIOC,REPA               TIOCRPT ADDRESS
         USING TIOCRPT,RTIOC            TIOCRPT ADDRESSABILITY
         SR    RWRK2,RWRK2              ZERO WORK REGISTER
         IC    RWRK2,TIOCTSBS           SIZE OF TSB
         LH    RWRK3,TIOCNTSB           NUMBER OF TSBS
         BCTR  RWRK3,E0                 ADJUST FOR OFFSET
         MR    RWRK2,RWRK2              OFFSET TO LAST TSB
         L     RTSB,TIOCTSB-E1          START OF TSBS
         LA    RTSB,E0(,RTSB)           CLEAR HIGH ORDER BYTE
         AR    RWRK3,RTSB               ADDR OF LAST TSB
         SR    RWRK2,RWRK2              ZERO WORK REGISTER
         IC    RWRK2,TIOCTSBS           SIZE OF TSB
         USING TSB,RTSB                 TSB ADDRESSABILITY
NEXTTSB  EQU   *
         L     RWRK5,TSBASCBA           LOAD ADR OF ASCB        ZA01520
         LA    RWRK5,0(,RWRK5)          CLEAR HIGH ORDER BYTE   ZA01520
         LTR   RWRK5,RWRK5              DOES ASCB EXIST?        ZA01520
         BZ    NEXT                     NO,CHECK NEXT TSB       ZA01520
         TM    TSBFLG4,TSBHUNG          IS USER DISCONNECTED
         BNO   NEXT                     NO,CHECK NEXT TSB
         L     RWRK5,TSBMINL            RECONNECT TIME LIMIT
         LTR   RWRK5,RWRK5              RECONNECT TIME EXPIRED
         BNP   NEXT                     YES,CHECK NEXT TSB
         BCTR  RWRK5,E0                 DECREMENT BY 1 MINUTE
         ST    RWRK5,TSBMINL            SAVE NEW TIME LIMIT
         LTR   RWRK5,RWRK5              RECONNECT TIME EXPIRED NOW
         BZ    CANCEL                   YES,CANCEL USER
         BCTR  RWRK10,E0                INDICATE RECONNECT PERIOD LEFT
NEXT     EQU   *
         BXLE  RTSB,RWRK2,NEXTTSB       CHECK NEXT TSB
         LTR   RWRK10,RWRK10            RECONNECT PERIOD OUTSTANDING
         BZ    RC4                      NO,RETURN CODE 4
         LA    RCODE,E0                 RETURN CODE OF 0
         BR    RLINK                    RETURN TO CALLER
RC4      EQU   *
         XC    TIOCTECB(E4),TIOCTECB    CLEAR ECB
         LA    RCODE,E4                 RETURN CODE OF 4
         BR    RLINK                    RETURN TO CALLER
         SPACE 3
CANCEL   EQU   *
***********************************************************************
*        CANCEL USER WITH 622 ABEND WHEN RECONNECT PERIOD HAS         *
*         EXPIRED.                                                    *
*          REGISTER INPUT TO SYSTEM INITIATED CANCEL(SIC)             *
*            0 = POST CODE (622 ABEND)                                *
*            1 = ASCB ADDRESS                                         *
*           13 = SAVEAREA ADDRESS                                     *
*           14 = RETURN ADDRESS                                       *
*           15 = ENTRY POINT TO SIC                                   *
***********************************************************************
         L     RWRK0,SICODE             622 ABEND CODE
         L     RWRK1,TSBASCBA           ASCB ADDRESS
         LA    RSAVE,TIOCSAVE           SAVE AREA ADDRESS
         LR    RWRK5,RLINK              SAVE RETURN ADDDRESS
         L     RLINK,CVTPTR             ADDRESS OF CVT
         USING CVT,RLINK                CVT ADDRESSABILITY
         L     REPA,CVTSIC              ENTRY TO SIC
         MODESET EXTKEY=SUPR            SUPERVISOR KEY FOR SIC
         DROP  RLINK
         BALR  RLINK,REPA               GO SIC'M
         MODESET EXTKEY=TCAM            BACK TO TCAMS KEY
         LR    RLINK,RWRK5              RESTORE RETURN ADDRESS
         B     NEXT                     CHECK NEXT TSB
*   CONSTANTS
*
TPQEPA   DC    V(IEDAYTPQ)              ADDR OF IEDAYTPQ
TQCBMASK DC    AL1(TSBTPQCB,0,0,0)      REQUEST QCB TPOST
DISCMASK DC    AL1(0,0)                 DON'T ALTER QCBTSOF2
         DC    AL1(QCBTPUT+QCBWRBRK+QCBDISC) MASK BYTE
         DC    AL1(QCBTPUT+QCBWRBRK+QCBDISC) VALUE BYTE
QTIP14AD DC    V(QTIP0140)              ADDR OF QTIP 14
AYOOBASE DS    0A                       BASE ADDR OF QTIP 14
QIOBASE  DC    V(QTIP0110)              BASE ADDR FOR IEDAYQIO
QIO      DC    V(IEDAYQIO)              ENTRY POINT
TPMSK    DC    AL1(TSBNEWID,0,0,0)      .TELL IEDAYP TO UPDATE QCBTJID
TPQCBMSK DC    AL1(0,0,QCBTPUT,QCBTPUT) TURN ON QCBTPUT
SICODE   DS    0F
         DC    XL4'00000622'            622 ABEND FOR SIC
PATCH    DC   16F'0'                    PATCH SPACE
         EJECT
         IHAASCB
         EJECT
CVT      DSECT
         CVT
         EJECT
         TQCBD
         EJECT
         IHARMPL
         EJECT
         TTCXD
         EJECT
         IKJTIOCP
         EJECT
         IKJTSB
         EJECT
CSCB     DSECT
         IEECHAIN
         IKJTCB
         EJECT
         IHAPSA
         END
