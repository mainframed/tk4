         TITLE 'IEAVAD31 - ABDUMP FORMAT AND FORMAT01 ROUTINES'
* /* START OF SPECIFICATIONS ****
*
*01*  MODULE-NAME = IEAVAD31 ( VS2 ) WITH SU33                 G33SPHW
*
*01*  DESCRIPTIVE-NAME = ABDUMP FORMAT AND FORMAT01 ROUTINES
*
*01*  COPYRIGHT = NONE
*
*01*  STATUS = OS/VS2 REL 3.7 WITH SU33                         G33SPHW
*
*01*  CHANGE-ACTIVITY = AS FOLLOWS
*
*02*    D = 113600-113800                                        YM2513
*02*    APAR ZA13835 - PIE ADDR                                 G33SPHW
*
*01*  FUNCTION = TO UNPACK DATA INTO THE OUTPUT LINE WHILE ASSOCIATING
*     A LABEL WITH EACH PIECE OF THE DATA
*
*01*  NOTES
*
*02*    CHARACTER-CODE-DEPENDENCIES = THIS MODULE IS EBCDIC CHARACTER
*       CODE DEPENDENT.
*
*02*    DEPENDENCIES = NONE
*
*02*    PERFORMANCE = TO CONVERT DATA TO DECIMAL AND UNPACK IT INTO
*       ABDLINEA WITH LABELS AND AN OPTIONAL INDENTATION FACTOR.
*
*02*    RESTRICTIONS = NONE
*
*02*    REGISTER-CONVENTIONS = THE REGISTER CONVENTIONS ARE DESCRIBED
*       UNDER REGISTER EQUATES.
*
*02*    PATCH-LABEL = NONE
*
*01*  MODULE-TYPE = MODULE
*
*02*    PROCESSOR = ASSEMBLER-370R
*
*02*    MODULE-SIZE = 300 BYTES
*
*02*    ATTRIBUTES = REENTERABLE
*
*01*  ENTRY = IEAVAD31
*
*02*    PURPOSE = TO CONVERT DATA TO DECIMAL AND UNPACK IT INTO ABDLINE
*       ASSOCIATING LABELS WITH THE FIELDS. IT ALSO ALLOWS AN
*       INDENTATION FACTOR TO LOCATION IN ABDLINE WHERE THE DATA IS
*       TO BE PLACED.
*
*02*    LINKAGE = BALR
*
*02*    INPUT = GPR1 CONTAINS THE ADDRESS OF ABDAREA,GPR13 THE ADDRESS
*       OF THE CALLERS SAVE AREA, GPR14 THE RETURN ADDRESS AND GPR15
*       THE ADDRESS OF IEAVAD31. IN ADDITION, ABDBPTR MUST CONTAIN THE
*       ADDRESS OF THE DATA TO BE PLACED INTO ABDLINE AND ABDLLINE
*       MUST CONTAIN THE ADDRESS OF THE LAYOUT LINE TO BE USED TO
*       CONTROL THE PLACEMENT.
*
*02*    OUTPUT = ALL REGISTERS EXCEPT GPR 15 ARE PRESERVED. GPR15 WILL
*       CONTAIN ZERO IF NO PROBLEMS WERE ENCOUNTERED AND EIGHT IF AN
*       ANTICIPATED UPR OCCURRED. IF GPR15 IS ZERO THEN ABDBPTR WILL BE
*       POSITIONED AT ONE PAST THE LAST FORMATTED FIELD, ABDLLINE
*       WILL POINT TO THE BYTE FOLLOWING THE END OF THE LAYOUT LINE AND
*       ABDLPTR WILL CONTAIN THE NEXT AVAILABLE ADDRESS IN ABDLINE.
*       OTHERWISE, THESE FIELDS ARE UNPREDICTABLE.
*
*02*    EXIT-NORMAL = BACK TO CALLER
*
*02*    EXIT-ERROR = AN 0C4 ABEND WILL OCCUR IF AN UNEXPECTED UPR
*       OCCURS.
*
*01*  ENTRY = IEAVAD41
*
*02*    PURPOSE = SAME AS FOR IEAVAD31 EXCEPT THAT NO INDENTATION
*       FACTOR IS ANTICIPATED IN THE LAYOUT LINE. IN OTHER WORDS,
*       PLACEMENT OF DATA IN ABDLINE STARTS WITH THE ADDRESS
*       IN ABDLPTR.
*
*02*    LINKAGE = BALR
*
*02*    INPUT = SAME AS FOR IEAVAD31 EXCEPT THAT GPR15 CONTAINS ADDRESS
*       OF IEAVAD41 AND ABDLPTR MUST CONTAIN ADDRESS OF POSITION WITHIN
*       ABDLINE WHERE DATA IS TO BE PLACED. NORMALLY, A CALL TO
*       IEAVAD41 IS PRECEDED BY A CALL TO IEAVAD31 WHICH ESTABLISHES
*       THIS FIELD.
*
*02*    OUTPUT = SAME AS FOR IEAVAD31
*
*02*    EXIT-NORMAL = BACK TO CALLER
*
*02*    EXIT-ERROR = SAME AS IEAVAD31
*
*01*  EXTERNAL-REFERENCES = NONE
*
*01*  TABLES = ABDAREA - ABDUMP WORK AREA
*
*01*  MACROS = IHAABDA
*
*
**** END OF SPECIFICATIONS ***/
IEAVAD31 CSECT
         ENTRY IEAVAD41
* REGISTER EQUATES
R0       EQU   0                       REGISTER 0
R1       EQU   1                       REGISTER 1               ZA13835
PARAM    EQU   1                       ADDRESS OF ABDAREA
PIEADDR  EQU   1                       ADDRESS OF PIE
R2       EQU   2                       REGISTER 2               ZA13835
* LABELM1 AND FIELDM1 MUST REMAIN AN EVEN-ODD REGISTER PAIR
LABELM1  EQU   2                       LENGTH OF LABEL MINUS 1
FIELDM1  EQU   3                       LENGTH OF DATA FIELD MINUS 1
* OFFSET AND SEPBLNKS MUST REMAIN AN EVEN-ODD REGISTER PAIR
OFFSET   EQU   4                       INCREMENT TO ABDBPTR TO FIELD
*                                      TO BE FORMATTED
SEPBLNKS EQU   5                       NUMBER OF SPACES BETWEEN LABEL
*                                      AND FIELD
* REGISTERS DATAREG, LAYOUT AND OUTLINE MUST REMAIN CONSECTIVE
DATAREG  EQU   6                       ADDR OF BLOCK TO BE FORMATTED
LAYOUT   EQU   7                       ADDR OF LAYOUT LINE
OUTLINE  EQU   8                       NEXT AVAIL POSITION IN ABDLINE
WORK     EQU   9                       GENERAL WORK REGISTER
BASE     EQU   12                      BASE REGISTER
SAVEAREA EQU   13                      POINTER TO CALLERS SAVE AREA
RETREG   EQU   14                      RETURN ADDR
BASEIN   EQU   15                      BASE TO BRANCH BY ID FIELD
RETCODE  EQU   15                      RETURN CODE REGISTER
*
* GENERAL EQUATES
ZERO     EQU   0                       CONSTANT
ONE      EQU   1                       CONSTANT
TWO      EQU   2                       CONSTANT
THREE    EQU   3                       CONSTANT
FOUR     EQU   4                       CONSTANT
FIVE     EQU   5                       CONSTANT
EIGHT    EQU   8                       CONSTANT
NINE     EQU   9                       CONSTANT
TWELVE   EQU   12                      CONSTANT
TWENTY   EQU   20                      CONSTANT
TWENTY4  EQU   24                      CONSTANT
TWENTY8  EQU   28                      CONSTANT
K240     EQU   240                     CONSTANT
INDENT   EQU   0                       OFFSET IN LAYOUT LINE TO
*                                      INDENTATION FACTOR
ENDING   EQU   X'FF'                   END OF LAYOUT LINE INDICATOR
ALL      EQU   X'FF'                   ALL BITS ON IN BYTE MASK
         USING *,BASEIN
         MODID BR=YES
         DROP  BASEIN
         STM   RETREG,BASE,TWELVE(SAVEAREA) SAVE CALLERS REGISTERS
         BALR  BASE,ZERO               ESTABLISH ADDRESSIBILITY
         USING *,BASE
* ESTABLISH ADDR FOR ABDAREA BASED ON USER PARAM IN R1
         USING ABDAREA,PARAM
OLDVAL   LM   DATAREG,LAYOUT,ABDBPTR   OBTAIN VALUES FROM ABDAREA FOR
*                                      LAYOUT LINE ADDR AND BLOCK ADDR
         LA    OUTLINE,ABDLINE         SET INIT PRNT POS TO START LINE
         SR    WORK,WORK               ESTABLISH ZERO VALUE
         IC    WORK,INDENT(LAYOUT)     OBTAIN INDENTATION VALUE
         AR    OUTLINE,WORK            INDENT BEGINNING OF LINE
         LA    LAYOUT,ONE(LAYOUT)      ADVANCE OVER INDENTATION VALUE
COMMON   CLI   ZERO(LAYOUT),ENDING     END OF LAYOUT LINE REACHED
         BE    DONE                    YES, BRANCH
         SR    LABELM1,LABELM1         INIT VALUE
         SR    OFFSET,OFFSET           INIT VALUE
         IC    LABELM1,ZERO(LAYOUT)    OBTAIN LABEL SIZE AND FIELD SIZE
         SRDL  LABELM1,FOUR            ISOLATE THE TWO VALUES
         SRL   FIELDM1,TWENTY8         INTO SEPARATE REGISTERS
         IC    OFFSET,ONE(LAYOUT)      GET OFFSET AND SEPBLNKS FIELDS
         SRDL  OFFSET,FOUR             ISOLATE THE TWO VALUES
         SRL   SEPBLNKS,TWENTY8        INTO SEPARATE REGISTERS
         AR    DATAREG,OFFSET          INCREMENT TO FIELD TO BE DUMPED
         EX    LABELM1,MOVE1           MOVE LABEL TO OUTPUT LINE
         AR    OUTLINE,LABELM1         BUMP LINE POS BY LABEL LNG - 1
         LA    OUTLINE,ONE(OUTLINE,SEPBLNKS)     BUMP LINE POS BY ONE
*                                                MORE THAN NUMBER OF
*                                                BLANKS BETWEEN LABEL
*                                                AND DATA
         TM    ABDUPRF,UPRFMAT         IS UPR ANTICIPATED BY CALLER
         BZ    NOUPR                   NO, BRANCH
         LA    WORK,FORMATX            ADDR OF UPR HANDLER ROUTINE
         ST    WORK,ABDUPRXT           SET INTO ABDAREA
NOUPR    EX    FIELDM1,MOVE2           MOVE DATA TO WORK AREA
         XC    ABDUPRXT,ABDUPRXT       NO UPR EXIT ANY MORE
         UNPK  ABDFMTWK(NINE),ABDFMTWK(FIVE)     UNPACK DATA IN WA
         TR    ABDFMTWK(EIGHT),TBL-K240     TRANSLATE TO PRNTABLE CHAR
         LA    FIELDM1,ONE(FIELDM1,FIELDM1) CALC SIZE OF CONVERTED FLD
         EX    FIELDM1,MOVE3           MOVE FORMATTED DATA TO ABDLINE
         TM    ABDBLNKS,ABDBLKN3       FIELDS TO BE SEPARATED BY
*                                      THREE BLANKS
         BO    SPECSPAC                NO, BRANCH
         LA    OUTLINE,FOUR(FIELDM1,OUTLINE)     BUMP TO NEXT AVAIL
*                                                PRINT POS IN LINE
*                                                LEAVING 3 BLANKS
*                                                BETWEEN FIELDS
BUMPLINE LA    LAYOUT,THREE(LAYOUT,LABELM1) BUMP TO NEXT LAYOUT ENTRY
         B     COMMON                  LOOP
SPECSPAC SR    WORK,WORK               ZERO VALUE
         IC    WORK,ABDBLNKS           OBTAIN COUNT OF BLANKS
         N     WORK,S4TO7              ZERO MEANINGLESS BITS
* THE RESIDUAL COUNT IS THEN THE NUMBER OF BLANKS TO BE LEFT BETWEEN
* FIELDS
         AR    OUTLINE,WORK            CALCULATE NEXT AVAIL PRINT POS
         LA    OUTLINE,ONE(FIELDM1,OUTLINE) LEAVING NUMBER OF BLANKS
*                                           RQUESTED BY USER
         B     BUMPLINE                RESUME NORMAL PATH
DONE     LA    LAYOUT,ONE(LAYOUT)      POS OVER END INDICATOR
         LA    RETCODE,ZERO            INDICATE NORMAL COMPLETION
EXIT     STM   DATAREG,OUTLINE,ABDBPTR UPDATE ABDAREA FIELDS
         NI    ABDUPRF,ALL-UPRFMAT     RESET UPR ANTICIPATED BIT
         L     RETREG,TWELVE(SAVEAREA) RESTORE CALLERS REGISTERS
         LM    R0,BASE,TWENTY(SAVEAREA) RESTORE CALLERS REGISTERS
         BR    RETREG                  RETURN TO CALLER
* THE FOLLOWING ENTRY POINT IS USED TO BYPASS INDENTATION PROCESSING
* AND TO POSITION THE FIRST PRINT POSITION AT OTHER THAN THE BEGINNING
* OF THE LINE
         DROP  BASE
         USING *,BASEIN
IEAVAD41 STM   RETREG,BASE,TWELVE(SAVEAREA)
         BALR  BASE,ZERO               OBTAIN ADDRESS OF KNOWN LOCATION
NEWVAL   LA    WORK,NEWVAL-OLDVAL      DIFFERENCE IN BASE ADDRESSES
         SR    BASE,WORK               BASE OF IEAVAD31 IS IN REG
         DROP  BASEIN
         USING OLDVAL,BASE
         LM    DATAREG,OUTLINE,ABDBPTR OBTAIN VALUES FOR MAINLINE
         B     COMMON                  ENTER MAINLINE CODE AFTER
*                                      INDENTATION PROCESSING
* THIS SECTION OF CODE RECEIVES CONTROL WHEN A UPR INTERRUPT OCCURS
* AND THE USER REQUESTED THAT THE ROUTINE FIELD THE EVENT
FORMATX  LR    R1,R2                   RESTORE ABDUMP PARM ADDRESS
*                                                              ZA13835
         LA    RETCODE,EIGHT           SET RETURN CODE TO CALLER
         B     EXIT                    EXECUTE CLEANUP
*
* EXECUTED INSTRUCTIONS
*
MOVE1    MVC   ZERO(ONE,OUTLINE),TWO(LAYOUT)     EXECUTED TO MOVE LABEL
*                                                TO LINE
MOVE2    MVC   ABDFMTWK(ONE),ZERO(DATAREG)  EXECUTED TO MOVE DATA
*                                           TO WORK AREA
MOVE3    MVC   ZERO(ONE,OUTLINE),ABDFMTWK   EXECUTED TO MOVE DATA TO
*                                           OUTPUT LINE
*
* CONSTANTS
*
         DS    0F
S4TO7    DC    X'0000000F'             MASK TO ZERO RSET OF BITS
* THE ORIGIN OF TBL MUST BE AT LEAST 240 BYTES FROM THE POINT OF
* ADDRESSIBILITY SINCE IT IS REFERENCED AS TBL-240
         DC    2F'0'                   SPACE TO INSURE TABLE CAN BE
*                                      ADDRESSED
TBL      DC    C'0123456789ABCDEF'     TABLE TO TRANSLATE DATA
* THE FOLLOWING MACRO PRODUCES THE MAPPING OF THE ABDUMP WORK AREA
         IHAABDA
         END
