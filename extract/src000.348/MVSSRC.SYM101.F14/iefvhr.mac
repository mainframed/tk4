*/*
**********************************************************************
*
*   MODULE NAME= IEFVHR
*
*   DESCRIPTIVE NAME= OPERATOR MESSAGE MODULE
*
*   COPYRIGHT= V/A
*
*   STATUS= OS/VS2 RELEASE 2
*
*   FUNCTION= IEFVHR WRITES ERROR MESSAGES TO THE OPERATOR
*      OPERATION= WHEN IEFVHR RECEIVES CONTROL, REGISTER 5 CONTAINS
*                 THE MESSAGE CODE.  THIS CODE IS USED AGAINST A LOOK
*                 UP TABLE TO ISSUE THE CORRECT WTO. RETURN TO CALLER.
*
*   NOTES
*      CHARACTER CODE DEPENDENCIES= EBCDIC
*      DEPENDENDIES= NONE
*      RESTRICTIONS= NONE
*      REGISTER CONVENTIONS= R0-RF
*      PATCH-LABEL= PATCH
*
*   MODULE TYPE= PROCEEDURE
*      PROCESSOR= ASSEMBLER
*      MODULE SIZE= 1150
*      ATTRIBUTES= NCAL,LET,LIST,XREF,RENT,REFR
*
*   ENTRY POINT= IEFVHR
*      PRUPOSE= ISSUE OPERATOR MESSAGES
*      LINKAGE= LOAD VCON AND BRANCH REGISTER
*      INPUT DATA= MESSAGE POINTER (REG.5) IWA POINTER (REG. 12)
*      REGISTERS SAVED= 14,12
*      REGISTER CONTENTS DURING PROCESSING=
*   REGISTERS RESTORED= 14,12
*
*   EXIT - NORMAL= BRANCH REGISTER 14
*      CONDITIONS= MESSAGE IS WRITTEN TO OPERATOR
*      OUTPUT DATA= NONE
*      RETURN CODES= NONE
*
*   EXET - ERROR= NONE
*
*   EXTERNAL REFERENCES=
*      ROUTINES= NONE
*      DATA AREAS= IWA,CWA
*      CONTROL BLOCKS= UCB
*      TABLE= JCT
*      MACROS=SAVE,IEFSAVER,LINK,FREEMAIN,GETMAIN,WTO,IEFRELSE,RETURN
*      ENQUEUE RESOURCES= NONE
*      CHANGE LEVEL=
*
*   MESSAGES=
*
**********************************************************************
**/
         EJECT
         IEFCOMWA                                                Y02668
         EJECT
HIUCB    DSECT
         IEFUCBOB
         EJECT
WTOBUFER DSECT                                                     MCS
         USING WTOBUFER,R3                                         MCS
         DS    0D                                                  MCS
TEXT     DS    18F                     MESSAGE WROK AREA           MCS
         ORG   WTOBUFER
TEXT1    DS    CL25                MSGLGTH,MCSFLGS,MSGTXT AREA         *
                                     XMCS
UCBNM    DS    CL3                 UCBNM FILLER FOR MSG6               *
                               XMCS
TEXT2    DS    11F                REST OF MSG TEXT,ROUTCDES AND DESCS
*                                                                  XMCS
*********
*
*********
PARAM    DSECT
         USING PARAM,R2
         DS    0D                                                  XMCS
UCBPTR   DS    F                   UCB POINTER                     XMCS
TABPTR   DS    F                   JOBNAME LOC PTR                 XMCS
NUMENTRY DS    F                   NUMBER OF ENTRIES               XMCS
JOBNM    DS    2F                  JOBNAME                         XMCS
UNUSED   DS    F                                                   XMCS
*
         ORG   UCBPTR
FLAG     DS    CL1                 USER COUNT SWITCH               XMCS
PTR      DS    CL3                 UCB PTR                         XMCS
D4       EQU   4                                                 A30871
D17      EQU   17                                                A30871
*********
*
IEFVHR   CSECT
         SAVE  (14,12)
         BALR  RB,0
         USING *,RB
*****************************************************************Y02668
* MODULE TRACE CODE - FOR TESTING.                               Y02668
         L     RF,TRACEV           LOAD TRACE RTNE ADDR.         Y02668
         BALR  RE,RF               ENTER MOD ID IN TRACE RECORD. Y02668
TRACEV   DC    V(TRACE)            TRACE RTNE ADDR.              Y02668
         DC    C'VHR '             MOD ID USED BY TRACE.         Y02668
* TRACE RETURNS HERE.                                            Y02668
*****************************************************************Y02668
         B     HR1
         MODID BR=NO                                             Y01886
         DC    C'SPLT'             FUNCTION ID AND               Y02668
         DC    C'02'               LEVEL # (NEL CODE DELETION).  Y02668
         DC    C'MVM'              FUNCTION ID AND               Y02668
         DC    C'10'         LEVEL # ( MSG3 AND MSG5 FIX )      YM00372
HR1      DS    0H
         IEFSAVER SAVEPTR
         GETMAIN R,LV=96           AREA FOR WTOBUF AND PARAMLIST   XMCS
         LA    R2,72(R1)           PARAM LIST PTR                  XMCS
         LR    R3,R1               WTO BUFFER PTR                  XMCS
         XC    0(96,R1),0(R1)      CLEAR OUT AREAS                 XMCS
*
*        PREPARE FOR ERROR MSG WRITE TO OPERATOR
*
HR12     DS    0H
         BCTR  R5,0                    MINUS 1
         LR    R4,R5
         SLA   R5,2                    TIMES 4
         LA    R6,RTABLE                MSG TABLE
         L     R7,0(R5,R6)             MSG LOCATION
         LH    R5,0(R7)                LENGTH OF MESSAGE           MCS
         A     R5,THREE                PLUS ROUTCDE AND DESC LEN   MCS
         EX    R5,MOVE                                             MCS
HR3      DS    0H
         LA    R6,RTEXT            DISPLACEMENT TABLE              XMCS
         AR    R6,R4                                               XMCS
         SR    R5,R5               CLEAR REGISTER
         IC    R5,0(R6)            AMOUNT OF DISPL.                XMCS
         AR    R5,R3               REG5 PTS TO WHERE JOBNAME MUST BEMCS
*                                  FILLED IN                       XMCS
         MVC   0(8,R5),WAJBNAME    ENTER JOBNAME INTO MESSAGE.   Y02668
ISSUE1   DS    0H
         SR    R0,R0                                               GMCS
         C     R0,IWACONID         ANY CONSOLE ID PTR              GMCS
         BE    ISSUE2              NO, DEFAULT TO MASTER CONSOLE   GMCS
         L     R1,IWACONID         PTR TO CONSOLE ID               GMCS
         IC    R0,0(R1)            CONSOLE ID                      GMCS
ISSUE2   DS    0H
         WTO   MF=(E,(3))
HR331    DS    0H                                                PTM725
         IEFRELSE SAVEPTR
         LR    R1,R3               RESET PTR TO GOTTEN CORE        XMCS
         FREEMAIN R,LV=96,A=(1)                                    XMCS
         RETURN (14,12)                                           M1977
MOVE     MVC   TEXT(0),0(R7)
RTABLE   DS    0F
         DC    A(MSG1)
         DC    A(MSG2)                                           Y02668
         DC    A(MSG3)
         DC    A(MSG4)
         DC    A(MSG5)
         DC    A(MSG6)                                           Y02668
RTEXT    DS    0F                                                  XMCS
         DC    AL1(39)             MSG1                          Y02668
         DC    AL1(53)             MSG2                          Y02668
         DC    AL1(43)             MSG3                         YM00372
         DC    AL1(65)             MSG4                          Y02668
         DC    AL1(71)             MSG5                         YM00372
         DC    AL1(12)             MSG6                          Y02668
MSG1     WTO   'IEF677I WARNING MESSAGE(S) FOR JOB          ISSUED',   $
               MF=L,MCSFLAG=(REG0),DESC=(4),ROUTCDE=(2)
MSG2     WTO   'IEF417I PROCLIB DEVICE I/O ERROR READING FOR JOB       *
                 ',MF=L,ROUTCDE=(2,10),MCSFLAG=(REG0),DESC=(4)
*                                                                  XMCS
*
MSG3     WTO   'IEF430I RESTART STEP NOT FOUND FOR JOB         ',      *
               MF=L,MCSFLAG=(REG0),DESC=(4),ROUTCDE=(2)
MSG4     WTO   'IEF679I DEVICE I/O ERROR CONVERTING/INTERPRETING JCL FO*
               R JOB         ',                                        *
               MF=L,MCSFLAG=(REG0),DESC=(4),ROUTCDE=(2,10)
MSG5     WTO   'IEF680I DEVICE I/O ERROR WRITING TO SYSTEM MESSAGE DATA*
               SET FOR JOB         ',                                  *
               MF=L,MCSFLAG=(REG0),DESC=(4),ROUTCDE=(2,10)
MSG6     WTO   'IEF452I          JOB NOT RUN - JCL ERROR',             *
               MF=L,MCSFLAG=(REG0),DESC=(4),ROUTCDE=(2,10)
RETCD    EQU   8                   R/I TERMINAL ERROR            A38453
RETCDREG EQU   16                  DISPLACEMENT TO RETURN CODE    M1977
THREE    DC    F'3'
XMSGB    DC    A(5)
SIX      DC    F'6'                    SPOOL FULL AND WAITING CODE 0416
*                                      MINUS ONE                   0416
CON11    DC    H'11'               MESSAGE 12 CONSTANT             AACA
PATCH    DC    25F'0'              **********PATCH SPACE*********Y02668
         END
