***********************************************************************
*
*   MODULE NAME= IEFVHEB
*
*   DESCRIPTIVE NAME= PRE-SCAN ROUTINE
*
*   COPYRIGHT= N/A
*
*   STATUS= OS/VS2 RELEASE 2
*
*   FUNCTION= PERFORMS MISCELLANEOUS PRESCAN FUNCTIONS AND SMF
*             INITIALIZATION, AND ENTERS STATEMENTS (VIA IEFVGM) IN THE
*             LIST DATA SET
*      OPERATION= 1. CHECKS MSGLEVEL FLAGS AND ENTERS STATEMENT IN
*                    LIST DATA SET ACCORDING TO FLAG SETTINGS.
*                 2. CHECKS PRINT SWITCH TO SEE IF MODULE WAS ENTERED
*                    IN THE NORMAL FLOW, OR IS BEING USED AS A SUB-
*                    ROUTINE TO ENTER A STATEMENT IN THE LIST DATA SET.
*                 3. COMPUTES STATEMENT NUMBER AND ENTERS IT IN OUTPUT
*                    BUFFER PRIOR TO LISTING THE STATEMENT.
*                    CONTINUATIONS, COMMENTS, AND COMMANDS
*                    DO NOT HAVE STATEMENT NUMBERS;THEREFORE,
*                    NO COMPUTATION IS DONE.
*                 4. IF A SYSCHK DD IS BEING PROCESSED, MODULE WILL
*                    OBTAIN STORAGE AND SAVE THE STATEMENT AND PASS
*                    INSTEAD, TO THE SCAN ROUTINE, A GENERATED EXEC
*                    STATEMENT FOR PROCESSING.
*                 5. MOVES THE JMR FROM THE JESS WORK AREA TO THE CWA,
*                    DOES SOME INITIALIZATION, AND BRANCHES TO IEFUJV
*                    TO PERMIT USER SMF PROCESSING.
*
*   NOTES=
*      CHARACTER CODE DEPENDENCIES= EBCDIC
*      DEPENDENCIES= NONE
*      RESTRICTIONS= NONE
*      REGISTER CONVENTIONS=
*                            R5- POINTER TO JMR
*                            R9- POINTER TO JCL STATEMENT
*                            RA- POINTER TO STATEMENT PARAMETER LIST
*                            RB- BASE REGISTER
*                            RC- POINTER TO CWA
*                            RD- SAVE AREA POINTER
*      PATCH-LABEL= PATCH
*
*   MODULE TYPE= BAL
*      PROCESSOR= ASSEMBLER
*      ATTRIBUTES= NCAL,LET,LIST,XREF,REFR,RENT
*
*   ENTRY POINT= IEFVHEB
*      PURPOSE= PRESCAN FUNCTIONS
*      LINKAGE= BRANCH ON REGISTER
*      INPUT DATA= JCL STATEMENT
*                  STATEMENT PARAMETER LIST
*                  CONVERTER/INTERPRETER ENTRY LIST (NEL)
*                  CONVERTER WORK AREA
*                  R9- POINTS TO JCL STATEMENT
*                  RA- POINTS TO STATEMENT PARAMETER LIST
*                  RC- POINTS TO CWA
*      REGISTERS SAVED= NONE
*      REGISTER CONTENTS DURING PROCESSING= SEE REGISTER CONVENTIONS
*      REGISTERS RESTORED= NONE
*
*   EXIT - NORMAL= IEFVFA
*      CONDITIONS= UNCONDITIONALLY (IF NOT USED AS STMT # SUBRTN)
*      OUTPUT DATA= INITIALIZED JMR
*                   GENERATED EXEC IF SYSCHK DD FOUND
*                   MODIFIED STATEMENT IMAGE FOR STMT WITH PROC VERB
*      RETURN CODES= NONE
*
*   EXIT - NORMAL= CALLER
*      CONDITIONS= MSGLEVEL FLAGS INITIALIZED, PRINT SWITCH ON
*                  WHEN MODULE WAS ENTERED
*      OUTPUT DATA= STMT NO. CALC AND STMT PRINTED IF MSGLEVEL=1
*      RETURN CODES= NONE
*
*   EXIT - ERROR= IEFVFA
*      CONDITIONS= UNCONDITIONALLY
*      OUTPUT DATA= INITIALIZED JMR
*                   ERROR MESSAGE WRITTEN TO MESSAGE DATA SET
*      RETURN CODES= NONE
*
*   EXTERNAL REFERENCES=
*      ROUTINES= BRANCH TO IEFVGM- WRITE JCL STATEMENT TO LIST DATA
*                                  SET, OR ERROR MESSAGE TO MESSAGE
*                                  DATA SET
*      DATA AREAS= CONVERTER WORK AREA
*                  JCL STATEMENT
*                  STATEMENT PARAMETER LIST
*      CONTROL BLOCKS= JMR, NEL, CWA
*      TABLES= NONE
*      MACROS= IEFVMCWA, IEFJMR, IEFNEL
*      ENQUEUE RESOURCES= NONE
*      CHANGE LEVEL= NONE
*
*   MESSAGES= IEF611I OVERRIDDEN STEP NOT FOUND IN PROCEDURE
*
***********************************************************************
         EJECT
         IEFJMR
         EJECT
         IEFCOMWA                                                Y02668
         IEFCVRWA                                                Y02668
JCTD     DSECT
         IEFAJCTB
         IEFTXTFT                                                Y02668
NEL      DSECT                                                     AACA
         IEFNEL SUBCOM=C
         EJECT
IEFVHEB  CSECT
* C 012930,012960,014175-014193,024400 (10/21/74)              @OZ02100
* A 014175-014185 (10/21/74)                                   @OZ02100
*                   R9=STMT PTR
*                   RA=STMT PARM LIST PTR
*                   RD=PTR TO CURRENT REG SAVE AREA
         SPACE 1                                                 Y02668
ZONE     EQU   X'F0'                                             Y02668
BLANK    EQU   C' '                                              Y02668
EXEC     EQU   2                                                 Y02668
DD       EQU   4
JOB      EQU   1
STNLTH   EQU   10                  STMT # FIELD LENGTH           Y02668
STNBUFL  EQU   90                  STMT# + MSG LENGTH            Y02668
BUFL     EQU   STNBUFL-STNLTH      MSG LENGTH                    Y02668
MSG0B    EQU   11                                                Y02668
         BALR  RB,0
         USING *,RB          *
         ST    RE,AOHEBSA          SAVE EXIT ADDR FOR RE RETURN. Y02668
*****************************************************************Y02668
* MODULE TRACE CODE - FOR TESTING.                               Y02668
         L     RF,TRACEV           LOAD TRACE RTNE ADDR.         Y02668
         BALR  RE,RF               ENTER MOD ID IN TRACE RECORD. Y02668
TRACEV   DC    V(TRACE)            TRACE RTNE ADDR.              Y02668
         DC    C'VHEB'             MOD ID USED BY TRACE.         Y02668
* TRACE RETURNS HERE.                                            Y02668
*****************************************************************Y02668
         B     C2
         MODID BR=NO                                             Y01886
         DC    C'SPLT'             FUNCTION ID AND               Y02668
         DC    C'07'               LEVEL # (STMT NUMBERING FIX). Y02668
         DC    C'REL3'              FUNCTION ID AND            @OZ02100
         DC    C'01'   LEV #(SMF EXIT ENTERED TOO MANY TIMES). @OZ02100
C2       DS    0H
*                                                                Y02668
*    MOVE STMT INTO WRITE BUFFER; FIRST SIX BYTES OF BUFFER      Y02668
*    ARE FOR THE STMT NUMBER.                                    Y02668
*                                                                Y02668
         L     R1,AOWSTMT          GET BUFFER ADDR               Y02668
         MVI   0(R1),BLANK         BLANK OUT THE BUFFER          Y02668
         MVC   1(STNBUFL,R1),0(R1) BEFORE MOVING IN STMT         Y02668
         MVC   STNLTH(BUFL,R1),0(R9) MOVE STMT INTO BUFFER       Y02668
         TM    AOSW1,AOPRTSW       IS A PRINT OPERATION INDIC'D  Y02668
         BZ    AOEP0100            NO, CHECK FOR OVERRIDDEN STMT Y02668
         EJECT
*************************************************************** YM03455
*                        PRINT SUBROUTINE                       YM03455
*         THIS SUBROUTINE RETURNS TO IEFVHEB'S CALLER           YM03455
*************************************************************** YM03455
         NI    AOSW1,255-AOPRTSW   YES, RE-INITIALIZE PRINT SW   Y02668
         TM    AOSW1,AOILGLOP      Q  A MESSAGE                  Y02668
         BZ    AOEP0010            NO, DO NOT INCRE STMT #       Y02668
         NI    AOSW1,255-AOILGLOP  RE-INITIALIZE SWITCH          Y02668
         LH    R1,WASTMTNO         YES,INCREASE STMT # COUNT     Y02668
         LA    R1,1(R1)            BY ONE                        Y02668
         STH   R1,WASTMTNO         SAVE UPDATED STMT NUMBER      Y02668
         L     RF,IEFVGMV          PUT MESSAGE OUT               Y02668
         BALR  RE,RF                                             Y02668
         BAL   RE,AOEPCVT          CONVERT AND STORE STMT #      Y02668
AOEP0010 DS    0H                                                Y02668
         TM    AOSW4,CWATERM       PROCESSING RDR NULL ?        YM03455
         BZ    AOEP0015            NO BYPASS SMF                YM03455
         BAL   R6,TRYSMF           GO TO SMF ROUTINE            YM03455
AOEP0015 DS    0H                                               YM03455
         TM    IWAJMSGL,AOMSGLV1+AOMSGLV2  MSGLEVEL= 1 OR 2 ?   YA02015
         BZ    AOEP0020            NO,DO NOT PRINT STMT          Y02668
         TM    SWE,GPI       INPUT READ FROM PROC?              YA02015
         BZ    AOEP0030            NO,PRINT STMT                 Y02668
         TM    IWAJMSGL,AOMSGLV2   MSGLEVEL=2?                   Y02668
         BO    AOEP0020            YES, DO NOT PRINT STMT        Y02668
AOEP0030 DS    0H                **PRINT STMT RTN                Y02668
         SR    R2,R2               SET LIST STMT CODE FOR VGM.   Y02668
         L     RF,IEFVGMV          MSG RTN ADDR                  Y02668
         BALR  RE,RF               GO TO PUT OUT STMT            Y02668
AOEP0020 DS    0H                **DO NOT PRINT,INSTEAD,RETURN   Y02668
         L     RF,AOHEBSA          LOAD RETURN ADDRESS.          Y02668
         BR    RF                  EXIT                          Y02668
         EJECT
*************************************************************** YM03455
*            CONVERT & STORE STMT # ROUTINE                     YM03455
*************************************************************** YM03455
AOEPCVT  DS    0H                **CONVERT AND STORE RTN         Y02668
         LH    R6,WASTMTNO         GET NUMBER OF THIS STMT       Y02668
         CVD   R6,AOEPWORK         CONVERT STMT # TO DECIMAL     Y02668
         L     R5,AOWSTMT          R5 PTS TO WRITE BUFFER        Y02668
         UNPK  0(5,R5),AOEPWORK(8) UNPK STMT # INTO FIRST        Y02668
*                                  5 BYTES OF BUFFER             Y02668
         OI    4(R5),ZONE          CHANGE SIGN TO ZONE           Y02668
AOEPCVT1 DS    0H                                                Y02668
         CLI   0(R5),ZONE          CHECK FOR LEADING ZERO        Y02668
         BNE   AOEPCVT2            NO, BRANCH AROUND BLANKING    Y02668
         MVI   0(R5),BLANK         CHANGE LEADING ZERO TO BLANK  Y02668
         LA    R5,1(R5)            BUMP PTR                      Y02668
         B     AOEPCVT1            GO TO CHECK AGAIN             Y02668
AOEPCVT2 DS    0H                                                Y02668
         BR    RE                                                Y02668
         EJECT
*************************************************************** YM03455
*                   PRE  SCAN  ROUTINE                          YM03455
*************************************************************** YM03455
AOEP0100 DS    0H                                                Y02668
         TM    CRSW1,CPFLG         C/R EXEC? (INTERNAL STMT).   YM00371
         BO    AOEP0500            BR, DO NOT UPDATE STMT #.    YM00371
         CLC   0(2,R9),AOEPOVR     OVERRIDDEN STMT?              Y02668
         BE    AOEP0500            YES, NO STMT # NEEDED         Y02668
         CLC   0(2,R9),AOEPOVR1   O'RIDDEN STMT FROM INSTR PROC YM02704
         BE    AOEP0500            YES NO STMT # NEEDED         YM02704
         TM    SWB,CXP             CONT. EXPECTED ?              Y02668
         BO    AOEP0500            YES DON'T INCR. STMT #       YM00370
         LH    R6,WASTMTNO         GET NUMBER OF LAST STMT       Y02668
         LA    R6,1(R6)            INCREMENT BY ONE              Y02668
         STH   R6,WASTMTNO         SAVE NUMBER OF CURRENT STMT   Y02668
         BAL   RE,AOEPCVT          CONVERT AND INSERT STMT #     Y02668
AOEP0500 DS    0H                **END OF STMT # PROCESSING      Y02668
         TM    VERB(RA),1               JOB
         BO    C22                      BR YES
         TM    CRSW1,CPFLG         Q. CKPT/REST FLAG ON            AACA
         BZ    C20                 NO, GOTO CHECK MSGLEVEL       Y02668
         LA    R0,CKPTLGT          AREA FOR DD STMT                AACA
         GETMAIN R,LV=(0)                                          AACA
         MVC   0(80,R1),0(R9)      SAVE SYSCHK DD STMT AND         AACA
         MVC   80(8,R1),0(RA)      SAVE STMT CONTROL DATA FOR SCAN AACA
         ST    R1,CPSTMTP           ADDR OF SAVE AREA              AACA
         MVC   0(CPSTMTED,R9),CPSTMTC BUILD EXEC STMT FOR CKPT     AACA
   MVC CPSTMTL(80-CPSTMTL,R9),CPSTMTL-1(R9)  BLK AREA LEFT         AACA
         MVI   NAMEL(RA),8         SET UP NAME LENGTH,             AACA
         MVI   VERB(RA),2           VERB ID AND                    AACA
         MVI   LISTPTR(RA),16       REL. LIST PTR OFFSET           AACA
         OI    SWH,CPSYSFLG        SET C/R EXEC FLAG               AACA
         B     C213                                                AACA
C20      DS    0H                                                  AACA
         TM    IWAJMSGL,AOMSGLV1   MSGLEVEL=1?                   Y02668
         BO    C23                      BR YES
         TM    IWAJMSGL,AOMSGLV2   MSGLEVEL=2?                   Y02668
         BZ    C201                    LEVEL=0 - TRY SMF EXIT    A32886
         LA    R3,RSTMT                PTR TO ADDR OF RDR STMNT   M0528
         CR    R3,RA                   RA POINTS TO ADDR OF THE   M0528
*                                      BUFFER CURR BEING USED     M0528
         BE    C23                     IF EQUAL THIS CARD IS      M0528
*                                      FROM THE INPUT STREAM      M0528
*                                      GO PUT INTO SMB            M0528
C201     TM    SWB,CXPN       CONTINUATION EXPECTED BUT NOT    @OZ02100
*                                    RECEIVED?                 @OZ02100
         BO    C21            YES, DO NOT GO TO SMF USER EXIT  @OZ02100
         BAL   R6,TRYSMF             SMF USER EXIT AVAILABLE?  @OZ02100
C21      DS    0H
         CLI   VERB(RA),8            **Q. PROC VERB
         BNE   C213                  ** BR-NO
         TM    SWB,CXP               CONT. OF PROC STMT           19253
         BO    C213                  BRANCH-YES                   19253
         CLI   LISTPTR(RA),71        **Q. NULL OPERANDS
         BE    C212                  ** BRANCH - YES
         SR    R4,R4                 **LOAD REL. LIST DISPL.
         IC    R4,LISTPTR(RA)        **
         SH    R4,CON4               **ADJUST REL. LIST
         STC   R4,LISTPTR(RA)        ** PTR. FOR 'PROC,'
         AR    R4,R9                 **COMPUTE PROC NAME ADDRESS
         MVC   0(5,R4),PROCA         **INSERT PROC NAME IN OPERAND
         B     C216                  ** EXIT TO SCAN RTN
C212     DS    0H                    **
         LA    R4,16                 **ADJUST THE REL.
         STC   R4,LISTPTR(RA)        ** LIST PTR.
         AR    R4,R9                 **COMPUTE ADDRESS OF INSERTION
         MVC   0(4,R4),PROCA         ** AND MOVE PROC OPERAND
         B     C216                  **
C213     DS    0H                    **
         TM    VERB(RA),SSE+CRE    DD STMT SEQ. ERROR              AACA
         BNZ   C217                 BR YES                         AACA
         B     C216                GO TO VFA EXIT                Y02668
         EJECT
***********************************************************************
*        SMF OPTION                                                SMF1
***********************************************************************
TRYSMF   EQU   *                       *                           SMF1
         L     R5,WANELPTR         LOAD NEL ADDRESS.             Y02668
         USING NEL,R5              NEL ADDRESSABILITY.           Y02668
         TM    NELOPSWT,NELSMF     SMF  REQUESTED?               Y02668
         BCR   8,R6                BRANCH NO.                    Y02668
         L     R5,CWAJMRPT         LOAD JMR ADDRESS.             Y02668
         DROP  R5                                                Y02668
         USING JMR,R5              JMR ADDRESSABILITY.           Y02668
         L     RF,JMRUJVP          LOAD USER EXIT ADDR, IF ANY.  Y02668
         TM    JMROPT,JMREXITS     HAS A USER EXIT BEEN REQUESTEDY02668
         BCR   IFZERO,R6           NO, RETURN                    Y02668
         LA    R1,JMRPTRS              GET ADDR OF PARAM LIST      SMF1
         LA    R4,JMRJOB               GET ADDR OF JOB LOG         SMF1
         ST    R4,JMRJOBP              STORE JOB LOG ADDR IN       SMF1
*                                           PARAM LIST             SMF1
         ST    R9,JMRJCLP              STORE ADDR OF CARD IMAGE    SMF1
         MVC   JMRJCLCD(DEC1),VERB(RA) PUT VERB CODE IN LIST     Y02668
         NI    JMRJCLCD,ERBITOFF   CLEAR ALL BITS EXCEPT THOSE   Y02668
*                                  INDICATING VERB CODE          Y02668
         LA    R4,JMRJCLCD         GET PTR TO VERB CODE          Y02668
         ST    R4,JMRJCLCP         SAVE PTR IN PARMLIST          Y02668
         LA    R4,LISTPTR(RA)          POINT TO OPERAND OFFSET     SMF1
         ST    R4,JMRJCLCP+DEC4        *                           SMF1
         L     RF,JMRUJVP          GET PTR TO USER EXIT          Y02668
         BALR  RE,RF                   GO TO USER ROUTINE          SMF1
         L     R1,AOWSTMT          GET BUFFER ADDR              YM03455
         MVC   STNLTH(BUFL,R1),0(R9) UPDATE BUFFER TO REFLECT   YM03455
*                                  POSSIBLE USER UPADTE         YM03455
         LA    R4,DEC4                 *                           SMF1
         CR    RF,R4                   CHECK FOR JOB CANCEL        SMF1
         BCR   DEC7,R6                 *                           SMF1
         LA    R5,WAJOBPFX         ADDR OF PSEUDO JOB PFX.       Y02668
         USING TEXT,R5             PREFIX ADDRESSABILITY.        Y02668
         OI    STRJINDC,JTXJOBFL   SET JOB FAILED BIT.           Y02668
         DROP  R5                                                Y02668
         BR    R6                      *                           SMF1
C217     DS    0H
         LA    R2,MSG59            SET UP C/R ERROR MESSAGE        AACA
         TM    VERB(RA),CRE        Q. C/R ERROR                    AACA
         BO    C218                 BR YES                         AACA
         NI    VERB(RA),255-SSE         SEQ ERR OFF
         LA    R2,MSG0B                *OVERRIDDEN STEP
C218     DS    0H                                                  AACA
         NI    VERB(RA),255-CRE    CLEAR C/R ERROR FLAG            AACA
         L     R5,IEFVGMV          LOAD BRANCH ADDR FOR MSG.     Y02668
         B     C2161                                             Y02668
C22      DS    0H
         TM    SWB,CXP                  JOB STMT CONTINUATION
         BO    C23                      BR YES
         NI    SWD,255-FEXRCV           1ST EXEC RCVD OFF
         LA    R5,WAJBNAME         SET WAJBNAME AS TARGET FIELD. Y02668
         BAL   RE,AOMVJBNM         MOVE JOBNAME TO WAJBNAME.     Y02668
         B     C225                                              Y02668
C225     DS    0H
         SR    R2,R2                    SET FOR MSG ROUTINE
         BAL   R6,TRYSMF               *                           SMF1
DEC4     EQU   4                       NUMERIC EQUATE
DEC8     EQU   8                       NUMERIC EQUATE
DEC7     EQU   7                       NUMERIC EQUATE
DEC5     EQU   5                       NUMERIC EQUATE
HEX60    EQU   X'60'                   HEX EQUATE
DEC3     EQU   3                       NUMERIC EQUATE
DEC20    EQU   20                      NUMERIC EQUATE
DEC1     EQU   1                       NUMERIC EQUATE
HEX01    EQU   X'01'                   HEX EQUATE
HEX40    EQU   X'40'                   HEX EQUATE
DEC11    EQU   11                      NUMERIC EQUATE
DEC0     EQU   0                       NUMERIC EQUATE
DEC2     EQU   2                       NUMERIC EQUATE
         L     RF,IEFVGMV
         BALR  RE,RF                    STMT TO SMB
         B     C21                                                 AACA
C23      DS    0H
         TM    SWB,CXPN                 CONT NOT RCVD
         BO    C216                    BRANCH YES              @OZ02100
         TM    SWB,CXPC                 CANCELLED
         BZ    C225                     BR NO
         CLI   2(R9),C' '         COL 3 BLANK                      AABA
         BNE   C225               BRANCH NO                        AABA
         CLC   3(68,R9),2(R9)     4 TO 71 BLANK, NULL              AABA
         BNE   C225               BRANCH NO                        AABA
C24      EQU   *
         TM    IWAJMSGL,AOMSGLV1   MSGLEVEL=1?                   Y02668
         BZ    C216                NO, ALREADY BEEN TO TSTSMF    Y02668
         BAL   R6,TRYSMF               YES, GO TO SMF
C216     DS    0H                                                Y02668
         L     R5,IEFVFAV          LOAD BRANCH ADDR FOR SCAN.    Y02668
C2161    DS    0H                                                Y02668
C2160    DS    0H                                                Y02668
         LR    RF,R5               MOVE BRANCH ADDR TO BR REG.   Y02668
         L     RE,IEFVFAV          LOAD RETURN TO SCAN IN CASE   Y02668
         BR    RF                  BRANCH IS TO MSG MODULE.      Y02668
         EJECT                                                   Y02668
*****************************************************************Y02668
*                                                               *Y02668
*        AOMVJBNM BLANKS  OUT AN 8 BYTE TARGET FIELD AND THEN   *Y02668
*        MOVES THE JOBNAME INTO IT.                             *Y02668
*                                                               *Y02668
*        INPUT -                                                *Y02668
*           R5 - ADDRESS OF TARGET FIELD.                       *Y02668
*           R9 - ADDRESS OF CARD IMAGE.                         *Y02668
*           RA - ADDRESS OF STATEMENT PARAMETER LIST (RSTMT).   *Y02668
*                                                               *Y02668
*        NOTE -                                                 *Y02668
*           R1 IS USED AS A WORK REGISTER AND ITS CONTENTS      *Y02668
*           ARE NOT SAVED.                                      *Y02668
*                                                               *Y02668
*****************************************************************Y02668
         SPACE 2                                                 Y02668
AOMVJBNM DS    0H                                                Y02668
         MVC   0(8,R5),AOBLKNAM    BLANK THE TARGET FIELD.       Y02668
         SR    R1,R1               CLEAR LTH REG.                Y02668
         IC    R1,NAMEL(RA)        LOAD JOBNAME LTH.             Y02668
         LTR   R1,R1               LENGTH = 0?                   Y02668
         BZ    AOMVJ090            BRANCH YES TO EXIT.           Y02668
         C     R1,AOMAXLTH         LENTH EXCEED MAX OF 8?        Y02668
         BNH   AOMVJ010            BRANCH NO.                    Y02668
* LENGTH EXCEED MAX OF 8.                                        Y02668
         LA    R1,8                SET DEFAULT OF 8. JOBNAME     Y02668*
                                   ERRORS WILL BE CAUGHT LATER.  Y02668
AOMVJ010 DS    0H                                                Y02668
         BCTR  R1,0                DECREMENT LTH FOR MOVE.       Y02668
         EX    R1,AOEXMOV          EXECUTE MOVE OF JOBNAME.      Y02668
AOMVJ090 DS    0H                                                Y02668
         BR    RE                  RETURN.                       Y02668
         SPACE 2                                                 Y02668
AOEXMOV  MVC   0(0,R5),2(R9)       EXECUTED MOVE OF JOBNAME.     Y02668
AOBLKNAM DC    CL8' '              8 BLANKS.                     Y02668
AOMAXLTH DC    F'8'                CONSTANT FOR MAX JOBNAME LTH. Y02668
         EJECT                                                   Y02668
IEFVFAV  DC    V(IEFVFA)           SCAN ROUTINE.                 Y02668
IEFVGMV  DC    V(IEFVGM)                MSG
IEFVHQV  DC    V(IEFVHQ)                Q MGR LINK
IEFVHAV  DC    V(IEFVHA)           CONVERTER GET RTN             Y02668
AOEPOVR  DC    C'X/'               OVERRIDDEN STMT SYMBOL        Y02668
AOEPOVR1 DC    C'+/'      O'RIDDEN STMT FROM INSTR PROC         YM02704
CON4     DC    H'5'                  **CONSTANT
AOTBUFL  DC    H'4000'             TEXT BUF LENGTH.              Y02668
PROCA    DC    C'PROC,'              **PROC NAME CONSTANT
CPSTMTC  DC    C'//IEFDSDRP EXEC PGM=IEFDSDRP ' GEND C/R EXEC.   Y02668
CPSTMTS  EQU   *                                                   AACA
CPSTMTED EQU   CPSTMTS-CPSTMTC                                     AACA
CPSTMTL  EQU   L'CPSTMTC           LTH OF GEN'D EXEC STMT.       Y02668
CKPTLGT  EQU   88                  AREA NEEDED FOR SYSCHK SAVEAREA AACA
CPSTMTP  EQU   IWAJOBS5            SYSCHK DD STMT SAVE AREA PTR    AACA
MSG59    EQU   59                  MISPLACED SYSCHK DD STMT        AACA
*                                   ERROR MESSAGE INDEX            AACA
         SPACE 1                                                 Y02668
INPUTA   EQU   40                  INPUT QUEUE 'A' QMPA INDICATORA44202
IFZERO   EQU   8                                                 Y02668
ERBITOFF EQU   X'0F'                                             Y02668
PATCH    DC    25F'0'              **********PATCH SPACE*********Y02668
         END
