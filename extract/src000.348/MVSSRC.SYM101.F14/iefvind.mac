*                                                                     *
*  TITLE: IEFVIND-EXPAND INTERFACE                                    *
*                                                                     *
*  FUNCTION: THE MODULE IS USED AS A PSEUDO ACCESS METHOD. THE ADDRESS*
*            OF THE ENTRY POINT HAS BEEN PUT INTO THE DCBREAD FIELD OF*
*            THE BPAM DCB. WHENEVER A READ IS ISSUED IN IEFVHA AND THE*
*            ADDRESS IS IN THE DCB THIS MODULE IS ENTERED. IT SETS UP *
*            PARAMETER LISTS AND BRANCHES TO IEZDCODE TO EXPAND THE   *
*            RECORD. IF ANOTHER QUEUE RECORD IS NEEDED, IT BRANCHES TO*
*            IEFVHQ TO READ THE NEXT RECORD.                          *
*                                                                     *
* ENTRY POINT: IEFVIND                                                *
*                                                                     *
* INPUT: RD POINTS TO THE SAVE AREA                                   *
*        RE RETURN ADDRESS                                            *
*        RC POINTS TO THE IWA                                         *
*                                                                     *
* EXITS: RETURNS ONLY TO THE CALLER.                                  *
*                                                                     *
* OUTPUT: ALL REGISTERS ARE RESTORED                                  *
*                                                                     *
* INTERNAL EXITS: IEFVHQ-READ ANOTHER RECORD FROM THE QUEUE           *
*                 IEZDCODE-EXPAND A RECORD                            *
*                                                                     *
* WORKAREAS AND TABLES-                                               *
*     .IEFVMIWA-IWA                                                   *
*     .IEFQMNGR-QMPA                                                  *
*     .WORKAREA-WORK AREA AND DIRECTORY                               *
*     .IEZPARM-PARAMETER LIST                                         *
*     .DCBD - BPAM DCB                                       M3739O106*
*                                                                     *
* REGISTER USEAGE: R4-PARAMETER LIST TO IEZDCODE                      *
*                  R6-QMPA                                            *
*                  R7-WORK AREA                                       *
*                  RB-ADDRESSIBILITY OF MODULE                        *
*                  RC-IWA                                             *
*                                                                     *
* MACROES USED: IEFSAVER                                              *
*               IEFRELSE                                              *
*               SAVE                                                  *
*               RETURN                                                *
*                                                                     *
* OPERATION:.REGISTERS ARE SAVED                                      *
*           .A SAVE AREA IS GOTTEN                                    *
*           .ADDRESSIBILITY IS ESTABLISHED FOR THE WORK AREA AND THE  *
*           .QMPA                                                     *
*           .THE ADDRESS OF THE PARAMETER LIST IS GOTTEN FROM IWAPARM,*
*            OFFSET X'100' IN THE IWA                                 *
*           .THE PARAMETER LIST IS COMPLETED IF NECESSARY             *
*           .IEZDCODE EXPANDS A RECORD                                *
*           .A RETURN CODE IS TESTED                                  *
*            A ZERO INDICATES THAT THE RECORD WAS EXPANDED.           *
*            THE SAVE AREA RELEASED AND CONTROL RETURNED TO IEFVHA    *
*           IF THE RETURN CODE IS FOUR, THE LAST COMPRESSED           *
*           RECORD HAS ALREADY BEEN EXPANDED. IEFVHQ READS A NEW RECORD
*           FROM THE QUEUE AND THE RECORD IS EXPANDED.                *
*                                                                     *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         EJECT
WORKAREA DSECT
         USING WORKAREA,R7
WKTTR    DS    CL4                     POINTER TO NEXT RECORD
WKQMPAPT DS    CL4                     ADDRESS OF QMPA
WKRECORD DS    CL168                   COMPRESSED RECORD
WKPTR1   DS    CL4                     POINTER TO ITSELF
WKCT     DS    CL1                     NUMBER OF ENTRIES MADE IN
*                                      THIS SEGMENT OF THE DIRECTORY
WKPTR2   DS    CL3                     HOLDER OF TTR FROM QUEUE   O106
*                                      MANAGER                    O106
WKPROCN1 DS    CL8                     PROCEDURE NAME
WKTTR1   DS    CL3                     TTR OF FIRST RECORD
WKPROCN2 DS    CL8                     PROCEDURE NAME
WKTTR2   DS    CL3                     TTR OF FIRST RECORD
WKPROCN3 DS    CL8                     PROCEDURE NAME
WKTTR3   DS    CL3                     TTR OF FIRST RECORD
WKPROCN4 DS    CL8                     PROCEDURE NAME
WKTTR4   DS    CL3                     TTR OF FIRST RECORD
WKPROCN5 DS    CL8                     PROCEDURE NAME
WKTTR5   DS    CL3                     TTR OF FIRST RECORD
WKPROCN6 DS    CL8                     PROCEDURE NAME
WKTTR6   DS    CL3                     TTR OF FIRST RECORD
WKPROCN7 DS    CL8                     PROCEDURE NAME
WKTTR7   DS    CL3                     TTR OF FIRST RECORD
WKPROCN8 DS    CL8                     PROCEDURE NAME
WKTTR8   DS    CL3                     TTR OF FIRST RECORD
WKPROCN9 DS    CL8                     PROCEDURE NAME
WKTTR9   DS    CL3                     TTR OF FIRST RECORD
WKPROCNA DS    CL8                     PROCEDURE NAME
WKTTRA   DS    CL3                     TTR OF FIRST RECORD
WKPROCNB DS    CL8                     PROCEDURE NAME
WKTTRB   DS    CL3                     TTR OF FIRST RECORD
WKPROCNC DS    CL8                     PROCEDURE NAME
WKTTRC   DS    CL3                     TTR OF FIRST RECORD
WKPROCND DS    CL8                     PROCEDURE NAME
WKTTRD   DS    CL3                     TTR OF FIRST RECORD
WKPROCNE DS    CL8                     PROCEDURE NAME
WKTTRE   DS    CL3                     TTR OF FIRST RECORD
WKPROCNF DS    CL8                     PROCEDURE NAME
WKTTRF   DS    CL3                     TTR OF FIRST RECORD
WKSAVE1  DS    CL1                                               A33705
         EJECT
         IEFCOMWA
         IEFCVRWA
         EJECT
         IEFQMNGR
         EJECT
QMPADDR  DS    CL4                     ADDRESS OF WORK BUFFER
QMPATTR  DS    CL3                     TTR OF JOB QUEUE RECORD
QMPAZER  DS    CL1                     RESERVED FIELD
         EJECT
IEZPARM  DSECT                         PARAMETER AREA
PARMADDR DS    CL4                     ADDRESS OF PARAMETER LIST
PROCNAME DS    CL8                     PROCEDURE NAME
DIRECT   DS    CL4                     ADDRESS OF DIRECTORY
IWAPT    DS    CL4                     ADDRESS OF IWA
RTCD     DS    CL4                     RETURN CODE FOR IEFVINB
         ORG   PARMADDR
IEZADDR  DS    CL4                     ADDRESS OF WORK AREA        O106
IEZUNCOM DS    A                       PTR TO UNCOMPRESSED RECORD
IEZCOMPR DS    A                       PTR TO NEXT COMPRESSED RECORD
IEZLEN   DS    H                       LENGTH OF UNCOMPRESSED RECORD
IEZOUT   DS    H                       LENGTH OF AREA FOR OUTPUT RECORD
IEZCHAR  DS    C                       CHARACTER CODE OF BLANK
IEZTYPE  DS    B                       FLAG ASSOCIATED WITH RECORD
IEZRES   DS    H                       WORK AREA
*                                      THE IWA
         EJECT
         DCBD  DSORG=(PS)
         EJECT
IEFVIND  CSECT
         SAVE (14,12)
         BALR  RB,0
         USING *,RB
**************************************************************** Y02668
* MODULE TRACE CODE - FOR TESTING                                Y02668
         L     RF,TRACEV           LOAD TRACE RTNE ADDR.         Y02668
         BALR  RE,RF               ENTER MOD ID IN TRACE RECORD. Y02668
TRACEV   DC    V(TRACE)            TRACE RTNE ADDR.              Y02668
         DC    C'VIND'             MOD ID USED BY TRACE          Y02668
* TRACE RETURNS HERE.                                            Y02668
**************************************************************** Y02668
         B     VIND000
         SPACE
         MODID BR=NO                                            Y01886
         DC    C'SPLT'             FUNCTION ID AND               Y02668
         DC    C'02'               LEVEL # (NEL DELETION FIX).   Y02668
         DC    C'MVM'              FUNCTION ID AND               Y02668
         DC    C'04'         LEVEL # (WA MACRO UPDATES).         Y02668
         SPACE
VIND000  DS    0H
         IEFSAVER
         L     R7,IWAWKBF              ADDRESS OF WORK AREA        3391
         L     R6,WKQMPAPT             ADDRESS OF IN-STREAM QMPA   3391
         SPACE
         USING IOPARAMS,R6
         L     R2,QMPCL                ADDR OF EXTERNAL PARM LIST  3391
         SPACE
***********************************************************************
*                                                                     *
*  IF THIS IS THE FIRST STATEMENT OF THE PROCEDURE, CORE IS GOTTEN FOR*
*  THE PARAMETER LIST, THE ADDRESS IS SAVED IN THE IEA, AND THE   4399*
*  PARAMETER LIST IS INITIALIZED.  THE EXTERNAL PARAMETER LIST    4399*
*  FOR IEFVHQ AND IEFQMRAW ARE INITIALIZED TO POINT TO THE FIRST  4399*
*  RECORD OF THE PROCEDURE AND THAT 953694 Z2-92 4                4399*
*                                                                 4399*
***********************************************************************
*                                                                     *
***********************************************************************
         SPACE
         L     R4,IWAPARM              ADDRESS OF PARAMETER LIST
         USING IEZPARM,R4
         LA    R4,DISPLZER(R4)         CLEAR HIGH ORDER BYTE       4399
         NI    SWI,UNBLKPC             TURN OFF SWITCHES FRO BLOCKED
*                                      PROCLIB
         SPACE
         LTR   R4,R4                   IS THIS THE FIRST STMT OF THE
*                                      PROCEDURE                   4399
         BNZ   VIND025                 NO-EXPAND THE STATEMENT     4399
         LA    R0,VINDPARM             LENGTH OF PARM LIST         4399
         GETMAIN R,LV=(0)
         O     R1,IWAPARM              RETAIN HIGH ORDER BYTE INFOM4268
         ST    R1,IWAPARM              ADDR OF PARMLIST           M4268
         LR    R4,R1                   ADDRESS OF PARM LIST        4399
         USING IEZPARM,R4                                          4399
         LA    R3,DISPLFOR(R1)         ADDRESS OF PARM INFORMATION 4399
         ST    R3,IEZADDR              ADDRESS OF PARM INFORMATION 4399
         MVC   IEZUNCOM(MVFOUR),PSTMT     ADDRESS OF OUTPUT BUFFER 4399
         MVI   IEZCHAR,CHAR            CHARACTER TO BE ADDED       4399
         MVI    IEZTYPE,TYPE           NOT A SPECIAL RECORD        4399
         LA  R3,INLN                   LENGTH OF OUTPUT BUFFER     4399
         STH   R3,IEZLEN               LENGTH OF OUTPUT BUFFER     4399
         MVC   MVFOUR(MVTHREE,R2),WKPTR2 TTR OF FIRST RECORD OF PROC
         LA    R5,VIND025              ADDR ROUTINE TO EXPAND RECORD
         SPACE
VIND010  DS    0H
***********************************************************************
*                                                                     *
*  READ THE RECORD FROM THE JOB QUEUE AND THEN REINITIALIZE THE       *
* PARAMETER LIST FOR IEZDCODE                                         *
*                                                                     *
***********************************************************************
         ST    R6,QPARMP               REPLACE R/I QMPA PTR
         MVC   DISPLZER(MVFOUR,R2),IWAWKBF ADDRESS OF WORK BUFFER  3391
        MVI   QMPOP,QMREAD            READ
         L     RF,VIEFVHQ              ADDRESS OF Q MANAGER INTERFACE
         BALR  RE,RF                   Q MANAGER INTERFACE
         LA    R1,QPARM                ADDRESS OF QMPA (R/I)
         ST    R1,QPARMP               REPLACE IN-LINE PROC QMPA PTR
         LA    R3,OUTLEN               LENGTH OF INPUT BUFFER      4361
         STH   R3,IEZOUT           LENGTH OF INPUT BUFFER         M3667
         LA    R3,WKRECORD             ADDRESS OF WORK BUFFER
         ST    R3,IEZCOMPR             ADDRESS OF WORK BUFFER
         BR    R5                      RETURN TO CALLER            4399
         SPACE
***********************************************************************
*                                                                     *
* EXPAND A RECORD                                                     *
*                                                                     *
***********************************************************************
VIND025  DS    0H                                                  4399
         L     R1,IEZCOMPR             LOAD THE ABSOLUTE ADDR OF A33705
*                                      NEXT STMT TO BE EXPANDED  A33705
         SR    R1,R7                   CALC RELATIVE DISPLMNT    A33705
*                                      INTO THE WORK BUFFER OF   A33705
*                                      NEXT STMT TO BE EXPANDED  A33705
         STC   R1,WKSAVE1              STORE THIS RELATIVE       A33705
*                                      DISPLACEMENT INTO THE     A33705
*                                      DIRECTORY. DURING RDR     A33705
*                                      SUSPENSION IN PCP THIS    A33705
*                                      DISPLACEMENT IS USED TO   A33705
*                                      RESTORE THE RDR TO THE POIA33705
*                                      AT WHICH IT WAS,WHEN IT   A33705
*                                      WAS SUSPENDED.            A33705
         SPACE
         L     R1,IWAPARM              ADDRESS OF PARAMETER LIST
         L     R8,VIEDCODE             IEZDCODE VCON             Y01029
         BALR  RE,R8                   BRANCH TO IEZDCODE        Y01029
         LTR   RF,RF                   WAS RECORD THERE
         BNZ   VIND040             A NON-ZERO RETURN CODE         M3738
*                                  INDICATES THERE ARE NO MORE    M3738
*                                  RECORDS (CODE 4) OR A PARAM    M3738
*                                  LIST ERROR (CODE 8). NON-ZERO  M3738
*                                  IS ASSUMED TO BE CODE 4, SINCE M3738
*                                  THE ONLY CONDITION UNDER WHICH M3738
*                                  CODE 8 WILL BE RETURNED IS IN  M3738
*                                  THE EVENT OF SOME WILD STORE   M3738
*                                  INTO THE PARAMETER LIST. A     M3738
*                                  RETURN CODE OF 8 WILL RESULT   M3738
*                                  IN TRUNCATION OR NON-EXPANSION M3738
*                                  OF THE INSTREAM PROCEDURE.     M3738
         SPACE
VIND030  DS    0H
***********************************************************************
*                                                                     *
*  THE RECORD WAS EXPANDED.                                           *
*   RETURN TO PROCESS THE EXPANDED RECORD AS IF IT HAD COME FROM      *
*   PROCLIB                                                           *
*                                                                     *
***********************************************************************
         SPACE
         IEFRELSE
         SPACE
         RETURN (14,12)
         SPACE
VIND040  DS    0H
***********************************************************************
*                                                                     *
* CHECK TO SEE IF THIS IS THE END OF THE RECORD.  THE FIRST WORD      *
* WILL CONTAIN A TTR OF THE NEXT RECORD IF ONE EXISTS                 *
*                                                                     *
***********************************************************************
         L     R3,WKTTR                TTR OF NEXT RECORD          3391
         LTR   R3,R3                   END OF PROCEDURE            3391
         BE    VIND050                 END OF PROCEDURE
         SPACE
***********************************************************************
*                                                                     *
* BUILD THE QMPA AND READ A RECORD                                    *
*                                                                     *
***********************************************************************
         MVC   WKPTR2(MVTHREE),WKTTR   UPDATE PTR TO REFLECT TTR A33705
*                                      OF PORTION OF PROCEDURE   A33705
*                                      BEING EXPANDED            A33705
         MVC   MVFOUR(MVTHREE,R2),WKTTR  READ A RECORD             3391
         LA    R5,VIND025              COMPRESS THE RECORD         4399
         B     VIND010
         SPACE
VIND050  DS    0H
***********************************************************************
*                                                                 4399*
*  END OF FILE HAS BEEN REACHED.  THE ADDRESS OF BPAM IS PUT BACK INTO
*  THE BPAM DCB, THE ADDRESS OF THE EODAD PLACED IN REGISTER 14, THE  *
*  PARM LIST FOR IEZDCODE IS FREED AND THE POINTER TO IT IS CLEARED   *
*  CONTROL IS RETURNED TO IEFVHAS END OF PROCEDURE ROUTINE VIA REG 14
*                                                                 4399*
***********************************************************************
         L     RA,PDCBP                ADDRESS OF PROC DDB
         MVC   DCBREAD+DISPLONE-IHADCB(MVTHREE,RA),IWABPAM    M3739O106
         L     R4,MVFOUR(RD)           ADDR OF SAVE AREA SET       O106
*                                      REGISTER 14 TO THE EODAD   O106
*                                      ROUTINE BECAUSE THIS IS THEO106
*                                      END OF THE IN-LINE PROCEDURE106
         MVC   DISPL13(MVTHREE,R4),DCBEODAD+DISPLONE-IHADCB(RA)
         L     R1,IWAPARM              ADDRESS OF PARM LIST        4399
         LA    R0,VINDPARM             LENGTH OF PARM LIST         4399
         FREEMAIN R,A=(1),LV=(0)
         XC    IWAPARM+DISPLONE(MVTHREE),IWAPARM+DISPLONE  CLEAR   4399
*                                      ADDRESS OF PARM LIST        4399
         B     VIND030                 END OF IN-LINE PROCEDURE
         SPACE
***********************************************************************
*                                                                     *
*   CONSTANTS AND EQUATES                                             *
*                                                                     *
***********************************************************************
VIEFVHQ  DC    V(IEFVHQ)               ADDRESS OF Q MANAGER INTERFACE
VIEDCODE DC    V(IEZDCODE)             DECOMPRESS RECORD ROUTINE Y01029
DISPLZER EQU   0                       CLEAR THE HIGH ORDER BIT    4399
VINDPARM EQU   24                      LENGTH OF PARM LIST         4399
INSTPROC EQU   X'80'                   INSTREAM PROC BEING PROCESSE4399
DISPLFOR EQU   4                       DISPLACEMENT                4399
CHAR     EQU   X'40'                   CHARACTER BEING ADDED       4399
TYPE     EQU   X'00'                   NOT A SPECIAL RECORD        4399
INLN     EQU   X'50'                   LENGTH OF OUTPUT BUFFER     4399
DISPLONE EQU   1                        DISPLACEMENT               4399
OUTLEN   EQU   168                     LENGTH OF OUTPUT BUFFER     4399
MVTHREE  EQU   3                       LENGTH OF MOVE
UNBLKPC  EQU   X'77'                   TURN OFF THE BLO KED PROCLIB
*                                       SWITCHS IN SWI IN THE IWA
MVFOUR   EQU   4                       LENGTH OF MOVE
DISPL12  EQU   12                      DISPLACEMENT               O106
DISPL13  EQU   13                      DISPLACEMENT
PATCH    DC    25F'0'              PATCH SPACE                   Y02668
   END
