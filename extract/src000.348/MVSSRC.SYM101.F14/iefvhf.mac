***********************************************************************
*
*  MODULE NAME - IEFVHF
*
*  DESCRIPTIVE NAME - CONVERTER TERMINATION ROUTINE
*
*  COPYRIGHT - N/A
*
*  STATUS - OS/VS2 RELEASE 3.0 AND 3.7 PTF                     @ZA16823
*
*                                                                     *
*  FUNCTION - THIS ROUTINE FREES AREAS GOTTEN BY THE INITIALIZATION
*             ROUTINE, ISSUES MESSAGES TO THE OPERATOR INDICATING
*             JOB STATUS AT THE END OF CONVERSION, AND SETS A RETURN
*             CODE FOR THE CONVERTER'S CALLER.   IT ALSO PROCESSES
*             THE SYSCHK DD AND EXITS TO THE PRE SCAN RTN(IEFVHEB).
*             IT ALSO ENSURES THAT TEXT IS COMPLETE FOR A STMT.
*
*             THE ESTAE ENVIRONMENT IS CANCELLED IN THIS ROUTINE.
*
*     OPERATION -
*               .IF TERMINATION IS TO BE PERFORMED, CHECK IF A
*                WARNING MSG HAS BEEN ISSUED.   IF YES ISSUE WTO
*                TO JESS JOB LOG.   IF A JCL ERROR, ISSUE WTO
*                AND SET R15 TO 4.   IF AN I/O ERROR SET R15 TO 8.
*                IF JOB SUCCESSFUL SET R15 TO 0.
*                   MOVE QMPA BACK TO CALLER'S CORE.   MOVE JMR BACK
*                BACK TO CALLER'S(JESS) CORE.   FREE JMR AND LOCAL
*                WORK AREA CORE.CANCEL THE ESTAE ENVIRONMENT.
*               .FREE WORK AREA FOR INSTREAM PROCEDURES, IF    @ZA16823
*                THERE IS ONE.                                 @ZA16823
*                FREE THE CONVERTER'S GOTTEN CORE IN SP0. RESET
*                R1 TO POINT TO NEL AND EXIT TO CONVERTER'S CALLER
*               .IF TERMINATION NOT TO BE PERFORMED, IS CHECKPOINT
*                RESTART FLAG ON.   IF YES , REINSTATE SYSCHK DD
*                STMT AND EXIT TO IEFVHEB.
*               . CHECK IF CONTINUATION EXPECTED.   IF YES, EXIT
*                TO IEFVHA TO READ THE NEXT CARD SO TEXT CAN BE
*                COMPLETED.
*               .IF CONTINUATION CANCELLED EXIT TO IEFVHA TO GET
*                NEXT STMT.
*               .IF CONTINUATION EXPECTED AND NOT RECV'D EXIT TO
*                IEFVHCB.
*
*
*  NOTES -
*     CHARACTER CODE DEPENDENCIES - EBCDIC
*     DEPENDENCIES - NONE
*     RESTRICTIONS - NONE
*     REGISTER CONVENTIONS - R11 - BASE REG
*                            R12 - PTR TO CONVERTER WORK AREA
*                            R13 - PTR TO REG SAVE AREA
*     PATCH-LABEL - PATCH
*
*
*  MODULE TYPE - BAL CSECT
*     PROCESSOR - ASSEMBLER
*     ATTRIBUTES - REFRESHABLE, REENTRANT, PRIVILEGED, KEY 0, PAG LPA
*
*
*  ENTRY POINT - IEFVHF FROM IEFVFA
*     PURPOSE - TO TERMINATE CONVERTER OR COMPLETE TEXT
*     LINKAGE - BALR
*     INPUT -
*            REG 12 - PTR TO CONVERTER WORK AREA
*            REG 13 - PTR TO REG SAVE AREA
*     REGISTERS SAVED - NONE
*     REGISTERS USED IN PROCESSING -
*                        R0 - R2 - WORK REGS
*                             R3 - LOCAL WORK AREA BASE
*                        R4 - R7 - UNUSED
*                             R8 - PSEUDO JOB PREFIX
*                             R9 - STMT PTR
*                            R10 - STMT PTR
*                            R11 - MODULE BASE REG
*                            R12 - PTR TO CONVERTER WORK AREA
*                            R13 - PTR TO REG SAVE AREA
*                            R14 - LINKAGE AND WORK REG
*                            R15 - LINKAGE AND WORK REG
*     REGISTERS RESTORED - R14 - R12
*  EXIT-NORMAL - CONVERTER'S CALLER(JESS)
*     CONDITIONS - TERMINATION COMPLETE
*     OUTPUT - REG 1 - PTR TO NEL, REG 15 - RETURN CODE
*     RETURN CODES - 0 -SUCCESSFUL
*                    4 -JCL ERROR
*                    8 -I/O ERROR
*
*
*  EXIT-NORMAL - IEFVHA
*     CONDITIONS - CONTINUATION EXPECTED OR CANCELLED
*     OUTPUT - REG 12 - PTR TO CWA, REG 13 - PTR TO RSA
*     RETURN CODES - NONE
*
*
*  EXIT-NORMAL - IEFVHCB
*     CONDITION  - CONTINUATION NOT RECEIVED
*     OUTPUT - REG 12 - PTR TO CWA
*     RETURN CODES - NONE
*
*
*  EXIT-NORMAL - IEFVHEB
*     CONDITIONS - SYSCHK DD TO BE REINSTATED
*     OUTPUT - REG 12 - PTR TO CWA
*     RETURN CODES - NONE
*
*
*  EXIT - ERROR - NONE
*
*  EXTERNAL REFERENCES -
*     ROUTINES - IEFVHR
*     DATA AREAS - CWA, LWA, I/O BUFFERS, QMPA
*     CONTROL BLOCKS - JMR, NEL, QMPA
*     TABLES - NONE
*     MACROS - IEFJMR, GETMAIN, FREEMAIN, DELETE,
*
*     CHANGE LEVEL - Y02668,OZ10816,OZ16823                    @ZA16823
*
*
*  MESSAGES - IEF607I  JOB HAS NO STEPS
*
***********************************************************************
         EJECT
         IEFCOMWA                                                Y02668
         IEFCVRWA                                                Y02668
         IEFVMCWA
         EJECT
NEL      DSECT
         IEFNEL SUBCOM=C,EXITS=(TXT,RTN,QEP,SMF,IAM,FAM)
         EJECT
         IEFTXTFT
         IEFJMR                                                  Y02668
         DCBD  DSORG=(PS)                                        Y02668
         IEFZB9RD                                                Y02668
         EJECT
IEFVHF   CSECT
* CODE ADDED NEAR LABEL VHF0050                                @ZA10816
* CODE ADDED AFTER LABEL VHFNOTRC                              @ZA16823
         BALR  RB,0
         USING *,RB
*****************************************************************Y02668
* MODULE TRACE CODE - FOR TESTING.                               Y02668
         L     RF,TRACEV           LOAD TRACE RTNE ADDR.         Y02668
         BALR  RE,RF               ENTER MOD ID IN TRACE RECORD. Y02668
TRACEV   DC    V(TRACE)            TRACE RTNE ADDR.              Y02668
         DC    C'VHF '             MOD ID USED BY TRACE.         Y02668
* TRACE RETURNS HERE.                                            Y02668
*****************************************************************Y02668
         B     C6
         MODID BR=NO                                             Y01886
*        DC    C'SPLT'             FUNCTION ID AND               Y02668
*        DC    C'03'               LEVEL NUMBER (C/I LOGIC)      Y02668
*        DC    C'MVM'              FUNCTION ID                   Y02668
*        DC    C'23'   LEV # (DD * OVERRIDE FIX-VS02712).       YM02712
*        DC    C'01'   LEV # (ZERO BUFFER ADDR AFTER FREEING)  @ZA10816
         DC    C'02'   LEV # (FREE INSTREAM PROC WK AREA IF ANY@ZA16823
C6       DS    0H                                                Y02668
         LA    R8,WAJOBPFX         ADDR OF PSEUDO JOB PFX.      YM01572
         USING TEXT,R8             PREFIX ADDRESSABILITY.       YM01572
         TM    AOSW4,CWATERM       SHOULD THE CONVERTER BE       Y02668
*                                  TERMINATED                    Y02668
         BZ    VHFRESRT            NO, GO TO CHECK IF RESTART    Y02668
         EJECT
*****************************************************************Y02668
*                                                               *Y02668
*  SMF USER EXIT AT END OF CONVERSION                           *Y02668
*                                                               *Y02668
*****************************************************************Y02668
         L     R3,WANELPTR         GET PTR TO NEL                Y02668
         USING NEL,R3              ADDRESS NEL                   Y02668
         TM    NELOPSWT,NELSMF     IS SMF BEING USED             Y02668
         BZ    VHF0005             NO, DON'T BOTHER WITH EXIT    Y02668
         L     R2,CWAJMRPT         GET JMR PTR                   Y02668
         USING JMR,R2              ADDRESS JMR                   Y02668
         TM    JMROPT,JMREXITS     IS A USER EXIT REQUESTED      Y02668
         BZ    VHF0005             NO, THEN DON'T TAKE ONE       Y02668
         LA    R1,JMRPTRS          GET ADDR OF PARMLIST          Y02668
         LA    RE,JMRJOB           GET ADDR OF JOB LOG           Y02668
         ST    RE,JMRJOBP          AND PUT IT IN PARMLIST        Y02668
         MVI   JMRJCLCD,EXCODE     SET EXIT CODE TO INDICATE     Y02668
*                                  END OF CONVERSION             Y02668
         LA    RE,JMRJCLCD         GET PTR TO EXIT CODE AND      Y02668
         ST    RE,JMRJCLCP         PUT IT IN THE PARMLIST        Y02668
         L     RF,JMRUJVP          GET USER EXIT RTN ADDR        Y02668
         BALR  RE,RF               GO TO SMF USER EXIT           Y02668
*                                                                Y02668
         LA    R2,JCLERROR         GET ERROR RETCODE VALUE       Y02668
         CR    RF,R2               DOES USER WANT TO CANCEL      Y02668
         BNE   VHF0005             NO, CONTINUE                  Y02668
*  YES, FAIL JOB.                                                Y02668
         OI    STRJINDC,JTXJOBFL   SET JOB FAILED FLAG.          Y02668
VHF0005  DS    0H                  END OF SMF USER EXIT          Y02668
         EJECT                                                   Y02668
*****************************************************************Y02668
*                                                               *Y02668
*                                                               *Y02668
*  TERMINATION - RETURN CODE PROCESSING                         *Y02668
*                                                               *Y02668
*****************************************************************Y02668
         SPACE 2                                                 Y02668
VHF0010  DS    0H                                                Y02668
         L     R1,WORD(RD)         PICK UP PTR TO REG SAVE AREA  Y02668
*                                  OF CONVERTER'S CALLER         Y02668
         CL    R1,CWASAVPT         IS THIS, IN FACT, THE CORRECT Y02668
*                                  SAVE AREA PTR(SAVED ON ENTRY  Y02668
*                                  BY VH1)                       Y02668
         BE    VHF0020             YES, CONTINUE TERMINATION     Y02668
*                                  NO, FREE EXCESS RSA AND CHECK Y02668
         IEFRELSE  ,               NEXT BACWARD CHAIN PTR        Y02668
         B     VHF0010             GO BACK AND CHECK AGAIN       Y02668
VHF0020  DS    0H                **RETURN CODE PROCESSING        Y02668
         L     R1,WORD(RD)         ADDRESS CALLER'S RSA          Y02668
         XC    SAVERF(WORD,R1),SAVERF(R1) ZERO RETURN CODE REG INY02668
*                                  CALLER'S SAVE AREA            Y02668
         CLC   IWARET(2),IOERROR   TEMP *****                    Y02668
         BL    VHF0030             NO, CHECK FOR JCL ERROR       Y02668
         MVC   SAVERF+2(HALFWORD,R1),IWARET INITIALIZE CALLER'S  Y02668
*                                  REG15 TO RETURN CODE          Y02668
         B     VHF0040                                           Y02668
VHF0030  DS    0H                                                Y02668
         TM    STRJINDC,JTXJOBFL   DOES JOB HAVE A JCL ERROR     Y02668
         BZ    VHF0040             NO, THEREFORE, JOB IS         Y02668
*                                  SUCCESSFUL AND REG15=0        Y02668
         MVI   SAVERF+3(R1),JCLERROR INDICATE JCL ERROR IN CALLERY02668
         DROP  R8                                                Y02668
*                                  R15                           Y02668
         LA    R5,IEF452I          GET JCL ERROR MSG KEY         Y02668
         L     RF,IEFVHRV          GET WTO ROUTINE ADDR          Y02668
         BALR  RE,RF               GO TO ISSUE WTO               Y02668
         EJECT                                                   Y02668
*****************************************************************Y02668
*                                                               *Y02668
*  EXIT LIST PROCESSING   - WTO  PROCESSING                     *Y02668
*                                                               *Y02668
*****************************************************************Y02668
VHF0040  DS    0H                                                Y02668
         EJECT                                                   Y02668
*****************************************************************Y02668
*                                                               *Y02668
* RE-INITIALIZE QMPA AND JMR IN CORE OF CONVERTER'S CALLER      *Y02668
* DELETE IEFUJV                                                 *YM0581
*                                                               *Y02668
*****************************************************************Y02668
         SPACE 2                                                 Y02668
         L     R3,WANELPTR         GET PTR TO NEL                Y02668
         USING NEL,R3              ADDRESS NEL                   Y02668
         L     RE,NELQMPA          GET PTR TO QMPA IN CALLER'S   Y02668
*                                  CORE                          Y02668
         MVC   NONE(QMPALEN,RE),QPARM PUT QMPA BACK              Y02668
         TM    NELOPSWT,NELSMF     IS SMF BEING USED             Y02668
         BZ    VHF0050             NO, BYPASS JMR PROCESSING     Y02668
         L     RE,NELJMR           GET PTR TO JMR IN CALLER'S    Y02668
*                                  CORE                          Y02668
         L     RF,CWAJMRPT         GET PTR TO JMR IN             Y02668
*                                  CONVERTER'S CORE              Y02668
         LTR   RF,RF               ALREADY FREED IF ZERO       @ZA10816
         BZ    VHF0050             BYPASS FREEMAIN IF ZERO     @ZA10816
         MVC   NONE(JESSJMR,RE),NONE(RF) PUT JMR BACK            Y02668
         LR    R1,RF               GET ADDR OF JMR GOTTEN CORE   Y02668
         LA    R0,TABLEN           GET LENGTH OF CORE            Y02668
         FREEMAIN R,LV=(0),A=(1)   FREE CORE JMR WAS IN          Y02668
         XC    CWAJMRPT,CWAJMRPT   ZERO PTR SO WON'T FREE IF RETRY
*                                                              @ZA10816
         DELETE EP=IEFUJV          DELETE USER ROUTINE.         YM00581
VHF0050  DS    0H                                                Y02668
         EJECT                                                   Y02668
*****************************************************************Y02653
*                                                                Y02653
*  DEACTIVATE THE ESTAE ENVIRONMENT OVER THE CONVERTER.          Y02653
*                                                                Y02653
*****************************************************************Y02653
         SPACE 1
         L      R1,AOSTAPRM    LOAD ESTAE USER PARAM ADDR        Y02653
         LTR   R1,R1         WAS ESATE ACTIVE IN CONVERTER       Y02653
         BZ    VHFBYPAS    IF POINTER ZERO THEN ESTAE WAS NOT    Y02653
*              ACTIVE IN CONVERTER.BYPASS CANCELLING IT          Y02653
         LA    R0,EREPLEN          LENGTH OF USER PARM AREA      Y02668
         FREEMAIN R,LV=(0),A=(1)   ESTAE ACTIVE SO FREE PARM AREAY02668
         XC    AOSTAPRM,AOSTAPRM   ZERO PTR AFTER BUFFER FREED @ZA10816
         ESTAE 0                                                 Y02653
         SPACE 1
*        THE ESTAE ENVIRONMENT IS NOW CANCELLED.                 Y02653
VHFBYPAS DS    0H                                                Y02653
         EJECT
*****************************************************************Y02668
*                                                               *Y02668
*  FREE ALL OF THE CONVERTERS WORKING STORAGE AND EXIT          *Y02668
*                                                               *Y02668
*****************************************************************Y02668
         SPACE 2                                                 Y02668
         L     R3,WANELPTR         GET PTR TO NEL                Y02668
         USING NEL,R3              ADDRESS NEL                   Y02668
         L     R0,CWALINKL         GET LENGTH FOR POSSIBLE FREE YM00373
         LTR   R0,R0               POST SCAN EXIT AREA ?        YM00373
         BZ    VHFNOPSE            NO DO NOT FREE AREA          YM00373
         L     R1,CWALINK          GET PTR TO POST SCAN AREA    YM00373
         FREEMAIN R,LV=(0),A=(1)   FREE POST SCAN EXIT AREA     YM00373
VHFNOPSE DS    0H                                               YM00373
         L     RF,NELPROCB         GET PROCLIB DCB PTR           Y02668
         LTR   RF,RF               WAS A PROCLIB SPECIFIED       Y02668
         BZ    VHFNOPRC            NO, THEN A PROC BUFFER WASN'T Y02668
*                                  GOTTEN                        Y02668
         USING IHADCB,RF           IF YES, GE TPROC BUFFER       Y02668
         LH    R0,DCBBLKSI         LENGTH OUT OF DCB(MAX BLKSI)  Y02668
         L     R1,PSTMT            GET ADDR OF PROC BUFFER       Y02668
         FREEMAIN R,LV=(0),A=(1)   FREE PROC BUFFER              Y02668
VHFNOPRC DS    0H                                                Y02668
         L     R1,BUFADDR          GET 'TRACE ' RTN BUFFER ADDR  Y02668
         LTR   R1,R1               WAS 'TRACE' ACTIVE?           Y02668
         BZ    VHFNOTRC            NO, GO TO FREE OTHER WORK AREAY02668
         LH    R0,BUFLTH           YES, GET BUFFER LENGTH        Y02668
         FREEMAIN R,LV=(0),A=(1)   FREE THE 'TRACE' BUFFER       Y02668
VHFNOTRC DS    0H                                                Y02668
         L     R1,IWAWKBF         GET ADDR OF INSTREAM PROC    @ZA16823
*                                  WORK AREA IF ANY            @ZA16823
         LTR   R1,R1              IS THERE A WORK AREA?        @ZA16823
         BZ    VHFNOINP           NO, BYPASS FREEMAIN          @ZA16823
         LA    R0,WORKLN          LENGTH OF INSTREAM PROC AREA @ZA16823
         FREEMAIN R,LV=(0),A=(1)  FREE INSTREAM PROC WORK AREA @ZA16823
VHFNOINP EQU   *                  NO INSTREAM PROC BRCH TARGET @ZA16823
         LR    R1,RC               GET ADDR OF LARGE AREA GOTTEN Y02668
*                                  AT INITIALIZATION             Y02668
         L     R0,WAWRKLTH         GET LENGTH OF COMBINED        Y02668
*                                  WORK AREAS                    Y02668
         FREEMAIN R,LV=(0),A=(1)   FREE CONVERTER WORK AREAS     Y02668
         IEFRELSE  ,               RELEASE CONVT SAVE AREA       Y02668
         RETURN (14,12)            RETURN TO CONVERTER'S CALLER  Y02668
         EJECT                                                   Y02668
*****************************************************************Y02668
*                                                               *Y02668
*  CHECK POINT RESTART PROCESSING                               *Y02668
*                                                               *Y02668
*****************************************************************Y02668
         SPACE 2                                                 Y02668
VHFRESRT DS    0H                                                Y02668
         TM    CRSW1,CPFLG         HAS A SYSCHK DD STMT BEEN     Y02668
*                                  ENCOUNTERED                   Y02668
         BZ    VHFCONTU            NO, CHECK IF  CONTINUATION    Y02668
         L     R9,RSTMT            GET PTR TO JCL D. S. INPUT    Y02668
*                                  BUFFER.THIS IS THE BUFFER     Y02668
*                                  INTO WHICH THE SYSCHK DD      Y02668
*                                  STMT IS TO BE RE-ENSTATED     Y02668
         LA    RA,RSTMT            GET PTR TO PARMLIST           Y02668
*                                  DISCRIBING STMT               Y02668
         L     RF,CPSTMTP          GET PTR TO SYSCHK DD STMT     Y02668
*                                  PREVIOUSLY SAVED BY VHEB      Y02668
         MVC   NONE(RECLEN,R9),NONE(RF) RE-ENSTATE SYSCHK DD     Y02668
*                                  IN JCL D. S. INPUT BUFFER     Y02668
         MVC   WORD(WORD,RA),SPARMLOC(RF) REINITIALIZE STMT      Y02668
*                                  PARM LIST INFO SO SYSCHK DD   Y02668
*                                  CAN BE PROCESSED AS IF IT     Y02668
*                                  WERE JUST READ FROM JCL D. S. Y02668
         NI    CRSW1,255-CPFLG     CLEAR RESTART FLAG            Y02668
         L     RF,IEFVHCBV         ADDR OF TEXT WRITE RTNE.     YM00371
         BR    RF                  WRITE C/R EXEC TEXT,         YM00371*
                                   PROCESS SYSCHK DD.           YM00371
         EJECT                                                   Y02668
*****************************************************************Y02668
*                                                               *Y02668
*  THE FOLLOWING PROCESSING INSURES THAT,FOR A PARTICULAR JCL   *Y02668
*  STMT, THE INTERNAL TEXT STRING IS COMPLETE.  IT MAKES SURE   *Y02668
*  CONTINUATIONS AND OVERRIDES ARE NOT OUTSTANDING              *Y02668
*                                                               *Y02668
*****************************************************************Y02668
         SPACE 2
VHFCONTU DS    0H                                                Y02668
         TM    SWE,GPI             IS A PROCEDURE STMT          YM00370
*                                  BEING PROCESSED              YM00370
         BZ    VHF0021             NO, ASSUME JCL D.S. STMT     YM00370
         LA    RA,PSTMT            YES, POINT AT PROCEDURE STMT YM00370
         B     VHF0041             PARM LIST                    YM00370
VHF0021  DS    0H                                               YM00370
         LA    RA,RSTMT            POINT AT JCL STMT PARMLIST   YM00370
VHF0041  DS    0H                                               YM00370
         L     R9,NONE(RA)         POINT TO BUFFER OF STMT      YM00370
*                                  BEING PROCESSED              YM00370
VHF0060  DS    0H                                                Y02668
         TM    SWB,CXPN            WAS CONTINUATION EXPECTED     Y02668
*                                  AND NOT RECEIVED              Y02668
         BZ    VHF0070             NO, CHECK FOR CONT. CANCEL    Y02668
         NI    SWB,255-CXPN        YES, CLEAR FLAG               Y02668
         B     VHFVHCB             EXIT TO VERB ID TO PROCESS    Y02668
*                                  CURRENT STMT                  Y02668
VHF0070  DS    0H                                                Y02668
         TM    SWB,CXPC            WAS CONTINUATION EXPECTED     Y02668
*                                  AND CANCELED                  Y02668
         BZ    VHFVHA              NO, GOTO GET ANOTHER STMT     Y02668
         NI    SWB,255-CXPC        CLEAR FLAG                    Y02668
         B     VHFVHA              GO TO GET ANOTHER STMT       YM00370
         EJECT                                                   Y02668
*****************************************************************Y02668
*                                                               *Y02668
*                        EXITS                                  *Y02668
*                                                               *Y02668
*****************************************************************Y02668
         SPACE 2                                                 Y02668
VHFVHA   DS    0H                  EXIT TO CONVERTER GET RTN     Y02668
         L     RF,IEFVHAV          LOAD AND                      Y02668
         BR    RF                  GO                            Y02668
VHFVHEB  DS    0H                  EXIT TO CONVERTER PRE-SCAN RTNY02668
         L     RF,IEFVHEBV         LOAD AND                      Y02668
         BR    RF                  GO                            Y02668
VHFVHCB  DS    0H                  EXIT TO CONVERTER VERB ID RTN Y02668
         L     RF,IEFVHCBV         LOAD AND                      Y02668
         BR    RF                  GO                            Y02668
*****************************************************************Y02668
*                                                               *Y02668
*                          EQUATES                              *Y02668
*                                                               *Y02668
*****************************************************************Y02668
         SPACE 2
WORD     EQU   4
SAVERF   EQU   16
HALFWORD EQU   2
JCLERROR EQU   4
NONE     EQU   0
QMPALEN  EQU   32
EXCODE   EQU   X'10'          SMF EXIT CODE - CONVERSION OVER    Y02668
JESSJMR  EQU   56             SUBSYSTEM JMR LENGTH               Y02668
ZERO     EQU   X'00'
RECLEN   EQU   80                  RECORD LENGTH                 Y02668
SPARMLOC EQU   84                  STMT PARMLIST LOCATION        Y02668
CPSTMTP  EQU   IWAJOBS5                                          Y02668
BLANK    EQU   C' '                                              Y02668
COL4     EQU   3                                                 Y02668
NULLEN   EQU   68                                                Y02668
IEF452I  EQU   6                   JCL ERROR MSG KEY             Y02668
TABLEN   EQU   X'50'               LTH OF AREA OBTAINED FOR JMR.YM01556
MSG7     EQU   7                   JOB HAS NO STEPS MSG CODE     Y02668
WORKLN   EQU   352               INSTREAM PROC WORK AREA LENGTH@ZA16823
*****************************************************************Y02668
*                                                               *Y02668
*  DECLARED  STORAGE                                            *Y02668
*                                                               *Y02668
*****************************************************************Y02668
IEFVHEBV DC    V(IEFVHEB)          PRE-SCAN RTN                  Y02668
IEFVGMV  DC    V(IEFVGM)           SYSOUT MSG RTN                Y02668
IEFVHCBV DC    V(IEFVHCB)          VERB ID RTN                   Y02668
IEFVHAV  DC    V(IEFVHA)           GET RTN                       Y02668
IEFVHRV  DC    V(IEFVHR)           WTO RTN                       Y02668
IOERROR  DC    X'0008'
BUFLTH   DC    H'2048'             'TRACE' BUFFER LENGTH         Y02668
PATCH    DC    25F'0'              ZAP SPACE                     Y02668
         END   IEFVHF
