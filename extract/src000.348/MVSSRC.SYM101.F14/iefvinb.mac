* TITLE: IEFVINB-DIRECTORY SEARCH                                     *
*                                                                     *
* FUNCTION: THIS MODULE SEARCHES THROUGH THE DIRECTORY FOR A DUPLICATE*
*           PROCEDURE NAME. IT SETS A RETURN CODE INDICATING WHETHER  *
*           THE PROCEDURE WAS FOUND OR NOT. A ZERO INDICATES THE PRO-*
*           CEDURE WAS NOT FOUND.  IF THE PROCEDURE WAS FOUND, THE    *
*           RETURN CODE WILL CONSIST OF THE TTR OF THE FIRST RECORD   *
*           OF THAT PROCEDURE.                                        *
*                                                                     *
* ENTRY POINT: IEFVINB                                                *
*                                                                     *
* INPUT: R1 POINTS TO A 20 BYTE PARAMETER LIST                        *
*                                                                     *
*        ***************************************************          *
*        *                                                 *          *
*        *     BYTES     PURPOSE                           *          *
*        *                                                 *          *
*        *     0-3       ADDRESS OF THE PARAMETER LIST     *          *
*        *     4-11      PROCEDURE NAME                    *          *
*        *     12-15     ADDRESS OF THE DIRECTORY          *          *
*        *     16-19     RETURN CODE FROM IEFVINB          *          *
*        *     20-23     RETURN CODE FROM IEFVINC          *          *
*        *                                                 *          *
*        ***************************************************          *
*                                                                     *
* EXITS: RETURNS TO CALLER                                            *
*                                                                     *
* OUTPUT: RETURN CODE                                                 *
*           0-PROCEDURE WAS NOT FOUND IN DIRECTORY                    *
*         TTR-TTR OF FIRST RECORD IN PROCEDURE                        *
*                                                                     *
* WORKAREAS OR TABLES                                                 *
*       PARM-PARAMETER LIST                                           *
*       WORK AREA-WORK BUFFER AND DIRECTORY                           *
*       IEFQMNGR-QMPA                                                 *
*       IEFVMIWA-IWA                                                  *
*                                                                     *
* REGISTER USEAGE-R8-QMPA                                             *
*                 R7-WORK BUFFER                                      *
*                                                                     *
* MACROES USED-RETURN                                                 *
*              SAVE                                                   *
*                                                                     *
* OPERATION-.SETS UP ADDRESSIBILITY FOR QMPA AND WORK AREA            *
*           .COMPARES EACH PROCEDURE NAME IN THE DIRECTORY WITH THE   *
*            PROCEDURE NAME PASSED AS A PARAMETER                     *
*           .IF THE PROCEDURE DOES NOT EXIST, A ZERO IS PLACED IN THE *
*            RTCD FIELD IN THE PARAMETER LIST                         *
*           .IF THE PROCEDURE NAME DOES EXIST, THE TTR OF THE FIRST   *
*            RECORD OF THAT PROCEDURE IS PLACED IN THE RTCD FIELD     *
*            OF THE PARAMETER LIST                                    *
*                                                                     *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         EJECT
         IEFCOMWA
         IEFCVRWA
         EJECT
         IEFQMNGR
QMPADDR  DS    CL4                     ADDRESS OF WORK BUFFER
QMPATTR  DS    CL3                     TTR OF JOB QUEUE RECORD
QMPAZER  DS    CL1  N                  RESERVED FIELD
         EJECT
WORKAREA DSECT
         USING WORKAREA,R7
WKTTR    DS    CL4                     POINTER TO NEXT RECORD
WKQMPAPT DS    CL4                     ADDRESS OF QMPA
WKRECORD DS    CL168                   COMPRESSED RECORD
WKPTR1   DS    CL4                     POINTER TO ITSELF
WKCT     DS    CL1                     NUMBER OF ENTRIES MADE IN
*                                      THIS SEGMENT OF THE DIRECTORY
WKPTR2   DS    CL3                     HOLDER OF TTR ASSIGNED BY  O106
*                                      THE QUEUE MANAGER          O106
WKPROCN1 DS    CL8                     PROCEDURE NAME
WKTTR1   DS    CL3                     TTR OF FIRST RECORD
WKPROCN2 DS    CL8                     PROCEDURE NAME
WKTTR2   DS    CL3                     TTR OF FIRST RECORD
WKPROCN3 DS    CL8                     PROCEDURE NAME
WKTTR3   DS    CL3                     TTR OF FIRST RECORD
WKPROCN4 DS    CL8                     PROCEDURE NAME
WKTTR4   DS    CL3                     TTR OF FIRST RECORD
WKPROCN5 DS    CL8                     PROCEDURE NAME
WKTTR5   DS    CL3                     TTR OF FIRST RECORD
WKPROCN6 DS    CL8                     PROCEDURE NAME
WKTTR6   DS    CL3                     TTR OF FIRST RECORD
WKPROCN7 DS    CL8                     PROCEDURE NAME
WKTTR7   DS    CL3                     TTR OF FIRST RECORD
WKPROCN8 DS    CL8                     PROCEDURE NAME
WKTTR8   DS    CL3                     TTR OF FIRST RECORD
WKPROCN9 DS    CL8                     PROCEDURE NAME
WKTTR9   DS    CL3                     TTR OF FIRST RECORD
WKPROCNA DS    CL8                     PROCEDURE NAME
WKTTRA   DS    CL3                     TTR OF FIRST RECORD
WKPROCNB DS    CL8                     PROCEDURE NAME
WKTTRB   DS    CL3                     TTR OF FIRST RECORD
WKPROCNC DS    CL8                     PROCEDURE NAME
WKTTRC   DS    CL3                     TTR OF FIRST RECORD
WKPROCND DS    CL8                     PROCEDURE NAME
WKTTRD   DS    CL3                     TTR OF FIRST RECORD
WKPROCNE DS    CL8                     PROCEDURE NAME
WKTTRE   DS    CL3                     TTR OF FIRST RECORD
WKPROCNF DS    CL8                     PROCEDURE NAME
WKTTRF   DS    CL3                     TTR OF FIRST RECORD
         EJECT
PARAM    DSECT
         USING PARMADDR,R1
PARMADDR DS    CL4                     ADDRESS OF PARAMETER LIST
PROCNAME DS    CL8                     PROCEDURE NAME
DIRECT   DS    CL4                     ADDRESS OF DIRECTORY
IWAPTR   DS    CL4                     ADDRESS OF IWA
RTCD     DS    CL4                     RETURN CODE FORM IEFVINB
         EJECT
IEFVINB  CSECT
         SAVE  (14,12)
         BALR  RB,0
         USING *,RB
**************************************************************** Y02668
* MODULE TRACE CODE - FOR TESTING                                Y02668
         L     RF,TRACEV           LOAD TRACE RTNE ADDR.         Y02668
         BALR  RE,RF               ENTER MOD ID IN TRACE RECORD. Y02668
TRACEV   DC    V(TRACE)            TRACE RTNE ADDR.              Y02668
         DC    C'VINB'             MOD ID USED BY TRACE          Y02668
* TRACE RETURNS HERE.                                            Y02668
**************************************************************** Y02668
         B     VINB000                 BRANCH AROUND IDENTIFICATION
         MODID BR=NO                                             Y01886
         DC    C'MVM'              FUNCTION ID AND               Y02668
         DC    C'04'         LEVEL # (WA MACRO UPDATES).         Y02668
         SPACE
VINB000  DS    0H
         L     R7,IWAWKBF              ADDRESSIBILITY OF WORK AREA
         XR    R3,R3                   CLEAR REGISTER FOR COUNT
         IC    R3,WKCT                 NUMBER OF ENTRIES IN SEGMENT
         LA    R4,WKPROCN1             ADDRESS OF FIST PROCNAME
         SPACE
****************************************************************
*                                                              *
* SEE IF PROCEDURE NAME EXISTS IN DIRECTORY                    *
*                                                              *
****************************************************************
VINB030  DS    0H
         CLC   DISPLZER(MVEIGHT,R4),PROCNAME COMPARE FOR PROC NAME
         BE    VINB050                 PROCNAME WAS FOUND
         LA    R4,DISPLELE(R4)         INCREMENT TO NEXT PROCNAME
         BCT   R3,VINB030              SEE IF MORE ENTRIES EXIST
         SPACE
****************************************************************
*                                                              *
*  PROCEDURE NAME WAS NOT FOUND.  RETURN CODE EQUALS ZERO      *
*                                                              *
****************************************************************
         XC    RTCD(CLFOUR),RTCD       PROCEDURE NAME NOT FOUND
         RETURN (14,12)
         SPACE
****************************************************************
*                                                              *
* PROCEDURE NAME WAS FOUND.  RETURN CODE EQUALS THE TTR        *
* OF THE FIRST RECORD OF THE PROCEDURE                         *
*                                                              *
****************************************************************
         SPACE
VINB050  DS    0H
         MVC   RTCD(MVTHREE),DISPLATE(R4) PROCEDURE WAS FOUND
         RETURN (14,12)
MVTHREE  EQU   3                       LENGTH OF MOVE
MVEIGHT  EQU   8                       LENGTH OF MOVE
CLFOUR   EQU   4                       LENGTH OF CORE TO BE CLEARED
DISPLZER EQU   0                       DISPLACEMENT
DISPLATE EQU   8                       DISPLACEMENT
DISPLELE EQU   11                      DISPLACEMENT
         END
