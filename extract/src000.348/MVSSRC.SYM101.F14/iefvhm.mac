*/*
**********************************************************************
*
*   MODULE NAME= IEFVHM
*
*   DESCRIPTIVE NAME= COMMAND VERB VALIDATION
*
*   COPYRIGHT= N/A
*
*   STATUS= OS/VS2 - SERVICE DATA IMPROVEMENTS                 @G17SPTJ
*
*   FUNCTION= IEFVHM VALIDITY CHECKS A COMMAND VERB.
*      OPERATION= WHEN IEFVHM RECEIVES CONTROL REGISTER 5 POINTS TO A
*                 VERB WHICH IS THEN COMPARED AGAINST A TABLE
*                 TO VALIDATE IT AS A COMMAND.  IF A COMMAND
*                 IS FOUND, A MESSAGE IS SENT TO THE OPERATOR.
*                 IF REPLY IS YES ISSUE SVC34. IF NO BYPASS SVC.
*                 CONTROL THEN IS PASSED TO IEFVHA.  IF THE
*                 COMAND IS NOT VALID THE JOB IS FAILED, AN ERROR
*                 MESSAGE IS ISSUED, IS PASSED TO IEFVHA.
*
*   NOTES
*      CHARACTER CODE DEPENDENCIES= EBCDIC
*      DEPENDENCIES= SVC 34
*      RESTRICTIONS= NONE
*      PATCH-LABEL= PATCH
*      REGISTER CONVENTIONS= R0 THROUGH RF
*
*   MODULE TYPE= PROCEEDURE
*      PROCESSOR= ASSEMBLER
*      MODULE SIZE= X'4B8'                                     @G17SPTJ
*      ATTRIBUTES= NCAL,LET,LIST,XREF,RENT,REFR
*
*   ENTRY POINT= IEFVHM
*      PURPOSE= VALIDATE VERB AS A COMMAND
*      LINKAGE= LOAD VCON, BRANCH REGISTER
*      INPUT DATA= REGISTER 5 POINTS TO THE VERB
*                  REGISTER 9 POINTS TO THE STATEMENT
*                  REGISTER 10 POINTS TO THE STATEMENT PARAMETER LIST
*                  REGISTER 13 POINTS TO THE SAVEAREA
*      REGISTERS SAVED= NONE
*      REGISTER CONTENTS DURING PROCESSING=
*      REGISTERS RESTORED= NONE
*
*   EXIT - NORMAL= LOAD IEFVHA VCON, BRANCH REGISTER
*      CONDITIONS= VERB IS A VALID COMMAND
*      OUTPUT DATA= NONE
*      RETURN CODES= NONE
*
*   EXIT - ERROR= LOAD IEFVHA VCON, BRANCH REGISTER
*      CONDITIONS= VERB NOT A VALID COMMAND
*      OUTPUT DATA= REGISTER 2 POINTS TO A MESSAGE
*      RETURN CODES= NONE
*
*   EXTERNAL REFERENCES=
*      ROUTINES= IEFVHEB TO LIST THE COMMAND
*      DATA AREAS= IWA,JCT
*      CONTROL BLOCKS= NONE
*      TABLE= COMMAND COMPARE TABLE
*      MACROS= GETMAIN,FREEMAIN,WTO,WTOR,WAIT,GMCR
*      ENQUEUE RESOURCES= NONE
*
*   MESSAGES= (UNIDENTIFIED OPERATION FIELD)
*             THIS MESSAGE IS PASSED TO IEFVHEB
*
*   CHANGE LEVEL = Z40WPSM, Z40BPSV, G17SPTJ                   @G17SPTJ
*
**********************************************************************
**/
         EJECT
         IEFCOMWA                                                Y02668
         IEFCVRWA                                                Y02668
         EJECT
         IEFVMCWA
         EJECT
         IEFTXTFT                                                Y02668
NEL      DSECT                                                  YM00386
         IEFNEL SUBCOM=C
         EJECT
*                                                                  XMCS
WTOBUF   DSECT                                                     XMCS
         USING WTOBUF,R2                                           XMCS
         DS    0D
WTOLENG  DS    H                   MSG LENGTH                      IMCS
MCSFLG   DS    H                   MCS FLAGS                       IMCS
MSGIDBF  DS    2F                  MSG ID                          IMCS
MSGTEXT  DS    16F                 MSG CONTENT.                  Y02668
DESCRTC  DS    F                   DESCRIPTOR AND ROUTING CODES    IMCS
ECBBUF   DS    F                   WAIT ECB                        IMCS
REPLYBUF DS    C                   REPLY LOCATION                  IMCS
DUMM     DS    CL3                 UNUSED                          IMCS
CMDLENG  DS    F                   REPLY LENGTH                    IMCS
*                                                                  XMCS
*                                                                  XMCS
         ORG WTOBUF                BUFFER USED FOR SVC 34          IMCS
MSGLEN   DS    F                   MSG LENGTH, MCS FLAGS           IMCS
COMDTXT  DS    20F                 COMMAND IMAGE                   IMCS
*                                                                  XMCS
IEFVHM   CSECT
EBLANK   EQU   C' '
ECHARN   EQU   C'N'
ECHARY   EQU   C'Y'
EHEX01   EQU   X'01'
EHEX40   EQU   X'40'
IGNOREC  EQU   X'F3'                                            YM00386
EXONLY   EQU   X'F0'                                            YM00386
ASKOPER  EQU   X'F2'                                            YM00386
EBCDICF  EQU   X'C6'                                            YM00386
AUTHLEN  EQU   4                                                YM00386
E0       EQU   0
E1       EQU   1
E2       EQU   2
E4       EQU   4
E8       EQU   8
E12      EQU   12
E16      EQU   16
E61      EQU   61
E76      EQU   76
E79      EQU   79
E80      EQU   80
E88      EQU   88
E91      EQU   91
* THE ABOVE EQUATES (EBLANK THRU E91) WERE MOVED TO ELIMINATE   YM01559
*  THE EXISTENCE OF A PRIVATE CSECT FOR THIS MODULE.            YM01559
         BALR  RB,0
         USING *,RB
*****************************************************************Y02668
* MODULE TRACE CODE - FOR TESTING.                               Y02668
         L     RF,TRACEV           LOAD TRACE RTNE ADDR.         Y02668
         BALR  RE,RF               ENTER MOD ID IN TRACE RECORD. Y02668
TRACEV   DC    V(TRACE)            TRACE RTNE ADDR.              Y02668
         DC    C'VHM '             MOD ID USED BY TRACE.         Y02668
* TRACE RETURNS HERE.                                            Y02668
*****************************************************************Y02668
         B     F6
         MODID BR=NO                                             Y01886
         DC    C'SU 17'            FUNCTION ID AND             @G17SPTJ
         DC    C'01'               LEV #(SLIP COMMAND)         @G17SPTJ
F6       DS    0H
         CLI   2(R9),X'40'                                        20107
         BNE   F64                                                20107
         LR    R6,R5                    VERB
         LA    R2,1                     INCREMENT
         LA    R3,8
         AR    R3,R6                    LIMIT
F63      DS    0H
         BXH   R6,R2,F64                SCAN FOR A BLANK
         CLI   0(R6),C' '
         BNE   F63
         SR    R6,R5                    FOUND, CALC VERB LENGTH
         LA    R1,TBL                   COMD LIST
         LA    R2,9                     INCREMENT
         LA    R3,ENDTBL
F635     DS    0H                       SCAN COMMAND LIST
         EX    R6,COMPARE
         BE    F66                      FOUND COMD
         BXLE  R1,R2,F635
* NOW SCAN ABBREVIATED COMMAND LIST
         LA    R3,ENDTBL2              END OF COMMAND TABLE COMPARAND
         LA     R2,2                   INCREMENT
         LA    R1,TBL2                 TABLE START
F636     DS    0H
         EX    R6,COMPARE
         BE    F66                     FOUND
         BXLE  R1,R2,F636              LOOP UNTIL TABLE IS DONE
F64      DS    0H
         TM    SWA,JTOP              **Q. JOB TO PROCESS
         BZ    F65                   ** BRANCH - NO
         LA    R8,WAJOBPFX         ADDR OF PSEUDO JOB PFX.       Y02668
         USING TEXT,R8             PREFIX ADDRESSABILITY.        Y02668
         OI    STRJINDC,JTXJOBFL   SET JOB FAILED BIT            Y02668
         DROP  R8                                                Y02668
         L     RE,CTRLWAP          GET LOCAL WORK AREA PTR       Y02668
         USING CWA,RE              SET BASE                      Y02668
         XC    MSGKEY,MSGKEY       ZERO SECONDARY MSG. KEY       Y02668
         DROP  RE                                                Y02668
         LA    R2,MSG5                  MSG-NO VERB
         OI   AOSW1,AOILGLOP       SET ILLEGAL OP SWITCH         Y02668
*                                  INDICATING A MESSAGE TO       Y02668
*                                  BE PRINTED                    Y02668
F65      DS    0H
         OI    AOSW1,AOPRTSW       INDICATE PRINT OP TO VHEB     Y02668
         L     RE,IEFVHAV          SET UP EXIT                   Y02668
         L     RF,IEFVHEBV         PRINT STMT AND MESSAGE IN     Y02668
         BR    RF                  VHEB BEFORE GOING TO VHA      Y02668
F66      DS    0H                       SET UP STMT FOR SVC 34
         GETMAIN R,LV=92      MSG BUFFER MAX LENGTH FOR WTO        XMCS
*                      AND WTOR - ECB REPLY CORE IF WTOR           GMCS
         LR    R2,R1                                               GMCS
         MVC   MSGTEXT(E80),E0(R9)                               Y02668
         LA    R4,MSGTEXT+E79                                    Y02668
         LA    R3,E80                                              IMCS
F67      EQU   *
         CLI   E0(R4),EBLANK                                       IMCS
         BNE   F68                 FOUND IT                        GMCS
         BCTR  R4,R0               DECREASE PTR                    GMCS
         BCT   R3,F67              DECREASE LENGTH                 GMCS
F68      EQU   *                                                   GMCS
         L     R1,WANELPTR         GET PTR TO NEL               YM00386
         USING NEL,R1              ADDRESS NEL                  YM00386
         CLI   NELCOMDS,IGNOREC    SHOULD COMMAND BE IGNORED    YM00386
         BE    F65                 YES, DO NOT EXEC COMMAND     YM00386
         CLI   NELCOMDS,EXONLY     SHOULD COM BE DISPLAYED      YM00386
         BE    F68G                NO, ONLY EXECUTE IT          YM00386
         DROP  R1                                               YM00386
MSGWTOMV EQU   *                                                   GMCS
         MVC   MSGIDBF(E8),MSGID   MOVE MSG ID TO WTO BUFFER       IMCS
         ST    R3,CMDLENG          STORE COMMAD LENGTH FOR SVC USE XMCS
         C     R3,NUM64            IS LENGTH OF COMMAND GREATER    GMCS
*                                  THAN 64                         GMCS
         LA    R1,WTO1             ..                              XMCS
         MVC   MCSFLG(E2),E2(R1)   MCS FLAGS TO WTO BUFFER         IMCS
         BNH   F68A                SINGLE WTO                      XMCS
         B     F68B                MULTIPLE WTO'S TO BE SENT       XMCS
F68A     EQU   *                                                   GMCS
         A     R3,HEX08            ADD 8 FOR MSG ID LENGTH         GMCS
         STH   R3,WTOLENG          MESSAGE LENGTH                  XMCS
         MVC   E1(E4,R4),E8(R1)    DESC AND ROUTCDE PARAMETERS     IMCS
ISSUE    EQU   *                                                   GMCS
         LA    R3,ECBBUF           ECB PTR FOR WTOR                XMCS
         LA    R4,REPLYBUF         REPLY BUFFER PTR FOR WTOR       XMCS
         WTO   MF=(E,(2))          ISSUE MESSAGE                   GMCS
         B     F68C                TEST IF WTOR REQUIRED           XMCS
F68B     EQU   *                   MULTIPLE WTO                    GMCS
         MVC   DESCRTC(E4),E8(R1)  DESC AND ROUTCODE PARAMETERS    IMCS
         LA    R4,E76                                              IMCS
         STH   R4,WTOLENG          MSG LENGTH                      XMCS
         WTO    MF=(E,(2))         ISSUE FIRST PART OF MSG         GMCS
         S     R3,NUM64            LENGTH OF COMMAND LEFT          GMCS
         LR    R7,R9                                               GMCS
         A     R7,NUM64                                            XMCS
         LR    R4,R3                                               GMCS
         BCTR  R4,E0               SUBTRACT 1                      IMCS
         EX    R4,MVIT             MOVE REST OF MESSAGE            IMCS
         LA    R4,E12(R2)                                          IMCS
         AR    R4,R3               PT TO LAST CHAR IN MSG          IMCS
         BCTR  R4,E0               SUBTRACT 1                      IMCS
         LA    R1,WTO1        RE-SET PTR TO WTO TEXT              M3178
         B     F68A                GO ISSUE 2ND PART OF MSG        IMCS
MVIT     DS    0H                                                Y02668
         MVC   MSGTEXT(E0),E0(R7)  MOVE CMD TO DISPLAY TO OPER.  Y02668
MVIT1    MVC   COMDTXT(E0),E0(R9)  MOVE CMD IMAGE FOR SVC 34       IMCS
F68C     DS    0F                                                  GMCS
*                             MUST BE WTOR OR WTO AND MGCR
         L     R1,WANELPTR         GET PTR TO NEL               YM00386
         USING NEL,R1              ADDRESS NEL                  YM00386
         CLI   NELCOMDS,ASKOPER    MUST OPERATOR BE ASKED TO    YM00386
*                                  PROVIDE COMMAND DISP         YM00386
         BNE   F68F1               NO, JUST EXEC COMMAND        YM00386
         DROP  R1                                               YM00386
F68D     EQU   *                                                   XMCS
         MVC   E0(E61,R2),WTOREP                                   IMCS
         LR    R1,R2                                               IMCS
         ST    R4,E0(R1)           PTR TO REPLY                    IMCS
         ST    R3,E4(R1)           PRT TO ECB                      IMCS
         XC    ECBBUF(E4),ECBBUF   CLEAR OUT ECB                   IMCS
         XC    REPLYBUF(E1),REPLYBUF  AND REPLY BUFFERS            IMCS
         MVI   E0(R1),EHEX01       REPLY LENGTH                    IMCS
         WTOR  ,0(R4),1,0(R3),MF=(E,(1))                           XMCS
         WAIT  1,ECB=(R3)                                          GMCS
         CLI   E0(R4),ECHARY                                       IMCS
         BE    F68F1               YES, GO ISSUE COMMAND           IMCS
         CLI   E0(R4),ECHARN       IS IT A 'NO' ANSWER             IMCS
         BNE   F68D                NO, OPERATOR ERROR, TRY AGAIN   XMCS
         LA    R4,F65              YES, PRESET FINAL BRANCH AND    XMCS
*                                  OMIT COMMAND                    XMCS
F68E     EQU   *                                                   XMCS
         LR   R1,R2                                                GMCS
         FREEMAIN R,LV=92,A=(1)                                    XMCS
         BR    R4                  RETURN                          IMCS
F68G     EQU   *                                                   XMCS
         LR    R1,R3                                               XMCS
         B     F68F2               GO TO SET UP FOR SVC 34         XMCS
F68F1    EQU  *                                                    XMCS
         L     R1,E88(R2)          COMMAND LENGTH
F68F2    EQU   *
         BCTR  R1,E0               SUBTRACT 1                      IMCS
         MVI   MSGLEN,EHEX40                                       IMCS
         MVC   MSGLEN+E1(E91),MSGLEN                               IMCS
         EX    R1,MVIT1            MOVE CMD IMAGE FOR SVC 34       XMCS
         A     R1,HEX04                                            XMCS
         SLL   R1,E16
         ST    R1,MSGLEN           COMMAND LENGTH                  XMCS
F686     DS    0H
****************************************************************YM00386
*                                                              *YM00386
*  CONVERT COMMAND AUTHORITY WHICH IS IN PRINTABLE HEX         *YM00386
*                 TO  BINARY TO PASS TO SVC 34                 *YM00386
*                                                              *YM00386
****************************************************************YM00386
         SPACE 2                                                YM00386
         L     R3,CDAUTHWA         LOAD WORK AREA CONTENTS.     YM02718
         LTR   R3,R3               HAS AUTH BEEN CONVERTED      YM00386
         BNZ   VHMSVC34            YES, BYPASS CONVERT          YM00386
         L     R3,WANELPTR         GET PTR TO NEL               YM00386
         USING NEL,R3              ADDRESS NEL                  YM00386
         LA    R4,NELAUTH          GET PTR TO COMMAND AUTH      YM00386
         LA    R7,CDAUTHWA         ADDRESS OF WORK AREA.        YM02718
         LA    R5,AUTHLEN          LENGTH OF AUTH FIELD         YM00386
*                                  AN UNPACKED FORMAT OF FA-FF. YM00386
VHMCONVT DS    0H                                               YM00386
         CLI   0(R4),EBCDICF       IS CHAR ALPHA(A THRU F)      YM00386
         BH    VHMNUM              NO, ASSUME   IT IS NUMERIC   YM00386
         SR    R6,R6               CLEAR R6 FOR INSERT          YM00386
         IC    R6,0(R4)            PUT CHAR IN R6               YM00386
         LA    R6,57(R6)           ADD 57 TO CHAR VALUE         YM00386
*                                  BY ADDING 57, CHAR VALUES    YM00386
*                                  A-F(C1-C6) ARE CHANGED TO    YM00386
*                                  WHEN THESE VALUES(FA-FF) ARE YM00386
*                                  PACKED THE LOW ORDER         YM00386
*                                  BITS(A-F) ARE RETAINED.      YM00386
         STC   R6,0(R7)            SAVE CONVRTED VALUE IN W.AREAYM00386
         LA    R7,1(R7)            INCRE TO NEXT FIED IN W.AREA YM00386
         LA    R4,1(R4)            INCRE TO NEXT COMAUTH CHAR   YM00386
         BCT   R5,VHMCONVT         GO BACK IF MORE CHARS        YM00386
         B     VHMPACK             IF FINISHED GO TO PACK       YM00386
VHMNUM   DS    0H                                               YM00386
         MVC   E0(E1,R7),E0(R4)    MOVE F0-F9 DIRECTLY FOR PACK YM01877
         LA    R7,1(R7)            INCRE TO NEXT FIELD IN AREA  YM00386
         LA    R4,1(R4)            INCRE TO NEXT COMAUTH CHAR   YM00386
         BCT   R5,VHMCONVT         GO BACK IF MORE CHARS        YM00386
VHMPACK  DS    0H                                               YM00386
         PACK  CDAUTHWA(4),CDAUTHWA PACK CONVERTED CHARS.       YM02718
VHMSVC34 DS    0H
         L     R0,CDAUTHWA         LOAD INTO REG FOR SVC 34.    YM02718
         SRL   R0,4                SHIFT OUT SIGN BITS          YM00386
         O     R0,NEGMASK          INDIC(TO SVC34) THAT         YM00386
*                                  COMMAND IS FROM INPUT STREAM YM00386
         MVI   4(R2),C'/'         PUT / IN PLACE OF X             19687
         MVI   5(R2),C'/'         PUT / IN PLACE OF X             19687
*                                  IN CASE COMD WAS IN A PROC.  YM00386
         MGCR  (2)                      SCHEDULE COMMAND'S EXECUTION
         LA    R4,F65                                              XMCS
         B     F68E           FREE BUF AND READ NEXT CARD          XMCS
COMPARE  CLC   0(1,R1),0(R5)
TBL      EQU   *                        COMMAND LIST
         DC    CL9'DISPLAY'
         DC    CL9'START'
         DC    CL9'STOP'
         DC    CL9'MOUNT'
         DC    CL9'UNLOAD'
         DC    CL9'HOLD'
         DC    CL9'LOG'
         DC    CL9'MODIFY'
         DC    CL9'RELEASE'
         DC    CL9'REPLY'
         DC    CL9'WRITELOG'
         DC    CL9'RESET'
         DC    CL9'CANCEL'
         DC    CL9'SET'
         DC    CL9'VARY'
         DC    CL9'CHNGDUMP'                                     Y02668
         DC    CL9'MONITOR'                                       M0381
         DC    CL9'SEND'                                          M0381
         DC    CL9'SE'             SEND ABBREVIATION              M4557
         DC    CL9'MN'             MONITOR                        M0381
         DC    CL9'CD'             CHNGDUMP ABBREVIATION         Y02668
         DC    CL9'SHIFT'
         DC    CL9'MSG'
         DC    CL9'CENOUT'
         DC    CL9'BRDCST'
         DC    CL9'STOPMN'                                       Y02668
         DC    CL9'PM'             STOPMN ABBREVIATION           Y02668
         DC    CL9'USERID'
         DC    CL9'PAGEADD'                                    @Z40WPSM
         DC    CL9'PA'         PAGE ADD ABBREVIATION           @Z40WPSM
         DC    CL9'SETDMN'                                     @Z40BPSV
         DC    CL9'SD'         SETDMN ABBREVIATION             @Z40BPSV
         DC    CL9'SLIP'                                       @G17SPTJ
         DC    CL9'SL'         SLIP ABBREVIATION               @G17SPTJ
ENDTBL   DC    CL9'SHOW'
* ACCEPTABLE ABBREVIATIONS FOR COMMANDS
*
TBL2     EQU   *
         DC    CL2'D'                  DISPLAY
         DC    CL2'S'                  START
         DC    CL2'P'                  STOP
         DC    CL2'M'                  MOUNT
         DC    CL2'U'                  UNLOAD
         DC    CL2'H'                  HOLD
         DC    CL2'L'                  LOG
         DC    CL2'F'                  MODIFY
         DC    CL2'A'                  RELEASE
         DC    CL2'R'                  REPLY
         DC    CL2'W'                  WRITELOG
         DC    CL2'E'                  RESET
         DC    CL2'C'                  CANCEL
         DC    CL2'T'                  SET
ENDTBL2  DC    CL2'V'                  VARY
MSG5     EQU   5                        NO VERB
         DS    0F                                                  XMCS
CLEAR0F  DC    X'0000FFFF'         MASKS TO SPECIFY COMMAND FROM   IMCS
NEGMASK  DC    X'80000000'           INPUT STREAM
IEFVHAV  DC    V(IEFVHA)                GET
IEFVHEBV DC    V(IEFVHEB)                                        Y02668
       DS    0F                                                    GMCS
MSGID    DC    CL8'IEF165I '       MSG ID                          IMCS
REPLYDMY DS    C                   AREA FOR WTOR REPLY             IMCS
ECBDMY   DS    F                   WAIT ECB ADDR                   IMCS
         DS    0F
HEX04    DC    F'5'                SIZE OF ROUT-DESC AREA PLUS 1   IMCS
HEX08    DC    F'12'               MSG ID LENGTH                   IMCS
NUM64    DC    X'00000040'         MAX LENGTH OF WTO               IMCS
HEX02    EQU   X'02'
         DS   0F                                                   GMCS
WTO1     WTO   '    ',MF=L,MCSFLAG=(RESP),DESC=(5),ROUTCDE=(1)
*                                                                  GMCS
WTOREP   WTOR  'IEF166D REPLY Y/N TO EXECUTE/SUPPRESS COMMAND',REPLYDMY*
               ,1,ECBDMY,MF=L,MCSFLAG=(RESP),DESC=(2),ROUTCDE=(1)
*                                                                  GMCS
PATCH    DC    25F'0'              **********PATCH SPACE*********Y02668
         END
