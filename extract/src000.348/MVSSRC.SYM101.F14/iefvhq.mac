         TITLE 'IEFVHQ'
***********************************************************************
*
*   MODULE NAME= IEFVHQ
*
*   DESCRIPTIVE NAME= SWA MANAGER INTERFACE
*
*   COPYRIGHT= N/A
*
*   STATUS= VS2 SU16 - IN THIS MODULE, SU 16 SUPPORTS          @G16AP2F
*           VPSS SUPPORT (SU 29 - @G29AN2E & @G29AN2F).        @G16AP2F
*           MERGE SU4,10,16 CODE TO REL 037 PTF                @ZA27753
*
*   FUNCTION= GENERAL DESCRIPTION:
*             IEFVHQ PROVIDES AN INTERFACE BETWEEN EITHER THE
*             CONVERTER OR INTERPRETER AND THE SWA MANAGER.
*             IT ALSO PROVIDES AN INTERFACE BETWEEN THE INTER- @G29AN2F
*             PRETER AND THE ALTERNATE SWA MANAGERS FOR MOVE   @G29AN2F
*             MODE AND LOCATE MODE.  (THE ALTERNATE SWA MANAGER@G29AN2F
*             FOR MOVE MODE IS SOMETIMES REFERRED TO AS THE    @G29AN2F
*             JOURNAL MERGE ROUTINE.)                          @G29AN2F
*
*             SWA SUPPORT:
*             FOR SWA SUPPORT,ALL TRACK STACKING CODE HAS
*             BEEN REMOVED.
*             ERROR PROCESSING CODE HAS BEEN SUBSTITUTED WITH
*             NEW ERROR CONDITION CODE.
*
*      OPERATION= IEFVHQ RECEIVES CONTROL VIA A BALR.          @G29AN2E
*                                                              @G29AN2E
*                MOVE MODE PROCESSING WILL BE DONE             @G29AN2E
*                IF AOSWALOC IS TURNED OFF IN THE COMWA.       @G29AN2E
*                LOCATE MODE PROCESSING WILL BE DONE IF        @G29AN2E
*                AOSWALOC IS TURNED ON.                        @G29AN2E
*                                                              @G29AN2E
*                IF PROCESSING IN THE MOVE MODE IS TO BE DONE, @G29AN2E
*                IT LOADS REGISTER 1 WITH THE ADDRESS OF THE   @G29AN2E
*                QMPA AND BRANCHES TO THE SWA MANAGER MOVE     @G29AN2E
*                MODE VIA THE IEFQMREQ MACRO.  IF IEFVHQ FINDS @G29AN2E
*                THAT A JOURNAL MERGE ROUTINE EXISTS, IT WILL  @G29AN2E
*                BRANCH TO THE JOURNAL MERGE ROUTINE INSTEAD   @G29AN2E
*                OF GOING TO THE SWA MANAGER.                  @G29AN2E
*                                                              @G29AN2E
*                IF PROCESSING IN THE LOCATE MODE IS TO BE     @G29AN2E
*                DONE, IT LOADS REGISTER 1 WITH THE ADDRESS OF @G29AN2E
*                THE PARAMETER LIST (POINTED TO BY AOLOCPL IN  @G29AN2E
*                THE COMWA) AND BRANCHES TO THE SWA MANAGER    @G29AN2E
*                LOCATE MODE. IF IEFVHQ FINDS THAT AN          @G29AN2F
*                ALTERNATE SWA MANAGER FOR LOCATE MODE EXISTS, @G29AN2F
*                IT WILL BRANCH TO IT INSTEAD OF THE DEFAULT   @G29AN2F
*                SWA MANAGER FOR LOCATE MODE (IEFQB555).       @G29AN2F
*
*   NOTES
*      CHARACTER CODE DEPENDENCIES= EBCDIC
*      DEPENDENCIES= NONE
*      RESTRICTIONS= NONE
*      REGISTER CONVENTIONS= REGISTERS SAVED 14-12
*      PATCH-LABEL= N/A
*
*   MODULE TYPE= BAL
*      PROCESSOR= ASSEM
*      MODULE SIZE= N/A
*      ATTRIBUTES= NCAL,RENT,REFR,XREF,LIST
*
*   ENTRY POINT= IEFVHQ
*      PURPOSE= PROVIDE SWA MANAGER INTERFACE
*      LINKAGE= BALK TO ENTRY POINT ADDRESS
*      INPUT DATA= IWA DATA IN FORM OF QMPA POINTER.
*                  COMWA DATA:                                 @G29AN2E
*                    AOLOCPL    ADDR OF LOCATE PARM LIST       @G29AN2E
*                    AOSWALOC   LOCATE MODE FLAG               @G29AN2E
*      REGISTERS SAVED= 14-12
*      REGISTER CONTENTS DURING PROCESSING= (SEE REGISTER CONVENTIONS)
*      REGISTERS RESTORED= 14-12
*
*   EXIT - NORMAL= RETURN TO CALLER
*      CONDITIONS= SWA MOVE MODE REQUEST HANDLED SUCCESSFULLY @G29AN2E
*      OUTPUT DATA= QMPA RE-INITIALIZED
*      RETURN CODES= ZERO
*
*   EXIT - ERROR= NONE
*      CONDITIONS= NONE
*      OUTPUT DATA= NONE
*      RETURN CODES= NONE
*
*   EXTERNAL REFERENCES=
*      ROUTINES= IEFQB555                                      @G29AN2E
*      DATA AREAS= IWA,COMWA                                   @G29AN2E
*      CONTROL BLOCKS= JCT,CVT,JESCT,QMPA
*      TABLE= NONE
*      MACROS= IEFVMIWA,IEFQMNGR,IEFAJCTB,CVT,IEFJESCT,IEFQMREQ
*      ENQUEUE RESOURCES= NONE
*      CHANGE LEVEL= Y02621,OZ11352,G29AN2E,G29AN2F,OZ27753    @ZA27753
*
*   MESSAGES= NONE
*
***********************************************************************
         EJECT
         IEFCOMWA                                                Y02668
         EJECT
         CVT
         EJECT
         IEFJESCT
         EJECT
         IEFQMNGR
         EJECT
********* E Q U A T E S ******************************************M2551
VHQ2     EQU   2                                                  M2551
VHQ7     EQU   7                                               @ZA11352
VHQ8     EQU   8                                               @ZA11352
         EJECT
IEFVHQ   CSECT
*                   RD=PTR TO CURRENT REG SAVE AREA
* CODE ADDED AFTER LABEL H16A                                  @ZA11352
         SAVE  (14,12)
         BALR  RB,0
         USING *,RB          *
         USING IOPARAMS,R1   *
*****************************************************************Y02668
* MODULE TRACE CODE - FOR TESTING.                               Y02668
         L     RF,TRACEV           LOAD TRACE RTNE ADDR.         Y02668
         BALR  RE,RF               ENTER MOD ID IN TRACE RECORD. Y02668
TRACEV   DC    V(TRACE)            TRACE RTNE ADDR.              Y02668
         DC    C'VHQ '             MOD ID USED BY TRACE.         Y02668
* TRACE RETURNS HERE.                                            Y02668
*****************************************************************Y02668
         B     HQ1                 BRANCH AROUND ID.             Y02668
         MODID BR=NO                                             Y02668
*        DC    C'SU16'             FUNCTION ID AND             @G16AN2F
*        DC    C'01'               LEVEL # (SUBSYS)            @G29AN2E
         DC    C'02'  LEV # (MERGE SU4,10,16 CODE TO 037 PTF)  @ZA27753
HQ1      DS    0H                                                Y02668
         ST    RD,IWASACHN         SAVE - SAVE AREA POINTER     YM02701
         LA    RD,IWAIOSA          GET SAVE AREA POINTER        YM02701
         TM    AOSW4,AOSWALOC      SWA LOCATE MODE REQUESTED?  @G29AN2E
         BO    HQL1                YES, GO PROCESS LOCATE MODE @G29AN2E
H14      DS    0H
         L     R1,QPARMP                Q-MGR PARMLIST POINTER
         L     RF,AOJMRGEP LOAD ADDR OF JOURNAL MERGE INTERFACE  Y02621
         LTR   RF,RF   DOES A JOURNAL MERGE INTERFACE RTN EXIST? Y02621
         BZ    H16   NO,USE SWA MANAGER INTERFACE AS DEFAULT.    Y02621
         BALR  RE,RF  GO TO JOURNAL MERGE INTERFACE TRN.         Y02621
         B     H16A   CONTINUE PROCESSING                        Y02621
H16      DS    0H
*              CALL THE SWA MANAGER VIA THE QMREQ MACRO.         Y02621
         SPACE 1
         IEFQMREQ
         SPACE 1
H16A     DS    0H
*        RE-SET QMPA VALUES
         L     R1,QPARMP                Q-MGR PARMLIST POINTER
         CLI   QMPOP,QMREAD        WAS FUNCTION READ?          @ZA11352
         BNE   H22                 NO, BYPASS                  @ZA11352
         L     R2,QMPCL            GET ADDR OF EXT PARMLIST    @ZA11352
         IC    R3,QMPCL            GET # OF BLOCKS             @ZA11352
         N     R3,HQANDOFF         TURN OFF HI ORDER BITS      @ZA11352
H20      EQU   *                                               @ZA11352
         MVI   VHQ7(R2),X'00'      CLEAR BLOCKID FIELD IF READ @ZA11352
* TO PREVENT INCORRECT IDENTIFICATION OF NEXT BLOCK WRITTEN    @ZA11352
         BCTR  R3,R0               DECREMENT # RECORDS READ    @ZA11352
         LTR   R3,R3               DONE CLEARING BLOCKIDS?     @ZA11352
         BZ    H22                                             @ZA11352
         LA    R2,VHQ8(R2)         PT TO NEXT ENTRY IN EXT PARM@ZA11352
         B     H20                 GO CLEAR NEXT BLOCKID       @ZA11352
H22      EQU   *                   SET QMPA FOR ASSUMED NEXT FUNCT.
*                                                              @ZA11352
         MVI   QMPCM,X'11'              SET ASSIGN 1 & WRITE 1
         MVI   QMPOP,QMWRTA             SET FOR WRITE & ASSIGN
         B     HQEXIT                                          @G29AN2E
         EJECT                                                 @G29AN2E
***************************************************************@G29AN2E
*                                                             *@G29AN2E
*    SWA MANAGER LOCATE MODE INTERFACE                        *@G29AN2E
*                                                             *@G29AN2E
***************************************************************@G29AN2E
         SPACE 2                                               @G29AN2E
HQL1     DS    0H                                              @G29AN2E
         L     R1,AOLOCPL          REG1 PTS TO PARM LIST       @G29AN2E
         L     RF,AOQLOCEP         LOAD ADDR OF ALTERNATE      @G29AN2F
*                                  LOCATE MODE SWA MANAGER     @G29AN2F
         LTR   RF,RF               DOES ALTERNATE EXIST?       @G29AN2F
         BZ    HQL2                NO, USE IEFQB555            @G29AN2F
         BALR  RE,RF               YES, GO TO ALTERNATE        @G29AN2F
         B     HQL3                CONTINUE PROCESSING         @G29AN2F
HQL2     DS    0H                                              @G29AN2F
         L     RF,CVTPTR(R0,R0)    ESTABLISH ADDRESSABILITY    @G29AN2E
         USING CVTMAP,RF           FOR CVT                     @G29AN2E
         L     RF,CVTJESCT         ESTABLISH ADDRESSABILITY    @G29AN2E
         USING JESCT,RF            FOR JESCT                   @G29AN2E
         L     RF,JESWAA           GET ADDR SWA MGR LOCATE MODE@G29AN2E
         BALR  RE,RF               GO TO IEFQB555              @G29AN2E
         DROP  RF                                              @G29AN2E
HQL3     DS    0H                                              @G29AN2F
         NI    AOSW4,X'FF'-AOSWALOC TURN OFF LOCATE MODE SWITCH@G29AN2F
         SPACE 3                                               @G29AN2E
HQEXIT   DS    0H                                              @G29AN2E
         L     RD,IWASACHN         RESTORE SAVE AREA POINTER   @G29AN2F
         RETURN (14,12),T          RETURN TO CALLER
PATCH    DC    25F'0'              **********PATCH SPACE*********Y02668
HQANDOFF DC    X'0000000F'       CLEAR # OF RECORDS TO ASSIGN  @ZA11352
         END
