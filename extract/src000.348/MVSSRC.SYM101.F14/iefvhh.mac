        TITLE 'IEFVHH'
***********************************************************************
*
*   MODULE NAME= IEFVHH
*
*   DESCRIPTIVE NAME= ENQUEUE ROUTINE
*
*   COPYRIGHT= N/A
*
*   STATUS= OS/VS2 RELEASE 3.7 SU04 PTF                        @ZA19982
*
*   FUNCTION= 1. WRITES JOB AND STEP TABLES TO SWA
*             2. PERFORMS MISCELLANEOUS CLEANUP FUNCTIONS
*      OPERATION= 1. CHECKS IF ALL EXEC OVERRIDES HAVE BEEN COMPLETED
*                    AT PROCEDURE END OF FILE, AND FAILS THE JOB IF
*                    ANY REMAIN.
*                 2. DELETED                                    YM04952
*                 3. PROVIDES SMF SUPPORT BY BRANCHING TO USER'S
*                    ROUTINE AND CHECKING THE RETURN CODE, AND ENTERING
*                    INTERPRETER STOP TIME IN THE JMR.
*                    THE JSCB ACTIVE POINTER IN THE TCB IS      YM04108
*                    SWITCHED BEFORE GOING TO THE USER EXIT.    YM04108
*                    THE ACTUAL ACTIVE JSCB ADDRESS IS REPLACED YM04108
*                    WITH THE INITIATOR'S JSCB ADDRESS(ALWAYS   YM04108
*                    THE FIRST JSCB IN THE CHAIN.).THIS ALLOWS  YM04108
*                    THE USER EXIT TO USE A DATA SET DEFINED IN YM04108
*                    THE INITIATOR PROC.                        YM04108
*                    THE ORIGINAL ACTIVE JSCB POINTER IS PUT    YM04108
*                    BACK INTO THE TCB UPON RETURN FROM THE     YM04108
*                    USER EXIT.                                 YM04108
*                    IF AN ABEND OCCURS WHILE THE POINTER IS    YM04108
*                    SWITCHED,THE INTERPRETER RECOVERY ROUTINE  YM04108
*                    IEFNB9IT WILL SWITCH IT BACK..             YM04108
*                 4. WRITES APPROPRIATE JOB AND STEP TABLES TO SWA AS
*                    INDICATED BY SWITCH SETTINGS.
*                 5. DELETED                                    YM04952
*                 6. CLEARS THE SCT.
*      CHARACTER CODE DEPENDENCIES= EBCDIC
*      DEPENDENCIES= NONE
*      RESTRICTIONS= NONE
*      REGISTER CONVENTIONS=
*                            R6- SWA MANAGER PARMLIST POINTER
*                            R7- SCT POINTER
*                            R8- JCT POINTER
*                            R9- STATEMENT POINTER
*                            RA- POINTER TO STATEMENT PARAMETER LIST
*                            RB- BASE REGISTER
*                            RC- IWA POINTER
*                            RD- SAVE AREA POINTER
*      PATCH-LABEL= PATCH
*
*   MODULE TYPE= BAL
*      PROCESSOR= ASSEMBLER
*      ATTRIBUTES= NCAL, LET,LIST,XREF,RENT,REFR
*
*   ENTRY POINT= IEFVHH
*      PURPOSE= JOB AND STEP ENQUEUE PROCESSING
*      LINKAGE= BRANCH ON REGISTER
*      INPUT DATA= INTERPRETER WORK AREA
*                  RC- POINTS TO IWA
*                  JCT, SCT, JMR, QMPA, NEL
*      REGISTERS SAVED= NONE
*      REGISTER CONTENTS DURING PROCESSING= SEE REGISTER CONVENTIONS
*      REGISTERS RESTORED= NONE
*
*   EXIT - NORMAL= BRANCH TO IEFVHN
*      CONDITIONS= SWITCH JCTRTN IS ON AND JOB HAS NOT BEEN FAILED
*      OUTPUT DATA= ALL JOB TABLES WRITTEN TO SWA
*      RETURN CODES= NONE
*
*   EXIT - ERROR= BRANCH TO IEFVHN
*      CONDITIONS= SWITCH JCTRTN IS ON AND JOB HAS BEEN FAILED
*      OUTPUT DATA= ALL JOB TABLES WRITTEN TO SWA
*                   IF ERROR FOUND IN IEFVHH, ERROR MSG WRITTEN
*                            TO MESSAGE DATA SET
*      RETURN CODES= NONE
*
*   EXTERNAL REFERENCES=
*      ROUTINES= BRANCH TO IEFVGM- WRITE ERROR MESSAGE TO MSG DATA SET
*                BRANCH TO IEFVHQ- WRITE JOB TABLES TO SWA
*
*      DATA AREAS= INTERPRETER WORK AREA
*      CONTROL BLOCKS= JCT, SCT, CVT, NEL, ACT, DSNT, DSENQT, QMPA,JCTX
*      TABLE= WORKAREA
*
*      MACROS= IEFVMIWA, IEFAJCTB, IEFASCTB, IEFJMR, IEFQMNGR, IEFNEL,
*              CVT
*      ENQUEUE RESOURCES= NONE
*
*   MESSAGES= IEF611I OVERRIDDEN STEP NOT FOUND IN PROCEDURE
*             IEF661I  RESTART STEP NOT FOUND
*             IEF686I  DDNAME REFERRED TO NOT DEFINED IN STEP  @ZA19982
*
*   CHANGE LEVEL = Z40MPTH, Z40RPSM, ZA19982
*
***********************************************************************
         EJECT
         IEFJMR
         EJECT
         IEFCOMWA
         IEFVMIWA
JCTD     DSECT
         IEFAJCTB
SCTD     DSECT
         IEFASCTB
         IEFQMNGR
         EJECT
*                                 NEL DSECT FOLLOWS               M0780
NEL      DSECT
         IEFNEL SUBCOM=I                                         Y02668
         IEFVMCWA
CVTDSECT DSECT
         CVT
         IEZJSCB
         IKJTCB
         EJECT
IEFVHH   CSECT
* CODE ADDED AFTER LABEL DEC255                                @ZA19982
         BALR  RB,0
         USING *,RB          *
         USING IOPARAMS,R6   *
         USING SCTD,R7       *
         USING JCTD,R8       *
*****************************************************************Y02668
* MODULE TRACE CODE - FOR TESTING.                               Y02668
         L     RF,TRACEV           LOAD TRACE RTNE ADDR.         Y02668
         BALR  RE,RF               ENTER MOD ID IN TRACE RECORD. Y02668
TRACEV   DC    V(TRACE)            TRACE RTNE ADDR.              Y02668
         DC    C'VHH '             MOD ID USED BY TRACE.         Y02668
* TRACE RETURNS HERE.                                            Y02668
*****************************************************************Y02668
         B     D1
         MODID BR=NO                                             Y01886
         DC    C'REL4'              FUNCTION ID AND            @Z40MPTH
*        DC    C'02'   LEV # (RACF)                            @Z40RPSM
         DC    C'03'   LEV # (DD REFERRED TO NOT DEFINED)      @ZA19982
D1       DS    0H
         LA    R6,QPARM                Q PARM LIST
         L     R5,QMPCL                 WORK AREA FOR Q PARMS
         XC    CLFOUR(CLFOUR,R5),CLFOUR(R5)     CLEAR Q-MGR      Y02621
*              QMPA PARAM LIST BLOCK POINTER BY ZEROING IT.      Y02621
         L     RF,CTRLWAP          ADDRESS OF LOCAL WORK AREA.  YM02714
         USING CWA,RF              WORK AREA ADDRESSABILITY.    YM02714
         XC    MSGKEY(1),MSGKEY    CLEAR SEC MSG CODES, IF ANY. YM02714
         DROP  RF                                               YM02714
         LA    R8,JCT                  JCT BASE
         L     RA,IEFVHQV
         TM    SWA,SCTTQ               SCT TO NQ                   7586
         BZ    D4                       BR NO
         TM    SWE,PROC           EXEC FROM A PROCEDURE...?      Y02668
         BO    D11                YES,CONTINUE PROCESSING,       Y02668
         TM    SWE,POVRX                EXEC ORIDES COMPLETE
         BZ    D11                      BR YES
*  NOT ALL EXEC OVERRIDES RESOLVED - ERROR CONDITION.           YM02714
         NI    SWE,255-POVRX       CLEAR EXEC OVERRIDE SW.      YM02714
         OI    JCTJSTAT,INCMSTS         FAIL JOB
         LH    R4,WASTMTNO         SAVE CURRENT STMT NBR.       YM02714
         MVC   WASTMTNO(2),IWAEXPNO MOVE IN EXEC PROC STMT NBR  YM02714*
                                   FOR THIS MSG.                YM02714
         LA    R2,MSG11                 EXEC ORIDE NOT FOUND
         L     RF,IEFVGMV
         BALR  RE,RF                    PUT MSG
         STH   R4,WASTMTNO         REPLACE CURRENT STMT NBR.    YM02714
D11      DS    0H
         TM    SWC,DSNTQ                DSN TO NQ
         BZ    D16                      BR NO
         LA    R4,DSNAME               DSN TABLE BASE
         ST    R4,0(0,R5)               STORE PTR TO DSN IN WORK AREA
         MVC   4(3,R5),0(R4)            TTR TO WORK AREA+4
         MVI   QMPOP,QMWRTE             WRITE ONLY
         BALR  RE,RA                    DSN TO Q
D16      DS    0H
         LA    R7,SCT                  SCT BASE
         TM    AOSW2,AOMAXERR      HAS NUMBER OF STATIC DD'S     Y02668
*                                 EXCEEDED THE MAXIMUM           Y02668
         BO    VHH170              YES, DON'T BOTHER ADDING      Y02668
*                                  THE DYNAMIC DD COUNT TO THE   Y02668
*                                 STATIC DD COUNT                Y02668
         LH    RE,SCTLALOC         GET THE NUMBER OF STATIC DD'S Y02668
         AH    RE,SCTNIUSL         ADD STATIC AND DYNAMIC DD     Y02668
*                                  COUNTS                        Y02668
         LH    RF,MAXDDS           GET MAX COUNT ALLOWABLE       Y02668
         CLR   RE,RF               HAS THE MAX BEEN EXCEEDED     Y02668
         BH    VHH160              YES, USE MAX AND WARN         Y02668
         STH   RE,SCTNIUSL         NO, SAVE TOTAL IN SCT         Y02668
         B     VHH170              CONTINUE                      Y02668
VHH160   DS    0H                                                Y02668
         STH   RF,SCTNIUSL         USE MAXIMUM - THIS, IN EFFECT,Y02668
*                                  REDUCES THE NUMBER OF DYNAMIC Y02668
*                                  DD'S REQUESTED                Y02668
         LA    R2,MAXDDMSG         WARNING MSG - DYNAMIC DD'S   YM01901
*                                  REDUCED                      YM01901
         L     RF,IEFVGMV          ADDR OF MSG RTN               Y02668
         BALR  RE,RF               PUT OUT WARNING MSG           Y02668
VHH170   DS    0H                                                Y02668
         NI    SWC,255-DSNTQ            DSN SW OFF               Y02668
         NI    SWH,255-CPSYSFLG    CLEAR C/R EXEC STMT FLAG        AACA
         CLC   SCTAFACT(3),D3ZERO   *ACT TTR EQUAL ZERO.
         BNE   D16A               *NO.  ACT HAS BEEN PUT OUT.
         MVI   QMPOP,QMASGN      * ASSIGN TTR FOR ACT.
         MVI   QMPCM,X'10'         SET ASSIGN OF 1 RECORD.      YM02717
         BALR  RE,RA             *
         XC    DSNAME(176),DSNAME  USE TABLE AREA FOR ACT BUILD.YM04952
         MVC   DSNAME(3),0(R5)     * MOVE IN ACT TTR.            Y02668
         MVI   DSNAME+3,X'01'      * SET ACT ID.                 Y02668
         MVC   4(4,R5),0(R5)     * MOVE TTR TO PARM FIELD.
         MVC   SCTAFACT(3),0(R5) * MOVE TTR TO SCT.
         MVI   QMPOP,QMWRTE      * SET TO WRITE ONLY.
         LA    R9,DSNAME         *                               Y02668
         ST    R9,0(R5)          * SET ACT ADDRESS.
         BALR  RE,RA             * LINK TO PUT OUT ACT.
D16A     DS    0H                *
         ST    R7,0(0,R5)               STORE PTR TO SCT IN WORK AREA
         MVC   4(3,R5),SCTDISKA         TTR IN WORK AREA+4
         MVC   SCTANSCT(3),TNEXT        CHAIN TO NEXT SCT
         TM    SWA,JCTTQ                LAST STEP THIS JOB
         BZ    D17                      BR NO
         MVI   QMPOP,QMWRTE             WRITE ONLY
         XC    SCTANSCT(3),SCTANSCT     CLEAR CHAIN PTR
D17      DS    0H
***********************************************************************
*   SMF OPTION                                                     SMF1
***********************************************************************
         L     R4,WANELPTR         GET PTR TO NEL                Y02668
         USING NEL,R4              ADDRESS THE NEL               Y02668
         TM    NELOPSWT,NELSMF     IS SMF BEING USED             Y02668
         BZ    D17A                NO, BYPASS SMF PROCESSING     Y02668
         L     R4,IWAJMRPT         YES, GET JMR PTR              Y02668
         USING JMR,R4
         TM    SWA,JCTTQ               JCT TO N Q
         BO    D17A1                   YES,DON'T UP STP CT.
         SR    R2,R2                   *                           SMF1
         IC    R2,JMRSTEP              *                           SMF1
         LA    R2,DEC1(R2)             *                           SMF1
         STC   R2,JMRSTEP              INCREMENT STEP COUNT        SMF1
D17A1    EQU   *                       *
         L     R2,SCTSMF               SYSIN CT                    SMF1
         A     R2,JMRJOBIN             *                           SMF1
         ST    R2,JMRJOBIN             TOTAL FOR JOB               SMF1
D17A     BALR  RE,RA                   SCT TO Q                    SMF1
         XC    SCTSMF(4),SCTSMF        CLEAR STEP SYSIN COUNT      SMF1
*                                  IN SCT FOR NEXT STEP          Y02668
         MVC   TNEXT,0(R5)              NEW TTR, GARBAGE IF LAST STEP
*****************************************************************Y01113
*        * PRIOR TO AOS/AM0 RELEASE DDSWX1 WAS ZEROED IN AN XC   Y01113
*        * INSTRUCTION ALONG WITH 78 BYTE DNRT.                  Y01113
*        * NEXT INSTRUCTION ADDED TO PREVENT NEW BITS FROM BEING Y01113
*        * OFF ALONG WITH CLEARING OF DDNAME REF SW & TABLE.     Y01113
*****************************************************************Y01113
DEC78    EQU   78                 DDNAME REFERENCE TABLE LENGTH  Y01113
DEC255   EQU   255                DECIMAL EQUATE                 Y01113
         NI    DDSWX1,DEC255-SDDNSW-IWASCAT2 SET SWITCHES OFF.   Y02668
         LA    R1,DNRT        ADDRESS OF DDNAME TABLE          @ZA19982
         LA    RF,DEC5        SET LOOP FOR 5                   @ZA19982
D30      DS    0H                                              @ZA19982
         OC    0(8,R1),0(R1)  COMPARE ENTRY TO ZERO            @ZA19982
         BNZ   D6                                              @ZA19982
         LA    R1,14(R1)      INCREMENT TO NEXT ENTRY          @ZA19982
         BCT   RF,D30         LOOP BACK                        @ZA19982
         B     D4             TABLE EMPTY,ALL NAMES DEFINED    @ZA19982
D6       DS    0H                                              @ZA19982
         XC    DNRT(DEC78),DNRT  CLEAR DDNAME REF TABLE        @ZA19982
         LA    R2,MSG686      SET UP CODE FOR IEF686I          @ZA19982
         L     RF,IEFVGMV     LOAD MSG MODULE ADDRESS          @ZA19982
         BALR  RE,RF          GO TO PUT OUT MESSAGE            @ZA19982
D4       DS    0H
         TM    SWA,JCTTQ                JCT TO NQ
         BO    D41                      BR YES
D4A      DS    0H                                                A45375
         NI    SWA,255-SCTTQ       INDICATE SCT HAS BEEN PUT TO  Y02668
*                                  SWA                           Y02668
         L     RF,IEFVHEV          GO TO PROCESS NEXT STEP       Y02668
         BR    RF                  HOUSEKEEPING ROUTINE            AAAA
D41      DS    0H
         L     R4,DSENQTP               ADDR OF DSENQ TABLE IF
         LTR   R4,R4                         NON-ZERO MUST BE PUT OUT
         BZ    D414                     ZERO. DO NOT PUT OUT     Y02668
         TM    SWD,DBFST          US THERE DSENQ TABLE FOR THIS JOB.292
         BO    D414               NO.  BYPASS ASSIGN.            Y02668
         MVI   QMPOP,QMASGN            *ASSIGN ONLY
         BALR  RE,RA                   *
         MVC   TNEXT,0(R5)             *NEW TTR
         ST    R4,0(0,R5)         *DSENQ PTR TO REMOTE LIST
         MVC   0(3,R4),TNEXT            TTR TO DSENQ TABLE
         MVC   JCTDETDA(3,R8),TNEXT     SAME TTR TO JCT
         MVC   4(3,R5),TNEXT            TTR FROM TABLE TO WORKAREA+4
         MVI   QMPOP,QMWRTE       *INDICATE WRITE ONLY
         BALR  RE,RA
         EJECT
*****************************************************************Y02668
*                                                               *Y02668
*    SMF USER EXIT - PRIOR TO PUTTING THE JCT TO SWA            *Y02668
*                                                               *Y02668
*****************************************************************Y02668
*                                                               YM04952
D414     DS    0H                                               YM04952
         L     R4,WANELPTR         GET PTR TO NEL                Y02668
         USING NEL,R4              ADDRESS NEL                   Y02668
         TM    NELOPSWT,NELSMF     IS SMF BEING USED             Y02668
         BZ    D415A               NO, BYPASS PUTTING JMR TO SWA Y02668
         L     R4,IWAJMRPT         GET JMRPTR                    Y02668
         USING JMR,R4             ADDRESS JMR                    Y02668
         TM    JMROPT,JMREXITS    IS A USER EXIT REQUESTED?     Y02668
         BZ    D415B              NO,THEN PUT JMR TO SWA..      Y02668
         L     R3,CVTPTR          ADDRESS OF CVT                YM04108
         USING CVTMAP,R3
         L     R3,CVTTCBP         ADDRESS OF DOUBLEWORD POINTER YM04108
*                                FOR CURRENT AND OLD TCBS.      YM04108
         DROP  R3
         L     R3,CURRTCB(R3)    ADDRESS OF CURRENT TCB...      YM04108
         USING TCB,R3                                           YM04108
         L     R3,TCBJSCB         ADDRESS OF FIRST JSCB OFF TCB YM04108
         DROP  R3
         ST    R3,IWAJSCBF        STORE ADDRESS FOR LATER USE.  YM04108
         USING IEZJSCB,R3                                       YM04108
         L     R1,JSCBACT         ADDRESS OF ACTIVE JSCB        YM04108
         ST    R1,IWAJSCBA        STORE ADDRESS FOR LATER USE.  YM04108
         MVC   JSCBACT(4),IWAJSCBF  MAKE FIRST JSCB ACTIVE JSCB.YM04108
         DROP  R3
         LA    R1,JMRPTRS              GET ADDR OF PARAM LIST      SMF1
         LA    R3,JMRJOB               GET ADDR OF JOB LOG         SMF1
         ST    R3,JMRJOBP              STORE JOB LOG ADDR IN       SMF1
*                                           PARAM LIST             SMF1
         MVI   JMRJCLCD,JCTSMFEX   SET EXIT CODE IN PARMLIST     Y02668
         LA    R3,JMRJCLCD             *                           SMF1
         ST    R3,JMRJCLCP             TERMINATION STATUS          SMF1
         L     RF,JMRUJVP    GET ADDRESS OF USER EXIT RTN..      Y02668
         BALR  RE,RF              GO TO SMF USER EXIT..            SMF1
         L     R3,IWAJSCBF        PTR TO FIRST JSCB OFF TCB.    YM04108
         USING IEZJSCB,R3                                       YM04108
         MVC   JSCBACT(4),IWAJSCBA   RESET ACTIVE JSCB POINTER  YM04108
         DROP  R3
         SR    R3,R3              ZERO REGISTER 3               YM04108
         ST    R3,IWAJSCBF        SET FIRST JSCB POINT TO ZERO  YM04108
         ST    R3,IWAJSCBA        SET ACTIVE JSCB PTR TO ZERO.  YM04108
         LA    R2,DEC4                 *                           SMF1
         CR    RF,R2                   CHECK FOR CANCELLATION      SMF1
         BNE   D415B                   NO CANCEL                   SMF1
CANCENQ  EQU   X'04'                   *                           SMF1
         OI    JCTJMROP+DEC1,CANCENQ   MARK CANC IN JCT            SMF1
         OI    JCTJSTAT,INCMSTS        SET JOB CANCELLED BIT       SMF1
D415B    ST    R4,DEC0(DEC0,R5)        PTR TO JMR IN WORK AREA     SMF1
         MVC   DEC4(DEC3,R5),JCTJMR    MOVE JMR TTR TO WORK AREA   SMF1
         MVI   DISPLID(R5),JMRID       BLOCK ID TO REMOTE LIST   Y02621
         MVI   QMPOP,QMWRTE            WRITE ONLY                  SMF1
         BALR  RE,RA                   JMR TO Q                    SMF1
         MVI   DISPLID(R5),DEC0    CLEAR BLOCK ID IN SWA MANGR   Y02668
*                                  WORK AREA                     Y02668
DEC1     EQU   1                       NUMERIC EQUATE
DEC4     EQU   4                       NUMERIC EQUATE
DEC0     EQU   0                       NUMERIC EQUATE
DEC3     EQU   3                       NUMERIC EQUATE
DEC16    EQU   16
D415A    DS    0H                                                Y02668
         L     RE,WANELPTR         LOAD NEL ADDRESS.             Y02668
         USING NEL,RE              NEL ADDRESSABILITY.           Y02668
         TM    NELOPSWT,NELRECVY   MSGS TO BE SUPPRESSED?        Y02668
         DROP  RE                                                 M3144
         BZ    D40                 BYPASS SETTING SUPPRESS BIT.  Y02668
         OI    JCTSTAT2,JCTSWSM  SET SUPPRESS WARM START MSG BIT  M3144
D40      DS    0H                                                Y02668
         TM    SWH,CPSTPFL         HAS THE RESTART STEP BEEN    YM01573
*                                  FOUND                        YM01573
         BZ    VHH0100             YES, DON'T FAIL THE JOB      YM01573
         IEFSAVER SAVEPTR          GET SAVEAREA FOR WTO RTN     YM01573
         LA    R5,RSNOTFND         INDICATE RESTART STEP NOT    YM01573
*                                  FOUND TO WTO RTN             YM01573
         L     RF,IEFVHRV          GET ADDR OF WTO RTN          YM01573
         BALR  RE,RF               ISSUE WTO                    YM01573
         IEFRELSE SAVEPTR          RELEASE WTO RTN SAVEAREA     YM01573
         LA    R2,MSG61            RESTART STEP NOT FOUND MSG   YM01573
*                                  TO PROGRAMMER                YM01573
         LA    RF,1                RESET STMT NBR TO 1 FOR      YM02714
         STH   RF,WASTMTNO         JOB STMT, FOR THIS EOJ MSG.  YM02714
         L     RF,IEFVGMV          GET ADDR OF SYSTEM MSG RTN   YM01573
         BALR  RE,RF               PUT OUT MSG                  YM01573
         OI    JCTJSTAT,INCMSTS    FAIL THE JOB                 YM01573
         L     R5,QMPCL            RE-INIT EXTRN PARMLIST PTR   YM01573
VHH0100  DS    0H                                               YM01573
***  WRITE JCT AND JCTX TO SWA. ***                            @Z40RPSM
         ST   R8,0(0,R5)          ADDR. OF JCT STORED IN WORK
*                                 AREA                         @Z40RPSM
         LA   R0,JCTX             ADDR.OF JCTX STORED          @Z40RPSM
         ST   R0,D8(R5)          IN WORK AREA                  @Z40RPSM
         MVC  D04(D3,R5),JCTDSKAD TTR IN WORK AREA+4           @Z40RPSM
         MVI  D7(R5),NULLID      ZERO ID FOR JCT               @Z40RPSM
         MVC  D12(D3,R5),JCTJCTX JCTX TTR INTO WORK AREA       @Z40RPSM
         MVI  D15(R5),NULLID     ZERO ID FOR JCTX INTO WORK
*                                AREA                          @Z40RPSM
         MVI  QPARM+QMPOP-QMNAM,QMWRTE     SET WRITE           @Z40RPSM
         MVI  QPARM+QMPNC-QMNAM,D2         SET 2 WRITES        @Z40RPSM
         BALR RE,RA                        JCT ' JCTX TO Q     @Z40RPSM
D45      DS    0H                                                Y02668
         L     RF,IEFVHNV          GET INTERP TERMINATION RTN @  Y02668
         BR    RF                  JOB COMPLETE - GO TO          Y02668
*                                  TERMINATE THE INTERPRETER     Y02668
         EJECT
*****************************************************************Y02668
*                                                               *Y02668
*        VCONS, DECLARED STORAGE, AND EQUATES                   *Y02668
*                                                               *Y02668
*****************************************************************Y02668
IEFVHRV  DC    V(IEFVHR)           WTO ROUTINE                   Y02668
IEFVGMV  DC    V(IEFVGM)                MSG
IEFVHQV  DC    V(IEFVHQ)                Q MGR LINK
IEFVHEV  DC    V(IEFVHE)           INTERPRETER GET AND ROUTE RTN Y02668
IEFVHNV  DC    V(IEFVHN)           INTERPRETER TERMINATION       Y02668
D3ZERO   DC    X'000000'
         DS    0F
MSK01    DC    X'000000F0'
MSK02    DC    X'0000000F'
MAXDDS   DC    H'1635'             MAX # DD'S ALLOWED IN STEP    Y02668
MAXDDMSG EQU   X'4B'               DYNAMIC DD COUNT REDUCED     YM01901
HALFWORD EQU   2                                                 Y02668
MSG11    EQU   11                       ORIDDEN EXEC STMT MISSING
MSG686   EQU   X'50'                    DDNAME UNDEFINED MSG   @ZA19982
DEC5     EQU   5                      LOOP CNTRL FOR DNRT SRCH @ZA19982
MSGSYMP  EQU   X'39'              UNDEFINED SYMBOLIC PARAM
JCTDETDA EQU   88                       JCT OFFSET FOR DSENQT TTR
CLFOUR   EQU   4                        LENGTH OF FOUR           A35409
DISPLID  EQU   7       DISPLACEMENT OF BLOCK ID IN REMOTE LIST   Y02621
JMRID    EQU   X'1B'     JMR BLOCK ID FOR REMOTE LIST            Y02621
JCTSMFEX EQU   X'20'               SMF EXIT CODE                 Y02668
RSNOTFND EQU   3                   RESTART STEP NOT FOUND WTO    Y02668
MSG61    EQU   61                  RESTART STEP NOT FOUND MSG    Y02668
CURRTCB  EQU   4                                                YM04108
HFAIL    EQU   1
D2       EQU   2                                               @Z40RPSM
D3       EQU   3                                               @Z40RPSM
D04      EQU   4                                               @Z40RPSM
D5       EQU   5                                               @Z40RPSM
D7       EQU   7                                               @Z40RPSM
D8       EQU   8                                               @Z40RPSM
D12      EQU   12                                              @Z40RPSM
D15      EQU   15                                              @Z40RPSM
NULLID   EQU   X'00'       NULL ID FOR SWA MGR.                @Z40RPSM
PATCH    DC    25F'0'              **********PATCH SPACE*********Y02668
         END
