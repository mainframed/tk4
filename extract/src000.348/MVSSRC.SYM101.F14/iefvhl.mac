***********************************************************************
*
*   MODULE NAME= IEFVHL
*
*   DESCRIPTIVE NAME= NULL STATEMENT, OR END OF FILE, PROCESSOR
*
*   COPYRIGHT= N/A
*
*   STATUS= OS/VS2 RELEASE 3
*
*   FUNCTION= ENTERED AS A RESULT OF NULL STATEMENT GENERATED BY IEFVHA
*             AT END OF FILE ON THE JCL DATA SET OR PROCEDURE - MODULE
*             PERFORMS END OF FILE PROCESSING.
*      OPERATION= .CHECKS WHICH INPUT DATA SET REACHED END OF FILE,
*                  THEN CHECKS THE EOF FLAG FOR THE OTHER. IF EOF IS
*                  INDICATED FOR BOTH, MODULE SETS THE END OF
*                  CONVERSION FLAG FOR IEFVHF.
*                 .IF EOF HAS BEEN REACHED ONLY ON THE JCL DATA SET,
*                  FLAGS ARE SET TO CAUSE CONTINUED READING FROM THE
*                  PROCEDURE LIBRARY.
*                 .IF EOF HAS BEEN REACHED ONLY ON THE PROCEDURE, ALL
*                  PROCEDURE FLAGS ARE CLEARED TO CAUSE CONTINUED
*                  READING FROM THE JCL DATA SET.
*
*   NOTES
*      CHARACTER CODE DEPENDENCIES= NONE
*      DEPENDENCIES= NONE
*      RESTRICTIONS= NONE
*      REGISTER CONVENTIONS=
*                            R9- STATEMENT POINTER
*                            RA- STATEMENT PARAMETER LIST POINTER
*                            RB- BASE REGISTER
*                            RC- CWA POINTER
*                            RD- SAVE AREA POINTER
*      PATCH-LABEL= PATCH
*
*   MODULE TYPE= BAL
*      PROCESSOR= ASSEMBLER
*      ATTRIBUTES= NCAL,LET,LIST,XREF,REFR,RENT
*
*   ENTRY POINT= IEFVHL
*      PURPOSE= END OF FILE PROCESSING
*      LINKAGE= BRANCH ON REGISTER
*      INPUT DATA= CONVERTER WORK AREA
*                  RC- POINTS TO CWA
*      REGISTERS SAVED= NONE
*      REGISTER CONTENTS DURING PROCESSING= SEE REGISTER CONVENTIONS
*      REGISTERS RESTORED= NONE
*
*   EXIT - NORMAL= BRANCH TO IEFVHA
*      CONDITIONS= END OF FILE ON JCL DATA SET, A PROCEDURE LIBRARY IS
*                  STILL BEING READ, AND PROC BUFFER IS TO BE PRIMED
*      OUTPUT DATA= UPDATED FLAGS IN CWA
*      RETURN CODES= NONE
*
*   EXIT - NORMAL= BRANCH TO IEFVHEB
*      CONDITIONS= END OF FILE ON JCL DATA SET, A PROCEDURE LIBRARY IS
*                  STILL BEING READ, AND PROC BUFFER ALREADY CONTAINS A
*                  JCL STATEMENT TO BE PROCESSED
*      OUTPUT DATA= UPDATED FLAGS IN CWA
*                   R9- POINTS TO PROCEDURE INPUT BUFFER
*      RETURN CODES= NONE
*
*   EXIT - NORMAL= BRANCH TO IEFVHCB
*      CONDITIONS= END OF FILE ON PROCEDURE AND NO END OF FILE ON
*                  JCL DATA SET
*      OUTPUT DATA= UPDATED FLAGS IN CWA
*                   R9- POINTS TO JCL DATA SET INPUT BUFFER
*      RETURN CODES= NONE
*
*   EXIT - NORMAL= BRANCH TO IEFVHF
*      CONDITIONS= END OF FILE ON BOTH PROCEDURE AND JCL DATA SET
*      OUTPUT DATA= END OF CONVERSION FLAG SET
*      RETURN CODES= NONE
*
*   EXIT - ERROR= NONE
*
*   EXTERNAL REFERENCES=
*      ROUTINES= NONE
*      DATA AREAS= CONVERTER WORK AREA
*      CONTROL BLOCKS= NONE
*      TABLES= NONE
*      MACROS= IEFVMCWA
*      ENQUEUE RESOURCES= NONE
*      CHANGE LEVEL= OZ03636                                   @ZA03636
*
*   MESSAGES= NONE
*
***********************************************************************
         EJECT
         IEFCOMWA                                                Y02668
         IEFCVRWA                                                Y02668
         EJECT
IEFVHL   CSECT
*                                                                A26315
*                   R9=STMT PTR
*                   RA=STMT PARM PTR
*                   RD=PTR TO CURRENT REG SAVE AREA
         BALR  RB,0
         USING *,RB          *
*****************************************************************Y02028
* MODULE TRACE CODE - FOR TESTING.                               Y02028
         L     RF,TRACEV           LOAD TRACE RTNE ADDR.         Y02028
         BALR  RE,RF               ENTER MOD ID IN TRACE RECORD. Y02028
TRACEV   DC    V(TRACE)            TRACE RTNE ADDR.              Y02028
         DC    C'VHL '             MOD ID USED BY TRACE.         Y02028
* TRACE RETURNS HERE.                                            Y02028
*****************************************************************Y02028
         B     F1
         MODID BR=NO                                             Y01886
         DC    C'SPLT'             FUNCTION ID AND               Y02028
         DC    C'04'     LEVEL NUMBER( STMT # )                  Y02668
         DC    C'REL3 PTF'        FUNCTION ID AND              @ZA03636
         DC    C'01'         LEVEL (BAD PROC CLEANUP)          @ZA03636
F1       DS    0H
         TM    SWC,IOERR                I/O  ERROR               A26315
         BO    F23                      BR  YES                  A26315
         TM    SWE,GPI                  PROC STMT
         BO    F2                       BR YES
         OI    SWC,NRCV                 NULL STMT RCVD
         NI    SWB,255-FRCV             TURN OFF 1ST STMT RCVD
         TM    SWE,PROC                 PROCEDURE ON
         BO    F14
         TM    SWA,JTOP                 JOB TO PROCESS
         BZ    F16                      BR NO
         TM    SWA,EOFR                 RDR EOF
         BZ    F22                      BR NO
         B     F23
F14      DS    0H
         OI    SWE,GPI                  PROC STMT ON
         NI    SWE,255-POVRD            DD ORIDE OFF
         TM    SWE,PRCV                 PRIME PROC BUFF
         BO    F15
         LA    RA,PSTMT                 PROC PARM
         L     R9,0(0,RA)               PROC STMT
         L     RF,IEFVHEBV         EXIT                          Y02028
         BR    RF                  TO PRE-SCAN ROUTINE.          Y02028
F15      DS    0H
         NI    SWE,255-PRCV             PRIME OFF
         OI    SWD,SYMPRC            **SET 1ST ACCESS FLAG
F16      DS    0H
         L     RF,IEFVHAV
         BR    RF                  EXIT TO GET STMT
F2       DS    0H
         OI    SWE,PREF                 PROC EOF
         TM    SWA,EOFR                 RDR EOF
         BO    F23                      BR YES
         TM    SWC,NRCV                 RDR NULL RCVD
         BO    F22                      BR YES
         L     RF,IEFVHCBV         SET EXIT TO VERB ID
         B     F231
F22      DS    0H
         L     R9,RSTMT                 PTR TO RDR BUFFER
         OI    AOSW4,CWATERM       INDICATE CONVERSION COMPLETE. Y02668
         OI    AOSW1,AOPRTSW       TELL VHEB TO PRINT            Y02668
         L     RE,IEFVHFV          SET VHEB EXIT TO VHF          Y02668
         L     RF,IEFVHEBV         EXIT TO VHEB WHO IN TURN      Y02668
         BR    RF                  CALLS VGM WHO EXITS TO VHF    Y02668
F23      DS    0H
         OI    AOSW4,CWATERM       INDICATE CONVERSION COMPLETE. Y02668
         L     RF,IEFVHFV          SET EXIT TO CONTROL ROUTINE.  Y02028
F231     DS    0H
         TM    SWE,GPI                 PROC BEING HANDLED      @ZA03636
         BZ    NOTPROC                 NO                      @ZA03636
         NI    SWI,255-BLKPRC          RESET BLOCKING SWITCH   @ZA03636
         MVC   PSTMT(4),IWAINTS4       RESTORE BEGIN OF BUFFER @ZA03636
NOTPROC  EQU   *                       NOT DOING PROC          @ZA03636
         LA    RA,RSTMT                 STMT PARM
         L     R9,0(0,RA)               RDR STMT
         NI    SWE,255-PROC-GPI         PROC & GPI OFF
         BR    RF                  EXIT TO VERB ID OR JOB CONTROL C
IEFVHAV  DC    V(IEFVHA)                GET
IEFVHCBV DC    V(IEFVHCB)               VERB ID B
IEFVHEBV DC    V(IEFVHEB)          PRE-SCAN ROUTINE.             Y02028
IEFVHFV  DC    V(IEFVHF)           CONTROL ROUTINE.              Y02028
PATCH    DC    25F'0'              **********PATCH SPACE*********Y02028
         END
