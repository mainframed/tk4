*/*
**********************************************************************
*
*   MODULE NAME= IEFVHC
*
*   DESCRIPTIVE NAME= COMMENT OR CONTINUATION VALIDATION
*
*   COPYRIGHT= N/A
*
*   STATUS= OS/VS2 RELEASE 3                                   @ZA12458
*   FUNCTION= IEFVHC CHECKS INPUT FOR THE VALID PRESENCE OR NON-
*             PRESENCE OF A CONTINUATION OR COMMENT.
*      OPERATION= WHEN IEFVHC RECEIVES CONTROL REGISTER 9 POINTS TO THE
*                 BEGINNING OF THE STATEMENT.  IF A CONTINUATION IS
*                 EXPECTED AND REVEIVED EXIT TO IEFVHEB.
*                 IF A CONTINUATION IS NOT EXPECTED EXIT TO
*                 IEFVHCB.  IF A CONTINUATION IS EXPECTED AND NOT
*                 RECEIVED, EXIT TO IEFVHEB ( IEFVGM IS USED
*                 AS A SUBROUTINE TO PRINT ERROR MESSAGE
*                 PRIOR TO EXIT ). IF THE STATEMENT IS A COMMENT,
*                 LOAD IEFVHEB VCON AND BRANCH REGISTER TO PRINT IT,
*                 THEN EXIT TO IEFVHA.  (IF COMMENT CHANGE // TO **)
*                            (IF CATALOG PROCEEDURE CHANGE // TO XX)
*                           (IF INSTREAM PROCEEDURE CHANGE // TO ++)
*
*   NOTES
*      CHARACTER CODE DEPENDENCIES= EBCDIC
*      DEPENDENCIES= NONE
*      RESTRICTIONS= NONE
*      REGISTER CONVENTIONS= ACTUAL REGISTER NUMBERS ARE USED
*      PATCH-LABEL= PATCH
*
*   MODULE TYPE= PROCEDURE
*      PROCESSOR= ASSEMBLER
*      ATTRIBUTES= NCAL,LET,LIST,XREF,RENT,REFR
*
*   ENTRY POINT= IEFVHC
*      PURPOSE= DETERMINE PROPER EXIT, DEPENDING ON THE
*               VALIDITY OF THE CONTINUATION.
*      LINKAGE= LOAD VCON BRANCH REGISTERAC
*      INPUT DATA= REG 9 POINTS TO THE STATEMENT
*                  REG 10 POINTS TO THE STATEMENT PARAMETER LIST
*                  REG 12 POINTS TO THE CWA
*                  SWITCHES TESTED IN THE CWA
*                      (SWA SWB SWE SWZ)
*      REGISTERS SAVED= NONE
*      REGISTER CONTENTS DURING PROCESSING=
*      REGISTERS RESTORED= NONE
*
*   EXIT - NORMAL= LOAD IEFVHEB VCON, BRANCH REGISTER
*      CONDITIONS= CONTINUATION EXPECTED AND RECEIVED
*      OUTPUT DATA= NONE
*      RETURN CODES= NONE
*
*   EXIT - NORMAL= LOAD IEFVHA VCON, BRANCH REGISTER
*      CONDITIONS= COMMENT RECEIVED
*      OUTPUT DATA= NONE
*      RETURN CODES= NONE
*
*   EXIT - NORMAL= LOAD VCON IEFVHCB, BRANCH REGISTER
*      CONDITIONS= CONTINUATION NOT EXPECTED
*      OUTPUT DATA= NONE
*      RETURN CODES= NONE
*
*   EXIT - ERROR= LOAD VCON IEFVHEB, BRANCH REGISTER
*      CONDITIONS= CONTINUATION EXPECTED AND NOT RECIEVED
*      OUTPUT DATA= REGISTER 2 POINTS TO THE MESSAGE
*      RETURN CODES= NONE
*
*   EXTERNAL REFERENCES=
*      ROUTINES= IEFVGM (TO PUT OUT ERROR MESSAGE)
*                IEFVHEB (STATEMENT NUMBER PROCESSING)
*      DATA AREAS= IWA
*      CONTROL BLOCKS= NONE
*      TABLE= NONE
*      MACROS= IEFVMIWA
*      ENQUEUE RESOURCES= NONE
*      CHANGE LEVEL= ZA02374, ZA12458                          @ZA12458
*
*   MESSAGES= (EXPECTED CONTINUATION NOT RECEIVED)
*             PASSED TO IEFVGM TO BE PRINTED
*
**********************************************************************
**/
         EJECT
         IEFCOMWA                                                Y02668
         IEFCVRWA                                                Y02668
         EJECT
         IEFVMCWA
         EJECT
JCTD     DSECT
         IEFAJCTB
         IEFTXTFT                                                Y02668
         EJECT
IEFVHC   CSECT
* A 015960-015970,016520-016540                                @ZA02374
* A STMTS AFTER LABEL B1725                                    @ZA12458
         BALR  RB,0
         USING *,RB
*****************************************************************Y02668
* MODULE TRACE CODE - FOR TESTING.                               Y02668
         L     RF,TRACEV           LOAD TRACE RTNE ADDR.         Y02668
         BALR  RE,RF               ENTER MOD ID IN TRACE RECORD. Y02668
TRACEV   DC    V(TRACE)            TRACE RTNE ADDR.              Y02668
         DC    C'VHC '             MOD ID USED BY TRACE.         Y02668
* TRACE RETURNS HERE.                                            Y02668
*****************************************************************Y02668
         B     B1
         MODID BR=NO                                             Y01886
         DC    C'REL3'              FUNCTION ID AND
*        DC    C'02' LEV # (CONT. EXPECTED NOT RECD. MSG).     @ZA02374
         DC    C'03'          LEV # (CONT EXP NOT RECEIVED
*                             FOR OPERAND FIELD)               @ZA12458
B1       DS    0H
         DROP  RA           REMOVE CWA ADDRESSABILITY          @ZA00894
         TM    SWE,GPI                  PROC STMT
         BZ    B14                      BR NO
         LA    RA,PSTMT                 PROC STMT PARM LIST
         B     B142
B14      DS    0H
         LA    RA,RSTMT                 RDR STMT PARM LIST
B142     DS    0H
         L     R9,0(0,RA)               PTR TO STMT
         CLC   0(3,R9),COMMENT     IS THIS A COMMENT CARD (//*)?YM03493
         BNE   B146               BRANCH NO                        AABA
         MVI   0(R9),C'*'         MARK COMMENT CARD                AABA
         MVI   1(R9),C'*'         *                                AABA
         OI    AOSW1,AOPRTSW       TELL VHEB TO PRINT STMT       Y02668
         L     RF,IEFVHEBV         EXIT ADDR                     Y02668
         L     RE,IEFVHAV          RETURN ADDR AFTER PRINT       Y02668
         BR    RF                  EXIT TO PRINT STMT            Y02668
B146     DS    0H                                                  AABA
         TM    SWE,GPI            IS THIS STATEMENT IN A PROC      AABA
         BO    B180               BRANCH YES                       AABA
B148     DS    0H                                                  AABA
         TM    SWB,CXP                  CONT EXPECTED
         BO    B15
         L     RF,IEFVHCBV
         BR    RF             EXIT TO VERB ID
B15      DS    0H
         TM    SWE,GPI             IS THIS STATEMENT IN A PROC  YM03493
         BO    B152                YES - BYPASS (//) CHECK      YM03493
         CLC   0(2,R9),SLASH       IS THIS A JCL STATEMENT?     YM03493
*                                  (//) VALIDATION IS MADE ON   YM03493
*                                  EVERY CONTINUATION TO INSURE YM03493
*                                  JESS HAS NOT PASSED A DATA   YM03493
*                                  CARD TO INDICATE DATA WAS    YM03493
*                                  FOUND IN THE INPUT STREAM    YM03493
*                                  INSTEAD OF EXPECTED CONTINU. YM03493
         BE    B152                YES - GO TO VALIDATE CONT.   YM03493
         L     RE,IEFVHAV          NO - GET RETURN ADDR.        YM03493
         B     B1717               GO TO ISSUE ERROR MESSAGE    YM03493
*                                  AND FLUSH DATA CARD.         YM03493
B152     DS    0H                                               YM03493
         CLI   2(R9),C' '               COL 3 BLANK
         BNE   B1715               BRANCH NO                     Y02668
         CLC   3(12,R9),2(R9)           4 TO 15 BLANK
         BNE   B174               BRANCH NO                        AABA
         CLI   15(R9),C' '              COL 16 BLANK
         BE    B172                     BR YES
         TM    SWZ,ENDS           TEST END SCAN SW                 AABA
         BO    B173               BRANCH ON                        AABA
         TM    SWZ,CMT            IS COMMENT SW ON                 AABA
         BO    B173               YES, O'RIDING DD STMT            AABA
B154     DS    0H                                                  AABA
         MVI   LISTPTR(RA),15           SET REL LIST PTR IN PARM LIST
B16      DS    0H
         L     RF,IEFVHEBV
         BR    RF             EXIT TO JOB CONTROL, ENTRY B
B171     DS    0H                                                  AABA
         L     RE,IEFVHCBV         GET VHCB ADDR                 Y02668
         B     B1717               GO TO PUT MSG                 Y02668
B1715    DS    0H                                                Y02668
         L     RE,IEFVHEBV         GET IEFVHEB ADDR              Y02668
B1717    DS    0H                                                Y02668
         TM    SWB,CXPN     CONT EXP,NOT RECD ALREADY ON       @ZA02374
         BO    B1718        YES,MSG ALREADY PUT OUT            @ZA02374
         OI    SWB,CXPN           CONT EXP, NOT RECD               AABA
         LA    R8,WAJOBPFX         ADDR OF PSEUDO JOB PFX.       Y02668
         USING TEXT,R8             PREFIX ADDRESSABILITY.        Y02668
         OI    STRJINDC,JTXJOBFL   SET JOB FAILED BIT            Y02668
         DROP  R8                                                Y02668
         L     R2,CTRLWAP     GET WORK AREA ADDR               @ZA00894
         USING CWA,R2             CWA ADDRESSABILITY           @ZA00894
         MVI   MSGKEY,0           INDICATE NO SECONDARY MSG    @ZA00894
         DROP  R2                 REMOVE CWA ADDRESSABILITY    @ZA00894
         LA    R2,MSG15           CONT MISSING MSG                 AABA
         L     RF,IEFVGMV         PUT MSG - CONT MISSING AND       AABA
         BR    RF                 EXIT TO JOB CONTROL              AABA
B1718    DS    0H                                             @ZA02374
         BR    RE           EXIT TO IEFVHEB,IEFVHCB,OR IEFVHA  @ZA02374
*                                                               YM03493
*************************************************************** YM03493
*  CONTROL WILL BE RETURNED FROM VGM VIA REG14 WHICH HAS BEEN   YM03493
*  LOADED WITH AN  APPROPRIATE MODULE ADDRESS PRIOR TO ENTRY    YM03493
*  OF THIS SEGMENT OF CODE.                                     YM03493
*************************************************************** YM03493
*                                                               YM03493
B172     DS    0H
         CLC   16(55,R9),15(R9)   17 TO 71 BLANK                   AABA
         BE    B182               BRANCH YES                       AABA
B1725    DS    0H                                                  AABA
         TM    SWB,CXPOP           TEST CONT EXP FOR OPERANDS  @ZA12458
         BNO   B1726               NO, PROCEED                 @ZA12458
         MVI   2(R9),C'*'          MARK COMMENT CARD           @ZA12458
         B     B1715               HANDLE CONT NOT RECEIVED    @ZA12458
B1726    DS    0H                                              @ZA12458
         TM    SWZ,ENDS           TEST END SCAN SW                 AABA
         BO    B173               BRANCH IF ON                     AABA
         TM    SWE,POVRD          IS THIS AN O'RIDING DD STMT      AABA
         BO    B1727              YES, BRANCH                      AABA
         OI    SWB,CXPC                 CONT EXP & CANCELLED
         B    B173                                                 AABA
B1727    DS    0H                                                  AABA
         OI    SWZ,CMT            TURN ON COMMENT SW               AABA
B173     DS    0H                                                  AABA
         MVI   2(R9),C'*'         MARK COMMENT CARD                AABA
         B     B154                                                AABA
B174     DS    0H                                                  AABA
         TM    SWZ,ENDS           TEST END SCAN SW                 AABA
         BO    B173               TRANSFER END SCAN SW ON          AABA
         TM    SWZ,CMT            IS COMMENT SW ON                 AABA
         BO    B173               YES, O'RIDING DD STMT            AABA
         LA    R4,3(0,R9)         COMPUTE REL LIST PTR             AABA
B176     DS    0H                                                  AABA
         CLI   0(R4),C' '         *SEARCH FOR 1ST NON-BLANK        AABA
         BNE   B178               *BRANCH IF NON-BLANK FOUND       AABA
         LA    R4,1(0,R4)         *INCREMENT POINTER               AABA
         B     B176               *RETURN TO SEARCH                AABA
B178     DS    0H                                                  AABA
         SR    R4,R9              *COMPUTE DISPLACEMENT            AABA
         STC   R4,LISTPTR(RA)     *STORE IN PARM LIST              AABA
         B     B16                                                 AABA
         SPACE
B179     DS    0H                                                 O106
         MVC   DISPLZER(COMP2,R9),PLUS INDICATE IN-LINE PROC      O106
         B     B148                    CHECK FOR CONTINUATION     O206
         SPACE
B180     DS    0H                                                 M4763
         TM    IWAPARM,INSTPROC        IN-STREAM PROC BEING PROCE M4763
         BO    B179                    YES, CHANGE TO ++          M4763
         MVI   0(R9),C'X'              MARK PROC STMT             M4763
         MVI   1(R9),C'X'              MARK PROC STMT             M4763
         B     B148                                               M4763
         SPACE
B182     DS    0H                                                  AABA
         TM    SWE,PREF           PROC EOF                         AABA
         BO    B171               BRANCH YES                       AABA
         TM    SWA,EOFR           RDR EOF                          AABA
         BO    B171               BRANCH YES                       AABA
         B     B1725                                               AABA
IEFVGMV  DC    V(IEFVGM)                MSG ROUTINE
IEFVHCBV DC    V(IEFVHCB)               VERB ID
IEFVHEBV DC    V(IEFVHEB)               JOB CONTROL
IEFVHAV  DC    V(IEFVHA)          GET STATEMENT                    AABA
INSTPROC EQU   X'80'                   IN-STREAM PROC BEING PROCESM4763
PLUS     DC    C'++'                   IN-LINE PROCEDURE          O106
SLASH    DC    C'//'               JCL STATEMENT                YM03493
COMMENT  DC    C'//*'              COMMENT CARD                 YM03493
DISPLONE EQU   1                       DISPLACEMENT               O106
MV3      EQU   3                                                   O106
COMP2    EQU   2                       LENGTH OF MOVE AND COMPARE O106
DISPLZER EQU   0                       DISPLACEMENT               O106
MSG15    EQU   21
PATCH    DC    25F'0'              **********PATCH SPACE*********Y02668
         END
