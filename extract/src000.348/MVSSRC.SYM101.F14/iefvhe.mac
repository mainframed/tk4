***********************************************************************
*
*MODULE NAME= IEFVHE
*  CSECT-NAME= IEFVHE
*
*DESCRIPTIVE NAME= INTERPRETER GET AND  ROUTE ROUTINE
*
*COPYRIGHT= N/A
*
*STATUS= OS/VS2 037 PTF                                        @ZA27750
*
*FUNCTION= THIS ROUTINE  GETS TEXT STRINGS
*          FROM THE INTERNAL TEXT DATA SET AND
*          ROUTES THE STRINGS TO THE APPROPRIATE PROCESSOR.
*          IT ALSO RECOGNIZES WHEN AN SCT IS TO BE PUT TO SWA
*          AND END OF JOB(JCT TO BE PUT TO SWA).
*
*
*  OPERATION=  .THE RPL IN THE IWA IS  INITIALIZED TO ISSUE A
*               GET FROM THE INTERNAL TEXT DATA SET
*              .AN INTERNAL TEXT STRING IS GOTTEN
*              .A  CHECK IS MADE FOR DATA SET EOF
*               (END OF JOB)- IF YES, EXIT TO IEFVHEC.
*              .IF NOT END OF JOB,  THE STATEMENT IS IDENTIFIED
*               AND ROUTED TO THE APPROPRIATE PROCESSOR
*               PRIOR TO ROUTINE, STMT ORIENTED INFORMATION IS   Y02668
*               PROPAGATED FROM THE TEXT STRING PREFIX TO THE    Y02668
*               IWA. THIS ALLOWS THE PROCESSORS TO ACCESS IT MOREY02668
*               EASILY.                                          Y02668
*               TO SEE IF AN SCT IS TO BE PUT TO SWA, IF YES EXIT
*               IS MADE TO IEFVHH.
*
*
*NOTES
*  DEPENDENCIES= NONE
*  CHARACTER-CODE DEPENDENCIES= EBCDIC
*  RESTRICTIONS= NONE
*  REGISTER-CONVENTIONS=  REG 11 - MODULE BASE
*                         REG 12 - IWA POINTER
*                         REG 13 - REG SAVE AREA PTR
*  PATCH-LABEL= PATCH
*
*
*MODULE TYPE= BAL CSECT
*  PROCESSOR= ASSEMBLER
*  ATTRIBUTES= REFRESHABLE,PRIVILEGED,REENTRANT, KEY 0, PAGEABLE LPA
*
*ENTRY POINT= IEFVHE FROM IEFNB903 (INTERPRETER INITIALIZATION)
*  PURPOSE= TO GET A TEXT STRING AND ROUTE IT
*  LINKAGE= BR
*  INPUT= R12- PTR TO IWA, R13 - PTR TO REG SAVE AREA
*  REGISTERS SAVED= NONE
*  REGISTER USAGE=
*               R0  - WORK REG
*               R1  - WORK REG
*               R2  - RPL BASE
*               R3  - UNUSED
*               R4  - UNUSED
*               R5  - UNUSED
*               R6  - NEL BASE
*               R7  - TEXT PREFIX BASE
*               R8  - UNUSED
*               R9  - UNUSED
*               R10 - LOCAL WORK AREA PTR
*               R11 - MODULE BASE REG
*               R12 - IWA PTR
*               R13 - PTR TO REG SAVE AREA
*               R14 - LINKAGE AND WORK REG
*               R15 - LINKAGE AND WORK REG
*  REGISTERS RESTORED= NONE
*
*
*EXIT - NORMAL= IEFVJA
*  CONDITIONS= JOB STMT TEXT STRING ENCOUNTERED
*  OUTPUT= REG 12 - PTR TO IWA WHICH CONTAINS PTR TO TXT STR
*  RETURN CODES= NONE
*
*
*EXIT - NORMAL= IEFVEA
*  CONDITIONS= EXEC STMT TEXT STRING ENCOUNTERED
*  OUTPUT= REG 12 - PTR TO IWA WHICH CONTAINS PTR TO TXT STR
*  RETURN CODES= NONE
*
*
*EXIT - NORMAL= IEFVDA
*  CONDITIONS= DD STMT TEXT STRING ENCOUNTERED
*  OUTPUT= REG 12 - PTR TO IWA WHICH CONTAINS PTR TO TXT STR
*  RETURN CODES= NONE
*
*
*EXIT - NORMAL= IEFVHH                                          YM02714
*  CONDITIONS= JOB END  FOUND (EOF ON TEXT DATA SET)
*  OUTPUT= REG 12 - PTR TO IWA
*  RETURN CODES= NONE
*
*
*EXIT - NORMAL= IEFVHH
*  CONDITIONS= SCT TO PUT TO SWA
*  OUTPUT= REG 12 - PTR TO IWA
*  RETURN CODES= NONE
*
*EXIT - ERROR= IEFVHN  VIA BR 14
*  CONDITIONS=  I/O ERROR
*  OUTPUT= REG 12 - PTR TO IWA WHICH CONTAINS I/O ERROR FLAG
*  RETURN CODES= NONE
*
*
*EXTERNAL REFERENCES=
*  ROUTINES= IEFVHR (TO ISSUE WTO)
*  DATA AREAS= LWA, IWA
*  CONTROL BLOCKS=
*              RPL, ACB
*
*      TABLES= INTERNAL KEYS(IEFVKEYS)
*
*      MACROS= GET, IEFZB900
*         IEFVMIWA,IEFRPL,IEFACB,
*      SERIALIZATION= NONE
*.
*
*CHANGE LEVEL = Z40MPTH,Z40RPSM,OZ10512,OZ27750                @ZA27750
*CHANGE ACTIVITY= Y02668
*
*MESSAGES = IEF6XXI I/O ERROR INTERPRETING JOB
*
***********************************************************************
         EJECT
         IEFCOMWA
         IEFVMIWA
         SPACE 5
JCTD     DSECT
         IEFAJCTB
         SPACE 5
NEL      DSECT
         IEFNEL SUBCOM=I,EXITS=(TXT,RTN,QEP,SMF,IAM,FAM)
         SPACE 5
         IEFASCTB                                                Y02668
         EJECT                                                   Y02668
         IEFVMEWA                                                Y02668
         SPACE 5
         IEFVMJWA  NA
         SPACE 5
         IEFVMDWA  NA
         SPACE 5
         IEFQMNGR
         SPACE 5
         IEFJMR
         IEFVKEYS
         EJECT
         EJECT
         IEFTXTFT
         EJECT
         IFGRPL                                                  Y02668
         EJECT                                                   Y02668
         IDARMRCD                                                Y02668
         EJECT                                                   Y02668
IEFVHE   CSECT
* CODE MOVED FROM AFTER LABEL VHE0043 TO NEAR LABEL C1         @ZA10512
*****************************************************************
*                                                               *
*  READING AND ROUTING FUNCTIONS                                *
*                                                               *
*****************************************************************
         SPACE 2
         BALR  RB,0                MODULE BASE REG               Y02668
         USING *,RB                MODULE ADDRESSABILITY         Y02668
*****************************************************************Y02668
* MODULE TRACE CODE - FOR TESTING.                               Y02668
         L     RF,TRACEV           LOAD TRACE RTNE ADDR.         Y02668
         BALR  RE,RF               ENTER MOD ID IN TRACE RECORD. Y02668
TRACEV   DC    V(TRACE)            TRACE RTNE ADDR.              Y02668
         DC    C'VHE '             MOD ID USED BY TRACE.         Y02668
* TRACE RETURNS HERE.                                            Y02668
*****************************************************************Y02668
         B     C1
         MODID BR=NO                                             Y01886
         DC    C'037 PTF'           FUNCTION ID                @ZA10512
*        DC    C'03'   LEV # (SAVE CORRECT STATEMENT NUMBER)   @ZA10512
         DC    C'04'  LEV # (MERGE SU4,10,16 CODE TO 037 PTF)  @ZA27750
C1       DS    0H
         L     R7,TEXTBUFP         GET TEXT BUF ADDRESS         YM00397
         USING TEXT,R7             GET TXT PFX ADDRESSABILITY   YM00397
         TM    AOSW2,AONOREAD      HAS STMT IN TEXTBUFFER BEEN   Y02668
*                                  PROCESSED YET                 Y02668
         BZ    VHE0010             YES, SET UP FOR 'GET'         Y02668
         NI    AOSW2,255-AONOREAD  NO, CLEAR SWITCH AND          Y02668
         TM    STREINDC,ETXPRCV    STMT INVOKE A PROC?         @ZA10512
         BZ    VHE0050             NO, BYPASS 1ST PASS PROC.   @ZA10512
         TM    STRINDCS,PROCSTR    REALLY A PROC STMT(DUM EXEC)@ZA10512
         BO    VHE0050             BR YES, WANT REAL EXEC ONLY @ZA10512
         MVC   IWAEXPNO(2),WASTMTNO SAVE STMT NBR OF REAL EXEC @ZA10512
*                                  FOR PROC O'RIDE ERR MSG.    @ZA10512
         B     VHE0050             BYPASS FIRST PASS PROCESSING YM00382
         EJECT
*****************************************************************Y02668
*                                                               *Y02668
*  GET FROM THE INTERNAL TEXT DATA SET - EODAD & SYNAD RTNS     *Y02668
*                                                               *Y02668
*****************************************************************Y02668
         SPACE 2                                                 Y02668
VHE0010  DS    0H                                                Y02668
         L     R6,WANELPTR         GET BASE ADDRESS OF NEL       Y02668
         USING NEL,R6              SET BASE                      Y02668
         LA    R2,RPLADDR          GET BASE ADDRESS OF RPL       Y02668
         USING IFGRPL,R2           SET BASE                     Y02668
         MVI   RPLREQ,RPLGET       OPERATION TO BE A GET         Y02668
         MVC   RPLDACB,NELTXTCB    MOVE IN ACB POINTER           Y02668
         MVC   RPLAREA,TEXTBUFP    POINTER TO RECORD             Y02668
         XC    RPLARG(4),RPLARG    ZERO FIELD                    Y02668
         MVI   RPLOPT1,RPLSEQ      INDICATE TXT D.S. IS SEQENTIALY02668
         XC    RPLRLEN(WORDLEN),RPLRLEN RECORD LENGTH NOT NEEDED Y02668
*                                  FOR GET                       Y02668
         LH    R0,TXTBUFLT         GET LENGTH OF TEXT BUFFER     Y02668
*                                  INTO WHICH RECORD IS TO BE    Y02668
*                                  READ                          Y02668
         ST    R0,RPLBUFL          SAVE IN RPL BUF LENGTH        Y02668
         XC    RPLDDDD(4),RPLDDDD  ZERO FIELD                    Y02668
         XC    RPLERRCD(1),RPLERRCD ZERO UNUSED FIELD            Y02668
         ST    RD,IWASACHN         SAVE SAVE AREA POINTER        Y02668
         LA    RD,IWAIOSA          SAVE AREA FOR ACCESS METHOD   Y02668
         LR    R1,R2               POINT TO RPL                  Y02668
         DROP  R6                                                Y02668
         GET   RPL=(1)
         L     RD,IWASACHN         RESTORE SAVE AREA POINTER     Y02668
         LTR   RF,RF               ERROR ATTEMPTING GET ?        Y02668
         BZ    VHE0020             NO. BRANCH TO CONTINUE        Y02668
         CLI   RPLRTNCD,RPLLOGER   IS THIS A LOGICAL ERROR ?     Y02668
         BNE   VHE0015             NO. GO TO ERROR EXIT          Y02668
         CLI   RPLERRCD,RPLEODER   IS END OF DATA DETECTED ?     Y02668
         BNE   VHE0015             NO. GO TO ERROR EXIT          Y02668
         NI    SWE,255-PROC        TEXT EOF - PROC ENDED        YM00382
         OI    SWA,JCTTQ           INDICATE END OF JOB-SO PUT    Y02668
*                                  THE JCT TO SWA                Y02668
         L     RF,IEFVHHV          GET PTR TO RTN THAT PUTS      Y02668
*                                  CONTROL BLOCKS TO SWA         Y02668
         BR    RF                  GO TO PROCESS EOF             Y02668
VHE0015  DS    0H                                                Y02668
         LTR   RF,RF               ERROR IN ATTEMPTING GET?      Y02668
         BZ    VHE0020             NO ERROR, 'GET' COMPLETE     YM01588
         LA    R5,4                I/O ERROR MSG CODE(FOR WTO)   Y02668
         L     RF,IEFVHRV          GET WTO RTN ADDR              Y02668
         BALR  RE,RF               WRITE I/O ERROR MSG TO CONSOLEY02668
         LA    R2,20               I/O ERROR MSG CODE(SYS OUTPUT)Y02668
         L     RF,IEFVGMV          GET MSG RTN ADDR              Y02668
         BALR  RE,RF               WRITE MSG TO SYS MSG DATA SET Y02668
         MVC   IWARET(2),PGMERROR  SET RETURN CODE FOR CALLER    Y02668
*                                  OF INTERPRETER                Y02668
         L     RF,TERMRTN          GET INTERPRETER TERM RTN ADDR Y02668
         BR    RF                  GO TO TERMINATE THE           Y02668
*                                  INTERPRETER                   Y02668
         EJECT
*****************************************************************Y02668
*                                                               *Y02668
*  DD STMT: IDENTIFY, COPY PREFIX INFO INTO IWA, AND ROUTE      *Y02668
*           TO DD PROCESSOR                                     *Y02668
*                                                               *Y02668
*****************************************************************Y02668
         SPACE 2                                                 Y02668
*                                                               YM00371
*  UPDATE STATEMENT # FOR ALL INPUT STATEMENTS.                 YM00371
*                                                               YM00371
VHE0020  DS    0H                                                Y02668
         TM    STRINDCS,EXECSTR    EXEC STMT?                   YM00371
         BZ    VHE0021             NO, CONTINUE NORMALLY.       YM00371
         TM    STREINDC,ETXCPFLG   C/R EXEC? (INTERNAL STMT).   YM00371
         BO    VHE0025             YES, INTERNAL STMT, DO NOT   YM00371*
                                   UPDATE STMT #.               YM00371
VHE0021  DS    0H                                               YM00371
         LH    RF,WASTMTNO         GET STMT NO OF LAST           Y02668
*                                  TEXT STRING PROCESSED         Y02668
         LA    RF,NEXT(RF)         INCREMENT STMT NO, BY ONE     Y02668
         STH   RF,WASTMTNO         STORE STMT NO. OF TEXT STRING Y02668
*                                  JUST READ-USED BY MSG RTN TO  Y02668
*                                  CORRELATE MSGS WITH STMTS     Y02668
VHE0025  DS    0H                  START STMT IDENTIFICATION    YM01588
         TM    STRINDCS,DDSTR      IS THIS A DD STMT STRING      Y02668
         BZ    VHE0030             NO,CHECK FOR EXEC STMT        Y02668
         XC    SWY2(1),SWY2        CLEAR SWITCHES FOR THIS DD.   Y02668
         XC    SWI(1),SWI          CLEAR SWITCHES FOR THIS DD.   Y02668
         TM    STRDINDC,DTXDSNLT   DSN FOR THIS DD A LITERAL?    Y02668
         BZ    VHE00252            BRANCH NO.                    Y02668
         OI    SWI,DSNLIT          SET WORK AREA DSN LITERAL SW. Y02668
VHE00252 DS    0H                                                Y02668
         TM    STRDINDC,DTXDDNM    DDNAME= SPECIFIED ON STMT?    Y02668
         BZ    VHE00255            BRANCH NO.                    Y02668
         OI    SWI,IWADDNM         YES, SET DDNAME SW IN IWA.    Y02668
VHE00255 DS    0H                                                Y02668
         TM    STRDINDC,DTXDYNAM   DYNAM SPECIFIED ON STMT?      Y02668
         BZ    VHE0027             BRANCH NO.                    Y02668
         OI    SWY2,DYNAMSW        SET DYNAM SW FOR VDA.         Y02668
VHE0027  DS    0H                                                Y02668
         TM    STRDINDC,DTXDUMMY   DUMMY SPECIFIED ON STMT?     YM02714
         BZ    VHE00272            BRANCH NO.                   YM02714
         OI    SWY2,DUMMYSW        SET DUMMY SW FOR VDA.        YM02714
VHE00272 DS    0H                                               YM02714
         L     RF,IEFVDAV          GET PTR TO DD STMT PROCESSOR  Y02668
         BR    RF                  EXIT TO PROCESS DD STRING     Y02668
         EJECT                                                   Y02668
*****************************************************************Y02668
*                                                               *Y02668
*  EXEC & PROC STMTS: IDENTIFY, COPY PREFIX INFO INTO IWA, AND  *Y02668
*                     ROUTE TO EXEC PROCESSOR                   *Y02668
*                                                               *Y02668
*****************************************************************Y02668
         SPACE 2                                                 Y02668
VHE0030  DS    0H                                                Y02668
         TM    STRINDCS,JOBSTR     IS THIS A JOB STRING ?       YM00382
         BO    VHE0060             YES  GO TO PROCCESS JOB      YM00382
VHE0040  DS    0H                                                Y02668
         OI    SWA,JHS             INDICATE JOB HAS A STEP.      Y02668
         TM    STREINDC,ETXPROC    EXEC FROM A PROCEDURE?        Y02668
         BZ    VHE0042             BRANCH NO.                    Y02668
         OI    SWE,PROC            SET PROC SWITCH FOR VEA.      Y02668
         B     VHE0043             CONTINUE PROCESSING.          Y02668
VHE0042  DS    0H                                                Y02668
         NI    SWE,255-PROC        CLEAR PROC SWITCH, STATEMENT  Y02668*
                                   NOT FROM PROC.                Y02668
VHE0043  DS    0H                                                Y02668
         TM    STREINDC,ETXPRCV    STMT INVOKE A PROC?           Y02668
         BZ    VHE0044             BRANCH NO.                    Y02668
         OI    SWE,PRCV            SET PROC INVOKED FLAG FOR VEA.Y02668
         TM    STRINDCS,PROCSTR    REALLY A PROC STMT(DUM EXEC)?YM02714
         BZ    VHE0044A            CONTINUE PROCESSING.        @ZA10512
VHE0044  DS    0H                                                Y02668
         NI    SWE,255-PRCV        CLEAR PROC INVOKED SWITCH.    Y02668
VHE0044A DS    0H                                                Y02668
         TM    STREINDC,ETXCPFLG   CHECKPT/RESTART EXEC?         Y02668
         BZ    VHE0045             BRANCH NO.                    Y02668
         OI    SWH,CPSYSFLG        SET C/R FLAG IN WORK AREA.    Y02668
         B     VHE0046             CONTINUE PROCESSING.          Y02668
VHE0045  DS    0H                                                Y02668
         NI    SWH,255-CPSYSFLG    CLEAR C/P FLAG .              Y02668
VHE0046  DS    0H                                                Y02668
         TM    STREINDC,ETXPRCV    STMT INVOKE A PROC?         @ZA10512
         BO    VHE0048             GO CHECK O'RIDE STEP NOT FOUND
*                                                              @ZA10512
         TM    SWA,SCTTQ           IS THERE AN SCT FOR A         Y02668
*                                  PREVIOUS STEP THAT SHOULD BE  Y02668
*                                  PUT TO SWA                    Y02668
         BZ    VHE0050             NO, JUST PROCESS STMT         Y02668
VHE0048  DS    0H                                              @ZA10512
         OI    AOSW2,AONOREAD      INDICATE AFTER SCT IS         Y02668
*                                  PUT TO SWA,THE EXEC IN TXTBUF Y02668
*                                  MUST BE PROCESSED BEFORE THE  Y02668
*                                  NEXT STMT IS READ             Y02668
         L     RF,IEFVHHV          YES, GET CONTROL BLK'PUT'RTN  Y02668
         BR    RF                  GO TO PUT SCT TO SWA          Y02668
VHE0050  DS    0H                                                Y02668
         MVC   WAEXCPFX(STREPFXL),0(R7) SAVE EXEC PREFIX.        Y02668
         L     RF,IEFVEAV          GET PTR TO EXEC STMT PROCESSORY02668
         BR    RF                  EXIT TO PROCESS EXEC STRING   Y02668
         EJECT                                                   Y02668
*****************************************************************Y02668
*                                                               *Y02668
*  JOB STMT: IDENTIFY, COPY PREFIX INFO INTO IWA, AND ROUTE    **Y02668
*            TO JOB PROCESSOR                                   *Y02668
*                                                               *Y02668
*****************************************************************Y02668
         SPACE 2
VHE0060  DS    0H                  ASSUME JOB TEXT STRING        Y02668
         TM    STRJINDC,JTXSYSCK   JOB HAVE SYSCHK DD?           Y02668
         BZ    VHE0064             BRANCH NO.                    Y02668
         LA    R8,JCT              ADDRESS OF JCT.               Y02668
         USING JCTD,R8             JCT ADDRESSABILITY.           Y02668
         OI    JCTRSW2,JCTSYSCK    SET SYSCHK DD RECEIVED FLAG.  Y02668
         DROP  R8                                                Y02668
VHE0064  DS    0H                                                Y02668
         TM    STRJINDC,JTXCPSTF   C/R STEP FLUSH BIT ON?        Y02668
         BZ    VHE0080             BRANCH NO.                    Y02668
         OI    SWH,CPSTPFL         SET WORK AREA STEP FLUSH BIT. Y02668
VHE0080  DS    0H                                                Y02668
         MVC   WAJBNAME(8),AOBLKNAM BLANK JOBNAME FIELD.         Y02668
         SR    R5,R5               CLEAR LENGTH REG.             Y02668
         IC    R5,STRJKEY+2        LOAD JOBNAME LENGTH.          Y02668
         LTR   R5,R5               LENGTH = 0?                  YM08106
         BZ    VHE0081             YES, BYPASS MOVE OF NAME.    YM08106
         BCTR  R5,0                DECREMENT FOR EXECUTED MOVE. YM04716
         EX    R5,AOMVJBNM         MOVE JOBNAME FROM TEXT TO WA. Y02668
VHE0081  DS    0H                                               YM08106
         MVC   WAJOBPFX(STRJPFXL),0(R7) SAVE JOB PREFIX.         Y02668
         MVC   DINBPLP1(1),STRJLABD MOVE BPL DEFAULT TO WORK     Y02668*
                                   AREA FOR IEFVDA.              Y02668
         TM    STRJINDC,JTXJOBFL   JOB FAILED BY CONVERTER?      Y02668
         BZ    VHE0082             BRANCH NO.                    Y02668
* JOB WAS FAILED IN CONVERTER. PROPAGATE JOB FAILED BIT TO JCT.  Y02668
         LA    R4,JCT              JCT ADDRESS.                  Y02668
         USING JCTD,R4             JCT ADDRESSABILITY.           Y02668
         OI    JCTJSTAT,INCMSTS    SET JOB FAILED BIT.           Y02668
         DROP  R4                                                Y02668
VHE0082  DS    0H                                                Y02668
         L     RF,IEFVJAV          GET PTR TO JOB STMT PROCESSOR Y02668
         BR    RF                  EXIT TO PROCESS JOB STRING    Y02668
         SPACE 1
AOMVJBNM MVC   WAJBNAME(0),STRJKEY+3 EXECUTED MOVE OF JOBNAME.   Y02668
         EJECT
*****************************************************************Y02668
*                                                               *Y02668
*                        EQUATES                                *Y02668
*                                                               *Y02668
*****************************************************************Y02668
         SPACE 2                                                 Y02668
LNGTHAWA EQU   ENDAWA-QLINKWA                                    Y02668
LNGJACT  EQU   JACT-JASW1                                        Y02668
LNGSIOT  EQU   SIOT-DWA                                          Y02668
LNGJFCB  EQU   JFCB-DWA                                          Y02668
LNGJFCBX EQU   JFCBX-DWA                                         Y02668
WORDLEN  EQU   4                                                 Y02668
QMPALEN  EQU   32                                                Y02668
NONE     EQU   0                                                 Y02668
JMRLEN   EQU   X'4C'                                             Y02668
TABLEN   EQU   176                                               Y02668
AOWBFLTH EQU   90                                                Y02668
DBLEWORD EQU   8                                                 Y02668
SIGNDIGT EQU   7                                                 Y02668
BYTE0    EQU   0                                                 Y02668
HEX00    EQU   0                                                 Y02668
NEXT     EQU   1                                                 Y02668
SIGN     EQU   X'F0'                                             Y02668
DATEDSPL EQU   3                                                 Y02668
DOT      EQU   C'.'                                              Y02668
T        EQU   C'T'                                              Y02668
R        EQU   C'R'                                              Y02668
A        EQU   C'A'                                              Y02668
BSIGN    EQU   X'0F'                                             Y02668
LEN3     EQU   3                                                 Y02668
LEN4     EQU   4                                                 Y02668
LEN5     EQU   5                                                 Y02668
LEN6     EQU   6                                                 Y02668
LEN8     EQU   8                                                 Y02668
RECNO    EQU   6                   RECORD NUMBER                 Y02668
SVALEN   EQU   3                   SWA VIRTUAL ADDR LENGTH       Y02668
DISPL20  EQU   20                                                Y02668
DISPL3   EQU   3                                                 Y02668
DISPL8   EQU   8                                                 Y02668
DISPL16  EQU   16                                                Y02668
         EJECT
*****************************************************************Y02668
*                                                               *Y02668
*              DECLARED STORAGE                                 *Y02668
*                                                               *
*****************************************************************Y02668
IEFVDAV  DC    V(IEFVDA)           DD STMT PROCESSOR             Y02668
IEFVEAV  DC    V(IEFVEA)           EXEC STMT PROCESSOR           Y02668
IEFVJAV  DC    V(IEFVJA)           JOB STMT PROCLESSOR           Y02668
IEFVHHV  DC    V(IEFVHH)           CONTROL BLOCK PUT RTN         Y02668
IEFVHRV  DC    V(IEFVHR)           WTO RTN                       Y02668
IEFVGMV  DC    V(IEFVGM)           MSG RTN                       Y02668
*                                                                Y02668
*        C O N S T A N T S                                       Y02668
*                                                                Y02668
AOBLKNAM DC    CL8' '              8 BLANKS.                     Y02668
PGMERROR DC    X'0024'             PGM ERROR CODE (36)           Y02668
TXTBUFLT DC    H'8184'             MAX TXT REC LTH, BUFLTH-HDR.  YM1545
PATCH1   DC    25F'0'              ZAP SPACE                     Y02668
         END   IEFVHE
