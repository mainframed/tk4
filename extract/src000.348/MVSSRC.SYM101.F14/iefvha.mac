***********************************************************************
*
*   MODULE NAME= IEFVHA
*
*   DESCRIPTIVE NAME= GET ROUTINE
*
*   COPYRIGHT= N/A
*
*   STATUS= OS/VS2 RELEASE 3                                   @ZA08442
*
*   FUNCTION= READS JCL STATEMENTS FROM THE JCL DATA SET, THE PROCEDURE
*             LIBRARY, AND INSTREAM PROCEDURES, AS REQUIRED; AND
*             PERFORMS END OF FILE PROCESSING FOR EACH OF THE ABOVE
*      OPERATION= 1. READS FROM THE JCL DATA SET OR A PROCEDURE ACCORD-
*                    ING TO READ FLAG SETTINGS.
*                 2. PERFORMS EOF PROCESSING FOR THE JCL DATA SET AS
*                    FOLLOWS: SETS SWITCH EOFR AND MOVES A NULL STATE-
*                    MENT INTO THE INPUT BUFFER AND TAKES A NORMAL
*                    EXIT.
*                 3. PERFORMS END OF FILE PROCESSING FOR A PROCEDURE
*                    AS FOLLOWS: CHECKS IF ANY SYMBOLIC PARAMETERS
*                    REMAIN UNUSED, AND FAILS THE JOB IF ANY DO. SETS
*                    SWITCH PREF AND CLEARS ALL PROC SWITCHES, MOVES
*                    A NULL STATEMENT INTO THE INPUT BUFFER AND EXITS.
*                 4. PROVIDES INSTREAM PROCEDURE SUPPORT AS FOLLOWS:
*                    REPLACES THE BSAM ROUTINE ADDRESS IN THE DCB WITH
*                    THE ADDRESS OF THE INSTREAM PROC GET ROUTINE
*                    (IEFVIND), WHEN AN INSTREAM PROCEDURE IS TO BE
*                    READ FROM SWA.
*
*   NOTES
*      CHARACTER CODE DEPENDENCIES= EBCDIC
*      DEPENDENCIES= NONE
*      RESTRICTIONS= NONE
*      REGISTER CONVENTIONS=
*                            R2- MESSAGE CODE FOR IEFVGM
*                            R9- STATEMENT POINTER
*                            RB- BASE REGISTER
*                            RC- CWA POINTER
*                            RD- SAVE AREA POINTER
*      PATCH-LABEL= PATCH
*
*   MODULE TYPE= BAL
*      PROCESSOR= ASSEMBLER
*      ATTRIBUTES= NCAL,LET,LIST,XREF,REFR,RENT
*
*   ENTRY POINT= IEFVHA
*      PURPOSE= READ JCL STATEMENTS
*      LINKAGE= BRANCH ON REGISTER
*      INPUT DATA= CONVERTER WORK AREA
*                  RC- POINTS TO CWA
*      REGISTERS SAVED= NONE
*      REGISTER CONTENTS DURING PROCESSING= SEE REGISTER CONVENTIONS
*      REGISTERS RESTORED= NONE
*
*   EXIT - NORMAL= BRANCH TO IEFVHC
*      CONDITIONS= UNCONDITIONALLY
*      OUTPUT DATA= JCL STATEMENT IN BUFFER
*                   R9- POINTS TO INPUT BUFFER
*      RETURN CODES= NONE
*
*   EXIT - ERROR= BRANCH TO IEFVHAA
*      CONDITIONS= ERRORS ON EOF(JCL D. S. AND PROCLIB)
*      OUTPUT DATA= ERROR MESSAGE WRITTEN TO MSG DATA SET
*      RETURN CODES= NONE
*
*   EXIT - ERROR= BRANCH TO IEFVHC
*      CONDITIONS= NULL STMT FOUND IN PROCEDURE
*      OUTPUT DATA= ERROR MESSAGE WRITTEN TO THE MESSAGE DATA SET
*      RETURN CODES= NONE
*
*   EXIT - ERROR= BRANCH TO IEFVHF
*     CONDITIONS= I/O ERROR READING
*     OUTPUT DATA= SET TERMINATION BIT(CWATERM) IN CWA
*     RETURN CODES= INITIALIZE CWARET TO 8
*
*   EXTERNAL REFERENCES=
*      ROUTINES= BRANCH TO IEFVGM- WRITE MESSAGE TO MESSAGE DATA SET
*      ROUTINES= BRANCH TO IEFVGM- WRITE INVALID STATEMENT IN PROCED-
*                                  URE TO LIST DATA SET
*                BRANCH TO IEFVHR- WRITE MESSAGE TO CONSOLE
*                BRANCH TO IEFVIND VIA DCBREAD- READ STATEMENT FROM
*                                               INSTREAM PROC
*                BRANCH TO IEFVHEB- WRITE INSTREAM PROC STATEMENT TO
*                                   LIST DATA SET
*                BRANCH TO IEFVHQ- READ SYMBUF FROM SWA
*      DATA AREAS= JCL DATA SET INPUT BUFFER
*                  PROCEDURE LIBRARY INPUT BUFFER
*                  CWA, SYMBUF, INSTREAM PROC PARMLIST
*      CONTROL BLOCKS= QMPA, DCB, RPL
*      TABLES= NONE
*      MACROS= IEFVMCWA, DCBD, IEFQMNGR, IEFVKEYS, IFGRPL
*      ENQUEUE RESOURCES= NONE
*      CHANGE LEVEL= OZ03606,OZ05366,OZ07517,OZ08442           @ZA08442
*
*   MESSAGES=
*      TO MESSAGE DATA SET=
*             IEF601I INVALID STATEMENT IN PROCEDURE
*             IEF602I INPUT DEVICE I/O ERROR READING FOR JOB
*             IEF603I PROCLIB DEVICE I/O ERROR READING FOR JOB
*             IEF610I PROCEDURE HAS NO STEP
*             IEF657I SYMBOL NOT DEFINED IN PROCEDURE
*      TO CONSOLE=
*             IEF417I PROCLIB DEVICE I/O ERROR READING FOR JOB JJJ
*             IEF419I INPUT DEVICE I/O ERROR READING FOR JOB JJJ
*
***********************************************************************
         EJECT
         IEFCOMWA                                                Y02668
         IEFCVRWA                                                Y02668
         IEFTXTFT                                                Y02668
         DCBD  DSORG=(PS)
NEL      DSECT
         IEFNEL SUBCOM=C
         IEFQMNGR
INTKEY3  EQU   X'F2'                                             Y02668
         EJECT
         IEFVMSWA
         EJECT
         IFGRPL
         EJECT                                                   Y02668
         IDARMRCD                                                Y02668
         EJECT
IEFVHA   CSECT
* D 019202-019212                                              @ZA01562
* C 036928                                                     @ZA01892
* ADDED STATEMENTS NEAR LABEL A644A                            @ZA05366
* ADDED CODE NEAR LABEL A644E, AND ADDED LABEL A644AB          @ZA07517
* DELETED IEFSAVER MACRO IN A9 SUBROUTINE                      @ZA08442
         BALR  RB,0
         USING *,RB          *
         USING IHADCB,R3
*****************************************************************Y02668
* MODULE TRACE CODE - FOR TESTING.                               Y02668
         L     RF,TRACEV           LOAD TRACE RTNE ADDR.         Y02668
         BALR  RE,RF               ENTER MOD ID IN TRACE RECORD. Y02668
TRACEV   DC    V(TRACE)            TRACE RTNE ADDR.              Y02668
         DC    C'VHA '             MOD ID USED BY TRACE.         Y02668
* TRACE RETURNS HERE.                                            Y02668
*****************************************************************Y02668
         B     A6
         MODID BR=NO                                             Y01886
         DC    C'SPLT'                                           Y02668
         DC    C'04'               LEVEL # (NEL CODE DELETION).  Y02668
         DC    C'REL3PTF'    TURN OFF COMMENTS SWITCH AT EOF   @ZA03606
*        DC    C'01' LEV # (PTF UZ00952 PREVENT 0C4 IN IEFVFA).@ZA03606
*        DC    C'02' LEV # (PTF UZ01541 PREVENT 0B0-C IN VHA). @ZA05366
*        DC    C'03' LEV # (PTF UZ03049).                      @ZA07517
         DC    C'04' LEV # (PTF UZ03361 DONT SAVE REGS)        @ZA08442
A6       DS    0H
         L     R8,CTRLWAP          ADDRESS OF SCAN WORK AREA.   YM02718
         TM    IWAPARM,EOFPROC         IS THIS A READER EOF FROM  M4268
*                                      IEFVINA FOR AN INSTREAM    M4268
*                                      PROC                       M4268
         BO    A66                     YES, RESTORE EODAD AND     M4268
*                                      SYNAD ADDRESSES TO QSAM    M4268
*                                      DCB                        M4268
         TM    SWE,GPI                  GET PROC INPUT
         BZ    A66                      BR NO
         L     R3,PDCBP           PROC DCB
         TM    SWI,BLKPRC         BLOCK RTN SWITCH ON?
         BO    A961               GOTO UNBLOCKING ROUTINE.
         OI    SWI,BLKPRC         TURN ON BLOCK SWITCH.
         MVC   IWAINTS4(4),PSTMT  SAVE THE BUFFER ADDRESS.
A62      LA    R2,0(0,0)
         STH   R2,IWANLRC     ZERO LOG REC COUNT
         LA    R2,A9                                            YM02718
         L     R3,PDCBP      PROC DCB
         ST    R2,20(RD)                TEMP SAVE
         MVC   DCBSYNAD+1(3),21(RD)     SYNAD
         LA    R2,A644                  EODAD
         ST    R2,20(RD)
         MVC   DCBEODAD+1(3),21(RD)
         LA    R2,QLINKWA     **USE QLINKWA AS A DECB
         L     R9,PSTMT                 BUFFEP PTR
         TM    IWAPARM,INSTPROC        SEE IF IN-STREAM PROCS ARE M4763
*                                      BEING USED                 M4763
         BZ    A623                    NO - ISSUE A READ          M4763
         MVC   DCBREAD+DISPLONE(MVTHREE),VIEFVIND+DISPLONE        M4763
*                                      ADDRESS OF PSEUDO ACCESS   M4763
*                                      METHOD TO BE USED TO READ  M4763
*                                      STATEMENTS FROM AN IN-STREAM4763
*                                      PROCEDURE                  M4763
A623     DS    0H                                                 M4763
         READ  (2),SF,(3),(9),MF=E
         TM    IWAPARM,INSTPROC        IN-STREAM PROCEDURE BEING  M4763
         BO    A63                     PROCESSED                  M4763
         LR    R1,R2                    DECB
         CHECK (1)
A63      DS    0H                                                 O106
         TM    SWC,IOERR                I/O ERROR
         BZ    A64                      BR NO
         LA    R2,MSG3                  I/O ERR
         BAL   RA,A686                  PUT MSG
         B     A644                     SET PROC EOF
A64      DS    0H
         CLC   NULL(2),0(R9)            // STMT
         BNE   A641          BR NO - INVALID STMT IN PROC       A25814
*        TEST FOR A NULL STATEMENT - NOT VALID IN PROC          A25814
         CLI   D2(R9),C' '   TEST COL 3 FOR A BLANK             A25814
         BNE   A68           BR NOT BLANK, STMT OK              A25814
         CLC   D2(LEN,R9),D3(R9)  TEST COLS 4-71 FOR BLANKS     A25814
         BNE   A68           BR NOT BLANKS, STMT OK             A25814
A641     EQU   *             HERE FOR NULL OR NOT //            A25814
         TM    IWAPARM,INSTPROC   IN-STREAM PROC BEING PROCESSED. M5288
         BZ    A641A         NO-INSTREAM CLEANUP NOT NEEDED.      M5288
**********************************************************************
D1       EQU   1             DISPLACEMENT TO COL 1                M5288
D2       EQU   2             DISPLACEMENT TO COL 2              A25814
D3       EQU   3             DISPLACEMENT TO COL 3              A25814
TWENTY4  EQU   24            LENGTH OF IN-STREAM LIST             M5288
LEN      EQU   68            NO OF COLS CHECKED TO SCANE        A25814
*                            FOR NULL STMT.                     A25814
*        LEN=68 CHECKS COLS 3-71.                               A25814
**********************************************************************
         SPACE 1
*        IN-STREAM PROC PARAM LIST MUST BE FREED AS A RESULT OF   M5288
*         THE INVALID CARD WITHIN THE INSTREAM PROC               M5288
         LA    R0,TWENTY4    LENGTH OF AREA TO BE FREED.          M5288
         L     R1,IWAPARM    ADDRESS OF AREA TO BE FREED.         M5288
         FREEMAIN R,LV=(0),A=(1)                                  M5288
         XC    IWAPARM+D1(D3),IWAPARM+D1  CLEAR IWA PARM POINTER  M5288
A641A    DS    0H                                                Y02668
         TM    IWAJMSGL,AOMSGLV1   MSGLEVEL=1?                   Y02668
         BZ    A642                     BR NO
         L     R2,AOWSTMT          GET 'WRITE' BUFFER ADDR       Y02668
         MVI   0(R2),C' '          BLANK OUT 'WRITE' BUFFER      Y02668
         MVC   1(BUFLTH,R2),0(R2)  BEFORE MOVING IN STMT         Y02668
         MVC   STNLTH(AOMSGLTH,R2),0(R9)  MOVE STMT TO BUFFER    Y02668
         SR    R2,R2
         BAL   RA,A687                  STMT TO SMB
A642     DS    0H
         LA    R2,MSG1                  INVALID STMT IN PROC
         BAL   RA,A686                  PUT MSG
*                                                               YM02718
A644     DS    0H                  PROC EODAD.                  YM02718
*****************************************************************Y02668
*                                                               *Y02668
*        CHECK EACH ENTRY IN THE SYMBOLIC PARAMETER TABLE       *Y02668
*        (THESE ENTRIES ARE MAPPED FROM THE PROC STMT).   MAKE  *Y02668
*        SURE IT HAS BEEN REFERENCED IN THE PROCEDURE.          *Y02668
*                                                               *Y02668
*****************************************************************Y02668
         L     RF,SYMTTR           LOAD PTR TO FIRST SYMBUF.    YM02718
         LTR   RF,RF               SYMB PARMS IN THIS PROCEDURE?YM02718
         BZ    A644G               NO, BYPASS SYMB PARM CHECK.  YM02718
         TM    AOSW2,X'80'   WAS SYMBOLIC DICTIONARY WRITTEN   @ZA05366
         BZ    A644B         NO, BYPASS SYMB DICTIONARY READ   @ZA05366
         L     RF,IEFVHQV          Q-MNGR ADDRESS                Y02668
         LA    R2,SYMBUF           SET UP TO READ SYMBUFF IN     Y02668
         L     R5,QPARM+QMPCL-QMNAM  FROM QUEUE                  Y02668
A644A    DS    0H                                                Y02668
         ST    R2,CLEAR(R5)        TELL QMNGR WHERE TO PUT SYMBUFY02668
         MVC   4(TTRLTH,R5),SYMTTR GIVE QMNGR TTR OF RECORD TO   Y02668
*                                  BE READ                       Y02668
A644AB   DS    0H                                              @ZA07517
         MVI   7(R5),CLEAR         ZERO BYTE FOLLOWING TTR       Y02668
         MVI   QPARM+QMPOP-QMNAM,QMREAD INDICATE READ OPERATION  Y02668
         BALR  RE,RF               GO TO READ SYMBUF             Y02668
A644B    DS    0H           COME HERE TO  BYPASS DICTIONARY RD @ZA05366
         LA    R2,SYMBUF+8         PTR TO FIRST ENTRY IN SYMBUF  Y02668
A644C    DS    0H                                                Y02668
         CLI   CLEAR(R2),CLEAR     Q. SYMBOLIC PARM FIELD ENTEREDY02668
         BE    A644E               NO, CHECK TO SEE IF SYMBUF    Y02668
*                                  HAS BEEN SPILLED              Y02668
         TM    1(R2),USEFLAG       Q. SYMBOLIC PARM USED         Y02668
         BZ    A644D               YES, CONTINUE SEARCH          Y02668
         LA    R2,MSGSYMP          NO, ERROR - SET MSG CODE      Y02668
         BAL   RA,A686             FAIL JOB AND ISSUE MSG.      YM02718
         B     A644F               CONTINUE PROCESSING.         YM02718
A644D    DS    0H                                                Y02668
         SR    RE,RE               CLEAR REG FOR INSERT          Y02668
         IC    RE,CLEAR(R2)        GET OFFSET TO NEXT ENTRY      Y02668
         AR    R2,RE               UPDATE PTR TO NEXT ENTRY      Y02668
         B     A644C               CONTINUE SEARCH               Y02668
A644E    DS    0H                                                Y02668
         L     R2,SYMBUF+4         Q. DID SYMBUF SPILL TO        Y02668
         LTR   R2,R2               ANOTHER BUFFER                Y02668
         BZ    A644F               NO, CONTINUE PROCESSING       Y02668
         MVC   4(TTRLTH,R5),SYMBUF+4  YES, SET UP TO READ      @ZA07517
         LA    R2,SYMBUF           NEXT SYMBUF INTO CORE         Y02668
         B     A644AB              GO BACK TO READ             @ZA07517
A644F    DS    0H                                                Y02668
A644G    DS    0H                                                Y02668
         OI    SWE,PREF                 PROC EOF
         XC    PSTMT+4,PSTMT+4     CLEAR VERB KEY FOR NEXT PROC.YM
          TM    SWC,PEXP               EXEC IN PROC EXPECTED     A27937
          BZ    A645                   NO,BRANCH                 A27937
          LA    R2,EAR49               YES,HAS NOT BEEN RECEIVED A27937
          BAL   RA,A686                PUT OUT MESSAGE           A27937
A645     DS    0H                                                 M4268
         NI    IWAPARM,EOFINST         IN-STREAM PROCEDURE IS NOT M4763
*                                      BEING PROCESSED ANY MORE   M4763
         NI    SWI,255-BLKPRC          TURN OFF UNBLOCKING RTN    M4268
*                                      SWITCH                     M4268
         NI    SWE,255-PROC-GPI   CLEAR PROC & GET PROC INPUT     11499
         MVC   0(3,R9),NULL             BUILD A NULL STMT
         MVC   3(77,R9),2(R9)
         MVC   PSTMT(4),IWAINTS4   RESTORE BUFFER POINTER
         NI    SWC,255-IOERR           TURN OFF ERROR FLAG       A49656
         B     A68
A66      DS    0H                       GET RDR STMT
         TM    IWAPARM,EOFPROC         EOF FOR RDR FROM IEFVINA   M4268
         BO    A95                     READER EOF FROM IEFVINA    M4268
         TM    SWB,FRCV                 HAS 1ST STMT BEEN RCVD
         BZ    A663                     BR NO
         L     R9,RSTMT                 PTR TO LAST STMT
         MVI   0(R9),X'FF'              CLEAR 1ST POS OF STMT
A663     DS    0H
         LA    R4,RPLADDR               GET ADDRESS OF RPL       Y02668
         USING IFGRPL,R4                SET BASE FOR RPL         Y02668
         L     R6,WANELPTR              GET ADDRESS OF NEL       Y02668
         USING NEL,R6                   SET BASE FOR NEL         Y02668
         MVI RPLREQ,RPLGET              REQUEST IS A GET         Y02668
         MVC   RPLDACB,NELJCLCB         SET POINTER TO ACB       Y02668
         MVC   RPLAREA,RSTMT            SET POINTER TO BUFFER    Y02668
         XC    RPLARG(4),RPLARG         ZERO UNUSED FIELD        Y02668
         MVI   RPLOPT1,RPLSEQ           GET IS SEQUENTIAL        Y02668
         XC    RPLRLEN(4),RPLRLEN       ZERO UNUSED FIELD        Y02668
         LA    R0,80                    GET BUFFER LENGTH        Y02668
         ST    R0,RPLBUFL               SET MAXIMUM BUFFER LENTH Y02668
         XC    RPLDDDD(4),RPLDDDD       ZERO UNUSED FIELD        Y02668
         DROP  R6                       RELEASE BASE FOR NEL     Y02668
         ST    RD,IWASACHN         SAVE SAVE AREA POINTER        Y02668
         LA    RD,IWAIOSA          SAVE AREA FOR ACCESS METHOD   Y02668
         LR    R1,R4                    POINT TO RPL             Y02668
         GET   RPL=(1)
         L     RD,IWASACHN         RESTORE SAVE AREA POINTER     Y02668
         LTR   RF,RF               CHECK FOR ERROR               Y02668
         BZ    A664                NO. BRANCH TO CONTINUE        Y02668
         CLI   RPLRTNCD,RPLLOGER   IS THIS A LOGICAL ERROR ?     Y02668
         BNE   ERREXIT             NO. GO TO ERROR EXIT          Y02668
         CLI   RPLERRCD,RPLEODER   IS END OF DATA DETECTED ?     Y02668
         BE    A95                 YES. GO TO EOF ROUTINE        Y02668
         DROP  R4                  RELEASE BASE FOR RPL          Y02668
         B     ERREXIT             NO. GO TO ERROR ROUTINE       Y02668
A664     DS    0H                      *
         L     R9,RSTMT                 STMT PTR                 Y02668
         TM    SWB,FRCV                 1ST STMT RCVD
         BO    A68                 BRANCH YES.                   Y02668
         NI    SWC,255-NRCV             TURN OFF RDR NULL
         OI    SWB,FRCV                 1ST STMT RCVD
A68      DS    0H
         L     RF,IEFVHCV      EXIT TO VERB ID
         BR    RF
         EJECT
*                                                               YM02718
*  FAIL JOB AND WRITE MESSAGE TO SYSOUT                         YM02718
*                                                               YM02718
A686     DS    0H
         LA    RF,WAJOBPFX         ADDR OF PSEUDO JOB PFX.      YM02718
         USING TEXT,RF             PREFIX ADDRESSABILITY.       YM02718
         OI    STRJINDC,JTXJOBFL   SET JOB FAILED BIT.           Y02668
         DROP  RF                                               YM02718
         XC    MSGKEY(1),MSGKEY    NO SECONDARY MSGS FROM VHA.  YM02718
A687     DS    0H
         L     RF,IEFVGMV               MSG ROUTINE
         BALR  RE,RF
         BR    RA                 RETURN                          11791
         EJECT
A9       DS    0H                      *SYNAD FOR PROCLIB
         USING IHADCB,R3               *
         NI    DCBIFLGS,X'00'          *TURN OFF DCB ERROR FLAGS
         OI    SWC,IOERR               *I/O ERROR
         LA    R5,MSG2                 *MSG CODE
         LR    RA,RE                        RETURN ADDRESS        11791
         LR    R6,RE               SAVE RETURN REGISTER           11791
         L     RF,IEFVHRV       **ERROR MSG ROUTINE ADDR.    PTM 14/039
         BALR  RE,RF               PUT OUT ERROR MESSAGE          11791
         LR    RE,R6               RESTORE RETURN REGISTER        11791
         BR    RA                  RETURN                         11791
         EJECT
*****************************************************************Y02668
*                                                               *Y02668
*   EODAD  ROUTINE  FOR  JCL  DATA  SET                         *Y02668
*                                                               *Y02668
*****************************************************************Y02668
         SPACE 2
A95      DS    0H                                                Y02668
         NI    IWAPARM,EOFRINST    TURN OFF EOF BIT FOR JCL DATA Y02668
*                                  SET, SET BY IEFVINA FOR AN    Y02668
*                                  INSTREAM PROCEDURE            Y02668
         L     R9,RSTMT            GET PTR TO CURRENT JCL        Y02668
*                                  STMT BUFFER                   Y02668
         OI    SWA,EOFR            INDICATE JCL D. S. EOF IN CWA Y02668
         MVC   0(MV3,R9),NULL      BUILD A NULL STMT IN THE      Y02668
         MVC   MV3(STMTLTH-MV3,R9),HALFWORD(R9) JCL D. S. BUFFER Y02668
         NI    CMTZ,255-CMT    TURN OFF COMMENTS SWITCH AT EOF @ZA03606
         L     RF,IEFVHCV          GET PTR TO VALIDATION RTN     Y02668
         BR    RF                  CONTINUE PROCESSING NULL STMT Y02668
         EJECT
*****************************************************************Y02668
*                                                               *Y02668
*    BLOCKED PROCLIB PROCESSING                                 *Y02668
*                                                               *Y02668
*****************************************************************Y02668
         SPACE 2
A961     L     R9,PSTMT           ADDRESS OF LAST RECORD.
         LA    R9,80(0,R9)        INCREMENT TO POINT TO NEXT RECORD.
         LA    R7,1(0,0)
         AH    R7,IWANLRC
         STH   R7,IWANLRC
         L     R7,IWAINTS4        ADDRESS OF BUFFER
         AH    R7,DCBBLKSI        ADD BLOCK SIZE-POINTER TO END OF BUFF
         CR    R7,R9              IS THIS END OF BLOCK?
         BE    A962               BR YES - READ NEW BLOCK.
         L     R2,DCBIOBA    R2 = ADDR OF IOB PREFIX
         SLL   R2,8          CLEAR FLAG BYTE
         SRL   R2,8
         LH    R5,22(0,R2)         R5=RESIDUAL BYTE CT IN CSW.  YM02718
         LTR   R5,R5               COUNT=0?                     YM02718
         BE    BLKSTR        YES-PROCESS NORMALLY-NO TRUNCATION
         SR    R7,R5       NO,FIND THE ACTUAL END OF THIS BLOCK.YM02718
         CR    R9,R7         HAVE ALL RECS IN THIS BLOCK BEEN PASSED?
         BE    A962          YES-GOTO READ NEW BLOCK           @ZA01892
BLKSTR   ST    R9,PSTMT      NO-PASS POINTER TO THIS RECORD
         B     A64                GOTO DATA CHECK RTN.
A962     MVC   PSTMT(4),IWAINTS4  RESTORE BUFFER ADDRESS FOR READ.
         B     A62                 GO TO READ ROUTINE.           Y02668
         EJECT
*****************************************************************Y02668
*                                                               *Y02668
*        SYNAD ROUTINE FOR JCL DATA SET                         *Y02668
*                                                               *Y02668
*****************************************************************Y02668
         SPACE 2
ERREXIT  DS    0H                                                Y02668
         LA    R5,4                I/O ERROR MSG CODE            Y02668
         L     RF,IEFVHRV               GET POINTER TO IEFVHR    Y02668
         BALR  RE,RF                    WRITE MSG TO OPERATOR    Y02668
         LA    R2,20               I/O ERROR MSG CODE            Y02668
         BAL   RA,A687             ISSUE ERROR MESSAGE.         YM02718
         MVC   IWARET(2),PGMERROR  SET RETURN CODE               Y02668
         OI    AOSW4,CWATERM       INDICATE TERMINATION          Y02668
         L     RF,TERMRTN          GET TERMINATION ADDRESS       Y02668
         BR    RF                  GO TO TERMINATION             Y02668
         EJECT
NULL     DC    C'// '
PGMERROR DC    X'0024'             PGM ERROR CODE (36)           Y02668
MVTHREE  EQU   3                       LENGTH OF MOVE             M4763
INSTPROC EQU   X'80'                   IN-STREAM PROCEDURE BEING  M4763
*                                      PROCESSED                  M4763
EOFINST  EQU   X'7F'                   INSTREAM PROCEDURE NOT     M4763
*                                      BEING PROCESSED            M4763
EOFRINST EQU   X'BF'                                              M4268
EOFPROC  EQU   X'40'                                              M4268
VIEFVIND DC    V(IEFVIND)              ADDRESS OF IEFVIND, PSEUDO O106
*                                      ACCESS NETHOD              O106
DISPLONE EQU   1                       DISPLACEMENT               O106
MV3      EQU   3                       LENGTH OF COMPARE          O106
MSG1     EQU   1                        INVALID STMT IN PROCEDURE
MSG2     EQU   2                        RDR I/O ERR
MSG3     EQU   3                        PROC I/O ERR
EAR49     EQU   X'0A'          PROC HAS NO STEP ERROR            A27937
MSGSYMP  EQU   X'39'               SYMBOLIC PARMS NOT USED       Y02668
USEFLAG  EQU   X'80'               INDICATES A SYM PARM WAS USED Y02668
TTRLTH   EQU   4                   LENGTH OF TTR FIELD           Y02668
CLEAR    EQU   0                                                 Y02668
HALFWORD EQU   2                                                 Y02668
STMTLTH  EQU   80                  JCL STMT LENGTH               Y02668
STNLTH   EQU   10                  STMT# FIELD LENGTH            Y02668
BUFLTH   EQU   90                  WHOLE STMT BUFFER LENGTH      Y02668
AOMSGLTH EQU   BUFLTH-STNLTH       MSG LENGTH                    Y02668
IEFVHCV  DC    V(IEFVHC)                VERB ID
IEFVGMV  DC    V(IEFVGM)
IEFVHRV  DC    V(IEFVHR)               *OPERATOR MSG RTN
IEFVHQV  DC    V(IEFVHQ)           Q/SWA MANAGER RTN             Y02668
PATCH    DC    25F'0'              **********PATCH SPACE*********Y02668
         END
