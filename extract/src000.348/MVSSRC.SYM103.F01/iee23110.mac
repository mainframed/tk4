         TITLE 'IEE23110  UNIT STATUS FOUR, PART OF SVC 110'
IEE23110 CSECT
***********************************************************************
*                                                                     *
* STATUS                                                              *
*    CHANGE LEVEL=0  VS 2 RELEASE 3
*
*  PTMS FIXED  @ZM30152,@ZM32568
*                                                                     *
* FUNCTION                                                            *
*    FIND VALID UCB'S TO SCAN                                         *
*    WRITE COMPLETED DISPLAY LINES                                    *
*                                                                     *
* ENTRY POINTS                                                        *
*         IEE23110                                                    *
*                                                                     *
* INPUT                                                               *
*    REG 11 CONTAINS THE CSCB ADDRESS WHICH CONTAINS ALL OTHER PTRS   *
*    REG 9  CONTAINS THE UCB ADDRESS FOUND BY IEE20110                X
*                                                                     *
* OUTPUT                                                              *
*    DISPLAY LINES ISSUED TO CONSOLE SUPPORT                          *
*                                                                     *
* EXTERNAL REFERENCES                                                 *
*         SVC 11   (TIME)
*         SVC 35   (WTO)                                              *
*                                                                     *
* EXITS,NORMAL                                                        *
*         IEE21110 TO BUILD DEVICE ENTRIES IN DISPLAY LINE            *
*         IEE22110 TO BUILD DATA CELL BIN ENTRIES IN DISPLAY LINE     *
*         IEE22110 TO RETURN WHEN DISPLAY COMPLETE                    *
*                                                                     *
* EXITS,ERROR                                                         *
*         IEE22110 TO ISSUE ERROR MESSAGES                            *
*                                                                     *
* TABLES/WORKAREAS                                                    *
*    CSCB                                                             *
*    XSA                                                              *
*    UCB                                                              *
*    WORKAREA                                                         *
*    CVT                                                              *
*                                                                     *
* ATTRIBUTES                                                          *
*    TYPE IV SVC, PRIVILEGED, RE-ENTRANT                         A36790
*                                                                     *
* CHARACTER CODE DEPENDENCY                                           *
*    NONE                                                             *
*                                                                     *
* NOTES                                                               *
*    NONE                                                             *
*                                                                     *
***********************************************************************
         EJECT
*/*IEE23110: CHART */
*/*IEE23110: E IEE23110 */
*/* P ESTABLISH ADDRESSABILITY */
*/* D (NO,,YES,WTORTN) PUT OUT DATA CELL WTO ? */
*/* D (NO,WTORTN,YES,) INITIAL ENTRY ? */
*/* P TURN OFF INITIAL ENTRY FLAG */
*/* P CLEAR ADDRESS TABLE */
*/*NEWLINE: P POINT TO EBCDIC ADDRESS */
*/* S COMPSET:GO CONVERT ADDRESS */
*/* P POINT TO UCB ADDRESS TABLE */
*/*DUMBHI: P DUMMY UP HIGH VALUE */
*/*VALIDCHK: D (NO,GETNEXT,YES,) CORRECT UCB TYPE ? */
*/* D (NO,,YES,STORADDR) ALL STATUS WANTED ? */
*/* D (NO,,YES,ONLINE) ONLINE REQUEST ? */
*/* D (NO,STORADDR,YES,GETNEXT) IS DEVICE ONLINE OR ALLOC ? */  SA52720
*/**ONLINE: D (NO,,YES,STORADDR) IS DEVICE ONLINE ? */
*/*GETNEXT: D (NO,BUMP,YES,EXMSG1) SINGLE DEVICE REQUEST ? */
*/*STORADDR: P INDICATE UCB FOUND */
*/* P HOLD POINTER TO CURRENT HIGH UCB */
*/* P (,BUMP) HOLD CONVERTED ADDRESS */
*/*LOOP: P GET UCB ADDR FROM LOOK UP TABLE */
*/* D (NO,UCB,YES,EOL) END OF TABLE ? */
*/*BUMP: P (,LOOP) POINT TO NEXT UCB ADDRESS */
*/*UCB: P LOCATE EBCDIC ADDR VALUE */
*/* S COMPSET:GO CONVERT ADDR */
*/* D (NO,,YES,BUMP) NEW LOWER THAN OLD LOW ? */
*/* D (NO,VALIDCHK,YES,BUMP) HIGHER THAN OLD HIGH ? */
*/*EOL: D (NO,NOADDR,YES,) ANY NEW HIGH STORED? */
*/* P PUT HIGH VALUE INTO LOW */
*/* P RESET UCB FOUND BIT */
*/* D (NO,,YES,ENDSCN) NUM UCBS FOUND = NUM WANTED */
*/*GETONE: P POINT TO UCB LOOK UP TABLE */
*/* D (NO,LOOP,YES,) ENOUGH TO FILL ONE LINE ? */
*/*EXIT: P SET EXIT TO IGC22110 */
*/* D (NO,XCTL,YES,) FINISH DATA CELL ? */
*/* P (,EXUN3) POINT TO FIRST UCB ADDRESS */
*/*NOADDR: D (NO,EXMSG1,YES,) ANY DISPLAY OUTPUT ? */
*/* D (NO,SETND,YES,) LINE DEFAULT NOT TAKEN ? */
*/* P (,SETND) TELL UNIT 3 TO PUT OUT MESSAGE */
*/*ENDSCN: D (NO,,YES,EXIT) ENOUGH UNITS TO FILL LINE ? */
*/*SETND: P (,EXIT) TURN ON DISPLAY ENDED FLAG */
*/*COMPSET: E COMPSET */
*/* P PREPARE TO LOOP THRU NUMBER */
*/*COMPSET2: D (NO,,YES,COMPSET5) IS THIS DIGIT NUMERIC ? */
*/* P ADD NINE (9) TO DIGIT */
*/*COMPSET3: P HOLD RESULTING DIGIT OF ZONE/DIGIT */
*/* D (NO,COMPSET2,YES,) DONE THREE DIGITS ? */
*/*COMPSET4: R BR 14 */
*/*COMPSET5: P (,COMPSET3) HOLD DIGIT */
*/*WTORTN: P PREPARE WTO PARM LIST */
*/* D (NO,,YES,SKIPHEAD) HAVE HEADINGS BEEN WRITTEN  ? */
*/* P INDICATE HEADINGS WRITTEN */
*/* P SAVE DEVICE OUTPUT AND BUILD HEADING */
*/* S TIME SVC 11: GET TIME OF DAY */
*/*TIME: P CONVERT TIME FOR PRINT AND PUT IN MSG */
*/* S WTO RTN:GO ISSUE HEADING LINE */
*/* P PICK UP NUMBER OF DEVICES PER LINE */
*/* D (NO,CONT,YES,) OFFLINE DISPLAY ? */
*/* P USE OFFLINE LABELS IN LABEL LINE */
*/*CONT: P DECREMENT LABEL LENGTH */
*/*LABLOOP: P PICK UP FIRST ENTRY IN LINE */
*/* D (NO,LABEND,YES,) ANY MORE ENTRIES ? */
*/* P PUT CORRECT LABEL IN LINE */
*/* D (NO,LABLOOP,YES,) ALL REQUIRED LABELS IN LINE ? */
*/*LABEND: D (NO,LABENDMV,YES,) REQUEST FOR SINGLE DEVICE ? */
*/* D (NO,LABENDMV,YES,) 2321 DATA CELL ? */
*/* P MOVE IN SECOND HALF LABEL LINE */
*/*LABENDMV: S WTORTN:GO ISSUE LABEL LINE */
*/* P (,WTORTN) RESTORE DEVICE TEXT OUTPUT */
*/*SKIPHEAD: P SET LINE TYPE TO DATA */
*/* D (NO,,YES,MSGOUT) DATA CELL IN PROCESS */
*/* D (NO,,YES,SKIPFR) IS THIS END OF DISPLAY ? */
*/* S WTO RTN: GO ISSUE DATA LINE */
*/* D (NO,NEWLINE,YES,) IS DATA CELL COMPLETE? */
*/* P (,GETONE) UP DEVICE DONE COUNT BY 2 */
*/*SKIPFR: D (NO,OSU,YES,) WAS LAST LINE AN END LINE ? */
*/* S WTO RTN:GO WRITE MESSAGE */
*/* P MOVE MSG TEXT INTO WTO PARM LIST */
*/*OSU: P SET LINETYPE = DATA END */
*/* S WTO RTN:GO OUTPUT MESSAGE */
*/*EXIT3: P (,EXUN3A) SET EXIT TO IGC22110 */
*/*MSGOUT: S WTO RTN: GO OUTPUT MESSAGE */
*/*EXUN3: P INDICATE FINISH DATA CELL */
*/*EXUN3A: P PUT EXIT NAME (IGC22110) IN XCTL LIST */
*/*XCTL: R IGC22110 */
*/*WTO: E WTO RTN */
*/*WTOLOOP: P FIND END OF MESSAGE TEXT */
*/*HITEXT: P TAG DESCRIPTOR CODES ON END OF MESSAGE */
*/* P COMPUTE AND PUT LENGTH INTO WTO LIST */
*/* S WTO SVC 35:OUTPUT MESSAGE TO OPERATOR */
*/* P SAVE CONSOLE ID AND MESSAGE NUMBER */
*/* P CLEAR TEXT LINE */
*/* R BR 14 */
*/*EXMSG1: P (,EXUN3A) INDICATE MESSAGE FOR UNIT 3 TO WRITE */
*/*IEE23110: END */
         EJECT
         BALR  PBASE,R0            ESTABLISH BASE REGISTER
         USING *,PBASE             DECLARE PROGRAM BASE
         B     ICATCH              BRANCH AROUND EYECATCHER   MB YM1902
         DC    C'01 MODULE IGC23110, FICHE IEE23110  '        MB YM1902
         DC    XL2'4211'           JULIAN DATE OF LAST CHANGE
         DC    CL8'&SYSDATE'       DATE OF LAST ASSEMBLY
         DC    S(*)
ZAPAREA  DC    12C'ZAP'  RESERVED FOR FIELD MODIFICATION      MB YM1902
ICATCH   EQU   *                                              MB YM1902
         USING XSASET,XSABASE      DECLARE XSA BASE
         L     WBASE,XAL           POINT TO WORK AREA
         USING WORKAREA,WBASE      DECLARE WORK AREA BASE
         LH    R13,DEVSPRE         GET NUMBER ALREADY PUT OUT
         TM    FLAG5,INITENT       INITIAL ENTRY TO SET UP UCB TABLE
         BNO   WTORTN              NO ISSUE NORMAL WTO AND UPDATE TABLE
         SPACE 2
***********************************************************************
*                                                                     *
*  BUILD LIST OF VALID UCB ADDRESSES                                  *
*                                                                     *
***********************************************************************
         NI    FLAG5,N255-INITENT   TURNOFF INITIAL ENTRY FLAG
         XC    UCBADDRA+N2(N12),UCBADDRA+N2 CLEAR TABLE
         LR    R7,R9               GET STARTING UCB ADDRESS
NEWLINE  EQU   *
         STH   R7,CURRENT         PUT VALUE INTO CURRENT WORD
         LA    R15,N13(R7)        GET PTR TO EBCICIC ADDRESS
         BAL   RETURN,COMPSET     GO CONVERT IT
         ST    R8,LOW             SAVE CONVERTED VALUE
         LH    ADDRPT,DEVNOLN      GET NUMBER OF DEVICES FOR LINE
         LA    ADDRPT,N1(ADDRPT)   KICK UP ADDRESS BY ONE
         LA    ADDR,UCBADDRA       POINT TO UCB ADDRESS TABLE
         LA    R3,CVT              POINT TO CVT ADDRESS
         L     R3,ZERO(R3)         GET CVT ADDRESS
         USING CVTDSECT,R3
         L     R3,CVTILK2         GET LOOK-UP TABLE ADDRESS
DUMBHI   EQU   *
         MVI   HIGH+N2,N255       DUMMY UP HIGH VALUE
VALIDCHK EQU   *
         USING UCBDSECT,UCBADDR    DECLARE UCB ADDRESS
         SR    WORK,WORK           CLEAR REGISTER
         IC    WORK,UCBTBYT3       GET UCB TYPE
         LA    POINT,FLAG1         POINT TO RIGHT FLAG BYTE
         CLI   UCBTBYT3,CTCTYPE    CHANNEL-CHANNEL ADAPTOR  JE @ZM32568
         BE    TSTFLG2             YES, GO TEST FLAG2       JE @ZM32568
         EX    WORK,TESTMASK       CHECK TYPE
         BNO   BUMP                NO MATCH GET NEXT UCB      MH Y01043
TSTFLG2  TM    FLAG2,ALLS          ALL STATUS WANTED        JE @ZM32568
         BO    STORADDR            YES SAVE THIS ADDRESS
         TM    FLAG2,ONLINS        ONLINE REQUEST
         BO    ONLINE              YES TEST UCB ONLINE
*                                 NO, ASSUME OFFLINE REQUEST ME SA52720
         TM    SRTESTAT,SRTEONLI+SRTEALOC  IS DEVICE         ME SA52720
*                                  ONLINE OR ALLOCATED       ME SA52720
         BZ    STORADDR            NEITHER, MUST BE OFFLINE  ME SA52720
         B     BUMP                GET NEXT DEVICE           MH Y010430
ONLINE   EQU   *
         TM    SRTESTAT,ONLINS     IS DEVICE ONLINE
         BO    STORADDR            YES STORE ADDRESS
         B     BUMP                 NOT ONLINE, BRANCH        MH Y01043
STORADDR EQU   *
         OI    FLAG5,DISPBIT       TURN ON UCB FOUND BIT
         STH   R7,CURRENT          STORE POINTER TO CURRENT HIGH UCB
         ST    R8,HIGH             STORE CONVERTED DEVICE ADDRESS
         B     BUMP                GO GET ANOTHER
LOOP     EQU   *
         SR    R7,R7                   CLEAR REGISTER         JE YM6838
         ICM   R7,3,NO(R3)          GET UCB POINTER           JE YM6838
         BZ    BUMP                BRANCH IF ADDRESS NULL     JE YM6838
         CLM   R7,3,FOXFOX          END OF UCB TABLE           @Z30AAJE
         BE    EOL                 YES                        JE YM6838
         B     UCB                 GO LOOK AT UCB              @Z30AAJE
BUMP     EQU   *                   FALL THROUGH ON ZERO
         LA    R3,N2(R3)          BUMP POINTER TO THE NEXT ADDRESS
         B     LOOP                  BY TWO AND GO TO GET NEXT
UCB      EQU   *
         LA    R15,UCBNAME         POINT TO EBCIDIC ADDRESS VALUE
         BAL   RETURN,COMPSET      CONVERT ADDRESS FOUND BY 'LOOP'
         STC   R8,UNITADDA             STORE CONVERTED ADDRESSMH Y02139
         NI    UNITADDA,ZEROFOX        ZERO SUBCHANNEL PORTIONMH Y02139
         MVC   UNITADDB(L1),UCBUA      STORE HEX ADDRESS      MH Y02139
         NI    UNITADDB,ZEROFOX        ZERO SUBCHANNEL PORTIONMH Y02139
         CLC   UNITADDA(L1),UNITADDB   COMPARE ADDRESS        MH Y02139
         BNE   BUMP                NOT EQUAL, NOT BASE UCB    MG YM3885
         CL    R8,LOW              COMPARE AGAINST LOW VALUE
         BNH   BUMP                IF NEW VALUE LOW BRANCH
         CL    R8,HIGH             COMPARE AGAINST HIGH VALUE
         BH    BUMP                NEW VALUE HIGHER THAN OLD HIGH
         B     VALIDCHK           GO TO VALIDITY CHECK ROUTINE
EOL      EQU   *
         CH    R13,NODEVICS        REQUEST SATISFIED          MG YM1826
         BNL   SETND               YES, HAVE ENOUGH           MG YM1826
         TM    FLAG5,DISPBIT       ANY UCB FOUND THIS PASS    MG YM1826
         BNO   NOADDR              NO, ALL HAVE BEEN FOUND    MG YM1826
         MVC   LOW+N2(L2),HIGH+N2     PUT HIGH VALUE INTO LOW
         MVC   NO(L2,ADDR),CURRENT MOVE UCB POINTER INTO SAVE LIST
         MVC   UCBADDRB(L4),CURRENT     SAVE LAST VALUE FOUND
         LA    ADDR,N2(ADDR)      BUMP POINTER
         LA    R13,N1(R13)         BUMP NUMBER FOUND BY 1
         NI    FLAG5,N255-DISPBIT      TURN OFF UCB FOUND BIT
GETONE   EQU   *
         LA    R3,CVT              POINT TO CVT ADDRESS
         L     R3,ZERO(R3)         GET CVT ADDRESS
         L     R3,CVTILK2         GET LOOKUP POINTER
         MVI   HIGH+N2,N255       DUMMY UP HIGH UCB
         BCT   ADDRPT,LOOP        COUNT NUMBER IN LINE
EXIT     EQU   *
         MVI   XAX+L4,F1           SECOND LOAD NAME
         B     XCTL                EXIT
NOADDR   EQU   *
         LTR   R13,R13             ANY DISPLAY
         BZ    EXMSG1              ISSUE MESSAGE
         TM    FLAG5,NODEF        TEST FOR DEFAULT
         BNO   SETND              BRANCH TO SET END FLAG
         OI    FLAG5,CRTMSG2      TURN ON MESSAGE BIT FOR U3
SETND    EQU   *
         OI    FLAG5,DISPEND      TURN ON DISPLAY FINISHED
         B     EXIT                BUILD LINE
         SPACE 2
***********************************************************************
*                                                                     *
*  THIS ROUTINE CHANGES EBCDIC DEVICES TO BINARY.                     *
*  REG 15 POINTS TO THE NUMBER TO BE CONVERTED.                       *
*  THE CONVERTED NUMBER IS RETURNED IN REG 8.                         *
*  RETURN IS VIA BR 14.                                               *
*  REGS 8,9,4 AND 15 ARE DESTROYED                                    *
*                                                                     *
***********************************************************************
COMPSET  EQU   *
         SR    WORK,WORK          ZERO REGISTER
         LA    WORK,L3(WORK)      INITIALIZE COUNTER
         SR    TEXTPTR,TEXTPTR     CLEAR WORK REF
COMPSET2 EQU   *
         SR    R9,R9               CLEAR WORK REG
         TM    N0(R15),F0          THIS DIGIT NUMERIC
         BO    COMPSET5            YES HANDLE
         IC    R9,N0(R15)          ISOLATE DIGIT
         LA    R9,N9(R9)           UP BY NINE
COMPSET3 EQU   *
         SLL   R9,N28              SHIFT DIGIT TO
         SLDL  TEXTPTR,N4          LOW ORDER NIBBLE OF REG 8
         LA    R15,N1(R15)         POINT TO NEXT DIGIT
         BCT   WORK,COMPSET2           DO ALL DIGITS
COMPSET4 EQU   *
         BR    RETURN              RETURN TO CALLER
COMPSET5 EQU   *
         IC    R9,N0(R15)          ISOLATE CHARACTER
         B     COMPSET3            CONVERT IT
         SPACE 2
***********************************************************************
*                                                                     *
*  ISSUE WTO ON THE COMPLETED DISPLAY LINES                           *
*                                                                     *
***********************************************************************
WTORTN   EQU   *
         MVC   DESCODE(L4),DCODE   MOVE WTO TAIL
         MVC   AID(L1),IDFLD      MOVE IN AREA ID
         MVI   MCSONE,SETONE      SET FIRST BIT OF MCS FIELD
         MVI   MCSTWO,SETTWO      SET SECOND BIT OF MCS FLAGS
         MVI   LNUM,L1             RE/SET LINES IN LIST
         MVI   TYPEFLAG+N1,ZERO    ZERO OUT LINE TYPE BYTE 2
         TM    FLAG3,HEADOUTF      Q.  HAVE HEADING LINE BEEN WRITTEN
         BO    SKIPHEAD            A. YES SKIP THEM
         OI    FLAG3,HEADOUTF      INDICATE HEADING LINES OUT
         MVC   SAVE+N12(N72),TEXT  SAVE LINE BUILT BY IEEUNIT2/IEEUNIT3
         MVI   TEXT,BLANK          BLANK TEXT
         MVC   TEXT+N1(L71),TEXT   LINE OUT
         MVC   TEXT(HEADLGN),HEAD       MOVE HEADING INTO TEXT
         TIME  DEC
TIME     EQU   *
         ST    R0,WORKING          HOLD TIME
         UNPK  WORKING(N8),WORKING(N4)   UNPACK TIME FOR PRINT
         MVC   TITLEDGH(N2),WORKING+N1      HH
         MVC   TITLEDGM(N2),WORKING+N3      HH.MM
         MVC   TITLEDGS(N2),WORKING+N5      HH.MM.SS
         MVI   TYPEFLAG,ATE        SET CONTROL LINE TYPE
         BAL   RETURN,WTO          ISSUE WTO FOR HEADIND
         LH    WORK,DEVNOLN        GET NO. OF DEVICES PER LINE
         LA    TEXTPTR,TEXT        POINT TO TEXT LINE
         LA    R15,UCBADDRA        POINT TO UCB ADDRESS LIST
         SR    POINT,POINT         ZERO REGISTER
         LA    SCRATCH,LABELA      POINT TO CORRECT LABEL
         LA    POINT,LABALGN(POINT)     LENGTH OF LABEL FOR MOVE
         MVC   WORKING(N2),ONLGN   TEXT INCREMENT
         TM    FLAG2,OFFLNS       TEST FOR OFFLINE
         BNO   CONT               BRANCH IF NOT
         LA    R5,LABOLGN         SET UP FOR OFFLINE DISPLAY
         MVC   WORKING(N2),OFFLGN      SET FOR OFFLGN HEADDER SPACING
CONT     EQU   *
         BCTR  POINT,R0            DECREMENT LENGTH OF LABEL BY ONE
LABLOOP  EQU   *
         LH    ADDR,N0(R15)        PICK UP UCB ADDRESS
         N     ADDR,FOXAND         CLEAR HI ORDER 2 BYTES      @Z30AAJE
*                                                              @ZM30152
         LTR   ADDR,ADDR           Q. ZERO POINTER
         BZ    LABEND              YES ISSUE LABEL LINE
         EX    POINT,LABMOVE       PUT CORRECT LABEL IN LINE
         AH    TEXTPTR,WORKING     NO INCREMENT  TEXT POINTER
         LA    R15,N2(R15)         UP TO NEXT ENTRY IN LIST
         BCT   WORK,LABLOOP        DO ALL LABELS REQUIRED
LABEND   EQU   *
         TM    FLAG2,SINGLE        SINGLE REQUEST                A35057
         BNO   LABENDMV            NO SKIP REQUEST               A35057
         TM    FLAG3,UNIT3RET      IF UNIT3 THEN SINGLE 2321 WHICH
         BNO   LABENDMV            REQUIRES A FULL LABEL, ELSE HALF.
         EX    POINT,LABMOVE       MOVE SECOND HALF OF LABEL LINE
LABENDMV EQU   *
         MVI   TYPEFLAG,FORE       SET LABEL LINE TYPE
         BAL   RETURN,WTO          WRITE LABEL LINE
         MVC   TEXT(N72),SAVE+N12       RESTORE TEXT LINE
         B     WTORTN              RE-INITIALIZE WTO TAIL
SKIPHEAD EQU   *
         MVI   TYPEFLAG,TWO        SET DATA LINE TYPE
         TM    FLAG5,DISPEND       IS THIS END OF DISPLAY
         BO    SKIPFR              YES, ISSUE WTO AND EXIT
         BAL   RETURN,WTO          NO PUT OUT TEXT LINE
         XC    UCBADDRA(N14),UCBADDRA  CLEAR TABLE
         LH    UCBADDR,UCBADDRB    GET NEXT VALID UCB
         N     UCBADDR,FOXAND      CLEAR HI ORDER 2 BYTES      @Z30AAJE
*                                                              @ZM30152
         B     NEWLINE             START NEW LINE
SKIPFR   EQU   *
         TM    FLAG5,CRTMSG2      SEE IF LAST STATUS LINE IS END LINE
         BNO   OSU                BRANCH IF NO ERROR MSG
         CLC   TEXT(N7),MSG2A     MSG2A IN TEXT AREA          MG YM1826
         BE    OSU                YES, SET END AND WRITE IT   MG YM1826
         BAL   RETURN,WTO         GO WRITE DATA LINE
         MVC   TEXT(MSG2ALGN),MSG2A   MOVE TEXT IN FOR MSG2
         B     WTORTN             RE-INIT WTO PARAMETERS      MG YM1826
OSU      EQU   *
         MVI   TYPEFLAG,THREE      SET DATA-END LINE TYPE
         BAL   RETURN,WTO         GO WRITE MSG2
EXIT3    EQU   *
         MVI   FLAG4,EXITBIT       GO TO THIRD LOAD AND EXIT
EXUN3A   EQU   *
         MVI   XAX+L4,F2           THIRD LOAD NAME
XCTL     EQU   *
         LA    R15,XSA             POINT TO  PARA  LIST
         XCTL  SF=(E,(R15))
         SPACE 2
***********************************************************************
*
*  ROUTINE ISSUES SVC 35 ON COMPLETED DISPLAY LINES
*
***********************************************************************
WTO      EQU   *
         LA    ADDRPT,TEXT+L71     POINT TO SEARCH BEGIN
WTOLOOP  EQU   *         SEARCH BACK FOR LAST TEXT CHARACTER
         CLI   ZERO(ADDRPT),BLANK   IS THIS CARACTER A BLANK
         BNE   HITEXT              NO   FOUND END - FINISH WTO
         BCT   ADDRPT,WTOLOOP      GO BACK ONE
HITEXT   EQU   *
         MVC   ONE(N8,ADDRPT),DESCODE       BACK UP TAIL TO TEXT
         LA    ADDRPT,N5(ADDRPT)   UP LENGTH FOR SEARCH AND CODES
         LA    R0,TEXT             POINT TO TEXT BEGINNING
         SR    ADDRPT,R0           FIND LENGTH OF WTO
         STH   ADDRPT,WTOSET       STORE LENGTH IN LIST
         L     R0,HOLD2           PUT IN AREA  CONSOLE ID AND MLWTO ID
         LA    ADDRPT,WTOSET       POINT TO WTO LIST
         WTO   MF=(E,(1))          EXECUTE WTO LIST
         LTR   R15,R15             TEST RETURN CODE           MC YM3872
         BNZ   EXIT3               DISPLAY CANCELLED, EXIT    MC YM3872
         SLL   R1,N8               SHIFT TO HIGH ORDER BYTES
         IC    R1,HOLD2+N3         GET CONSOLE ID
         ST    R1,HOLD2           STORE THEM BACK
         MVI   TEXT,BLANK          BLANK TEXT AREA
         MVC   TEXT+L1(L71),TEXT   PROPAGATE BLANKS
         BR    14                  EXIT TO CALLER
         SPACE 2
EXMSG1   EQU   *
         MVI   FLAG4,MSG1          IEEUNIT3 - WRITE MSG1A
         B     EXUN3A              GO TO IEEUNIT3
         EJECT
***********************************************************************
*                                                                     *
*  EXECUTED INSTRUCTIONS                                              *
*                                                                     *
***********************************************************************
TESTMASK EQU   *
         TM    N0(POINT),N0        EXECUTED TEST UNDER MASK
LABMOVE  EQU   *
         MVC   ZERO(ZERO,TEXTPTR),ZERO(SCRATCH)   LABEL MOVE
COMPUNIT EQU   *                                              MG YM3885
         CLI   UCBUA,N0            COMP DEV ADDR LESS CHANNEL MG YM3885
         SPACE 2
***********************************************************************
*                                                                     *
*                             EQUATES                                 *
*                                                                     *
***********************************************************************
R0       EQU   0                   GENERAL REGISTER NOTATION
ADDRPT   EQU   1                   *                       *
ADDR     EQU   2                   *                       *
SCRATCH  EQU   3                   *                       *
R1       EQU   1                   *                       *
R3       EQU   3                   *                       *
WORK     EQU   4                   *                       *
R5       EQU   5                   *                       *
POINT    EQU   5                   *                       *
LINEOUT  EQU   6                   *                       *
R7       EQU   7                   *                       *
UCBADDR  EQU   7                   *                       *
R8       EQU   8                   *                       *
TEXTPTR  EQU   8                   *                       *
OUTWTO   EQU   7                   *                       *
R9       EQU   9                   *                       *
WBASE    EQU   10                  *                       *
XSABASE  EQU   11                  *                       *
PBASE    EQU   12                  *                       *
R13      EQU   13                  *                       *
RETURN   EQU   14                  *                       *
R15      EQU   15                  *                       *
         SPACE 2
NO       EQU   0                   LENGTHS AND DISPLACEMENTS
N0       EQU   0                   *          *            *
N1       EQU   1                   *          *            *
N2       EQU   2                   *          *            *
N3       EQU   3                   *          *            *
N4       EQU   4                   *          *            *
N5       EQU   5                   *          *            *
N6       EQU   6                   *          *            *
N7       EQU   7                   *          *            *
N8       EQU   8                   *          *            *
N9       EQU   9                   *          *            *
N12      EQU   12                  *          *            *
N13      EQU   13                  *          *            *
N14      EQU   14                  *          *            *
N28      EQU   28                  *          *            *
N72      EQU   72                  *          *            *
N255     EQU   255                 *          *            *
L1       EQU   1                   *          *            *
L3       EQU   3                   *          *            *
L4       EQU   4                   *          *            *
L2       EQU   2                   *          *            *
L8       EQU   8                   *          *            *
L12      EQU   12                  *          *            *
L24      EQU   24                  *          *            *     A36165
L32      EQU   32                  *          *            *
L71      EQU   71                  *          *            *
ATE      EQU   X'80'               CONTROL LINE
FORE     EQU   X'40'               LABEL LINE
THREE    EQU   X'30'               DATA END LINE
TWO      EQU   X'20'               DATA LINE
ZERO     EQU   0                   *          *            *
ONE      EQU   1                   *          *            *
CVT      EQU   16                  ADDRESS OF CVT POINTER
F0       EQU   C'0'                EBCDIC ZERO
F1       EQU   C'1'                EBCDIC 1
F2       EQU   C'2'                EBCDIC 2
BLANK    EQU   C' '                BLANK
ZEROFOX  EQU   X'0F'               MASK TO ZERO SUBCHANNEL    MH Y02139
XFF      EQU   X'FF'               UCB LOOKUP TABLE STOPPER   JE YM6838
         EJECT
***********************************************************************
*                                                                     *
*     MESSAGES                                                        *
*                                                                     *
***********************************************************************
MSG2A    DC    C'IEE452I UNIT STATUS NUMBER OF UNITS REQUESTED EXCEEDS X
               NUMBER AVAILABLE'        ERROR MESSAGE 2
MSG2AEND EQU   *                   END PF MSG2A
MSG2ALGN EQU   MSG2AEND-MSG2A      LENGTH OF MSG2A
HEAD     DC    C'IEE450I   .  .   UNIT STATUS'
HEADEND  EQU   *                   END OF HEADING LINE
HEADLGN  EQU   HEADEND-HEAD
*              THE FIRST NINE BYTES OF LABELA ARE ALSO USED FOR LABELO
LABELO   EQU   *                  MUST IMMEDIATELY PRECEED LABELA
LABELA   DC    C'UNIT TYPE STATUS  VOLSER VOLSTATE'
LABAEND  EQU   *                   END OF LABELA
LABALGN  EQU   LABAEND-LABELA      LENGTH OF LABELA
LABOLGN  EQU   9                  LENGTH FOR OFFLINE LABEL
         EJECT
***********************************************************************
*                                                                     *
*                             CONSTANTS                               *
*                                                                     *
***********************************************************************
ONLGN    DC    H'36'               FIRST HALF OF ONLINE LINE LENGTH
OFFLGN   DC    H'10'               LENGTH OF ONE OFFLINE ENTRY
         DS    0F
FOXAND   DC    XL4'0000FFFF'       ANDING MASK                 @Z30AAJE
*                                                              @ZM30152
FOXFOX   DC    XL2'FFFF'           END OF UCB TABLE            @Z30AAJE
DCODE    DC    XL4'09800000'       DESCRIPTOR CODES 5,8,9 AND
*                                  ROUTING CODES (0)
         EJECT
*********************************************************************
*                                                                     *
*        DSECTS                                                       *
*                                                                     *
*********************************************************************
CVTDSECT DSECT
         CVT
         EJECT
UCBDSECT DSECT
         IEFUCBOB
         EJECT
XSASET   DSECT
         IEECHAIN
         ORG   CHAIN+L32
         IEEXSA
         ORG   XSA+L24                                           A36165
WORKING  DS    5F                  WORKING STORAGE
DEVNOLN  EQU   WORKING+L8          NUMBER OF DEVICES PER LINE
DEVSPRE  EQU   DEVNOLN+L2          NUMBER OF DEVICES DISPLAYED
LASTUCB  EQU   DEVSPRE+L3          STARTING UCB ADDRESS/BIN NUMBER
FROMID   EQU   LASTUCB+L4          REQUESTING CONSOLE ID
SAVE     DS    21F                 SAVE AREA BETWEEN MODULES
UNITADDA DS    C                   DEVICE ADDRESS FROM EBCDIC MH Y02139
UNITADDB DS    C                   DEVICE ADDRESS FROM HEX    MH Y02139
         ORG   SAVE+L12
PACKWORD DS    F
CVBWORD   DS   F
         EJECT
WORKAREA DSECT
         SPACE 2
         DS    0D
UCBADDRA DS    7H                  STORAGE FOR UCB ADDRESSES
UCBADDRB DS    H                   FOUND BY UNIT4
SWORD    DC    3F'0'               SAVE AREA ACROSS MODULES
UNIT1    DS    F                   HOLD ADDRESS ONE
UNIT2    DS    F                   HOLD ADDRESS TWO
UNIT3    DS    F                   HOLD ADDRESS THREE
         SPACE 4
FLAG2    DC    X'00'               COMMUNICATIONS BYTE
ONLINS   EQU   128                 DISPLAY ONLINE DEVICES
OFFLNS   EQU   64                  DISPLAY OFFLINE DEVICES
ALLS     EQU   32                  DISPLAY OFFLINE + ONLINE DEVICES
SUFEXIST EQU   16                  REQUESTING CONSOLE IS A CRT DEVICE
SINGLE   EQU   8                   SINGLE DEVICE REQUEST
COREGOT  EQU   4                   CORE GOTTEN FOR REGION
OPEN22   EQU   2                   OPEN FLAG
R13UCBNM EQU   1                   FLAG FOR COMPSET ROUTINE
         SPACE 2
FLAG3    DC    X'00'               COMMUNICATIONS FLAGS
U2       EQU   128                 SCAN CDE'S FOR DEVNAMET
UNIT3RET EQU   64                  IEE22110 RETURNING TO IEE21110
OPEN332  EQU   32                  OPEN FLAG
DEVDEF   EQU   16                  DEVICE ADDRESS DEFAULTED
HEADOUTF EQU   8                   HEADING AND LABELS ARE OUT
OPEN34   EQU   4                   OPEN FLAG
OPEN32   EQU   2                   OPEN FLAG
OPEN31   EQU   1                   OPEN FLAG
         SPACE 2
*  FLAG4 BYTE IS USED FOR COMMUNICATIONS BETWEEN IEEUNIT3 AND THE
*  OTHER  D UNIT  MODULES.
FLAG4    DC    XL1'00'             COMMUNICATIONS BYTE        MC
EXITBIT  EQU   X'00'               RETURN TO CALLER           MC
MSG1     EQU   X'04'               ISSUE MESSAGE MSG1A        MC
MSG3     EQU   X'08'               ISSUE MESSAGE MSG3A        MC
MSG4     EQU   X'0C'               ISSUE MESSAGE MSG4A        MC
         SPACE 2
FLAG5    DS    XL1
NODEF    EQU   X'80'               LINE DEFAULT NOT TAKEN
INITENT  EQU   X'40'               INITIAL ENTRY TO UNIT STATUS FOUR
INITUNT2 EQU   X'20'               INITIAL ENTRY TO UNIT STATUS TWO
CRTMSG2  EQU   X'10'               MSG2 WITH HEADINGS FOR DIDOCS
UN4BINLT EQU   X'08'               LASTBIN FLAG FOR UNIT4
DISPBIT  EQU   X'04'               VALID ADDRESS FOUND
DISPEND  EQU   X'02'               END OF FRAME AND DISPLAY
OPEN51   EQU   X'01'               OPEN FLAG
         SPACE 2
         DS    0F                  FORCE FULL WORD FOR WTO
WTOSET   DS    XL1                 ZERO
TEXTLEN  DS    XL1                 OUTPUT LENGTH
LENGTH   EQU   76                  FULL LINE
MCSONE   DS    XL1                 MCS FLAGS PART ONE
SETONE   EQU   X'E1'               QUEUE BY ID, ROUTING + DESCRIPTOR
*                                  CODES EXIST, COMMAND RESPONSE, QUEUE
*                                  BY ID ONLY.
MCSTWO   DS    XL1                 MCS FLAGS PART TWO
SETTWO   EQU   X'40'               MLWTO
         SPACE 1
MSGT     DS    0C                  BEGIN TEXT LINE
TEXT     DS    XL72                TEXT FIELD
         ORG   TEXT
TITLEID  DS    XL8                 MESSAGE ID NUMBER
TITLEDGH DS    XL3                 HOURS.
TITLEDGM DS    XL3                 MINUTES.
TITLEDGS DS    XL2                 SECONDS
TITLENM  DS    XL11               NAME OF DISPLAY
         ORG   TEXT
UCBN1    DS    XL5                 UNIT ADDRESS
MODL1    DS    XL5                 DEVICE TYPE
USTAT1   DS    XL1                 STATUS (O,A,S,C,OFFLINE)
USTAT2   DS    XL4                 STATUS (-BSY,-MTP,-NRD)
USTAT3   DS    XL3                 STATUS (-R)
VOLID1   DS    XL7                 VOLUME SERIAL NUMBER
VOST1A   DS    XL4                 VOLUME STATE (PUB,PRIV,STRG)
VOST1B   DS    XL6                 VOLUME STATE (-REMOV,-RSDNT,-RSERV)
         SPACE 2
         ORG   TEXT+72            ORG FOR WTO TAIL
DESCODE  DS    XL2                 DESCRIPTOR CODES
RTECODE  DS    XL2                 ROUTING CODES
TYPEFLAG DS    XL2                 MESSAGE TYPES
AID      DS    XL1                AREA ID FIELD
LNUM     DS    XL1                NUMBER OF LINES
         SPACE 2
HOLD2    DS    F                   MLWTO ID/ CONSOLE ID FOR WTO
NODEVICS DS    H                   NUMBER OF UNITS REQUESTED
NODEVDEF EQU   100                 DEFAULT
         SPACE 2
         SPACE 4
*  FLAG1 BYTE IS SET BY IEEUNIT1 TO REFLECT THE DEVICE TYPE(S)
*  REQUESTED BY THE D U  COMMAND.  THE BITS SET CORRESPOND TO THE BIT
*  SETTINGS IN BYTE 3 OF THE UCBTYP FIELD.
         SPACE 2
FLAG1    DC    X'00'               FLAG1 FIELD
TATYPE   EQU   UCB3TAPE           TAPE DEVICES
TPTYPE   EQU   UCB3COMM           TELECOMMUNICATIONS DEVICES
DATYPE   EQU   UCB3DACC           DIRECT ACCESS DEVICES
GRTYPE   EQU   UCB3DISP            GRAPHIC DISPLAY DEVICES
URTYPE   EQU   UCB3UREC+UCB3CHAR        UNIT RECORD + CHAR RDR DEVICES
ALLTYPE  EQU   TATYPE+TPTYPE+DATYPE+GRTYPE+URTYPE     ALLDEVICE TYPES
CTCTYPE  EQU   X'41'           CHANNEL-CHANNEL ADAPTOR TYPE JE @ZM32568
         SPACE 2
IDFLD    DS    XL1                AREA ID SAVE AREA
WEND     EQU   *                   END OF WORK AREA
WAL      EQU   WEND-WORKAREA       LENGTH OF WORK AREA
LOW      EQU   UNIT1
CURRENT  EQU   UNIT2
HIGH     EQU   UNIT3
         END
