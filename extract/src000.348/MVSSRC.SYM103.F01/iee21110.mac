         TITLE 'IEE21110  UNIT STATUS TWO, PART OF SVC 110'
IEE21110 CSECT
***********************************************************************
*                                                                     *
* STATUS                                                              *
*    CHANGE LEVEL=0 VS2 RELEASE 4                                     *
*     PTMS FIXED --ZM32568                                            *
*     APARS FIXED -- ZA09316,ZA11454,ZA13204,ZA17706                  *
*                                                                     *
* FUNCTION                                                            *
*    CREATE DISPLAY LINE ENTRIES                                      *
*    LOAD DEVICE NAME TABLE                                           *
*                                                                     *
* ENTRY POINTS                                                        *
*         IEE21110                                                    *
*                                                                     *
* INPUT                                                               *
*    REG 11 CONTAINS THE CSCB ADDRESS WHICH CONTAINS ALL OTHER PTRS   *
*                                                                     *
* OUTPUT                                                              *
*    DISPLAY LINE ENTRIES                                             *
*                                                                     *
* EXTERNAL REFERENCES                                                 *
*         SVC 8    (LOAD)                                             *
*                                                                     *
* EXITS,NORMAL                                                        *
*         IEE23110 TO ISSUE DISPLAY LINE BUILT                        *
*                                                                     *
* EXITS,ERROR                                                         *
*             NONE                                                    *
*                                                                     *
* TABLES/WORKAREAS                                                    *
*    CSCB                                                             *
*    XSA                                                              *
*    DEVICE NAME TABLE                                                *
*    WORKAREA                                                         *
*    UCB                                                              *
*                                                                     *
* ATTRIBUTES                                                          *
*    TYPE IV SVC, PRIVILEGED, RE-ENTRANT                         A36790
*                                                                     *
* CHARACTER CODE DEPENDENCY                                           *
*    NONE                                                             *
*                                                                     *
* NOTES                                                               *
*    NONE                                                             *
*                                                                     *
***********************************************************************
         EJECT
         BALR R12,0
         USING *,R12
         B     ICATCH              BRANCH AROUND EYECATCHER   MB YM1902
         DC    CL8'IEE21110'
         DC    CL8'&SYSDATE'       DATE LAST ASSEMBLY
         DC    XL2'5043'           JULIAN DATE LAST CHANGE
         DC    S(*)                BASE DISPL. OF HERE
ZAPAREA  DC    10CL4'ZAP '         PATCH AREA
ICATCH   EQU   *                                              MB YM1902
         USING CSCB,R11
         L     R10,XAL             OBTAIN WORKAREA ADDRESS
         USING WORKAREA,R10
         TM    FLAG5,INITUNT2      FIRST TIME IN
         BO    LOADSTRT           YES, LOAD DEVICE NAME TABLE MB YM1902
         L     R13,SWORD+L8        RESTORE NAME TABLE ADRESS  MB YM1902
         B     SETEXT              GO SET UP TEXT             MB YM1902
LOADSTRT EQU   *
         NI    FLAG5,L255-INITUNT2     TURN OFF FLAG
         LOAD  EP=DEVNAMET         LOAD DEVICE NAME TABLE
         LR    R13,R0              GET DEVICE NAME TABLE ADDRESS
         SPACE 2
***********************************************************************
*                                                                     *
*     SCAN UCB'S BASED ON COMMAND INPUT, BUILD DISPLAY LINE           *
*     OBTAIN DATA FROM UCB AND DEVICE NAME TABLE                      *
*                                                                     *
***********************************************************************
SETEXT   EQU   *
         LA    R8,TEXT             ESTABLISH LINE BASE
         USING TEXT,R8
         USING UCB,R7
         LH    R15,DEVNOLN         GET NUMBER OF DEVICES IN LINE
START    EQU   *
         LA    R9,UCBADDRA         POINT TO ADDRESS LIST
SCAN1    EQU   *
         LH    R7,ZERO(R9)         GET FIRST ADDRESS
         SLL   R7,L16              CLEAR TOP
         SRL   R7,L16              HALF OF REGISTER
SCAN2    EQU   *
         LH    R5,DEVSPRE          GET NUMBER ALREADY DONE
         LA    R5,ONE(R5)          BUMP BY THIS DEVICE
         STH   R5,DEVSPRE          SAVE COUNT
         MVC   UCBN1(L3),UCBNAME   MOVE UNIT ADDRESS TO LINE
         TM    UCBTBYT3,UCB3COMM   COMMUNICATION DEVICE TYEP
         BO    SCAN5               YES
         L     R2,ZERO(R13)        R2 = NO.DEVICE NAME TABLE ENTRIES
         LR    R14,R13             POINT TO TABLE
         USING DNTMASK,R14
         LA    R14,L4(R14)         BUMP PAST NUMBER OF ENTRIES
SCAN3    CLC   UCBTYP+L2(L2),DNTUCBTP   MATCHING UCBTYPES
         BE    SCAN4               YES, MOVE TYPE
         LA    R14,L12(R14)        BUMP TO NEXT ENTRY
         BCT   R2,SCAN3            TRY AGAIN
UNDMOVE  MVC   MODL1(L3),UND       UNDEFINED TYPE
         B     SCAN7               PROCEED
SCAN4    EQU   *
         MVC   MODL1(L4),DNTTYPE   MOVE DEVICE TYPE TO LINE
         CLI   UCBTYP+L2,DATYPE    IS THIS A DA DEVICE         @ZA09316
         BNE   SCAN7                                           @ZA09316
         CLI   UCBTYP+L3,MOD11     IS THIS 3330 MOD11          @ZA09316
         BNE   SCAN4A                                          @ZA09316
         MVI   MODL1+L3,SUFFIX1    MARK WITH A 1               @ZA17706
         B     SCAN7                                           @ZA09316
SCAN4A   EQU   *                                               @ZA09316
         CLI   UCBTYP+L3,MOD1      IS THIS 3330 MOD1           @ZA09316
         BNE   SCAN7                                           @ZA09316
         TM    UCBTYP+L1,VIRTUAL   IS THIS MSS                 @ZA09316
         BNO   SCAN7                                           @ZA09316
         MVI   MODL1+L3,SUFFIXV    MARK WITH A V               @ZA17706
         B     SCAN7               PROCEED
SCAN6    EQU   *
         LA    R3,L5(R3)           BUMP TO NEXT ENTRY
         BCT   R1,SCAN5A           LOOP CONTROL
         B     UNDMOVE             NOT FOUND  UNDEFINED
CTCMOVE  MVC   MODL1(L4),CTCID     CTC TYPE                 JE @ZM32568
         B     SCAN7               PROCEED                  JE @ZM32568
SCAN5    EQU   *
         SR    R1,R1               CLEAR REGISTER
         IC    R1,AAA              INSERT NUMBER OF ENTRIES
         LA    R3,AAA+ONE          POINT TO FIRST ENTRY
SCAN5A   EQU   *
         SR    R2,R2               CLEAR REGISTER
         IC    R2,UCBTBYT4         GET UNIT TYPE              MH Y01572
         TM    UCBTBYT4,F0         ADAPTOR TYPE ALL ONES       @Z40XXJE
         BO    SCAN5B              YES,DON'T CLEAR HI NIMBLE   @Z40XXJE
         SLL   R2,L28              IGNORE ADAPTER             MH Y01572
         SRL   R2,L28                 TYPE                    MH Y01572
SCAN5B   DS    0H                                              @Z40XXJE
         CLI   UCBTBYT3,CTCTYPE    CHANNEL-CHANNEL ADAPTOR  JE @ZM32568
         BE    CTCMOVE             YES, MOVE IN ID          JE @ZM32568
         EX    R2,SCAN5TST         COMPARE
         BNE   SCAN6               NOT EQUAL GET ANOTHER ENTRY
         MVC   MODL1(L4),ONE(R3)   MOVE DEVICE TYPE
SCAN7    EQU   *
         DROP  R14
         TM    FLAG2,OFFLNS        OFFLINE DISPLAY
         BO    SCANEND             YES, BUILD ANOTHER ENTRY
         TM    SRTESTAT,SRTEONLI   IS DEVICE ONLINE
         BO    SCAN7A              CONTINUE TEST
         TM    SRTESTAT,SRTEALOC   IS DEVICE ALLOCATED       ME SA52720
         BO    ALLOCATE            YES,  MUST BE T/P DEVICE  ME SA52720
         MVC   USTAT1(L7),OFFLIN    PUT OFFLINE INTO TEXT
         B     SCANEND             END OF SCAN
SCAN7A   EQU   *
         TM    SRTESTAT,USYSPCON   SYSRES OR CONSOLE
         BNO   SCAN7C              NO
         TM    SRTESTAT,SRTEPRES   RESIDENT VOLUME
         BNO   SCAN7B              NO, THEN CONSOLE
         LA    R1,SYSRES           ADDRESS OF SYSRES
MSTAT1   EQU   *
         MVC   USTAT1(L1),ZERO(R1)      MOVE CHARACTER
         B     SCAN7E              PROCEED
SCAN7B   EQU   *
         LA    R1,CONSOL           MOVE
         B     MSTAT1              CHARACTER
SCAN7C   EQU   *
         TM    SRTESTAT,USALLOC    ALLOCATED
         BNO   SCAN7D              NO
ALLOCATE EQU   *                                             ME SA52720
         LA    R1,ALLOC            YES
         B     MSTAT1              MOVE CHARACTER
SCAN7D   EQU   *
         LA    R1,ONLINE           ASSUME ONLINE
         B     MSTAT1              MOVE CHARACTER
SCAN7F   EQU   *
         TM    UCBTBYT3,USTAORDA   TAPE OR DASD
         BZ    SCAN7H              NO, MOUNT NOT POSSIBLE
         TM    UCBTBYT3,UCB3DACC   DASD
         BNO   SCAN7G              NO, CHECK TAPE
         TM    SRTEDMCT,USMNTPOS   MOUNT PENDING
         BNO   SCAN7H              NO
SCAN7F1  LA    R1,MOUNT            YES
         B     MSTAT2              MOVE WORD
SCAN7G   EQU   *
         TM    UCBFL2,USNREADY     TAPE AND NOT READY
         BNO   SCAN7H              NO
         TM    SRTEDMCT,USMNTPOS   MOUNT PENDING
         BO    SCAN7F1             YES
SCAN7H   EQU   *
         TM    UCBFL2,USNREADY     NOT READY
         BNO   SCAN7I              NO
         LA    R1,NREADY           YES
         B     MSTAT2              MOVE WORD
SCAN7E   EQU   *
         TM    UCBFL2,USBUSY       DEVICE BUSY
         BNO   SCAN7F              NO
         LA    R1,BUSY             YES
MSTAT2   EQU   *
         MVC   USTAT2(L4),ZERO(R1)      MOVE WORD
SCAN7I   EQU   *
         LR    R5,R7               LOAD UCB ADDRESS
         IOSGEN DETECTOR,UCB=(5)                               @ZA13204
         BZ    SCAN8               NOT RESERVED DASD
         MVC   USTAT3(L2),RESR     MOVE WORD
SCAN8    EQU   *
         TM    UCBTBYT3,USTAORDA   TAPE OR DASD
         BZ    SCANEND             NO
         TM    UCBTBYT3,UCB3TAPE   TAPE
         BNO   SCAN8A              NO
         TM    SRTEVOLI,FF         INTERNAL NUMBER
         BO    SCAN9               YES, DO NOT USE
SCAN8A   EQU   *
         NC    SRTEVOLI(L6),SRTEVOLI    Q.   VOLUME SERIAL EQUAL ZERO
         BZ    SCAN9               YES  SKIP IT
         MVC   VOLID1(L6),SRTEVOLI      MOVE SERIAL NUMBER
SCAN9    EQU   *
         TM    SRTESTAB,SRTEBPRV   PRIVATE VOLUME
         BNO   SCAN9A              NO
         LA    R1,PRIV             YES
MVSTAT1  EQU   *
         MVC   VOST1A(L4),ZERO(R1)      MOVE WORD
         B     SCAN9C              TEST FOR OTHER VOLUME STATES
SCAN9A   EQU   *
         TM    SRTESTAB,SRTEBPUB   PUBLIC VOLUME
         BNO   SCAN9B              NO
         LA    R1,PUB              YES
         B     MVSTAT1             MOVE WORD
SCAN9B   EQU   *
         TM    SRTESTAB,SRTEBSTR   STORAGE
         BNO   SCAN9C              NO
         LA    R1,STRG             YES
         B     MVSTAT1             MOVE WORD
SCAN9C   EQU   *
         TM    SRTESTAT,SRTEPRES   PERM/RES VOLUME
         BNO   SCAN9D              NO
         LA    R1,RESDNT           YES
MVSTAT2  EQU   *
         MVC   VOST1B(L6),ZERO(R1)      MOVE WORD
         B     SCANEND             DONE
SCAN9D   EQU   *
         TM    SRTESTAT,SRTERESV   RESERVED VOLUME
         BNO   SCAN9E              NO
         LA    R1,RESERV           YES
         B     MVSTAT2             MOVE WORD
SCAN9E   EQU   *
         LA    R1,REMOVE           MOVE DEFAULT
         B     MVSTAT2             MOVE WORD
         SPACE 2
***********************************************************************
*                                                                     *
*  DETERMINE IF LINE CAN BE WRITTEN OR SET UP FOR NEXT ENTRY          *
*                                                                     *
***********************************************************************
SCANEND1 EQU   *
         L     R15,SAVE+L8         GET DEVICE COUNT
SCANEND  EQU   *
         LA    R9,L2(R9)           POINT TO NEXT UCB ADDRESS
         CLC   ZERO(L2,R9),NOADDRES    ANY ADDRESS
         BE    EXIT4               NO, GO TO IEEUNIT4
         BCT   R15,TESTOFF         RUN OUT OF DEVICES
         B EXIT4                   YES  RETURN TO IEEUNIT4
TESTOFF  EQU   *
         DROP  R8
         TM    FLAG2,OFFLNS        OFFLINE DISPLAY
         BO    ADDOFF              YES
         LA    R8,DISP(R8)         BASE FOR SECOND HALF OF LINE
         B     SCAN1               BUILD SECOND HALF
ADDOFF   EQU   *
         LA    R8,DISPO(R8)        INCREMENT FOR OFFLINE DISPLAY
         B     SCAN1               BUILD NEXT PORTION OF LINE
EXIT4    EQU   *
         MVI   XAX+4,X'F3'        SET NAME FOR EXIT TO UNIT4
XCTL     EQU   *
         ST    R13,SWORD+L8        SAVE TABLE POINTER
         LA    R15,XSA             POINT TO PARA LIST
         XCTL  SF=(E,(R15))        TRANSFER CONTROL
         EJECT
***********************************************************************
*                                                                     *
*     EXECUTED STATEMENTS                                             *
*                                                                     *
***********************************************************************
SCAN5TST EQU   *
         CLI   ZERO(R3),ZERO       USED BY SCAN5
         SPACE 2
***********************************************************************
*                                                                     *
*     EQUATES                                                         *
*                                                                     *
***********************************************************************
R0       EQU   0                   GENERAL REGISTER NOTATION
R1       EQU   1                   *                       *
R2       EQU   2                   *                       *
R3       EQU   3                   *                       *
R4       EQU   4                   *                       *
R5       EQU   5                   *                       *
R6       EQU   6                   *                       *
R7       EQU   7                   *                       *
R8       EQU   8                   *                       *
R9       EQU   9                   *                       *
R10      EQU   10                  *                       *
R11      EQU   11                  *                       *
R12      EQU   12                  *                       *
R13      EQU   13                  *                       *
R14      EQU   14                  *                       *
R15      EQU   15                  *                       *
         SPACE 3
L1       EQU   1                   *         *             *
L2       EQU   2                   *         *             *
L3       EQU   3                   *         *             *
L4       EQU   4                   *         *             *
L5       EQU   5                   *         *             *
L6       EQU   6                   *         *             *
L7       EQU   7                   *         *             *
L8       EQU   8                   *         *             *
L12      EQU   12                  *         *             *
L16      EQU   16                  *         *             *
L24      EQU   24                  *         *             *     A36165
L28      EQU   28                  *         *             *  MH Y01572
L32      EQU   32                  *         *             *
L255     EQU   255                 *         *             *
ZERO     EQU   0                   *         *             *
ONE      EQU   1                   DISPLACEMENT OF ONE
DISPO    EQU   10                  LENGTH OF OFFLINE DEVICE ENTRY
DISP     EQU   36                  TEXT+DISP IS SECOND HALF OF LINE
USYSPCON EQU   X'02'               SYSRES OR CONSOLE DEVICE
USALLOC  EQU   X'08'               ALLOCATED DEVICE MASK
USNREADY EQU   X'40'               NOT READY DEVICE MASK
USTAORDA EQU   X'A0'               TAPE OR DIRECT ACCESS DEVICE MASK
USMNTPOS EQU   X'80'               POSSIBLE MOUNT PENDING MASK
USBUSY   EQU   X'80'               DEVICE BUSY MASK
FF       EQU   X'FF'               USED AS TERMINATOR
F0       EQU   X'F0'               MASK FOR ADAPTOR TYPE       @Z40AKJE
         EJECT
***********************************************************************
*                                                                     *
*     CONSTANTS                                                       *
*                                                                     *
***********************************************************************
         DS    0F
NOADDRES DC    H'0'                USED TO VERIFY UCBARDRA ADDRESS
*
*                                  THE FOLLOWING CONSTANTS ARE USED
*                                  TO BUILD THE DISPLAY LINE
CONSOL   DC    C'C'                SYSTEM CONSOLE
SYSRES   DC    C'S'                SYSRES DEVICE
ONLINE   DC    C'O'                DEVICE IS ONLINE
OFFLIN   DC    C'OFFLINE'          DEVICE IS OFFLINE
ALLOC    DC    C'A'                DEVICE IS ALLOCATED
NREADY   DC    C'-NRD'             DEVICE IS NOT READY
BUSY     DC    C'-BSY'             DEVICE IS BUSY
MOUNT    DC    C'-MTP'             DEVICE HAS MOUNT REQUEST OUTSTANDING
PUB      DC    C' PUB'             PUBLIC VOLUME
PRIV     DC    C'PRIV'             PRIVATE VOLUME
STRG     DC    C'STRG'             STORAGE VOLUME
REMOVE   DC    C'/REMOV'           VOLUME IS REMOVEABLE
RESDNT   DC    C'/RSDNT'           VOLUME IS RESIDENT
RESERV   DC    C'/RSERV'           VOLUME IS RESERVED
UND      DC    C'UND '             DEVICE TYPE IS UNDEFINED
RESR     DC    C'-R'               RESERVED SHARED DASD DEVICE
CTCID    DC    CL4'CTC '         CHANNEL-CHANNEL ADAPTOR ID JE @ZM32568
*
*                                  THE FOLLOWING TABLE IS USED TO
*                                  IDENTIFY T/P CONTROL UNITS BY
*                                  MATCHING WITH THE UNIT TYPE
*                                  FIELD OF THE UCB.
*
AAA      DC    XL1'08'             NUMBER OF ENTRIES           @Z40XXJE
AAA1     DC    XL1'01'             UNIT TYPE X1               MH Y01572
         DC    C'2702'             DEVICE TYPE                MH Y01572
AAA2     DC    XL1'02'             UNIT TYPE X2               MH Y01572
         DC    C'2701'             DEVICE TYPE                MH Y01572
AAA3     DC    XL1'03'             UNIT TYPE X3               MH Y01572
         DC    C'2703'             DEVICE TYPE                MH Y01572
AAA4     DC    XL1'04'             UNIT TYPE X4               MH Y01572
         DC    C'2955'             DEVICE TYPE                MH Y01572
AAA5     DC    XL1'05'             UNIT TYPE X5               MH Y01572
         DC    C'3705'             DEVICE TYPE                MH Y01572
AAA6     DC    XL1'06'             UNIT TYPE X6               MH Y01572
         DC    C'UND '             DEVICE TYPE                MH Y01572
AAA7     DC    XL1'07'             UNIT TYPE X7               MH Y01572
         DC    C'UND '             DEVICE TYPE                MH Y01572
AAA8     DC    XL1'F1'             UNIT TYPE                   @Z40XXJE
         DC    CL4'3791'           DEVICE TYPE 3791 LOCAL      @Z40XXJE
         EJECT
***********************************************************************
*                                                                     *
*     DSECTS                                                          *
*                                                                     *
***********************************************************************
UCB      DSECT
         IEFUCBOB
         EJECT
CSCB     DSECT
         IEECHAIN
         ORG   CHAIN+L32
         IEEXSA
         ORG   XSA+L24                                           A36165
         DS    0F
WORKING  DS    5F                  WORKING STORAGE
DEVNOLN  EQU   WORKING+L8          NUMBER OF DEVICES PER LINE
DEVSPRE  EQU   DEVNOLN+L2          NUMBER OF DEVICES DISPLAYED
LASTUCB  EQU   DEVSPRE+L3          STARTING UCB ADDRESS/BIN NUMBER
FROMID   EQU   LASTUCB+L4          REQUESTING CONSOLE ID
SAVE     DS    21F                 SAVE AREA BETWEEN MODULES
         ORG   SAVE+L12
PACKWORD DS    F                   WORD USED FOR PACK INSTRUCTIONS
CVBWORD   DS   F                   WORD USED FOR CONVERT TO BINARY
         EJECT
DNTMASK  DSECT
*                                  DEVICE NAME TABLE ENTRY MASK
DNTTYPE  DS    XL8                 DEVICE TYPE IN EBCDIC
DNTJUNK  DS    XL2                 UNUSED BY THIS SUPPORT
DNTUCBTP DS    XL2                 FIELD MATCHED AGAINST UCB
         EJECT
WORKAREA DSECT
         SPACE 2
         DS    0D
UCBADDRA DS    7H                  UCB ADDRESS LIST
UCBADDRB DS    H                   FROM UNIT4
SWORD    DC    3F'0'               SAVE AREA ACROSS MODULES
UNIT1    DS    F                   HOLD ADDRESS ONE
UNIT2    DS    F                   HOLD ADDRESS TWO
UNIT3    DS    F                   HOLD ADDRESS THREE
         SPACE 4
FLAG2    DC    X'00'               COMMUNICATIONS BYTE
ONLINS   EQU   128                 DISPLAY ONLINE DEVICES
OFFLNS   EQU   64                  DISPLAY OFFLINE DEVICES
ALLS     EQU   32                  DISPLAY OFFLINE + ONLINE DEVICES
SUFEXIST EQU   16                  REQUESTING CONSOLE IS A CRT DEVICE
SINGLE   EQU   8                   SINGLE DEVICE REQUEST
COREGOT  EQU   4                   GET REGION DONE
OPEN22   EQU   2                   OPEN FLAG
R13UCBNM EQU   1                   FLAG FOR COMPSET ROUTINE
         SPACE 2
FLAG3    DC    X'00'               COMMUNICATIONS FLAGS
U2       EQU   128                TELL UNIT2 ENTRY FOR SCAN OF CDES
UNIT3RET EQU   64                  IEE22110 RETURNING TO IEE21110
OPEN332  EQU   32                  OPEN FLAG
DEVDEF   EQU   16                  DEVICE ADDRESS DEFAULTED
HEADOUTF EQU   8                   HEADING AND LABELS ARE OUT
OPEN34   EQU   4                   OPEN FLAG
OPEN32   EQU   2                   OPEN FLAG
OPEN31   EQU   1                   OPEN FLAG
         SPACE 2
*  FLAG4 BYTE IS USED FOR COMMUNICATIONS BETWEEN IEEUNIT3 AND THE
*  OTHER  D UNIT  MODULES.
FLAG4    DC    XL1'00'             COMMUNICATIONS BYTE        MB YM1902
EXITBIT  EQU   X'00'               RETURN TO CALLER           MD
MSG1     EQU   X'04'               ISSUE MESSAGE MSG1A        MD
MSG3     EQU   X'08'               ISSUE MESSAGE MSG3A        MD
MSG4     EQU   X'0C'               ISSUE MESSAGE MSG4A        MD
         SPACE 2
FLAG5    DS    XL1
NODEF    EQU   X'80'               LINE DEFAULT NOT TAKEN
INITENT  EQU   X'40'               INITIAL ENTRY TO IEEUNIT4
INITUNT2 EQU   X'20'               SET FOR INITIAL UNIT2 ENTRY
CRTMSG2  EQU   X'10'               MSG2 WITH HEADINGS FOR DIDOCS
UN4BINLT EQU   X'08'               LASTBIN FLAG FOR UNIT4
DISPBIT  EQU   X'04'               VALID ADDRESS FOUND
DISPEND  EQU   X'02'               END OF FRAME AND DISPLAY
OPEN51   EQU   X'01'               OPEN FLAG
         SPACE 2
         DS    0F                  FORCE FULL WORD FOR WTO
WTOSET   DS    XL1                 ZERO
TEXTLEN  DS    XL1                 OUTPUT LENGTH
LENGTH   EQU   76                  FULL LINE
MCSONE   DS    XL1                 MCS FLAGS PART ONE
SETONE   EQU   X'E1'               QUEUE BY ID, ROUTING AND DESCRIPTOR
*                                  CODES EXIST, COMMAND RESPONSE, QUEUE
*                                  BY ID ONLY
MCSTWO   DS    XL1                 MCS FLAGS PART TWO
SETTWO   EQU   X'40'               MLWTO
         SPACE 1
MSGT     DS    0C                  BEGIN TEXT LINE
TEXT     DS    XL72                TEXT FIELD
         ORG   TEXT
TITLEID  DS    XL8                 MESSAGE ID
TITLEDGH DS    XL3                 HOURS.
TITLEDGM DS    XL3                 MINUTES.
TITLEDGS DS    XL2                 SECONDS
TITLENM  DS    XL11                DISPLAY NAME
         ORG   TEXT
UCBN1    DS    XL5                 UNIT ADDRESS
MODL1    DS    XL5                 DEVICE TYPE
USTAT1   DS    XL1                 STATUS (O,A,S,C,OFFLINE)
USTAT2   DS    XL4                 STATUS (-BSY,-MTP,-NRD)
USTAT3   DS    XL3                 STATUS (-R)
VOLID1   DS    XL7                 VOLUME SERIAL NUMBER
VOST1A   DS    XL4                 VOLUME STATE (PUB,PRIV,STRG)
VOST1B   DS    XL6                 VOLUME STATE (-REMOV,-RSDNT,-RSERV)
         ORG   TEXT+72            ORG FOR WTO TAIL
DESCODE  DS    XL2                 DESCRIPTOR CODES
RTECODE  DS    XL2                 ROUTING CODES
TYPEFLAG DS    XL2                 MESSAGE TYPES
AID      DS    XL1                AREA ID FIELD
LNUM     DS    XL1                NUMBER OF LINES
HOLD2    DS    F                   MLWTO ID AND CONSOLE ID
NODEVICS DS    H                   NUMBER OF UNITS REQUESTED
NODEVDEF EQU   100                 DEFAULT
         SPACE 4
*  FLAG1 BYTE IS SET BY IEEUNIT1 TO REFLECT THE DEVICE TYPE(S)
*  REQUESTED BY THE D U  COMMAND.  THE BITS SET CORRESPOND TO THE BIT
*  SETTINGS IN BYTE 3 OF THE UCBTYP FIELD.
         SPACE 2
FLAG1    DC    X'00'               FLAG1 FIELD
TATYPE   EQU   UCB3TAPE           TAPE DEVICES
TPTYPE   EQU   UCB3COMM           TELECOMMUNICATIONS DEVICES
DATYPE   EQU   UCB3DACC           DIRECT ACCESS DEVICES
GRTYPE   EQU   UCB3DISP            GRAPHIC DISPLAY DEVICES
URTYPE   EQU   UCB3UREC+UCB3CHAR        UNIT RECORD + CHAR RDR DEVICES
ALLTYPE  EQU   TATYPE+TPTYPE+DATYPE+GRTYPE+URTYPE     ALLDEVICE TYPES
CTCTYPE  EQU   X'41'  CHANNEL-CHANNEL ADAPTOR TYPE          JE @ZM32568
MOD1     EQU   X'09'                                           @ZA09316
MOD11    EQU   X'0D'                                           @ZA09316
VIRTUAL  EQU   X'08'                                           @ZA09316
SUFFIX1  EQU   C'1'                                            @ZA09316
SUFFIXV  EQU   C'V'                                            @ZA09316
IDFLD    DS    XL1                AREA ID SAVE AREA
WEND     EQU   *                   END OF WORK AREA
WAL      EQU   WEND-WORKAREA       LENGTH OF WORK AREA
         END
