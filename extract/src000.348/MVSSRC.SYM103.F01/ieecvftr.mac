* /* START OF SPECIFICATIONS ****
*02*  PROCESSOR = ASSEMBLER;
**** END OF SPECIFICATIONS ***/
IEECVFTR CSECT
*/*IEECVFTR: CHART */
*/*HEADER
*/*        CHART ??     IEECVFTR  IGC6R07B                    DIDOCS */
*/*2260 I/O ROUTINE 2                               PAGE #    */
*/* E IEECVFTR */
         TITLE 'IEECVFTR IGC6R07B 2260 IO 2'
*                                                                     *
*  STATUS
*     CHANGE LEVEL 0
*     CREATED FOR O/S RELEASE 21, LINE ITEM S21003
*
* FUNCTION
*    PERFORMS A STATUS SWITCH BY MODIFYING FIELDS IN THE
*     DISPLAY CONTROL MODULE
*
* ENTRY POINTS
*         IEECVFTR FOR ALL FUNCTIONS
*
* INPUT
*    REGISTER 1 - POINTER TO CSCB XSA
*
* OUTPUT
*    MODIFIED DISPLAY CONTROL MODULE
*
* EXTERNAL REFERENCES
*         XCTL - FOR EXIT
*
* EXITS,NORMAL
*         IEECVFTG - CLEANUP MODULE ON FIRST PASS
*         IEECVETK - TIMER/INTERPRETER ON 2ND PASS MESSAGE STREAM
*         IEECVETR - 2260 I/O 1 ON 2ND PASS NON MESSAGE STREAM
*
* EXITS,ERROR
*         IEECVET1 - PROCESSOR 1 ON INVALID ENTRY
*
* TABLES/WORK AREAS
*    RESIDENT AND TRANSIENT DCMS AND UCM.  SEE DSECTS AT
*     END OF MODULE
*
* ATTRIBUTES
*    REFRESHABLE, PRIVILEGED, TYPE 4 SVC
*
* CHARACTER CODE DEPENDENCY
*    NONE
*
* NOTES
*    THE FOLLOWING FLAG BITS ARE USED:
*     THE ACTION CULUMN INDICATES WHETHER THE BIT IS TURNED ON, OFF OR
*     JUST TESTED BY THIS MODULE
*
*      NAME       ACTION            FUNCTION
*
*    DCMCMSG3
*     DCMCMRLL      ON          WRITE ROLL MODE MESSAGE
*
*    DCMFLG1
*     DCMOUTPT  ON/OFF/TEST     INDICATES SECOND PASS THROUGH MODULE
*
*    DCMOPTST
*     DCMOPRLL      ON          ROLL MODE
*     DCMOPTAD      OFF         AUTO DELETE
*
*    DCMR3FLG
*     DCMSTSWT    OFF/TEST      STATUS SWITCH REQUIRED
*
*    DCMTIMES
*     DCMOPTTI      ON          DUMMY ROUTE FROM OPTIONS TO TIM/INT
*     DCMOTTMM      ON          ROUTE FROM TIM/INT TO MESSAGE MODULE
*
*    UCMDISP
*     UCMDISPE     TEST         FULL CAPABILITY CONSOLE
*     UCMDISPF     TEST         MESSAGE STREAM CONSOLE
*
         EJECT
**                                                                   **
*                        REGISTER EQUATES                             *
**                                                                   **
X1PARM   EQU   1
XFBRANCH EQU   15
PARM     EQU   1             HOLDS CXSA ADDRESS
RBASE    EQU   4             RES DCM BASE REG
RWTINT   EQU   5             END OF MESG AREA
RLSCRN   EQU   6             LAST MESG LINE PTR
RMSGAL   EQU   7             NO. OF MESSAGE LINES
RADDRL   EQU   8             ADDR OF LAST SCT
UCMBASE  EQU   9             UCM BASE
DCMBASE  EQU   10            DCM BASE
PBASE    EQU   11            PROGRAM BASE
PXSA     EQU   12            CXSA ADDRESS
UCMENTRY EQU   13            UCMENTRY BASE
RETURN   EQU   14            RETURN REGISTER
         EJECT
**                                                                   **
*              INITIALIZE PROGRAM                                     *
**                                                                   **
         BALR  PBASE,N0            SET UP
         USING *,PBASE             ADDRESSABILITY
         B     ICATCH              BRANCH AROUND EYECATCHER
         DC    C'   MODULE IGC6R07B, FICHE IEECVFTR  ' EYECATCHER AND
ZAPAREA  DC    12C'ZAP'               RESERVED FOR FIELD MODIFICATION
ICATCH   LR    PXSA,PARM           SAVE CXSA ADDRESS
         USING CXSA,PXSA           DECLARE CXSA ADDRESS
         L     UCMENTRY,CSAUCM     GET UCM ENTRY ADDRESS
         USING UCMLIST,UCMENTRY    DECLARE UCMENTRY BASE
         L     RBASE,UCMXB         LOAD RES DCM BASE
         USING DCMTSRT,RBASE          ESTABLISH ADDRESSABILITY
         L     DCMBASE,DCMADTRN   LOAD TRANSIENT DCM BASE
         USING DCMSTRT,DCMBASE       DECLARE DCM BASE
         L     UCMBASE,CSACTLM     GET UCM POINTER
         USING UCM,UCMBASE         DECLARE UCM BASE
         MVC   DCMTRACE(DCMTRLEN),DCMTRAC2 SHIFT OLD TRACE ENTRIES
         MVI   DCMTREN1,ID1        PUT CSECT ID INTO
         MVI   DCMTREN2,ID2        NEW TRACE ENTRY
         EJECT
**                                                                   **
*       TEST REASON FOR ENTRY AND TAKE APPROIATE ACTION               *
**                                                                   **
*/* D (YES,%TEST2,NO,PROCEXIT) IS STATUS SWITCH REQUIRED? */
*************************************************************
TESTXX   TM    DCMR3FLG,DCMSTSWT   IS STATUS SWITCH REQUIRED
         BNO   PROCEXIT             NO, CONTINUE WITH NEXT TEST
         SPACE 4
*/*%TEST2: D (NO,TESTEXIT,YES,TYPE) IS REOPEN BIT ON? */
*************************************************************
         TM    DCMCS,DCMCSO        IS REOPEN BIT ON
         BNO   TESTEXIT             NO, TEST FOR SECOND PASS
         SPACE 4
*/*TYPE: D (NO,OUTONLY,YES,) CHANGING TO FULL CAPACITY? */
*************************************************************
TYPE     TM    UCMDISP,UCMDISPE    ARE WE CHANGING TO FULL CAP
         BNO   OUTONLY              NO, SET FOR OUTPUT ONLY
         SPACE 4
*/* P (,COMMON) SET UP FULL CAPACITY PARAMETERS */
*************************************************************
         LA    RMSGAL,N8           SET PARMS FOR FULL CAP
         LA    RADDRL,DCMMSG8      LAST MESSAGE LINE SCT
         LA    RWTINT,DCMINSTR     LAST MESSAGE LINE + 1
         LA    RLSCRN,DCMMLAST     LAST LINE ON SCREEN
         MVI   DCMSEC9,DCMMSGST    STOP SECONDARY SCT
         B     COMMON              GO TO INSERT VALUES
         SPACE 4
*/*OUTONLY: P (,COMMON) SET UP OUTPUT ONLY PARAMETERS */
*************************************************************
OUTONLY  LA    RMSGAL,N11          NUMBER OF MESSAGE LINES
         LA    RADDRL,DCMMSG11     LAST MESSAGE LINE SCT
         LA    RWTINT,DCMWARN      LAST MESSAGE LINE + 1
         LA    RLSCRN,DCMENTR2     LAST LINE ON SCREEN
         MVI   DCMSEC11+N1,DCMMSGST STOP SECONDARY SCT
         MVI   DCMSEC9,N0          REMOVE OLD STOPPER
         MVI   DCMMSG9,N0          REMOVE OLD SCT STOPPER
         EJECT
*/*COMMON: P PUT PARAMETERS IN DCM */
*************************************************************
COMMON   STH   RMSGAL,DCMMSGAL     UPDATE NO. OF MSG LINES
         MVC   DCMRMSAL(N2),DCMMSGAL       IN BOTH DCMS
         ST    RADDRL,DCMADDRL     UPDATE ADDR OF LAST SCT
         ST    RWTINT,DCMWTINT     END OF FULL SCREEN WRITE
         ST    RLSCRN,DCMLSCRN     UPDATE LAST MESG LINE
         MVI   N2(RADDRL),DCMMSGST STOP SCT TABLE
         SPACE 5
*/* P INDICATE REOPEN AND SECOND PASS */
*************************************************************
         OI    DCMCS,DCMCSO        INDICATE REOPEN
         OI    DCMFLG1,DCMOUTPT    INDICATE SECOND PASS
         SPACE 5
*/* P (,EXIT) SET EXIT TO CLEANUP (IEECVFTG) */
*************************************************************
         L     XFBRANCH,DCMNCLN    LOAD CLEANUP ROUTINE ADDRESS
*                                  FOR EXIT TO IEECVFTG
         SPACE 4
**                                                                   **
*                     EXIT PROCEEDURE                                 *
**                                                                   **
*/*EXIT: R XCTL */
*************************************************************
EXIT     EQU   *                       INITIALIZE FOR EXIT
         L     X1PARM,DCMCXSVE     RESTORE CXSA ADDRESS
         BR    XFBRANCH            EXIT ADDRESS ALREADY SET
         SPACE 4
*/*PROCEXIT: P (,EXIT) SET EXIT TO PROC 1 (IEECVET1) */
*************************************************************
PROCEXIT L     XFBRANCH,DCMNPROC   LOAD PROC 1 ADDRESS
         B     EXIT                EXIT TO IEECVET1
         EJECT
*/*TESTEXIT: D (NO,TYPE,YES,) IS THIS SECOND PASS? */
*************************************************************
TESTEXIT TM    DCMFLG1,DCMOUTPT    IS THIS SECOND PASS
         BNO   TYPE                 NO, MODIFY DCM
         SPACE 4
*/* P TURN OFF STATUS SWITCH AND SECOND PASS */
*************************************************************
         NI    DCMR3FLG,X'FF'-DCMSTSWT  TURN OFF STATUS SW. BIT
         NI    DCMFLG1,X'FF'-DCMOUTPT   TURN OFF SECOND PASS SWITCH
         SPACE 4
*/* D (NO,,YES,TIMEXIT) IS CONSOLE NOW MESSAGE STREAM? */
*************************************************************
         TM    UCMDISP,UCMDISPF    IS IT NOW MESSAGE STREAM
         SPACE 4
*/* P (,EXIT) SET EXIT FOR 2260 I/O 1 (IEECVETR) */
*************************************************************
         BO    TIMEXIT
         L     XFBRANCH,DCMIORTN
         B     EXIT                 RETURN TO I/O 1
         SPACE 4
*/*TIMEXIT: P SET ROLL DELETE MODE */
*************************************************************
TIMEXIT  MVC   DCMDEL(N2),RD       FORCE ROLL DELETE MODE
         OI    DCMOPTST,DCMOPRLL   TURN ON ROLL BIT
         NI    DCMOPTST,X'FF'-DCMOPTAD   AUTO DELETE BIT OFF
         OI    DCMTIMES,DCMOTTMM+DCMOPTTI   GO FROM T/I TO MESG
         OI    DCMCMSG3,DCMCMRLL   WRITE ROLL MODE MESG
         SPACE 5
*/* P (,EXIT) SET EXIT TO TIMER/ INTERPRETER (IEECVETK) */
*************************************************************
         L     XFBRANCH,DCMNTIMR   LOAD TIMER INTERPRETER ADDRESS
         B     EXIT                EXIT TO IEECVETK
         EJECT
RD       DC    C'RD'               ROLL DELETABLE MODE
**                                                                   **
*              MISC  EQUATES                                          *
**                                                                   **
ID1      EQU   C'F'                1ST CHARACTER OF CSECT ID
ID2      EQU   C'R'                2ND CHARACTER OF CSECT ID
N0       EQU   0                       NUMBER
N1       EQU   1                       NUMBER
N2       EQU   2                       NUMBER
N3       EQU   3                       NUMBER
N4       EQU   4                       NUMBER
N8       EQU   8                       NUMBER
N11      EQU   11                      NUMBER
         SPACE 5
*/*IEECVFTR: END */
         EJECT
         IHACTM  CXSA
         EJECT
         IEECUCM FORMAT=NEW
         EJECT
         IEERDCM
         EJECT
DCMSTRT  DSECT
         IEECDCM DEVICE=C
         END
