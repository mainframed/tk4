         TITLE 'IEECVET1, ALIAS - IGC5107B - PROCESSOR 1'
IEECVET1 CSECT
         ENTRY IGC5107B
* A 085100-085492,253530-253546,312404-313621                 JE YM6718
* ET1RTY(A,C,D) APPROX. 817000                                 @ZA29665
* CONSWTCH(A) APPROX. 870000                                   @ZA29665
* NOESTAE(C) APPROX. 164000                                    @ZA29401
         EJECT
*
* STATUS       CHANGE LEVEL 1 VS2 RELEASE 3
*
*
* FUNCTION     ROUTE CONTROL TO ROUTINES ON THE BASIS OF FUNCTION
*              NEEDED.
*
* STATUS -
*
*  PTMS FIXED @YM8452
*  APARS FIXED ZA00506,ZA00513,ZA00539,ZA02303,ZA02341,ZA02835,ZA04689,
*              ZA06411,ZA10109,ZA13458,ZA14126,ZA16376,ZA16389,ZA13189,
*              ZA29665,ZA29401
* ENTRY POINT  IEECVET1
*
* INPUT        REGISTER 1 CONTAINS A POINTER TO A PARAMETER LIST MAPPED
*              BY CXSA
*              BIT SETTINGS ON INPUT INDICATING TYPE OF ACTION REQUIRED
*                UCMSTS
*                  UCMAF  ATTENTION
*                  UCMPF  OUTPUT PENDING
*                  UCMCF  CLOSE REQUEST
*                  UCMTA  OPEN PENDING
*                UCMSDS5
*                  UCMSDS5B  INLINE OUTPUT PENDING
*                  UCMSDSD8  OUT OF LINE OUTPUT PENDING
*                UCMDEVC
*                  UCMDEVD  DOM ISSUED
*                DCMCS
*                  DCMCSO   REOPEN DEVICE
*                UCMMODE
*                  HARDBIT  NO HARD COPY
*
*                DCMCOM1
*                  DCMIOPRD  A READ WAS PERFORMED- EXAMINE INPUT
*                                  (FOR DEVICES THAT DO RMI AND READ
*                                   IN SEPARATE I/O)
*                  DCMCOMRM  RMI PERFORMED -CHECK RMI INPUT
*                DCMCOM2
*                   DCMCMIN5 RETURN TO INTERFACE 5
*                DCMCOM3
*                  DCMRTPFK  RETURN TO PFK ROUTINE
*                  DCMVLPFK  COMMAND BEING VERIFIED
*                  DCMOLUNV  NEED UNVIEWABLE MSG MESSAGE
*                  DCMSTSWT  SWITCH USE OF CONSOLE
*                  DCMCMIN7  RETURN TO INTERFACE 7
*                DCMIOCM3
*                  DCMPFKAT  PFK ATTENTION
*                  DCMACPFK  LIGHT ACTIVE PFK
*                  DCMLTPFK  LIGHT ALLOCATED PFKS
*                DCMTIMES
*                  DCMTIMER  TIMER HAS ELAPSED
*                  DCMGROLL  READY FOR ROLL
*                  DCMTASYN  PERFORM CANCEL AFTER AN ASYNCHRONOUS
*                                   ERROR
*                DCMADKP - WORD
*                  IF NON-ZERO, INDICATES ROUTED COMMAND ON Q
*
* OUTPUT       STATUS IN UCM ENTRY AND DCM
*              ON EXIT TO THE SECOND LOAD, REGISTER 2 CONTAINS
*                   A BRANCH CODE ACCORDING TO THE FUNCTION
*                   TO BE PERFORMED
*
* EXTERNAL REFERENCES  NONE
*
* EXITS-NORMAL      1. IEECVETG OPEN-CLOSE
*                   2. IEECVETC ASYNCHRONOUS ERROR
*                   3. IEECVET4 COMMAND
*                   4. IEECVET2 DISPLAY 1
*                   5. IEECVET7 DELETE 2
*                   6. IEECVET9 DELETE 4
*                   7. IEECVFTP STATUS DISPLAY INTERFACE 5
*                   8. IEECVETJ ROLL
*                   9. IEECVETK TIMER-INTERPRETER
*                   10.IEECVFTM STATUS DISPLAY INTERFACE 2
*                   11.IEECVETF LIGHT PEN-CURSOR
*                   12.IEECVETH OR P OR R  DEVICE-DEPENDENT IO
*                   13.ROUTER                                 MD Y01958
*                   14.IEECVFTA PFK ROUTINE 1
*                   15.IEECVFTL STATUS DISPLAY INTERFACE 1
*                   16.IEECVFT1 PROCESSOR 0
*                   17.IEECVFT7 STATUS DISPLAY INTERFACE 7
*                   18.IEECVETD MESSAGE 1
*
* TABLES/WORK AREAS
*                   DCM - DISPLAY CONTROL MODULE
*                        SPLIT IN TWO PORTIONS
*                        1. RESIDENT PORTION - ALWAYS IN CORE
*                        2. PAGEABLE  PORTION - IN VIRTUAL MEMORY
*                   UCM - UNIT CONTROL MODULE
*                   CXSA - COMMUNICATIONS TASK EXTENDED SAVE AREA
*
* ATTRIBUTES   REFRESHABLE, PRIVILEGED, TYPE 4 SVC
*
* NOTES        THE OPERATION OF THIS MODULE DOES NOT DEPEND UPON
*              A PARTICULAR INTERNAL REPRESENTATION OF THE
*              EXTERNAL CHARACTER SET
*
*
*
*        REGISTER EQUATES
*
XFBRANCH EQU   15        BRANCH EXIT TO OTHER ROUTINES
*                        CONTENTS MAY CHANGE FOR SVC CALLS
XERTRN   EQU   14        RETURN BRANCH REG
*                        CONTENTS MAY CHANGE FOR SVC CALLS
XDCXSA   EQU   13        CXSA BASE REG
XCPBASE  EQU   12        PROGRAM BASE REG
XBTDCM   EQU   11        PAGEABLE DCM BASE REG
XARDCM   EQU   10        RESIDENT DCM BASE REG
X9UCM    EQU   9         UCM BASE REG
X8UCME   EQU   8         UCM ENTRY BASE REG
X7UCB    EQU   7         UCB BASE
X6       EQU   6
X5       EQU   5
X4WORK   EQU   4
X3MASK   EQU   3         MASK REGISTER
X2WORK   EQU   2         WORK REG
X1PARM   EQU   1         CXSA ADDRESS ON ENTRY AND EXIT
*                        CONTENTS MAY CHANGE FOR SVC CALLS
X0       EQU   0         CONTENTS MAY CHANGE FOR SVC CALLS
         EJECT
IGC5107B BALR  XCPBASE,N0          ESTABLISH REG FOR
         USING *,XCPBASE           PROGRAM ADDRESSABILITY
         B     BEGIN               BRANCH AROUND PATCH AREA
ICATCH   DC    CL8'IEECVET1'       EYECATCHER AND FIELD MAINTENANCE
         DC    CL16' ALIAS IGC5107B'
         DC    XL2'4309'           JULIAN DATE LAST CHANGE  JE @ZA02303
         DC    CL8'&SYSDATE'       DATE LAST ASSEMBLY
         DC    S(*)
         DC    CL40' '             PATCH AREA
BEGIN    EQU   *                   END OF PATCH AREA
         USING CXSA,XDCXSA         CXSA ADDRESSABILITY
         LR    XDCXSA,X1PARM       SET CXSA ADDRESS           MB YM6305
         L     X8UCME,CSAUCM       ESTABLISH REG FOR
         USING UCMLIST,X8UCME      UCM ENTRY ADDRESSABILITY
*
*                 SETUP ESTAE ENVIRONMENT
*
         ICM   X4WORK,15,CSANPTR   GET RECOVERY AREA          JE YM6718
         BZ    NOESTAE             NOT THERE, BYPASS          JE YM6718
         USING PARMLIST,X4WORK
         LA    X2WORK,ET1RTY       GET COMMON RECOVERY       JE @YM8452
         C     X2WORK,PARMRTAD    OUR RETRY ALREADY IN STACK JE @YM8452
         BE    NOESTAE             YES, LEAVE IT             JE @YM8452
         MVC   CSAXB(N8),RELEASED  NO,ESTAB LOCKING ADDRESSES          *
                                   FOR RETRY ROUTINE         JE @YM8452
         MVC   CSANAME(8),PARMRTAD SAVE RETRY & REGSAVE       JE YM6718
         ST    X2WORK,PARMRTAD      ADDR & SAVE IT            JE YM6718
         DROP X4WORK
NOESTAE  DS    0H                                             JE YM6718
         L     XARDCM,UCMXB        ESTABLISH REG FOR
         USING DCMTSRT,XARDCM      RESIDENT DCM ADDRESSABILITY
         L     XBTDCM,DCMADTRN     ESTABLISH REG FOR
         USING DCMSTRT,XBTDCM      PAGEABLE DCM ADDRESSABILITY
         ICM   X4WORK,15,DCMCXSVE  CSA POINTER INIT            @ZA29401
         BNZ   GOTDCM              YES DCM EXISTS              @ZA29401
         LA    0,SP241             SUBPOOL FOR GETMAIN        MB YM4087
         SLL   0,24                PUT IT IN HI ORDER BYTE    MB YM4087
         ICM   0,M3,DCMLEN         LENGTH FOR GETMAIN         MB YM4087
         GETMAIN R,LV=(0)          GET DCM CORE               MB YM4087
         LH    X3MASK,DCMLEN       DCM LENGTH                 MB YM4087
         LR    X5,X3MASK           FOR COPYING DCM            MB YM4087
         LR    X2WORK,X1PARM       MOVE TO ADDRESS            MB YM4087
         LR    X4WORK,XBTDCM       MOVE FROM ADDRESS          MB YM4087
         MVCL  X2WORK,X4WORK       COPY TDCM                  MB YM4087
         LR    X4WORK,XBTDCM       MODEL ADDRESS              MB YM4087
         LR    XBTDCM,X1PARM       NEW TDCM ADDRESS           MB YM4087
         ST    XBTDCM,DCMADTRN     RDCM POINTS TO TDCM        MB YM4087
         ST    X4WORK,DCMSTRT      TDCM POINTS TO MODEL       MB YM4087
         LA    X5,DCMBUFAD         FIRST ADCON                MB YM4087
         LA    X2WORK,4            BXLE INCREMENT             MB YM4087
         LA    X3MASK,DCMPFKLN     LAST ADCON                 MB YM4087
RELOC    EQU   *         TOP OF BXLE LOOP                     MB YM4087
         L     X1PARM,N0(X5)       GET ADCON VALUE            MB YM4087
         SR    X1PARM,X4WORK       GET DCM DISPLACEMENT       MB YM4087
         BM    NXTADDR             SKIP NEG DISPLACEMENT      MB YM4087
         AR    X1PARM,XBTDCM       GET RELOCATED VALUE        MB YM4087
         ST    X1PARM,N0(X5)       STORE IT                   MB YM4087
NXTADDR  BXLE  X5,X2WORK,RELOC     DO THE REST                MB YM4087
GOTDCM   ST    XDCXSA,DCMCXSVE     SAVE CXSA POINTER
         L     X9UCM,CSACTLM       ESTABLISH REG FOR
         USING UCM,X9UCM           UCM ADDRESSABILITY
         TM    DCMMCSST,DCMDUSE    RE-ENTRY FROM DIDOCS
         BZ    SAVE14              NO, SET UP INITIAL TRACE ENTRY
         TM    UCMSTS,UCMTA        OPEN PENDING ON RE-ENTRY
         BZ    TRACE               NO, ET1 ENTRY IS RECURSIVE
* NOTE: IF DCMDUSE AND UCMTA ARE BOTH ON, THIS IS AND ENTRY
*       TO OPEN THE CONSOLE AFTER A COMTASK ABEND THAT HAPPENED
*       WHILE DIDOCS WAS IN CONTROL FOR THIS CONSOLE AND DCMDUSE
*       WAS NEVER TURNED OFF.
SAVE14   EQU   *
         ST    XERTRN,CSAXA        SAVE RETURN REG
         OI    DCMMCSST,DCMDUSE    INDICATE OUR ROUTINES IN CONTROL
         MVC   CSAXB(N8),RELEASED INITIALIZE SETLOCK ADDRESSES  @YM8452
         MVC   DCMTRACE(DCMTRLEN),DCMTRAC2 SHIFT OLD ENTRIES
         MVI   DCMTREN1,CSTAR      MOVE IN '**' TO
         MVI   DCMTREN2,CSTAR      INDICATE NEW DIDOCS ENTRY
         B     OBTAIN              GET LOCKS
TRACE    EQU   *                   LOG ET1 ENTRY
         MVC   DCMTRACE(DCMTRLEN),DCMTRAC2 SHIFT OLD ENTRIES
         MVI   DCMTREN1,ID1        MOVE IN MODULE ID OF
         MVI   DCMTREN2,ID2        'E1' FOR IEECVET1
OBTAIN   EQU   *
         LR    X1PARM,XDCXSA       SET INPUT ADDRESS          MB YM6305
         L     XFBRANCH,CSAXB      ADDRESS GETLOCK SUBROUTINE
         BALR  XERTRN,XFBRANCH     OBTAIN LOCKS
TESTOP   EQU   *                                               @ZA16376
         TM    UCMSTS,UCMTA        Q. OPEN PENDING             @ZA16376
         BO    SETOPEN             YES, GO OPEN                @ZA16376
TSTDOM   EQU   *
*        TEST FOR DOM BEFORE ALL ELSE
         TM    UCMDEVC,UCMDEVD     WAS A DOM DONE
         BNO   BUSY                NO, CONTINUE
         L     XFBRANCH,DCMNDEL2   LOAD DELETE 2 ADDRESS
         B     PGONE               EXIT TO IEECVET7            @ZA10109
*  THIS SECTION OF CODE WILL TEST FOR POSTING OF THE ECB
*    IF THE ECB WAS POSTED FOR THE TIMER, AN EXIT WILL EVENTUALLY BE
*      MADE TO THE ROLL MODE OR TIMER INTERVAL CONTROL RTN
*    IF THE ECB WAS POSTED TO SIGNAL QUEUEING OF A ROUTED
*      COMMAND, THE COMMAND WILL EVENTUALLY BE FOUND ON THE
*      QUEUE AND HANDLED.
*    IF THE ECB WAS POSTED TO SIGNAL THAT THE USE OF THE USE OF
*      THE CONSOLE IS TO BE CHANGED, ALL OTHER WORK IS HANDLED
*      FIRST, AND THEN EXIT IS MADE TO THE DEVICE DEPENDENT IO
*      MODULE
*    IF THE ECB WAS POSTED FOR AN I/O COMPLETE, A TEST WILL BE MADE TO
*      DETERMINE IF THE I/O WAS GOOD
*    IF THE I/O WAS NOT GOOD, AN EXIT WILL BE MADE TO THE ERROR RTN SO
*      THAT CONSOLE SWITCHING CAN BE PERFORMED
BUSY     EQU   *
         CLI   UCMECB,BLANK        ECB POSTED OTHER THAN FOR IO
         BNE   CHKIO               NO, GO CHECK FOR IO
         CLI   UCMECB+N3,N255      LOW ORDER BYTE FF (ROLL MODE)
         BE    ZEROECB             YES, ZERO ECB
         CLI   UCMECB+N3,CMNDCODE  ECB POSTED FOR ROUTED COMMAND
         BE    ZEROECB             YES, ZERO ECB
         CLI   UCMECB+N3,KV        CONSOLE USE TO BE CHANGED
         BE    ZEROECB             YES, ZERO ECB
         B     ERROR               IF NONE OF THESE, GO TO
*                                  ASYNCHRONOUS ERROR
CHKIO    EQU   *
         TM    UCMSTS,UCMBF        CONSOLE BUSY
         BNO   TEST                NO. TEST REASON FOR ENTRY  MB YM5671
         TM    UCMECB,IOWAIT       WAIT BIT ON
         BO    TEST                YES, TEST REASON FOR ENTRY MB YM5671
         CLI   UCMECB,ZERO         Q.IS ECB ZERO
         BNE   CHKPFK1             YES, TEST FOR PFK I/O    JE @ZA00513
         TM    UCMDEVC,UCMDEVE     I/O COMPLETE ON          JE @ZA00513
         BO    ERROR               YES, GO SWITCH CONSOLES  JE @ZA00513
         B     TEST                ELSE, SEE WHY ENTERED    JE @ZA00513
         SPACE 5
CHKPFK1  DS    0H                                           JE @ZA00513
         TM    DCMR3FLG,DCMR3PKA   IS THIS IO COMPLETE FOR PFK
         BO    PRC0EXIT            YES LET PROC 0 HANDLE IT
         SPACE 5
         NI    UCMDEVC,N255-UCMDEVE  TURN OFF I/O COMPLETE FLAG
         NI    UCMSTS,N255-UCMBF   YES, TURN OFF BUSY BIT
         TM    UCMECB,IOGOOD       WAS I/O GOOD
         BO    ZEROECB             YES, GO ZERO ECB
         TM    DCMR3FLG,DCMCLPR    CLOSE IN PROGRESS
         BO    ZEROECB             YES, IGNORE BAD I/O
ERROR    EQU   *
         L     XFBRANCH,DCMNERRO   LOAD ERROR RTN ADDRESS
         B     PMGONE              EXIT TO IEECVETC
SETOPEN  EQU   *
         MVI   CSACODE,CSAOPEN     INDICATE OPEN
*              ONE LINE OF CODE DELETED HERE                   @ZA16389
         NI    UCMDEVC,N255-UCMDEVE  TURN OFF I/O COMPLETE     @ZA13458
OPENEXT  EQU   *                   EXIT TO IEECVETG
         L     XFBRANCH,VOPCL      LOAD OPEN RTN ADDRESS
PMGONE   EQU   *
         NI    DCMCOM1,N255-DCMLPENT-DCMCOMRM  TURN OFF BITS
PGONE    EQU   *                                               @ZA10109
         L     X1PARM,DCMCXSVE     RESTORE CXSA ADDRESS
         BR    XFBRANCH            EXIT TO ADDRESSED ROUTINE
ZEROECB  EQU   *
         MVI   UCMECB,ZERO         CLEAR I/O ECB
         NI    UCMDEVC,N255-UCMDEVE TURN OFF I/O COMPLETE   JE @ZA00513
TEST     EQU   *                   TEST ASYNCHRONOUS SENSE    MB YM5671
         TM    UCMSTS,UCMAF        ATTENTION RECEIVED         MB YM5671
         BNO   TSTBSY              NO, GO TO NEXT TEST        MB YM5671
         L     X7UCB,UCMUCB        ADDRESS CONSOLE UCB        MB YM5671
         USING UCBOB,X7UCB         UCB ADDRESSABILITY         MB YM5671
         TM    UCBSNS,ASYNCHK      ASYNCHRONOUS ERROR         MB YM5671
         BNZ   ERROR               YES, GO CHECK IT OUT       MB YM5671
         NI    DCMASYNC,N255-DCMASDA-DCMASIN-DCMASBA-DCMASLOG
*                                  RESET ERROR BITS            @ZA14126
TSTBSY   EQU   *
         TM    UCMSTS,UCMBF        Q. IS BUSY BIT ON
         BO    REST14              YES, WAIT FOR I/O
         TM    DCMR3FLG,DCMCLPR    Q. CLOSE IN PROGRESS
         BO    TESTCL              YES, CONTINUE HANDLING CLOSE
***********************************************************************
*              THREE LINES MOVED JUST PRIOR TO LABEL TSTDOM   @ZA16376*
***********************************************************************
TESTREO  EQU   *
         TM    DCMCS,DCMCSO        Q. REOPEN DEVICE
         BNO   TESTCL              NO, GO TEST FOR CLOSE
         OI    DCMR3FLG,DCMSTSWT   YES,INDICATE STATUS CHANGING
IOEXIT   EQU   *
         L     XFBRANCH,DCMIORTN   LOAD IO ROUTINE ADDRESS
         B     PMGONE              EXIT TO IEECVET(H,P,R, OR U)
TESTCL   EQU   *
         TM    UCMSTS,UCMCF        Q.WAS CLOSE REQUESTED
         BO    CLOSE            EXIT TO LOAD 2
TSTCLCL  EQU   *
         TM    DCMR3FLG,DCMCLPR    Q. CLOSE IN PROGRESS
         BNO   TESTRD             NO, GO TEST FOR DOM
         NI    DCMR3FLG,N255-DCMCLPR  TURN OFF CLOSE IN PROGRESS
         B     PFKCODE             RETURN TO MCS
TESTRD   EQU   *
         TM    DCMCOM1,DCMIOPRD    Q.WAS A READ PERFORMED
         BO    FIND                YES, GO EXAMINE INPUT
         TM    DCMCOM1,DCMCOMRM    Q. WAS A RMI PERFORMED
         BO    TSTRMI              YES, GO CHECK RMI INPUT
CHKCMDQ  EQU   *
         L     X2WORK,DCMADKP      GET POINTER TO QUEUE OF
*                                       ROUTED COMMANDS
* A NON-ZERO POINTER INDICATES A ROUTED COMMAND ON THE QUEUE.
*    IF THE COMMAND HAS BEEN HANDLED ALREADY, THE PARAMETER LIST
*    MUST BE FREED.  OTHERWISE BRANCH TO THE MODULE ADDRESSED IN
*    THE PARAMETER LIST TO HANDLE THE COMMAND.
TESTKQ   EQU   *
         LTR   X2WORK,X2WORK       ANY COMMANDS ON Q
         BZ    LPCENT              NO, GO TEST NEXT FUNCTION
         USING KPARAM,X2WORK
RTCMDQ   EQU   *
         TM    KFLGS,KGM           WAS THIS PARAMETER LIST             X
                                        GETMAINED
         BO    GMPARAM             YES, GO HANDLE GETMAINED TYPE
         XC    DCMADKP(N4),DCMADKP NO, THERE CAN ONLY BE ONE           X
                                   COMMAND - DEQUEUE IT
         B     KMODEXIT            EXIT TO INDICATED ROUTINE
GMPARAM  EQU   *
         TM    KFLGS,KSRVCD        HAS THIS COMMAND BEEN SERVICED
         BO    FREE                YES, BRANCH TO FREE THE
*                                       PARAMETER LIST
         OI    KFLGS,KSRVCD        INDICATE SERVICED
KMODEXIT EQU   *
         LA    X4WORK,KOPN         GET ADDRESS OF OPERAND FIELD
         ST    X4WORK,DCMADOPN     STORE ADDR OF OPERAND FIELD
         L     XFBRANCH,KMOD       BRANCH TO MODULE INDICATED IN
         B     PMGONE                   PARAMETER LIST
FREE     EQU   *
         L     X1PARM,KCHAIN     BASE NEXT KPARAM LIST
         ST    X1PARM,DCMADKP    AND SAVE ITS ADDRESS
         LR    X1PARM,XDCXSA       ADDRESS CXSA FOR FREELOCK  MB Y02958
         L     XFBRANCH,CSAXC      ADDRESS RELEASE SUBROUTINE MB Y02958
         BALR  XERTRN,XFBRANCH     RELEASE LOCKS FOR FREEMAIN MB Y02958
         FREEMAIN R,SP=SP241,LV=KPLLNGTH,A=(X2WORK)           MB Y02958
         LR    X1PARM,XDCXSA       ADDRESS CXSA FOR FREELOCK  MB Y02958
         L     XFBRANCH,CSAXB      ADDRESS OBTAIN SUBROUTINE  MB Y02958
         BALR  XERTRN,XFBRANCH     OBTAIN LOCKS               MB Y02958
         B     CHKCMDQ           BRANCH TO CHECK NEXT LIST
         DROP  X2WORK
         EJECT
CLOSE    EQU   *
*   ERASE SCREEN BEFORE CLOSING DEVICE
         TM    DCMCOM2,DCMERPF     Q. HAS SCREEN BEEN ERASED
         BO    FINCLOSE            YES, EXIT TO OPEN/CLOSE RTN
         OI    DCMR3FLG,DCMCLPR    INDICATE CLOSE IN PROGRESS
         OI    DCMCOM2,DCMERPF     INDICATE ERASE PERFORMED
         OI    DCMIOCM2,DCMERASE   INDICATE ERASE SCREEN
         OI    DCMIOCM3,DCMACPFK   INDICATE TURN OFF PFKS
         B     IOEXIT              EXIT TO I/O RTN
FINCLOSE EQU   *
         OI    DCMCS,DCMCSO        INDICATE REOPEN FOR NEXT ENTRY
         MVI   CSACODE,CSACLOSE    CLOSE INDICATION TO CLOSE RTN
         B     OPENEXT             EXIT TO IEECVETG
LPCENT   EQU   *
         TM    DCMCOM1,DCMLPENT    Q. ENTER PERFORMED USING CURSOR OR
*                                   LIGHT PEN
         BO    FIND                YES, GO EXAMINE INPUT
         TM    UCMSTS,UCMAF        Q. IS THERE AN ATTENTION
         BO    ATTEN               YES GO FIND TYPE
TEST1    EQU   *
         TM    DCMR2FLG,DCMRXTIM   HAS TIMER ELAPSED
         BNO   TSTINT7             NO, CONTINUE TESTS
TIMEXIT  EQU   *
         L     XFBRANCH,DCMNTIMR   LOAD TIMER RTN ADDRESS
         B     PMGONE              EXIT TO IEECVETK
TSTINT7  EQU   *
         TM    DCMCOM3,DCMCMIN7    RETURN TO INTERFACE 7
         BNO   TSTINT5             NO, GO TO NEXT TEST
INT7EXIT EQU   *
         L     XFBRANCH,DCMNINT7   LOAD INTERFACE 7 ADDRESS
         B     PMGONE              EXIT TO IEECVFTT
TSTINT5        EQU                 *
         TM    DCMCOM2,DCMCMIN5    Q. RETURN CONTROL TO                X
                                        INTERFACE 5 FOR BLANKING
         BNO   SEARCH              NO, CONTINUE LOOKING FOR            X
                                        SOMETHING TO DO
INT5EXIT EQU   *
         L     XFBRANCH,DCMNINT5   LOAD INTERFACE 5 ADDRESS
         B     PMGONE              EXIT TO IEECVFTQ
         SPACE
SEARCH   EQU   *
* CHECK FOR ANY MESSAGES ON THE QUEUE TO BE OUTPUT
* THE OUTPUT PENDING BITS OPERATE AS FOLLOWS:
*     UCMPF - OUTPUT PENDING, EITHER INLINE OR OUT OF LINE
*              THIS BIT CAUSES THE PROCESSOR TO RECEIVE CONTROL.
*     UCMSDS5B - INLINE OUTPUT PENDING
*              THIS BIT IS ON WHENEVER MESSAGES TO BE WRITTEN
*              IN LINE ARE ON THE QUEUE, EVEN IF A FULL SCREEN
*              CONDITION EXISTS, UNLESS THE CONSOLE IS IN THE
*              MIDDLE OF WRITING OUT AN INLINE MLWTO.  IF THE
*              CONSOLE IS HANDLING AN MLWTO, THE INLINE PENDING
*              BIT WILL BE TURNED ON ONLY WHEN MORE LINES OF THE
*              SAME MLWTO ARE ADDED.
*     UCMSDS5C - OUT OF LINE OUTPUT PENDING
*              THIS BIT IS ON WHENEVER A MESSAGE WHICH CAN NOW
*              BE WRITTEN OUT OF LINE IS ON THE QUEUE.  THIS BIT
*              IS NOT ON IF THE ONLY MESSAGES ON THE QUEUE TO BE
*              WRITTEN OUT OF LINE ARE TO BE WRITTEN TO AN AREA
*              WITH A FULL FRAME CONDITION.  IT IS TURNED OFF
*              IF THE CONSOLE IS IN HOLD MODE.
         TM    UCMSDS5,UCMSDS5B    Q. INLINE OUTPUT PENDING
         BO    DISPTST             YES
         CLI   DCMDEL,R            CONSOLE IN ROLL MODE     JE @ZA02835
         BNE   OUTLNTST            NO, BYPASS NEXT TEST     JE @ZA02835
         TM    DCMR2FLG,DCMRXRLL   Q. READY FOR ROLL
         BO    ROLEXIT             YES, EXIT TO ROLL ROUTINE
OUTLNTST EQU   *
         TM    UCMSDS5,UCMSDS5C    Q. OUT OF LINE OUTPUT PENDING
         BO    TESTHOLD            YES, GO TEST FOR HOLD MODE
         TM    DCMCOM3,DCMOLHLD    Q. OUT OF LINE MSGS HELD
         BNO   OFFOP               NO, GO TURN OFF OUTPUT
*                                       PENDING FLAG
TESTHOLD EQU   *
         TM    DCMIOUNQ,DCMFRSCN   Q. SCREEN IN HOLD MODE
         BO    ONOLHLD             YES, HOLD OUT OF LINE MSGS
         NI    DCMCOM3,N255-DCMOLHLD TURN OFF OUT OF LINE
*                                       MSGS HELD BIT
INT2EXIT EQU   *
         L     XFBRANCH,DCMNINT2   LOAD INTERFACE 2 ADDRESS
         B     PMGONE              EXIT TO IEECVFTM
ONOLHLD  EQU   *
         OI    DCMCOM3,DCMOLHLD    INDICATE OUT OF LINE
*                                       MESSAGES HELD
         NI    UCMSDS5,N255-UCMSDS5C TURN OFF OUT OF LINE BIT
OFFOP    EQU   *
         NI    UCMSTS,X'FF'-UCMPF  TURN OFF OUTPUT PENDING FLAG,
*                                       SINCE BOTH INLINE AND
*                                       OUT OF LINE OUTPUT HAVE
*                                       BEEN HANDLED BY THIS
*                                       POINT
         SPACE
TSTUNVW  EQU   *
         TM    DCMCOM3,DCMOLUNV    DID OUT OF LINE MSG CAUSE           X
                                        UNVIEWABLE MSGS
         BNO   TSTPFK              NO, CONTINUE LOOKING FOR            X
                                        SOMETHING TO DO
         NI    DCMCOM3,N255-DCMOLUNV  YES, TURN OFF BIT
         TM    DCMR2FLG,DCMRXUNV+DCMRXSFL Q. UNVIEWABLE MSG MSG        X
                                        ALREADY ON SCREEN OR
*                                       SCREEN FULL (MSG WAITING       X
                                        MSG ON SCREEN)
         BNZ   TSTPFK              YES, CONTINUE LOOKING FOR           X
                                   SOMETHING TO DO
         CLI   DCMDEL,Y            DEL = Y
         BNE   WRUNVW              NO, GO WRITE UNVIEWABLE MSG
         OI    DCMCOM1,DCMCOMAU    YES, INDICATE PERFORM AUTO          X
                                        DELETE
         OI    DCMDSTAT,DCMDSAUT   INDICATE AUTO DELETE TRIED
DEL4EXIT EQU   *
         L     XFBRANCH,DCMNDEL4   LOAD DELETE 4 ADDRESS
         B     PMGONE              EXIT TO IEECVET9
WRUNVW   EQU   *
         OI    DCMCMSG1,DCMUNMSG   TURN ON UNVIEWABLE MSG BIT
         CLI   DCMDEL,R            ROLL MODE (OR ROLL DELETABLE)
         BE    XDISP               YES, EXIT TO DISPLAY 1
         L     XFBRANCH,DCMNMSG1   NO, LOAD MESSAGE 1 ADDRESS
         B     PMGONE              EXIT TO IEECVETD
TSTPFK   EQU   *
         TM    DCMIOCM3,DCMACPFK+DCMLTPFK   ANY PFKS TO BE LIT
         BO    IOEXIT              YES, GO DO IT
         TM    DCMCOM3,DCMRTPFK    MORE PFK WORK TO DO
         BNO   ONWARD              NO, CHECK NEXT FUNCTION
         TM    DCMCOM3,DCMVLPFK     YES, ARE WAITING FOR A VERIFY
         BNO   PFK1A                 NO, GO TO FINISH PFK WORK
ONWARD   EQU   *
*** TESTS FOR MORE MORE FUNCTIONS MAY BE MADE HERE ***
         TM    DCMR3FLG,DCMSTSWT   CONSOLE USE TO BE CHANGED
         BO    IOEXIT              YES, EXIT TO IO ROUTINE
PFKCODE  TM    DCMCOM3,DCMPFKWR    SHOULD PFKS BE WRITTEN
         BO    PRC0EXIT            YES, GO TO PROC ZERO
         B     ROUTOUT             EXIT TO ROUTER
DISPTST  EQU   *
         CLI   DCMDEL,R            CONSOLE IN ROLL MOD      JE @ZA02835
         BNE   SCRFUL              NO, CONTINUE LOOKING     DC @ZA04689
         TM    DCMR2FLG,DCMRXRLL    Q. READY FOR ROLL
         BO    ROLEXIT             YES
SCRFUL   TM    DCMR2FLG,DCMRXSFL   IS SCREEN FULL           DC @ZA04689
         BO    TSTROLL             GO TEST FOR ROLL MODE         A38905
         TM    UCMSTS,UCMTC        WORKING ON AN INLINE MLWTO
         BO    INT1EXIT            YES, CALL INTERFACE ROUTINES
XDISP    L     XFBRANCH,DCMNDSP1   LOAD DISPLAY ROUTINE ADDRESS
         B     PMGONE              EXIT TO IEECVET6
INT1EXIT EQU   *
         L     XFBRANCH,DCMNINT1   LOAD INTERFACE 1 ADDRESS
         B     PMGONE              EXIT TO IEECVFTN
         SPACE 2
TSTROLL  EQU   *
         CLI   DCMDEL,R            Q. ROLL MODE
         BNE   OUTLNTST            NO, CONTINUE LOOKING
         L     X2WORK,DCMSUBAD     ESTABLISH REG FOR
         USING DSUB,X2WORK         SUB ADDRESSABILITY
         L     X2WORK,SUBPBASE     ESTABLISH REG FOR
         USING SUBDCMB,X2WORK      BASE DCM ADDRESSABILITY
         TM    DCMIND,DCMTMRST     IS STIMER IN EFFECT YET
         BO    OUTLNTST            YES, CONT LOOKING FOR
*                                  SOMETHING TO DO
         CLC   DCMWTBUF(N4),DCMASCRN STATUS DISPLAYS COVER             *
               THE SCREEN                                     MB YM4360
         BE    OUTLNTST            YES,LOOK FOR OTHER WORK    MB YM4360
         B     XDISP               NO, GO TO DISPLAY ROUTINE
*                                  TO GET TIMER SET
PRC0EXIT EQU   *
         NI    DCMR3FLG,N255-DCMR3PKA     TURN OFF EXIT BIT
         L     XFBRANCH,DCMNPRZ    LOAD PROC 0 ADDRESS
         B     PMGONE              EXIT TO IEECVETZ
ROUTOUT  EQU   *      EXIT TO ROUTER
         TM    DCMRFLGS,DCMDOM     DOM DONE WHILE BUSY        MB YM4087
         BNO   REST14              NO, CONTINUE LOOKING       MB YM4087
         TM    UCMSTS,UCMBF        STILL BUSY                 MB YM4087
         BO    REST14              YES, WAIT FOR IO COMPLETE  MB YM4087
         NI    DCMRFLGS,N255-DCMDOM TURN OFF DOM DONE         MB YM4087
         TM    UCMATR,UCMUF        CONSOLE STILL ACTIVE     JE @ZA02303
         BNO   REST14              NO, EXIT TO COMM TASK    JE @ZA02303
         OI    DCMIOCM1,DCMWRMSG   WRITE MESSAGE AREA         MB YM4087
         B     IOEXIT              EXIT TO IO ROUTINE         MB YM4087
REST14   EQU   *                   NO LEFTOVER DOM WORK       MB YM4087
         NI    DCMMCSST,N255-DCMDUSE  INDIC OUR RTNS NO LONGER IN CONT
         L     XERTRN,CSAXA        RESET RETURN REG
         ICM   X4WORK,15,CSANPTR   GET RECOVERY AREA          JE YM6718
         BZ    NOESTAE1            NOT THERE, BYPASS          JE YM6718
         USING PARMLIST,X4WORK
         MVC   PARMRTAD(8),CSANAME RESOTRE RETRY & REGSAVE    JE YM6718
NOESTAE1 DS    0H                                             JE YM6718
         TM    UCMATR,UCMUF        CONSOLE STILL ACTIVE       MB YM4087
         BO    DONTFREE            YES,KEEP GETMAINED DCM     MB YM4087
         MVC   DCMADTRN(N4),DCMSTRT POINT RDCM TO MODEL DCM   MB YM4087
         LA    XERTRN,FREEDCM      RETURN FROM FREELOCK       MB YM4087
DONTFREE EQU   *        RELEASE LOCKS FOR EXIT OR FREEMAIN    MB YM4087
         LR    X1PARM,XDCXSA       GET CXSA ADDR
         LA    XFBRANCH,FREELOCK   ADDRESS FREELOCK SUBROUTINE
         BR    XFBRANCH            RELEASE LOCKS
FREEDCM  EQU   *         FREE TDCM IF CONSOLE NOT ACTIVE      MB YM4087
         LH    X0,DCMLEN           FREEMAIN LENGTH            MB YM4087
         FREEMAIN RC,SP=SP241,LV=(0),A=(XBTDCM)               MB YM4087
         LR    X1PARM,XDCXSA       RESET CXSA IN REG 1        MB YM4087
         L     XERTRN,CSAXA        RESET RETURN REG           MB YM4087
         BR    XERTRN              EXIT FROM DIDOCS           MB YM4087
ROLEXIT  EQU   *
         TM    DCMTIMES,DCMTASYN   Q. HAS TIMER ELAPSED FOR ASYN ERROR
         BO    CANCEL              YES, GO TO REWRITE SCREEN
         CLI   DCMRQDEL,BLANK      Q. IS A DELETE REQUEST PENDING
         BNE   XDISP               YES DO NOT ROLL
         L     XFBRANCH,DCMNROLL   LOAD ROLL RTN ADDRESS
         B     PMGONE              EXIT TO IEECVETJ
FIND     EQU   *
         NI    DCMCOM1,N255-DCMLPENT-DCMIOPRD  TURN OFF BITS
* CODE WAS MOVED FROM HERE TO IEECVET4
CMD1EXIT EQU   *
         L     XFBRANCH,DCMNCMD1        LOAD COMMAND RTN ADDRESS
         B     PMGONE                   EXIT TO IEECVET4
CANCEL   EQU   *
         OI    DCMCOM1,DCMCANCL         INDICATE CANCEL KEY
         B     CMD1EXIT                 EXIT
* ROUTINE FOR ROUTING ATTENTIONS
ATTEN    EQU   *
         NI    UCMSTS,N255-UCMAF   TURN OFF ATTENTION FLAG
         TM    UCMDISP,UCMDISPG    Q. STATUS DISPLAY CONSOLE
         BO    TEST1               YES, IGNORE ATTENTION
         OI    DCMIOCM1,DCMDORMI   TURN ON BIT - ISSUE RMI
         B     IOEXIT              GO TO ROUTINE
TSTRMI   EQU   *
         TM    DCMIOUNQ,DCMIO226   Q.READ RMI PERF FOR 2260
         BO    IOEXIT              YES, EXIT TO I/O RTN -FIND CURSOR
         LA    X2WORK,DCMDSAV      GET ADDR WHERE RMI INFO RETURNED
         AH    X2WORK,DCMRMINC     ADD INCRE TO IT TO ACCOUNT FOR
*                                  DIFFERENT DEVICES
         IC    X3MASK,DCMASKEN      LOAD REG WITH MASK FOR ENTER
         EX    X3MASK,TESTMASK      Q. ENTER KEY
         BO    ENTER               YES, GO READ INPUT
         IC    X3MASK,DCMASKLP      LOAD MASK FOR LIGHT PEN
         EX    X3MASK,TESTMASK      Q. LIGHT PEN
         BO    ENTER                YES, GO READ INPUT
         IC    X3MASK,DCMASKCN      LOAD MASK FOR CANCEL
         EX    X3MASK,TESTMASK      Q. CANCEL KEY
         BO    CANCEL              YES, GO HANDLE CANCEL
PFK      IC    X3MASK,DCMASKPF      LOAD MASK FOR PFK ATTENTION
         EX    X3MASK,TESTMASK     WAS IT A PFK
         BNO   ENTER               NO, CHECK NEXT FUNCTION
         B     PFK1                YES, GO TO TAKE CARE OF PFK
* ASSUME ENTER
ENTER    EQU   *
         TM    DCMTIMES,DCMTASYN   IS ASYNCHRONOUS ERR MSG ON SCREEN
         BO    CANCEL              YES, GO CANCEL
         IC    X3MASK,DCMASKCR     INSERT CURSOR MASK
         EX    X3MASK,TESTMASK      Q.CURSOR FOR 22D0C OR
         BO    TSTCRS              YES, GO TEST FOR CURSOR POSITION
* ANY DEVICE WITH PFK CAPABILITY MUST HAVE RMI & READ DONE SEPARATELY
         TM    DCMIOUNQ,DCMRDARM   Q. PERFORM A READ
         BNO   FIND                NO, TEST INPUT
         OI    DCMIOCM2,DCMIOCRD   TURN ON BIT- ISSUE READ
         B     IOEXIT              GO ISSUE  I/O
TSTCRS   EQU   *                   MUST BE 220C,22D0C -CURS POS IN BYTE
         L     X3MASK,DCMBUFAD      GET DISPLACEMENT
         LA    X3MASK,N4(X3MASK)      OF DCMBAENT
         CLC   DCMDSAV(N2),N0(X3MASK)  Q. CURSOR IN MESSAGE AREA
         BL    FNDCRS              YES, GO TO LIGHT PEN/CURSOR
         CLC   DCMDSAV(N2),N2(X3MASK)  Q.CURSOR IN WARNING LINE
         BL    FIND                NO, GO EXAMINE INPUT
FNDCRS   EQU   *
         L     XFBRANCH,DCMNLPCR  LOAD CURSOR/LIGHT PEN ADDRESS
         B     PMGONE              EXIT TO IEECVETF
PFK1     EQU   *
         OI    DCMIOCM3,DCMPFKAT  INDICATE PFK ATTENTION OCCURRED
PFK1A    EQU   *
         L     XFBRANCH,DCMNPFK1   LOAD PFK1 ROUTINE ADDRESS
         B     PMGONE              EXIT TO IEECVFTA
*
*        EXECUTED INSTRUCTIONS
TESTMASK TM    N0(X2WORK),ZERO  TEST FOR DETERMINING ATTENTION TYPE
         EJECT
*
*        OBTAIN LOCKS SUBROUTINE
*
         DROP X8UCME,X9UCM                                  JE @ZA02341
         USING UCM,14                                       JE @ZA02341
GETLOCK  EQU   *
         USING *,XFBRANCH          SUBROUTINE ADDRESSABILITY BASED ON
*                                  ENTRY REGISTER
         DROP  XDCXSA              DROP OLD CXSA BASE
         USING CXSA,X1PARM         REG 1 ADDRESSES THE CXSA AS
*                                  AN INPUT PARAMETER
         STM   11,13,CSAXD SAVE REGS OVER SETLOCK CALL       JE @YM8452
         LR    0,14                SAVE 14 OVER SETLOCK      JE @YM8452
GETLOCL  SETLOCK OBTAIN,TYPE=LOCAL,MODE=UNCOND,                        *
               RELATED=(WQE,IEECVET1(FREELOCL))
GETCMS   SETLOCK OBTAIN,TYPE=CMS,MODE=UNCOND,                          *
               RELATED=(UCM,IEECVET1(FREECMS))
         L     14,CSACTLM          GET UCME ADDR             JE @YM8452
         L     11,UCMFRRAD         GET VCTR FRR RTN ADDR     JE @YM8452
         USING PSA,0                                         JE @YM8452
GETFRR   SETFRR A,FRRAD=(11),PARMAD=(13),WRKREGS=(12,13),              *
                RELATED=(CMSLOCK,IEECVET1(FREEFRR))          JE @YM8452
         ST    13,CSADCBP  SAVE FRR PARMLIST PTR             JE @YM8452
         USING PARMLIST,13                                   JE @YM8452
         ICM   12,15,CSANPTR       GET ESTAE PARMLIST PTR    JE @YM8452
         BZ    FRRA                NOT THERE                 JE @YM8452
         MVC   PARMLIST(PARMSIZE),0(12) COPY ESTAE PARMLIST TO         *
                                   FRR PARMLIST              JE @YM8452
FRRA     MVC   PARMID,SVC72ID      SET UP PROCESSOR ID       JE @YM8452
         LM    11,13,CSAXD         RESTORE REGS AFTER SETLOCK   @YM8452
         LR    14,0                RESTORE 14                JE @YM8452
GETEXIT  MVC   CSAXB(N8),OBTAINED  RESET SUBROUTINE ADDRESSES
         LR    XFBRANCH,XERTRN     COPY EXIT ADDRESS
         BR    XERTRN              EXIT FROM GETLOCK SUBROUTINE
         SPACE 5
*
*        RELEASE LOCKS SUBROUTINE
*
FREELOCK EQU   *
         USING *,XFBRANCH          SUBROUTINE ADDRESSABILITY BASED ON
*                                  ENTRY REGISTER
         USING CXSA,X1PARM         REG 1 ADDRESSES THE CXSA AS
*                                  AN INPUT PARAMETER
         STM   11,13,CSAXD         SAVE REGS OVER SETLOCK CALL  @YM8452
         LR    0,14                SAVE REG 14               JE @YM8452
FREEFRR SETFRR D,WRKREGS=(12,13),                                      *
                RELATED=(CMSLOCK,IEECVET1(GETFRR))           JE @YM8452
         XC    CSADCBP,CSADCBP     ZERO PTR                  JE @YM8452
FREECMS  SETLOCK RELEASE,TYPE=CMS,                                     *
               RELATED=(UCM,IEECVET1(GETCMS))
FREELOCL SETLOCK RELEASE,TYPE=LOCAL,                                   *
               RELATED=(WQE,IEECVET1(GETLOCL))
         DROP  13,14,0                                       JE @YM8452
         USING UCMLIST,X8UCME                                JE @YM8452
         LM    11,13,CSAXD        RESTORE REGS AFTER SETLOCK JE @YM8452
         LR    14,0                RESTORE 14                JE @YM8452
FREEXIT  MVC   CSAXB(N8),RELEASED  RESET SUBROUTINE ADDRESSES
NOOPLOCK LR    XFBRANCH,XERTRN     COPY EXIT ADDRESS
         BR    XERTRN              EXIT FROM FREELOCK SUBROUTINE
         EJECT
*
*              ESTAE RETRY ROUTINE FOR DIDOCS PROCESSING
*
ET1RTY   DS    0D                                             JE YM6718
         BALR  XFBRANCH,0          GET BASE                   JE YM6718
         USING *,XFBRANCH          TELL ASSM                  JE YM6718
         L     XCPBASE,ADET1       SETUP STND BASE            JE YM6718
         DROP XFBRANCH,X1PARM                                 JE YM6718
         USING CXSA,XDCXSA                                   JE @YM8452
         USING IGC5107B+2,XCPBASE                             JE YM6718
         USING PSA,0                                          JE YM6718
         L     X5,PSATOLD          GET OUR TCB PTR            JE YM6718
         L     X5,TCBRBP-TCB(X5)   GET SVRB PTR               JE YM6718
         LA    XDCXSA,RBEXSAVE-RBSECT(X5) GET CXSA PTR      JE @ZA00539
         L     X8UCME,CSAUCM       RELOAD UCME PTR            JE YM6718
         L     XARDCM,UCMXB        RELOAD RDCM PTR            JE YM6718
         L     XBTDCM,DCMADTRN     RELOAD TDCM PTR            JE YM6718
*
*              END OF REGISTER SETUP CODE
*
         MVC   DCMTRACE(DCMTRLEN),DCMTRAC2  SHIFT OLD ENTRIES
         MVI   DCMTREN1,ID3        MOVE IN MOD ID OF 'ES'
         MVI   DCMTREN2,ID4        FOR IEECVET1 ESTAE RTN
*
*     GET LOCAL AND CMS LOCKS
*
         LR    X1PARM,XDCXSA       POINT TO CXSA             JE @YM8452
         L     XFBRANCH,CSAXB      ADDR GETLOCKS RTN         JE @YM8452
         BALR  XERTRN,XFBRANCH     OBTAIN LOCKS              JE @YM8452
*
         ICM   X4WORK,15,CSANPTR      GET RECOVERY AREA       JE YM6718
         BZ    NORCVRY             NOT THERE SWITCH CONSOLES   @ZA29665
         MVI   PARMFTPT,0          CLEAR FOOTPRINTS           JE YM6718
         TM    UCMSTS,UCMBF        CONSOLE BUSY               JE YM6718
         BO    BSYRCVRY            YES, GO HANDLE             JE YM6718
         TM    UCMSTS,UCMCF        CLOSE PENDING              JE YM6718
         BO    CLSRCVRY            YES, GO HANDLE             JE YM6718
         TM    UCMSTS,UCMTA        OPEN PENDING               JE YM6718
         BO    OPNRCVRY            YES, GO HANDLE             JE YM6718
*
*     ADDITIONAL RECOVERY CODE CAN GO HERE
*
NORCVRY  NI    UCMDEVC,N255-UCMDEVE   TURN OFF I/O COMPLETE  JE @YM8452
         NI    DCMMCSST,N255-DCMDUSE  RTN NO LONGER IN CTRL D02 ZA06411
         L     XFBRANCH,CSAXC      ADDR RELEASE LOCK SBRTN   JE @YM8452
         BALR  XERTRN,XFBRANCH     GO RELEASE LOCKS          JE @YM8452
*
         L     XERTRN,CSAXA        RESET RETURN REGISTER       @ZA29665
         ICM   X4WORK,15,CSANPTR   RECOVERY AREA PRESENT?      @ZA29665
         BZ    SETSWCH          NO,SET CONSOLE SWITCH PARMLIST @ZA29665
PARMCTN  MVC   PARMRTAD(N8),CSANAME RESTORE RETRY AND REGSAVE  @ZA29665
SETSWCH  MVI   CSACODE,CSASWTCH    INDICATE CONSOLE SWITCH     @ZA29665
         MVC   CSANAME(N8),CONSWTCH MODULE NAME OF SWITCH      @ZA29665
         L     XFBRANCH,CSACTLM    SET UCM BASE ADDRESS        @ZA29665
         L     XFBRANCH,UCMSWCH-UCM(XFBRANCH) ADDR OF CON SW   @ZA29665
         LR    X1PARM,XDCXSA       ADDRESS OF CXSA IN REG1     @ZA29665
         BR    XFBRANCH            EXIT TO CONSOLE SWITCH      @ZA29665
*
*      CLOSE RECOVERY
*
CLSRCVRY DS    0H                                             JE YM6718
         MVI   CSACODE,CLSRCV      SET CLOSE RECOVERY CODE    JE YM6718
         B     OPENEXT             GO TO ETG                  JE YM6718
*
*     OPEN RECOVERY
*
OPNRCVRY DS    0H                                             JE YM6718
         MVI   CSACODE,OPNRCV      SET OPEN RECOVERY CODE     JE YM6718
         B     OPENEXT             GO TO ETG                  JE YM6718
*
*     EXCP ERECOVERY
*
BSYRCVRY DS    0H                                             JE YM6718
         NI    UCMSTS,N255-UCMBF   TURN OFF BUSY              JE YM6718
         NI    UCMDEVC,N255-UCMDEVE   TURN OFF I/O COMPLETE  JE @YM8452
         MVI   UCMECB,BADIO        SET PERM I/O ERROR       JE @ZA02303
         L     XFBRANCH,CSAXC      ADDR RELEASE LOCK SBRTN    JE YM6718
         BALR  XERTRN,XFBRANCH     GO RELEASE LOCKS           JE YM6718
         B     ERROR               TREAT AS BAD I/O AND       JE YM6718
*                                  SWITCH CONSOLES            JE YM6718
*
CLSRCV   EQU   12                  CLOSE RECOVERY CODE        JE YM6718
OPNRCV   EQU   8                   OPEN RECOVERY CODE         JE YM6718
BADIO    EQU   X'41'               PERM I/O ERROR POST CODE JE @ZA02303
         SPACE 5
*
*   CONSTANTS
*
         DS    0F
SVC72ID  DC    CL4'VCTR'                                     JE @YM8452
CONSWTCH DC    CL8'IGC0407B'  NAME OF CONSOLE SWITCH RTN       @ZA29665
VOPCL    DC    V(IEECVETG)
ADET1    DC    A(IGC5107B+2)       BASE OF MODULE             JE YM6718
RELEASED DC    A(GETLOCK)
OBTAINED DC    A(NOOPLOCK)
         DC    A(FREELOCK)
*
*   EQUATES
*
ID1      EQU   C'E'                1ST CHAR OF CSECT ID
ID2      EQU   C'1'                2ND CHAR OF CSECT ID
ID3      EQU   C'E'                1ST CHAR OF ESTAE ID
ID4      EQU   C'S'                2ND CHAR OF ESTAE ID
CSTAR    EQU   C'*'                ID ENTRY TO INDICATE 1ST TIME
SP241    EQU   241                                            MB Y02958
IOBCOUNT EQU   14                  IOB COUNT FIELD
CMNDCODE EQU   X'FE'               POST CODE FOR ROUTED COMMAND
KV       EQU   X'FD'               POST CODE FOR K V
IOWAIT   EQU   X'80'               WAIT FOR I/O
DISPBIT  EQU   X'10'        DISPLAY BIT
IOGOOD   EQU   X'7F'     I/O GOOD
BLANK    EQU   X'40'     BLANK CHAR.
UCMPOST  EQU   X'80'          POST BIT FOR ECB
ZERO     EQU   X'00'               ZERO FOR UTILITY BYTE& ECB
R        EQU   C'R'                DEL = R OR RD
Y        EQU   C'Y'                DEL = Y
N0       EQU   0              ZERO
N1       EQU   1              ONE
N2       EQU   2                       2
N3       EQU   3                       3
N4       EQU   4         FIELD INCREMENT FOR CXSA
N8       EQU   8
N22      EQU   22        FIELD INCREMENT FOR  UCB
N255     EQU   255       USED TO TURN OFF SINGLE BITS
ASYNCHK  EQU   X'1B'                    ASYNCH ERRORS
CONSOLE  EQU   X'02'               UCB CONSOLE FLAG
M3       EQU   3                   MASK FOR INSERT CHARACTER  MB YM4087
         EJECT
DKPARAM  DSECT
         IEECVMAP KPARAM
         EJECT
         IHACTM FTPT               RECOVERY WORKAREA MAPPING  JE YM6718
         EJECT
         IHAFRRS                   MAP FRR PARMLIST          JE @YM8452
         EJECT
         IHARB DSECT=YES                                      JE YM6718
         EJECT
         IKJTCB                                               JE YM6718
         EJECT
         IEERDCM
         EJECT
         IEETDCM
         EJECT
         IHACTM  CXSA
         EJECT
         IEECUCM  FORMAT=NEW
         EJECT
*
DSUB     DSECT
         IEECSUB Y,N,N,N,N,Y
         EJECT
         IHAPSA
         EJECT
         CVT   DSECT=YES
         EJECT
ET1UCB   DSECT
         IEFUCBOB LIST=YES
         END
