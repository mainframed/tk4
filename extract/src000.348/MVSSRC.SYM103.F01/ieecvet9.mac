         TITLE 'IEECVET9  DELETE FOUR'
IEECVET9 CSECT
*A151600-151900                                               MH Y01564
*C151500,152000                                               MH Y01564
*A079020,079040,079100,079200,084100,084200                   MH Y01270
*D077500,078000,081000                                        MH Y01270
**********************************************************************
* STATUS       CHANGE LEVEL=0
*
* FUNCTION
*              1. REMOVAL  OF MESSAGES FOR A REGULAR DELETE REQUEST
*                INITIATED BY A CONTROL E COMMAND
*              2. AUTOMATIC MESSAGE DELETION ON A FULL SCREEN CONDITION
*              3. NUMBERING OF MESSAGES AS REQUESTED BY K D,N
*
* ENTRY POINT  IEECVET9
*
* INPUT        INDICATION OF MESSAGES TO BE REMOVED
*                   BIT SETTINGS ON INPUT INDICATING ACTION REQUIRED
*                       DCMCOM1
*                         DCMCOMAU   AUTOMATIC DELETION REQUIRED
*                         DCMCOMNM NUMBER MESSAGES
*
* OUTPUT       ALTERATION OF DISPLAY- MESSAGE REMOVAL
*              MESSAGES NUMBERED FOR K D,N REQUEST
*
* EXTERNAL REFERENCES NONE
*
* EXITS - NORMAL  IEECVETH OR P OR R  DEVICE-DEPENDENT IO
*                 IEECVET2 DISPLAY 1  (IF AUTOMATIC DELETE WORKS)
*       - ERROR   NONE
*
* TABLES/WORK AREAS DISPLAY CONTROL MODULE (CONTAINED IN DSECT)
*                   RESIDENT DISPLAY CONTROL MODULE CONTAINED    S21003
*                   IN A DSECT                                   S21003
*
* ATTRIBUTES   REFRESHABLE, PRIVILEGED, TYPE 4 SVC
*
* NOTES        THE OPERATION OF THIS MODULE DOES NOT DEPEND UPON A
*              PARTICULAR INTERNAL REPRESENTATION OF THE EXTERNAL
*              CHARACTER SET.
*
***********************************************************************
         EJECT
*/*IEECVET9: CHART */
*/*CSECT: E IEECVET9 DELETE 4 */
*/*START: P SET ALL BASES */
*/*%AUTO: D (YES,AUTODEL,NO,) AUTO DELETE REQUEST */
*/*%KDN: D (YES,NUMBER,NO,) K D,N OR K D REQUEST */
*/*%DELBUF: P BLANK DELETE REQUEST BUFFER */
*/*%WR: P SET WRITE FULL SCREEN, POSITION CURSOR HOME */
*/*PRELOOP: P POINT TO TOP OF SCT SIB */
*/*PRELOOP1: D (YES,,NO,PRELOOP2) ANY MSG IN LINE */
*/*%NEXTSCT: P (,PRELOOP1) GET NEXT SCT ENTRY */
*/*PRELOOP2: D (YES,BLANKOUT,NO,) THIS MESSAGE TO BE DELETED */
*/*MOVEUP: P (,IN) MOVE MESSAGE UP TO NEXT FREE LINE */
*/*%DOMM: P MOVE UP DOM + TJID TO NEXT FREE DOM ENTRY */
*/*%SCTM: P MOVE UP SCT ENTRY TO NEXT FREE ENTRY */
*/*IN: P INCREMENT TO NEXT SCT AND DOM ENTRY */
*/*%D1: D (YES,,NO,NOBAR) MESSAGE AUTO DELETEABLE */
*/*%BAR: P PUT BAR IN MESSAGE */
*/*NOBAR: D (YES,BLANKOUT,NO,) SHOULD LINE MOVED FROM BE BLANKED */
*/*INCRE: P GET NEXT LINE */
*/*%ANY: D (YES,PRELOOP2,NO,) ANY MORE LINES */
*/*TAUTO: D (YES,TNUM,NO,) DEL = Y */
*/*%NUMH: D (YES,NUMBER1,NO,TESTDISP) HOLD NUMBERS */
*/*BLANKOUT: P BLANK OUT THIS LINE */
*/*%DOMB: P CLEAR DOM AND TJID ENTRY */
*/*%SCTB: P CLEAR SCT ENTRY */
*/*%DELO: P (,INCRE) SET DELETION HAS OCCURRED */
*/*TNUM: D (YES,NUMBER1,NO,TESTDISP) ARE MSGS NUMBERED */
*/*NUMBER: D (YES,NUMBER2,NO,) HOLD NUMBERS */
*/*%MN: D (YES,NUMBER2,NO,) MESSAGES NUMBERED NOW */
*/*NUMBER0: D (YES,NUMBERR,NO,) IN ROLL MODE */
*/*NUMBER1: P SET MSGS NOW NUMBERED */
*/*NLOOP: S (,NOUT) MOVENUM:NUMBER ALL MSGS ON SCREEN */
*/*NUMBER2: D (YES,NUMBERR,NO,) MSG NO.'S HELD */
*/*%NOHOLD: P (,NUMBER0) SET NO LONGER HELD */
*/*NUMBERR: P (,MSGEXIT) SET CURSOR, SET REQUEST INCONSISTANT MSG */
*/*NOUT: P SET CURSOR, SET FULL SCREEN WRITE */
*/*%EN: D (YES,IOEXIT,NO,TESTDISP) ENTRY JUST TO NUMBER MSGS */
*/*TESTDISP: D (YES,,NO,FULLTEST) IS A MSG IN INFO DISPLAY */
*/*%UNVM: P SET UNVIEWABLE MSG BIT */
*/*FULLTEST: D (YES,,NO,IOEXIT) IS SCREEN FULL */
*/*%DELSUC: D (YES,DISPEXIT,NO,) WAS DELETEION SUCESSFUL */
*/*%FG: D (YES,IOEXIT,NO,) DEVICE IN ROLL MODE */
*/*%MSGWT: P SET MSG WAITING BIT, OFF UNVIEW. MSG BIT */
*/*IOEXIT: D (YES,MSGEXIT,NO,) UNVIEWABLE MSG */
*/*IOE: P (,XCTL) SET EXIT TO IO RTN */
*/*DISPEXIT: P (,XCTL) TURN OFF SCREEN FULL, SET EXIT TO IEECVET2 */
*/*MSGEXIT: P SET EXIT TO IEECVETD */
*/*XCTL: R XCTL TO PRESET MODULE */
*/*AUTODEL: P (,PRELOOP) SET UP FOR AUTO DELETE */
*/*IEECVET9: END */
         EJECT
***********************************************************************
*
*   REGISTER EQUATES
*
***********************************************************************
X0       EQU   0         REG 0
XMOVECTR EQU   1         ADDR CTR FOR MOVING UNDELETED MSGS.
X1       EQU   1         REG 1
XNUM     EQU   1         NUMBER REG
XWORK    EQU   2         WORK REG.
XHCTR    EQU   3         HIGH VALUE FOR NN
XSCTCTR  EQU   3         SCT REG
XINC     EQU   4         INCREMENT REG (BXLE)
XCOMP    EQU   5         COMPARAND (BXLE)
XLCTR    EQU   6         LOW VALUE FOR NN
X7       EQU   7         UCM BASE REG
XMSG     EQU   8         WORK REG
RETURN   EQU   8         WORK REG
X9       EQU   9         DCM BASE REG
XA       EQU   10        PROGRAM BASEREG.
XB       EQU   11        CXSA BASE REG.
XDOM1    EQU   11            PTR TO DOM ID NUMBER
XC       EQU   12        UCM ENTRY BASE REG.
XDOM2    EQU   12            PTR TO DOM ID FOR UNDELETED MSG
XIOB     EQU   13        IOB PTR. REG.
XLINK    EQU   13        WORK REG
XCCWCTR  EQU   14        REG TO COUNT CCW'S CONSTRUCTED
XE       EQU   14        WORK REG
XMASK    EQU   15                 MASK REGISTER
XF       EQU   15        REGISTER 15
X1PARM   EQU   1
XFBRANCH EQU   15
         EJECT
         BALR  XA,N0               ESTABLISH PROGRAM ADDRESSIBILITY
         USING *,XA
         B     BEGIN
ICATCH   DC    CL8'IEECVET9'
         DC    CL8'&SYSDATE',CL56' '
BEGIN    EQU   *
         LR    XB,X1               ESTABLISH CXSA ADDRESS
         USING CXSA,XB
         L     XC,CSAUCM           ESTABLISH UCM ADDRESS
         USING UCMLIST,XC
         L     X9,UCMXB            GET POINTER TO RESIDENT DCM   S21003
         L     X9,N0(X9)           GET POINTER TO T DCM          S21003
         USING DCMSTRT,X9
         L     X7,CSACTLM          GET POINTER TO UCM
         USING UCM,X7              ESTABLISH UCM BASE REGISTER
         MVC   DCMTRACE(DCMTRLEN),DCMTRAC2 SHIFT OLD TRACE ENTRIES
         MVI   DCMTREN1,ID1        PUT CSECT ID INTO
         MVI   DCMTREN2,ID2        NEW TRACE ENTRY
*   THIS ROUTINE WILL REINITIALIZE THE INSTRUCTION LINE AND
*   DELETE MSG. LINES MARKED WITH DELETE REQUEST BIT
         MVI   DCMUTILT,ZERO    ZERO UTILITY BYTE
         SR    XMASK,XMASK         ZERO MASK REGISTER
         LH    XINC,DCMSCTCN       LOAD SCREEN CONTROL TABLE INCREMENT
         TM    DCMCOM1,DCMCOMAU    Q. AUTOMAT DELETE REQUEST
         BO    AUTODEL             YES
         L     XWORK,DCMADOPN      POINT TO COMMAND OPERAND      S21002
         CLC   ZERO(N3,XWORK),DN   K D,N REQUEST                 S21002
         BE    NUMBER              YES, GO NUMBER MESSAGES       S21002
         CLC   ZERO(N2,XWORK),DB   K D DEFAULT                   S21002
         BE    NUMBER              YES, GO NUMBER LINES          S21002
* ASSUME REGULAR DELETION
         L     X1,UCMXB            POINT TO RESIDENT DCM         S21003
         USING DCMTSRT,X1
         CLI   DCMRQDEL,BLANK      INTERNAL REQUEST           MH Y01270
         BE    INTERN               YES, DONT ALT. SCREEN     MH Y01270
         NI    DCMDSTAT,N255-DCMDSAUT  TURN OFF AUTO DELETE   MH Y01270
*                                  TRIED BIT                  MH Y01270
         MVI   DCMRQDEL,BLANK      BLANK THE
         MVC   DCMRQDEL+N1(N7),DCMRQDEL      DELETE REQUEST BUFFER
         DROP  X1                                                S21003
         OI    DCMIOCM1,DCMWRWRN+DCMWRMSG+DCMWRENT+DCMINSC+DCMWRINS
         MVI   DCMCULNO,N1         INDICATE INSERT CURSOR IN POSITION
         MVI   DCMPOSCU,N1          ONE OF THE ENTRY AREA
         OI    DCMIOCM2,DCMBLENT   INDICATE BLANK ENTRY AREA
         NI    DCMCOM1,N255-DCMLPENT    TURN OFF LIGHT PEN ENTRY BIT
         OI    DCMIOCM2,DCMINSSH   REINITIALIZE INSTRUC LINE
INTERN   NI    DCMDSTAT,X'FF'-DCMDSINR INT. REQ. DEL. TRIED   MH Y01270
         IC    XMASK,MASKBIT+N1 MASK FOR DEL. REQ. SCT BIT    MH Y01270
PRELOOP  EQU   *
         STM   XB,XC,DCMDSAV+N4    SAVE REGISTERS WHILE DELETING
         L     XMOVECTR,DCMASCRN   GET ADDRESS OF FIRST MESSAGE IN
         LA    XMOVECTR,N0(XMOVECTR)   CLEAR HIGH ORDER BYTE
         LR    XMSG,XMOVECTR        THE SCREEN IMAGE BUFFER
         L     XWORK,DCMAMTAB      GET ADDR OF FIRST ENTRY IN THE SCT
         LR    XCOMP,XWORK         SET UP FOR TOP OF SCT         S21003
PRELOOP1 EQU   *                                                 S21003
         TM    N0(XCOMP),DCMMSGIN   ANY ENTRY                    S21003
         BNO   PRELOOP2            NO, END OF ENTRIES            S21003
         LA    XCOMP,N2(XCOMP)     NEXT ELEMENT                  S21003
         B     PRELOOP1            TEST FOR ENTRY                S21003
PRELOOP2 EQU   *                                                 S21003
         BCTR  XCOMP,X0            POINT TO THE                  S21003
         BCTR  XCOMP,X0            LAST SCT ENTRY FOR BXLE       S21003
         LR    XSCTCTR,XWORK
         L     XDOM1,DCMDOMPK      GET ADDRESS OF FIRST DOM ID NUMBER
         LR    XDOM2,XDOM1
         LH    XE,DCMLGNTH         GET LENGTHN OF LINE
         BCTR  XE,N0               DECREENT BY 1
         LR    X7,XE               GET LENGTH FOR MOVING LINE
         BCTR  XE,N0               GET LENGTH FOR BLANKING LINES
DNEXT    EQU   *
         EX    XMASK,TUMASK        Q. DELETE THIS MESSAGE LINE
         BZ    MOVEUP              NO, MOVE IT UP
         TM    N1(XWORK),DCMMSGIR  Q. AN INTERVENTION REQUIRED MESSAGE
         BNO   BLANKOUT            NO
         LH    XLINK,DCMIRCTR      LOAD CTR FOR INTERV REQ'D MSGS
         SH    XLINK,ONE           SUBTRACT ONE FROM 17
         STH   XLINK,DCMIRCTR      STORE IT BACK
BLANKOUT EQU   *
         MVI   N0(XMSG),BLANK      BLANK ONE BYTE
         EX    XE,MVEBLANK         THEN PROPAGATE BLANKS THRO LINE
         MVC   N0(N2,XWORK),CZERO   CLEAR SCT ENTRY FLAGS        S21002
         XC    N0(N8,XDOM1),N0(XDOM1) CLEAR DOM BYTES
         OI    DCMUTILT,DCMUTILA   SET BIT - DELETION HAS OCCURRED
         B     INCRE               GO INCREMENT COUNTERS
MOVEUP   EQU   *
         EX    X7,MVELINE          MOVE MSG LINE UP
         MVC   N0(N2,XMOVECTR),CBLANK   BLANK NUMBER
         MVC   N0(N8,XDOM2),N0(XDOM1)  MOVE DOM ID INTO CORRES. POSITIO
         MVC   N0(N2,XSCTCTR),N0(XWORK)   HOLD NEW SCT ENTRY
IN       EQU   *
         LA    XDOM2,N8(XDOM2)     INCREMENT DOM REGISTER
         LA    XSCTCTR,N0(XSCTCTR,XINC)
         TM    N0(XWORK),DCMMSGAD  Q. MSG. AUTOMATICALLY DELETABLE
         BZ    NOBAR               NO VERTICAL BAR REQUIRED
         MVI   N2(XMOVECTR),VERTBAR REPLACE VERTICAL BAR
NOBAR    EQU   *
         LH    XLINK,DCMCORLN      LOAD REG FOR INCREMENT     MB  M2070
         CR    XMOVECTR,XMSG       DETERMINE WHETHER LINE SHOULD BE
*                                   BLANKED OUT
         LA    XMOVECTR,N0(XLINK,XMOVECTR) INCR TO NXT LINE   MB  M2070
         BNE   BLANKOUT  GO BLANK OUT LINE
INCRE    EQU   *
         LA    XDOM1,N8(XDOM1)     INCREMENT DOM REGISTER
         AH    XMSG,DCMCORLN       INCREMENT ADDR OF MSG LINE MB  M2070
BXLE     EQU   *
         BXLE  XWORK,XINC,DNEXT    INCREMENT + BR
         LM    XB,XC,DCMDSAV+N4        RESTORE REGISTERS 11 AND 12
TAUTO    EQU   *
         TM    DCMCOM1,DCMCOMAU    TEST FOR AUTOMATIC DELETE
         BO    TNUM      YES
         TM    DCMDSTAT,DCMDSTNH   Q. NUMBER HOLD OPTION IN EFFECT
         BO    NUMBER1             YES, GO NUMBER                S21002
         NI    DCMDSTAT,N255-DCMDSTNM  TURN OFF MSGS ARE NUMB. BIT
         B     TESTDISP            TEST FOR WARNING MESSAGES
TNUM     EQU   *
         TM    DCMDSTAT,DCMDSTNM    Q. ARE MESSAGES NUMBERED
         BZ    TESTDISP  TEST FOR WARNING MSG
         B     NUMBER1             YES, GO NUMBER                S21002
NUMBER   EQU   *
         CLC   ZERO(N8,XWORK),DNHOLD   K D,N,HOLD REQUEST        S21002
         BE NUMBER2                YES                           S21002
         TM    DCMDSTAT,DCMDSTNM   ARE MESSAGES NUMBERED         S21002
         BO    NUMBER3             YES, SEE IF IN HOLD MODE      S21002
NUMBER0  EQU   *                                                 S21002
         CLI   DCMDEL,CR           ARE WE IN ROLL MODE           S21002
         BE    NUMBERR             YES, DO NOT NUMBER            S21002
NUMBER1  EQU   *                                                 S21002
         OI    DCMDSTAT,DCMDSTNM   INDICATE MESSAGES NUMBERED    S21002
         L     XMSG,DCMASCRN       ADDR OF FIRST MSG IN BUFFER
         L     XWORK,DCMAMTAB      GET ADDR OF FIRST MSG IN SCT
         SR    XNUM,XNUM           ZERO NUMBER REGISTER
NLOOP    EQU   *
         TM    N0(XWORK),DCMMSGIN  Q. IS THERE A MESSAGE IN THE LINE
         BZ    NOUT                NO, EXT
         AH    XNUM,ONE            INCREMENT TO NEXT NUMBER
         CVD   XNUM,DCMPACK        CONVERT NUMBER TO DECIMAL
         UNPK  DCMCVBIN+N2(N2),DCMCVBIN+N2(N2)  UNPACK NUMBER
         OI    DCMCVBIN+N3,CF0     FIX SIGN
         CLI   DCMCVBIN+N2,CF0     Q. A ONE DIGIT NUMBER
         BNE   MOVENUM             NO
         MVI   DCMCVBIN+N2,BLANK   YES, BLANK OUT FIRST DIGIT
MOVENUM  EQU   *
         MVC   N0(N2,XMSG),DCMCVBIN+N2  MOVE NUMBER INTO MSG LINE
         AH    XMSG,DCMCORLN       INCRE TO NEXT MSG LINE     MB  M2070
         LA    XWORK,N2(XWORK)     POINT TO NEXT SCT ENTRY       S21002
         B     NLOOP               LOOP TO LAST MESSAGE          S21002
NUMBER2  EQU   *                                                 S21002
         TM    DCMDSTAT,DCMDSTNH   ARE MESSAGE NUMBERS HELD      S21002
         BO    NUMBERR             YES, DO NOT NUMBER            S21002
         CLI   DCMDEL,CR           ARE WE IN ROLL MODE           S21002
         BE    NUMBERR             YES, DO NOT NUMBER            S21002
         OI    DCMDSTAT,DCMDSTNH   INDICATE NUMBERS HELD         S21002
         B     NUMBER1             NUMBER MESSAGES               S21002
NUMBER3  EQU   *                                                 S21002
         TM    DCMDSTAT,DCMDSTNH   ARE MESSAGE NUMBERS HELD      S21002
         BNO   NUMBERR             NO, ERROR                     S21002
         NI    DCMDSTAT,N255-DCMDSTNH   NO LONGER HELD           S21002
         B     NUMBER0             CHECK FOR ROLL MODE           S21002
NUMBERR  EQU   *                                                 S21002
         L     X1,DCMAENTR         POINT TO ENTRY AREA           S21002
         SR    XWORK,X1            FIND OPERAND DISPLACEMENT     S21002
         LA    XWORK,N1(XWORK)     CURSOR POSITION               S21002
         CH    XWORK,DCMLGNTH      LINE TWO                      S21002
         BH    NUMBERR2            YES                           S21002
         MVI   DCMCULNO,N1         LINE ONE                      S21002
NUMBERR1 EQU   *                                                 S21002
         STC   XWORK,DCMPOSCU      POSITION CURSOR               S21002
         OI    DCMIOCM1,DCMINSC    INSERT CURSOR                 S21002
         OI    DCMCMSG2,DCMRQINC   REQUEST INCONSISTANT          S21002
         CLI   DCMDEL,CR           ARE WE IN ROLL MODE        MF  M1680
         BE    MSGEXIT             YES, JUST REQUEST          MF  M1680
*                                       INCONSISTENT          MF  M1680
         OI    DCMCMSG1,DCMSTEX    NO, INDICATE STATUS        MF  M1680
*                                       ALREADY EXISTS        MF  M1680
         B     MSGEXIT             GO TO MESSAGE MODULE          S21002
NUMBERR2 EQU   *                                                 S21002
         SH    XWORK,DCMLGNTH      COMPUTE LINE 2 OPERAND        S21002
         MVI   DCMCULNO,N2         LINE NUMBER 2                 S21002
         B     NUMBERR1            CONTINUE                      S21002
NOUT     EQU   *
         STH   XNUM,DCMADNUM       STORE NUMBER
         MVI   DCMLINEN,N1         START AT LINE ONE             S21002
         OI    DCMIOCM1,DCMWRMSG+DCMWRINS                     MH Y01564
         L     XWORK,DCMADOPN      POINT TO OPERAND           MH Y01564
         CLI   N0(XWORK),BLANK     IS REQUEST FROM OPERATOR   MH Y01564
         BE    DONTWRT             NO, DONT REWRT ENTRY AREA  MH Y01564
         OI    DCMIOCM1,DCMWRENT+DCMINSC YES, WRITE ENTRY     MB YM4087
DONTWRT  OI    DCMIOCM2,DCMBLENT+DCMINSSH      BLANK ENTR     MH Y01564
         MVI   DCMCULNO,N1         INDICATE SET CURSOR
         MVI   DCMPOSCU,N1           IN POSITION ONE OF ENTRY AREA
         TM    DCMCOM1,DCMCOMNM    JUST NUMBERING TO DO          S21002
         BNO   TESTDISP            NO, CHECK OTHER               S21002
         B     IOEXIT              EXIT TO I/O ROUTINE
TESTDISP EQU   *
         OI    DCMIOCM2,DCMBLWRR   BLANK RIGHT HALF OF WARNING LINE
         OI    DCMIOCM1,DCMWRWRN+DCMWRMSG  INDIC WRITE WARN AND MSG AR
         L     XWORK,DCMADSEC      POINT TO TOP OF SSCT          S21002
         SR    XNUM,XNUM           CLEAR COUNT REG               S21002
TESTLOOP EQU   *                                                 S21002
         TM    N0(XWORK),SSCTF0    ANY ENTRY IN LINE             S21002
         BNZ   TESTD1              YES, STATUS DISPLAY ON SCREEN S21002
         TM    N0(XWORK),DCMSECST   END OF SSCT                  S21002
         BO    FULLTEST            YES, NO DISPLAY ON SCREEN     S21002
         LA    XWORK,N1(XWORK)     POINT TO NEXT SSCT ELEMENT    S21002
         LA    XNUM,N1(XNUM)       RECODR ONE NON-DISPLAY LINE   S21002
         B     TESTLOOP            TEST ELEMENT                  S21002
TESTD1   EQU   *                                                 S21002
         L     XWORK,DCMAMTAB      GET POINTER TO SCT            S21002
         SLL   XNUM,N1             FIND FIRST DISPLAY LINE       S21002
         AR    XWORK,XNUM          EQUIVALENT IN SCT             S21002
         TM    N0(XWORK),DCMMSGIN  Q. IS MSG IN INFO DISPLAY
         BNO   FULLTEST            NO GO TEST . FOR FULL SCREEN BIT
         OI    DCMCMSG1,DCMUNMSG   INDICATE WRITE UNVIEW MSG
FULLTEST EQU   *
         L     X1,UCMXB            POINT TO RESIDENT DCM         S21003
         USING DCMTSRT,X1
         TM    DCMR2FLG,DCMRXSFL   Q. IS SCREEN FULL BIT ON
         BNO   IOEXIT              NO, EXIT
         DROP  X1                                                S21003
         TM    DCMUTILT,DCMUTILA   Q. WAS DELETION SUCCESSFUL
         BO    DISPEXIT            YES, EXIT TO DISPLAY RTN
         CLI   DCMDEL,CR           Q. ARE WE IN ROLL MODE
         BE    IOEXIT              YES, DONT WRITE MESSAGE WAITING
         OI    DCMCMSG1,DCMMSGWT   MOVE IN MSG WAITING WARNING
         NI    DCMCMSG1,N255-DCMUNMSG TURN OFF UNVIEWABLE MSG BIT
IOEXIT   EQU   *
         TM    DCMCMSG1,DCMUNMSG   Q. MOVE IN UNVIEWABLE MSG
         BO    MSGEXIT             YES, EXIT TO MESSAGE MODULE 1
         L     XFBRANCH,DCMIORTN   LOAD IO ROUTINE ADDRESS
         B     XCTL                EXIT TO IEECVET(H,P,R, OR U)
DISPEXIT EQU   *
         NI    UCMDEVC,N255-UCMDEVA    TURN OFF UCM SCREEN FULL BIT
         L     X1,UCMXB            POINT TO RESIDENT DCM         S21003
         USING DCMTSRT,X1
         NI    DCMR2FLG,N255-DCMRXSFL   SET OFF FULL SCREEN      S21003
         DROP  X1                                                S21003
         NI    DCMCMSG1,N255-DCMMSGWT  TURN OFF MESSAGE WAITING BIT
         L     XFBRANCH,DCMNDSP1   LOAD DISPLAY 1 ADDRESS
         B     XCTL                EXIT TO IEECVET2
MSGEXIT  EQU   *
         L     XFBRANCH,DCMNMSG1   LOAD MESSAGE 1 ADDRESS
*                                  FOR EXIT TO IEECVETD
XCTL     EQU   *
         NI    DCMCOM1,N255-DCMCOMAU-DCMCOMNM  TURN OFF BITS
         L     X1PARM,DCMCXSVE     RESTORE CXSA ADDRESS
         BR    XFBRANCH            EXIT TO ADDRESS ALREADY SET
AUTODEL  EQU   *
         NI    DCMDSTAT,N255-DCMDSAUT   TURN OFF AUTO DELETE TRIED BIT
         IC    XMASK,MASKBIT    MASK FOR AUTO DELETE
         B     PRELOOP NO-DON'T REINIT INSTRUC LINE
         EJECT
***********************************************************************
*                                                                     *
*        EXECUTABLE INSTRUCTIONS                                      *
*                                                                     *
***********************************************************************
MVEBLANK MVC   N1(N0,XMSG),N0(XMSG)  USED FOR BLANKING LINES
MVELINE  MVC   N0(N0,XMOVECTR),N0(XMSG) MOVE MESSAGE LINE
TUMASK   TM    N0(XWORK),HEXZERO
         SPACE 3
***********************************************************************
*                                                                     *
*        CONSTANTS                                                    *
*                                                                     *
***********************************************************************
DN       EQU   *                                                 S21002
DNHOLD   EQU   *
         DC    C'D,N'              K D,N COMPARAND               S21002
         DC    C',HOLD'            K D,N,HOLD COMPARAND          S21002
DB       DC    C'D '               K D DEFAULT COMPARAND         S21002
CBLANK   DC    C'  '               BLANK CHARACTER
ONE      DC    H'1'                CONSTANT  OF ONE
MASKBIT  DC    X'0804'             AUTO.DELETE- DEL. REQUEST BIT
CZERO    DC    F'0'                ZERO
HSIX     DC    H'6'                CONSTANT OF SIX
         SPACE 3
***********************************************************************
*                                                                     *
*        EQUATE STATEMENTS                                            *
*                                                                     *
***********************************************************************
ID1      EQU   C'E'                1ST CHARACTER OF CSECT ID
ID2      EQU   C'9'                2ND CHARACTER OF CSECT ID
TMASK    EQU   X'80'               FOR FINDKEY TEST              S21003
SSCTF0   EQU   X'F0'               ENTRY EXISTS IN SSCT          S21002
ZERO     EQU   0         NUMBER
VERTBAR  EQU   C'×'                VERTICAL BAR
CF0      EQU   C'0'          CHARACTER 0
HEXZERO  EQU   X'00'               HEX ZERO
N0       EQU   0         NUMBER
N1       EQU   1         NUMBER
N2       EQU   2         NUMBER
N3       EQU   3         NUMBER
N4       EQU   4         NUMBER
N6       EQU   6         NUMBER
N7       EQU   7         NUMBER
N8       EQU   8         NUMBER
N15      EQU   15        NUMBER
N255     EQU   255       NUMBER
ENDCHAIN EQU   X'20'     NUMBER
DASH     EQU   C'-'          DASH
BLANK    EQU   C' '          BLANK CHARACTER
CR       EQU   C'R'                CHARACTER FOR ROLL MODE TEST
         EJECT
***********************************************************************
*                                                                     *
*        DSECTS                                                       *
*                                                                     *
***********************************************************************
         IEERDCM
         EJECT
         IEETDCM
         EJECT
         IEECUCM  FORMAT=NEW
         EJECT
         IHACTM  CXSA
         SPACE 5
         END
