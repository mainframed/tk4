IEECVET6 CSECT
***********************************************************************
         TITLE 'IEECVET6  DELETE 1'
***********************************************************************
*                                                                     *
* STATUS       CHANGE LEVEL=0                                         *
*                                                                     *
* APARS FIXED  ZA08082,ZA14031                                        *
*                                                                     *
* FUNCTION     ANALYZE DELETION OF MESSAGES REQUESTED BY K E,F OR     *
*              K E,NN CONTROL COMMAND                                 *
*                                                                     *
* ENTRY POINT  IEECVET6                                               *
*                                                                     *
* INPUT        INDICATION OF K E,NN OR K E,F DELETE REQUEST.          *
*                                                                     *
* OUTPUT       ALTERATION OF DISPLAY- INDICATION OF MESSAGES TO BE    *
*              DELETED                                                *
*                                                                     *
* EXTERNAL REFERENCES NONE                                            *
*                                                                     *
* EXITS - NORMAL                                                      *
*              IEECVETH OR P OR R  DEVICE-DEPENDENT IO                *
*              IEECVET9 DELETE 4  WHEN NO VERIFICATION NECESSARY      *
*       - ERROR                                                       *
*              IEECVETE MESSAGE 2                                     *
*                                                                     *
* TABLES/WORK AREAS DISPLAY CONTROL MODULE (CONTAINED IN DSECT)       *
*                   SCREEN CONTROL TABLE (SCT)                        *
*                   SECONDARY SCREEN CONTROL TABLE (SSCT)             *
*                                                                     *
* ATTRIBUTES   REFRESHABLE, PRIVILEGED, TYPE 4 SVC                    *
*                                                                     *
* NOTES        THE OPERATION OF THIS MODULE DOES NOT DEPEND UPON A    *
*              PARTICULAR INTERNAL REPRESENTATION OF THE EXTERNAL     *
*              CHARACTER SET.                                         *
*                                                                     *
***********************************************************************
         EJECT
*
*   REGISTER EQUATES
*
X0       EQU   0         SECONDARY SCREEN CONTROL TABLE PTR      S21002
X1       EQU   1         REG 1
XNUM     EQU   1         REG 1
XWORK    EQU   2         WORK REG.
XHCTR    EQU   3         HIGH VALUE FOR NN
XINC     EQU   4         INCREMENT REG (BXLE)
XCOMP    EQU   5         COMPARAND (BXLE)
XLCTR    EQU   6         LOW VALUE FOR NN
X7       EQU   7         UCM BASE REG
XMSG     EQU   8         BRANCH REG.
X9       EQU   9         DCM BASE REG
XA       EQU   10        PROGRAM BASEREG.
XB       EQU   11        CXSA BASE REG.
XC       EQU   12        UCM ENTRY BASE REG.
X13      EQU   13                  RESIDENT DCM BASE REGISTER    S21002
XE       EQU   14        REG 14
XMASK    EQU   15        MASK REGISTER
XFBRANCH EQU   15
XERTRN   EQU   14
X1PARM   EQU   1
         EJECT
         BALR  XA,N0               ESTABLISH PROGRAM ADDRESSIBILITY
         USING *,XA
         LR    XB,X1               ESTABLISH CXSA ADDRESS
         B     BEGIN               BRANCH AROUND PATCH AREA
ICATCH   DC    C'IEECVET6'         EYECATCHER AND FIELD MAINTENANCE
         DC    CL8'&SYSDATE',CL56' '
BEGIN    EQU   *                   END OF PATCH AREA
         USING CXSA,XB
         L     XC,CSAUCM           ESTABLISH UCM ADDRESS
         USING UCMLIST,XC
         L     X13,UCMXB           GET PTR TO RESIDENT DCM       S21002
         L     X9,N0(X13)          GET PTR TO TRANSIENT DCM      S21002
         USING DCMTSRT,X13         ADDRESSIBILITY RESIDENT DCM   S21002
         USING DCMSTRT,X9         ESTABLISH DCM BASE REGISTER
         L     X7,CSACTLM          GET POINTER TO UCM
         USING UCM,X7              ESTABLISH UCM BASE REGISTER
         MVC   DCMTRACE(DCMTRLEN),DCMTRAC2 SHIFT OLD TRACE ENTRIES
         MVI   DCMTREN1,ID1         PUT CSECT ID INTO
         MVI   DCMTREN2,ID2         NEW TRACE ENTRY
         L     XFBRANCH,CSAXC       ADDRESS FREELOCK SUBROUTINE
         BALR  XERTRN,XFBRANCH      RELEASE LOCKS
         LH    XINC,DCMSCTCN        INCREMENT FOR SCT          @ZA08082
         CLC   DCMASCRN,DCMWTBUF    ANY VISABLE MESSAGES       @ZA08082
         BE    ERROR2               NO,NO DELETABLES           @ZA08082
         MVI   DCMUTILT,ZERO   ZERO UTILITY BYTE
         SR    XMASK,XMASK         ZERO MASK REGISTER
         LA    XLCTR,N1            SET LOW NUMBER REG TO ONE     S21002
         L     XWORK,DCMADOPN     GET ADDR OF OPERAND
         MVC   DCMRQDEL(N8),N0(XWORK)
         CLI   N1(XWORK),COMMA     Q. IS SECOND CHARACTER COMMA
         BNE   ERROR1              NO, INVALID OPERAND
         CLC   N2(N2,XWORK),FDEL   IS OPERAND  E,F
         BE    DF                  YES
         TM    N2(XWORK),FZERO     Q. IS OPERAND A NUMBER
         BNO   ERROR1              NO, INVALID OPERAND
***
***  K E,NN(,NN) DELETE REQUEST
***
         ST    XMASK,DCMCVBIN      INIT DCMPACK & DCMCVBIN TO 0
         OI    DCMCVBIN+N2,FZERO   PUT 'F0' IN 3RD BYTE IN CASE ONE
         MVC   DCMPACK(N4),DCMCVBIN DIGIT NUMBER SPECIFIED
***
***  TEST FOR  VALIDITY OF   NN(,NN)
***
         CLI   N3(XWORK),BLANK     Q  IS COLUMN 2 BLANK
         BNE   COM2                NO - GO TEST FOR COMMA
         MVC   DCMPACK+N3(N1),N2(XWORK) MOVE COL 1 TO FIELD
         B     ONENUM              ONLY ONE NUMBER SPECIFIED
COM2     EQU   *
         CLI   N3(XWORK),COMMA     Q. IS COLUMN 2 A COMMA
         BNE   C3                  NO- GO TEST FOR COMMA IN COL.3
         MVC   DCMPACK+N3(N1),N2(XWORK) YES,MOVE COL 1 TO FIELD
         CLI   N5(XWORK),BLANK     Q. IS COL 4 BLANK
         BNE   M34                      NO-GO TO M34
         MVC   DCMCVBIN+N3(N1),N4(XWORK) MOVE COL 3 TO FIELD
         B     TEST                GO TEST VALIDITY OF NUMBERS
M34      MVC   DCMCVBIN+N2(N2),N4(XWORK) MOVE COL 3 AND 4 TO FIELD1
         CLI   N6(XWORK),BLANK     Q. IS COL 5 BLANK
         BE    TEST                YES- GO TEST FOR VALIDITY OF NUMBERS
         BNE   ERROR               NO- ERROR
C3       MVC   DCMPACK+N2(N2),N2(XWORK) MOVE COL 1+2 TO FIELD
         CLI   N4(XWORK),COMMA     Q. IS COL 3 A COMMA
         BNE   C3B                 NO-GO TEST FOR BLANK
         CLI   N6(XWORK),BLANK     Q. IS COLS BLANK
         BNE   M45                 NO-GO TO M45
         MVC   DCMCVBIN+N3(N1),N5(XWORK) MOVE COL 4 TO FIELD1
         B     TEST                GO TEST FOR VALIDITY OF NUMBERS
M45      EQU   *
         CLI   N7(XWORK),BLANK     Q. IS COLUMN 6 BLANK
         BNE   ERROR     NO-INVALID OPERAND
         MVC   DCMCVBIN+N2(N2),N5(XWORK) MOVE COL 4 AND 5 TO FIELD1
         B     TEST                GO TEST VALIDITY OF NUMBERS
C3B      CLI   N4(XWORK),BLANK     Q. IS COL 3 BLANK
         BNE   ERROR     NO- DELETE REQ INCONSISTENT
ONENUM   EQU   *
         MVC   DCMCVBIN+N2(N2),DCMPACK+N2 SET FIELD1=FIELD
TEST     EQU   *
         CLI   DCMPACK+N2,FZERO        TEST VALIDITY OF NUMBER
         BL    ERROR                   NO, ERROR
         CLI   DCMPACK+N3,FZERO        TEST VALIDITY OF NUMBER
         BL    ERROR                   NO, ERROR
         CLI   DCMCVBIN+N2,FZERO       Q, IS NUMBER VALID
         BL    ERROR                   NO, ERROR
         CLI   DCMCVBIN+N3,FZERO      Q.    IS NUMBER VALID
         BL    ERROR                   NO, ERROR
         CLC   DCMPACK+N2(N2),DCMCVBIN+N2 COMPARE FIELD WITH FIELD1
         BH    ERROR     ERROR- DELETE REQUEST INCONSISTENT
         L     XLCTR,DCMPACK
         BAL   XMSG,BCONVERT   CONVERT HIGH NUMBER TO BINARY
         LR    XHCTR,XWORK
         LH    XWORK,DCMMSGAL      GET LENGTH OF MESSAGE AREA
         CR    XHCTR,XWORK         Q. IS HIGH NUMBER WITHIN MESSAGE ARE
         BH    ERROR               NO, ERROR
         ST    XLCTR,DCMCVBIN   CONVERT LOW NUMBER TO BIN
         BAL   XMSG,BCONVERT       GO TO CONVERSION RTN.
         LR    XLCTR,XWORK   BIN VALUE COMES BACK IN XWORK
         LTR   XLCTR,XLCTR             Q. IS FIRST NUMBER ZERO
         BZ    ERROR                   YES, DELETE REQ INCONSISTENT
WIDTEST  EQU   *
         LR    XCOMP,XHCTR         COMPUTE SCREEN CONTROL TABLE
         MH    XCOMP,DCMSCTCN        DISPLACEMENT OF HIGH NUMBER
         SR    XCOMP,XINC
         L     XWORK,DCMAMTAB      GET ADDR OF THE SCREEN CONTROL TABLE
         LA    XCOMP,N0(XCOMP,XWORK)   INCREMENT TO SCT ADDRESS OF HIGH
*                                      NUMBER IN RANGE FOR DELETION
         TM    N0(XCOMP),DCMMSGIN  Q. IS THERE A MSG. IN LINE
         BZ    ERROR                    NO- ERROR
         L     XWORK,DCMADSEC      ADDR OF THE SECONDARY SCT     S21002
         AR    XWORK,XHCTR         INCR TO SSCT HIGH NUMBER MSG  S21002
         BCTR  XWORK,N0            DECREMENT TO LAST SSCT ENTRY  S21002
*                                  IS LAST MSG IN STATUS DISPLAY S21002
         TM    N0(XWORK),DCMSECCL+DCMSECLL+DCMSECDL+DCMSECBL     S21002
         BC    5,ERROR             BRANCH YES                    S21002
         TM    N1(XCOMP),DCMMSGAC  Q. IS LAST MSG. A WTOR
         BZ    SETMASK                  NO- GO SET UP MASK FOR DELETE
         CR    XLCTR,XHCTR              Q. ONLY ONE MESSAGE SPECIFIED
         BNE   SETMASK             NO, GO ON TO DELETE
         OI    DCMUTILT,DCMUTILC   BIT TO INDICATE WTOR DELETABLE
SETMASK    EQU   *
         IC    XMASK,MASKBIT+N1        SET UP MASK FOR DEL REQUEST
***
***  THIS ROUTINE WILL SET THE DELETION REQUEST BIT FOR ALL MESSAGES
***  WITH THE APPROPRIATE MASK
***
INIT     EQU   *
         L     X0,DCMADSEC         START OF SSCT              MB YM4087
         LR    XE,X0               SAME FOR HIGH LINE         MB YM4087
         BCTR  XE,0                BACK UP ONE                MB YM4087
         AR    XE,XHCTR            HIGH LINE SSCT             MB YM4087
         MH    XHCTR,DCMSCTCN  MULT. HIGH NUMBER BY SCT INCRE.
         SR    XHCTR,XINC          AND COMPUTE SCT ADDRESS FOR HIGH NUM
         L     XWORK,DCMAMTAB      SET UP REGISTERS FOR BXLE
         L     XCOMP,DCMADDRL      GET ADDR OF LAST MSG IN SCT
         AR    XHCTR,XWORK         HIGH LINE SCT              MB YM4087
         MH    XLCTR,DCMSCTCN  COMPUTE SCT ADDR. FOR LOW NUMBER
         SR    XLCTR,XINC
         AR    XLCTR,XWORK         LOW LINE SCT               MB YM4087
         SR    XNUM,XNUM           ZERO NUMBER REGISTER
         OI    DCMDSTAT,DCMDSTNM    SET BIT- MSGS. NUMBERED
         L     XMSG,DCMASCRN       ADDR OF FIRST MSG IN BUFFER
***
***  XLCTR AND XHCTR ARE THE LIMITS FOR DELETION
***  TEST FOR CONTINUATION LINES IN FIRST OR LAST MESSAGE WITHIN RANGE
***  OF DELETION
***
CONT1    TM    N1(XLCTR),DCMMSGCT  Q. IS FIRST LINE A CONTINATION LINE
         BNO   GOON                NO
         SR    XLCTR,XINC          DECREMENT TO INCLUDE PREVIOUS LINE
         B     CONT1               CHECK FOR FURTHER CONTIN
GOON     EQU   *
         TM    N0(XHCTR),DCMMSGCN  Q. IS LAST LINE CONTINUED
         BNO   LOOP                NO
***                                                              S21002
***         IS THE CONTINUED LINE IN A STATUS DISPLAY            S21002
***                                                              S21002
         TM    N1(XE),DCMSECCL+DCMSECLL+DCMSECDL+DCMSECBL        S21002
         BZ    CINCRE              BRANCH NO                     S21002
         SR    XHCTR,XINC          YES, DO NOT DELETE IT
         CLR   XLCTR,XHCTR         Q. ARE THERE ANY DELETABLE MSGS
         BH    ERROR2              NO, ERROR
         B     LOOP                YES, CONTINUE
CINCRE   EQU   *
         AR    XHCTR,XINC          INCREMENT TO INCLUDE CONTINUED LINE
         LA    XE,X1(XE)           AND CONTINUED LINE SSCT    MB YM4087
         B     GOON                CHECK FOR FURTHER CONTIN
LOOP    EQU    *
         TM    N0(XWORK),DCMMSGIN  Q. IS THERE A MSG IN THE LINE
         BZ    NOUT      NO/ NO MORE MESSAGES
         BAL   XE,NUMBER           GO NUMBER MESSAGE
         CLR   XWORK,XLCTR         IF MESSAGE BEING TESTED IS NOT
         BL    TURNOFF                BETWEEN HIGH AND LOW NUMBERS
         CLR   XWORK,XHCTR            SPECIFIED - TURN OFF DEL.REQ.BIT
         BH    TURNOFF                AND REMOVE VERTICAL BAR
SLOOP   EQU   *
         EX    XMASK,TUMASK  DLETEION FOR THIS MSG
         BNZ   HERE                YES-DELETE THIS MESSAGE
TURNOFF   EQU   *
         NI    N0(XWORK),N255-DCMMSGRD TURN OFF DELETE REQUEST BIT
         TM    N0(XWORK),DCMMSGIF  IS THERE AN INFORMATIONAL MSG IN LIN
         BZ    REMOVE     NO-GO REMOVE BAR
         MVI   N2(XMSG),HORBAR     REPLACE HORIZ. BAR
         B     NNEXT               GO INCREMENT CTRS.
REMOVE    EQU   *
         MVI   N2(XMSG),BLANK      REMOVE VERTICAL BAR
         TM    N1(XWORK),DCMMSGAC      Q. AN ACTION MESSAGE
         BZ    NNEXT                   NO, GO INCRE COUNTERS
         TM    N1(XWORK),DCMMSGPP   IS IT A PROB PROG ACTION MSG.Y01958
         BNO   ASTER                NO MARK WITH ASTERISK     MD Y01958
         MVI   N3(XMSG),PPACT       MARK WITH A '@'           MD Y01958
         B     NNEXT                GET NEXT MSG              MD Y01958
ASTER    EQU   *                                              MD Y01958
         MVI   N3(XMSG),ASTERISK       MOVE IN ASTERISK
         B     NNEXT               GO INCREMENT CTRS
HERE     EQU   *
         TM    N1(XWORK),DCMMSGAC  Q. IS MSG A WTOR
         BZ    SET   NO-  SET BIT
         TM    DCMUTILT,DCMUTILC   Q. IS WTOR DELETABLE
         BNO   TURNOFF             NO,  DO NOT MARK FOR DELETION
SET      EQU   *
         TM    N0(XWORK),DCMMSGCN  Q. IS THIS MSG CONTINUED
         BNO   GODEL               NO, GO DELETE IT
         LR    XE,X0               ADDRESS OF SSCT ENTRY         S21002
***                                                              S21002
***        IS THE CONTINUED LINE IN A STATUS DISPLAY             S21002
***                                                              S21002
         TM    N1(XE),DCMSECCL+DCMSECLL+DCMSECDL+DCMSECBL        S21002
         BNZ   TURNOFF           BRANCH YES                    S21002
GODEL    EQU   *
         OI    DCMUTILT,DCMUTILA    SET BIT-DELETION POSSIBLE
         OI    N0(XWORK),DCMMSGRD  SET DELETION REQUEST BIT IN SCT
         MVI   N3(XMSG),BLANK      REMOVE ASTERISK IF PRESENT
         MVI   N2(XMSG),VERTBAR    MARK MESSAGE WITH VERTICAL BAR
NNEXT    EQU   *
         AH    XMSG,DCMCORLN       INCREMEMT TONEXT MSG LINE     S21003
         AH    X0,ONE              INCREMENT TO NEXT SSCT ENTRY  S21002
         BXLE  XWORK,XINC,LOOP     LOOP IF MORE MESSAGES TO CHECK
NOUT     EQU   *
         STH   XNUM,DCMADNUM       STORE NUMBER
         TM    DCMUTILT,DCMUTILA     Q. IS DELETION POSSIBLE
         BZ    ERROR2              NO, ERROR- NO DELETABLE MESSAGES
         CLI   DCMDEL,R            ROLL OR ROLL DELETE MODE    @ZA14031
         BE    DEL4EXIT            BRANCH TO IEECVET9 EXIT     @ZA14031
         TM    DCMOPTST,DCMOPTVR    Q.  IS VERIFICATION REQUIRED
         BO    VERIFMSG                YES, GO SET UP VERIF MSG
DEL4EXIT EQU   *
         L     XFBRANCH,DCMNDEL4    LOAD DELETE 4 ADDRESS
         B     XCTL                 EXIT TO IEECVET9
VERIFMSG EQU   *
         OI    DCMCMSG2,DCMDLREQ        INDIC MOVE IN VERIFIC INSTRUC
         OI    DCMIOCM1,DCMWRMSG+DCMWRINS+DCMWRENT+DCMINSC
         BAL   XE,CURSCOMP         GO COMPUTE CURSOR POSITION
         L     XFBRANCH,DCMNMSG1    LOAD MESSAGE 1 ADDRESS
*                                   FOR EXIT TO IEECVETD
XCTL     EQU   *         EXIT FROM MODULE
         L     X1PARM,DCMCXSVE      PASS CXSA ADDRESS IN REG 1
         BR    XFBRANCH             EXIT ADDRESS ALREADY SET
ERROR1   EQU   *
         OI    DCMCMSG3,DCMCDLR5   INDICATE INVALID OPERAND
         B     ERRORALL            GO INDICATE WRITE DEL REQ INCONSIST
ERROR2   EQU   *
         OI    DCMCMSG3,DCMCDLR1   INDICATE NO DELETABLE MESSAGES
         B     ERRORALL            GO INDICATE WRITE DEL REQ INCONSIST
ERROR    EQU   *
         OI    DCMCMSG3,DCMCDLR2    INDICATE INVALID RANGE
ERRORALL EQU   *
         MVI   DCMRQDEL,BLANK      BLANK DCMRQDEL
         MVC   DCMRQDEL+N1(N7),DCMRQDEL
         OI    DCMCMSG2,DCMDELRI   INDICATE WRITE ERROR MSG
         OI    DCMIOCM1,DCMWRMSG+DCMWRINS+DCMINSC
         BAL   XE,CURSCOMP         GO COMPUTE CURSOR POSITION
ERRNUM   EQU   *
         SR    XNUM,XNUM           ZERO NUMBER CTR
         L     XWORK,DCMAMTAB      GET ADDR OF FIRST MSG IN SCT
         L     XCOMP,DCMADDRL      GET ADDR OF LAST NUM
         L     XMSG,DCMASCRN       GET ADDR OF FIRST MSG IN BUFFER
LOOP1    EQU   *
         TM    N0(XWORK),DCMMSGIN    Q. MSG IN LINE
         BNO   OUT                 EXIT
         BAL   XE,NUMBER           GO MOVE IN NUMBER
         AH    XMSG,DCMCORLN       INCRE TO NEXT MSG IN BUFFER   S21003
         BXLE  XWORK,XINC,LOOP1    LOOP UNTIL NO MORE MSGS
OUT      EQU   *
         STH   XNUM,DCMADNUM       STORE NUMBER
MSGEXIT  EQU   *
         L     XFBRANCH,DCMNMSG2    LOAD MESSAGE 2 ADDRESS
         B     XCTL                EXIT TO IEECVETE
***
***  NUMBER MESSAGE
***
NUMBER   EQU   *
         AH    XNUM,ONE            INCREMENT TO NEXT NUMBER
         CVD   XNUM,DCMPACK        CONVERT NUMB TO DECIMAL
         UNPK  DCMCVBIN+N2(N2),DCMCVBIN+N2(N2)    UNPACK NUMBER
         OI    DCMCVBIN+N3,CF0     FIX SIGN
         CLI   DCMCVBIN+N2,CF0     Q. A ONE-DIGIT NUMBER
         BNE   MOVENUM             NO, GO MOVE 2 DIGITS
         MVI   DCMCVBIN+N2,BLANK   YES, BLANK OUT FIRST DIGIT
MOVENUM  EQU   *
         MVC   N0(N2,XMSG),DCMCVBIN+N2  MOVE NUMBER INTO MESSAGE LINE
         BR    XE                  RETURN TO CALLER
DF       EQU   *
         L     XFBRANCH,DCMWTBUF   TOP DISPLAY LINE           MB YM4087
         S     XFBRANCH,DCMASCRN   DISPLACEMENT IN BYTES      MB YM4087
         SR    XE,XE               CLEAR EVEN REG             MB YM4087
         LH    XHCTR,DCMCORLN      BYTES PER LINE             MB YM4087
         DR    XE,XHCTR            TOP DISPLAY LINE NUMBER    MB YM4087
         LR    XHCTR,XFBRANCH      HIGH LINE FOR DELETES      MB YM4087
         IC    XMASK,MASKBIT      SET UP MASK                 MB YM
         B     INIT                GO SET DELETION REQ.BIT
***
***  THIS ROUTINE WILL CONVERT A NUMBER IN DCMCVBIN TO BINARY-
***  THE RESULT WILL BE RETURNED IN XWORK
***
BCONVERT    EQU   *
         SR    XWORK,XWORK
         ST    XWORK,DCMPACK   STORE ZERO IN DCMPACK
         NI    DCMCVBIN+N3,CCF     TAKE CARE OF SIGN BIT
         PACK  DCMCVBIN+N2(N2),DCMCVBIN+N2(N2) PACK NUMBER AND
         CVB   XWORK,DCMPACK               CONVERT TO BINARY
         BR    XMSG   RETURN TO CALLER
CURSCOMP EQU   *
         L     X1,DCMADOPN         GET ADDR OF OPERAND
         L     XWORK,DCMAENTR      GET ADDR OF ENTRY AREA
         SR    X1,XWORK            SUBTRACT TO GET POS OF OPERAND
         CH    X1,DCMCORLN         Q. IS OPERAND ON LINE 2       S21003
         BH    LINE2               YES
         MVI   DCMCULNO,N1         SET LINE NUMBER(IN ENTRY AREA) TO 1
STCURS   EQU   *
         LA    X1,N3(X1)           INCREMENT POSITION BY THREE   S21002
         STC   X1,DCMPOSCU         SET CURSOR POSITION
         BR    XE                  RETURN TO CALLER
LINE2    EQU   *
         MVI   DCMCULNO,N2         SET LINE NUMBER TO 2
         SH    X1,DCMCORLN         THEN GO TO                    S21003
         B     STCURS              STORE CURSOR POSITION
TUMASK   TM    N0(XWORK),ZERO
         EJECT
***
***      EQUATES
***
ID1      EQU   C'E'                 1ST CHARACTER OF CSECT ID
ID2      EQU   C'6'                 2ND CHARACTER OF CSECT ID
ZERO     EQU   0                   NUMBER 0
CF0      EQU   C'0'          CHARACTER OF ZERO
VERTBAR  EQU   C'×'                VERTICAL BAR
COMMA    EQU   C','                COMMA
BLANK    EQU   C' '                BLANK
FZERO    EQU   C'0'                CHARACTER 0
HORBAR   EQU   C'-'                HORIZONTAL BAR
ASTERISK EQU   C'*'          ASTERISK
N0       EQU   0              NUMBER 0
N1       EQU   1                     1
N2       EQU   2                     2
N3       EQU   3                     3
N4       EQU   4                     4
N5       EQU   5                     5
N6       EQU   6                     6
N7       EQU   7                     7
N8       EQU   8                     8
C20      EQU   X'20'          CHAR 20
CCF      EQU   X'CF'          FIX FOR SIGN BIT
N255     EQU   255     FIELD INCREMENT
PPACT    EQU   C'@'             PROBLEM PROGRAM ACTION MARK   MD Y01958
R        EQU   C'R'             DEL=R OR RD                   @ZA14031
         EJECT
***
***  CONSTANTS
***
FDEL     DC    C'F '     CHARACTER F
ONE      DC    H'1'                NUMBER 1
MASKBIT  DC    X'0A40'    FLAGGED MSGS  -- K E,SEG
         EJECT
         IEERDCM
         EJECT
         IEETDCM
         EJECT
         IEECUCM  FORMAT=NEW
         EJECT
         IHACTM  CXSA
         END
