* /* START OF SPECIFICATIONS ******************************************
*
*01*  MODULE-NAME = IEECVET4
*
*01*  DESCRIPTIVE-NAME = COMMAND ROUTINE
*
*01*  COPYRIGHT = NONE
*
*01*  STATUS = CHANGE LEVEL 1 FOR VS2 RELEASE 2
*
*     APARS FIXED --- ZA11437,ZA18576
*
*01*  FUNCTION = TO CANCEL THE COMMAND IN THE ENTRY AREA IN RESPONSE TO
*     A CANCEL ATTENTION,  OR ANALYZE THE CONTENTS OF THE ENTRY AREA IN
*     THE DCM WHEN A COMMAND IS ENTERED VIA THE KEYBOARD OR A PFK.
*     COMMANDS ARE CHECKED  FOR LENGTH, AND REWRITTEN TO THE ENTRY AREA
*     WITH A MESSAGE IN THE INSTRUCTION LINE IF TOO LONG.
*     IF  THE  COMMAND  IS  'K'  WITH NO OPERAND,  OR  A DELETE REQUEST
*     VERIFICATION, CONTROL IS GIVEN IMMEDIATELY  TO  THE PROPER DELETE
*     ROUTINE.  FOR REPLY COMMANDS,  THE REPLY ID IS CHECKED FOR ON THE
*     QUEUE OF  OUTSTANDING  OPERATOR  REQUESTS TO SEE IF IT WAS ISSUED
*     WITH A  ROUTING  CODE  OF 9.  IF SO,  THE TEXT OF THE  OPERATOR'S
*     REPLY IS SUPPRESSED.   A WTO IS ISSUED INDICATING NO HARD-COPY TO
*     COPY ALL COMMANDS  EXCEPT 'K' COMMANDS TO THE MESSAGE AREA OF THE
*     SCREEN.  SVC 34  IS ISSUED TO ENTER COMMANDS  INTO THE SYSTEM VIA
*     SCHEDULER PROCESSING.  ON RETURN FROM SVC 34 'K E,N' COMMANDS ARE
*     PROCESSED BY THIS ROUTINE.
*
*01*  NOTES = LOCAL AND CROSS-MEMORY LOCKS ARE RELEASED ON ENTRY VIA
*     THE FREELOCK SUBROUTINE ADDRESSED IN THE CXSA (CSAXC).
*
*02*    CHARACTER-CODE-DEPENDENCIES = EBCDIC CHARACTER REPRESENTATION
*       IS ASSUMED FOR ANALYZING THE CONTENTS OF THE ENTRY AREA.
*
*02*    DEPENDENCIES = THE SHORT FORM OF THE REPLY COMMAND SUPPORTED BY
*       JES2 IS RECOGNIZED BY THIS ROUTINE FOR TEXT SUPPRESSION  IF THE
*       FIRST CHARACTER OF A COMMAND IS NUMERIC.
*
*02*    RESTRICTIONS = NONE
*
*02*    REGISTER-CONVENTIONS = SEE REGISTER EQUATES
*
*02*    PATCH-LABEL = ICATCH A DC STATEMENT
*
*01*  MODULE-TYPE = MODULE
*
*02*    PROCESSOR = ASSEMF-370R
*
*02*    MODULE-SIZE = X'41B' BYTES
*
*02*    ATTRIBUTES = REENTERABLE
*
*01*  ENTRY-POINT = IEECVET4
*
*02*    PURPOSE = THIS IS THE ONLY ENTRY POINT TO PERFORM ALL FUNCTIONS
*
*02*    LINKAGE = BALR
*
*02*    INPUT = REGISTER 1 ADDRESSES THE CXSA
*       SEE ALSO CONTROL BLOCKS USAGE
*
*02*    OUTPUT = REGISTER 1 ADDRESSES THE CXSA
*       SEE ALSO CONTROL BLOCKS USAGE AND EXITS
*
*02*    EXIT-NORMAL = IEECVETC - ASYNCHRONOUS ERROR ROUTINE
*       AFTER HANDLING A CANCEL DURING ASYNCHRONOUS ERROR PROCESSING.
*
*02*    EXIT-NORMAL = IEECVET8 - DELETE ROUTINE 3
*
*02*    EXIT-NORMAL = IEECVET9 - DELETE ROUTINE 4
*
*02*    EXIT-NORMAL = IEECVET1 - PROCESSOR ROUTINE
*
*02*    EXIT-ERROR = IEECVETE - MESSAGE ROUTINE 2
*
*01*  EXTERNAL-REFERENCES = NONE
*
*02*    ROUTINES = FREELOCK SUBROUTINE - DEFINED IN IEECVET1, AND
*       ADDRESSED BY WORD CSAXC OF THE CXSA ON ENTRY.
*
*02*    DATA-SETS = NONE
*
*02*    DATA-AREA = NONE
*
*02*    CONTROL-BLOCKS = CXSA,UCM,WQE,CQE,RDCM,TDCM
*
*01*  TABLES = NONE
*
*01*  MACROS = EXECUTABLE - WTO, E-FORM
*              MAPPING - IHACTM  CXSA DSECT
*                        IEECUCM UCM DSECT
*                        IEERDCM RESIDENT DCM DSECT
*                        IEETDCM PAGEABLE DCM DSECT
*                        IEFUCBOB UCB DSECT
*                        IHAORE OPERATOR REQUEST ELEMENT DSECT
*                        IHAWQE WQE DSECT
*
*02*    ROUTINES = FREELOCK SUBROUTINE - DEFINED IN IEECVET1, AND
*       ADDRESSED BY WORD CSAXC OF THE CXSA ON ENTRY.
*
**** END OF SPECIFICATIONS *******************************************/
         TITLE 'IEECVET4 COMMAND   '
IEECVET4 CSECT
*A136100,136200,136300                                        MH Y02446
*C136000,136500                                               MH Y02446
***********************************************************************
*        REGISTER EQUATES
***********************************************************************
*
XFBRANCH EQU   15        BRANCH EXIT TO OTHER ROUTINES
XFQUOTEN EQU   15        ODD REG FOR DIVISION
XELEN    EQU   14
XERTRN   EQU   14        RETURN BRANCH REG
XEREMAIN EQU   14        EVEN REG FOR DIVISION
XDWORK   EQU   13
XCPBASE  EQU   12        PROGRAM BASE REG
XBTDCM   EQU   11        PAGEABLE DCM BASE REG
XARDCM   EQU   10        RESIDENT DCM BASE REG
X9SCNSRT EQU   9
X9UCM    EQU   9         UCM BASE REG
X8UCME   EQU   8         UCM ENTRY BASE REG
X7WQE    EQU   7         WQE BASE
X6ORE    EQU   6         ORE BASE
X5SCT    EQU   5         SCT ENTRY POINTER
X4MSG    EQU   4         MESSAGE AREA TEXT POINTER
X3END    EQU   3         END OF COMMAND
X2START  EQU   2         START OF COMMAND
X2WORK   EQU   2         WORK REG
X1PARM   EQU   1         CXSA ADDRESS ON ENTRY AND EXIT
X0       EQU   0
X0WORK   EQU   0
***********************************************************************
*    INITIALIZE PROGRAM
***********************************************************************
         BALR  XCPBASE,N0          LOAD PROGRAM BASE REGISTER
         USING *,XCPBASE           ESTABLISH PROG ADDRESSABILITY
         B     BEGIN               BRANCH AROUND PATCH AREA
ICATCH   DC    CL72'IEECVET4'      EYECATCHER AND FIELD MAINTENANCE
BEGIN    EQU   *                   END OF PATCH AREA
         USING CXSA,X1PARM         ESTABLISH CXSA ADDRESSABILITY
         USING UCMLIST,X8UCME      ESTABLISH UCMENTRY ADDRESSABILITY
         USING UCM,X9UCM           ESTABLISH UCM ADDRESSABILITY
         USING DCMTSRT,XARDCM      ESTABLISH R DCM ADDRESSABILITY
         USING DCMSTRT,XBTDCM      ESTABLISH DCM ADDRESS
         L     X8UCME,CSAUCM       UCM ENTRY BASE
         L     X9UCM,CSACTLM       UCM BASE
         L     XARDCM,UCMXB        RESIDENT DCM BASE
         L     XBTDCM,DCMADTRN     PAGEABLE DCM BASE
         MVC   DCMTRACE(DCMTRLEN),DCMTRAC2 SHIFT OLD TRACE ENTRIES
         MVI   DCMTREN1,ID1        PUT CSECT ID INTO
         MVI   DCMTREN2,ID2        NEW TRACE ENTRY
         L     XFBRANCH,CSAXC      ADDRESS FREELOCK SUBROUTINE
         BALR  XERTRN,XFBRANCH     RELEASE LOCKS
         NI    DCMCOM3,N255-DCMVLPFK   TURN OFF PFK VERIFY BIT   S21003
         TM    DCMCOM1,DCMCANCL    Q. PERFORM A CANCEL
         BNO   ENTRY               NO, ASSUME COMMAND ENTRY
         NI    DCMCOM1,N255-DCMCANCL   TURN OFF CANCEL FLAG
         TM    DCMR3FLG,DCMRXSCN   IS THIS CANCEL AFTER ASYN ERR
         BNO   NOTASYNC            NO
         NI    DCMR2FLG,N255-DCMRXSFL   NO SCREEN FULL IN R-DCM  S21003
NOTASYNC EQU   *
         L     X4MSG,DCMASCRN      FIRST MESSAGE ADDRESS
         L     X5SCT,DCMAMTAB      SCT ADDRESS
         TM    DCMDSTAT,DCMDSTNH   MSG NUMBERS HELD           MG  M2097
         BO    CANLOOP             YES, LEAVE NUMBERS         MG  M2097
         NI    DCMDSTAT,N255-DCMDSTNM   SET OFF NMBERING FLAG MG  M2097
CANLOOP  EQU   *
         TM    N0(X5SCT),DCMMSGIN     MESSAGE IN THIS LINE
         BNO   CANSUBR             NO, LEAVE LOOP
         NI    N0(X5SCT),N255-DCMMSGRD   TURN OF REMOVE MSG FLAG
         TM    DCMDSTAT,DCMDSTNH   NUMBERS HELD
         BO    SKIPNUMR            YES, DON'T BLANK NUMBER
         MVC   N0(N2,X4MSG),CBLANK    NO, BLANK NUMBER
         NI    DCMDSTAT,N255-DCMDSTNM   TURN OFF NUMBERING FLAG
SKIPNUMR EQU   *
         MVI   N2(X4MSG),BLANK        BLANK POSITION 3
         TM    N1(X5SCT),DCMMSGAC     ACTION MESSAGE
         BNO   CANIF               NO
         SPACE 5
***********************************************************************
*                         OS/370R OPTION 1 AND 2 SECURITY CODE
***********************************************************************
         TM    N1(X5SCT),DCMMSGPP     PROP. PROG. ACTION MSG
         BNO   SYSAST              NO PUT ASTERISK
         MVI   N3(X4MSG),PPACT     SET AT SIGN FOR PP ACTION
         B     CANIF               NEXT CHECK
SYSAST   EQU   *
***********************************************************************
         SPACE 5
         MVI   N3(X4MSG),ASTER        YES, MARK WITH ASTERISK
CANIF    EQU   *
         TM    N0(X5SCT),DCMMSGIF     INFORMATIONAL MESSAGE
         BNO   CANAUTO             NO
         MVI   N2(X4MSG),ISDASH       YES, MARK WITH DASH
CANAUTO  EQU   *
         TM    N0(X5SCT),DCMMSGAD     MESSAGE AUTO DELETABLE
         BNO   CANLOOP2            NO
         MVI   N2(X4MSG),SLASH        YES, MARK WITH VERTICAL BAR
CANLOOP2 EQU   *
         AH    X4MSG,DCMCORLN      BUMP TO NEXT LINE          MB YM4360
         AH    X5SCT,DCMSCTCN      AND NEXT SCT ENTRY         MB YM4360
         C     X5SCT,DCMADDRL      CHECK FOR END              MB YM4360
         BNH   CANLOOP             FLAG UNTIL END             MB YM4360
CANSUBR  EQU   *
         OI    DCMIOCM1,DCMWRMSG+DCMWRINS+DCMWRWRN SET WRITE  MB YM4360
         MVI   DCMLINEN,N1         START WRITE ON LINE ONE
         MVI   DCMRQDEL,BLANK      BLANK THE                  MB YM4360
         MVC   DCMRQDEL+N1(N7),DCMRQDEL DELETE REQUEST BUFFER MB YM4360
         TM    DCMCOM1,DCMCANCL    INTERNAL REQUEST           MB YM4360
         BNO   CANLP               NO, CHECK LP DELETE        MB YM4360
         NI    DCMCOM1,N255-DCMCANCL TURN OFF CANCEL REQUEST  MB YM4360
         B     SVC34               CONTINUE CMD PROCESSING    MB YM4360
CANLP    EQU   *                                              MB YM4360
         TM    DCMCOM1,DCMLPENT    WAS ERASE ENTERED WITH LP
         BNO   CANCURS             NO
         NI    DCMCOM1,N255-DCMLPENT YES, DON'T WRITE ENTRY AREA
         B     INSRT               EXIT
CANCURS  EQU   *
*                                  WRITE MESSAGE AREA TO OUT-OF-
*                                  LINE MESSAGE,INSERT CURSOR,
*                                  REINITIALIZE + SHIFT INSTRUCT
*                                  LINE, BLANK ENTRY AREA
         OI    DCMIOCM1,DCMWRMSG+DCMWRINS+DCMWRENT+DCMINSC+DCMWRWRN
INSRT    EQU   *
         OI    DCMIOCM2,DCMBLENT                              MH Y02446
         TM    DCMIOCM3,DCMRTPFK   IS PFK PROCESSING          MH Y02446
         BO    SKIPINLN            YES, SKIP INST. LINE       MH Y02446
         OI    DCMIOCM2,DCMINSSH   INIT. INST. LINE           MH Y02446
SKIPINLN MVI   DCMCULNO,N1         CURSOR POSITION            MH Y02446
         MVI   DCMPOSCU,N1         POSITION 1 OF DCMENTRY
         TM    DCMTIMES,DCMTASYN   ASYNCHRONOUS ERROR MSG UP
         BNO   IOEXIT              NO, WRITE SCREEN
         L     XFBRANCH,DCMNERRO   LOAD ASYNCH ERR ADDRESS
         L     X1PARM,DCMCXSVE     RESTORE CXSA ADDRESS
         BR    XFBRANCH            EXIT TO IEECVETC
         EJECT
ENTRY    EQU   *
         L     X2START,DCMAENTR    GET PTR TO ENTRY AREA
         CLI   DCMIONDX,NDX2260    INPUT DEVICE A 2260
         BNE   NORMTRAN            NO, JUST TRANSLATE FIRST            *
               QUADRANT CHARACTERS TO BLANKS
         TR    N0(N127,X2START),TAB2260   TRANSLATE OUT ANY 2260       *
               SPECIAL CHARACTERS AS WELL
         B     TSTLNG              CHECK COMMAND LENGTH
NORMTRAN TR    N0(N127,X2START),TRTAB   TRANSLATE
TSTLNG   CLI   N126(X2START),BLANK   CHAR 127 A BLANK
         BE    LNGTHOK             YES, INPUT LENGTH OK
         OI    DCMCMSG1,DCMELONG   INDICATE LENGTH TOO LONG
         L     XFBRANCH,DCMNMSG2   LOAD MESSAGE 2 ADDRESS
         L     X1PARM,DCMCXSVE     RESTORE CXSA ADDRESS
         BR    XFBRANCH            EXIT TO IEECVETE
LNGTHOK  EQU   *
         L     X2START,DCMAENTR    LOCATE START OF AREA
         LA    X3END,N125(X2START) SET END POINTER
FIND1    CLI   N0(X2START),BLANK   IS THERE A LEADING BLANK
         BNE   FINDEND             NO, START OF COMMAND IS FOUND
         LA    X2START,N1(X2START) POINT TO NEXT CHARACTER
         B     FIND1               CONTINUE SCAN FOR START
FINDEND  EQU   *
         CLI   N0(X3END),BLANK     IS THERE A TRAILING BLANK
         BNE   COMPLENG            NO, END OF COMMAND IS FOUND
         BCT   X3END,FINDEND       BACK UP ONE, AND CONTINUE
COMPLENG EQU   *
         LR    XELEN,X3END         LAST BYTE OF COMMAND
         SR    XELEN,X2START       ZERO ORIGIN LENGTH OF COMMAND
         BM    BLENT               LENGTH ZERO, DONE.         MB YM4360
         MVI   DCMINPUT,BLANK      INITIALIZE INPUT AREA
         MVC   DCMINPUT+N1(N125),DCMINPUT WITH BLANKS
         EX    XELEN,CMDMOVE       MOVE COMMAND TO INPUT AREA
         LA    X3END,DCMINPUT(XELEN) POINT TO LAST CHARACTER
         LA    XDWORK,N5(XELEN)    LENGTH FOR SVC'S
         ST    XDWORK,DCMDSAV      SAVE IT
         STH   XDWORK,DCMINLGN     PLACE IT IN PARM LIST
         LA    XDWORK,DCMINPUT     START OPERAND SCAN
ENDVERB  LA    XDWORK,N1(XDWORK)   LOCATE NEXT CHARACTER
         CR    XDWORK,X3END        PAST THE END OF THE COMMAND
         BL    NOTEND              NO, CHECK FOR END OF VERB
         LA    XDWORK,N1(XDWORK)   NO OPERAND TO BE FOUND
         B     CMDCHECK            CHECK FOR K OR R
NOTEND   CLI   N0(XDWORK),BLANK    END OF COMMAND VERB
         BNE   ENDVERB             NO KEEP LOOKING FOR END
STARTOP  LA    XDWORK,N1(XDWORK)   TRY NEXT CHARACTER
         CLI   N0(XDWORK),BLANK    START OF OPERAND
         BE    STARTOP             NO,SCAN OVER ANOTHER BLANK
         ST    XDWORK,DCMADOPN     SAVE OPERAND ADDRESS
CMDCHECK EQU   *
         MVI   DCMPACK,BLANK       BLANK AN EIGHT BYTE AREA
         MVC   DCMPACK+N1(N7),DCMPACK FOR INSPECTING THE COMMAND
         OC    DCMPACK(N8),DCMINPUT VERB IN UPPER CASE
         CLC   DCMPACK(N2),K       IS VERB K
         BE    PROC1               YES, CHECK FOR DELETE VERIFICATION
         CLC   DCMPACK(N8),CONTROL IS VERB CONTROL
         BE    PROC1               YES, CHECK FOR DELETE VERIFICATION
         CLC   DCMPACK(N3),MR      IS VERB MR                 MB YM4360
         BE    SVC34               YES, SKIP WTO FOR COMMAND  MB YM4360
         CLC   DCMPACK(N6),MSGRT   IS VERB MSGRT              MB YM4360
         BE    SVC34               YES, SKIP WTO FOR COMMAND  MB YM4360
         CLC   DCMPACK(N2),R       IS VERB R
         BE    RPYNUM              YES, CHECK REPLY ID
         CLC   DCMPACK(N6),REPLY   IS VERB REPLY
         BE    RPYNUM              YES, CHECK REPLY ID
         LA    XDWORK,DCMINPUT     RESET OPERAND PTR TO START OF VERB
         B     NUMSTART            SEE IF COMMAND STARTS WITH REPLY ID
*                                  THE FOLLOWING CODE CATCHES K
*                                  BY ITSELF OR K FOR VERIFICATION OF
*                                  A DELETE REQUEST. IF FOUND,
*                                  SVC 34 IS NOT ISSUED. INSTEAD
*                                  THE APPROPRIATE DIDOCS MODULE
*                                  IS GIVEN CONTROL.
*                            NOTE: THIS CODE WILL HAVE TO BE
*                                  REMOVED IF IT BECOMES POSSIBLE
*                                  TO ROUTE THESE COMMANDS.
*                                  XDWORK HAS ADDR OF FIRST CHARACTER
*                                  OF THE K COMMAND OPERAND
PROC1    EQU   *
         L     X2START,DCMAENTR    ADDRESS START OF ENTRY AREA
         MVI   N0(X2START),BLANK   MOVE BLANKS INTO ENTRY AREA
         MVC   N1(N126,X2START),N0(X2START) FOR 127 BYTES
         OC    N0(N126,X2START),DCMINPUT TRANSLATE COMMAND THERE
         CLI   N0(XDWORK),BLANK   BLANK OPERAND
         BNZ   VERIFY              NO, CHECK FOR DELETE VERIFICATION
         LA    X2START,DCMINPUT    GET INPUT AREA PTR          @ZA11437
         SR    XDWORK,X2START      COMPUTE OPERAND DISP        @ZA11437
         A     XDWORK,DCMAENTR     INDEX INTO ENTRY AREA       @ZA11437
         ST    XDWORK,DCMADOPN     SAVE PTR TO OP IN ENTRY AREA@ZA11437
         L     XFBRANCH,DCMNDEL3   LOAD DELETE 3 ADDRESS
         L     X1PARM,DCMCXSVE     RESTORE CXSA ADDRESS
         BR    XFBRANCH            EXIT TO IEECVET8
VERIFY   EQU   *         CHECK DELETE REQUEST VERIFICATION    MB YM4360
         CLI   DCMCON,Y            CONVERSATIONAL DELETE MODE MB YM4360
         BNE   SVC34               NO, ISSUE THE COMMAND      MB YM4360
         CLI   DCMRQDEL,BLANK      BLANK BUFFER               MB YM4360
         BE    SVC34               YES, NO NEED TO CHECK      MB YM4360
         SR    X3END,XDWORK        GET OPERAND LENGTH MINUS 1 MB YM4360
         LA    X3END,N1(X3END)     COMPARE FOR 1 MORE
         EX    X3END,VERCOMP       COMPARE OPERAND WITH DELETE REQUEST *
               BUFFER IN THE RESIDENT DCM
         BNE   NOVERIFY            NOT SUCCESSFUL VERIFIY     MB YM4360
         L     XFBRANCH,DCMNDEL4   LOAD DELETE 4 ADDRESS
         L     X1PARM,DCMCXSVE     RESTORE CXSA ADDRESS
         BR    XFBRANCH            EXIT TO IEECVET9
NOVERIFY EQU   *         A 'K' COMMAND HAS BEEN RECEIVED THAT FAILS TO
*              VERIFY A DELETE REQUEST.  PERFORM A CANCEL FUNCTION
*              IMMEDIATELY  THEN COMPLETE COMMAND PROCESSING
*              AT LABEL 'SVC34'.                              MB YM4360
         OI    DCMCOM1,DCMCANCL    INDICATE INTERNAL ET4 CALL MB YM4360
         B     NOTASYNC            PERFORM CANCEL LOGIC       MB YM4360
RPYNUM   CLI   N0(XDWORK),BLANK    REPLY WITH NO OPERANDS
         BZ    SVC35               NO OPERANDS, NO WTOR REPLY
NUMSTART EQU   *
         TM    N0(XDWORK),F0       FIRST CHARACTER NUMERIC
         BNO   SVC35               NO, CANT CHECK REPLY ID
         MVI   DCMPACK,F0         INITIALIZE TENS DIGIT TO 0
         TM    N1(XDWORK),F0       IS THERE A SECOND NUMBER
         BNO   ONENUM              NO, JUST ONE
         MVC   DCMPACK(N1),N0(XDWORK) MOVE FIRST IN AS TENS DIGIT
         LA    XDWORK,N1(XDWORK)   POINT TO UNITS DIGIT
ONENUM   EQU   *
         MVC   DCMPACK+N1(N1),N0(XDWORK) MOVE UNITS DIGIT
         L     X6ORE,UCMRPYQ    BASE FIRST ORE
         USING OREF,X6ORE
LASTORE  LTR   X6ORE,X6ORE   ZERO ADDRESS
         BZ    SVC35               END OF REPLY Q, NOT FOUND
         TM    OREXC,OREBUFB       VALID ORE
         BNO   NEXTORE             NO, LOOK AT NEXT ONE
         CLC   DCMPACK(N2),OREID  REPLY ID'S MATCH
         BNE   NEXTORE             NO, LOOK AT NEXT ONE
         L     X7WQE,OREWQE     BASE WQE FOR THIS WTOR
         USING WQE,X7WQE        ESTABLISH ADDRESSABILITY
         TM    WQEROUT2,WQEROUTI   SYSTEM SECURITY ROUT CODE
         BNO   SVC35               NO, DONT SUPPRESS TEXT
         LA    X1PARM,DCMINLGN     START OF PARAMETER AREA
         MVC   N1(L'SUPPRESS,XDWORK),SUPPRESS SUPRESS TEXT OF REPLY
         SR    XDWORK,X1PARM       GET LENGTH TO END OF REPLY ID
         LA    XDWORK,L'SUPPRESS+N1(XDWORK)  CALCULATE NEW LENGTH
         STH   XDWORK,DCMINLGN     SET UP ADJUSTED COMMAND LENGTH
         B     SVC35               WRITE THE REPLY COMMAND
NEXTORE  L     X6ORE,ORELKP     BASE NEXT ORE
         B     LASTORE             CONTINUE SEARCH
SVC35    EQU   *
         MVI   DCMMCSFL,WQEMCSB+WQEMCSH   QUEUE BY REG 0
         MVI   DCMMCSFL+N1,WQEMCSN BYPASS HARDCOPY
         LA    X1PARM,DCMINLGN     ADDRESS PARAMETER LIST
         SR    X0,X0               CLEAR REG ZERO             MG  M2097
         IC    X0,UCMID            GET UCMID                  MG  M2097
         SVC   N35                 ISSUE SVC                  MG  M2097
SVC34    EQU   *
         L     X1PARM,DCMDSAV      GET THE LENGTH FOR SVC 34
         STH   X1PARM,DCMINLGN     STORE LENGTH
         EX    XELEN,CMDMOVE       MOVE COMMAND                        *
               TO BE ISSUED AS TYPED BY THE OPERATOR
         LA    X1PARM,DCMINLGN     POINT TO INPUT
         SR    X0,X0               CLEAR REG ZERO
         IC    X0,UCMID            GET UCMID
         MVI   DCMSVC34,N0         ZERO OUT COMM. FLAGS FOR USE
*                                  BY SVC 34 AND K COMMAND
         XC    DCMMCSFL(N2),DCMMCSFL   ZERO MCS FLAGS
         SVC   N34                 ISSUE SVC 34
         TM    DCMSVC34,DCMMYCMD   VALID K COMMAND TO THIS CONS.
         BO    FORCOM              YES, DOES THIS ROUTINE DO IT
         TM    DCMSVC34,DCMINVLD   INVALID K COMMAND
         BO    KCURSCMP            COMPUTE CURSOR POSITION
BLENT    EQU   *
         OI    DCMIOCM1,DCMWRENT+DCMWRINS+DCMINSC TURN ON WRITE BITS
         B     INSRT               COMPUTE CURS POS AND EXIT
FORCOM   EQU   *
         LA    X1PARM,DCMINLGN     GET POINTER TO PARAMETER LIST
         TM    DCMSVC34,DCMTYPE1   POINT ADOPN TO ENTRY AREA
         BO    FORCOM1             YES
         ST    X1PARM,DCMADKP      STORE PTR IN RESIDENT DCM
         B     BLENT               BLANK ENTRY AREA
FORCOM1  EQU   *
         USING KMOD,X1PARM         BASE KPARAM LIST
         L     XDWORK,DCMADOPN     GET OPERAND POINTER
         LA    X2START,DCMINPUT    GET INPUT AREA PTR
         SR    XDWORK,X2START      COMPUTE OPERAND DISPLACEMENT
         A     XDWORK,DCMAENTR     INDEX INTO ENTRY AREA
         ST    XDWORK,DCMADOPN     SAVE PTR TO OP IN ENTRY AREA
         CLC   KMOD(N4),DCMNCMD1   IS K COMMAND DONE HERE
         BE    CHECKOPR            YES, CHECK OPERAND
         L     XFBRANCH,KMOD       ADDRESS ROUTINE TO HANDLE COMMAND
         L     X1PARM,DCMCXSVE     RESTORE CXSA ADDRESS
         BR    XFBRANCH
         DROP  X1PARM
CHECKOPR EQU   *                   XDWORK POINTS TO FIRST OPERAND CHAR
         CLC   N0(N3,XDWORK),EN    K E,N
         BE    ERASNUM             YES, ERASE NUMBERS
         L     XFBRANCH,DCMNPROC   LOAD PROCESSOR ROUTINE ADDRESS
         L     X1PARM,DCMCXSVE     RESTORE CXSA ADDRESS
         BR    XFBRANCH            EXIT TO IEECVET1
CURSCOMP EQU   *
         L     XDWORK,DCMAENTR     START OF COMMAND
         L     XELEN,DCMADOPN      GET OPERAND DISPLACEMENT
         SR    XELEN,XDWORK
CURSCOM1 EQU   *
*                                  INSERT CURSOR, WRITE ENTRY
*                                  AREA, WRITE INSTRUCTION LINE,
*                                  WRITE WARNING LINE.
         OI    DCMIOCM1,DCMINSC+DCMWRINS+DCMWRENT+DCMWRWRN
         CH    XELEN,DCMLGNTH      OPERAND IN SECOND LINE
         BNL   LINE2               YES DISPLACEMENT OF OPERAND @ZA18576
         MVI   DCMCULNO,N1         SET LINE NUMBER (IN ENTRY AREA) TO 1
STCURS   EQU   *
         LA    XELEN,N1(XELEN)     ADD ONE TO LENGTH
         STC   XELEN,DCMPOSCU      STORE CURSOR POSITION
         L     X1PARM,DCMCXSVE     RESTORE CXSA ADDRESS
         BR    XFBRANCH            EXIT ADDRESS ALREADY SET
LINE2    EQU   *
         MVI   DCMCULNO,N2         SET LINE NUMBER (IN ENTRY AREA) TO 2
         SH    XELEN,DCMLGNTH      GET CURSOR POSITION ON LINE 2 OF
         B     STCURS                ENTRY AREA AND STORE IT
KCURSCMP EQU   *
         LH    XELEN,DCMINLGN      POINT TO BAD OPERAND IN ENTRY
*                                  AREA BASED ON DISPLACEMENT IN
*                                  K LIST FROM SVC 34
         NI    DCMR3FLG,N255-DCMRXSCN   TURN OFF ASYNC ERROR FLG
         L     XFBRANCH,DCMIORTN   LOAD IO ROUTINE ADDRESS
*                                  FOR EXIT TO IEECVET(H,P,R, OR U)
         B     CURSCOM1            STORE CURSOR POSITION
ERASNUM  EQU   *
         TM    DCMDSTAT,DCMDSTNM   Q. ARE MESSAGES NUMBERED
         BNO   ERRORNDL            NO, ERROR- NO DELET MSGS
*                                  YES, ERASE NUMBERS
NUMERAS  EQU   *
         NI    DCMDSTAT,N255-DCMDSTNM-DCMDSTNH   TURN OFF NUMBER FLAGS
         L     X2START,DCMASCRN    GET ADDR OF FIRST MESSAGE LINE
         LH    XELEN,DCMMSGAL      GET NUMBER OR LINES IN MESSAGE AREA
REMVNUM  EQU   *
         MVC   N0(N2,X2START),CBLANK BLANK NUMBERS
         AH    X2START,DCMCORLN    INCRE TO NEXT LINE             M1517
         BCT   XELEN,REMVNUM       LOOP UNTIL ALL NUMBERS ARE BLANKED
         B     CANSUBR             SET UP FOR WRITE
ERRORNDL EQU   *
         OI    DCMCMSG2,DCMDELRI   REQUEST INCONSISTENT MESSAGE
         L     XFBRANCH,DCMNMSG2   LOAD MESSAGE 2 ADDRESS
*                                  FOR EXIT TO IEECVETE
         B     CURSCOMP            POSITION CURSOR
IOEXIT   EQU   *
         NI    DCMR3FLG,N255-DCMRXSCN    TURN OFF ASYNC ERROR INDICATOR
         L     XFBRANCH,DCMIORTN   LOAD IO ROUTINE ADDRESS
         L     X1PARM,DCMCXSVE     RESTORE CXSA ADDRESS
         BR    XFBRANCH            EXIT TO IEECVET(H,P,R, OR U)
VERCOMP  CLC   DCMRQDEL(N0),N0(XDWORK)
CMDMOVE  MVC   DCMINPUT(N0),N0(X2START)
         EJECT
***********************************************************************
*
*        CONSTANTS
*
***********************************************************************
TAB2260  EQU   *
         DC    64C' '
         DC    X'40414243444546474849404B4C4D4E4F' BLANK X'4A'
         DC    X'505152535455565758595A5B5C5D5E5F'
         DC    X'60616263646566676869406B6C6D6E6F' BLANK X'6A'
         DC    X'707172737475767778797A7B7C7D7E7F'
         DC    X'808182838485868788898A8B8C8D8E8F'
         DC    X'909192939495969798999A9B9C9D9E9F'
         DC    X'A0A1A2A3A4A5A6A7A8A9AAABACADAEAF'
         DC    X'B0B1B2B3B4B5B6B7B8B9BABBBCBDBEBF'
         DC    X'C0C1C2C3C4C5C6C7C8C9CACBCCCDCECF'
         DC    X'D0D1D2D3D4D5D6D7D8D9DADBDCDDDEDF'
         DC    X'E0E1E2E3E4E5E6E7E8E9EAEBECEDEEEF'
         DC    X'F0F1F2F3F4F5F6F7F8F9FAFBFCFDFEFF'
TRTAB    EQU   *
         DC    64C' '
         DC    X'404142434445464748494A4B4C4D4E4F'
         DC    X'505152535455565758595A5B5C5D5E5F'
         DC    X'606162636465666768696A6B6C6D6E6F'
         DC    X'707172737475767778797A7B7C7D7E7F'
         DC    X'808182838485868788898A8B8C8D8E8F'
         DC    X'909192939495969798999A9B9C9D9E9F'
         DC    X'A0A1A2A3A4A5A6A7A8A9AAABACADAEAF'
         DC    X'B0B1B2B3B4B5B6B7B8B9BABBBCBDBEBF'
         DC    X'C0C1C2C3C4C5C6C7C8C9CACBCCCDCECF'
         DC    X'D0D1D2D3D4D5D6D7D8D9DADBDCDDDEDF'
         DC    X'E0E1E2E3E4E5E6E7E8E9EAEBECEDEEEF'
         DC    X'F0F1F2F3F4F5F6F7F8F9FAFBFCFDFEFF'
H4       DC    H'4'                CONSTANT
EN       DC    C'E,N'              COMPARAND FOR K E,N
CONTROL  DC    C'CONTROL '         COMPARAND FOR CONTROL VERB
K        DC    C'K '               COMPARAND FOR K VERB
R        DC    C'R '               COMPARAND FOR R VERB
REPLY    DC    C'REPLY '           COMPARAND FOR REPLY VERB
MR       DC    C'MR '              COMPARAND FOR MR VERB
MSGRT    DC    C'MSGRT'            COMPARAND FOR MSGRT VERB
SUPPRESS DC    C' SUPPRESSED'
CBLANK   DC    C'  '               BLANK CHARACTERS
***********************************************************************
*
*        EQUATES
*
***********************************************************************
ID1      EQU   C'E'                1ST CHARACTER OF CSECT ID
ID2      EQU   C'4'                2ND CHARACTER OF CSECT ID
N0       EQU   0                   FIELD INCREMENT
N1       EQU   1                   FIELD INCREMENT
N2       EQU   2                   FIELD INCREMENT
N3       EQU   3                   FIELD INCREMENT
N4       EQU   4                   FIELD INCREMENT
N5       EQU   5                   FIELD INCREMENT
N6       EQU   6                   FIELD INCREMENT
N7       EQU   7                   FIELD INCREMENT
N8       EQU   8                   FIELD INCREMENT
N34      EQU   34                  FIELD INCREMENT
N35      EQU   35                  FIELD INCREMENT
N56      EQU   56                  FIELD INCREMENT
N57      EQU   57                  FIELD INCREMENT
N125     EQU   125                 FIELD INCREMENT
N126     EQU   126                 FIELD INCREMENT
N127     EQU   127                 FIELD INCREMENT
N255     EQU   255                 FIELD INCREMENT
ISDASH   EQU   C'-'                DASH
SLASH    EQU   C'×'                SLASH
ASTER    EQU   C'*'                ASTERISK
BLANK    EQU   C' '                BLANK
NDX2260  EQU   12                  DCMIONDX VALUE FOR 2260
PPACT    EQU   C'@'                SIGN FOR PP ACTION
F0       EQU   C'0'
Y        EQU   C'Y'                CON=Y OPTION COMPARE       MB YM4360
         EJECT
***********************************************************************
*
*        DSECTS
*
***********************************************************************
         SPACE
         IHAORE
         EJECT
UCBBLK   DSECT
         IEFUCBOB
         EJECT
         IEERDCM
         EJECT
         IEETDCM
         EJECT
         IHAWQE  DSECT=YES
         EJECT
         IHACTM  CXSA
         EJECT
         IEECVMAP KPARAM
         EJECT
         IEECUCM  FORMAT=NEW
    END
