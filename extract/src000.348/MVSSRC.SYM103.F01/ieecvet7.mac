         TITLE 'IEECVET7  DELETE 2'
IEECVET7 CSECT
         SPACE 1
* D 076000-115100,166500-167000                               JE YM6828
*A149600-149800,155100,165600,165700,085600,085700            MH Y02131
*C 86500,104000                                              MC SA52275
*C 150000                                                     MB YM1956
*D 150500,151000,152000,161500-162500,172500,173000           MB YM1956
*A 140100,140200,152100,152200                                MB YM0664
*A610500-610700,C615000,C619000,A619500,C630000,A657300       MG  M1543
*A657720-657740                                               MG  M1543
*A267500-269500                                               MF A45435
*A459500-460000,A468500,A501500,C516500,A546020,C594000       MG  M1427
*C605000,A610500,A616500-616600,C629000,D659200,A729500       MG  M1427
*A609100-669200                                                  S21002
*A047300-047600,A168500,A178000,A186000,A191000-192000           S21003
*A307000-309000,A438500,A444000-447000,A462500,A516500,A546500   S21003
*A576000-580500,C585000-633000,A646000-647000,A657400-657600     S21003
*A659000-659200,A761100-761400                                   S21003
**********************************************************************
*
*
*
* STATUS --
*
* PTMS FIXED YM6718,YM6828,YM8648
* APARS FIXED ZA00512
*
* FUNCTION     1 CHECK FOR INTERVENTION REQUIRED MESSAGES. IF THE
*                DEVICE NAMED IN THE MESSAGE IS READY, THE MESSAGE IS
*                MARKED AUTOMATICALLY DELETABLE
*              2 DOM INDICATED MESSAGES EITHER BY PROTECT KEY OR ID.
* ENTRY POINT  IEECVET7
*
* INPUT        INDICATION THAT DOMMING IS TO BE PERFORMED
*              OTHERWISE, IT IS ASSUMED THAT INTERVENTION REQ'D MESSAGE
*              TO BE INTERROGATED
* OUTPUT       ALTERATION OF DISPLAY OR INDICATION THAT AUTO DELETION
*              TO BE TRIED BY DELETE RTN 4
* EXTERNAL REFERENCES NONE
*
* EXITS - NORMAL  IEECVETH OR P OR R  DEVICE-DEPENDENT IO
*                 IEECVET9 DELETE 4
*                 IEECVET1 PROCESSOR
*       - ERROR   NONE
*
* TABLES/WORK AREAS DISPLAY CONTROL MODULE (CONTAINED IN DSECT)
*                   RESIDENT DISPLAY CONTROL MODULE (CONTAINED   S21003
*                   IN DSECT)                                    S21003
*
* ATTRIBUTES   REFRESHABLE, PRIVILEGED, TYPE 4 SVC
*
* NOTES        THE OPERATION OF THIS MODULE DOES NOT DEPEND UPON A
*              PARTICULAR INTERNAL REPRESENTATION OF THE EXTERNAL
*              CHARACTER SET.
***********************************************************************
         EJECT
*/*IEECVET7: CHART */
*/*%START: E IEECVET7 DELETE 2 */
*/*%GO:        P       SET BASES */
*/*%TSTDOM:    D       (YES,DOMRTN,NO,) WAS A DOM ISSUED */
*/*TEST1: D (YES,,NO,XCTL) ANY INT. REQ'D MSGS */
*/*%A1: P SET COUNT OF 20 INT REQ'D MSGS */
*/*LISTINTR: P POINT TO CCW AREA SCT, SIB AND END OF SCT */
*/*%A2: D (YES,,NO,INTLOOP2) ANY MSGS LEFT TO DO */
*/*INTLOOP: D (YES,,NO,INCRE2) THIS MSG AN INT REQ'D MSG */
*/*%A3: P SAVE DEVICE ADDRESS */
*/*%A4: D (YES,OUT,NO,) 20 MSGS DONE */
*/*INCRE2: P INCRE SCT, SIB CCW AREA PTRS */
*/*%A5: D (YES,,NO,INTLOOP) END OF SCREEN */
*/*OUT: P SAVE REGS 1 AND 2 */
*/*%A7: S IOSGEN:FIND SATISFIED INT REQD CONDITIONS */
*/*MARK: P (,LISTINTR) ZERO COUNT OF 20 MSGS (IF NOT ZERO ALREADY) */
*/*INTLOOP2: D (YES,,NO,INCRE3) THIS A INT REQD MSG */
*/*%B1: D (YES,,NO,INCCCW) THIS MSG SATISFIED */
*/*%B2: P MARK MSG DELETEABLE REDUCE INT REQD COUNT */
*/*INCCCW: D (YES,OUT2,NO,) END OF DOM LIST */
*/*%B3: P POINT TO NEXT INT REQD ELEMENT */
*/*INCRE3: P INCREMENT SCT SIB */
*/*%B4: D (YES,,NO,INTLOOP2) END OF SCREEN */
*/*OUT2: P RESTORE REGS 1 AND 2 */
*/*%B5: D (YES,FINISHR,NO,) ALL MSGS TESTED FOR INT REQD */
*/*%B6: P (,INTLOOP) POINT TO CCW AREA SET CNT OF 20 MSGS */
*/*FINISHR: P INDICATE INT REQ DEL TRIED */
*/*%B7:  D (YES,XCTL,NO,) DELETE REQUEST PENDING */
*/*%B8: D (YES,,NO,BUILDCCW) DEL=Y */
*/*%B9: P SET AUTO DELETE TRIED */
*/*%C1: P SET TRY AUTO DELETE */
*/*DEL4: P SET DELETE 4 NAME IN XSA */
*/*XCTL: R TO ROUTINE SET IN XSA */
*/*BR14: P TURN OFF OUR RTNS IN CONTROL */
*/*%C2: R BR REG14 */
*/*DOMRTN: P TURN OFF DOM FLAG */
*/*%C3: P POINT TO START OF SCT,SIB,DOM TABLE */
*/*%C4: P GET DOMLIST POINTER */
*/*%C5: D (YES,,NO,BR14) DOES A DOMLIST EXIST */
*/*ELTEST: D (YES,,NO,NEXTLINK) USE THIS ELEMENT */
*/*%CX: P SAVE POSSIBLE LINK POINTER */
*/*%SAVALL: P SAVE TCBLSQA INDEX AND TJID */
*/*%USED: P INDICATE THIS ELEMENT USED */
*/*LOOP2: P SAVE POSSIBLE DOM MSG ID */
*/*IDCOMP: D (YES,,NO,NEXT) MSG IN THIS LINE */
*/*%TCBTJID: D (YES,,NO,INCRE4) DO TCBLSQA INDEX AND TJID MATCH */
*/*%LSQA: D (YES,MARK1,NO,) IS THIS DOM BY LSQA INDEX */
*/*%DOMID: D  (YES,MARK1,NO,) DO MSG IDS MATCH */
*/*INCRE4: P POINT TO NEXT SCT,SIB,AND DOM ELEMENT */
*/*%SCREND: D (YES,NEXT,NO,IDCOMP) END OF SCREEN */
*/*NEXT: D (YES,NEXTLINK,NO,LOOP2) END OF THIS DOM LIST */
*/*NEXTLINK: D (YES,SETKEY3,NO,ELTEST) END OF DOM CHAIN */
*/*SETKEY3: D (YES,BR14,NO,) DELETE REQUEST PENDING */
*/*BUILDCCW: D (YES,DEL4,NO,) INT REQD MSGS TO DELETE */
*/*%WRMSG: P INDICATE WRITE MSG AREA */
*/*%IO: P (,XCTL) SET XSA FOR IO ROUTINE */
*/*MARK1: P (,INCRE4) MARK MESSAGE AS AUTO DELETEABLE */
*/*IEECVET7: END */
         EJECT
***********************************************************************
*
*        REGISTER EQUATES
*
***********************************************************************
X0       EQU   0              CTR FOR MSGS MOVED INTO CCW AREA
X1       EQU   1              PARM REG
XMSG     EQU   1              PTR TO MESSAGE AREA
XWORK    EQU   2              WORK REG
XSCT     EQU   2        POINTER TO SCREEN CONTROL TABLE
XWORK2   EQU   3              WORK REG
XDOMPTR  EQU   3        POINTER TO DOM ELEMENT
XINC     EQU   4              INCRE FOR SCREEN CONTROL TABLE REG
XCOMP    EQU   5              ADDR OF LAST MSG IN SCT
XDOMEL   EQU   6        POINTER TO DOM ID IN DOM ELEMENT
XWORK3   EQU   6              WORK REG
X7       EQU   7             UCM BASE
XCCWCTR  EQU   8              PTR TO CCW AREA
X9       EQU   9              DCM BASE REG
XA       EQU   10             PROGRAM BASE REG
XB       EQU   11             CXSA BASE REG
XC       EQU   12             UCM ENTRY BASE REG
XIOB     EQU   13             IOB PTR REG
XLINK    EQU   14                  LINK REGISTER
XE       EQU   14             REG 14
XERTRN   EQU   14
XDCMDOM  EQU   15                  POINTER TO DCM
XF       EQU   15             REG 15
X1PARM   EQU   1
X8UCME   EQU   8
XFBRANCH EQU   15
ENDLIST  EQU   X'80'          END OF LIST INDICATOR
ZERO     EQU   0              ZERO
         SPACE 3
         BALR  XA,N0     ESTABLISH PROGRAM ADDRESSIBILITY
         USING *,XA
         B     BEGIN               BRANCH AROUND PATCH AREA
ICATCH   DC    CL8'IEECVET7'       EYECATCHER AND FIELD MAINTENANCE
         DC    CL8'&SYSDATE',CL56' '
BEGIN    EQU   *                   END OF PATCH AREA
         LR    XB,X1               ESTABLISH CXSA ADDRESS
         USING CXSA,XB
         L     XC,CSAUCM           ESTABLISH UCM ADDRESS
         USING UCMLIST,XC
         L     XDCMDOM,UCMXB            GET POINTER TO DCM
         USING DCMTSRT,XDCMDOM
         L     X9,DCMADTRN            LOAD T DCM BASE
         USING DCMSTRT,X9          ESTABLISH DCM BASE REGISTER
         L     X7,CSACTLM          GET POINTER TO UCM
         USING UCM,X7              ESTABLISH UCM BASE REGISTER
         MVC   DCMTRACE(DCMTRLEN),DCMTRAC2 SHIFT OLD TRACE ENTRIES
         MVI   DCMTREN1,ID1        PUT CSECT ID INTO
         MVI   DCMTREN2,ID2        NEW TRACE ENTRY
         MVI   DCMUTILT,ZERO       CLEAR UTILITY FLAGS           S21003
         LH    XINC,DCMSCTCN            SCT INCREMENT
         TM    UCMDEVC,UCMDEVD          Q. DOM ISSUED            S21003
         BO    DOMRTN                   YES,GO HANDLE DOM
         DROP  XDCMDOM
XCTL     EQU   *
         L     XFBRANCH,DCMNPROC   LOAD PROC 1 ADDRESS
         L     X1PARM,DCMCXSVE     RESTORE CXSA ADDRESS
         BR    XFBRANCH            EXIT TO IEECVET1
BR14     EQU   *
         ICM   XWORK,15,CSANPTR    PTR TO RECOVERY AREA       JE YM6718
         BZ    NOESTAE             NOT THERE, BYPASS NSI      JE YM6718
         MVC   PARMRTAD-PARMLIST(8,XWORK),CSANAME RESTORE     JE YM6718
NOESTAE  DS    0H                                             JE YM6718
         NI    DCMMCSST,N255-DCMDUSE   INDIC OUR RTNS NOT IN CONTROL
         L     XE,CSAXA            PICK UP RETURN ADDRESS
         L     XFBRANCH,CSAXC      ADDRESS FREELOCK SUBROUTINE
         L     X1PARM,DCMCXSVE     PASS CXSA ADDRESS IN REG 1
         BR    XFBRANCH            RELEASE LOCKS AND RETURN TO MCS
DOMRTN   EQU   *        THIS RTN WILL MARK DOMMED MSGS DELETABLE
         L     XFBRANCH,CSAXB      ADDRESS GETLOCK SUBROUTINE
         L     X1PARM,DCMCXSVE     PASS CXSA ADDRESS IN REG 1
         BALR  XERTRN,XFBRANCH     OBTAIN LOCAL AND CMS LOCKS
         NI    UCMDEVC,N255-UCMDEVD    TURN OFF DOM FLAG         S21003
         L     XCOMP,DCMADDRL      ADDR OF LAST MSG LINE IN SCT
         LR    XMSG,X7                 GET UCM POINTER           S21003
         SH    XMSG,FOUR               POINT TO PREFIX           S21003
         L     XMSG,N0(XMSG)           GET PTR TO UCM PREFIX
         USING UCMPRFX,XMSG            ESTABLISH BASE FOR UCM PREFIX
         L     XDOMPTR,UCMDOME         PTR TO FIRST DOM ELEMENT
         USING DOMC,XDOMPTR        DOM ELEMENT ADDRESSABILITY MB Y02958
         LA    XDOMPTR,N0(XDOMPTR) CLEAR HIGH ORDER BYTE       MD
         LTR   XDOMPTR,XDOMPTR     IS POINTER ZERO             MD
         BZ    BR14                YES FALSE ENTRY EXIT        MD
         DROP  XMSG
         NI    DCMDSTAT,N255-DCMDSAUT    TURN OFF AUTO DEL TRIED BIT
ELTEST   EQU   *              TEST WHETHER TO USE THIS DOM ELEMENT
         L     XIOB,DCMASCRN           POINT TO FIRST MESSAGE MG  M1427
         LR    XDOMEL,XDOMPTR     SAVE PTR TO LINK PTR      JE @ZA00512
         TM    DOMCNTRL,DOMCPROC   USE THIS DOM ELEMENT       MB Y02958
         BNO   NEXTLINK                 NO, GO ON TO NEXT ELEMENT
         TM    DOMCNTRL,DOMCAUTH   AUTHORIZED MSGID CALL      ZZ
         BO    LOOP2               YES,DONT GET ASID AND JSTCB
         TM    DOMCNTRL,DOMCDBL+DOMCDBAJ  ANY MSG ID'S                 X
                                   IN THIS BLOCK              MB Y02958
         BNZ   SAVID               NO SAVE ID AND TJID
KICK4    EQU   *
         LA    XDOMPTR,DOMCID      ADDRESS MSG ID ENTRIES     MB Y02958
         USING DOMCID,XDOMPTR      ADDRESSABILITY FOR                  X
                                   MESSAGE ID ENTRIES         MB Y02958
         TM    DOMCFLAG,DOMCEND    IS THIS LAST ELEMENT       MB Y02958
         BNO   KICK4               NO GET NEXT ELEMENT        MD
SAVID    EQU   *                                              MD
         MVC   DCMDSAV+N4(N2),DOMCASID SAVE ASID              MB Y02958
         MVC   DCMDSAV+N6(N3),DOMCJTCB+N1 SAVE JSTCB ADDRESS  MB Y02958
         LR    XDOMPTR,XDOMEL      RESTORE PTR TO START OF LSTMD
         USING DOMC,XDOMPTR        MESSAGE ID ENTRY POINTER NOT        X
                                   VALID UNTIL INCREMENTED    MB Y02958
LOOP2    EQU   *              LOOP THRO DOM ID S TESTING FOR MATCH WITH
*                             MSGS ON THE SCREEN
         L     XSCT,DCMAMTAB       PTR TO BEGIN OF SCT        MD
         L     XDCMDOM,DCMDOMPK    POINT TO DOM TABLE         MD
         TM    DOMCNTRL,DOMCDBL+DOMCDBAJ DOM BY ASID OR JSTCB MB Y02958
         BNZ   IDCOMP              YES DON'T SAVE SEQ NO.     MD
         AH    XDOMEL,FOUR         GET FIRST ID               MD
         USING DOMCID,XDOMEL       ID ENTRY BASE VALID NOW    MB Y02958
         MVC   DCMDSAV+N9(N3),DOMCIDA SAVE ID                 MB Y02958
         L     XIOB,DCMASCRN       POINT TO FIRST MESSAGE     MG  M1427
IDCOMP   EQU   *
         TM    N0(XSCT),DCMMSGIN   MESSAGE IN LINE
         BNO   NEXT                NO GET NEXT DOM ELEMENT
         TM    DOMCNTRL,DOMCDBL+DOMCDBAJ DOM ASID OR JSTCB    MB Y02958
         BNZ   CHECK7              YES, TEST FOR DECRIPTOR CODE 7
         TM    DOMCNTRL,DOMCAUTH   AUTHORIZED REQUEST         MB Y02958
         BNO   FULLCOMP            NO, CHECK TCB AND MSG ID   MB Y02958
         CLC   N5(N3,XDCMDOM),DCMDSAV+N9 CHECK MSG ID         MB Y02958
         BE    MARK1               MARK MSG DELETABLE         MB Y02958
         B     INCRE4              NOT FOUND, KEEP LOOKING    MB Y02958
FULLCOMP EQU   *          CHECK ASID AND JSTCB WITH MSG ID    MB Y02958
         CLC   N0(N8,XDCMDOM),DCMDSAV+N4  COMPARE ID NUMBERS  MB Y02958
         BE    MARK1                    YES, GO MARK MSG DELETABLE
INCRE4   EQU   *
         LA    XDCMDOM,N8(XDCMDOM)
         AH    XIOB,DCMCORLN            POINT TO NEXT LINE    MG  M1427
         BXLE  XSCT,XINC,IDCOMP         INCRE TO NEXT MSG IN SCT
NEXT     EQU   *
         TM    DOMCNTRL,DOMCDBL+DOMCDBAJ DOM BY ASID OR JSTCB MB Y02958
         BNZ   NEXTLINK            YES NO MORE IDS            MD
         TM    DOMCFLAG,DOMCEND    END OF DOM ELEMENT CHAIN   MB Y02958
         BNO   LOOP2                    NO,TEST NEXT DOM ID FOR MATCH
NEXTLINK EQU   *
         L     XDOMPTR,DOMCLNK     GET NEXT DOM ELEMENT       MB Y02958
         LA    XDOMPTR,N0(XDOMPTR)      CLEAR HIGH ORDER BYTE
         LTR   XDOMPTR,XDOMPTR          Q. IS LINK PTR ZERO
         BNZ   ELTEST                   NO,GO TEST TO SEE WHETHER TO
*                                       USE THIS DOM ELEMENT
         TM    DCMUTILT,DCMUTILB   IO TO DO                   MB Y02958
         BNO   BR14                NO, RETURN IMMEDIATELY     MB Y02958
WHATELSE TM    UCMSTS,UCMBF        IS THE DEVICE BUSY         MG S21003
         BNO   SETKEY3             NO, KEEP GOING             MG S21003
         TM    DCMUTILT,DCMUTILB   WAS ANYTHING DOMMED        MH Y02131
         BZ    BR14                 NO, GO TO MCS             MH Y02131
         NI    DCMUTILT,X'FF'-DCMUTILB  TURN OFF FLAG         MH Y02131
         L     XDCMDOM,UCMXB            POINT TO RDCM
         USING DCMTSRT,XDCMDOM           BASE RDCM
         OI    DCMRFLGS,DCMDOM     IND SCRN UPDATE NEEDED
         B     BR14                RETURN TO MCS              MG S21003
         DROP  XDCMDOM
MARK1    EQU   *
         NI    N0(XSCT),N255-DCMMSGWR-DCMMSGIF   TURN OFF SCT    S21003
         NI    N1(XSCT),N255-DCMMSGAC-DCMMSGC7-DCMMSGDM   FLAGS  S21003
         MVI   N2(XIOB),VERTBAR         MARK WITH VERTICLE BAR   S21003
         MVI   N3(XIOB),CBLANK          REMOVE ACTION *          S21003
         OI    N0(XSCT),DCMMSGAD        MARK AUTO DELETABLE      S21003
         C     XIOB,DCMWTBUF       MESSAGE BEHIND A DISPLAY   MB YM4087
         BNL   INCRE4              YES, DONT DO IO            MB YM4087
         OI    DCMUTILT,DCMUTILB   IO TO DO                   MH Y02131
         B     INCRE4                   GO FINISH SEARCH
CHECK7   EQU   *                                              MD
         TM    N1(XSCT),DCMMSGC7   DESC. CODE 7 MSG           MD
         BNO   INCRE4              NO NOT DOMMABLE            MD
         TM    DOMCNTRL,DOMCDBL MATCH ON ASID ONLY            MB Y02958
         BO    COMPASID            YES CHECK ASID ONLY
         CLC   DCMDSAV+N4(N5),N0(XDCMDOM) DO JSTCBS MATCH
         BNE   INCRE4              NO CHECK NEXT LINE         MD
         B     MARK1               YES, MARK FOR DELETION
COMPASID EQU   *
         CLC   DCMDSAV+N4(N2),N0(XDCMDOM) ASID MATCH
         BE    MARK1               YES MARK MSG               MD
         B     INCRE4              GET NEXT LINE              MD
SETKEY3  EQU   *                                                 S21003
         USING DCMTSRT,XF           BASE RDCM
         L     XF,UCMXB            GET POINTER TO RDCM        MD
         CLI   DCMRQDEL,BLANK          Q. IS A DELETE REQUEST PENDING
         DROP  XF                  DROP RDCM BASE             MD
         BNE   BR14                EXIT TO MCS
         TM    DCMUTILT,DCMUTILB   WAS ANYTHING DOMMED        MH Y02131
         BZ    BR14                NO, GO TO MCS              MH Y02131
BUILDCCW EQU   *
         OI    DCMIOCM1,DCMWRMSG   INDICATE WRITE MESSAGE AREA
IOEXIT   EQU   *
         L     XFBRANCH,DCMIORTN   LOAD IO ROUTINE ADDRESS
         L     X1PARM,DCMCXSVE     RESTORE CXSA ADDRESS
         BR    XFBRANCH            EXIT TO IEECVET(H,P,R, OR U)
         EJECT
***********************************************************************
*
*        CONSTANTS
*
******************************************************************
FOUR     DC    H'4'                FOUR
H20      DC    H'20'               NUMBER
         SPACE 3
***********************************************************************
*
*        EQUATE STATEMENTS
*
***********************************************************************
ID1      EQU   C'E'                1ST CHARACTER OF CSECT ID
ID2      EQU   C'7'                2ND CHARACTER OF CSECT ID
DOMSET   EQU   X'40'              INDICATES MSG TO BE DOMMED
N0       EQU   0                  NUMBER
N1       EQU   1                  NUMBER
N2       EQU   2                  NUMBER
N3       EQU   3                  NUMBER
N4       EQU   4                  NUMBER
N5       EQU   5                  NUMBER
N6       EQU   6                  NUMBER
N7       EQU   7                  NUMBER                      MD
N8       EQU   8                  NUMBER
N9       EQU   9                   NUMBER
N12      EQU   12                 NUMBER
N14      EQU   14                 NUMBER
N15      EQU   15                 NUMBER
N17      EQU   17                 NUMBER
N21      EQU   21                 NUMBER
N28      EQU   28                 NUMBER
N32      EQU   32                 NUMBER
N39      EQU   39                 NUMBER
N40      EQU   40                 NUMBER
N80      EQU   X'80'              DOM KEY BIT                 MG  M1427
N255     EQU   255                NUMBER
VERTBAR  EQU   C'×'          VERTICAL BAR
CBLANK   EQU   C' '          BLANK
BLANK    EQU   C' '          BLANK CHARACTER
CN       EQU   C'N'          CHARACTER N
CHARY    EQU   C'Y'          CHARACTER Y
         EJECT
***********************************************************************
*
*        DSECTS
*
***********************************************************************
         EJECT
         IHACTM FTPT                                          JE YM6718
         IEERDCM
         EJECT
         IEETDCM
         EJECT
         IEECUCM  FORMAT=NEW
         EJECT
         IHACTM  CXSA
         EJECT
         IHADOMC
         EJECT
UNIT     DSECT
         IEFUCBOB
         EJECT
         CVT   DSECT=YES
    END
