IEECVET8 CSECT
***********************************************************************
         TITLE 'IEECVET8 IGC5807B DELETE 3'
***********************************************************************
*                                                                     *
* STATUS       CHANGE LEVEL=0                                         *
*                                                                     *
* APARS FIXED ZA08078                                                 *
*                                                                     *
* FUNCTION     THIS ROUTINE ANALYZES DELETION OF MESSAGES REQUESTED BY*
*              K E,SEG OR BY CURSOR OR LIGHT PEN DETECT               *
*                                                                     *
* ENTRY POINT  IEECVET8                                               *
*                                                                     *
* INPUT        INDICATION OF K E,SEG,CURSOR DELETE                    *
*              THE FOLLOWING BIT INDICATES A CURSOR OR LIGHT PEN      *
*              REQUEST                                                *
*                   DCMCOM1,DCMCOMRD  X'08'                           *
*              DCMDSAV  CONTAINS THE NUMBER OF THE MESSAGE THE DETECT *
*                       OCCURRED ON                                   *
*              DCMDSAV+8 CONTAINS THE SCREEN CONTROL TABLE ENTRY FOR  *
*                        THE MESSAGE THE DETECT OCCURRED ON           *
*              THE APPROPRIATE MESSAGES ARE MARKED FOR DELETION, AND  *
*              ALL VISIBLE MESSAGES ARE NUMBERED.                     *
*                                                                     *
* OUTPUT       ALTERATION OF DISPLAY- INDICATION OF MESSAGES TO BE    *
*              DELETED                                                *
*                                                                     *
* EXTERNAL REFERENCES NONE                                            *
*                                                                     *
* EXITS - NORMAL  IEECVETH OR P OR R  DEVICE-DEPENDENT IO             *
*                 IEECVET9 DELETE 4                                   *
*       - ERROR   IEECVETE MESSAGE 2                                  *
*                                                                     *
* TABLES/WORK AREAS DISPLAY CONTROL MODULE (CONTAINED IN DSECT)       *
*                   SCREEN CONTROL TABLE (SCT)                        *
*                   SECONDARY SCREEN CONTROL TABLE (SSCT)             *
*                                                                     *
* ATTRIBUTES   REFRESHABLE, PRIVILEGED, TYPE 4 SVC                    *
*                                                                     *
* NOTES        THE OPERATION OF THIS MODULE DOES NOT DEPEND UPON A    *
*              PARTICULAR INTERNAL REPRESENTATION OF THE EXTERNAL     *
*              CHARACTER SET.                                         *
*                                                                     *
*                                                                     *
***********************************************************************
         EJECT
*/*IEECVET8: CHART */
*/*IEECVET8: E DELETE RTN 3 */
*/* P ESTABLISH BASE REGISTERS */
*/* D (NO,,YES,LPDEL) LP/CURSOR DELETION ? */
*/* D (NO,,YES,ERROR) IS SEG = 0 ? */
*/* P FIND LAST MSG ENTRY IN SCT */
*/*DELET: D (NO,DSETUP,YES,) IS LAST MSG IN SEG CONTINUED ? */
*/* P INCLUDE CONTINUED LINE */
*/*DSETUP: P FIND FIRST MSG ENTRY IN SCT */
*/*DSETUP1: P SET UP TO LOOP THRU MESSAGES */
*/*LOOP: D (NO,NOUT,YES,) IS A MSG IN THIS LINE ? */
*/* S NUMBER RTN: GO NUMBER THE MESSAGE */
*/* D (NO,,YES,NNEXT) MSG IN A STATUS DISPLAY ? */
*/* D (NO,L1,YES,NNEXT) CONTINUED LINE IN A STATUS DISP ? */
*/*L1: D (NO,,YES,TURNOFF) MSG ABOVE DELETE RANGE ? */
*/*SLOOP: D (NO,,YES,RESET) IS MSG AN ACTION MSG ? */
*/*SET: P INDICATE DELETION POSSIBLE */
*/* P MARK MSG WITH VERTICLE BAR */
*/*NNEXT: P GO TO NEXT MESSAGE */
*/* D (NO,,YES,LOOP) MORE MESSAGES TO DO ? */
*/*NOUT: D (NO,,YES,MOVENT) LP/CURSOR DELETE WTOR ? */
*/* D (NO,ERROR1,YES,) ANY MESSAGES TO DELETE? */
*/* P PUT COMMAND IN ENTRY AREA */
*/*MOVENT: P PUT COMMAND IN DELETE REQUEST BUFFER */
*/* D (NO,,YES,VERIFMSG) IS VERIFY REQUIRED ? */
*/*DEL4EXIT: P (,XCTL) SET EXIT TO IGC5907B */
*/*VERIFMSG: P INDICATE VERIFICATE INSTR NEEDED */
*/*CURSCOMP: P SET CURSOR POS TO LINE 1, CHAR 5 */
*/*IOEXIT: P SET EXIT TO IGC5D07B */
*/*XCTL: R NEST LOAD */
*/*ERROR1: P (,ERRORALL) INDICATE NO DELETABLE MESSAGES */
*/*ERROR: P INDICATE SEG = 0 */
*/*ERRORALL: P INDICATE WRITE ERROR MSG */
*/* P FIND POSITION OF OPERAND */
*/* D (NO,,YES,LINE2) SET LINE NUMBER TO 1 */
*/*STCURS: P SET CURSOR POSITION IN LINE */
*/*ERRNUM: P SET UP LOOP THRU MSGS */
*/*LOOP1: D (NO,OUT,YES,) MESSAGE IN LINE ? */
*/* S NUMBER RTN: GO NUMBER MESSAGES */
*/* D (NO,LOOP1,YES,) ALL MESSAGES DONE ? */
*/*OUT: P HOLD LAST NUMBER */
*/*MSGEXIT: P (,XCTL) SET EXIT TO IGC5E07B */
*/*NUMBER: E NUMBER RTN */
*/* P CONVERT NUMBER FOR PRINT */
*/* D (NO,MOVENUM,YES,) ONE DIGIT NUMBER ? */
*/* P BLANK FIRST DIGIT */
*/*MOVENUM: P PUT NUMBER IN MESSAGE LINE */
*/* R BR 14 */
*/*LINE2: P (,STCURS) SET LINE NUMBER TO TWO */
*/*TURNOFF: P TURN OFF DELETE REQUEST BIT */
*/* D (NO,REMOVE,YES,) IS THIS AN INFORM MSG ? */
*/* P PUT HORIZONTAL BAR IN MSG */
*/*RESET: P TURN OFF DELETE REQUEST FLAG */
*/*REMOVE: P PUT VERTICLE BAR IN MESSAGE */
*/* D (NO,EXITB,YES,) IS THIS AN ACTION MESSAGE ? */
*/* P PUT AN ASTERISK IN THE MESSAGE */
*/*EXITB: R BR 14 */
*/*LPDEL: P GET SCT ADDR FOR DELETION */
*/* D (NO,DELET,YES,) IS THIS AN ACTION MSG ? */
*/* P INDICATE THIS IS A WTOR */
*/*MARKWTOR: D (NO,MARKOK,YES,) IS THIS A CONTINUATE LINE ? */
*/* P ALTER POINTERS TO INCLUDE FIRST LINE */
*/*MARKOK: P INDICATE DELETE REQUESTED AND MSG DELETABLE */
*/* D (NO,ENTSETUP,YES,) IS WTOR CONTINUED ? */
*/* P (,MARKWTOR) GO TO NEXT LINE */
*/*ENTSETUP: P BLANK THE ENTRY AREA */
*/* P PUT COMMAND (K E,NN) IN ENTRY AREA */
*/* P INDICATE MESSAGES NUMBERED */
*/*NUMTURN: D (NO,NOUT,YES,) MESSAGE IN THIS LINE ? */
*/* S NUMBER RTN:NUMBER MESSAGE */
*/* D (NO,,YES,TURNOFF) MSG OUTSIDE DELETE RANGE ? */
*/*UPDTE: P (,NOUT) GO TO NEXT MESSAGE */
*/*IEECVET8: END */
          EJECT
***                                                                   *
***      REGISTER EQUATES                                             *
***                                                                   *
X0       EQU   0         SECONDARY SCREEN CONTROL TABLE PTR      S21002
X1       EQU   1         REG 1
XNUM     EQU   1         REG 1
XWORK    EQU   2         WORK REG.
XSCT     EQU   2                        SCREEN CONTROL TABLE PTR
XHCTR    EQU   3         HIGH VALUE FOR NN
XDOMEL   EQU   3                        PTR TO DOM ID IN DOM ELEMENT
XINC     EQU   4         INCREMENT REG (BXLE)
XCOMP    EQU   5         COMPARAND (BXLE)
XLCTR    EQU   6         LOW VALUE FOR NN
XDCMDOM  EQU   6         PTR TO DOM ID IN DCM
X7       EQU   7         UCM BASE REG
R7SCT    EQU   7         SCT TABLE POINTER
XMSG     EQU   8         BRANCH REG.
X9       EQU   9         DCM BASE REG
XA       EQU   10        PROGRAM BASEREG.
XB       EQU   11        CXSA BASE REG.
XC       EQU   12        UCM ENTRY BASE REG.
XE       EQU   14        REG 14
X13      EQU   13        RESIDENT DCM BASE REGISTER              S21002
XCCWCTR  EQU   14        REG TO COUNT CCW'S CONSTRUCTED
XF       EQU   15        REG 15
XMASK    EQU   15        MASK REGISTER
XRETURN  EQU   15        REG 15
RETURN   EQU   15        REG 15
X1PARM   EQU   1
XFBRANCH EQU   15
         EJECT
***
***  ESTABLISH BASE REGISTERS
***
         BALR  XA,N0                    ESTABLISH PROGRAM ADDRESSIBIL
         USING *,XA
         B     ICATCH          BRANCH AROUND EYECATCHER
         DC    C'01 MODULE IGC5807B, FICHE IEECVET8  '  EYECATCHER AND
ZAPAREA  DC    12C'ZAP'    RESERVED FOR FIELD MODIFICATION
ICATCH   LR    XB,X1                    ESTABLISH CXSA ADDRESS
         USING CXSA,XB
         L     XC,CSAUCM                ESTABLISH UCM ADDRESS
         USING UCMLIST,XC
         L     X13,UCMXB           GET PTR TO RESIDENT DCM       S21002
         L     X9,N0(X13)          GET PTR TO TRANSIENT DCM      S21002
         USING DCMTSRT,X13         ADDRESSIBILITY RESIDENT DCM
         USING DCMSTRT,X9
         L     X7,CSACTLM               GET PTR TO UCM
         USING UCM,X7
         MVC   DCMTRACE(DCMTRLEN),DCMTRAC2 SHIFT OLD TRACE ENTRIES
         MVI   DCMTREN1,ID1        PUT CSECT ID INTO
         MVI   DCMTREN2,ID2        NEW TRACE ENTRY
         MVI   DCMUTILT,ZERO       ZERO UTILITY BYTE
         LH    XINC,DCMSCTCN       GET INCREMENT FOR SCREEN CONTROL TAB
         SR    XHCTR,XHCTR         ZERO REGISTER
         L     XMSG,DCMASCRN       ADDR OF FIRST MSG LINE IN BUFFER
         L     XLCTR,DCMAMTAB      ADDR OF SCT FOR MSG 1
         TM    DCMCOM1,DCMCOMRD    Q. PERFORM DELETE AS INDICATED BY
*                                     LIGHT PEN OR CURSOR
         BO    LPDEL               YES, GO SET UP FOR IT
         IC    XHCTR,DCMSEG        GET SEG VALUE
         LTR   XHCTR,XHCTR         Q. IS SEG VALUE EQUAL TO ZERO
         BZ    ERROR               YES, ERROR
         MH    XHCTR,DCMSCTCN      COMPUTE SCREEN CONTROL ADDR OF
         SR    XHCTR,XINC           LAST MSG OF SEGMENT
         AR    XHCTR,XLCTR
DELET    EQU   *
         LA    XF,NNEXT            GET  RETURN ADDR FOR TURNOFF RTN
CONT1    TM    N0(XHCTR),DCMMSGCN  Q. IS LAST MSG IN SEG CONTINUED
         BNO   DSETUP              NO
         AR    XHCTR,XINC          INCREMENT TO INCLUDE CONTINUED LINE
         B     CONT1               CHECK FOR FURTHER CONTIN
DSETUP   EQU   *
         LR    XWORK,XLCTR         ADDR OF FIRST MSG IN SCT
DSETUP1  EQU   *
         L     XCOMP,DCMADDRL      ADDR OF LAST MSG IN SCT
         SR    XNUM,XNUM           ZERO NUMBER REG
         OI    DCMDSTAT,DCMDSTNM   TURN ON MSGS NUMBEREA BIT
         L     X0,DCMADSEC         ADDR OF STATUS DISPLAYS SSCT  S21002
LOOP     EQU   *
         TM    N0(XWORK),DCMMSGIN  Q. IS THERE A MESSAGE IN THIS LINE
         BZ    NOUT                NO, EXIT
***
***   NUMBER MESSAGES
***
         BAL   XE,NUMBER           GO NUMBER MESSAGES
         LR    XE,X0               ADDR OF SSCT MSG ENTRY        S21002
         LR    R7SCT,XWORK         GET SCT POINTER
***                                                              S21002
***        IS THIS MESSAGE LINE IN A STATUS DISPLAY              S21002
***                                                              S21002
TESTDISP TM    N0(XE),DCMSECCL+DCMSECLL+DCMSECDL+DCMSECBL        S21002
         BNZ   NNEXT               BRANCH YES                    S21002
         TM    N0(R7SCT),DCMMSGCN  Q. DOES IT HAVE A CONTINUATION LINE
         BNO   L1                  NO
         LA    XE,N1(XE)          POINT TO NEXT SSCT
         AH    R7SCT,DCMSCTCN     POINT TO NEXT SCT
         B     TESTDISP           TEST FOR FURTHER CONTIN
***                                                              S21002
***        IS THE CONTINUED LINE IN A STATUS DISPLAY             S21002
***                                                              S21002
         TM    N1(XE),DCMSECCL+DCMSECLL+DCMSECDL+DCMSECBL        S21002
         BZ    L1                  BRANCH NO                     S21002
         B     NNEXT               GO ON
L1       EQU   *
         CLR   XWORK,XHCTR         Q. IS MSG ABOVE RANGE FOR DELETION
         BH    TURNOFF             YES, REMOVE VERTICAL BAR, IF ANY
SLOOP    EQU   *
         TM    N1(XWORK),DCMMSGAC  Q. IS MSG AN ACTION MESSAGE
         BO    RESET               YES REMARK IF PREVIOUSLY DELETABLE
SET      EQU   *
         OI    DCMUTILT,DCMUTILA   SET BIT- DELETION POSSIBLE
         OI    N0(XWORK),DCMMSGRD  TURN ON DELETE REQUEST BIT
         ST    XMSG,CSAXD          SAVE ADDRESS OF MSG NUMBER
         MVI   N2(XMSG),VERTBAR    MARK MSG WITH VERTICAL BAR
NNEXT    EQU   *
         AH    XMSG,DCMCORLN       INCRE ADDR OF MSG IN BUFFER   S21003
         AH    X0,ONE              INCREMENT SSCT BY LENGTH      S21002
         BXLE  XWORK,XINC,LOOP     LOOP UNTIL ALL MSGS ARE SEARCHED
NOUT     EQU   *
         STH   XNUM,DCMADNUM       SAVE LAST NUMBER USED
         L     XE,DCMAENTR         GET ADDR OF ENTRY AREA
         TM    DCMUTILT,DCMUTILB   Q. LP OR CURSOR DELETE OF A WTOR
         BO    MOVENT              YES
         TM    DCMUTILT,DCMUTILA   Q. ANY MESSAGES DELETABLE
         BNO   ERROR1              NO, ERROR- NO DELETABLE MSGS
         MVI   N0(XE),BLANK        MOVE BLANK INTO ENTRY AREA
         LH    XWORK,DCMELGN       GET LENGTH OF ENTRY FOR BLANKING
         EX    XWORK,MVBLNK             EXECUTE MOVE INSTRUC
         MVC   N0(N6,XE),KCOMMAND  MOVE COMMAND INTO ENTRY AREA
         L     XMSG,CSAXD          RESTORE ADDRESS OF MSG NUMBER
         MVC   N6(N2,XE),N0(XMSG)  MOVE IN NUMBER                S21002
         CLI   N6(XE),CBLANK       Q. A ONE DIGIT NUMBER
         BNE   MOVENT              NO
         MVC   N6(N1,XE),N7(XE)    YES, MOVE OVER DIGIT
         MVI   N7(XE),CBLANK       BLANK OUT FORMER POSITION
MOVENT   EQU   *
         MVC   DCMRQDEL(N6),N2(XE) MOVE COMMAND INT DEL REQUEST BUF
         CLI   DCMDEL,R            ROLL OR ROLL DELETE MODE?   @ZA08078
         BE    DEL4EXIT            YES, BR. TO IEECVET9 EXIT   @ZA08078
         TM    DCMOPTST,DCMOPTVR        Q. IS VERIFICATION REQUIRED
         BO    VERIFMSG                 YES, GO SET UP VERIF MSG
DEL4EXIT EQU   *
         L     XFBRANCH,DCMNDEL4   LOAD DELETE 4 ADDRESS
         B     XCTL                EXIT TO IEECVET9
VERIFMSG EQU   *
         NI    DCMCOM1,N255-DCMLPENT   TURN OFF LIGHT PEN DELETE FLAG
         OI    DCMCMSG2,DCMDLREQ        INDIC MOVE IN VERIFIC INSTRUC
         OI    DCMIOCM1,DCMWRMSG+DCMWRINS+DCMWRENT+DCMINSC
CURSCOMP EQU   *
         MVI   DCMCULNO,N1         SET LINE NUMBER (IN ENTRY AREA) TO 1
         MVI   DCMPOSCU,N5         MOVE IN CURSOR POSITION
IOEXIT   EQU   *
         L     XFBRANCH,DCMNMSG1   LOAD MESSAGE 1 ADDRESS
*                                  FOR EXIT TO IEECVETD
XCTL     EQU   *         EXIT FROM MODULE
         NI    DCMCOM1,N255-DCMCOMRD   TURN OFF DELET FLAG
         L     X1PARM,DCMCXSVE     RESTORE CXSA ADDRESS
         BR    XFBRANCH            EXIT TO ADDRESS ALREADY SET
ERROR1   EQU   *
         OI    DCMCMSG3,DCMCDLR1    INDICATE NO DEL ETABLE MSGS
         B     ERRORALL            GO INDIC WRITE DELETE REQ INCONS
ERROR    EQU   *
         OI    DCMCMSG3,DCMCDLR3   INDICATE SEG EQUAL TO 0
ERRORALL EQU   *
         MVI   DCMRQDEL,CBLANK     BLANK THE DELETE REQUEST BUFFER
         MVC   DCMRQDEL+N1(N7),DCMRQDEL
         OI    DCMCMSG2,DCMDELRI   INDICATE WRITE ERROR MSG
         OI    DCMIOCM1,DCMWRMSG+DCMWRINS+DCMINSC
         L     X1,DCMADOPN         GET ADDR OF OPERAND
         L     XWORK,DCMAENTR      GET ADDR OF ENTRY AREA
         SR    X1,XWORK            SUBTRACT TO GET DOS OF OPERAND
         CH    X1,DCMCORLN         Q. IS OPERAND ON LINE 2       S21003
         BH    LINE2               YES
         AH    X1,ONE              NO, INCREMENT POS BY 1
         MVI   DCMCULNO,N1         SET LINE NUMBER(IN ENTRY AREA) TO 1
STCURS   EQU   *
         STC   X1,DCMPOSCU         SET CURSOR POSITION
ERRNUM   EQU   *
         SR    XNUM,XNUM           ZERO NUMBER CTR
         L     XWORK,DCMAMTAB      GET ADDR OF FIRST MSG IN SCT
         L     XCOMP,DCMADDRL      GET ADDR OF LAST NUM
         L     XMSG,DCMASCRN       GET ADDR OF FIRST MSG IN BUFFER
LOOP1    EQU   *
         TM    N0(XWORK),DCMMSGIN    Q. MSG IN LINE
         BNO   OUT                 EXIT
         BAL   XE,NUMBER           GO MOVE IN NUMBER
         AH    XMSG,DCMCORLN       INCRE TO NEXT MSG IN BUFFER   S21003
         BXLE  XWORK,XINC,LOOP1    LOOP UNTIL NO MORE MSGS
OUT      EQU   *
         STH   XNUM,DCMADNUM       STORE NUMBER
MSGEXIT  EQU   *
         L     XFBRANCH,DCMNMSG2   LOAD MESSAGE 2 ADDRESS
         B     XCTL                EXIT TO IEECVETE
***
***   NUMBER MESSAGE
***
NUMBER   EQU   *
         AH    XNUM,ONE            INCREMENT TO NEXT NUMBER
         CVD   XNUM,DCMPACK        CONVERT NUMB TO DECIMAL
         UNPK  DCMCVBIN+N2(N2),DCMCVBIN+N2(N2)    UNPACK NUMBER
         OI    DCMCVBIN+N3,CF0     FIX SIGN
         CLI   DCMCVBIN+N2,CF0     Q. A ONE-DIGIT NUMBER
         BNE   MOVENUM             NO, GO MOVE 2 DIGITS
         MVI   DCMCVBIN+N2,BLANK   YES, BLANK OUT FIRST DIGIT
MOVENUM  EQU   *
         MVC   N0(N2,XMSG),DCMCVBIN+N2  MOVE NUMBER INTO MESSAGE LINE
         BR    XE                  RETURN TO CALLER
LINE2    EQU   *
         MVI   DCMCULNO,N2         SET LINE NUMBER TO 2
         SH    X1,DCMCORLN         THEN GO TO                    S21003
         B     STCURS              STORE CURSOR POSITION
TURNOFF  EQU   *
         NI    N0(XWORK),N255-DCMMSGRD  TURN OFF DELETE REQUEST BIT
         TM    N0(XWORK),DCMMSGIF       Q. AN INFORMATIONAL MSG
         BZ    REMOVE                   NO,GO REMOVE BAR
         MVI   N2(XMSG),HORBAR          REPLACE HORIZONTAL BAR
         BR    XF                  RETURN TO CALLER
RESET    NI    N0(XWORK),N255-DCMMSGRD   TURN OFF DELETE BIT
REMOVE   EQU   *
         MVI   N2(XMSG),CBLANK           REMOVE VERTICAL BAR
         TM    N1(XWORK),DCMMSGAC      Q. AN ACTION MESSAGE
         BNO   EXITB               NO, RETURN TO CALLER
         TM    N1(XWORK),DCMMSGPP  THIS A PP ACTION MSG.      MD Y01958
         BNO   ASTER               NO MARK WITH AN ASTERISK   MD Y01958
         MVI   N3(XMSG),PPACT      MOVE IN '@'                MD Y01958
         B     EXITB               EXIT THIS SUBROUTINE       MD Y01958
ASTER    EQU   *                                              MD Y01958
         MVI   N3(XMSG),ASTERISK       MOVE IN ASTERISK
EXITB    EQU   *
         BR    XF                  RETURN TO CALLER
LPDEL    EQU   *
         L     XHCTR,DCMDSAV+N8    GET SCT ADDR FOR DELETION AS PASSED
*                                   BY LIGHT PEN/CURSOR ROUTINE
         TM    N1(XHCTR),DCMMSGAC  Q. IS THIS AN ACTION MESSAGE
         BNO   DELET               NO, GO PERFORM DELETION
         OI    DCMUTILT,DCMUTILB   INDICATE A WTOR
         L     XMSG,DCMDSAV+N4     GET BUFFER ADDR OF MESSAGE
         LR    XLCTR,XHCTR         GET ADDR OF SCT FOR DELETABLE WTOR
MARKWTOR EQU   *
         TM    N1(XLCTR),DCMMSGCT  Q. IS THIS A CONTINUATION LINE
         BNO   MARKOK              NO
         SR    XLCTR,XINC          YES, DECREMENT TO INCLUDE FIRST LINE
         SH    XMSG,DCMCORLN       MESSAGE ADDRESS AND SCT ADDR  S21003
         LR    XHCTR,XLCTR         OF THE WTOR
         B     MARKWTOR            CHECK FOR FURTHER CONTIN
MARKOK   EQU   *
         OI    N0(XHCTR),DCMMSGRD  MARK SCT ENTRY- DELETION REQUESTED
         MVI   N2(XMSG),VERTBAR    MARK MSG DELETABLE
         MVI   N3(XMSG),BLANK      BLANK OUT ACTION INDICATOR
         TM    N0(XHCTR),DCMMSGCN  Q. IS WTOR CONTINUED
         BNO   ENTSETUP            NO, GO SET UP COMMAND IN ENTRY
         AH    XMSG,DCMCORLN       INCREMENT TO NEXT LINE        S21003
         AR    XHCTR,XINC          INCREMENT TO NEXT SCT ENTRY
         B     MARKWTOR            GO MARK CONTINUATION LINE
ENTSETUP EQU   *
         L     XE,DCMAENTR         GET ADDR OF ENTRY AREA
         LH    XWORK,DCMELGN       GET INCRE FOR END OF ENTRY
         MVI   N0(XE),CBLANK       BLANK FIRST CHARACTER
         EX    XWORK,MVBLNK       EXECUTE MOVE FOR BLANKING ENTRY
         LA    XMSG,N4(XE)         GET ADDR TO MOVE NUMBER INTO
         SR    XNUM,XNUM           ZERO REG
         IC    XNUM,DCMDSAV        GET NUMBER OF MSG TO BE DELETED
         MVC   N0(N4,XE),KCOMMAND  MOVE IN K COMMAND
         BAL   XE,NUMBER           CONVERT LINE NUMBER TO BINARY
         CLI   N0(XMSG),CBLANK     Q. A ONE DIGIT NUMBER
         BNE   OK                  NO
         MVC   N0(N1,XMSG),N1(XMSG)  MOVE NUMBER OVER
         MVI   N1(XMSG),CBLANK
OK       EQU   *
         L     XMSG,DCMASCRN       RESTORE ADDR OF BUFFER
         L     XWORK,DCMAMTAB      AND ADDR OF SCT
         L     XCOMP,DCMADDRL      GET LAST SCREEN CONTROL TABLE ADDR
         LA    XF,UPDTE            GET RETURN ADDR FOR TURNOFF RTN
         SR    XNUM,XNUM           ZERO NUMBER REGISTER
         OI    DCMDSTAT,DCMDSTNM   TURN ON MSGS NUMBERED BIT
NUMTURN  EQU   *
         TM    N0(XWORK),DCMMSGIN  Q. IS THERE A MESSAGE IN THIS LINE
         BZ    NOUT                NO, EXIT
         BAL   XE,NUMBER           YES, GO NUMBER MESSAGE
         CLR   XWORK,XLCTR         Q. IS THIS MESSAGE OUTSIDE RANGE
         BL    TURNOFF             YES                   OF DELETION
         CLR   XWORK,XHCTR         Q. IS THIS MESSAGE OUTSIDE RANGE
         BH    TURNOFF             YES, GO TURN OFF DELETE REQUEST
UPDTE    EQU   *                        FLAGS IF ANY PRESENT
         AH    XMSG,DCMCORLN       INCREMENT TO NEXT MESSAGE     S21003
         BXLE  XWORK,XINC,NUMTURN  INCREMENT POINTER TO SCREEN CONTRL T
         B     NOUT                GO TEST FOR VERIFICATION DESIRED
MVBLNK   MVC   N1(N0,XE),N0(XE)    BLANK ENTRY AREA
         EJECT
***
***       EQUATES
***
ID1      EQU   C'E'                1ST CHARACTER OF CSECT ID
ID2      EQU   C'8'                2ND CHARACTER OF CSECT ID
BLANK    EQU   C' '                BLANK CHARACTER
CF0      EQU   C'0'                CHARACTER OF ZERO
N0       EQU   0                       NUMBER
N1       EQU   1                       NUMBER
N2       EQU   2                       NUMBER
N3       EQU   3                       NUMBER
N4       EQU   4                       NUMBER
N5       EQU   5                       NUMBER
N6       EQU   6                       NUMBER
N7       EQU   7                       NUMBER
N8       EQU   8                       NUMBER
TWENTY4  EQU   24            NUMBER
N255     EQU   255                     NUMBER
REGDELET EQU   64                      PERFORM REGULAR DELET IN DEL 2
LINE29   EQU   28            LINE 29
CCF      EQU   X'CF'
VERTBAR  EQU   C'×'                VERTICAL BAR
ZERO     EQU   0                   NUMBER
ASTERISK EQU   C'*'          ASTERISK
HORBAR   EQU   C'-'                    HORIZONTAL BAR
C20      EQU   X'20'                   END OF CCW CHAIN INDICATOR
CBLANK   EQU   C' '                    BLANK
PPACT    EQU   C'@'                PROBLEM PROGRAM ACTION     MD Y01958
R        EQU   C'R'                    DEL = R OR RD           @ZA08078
         EJECT
*
*        CONSTANTS
*
KCOMMAND DC    C'K E,1,'               CONSTANT
ONE      DC    H'1'                    CONSTANT
         EJECT
         IEERDCM
         EJECT
         IEETDCM
         EJECT
         IEECUCM  FORMAT=NEW
         EJECT
         IHACTM  CXSA
         END
