IEECVETG CSECT
* C (PJEXITC) APPROX. 250000  OPEN STATUS NOT SET              @ZA25731
* /* START OF SPECIFICATIONS ****
*02*  PROCESSOR = ASSEMBLER;
**** END OF SPECIFICATIONS ***/
         TITLE 'IEECVETG                OPEN-CLOSE'
* STATUS       CHANGE LEVEL 1
*
*     APARS FIXED -- ZA02303,ZA02347,ZA16376,ZA25731
*
* FUNCTION     THIS MODULE OPENS, AND LATER CLOSES, A UNIT DESIGNATED
*              AS A CONSOLE SUPPORT UNIT.
*
* ENTRY POINT                                                    S21003
*     IEECVETG FOR ALL FUNTIONS.                                 S21003
*
* INPUT        R1 POINTS TO 40 BYTE SCRATCH PAD AREA(SVRB EXTENSION)
*              MAPPED BY MACRO =IEECVMUG CXSA=.
*
* OUTPUT       FOR OPEN...  SUPPLIES OPENED DCB.
*              FOR CLOSE..  CLOSES DCB AND FREES THE CORE.
*
* EXTERNAL REFERENCES NONE
*
* EXITS-NORMAL IEECVET1 PROCESSOR  AFTER OPEN
*              IEECVETK TIMER-INTERPRETER  BEFORE CLOSE, IF DEL=R
*              IEECMDSV MCS DEVICE SERVICE  AFTER CLOSE
*              IEECVFTG CLEANUP                                  S21003
*      -ERROR  NONE
*
* TABLES/WORK AREAS  DISPLAY CONTROL MODULE (CONTAINED IN DSECT)
*
* ATTRIBUTES   REFRESHABLE, PRIVILEGED, TYPE 4 SVC
*
* NOTES        THE OPERATION OF THIS MODULE DOES NOT DEPEND UPON A
*              PARTICULAR INTERNAL REPRESENTATION OF THE EXTERNAL
*              CHARACTER SET.
*
*
*
R0       EQU   0        LOCAL SAVE REGISTER.
R1       EQU   1        INTERFACE REGISTER.
RXS      EQU   2        EXTENDED SVRB AREA PTR.
RTB      EQU   3        SCRATCH.
RTC      EQU   4        SCRATCH.
RUCM     EQU   5                  UCM BASE REG                   S21003
RXI      EQU   6        INDEXING                                 S21003
RXE      EQU   7        INDEXING.
RP       EQU   8        OPEN CONTROL BLOCK BASE /UCM,EIL.
RI       EQU   9        ODD INDEXER.
RD       EQU   10       DATA CONTROL BLOCKS BASE.
RU       EQU   11       UCM ENTRY BASE.
RBASE    EQU   12       PROGRAM BASE.
RDBASE   EQU   13       RES DCM BASE DURING CLOSE                S21003
RR       EQU   14       RETURN PT.
RPL      EQU   15       XCTL PARAMETER LIST PTR.
X1PARM   EQU   1
XFBRANCH EQU   15
XERTRN   EQU   14
         BALR  RBASE,N0                ESTABLISH PROG BASE REGISTER
         USING *,RBASE
         B     BEGIN               BRANCH AROUND EYECATCHER
         DC    CL8'IEECVETG'       MODULE NAME
         DC    XL2'4309'           JULIAN DATE LAST CHANGE  JE @ZA02303
         DC    CL8'&SYSDATE'        DATE LAST ASSEMBLY
         DC    S(*)
         DC    CL56' '             PATCH AREA
BEGIN    EQU   *
         LR    RXS,R1                                            S21003
         USING CXSA,RXS
         L     RU,CSAUCM               UCM ENTRY PTR.
         USING UCMLIST,RU
         L     RUCM,CSACTLM       UCM ADDRESS                    S21003
         USING UCM,RUCM                                          S21003
         L     RP,UCMXB
         USING DCMTSRT,RP
         L     RP,DCMADTRN
         DROP  RP
         USING DCMSTRT,RP
         MVC   DCMTRACE(DCMTRLEN),DCMTRAC2 SHIFT OLD TRACE ENTRIES
         MVI   DCMTREN1,ID1        PUT CSECT ID INTO
         MVI   DCMTREN2,ID2        NEW TRACE ENTRY
         L     XFBRANCH,CSAXB      GETLOCK ADDRESS
         BALR  XERTRN,XFBRANCH     OBTAIN LOCKS
         NI    UCMATR,N255-UCMAT04  TURN OFF STATUS CHANGE BIT
         SR    RTC,RTC                 CLEAR.
         IC    RTC,CSACODE             ENTRY CODE.
         MVI   CSACODE,N0
         B     *+N4(RTC)                FUNCTION SELECTION
         B     PJOPEN              0 - TO OPEN.               JE YM6718
         B     PJCLOSE             4 - TO CLOSE               JE YM6718
         B     OPENRCVY            8 - TO OPEN RECOVERY       JE YM6718
         B     PJEXITC             12 - TO CLOSE RECOVERY     JE YM6718
         EJECT
*              CLOSE DEVICE
PJCLOSE  DS    0H                                             JE YM6718
         L     RI,UCMDCB
         LTR   RI,RI
         BZ    PJEXITC                 NEVER WAS OPENED.
         CLI   DCMDEL,R            CONSOLE IN ROLL MODE
         BNE   NOROLL              NO SKIP EXIT
         OI    DCMTIMES,DCMOPTTI+CLOSERET TI RESET TIMER AND RETURN CLO
         L     XFBRANCH,DCMNTIMR   LOAD TIMER INTERPRETER ADDRESS
         L     X1PARM,DCMCXSVE     RESTORE CXSA ADDRESS
         BR    XFBRANCH            EXIT TO IEECVETK
NOROLL   EQU   *
         USING IHADCB,RI
         MVI   DMX4,DMX4C          INDICATE END OF REMOTE PARM LIST
         L     XFBRANCH,CSAXC      ADDRESS FREELOCK SUBROUTINE
         LR    X1PARM,RXS          CXSA ADDRESS IN REG 1
         BALR  XERTRN,XFBRANCH     RELEASE LOCKS FOR SVC'S
         LA    R1,DMX4
         CLOSE   ((RI)),MF=(E,(1))
         SPACE
*                                      PREPARE REMOTE LIST.
*                                      NO MORE DCB.
         LR    R1,RI
      FREEMAIN R,SP=255,LV=DMCORE,A=(1)
         DROP  RI
PJEXITC  EQU   *
         L     XFBRANCH,CSAXB      ADDRESS GETLOCK SUBROUTINE
         LR    X1PARM,RXS          PASS CXSA ADDRESS IN REG 1
         BALR  XERTRN,XFBRANCH     OBTAIN LOCKS TO RESET UCM
         XC    UCMECB,UCMECB           CLEAR ECB.
         XC    UCMDCB,UCMDCB           CLEAR DCB PTR.
         NI    UCMSTS,N255-UCMBF-UCMCF-UCMAF-UCMTA    NOT BUSY @ZA25731
*                                  CLOSED, NO ATTN, NOT OPEN   @ZA25731
         NI    UCMDEVC,N255-UCMDEVA-UCMDEVD    INDICATE SCREEN NOT FULL
*                                  AND NO DOM PENDING
         L     RDBASE,UCMXB        RES DCM                  JE @ZA02303
         USING DCMTSRT,RDBASE                               JE @ZA02303
         NI    DCMRFLGS,N255-DCMDOM TURN OFF DOM DONE       JE @ZA02303
         DROP  RDBASE                                       JE @ZA02303
         SPACE
         L     RTC,UCMUCB              GET TO ASSOCIATED UCB        MCS
         USING UCBOB,RTC
         TM    UCBSTAT,UCBCHGS
         BZ    DEVSTAT
         NIL   UCBSTAT,N255-UCBONLI-UCBCHGS-UCBALOC-UCBSYSR-UCBDADI,REF*
               =UCBOB
         B     CONTINUE
DEVSTAT  NIL   UCBSTAT,N255-UCBALOC-UCBSYSR-UCBDADI,REF=UCBOB
CONTINUE NI    UCMATR,N255-UCMAT04-UCMUF
         L     RTC,UCBEXTPT        BASE FOR COMMON EXTENSION  MB YM4087
         USING UCBCMEXT,RTC        ADDRESSABILITY             MB YM4087
         MVC   UCBATI(N1),DCMATI   RESTORE ATTENTION INDEX    MB YM4087
         L     RTC,UCMUCB          BASE FOR UCB               MB YM4087
         USING UCBOB,RTC           ADDRESSABILITY TO UCB      MB YM4087
         XC    UCBSNS,UCBSNS       CLEAR SENSE INDICATORS     MB YM5671
         CLI   N19(RTC),TYP2250    IS DEVICE A 2250
         BNE   THATSIT             NO WERE FINISHED
         CLI   N16(RTC),MOD3       IS 2250 A MOD3
         BNE   THATSIT             NO EXIT
         SR    RI,RI                   CLEAR REG
         L     RTB,DCMAWARN            GET WARNING LINE ADDRESS
         STH   RI,N76(RTB)             UPDATE BUFFER ADDRESS
         L     RTB,DCMBUFAD        POINT TO BUFFER ADDRESS TABLE
         LH    RXE,N0(RTB)         GET MOD 3 BUFFER ADDRESS
         LA    RTB,N10(RTB)       POINT TO LAST TABLE ENTRY      S21003
BUFFLOOP LH    RI,N0(RTB)         GET MOD 3 ADDRESS              S21003
         SR    RI,RXE             SUBTRACT TO RESET ADDRESS      S21003
         STH   RI,N0(RTB)         UPDATE ADDRESS                 S21003
         BCTR  RTB,N0             REDUCE POINTER                 S21003
         BCTR  RTB,N0                BY TWO                      S21003
         C     RTB,DCMBUFAD       IS THIS FIRST ENTRY            S21003
         BNL   BUFFLOOP            NO, UPDATE NEXT ENTRY         S21003
*                                  YES, CONTINUE                 S21003
         SPACE 2                                                 S21003
THATSIT  EQU   *                                                 S21003
         NI    UCMATR,N255-UCMUF        TURN OFF ACTIVE FLAG        MCS
         L     XFBRANCH,DCMNCLN    LOAD CLEANUP ADDRESS
         L     X1PARM,DCMCXSVE     RESTORE CXSA ADDRESS
         BR    XFBRANCH            EXIT TO IEECVFTG
         DROP  RP
         EJECT
*
*                       OPEN 1052 FOR EXCP.
*
PJOPEN   EQU   *
         L     RTC,UCMUCB          GET UCB ADDRESS
         TM    SRTESTAT,CONSL IS DEVICE IN ALLOCATION       JE @ZA02347
         BNO   GETOUT              RETURN DIRECTLY          JE @ZA02347
         L     XFBRANCH,CSAXC      ADDRESS FREELOCK SUBROUTINE
         LR    X1PARM,RXS          CXSA ADDRESS IN REG 1
         BALR  XERTRN,XFBRANCH     RELEASE LOCKS FOR SVC'S
       GETMAIN R,SP=255,LV=DMCORE
         LR    RD,R1
         MVC   N0(DMCORE,RD),DMDCB
         USING IHADCB,RD
*                                      RELOCATE POINTERS.
         ST    RD,UCMDCB               PT UCM AT BLOCKS.      JE YM6718
         SR    RXI,RXI
         LA    RXE,DMIOB
         MVI   DMCREAD,N0            *** GRAPHICS BYTE
         ST    RXE,DCBIOBAD
         ST    RD,DMIDCB
         LA    RXE,DMCNOP
         STCM  RXE,M7,DMCTRA+N1
         EJECT
         GETMAIN R,SP=255,LV=PNCORE
         LR    RP,R1                   BASE OPEN BLOCKS.
         USING PNOPEND,RP
         XC    PNOPEND(PNCORE),PNOPEND
         SR    R1,R1               CLEAR FOR IC INSTRUCTION
         IC    R1,UCMID            GET CONSOLE I.D. IN BINARY
         CVD   R1,PNWORKD          CONVERT TO PACKED DECIMAL
         UNPK  PNWORKD(N5),PNWORKH4(N3)
         MVC   DCBDDNAM(L'BASENAM),BASENAM CONSTANT PART OF DDNAME
         MVC   DCBDDNAM+L'BASENAM(N3),PNWORKD REST OF NAME
         MVC   JFCBDSNM(L'PNDSN),PNDSN
         MVC   JFCBDSNM+8(36),JFCBDSNM+7
         OI    JFCBTSDM,JFCTTR+JFCNWRIT+JFCNDSCB+JFCNDCB+JFCPAT
         LA    RTB,JFCB
         STCM  RTB,M7,PNXLST+N1
         OI    PNXLST,LASTNTRY+EXITJFCB
         LA    RTB,PNXLST
         STCM  RTB,M7,DCBEXLST+N1
         OI    PNJEF,LASTNTRY      END OF OPEN-J LIST         MB YM3194
         EJECT
         OPEN  ((RD),INOUT),TYPE=J,MF=(E,PNJEF)
         LR    R1,RP                        OPEN BLOCKS PTR.
         FREEMAIN R,SP=255,LV=PNCORE,A=(1)
         DROP  RP
OPNRECV  DS    0H                                             JE YM6718
         XC    DCBEXLST+N1(N3),DCBEXLST+N1
         L     XFBRANCH,CSAXB      ADDRESS GETLOCK SUBROUTINE
         LR    X1PARM,RXS          PASS CXSA ADDRESS IN REG 1
         BALR  XERTRN,XFBRANCH     OBTAIN LOCKS TO RESET UCM
         L     RDBASE,UCMXB        RES DCM
         USING DCMTSRT,RDBASE
         L     RDBASE,DCMADTRN     PAGEABLE DCM
         DROP  RDBASE
         USING DCMSTRT,RDBASE
         L     RTC,UCBEXTPT        UCB COMMON EXTENSION       MB YM4087
         USING UCBCMEXT,RTC        ADDRESSABILITY             MB YM4087
         MVC   DCMATI(N1),UCBATI   SAVE ATTENTION INDEX       MB YM4087
         MVI   UCBATI,X04          COMM TASK ATTN HANDLER     MB YM4087
         L     RTC,UCMUCB          RESET UCB BASE             MB YM4087
         USING UCBOB,RTC           AND ADDRESSABILITY         MB YM4087
         XC    UCBSNS,UCBSNS       CLEAR SENSE INDICATORS     MB YM5671
         MVI   DCBIFLGS,N0           USE STD ERP                  MCS
         OI    UCMATR,UCMUF            TURN ON ACTIVE BIT           MCS
         NI    UCMSTS,N255-UCMTA   TURN OFF OPEN PENDING
         SR    XFBRANCH,XFBRANCH   CLEAR REG
         IC    XFBRANCH,DCMIONDX   GET IO ROUTINE CODE
         L     XFBRANCH,IOTAB(XFBRANCH) ADDRESS IO ROUTINE
         ST    XFBRANCH,DCMIORTN   PUT IT IN DCM
         MVC   DCMNPRZ(TABLEN),TABLE MOVE IN ADCON TABLE
         L     RP,DCMADCHP         GET CHANNEL PROGRAM ADDRESS   S21003
         ST    RP,DMICPA           POINT IOB AT CHANNEL PROGRAM
         ST    RU,DMIECBP              PT IOB AT ECB.
*                            PT WAIT AT ECB THRU EIL.
         SPACE
*                      TWO LINES                               @ZA16376
*                                  DELETED HERE                @ZA16376
         SPACE
         LR    RPL,RUCM            PTR TO MCS PREFIX TO UCM IS @ZA16376
         SH    RPL,FOUR            FOUR BYTES BEFORE THE UCM
         L     RPL,N0(RPL)             GET PTR TO PREFIX
         USING UCMPRFX,RPL
         TM    UCMSFLG2,UCMSYSL        IS AUTO SWITCH REQUESTING OPEN
         DROP  RPL
         BNO   NONROUTR                NO, RETURN TO RPOC MODULE
         TM    DCBOFLGS,DCBOFOPN   DID DCB OPEN               JE YM6718
         BO    NONROUTR            YES, CONTINUE              JE YM6718
EXIT2    LR    X1PARM,RXS          PASS CXSA ADDRESS IN REG 1 MB YM4087
         L     XFBRANCH,CSAXC      FREELOCK ADDRESS           MB YM4087
         BALR  XERTRN,XFBRANCH     RELEASE LOCKS TO SWITCH    MB YM4087
         ICM   RTC,15,CSANPTR      GET RECOVERY AREA          JE YM6718
         BZ    NOESTAE1            NOT THERE, BYPASS NSI      JE YM6718
         MVC   PARMRTAD-PARMLIST(8,RTC),CSANAME RESTORE       JE YM6718
NOESTAE1 DS    0H                                             JE YM6718
         MVI   CSACODE,CSASWTCH    SET CONSOLE SWITCH         JE YM6718
         MVC   CSANAME+N3(N2),PMXTRNL  ADJUST CSA NAME
         L     XFBRANCH,UCMSWCH    GET CONSOLE SWITCH ENTRY ADDR
EXIT     EQU   *                                              MB YM4087
         L     XERTRN,CSAXA        PROVIDE RETURN ADDRESS
         LR    X1PARM,RXS          ALSO, CXSA ADDRESS
         NI    DCMMCSST,X'FF'-DCMDUSE DIDOCS NOT IN CONTROL
         BR    XFBRANCH            EXIT TO CONSOLE SWITCH
NONROUTR EQU   *
         L     XFBRANCH,DCMNPROC   LOAD PROC 1 ADDRESS
         L     X1PARM,DCMCXSVE     RESTORE CXSA ADDRESS
         BR    XFBRANCH            EXIT TO IEECVET1
GETOUT   LR    RDBASE,RP           SET DCM BASE               MB YM4087
         ICM   RTC,15,CSANPTR      GET RECOVERY AREA          JE YM6718
         BZ    NOESTAE2            NOT THERE, BYPASS NSI      JE YM6718
         MVC   PARMRTAD-PARMLIST(8,RTC),CSANAME RESTORE       JE YM6718
NOESTAE2 DS    0H                                             JE YM6718
         L     XFBRANCH,CSAXC      FREELOCK ADDRESS           MB YM4087
         B     EXIT                RELEASE LOCKS AND LEAVE    MB YM4087
*
OPENRCVY DS    0H                                             JE YM6718
         ICM   RD,15,UCMDCB        GET DCB FROM UCM           JE YM6718
         BZ    EXIT2               NEVER THERE, GET OUT       JE YM6718
         B     OPNRECV             THERE, GO RECOVER          JE YM6718
*
*        EQUATES
*
ID1      EQU   C'E'                1ST CHARACTER OF CSECT ID
ID2      EQU   C'G'                2ND CHARACTER OF CSECT ID
M7       EQU   7                   STORE CHARACTER MASK
X04      EQU   X'04'               04 F/R ATTENTION TABLE
X80      EQU   X'80'               UCB STATUS BIT
XF0      EQU   X'F0'
CONSL    EQU   X'02'               UCB STATUS BIT           JE @ZA02347
MOD3     EQU   X'33'               MOD 3 MASK
P        EQU   C'P'                CONSTANT
R        EQU   C'R'                ROLL MODE INDICATOR
C6       EQU   C'6'               PROC 0 NAME CHARACTER          S21003
CLOSERET EQU   2                   TIMER INT RETURN TO CLOSE
OPENBIT  EQU   X'10'                   INDIC. OPEN FOR AUTO SWITCH
N0       EQU   0                        NUMBER
N1       EQU   1                        NUMBER
N2       EQU   2                   NUMBER
N3       EQU   3                        NUMBER
N4       EQU   4                        NUMBER
N5       EQU   5
N6       EQU   6
N8       EQU   8                        NUMBER
N9       EQU   9                        NUMBER
N10      EQU   10                  NUMBER
N11      EQU   11                  FIELD INCREMENT
N12      EQU   12                       NUMBER
N16      EQU   16                  CONSTANT
N19      EQU   19
N22      EQU   22                  UCB SENSE DISPLACEMENT     MH Y02173
N23      EQU   23                  UCB DISPLACEMENT           ME S21003
N44      EQU   44                       NUMBER
N76      EQU   76                      NUMBER
N176     EQU   176                      NUMBER
N255     EQU   255                      NUMBER
TYP2250  EQU   2
LASTNTRY EQU   X'80'                    END OF LIST BIT
EXITJFCB EQU   X'07'               JFCB EXIT LIST FLAGS
RECOVBIT EQU   X'0C'                    LOCAL ERROR RECOVERY
TERMBIT  EQU   X'80'                    TERMINAL FLAG CLOSE
REWRSTOP EQU   X'FF'                    REWRITE STOP
CVTPTR   EQU   16                      LOCATION OF CVT POINTER
CVTTCBP  EQU   0                       ADDR OF A DOUBLE WORD, THE FIRST
*                            CONTAINING THE NEXT-TO BE- DISPATCHED TCB
*                            ADDR, THE SECOND CONTAINING THE CURRENT
*                            TCB ADDR
FOUR     DC    H'4'                    NUMBER
         DS    0F
IOTAB    EQU   *-4
         DC    V(IEECVETH)         3066 ROUTINE, IOTAB+4
         DC    V(IEECVETP)         2250 ROUTINE, IOTAB+8
         DC    V(IEECVETR)         2260 ROUTINE, IOTAB+12
         DC    V(IEECVETU)         3277 ROUTINE, IOTAB+16
TABLE    EQU   *                   ADCON TABLE
         DC    V(IEECVFT1)
         DC    V(IEECVET1)
         DC    V(IEECVET2)
         DC    V(IEECVET3)
         DC    V(IEECVFT2)
         DC    V(IEECVET4)
         DC    V(IEECVET6)
         DC    V(IEECVET7)
         DC    V(IEECVET8)
         DC    V(IEECVET9)
         DC    V(IEECVETA)
         DC    V(IEECVFTA)
         DC    V(IEECVFTB)
         DC    V(IEECVETC)
         DC    V(IEECVETD)
         DC    V(IEECVETE)
         DC    V(IEECVFTD)
         DC    V(IEECVETF)
         DC    V(IEECVETG)
         DC    V(IEECVFTG)
         DC    V(IEECVETJ)
         DC    V(IEECVETK)
         DC    V(IEECVFTL)
         DC    V(IEECVFTM)
         DC    V(IEECVFTN)
         DC    V(IEECVFTO)
         DC    V(IEECVFTP)
         DC    V(IEECVFTQ)
         DC    V(IEECVFTT)
TABLEN   EQU   *-TABLE
BASENAM  DC    C'IEAVM'            CONSTANT PART OF CONSOLE DDNAME
PMXTRNL  DC    CL2'04'             EXTERNAL PROCESSOR NAME.
PNDSN    DC    C'CONSOLE '         DSNAME FOR JFCB
         DS    0D                  ALIGN TO DOUBLE WORD
         TITLE 'PN  DATA CONTROL BLOCKS'
         IEEUCDX
         TITLE 'PJ OPEN-JFCB'
*                       DEFINITION OF DYNAMIC CORE FOR CONSOLE OPEN,
PNOPEND  DSECT
JFCB     DS    0D
         IEFJFCBN ,
*                            OPENJ E-FORM SLOT.
PNJEF    DC    X'80'              LAST ENTRY.
         DC    AL3(0)             DCB PTR.
         SPACE
*                            OPENJ JFCB EXLST PTR.
PNXLST   DC    X'87'              LAST ENTRY AND JFCB 07 CODE.
         DC    AL3(0)                   JFCB PTR. DCBEXLST PTS HERE.
PNWORKD  DC    D'0'                DOUBLE WORD FOR CVD
         ORG   PNWORKD+6
PNWORKH4 DC    H'0'                LAST HALFWORD
PNWORKX  DC    X'0'                EXTRA BYTE FOR UNPK
PNCORE   EQU   *-PNOPEND            DYNAMIC CORE REQIREMENT.
         EJECT
         IHACTM FTPT                                          JE YM6718
         TITLE 'IEECVETG    OPEN/CLOSE   UCB'
UCBD     DSECT
         IEFUCBOB
         TITLE 'IEECVETG    OPEN/CLOSE   CXSA'
         IHACTM  CXSA
         TITLE 'IEECVETG    OPEN/CLOSE   RESIDENT DCM'
         IEERDCM
         TITLE 'IEECVETG    OPEN/CLOSE   PAGEABLE DCM'
         IEETDCM
         TITLE 'PJ UCM'
         IEECUCM  FORMAT=NEW
         END
