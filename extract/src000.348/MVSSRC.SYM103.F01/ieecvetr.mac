* /* START OF SPECIFICATIONS ****
*02*  PROCESSOR = ASSEMBLER;
**** END OF SPECIFICATIONS ***/
IEECVETR CSECT
*A051100,134600,236600-236900                                 MH Y02158
*C126000,144500,150000                                        MH Y02158
*A111600                                                      MH Y01563
* A 45100-45300,75100-75300,75600,101600-101800,102600        MC YM4102
         TITLE 'IEECVETR IGC5R07B 2260 IO'
*                                                                     *
* STATUS       CHANGE LEVEL=0                                         *
*                                                                     *
* FUNCTION     ANALYZE COMMUNICATIONS BYTES AND PERFORM REQUESTED     *
*              I/O. RETURN INDICATION THAT I/O IS IN PROGRESS         *
*                                                                     *
* INPUT        BITS SET IN DISPLAY CONTROL MODULE                     *
*                                                                     *
* OUTPUT       I/O OPERATION PERFORMED                                *
*                                                                     *
* EXTERNAL REFERENCES NONE
*
* EXITS - NORMAL IEECVET1 PROCESSOR ROUTINE
*                IEECVETF LIGHTPEN/CURSOR ROUTINE
*                IEECVFTR 2260 I/O 2                             S21003
*       - ERROR  NONE
*
* TABLES/WORK AREAS DISPLAY CONTROL MODULE (CONTAINED IN DSECT)
*
* ATTRIBUTES   REFRESHABLE, PRIVILEGED, TYPE 4 SVC
*
* NOTES        THE OPERATION OF THIS MODULE DOES NOT DEPEND UPON A
*              PARTICULAR INTERNAL REPRESENTATION OF THE EXTERNAL
*              CHARACTER SET.
*                                                                     *
         EJECT
**                                                                   **
*              INITIALIZE PROGRAM                                     *
**                                                                   **
         BALR  PBASE,N0            SET UP
         USING *,PBASE             ADDRESSABILITY
         B     ICATCH              BRANCH AROUND MODULE NAME  MC YM4102
         DC    C'   MODULE IGC5R07B, FICHE IEECVETR  '        MC YM4102
ZAPAREA  DC    12C'ZAP'            FIELD MODIFICATION         MC YM4102
ICATCH   LR    PXSA,PARM           SAVE CXSA ADDRESS             S21003
         USING CXSA,PXSA           DECLARE CXSA ADDRESS
         L     UCMENTRY,CSAUCM     GET UCM ENTRY ADDRESS
         USING UCMLIST,UCMENTRY    DECLARE UCMENTRY BASE
         L     RBASE,UCMXB         LOAD R DCM BASE               S21003
         USING DCMTSRT,RBASE          ESTABLISH ADDRESSABILITY
         L     DCMBASE,N0(RBASE)   LOAD T DCM BASE               S21003
         USING DCMSTRT,DCMBASE       DECLARE DCM BASE
         L     UCMBASE,CSACTLM     GET UCM BASE ADDRESS      MD
         USING UCM,UCMBASE         DECLARE ADDRESS           MD
         MVC   DCMTRACE(DCMTRLEN),DCMTRAC2 SHIFT OLD TRACE ENTRIES
         MVI   DCMTREN1,ID1        PUT CSECT ID INTO
         MVI   DCMTREN2,ID2        NEW TRACE ENTRY
         L     XFBRANCH,CSAXC      ADDRESS FREELOCK SUBROUTINE
         LR    X1PARM,PXSA         PASS CXSA ADDRESS IN REG 1
         BALR  XERTRN,XFBRANCH     RELEASE LOCKS
         L     CCWPTR,DCMADCHP     POINT TO CHANNEL PROGRAM
         MVI   DCMLNEA,NF9         INIT EA LINE DATA CHAR     MH Y02158
         EJECT
**                                                                   **
*       TEST REASON FOR ENTRY AND TAKE APPROIATE ACTION               *
**                                                                   **
TESTXX   TM    DCMR3FLG,DCMSTSWT   IS STATUS SWITCH REQUIRED     S21003
         BNO   TESTXY              NO,CONTINUE WITH NEXT TEST
         L     XFBRANCH,VCONIO2    LOAD SECOND LOAD ADDRESS
         B     EXIT2               EXIT TO IEECVFTR
         SPACE 5                                                 S21003
TESTXY   TM    UCMDISP,UCMDISPF+UCMDISPG IS IT OUTPUT ONLY STAT  S21003
         BZ    TEST00               NO, CONTINUE                 S21003
         NI    DCMIOCM1,X'FF'-DCMDORMI-DCMWRINS-DCMWRENT  YES,   S21003
         NI    DCMIOCM2,X'FF'-DCMBLENT-DCMINSSH    TURN OFF BITS S21003
*                                                                S21003
TEST00   EQU   *        THIS CODE CHECKS FOR RMI REQUEST         S21003
*                                                                S21003
         TM    DCMIOCM1,DCMDORMI       TEST SWITCH FOR RMI
         BZ    TEST01                  NO   BRANCH
         OI    DCMCOM1,DCMCOMRM        IND RMI PERFORMED
         OI    DCMIOUNQ,DCMIO226       IND 2260 TO BE RENT FOR FNDCUR
         LA    DATA,DCMENTRY           LOAD DATA ADDR
         LA    CODE,RMICODE            LOAD CHAN COMMAND         S21003
         MVI   N4(CCWPTR),SURPDATA     SET CHAIN INDICATOR
         LH    LENGTH,SCRLEN           LOAD LENGTH
         BAL   LINKREG,BUILDCCW        GO TO BUILD CCW           S21003
         B     EXCP                    GO TO EXEC CHAN PROG
*                                                                     *
TEST01   EQU   *       THIS CODE PROCESSES FIND CURSOR
*                                                                     *
         TM    DCMIOUNQ,DCMIO226       FIND CURSOR
         BZ    TEST02                  NO    BRANCH
         NI    DCMIOUNQ,N255-DCMIO226  SET OFF SWITCH
         L     WORK,UCMDCB             GET DCB ADDRESS
         L     WORK,N28(WORK)          GET IOB ADDR
         CLC   N14(N2,WORK),SCRLEN     TEST IOBCOUNT FIELD
         BL    EAREA                   NOT FREEZE OR CANCEL  BRANCH
         MVC   DCMCULNO(N2),XCOMP      INITIALIZE CURSOR LOCATION
         TM    DCMIOUNQ,DCMFRSCN       IS OUTPUT IN HOLD MODE
         BZ    STOPOUT                 NO  FREEZE IT
         OI    DCMCOM1,DCMCANCL        INDICATE CANCEL
         L     XFBRANCH,DCMNCMD1   LAOD COMMAND ROUTINE ADDRESS
         B     EXIT                EXIT TO IEECVET4
STOPOUT  OI    DCMIOUNQ,DCMFRSCN       IND SCREEN IN HOLD MODE
         CLI   DCMRQDEL,BLANK          IS A DEKETE REQUEST PENDING
         BNE   OK                      YES
         MVI   DCMRQDEL,CQ             MAKE IT LOOK AS ONE IS PENDING
OK       EQU   *             DELETE ESSENTIALLY PENDING
         TM    DCMR2FLG,DCMRXSFL   IS SCREEN FULL             MC YM4102
         BO    OK3                 YES, DON'T SIMULATE        MC YM4102
         OI    DCMR3FLG,DCMRXHMT   SIMULATE FULL SCREEN       MC YM4102
         OI    DCMR2FLG,DCMRXSFL       SET ON SWITCH
OK3      EQU   *                   CONTINUE                   MC YM4102
         OI    UCMDEVC,UCMDEVA         SET ON SWITCH
         MVI   DCMENTRY,BLANK      PUT BLANK IN ENTRY AREA
         MVC   DCMENTRY+N1(N159),DCMENTRY  BLANK REST OF ENTRY AREA
         MVC   DCMENTRY+N132(N27),MESSAGE      MOVE WARNING INTO EA
         OI    DCMIOCM1,DCMWRENT       IND WRITE ENTRY AREA
         B     TEST02                  BRANCH
EAREA    CLC   N14(N2,WORK),XCITE      TEST IOBCOUNT FIELD
         BL    NOTINEA                 NOT IN ENTRY AREA  BRANCH
         OI    DCMCOM1,DCMIOPRD        INDICATE READ PERFORMED
         LA    DATA,DCMENTRY           LOAD DATA ADDRESS
         LA    CODE,READ               LOAD COMMAND CODE         S21003
         MVI   N4(CCWPTR),DUMCHAIN     SET CHAIN INDICATOR
         LH    LENGTH,SURPLEN          LOAD LENGTH
         BAL   LINKREG,BUILDCCW        GO TO BUILD CCW           S21003
         LH    LENGTH,ENTLEN           LOAD LENGTH
         BAL   LINKREG,BLDCCWE         GO TO BUILD CCW           S21003
         B     EXCP                    GO TO EXEC CHAN PROGRAM
NOTINEA  SR    CHAR,CHAR               ZERO REGISTER
         LH    LINE,SCRLEN             LOAD NUMBER CHARS/DISPLAY
         SH    LINE,N14(WORK)          SUBTRACT RESIDUAL COUNT
         LH    WORK,DCMLGNTH           LOAD NUMBER CHARS/LINE    S21003
         DR    CHAR,WORK               COMPUTE INIT LINE/CHAR
         BCTR  WORK,N0                 DECREMENT FOR COMPARE
         CR    CHAR,WORK               TEST FOR MAX CHAR POS
         BL    NOTMAX                  BRANCH IF NOT MAX
         SR    CHAR,CHAR               ZERO REGISTER
         LA    LINE,N1(LINE)           INCREMENT LINE COUNT
GOBACK   LA    LINE,LINENO(LINE)       TRANSLATE LINE NUMBER
         MVC   DCMDSAV(N1),N0(LINE)     STORE LINE NUMBER        S21003
         STC   CHAR,DCMDSAV+N1         STORE CHAR POSITION
         L     XFBRANCH,DCMNLPCR   LOAD LIGHT-PEN/CURSOR ADDRESS
         B     EXIT                EXIT TO IEECVETF
NOTMAX   LA    CHAR,N1(CHAR)           INCREMENT CHAR COUNT
         B     GOBACK                  BRANCH TO STORE LINE/CHAR
*                                                                     *
TEST02   EQU   *        THIS CODE CHECKS FOR AN ERASE CONDITION       *
*                                                                     *
         TM    DCMIOCM2,DCMERASE       ERASE SCREEN
         BZ    TEST03                  NO    BRANCH
         LA    CODE,ERASE              LOAD CODE              MH S21003
         LA    LENGTH,LENTWO           LOAD LENGTH            MH S21003
         BAL   LINKREG,BLDCCWE         GO TO BUILD CCW           S21003
         B     EXCP                    GO TO EXEC CHAN PROG
*                                                                     *
TEST03   EQU   *        THIS CODE BLANKS ENTRY AREA UPON REQUEST      *
*                                                                     *
         TM    DCMIOCM2,DCMBLENT       BLANK ENTRY AREA
         BZ    TEST04                  NO    BRANCH
         CLI   DCMRQDEL,CQ             DID IO ROUTINE MOVE CHAR IN
         BNE   OK1                     NO
         MVI   DCMRQDEL,BLANK          YES MOVE BLANK INTO DEL REQ
OK1      EQU   *             DELETE ESSENTIALLY NOT PENDING
         TM    DCMR3FLG,DCMRXHMT   FULL SCREEN SIMULATED      MC YM4102
         BZ    OK2                 NO, BRANCH                 MC YM4102
         NI    DCMR3FLG,N255-DCMRXHMT   SIM FLAG OFF          MC YM4102
         NI    UCMDEVC,N255-UCMDEVA     SET OFF SWITCH
         NI    DCMR2FLG,N255-DCMRXSFL   SET OFF SWITCH
OK2      EQU   *                   CONTINUE                   MC YM4102
         NI    DCMIOUNQ,N255-DCMFRSCN  SET OFF FREEZE SWITCH
         MVI   DCMENTRY,BLANK          BLANK ENTRY AREA
         MVC   DCMENTRY+N1(N159),DCMENTRY   BLANK ENTRY AREA
*                                                                     *
TEST04   EQU   *        THIS CODE BLANKS LEFT HALF OF WARNING         *
*                                                                     *
         TM    DCMIOCM2,DCMBLWRL       BLANK LEFT HALF WARNING
         BZ    TEST05                  NO   BRANCH
         MVI   DCMWARN,BLANK           BLANK LEFT HALF WARNING
         MVC   DCMWARN+N1(N39),DCMWARN BLANK LEFT HALF WARNING
*                                                                     *
TEST05   EQU   *        THIS CODE BLANKS RIGHT HALF OF WARNING        *
*                                                                     *
         TM    DCMIOCM2,DCMBLWRR       BLANK RIGHT HALF WARNING
         BZ    TEST05A                 NO  BRANCH
         MVI   DCMWARN+N40,BLANK       BLANK RIGHT HALF WARNING
         MVC   DCMWARN+N41(N39),DCMWARN+N40      BLANK RIGHT HALF WARN
         NI    DCMR2FLG,N255-DCMRXUNV   SET OFF SAME RDCM BIT    S21003
         NI    DCMCOM3,X'FF'-DCMOLUNV AND TDCM BIT            MH Y01563
*                                                                     *
TEST05A  EQU   *        THIS CODE CONTROLS INSTRUCTION LINE           *
*                                                                     *
         TM    DCMIOCM2,DCMINSSH       BLANK INSTRUCTION LINE
         BZ    TEST05B                 NO    BRANCH
         MVI   DCMINSTR,BLANK               BLANK
         MVC   DCMINSTR+N1(N79),DCMINSTR    INSTRUCTION LINE
         OI    DCMIOCM1,DCMWRINS       IND WRITE INSTRUCTION LINE
*                                                                     *
*        THIS CODE MOVES MESSAGE TO WARNING LINE                      *
*                                                                     *
TEST05B  TM    DCMCMSG1,DCMMSGWT       MOVE MSG TO WARNING LINE
         BZ    TEST06                  NO  BRANCH
         NI    DCMCMSG1,N255-DCMMSGWT  SET OFF SWITCH
         MVI   DCMWARN+N40,BLANK                 BLANK RIGHT HALF
         MVC   DCMWARN+N41(N39),DCMWARN+N40      OF WARNING LINE
         MVC   DCMWARN+N57(N23),HOLDMSG     MOVE HOLD MSG TO WARN LINE
*                                                                     *
TEST06   EQU   *        THIS CODE CAUSES WRITING OF MSG AREA          *
*                                                                     *
         TM    DCMIOCM1,DCMWRMSG        WRITE MESSAGE AREA       S21003
         BZ    TEST06AA                NO  BRANCH
         NI    DCMIOCM1,X'FF'-DCMWRPAR  DO NOT WRITE PARTIAL     S21003
         L     LENGTH,DCMWTBUF         ADDR OF I D FIRST LINE
         L     DATA,DCMASCRN           LOAD ADDR OF INFO DISPLAY
         SR    LENGTH,DATA             COMPUTE LENGTH
         BZ    TEST06AA                YES   GO TO NEXT TEST
         STH   LENGTH,MSGLEN           SAVE LENGTH
         MVI   DCMLNE+N1,NF0           INIT FOR LINE WRITE    MH Y02158
         LA    DATA,DCMLNE+N1          LOAD LINE ADDRESS
         BAL   LINKREG,BLDCCWD1        GO TO BUILD CCW           S21003
         LA    DATA,DCMMFRST           LOAD DATA ADDRESS
         LH    LENGTH,MSGLEN           LOAD LENGTH
         BAL   LINKREG,BLDCCWC         GO TO BUILD CCW           S21003
*                                                                     *
TEST06AA EQU   *        THIS CODE CAUSES WRITING OF INFO DISPLAY      *
*                         AND PARTIAL SCREEN.                    S21003
*                                                                     *
         TM    DCMIOCM2,DCMWINFD       WRITE I D
         BO    WRITID               YES, DO IT                   S21003
         TM    DCMIOCM1,DCMWRPAR   IS WRITE PARTIAL REQUIRED     S21003
         BZ    TEST06A                 NO  BRANCH
WRITID   SR    RSAVE,RSAVE         CLEAR REGISTER                S21003
         IC    RSAVE,DCMLINEN         LOAD LINE ADDRESS          S21003
         BCTR  RSAVE,N0               DECREMENT BY ONE           S21003
         STC   RSAVE,DCMLNE+N3        STORE FOR CHANNEL PROGRAM  S21003
         OI    DCMLNE+N3,NF0       CONVERT TO VALID LINE ADDR MH Y02158
         LA    DATA,DCMLNE+N3          LOAD DATA ADDR
         BAL   LINKREG,BLDCCWD1        GO TO BUILD CCW           S21003
         TM    DCMIOCM1,DCMWRPAR       IS THIS WRITE PARTIAL     S21003
         BZ    WRINFO               NO, MUST BE WRITE INFO       S21003
         MH    RSAVE,DCMCORLN     MULT LINE NO. BY LINE LENGTH   S21003
         L     DATA,DCMASCRN       POINT TO START OF SCREEN      S21003
         AR    DATA,RSAVE         ADD INDEX VALUE                S21003
         LH    LENGTH,DCMBYTCT     GET BYTE COUNT                S21003
         BAL   LINKREG,BLDCCWC     GO BUILD CCW                  S21003
         NI    DCMIOCM1,X'FF'-DCMWRPAR  TURN OFF WRITE PARTIAL   S21003
         B     TEST06AA            TEST FOR INFO DISP            S21003
WRINFO   LA    DATA,DCMINSTR           LOAD DATA ADDR            S21003
         LH    LENGTH,DCMBYTCT         LOAD LENGTH
         BAL   LINKREG,BLDCCWC         GO TO BUILD CCW           S21003
*                                                                     *
TEST06A  EQU   *        THIS CODE WRITES INSTRUCTION LINE             *
*                                                                     *
         TM    DCMIOCM1,DCMWRINS       WRITE INSTRUCTION LINE
         BZ    TEST07                  NO  BRANCH
         MVI   DCMLNE+N2,NF8           INIT FOR WRITE LINE
         LA    DATA,DCMLNE+N2          LOAD DATA ADDRESS
         BAL   LINKREG,BLDCCWD1        GO TO BUILD CCW           S21003
         LA    DATA,DCMINSTR           LOAD DATA ADDRESS
         LH    LENGTH,DCMCORLN         LOAD LENGTH               S21003
         BAL   LINKREG,BLDCCWC         GO TO BUILD CCW           S21003
*                                                                     *
TEST07   EQU   *        THIS CODE CAUSES WRITING OF WARNING LINE      *
*                                                                     *
         TM    DCMIOCM1,DCMWRWRN       WRITE WARNING
         BZ    TEST08                  NO   BRANCH
         MVI   DCMLNE,NFB              LINE POS TO WRITE WARNING
         LA    DATA,DCMLNE             LOAD DATA ADDR (LINE NUMBER)
         BAL   LINKREG,BLDCCWD1        GO TO BUILD CCW           S21003
         LA    DATA,DCMWARN            LOAD DATA ADDRESS
         LH    LENGTH,DCMCORLN         LOAD LENGTH               S21003
         BAL   LINKREG,BLDCCWC         GO TO BUILD CCW           S21003
*                                                                     *
TEST08   EQU   *        THIS CODE CAUSES WRITING OF ENTRY AREA        *
*                                                                     *
         TM    DCMIOCM1,DCMWRENT       WRITE ENTRY AREA
         BZ    TEST09                  NO   BRANCH
         LA    DATA,DCMLNEA            LOAD DATA ADDR (LINE NUMBER)
         LA    CODE,WRITEL             LOAD CODE                 S21003
         LA    LENGTH,LENTWO           LOAD LENGTH               S21003
         BAL   LINKREG,BLDCCWD         GO TO BUILD CCW           S21003
         LA    DATA,DCMENTRY           LOAD DATA ADDR
         LH    LENGTH,ENTLEN           LOAD LENGTH
         BAL   LINKREG,BLDCCWC         GO TO BUILD CCW           S21003
*                                                                     *
TEST09   EQU   *        THIS CODE POSITIONS SOM AND CURSOR IN EA      *
*                                                                     *
         TM    UCMDISP,UCMDISPF+UCMDISPG IS IT OUTPUT ONLY STAT  S21003
         BNZ   NOP                  YES, DON'T INSERT CURSOR     S21003
         LA    DATA,DCMLNEA            LOAD DATA ADDR
         LA    CODE,WRITEL             LOAD CODE                 S21003
         LA    LENGTH,LENTWO           LOAD LENGTH               S21003
         CLC   DCMCULNO(N2),XCOMP      SHOULD CURSOR BE ADJ SOM
         BE    TEST12                  YES  BRANCH
         BAL   LINKREG,BLDCCWD         GO TO BUILD CCW           S21003
         SR    LENGTH,LENGTH           ZERO REGISTER
         IC    LENGTH,DCMPOSCU         LOAD CURSOR CHAR POSITION
         CLI   DCMCULNO,N1             ENTRY LINE NUMBER ONE
         BE    TEST10                  YES   BRANCH
         LA    LENGTH,N80(LENGTH)      ADD LENGTH OF A LINE
TEST10   BCTR  LENGTH,N0               ADJEST TO CHAR POSITION
         TM    DCMIOUNQ,DCMRPCUR       NEED TO ADVANCE CURSOR
         BZ    TEST11                  NO  BRANCH
         NI    DCMIOUNQ,N255-DCMRPCUR  SET OFF SWITCH
         LA    CHAR,DCMENTRY(LENGTH)   POINT TO ENTRY AREA
TESTLOOP CLI   N0(CHAR),BLANK          FOUND BLANK IN ENT AREA
         BE    TEST11                  YES   BRANCH
         LA    CHAR,N1(CHAR)           ADJUST POINTER
         LA    LENGTH,N1(LENGTH)       ADJUST LENGTH
         B     TESTLOOP                KEEP LOOKING FOR BLANK
TEST11   LA    DATA,DCMENTRY           LOAD DATA ADRESS
TEST12   BAL   LINKREG,BLDCCWE         GO TO BUILD CCW           S21003
         EJECT
**                                                                   **
*              SET UP TO EXECUTE CHANNEL PROGRAM                      *
**                                                                   **
EXCP     EQU   *                       EXECUTE CHANNEL PROGRAM
         L     XFBRANCH,CSAXB      ADDRESS GETLOCK SUBR       JE YM8371
         LR    X1PARM,PXSA         PASS CXSA ADDRESS IN REG 1 JE YM8371
         BALR  XERTRN,XFBRANCH     OBTAIN LOCKS               JE YM8371
         OI    UCMSTS,UCMBF        TURN ON BUSY BIT
         XC    UCMECB,UCMECB       CLEAR ECB                  JE YM8371
         L     WORK,UCMDCB         GET DCB ADDRESS
         L     WORK,N28(WORK)      GET IOB ADDRESS
         L     XFBRANCH,CSAXC      ADDRESS FREELOCK SUBR      JE YM8371
         LR    X1PARM,PXSA         PASS CXSA ADDRESS IN REG 1 JE YM8371
         BALR  XERTRN,XFBRANCH     RELEASE LOCKS              JE YM8371
         EXCP  (WORK)                  INITIATE I/O
         SPACE 4                                                 S21003
         L     XFBRANCH,DCMNPROC   LOAD PROC 1 ADDRESS
*                                  FOR EXIT TO IEECVET1
**                                                                   **
*                     EXIT PROCEEDURE                                 *
**                                                                   **
EXIT     EQU   *                       INITIALIZE FOR EXIT
         MVI   DCMIOCM1,N0         SET OFF FLAGS
         NI    DCMIOCM2,N3             SET OFF SWITCHES
EXIT2    EQU   *
         L     X1PARM,DCMCXSVE     RESTORE CXSA ADDRESS
         BR    XFBRANCH            EXIT ADDRESS ALREADY SET
         SPACE 4                                                 S21003
NOP      EQU   *                                                 S21003
         LA    CODE,NOOP           SET CODE TO NO-OP             S21003
         LA    LENGTH,N1           SET BYTE COUNT                S21003
         B     TEST12              GO TO BUILD CCW               S21003
         EJECT                                                   S21003
**                                                                   **
*       BUILDS ALL CCW'S BASED ON INFORMATION PASSED BY CALLER        *
**                                                                   **
BLDCCWC  MVI   N4(CCWPTR),COMCHAIN INDICATE COMMAND CHAINING     S21003
         B     BUILDCCW            GO BUILD CCW                  S21003
BLDCCWD1 LA    CODE,WRITEL         LOAD WRITE CCW CODE           S21003
         LA    LENGTH,LENONE       LOAD LENGTH OF LINE ADDR      S21003
BLDCCWD  MVI   N4(CCWPTR),DATACHAN INDICATE DATA CHAINING        S21003
         B     BUILDCCW            GO BUILD CCW                  S21003
BLDCCWE  MVI   N4(CCWPTR),ENDCHAIN INDICATE NO CHAINING          S21003
BUILDCCW ST    DATA,N0(CCWPTR)    STORE DATA ADDR
         STC   CODE,N0(CCWPTR)    STORE COMMAND CODE
         STH   LENGTH,N6(CCWPTR)  STORE LENGTH
         MVI   N5(CCWPTR),N0           ZERO CCW BYTE
         LA    CCWPTR,N8(CCWPTR)  INCREMENT CCW POINTER
         BR    LINKREG            RETURN
         EJECT
VCONIO2  DC    V(IEECVFTR)         ADDRESS OF SECOND LOAD
MESSAGE  DC  CL27'IEE167E OUTPUT IN HOLD MODE'   WARNING MESSAGE
HOLDMSG  DC    CL23'IEE159E MESSAGE WAITING'     SCREEN FULL MESSAGE
XCOMP    DC    X'0101'       COMPARE FOR FIRST LINE/FIRST CHARACTER
XCITE    DC    H'802'        CONSTANT
LINENO   DC    X'090A0B00010203040506070809'     TO COMPUTE LINE NO
         SPACE 2                                                 S21003
**                                                                   **
*                  COMMAND CODE EQUATES                          S21003
**                                                                   **
RMICODE  EQU   X'0A'                   RMI CODE                  S21003
WRITEL   EQU   X'05'                   WRITE LINE                S21003
ERASE    EQU   X'07'                   ERASE SCREEN              S21003
READ     EQU   X'06'               CODE FOR READ DS FULL BUFFER  S21003
NOOP     EQU   X'03'               NO-OP                         S21003
         SPACE 2                                                 S21003
**                                                                   **
*                  CCW CHAINING EQUATES
**                                                                   **
COMCHAIN EQU   X'60'                   COMMAND CHAINING
DATACHAN EQU   X'A0'                   DATA CHAININD
ENDCHAIN EQU   X'20'                   STOP CHAINING
SURPDATA EQU   X'30'                   STOP CHAIN AND SURPRESS DATA
DUMCHAIN EQU   X'B0'                   SLI,SKIP,AND DATA CHAIN
         SPACE 2                                                 S21003
**                                                                   **
*                  CCW LENGTH FIELDS CONSTANTS                        *
**                                                                   **
ENTLEN   DC    H'159'                  LENGTH OF ENTRY AREA
SCRLEN   DC    H'960'                  TOTAL NUMBER CHAR POSITIONS
LENONE   EQU   1                   CONSTANT                      S21003
LENTWO   EQU   2                   CONSTANT                      S21003
MSGLEN   DC    H'640'                  LENGTH OF MESSAGE AREA
SURPLEN  DC    H'721'                  LENGTH OF SURPRESSED READ
         EJECT
**                                                                   **
*                        REGISTER EQUATES                             *
**                                                                   **
X1PARM   EQU   1
XERTRN   EQU   14
XFBRANCH EQU   15
PARM     EQU   1             HOLDS CXSA ADDRESS
DATA     EQU   2             CONTAINS DATA ADDR FOR CCW BUILD
CHAR     EQU   2             HOLDS CHAR WHEN COMPUTING LINE/CHAR
CODE     EQU   3             HOLDS COMMAND CODE FOR CCW BUILD
LINE     EQU   3                   HOLDS LINE NUMBER             S21003
RBASE    EQU   4             R DCM BASE REGISTER                 S21003
LENGTH   EQU   5             HOLDS LENGTH FOR CCW BUILD
LINKREG  EQU   6             BALR REG FOR CCW BUILD
WORK     EQU   6             WORK REGISTER
CCWPTR   EQU   7             POINTER TO CCW CHAIN
RSAVE    EQU   8             WORK REG                            S21003
UCMBASE  EQU   9             UCM BASE
DCMBASE  EQU   10            DCM BASE
PBASE    EQU   11            PROGRAM BASE
PXSA     EQU   12            CXSA ADDRESS
UCMENTRY EQU   13            UCMENTRY BASE
RETURN   EQU   14            RETURN REGISTER
XCTLREG  EQU   15            XCTL PARAMETER REGISTER
         EJECT
**                                                                   **
*              MISC  EQUATES                                          *
**                                                                   **
ID1      EQU   C'E'                1ST CHARACTER OF CSECT ID
ID2      EQU   C'R'                2ND CHARACTER OF CSECT ID
N0       EQU   0                       NUMBER
N1       EQU   1                       NUMBER
N2       EQU   2                       NUMBER
N3       EQU   3                       NUMBER
N4       EQU   4                       NUMBER
N5       EQU   5                       NUMBER
N6       EQU   6                       NUMBER
N8       EQU   8                       NUMBER
N11      EQU   11                      NUMBER
N14      EQU   14                      NUMBER
N23      EQU   23                      NUMBER
N27      EQU   27                      NUMBER
N28      EQU   28                      NUMBER
N39      EQU   39                      NUMBER
N40      EQU   40                      NUMBER
N41      EQU   41                      NUMBER
N57      EQU   57                      NUMBER
N79      EQU   79                      NUMBER
N80      EQU   80                      NUMBER
N132     EQU   132                     NUMBER
N159     EQU   159                     NUMBER
N255     EQU   255                     NUMBER
BLANK    EQU   C' '                    BLANK NUMBER
CQ       EQU   C'Q'                    CHARACTER
NF0      EQU   X'F0'               LINE ZERO                  MH Y02158
NF8      EQU   X'F8'               LINE EIGHT                 MH Y02158
NF9      EQU   X'F9'               LINE NINE                  MH Y02158
NFB      EQU   X'FB'               LINE ELEVEN                MH Y02158
         EJECT
         IHACTM  CXSA
         EJECT
         IEECUCM FORMAT=NEW
         EJECT
         IEERDCM
         EJECT
DCMSTRT  DSECT
         IEECDCM  DEVICE=C
         END
