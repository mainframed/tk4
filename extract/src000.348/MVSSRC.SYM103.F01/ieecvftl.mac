         TITLE 'IEECVFTL IN-LINE MLWTO MODULE'
IEECVFTL CSECT
*EQUATES(C) APPROX 202000                                      @ZA30599
***********************************************************************
*                                                                     *
*STATUS -- CHANGE LEVEL 1                                             *
*                                                                     *
*APARS FIXED = OZ14125, ZA30599                                       *
*                                                                     *
*FUNCTION -- HANDLE THE DISPLAY OF IN-LINE MULTIPLE LINE WRITE TO     *
*    OPERATOR (MLWTO) FOR DIDOCS. CONTROL IS PASSED FROM IEECVFT2,    *
*    THE DISPLAY 3 ROUTINE,WHEN AN IN-LINE MLWTO IS FOUND ON THE OUT- *
*    PUT QUEUE.                                                       *
*                                                                     *
*ENTRY POINTS -- IEECVFTL                                             *
*                                                                     *
*INPUT -- REGISTER ONE ADDRESS OF THE COMMUNICATION EXTENDED SAVE     *
*    AREA (CXSA)                                                      *
*          REGISTER TEN ADDRESS OF THE IN-LINE MLWTO TO BE DISPLAYED  *
*                                                                     *
*OUTPUT -- MESSAGE TEXT MOVED FROM THE WRITE QUEUE ELEMENT(WQE) TO THE*
*    DISPLAY CONTROL MODULE (DCM) FOR THE I/O ROUTINE TO WRITE OUT.   *
*    UPDATE OF SCT, SSCT, AND DOM TABLE WHEN REQUIRED                 *
*                                                                     *
*EXITS,NORMAL -- TO IEECVFT2, DISPLAY 3 ROUTINE                       *
*                TO IEECVET2, DISPLAY 1 ROUTINE                       *
*                TO DEPENDENT I/O ROUTINES                            *
*                TO IEECVET1, PROCESSOR 1 ROUTINE                     *
*                TO IEECVETD, MESSAGE 1 ROUTINE                       *
*                                                                     *
*EXITS,ERROR -- NONE                                                  *
*                                                                     *
*TABLES/WORK AREA -- UNIT CONTROL MODULE (UCM)                        *
*                    DISPLAY CONTROL MODULE (DCM)                     *
*                    WRITE QUEUE ELEMENT (WQE)                        *
*                    COMMUNICATION EXTENDED SAVE AREA (CXSA)          *
*                    SCREEN CONTROL TABLE (SCT)                       *
*                    SCREEN SECONDARY CONTROL TABLE (SSCT)            *
*                    DELETE OPERATOR MESSAGE TABLE (DOM)              *
*                                                                     *
*ATTRIBUTES -- TRANSIENT,PRIVILEGED,REFRESHABLE                       *
*                                                                     *
*NOTE -- THIS MODULE WAS CREATED FOR RELEASE 21 TO SUPPORT IN-LINE    *
*    MLWTO FOR DIDOCS.                                                *
*                                                                     *
***********************************************************************
         EJECT
         EJECT
********                                                              *
******** REGISTER EQUATES                                             *
********                                                              *
X1PARM   EQU   1
XERTRN   EQU   14
XFBRANCH EQU   15
R0       EQU   0                   WORK REG
R1       EQU   1                   FIRST AND CURRENT DOM ENTRY
R2       EQU   2                   FIRST AND CURRENT SCT ENTRY
R3       EQU   3                   FIRST AND CURRENT SSCT ENTRY
R4       EQU   4                   LENGTH OF ONE SCT ENTRY FOR INCREMEN
R5       EQU   5                   LAST SCT ENTRY
R6       EQU   6                   FIRST AND CURRENT SCREEN IMAGE BUFFE
R7       EQU   7                   BASE REGISTER FOR THE UCM ENTRY
R8       EQU   8                   BASE REGISTER FOR THE UCM BASE
R9       EQU   9                   BASE REGISTER FOR THE TRANSIENT DCM
R10      EQU   10                  ADDRESS OF MLWTO WQE FROM DISPLAY 1
R11      EQU   11                  BASE REGISTER FOR THE CXSA
R12      EQU   12                  BASE REGISTER FOR THIS ROUTINE
R13      EQU   13                  ADDRESS OF LAST WQE ENTRY
R14      EQU   14                  WORK REG
RRDCM    EQU   14                  TEMP R DCM BASE REGISTER
R15      EQU   15                  WORK REG
         EJECT
********                                                              *
******** NUMBER EQUATES                                               *
********                                                              *
ID1      EQU   C'F'                1ST CHARACTER OF CSECT ID
ID2      EQU   C'L'                2ND CHARACTER OF CSECT ID
N0       EQU   0                   NUMBER CONSTANT
N1       EQU   1                   NUMBER CONSTANT
N2       EQU   2                   NUMBER CONSTANT
N3       EQU   3                   NUMBER CONSTANT
N4       EQU   4                   NUMBER CONSTANT
N5       EQU   5                   NUMBER CONSTANT
N6       EQU   6                   NUMBER CONSTANT
N8       EQU   8                   NUMBER CONSTANT
N12      EQU   12                  NUMBER CONSTANT
N65      EQU   65                  NUMBER CONSTANT
BLK      EQU   X'40'               EBCDIC BLANK
ACTB     EQU   X'01'               ACTIVE FLAG FOR WQE Q
PURGE    EQU   X'02'               PURGE FLAG FOR WQE Q
X08      EQU   X'08'               HARD COPY FLAG FOR WQE Q
X10      EQU   X'10'               REUSE WQE FLAG FOR WQE Q
X80      EQU   X'80'               END FLAG FOR WQE Q
XC0      EQU   X'C0'               CONTINUE FLAG FOR WQE Q
TF       EQU   X'FF'               FLAG FOR TURNING OFF SWITCHES
DEFAULT  EQU   X'00'               DESCRIPTOR CODE ZERO
DEFCODE  EQU   X'38'               DESCRIPTOR CODES 3,4,AND 5
DNCODE   EQU   X'80'               DESCRIPTOR CODE NINE
ACTCODE  EQU   X'C0'               ACTION MESSAGES CODES
PPACT    EQU   C'@'                ACTION SYMBOL PROB PROG
INFOFLAG EQU   X'04'               INFORMATIONAL MSG FLAG
DISP7    EQU   X'02'               DISPOSITION FLAG ACTION MSG
NULL     EQU   X'00'               ZERO VALUE
F0       EQU   C'0'                EBCDIC ZERO
ISDASH   EQU   C'-'                EBCDIC DASH
SLASH    EQU   C'×'                EBCDIC VERTICAL BAR         @ZA30599
ROLL     EQU   C'R'                CHARACTER R FOR ROLL MODE
         EJECT
         BALR  R12,N0              LOAD BASE REG
         USING *,R12               ESTABLISH ADDRESSIBILITY OF THE RTN
         B     BEGIN               BRANCH AROUND PATCH AREA
ICATCH   DC    CL8'IEECVFTL'       EYECATCHER AND FIELD MAINTENANCE
         DC    CL8'&SYSDATE',CL56' '
BEGIN    EQU   *                   END OF PATCH AREA
         LR    R11,R1              ADDRESS OF CXSA
         USING CXSA,R11            ADDRESSIBILITY OF CXSA
         LM    R7,R8,CSAUCM        ADDRESS OF UCM ENTRY AND THE UCM
         USING UCM,R8              ADDRESSIBILITY OF UCM
         USING UCMLIST,R7          ADDRESSIBILITY OF THE UCM ENTRY
         L     R9,UCMXB            ADDRESS OF RESIDENT DCM
         L     R9,N0(R9)           ADDRESS OF TRANSIENT DCM
         USING DCMSTRT,R9          ADDRESSIBILITY OF THE TRANSIENT DCM
         MVC   DCMTRACE(DCMTRLEN),DCMTRAC2 SHIFT OLD TRACE ENTRIES
         MVI   DCMTREN1,ID1        PUT CSECT ID INTO
         MVI   DCMTREN2,ID2        NEW TRACE ENTRY
         L     XFBRANCH,CSAXB      ADDRESS GETLOCK SUBROUTINE
         LR    X1PARM,R11          PASS CXSA ADDRESS IN REG 1
         BALR  XERTRN,XFBRANCH     OBTAIN LOCKS
         USING CQE,R13             ADDRESSIBILITY OF CQE
         USING WMJM,R10            ADDRESSIBILITY OF WQE
         L     R13,UCMWLAST        ADDRESS OF LAST CQE ON QUE
         L     R10,CQEWQE          ADDRESS OF MAJOR WQE
         ST    R10,CSAXD           SAVE ADDRESS OF MAJOR WQE  ME
         SR    R0,R0               CLEAR REG
***                                                                   *
***  GET ADDRESSES OF DELETE OPERATOR MESSAGES (DOM),SCREEN CONTROL   *
***  TABLE (SCT), AND THE SECONDARY SCREEN CONTROL TABLE (SSCT)       *
***                                                                   *
CHECKRM  EQU   *
         LM    R1,R3,DCMDOMPK      ADDRESS OF THE FIRST DOM ENTRY, THE
*                                   FIRST SCT ENTRY, AND THE FIRST SSCT
         LM    R5,R6,DCMADDRL      ADDRESS OF THE LAST SCT ENTRY
*                                  ADDRESS OF SCREEN IMAGE BUFFER(SIB)
         TM    UCMSTS,UCMTC        WORKING ON A MLWTO
         BO    MINTEST             BRANCH YES
         ST    R0,UCMMLAST         SET LAST MINOR INDICATOR TO ZERO
         NI    UCMSDS5,TF-UCMSDS5F TURN OFF MLAST VALID       JE YM8408
PURGCON  EQU   *
         TM    WMJMDSP,WMJMDSPA+WMJMDSPF IS PURGE OR DOM ON   ME YM4153
         BZ    ROOM                BRANCH NO TO CHECK ROOM    ME A46454
         TM    DCMOPTST,DCMOPTAD   IS AUTO DELETE REQUIRED
         BO    TURNOFF             BRANCH YES TO EXIT
***                                                                   *
***  FIND FREE LINE TO START MLWTO                                    *
***                                                                   *
ROOM     EQU   *
         LH    R4,DCMSCTCN         LENGTH OF SCT ENTRY
         TM    N0(R2),DCMMSGIN     IS THERE A FREE LINE?
         BNO   NOTFULL             YES INSERT WQE FROM MLWTO
         AH    R6,DCMCORLN         GET NEXT LINE OF SIB          S21003
         LA    R3,N1(R3)           NEXT SSCT ENTRY
         LA    R1,N8(R1)           NEXT DOM  ENTRY
         BXLE  R2,R4,ROOM          INCREMENT TO NEXT SCT ENTRY
FULLDIS  EQU   *
         OI    UCMSDS5,UCMSDS5F    SET BIT FOR VALID UCMMLAST
         ST    R10,UCMMLAST        SAVE ADDRESS OF NEXT WQE TO WRITE
         NI    DCMCMSG1,TF-DCMUNMSG TURN OFF UNVIEWABLE MESSAGE IF FULL
         OI    UCMDEVC,UCMDEVA     INDICATE FULL SCREEN IN UCM
         L     RRDCM,UCMXB         ADDRESS OF RESIDENT DCM
         USING DCMTSRT,RRDCM       ADDRESSIBILITY OF RESIDENT DCM
         OI    DCMR2FLG,DCMRXSFL   INDICATE FULL SCREEN IN RESIDENT DCM
         CLI   DCMDEL,ROLL         IS DEVICE IN ROLL MODE
         BE    DISPLAY1            BRANCH YES                 ME  M1544
         DROP  RRDCM               DROP R DCM ADDRESSABILITY
         OI    DCMCMSG1,DCMMSGWT   SET WRITE MESSAGE WAITING BIT
         OI    DCMIOCM1,DCMWRWRN   SET WRITE WARNING LINE
         B     DISPLAY1            BRANCH TO I/O ROUTINE EXIT ME  M1544
NOTFULL  EQU   *
         TM    N0(R3),DCMSECDD     IS LINE RESERVED FOR DYNAMIC DISPLAY
         BO    FULLDIS             BRANCH YES TO RETURN TO DISPLAY
         OI    UCMSTS,UCMTC        TURN ON WORKING ON MLWTO INDICATOR
         SR    R14,R14             CLEAR REG
         IC    R14,WMJMTXTL+N1     LENGTH OF MSG FROM WQE     MB Y01958
         LTR   R14,R14             IS LENGTH ZERO
         BZ    ROOMDCM             BRANCH YES DO NOT MOVE
***                                                                   *
***  CLEAR A LINE IN THE DCM BY INSERTING BLANKS                      *
***                                                                   *
         MVI   N2(R6),BLK          MOVE A BLANK INTO SIB LINE ME
         LH    R4,DCMLGNTH         LENGTH OF ONE SIB LINE
         SH    R4,FOUR             DECREMENT LENGTH BY FOUR   ME
         EX    R4,BLKTXT           BLANK OUT SIB LINE
         TM    N0(R2),DCMMSGJK     IS JUNK IN LINE            ME
         BNO   MOVEA               BRANCH NO                  ME
         NI    N0(R2),TF-DCMMSGJK  TURN OFF JUNK BIT IN SCT
         MVC   N0(N2,R6),N2(R6)    BLANK OUT FIRST TWO CHAR   ME
***                                                                   *
***  PREPARE TO MOVE TEXT OF MAJOR WQE INTO DCM                       *
***                                                                   *
MOVEA    EQU   *
         DROP  R10
         USING WMJM,R15            ADDRESSIBILITY OF MAJOR WQEME
         L     R15,CSAXD           ADDRESS OF MAJOR WQE       ME
         MVC   N0(N2,R1),WMJMASID  MEMORY ID TO DOM TABLE
         MVC   N2(N3,R1),WMJMJTCB+N1 MOVE TCB ADDRESS TO DOM TABLE
         MVC   N5(N3,R1),WMJMSEQ   MOVE SEQ NUMBER TO DOM TABLE
         DROP  R15                                            ME
         USING WMJM,R10            ADDRESSIBILITY OF WQE      ME
         TM    WMJMMLW,WMJMMLWB    IS THIS A MAJOR WQE        MB Y01958
         BNO   MOVEDOM             BRANCH NO
         OI    WMJMBUF,WMJMBUFC    INDICATE RDY FOR HARDCOPY  MB YM7435
         LA    R15,WMJMTXT         BEGINNING OF TEXT FROM WQE ME
         TM    UCMDISP2,UCMDISPI   TIME STAMP REQ'D           JE YM6828
         BZ    TSTDISPJ            NO, TEST FOR JOBID         JE YM6828
         LA    R14,19(0,R14)       INCR FOR TIME              JE YM6828
         LA    R15,WMJMPAD         POINT TO TIME STAMP        JE YM6828
         B     MOVEA1              CONTINUE                   JE YM6828
TSTDISPJ TM    UCMDISP2,UCMDISPJ   JOBID REQ'D                JE YM6828
         BZ    MOVEA1              NO,CONTINUE                JE YM6828
         LA    R14,10(0,R14)       INCR FOR JOBID             JE YM6828
         LA    R15,WMJMPAD1        POINT TO JOBID             JE YM6828
MOVEA1   EQU   *
         CH    R14,DCMAXLGN        IS LENGTH NOW OVER LINE LENGTH?
         BL    MOVEA2              BRANCH NO TO CONTINUE
         LH    R14,DCMAXLGN        TRUNCATE MESSAGE USING MAXIMUM LEN
MOVEA2   EQU   *
         BCTR  R14,N0              DECREMENT LENGTH FOR EXECUTE
         TM    WMJMLTYP,WMJMLTYA   IS THIS MAJOR A CONTROL LINE
         BNO   SETSCT1             BRANCH NO
         OI    N1(R2),DCMMSGCL     SET CONTROL LINE BIT IN SCT
         TM    WMJMDEC+N1,DNCODE   IS MLWTO DESCRIPTOR CODE NINE
         BNO   SETSCT1             BRANCH NO
         MVC   N65(N2,R6),ASTCHAR  MOVE IN ASTERIST AND C FOR LIGHT PEN
SETSCT1  EQU   *
         EX    R14,MSGTXT          MOVE MSG TEXT INTO DCM BUFFER
SETSCT   EQU   *
         OI    N0(R2),DCMMSGIN     INDICATE LINE IN USE
         TM    WMJMDSP,WMJMDSPA+WMJMDSPF IS PURGE OR DOM ON   ME YM4153
         BNZ   PURGFLG             BRANCH YES TO FLAG IT
         TM    WMJMCS1,WMJMCS1A    DO ROUTING OR DESCRIPTOR CODES EXITS
         BNO   INFOMSG             BRANCH NO TO DEFAULT CODE 6
         CLI   WMJMDEC,DEFAULT     IS THIS CODE ZERO?
         BE    INFOMSG             BRANCH YES TO DEFAULT CODE 6
         TM    WMJMDEC,DISP7       IS THIS CODE 7 ?            @ZA14125
         BNO   ACTMSG              BRANCH NO TO ACTION MSG     @ZA14125
         OI    N1(R2),DCMMSGC7     FLAG SCT AS DESC CODE 7     @ZA14125
ACTMSG   TM    WMJMDEC,ACTCODE     IS THIS AN ACTION MSG?
         BNZ   FLGACT              BRANCH YES TO FLAG IT
         TM    WMJMDEC,DEFCODE     DESCRIPTOR CODE 3,4,OR 5 ?
         BNZ   NUMTST              BRANCH YES
         TM    WMJMDEC,INFOFLAG    IS THIS AN INFORMATION MSG?
         BO    INFOMSG             BRANCH YES TO MARK IT
         TM    WMJMDEC,DISP7       IS THIS CODE 7?
         BNO   NUMTST              BRANCH NO TO NUMBER CHECK
****************** DELETE ONE LINE OF CODE ******************* @ZA14125
         B     INFOMSG             BRANCH
FLGACT   EQU   *
         MVC   N3(N1,R6),WMJMTXT   COPY ACTION CODE           JE YM6828
         OI    N1(R2),DCMMSGAC     INDICATE ACTION MSG IN SCT
         OI    DCMIOCM1,DCMSOUND   INDICATE SOUND ALARM
         CLI   WMJMTXT,PPACT       IS THIS A PROB PROG ACTION MSG
         BNE   NUMTST              NO CONTINUE NORMALLY
         OI    N1(R2),DCMMSGPP     SET MSG IS PROB PGOG
         B     NUMTST              BRANCH TO NUMBER TEST
INFOMSG  EQU   *
         MVI   N2(R6),ISDASH       FLAG INFORMATION MSG
         OI    N0(R2),DCMMSGIF     UPDATE SCT WITH INFO BIT
NUMTST   EQU   *
         TM    DCMDSTAT,DCMDSTNM   ARE MESSAGES NUMBERED?
         BZ    GOON                BRANCH NO
         LH    R15,DCMADNUM        GET CURRENT NUMBER
         LA    R15,N1(R15)         INCREMENT COUNT
         STH   R15,DCMADNUM        STORE NEW NUMBER
         CVD   R15,DCMPACK         CONVERT TO DECIMAL
         UNPK  DCMCVBIN+N2(N2),DCMCVBIN+N2(N2) UNPACK NUMBER
         OI    DCMCVBIN+N3,F0      MAKE IT PRINTABLE
         CLI   DCMCVBIN+N2,F0      IS FIRST CHARACTER ZERO
         BNE   MOVEALL             BRANCH NO TO MOVE
         MVI   DCMCVBIN+N2,BLK     REPLACE ZERO
MOVEALL  EQU   *
         MVC   N0(N2,R6),DCMCVBIN+N2 MOVE NUMBER ON SCREEN
GOON     EQU   *
         TM    N0(R3),DCMSECCL+DCMSECLL+DCMSECDL+DCMSECBL IS LINE A
*                                  STATUS DISPLAY
         BNZ   UNVIEW              BRANCH YES
         CLI   DCMLINEN,N0         IS THIS THE FIRST MESSAGE?
         BNZ   VISA                BRANCH NO-DO NOT UP WRITE COUNTER
         L     R0,DCMASCRN         GET ADDRESS SIB
         SR    R14,R14             ZERO OUT EVEN REG
         LR    R15,R6              GET CURRENT LINE POINTER
         SR    R15,R0              GET DISPLACEMENT
         AH    R15,DCMCORLN        ADD ONE LINE                  S21003
         LH    R0,DCMCORLN         GET DIVISOR                   S21003
         DR    R14,R0              QUOTIENT IS LINE NUMBER
         STC   R15,DCMLINEN        SAVE LINE NUMBER
VISA     EQU   *
         OI    DCMIOCM1,DCMWRPAR   WRITE PARTIAL MSG AREA
         LH    R15,DCMBYTCT        GET CURRENT BYTE COUNT
         AH    R15,DCMCORLN        ADD LENGTH OF LINE            S21003
         STH   R15,DCMBYTCT        SAVE NEW LENGTH
SDSON    EQU   *
         NC    UCMMLAST(N4),UCMMLAST HAS A MINOR BEEN WRITTEN
         BZ    MAJWRITA            BRANCH NO FOR ADDRESS OF MINOR
         L     R10,UCMMLAST        ADDRESS OF LAST WRITTEN MINOR
         DROP  R10
         USING WMNM,R10
         NC    WMNMNX1(N3),WMNMNX1 IS THERE A NEXT MINOR TO WRITE
         BNZ   MINWRIT             BRANCH YES FOR NEXT MINOR
VISA1    EQU   *
         TM    WMNMLT1,WMNMLT1D    IS END IN MINOR WQE?
         BNO   IORET1              BRANCH NO
***                                                                   *
***  PREPARE TO EXIT TO I/O ROUTINES IF LAST WQE ON OUTPUT QUE        *
***  OR RETURN TO DISPLAY THREE IF MORE WORK ON QUE                   *
***                                                                   *
TURNOFF  EQU   *
         NI    UCMSTS,TF-UCMTC     TURN OFF WORKING ON MLWTO IND.
         OI    UCMSTS,UCMTB        SET DEQUE WQE BIT
         OI    CQEFLAG,CQEAVAIL    SET PURGE BIT IN QUE AND   ME
         NI    CQEFLAG,TF-CQEENTR  TURN OFF ACTIVE FLAG
         TM    CQEFLAG,CQEEOQ      IS THIS THE LAST QUE ENTRY
         BO    IORET               BRANCH YES
         LA    R13,N4(R13)         NEXT QUE ELEMENT
         TM    CQEFLAG,CQEEOB      IS THIS A CONTINUE QUE ENTRY
         BNO   SETUP               BRANCH NO
         L     R13,CQEWQE          NEXT QUE ENTRY
SETUP    EQU   *
         OI    DCMCOM3,DCMXINT1    SET RETURN BIT FOR DISPLAY3
         ST    R13,UCMWLAST        UPDATE NEXT QUE ENTRY FOR DISPLAY3
         L     XFBRANCH,DCMNDSP3   LOAD DISPLAY 3 ADDRESS
         B     PEXIT               EXIT TO IEECVFT2
***
***   THIS HANDLES MINOR WQE BY MOVING THEM INTO THE DCM
***
MINTEST  EQU   *
         DROP  R10
         USING WMJM,R10
         STH   R0,DCMBYTCT         ZERO BYTE COUNT FOR I/O PARIAL WRITE
         MVI   DCMLINEN,NULL       ZERO LINE NUMBER FOR PARTIAL WRITE
         TM    CQEFLAG,CQEATTOP    IS START AT TOP ON
         BO    MAJWRITA            BRANCH YES
         NC    UCMMLAST(N4),UCMMLAST WAS THE LAST WRITE A MAJOR?
         BZ    MAJWRIT             BRANCH YES
         TM    UCMSDS5,UCMSDS5F    UCMMLAST POINT TO NXT WQE TO OUTPUT
         BO    ROOMGO              BRANCH YES
QSEARCH  EQU   *
         CLC   UCMMLAST(N4),WMJMMIN IS MLAST VALID
         BE    QFOUND              BRANCH YES
         L     R10,WMJMMIN         ADDRESS OF QUED MINOR
         DROP  R10
         USING WMNM,R10            MINOR ADDRESSIBILITY
NEXTELM  EQU   *
         CLC   UCMMLAST+N1(N3),WMNMNX1 IS MLAST VALID
         BE    QFOUND              BRANCH YES
         L     R10,WMNMNX1-N1      ADDRESS OF NEXT MINOR
         LA    R10,N0(R10)         ZERO USE COUNT
         LTR   R10,R10             IS POINTER ZERO
         BNZ   NEXTELM             BRANCH NO TO CONTINUE SEARCH
QNFOUND  EQU   *
         L     R10,CSAXD           ADDRESS OF MAJORWQE
         B     MAJWRIT1            BRANCH TO WRITE QUEUED MINOR
QFOUND   EQU   *
         L     R10,UCMMLAST        SET BASE FOR MINOR
         NC    WMNMNX1(N3),WMNMNX1 DOES NEXT MINOR EXIST?
         BNZ   MINWRIT             BRANCH YES
RETR     EQU   *
         NI    UCMSDS5,TF-UCMSDS5B TURN OFF IN-LINE BIT
         L     XFBRANCH,DCMNPROC   LOAD PROC 1 ADDRESS
         B     PEXIT               EXIT TO IEECVET1
ROOMGO   EQU   *
         NI    UCMSDS5,TF-UCMSDS5F TURN OFF MLAST VALID BIT
         L     R10,UCMMLAST        ADDRESS OF NEXT WQE TO PUT OUT
         B     ROOM                BRANCH TO FIND LINE
DISPLAY1 EQU   *                                              ME  M1544
         OI    DCMCOM3,DCMCDSP3    IND WORK DONE FOR DISP RTN ME  M1544
         L     XFBRANCH,DCMNDSP1   LOAD DISPLAY 1 ADDRESS
         B     PEXIT               EXIT TO IEECVET2
***                                                                   *
***  PREPARE TO MOVE TEXT OF MINOR WQE'S INTO DCM                     *
***                                                                   *
MOVEDOM  EQU   *
         LA    R15,WMNMTXT1        BEGINNING OF TEXT FROM MINOR WQE
MOVED1   EQU   *
         CH    R14,DCMAXLGN        IS LENGTH NOW OVER LINE LENGTH?
         BL    MOVED2              BRANCH NO TO CONTINUE
         LH    R14,DCMAXLGN        TRUNCATE MESSAGE USING MAXIMUM LEN
MOVED2   EQU   *
         BCTR  R14,N0              DECREMENT LENGTH FOR EXECUTE
         EX    R14,MSGTXT          BRANCH BACK FOR TEXT MOVE
ROOMDCM  EQU   *
         CLI   WMNMLT1,N0          IS THIS A NULL MINOR       JE YM9999
         BE    VISA1               BRANCH YES TO RETURN       MB YM0664
         SR    R15,R15             CLEAR WORK REG
         ICM   R15,1,WMNMUC1       GET USE COUNT FROM WQE     JE YM8408
         BZ    RMDCM2              ALREADY ZERO, DON'T DECRMT JE YM8408
         BCTR  R15,N0              DECREMENT USE COUNT BY ONE
         STC   R15,WMNMUC1         RESTORE USE COUNT TO WQE
         LTR   R15,R15             IS USE COUNT NOW ZERO
         BNZ   SETMLAST            BRANCH NO
RMDCM2   OI    UCMSTS,UCMTB        TELL DEQUE TO CHECK CHAIN  JE YM8408
         L     R15,CSAXD           RESTORE MAJOR WQE ADDRESS
         USING WMJM,R15
         OI    WMJMMLW,WMJMMLWG    INDICATE A MINOR WQE CAN BE DECHAIN
         DROP  R15
SETMLAST EQU   *
         ST    R10,UCMMLAST        SAVE ADDRESS OF LAST WRITTEN WQE
         LTR   R14,R14             IS LENGTH EQUAL TO ZERO
         BZ    VISA1               BRANCH YES
         L     R10,CSAXD           ADDRESS OF MAJOR WQE
         B     SETSCT              RETURN TO SET UP SCT TABLE
MINWRIT  EQU   *
         L     R10,WMNMUC1         ADDRESS OF NEXT MINOR WQE
         LA    R10,N0(R10)         ZERO USE COUNT
         B     ROOM                RETURN TO FIND ROOM FOR MINOR WQE
IORET1   EQU   *
         NI    UCMSDS5,TF-UCMSDS5B TURN OFF IN-LINE BIT
IORET    EQU   *
         TM    DCMCMSG1,DCMUNMSG   IS WRITE UNVIEWABLE MSG ON
         BO    MSGEXIT             BRANCH YES TO MSG EXIT
         L     XFBRANCH,DCMIORTN   LOAD IO ROUTINE ADDRESS
*                                  FOR EXIT TO IEECVET(H,P,R, OR U)
PEXIT    EQU   *
         L     X1PARM,DCMCXSVE     RESTORE CXSA ADDRESS
         BR    XFBRANCH            EXIT ADDRESS ALREADY SET
MSGEXIT  EQU   *
         L     XFBRANCH,DCMNMSG1   LOAD MESSAGE 1 ADDRESS
         B     PEXIT               EXIT TO IEECVETD
PURGFLG  EQU   *
         OI    N0(R2),DCMMSGAD     FLAG AS AUTO DELETEABLE
         L     RRDCM,UCMXB         ADDRESS OF RESIDENT DCM
         USING DCMTSRT,RRDCM       ADDRESSIBILITY OF RESIDENT DCM
         CLI   DCMRQDEL,BLK        IS A DELETE REQ PENDING?
         BNE   NUMTST              NO DO NOT UPDATE SCREEN
         DROP  RRDCM               DROP R DCM ADDRESSABILITY
         MVI   N2(R6),SLASH        FLAG ON SCREEN
         B     NUMTST              BRANCH TO NUMBER TEST
UNVIEW   EQU   *
         OI    DCMCMSG1,DCMUNMSG   TURN ON WRITE UNVIEWABLE MSG BIT
         B     SDSON               BRANCH TO CHECK MLWTO TYPE
MAJWRITA EQU   *
         NI    CQEFLAG,TF-CQEATTOP TURN OFF START-AT-TOP
MAJWRIT  EQU   *
         XC    UCMMLAST(N4),UCMMLAST ZERO LAST MINOR ADDRESS
         NI    UCMSDS5,TF-UCMSDS5F TURN OFF MLAST VALID       JE YM8408
         TM    WMJMLTYP,WMJMLTYD   IS END INDICATED IN MAJOR WQE
         BO    TURNOFF             BRANCH YES TO EXIT
MAJWRIT1 EQU   *
         TM    WMJMMLW,WMJMMLWH    IS MINOR POINTER VALID
         BO    IORET1              BRANCH NO
         L     R10,WMJMMIN         ADDRESS OF MINOR FROM MAJOR
         B     ROOM                BRANCH YES FOR NEXT LINE
BLKTXT   MVC   N3(N0,R6),N2(R6)    MOVE TO BLANK OUT SIB LINE
MSGTXT   MVC   N3(N0,R6),N0(R15)   MOVE TEXT FROMWQE TO DCM
         EJECT
***                                                                   *
***  CONSTANT USED BY THIS ROUTINE                                    *
***                                                                   *
FOUR     DC    H'4'                FOR SUBTRACTING
ASTCHAR  DC    C'*C'               ASTERISK AND C FOR LIGHT PEN
         EJECT
         IEERDCM
         EJECT
         IEETDCM
         EJECT
         IEECUCM  FORMAT=NEW
         EJECT
         IHACTM  CXSA
         EJECT
         IHACTM CQE
         EJECT
         IHAWQE  DSECT=YES
         END
