* /* START OF SPECIFICATIONS ******************************************
*
*01*  MODULE-NAME = IEECVFT2
*
*01*  DESCRIPTIVE-NAME = DISPLAY 3 ROUTINE
*
*01*  COPYRIGHT = NONE
*
*01*  STATUS = CHANGE LEVEL 1 FOR VS2 RELEASE 2
*
* D 165500-168000                                             JE YM6828
* A 125100-125484,170600-170980,189500                        JE YM6828
* C 214500                                                    JE YM6828
*01*  FUNCTION = TO MOVE THE TEXT OF MESSAGES FOUND ON THE CONSOLE
*     QUEUE INTO THE SCREEN IMAGE BUFFER LINES OF THE DCM ALONG WITH
*     THE APPROPRIATE MESSAGE FLAGGING. WHEN AN IN-LINE MLWTO IS FOUND
*     ON THE QUEUE, CONTROL IS PASSED TO IEECVFTL TO HANDLE IT. WHEN
*     THE SCREEN IS FULL, OR NO MORE MESSAGES REMAIN ON THE QUEUE,
*     CONTROL IS RETURNED TO IEECVET2, DISPLAY 1, TO CONTROL FURTHER
*     PROCESSING.
*
*01*  NOTES = LOCAL AND CROSS-MEMORY LOCKS ARE OBTAINED ON ENTRY VIA
*     THE GETLOCK SUBROUTINE ADDRESSED IN THE CXSA (CSAXB).
*
*02*    CHARACTER-CODE-DEPENDENCIES = NONE
*
*02*    DEPENDENCIES = NONE
*
*02*    RESTRICTIONS = NONE
*
*02*    REGISTER-CONVENTIONS = SEE REGISTER EQUATES
*
*02*    PATCH-LABEL = ICATCH A DC STATEMENT
*
*01*  MODULE-TYPE = MODULE
*
*02*    PROCESSOR = ASSEMF-370R
*
*02*    MODULE-SIZE = X'3D8' BYTES
*
*02*    ATTRIBUTES = REENTERABLE
*
*01*  ENTRY-POINT = IEECVFT2
*
*02*    PURPOSE = THIS IS THE ONLY ENTRY POINT TO PERFORM ALL FUNCTIONS
*
*02*    LINKAGE = BALR
*
*02*    INPUT = REGISTER 1 ADDRESSES THE CXSA
*       SEE ALSO CONTROL BLOCKS USAGE
*
*02*    OUTPUT = REGISTER 1 ADDRESSES THE CXSA
*       SEE ALSO CONTROL BLOCKS USAGE AND EXITS
*
*02*    EXIT-NORMAL = IEECVET2 - DISPLAY ROUTINE 1
*
*02*    EXIT-NORMAL = IEECVFTL - IN-LINE MLWTO DISPLAY ROUTINE
*
*02*    EXIT-ERROR = NONE
*
*01*  EXTERNAL-REFERENCES = NONE
*
*02*    ROUTINES = GETLOCK SUBROUTINE - DEFINED IN IEECVET1, AND
*       ADDRESSED BY WORD CSAXB OF THE CXSA ON ENTRY.
*
*02*    DATA-SETS = NONE
*
*02*    DATA-AREA = NONE
*
*02*    CONTROL-BLOCKS = CXSA,UCM,WQE,CQE,RDCM,TDCM
*
*01*  TABLES = NONE
*
*01*  MACROS = EXECUTABLE - NONE
*              MAPPING - IHAWQE  WQE DSECT
*                        IHACTM  CXSA DSECT
*                        IHACTM  CQE DSECT
*                        IEECUCM UCM DSECT
*                        IEERDCM RESIDENT DCM DSECT
*                        IEETDCM PAGEABLE DCM DSECT
*
*02*    ROUTINES = GETLOCK SUBROUTINE - DEFINED IN IEECVET1, AND
*       ADDRESSED BY WORD CSAXB OF THE CXSA ON ENTRY.
*
**** END OF SPECIFICATIONS *******************************************/
         TITLE 'IEECVFT2 DISPLAY 3 '
*/*IEECVFT2:     CHART */
*/*              HEADER
*/*          IEECVFT2                   DISPLAY 3
*/*           CHART F2           PAGE #    */
*/*      E     IEECVFT2  */
IEECVFT2 CSECT
***********************************************************************
*        REGISTER EQUATES
***********************************************************************
*
XFBRANCH EQU   15        BRANCH EXIT TO OTHER ROUTINES
XFQUOTEN EQU   15        ODD REG FOR DIVISION
XEMVC    EQU   14        TO ADDRESS EXECUTED MOVE INSTRUCTIONS
XERTRN   EQU   14        RETURN BRANCH REG
XEREMAIN EQU   14        EVEN REG FOR DIVISION
XDWORK   EQU   13        TEMPORARY WORK REGISTER
XCPBASE  EQU   12        PROGRAM BASE REG
XBTDCM   EQU   11        PAGEABLE DCM BASE REG
XARDCM   EQU   10        RESIDENT DCM BASE REG
X9SCNSRT EQU   9         TO BACKSCAN TEXT FOR SPLIT POINTS
X9UCM    EQU   9         UCM BASE REG
X8UCME   EQU   8         UCM ENTRY BASE REG
X7WQE    EQU   7         WQE BASE
X7CQE    EQU   7         CQE BASE
X6DOM    EQU   6         DOM TABLE ENTRY POINTER
X5SCT    EQU   5         SCT ENTRY POINTER
X4MSG    EQU   4         MESSAGE AREA TEXT POINTER
X3PADS   EQU   3         NUMBER OF CONTINATION LINES PASSED TO ET3
X2START  EQU   2         START POINT FOR MOVING MESSAGE TEXT
X2WORK   EQU   2         WORK REG
X1PARM   EQU   1         CXSA ADDRESS ON ENTRY AND EXIT
X0WORK   EQU   0         TEMPORARY WORK REGISTER
         EJECT
***********************************************************************
*    INITIALIZE PROGRAM
*
***********************************************************************
         BALR  XCPBASE,N0          LOAD PROGRAM BASE REGISTER
         USING *,XCPBASE           ESTABLISH PROG ADDRESSABILITY
         B     BEGIN               BRANCH AROUND PATCH AREA
ICATCH   DC    CL72'IEECVFT2'      EYECATCHER AND FIELD MAINTENANCE
BEGIN    EQU   *                   END OF PATCH AREA
         USING CXSA,X1PARM         ESTABLISH CXSA ADDRESSABILITY
         USING UCMLIST,X8UCME      ESTABLISH UCMENTRY ADDRESSABILITY
         USING UCM,X9UCM           ESTABLISH UCM ADDRESSABILITY
         USING DCMTSRT,XARDCM      ESTABLISH R DCM ADDRESSABILITY
         USING DCMSTRT,XBTDCM      ESTABLISH DCM ADDRESS
         L     X8UCME,CSAUCM       UCM ENTRY BASE
         L     X9UCM,CSACTLM        UCM BASE
         L     XARDCM,UCMXB        RESIDENT DCM BASE
         L     XBTDCM,DCMADTRN     PAGEABLE DCM BASE
         MVC   DCMTRACE(DCMTRLEN),DCMTRAC2 SHIFT OLD ENTRIES
         MVI   DCMTREN1,ID1        MOVE IN CSECT ID OF
         MVI   DCMTREN2,ID2        'F2' FOR IEECVFT2
         SPACE 2
*/*      S     GETLOCK: OBTAIN LOCAL AND CROSS-MEMORY LOCKS  */
***********************************************************************
         L     XFBRANCH,CSAXB      ADDRESS GETLOCK SUBROUTINE
         BALR  XERTRN,XFBRANCH     OBTAIN LOCKS
         SPACE 3
*/*     D (YES,,NO,NONIP) IS THIS FIRST MESSAGE */
***********************************************************************
*        ADJUSTMENTS FOR FIRST WRITE OF IPL'ED CONSOLE                *
***********************************************************************
         CLI   DCMLINEN,N0         IS THIS FIRST MSG.
         BNZ   NONIP               NO,BRANCH
         TM    DCMRFLGS,DCMNIPP    THIS DCM USED BY NIP
         BZ    NONIP               NO, BRANCH
         NI    DCMRFLGS,N255-DCMNIPP    SET OFF NIP USE FLAG
         MVI   DCMLINEN,N1         INITIALIZE POINTER
         LA    X2WORK,N8           GET NUMBER OF NIPS MSGS.
         MH    X2WORK,DCMCORLN     MULTIPLY BY LINE LENGTH
         AH    X2WORK,DCMBYTCT     ADD WRITE BYTE COUNT
         STH   X2WORK,DCMBYTCT     UPDATE WRITE COUNT
         EJECT
*/*NONIP: D    (NO,,YES,INTFACE1)  WORKING ON MLWTO  */
***********************************************************************
NONIP    EQU   *                   CONTINUE NORMAL PROCESSING
         LA    X1PARM,N1           BEGIN LINE COUNT
         L     X5SCT,DCMAMTAB      POINT TO SCT
         L     X6DOM,DCMDOMPK      POINT TO DOM TABLE
         L     X4MSG,DCMASCRN      POINT TO SIB
         TM    UCMSTS,UCMTC        IF BIT IS ON THEN MLWTO WRITE
         BO    INTFACE1            FOR INT1 IS NOT FINISHRD
         SPACE 2
*/*      D     (YES,,NO,MSGTEST)   RETURN FROM MLWTO HANDLING  */
***********************************************************************
         TM    DCMCOM3,DCMXINT1    RETURN FROM INTERFACE 1 RTN
         BNO   MSGTST              NO--SKIP
         NI    DCMCOM3,N255-DCMXINT1 TURN OFF FLAG BIT FOR INT1
         SPACE 2
*/*      P     (,STARTEST)         GET NEXT CQE  */
***********************************************************************
         L     X7CQE,UCMWLAST      YES-LOAD ADDR OF NEXT CQE TO
         USING CQE,X7CQE           DECLARE CQE BASE
         B     STARTEST            START WORKING ON
         EJECT
*/*MSGTEST: D  (NO,,YES,STARTEST)  ANY MESSAGES ON CONSOLE Q  */
***********************************************************************
MSGTST   EQU   *
         L     X7CQE,UCMOUTQ       CONSOLE Q POINTER
         LA    X7CQE,N0(X7CQE)     CLEAR HIGH ORDER BYTE
         LTR   X7CQE,X7CQE         VALID ADDRESS
         BNZ   STARTEST            YES, START PROCESSING MESSAGES
         SPACE 2
*/*FINISH: P   TURN OFF OUTPUT PENDING IN UCM ENTRY  */
***********************************************************************
FINISH   EQU   *
         NI    UCMSDS5,N255-UCMSDS5B TURN OFF OUTPUT FLAG
         SPACE 2
*/*DISP1: R    BRANCH IEECVET2  */
***********************************************************************
DISP1    EQU   *
         OI    DCMCOM3,DCMCDSP3    INDICATE ENTRY IS FROM HERE
         L     XFBRANCH,DCMNDSP1   LOAD DISPLAY 1 ADDRESS
         L     X1PARM,DCMCXSVE     PASS CXSA ADDRESS IN REG 1
         BR    XFBRANCH            EXIT TO IEECVET2
         SPACE 2
*/*STARTEST: D (YES,,NO,TESTDISP)  END OF THIS CQE BLOCK  */
***********************************************************************
STARTEST EQU   *
         TM    CQEFLAG,CQEEOB      IS THIS A POINTER TO A NEW BLK
         BNO   TESTDISP            NO TEST FOR DISPLAY
         SPACE 2
*/*      P     GET NEXT BLOCK OF CQES  */
***********************************************************************
         L     X7CQE,CQEWQE        PICK UP NEXT BLOCK PTR
         B     STARTEST            RETRY
         SPACE 2
*/*TESTDISP: D (NO,,YES,INSRT)     THIS CQE TO BE SERVICED  */
***********************************************************************
TESTDISP EQU   *
         TM    CQEFLAG,CQEENTR     ENTRY TO BE SERVICED
         BO    INSRT               YES, SEE IF PURGE BIT IS ON
         SPACE 2
*/*ENDLIST: D  (NO,,YES,FINISH)    END OF CONSOLE Q  */
***********************************************************************
ENDLIST  EQU   *
         TM    CQEFLAG,CQEEOQ      END OF LIST
         BO    FINISH              YES, EXIT
         SPACE 2
*/*      P     (,STARTEST)         GET POINTER TO NEXT MESSAGE  */
***********************************************************************
         LA    X7CQE,N4(X7CQE)     NO, GET NEXT WORD
         B     STARTEST            RESTART
INSRT    EQU   *
         ST    X7CQE,UCMWLAST      SAVE LIST PT
         TM    CQEFLAG,CQEMAJOR    START OF MLWTO
         BO    MLWTO               YES-HANDLE MLWTO
         L     X7WQE,CQEWQE        ADDRESS THIS WQE
         USING WQE,X7WQE           DECLARE WQE BASE
PURGCONT EQU   *
         TM    WQEXA,WQEPURGE+WQEDOM IS WQE PURGE OR DOM ON
         BZ    ROOM                NO,FIND ROOM ON SCREEN
MARK     EQU   *                   FLAG AS NO LONGER NEEDED
         L     X7CQE,UCMWLAST      GET POINTER TO ACTIVE CQE
         USING CQE,X7CQE           DECLARE CQE BASE
         OI    UCMSTS,UCMTB        INDICATE DEQUE WQE
         NI    CQEFLAG,N255-CQEENTR TURN OFF ENTRY ACTIVE
         OI    CQEFLAG,CQEAVAIL    INDICATE NO LONGER NEEDED
         B     ENDLIST             SEE IF THERE ARE MORE
MLWTO    EQU   *
*    IF MLWTO HAS BOTH DESCRIPTOR CODES 8 AND 9, AND THE TARGET
*    AREA IS NOT Z (FOR INLINE), THEN IT WILL GO OUT OF LINE
*    AND THIS ROUTINE IGNORES IT.  OTHERWISE, PASS CONTROL TO
*    INTERFACE 1 TO PUT THE MLWTO INLINE.
         L     X7WQE,CQEWQE        GET WQE ADDRESS
         USING WQE,X7WQE           DECLARE WQE BASE
         TM    WQEMCSF,WQEMCSA     DO DESCRIPTOR CODES EXIST
         BNO   INTFACE1            NO, GO TO INTERFACE 1
         TM    WQEDESCD,WQEDCH     DESCRIPTOR CODE 8
         BNO   INTFACE1            NO, GO TO INTERFACE 1
         TM    WQEDC2,WQEDCI       DESCRIPTOR CODE 9
         BNO   INTFACE1            NO, GO TO INTERFACE 1
         CLI   WMJMAREA,Z          AREA ID = Z
         BE    INTFACE1            YES-GO TO INTERFACE 1
         L     X7CQE,UCMWLAST       GET LIST PT OF ACTIVE WQE
         B     ENDLIST             IGNORE MLWTO, CONTINUE SEARCH
INTFACE1 EQU   *
*    EXIT TO THE INTERFACE MODULE 1 TO DISPLAY IT INLINE
*
         L     XFBRANCH,DCMNINT1   LOAD IN-LINE DISPLAY ADDRESS
         L     X1PARM,DCMCXSVE     PASS CXSA ADDRESS IN REG 1
         BR    XFBRANCH            EXIT TO IEECVFTN
*
ROOM     EQU   *
         TM    N0(X5SCT),DCMMSGST  END OF TABLE
         BO    FULTST              YES,NO MORE ROOM
         TM    N0(X5SCT),DCMMSGIN  IS THIS LINE FREE
         BNO   NOTFULL             YES, INSERT WQE
         AH    X5SCT,DCMSCTCN      NO, GET NEXT SCT ENTRY
         LA    X6DOM,DCMDOMLN(X6DOM) GET NEXT DOM ENTRY
         AH    X4MSG,DCMCORLN      GET NEXT LINE
         LA    X1PARM,N1(X1PARM)   AND NEXT LINE NUMBER
         CH    X1PARM,DCMMSGAL     STILL IN MSG AREA
         BNH   ROOM                YES, CHECK FOR A FREE LINE
FULTST   EQU   *
         OI    DCMR2FLG,DCMRXSFL   SET ON R DCM SCREEN FULL
         OI    UCMDEVC,UCMDEVA       INDICATE THAT SCREEN IS FULL
         CLI   DCMDEL,R            IS DEVICE IN ROLL MODE
         BE    DISP1               YES DO NOT WRITE MESSAGE WAITIND
         OI    DCMCMSG1,DCMMSGWT   INDICATE WRITE MESSAGE WAITING BIT
         OI    DCMIOCM1,DCMWRWRN   WRITE WARNING LINE
         B     DISP1
NOTFULL  EQU   *
         LH    XFQUOTEN,WQENBR+N2  GET TEXT LENGTH
         TM    UCMDISP2,UCMDISPI   TIME STAMP REQ'D           JE YM6828
         BZ    TSTDISPJ            NO, TEST FOR JOBID         JE YM6828
         LA    XFQUOTEN,19(0,XFQUOTEN) INCR LENGTH FOR TIME   JE YM6828
         B     FULTST1             CONTINUE                   JE YM6828
TSTDISPJ TM    UCMDISP2,UCMDISPJ   JOBID REQ'D                JE YM6828
         BZ    FULTST1             NO, CONTINUE               JE YM6828
         LA    XFQUOTEN,10(0,XFQUOTEN) INCR LENGTH FOR JOBID  JE YM6828
FULTST1  DS    0H                                             JE YM6828
         LH    X3PADS,DCMAXLGN     GET MAX TEXT/LINE
         SR    XFQUOTEN,X3PADS     SUB LENGTH OF 1ST LINE
         BNP   ONELINE             NOTHING LEFT, NO CONT.
         BCTR  X3PADS,N0           REDUCE BECAUSE CONT.
         BCTR  X3PADS,N0           LINES ARE SHORTER
         SR    XEREMAIN,XEREMAIN   CLEAR EVEN REG FOR DIVIDE
         DR    XEREMAIN,X3PADS     DIVIDE BY LINE LENGTH
         LTR   XEREMAIN,XEREMAIN   IS THERE A REMAINDER
         BZ    NOPAD               NO, QUOTIENT IS CORRECT
         LA    XFQUOTEN,N1(XFQUOTEN) YES, NEED ANOTHER LINE
         SR    X3PADS,XEREMAIN     # PAD CHARS AVAIL FOR SPLIT
         B     LONG                SEE IF CONTINUED LINES FIT
ONELINE  EQU   *         MESSAGE WILL NOT BE CONTINUED        MB YM6340
         LR    XFQUOTEN,X5SCT      SCT FOR LINE TO DISPLAY    MB YM6340
         B     DYNTEST             SEE IF IT FITS             MB YM6340
NOPAD    EQU   *
         SR    X3PADS,X3PADS       NO PAD CHARS AVAILABLE
LONG     EQU   *
         MH    XFQUOTEN,DCMSCTCN   MULT BY SCT ENTRY LENGTH
         AR    XFQUOTEN,X5SCT      ADD TO CURRENT POINTER
DYNTEST  EQU   *          BE SURE MESSAGE WILL FIT            MB YM6340
         C     XFQUOTEN,DCMADDRL   HAVE WE EXCEEDED SCREEN SIZE
         BH    FULTST               YES, TEST FOR AUTO DELETE
*    IF THE MESSAGE AREA LINE ADDR IS EQUAL TO DYNAMIC DISPLAY
*    START ADDR SAVED ABOVE, CONSIDER FULL SCREEN CONDITION
         NC    DCMDSAV+N16(N4),DCMDSAV+N16   DOES A
*                                     DYNAMIC DISPLAY EXIST
         BZ    NOTBAD               NO, CONTINUE
         C     XFQUOTEN,DCMDSAV+N16 DOES LINE ADDR = DYN ADDR
         BNL   FULTST              YES, DON'T INSERT WQE
NOTBAD   EQU   *
         OI    WQEAVAIL,WQEBUFC    FLAG AS READY FOR HARDCOPY
         MVI   N2(X4MSG),BLANK     INITIALIZE MESSAGE FLAGS
         TM    WQEDESCD,WQEDCG     IS THIS CODE 7             MB YM6340
         BNO   NOT7                NO, CONTINUE CHECKING      MB YM6340
         OI    N1(X5SCT),DCMMSGC7  FLAG AS DESC CODE 7        MB YM6340
NOT7     EQU   *                                              MB YM6340
         TM    WQEXA,WQEWTOR       IS THIS A WTOR
         BNO   DESCTST             NO, TEST DESCRIPTOR CODES
         OI    N0(X5SCT),DCMMSGWR  MARK MSG AS A WTOR
         OI    N1(X5SCT),DCMMSGC7  INDICATE WTOR
         B     ACTION1             FLAG WTORS AS ACTION MESSAGES
DESCTST  EQU   *
         TM    WQEDESCD,WQEDCA+WQEDCB IS THIS AN ACTION MSG   MB YM6340
         BNZ   ACTION1             YES FLAG IT                MB YM6340
         TM    WQEDESCD,WQEDCC+WQEDCD+WQEDCE CODE 3,4,OR 5    MB YM6340
         BNZ   MOVE                YES, LEAVE FLAG BLANK      MB YM6340
INFOMSG  EQU   *
         MVI   N2(X4MSG),ISDASH    FLAG MSG
         OI    N0(X5SCT),DCMMSGIF  UPDATE SCT
         B     MOVE                READY TO MOVE MESSAGE IN
ACTION1  EQU   *
         OI    N1(X5SCT),DCMMSGAC  INDICATE ACTION MSG IN LINE
         OI    DCMIOCM1,DCMSOUND   INDICATE SOUND ALARM
         CLI   WQETXT,ACTMSG       IS THIS A SYSTEM ACTION MESSAGE
         BE    VISTST              YES, NOT FROM PROBLEM PROGRAM
         OI    N1(X5SCT),DCMMSGPP  SET PP ACTION MESSAGE
VISTST   EQU   *
MOVE     EQU   *
         MVC   N0(N2,X6DOM),WQEASID    MEMORY ID TO DOM TABLE
         MVC   N2(N3,X6DOM),WQEJSTCB+N1 MOVE TCB ADDRESS INTO          *
                                   DCM DOM TABLE              MB Y02958
         MVC   N5(N3,X6DOM),WQESEQN    MOVE SEQ NO. TO DOM TAB
         LA    X2START,WQETXT      POINT TO START OF TEXT
         TM    UCMDISP2,UCMDISPI   TIME STAMP REQ'D           JE YM6828
         BZ    TSTDJ2              NO, TEST FOR JOBID         JE YM6828
         LA    X2START,WQEPAD      POINT TO TIME              JE YM6828
         B     MOVE1               CONTINUE                   JE YM6828
TSTDJ2   TM    UCMDISP2,UCMDISPJ   JOBID REQ'D                JE YM6828
         BZ    MOVE1               NO, CONTINUE               JE YM6828
         LA    X2START,WQEPAD1     POINT TO JOBID             JE YM6828
MOVE1    DS    0H                                             JE YM6828
         LA    XEMVC,MSG1          POINT TO FIRST MVC
         LR    X9SCNSRT,X2START    SETUP TO CALCULATE BACKSCAN
BIGLOOP  EQU   *
         AH    X9SCNSRT,DCMAXLGN   BUMP SCAN START ADDRESS
         BCTR  X9SCNSRT,N0         POINT TO LAST CHARACTER
         LA    X0WORK,WQETXT-N1    GET TEXT START ADDRESS     MB YM6340
         AH    X0WORK,WQENBR+N2    ADD TEXT LENGTH
         CR    X0WORK,X9SCNSRT     END OF TEXT PAST SCAN START
         BNH   LASTLINE            NO, MOVE TO END OF TEXT
         OI    N0(X5SCT),DCMMSGCN  INDICATE CONTINUED LINE
         LTR   XDWORK,X3PADS       SAVE NUMBER OF PAD CHARS
         BZ    SPLIT               IF NONE LEFT, SPLIT AT MAX
         LR    X0WORK,X9SCNSRT     SAVE SCAN START POINT
SPLTLOOP EQU   *
         CLI   N0(X9SCNSRT),BLANK  IS THIS CHAR A BLANK
         BE    SPLIT               YES HOLD SPLIT POINT
         CLI   N0(X9SCNSRT),COMMA  IS IT A COMMA
         BE    SPLIT               YES HOLD SPLIT POINT
         BCTR  X9SCNSRT,N0         GO BACK ONE BYTE
         BCT   X3PADS,SPLTLOOP     LOOP TILL ZERO
         LR    X3PADS,XDWORK       NO PAD CHARS USED
LASTLINE EQU   *
         LR    X9SCNSRT,X0WORK     NO SPLITTING DONE
*                                  MOVE TO END OF TEXT OR MAX
SPLIT    EQU   *
         SR    X9SCNSRT,X2START    CALCULATE MOVE LENGTH
         OI    N0(X5SCT),DCMMSGIN  INDICATE LINE IS IN USE
         NI    N0(X5SCT),N255-DCMMSGJK
         MVI   N3(X4MSG),BLANK     BLANK OUT LINE
         LH    XDWORK,DCMAXLGN     LOAD FULL LENGTH OF LINE
         BCTR  XDWORK,N0           REDUCE ONE FOR EXECUTE
         BCTR  XDWORK,N0           ONE MORE FOR ONE THATS THERE
         EX    XDWORK,BLKTXT       SPREAD BLANKS IN MSG LINE
         EX    X9SCNSRT,N0(XEMVC)  MOVE TEXT INTO DCM
         MVC   N3(N1,X4MSG),WQETXT COPY ACTION CODE           JE YM6828
         LA    X2START,N1(X9SCNSRT,X2START) START FOR NEXT MOVE
         TM    DCMDSTAT,DCMDSTNM   ARE MESSAGES NUMBERED
         BZ    NONUM               NO, CHECK FOR STATUS DISPLAY
         STH   X1PARM,DCMADNUM     STORE LINE NUMBER
         CVD   X1PARM,DCMPACK      CONVERT TO DECIMAL
         UNPK  DCMCVBIN+N2(N2),DCMCVBIN+N2(N2) UNPACK
         OI    DCMCVBIN+N3,CF0     MAKE IT EBCDIC
         CLI   DCMCVBIN+N2,CF0     IS FIRST CHARACTER A ZERO
         BNE   MOVEALL             NO, MOVE IN AS IS
         MVI   DCMCVBIN+N2,BLANK   REPLACE ZERO WITH A BLANK
MOVEALL  EQU   *
         MVC   N0(N2,X4MSG),DCMCVBIN+N2 MOVE NUMBER ON SCREEN
NONUM    EQU   *
         LR    XDWORK,X1PARM        GET LINE NUMBER
         BCTR  XDWORK,N0            ZERO ORIGIN
         A     XDWORK,DCMADSEC      INDEX INTO SSCT
         TM    N0(XDWORK),DCMSECCL+DCMSECLL+DCMSECDL+DCMSECBL
         BZ    NODISP               LINE NOT UNDER A STATUS DISPLAY
         OI    DCMCMSG1,DCMUNMSG   TURN ON WRITE UNVIEWABLE MSG
         B     CONTCHK              TEST FOR A CONTINUATION LINE
NODISP   EQU   *
         CLI   DCMLINEN,N0         IS THIS FIRST MSG.
         BNZ   VISA                NO, DONT SAVE LINE NUMBER
         STC   X1PARM,DCMLINEN     WRITE PARTIAL START LINE
VISA     EQU   *
         OI    DCMIOCM1,DCMWRPAR   WRITE PARTIAL MSG AREA
         LH    XDWORK,DCMBYTCT     GET CURRENT BYTE COUNT
         AH    XDWORK,DCMCORLN     ADD LENGTH OF A LINE
         STH   XDWORK,DCMBYTCT     SAVE NEW LENGTH
CONTCHK  EQU   *
         SR    XDWORK,XDWORK       CLEAR FOR COPYING LINE FLAG
         TM    N0(X5SCT),DCMMSGCN  IS THIS LINE CONTINUED
         BNO   BUMPEM              NO, DONT COPY FLAGGING
         IC    XDWORK,N2(X4MSG)    PICK UP DIDOCS LINE FLAG
         MVC   DCMDOMLN(DCMDOMLN,X6DOM),N0(X6DOM) COPY DOM INFO
         MVC   N2(N2,X5SCT),N0(X5SCT) COPY SCT ENTRY
         OI    N3(X5SCT),DCMMSGCT  THIS IS CONTINUATION LINE
         NI    N2(X5SCT),N255-DCMMSGCN THIS ONE MAY NOT BE CONTINUED
BUMPEM   AH    X5SCT,DCMSCTCN      POINT TO NEXT SCT ENTRY
         LA    X6DOM,DCMDOMLN(X6DOM)  POINT TO NEXT DOM ENTRY
         AH    X4MSG,DCMCORLN      POINT TO NEXT LINE
         LA    X1PARM,N1(X1PARM)   GET NEXT LINE NUMBER
         LTR   XDWORK,XDWORK       ARE WE CONTINUING LINES
         BZ    MARK                NO,GET NEXT CQE
         STC   XDWORK,N2(X4MSG)    COPY DIDOCS LINE FLAG
         LR    X9SCNSRT,X2START    PRIME SCAN START POINT
         BCTR  X9SCNSRT,N0         CONTINUATION LINES ARE
         BCTR  X9SCNSRT,N0         2 CHARACTERS SHORTER
         LA    XEMVC,MSG2          POINT TO 2ND MVC
         B     BIGLOOP             LOOP UNTIL DONE
BLKTXT   MVC   N4(N0,X4MSG),N3(X4MSG) BLANK OUT LINE
MSG1     EQU   *
         MVC   N3(N0,X4MSG),N0(X2START) MOVE FOR FIRST HALF MSG  YM6828
MSG2     EQU   *
         MVC   N5(N0,X4MSG),N0(X2START) MOVE FOR 2ND HALF MSG
***********************************************************************
**                                                                   **
***                        CONSTANTS                                ***
****                                                               ****
***********************************************************************
TWODIV   DC    F'2'                CONSTANT
FOUR     DC    H'4'                CONSTANT
SIX      DC    H'6'                DOM TABLE INCREMENT
CHARINCR DC    H'1'                INCREMENT OF ONE BYTE
INTVRQ   DC    C'IEA000A '         ID NUMBER FOR INTERVENTION REQ'D MSG
***********************************************************************
         EJECT
***********************************************************************
***                        EQUATES                                   **
****                                                               ****
***********************************************************************
ID1      EQU   C'F'                1ST CHARACTER OF CSECT ID
ID2      EQU   C'2'                2ND CHARACTER OF CSECT ID
Z        EQU   C'Z'                CONSTANT
N0       EQU   0                   FIELD INCREMENT
N1       EQU   1                   FIELD INCREMENT
N2       EQU   2                   FIELD INCREMENT
N3       EQU   3                   FIELD INCREMENT
N4       EQU   4                   FIELD INCREMENT
N5       EQU   5
N8       EQU   8                   FIELD INCR
N12      EQU   12                  FIELD INCREMENT
N16      EQU   16                  FIELD INCR
N28      EQU   28                  FIELD INCR
N255     EQU   255                 USED TO TURN OFF SELECTED BITS
ACTMSG   EQU   C'*'                ASTERISK   *
BLANK    EQU   C' '                BLANK COMPARAND
COMMA    EQU   C','
ISDASH   EQU   C'-'                DASH COMPARAND
Y        EQU   C'Y'                CHARACTER FOR COMPARE
R        EQU   C'R'                R COMPARAND
TWO      EQU   2                   SCT INCREMENT FOR TM AND OI
X0A      EQU   X'0A'               MASK
X04      EQU   X'04'               MASK
X20      EQU   X'20'               MASK
X0C      EQU   X'0C'               MASK
PPACT    EQU   C'@'                PROBLEM PROGRAM ACTION MESSAGE
DCMDOMLN EQU   8
         EJECT
***********************************************************************
*        FLOW CHART STATEMENTS                                        *
***********************************************************************
*/*IEECVFT2: CHART */
*/*%A: E (,%B) ENTRY POINT */
*/*%B: P (,%CC) PROGRAM INITIALIZATION */
*/*%DD: D (YES,%EE,NO,%C) THIS DCM USED BY NIP */
*/*%EE: P (,%C) ADJUST LINE NUMBER AND BYTE COUNT FIRST TIME */
*/*%C: P (,%D) INITIALIZE POINTERS */
*/*%D: D (YES,%E,NO,INTFACE1) IS MLWTO WRITE FINISHED */
*/*%E: D (YES,%F,NO,SPLITCHK) RETURN FROM INTERFACE 1 ROUTINE */
*/*%F: P (,STARTEST) GET POINTER TO NEXT WQE */
*/*SPLITCHK: D (YES,%G,NO,MSGTST) RETURN FROM SPLIT */
*/*%G: P (,NOTBAD) PICK UP SAVED REGISTERS */
*/*MSGTST: D (YES,STARTEST,NO,FINISH) VALID ADDRESS */
*/*STARTEST: D (YES,%H,NO,TESTDISP) IS THIS A POINTER TO A NEW BLOCK */
*/*%H: P (,STARTEST) PICK UP NEXT BLOCK POINTER */
*/*TESTDISP: D (YES,INSRT,NO,ENDLIST) HAS MESSAGE BEEN DISPLAYED */
*/*ENDLIST: D (YES,FINISH,NO,%I) END OF LIST */
*/*%I: P (,STARTEST) GET NEXT WORD */
*/*INSRT: D (YES,INTFACE1,NO,%J) MLWTO FOR HARDCOPY */
*/*%J: D (YES,MLWTO,NO,PURGCONT) OTHER MLWTO */
*/*PURGCONT: D (YES,%K,NO,ROOM) IS THE PURGE BIT ON */
*/*%K: D (YES,MARK,NO,ROOM) IS AUTO DELETE REQUIRED */
*/*MARK: P (,ENDLIST) SET ON DISPLAY FLAG */
*/*MLWTO: D (YES,ENDLIST,NO,INTFACE1) ARE DESCRIPTOR CODES 8 OR 9 */
*/*INTFACE1: P (,PEXIT2) INIT FOR INTERFACE 1 EXIT */
*/*ROOM: D (YES,NOTFULL,NO,%L) IS THERE A FREE LINE */
*/*%L: P (,ROOM) ADJUST POINTERS */
*/*FULTST: P (,%M) SET ON FULL SCREEN INDICATORS */
*/*%M: D (YES,DISPTST,NO,%N) IS DEVICE IN ROLL MODE */
*/*%N: P (,DISPTST) SET WRITE MESSAGE WAITING FLAG */
*/*DISPTST: D (YES,DISP1,NO,MSGTST) IS SCREEN FULL */
*/*NOTFULL: D (YES,FULTST,NO,%O) USED BY DYNAMIC DISPLAY */
*/*%O: P (,NOTBAD) MOVE IN MESSAGE AND MARK DOM AND SCT */
*/*NOTBAD: P (,REPLY) EXAMINE MESSAGE TYPE AND MARK ACCORDINGLY */
*/*REPLY: D (YES,%P,NO,VISTST) IS THIS AN ACCEPTED REPLY */
*/*%P: P (,VISTST) INDICATE ACCEPTED REPLY */
*/*VISTST: D (YES,%Q,NO,GOON) INTERVENTION REQUIRED MSG */
*/*%Q: P (,GOON) MARK SCT AND INCREMENT COUNTER */
*/*GOON: P (,%R) COMPUTE LINE NUMBER */
*/*%R: D (YES,STATCHK,NO,NODISP) DYNAMIC DISPLAY IN AREA */
*/*NODISP: D (YES,%S,NO,VISA) IS THIS FIRST MESSAGE */
*/*%S: P (,VISA) COMPUTE LINE NUMBER */
*/*VISA: P (,CONTCHK) COMPUTE AND SAVE NEW LENGTH */
*/*STATCHK: P (,CONTCHK) SET ON WRITE UNVIEWABLE MESSAGE FLAG */
*/*CONTCHK: D (YES,%U,NO,MARK) IS THIS MESSAGE LINE CONT. */
*/*%U: P (,NOTBAD) ADJUST POINTERS */
*/*PEXIT: P (,PEXIT2) INIT FOR XCTL */
*/*PEXIT2: R XCTL TO PROPER DIDOCS MODULE */
*/*FINISH: P (,DISP1) TURN OFF OUTPUT FLAG */
*/*DISP1: P (,PEXIT) INIT FOR DISPLAY 2 XCTL */
*/*IEECVFT2: END */
         EJECT
***********************************************************************
*     ALL DSECTS FOLLOW THIS POINT:
***********************************************************************
         IEERDCM
         EJECT
         IEETDCM
         EJECT
         IHAWQE  DSECT=YES
         EJECT
         IHACTM  CXSA
         EJECT
         IHACTM  CQE
         IHADOMC
         EJECT
         IEECUCM FORMAT=NEW
         EJECT
         IEZIOB
SLASH    EQU   C'×'                AUTO DELETE BAR
X80      EQU   X'80'               END OF LIST FLAG
X01      EQU   X'01'               NOT DISPLAYED FLAG
C0       EQU   X'C0'               POINTER TO NEW LIST
X03      EQU   X'03'               MASK
CF0      EQU   C'0'                F0
         SPACE 5
         END
