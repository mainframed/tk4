         TITLE 'IEE22110  UNIT STATUS THREE,  PART OF SVC 110'
IEE22110 CSECT
*D044000,050000,050500,051000,122500,123000,123500            MH Y01043
*C117500                                                      MH Y01044
*C 65500                                                     ME SA51384
*A 61100,65600                                               ME SA51384
*A 035600-035900                                              MB YM1902
*        THIS MODULE CREATED FOR RELEASE 21 OF OS/360            S21002
***********************************************************************
*                                                                     *
* STATUS                                                              *
*    CHANGE LEVEL=0                                                   *
*                                                                     *
* FUNCTION                                                            *
*    ISSUE ERROR MESSAGES                                             *
*    FREE WORKAREA                                                    *
*    DELETE DEVNAMET                                                  *
*    RETURN TO FIRST LOAD SVC 110 (IEE00110)                          *
*                                                                     *
* ENTRY POINTS                                                        *
*         IEE22110                                                    *
*                                                                     *
* INPUT                                                               *
*    REG 11 CONTAINS THE CSCB ADDRESS WHICH CONTAINS ALL OTHER PTRS   *
*                                                                     *
* OUTPUT                                                              *
*    DISPLAY LINE ENTRIES FOR DATA CELL BINS                          *
*    ERROR MESSAGES                                                   *
*                                                                     *
* EXTERNAL REFERENCES                                                 *
*         SVC 35   (WTO)                                              *
*         SVC 10   (FREEMAIN)                                         *
*         SVC 9    (DELETE)                                           *
*                                                                     *
* EXITS,NORMAL                                                        *
*         IEE00110 TO RETURN                                          *
*                                                                     *
* EXITS,ERROR                                                         *
*         NONE                                                        *
*                                                                     *
* TABLES/WORKAREAS                                                    *
*    CSCB                                                             *
*    XSA                                                              *
*    WORKAREA                                                         *
*    UCB                                                              *
*                                                                     *
* ATTRIBUTES                                                          *
*    TYPE IV SVC, PRIVILEGED, RE-ENTRANT                         A36790
*                                                                     *
* CHARACTER CODE DEPENDENCY                                           *
*    NONE                                                             *
*                                                                     *
* NOTES                                                               *
*    NONE                                                             *
*                                                                     *
***********************************************************************
         EJECT
*/*IEE22110: CHART */
*/* E IEE22110 */
*/* P SET BASES */
*/*%RET: P SET RETURN TO UNIT 3 FLAG */
*/*%GETCODE: P GET BRANCH CODE */
*/* D (=0,PUTMSG0,=4,EXIT,=8.PUTMSG1,=12,PUTMSG3,=16,PUTMSG4) BRANCH
*/*TABLE */
*/*PUTMSG0: P (,WTO) SET UP INSUFFICIENT STORAGE MSG */
*/*PUTMSG1: P (,WTO) SET UP NO DEV WITH REQ ATTRIBUTES MSG */
*/*PUTMSG3:  P (,WTO) SET UP INVALID OPERAND MSG */
*/*PUTMSG4: P (,WTO) SET UP DEVICE DOES NOT EXIST MSG */
*/*WTO: S WTO: ISSUE SVC 35 ON ERROR MSG */
*/*EXIT: S DELETE:DEVNAMET */
*/*%FREE: S FREEMAIN:WORKAREA */
*/*OUT: R IEE00110 */
*/*IEE23110: END */
         EJECT
         BALR  R12,0
         USING *,R12
         B     ICATCH              BRANCH AROUND EYECATCHER   MB YM1902
         DC    C'01 MODULE IGC22110, FICHE IEE22110  '        MB YM1902
ZAPAREA  DC    12C'ZAP'  RESERVED FOR FIELD MODIFICATION      MB YM1902
ICATCH   EQU   *                                              MB YM1902
         USING CSCB,R11
         L     R10,XAL             GET POINTER TO WORKAREA
         USING WORKAREA,R10        ESTABLISH BASE
         SPACE 2
***********************************************************************
*                                                                     *
*                  TEST REASON FOR ENTRY
*                                                                     *
********************************************************************
         USING UCB,R7              ESTABLISH UCB BASE
         SR    R2,R2                CLEAR REGISTER AND
         IC    R2,FLAG4            GET BRANCH CODE
         B     *+4(R2)            INDEX INTO BRANCH TABLE
         B     EXIT                GET OUT
         B     PUTMSG1             PUT OUT MSG1A AND EXIT
         B     PUTMSG3             PUT OUT MSG3A AND EXIT
         SPACE 2
***********************************************************************
*                                                                     *
*     EXIT EFFECTING MODULES                                          *
*                                                                     *
***********************************************************************
PUTMSG1  EQU   *
         MVC   TEXT(MSG1ALGN),MSG1A     PUT MSG1A INTO WTO LIST
         B     WTO                 WRITE LINE
PUTMSG3  EQU   *
         MVC   TEXT(MSG3ALGN),MSG3A     PUT MSG3A INTO WTO TEXT
WTO      EQU   *
         LA    R1,TEXT+L71         BEGIN SEARCH AT END OF TEXT FIELD
WTOLOOP  EQU   *         SEARCH BACK FOR LAST TEXT CHARACTER
         CLI   ZERO(R1),BLANK      Q.   IS THIS CHARACTER A BLANK
         BNE   HITEXT              NO   FOUND END - FINISH WTO
         BCT   R1,WTOLOOP          CHECK PREVIOUS ENTRY
HITEXT   EQU   *
         MVC   ONE(L4,R1),DCODE    MOVE WTO TAIL AFTER TEXT
         LA    R1,L5(R1)           INCREMENT 5 TO COMPUT LENGTH
         LA    R0,TEXT             POINT TO TEXT BEGINNING
         SR    R1,R0               COMPUTE WTO LENGTH
         STH   R1,WTOSET           STORE LENGTH IN LIST
         MVI   MCSONE,SETONE       SET MCS FLAG FIELD
         MVI   MCSTWO,ZERO         ZERO OUT SECOND MCS BYTE
         SR    R0,R0               CLEAR REG FOR UCM ENTRY ID
         IC    R0,FROMID           PUT IN REQUESTOR'S ID
         LA    R1,WTOSET           POINT TO WTO LIST IN WORKAREA
         WTO   MF=(E,(1))          EXECUTE WTO LIST
EXIT     EQU   *
         DELETE EP=DEVNAMET        REMOVE SUPPORT FROM DNT USE COUNT
         LM    R3,R4,SWORD1        RESTORE REGS              ME SA51384
         LA    R1,SPN              WORK AREA SUBPOOL
         SLL   R1,HIGH             SHIFT TO HIGH ORDER
         LA    R0,WAL              LENGTH
         OR    R0,R1               COMBINE IN REGISTER ZERO
         LR    R1,R10              CORE POINTER
         FREEMAIN   R,LV=(0),A=(1)     FREE WORKAREA CORE
OUT      EQU   *
         LR    R2,R11             SHIFT CSCB POINTER
         LR    R13,R3             SET SAVE ADDRESS           ME SA51384
         LR    R14,R4             SET RETURN ADDRESS         ME SA51384
         MVC   XAX(L8),RETRNAME    GO BACK TO IGC00110
XCTL     EQU   *
         LA    R15,XSA             POINT TO PARA LIST
         XCTL  SF=(E,(R15))        TRANSFER CONTROL
         EJECT
***********************************************************************
*                                                                     *
*        EQUATE STATEMENTS                                            *
*                                                                     *
***********************************************************************
R0       EQU   0                   GENERAL REGISTER NOTATION
R1       EQU   1                   *                       *
R2       EQU   2                   *                       *
R3       EQU   3                   *                       *
R4       EQU   4                   *                       *
R5       EQU   5                   *                       *
R6       EQU   6                   *                       *
R7       EQU   7                   *                       *
R8       EQU   8                   *                       *
R9       EQU   9                   *                       *
R10      EQU   10                  *                       *
R11      EQU   11                  *                       *
R12      EQU   12                  *                       *
R13      EQU   13                  *                       *
R14      EQU   14                  *                       *
R15      EQU   15                  *                       *
         SPACE 2
L0       EQU   0                   THESE ARE LENGTHS AND DISPLACEMENTS
L1       EQU   1                   USED BY THIS MODULE               *
L2       EQU   2                   *                                 *
L3       EQU   3                   *                                 *
L4       EQU   4                   *                                 *
L5       EQU   5                   *                                 *
L6       EQU   6                   *                                 *
L7       EQU   7                   *                                 *
L8       EQU   8                   *                                 *
L9       EQU   9                   *                                 *
L10      EQU   10                  *                                 *
L11      EQU   11                  *                                 *
L12      EQU   12                  *                                 *
L13      EQU   13                  *                                 *
L14      EQU   14                  *                                 *
L15      EQU   15                  *                                 *
L16      EQU   16                  *                                 *
L24      EQU   24                  *                             A36165
L32      EQU   32                  *                                 *
L42      EQU   42                  *                                 *
L71      EQU   71                  *                                 *
SP       EQU   247                 SUBPOOL NUMBER FOR FREEMAIN
L255     EQU   255                 LENGTH OF 255
ZERO     EQU   0                   *                                 *
ONE      EQU   1                   *                                 *
HIGH     EQU   24                  USED FOR SHIFTING IN REGISTERS
DISP     EQU   36                  TEXT+DISP=SECOND HALF OF LINE
SPN      EQU   255                 WORKAREA SUBPOOL
USMOUNT  EQU   X'80'               MOUNT PENDING MASK
USONL    EQU   X'80'               ONLINE STATUS MASK
USALLOC  EQU   X'08'               ALLOCATED DEVICE MASK
CELLSTRG EQU   X'04'               STORAGE CELL MASK
CELLPUB  EQU   X'08'               PUBLIC CELL MASK
CELLPERM EQU   X'04'               PERMANENT CELL MASK
CELLPRIV EQU   X'10'               PRIVATE CELL MASK
CELLRESV EQU   X'20'               RESERVED CELL MASK
BLANK    EQU   C' '                USED TO BLANK WORKAREAS
F0       EQU   C'0'                USED TO CLEAR EBCDIC ZONES
BINLAST  EQU   C'9'                LAST BIN NUMBER
         EJECT
***********************************************************************
*                                                                     *
*     CONSTANTS                                                       *
*                                                                     *
***********************************************************************
H4000    DC    H'4096'             REGION SIZE - 4K
PUB      DC    C' PUB'             PUBLIC CELL
PRIV     DC    C'PRIV'             PRIVATE CELL
STRG     DC    C'STRG'             STORAGE CELL
REMOVE   DC    C'/REMOV'           REMOVEABLE CELL
RESDNT   DC    C'/RSDNT'           RESIDENT CELL
RESERV   DC    C'/RSERV'           RESERVED CELL
ONLINE   DC    C'O'                ONLINE DEVICE
OFFLIN   DC    C'OFFLINE'          OFFLINE DEVICE
ALLOC    DC    C'A'                ALLOCATED DEVICE
MOUNT    DC    C'-MTP'             MOUNT REQUEST OUTSTANDING
*                                  THE FOLLOWING BIN NUMBERS ARE A
*                                  TABLE. DO NOT DISTURB THE ORDER.
BIN0     DC    C'0'                BIN NUMBER ZERO
B1       DC    C'1'                BIN NUMBER ONE
B2       DC    C'2'                BIN NUMBER TWO
B3       DC    C'3'                BIN NUMBER THREE
B4       DC    C'4'                BIN NUMBER FOUR
B5       DC    C'5'                BIN NUMBER FIVE
B6       DC    C'6'                BIN NUMBER SIX
B7       DC    C'7'                BIN NUMBER SEVEN
B8       DC    C'8'                BIN NUMBER EIGHT
B9       DC    C'9'                BIN NUMBER NINE
DCODE    DC    XL4'01000000'       DESCRIPTOR CODE 5
RETRNAME DC    C'IGC00110'         FIRST LOAD SVC 110
         EJECT
***********************************************************************
*                                                                     *
*     MESSAGES                                                        *
*                                                                     *
***********************************************************************
MSG0A    DC    C'IEE931I INSUFFICIENT STORAGE FOR D U'        MH Y01044
MSG0ND   EQU   *
MSG0LN   EQU   MSG0ND-MSG0A       MESSAGE LENGTH
MSG1A    DC    C'IEE455I UNIT STATUS NO DEVICES WITH REQUESTED ATTRIBUTX
               ES'                ERROR MESSAGE 1
MSG1AEND EQU   *                   END OF MSG1A
MSG1ALGN EQU   MSG1AEND-MSG1A      LENGTH OF MSG1A
MSG3A    DC    C'IEE453I UNIT STATUS INVALID OPERAND RE-ENTER'    ERROR
MSG3AEND EQU   *                   END OF MSG3A
MSG3ALGN EQU   MSG3AEND-MSG3A      LENGTH OF MSG3A
         EJECT
***********************************************************************
*                                                                     *
*     DSECTS                                                          *
*                                                                     *
***********************************************************************
CSCB     DSECT
         IEECHAIN
CSCBL    EQU   CHLAST-CHAIN        LENGTH OF CSCB
         ORG   CHAIN+L32
         IEEXSA
         ORG   XSA+L24                                           A36165
WORKING  DS    5F                  WORKING STORAGE
DEVNOLN  EQU   WORKING+L8          NUMBER OF DEVICES PER LINE
DEVSPRE  EQU   DEVNOLN+L2          NUMBER OF DEVICES DISPLAYED
LASTUCB  EQU   DEVSPRE+L3          STARTING UCB ADDRESS/BIN NUMBER
FROMID   EQU   LASTUCB+L4          REQUESTING CONSOLE ID
SAVE     DS    21F                 SAVE AREA BETWEEN MODULES
         ORG   SAVE+L12
PACKWORD DS    F                   WORD USED FOR PACKING
CVBWORD  DS    F                   WORD USED FOR CONVERSION TO BINARY
         EJECT
UCB      DSECT
         IEFUCBOB
         EJECT
         EJECT
         SPACE 2
WORKAREA DSECT
         SPACE 2
         DS    0D
UCBADDRA DS    7H                  STORAGE FOR UCB ADDRESSES
UCBADDRB DS    H                   FOUND BY UNIT4
SWORD    DS    0F                  SAVE AREA ACROSS MODULES
SWORD1   DS    F                   SAVE AREA POINTER (R13)
SWORD2   DS    F                   RETURN ADDRESS (R14)
SWORD3   DS    F                   DEVICE NAME TABLE ADDRESS
UNIT1    DS    F                   HOLD ADDRESS ONE
UNIT2    DS    F                   HOLD ADDRESS TWO
UNIT3    DS    F                   HOLD ADDRESS THREE
         SPACE 4
FLAG2    DC    X'00'               COMMUNICATIONS BYTE
ONLINS   EQU   128                 DISPLAY ONLINE DEVICES
OFFLNS   EQU   64                  DISPLAY OFFLINE DEVICES
ALLS     EQU   32                  DISPLAY OFFLINE + ONLINE DEVICES
SUFEXIST EQU   16                  REQUESTING CONSOLE IS A CRT DEVICE
SINGLE   EQU   8                   SINGLE DEVICE REQUEST
COREGOT  EQU   4                   CORE GOTTEN FOR DEVICE NAME TABLE
OPEN22   EQU   2                   OPEN FLAG
R13UCBNM EQU   1                   FLAG FOR COMPSET ROUTINE
         SPACE 2
FLAG3    DC    X'00'               COMMUNICATIONS FLAGS
U2       EQU   128                TELL UNIT2 ENTRY FOR SCAN OF CDES
UNIT3RET EQU   64                  IEE22110 RETURNING TO IEE21110
OPEN332  EQU   32                  OPEN FLAG
DEVDEF   EQU   16                  DEVICE ADDRESS DEFAULTED
HEADOUTF EQU   8                   HEADING AND LABELS ARE OUT
OPEN34   EQU   4                   OPEN FLAG
OPEN32   EQU   2                   OPEN FLAG
OPEN31   EQU   1                   OPEN FLAG
         SPACE 2
*  FLAG4 BYTE IS USED FOR COMMUNICATIONS BETWEEN IEEUNIT3 AND THE
*  OTHER  D UNIT  MODULES.
FLAG4    DC    XL1'00'             COMMUNICATIONS BYTE        MC
EXITBIT  EQU   X'00'               RETURN TO CALLER           MC
MSG1     EQU   X'04'               ISSUE MESSAGE MSG1A        MC
MSG3     EQU   X'08'               ISSUE MESSAGE MSG3A        MC
MSG4     EQU   X'0C'               ISSUE MESSAGE MSG4A        MC
         SPACE 2
FLAG5    DS    XL1
NODEF    EQU   X'80'               LINE DEFAULT NOT TAKEN
INITENT  EQU   X'40'               INITIAL ENTRY TO UNIT STATUS FOUR
INITUNT2 EQU   X'20'               INITIAL ENTRY TO UNIT STATUS TWO
CRTMSG2  EQU   X'10'               MSG2 WITH HEADINGS FOR DIDOCS
UN4BINLT EQU   X'08'               LASTBIN FLAG FOR UNIT4
DISPBIT  EQU   X'04'               VALID ADDRESS FOUND
DISPEND  EQU   X'02'               END OF FRAME AND DISPLAY
OPEN51   EQU   X'01'               OPEN FLAG
         DS    0F                  FORCE FULL WORD FOR WTO
WTOSET   DS    XL1                 ZERO
TEXTLEN  DS    XL1                 OUTPUT LENGTH
LENGTH   EQU   76                  FULL LINE
MCSONE   DS    XL1                 MCS FLAGS PART ONE
SETONE   EQU   X'E1'                QUEUE BY ID, ROUTINE + DESCRIPTOR
*                                  CODES EXIST, COMMAND RESPONSE, QUEUE
*                                  BY ID ONLY.
MCSTWO   DS    XL1                 MCS FLAGS PART TWO
SETTWO   EQU   X'40'               MLWTO
         SPACE 1
MSGT     DS    0C                  BEGIN TEXT LINE
TEXT     DS    XL72                TEXT FIELD
         ORG   TEXT
TITLEID  DS    XL8                 MESSAGE ID
TITLEDGH DS    XL3                 HOURS.
TITLEDGM DS    XL3                 MINUTES.
TITLEDGS DS    XL2                 SECONDS
TITLENM  DS    XL11                DISPLAY NAME
         ORG   TEXT
UCBN1    DS    XL5                 UNIT ADDRESS
MODL1    DS    XL5                 DEVICE TYPE
USTAT1   DS    XL1                 STATUS (O,A,S,C,OFFLINE)
USTAT2   DS    XL4                 STATUS (-BSY,-MTP,-NRD)
USTAT3   DS    XL3                 STATUS (-R)
VOLID1   DS    XL7                 VOLUME SERIAL NUMBER
VOST1A   DS    XL4                 VOLUME STATE (PUB,PRIV,STRG)
VOST1B   DS    XL6                 VOLUME STATE (-REMOV,-RSDNT,-RSERV)
         ORG   TEXT+72            ORG FOR WTO TAIL
         SPACE 2
DESCODE  DS    XL2                 DESCRIPTOR CODES
RTECODE  DS    XL2                 ROUTING CODES
TYPEFLAG DS    XL2                 MESSAGE TYPES
AID      DS    XL1                AREA ID FIELD
LNUM     DS    XL1                NUMBER OF LINES
         SPACE 2
HOLD2    DS    F
NODEVICS DS    H                   NUMBER OF UNITS REQUESTED
NODEVDEF EQU   100                 DEFAULT
         SPACE 4
*  FLAG1 BYTE IS SET BY IEEUNIT1 TO REFLECT THE DEVICE TYPE(S)
*  REQUESTED BY THE D U  COMMAND.  THE BITS SET CORRESPOND TO THE BIT
*  SETTINGS IN BYTE 3 OF THE UCBTYP FIELD.
         SPACE 2
FLAG1    DC    X'00'               FLAG1 FIELD
TATYPE   EQU   UCB3TAPE           TAPE DEVICES
TPTYPE   EQU   UCB3COMM           TELECOMMUNICATIONS DEVICES
DATYPE   EQU   UCB3DACC           DIRECT ACCESS DEVICES
GRTYPE   EQU   UCB3DISP            GRAPHIC DISPLAY DEVICES
URTYPE   EQU   UCB3UREC+UCB3CHAR        UNIT RECORD + CHAR RDR DEVICES
ALLTYPE  EQU   TATYPE+TPTYPE+DATYPE+GRTYPE+URTYPE     ALLDEVICE TYPES
         SPACE 2
IDFLD    DS    XL1                AREA ID SAVE AREA
WEND     EQU   *                   END OF WORK AREA
WAL      EQU   WEND-WORKAREA       LENGTH OF WORK AREA
      END
