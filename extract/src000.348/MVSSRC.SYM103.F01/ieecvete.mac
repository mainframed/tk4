IEECVETE CSECT
         TITLE 'IEECVETE IGC5E07B MESSAGE 2'
**********************************************************************
*
* STATUS       CHANGE LEVEL 0
*
*FUNCTION      TO PLACE ERROR MESSAGES INTO THE INSTRUCTION LINE OR
*              PLACE THE ASSYNCHRONOUS ERROR MESSAGE INTO THE ENTRY
*              AREA.
*
*ENTRY POINT   IEECVETE
*
*INPUT         BIT SETTINGS IN DCMCMSG1 OR DCMCMSG2 OR DCMCMSG3 TO
*              INDICATE WHICH MESSAGE MUST BE MOVED INTO THE DCM.
*
*OUTPUT        INDICATIONS TO THE I/O ROUTINE OF WHAT TO WRITE
*
* EXTERNAL REFERENCES NONE
*
*EXITS - NORMAL  IEECVETH OR P OR R DEVICE-DEPENDENT IO
*       - ERROR  NONE
*
* TABLES/WORK AREAS DISPLAY CONTROL MODULE (CONTAINED IN DSECT)
*
* ATTRIBUTES   REFRESHABLE, PRIVILEGED, TYPE 4 SVC
*
*    CHANGES TO THIS MODULE HAVE BEEN MADE TO SUPPORT:
*         STATUS DISPLAY SUPPORT, LINE ITEM S21002, AND
*         OS/DIDOCS-ANR, LINE ITEM S21003.
*
* NOTES        THE OPERATION OF THIS MODULE DOES NOT DEPEND UPON A
*              PARTICULAR INTERNAL REPRESENTATION OF THE EXTERNAL
*              CHARACTER SET.
*
**********************************************************************
         EJECT
*/*IEECVETE: CHART */
*/*IEECVETE: E MESSAGE MOD 2 */
*/* P ESTABLISH ADDRESSABILITY */
*/* D (NO,NOTOUT,YES,) OUTPUT ONLY CONSOLE ? */
*/* P (,TESTQ) SET UP TO PUT ALL MSGS IN WARNING LINE */
*/*NOTOUT: P SET TO PUT MSGS IN INSTRUCTION LINE */
*/*TESTQ: D (NO,,YES,DELMOVE) DELETE REQ INCONSIST MSG ? */
*/* D (NO,,YES,LPMOVE) ILLEGAL L/P OPERATION MSG ? */
*/* D (NO,,YES,RAMOVE) RETRYABLE ASYNC ERR MSG ? */
*/* D (NO,,YES,CAMOVE) CONDITIONAL ASYNC ERR MSG ? */
*/* D (NO,,YES,LONGENT) COMMAND TOO LONG MSG ? */
*/* D (NO,,YES,REJECT) COMMAND REJECTED MSG ? */
*/*IOEXIT: D (NO,,YES,NOSOUND) COMMAND REJECTED MSG ? */
*/* P INDICATE SOUND ALARM */
*/*NOSOUND: P SET EXIT TO PROPER I/O ROUTINE */
*/*XCTL: R XCTL TO NEXT LOAD */
*/*DELMOVE: S BLINST RTN:GO BLANK INSTRUCTION LINE */
*/* P PUT ERROR MSG IN INSTRUCTION LINE */
*/* D (NO,DT2,YES,) NO DELETABLE MESSAGES ? */
*/* P COMPLETE MESSAGE */
*/*TURNOFF: P TURN OFF ALL MESSAGE FLAGS */
*/*INSTWRIT: P INDICATE WRITE INSTRUCTION LINE */
*/* D (NO,IOEXIT,YES,) DOES DEVICE HAVE A LITE PEN ? */
*/* P PUT ENTER - CANCEL MSG IN INSTR LINE */
*/* P (,IOEXIT) INDICATE ERROR MSG IN INSTR LINE */
*/*DT2: D (NO,DT3,YES,) INVALID DELETE RANGE ? */
*/* P (,TURNOFF) PUT REST OF MESSAGE IN LINE */
*/*DT3: D (NO,DT4,YES,) SEG = 0 ? */
*/* P (,TURNOFF) MOVE IN MESSAGE */
*/*DT4: D (NO,DT5,YES,) DISPLAY NOT ON SCREEN ? */
*/* P (,TURNOFF) MOVE IN MESSAGE */
*/*DT5: P (,TURNOFF) MOVE IN REST OF MESSAGE */
*/*LPMOVE: S BLINST RTN: BLANK INSTRUCTION LINE */
*/* P (,INSTWRIT) PUT MESSAGE IN INSTRUCTION LINE */
*/*RAMOVE: S BLENTRTN RTN: GO BLANK ENTRY AREA */
*/* P PUT ERROR MSG IN LINE */
*/* D (NO,,YES,IOEXIT) OUTPUT ONLY CONSOLE ? */
*/* P (,IOEXIT) SET UP TO WRITE BOTH LINES OF ENTRY AREA */
*/*CAMOVE: S BLENTRTN RTN: GO BLANK THE ENTRY AREA */
*/* P MOVE IN ERROR MESSAGE */
*/* D (NO,,YES,IOEXIT) OUTPUT ONLY CONSOLE ? */
*/* P (,IOEXIT) PUT MSG IN SECOND LINE OF ENTRY AREA */
*/*REJECT: S BLINST RTN:BLANK INSTRUCTION LINE */
*/* P (,INSTWRIT) PUT COMMND REJECT MSG IN INSTR LINE */
*/*BLENTRTN: E BLENTRTN RTN */
*/* P BLANK ENTRY AREA IN DCM */
*/* R BR 14 */
*/*BLINST: E BLINST RTN */
*/* P BLANK THE INSTRUCTION LINE IN DCM */
*/* R BR 14 */
*/*LONGENT: S BLINST RTN:BLANK INSTRUCTION LINE */
*/* P PUT ERROR MESSAGE IN LINE */
*/* P FIND WHERE TO INSERT CURSOR AND HOLD LOCATION */
*/* P (,INSTWRIT) INDICATE INSERT CURSOR */
*/*IEECVETE: END */
         EJECT
**********************************************************************
*  REGISTERS
*
**********************************************************************
X0       EQU   0              WORK REG
X1       EQU   1              WORK REG
X2       EQU   2              ADDR OF INSTRUC LINE
X3       EQU   3              ADDR OF ENTRY AREA
X4       EQU   4              LENGTH REG
XWORK    EQU   5              WORK REG
XWORK1   EQU   4                   WORK REG                      S21002
X6       EQU   6              WORK REG
X7       EQU   7              UCM BASE REGISTER
X8       EQU   8              WORK REG
X9       EQU   9              DCMBASE REGISTER
XA       EQU   10             PROGRAM BASE REGISTER
XB       EQU   11             CXSA BASE REGISTER
XC       EQU   12             UCM ENTRY BASE REGISTER
XD       EQU   13             WORK REG
XE       EQU   14             USED FOR BRANCHING
XF       EQU   15             WORK REG
X1PARM   EQU   1
X8UCME   EQU   8
XFBRANCH EQU   15
*
*  EQUATES
*
ID1      EQU   C'E'                1ST CHARACTER OF CSECT ID
ID2      EQU   C'E'                2ND CHARACTER OF CSECT ID
BLANK    EQU   C' '                BLANK CHARACTER
N0       EQU   0                   NUMBER
N1       EQU   1                   NUMBER
N2       EQU   2                   NUMBER
N4       EQU   4                   NUMBER
N7       EQU   7                   NUMBER
N13      EQU   13                  NUMBER
N15      EQU   15                  NUMBER
N21      EQU   21                  NUMBER
N34      EQU   34                  NUMBER
N36      EQU   36                  NUMBER                        S21002
N41      EQU   41                  NUMBER
N48      EQU   48                  DISPLACEMENT NUMBER           S21002
N50      EQU   50                  NUMBER
N52      EQU   52                  NUMBER
N59      EQU   59                  NUMBER
N62      EQU   62                  NUMBER
N65      EQU   65                  NUMBER
N255     EQU   255                 NUMBER
**********************************************************************
         EJECT
**********************************************************************
*
* ESTABLISH BASES
*
**********************************************************************
         BALR  XA,N0               ESTABLISH
         USING *,XA                  PROGRAM ADDRESSIBILITY
         LR    XB,X1               ESTABLISH
         USING CXSA,XB             CXSA ADDRESS
         L     XC,CSAUCM           ESTABLISH
         USING UCMLIST,XC            UCM ADDRESS
         L     X9,UCMXB            ESTABLISH
         L     X9,N0(X9)           LOAD TDCM POINTER             S21003
         USING DCMSTRT,X9         DCM ADDRESS
         L     X7,CSACTLM          GET POINTER TO UCM
         USING UCM,X7              ESTABLISH UCM ADDRESSIBILITY
         MVC   DCMTRACE(DCMTRLEN),DCMTRAC2 SHIFT OLD TRACE ENTRIES
         MVI   DCMTREN1,ID1        PUT CSECT ID INTO
         MVI   DCMTREN2,ID2        NEW TRACE ENTRY
*    IF THE CONSOLE IS AN OUTPUT ONLY CONSOLE, THEN              S21003
*    ALL MESSAGES WILL BE WRITTEN IN THE WARNING LINE.           S21003
         TM    UCMDISP,UCMDISPF+UCMDISPG Q. OUTPUT ONLY CONSOLE  S21003
         BZ    NOTOUT              NO--                          S21003
         L     X2,DCMAWARN         YES-LOAD WARNING LINE PT      S21003
         B     TESTQ               SKIP                          S21003
NOTOUT   EQU   *                                                 S21003
         L     X2,DCMAINS          GET ADDRESS OF INSTRUCTION LINE
TESTQ    EQU   *                                                 S21003
* TEST FOR ERROR MESSAGE TO BE MOVED IN
*
         TM    DCMCMSG2,DCMDELRI   Q. DELETE REQUEST INCONSISTENT
         BO    DELMOVE             YES
         TM    DCMCMSG2,DCMCILLP   Q. ILLEGAL LIGHT PEN OPERATION
         BO    LPMOVE              YES
         TM    DCMCMSG2,DCMASYRT   Q. RETRYABLE ASYNC ERROR
         BO    RAMOVE              YES
         TM    DCMCMSG2,DCMASYCD   Q. CONDITIONAL ASYNC ERROR
         BO    CAMOVE              YES
         TM    DCMCMSG1,DCMELONG   Q.MOVE IN ENTRY TOO LONG MSG  S21002
         BO    LONGENT             YES-                          S21002
         TM    DCMCMSG3,DCMDTBSY   Q. MOVE IN COMMAND REJECTED MSG
         BO    REJECT              YES
IOEXIT   EQU   *
         TM    DCMCMSG3,DCMDTBSY   COMMAND REJECTED MSG
         BO    NOSOUND             IF SO, DO NOT SOUND ALARM
         OI    DCMIOCM1,DCMSOUND   ELSE, INDICATE SOUND ALARM
NOSOUND  EQU   *
         L     XFBRANCH,DCMIORTN   LAOD IO ROUTINE ADDRESS
         L     X1PARM,DCMCXSVE     RESTORE CXSA ADDRESS
         BR    XFBRANCH            EXIT TO IEECVET(H,P,R, OR U)
DELMOVE  EQU   *
         BAL   XE,BLINST           GO BLANK INSTRUCTION LINE
         NI    DCMCMSG2,N255-DCMDELRI   TURN OFF BIT
         MVC   N0(N36,X2),DELRQINC  MOVE IN ERROR MESSAGE        S21002
         TM    DCMCMSG3,DCMCDLR1   Q. NO DELETABLE MESSAGES
         BNO   DT2                 NO, CONTINUE TESTING
         MVC   N36(N21,X2),DLRNODEL YES,MOVE IN REST OF ERROR MSG 21002
TURNOFF  EQU   *
         MVI   DCMCMSG3,N0         TURN OFF BITS
INSTWRIT EQU   *
         OI    DCMIOCM1,DCMWRINS   INDICATE WRITE INSTRUC LINE
         TM    DCMIOUNQ,DCMW2250   Q. IS DEVICE A LIGHT PEN DEVICE
         BNO   IOEXIT              NO, EXIT TO I/O RTN
         MVC   N59(N15,X2),ENCN   MOVE ENTER-CANCEL MSG INTO LINE
         NI    DCMIOUNQ,N255-DCMINNOR   TURN OFF NORMAL LINE BIT
         OI    DCMIOUNQ,DCMINERR   INDICATE ERROR MSG IN INST LINE
         B     IOEXIT              EXIT TO I/O ROUTINE
DT2      EQU   *
         TM    DCMCMSG3,DCMCDLR2   Q. INVALID RANGE
         BNO   DT3                 NO
         MVC   N36(N13,X2),DLRINVRG MOVE IN REST OF MSG          S21002
         B     TURNOFF             GO EXIT
DT3      EQU   *
         TM    DCMCMSG3,DCMCDLR3   Q. SEG=0
         BNO   DT4                 NO
         MVC   N36(N7,X2),DLRSEG0  MOVE IN MESSAGE               S21002
         B     TURNOFF             EXIT
DT4      EQU   *
         TM    DCMCMSG3,DCMCDLR4   Q. DISPLAY NOT ON SCREEN
         BNO   DT5                 NO
         MVC   N36(N21,X2),DLRNOTON YES,MOVE IN REST OF MESSAGE  S21002
         B     TURNOFF             EXIT
DT5      EQU   *                   INVALID OPERAND FOR DELETE REQ
         MVC   N36(N15,X2),DLRINVOP MOVE IN REST OF MSG          S21002
         B     TURNOFF             EXIT
LPMOVE   EQU   *
         NI    DCMCMSG2,N255-DCMCILLP   TURN OFF BIT
         BAL   XE,BLINST           BLANK INSTRUC LINE
         MVC   N0(N41,X2),ILLGHTPN MOVE IN MESSAGE            MH  M2070
         B     INSTWRIT            GO TO WRITE INSTRUC LIBE
RAMOVE   EQU   *
         BAL   XE,BLENTRTN         GO TO BLANK ENTRY AREA
         NI    DCMCMSG2,N255-DCMASYRT   TURN OFF BIT
         MVC   N0(N62,X3),RETASYN  MOVE IN ERROR MSG
*    IF CONSOLE IS OUTPUT ONLY, THEN WRTIE ONLY ONE LINE OF MSG. S21003
         TM    UCMDISP,UCMDISPF+UCMDISPG Q. OUTPUT ONLY CONSOLE  S21003
         BO    OUTONLY             WRITE ONLY ONE LINE           S21003
         AH    X3,DCMCORLN         INCREMENT TO LINE 2 OF ENTRY  S21003
         MVC   N0(N50,X3),RETASYN2 MOVE IN 2ND LINE
OUTONLY  EQU   *                                                 S21003
         B     IOEXIT              EXIT
CAMOVE   EQU   *
         BAL   XE,BLENTRTN         GO TO BLANK ENTRY AREA
         MVC   N0(N65,X3),CONDASYN MOVE IN ERROR MSG
*    IF CONSOLE IS OUTPUT ONLY, THEN WRTIE ONLY ONE LINE OF MSG. S21003
         TM    UCMDISP,UCMDISPF+UCMDISPG Q. OUTPUT ONLY CONSOLE  S21003
         BO    OUTONLY             WRITE ONLY ONE LINE           S21003
         AH    X3,DCMCORLN         INCREMENT TO LINE 2 OF ENTRY  S21003
         MVC   N0(N52,X3),CONDASY2 MOVE IN ERROR MSG
         NI    DCMCMSG2,N255-DCMASYCD   TURN OFF BIT
         B     IOEXIT              EXIT TO I/O ROUTINE
REJECT   EQU   *
         BAL   XE,BLINST           BLANK INSTRUCTION LINE
         MVC   N0(N34,X2),REJCTMSG      MOVE IN COMMAND REJECTED MSG
         B     INSTWRIT            GO TO WRITE INSTRUCTION LINE
BLENTRTN EQU   *         THIS ROUTINE BLANKS THE ENTRY AREA
         L     X3,DCMAENTR         GET ADDR OF ENTRY AREA
         MVI   N0(X3),BLANK        BLANK FIRST CHARACTER
         LH    X4,DCMLGNTH         GET LENGTH OF ONE LINE
         SLL   X4,N1               MULTIPLY BY 2
         BCTR  X4,N0               SUBTRACT
         BCTR  X4,N0                     TWO
         EX    X4,BLENT            EXECUTE INTO MOVE INSTRUCTION
         BR    XE                  RETURN
BLINST   EQU   *              THIS RTN BLANKS THE INSTRUCTION LINE
         LH    XWORK,DCMLGNTH      GET LENGTH OF A LINE
         BCTR  XWORK,N0            DECREMENT
         BCTR  XWORK,N0             BY TWO
         MVI   N0(X2),BLANK        BLANK FIRST CHAR OF INSTRUC LINE
         EX    XWORK,BLI           PROPAGATE BLANKS
         BR    XE                  RETURN
LONGENT  EQU   *                                                 S21002
         BAL   XE,BLINST           BLANK INSTRUCTION LINE        S21002
         MVC   N0(N48,X2),LONGEMSG MOVE IN ERROR MSG             S21002
*    COMPUTE WHERE CURSOR SHOULD BE INSERTED FOR ENTRY TOO LONG  S21002
         LH    XWORK,DCMLGNTH      GET LENGTH OF A LINE          S21002
         LH    XWORK1,H127         GET POSITION                  S21002
         SR    XWORK1,XWORK        FOR INSERTING CURSOR          S21002
         MVI   DCMCULNO,N2         INDICATE LINE 2 OF ENTRY AREA S21002
         STC   XWORK1,DCMPOSCU     INDICATE POSITION             S21002
         OI    DCMIOCM1,DCMINSC    INDICATE INSERT CURSOR        S21002
         B     INSTWRIT                                          S21002
         EJECT
**********************************************************************
*      EXECUTED INSTRUCTIONS:
*
BLI      MVC   N1(N0,X2),N0(X2)    INSTRC FOR BLANKING INSTRUCTION LINE
BLENT    MVC   N1(N0,X3),N0(X3)    INSTRUC FOR BLANKING ENTRY AREA
**********************************************************************
H127     DC    H'127'              CONSTANT                      S21002
DELRQINC DC    C'IEE151I DELETE REQUEST INCONSISTENT-' 36 CHARS  S21002
DLRNODEL DC    C'NO DELETABLE MESSAGES'      21 CHAR
DLRINVRG DC    C'INVALID RANGE'              13 CHAR
DLRSEG0  DC    C'SEG = 0'                    7  CHAR
DLRNOTON DC    C'DISPLAY NOT ON SCREEN'      21 CHAR
DLRINVOP DC    C'INVALID OPERAND'            15 CHAR
CONDASYN DC    C'IEE171I CONDITIONAL ERROR. RECENT ACTION MAY NEED TO BX
               E REPEATED.'                  65 CHAR
CONDASY2 DC    C'IEE171I PRESS CANCEL TO CONTINUE, OR SWITCH CONSOLES'
*                                            52 CHAR
RETASYN  DC    C'IEE170I RETRYABLE ERROR. RECENT ACTION MAY NEED TO BE X
               REPEATED'                     62 CHAR
RETASYN2 DC    C'IEE170I PRESS THE CANCEL KEY TO RESTORE THE SCREEN' 50
ILLGHTPN DC    C'IEE164I ILLEGAL LIGHT PEN - CURSOR DETECT'  41 CHAR
*                                                             MH  M2070
REJCTMSG DC    C'IEE342I K D,F REJECTED - TASK BUSY'  34 CHARACTERS
ENCN     DC    C'*ENTER**CANCEL*' 15 CHARS                       S21002
LONGEMSG DC    C'IEE153I ERROR- ENTRY GREATER THAN 126 CHARACTERS' 48 C
         EJECT
***********************************************************************
*                                                                     *
*                             PATCH AREA                              *
*                                                                     *
***********************************************************************
         SPACE 2
PATCH    DC    CL72'***PATCH AREA -- IGC5E07B***'                S21002
         EJECT
         IEETDCM
         EJECT
         IEECUCM  FORMAT=NEW
         EJECT
         IHACTM  CXSA
         END
