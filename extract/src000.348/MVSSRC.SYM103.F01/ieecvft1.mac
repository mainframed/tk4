IEECVFT1 CSECT
*C 042500                                                     ME YM3236
*A 121100                                                     ME YM3236
         TITLE  'IEECVFT1  PROCESSOR ZERO.'
*
* CHART ID.    F1
*
* FUNCTION.    TO INITIATE THE DIRECT ACCESS WRITE NECESSARY TO
*              UPDATE THE PFK DEFINATIONS ON SYS1.DCMLIB AND TO
*              PROCESS THE ASSOCIATED I/O COMPLETES.
*
* ATTRIBUTES.  REFRESHABLE,PRIVILEGED,TYPE 4 SVC
*
* ENTRY POINTS.          ONLY AT CSECT LABEL, IEECVFT1
*
* OPERATION.   THIS ROUTINE GAINS CONTROL FROM PFK ROUTINE 2
*              (IEECVFTB) WHEN A PFK DEFINATION HAS BEEN MADE
*              BY MEANS OF THE K N,PFK COMMAND. IT CONSTRUCTS
*              THE CHANNEL PROGRAM NECESSARY TO INITIATE THE
*              WRITE AND EXITS TO PROCESSOR ONE (IEECVET1).
*              PROCESSOR ONE GIVES CONTROL TO THIS ROUTINE UPON
*              COMPLETION OF A PFK UPDATE WRITE. THIS ROUTINE
*              DETERMINES WHETHER THE ENTIRE PFK AREA HAS BEEN
*              WRITTEN OUT, AND IF NOT, UPDATES THE CHANNEL PROGRAM
*              AND INITIATES THE NEXT WRITE. IF I/O CANNOT BE
*              STARTED, EXIT IS MADE TO THE DEVICE I/O ROUTINE
*              TO BLANK THE ENTRY AREA.
*
* EXITS ( ERROR ).       NONE.
*
* EXITS ( NORMAL ).      PROCESSOR ONE (IGC5107B), IF I/O IS STARTED.
*                        OTHERWISE, TO THE DEVICE I/O ROUTINE.
*
* INPUT.       REGISTER ONE CONTAINS A POINTER TO CXSA. IT IS ASSUMED
*              UPON ENTRY THAT A PFK UPDATE IS IN PROCESS OR DESIRED.
*
* EXTERNAL REFERENCES.   WTO (SVC 35).
*
* ERROR CONDITIONS.      DIRECT ACCESS I/O ERRORS ARE DETECTED AND AN
*                        APPROIATE MESSAGE IS PASSED TO THE OPERATOR.
*
* TABLES AND WORK AREAS.           DCM AND A RESIDENT AREA CONTAINING
*                                  CONTROL BLOCKS FOR DISK I/O.
*
* RESTRICTIONS.          NONE.
         EJECT
***********************************************************************
*        PROGRAM INITIALIZATION                                       *
***********************************************************************
*/*IEECVFT1: CHART */
*/*%A: E (,START) IEECVFT1 */
*/*START: P (,PFKTEST) PROGRAM INITIALIZATION */
*************************************************************
START    BALR  RBASE,N0            ESTABLISH                  MC
         USING *,RBASE             PROGRAM ADDRESSABILITY     MC
         B     BEGIN               BRANCH AROUND PATCH AREA
ICATCH   DC    CL72'IEECVFT1'      EYECATCHER AND FIELD MAINTENANCE
BEGIN    EQU   *                   END OF PATCH AREA
         LR    RCXSA,P1            ESTABLISH                  MC
         USING CXSA,RCXSA          CXSA ADDRESSABILITY        MC
         L     RENTRY,CSAUCM       ESTABLISH                  MC
         USING UCMLIST,RENTRY      UCMENTRY ADDRESSABILITY    MC
         L     RRDCM,UCMXB         ESTABLISH                  MC
         USING DCMTSRT,RRDCM      R DCM ADDRESSABILITY       MC
         L     RTDCM,DCMADTRN      ESTABLISH                  MC
         USING DCMSTRT,RTDCM       T DCM ADDRESSABILITY       MC
         L     RSUB,DCMSUBAD       ESTABLISH                  MC
         USING IEECVSUB,RSUB       SUB ADDRESSABILITY         MC
         L     RDCB,SUBBLK         LOAD DCB BASE REGISTER     MC
         USING SUBDCB,RDCB         ESTABLISH ADDRESSABILITY   MC
         MVC   DCMTRACE(DCMTRLEN),DCMTRAC2 SHIFT OLD TRACE ENTRIES
         MVI   DCMTREN1,ID1        PUT CSECT ID INTO
         MVI   DCMTREN2,ID2        NEW TRACE ENTRY
         L     XFBRANCH,CSAXC      ADDRESS FREELOCK SUBROUTINE
         LR    X1PARM,RCXSA        PASS CXSA ADDRESS IN REG 1
         BALR  XERTRN,XFBRANCH     RELEASE LOCKS
         EJECT
***********************************************************************
*        ROUTE CONTROL TO PFK UPDATE, I/O COMPLETE, OR EXIT.          *
***********************************************************************
*/*PFKTEST: D (YES,%B,NO,IOTEST) IS PFK UPDATE DESIRED */
*************************************************************
PFKTEST  TM    DCMCOM3,DCMPFKWR    UPDATE PFK MEMBER          MC
         BZ    IOTEST              NO, BRANCH                 MC
         NI    DCMCOM3,N255-DCMPFKWR    SET OFF FLAG          MC
         TM    SUBFLGS,SUBPKLIB    ARE PFKS SUPPORTED         MC
         BZ    EXIT                NO, IGNORE UPDATE REQUEST  MC
         OI    SUBFLGS,SUBPFKWR    SET ON SUB FLAG            MC
         SPACE 3
*/*%B: D (YES,EXIT,NO,PFKRTN) IS I/O ROUTINE BUSY */
*************************************************************
         TM    SUBDAIO,SUBIOUSE    IS I/O ROUTINE BUSY        MC
         BZ    PFKRTN              NO, BRANCH
         SPACE 3
*/*EXIT: P (,EXIT2) INIT FOR I/O ROUTINE EXIT */
*************************************************************
EXIT     L     XFBRANCH,DCMIORTN   LOAD IO ROUTINE ADDRESS
*                                  FOR EXIT TO IEECVET(H,P,R, OR U)
         SPACE 3
*/*EXIT2: P (,%P) ZERO ECB AND INIT FOR EXIT */
*************************************************************
EXIT2    MVI   UCMECB,N0           ZERO ECB                   MC
         L     X1PARM,DCMCXSVE     RESTORE CXSA ADDRESS
         SPACE 3
*/*%P: R XCTL */
*************************************************************
         BR    XFBRANCH            EXIT ADDRESS ALREADY SET
         EJECT
*/*PFKRTN: P (,%F) INITIALIZE POINTERS AND FLAGS */
*************************************************************
PFKRTN   NI    SUBFLGS,N255-SUBPFKWR  SET OFF UPDATE REQUEST  MC
         L     RB,SUBPFKAD         LOAD DATA ADDRESS          MC
         LA    RA,N4(RB)           POINT TO ELEMENT           MC
         ST    RA,SUBRLPTR         SAVE ELEMENT POINTER       MC
         SPACE 3
*/*%F: P (,EXCP) BUILD CHANNEL PROGRAM */
*************************************************************
         ST    RB,TYPE             STORE DATA POINTER         MC
         MVI   TYPE,ND             SET CCW WRITE CODE         MC
         MVC   LENGTH(N2),N0(RA)   MOVE LENGTH TO CCW         MC
         NI    LENGTH,NOSTOP       ZERO POSSIBLE STOPPER      MC
         MVC   SEEK(N8),N2(RA)     MOVE TTR TO IOB            MC
         ST    RENTRY,IOB+N4       STORE ECB POINTER          MC
         SPACE 3
*/*EXCP: P (,%G) SET ON BUSY FLAGS */
*************************************************************
EXCP     OI    UCMSTS,UCMBF        SET ON BUSY FLAG           MC
         OI    SUBDAIO,SUBIOUSE    IND I/O ROUTINE IS BUSY    MC
         OI    DCMR3FLG,DCMR3PKA   IND DA I/O COMPLETE        MC
         SPACE 3
*/*%G: S (,EXIT2) EXCP: EXECUTE CHANNEL PROGRAM */
*************************************************************
         LA    P1,IOB              LOAD IOB POINTER           MC
         SVC   0                   EXECUTE CHANNEL PROGRAM    MC
         L     XFBRANCH,DCMNPROC   LOAD PROC 1 ADDRESS
*                                  FOR EXIT TO IEECVET1
         B     EXIT2               RETURN                     MC
         EJECT
***********************************************************************
*        PROCESS I/O COMPLETE AND START NEXT PORTION IF NECESSARY     *
***********************************************************************
*/*IOTEST: D (YES,%E,NO,EXIT2) I/O COMPLETE POSTED */
*************************************************************
IOTEST   TM    UCMECB,IOCOMP       IS THERE AN I/O COMPLETE   ME YM3236
         BNZ   IODONE              YES, LOOK AT IT
         L     XFBRANCH,DCMNPROC   LOAD PROC 1 ADDRESS
         B     EXIT2               EXIT TO IEECVET1
         SPACE 3
*/*%E: P (,%N) INITIALIZE POINTERS AND FLAGS */
*************************************************************
IODONE   NI    SUBDAIO,N255-SUBIOUSE    SET OFF I/O RTN BUSY
         NI    UCMSTS,N255-UCMBF        SET OFF BUSY FLAG     MC
         L     RA,SUBRLPTR              POINT TO TTR ELEMENT  MC
         NI    UCMDEVC,N255-UCMDEVE     SET OFF I/O COMP FLG  MC
         SPACE 3
*/*%N: D (YES,%C,NO,ERROR) WAS I/O GOOD */
*************************************************************
GOODIO   CLI   UCMECB,GOOD         WAS I/O SUCCESSFUL         MC
         BNE   ERROR               NO, BRANCH                 MC
         SPACE 3
*/*%C: D (YES,CLEAR,NO,%D) WAS WRITE FINISHED */
*************************************************************
         TM    N0(RA),STOP         WAS WRITE FINISHED         MC
         BO    CLEAR               YES, BRANCH                MC
         SPACE 2
*/*%D: P (,EXCP) UPDATE CHANNEL PROGRAM */
*************************************************************
         L     RX,TYPE             LOAD DATA ADDRESS          MC
         AH    RX,LENGTH           ADD LENGTH WRITTEN         MC
         ST    RX,TYPE             STORE UPDATED ADDRESS      MC
         LA    RA,N10(RA)          UPDATE TTR POINTER         MC
         ST    RA,SUBRLPTR         SAVE POINTER               MC
         MVC   LENGTH(N2),N0(RA)   MOVE LENGTH TO CCW         MC
         NI    LENGTH,NOSTOP       ENSURE STOPPER IS OFF      MC
         MVC   SEEK(N8),N2(RA)     TTR DATA TO IOB            MC
         B     EXCP                BRANCH                     MC
         SPACE 2
*/*CLEAR: D (YES,PFKRTN,NO,EXIT) ANOTHER UPDATE REQUESTED */
*************************************************************
CLEAR    TM    SUBFLGS,SUBPFKWR    UPDATE PF KEYS             MC
         BO    PFKRTN              YES, BRANCH                MC
         B     EXIT                NO, BRANCH                 MC
         EJECT
***********************************************************************
*        OUTPUT ERROR MESSAGE                                         *
***********************************************************************
*/*ERROR: S (,NOMSG) WTO: PFK UPDATE FAILED */
*************************************************************
ERROR    GETMAIN  R,SP=255,LV=48                              MC
         LTR   REPT,REPT           WAS GETMAIN SUCCESSFUL     MC
         BNZ   NOMSG               NO, BRANCH                 MC
         LR    P2,P1               SAVE GETMAIN ADDRESS       MC
         MVC   N0(N48,P2),WTO+N4   MOVE MSG TO GETMAIN AREA   MC
         SVC   35                  ISSUE MESSAGE              MC
         LR    P1,P2               RESTORE GETMAIN PARAMETER  MC
         FREEMAIN  R,LV=48,A=(1),SP=255                       MC
         SPACE 2
*/*NOMSG: P (,EXIT) SET OFF SUPPORT AND UPDATE FLAGS */
*************************************************************
NOMSG    NI    SUBFLGS,N255-SUBPKLIB    NO LONGER UPDATE PFK  MC
         NI    SUBFLGS,N255-SUBPFKWR    SET OFF REQUEST FLAG  MC
         B     EXIT                EXIT                       MC
         SPACE 2
WTO      WTO   'IEE934I PFK UPDATE FAILED',ROUTCDE=(1)        MC
         SPACE 2
*/*IEECVFT1: END */
*************************************************************
         EJECT
***********************************************************************
*        REGISTER EQUATES                                             *
***********************************************************************
X1PARM   EQU   1
XERTRN   EQU   14
XFBRANCH EQU   15
P1       EQU   1                   PARAMETER REGISTER
P2       EQU   2                   WORK REGISTER
RBASE    EQU   3                   PROGRAM BASE
RSUB     EQU   4                   SUB BASE REGISTER
RENTRY   EQU   5                   UCMENTRY BASE REGISTER
RRDCM    EQU   6                   R DCM BASE REGISTER
RTDCM    EQU   7                   T DCM BASE REGISTER
RCXSA    EQU   9                   CXSA BASE REGISTER
RDCB     EQU   10                  TEMP BASE DURING EXCP ROUTINE
RA       EQU   11                  WORK REGISTER
RB       EQU   12                  WORK REGISTER
RX       EQU   13                  WORK REGISTER
RRTN     EQU   14                  CXSA BASE
REPT     EQU   15                  ENTRY POINT REGISTER
         SPACE 4
***********************************************************************
*        MISC PROGRAM EQUATES                                         *
***********************************************************************
ID1      EQU   C'F'                1ST CHARACTER OF CSECT ID
ID2      EQU   C'1'                2ND CHARACTER OF CSECT ID
N0       EQU   0                   NUMBER
N1       EQU   1                   NUMBER
N2       EQU   2                   NUMBER
N3       EQU   3                   NUMBER
N4       EQU   4                   NUMBER
N8       EQU   8                   NUMBER
N10      EQU   10                  NUMBER
N48      EQU   48                  NUMBER
N255     EQU   X'FF'               NUMBER
ND       EQU   X'0D'               NUMBER
NOSTOP   EQU   X'7F'               UNSTOPPER FLAG
GOOD     EQU   X'7F'               GOOD I/O INDICATOR
STOP     EQU   X'80'               STOPPER FLAG
IOCOMP   EQU   X'40'               I/O COMPLETE BIT           ME YM3236
         EJECT
***********************************************************************
*        PROGRAM DSECT AREA                                           *
***********************************************************************
         IHACTM  CXSA
         EJECT
         IEECUCM FORMAT=NEW
         EJECT
         IEERDCM
         EJECT
         IEETDCM
         EJECT
IEECVSUB DSECT
         IEECSUB  Y,N,N,N,Y,Y
         END
