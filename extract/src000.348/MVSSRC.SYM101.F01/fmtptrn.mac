         MACRO
&NAME    FMTPTRN &CODE,&LN1,&LN2,&OFFS1,&OFFS2,&LPTR,&DPTR,&LAST
         LCLA  &A,&B
&NAME    DS    0F                  TO ALIGN TO WORD BOUNDRY
         AIF   (N'&CODE EQ 1).CODE1
         AIF   (N'&CODE EQ 2).CODE2
         AIF   (N'&CODE EQ 3).CODE3
         AIF   (N'&CODE EQ 4).CODE4
         AIF   (N'&CODE EQ 5).CODE5
         DC    AL1(&CODE(1)+&CODE(2)+&CODE(3)+&CODE(4)+&CODE(5)+&CODE(6X
               ))                  CODE BYTE
         AGO   .LN
.CODE1   DC    AL1(&CODE(1))       CODE BYTE
         AGO   .LN
.CODE2   DC    AL1(&CODE(1)+&CODE(2)) CODE BYTE
         AGO   .LN
.CODE3   DC    AL1(&CODE(1)+&CODE(2)+&CODE(3)) CODE BYTE
         AGO   .LN
.CODE4   DC    AL1(&CODE(1)+&CODE(2)+&CODE(3)+&CODE(4)) CODE BYTE
         AGO   .LN
.CODE5   DC    AL1(&CODE(1)+&CODE(2)+&CODE(3)+&CODE(4)+&CODE(5))       X
                                   CODE BYTE
.LN      AIF   (T'&LN1 EQ 'O').NOLN1
         AIF   (&LN1 EQ 0).NOLN1
         AIF   (T'&LN2 EQ 'O').NOLN2
         AIF   (&LN2 EQ 0).NOLN2
         DC    AL1(&LN1*16+&LN2-17) LENGTHS FOR LABEL AND DATA
         AGO   .OFF
.NOLN1   AIF   (T'&LN2 EQ 'O').NOLN
         AIF   (&LN2 EQ 0).NOLN
         DC    AL1(&LN2-1)         LENGTH OF DATA
         AGO   .OFF
.NOLN2   DC    AL1(&LN1*16-16)     LENGTH OF LABEL
         AGO   .OFF
.NOLN    DC    AL1(0)              NO LABEL OR DATA LENGTHS
.OFF     AIF   (T'&OFFS1 EQ 'O').NOOFF1
         AIF   (T'&OFFS2 EQ 'O').NOOFF2
         DC    AL1(&OFFS1,&OFFS2)  LABEL AND DATA OFFSETS INTO LINE
         AGO   .PTRS
.NOOFF1  AIF   (T'&OFFS2 EQ 'O').NOOFFS
         DC    AL1(0,&OFFS2)       OFFSET FOR DATA INTO LINE
         AGO   .PTRS
.NOOFF2  DC    AL1(&OFFS1,0)       OFFSET OF LABEL INTO LINE
         AGO   .PTRS
.NOOFFS  DC    AL1(0,0)            NO LABEL OR DATA OFFSETS
.PTRS    AIF   (T'&LPTR EQ 'O').DPTR NO LABEL PTR IF OMMITED
         AIF   (N'&LPTR GT 1).LPTR2 FIRST VAL IS LABEL FOR PTR
         DC    A(&LPTR(1))         ADDR OF LABEL
         AGO   .DPTR
.LPTR2   ANOP
&LPTR(1) DC    A(&LPTR(2))         ADDR OF LABEL
.DPTR    AIF   (T'&DPTR EQ 'O').END NO DATA PTR IF OMITTED
         AIF   (N'&DPTR GT 1).DPTR2 FIRST VAL IS LABEL FOR PTR
         DC    A(&DPTR(1))         ADDR OF DATA
         AGO   .END
.DPTR2   ANOP
&DPTR(1) DC    A(&DPTR(2))         ADDR OF DATA
.END     AIF   (T'&LAST EQ 'O').ENDA
         AIF   ('&LAST' NE 'LAST').ENDA
         DC    AL1(0)              TO END PATTERN
.ENDA    MEND
