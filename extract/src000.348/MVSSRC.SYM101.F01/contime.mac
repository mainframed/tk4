         MACRO
&NAME    CONTIME &TIMER=,&LOCAL=YES,&STCLOCK=YES
* MACCOMP-SC1CM
* MACSTAT-@G17EP5W
* MACMEAN-CONVERT TIMER TO BINARY NUMBER REPRESENTING HH.MM.SS.T
         LCLB   &B        FLAG TO DETERMINE CODE GENERATION
&NAME    DS     0H
         AIF    ('&LOCAL' EQ 'YES' OR '&LOCAL' EQ 'NO').NEXT1
&B       SETB   (1)       SET FLAG TO TERMINATE
         IHBERMAC 54,,&LOCAL
.NEXT1   AIF    ('&STCLOCK' EQ 'YES' OR '&STCLOCK' EQ 'NO').NEXT2
&B       SETB   (1)       SET FLAG TO TERMINATE
         IHBERMAC 54,,&STCLOCK
.NEXT2   AIF   ('&TIMER' NE '').CONTNUE
&B       SETB  (1)        SET FLAG TO TERMINATE
         MNOTE 12,'THE NAME OF AN EIGHT CHARACTER FIELD WHICH CONTAINS C
                THE TIME VALUE TO BE CONVERTED MUST BE SPECIFIED.'
.CONTNUE AIF   (&B).EXIT  ANY ERRORS FOUND--EXIT
         AIF   ('&LOCAL' NE 'YES').NOLOC   LOCAL TIME REQUIRED
         L     1,CVTPTR                GET ADDRESS OF CVT TO ACCESS
         L     0,CVTTZ(1)              LOCAL TIME DIFFERENCE
.NOLOC   IHBINNRA &TIMER               LOAD REG 1 WITH ADDRESS
         AIF   ('&STCLOCK' EQ 'NO').NEXT3 NEED TO GET TOD VALUE?
         STCK  0(1)                    GET TOD CLOCK VALUE
         BC    8,IHBA&SYSNDX           IF SUCCESSFUL,CONVERT VALUE
         LA    0,4                     UNSUCCESFUL, SET BAD RETURN CODE
         SR    1,1                     UNSUCCESSFUL,SET ANSWER TO ZERO
         B     IHBB&SYSNDX             AND EXIT
.NEXT3   ANOP
IHBA&SYSNDX  L  1,0(1)                 GET HIGH ORDER WORD FROM TOD
         AIF   ('&LOCAL' NE 'YES').GMEANTM LOCAL TIME REQUIRED
         AR    1,0                     CHANGE TO LOCAL TIME
.GMEANTM SR    0,0                     ZERO EVEN REGISTER
         SLDL  0,7                     MULTIPLY BY 2**7
         CNOP  0,4                     BRANCH AROUND CONSTANTS
         B     *+8                     BRANCH AROUND CONSTANTS
         DC    F'10546875'             CONSTANT VALUE K
         D     0,*-4                   EVEN REG HAS 2**7 * NBR 2**20
*                                      U-SEC SINCE MIDNIGHT
         SRL   0,7                     FACTOR OUT 2**7 VALUE
         IHBINNRA &TIMER               LOAD ADDRESS IN REG 1
         L     1,4(1)                  GET LOW ORDER TOD VALUE
         SRDL  0,12                    REMOVE LOW ORDER BITS TO GET
*                                      RESOLUTION TO 1 MICRO-SEC
         D     0,=F'100000'            GET RESOLUTION TO .1 SEC IN
*                                      REG 1
         LTR   1,1                     IF RESULT ^= EXACTLY MIDNIGHT
         BC    7,IHBC&SYSNDX           USE THAT VALUE, OTHERWISE
         LA    1,1                     SET TO 1 INCREMENT PAST MIDNITE
IHBC&SYSNDX SR    0,0                  SET RETURN CODE TO GOOD
IHBB&SYSNDX DS   0H
.EXIT    MEXIT
         MEND
