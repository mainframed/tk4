         TITLE 'IEDQWI - CONFIGURATOR SCHEDULAR'
IEDQWI   CSECT
         ENTRY IEDQWI1                                           S99528
*CHANGE ACTIVITY AS FOLLOWS:
*A490000                                                        SA59008
*   TOTE ALIAS                                                 @OY13283
**************************************************************** S99528
*                                                              * S99528
* TITLE:                                                       * S22024
*                                                              * S22024
*    'IEDQWI - TOTE CONFIGURATOR SCHEDULER'                    * S22024
*                                                              * S22024
* MODULE NAME:                                                 * S22024
*                                                              * S22024
*    IEDQWI                                                    * S22024
*                                                              * S22024
* DESCRIPTIVE NAME:                                            * S22024
*                                                              * S22024
*    TOTE CONFIGURATOR SCHEDULER                               * S22024
*                                                              * S22024
* COPYRIGHT:                                                   * S22024
*                                                              * S22024
*    'NONE'                                                    * S22024
*                                                              * S99528
* STATUS:                                                      * S99528
*                                                              * S99528
*    CHANGE LEVEL 2                                            * S22024
*                                                              * S99528
* FUNCTIONS:                                                   * S99528
*                                                              * S99528
*    THIS MODULE CLEARS THE CDS WORK AREA AND CDS INPUT AREA,  * S99528
*    SETS UP THE OUTPUT AREA FOR CDS MEMBERS, AND PROMPTS THE  * S99528
*    USER FOR THE TYPE OF CONFIGURATION FUNCTION.              * S99528
*                                                              * S99528
*    FOR ADD REQUESTS THIS MODULE TRANSFERS CONTROL TO THE     * S99528
*    'ADD' SCHEDULER (IEDQWIA); FOR CHANGE OR DELETE REQUESTS  * S99528
*    IT TRANSFERS CONTROL TO THE 'DELETE' SCHEDULER (IEDQWID); * S22024
*    AND FOR EXHIBIT REQUESTS IT TRANSFERS CONTROL TO THE      * S99528
*    'EXHIBIT' SCHEDULER (IEDQWIE).                            * S99528
*                                                              * S99528
*    WHEN ALL REQUESTS HAVE BEEN SATISFIED, CONTROL IS RETURNED* S99528
*    TO THE OLT TEST CONTROL MODULE (IEDQWE).                  * S99528
*                                                              * S99528
* ENTRY POINTS:                                                * S99528
*                                                              * S99528
*         IEDQWI - RECEIVES CONTROL FROM IEDQWC VIA A 'XCTL'   * S99528
*                  WHEN A CONFIG REQUEST MESSAGE IS ENTERED.   * S99528
*                                                              * S99528
*         IEDQWI1- RECEIVES CONTROL FROM IEDQWIA VIA A 'XCTL'  * S99528
*                  WHEN PROCESSING OF A 'ADD' OR 'CHANGE'      * S99528
*                  FUNCTION IS COMPLETE;                       * S99528
*                                                              * S99528
*                - RECEIVES CONTROL FROM IEDQWID VIA A 'XCTL'  * S99528
*                  WHEN PROCESSING OF A 'DELETE' FUNCTION IS   * S99528
*                  COMPLETE OR A 'CHANGE' FUNCTION IS PREMA-   * S99528
*                  TURELY ENDED;                               * S99528
*                                                              * S99528
*                - RECEIVES CONTROL FROM IEDQWIE VIA A 'XCTL'  * S99528
*                  WHEN PROCESSING OF A 'EXHIBIT' FUNCTION IS  * S99528
*                  COMPLETE.                                   * S99528
*                                                              * S99528
* INPUT:                                                       * S99528
*                                                              * S99528
*    THE FOLLOWING REGISTERS ARE USED FOR INPUT:               * S99528
*                                                              * S99528
*    02 - OLTCB POINTER;                                       * S99528
*    13 - SAVE AREA ADDRESS;                                   * S99528
*    15 - ENTRY POINT ADDRESS.                                 * S99528
*                                                              * S99528
*    THE FOLLOWING OLTCB FIELDS ARE USED AS INPUT FROM IEDQWC: * S99528
*                                                              * S99528
*    CDSWORK - 256 BYTE AREA TO CREATE CNFG RECORDS FOR CDS;   * S99528
*    CDSINPUT-  80 BYTE AREA TO PASS INFO BETWEEN MODULES;     * S99528
*    CDSBLDL -  40 BYTE AREA USED AS BLDL LIST FOR PDS;        * S99528
*    CDSCESD -  56 BYTE AREA TO CREATE CESD RECORDS FOR CDS;   * S99528
*    CDSCNTL -  20 BYTE AREA TO CREATE CNTL RECORDS FOR CDS;   * S99528
*    TOTINBUF-  80 BYTE AREA FOR INPUT MESSAGES;               * S99528
*    TOTOTBUF-  80 BYTE AREA FOR OUTPUT MESSAGES;              * S99528
*    TOTSAVE4-  SAVE AREA FOR USE BY THIS MODULE.              * S99528
*                                                              * S99528
*    THE FOLLOWING OLTCB FIELD IS USED AS INPUT FROM IEDQWIA,  * S99528
*    IEDQWID, AND IEDQWIE:                                     * S99528
*                                                              * S99528
*    CDSRETFL- RETURN CODE: X'00' VALID;                       * S99528
*                           X'01' INVALID.                     * S99528
*                                                              * S99528
* OUTPUT:                                                      * S99528
*                                                              * S99528
*    THE FOLLOWING REGISTERS ARE USED FOR OUTPUT:              * S99528
*                                                              * S99528
*    02 - OLTCB POINTER;                                       * S99528
*    13 - SAVE AREA ADDRESS;                                   * S99528
*    15 - ENTRY POINT ADDRESS.                                 * S99528
*                                                              * S99528
*    THE FOLLOWING OLTCB FIELDS ARE USED FOR OUTPUT:           * S99528
*                                                              * S99528
*    CDSCESD - CESD RECORD FOR CDS MEMBERS;                    * S99528
*    CDSCNTL - CNTL RECORD FOR CDS MEMBERS;                    * S99528
*    CDSFNFLG- FUNCTION FLAG: X'02' ADD;                       * S99528
*                             X'04' DELETE;                    * S99528
*                             X'06' CHANGE;                    * S99528
*                             X'08' EXHIBIT.                   * S99528
*                                                              * S99528
* EXTERNAL ROUTINES:                                           * S99528
*                                                              * S99528
*         IEDQWIA - ADD SCHEDULER                              * S99528
*         IEDQWID - DELETE/CHANGE SCHEDULER                    * S99528
*         IEDQWIE - EXHIBIT SCHEDULER                          * S99528
*         IEDQWK  - MESSAGE MODULE                             * S99528
*         IEDQWE  - OLT CONTROL MODULE                         * S99528
*                                                              * S99528
* EXITS,NORMAL:                                                * S99528
*                                                              * S99528
*         IEDQWIA - XCTL AFTER 'PROCADD' FOR 'ADD' REQUEST     * S99528
*         IEDQWID - XCTL AFTER 'PROCHAG' FOR 'CHANGE' REQUEST  * S99528
*                   XCTL AFTER 'PROCDEL' FOR 'DELETE' REQUEST  * S99528
*         IEDQWIE - XCTL AFTER 'PROCEXB' FOR 'EXHIBIT' REQUEST * S99528
*         IEDQWE  - XCTL AFTER 'CLEANUP' FOR NORMAL COMPLETION * S99528
*                                                              * S99528
* EXITS,ERROR:                                                 * S99528
*                                                              * S99528
*         IEDQWE  - XCTL AFTER 'CLEANUP' IF 'CDSRETFL' IS X'01'* S99528
*                 - XCTL AFTER 'CLEANUP' IF 3 CONSECUTIVE      * S22024
*                   INVALID RESPONSES TO TYPE OF FUNCTION ARE  * S22024
*                   ENTERED                                    * S22024
*                                                              * S99528
* TABLES/WORK AREAS:                                           * S99528
*                                                              * S99528
*    CDSWORK - WORKAREA USED TO CREATE CDS IS CLEARED BY THIS  * S99528
*              MODULE.                                         * S99528
*                                                              * S99528
* CHARACTER CODE DEPENDENCY:                                   * S99528
*                                                              * S99528
*    NONE.                                                     * S99528
*                                                              * S99528
* NOTES:                                                       * S99528
*                                                              * S99528
*    'OLTLIB' AND 'CDSLIB' MUST HAVE BEEN PREVIOUSLY ALLOCATED * S99528
*    AND BE AVAILABLE TO TCAM.  THE UNIT CONFIGURATORS MUST    * S99528
*    HAVE BEEN PLACED IN 'OLTLIB' VIA A SOSP AND LINKAGE EDITOR* S22024
*    RUN.                                                      * S99528
*                                                              * S99528
**************************************************************** S99528
         EJECT
**************************************************************** S99528
*                                                              * S99528
*                        E Q U A T E S                         * S99528
*                                                              * S99528
**************************************************************** S99528
         SPACE
PARMREG0 EQU   0                       PARMETER REGISTER         S99528
PARMREG1 EQU   1                       PARMETER REGISTER         S99528
R2       EQU   2                       OLTCB POINTER             S99528
ERRORCNT EQU   7                       CONSECT. INVAL RESP CNT   S99528
BASREG   EQU   8                       IEDQWI BASE REGISTER      S99528
SAVEAREA EQU   13                      SAVE AREA POINTER         S99528
R14      EQU   14                      RETURN ADDRESS            S99528
SAVEADDR EQU   15                      ADDR. OF OWN SAVE AREA    S99528
ENTRYPT  EQU   15                      ENTRY POINT ADDRESS       S99528
NEXTLOC  EQU   1                       DISPLACEMENT TO NEXT BYTE S99528
MAXTRYS  EQU   3                       MAX.NO.OF INVAL RESPONSES S99528
PREVSAVE EQU   4                       DISPLACEMENT FOR PREV SAV S99528
D1       EQU   1                       LENGTH FOR COMPARE        S22024
D3       EQU   3                       LENGTH FOR COMPARE        S22024
D4       EQU   4                       LENGTH FOR COMPARE        S22024
D6       EQU   6                       LENGTH FOR COMPARE        S22024
D7       EQU   7                       LENGTH FOR COMPARE        S22024
CDSLEN   EQU   255                     LEN OF CDS REC MINUS 1    S99528
INPUTLEN EQU   76                      LENGTH OF CDS INPUT       S99528
FLGADD   EQU   X'02'                   ADD FUNCTION INDICATOR    S99528
FLGCHANG EQU   X'06'                   CHANGE FUNCTION INDICATOR S99528
FLGDELET EQU   X'04'                   DELETE FUNCTION INDICATOR S99528
FLGEXHBT EQU   X'08'                   EXHIBIT FUNCTION INDICATR S99528
FLGCLEAN EQU   X'01'                   CLEANUP, TERMINATE CONFIG S99528
ZERO     EQU   X'00'                   CONSTANT TO CLEAR AREAS   S99528
BLDLLEN  EQU   36                      LENGTH OF BLDL LIST       S99528
CESDLEN  EQU   56                      LENGTH OF CESD RECORD     S99528
CNTLLEN  EQU   20                      LENGTH OF CNTL RECORD     S99528
CALLERS  EQU   8                       OFFSET TO CALLERS SAVE    S99528
R14SVADR EQU   12                      OFFSET TO REG 14 IN SAVE  S99528
OWN      EQU   4                       OFFSET TO OWN SAVE        S99528
         EJECT
***********************************************************************
*                                                                     *
*                     I N I T I A L I Z A T I O N                     *
*                                                                     *
***********************************************************************
         SPACE
         SAVE  (14,12)                 SAVE CALLER'S REGISTERS   S22024
         LR    BASREG,ENTRYPT          LOAD BASE REG             S99528
         USING IEDQWI,BASREG           USE REG 8 AS PGM BASE     S99528
         USING TOTOLTCB,R2             USE REG 2 AS OLTCB BASE   S99528
IEDQWI   IEDHJN IEDQWII,HJN            SET DATE AND MODULE NAME  S22024
         LA    SAVEADDR,TOTSAVE4       LOAD ADR.OF OWN SAVE AREA S99528
         ST    SAVEADDR,CALLERS(SAVEAREA) STORE ADR.IN CALLERS   S99528
         ST    SAVEAREA,OWN(SAVEADDR)  STORE CALLERS IN QWI SAVE S99528
         LR    SAVEAREA,SAVEADDR       POINT TO SAVE AREA        S99528
         ENQ   ,MF=(E,ENQLIST)         DONT ALLOW ANOTHER CONFIG S22024
         LTR   ENTRYPT,ENTRYPT         OK                        S22024
         BNZ   DEQOUT                  NO BRANCH                 S22024
*                                      CONFIG STARTED MESSAGE    S99528
        IEDQMSG MSGID=100,FUNCT=CEC,LINK=YES,INBUF=CEMSG,INCNT=8 S99528
         EJECT
**************************************************************** S99528
*                                                              * S99528
*        C L E A R   F L A G S   A N D   W O R K A R E A S     * S99528
*                                                              * S99528
**************************************************************** S99528
         SPACE
PROCLEAR MVI   CDSWORK,ZERO            CLEAR CDS WORK AREA       S99528
         MVC   CDSWORK+NEXTLOC(CDSLEN),CDSWORK CLEAR CDS WORK    S99528
         MVC   CDSINPUT(INPUTLEN),CDSWORK CLEAR CDS INPUT AREA   S99528
         MVI   CDSFNFLG,ZERO           CLEAR FUNCTION FLAG       S99528
         MVI   CDSRETFL,ZERO           CLEAR RETURN FLAG         S99528
         SPACE
**************************************************************** S99528
*                                                              * S99528
*    S E T U P   O U T P U T   F O R M A T S   I N   O L T C B * S99528
*                                                              * S99528
**************************************************************** S99528
         SPACE
PROCBLDL EQU   *                       CREATE OUTPUT REC FOR PDS S99528
         MVC   CDSTTRNS(BLDLLEN),CNFGBLDL SET UP BLDL LIST       S99528
         MVC   CDSCESD(CESDLEN),CNFGCESD  SET UP CESD RECORD     S99528
         MVC   CDSCNTL(CNTLLEN),CNFGCNTL  SET UP CNTL RECORD     S99528
         B     REQFUNCT                GO REQUEST TYPE FUNCTION  S99528
         EJECT
**************************************************************** S99528
*                                                              * S99528
*     E N T R Y   P O I N T   F R O M:   I E D Q W I A         * S99528
*                                        I E D Q W I D         * S99528
*                                        I E D Q W I E         * S99528
*                                                              * S99528
**************************************************************** S99528
         SPACE
         USING *,ENTRYPT               USE REG 15 AS BASE REG    S99528
IEDQWI1  EQU   *                                                 S99528
         SAVE  (14,12),,IEDQWI1                                  S99528
         L     BASREG,BASE             SET UP BASE REG 8         S99528
         DROP  ENTRYPT                 DROP REG 15 AS BASE       S99528
         LA    SAVEADDR,TOTSAVE4       LOAD ADR.OF OWN SAVE AREA S99528
         ST    SAVEADDR,CALLERS(SAVEAREA)   STORE ADR.IN CALLERS S99528
         ST    SAVEAREA,OWN(SAVEADDR)  STORE CALLERS IN QWI SAVE S99528
         LR    SAVEAREA,SAVEADDR       POINT TO SAVE AREA        S99528
         CLI   CDSRETFL,FLGCLEAN       IS CLEANUP FLAG ON        S99528
         BE    CLEANUP                 YES, CLEANUP AND GET OUT  S99528
         B     PROCLEAR                NO,  CLR FLGS AND REQ FUN S99528
         EJECT
**************************************************************** S99528
*                                                              * S99528
*          R E Q U E S T   T Y P E   O F   F U N C T I O N     * S99528
*                                                              * S99528
**************************************************************** S99528
         SPACE
REQFUNCT LA    ERRORCNT,MAXTRYS        SET MAX NO INVALID RESP.  S99528
REQTYPE  EQU   *                       ENTER FUNCTION            S99528
         IEDQMSG MSGID=102,FUNCT=REP,INCNT=7                     S99528
         SPACE
**************************************************************** S99528
*                                                              * S99528
*        W A S   T Y P E   O F   F U N C T I O N   V A L I D   * S99528
*                                                              * S99528
**************************************************************** S99528
         SPACE
VALFUNCT EQU   *                       VALID FUNCTION CHECK      S99528
         CLC   TOTINBUF(D3),ADD        IS ADD FUNCTION SPECIFIED?S22024
         BE    PROCADD                 YES, GO PROCESS ADD REQ   S99528
         CLC   TOTINBUF(D1),A          WAS A ENTERED FOR ADD     S22024
*                                        REQUEST?                S22024
         BE    PROCADD                 YES, GO PROCESS ADD REQ   S99528
         CLC   TOTINBUF(D6),CHANGE     IS CHANGE FUNCTION        S22024
*                                        SPECIFIED?              S22024
         BE    PROCHAG                 YES, GO PROCESS CHANGE    S99528
         CLC   TOTINBUF(D1),C          WAS C ENTERED FOR CHANGE  S22024
*                                        REQUEST?                S22024
         BE    PROCHAG                 YES, GO PROCESS CHANGE    S99528
         CLC   TOTINBUF(D6),DELETE     IS DELETE SPECIFIED?      S22024
         BE    PROCDEL                 YES, GO PROCESS DELETE    S99528
         CLC   TOTINBUF(D1),D          WAS D ENTERED FOR DELETE? S22024
         BE    PROCDEL                 YES, GO PROCESS DELETE    S99528
         CLC   TOTINBUF(D7),EXHIBIT    IS EXHIBIT SPECIFIED?     S22024
         BE    PROCEXHB                YES, GO PROCESS EXHIBIT   S99528
         CLC   TOTINBUF(D1),E          WAS E ENTERED FOR EXHIBIT?S22024
         BE    PROCEXHB                YES, GO PROCESS EXHIBIT   S99528
         CLC   TOTINBUF(D4),NONE       NO FUNCTION REQUESTED?    S22024
         BE    CLEANUP                 GO CLEANUP AND GET OUT    S99528
         CLC   TOTINBUF(D1),N          WAS N ENTERED FOR NONE?   S22024
         BE    CLEANUP                 GO CLEANUP AND GET OUT    S99528
         B     BADENTRY                INVALID ENTRY             S99528
         EJECT
**************************************************************** S99528
*                                                              * S99528
*   C H E C K   N U M B E R   O F   C O N C   B A D E N T R Y S* S99528
*                                                              * S99528
**************************************************************** S99528
         SPACE
BADENTRY EQU   *                                                 S22024
         IEDQMSG MSGID=079,FUNCT=CEC                             S22024
         BCT   ERRORCNT,REQTYPE        TRY AGAIN IF CNT LOW      S99528
         B     CLEANUP                 COUNT EXCEEDED, GET OUT   S99528
         EJECT
**************************************************************** S99528
*                                                              * S99528
*     S E T   F U N C T I O N   F L A G;   R E S T O R E       * S99528
*       R E G S.;   T R A N S F E R   C O N T R O L            * S99528
*                                                              * S99528
**************************************************************** S99528
         SPACE
PROCADD  EQU   *
         OI    CDSFNFLG,FLGADD         SET FUNCT.FLG TO ADD      S99528
         L     SAVEAREA,PREVSAVE(SAVEAREA) ADDR.OF CALLERS SAVE  S99528
         LM    R14,R2,R14SVADR(SAVEAREA) RESTORE REGS 14,15,0-3  S99528
         XCTL  (2,12),EP=IEDQWIA       XCTL TO ADD SCHEDULER     S99528
         SPACE
PROCHAG  EQU   *                                                 S99528
         OI    CDSFNFLG,FLGCHANG       SET FUNCT.FLG TO CHANGE   S99528
         L     SAVEAREA,PREVSAVE(SAVEAREA) ADDR.OF CALLERS SAVE  S99528
         LM    R14,R2,R14SVADR(SAVEAREA) RESTORE REGS 14,15,0-3  S99528
         XCTL  (2,12),EP=IEDQWID       XCTL TO DELETE SCHEDULER  S99528
         SPACE
PROCDEL  EQU   *                                                 S99528
         OI    CDSFNFLG,FLGDELET       SET FUNCT.FLG TO DELETE   S99528
         L     SAVEAREA,PREVSAVE(SAVEAREA) ADDR.OF CALLERS SAVE  S99528
         LM    R14,R2,R14SVADR(SAVEAREA) RESTORE REGS 14,15,0-3  S99528
         XCTL  (2,12),EP=IEDQWID       XCTL TO DELETE SCHEDULER  S99528
         EJECT
PROCEXHB EQU   *                                                 S99528
         OI    CDSFNFLG,FLGEXHBT       SET FUNCT.FLG TO EXHIBIT  S99528
         L     SAVEAREA,PREVSAVE(SAVEAREA) ADDR.OF CALLERS SAVE  S99528
         LM    R14,R2,R14SVADR(SAVEAREA) RESTORE REGS 14,15,0-3  S99528
         XCTL  (2,12),EP=IEDQWIE       XCTL TO EXHIBIT SCHEDULER S99528
         EJECT
**************************************************************** S99528
*                                                              * S99528
*   C L E A N U P   A N D   R E T U R N   C N T L   T O   Q W E* S99528
*                                                              * S99528
**************************************************************** S99528
DEQOUT   EQU   *                                                 S22024
*        CONFIG CANNOT RUN BECAUSE THTER IS ALREADY A            S22024
*        CONFUG RUNNING. TRM IS ENDED                            S22024
         IEDQMSG MSGID=109,FUNCT=CEC   NON SHARE                 S22024
         SPACE
CLEANUP  EQU   *                                                 S99528
         CLOSE CDS40                   CLOSE LOCAL CONFIG PDS    S99528
         CLOSE CDS42                   CLOSE REMOTE CONFIG PDS   S99528
         DEQ   ,MF=(E,DEQLIST)         CONFIG LIBRARYS           S22024
         IEDQMSG MSGID=101,FUNCT=CEC   CONFIG COMPETE MSG        S99528
         L     SAVEAREA,PREVSAVE(SAVEAREA) ADDR.OF CALLERS SAVE  S99528
         LM    R14,R2,R14SVADR(SAVEAREA) RESTORE REGS           SA59008
         OI    TOTFLG06,TOTOTERM       SET SECTION TERMINATE FLG S99528
         XCTL  (2,12),EP=IEDQWE        TRANSFER TO TEST CONTROL  S99528
         EJECT
**************************************************************** S99528
*                                                              * S99528
*    I N I T I A L I Z A T I O N   C O N S T A N T S; P A T C H* S99528
*                                                              * S99528
**************************************************************** S99528
         SPACE
BASE     DC    A(IEDQWI)               ADCON TO SETUP BASE       S99528
         SPACE
CNFGBLDL  DS   0H                      CNFIG BLDL FOR PDS DIRECT S99528
         DC    XL4'00000000'           TTR                       S99528
         DC    XL2'03F2'               ATTRIBUTES                S99528
         DC    XL3'000100'             STORAGE REQUIREMENTS      S99528
         DC    XL2'0100'               LENGTH OF 1ST BLOCK       S99528
         DC    XL3'000000'             EP ADDRESS                S99528
         DC    XL3'000000'             1ST TEXT BLOCK ORIGIN     S99528
         DC    XL5'0000010000'         SSI                       S99528
         SPACE
CNFGCESD  DS   0H                      CESD RECORD INITIALIZTN   S99528
         DC    X'20'                   CESD ID                   S99528
         DC    XL3'000000'             SPARE                     S99528
         DC    XL2'0001'               ESDID OF 1ST ESD          S99528
         DC    XL2'0030'               COUNT                     S99528
         DC    CL8'        '           MEMBER NAME               S99528
         DC    X'00'                   TYPE-SD                   S99528
         DC    X'000000'               ADDRESS                   S99528
         DC    X'01'                   SEGMENT NUMBER            S99528
         DC    XL3'0000FF'             ID/LENGTH                 S99528
         DC    CL8'#OLT00  '           ESDID OF 2ND ESD          S99528
         DC    XL8'0200000000004040'   TYPE/ADDR/SEG.#/LEN       S99528
         DC    CL8'#CTLTOTE'           ESDID OF 3RD ESD          S99528
         DC    XL8'0200000000004040'   TYPE/ADDR/SEG.#/LEN       S99528
         SPACE
CNFGCNTL  DS   0H                      CNTL RECORD INITIAIZTN    S99528
         DC    X'0D'                   CNTL IDENTIFICATION       S99528
         DC    XL3'000000'             SPARE                     S99528
         DC    XL2'0004'               BYTES FOLLOWING CCW       S99528
         DC    XL2'0000'               COUNT                     S99528
         DC    XL8'0600000040000100'   CCW                       S99528
         DC    XL2'0001'               CESD ENTRY NO.            S99528
         DC    XL2'0100'               LENGTH OF TEXT RECORD     S99528
DEQLIST  DEQ   (QNAME,RNAME,8,STEP),RET=HAVE,MF=L                S22024
ENQLIST  ENQ   (QNAME,RNAME,E,8,STEP),RET=USE,MF=L               S22024
QNAME    DC    CL8'CDSLIB  '           MAJOR NAME                S22024
RNAME    DC    CL8'OLT2LIB '           MINOR NAME                S22024
         SPACE
         DS    0F                                                S99528
PATCH    DS    CL50                    RESERVED FOR MAINTENANCE  S99528
         EJECT
**************************************************************** S99528
*                                                              * S99528
*  C O N S T A N T S   U S E D   T O   C H E C K   F U N C T N * S99528
*                                                              * S99528
**************************************************************** S99528
         SPACE
ADD      DC    CL3'ADD'                ADD FUNCTION              S22024
A        DC    CL1'A'                  ADD FUNCTION              S22024
CHANGE   DC    CL6'CHANGE'             CHANGE FUNCTION           S22024
C        DC    CL1'C'                  CHANGE FUNCTION           S22024
DELETE   DC    CL6'DELETE'             DELETE FUNCTION           S22024
D        DC    CL1'D'                  DELETE FUNCTION           S22024
EXHIBIT  DC    CL7'EXHIBIT'            EXHIBIT FUNCTION          S22024
E        DC    CL1'E'                  EXHIBIT FUNCTION          S22024
NONE     DC    CL4'NONE'               NO FUNCTION               S22024
N        DC    CL1'N'                  NO FUNCTION               S22024
         EJECT
**************************************************************** S99528
*                                                              * S99528
*                       D C B ' S                              * S99528
*                                                              * S99528
**************************************************************** S99528
         SPACE
CDS40    DCB   DDNAME=OLTLIB,DSORG=PO,MACRF=(R,W),                     X
               LRECL=256,RECFM=U,BLKSIZE=256                     S99528
         SPACE
CDS42    DCB   DDNAME=CDSLIB,DSORG=PO,MACRF=(R,W),                     X
               LRECL=256,RECFM=U,BLKSIZE=256                     S99528
         EJECT
**************************************************************** S99528
*                                                              * S99528
*     T O T E   O N - L I N E   T E S T   C N T L   B L K      * S99528
*                                                              * S99528
**************************************************************** S99528
         SPACE
         OLTCB CONFG=YES                                         S99528
         END
