         TITLE 'IEDQWB2 - TOTE DUMMY TNT PROCESSOR'
IEDQWB2  CSECT
*A000000-999999                                                @Y16X5U0
* CHANGE ACTIVITY AS FOLLOWS:                                    S22024
*A428500          MOVE 425650 AFTER 428500                     @YA10965
*A169800,192000,204000,210400,210600,216400,285400,315000      @Y17XAUU
*A348400,382000,383000,391500,411200,411500,422400,425500      @Y17XAUU
*A431400,443400,498000,504000,593000,621000,695000,726500      @Y17XAUU
*A757000,761000,763000                                         @Y17XAUU
*C182500-182600,205000,608000-611000,670000,672000,            @Y17XAUU
*C756000-757000                                                @Y17XAUU
* A002921,210770,672000                                        @G36XRUV
* C210770                                                      @G36XRUV
*
         SPACE 2
         USING IEDQWB2,R15
IEDQWB2  IEDHJN STARB2,HJN
         SPACE 2
****************************************************************
*                                                              *
* MODULE NAME = IEDQWB2 (TCAM,TOTE)                            @Y17XAUU
*                                                              * S22024
* DESCRIPTIVE NAME = TOTE DUMMY TNT PROCESSOR                  @Y17XAUU
*                                                              * S22024
* COPYRIGHT = NONE                                             * S22024
*                                                              * S22024
* STATUS --                                                    *
*                                                              *
*    VERSION 10.0                                              @Y17XAUU
*                                                              *
* FUNCTION --                                                  *
*                                                              *
*    THE PURPOSE OF THIS ROUTINE IS TO BUILD DUMMY TNT ENTRIES *
*    FOR REMOTE TERMINALS SERVING AS CONTROL TERMINALS OR AS   *
*    ALTERNATE PRINTERS OR AS SNA TEST LU'S.                   @Y17XAUU
*                                                              *
*    THIS ROUTINE HAS TWO ENTRY POINTS. IEDQWB2 IS THE FIRST   *
*    ENTRY POINT. DURING THE FIRST PASS THE AMOUNT OF CORE     *
*    NEEDED BY TOTE TO BUILD DUMMY ENTRIES FOR A SINGLE REMOTE *
*    STATION IS CALCULATED. AT THIS POINT CONTROL IS RETURNED  *
*    TO THE CALLING PROGRAM TO REQUEST THE CORE. (IEDQWB2      *
*    BUILDS THE REQUEST ELEMENT FOR THE CORE NEEDED IN THE     *
*    OLTCB.) THE CALLING PROGRAM THEN ISSUES A POST TO IEDQWB  *
*    FOR THE CORE.                                             *
*                                                              *
*    IF THE REQUEST TO IEDQWB IS HONORED SUCCESSFULLY, CONTROL *
*    IS RETURNED TO THIS MODULE AT THE SECOND ENTRY POINT,     *
*    IEDQWB3. DURING THE SECOND PASS THE REAL ENTRIES FOR      *
*    THE SPECIFIED REMOTE TERMINALS ARE COPIED INTO THE AREA   *
*    OBTAINED BY IEDQWB. THE INITIAL STATUS OF REMOTE TERMINAL *
*    AND LINE ARE SAVED IN THE OLTCB.                          *
*                                                              *
* ENTRY POINTS --                                              *
*                                                              *
*    IEDQWB2 - TO CALCULATE THE AMOUNT OF CORE NEEDED TO BUILD *
*    DUMMY ENTRIES FOR A SPECIFIED REMOTE TERMINAL.            *
*                                                              *
*    IEDQWB3 - TO COPY THE REAL ENTRIES INTO THE DUMMY AREA    *
*    AND TO SET THE INITIAL STATUS FLAGS FOR THE TERMINALS.    *
*                                                              *
* PURPOSE -- SEE FUNCTION                                      @Y17XAUU
*                                                              @Y17XAUU
* MODULE TYPE = PROCEDURE                                      @Y17XAUU
*                                                              @Y17XAUU
* MODULE SIZE = 4K MAXIMUM                                     @Y17XAUU
*                                                              @Y17XAUU
* PROCESSOR = ASSEMBLER XF                                     @Y17XAUU
*                                                              @Y17XAUU
* LINKAGE = LINKED TO FROM IEDQWC,IEDQWJ2,IEDQWE,IEDQWV        @Y17XAUU
*                                                              @Y17XAUU
* MACROS = IEDHJN,AQCTL,SAVE,WAIT                              @Y17XAUU
*                                                              @Y17XAUU
* INPUT --                                                     *
*                                                              *
*    REGISTERS 02,12,13,14,15 CONTAIN THE FOLLOWING VALUES:    *
*                                                              *
*    02 - OLTCB POINTER                                        *
*    12 - BASE FOR PARAMETER LIST PASSED                       *
*    13 - SAVE AREA ADDRESS                                    *
*    14 - RETURN ADDRESS                                       *
*    15 - ENTRY POINT ADDRESS                                  *
*                                                              *
* OUTPUT --                                                    *
*                                                              *
*    REGISTER 15 CONTAIN A RETURN CODE:                        *
*                                                              *
*    X'00' - OPERATION COMPLETED SUCCESSFULLY                  *
*    X'04' - ERROR IN BUILDING DUMMY ENTRIES                   *
*                                                              *
* EXTERNAL REFERENCES --                                       *
*                                                              *
*    NONE                                                      *
*                                                              *
* EXITS,NORMAL --                                              *
*                                                              *
*    BRANCH REGISTER 14 TO CALLING PROGRAM WITH A ZERO RETURN  *
*    CODE.                                                     *
*                                                              *
* EXITS,ERROR --                                               *
*                                                              *
*    BRANCH REGISTER 14 TO CALLING PROGRAM WITH A FOUR RETURN  *
*    CODE.                                                     *
*                                                              *
* TABLES/WORK AREAS --                                         *
*                                                              *
*    OLTCB,AVT,TNT,TTE,QCB,DRQ,QCBE,PARAMETER LIST             *
*    LGB                                                       @Y17XAUU
*                                                              *
* ATTRIBUTES --                                                *
*                                                              *
*    ENABLED,PROBLEM PROGRAM MODE,TRANSIENT                    *
*                                                              *
* NOTES -- SEE BELOW                                           @Y17XAUU
*                                                              @Y17XAUU
*  DEPENDENCIES = EBCDIC CHARACTER CODE                        @Y17XAUU
*                                                              @Y17XAUU
*  REGISTERS CONVENTION = SEE REGISTERS ASSIGNMENT             @Y17XAUU
*                                                              @Y17XAUU
*  RESTRICTIONS = NONE                                         @Y17XAUU
*                                                              @Y17XAUU
*  PATCH LABEL = PATCH                                         @Y17XAUU
*                                                              *
*                                                              *
****************************************************************
         EJECT
***********************************************************************
*                                                                     *
*        EQUATES                                                      *
*                                                                     *
***********************************************************************
         SPACE 2
R0       EQU   0                  REG 0
R1       EQU   1                  REG 1
R2       EQU   2                  OLTCB POINTER/SCT POINTER
R3       EQU   3                  REG 3
R4       EQU   4                  REG 4
RAVT     EQU   4                  AVT BASE                     @Y17XAUU
R5       EQU   5                  REG 5
R6       EQU   6                  BASE REG
R7       EQU   7                  REG 7
RSAVT    EQU   7                  SAVT BASE                    @Y17XAUU
R8       EQU   8                  REG 8
R9       EQU   9                  REG 9
R10      EQU   10                 REG 10
R11      EQU   11                 REG 11
R12      EQU   12                 REG 12
R13      EQU   13                 SAVE AREA POINTER
R14      EQU   14                 RETURN ADDR
RSIB     EQU   14                 SIB BASE                     @Y17XAUU
R15      EQU   15                 ENTRY POINT ADDR
         SPACE 2
XFF      EQU   X'FF'              MASK SETTING
XF0      EQU   X'F0'              LCB MASK FOR ANR LOCAL
ANR      EQU   X'1E'              REQ TERM = ANR LO
ACTVBCD  EQU   X'46'              OP CONTROL ACTIVATE CMMD
ACTRTCD  EQU   X'08'              INVITATION LIST ACTIVE RT CD
INACT    EQU   X'48'              INACTIVATE VERB CODE
INACTRC  EQU   X'08'              TERMINAL ALREADY INACT RC
D0       EQU   0                  DISP OF 0
D1       EQU   1                  DISP OF 1
D2       EQU   2                  DISP OF 2
D3       EQU   3                  DISP OF 3
D4       EQU   4                  DISP OF 4
D5       EQU   5                  DISP OF 5
D6       EQU   6                  DISP OF 6
D7       EQU   7                  DISP OF 7
D8       EQU   8                  DISP OF 8
D9       EQU   9                  DISP OF 9
D10      EQU   10                 DISP OF 10
D11      EQU   11                 DISP OF 11
D12      EQU   12                 DISP OF 12
D13      EQU   13                 DISP OF 13
D14      EQU   14                 DISP OF 14
D15      EQU   15                 DISP OF 15
D16      EQU   16                 DISP OF 16
D20      EQU   20                 DISP OF 20
D22      EQU   22                 DISP OF 22
D24      EQU   24                 DISP OF 24
D28      EQU   28                 DISP OF 28
D32      EQU   32                 DISP OF 32
ACVT     EQU   16                 POINTER TO CVT ADDRESS
PRETERM  EQU   X'52'              DISP TO 1ST ENTRY IN TABLE
TRMAX    EQU   72                 TRM BUFFER LENGTH
SLASH    EQU   X'61'              A / CHAR
COMMA    EQU   X'6B'              A , CHAR
D17      EQU   17                 DISP OF 17
D18      EQU   18                 DISP OF 18
X40      EQU   X'40'              A BLANK CHAR
XE0      EQU   X'E0'              QCB MASK SETTING
DEFLEV   EQU   X'03'              DPRINT DEFAULT LEVEL
PARMREG0 EQU   0                  REG 0
PARMREG1 EQU   1                  REG 1
D72      EQU   72                 LENGTH OF TRM BUFFER
XBLANK   EQU   X'61'              A SLASH CHAR
TWO      EQU   2                  DISP OF TWO
FOUR     EQU   4                  DISP OF FOUR
TRMSCCT  EQU   X'10'              SECONDARY SYSTEM CONSOLE ENTRY ?
XFO      EQU   X'F0'              NUMERIC TESTER
X52      EQU   X'52'              1060 CT FLAG
X18      EQU   X'18'              DISP OF 24
X24      EQU   X'24'              DISP OF 36
X1C      EQU   X'1C'              DISP OF 28
X10      EQU   X'10'              DCB OPEN FLAG
X80      EQU   X'80'              LAST DUMMY ENTRY RQ FLAG
X14      EQU   X'14'              DISP OF 20
X07      EQU   X'07'              LINE STOPPED FLAG
HOLD     EQU   X'88'              'HOLD' OPERATOR CONTROL VERB CODE
RELEASE  EQU   X'87'              RELEASE VERB CODE            @Y17XAUU
UCB1060  EQU   X'52'              UCB SETTING FOR 1060 TERMINAL
ZERO     EQU   X'00'              CONSTANT VALUE
PROMPTFL EQU   X'01'              PROMPTER RQ FLAG
CONFIGFL EQU   X'02'              INDICATES CONFIGURATOR REQUEST
TRMMAX   EQU   256                MAXIMUM LENGTH OF TRM
ERRCD4   EQU   X'04'              ERROR RETURN CODE
ERRCD8   EQU   8                  LU ERROR RETURN CODE         @XM05678
HELDN    EQU   X'04'              HOLD MODE
SHFTBIT  EQU   128                BIT TESTER
RQINIT   EQU   X'0C'              START OF LIST
RQEND    EQU   X'80'              END OF LIST
LGBSZ    EQU   X'40'              LGB SIZE                     @Y17XAUU
MAXRUSIZ EQU   256                MAXIMUM RU SIZE              @YM08523
ADRMSK3  EQU   3                  ADDRESS MASK                 @YM08410
ADRMSK7  EQU   7                  ADDRESS MASK                 @Y17XAUU
ADRMSKF  EQU   15                 ADDRESS MASK                 @Y17XAUU
         EJECT
         SAVE  (14,12)
         SPACE
         DROP  R15
         LR    R6,R15                   LOAD BASE
         USING KTRM,R12
         USING TOTOLTCB,R2
         USING IEDQWB2,R6
         SPACE 2
         LA    R3,QWB2SAVE        MY SAVE AREA
         ST    R3,D8(R13)         STORE IN CALLER'S SAVE AREA
         ST    R13,D4(R3)         SAVE MY SAVE AREA ADDR
         LR    R13,R3                   SET R13 TO SAVE AREA
         SPACE
         L     R5,TOTAVTPT        AVT ADDRESS
         USING IEDQAVTD,R5
         LA    R7,AVTOPCOB        OP CONTROL QCB ADDRESS
         ST    R7,TOTOQCBA-D1     PUT IN P-LIST                @Y17XAUU
         MVI   TOTOKEY,D0         SET KEY FIELD                @Y17XAUU
         DROP  R5
         SPACE 2
         TM    TRMFLG01,TRMQWB3   SECOND PASS
         BO    IEDQWB3            YES - BRANCH
         SPACE 2
****************************************************************
*        ROUTINE TO CALCULATE THE AMOUNT OF CORE NEEDED TO     *
*        COPY DUMMY ENTRIES FOR A REMOTE TERMINAL.             *
****************************************************************
DUMMYTNT EQU   *
*        SET REQUEST ELEMENT FLAGS
         LA    R5,TOTWKSPC        ADDRESS OF WORK AREA         @Y17XAUU
         USING RQTNTENT,R5        SET REQUEST ELEMENT BASE     @Y17XAUU
         TM    TRMRQFLG,CTREQ     CT REQUEST                   @Y17XAUU
         BZ    DUMAPCHK           NO - CHECK AP REQUEST        @Y17XAUU
         MVI   RQTNTTYP,RQTNTCT   SET CT FLAG IN REQ ELEMENT   @Y17XAUU
         B     DUMRQGO            COMPLETE REQ ELEMENT SET UP  @Y17XAUU
         SPACE
DUMAPCHK EQU   *                                               @Y17XAUU
         TM    TRMRQFLG,APREQ     AP REQUEST                   @Y17XAUU
         BZ    DUMPRCHK           NO - CHECK PRI TD REQUEST    @Y17XAUU
         TM    TOTFLG09,TOTCTCON  CT EQU SYSCON                @Y17XAUU
         BO    DUMAPBD            YES - GO SET AP REQUEST      @Y17XAUU
         CLC   TOTCRTNT+D1(D3),TOTARTNT+D1 CT EQU AP           @YM08562
         BNE   DUMAPBD            NO - GO SET AP REQUEST       @Y17XAUU
         OI    TRMFLG01,TRMCTEAP  SHOW CT EQU AP               @Y17XAUU
         B     DUMHR              RETURN TO CALLER             @Y17XAUU
DUMAPBD  EQU   *                                               @Y17XAUU
         MVI   RQTNTTYP,RQTNTAP   SET AP FLAG IN REQ ELEMENT   @Y17XAUU
         B     DUMRQGO            COMPLETE REQ ELEMENT SET UP  @Y17XAUU
         SPACE
DUMPRCHK EQU   *                                               @Y17XAUU
         TM    TRMRQFLG,PRREQ     PRIMARY TD REQUEST           @Y17XAUU
         BZ    DUMSCCHK           NO - CHECK SEC TD REQUEST    @Y17XAUU
         TM    TOTFLG09,TOTCTCON  CT EQU SYSCON                @Y17XAUU
         BO    DUMPRAP            YES - CHECK AP               @Y17XAUU
         CLC   TOTCRTNT+D1(D3),TOTTNTPR+D1 CT EQU PRIMARY TD   @YM08562
         BNE   DUMPRAP            NO - CHECK AP                @Y17XAUU
         MVC   TOTPRENT(TOTCTEND-TOTCTENT),TOTCTENT SET UP     @Y17XAUU
*                                 PRIMARY TEST DEVICE FIELDS   @Y17XAUU
         MVI   TRMRQFLG,ALRDY     INDICATE COUNTERFEITING      @Y17XAUU
*                                 COMPLETE                     @Y17XAUU
         B     DUMHR              RETURN TO CALLER             @Y17XAUU
         SPACE
DUMPRAP  EQU   *                                               @Y17XAUU
         TM    TOTFLG09,TOTAPCON+TOTAPOUT AP EQU SYSCON/SYSOUT @Y17XAUU
         BNZ   DUMPRBD            YES - GO SET PRI TD REQ      @Y17XAUU
         TM    TOTFLG09,TOTCTCON+TOTPRECT CT EQU SYSCON AND AP
*                                 DEFAULTED                    @YM08474
         BO    DUMPRBD            YES - GO SET PRI TD REQ      @YM08474
         CLC   TOTARTNT+D1(D3),TOTTNTPR+D1 AP EQU PRIMARY TD   @YM08562
         BNE   DUMPRBD            NO - GO SET PRI TD REQ       @Y17XAUU
         MVC   TOTPRENT(TOTAPEND-TOTAPENT),TOTAPENT SET UP     @Y17XAUU
*                                 PRIMARY TEST DEVICE FIELDS   @Y17XAUU
         MVI   TRMRQFLG,ALRDY     INDICATE COUNTERFEITING      @Y17XAUU
*                                 COMPLETE(APPLIES TO QWE/QWV) @Y17XAUU
         B     DUMHR              RETURN TO CALLER             @Y17XAUU
DUMPRBD  EQU   *                                               @Y17XAUU
         MVI   RQTNTTYP,RQTNTPR   SET PRI TD FLAG IN REQUEST   @Y17XAUU
*                                 ELEMENT                      @Y17XAUU
         B     DUMRQGO            COMPLETE REQ ELEMENT SET UP  @Y17XAUU
         SPACE
DUMSCCHK EQU   *                                               @Y17XAUU
         TM    TRMRQFLG,SCREQ     SECONDARY TD REQUEST         @Y17XAUU
         BZ    DUMHR              NO - RETURN TO CALLER        @Y17XAUU
         TM    TOTFLG09,TOTCTCON  CT EQU SYSCON                @Y17XAUU
         BO    DUMSCAP            YES - CHECK AP               @Y17XAUU
         CLC   TOTCRTNT+D1(D3),TOTSRTNT+D1 CT EQU SECONDARY TD @YM08562
         BNE   DUMSCAP            NO - CHECK AP                @Y17XAUU
         MVC   TOTSCENT(TOTCTEND-TOTCTENT),TOTCTENT SET UP     @Y17XAUU
*                                 SECONDARY TEST DEVICE FIELDS @Y17XAUU
         MVI   TRMRQFLG,ALRDY     INDICATE COUNTERFEITING      @Y17XAUU
*                                 COMPLETE                     @Y17XAUU
         B     DUMHR              RETURN TO CALLER             @Y17XAUU
DUMSCAP  EQU   *                                               @Y17XAUU
         TM    TOTFLG09,TOTAPCON+TOTAPOUT AP EQU SYSCON/SYSOUT @Y17XAUU
         BNZ   DUMSCBD            YES - GO SET SEC TD REQ      @Y17XAUU
         CLC   TOTARTNT+D1(D3),TOTSRTNT+D1 AP EQU SECONDARY TD @YM08562
         BNE   DUMSCBD            NO - GO SET SEC TD REQ       @Y17XAUU
         MVC   TOTSCENT(TOTAPEND-TOTAPENT),TOTAPENT SET UP     @Y17XAUU
*                                 SECONDARY TEST DEVICE FIELDS @Y17XAUU
         MVI   TRMRQFLG,ALRDY     INDICATE COUNTERFEITING      @Y17XAUU
*                                 COMPLETE                     @Y17XAUU
         B     DUMHR              RETURN TO CALLER             @Y17XAUU
DUMSCBD  EQU   *                                               @Y17XAUU
         MVI   RQTNTTYP,RQTNTSC   SET SEC TD FLAG IN REQUEST   @Y17XAUU
*                                 ELEMENT                      @Y17XAUU
         SPACE
DUMRQGO  EQU   *                                               @Y17XAUU
         MVI   RQTNTFGS,RQTNTLST  INDICATE LAST ENTRY          @Y17XAUU
         DROP  R5                                              @Y17XAUU
         SPACE
         OI    TOTFLG10,TOTTNTRQ+TOTOLTRS  SET REQUEST/RESTART FLAGS
*
         L     R11,TNTENTRY       GET TTE ADDRESS
         LA    R8,TRMPRFSZ        GET TTE PREFIX SIZE          @Y17XAUU
         SLR   R11,R8             POINT TO TTE PREFIX          @Y17XAUU
         USING IEDNTRM,R11        SET TTE ADDRESSABILITY       @Y17XAUU
         L     R8,TRMDESTQ-D1     GET QCB ADDRESS
         LA    R8,D0(R8)          CLEAR HI-ORDER BYTE
         ST    R8,TRMQCB          SAVE IT
         USING IEDQQCB,R8
         TM    TRMSTATE,TRMPREF   3705 RESOURCE                @Y17XAUU
         BZ    DUM2A              NO
         L     R1,TOTRESPL        GET RESIDENT CONTROL BLK ADDR@Y17XAUU
         USING RESPL,R1           SET RESPL ADDRESSABILITY     @Y17XAUU
         L     R1,RESTOTMH        GET TOTE MH ADDRESS          @Y17XAUU
         DROP  R1                                              @Y17XAUU
         LTR   R1,R1              WAS MH GEN'ED                @Y17XAUU
         BZ    DUMFAIL            NO - TERMINATE REQUEST       @Y17XAUU
         OI    TRMFLG01,TRM3705   YES - SHOW IT
         CLI   TRMTYPE,TRMLUNT    LU ENTRY                     @Y17XAUU
         BNE   DUM2A              NO                           @Y17XAUU
         OI    TRMFLG01,TRMRNLU   SET LU FLAG                  @Y17XAUU
************************************************************** @Y17XAUU
*        VERIFY THAT LU IS IN A STATE WHERE BY TOTE          * @Y17XAUU
*        CAN USE THE DEVICE:                                 * @Y17XAUU
*        (1) AN SSCP-LU SESSION EXISTS AND                   * @Y17XAUU
*        (2) NO LU-LU SESSION EXISTS                         * @Y17XAUU
************************************************************** @Y17XAUU
         SLR   R1,R1              CLEAR REG                    @Y17XAUU
         IC    R1,TOTTTBEL        GET NAME LENGTH              @Y17XAUU
         BCTR  R1,R0              ADJUST LENGTH                @Y17XAUU
         L     R7,TRMTNT          GET TNT ADDRESS              @Y17XAUU
         EX    R1,TTENAME         COPY SYMBOLIC NAME           @ZM47913
         MVI   TOTOVBCD,HOLD      SET HOLD VERB CODE           @Y17XAUU
         MVC   TOTOLADR(D8),WORK1 SET RESOURCE NAME            @Y17XAUU
         MVC   TOTORLN(D1),QCBRELLN SET RLN                    @Y17XAUU
         BAL   R14,OPCREQ         ISSUE HOLD TERMINAL          @Y17XAUU
         CLI   TOTORTCD,ERRCD4    ERROR RETURN                 @Y17XAUU
         BE    DUMER1             YES - TERMINATE REQUEST      @XM05678
         SLR   RSIB,RSIB          CLEAR REG                    @Y17XAUU
         ICM   RSIB,ADRMSK7,TRMSIBPT SIB CHAIN POINTER         @Y17XAUU
         BZ    DUMER0             ERROR - NO SSCP-LU SESSION   @XM05678
         USING IEDSIBD,RSIB       SET SIB ADDRESSABILITY       @Y17XAUU
         L     RAVT,TOTAVTPT      GET AVT ADDRESS              @Y17XAUU
         USING IEDQAVTD,RAVT      SET AVT ADDRESSABILITY       @Y17XAUU
         ICM   RSAVT,ADRMSKF,AVTSAVTP GET SAVT ADDRESS         @Y17XAUU
         LA    RSAVT,D0(RSAVT)    CLEAR FLAG FIELD             @Y17XAUU
         USING IEDNSVTD,RSAVT     SET SAVT ADDRESSABILITY      @Y17XAUU
         CLC   SIBINDEX,SAVTSCPT  IF TTCIN EQU SSCP TTCIN THEN @Y17XAUU
*                                 ONLY AN SSCP-LU SESSION      @Y17XAUU
*                                 EXIST                        @Y17XAUU
         BE    DUM2A              YES - CONTINUE PROCESSING    @Y17XAUU
         DROP  RSAVT                                           @Y17XAUU
DUMER0   EQU   *                                               @XM05678
         MVI   TOTOVBCD,RELEASE   SET RELEASE VERB CODE        @Y17XAUU
         MVC   TOTOLADR(D8),WORK1 SET RESOURCE NAME            @Y17XAUU
         MVC   TOTORLN(D1),QCBRELLN SET RLN                    @Y17XAUU
         BAL   R14,OPCREQ         RELEASE RESOURCE             @Y17XAUU
DUMER1   EQU   *                                               @XM05678
         L     R14,TRMBYTE2       WORD CONTAINS TRMSTOTE FLAG  @G36XRUV
CSLOOP1  DS    0H                                              @G36XRUV
         LR    R15,R14            COPY                         @G36XRUV
         N     R15,OFFSTOTE       TURN OFF TOTE BIT IN REG     @G36XRUV
         CS    R14,R15,TRMBYTE2   RESET TRMSTOTE IN TTE        @G36XRUV
         BNZ   CSLOOP1            IF WORD ALTERED, TRY AGAIN   @G36XRUV
         L     R13,D4(R13)        RESTORE CALLERS SAVE AREA @  @XM05678
         LM    R14,R12,D12(R13)   RESTORE CALLERS REG          @XM05678
         LA    R15,ERRCD8         SET ERROR RETURN CODE        @XM05678
         BR    R14                RETURN TO CALLER             @XM05678
DUM2A    EQU   *
         TM    QCBTSOF1,QCBDELAY  QCB IN DELAY QUEUE
         BZ    DUM2B              NO - CONTINUE                  S22024
         OI    TRMFLG01,TRMDELY   SHOW QCB ON TIME DELAY QUEUE   S22024
DUM2B    EQU   *                                                 S22024
*        FIND LENGTH OF QCB
         LA    R9,QCBMSIZE        GET M-QCB SIZE
         TM    TRMFLG01,TRM3705   3705 MODE
         BZ    DUM2C              NO
         LA    R9,QCBPRFSZ(R9)    ADD ON QCB PREFIX SIZE       @Y17XAUU
DUM2C    EQU   *
         LA    R10,QCBPSIZE       GET P-QCB SIZE
*        FIND LEVEL ZERO PRIORITY QCB
         LA    R3,QCBMEND         POINT TO FIRST P-QCB
DUM2D    EQU   *
         CLI   D24(R3),D0         LEVEL ZERO ?
         BE    DUM2H              YES
         AR    R3,R10             POINT TO NEXT P-QCB
         B     DUM2D              MAKE ANOTHER CHECK
DUM2H    EQU   *
         ST    R3,KPQCB           SAVE ADDR OF P-QCB
         STH   R10,KPQCBSZ        SAVE SIZE OF P-QCB
         AR    R10,R9             GET TOTAL QCB SIZE
         STH   R10,REGSAVE        SAVE QCB LENGTH
         STH   R9,KMQCBSZ         SAVE SIZE OF MASTER QCB
         TM    TRMDEVFL+D1,TRMCONC CONC OR TERMINAL ATTACHED
         BZ    DUM2K              NO - CONTINUE NORMAL PROCESSING
         TM    QCBDSFLG,QCBDRQQ   TERMINAL A CONC
         BO    DUMFAIL            YES - TERMINATE TRM
*
*        TERMINAL IS ATTACHED TO A CONCENTRATOR
*
         OI    TRMFLG01,CONC      SHOW TERMINAL ATTACHED TO CONC
         LH    R3,KPQCBSZ         GET LEVEL 0 P-QCB SIZE
         A     R3,KPQCB           POINT TO QCB EXTENSION
         ST    R3,KEXT            SAVE EXTENSION ADDR
         USING IEDQQCBE,R3
         TM    QCBEFLG,XE0        PROCESS REQUEST
         BZ    DUM2J              YES
         B     DUMFAIL            NO - TERMINATE TRM
DUM2J    EQU   *
         SR    R9,R9              CLEAR REG
         IC    R9,QCBELGTH        LENGTH OF QCB EXTENSION
         SPACE
         SR    R4,R4              CLEAR REG
         LR    R5,R9              GET QCB EXT LENGTH
         D     R4,K4              DIVIDE TOTAL QCB EXT LEN BY 4
         SLL   R5,D2              RESTORE INTEGRAL PART
         LTR   R4,R4              REMAINDER
         BZ    DUM2J4             NO
         A     R5,K4              INSURE F-WORD BOUNDARY
         SPACE
DUM2J4   EQU   *
         STH   R5,KQCBESZ         SAVE QCBE LEN
         LR    R9,R5              SAVE VALUE
         AH    R9,KPQCBSZ         P-QCB + QCBE
         STH   R9,KPQCBSZ         SAVE COUNT
         LA    R9,QCBMSIZE        GET M-QCB LENGTH
         STH   R9,KMQCBSZ         SAVE IT
         AH    R9,KPQCBSZ         M-QCB + P-QCB+ QCBE
         STH   R9,REGSAVE         SAVE TOTAL
         L     R9,QCBECONC-D1     GET ADDR OF CONC TERMINAL ENTRY
         LA    R9,D0(R9)          CLEAR FLAG BYTE
         ST    R9,CONCTTE         SAVE TERMINAL ENTRY ADDR
         DROP  R3
         DROP  R11
         USING IEDQTRM,R9
         L     R3,TRMDESTQ-D1     GET ADDR OF DRQ
         LA    R3,D0(R3)          CLEAR FLAG BYTE
         ST    R3,KDRQ            SAVE DRQ ADDR
         LA    R3,QCBMSIZE        GET SIZE OF DRQ
         STH   R3,KDRQSZ          SAVE IT
         AH    R3,REGSAVE         M-QCB + P-QCB + QCBE + DRQ
         LR    R10,R3             SAVE TOTAL
DUM2K    EQU   *
         DROP  R8
         TM    TRMFLG01,CONC      TERMINAL ATTACHED TO CONC
         BZ    DUM2N              NO
         OI    TRMFLG01,TRMSPAS   SET SECOND PASS FLAG
         LA    R14,TRMOPNO-IEDQTRM INIT TTE COUNT FOR CONC TERM@Y17XAUU
         TM    TRMFLG01,TRM3705   3705 MODE
         BZ    DUM2L              NO
         LA    R14,TRMPRFSZ(R14) ADD ON TTE PREFIX SIZE        @Y17XAUU
DUM2L    EQU   *
         LR    R15,R14            INIT TTE COUNT FOR CONC
         SPACE
         DROP  R9
         USING IEDQTRM,R11
DUM2N    EQU   *
*        FIND LENGTH OF TTE
         L     R11,TNTENTRY       RESET TTE BASE               @YM07726
         LA    R0,D2              SET DEV DEP FIELDS TO CHECK
         LA    R8,TRMOPNO-IEDQTRM SIZE OF BASIC PORTION OF TTE @Y17XAUU
         LA    R9,TRMOPNO         POINT TO OPTION FIELD COUNT
         TM    TRMSTATE,TRMOPTFN  OPTION FIELDS PRESENT?
         BZ    DUMNOPT            NO
         SR    R7,R7              CLEAR REG 7
         IC    R7,TRMOPNO         GET OPTION FIELD COUNT
         AR    R8,R7              ADD OPTION COUNT
         TM    TRMFLG01,CONC      CONC TERMINAL
         BZ    DUMOP8             NO
         TM    TRMFLG01,TRMSPAS   TERMINAL CHECK
         BZ    DUMOP4             NO - CONC CHECK
         LA    R14,D3(R7,R14)     GET OPTION COUNT FOR CONC TERMINAL
         B     DUMOP8             BYPASS CONC
DUMOP4   EQU   *
         LA    R15,D3(R7,R15)     OPTION COUNT FOR CONC
DUMOP8   EQU   *
         LA    R10,D3(R8,R10)     ADD OPTION FIELDS LENGTH
         AR    R9,R7
         LA    R9,D3(R9)          POINT TO DEVICE DEPENENT FIELD
DUMNOPT  EQU   *   EVALUATE DEVICE DEPENDENT FIELD
         TM    TRMSTATE,TRMOPTFN  OPTION FIELDS PRESENT?
         BO    DUMNOPT1           YES
         AR    R10,R8             KEEP RUNNING TOTAL
DUMNOPT1 EQU   *
         LA    R3,D8              SET NUMBER OF BITS TO TEST
         SR    R5,R5              CLEAR REG 5
         LA    R4,SHFTBIT         SET INITIAL BIT TO TEST
DUMTEST  EQU   *
         EX    R4,DUMDEV          IS THIS BIT SET?
         BZ    DUMSHIFT           NO
         IC    R5,D0(R9)          CORE NEEDED FOR THIS ENTRY
         LA    R5,D1(R5)          TOTAL CORE
         TM    TRMFLG01,CONC      CONC TERMINAL
         BZ    DUMD8              NO
         TM    TRMFLG01,TRMSPAS   TERMINAL CHECK
         BZ    DUMD4              NO - CONC CHECK
         AR    R14,R5             TTE COUNT FOR CONC TERMINAL
         B     DUMD8              EXIT
DUMD4    EQU   *
         AR    R15,R5             TTE COUNT FOR CONC
DUMD8    EQU   *
         AR    R10,R5             KEEP RUNNING TOTAL
         AR    R9,R5              POINT TO NEXT ENTRY
DUMSHIFT EQU   *
         SRL   R4,1               ADJUST TO TEST NEXT BIT
         BCT   R3,DUMTEST         TEST NEXT BIT
         LA    R11,D1(R11)        GET ADDRESS OF NEXT BYTE TO TEST
         BCT   R0,DUMNOPT1        GO EXAMINE NEXT BYTE
         TM    TRMFLG01,CONC      TERMINAL ATTACHED TO CONC
         BZ    DUMSH4             NO
         TM    TRMFLG01,TRMSPAS   SECOND PASS COMPLETE
         BZ    DUMSH4             EXIT
         NI    TRMFLG01,XFF-TRMSPAS TURN OFF SECOND PASS FLAG
         L     R11,CONCTTE        POINT TO CONC TERMINAL ENTRY
         B     DUM2N              GET SIZE OF CONC TTE
DUMSH4   EQU   *
         TM    TRMFLG01,TRM3705   3705 MODE
         BZ    DUMSH6             NO
         LA    R10,IEDQTRM-IEDTRM(R10) ADD ON TTE PREFIX SIZE  @Y17XAUU
DUMSH6   EQU   *
         SR    R4,R4              CLEAR REG 4
         LR    R5,R10             COPY TOTAL CORE NEEDED
         D     R4,K4              DIVIDE TOTAL CORE BY 4
         SLL   R5,D2              RESTORE INTEGRAL PART OF TOTAL
         LTR   R4,R4              A  REMAINDER?
         BZ    DUMREQ             NO
         A     R5,K4              INSURE F-WORD BOUNDARY FOR QCB
DUMREQ   EQU   *
         LR    R10,R5             SAVE TOTAL CORE
         TM    TRMFLG01,CONC      CONC
         BZ    DUMR12             NO
         SPACE
         SR    R4,R4              CLEAR REG
         LR    R5,R14             GET CONC TERM TTE LEN
         D     R4,K4              DIVIDE BY 4
         SLL   R5,D2              RESTORE INTEGRAL PART
         LTR   R4,R4              REMAINDER
         BZ    DUMR4              NO
         A     R5,K4              INSURE F-WORD BOUNDARY
DUMR4    EQU   *
         STC   R5,TTETSZ          SAVE CONC TERM TTE LEN
         SPACE
         SR    R4,R4              CLEAR REG
         LR    R5,R15             GET CONC TTE LEN
         D     R4,K4              DIVIDE BY 4
         SLL   R5,D2              RESTORE INTEGRAL PART
         LTR   R4,R4              REMAINDER
         BZ    DUMR8              NO
         A     R5,K4              INSURE F-WORD BOUNDARY
DUMR8    EQU   *
         STC   R5,TTECSZ          SAVE CONC TTE SIZE
         A     R10,K12            SET UP FOR BOUNDARY ALIGNMENT
DUMR12   EQU   *
         TM    TRMFLG01,TRM3705   3705 MODE                    @Y17XAUU
         BZ    DUMR14             NO                           @Y17XAUU
         LA    R10,LGBSZ(R10)     ADD LGB SIZE TO TOTAL        @Y17XAUU
DUMR14   EQU   *                                               @Y17XAUU
         STH   R10,REGSAVE+D2     SAVE TOTAL CORE NEEDED
         LA    R1,TOTWKSPC        ADDRESS OF WORK AREA         @Y17XAUU
         USING RQTNTENT,R1        SET REQUEST ELEMENT BASE     @Y17XAUU
         STH   R10,RQTNTLNT       SET REQUEST LENGTH           @Y17XAUU
         DROP  R1                                              @Y17XAUU
         TM    TRMFLG01,TRM3705   3705 MODE                    @Y17XAUU
         BZ    DUMHR              NO - EXIT                    @Y17XAUU
         LA    R1,LGBSZ           GET LGB SIZE                 @Y17XAUU
         SLR   R10,R1             TTE/QCB SIZE                 @Y17XAUU
         STH   R10,REGSAVE+D2     SAVE LENGTH                  @Y17XAUU
         B     DUMHR              RETURN
         EJECT
OPCREQ   EQU   *                                               @Y17XAUU
         XC    TOTOLINK(D3),TOTOLINK CLEAR LINK FIELD          @Y17XAUU
         XC    TOTOTECB(D4),TOTOTECB CLEAR ECB                 @Y17XAUU
         MVI   TOTOPRI,PRISSOLT   SET POST PRIORITY            @Y17XAUU
         MVI   TOTOELNG,X18       SET ELEMENT LENGTH           @Y17XAUU
         XC    TOTORTCD,TOTORTCD  ZERO RETURN CODE FIELD       @Y17XAUU
         LA    R1,TOTOTECB        GET ECB ADDRESS              @Y17XAUU
         ST    R1,TOTOECBA        SET IT IN PLIST              @Y17XAUU
         LA    R1,TOTPLIST        GET PLIST ADDRESS            @Y17XAUU
         ST    R1,TRMSLPL         SAVE IN POST PLIST           @Y17XAUU
         ST    R1,TRMSLPL+D4      SAVE IN POST PLIST           @Y17XAUU
         MVI   TRMSLPL,RQINIT     SET REQUEST TYPE             @Y17XAUU
         MVI   TRMSLPL+D4,RQEND   SHOW END OF LIST             @Y17XAUU
         LA    R1,TRMSLPL         POINT TO POST PLIST          @Y17XAUU
         AQCTL                    POST REQUEST                 @Y17XAUU
         WAIT  ECB=TOTOTECB       WAIT FOR COMPLETION          @Y17XAUU
         BR    R14                RETURN TO CALLER             @Y17XAUU
         EJECT
****************************************************************
*        ROUTINE TO COPY DUMMY ENTRIES FOR A REMOTE TERMINAL.  *
****************************************************************
IEDQWB3  EQU   *
         NI    TRMFLG01,XFF-TRMQWB3 CLEAR SECOND PASS FLAG
         TM    TRMRQFLG,CTREQ     CT REQUEST                   @Y17XAUU
         BZ    DUM360             NO                           @Y17XAUU
         L     R3,TOTCTTNT        CT DUMMY TNT ADDRESS         @Y17XAUU
         LA    R4,TOTCTENT        CT DEVICE ENTRY ADDRESS      @Y17XAUU
         B     DUM390             CONTINUE                     @Y17XAUU
DUM360   EQU   *                                               @Y17XAUU
         TM    TRMRQFLG,APREQ     AP REQUEST                   @Y17XAUU
         BZ    DUM370             NO                           @Y17XAUU
         L     R3,TOTAPTNT        AP DUMMY TNT ADDRESS         @Y17XAUU
         LA    R4,TOTAPENT        AP DEVICE ENTRY ADDRESS      @Y17XAUU
         B     DUM390             CONTAINS                     @Y17XAUU
DUM370   EQU   *                                               @Y17XAUU
         TM    TRMRQFLG,PRREQ     PRIMARY TD REQUEST           @Y17XAUU
         BZ    DUM380             NO                           @Y17XAUU
         L     R3,TOTPDTNT        PRIMARY TD DUMMY TNT ADDRESS @Y17XAUU
         LA    R4,TOTPRENT        PRI TD DEVICE ENTRY ADDRESS  @Y17XAUU
         B     DUM390             CONTINUE                     @Y17XAUU
DUM380   EQU   *                                               @Y17XAUU
         L     R3,TOTSDTNT        SECOND TD DUMMY TNT ADDRESS  @Y17XAUU
         LA    R4,TOTSCENT        SEC TD DEVICE ENTRY ADDRESS  @Y17XAUU
DUM390   EQU   *                                               @Y17XAUU
         ST    R4,DEVENT          SAVE DEVICE ENTRY ADDRESS    @Y17XAUU
         SR    R4,R4              CLEAR REG 4
         L     R10,TERNMTAB       GET TERM NAME TABLE POINTER
         USING IEDQTNTD,R10
         IC    R4,TNTENLEN        GET MAX. NAME LENGTH
         DROP  R10
         BCTR  R4,D0              DECREMENT FOR EXECUTE
         L     R7,TRMTNT          POINT TO TRUE TNT
         EX    R4,DUMNAME         MOVE NAME TO DUMMY TNT
         EX    R4,TTENAME         NAME TO WORK AREA            @ZM47913
         LA    R5,D1(R4)          RESTORE MAX. NAME LENGTH
         AR    R5,R3              POINT TO TTE POINTER
         LR    R3,R5              SAVE TTE POINTER ADDR
         MVC   TRMSAV1+D1(D3),D0(R5)  GET TTE POINTER
         MVI   TRMSAV1,D0         ZERO HI-ORDER BYTE
         L     R5,TRMSAV1         PUT IT IN REG 5
         TM    TRMFLG01,TRM3705   3705 MODE
         BZ    DUM4E              NO
         LA    R5,IEDQTRM-IEDTRM(R5) POINT PASS TTE PREFIX     @Y17XAUU
         ST    R5,TRMSAV1         PUT IN WORK AREA
         MVC   D0(D3,R3),TRMSAV1+D1 REPLACE TTE ADDR IN TNT
         LA    R1,IEDQTRM-IEDTRM  TTE PREFIX SIZE              @Y17XAUU
         SLR   R5,R1              TTE PREFIX ADDRESS           @Y17XAUU
DUM4E    EQU   *
         TM    TRMFLG01,CONC      CONC TERMINAL
         BZ    DUMS4              NO
         SR    R3,R3              CLEAR REG
         IC    R3,TTETSZ          LENGTH OF CONC TERMINAL TTE ENTRY
         B     DUMS8              CONTINUE PROCESSING
DUMS4    EQU   *
         LH    R3,REGSAVE+D2      GET TOTAL CORE NEEDED
         SH    R3,REGSAVE         GET TTE SIZE
DUMS8    EQU   *
         BCTR  R3,D0              DECREMENT FOR EXECUTE
         L     R7,TNTENTRY        POINT TO TRUE TTE
         TM    TRMFLG01,TRM3705   3705 MODE
         BZ    DUMS10             NO
         LA    R1,IEDQTRM-IEDTRM  TTE PREFIX SIZE              @Y17XAUU
         SLR   R7,R1              TTE PREFIX ADDRESS           @Y17XAUU
DUMS10   EQU   *
         EX    R3,DUMTTE          COPY TTE
         OI    TRMLU-IEDTRM(R5),TRMLUTM SET SO THAT SESSION    @ZM47913
*                       WILL NOT BE TERMINATED WHEN Q RUNS DRY @ZM47913
         NI    TRMLU-IEDTRM(R5),XFF-TRMLUIT LU DOES NOT INIT   @ZM47913
*                                                    SESSION   @ZM47913
         LR    R10,R5             SAVE DUMMY TTE POINTER
         LA    R5,D1(R3,R5)       POINT TO PLACE FOR QCB
         TM    TRMFLG01,TRM3705   3705 MODE
         BZ    DUMS12             NO
         LA    R7,IEDQTRM-IEDTRM(R7) PT PASS TTE PREFIX (R)    @Y17XAUU
         LA    R10,IEDQTRM-IEDTRM(R10) PT PASS TTE PREFIX (D)  @Y17XAUU
         NI    TRMDEVFL-IEDQTRM(R10),XFF-TRMBUFS NO BUF SIZE   @YM08410
*                                  SPECIFIED FOR COUNTERFEIT   @YM08410
         LA    R5,QCBPRFSZ(R5)    POINT PASS PL FOR QCB PREFIX @Y17XAUU
         ST    R5,D0(R10)         PUT QCB ADDRESS IN TTE
         MVC   D0(D1,R10),D0(R7)  MOVE IN FLAG BYTE
         LA    R1,QCBPRFSZ        QCB PREFIX SIZE              @Y17XAUU
         SLR   R5,R1              POINT TO QCB PREFIX          @Y17XAUU
         B     DUMS13             BRANCH
DUMS12   EQU   *
         ST    R5,D0(R10)         PUT QCB ADDRESS IN TTE
         MVC   D0(D1,R10),D0(R7)  MOVE IN FLAG BYTE
*
DUMS13   EQU   *
         NI    D0(R10),XFF-HELDN        TURN OFF HOLD FLAG     @YA10965
         LH    R3,KMQCBSZ         GET M-QCB LENGTH
         BCTR  R3,D0              DECREMENT FOR EX
         L     R7,TRMQCB          POINT TO TRUE M-QCB
         TM    TRMFLG01,TRM3705   3705 MODE
         BZ    DUMS15             NO
         LA    R1,QCBPRFSZ        QCB PREFIX SIZE              @Y17XAUU
         SLR   R7,R1              POINT TO QCB PREFIX          @Y17XAUU
DUMS15   EQU   *
         EX    R3,DUMQCB          COPY M-QCB
         TM    TRMFLG01,CONC      CONC TERMINAL
         BZ    DUMHD              NO
         LA    R14,QCBMSIZE       GET M-QCB LENGTH
         LA    R15,QCBPSIZE       GET P-QCB LENGTH
         AR    R14,R15            OFFSET TO EXTENSION
         STH   R14,D24(R5)        PUT IN DUMMY M-QCB
         AR    R14,R5             GET ADDR OF PLACE FOR DUMMY EXT
         ST    R14,DUMEXTA        SAVE IT
DUMHD    EQU   *
         TM    TRMFLG01,TRM3705   3705 MODE
         BZ    DUMHD2             NO
         LA    R1,QCBPRFSZ        QCB PREFIX SIZE              @Y17XAUU
         SLR   R3,R1              ADJUST COUNT                 @Y17XAUU
         LA    R5,QCBPRFSZ(R5)    POINT PASS QCB PREFIX        @Y17XAUU
DUMHD2   EQU   *
         ST    R5,KMQCB           SAVE DUMMY M-QCB ADDR
         LA    R5,D1(R3,R5)       POINT TO PLACE FOR P-QCB
         TM    TRMFLG01,TRM3705   3705 MODE
         BZ    DUMHD4             NO
DUMHD4   EQU   *
         LH    R3,KPQCBSZ         GET SIZE OF P-QCB
         BCTR  R3,D0              DECREMENT FOR EX
         L     R4,KPQCB           GET ADDR OF TRUE P-QCB
         EX    R3,DUMPQCB         COPY P-QCB
         TM    TRMFLG01,CONC      CONC TERMINAL
         BZ    DUMHJ              NO
         LA    R5,D1(R3,R5)       PLACE FOR DUMMY CONC TTE
         ST    R5,KCTTE           SAVE IT
         SR    R3,R3              CLEAR REG
         IC    R3,TTECSZ          GET LENGTH OF CONC TTE
         BCTR  R3,R0              DECREMENT FOR EX
         L     R7,CONCTTE         REAL CONC TTE ADDR
         EX    R3,DUMCTTE         COPY DUMMY CONC TTE
         L     R14,DUMEXTA        DUMMY EXTENSION ADDR
         USING IEDQQCBE,R14
         MVC   QCBECONC(D3),KCTTE+D1 PUT DUMMY CONC TTE ADDR IN EXT
         XC    QCBEFLG(D1),QCBEFLG  CLEAR FLAG BYTE IN EXTENSION
         DROP  R14
         SPACE
         LA    R5,D1(R3,R5)       PLACE FOR DRQ
         L     R7,KDRQ            REAL DRQ
         LH    R3,KDRQSZ          SIZE OF DRQ
         BCTR  R3,R0              DECREMENT FOR EX
         EX    R3,DUMDRQ          COPE IT
         L     R14,KCTTE          ADDR OF DUMMY CONC TTE
         ST    R5,DKDRQ           SAVE DUMMY DRQ ADDR
         MVC   D1(D3,R14),DKDRQ+D1 PUT DRQ ADDR IN CONC TTE
         USING IEDQDRQ,R5
         NI    DRQFLAG1,XFF-DRQHELD TURN OFF STOP SENDING BIT
         NI    DRQSTAT,XFF-DRQTRMHO TURN OFF HELD BIT
         L     R1,TOTAVTPT        GET ADDRESS OF AVT
         USING IEDQAVTD,R1        SET UP ADDRESSIBILITY FOR AVT
         MVC   DRQELCHN(D3),AVTDELAD+D1 PUT DUMMY LAST ELEMENT
*                                         ADDRESS INTO DATA READY
*                                         QUEUE
         DROP  R1                 DROP AVT BASE
*
*        SET UP HM POINTER IN DRQ
*
         LR    R4,R7              ADDR OF REAL DRQ
         L     R3,D8(R4)          GET STCB CHAIN ADDR
         LA    R3,D0(R3)          CLEAR HI ORDER BYTE
         LA    R15,D8(R4)         GET ADDR OF 3RD WORD
         CR    R3,R15             3RD WORD POINT TO SELF
         BE    DUMHG              YES - HM PTR IS 4TH WORD
         ST    R3,DRQSTPRI        PUT IN 4TH WORD
         MVC   DRQSTPRI(D1),D12(R4) PUT FLAG BYTE IN
DUMHG    EQU   *
         LA    R3,DRQSTVTO        3RD WORD
         ST    R3,DRQSTVTO        PUT IT IN ITSELF
         MVC   DRQSTVTO(D1),D8(R4) SET INDEX
         XC    DRQCURQ(D3),DRQCURQ CLEAR CURRENT QCB POINTER
         MVC   DRQSCBOF(D1),D20(R4) MOVE IN FLAG BYTE
DUMHJ    EQU   *
         L     R7,TRMQCB          RESTORE TRUE M-QCB
         DROP  R5
         SPACE
*                                                              @Y17XAUU
*        COPY LGB IF 3705 RESOURCE                             @Y17XAUU
*                                                              @Y17XAUU
         TM    TRMFLG01,TRM3705   3705 MODE                    @Y17XAUU
         BZ    DUMHJ2             NO - NO LGB TO COPY          @Y17XAUU
         LA    R10,D1(R3,R5)      PLACE FOR DUMMY LGB          @Y17XAUU
         USING IEDQQCB,R7         SET REAL QCB ADDRESSABILITY  @Y17XAUU
         L     R8,QCBLGBAD-D1     GET REAL LGB ADDRESS         @Y17XAUU
         DROP  R7                                              @Y17XAUU
         LA    R3,LGBSZ           GET LGB SIZE                 @Y17XAUU
         BCTR  R3,D0              DECREMENT FOR EX             @Y17XAUU
         EX    R3,LGBMOVE         COPY LGB                     @Y17XAUU
         L     R5,KMQCB           RESTORE DUMMY M-QCB ADDRESS  @Y17XAUU
         USING IEDNQCB,R5         SET QCB ADDRESSABILITY       @Y17XAUU
         LA    R1,QCBPRFSZ        GET QCB PREFIX SIZE          @Y17XAUU
         SLR   R5,R1              POINT TO QCB PREFIX          @Y17XAUU
         L     R8,DEVENT          GET DEVICE ENTRY ADDRESS     @Y17XAUU
         MVC   QCBTTCIN(D2),D2(R8) SET DUMMY TTCIN IN QCB      @Y17XAUU
         STCM  R10,ADRMSK7,QCBLGBAD SET DUMMY LGB ADDRESS IN   @Y17XAUU
*                                 DUMMY QCB                    @Y17XAUU
         DROP  R5                                              @Y17XAUU
         USING IEDNLGB,R10        SET LGB ADDRESSABILITY       @Y17XAUU
         L     R5,TOTRESPL        RESIDENT CONTROL BLOCK ADDR  @Y17XAUU
         USING RESPL,R5           SET RESPL ADDRESSABILITY     @Y17XAUU
         L     R5,RESTOTMH        GET TOTE MH ADDRESS          @Y17XAUU
         STCM  R5,ADRMSK7,LGBMHA  SET MH ADDRESS IN LGB        @Y17XAUU
         LA    R5,MAXRUSIZ        SET MAXIMUM RU SIZE          @YM08523
         CLM   R5,ADRMSK3,LGBBUFSI IS RU SIZE GT BUF SIZE      @YM09025
         BNH   DUMHJ2             NO - BRANCH                  @YM09025
         STCM  R5,ADRMSK3,LGBBUFSI IN COUNTERFEIT              @YM08410
         DROP  R5                                              @Y17XAUU
         DROP  R10                                             @Y17XAUU
DUMHJ2   EQU   *                                               @Y17XAUU
         L     R5,KMQCB           RESTORE DUMMY M-QCB ADDRESS  @Y17XAUU
*
         USING IEDQQCB,R5
         BAL   R3,DUMQMGR         EVALUATE QUEUE METHOD
         TM    TRMFLG01,TRMRNLU   LU ENTRY                     @Y17XAUU
         BO    DUMHJ4             YES - BYPASS HOLD            @Y17XAUU
         MVI   TOTOVBCD,HOLD      SET HOLD VERB CODE           @Y17XAUU
         MVC   TOTOLADR(D8),WORK1 SET RESOURCE NAME            @Y17XAUU
         MVC   TOTORLN(D1),QCBRELLN SET RLN                    @Y17XAUU
         BAL   R14,OPCREQ         ISSUE HOLD TERMINAL          @Y17XAUU
DUMHJ4   DS    0H                 RETURN AFTER BRANCH
         TM    TRMFLG01,TRMDELY   QCB IN TIME DELAY QUEUE        S22024
         BZ    DUMHK              NO - CONTINUE                  S22024
         NI    QCBTSOF1,XFF-QCBDELAY YES - TURN IT OFF           S22024
DUMHK    EQU   *                                                 S22024
         XC    QCBMSGCT(D2),QCBMSGCT  CLEAR COUNT OF MSG IN QUEUE
         NI    QCBSTAT,XFF-QCBTRMHO  TURN OFF HOLD BIT IF ON
         L     R3,QCBSTVTO        GET STCB CHAIN ADDRESS
         LA    R3,D0(R3)          CLEAR HI-ORDER BYTE
         LA    R4,D8(R7)          GET ADDRESS OF 3RD WORD
         CR    R3,R4              3RD WORD POINT TO SELF
         BE    DUMHM              YES - HM POINTER IS 4TH WORD
         ST    R3,QCBSTPRI        PUT HM IN 4TH WORD OF DUMMY
         MVC   QCBSTPRI(D1),D12(R7)  PUT FLAG BYTE IN DUMMY
DUMHM    EQU   *
         LA    R3,QCBSTVTO        GET ADDR OF 3RD WORD IN DUMMY
         ST    R3,QCBSTVTO        PUT IT IN ITSELF
         MVC   QCBSTVTO(D1),D8(R7) SET INDEX IN DUMMY QCB
         ST    R5,QCBSCBOF        PUT DUMMY QCB ADDR IN CHAIN
         MVC   QCBSCBOF(D1),X14(R7)  MOVE IN FLAG BYTE
         NI    QCBFLAG,XFF-QCBSDFFO  TURN OFF CURRENTLY SENDING FEFO
*                                      FLAG
         NI    QCBDSFLG,XFF-QCBHELD  TURN OFF HELD BIT
         XC    QCBDATFL(D22),QCBDATFL CLEAR PQCB
         SPACE
DUMHR    EQU   *
         L     R13,D4(R13)        CALLLER'S SAVE AREA ADDRESS
         LM    R14,R12,D12(R13)   CALLER'S REGISTERS
         SR    R15,R15            CLEAR RETURN CODE REGISTER
         BR    R14                RETURN
         SPACE 2
DUMFAIL  EQU   *
         SPACE
         L     R13,D4(R13)        RESTORE CALLERS SAVE AREA ADDRESS
         LM    R14,R12,D12(R13)   RESTORE CALLER'S REG
         LA    R15,ERRCD4         ERROR RETURN CODE
         BR    R14                RETURN
         SPACE
         DROP  R11
         EJECT
DUMQMGR  EQU   *
         STM   R10,R12,WORK4      SAVE CALLER'S REG
         USING IEDQQCB,R5
*
         TM    TRMFLG01,TRMRNLU   LU ENTRY                     @Y17XAUU
         BNZ   DDD4               YES - BYPASS STATUS CHECK    @Y17XAUU
         TM    TRMRQFLG,CTREQ     CT REQUEST                   @Y17XAUU
         BO    DDD1               YES                          @Y17XAUU
         LA    R10,TOTAPDFL       AP INIT STATUS FLAGS
         LA    R1,TOTAPOFF        @ AP COUNTERFEIT TTCIN       @YM08136
         B     DDD2               EXIT
DDD1     EQU   *
         LA    R10,TOTCTDFL       CT INIT STATUS FLAGS
         LA    R1,TOTCTOFF        @ CT COUNTERFEIT TTCIN       @YM08136
DDD2     EQU   *
         TM    TRMFLG01,TRM3705   NCP PRE SNA ENTRY            @YM08136
         BZ    DDD20              NO - BYPASS ALT TTCIN SETUP  @YM08136
         OI    TOTOFLG,TOTOATC    USE ALTERNATE TTCIN          @YM08136
         MVC   TOTOALTT(D2),D0(R1) SET UP ALTERNATE TTCIN      @YM08136
DDD20    EQU   *                                               @YM08136
         TM    QCBSTAT,QCBTRMHO   TERMINAL INIT HELD ?
         BZ    DDD3               NO
         OI    D0(R10),X40        SHOW INT STATE
DDD3     EQU   *
*        DETERMINE IF INVITATION LIST INIT. INACTIVE
         LA    R1,TOTOTECB        GET ECB ADDR
         STCM  R1,7,TOTOECBA+1    PUT IT IN P-LIST             @YM08136
         MVC   TOTORLN(D1),QCBRELLN  PUT IN RLN                @Y17XAUU
         MVI   TOTOVBCD,INACT     INACTIVATE VERB CODE         @Y17XAUU
         MVI   TOTOELNG,X18       SET ELEMENT LENGTH           @Y17XAUU
         XC    TOTOLINK(D3),TOTOLINK  CLEAR LINK FIELD         @Y17XAUU
         MVI   TOTOPRI,PRISSOLT   SET POST PRIORITY            @Y17XAUU
         XC    TOTOTECB(D4),TOTOTECB  CLEAR ECB
         MVC   TOTOLADR(D8),WORK1 PUT IN TERMINAL NAME         @Y17XAUU
         LA    R1,TOTPLIST        ADDR OF ELEMENT
         ST    R1,TRMSLPL         PUT IN PLIST
         ST    R1,TRMSLPL+D4      PUT IN PLIST
         MVI   TRMSLPL,RQINIT     SET INITIAL ELEMENT FLAG
         MVI   TRMSLPL+D4,RQEND   SET LAST ELEMENT FLAG
         LA    R1,TRMSLPL         ADDR OF PLIST
         AQCTL                    ISSUE ACTIVATE REQUEST
         WAIT  ECB=TOTOTECB
         CLI   TOTORTCD,INACTRC   INIT INACTIVE
         BNE   DDD4               NO - INIT ACTIVE
DDD3E    EQU   *
         OI    D0(R10),TOTAPIAC   SHOW THAT LIST INACTIVE
DDD4     EQU   *
         L     R9,TOTAVTPT        GET AVT ADDRESS              @Y17XAUU
         USING IEDQAVTD,R9
         TM    QCBDSFLG,QCBREUS   REUSABLE DISK ?
         BZ    DUMQM1             NO
         MVC   QCBDNHDR(D3),AVTRADDR+D1  PUT R-DISK Q ADDR IN QCB
         L     R8,AVTRADDR        GET R-DISK Q ADDR
         LA    R8,D4(R8)          INCREMENT BY FOUR
         ST    R8,AVTRADDR        REPLACE IT
         LM    R10,R12,WORK4      RESTORE REG.
         BR    R3                 RETURN
*
DUMQM1   EQU   *
*
         TM    QCBDSFLG,QCBNREUS  NONREUSABLE DISK ?
         BZ    D0(R3)             NO - RETURN                  @Y17XAUU
         MVC   QCBDNHDR(D3),AVTNADDR+D1  PUT NR-DISK Q ADDR IN QCB
         L     R8,AVTNADDR        GET NR-DISK ADDRESS
         LA    R8,D4(R8)          INCREMENT BY FOUR
         ST    R8,AVTNADDR        PUT IT BACK IN AVT
         LM    R10,R12,WORK4      RESTORE REG
         BR    R3                 RETURN
         DROP  R9
         DROP  R5
         EJECT
QWB2SAVE DS    18F                IEDQWB2 SAVW AREA
TRMSLPL  DS    0F                 PARAMETER LIST FOR START/STOP LINE
         DC    X'0C'              START OF LIST INDICATOR
         DC    AL3(0)             ADDRESS OF REQUEST ELEMENT   @Y17XAUU
         DC    X'80'              END OF LIST INDICATOR
         DC    AL3(0)             ADDRESS OF REQUEST ELEMENT   @Y17XAUU
OFFSTOTE DS    0F                 RESETS TRMSTOTE W/ C&S       @G36XRUV
         DC    XL1'FF'                                         @G36XRUV
         DC    AL1(255-TRMSTOTE)                               @G36XRUV
         DC    XL2'FFFF'                                       @G36XRUV
         SPACE
WORK1    DC    8XL1'40'           PLACE FOR CT/AP/TD NAME      @Y17XAUU
WORK4    DS    3F                 WORK AREA
DEVENT   DS    A                  DEVICE ENTRY ADDRESS         @Y17XAUU
DUMEXTA  DS    A                  DUMMY QCBE ADDRESS           @Y17XAUU
K1       DC    F'1'               VALUE OF 1                   @Y17XAUU
K4       DC    F'4'               VALUE OF 4                   @Y17XAUU
K12      DC    F'12'              VALUE OF 12                  @Y17XAUU
KEXT     DS    A                  TRUE QCBE ADDRESS            @Y17XAUU
DCTTETA  DS    A                  DUMMY CONC TERM TTE ADDRESS  @Y17XAUU
DKDRQ    DS    A                  DUMMY DRQ ADDRESS            @Y17XAUU
KCTTE    DS    A                  DUMMY CONC TTE ADDRESS       @Y17XAUU
KQCBESZ  DS    H                  QCBE SIZE                    @Y17XAUU
         SPACE
DUMNAME  MVC   D0(D1,R3),D0(R7)   COPY TERMINAL NAME           @Y17XAUU
DUMTTE   MVC   D0(D1,R5),D0(R7)   COPY TTE                     @Y17XAUU
DUMQCB   MVC   D0(D1,R5),D0(R7)   COPY M-QCB                   @Y17XAUU
DUMDEV   TM    TRMDEVFL-IEDQTRM(R11),D0 TEST DEV DEP FIELDS    @Y17XAUU
DUMPQCB  MVC   D0(D1,R5),D0(R4)   COPY P-QCB                   @Y17XAUU
DUMCTTE  MVC   D0(D0,R5),D0(R7)   COPY CONC TTE                @Y17XAUU
DUMDRQ   MVC   D0(D0,R5),D0(R7)   COPY DRQ                     @Y17XAUU
CTTENAME MVC   D0(D0,R14),D0(R7)  SYMBOLIC NAME OF CONC        @Y17XAUU
         SPACE
TTENAME  MVC   WORK1(D0),D0(R7)   COPY TERMINAL NAME           @ZM47913
LGBMOVE  MVC   D0(D0,R10),D0(R8)  COPY LGB                     @Y17XAUU
         DC    20F'0'                   TEMP PATCH AREA
         EJECT
KTRM     DSECT
TERNMTAB DS    A                  TERMNAME TABLE ADDRESS
TNTENTRY DS    A                  TERMNAME ENTRY ADDRESS
TRMQCB   DS    A                  CT/AP QCB ADDRESS
TRMTNT   DS    A                  CT/AP TNT ADDRESS
TRMSAVE1 DS    3F                 REGISTERS SAVE AREA
TRMSAVE2 DS    5F                 REGISTERS SAVE AREA
TRMSAV1  DS    3F                 REGISTERS SAVE AREA
REGSAVE  DS    F                  WORK AREA
F32      DC    F'32'              VALUE OF 32
KPQCB    DS    A                  TRUE P-QCB ADDRESS           @Y17XAUU
KMQCB    DS    A                  DUMMY M-QCB ADDRESS          @Y17XAUU
CONCTTE  DS    A                  TRUE CONC TERM TTE ADDRESS   @Y17XAUU
KDRQ     DS    A                  TRUE DRQ ADDRESS             @Y17XAUU
KPQCBSZ  DS    H                  P-QCB SIZE                   @Y17XAUU
KMQCBSZ  DS    H                  M-QCB SIZE                   @Y17XAUU
KDRQSZ   DS    H                  DRQ SIZE                     @Y17XAUU
TTETSZ   DS    CL1                CONC TERM TTE SIZE           @Y17XAUU
TTECSZ   DS    CL1                CONC TTE SIZE                @Y17XAUU
TRMRQFLG DS    CL1                DUMMY TNT REQUEST FLAG
CTREQ    EQU   X'80'              CT REQUEST                   @Y17XAUU
APREQ    EQU   X'40'              AP REQUEST                   @Y17XAUU
PRREQ    EQU   X'20'              PRIMARY TD REQUEST           @Y17XAUU
SCREQ    EQU   X'10'              SECONDARY TD REQUEST         @Y17XAUU
ALRDY    EQU   X'00'              COUNTERFEIT STORAGE ALREADY  @Y17XAUU
*                                 EXIST FOR THIS REQUEST       @Y17XAUU
TRMFLG01 DS    CL1                CONTROL FLAG
CONC     EQU   X'80'              TERMINAL ATTACHED TO CONC
TRMSPAS  EQU   X'40'              PROCESS CONC TTE
TRMQWB3  EQU   X'20'              SECOND PASS FUNCTION
TRMRNLU  EQU   X'10'              SNA LU ENTRY                 @Y17XAUU
TRMDELY  EQU   X'08'              QCB IN TIME DELAY QUEUE
TRM3705  EQU   X'04'              3705 MODE
TRMCTEAP EQU   X'02'              CT EQU AP
WRKFLG   DS    CL1                WORK AREA
KTRMEND  DS    0C                 END OF KTRM DSECT            @Y17XAUU
************************************************************** @Y17XAUU
         EJECT
         TLGBD                                                   S22024
         EJECT
         TQCBED
         EJECT
         TDRQD
         EJECT
         EJECT
         TPRIOR
         EJECT
         TQCBD
         EJECT
DCBDSECT DCBD  DSORG=TX
         EJECT
         TSCBD
         EJECT
         TPRFD
         EJECT
         TLCBD
         EJECT
CVT      DSECT
         CVT
         EJECT
UCB      DSECT
         IEFUCBOB
         EJECT
         TAVTD
         EJECT
         TTRMD
         EJECT
         TDEBD
         EJECT
         TTNTD
         EJECT
         OLTCB
         EJECT
RQTNTERR EQU   X'FF'              ERROR RETURN CODE            @Y17XAUU
RQTNTENT DSECT                                                 @Y17XAUU
RQTNTFGS DS    BL1                TNT REQUEST FLAGS            @Y17XAUU
RQTNTLST EQU   X'80'              LAST ENTRY FLAG              @Y17XAUU
RQTNTTYP DS    BL1                TNT REQ ENTRY TYPE FIELD     @Y17XAUU
RQTNTCT  EQU   X'80'              CONTROL TERMINAL ENTRY       @Y17XAUU
RQTNTAP  EQU   X'40'              ALTERNATE PRINTER ENTRY      @Y17XAUU
RQTNTPR  EQU   X'20'              PRIMARY TEST DEVICE ENTRY    @Y17XAUU
RQTNTSC  EQU   X'10'              SECONDARY TEST DEVICE ENTRY  @Y17XAUU
RQTYPANY EQU   RQTNTCT+RQTNTAP+RQTNTPR+RQTNTSC ANY ENTRY FLAG  @Y17XAUU
RQTNTLNT DS    H                  TNT REQUEST BLOCK LENGTH     @Y17XAUU
RQTENLEN EQU   4                  TNT REQUEST ELEMENT LENGTH   @Y17XAUU
         EJECT
RESPL    RESPL
         EJECT
         TSIBD                                                 @Y17XAUU
         END
