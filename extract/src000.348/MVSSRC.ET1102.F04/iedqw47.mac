QW47     TITLE 'IEDQW47 - ROUTINE SERVICE MODULE'
IEDQW47  CSECT
*A000000-999999                                                @Y16X5U0
*CHANGE ACTIVITY AS FOLLOWS:
*A366000                                                        SA41596
*A273000,420000,432000                                          SA41599
*A405022-405026,683500-683880                                   SA64770
*D414000,417000                                                @YA11128
*A311800,393000,405000,510000,622000,872500,963000             @Y17XAUU
*C646000,656000,660000,670000,676000-680000,787000-825000      @Y17XAUU
* A660000                                                      @G36XRUV
* C662000                                                      @G36XRUV
*
*
         USING IEDQW47,R15
*
IEDQW47  IEDHJN START,HJN
         SPACE 2
***********************************************************************
*                                                                     *
* MODULE NAME:  IEDQW47 (TCAM,TOTE)                            @Y17XAUU
*                                                                     *
* DESCRIPTIVE NAME:  TOTE ROUTINE SERVICE MODULE                      *
*                                                                     *
* COPYRIGHT:  'NONE'                                                  *
*                                                                     *
* STATUS --                                                           *
*    VERSION 10.0                                              @Y17XAUU
*                                                                     *
* FUNCTIONS --                                                        *
*    THE PURPOSE OF THIS MODULE IS TO HANDLE THE SELECTION AND        *
*    RUNNING OF ROUTINES WITHIN AN OLT SECTION.  THE BASIC            *
*    FUNCTIONS PERFORMED ARE AS FOLLOWS:                              *
*         A. DETERMINE IF THE ROUTINE SHOULD BE RUN BY CHECKING       *
*            THE ROUTINE MASK IN THE SCT. IF THE ROUTINE MASK         *
*            IS OFF THE ROUTINE SERVICE MODULE WILL GET THE           *
*            POINTER TO THE NEXT ROUTINE FROM THE MACRO PARAMETER     *
*            LIST AND BRANCH TO THE NEXT ROUTINE.                     *
*                                                                     *
*         B. DETERMINE IF THE ROUTINE SHOULD BE RUN BY CHECKING       *
*            THE MI BIT IN THE SCT (WORD 9, BYTE 0, BIT 0),           *
*            IF MASK=MI IS SPECIFIED.                                 *
*                                                                     *
*         C. DETERMINE IF THE ROUTINE SHOULD BE RUN BY CHECKING       *
*            THE DCL BIT IN THE SCT (WORD 9, BYTE 0, BIT 2), IF       *
*            DCL IS SPECIFIED. IF THE DCL BIT IS ON AND THE           *
*            CONTROL TERMINAL OR ALTERNATE PRINTER IS ON THE          *
*            SAME COMMUNICATION LINE AS THE TEST TERMINAL, THE        *
*            ROUTINE WILL BE BYPASSED.                                *
*                                                                     *
*         D. RESET THE ERROR BIT (SCT WORD 0, BIT 24) AND             *
*            UPDATE THE ACTIVE ROUTINE NUMBER IN THE SECTION          *
*            PREFACE IF THE ROUTINE IS TO BE RUN.                     *
*                                                                     *
*         E. MOVE THE REFERENCE NUMBER INTO THE SECTION PREFACE       *
*            (IF THE REFNUM IS SPECIFIED).                            *
*                                                              @Y17XAUU
* PURPOSE -- SEE FUNCTION                                      @Y17XAUU
*                                                                     *
* ENTRY POINT --                                                      *
*    IEDQW47                                                          *
*                                                                     *
* INPUT --                                                            *
*    REGISTERS 1,2,13,14,15 CONTAIN THE FOLLOWING VALUES:             *
*                                                                     *
*         1 - ROUTINE PARAMETER LIST ADDRESS                          *
*         2 - SCT ADDRESS                                             *
*        13 - SAVE AREA ADDRESS                                       *
*        14 - RETURN ADDRESS                                          *
*        15 - ENTRY POINT ADDRESS FOR THIS MODULE                     *
*                                                                     *
* OUTPUT --                                                           *
*    NO OUTPUT VALUES ARE RETURNED                                    *
*                                                              @Y17XAUU
* MODULE TYPE -- PROCEDURE                                     @Y17XAUU
*                                                              @Y17XAUU
* PROCESSOR -- ASSEMBLER XF                                    @Y17XAUU
*                                                              @Y17XAUU
* MODULE SIZE -- 2K MAXIMUM                                    @Y17XAUU
*                                                              @Y17XAUU
* LINKAGE -- BALR 14,15 FROM IEDQWA                            @Y17XAUU
*                                                              @Y17XAUU
* MACROS -- AQCTL, IEDHJN, SAVE, WAIT                          @Y17XAUU
*                                                                     *
* EXTERNAL ROUTINES --                                                *
*    IEDQWO - ACCESS MANAGER TO PRINT MESSAGE                  * S22024
*                                                                     *
* EXITS,NORMAL --                                                     *
*    XCTL TO IEDQWM2 IF TRACE FUNCTION ACTIVE                         *
*    RETURN R14                                                       *
*                                                                     *
* EXITS,ERROR --                                                      *
*    XCTL TO IEDQWM2 IF UNSUPPORTED MACRO FUNCTION OR MACRO LEVEL     *
*                                                                     *
* TABLES/WORK AREAS --                                                *
*    SCT                                                              *
*                                                                     *
* ATTRIBUTES --                                                       *
*    ENABLED, PROBLEM PROGRAM MODE, RESIDENT, REENTRANT        @Y17XAUU
*                                                                     *
* NOTES -- SEE BELOW                                           @Y17XAUU
*                                                                     *
*  DEPENDENCIES -- EBCDIC CHARACTER CODE                       @Y17XAUU
*                                                              @Y17XAUU
*  REGISTERS CONVENTION -- SEE REGISTER ASSIGNMENT             @Y17XAUU
*                                                              @Y17XAUU
*  RESTRICTIONS -- NONE                                        @Y17XAUU
*                                                              @Y17XAUU
*  PATCH LABEL -- PATCH                                        @Y17XAUU
*                                                              @Y17XAUU
***********************************************************************
         SPACE 4
         EJECT
***********************************************************************
*                                                                     *
*        EQUATES                                                      *
*                                                                     *
***********************************************************************
         SPACE 2
R0       EQU   0                  WORK REGISTER
R1       EQU   1                  PARAMETER LIST ADDRESS
R2       EQU   2                  SCT ADDRESS
R3       EQU   3                  ADDRESS OF SECTION PREFACE PART 1
R4       EQU   4                  BASE REGISTER
R5       EQU   5                  REGISTER 5
R6       EQU   6                  REGISTER 6
R7       EQU   7                  REGISTER 7
R8       EQU   8                  REGISTER 8
R9       EQU   9                  ADDRESS OF SECTION PREFACE PART 2
R10      EQU   10                 REGISTER 10
R11      EQU   11                 PARAMETER LIST BASE ADDRESS
R12      EQU   12                 REGISTER 12
R13      EQU   13                 SAVE AREA ADDRESS
R14      EQU   14                 RETURN ADDRESS
R15      EQU   15                 ENTRY POINT ADDRESS
         SPACE 2
ZERO     EQU   0                  CONSTANT DISPLACEMENT
ONE      EQU   1                  CONSTANT DISPLACEMENT
TWO      EQU   2                  CONSTANT DISPLACEMENT
THREE    EQU   3                  CONSTANT DISPLACEMENT
FOUR     EQU   4                  CONSTANT DISPLACEMENT
EIGHT    EQU   8                  CONSTANT DISPLACEMENT
NINE     EQU   9                  CONSTANT DISPLACEMENT
TEN      EQU   10                 CONSTANT DISPLACEMENT
TWELVE   EQU   12                 CONSTANT DISPLACEMENT
SIXTN    EQU   16                 CONSTANT DISPLACEMENT
D0       EQU   0                  CONSTANT DISPLACEMENT         SA41599
D20      EQU   20                 CONSTANT DISPLACEMENT
D83      EQU   83                 CONSTANT DISPLACEMENT
TERM     EQU   X'40'              BLANK CHARACTER
MINUS    EQU   X'FF'              USED TO TURN OFF FLAG BITS
RTNEND   EQU   X'40'              LAST ROUTINE SPECIFIED
RTNMI    EQU   X'80'              MI ROUTINE SPECIFIED
D24      EQU   24                 DISP. OF 24
RUNRTN   EQU   X'80'              RUN ROUTINE FLAG
D1       EQU   1                  DISP OF 1                      S22024
D4       EQU   4                  DISP OF 4                      S22024
RQINIT   EQU   X'0C'              START OF LIST                  S22024
RQEND    EQU   X'80'              END OF LIST                    S22024
DISREG2  EQU   28                 DISPLACEMENT TO REG 2        @Y17XAUU
         SPACE 2
         SAVE  (14,12)
         SPACE 2
*
         USING PLIST,R11          PARAMETER LIST BASE IS 11
         USING TOTOLTCB,R2        OLTCB BASE
         USING SPREF1,R3          SECTION PREFACE DSECT PART 1
         USING SPREF2,R9          SECTION PREFACE DSECT PART 2
*
*
         DROP  R15
         USING IEDQW47,R4         BASE REG IS 4
         LR    R4,R15             SET UP BASE
         LA    R15,TOTSAVE1       GET ADDRESS OF OWN SAVE AREA
         ST    R15,EIGHT(R13)     PUT IN CALLER'S SAVE AREA
         ST    R13,FOUR(R15)      SAVE ADDRESS OF CALLER'S SAVE
         LR    R13,R15            SET REG 13 TO MY SAVE AREA
         SPACE 2
         LR    R11,R1             GET PLIST ADDR
         L     R3,TOTOLTEN        SECTION PREFACE BASE          SA41596
*
         CLI   RTNML,TWO          CORRECT MACRO LEVEL ?
         BE    RTN004             YES
*
         LA    R15,FOUR           SET INCORRECT MACRO LEVEL RET CODE
         SPACE 2
         STC   R15,TOTRTCOD       SAVE RETURN CODE
         L     R13,FOUR(R13)      RESTORE CALLER'S SAVE AREA PTR
         L     R14,TWELVE(R13)    RESTORE CALLER'S RETURN ADDR
         USING RESPL,R5           SET RESPL ADDRESSABILITY     @Y17XAUU
         L     R5,TOTRESPL        GET RESPL ADDRESS            @Y17XAUU
         L     R15,RESPLM2        GET TRACE ROUTINE ADDRESS    @Y17XAUU
         LM    R2,R12,DISREG2(R13) RESTORE CALLER'S REGISTERS  @Y17XAUU
         BR    R15                GO TO IEDQWM2                @Y17XAUU
         DROP  R5                                              @Y17XAUU
         SPACE 2
*
RTN004   EQU   *
         XC    RTNFLAG,RTNFLAG    CLEAR CONTROL FLAGS          @Y17XAUU
         MVC   RTNMSG(QTNMSGAE-QTNMSG),QTNMSG  WORK AREA       @Y17XAUU
         TM    RTNFLG,RTNDCL      DCL FLAG SET                   S22024
         BZ    RTN0040            NO                             S22024
         TM    $TOTFLG1,$CTEQTT   CT AND TT ON SAME SUB-CHAN     S22024
         BO    RTN0070            YES - BYPASS ROUTINE           S22024
RTN0040  EQU   *                                                 S22024
         SR    R9,R9              CLEAR REG
         LH    R9,RTNDISP         GET DISPLACEMENT TO RTN NUMBER
         AR    R9,R3              SET BASE FOR SECT PREF PART 2
*
         MVC   RTN#(ONE),RTNNUM   MOVE RTN NUMBER TO SECT PREF
         MVI   $RETMASK,ZERO      CLEAR RETURN CODE MASK
         NI    $ERROPT,MINUS-$TESTERR CLEAR TEST ERROR FLAG
         NI    $ERROPT,MINUS-$CATERR CLEAR CAT. ERROR FLAG
*
         MVC   REFNUM(ONE),REFNUMB MOVE REFNUM TO SECT PREF
*
         MVI   TOTRTCOD,ZERO      SET RETURN CODE
         TM    TOTFLG06,TOTNCMFG  NON-CONCURRENT MODE ASKED?
         BO    RTN005             YES BYPASS START LINE
         USING IOBLOCKS,R7
         L     R7,TOTSAVE5        GET ADDRESS OF CURRENT        SA41599
*                                   PRIMARY TEST DEVICE         SA41599
         TM    D0(R7),TOTTDLST    WAS LINE INITIALLY STOPPED?   SA41599
         BO    RTN005             YES, DO NOT START LINE        SA41599
         TM    D0(R7),TOTTDORN    TEST DEVICE ON 3705            S22024
         BO    RTN005             YES - BYPASS STARTLINE         S22024
         LA    R7,TOTPRIBK        SET UP TO START PRIMARY LINE
         BAL   R8,STRTLINE        START PRIMARY LINE
*
RTN004A  EQU   *
         L     R7,TOTGRABP        GET ADDRESS OF CURRENT        SA41599
*                                   SECONDARY TEST DEVICE       SA41599
         TM    D0(R7),TOTTDLST    WAS LINE INITIALLY STOPPED?   SA41599
         BO    RTN005             YES, DO NOT START LINE        SA41599
         LA    R7,TOTSCIBK        SET UP TO START SECONDARY LINE
         BAL   R8,STRTLINE        START SECONDARY LINE
*
RTN005   EQU   *
*
         BAL   R5,RTNCHK          DETERMINE IF ROUTINE SHOULD BE RUN
         TM    RTNFLAG,RUNRTN     RUN ROUTINE ?
         BZ    RTN007             NO - BYPASS ROUTINE
RTN006   EQU   *
*
*
*
         TM    RTNFLG,RTNMI       MI ROUTINE ?
         BZ    RTN008             NO - BYPASS CHECK
         TM    $OLTFLGS,$MANINTV  MI OPTION IN TRM ?
         BO    RTN008             YES - RUN ROUTINE
*
         MVI   TOTOTBUF,TERM      BLANK OUT THE
         MVC   TOTOTBUF+ONE(D83),TOTOTBUF  OUTPUT BUFFER
         MVC   RTNSID(EIGHT),TOTCROLT  MOVE SECTION ID TO MSG
         MVC   TOTOTBUF(RTNMSGE-RTNMSG),RTNMSG NOTIFY CT TERM
         LA    R5,RTNMSGE-RTNMSG  GET MSG LENGTH
         STC   R5,TOTOTCNT        PUT IT IN OLTCB
RTN0060  EQU   *                                                 S22024
         NI    TOTFLG05,X'FF'-TOTREPLY CLEAR REPLY FLAG
         NI    TOTFLG05,X'FF'-TOTPRINT CLEAR PRINT FLAG
         OI    TOTFLG05,TOTCECOM  SET CECOM FLAG
         SPACE
         USING RESPL,R5           SET RESPL ADDRESSABILITY     @Y17XAUU
         L     R5,TOTRESPL        GET RESPL ADDRESS            @Y17XAUU
         L     R15,RESPLWO        GET ACCESS MANAGER ADDRESS   @Y17XAUU
         BALR  R14,R15            REPORT THAT ROUTINE BYPASSED @Y17XAUU
         DROP  R5                                              @Y17XAUU
         SPACE
RTN007   EQU   *
         SPACE
         L     R13,FOUR(R13)      RESTORE CALLER'S ADDR
         B     RTN060             EXIT
         SPACE
RTN0070  EQU   *                                                 S22024
         MVI   TOTOTBUF,TERM      BLANK OUT THE                  S22024
         MVC   TOTOTBUF+ONE(D83),TOTOTBUF OUTPUT BUFFER          S22024
         MVC   RTNSIDA(EIGHT),TOTCROLT SECTION ID TO MSG         S22024
         MVC   TOTOTBUF(RTNMSGAE-RTNMSGA),RTNMSGA MSG            S22024
         LA    R5,RTNMSGAE-RTNMSGA MSG LENGTH                    S22024
         STC   R5,TOTOTCNT        PUT IT IN OLTCB                S22024
         B     RTN0060            CONTINUE                       S22024
*
RTN008   EQU   *
*
*
*
         L     R13,FOUR(R13)      GET SAVE AREA ADDRESS
         L     R14,TWELVE(R13)    RESTORE RETURN ADDR
         TM    RTNFLG,RTNEND      LAST ROUTINE ?
         BO    RTN062             YES - GET RTNTERM ADDR
         TM    $OLTFLGS,$TRACE    TRACE FTN REQUESTED ?
         BO    RTN080             YES
         LM    R14,R12,TWELVE(R13)  RESTORE CALLER'S REG
         BR    R14                RETURN TO EXECUTIVE
*
*
*
RTN060   EQU   *
         L     R14,TWELVE(R13)    RESTORE RETURN ADDR
         L     R5,NXTRTNA         GET RETURN ADDR
         ST    R5,TOTSMGRT        SAVE IT
RTN062   EQU   *
         TM    $OLTFLGS,$TRACE    TRACE FTN REQUESTED ?
         BO    RTN080             YES
         LM    R14,R12,TWELVE(R13) RESTORE CALLER'S REGISTERS
         BR    R14                RETURN TO NEXT RTN OR RTNTERM
         SPACE 4
RTN080   EQU   *
         LM    R0,R1,D20(R13)     RESTORE REG 0-1
         USING RESPL,R5           SET RESPL ADDRESSABILITY     Y17XAUU
         L     R5,TOTRESPL        GET RESPL ADDRESS            @Y17XAUU
         L     R15,RESPLM2        GET TRACE ROUTINE ADDRESS    @Y17XAUU
         LM    R2,R12,DISREG2(R13) RESTORE CALLER'S REGISTERS  @Y17XAUU
         BR    R15                GO TO IEDQWM2                @Y17XAUU
         DROP  R5                                              @Y17XAUU
         EJECT
***********************************************************************
*                                                                     *
*        START LINE VIA TCAM'S OP-CONTROL FACILITY                    *
*                                                                     *
***********************************************************************
         SPACE 1
STRTLINE EQU   *
         STM   R6,R12,STRTSAVE    SAVE REGS DURING STARTLINE
         MVI   TOTOVBCD,LINESTRT  MOVE IN STARTLINE VERB CODE  @Y17XAUU
*
         L     R6,TOTAVTPT        GET ADDRESS OF AVT
         USING IEDQAVTD,R6
         LA    R10,AVTOPCOB       GET POINTER TO OP CONTROL QCB
         ST    R10,TOTOKEY        PUT IN PARAMETER LIST        @Y17XAUU
         DROP R6
         MVC   TOTORLN,OLTTCRLN   MOVE IN RELATIVE LINE NUMBER @Y17XAUU
         SR    R6,R6              CLEAR                        @G36XRUV
         ICM   R6,3,OLTUCBA       GET UCB ADDRESS              @G36XRUV
         LTR   R6,R6              IS THERE A VALID UCB ADDRESS?
         BZ    STRT5              NO - DONT ISSUE STARTLINE
         USING UCB,R6
         MVC   TOTOLADR(THREE),UCBNAME GET UNIT NAME           @Y17XAUU
         DROP  R6
         LA    R1,TOTOTECB        GET TOTE ECB
         ST    R1,TOTOECBA        PUT IN PARAMETER LIST        @Y17XAUU
         XC    TOTOLINK(THREE),TOTOLINK CLEAR LINK FIELD       @Y17XAUU
         XC    TOTOTECB(FOUR),TOTOTECB  CLEAR TOTE ECB
         LA    R1,TOTPLIST        ADDR OF ELEMENT                S22024
         ST    R1,TRMSLPL         PUT IN PLIST                   S22024
         ST    R1,TRMSLPL+D4      PUT IN PLIST                   S22024
         MVI   TRMSLPL,RQINIT     SET INITIAL ELEMENT FLAG       S22024
         MVI   TRMSLPL+D4,RQEND   SET LAST ELEMENT FLAG          S22024
         LA    R1,TRMSLPL         ADDR OF PLIST                  S22024
         L     R6,TOTAVTPT        GET ADDRESS OF AVT            SA64770
         USING IEDQAVTD,R6        SET ADDRESSIBILITY FOR AVT    SA64770
         TM    AVTBIT1,AVTCLOSN   TCAM CLOSEDOWN IN PROGRESS?   SA64770
         BO    RTN062             YES - RETURN                  SA64770
         AQCTL                    POST ELEMENT TO OPERATOR CONTROL
*
         WAIT  ECB=TOTOTECB       WAIT FOR START LINE TO COMPLETE
*
STRT5    EQU   *
         LM    R6,R12,STRTSAVE    RESTORE REGS FROM STARTLINE
         BR    R8                 RETURN TO CALLER
*
         DROP  R7
         EJECT
*
RTNCHK   EQU   *   DETERMINE IF ROUTINE SHOULD BE RUN
*
         SR    R6,R6              CLEAR REG 6
         IC    R6,RTNNUM          GET ROUTINE NUMBER
         CL    R6,K16             ROUTINE NUMBER GREATER THAN 16 ?
         BH    RTNCHK6            YES - CHECK 2ND MASK LIST
*
*        ROUTINE NUMBER IS BETWEEN 1 AND 16
*
         LA    R8,TESET           POINT TO MASK COMPARE TABLE
         LA    R7,$RT0108         POINT TO START OF 1ST MASK FIELD
         CL    R6,K8              ROUTINE NUMBER GREATER THAN 8 ?
         BNH   RTNCHK2            NO
         LA    R7,$RT0916         POINT TO NEXT MASK BYTE
         S     R6,K8              ADJUST INDEX INTO COMPARE TABLE
RTNCHK2  EQU   *
         BCTR  R6,R0              ADJUST TO CORRECT MASK FIELD
RTNCHK4  EQU   *   TEST BIT SETTING
         AR    R6,R8              POINT TO RTN MASK COMPARAND
         SR    R10,R10            CLEAR REG 10
         IC    R10,ZERO(R6)       GET MASK BYTE FOR TEST
         EX    R10,RTNBIT         ROUTINE TO BE RUN ?
         BZ    RTNCHKE            NO - EXIT
         OI    RTNFLAG,RUNRTN     YES - SHOW THAT RTN IS TO BE RUN
         B     RTNCHKE            GO TO RETURN
*
RTNCHK6  EQU   *   ROUTINE NUMBER IS BETWEEN 17-256
*
         LA    R8,TESET           POINT TO MASK TABLE
         LA    R7,$R017024        POINT TO START OF 2ND MASK FIELD
         LA    R10,D24            INIT. COMPARAND
RTNCHK8  EQU   *
         CR    R6,R10             CORRECT MASK BYTE ?
         BH    RTNCHKA            NO - SET UP FOR ANOTHER CHECK
         A     R6,K8              ADJUST ROUTINE NUMBER
         SR    R6,R10             ADJUST INDEX INTO MASK TABLE
         BCTR  R6,R0              ADJUST AGAIN
         B     RTNCHK4            GO TEST BIT SETTING
RTNCHKA  EQU   *
         LA    R10,EIGHT(R10)     INCREMENT COMPARAND
         LA    R7,ONE(R7)         POINT TO NEXT RTN MASK BYTE
         B     RTNCHK8            GO CHECK AGAIN
RTNCHKE  EQU   *
         BR    R5                 RETURN TO CALLER
         SPACE 2
         EJECT                                                   S99528
*                                   IN EBCDIC
LINESTRT EQU   X'40'              VERB CODE FOR START LINE
LINESTOP EQU   X'41'              VERB CODE FOR STOP LINE
TERMSTRT EQU   X'45'              VERB CODE FOR START TERMINAL
TERMSTOP EQU   X'47'              VERB CODE FOR STOP TERMINAL
********************START OF COPY DATA************************ @Y17XAUU
*
         SPACE 2
QTNMSG   EQU   *                  ROUTINE MESSAGE              @Y17XAUU
         DC    C'IED346I '        MSG ID                         S22024
QTNSID   DS    CL8                SECTION ID                   @Y17XAUU
         DC    C' - '             CHARACTER 'DASH'
         DC    C'MI ROUTINE BYPASSED - MI NOT SPECIFIED IN TRM'  MSG
*                                 GENERATED WHEN MI NOT INDICATED
QTNMSGE  EQU   *                  END OF MESSAGE               @Y17XAUU
         SPACE
QTNMSGA  EQU   *                  ROUTINE MESSAGE              @Y17XAUU
         DC    C'IED239I '        MSG ID                         S22024
QTNSIDA  DS    CL8                SECTION ID                   @Y17XAUU
         DC    C' - '             CHAR DASH                      S22024
         DC    C'ROUTINE BYPASSED - CONTROL AND TEST TERMINALS ON SAME *
               LINE'                                             S22024
QTNMSGAE EQU   *                                               @Y17XAUU
**********************END OF COPY DATA************************@Y17XAUU
         EJECT                                                   S99528
*
K8       DC    F'8'               VALUE OF 8
K16      DC    F'16'              VALUE OF 16
TESET    DC    X'8040201008040201' RTN MASK CHECK TABLE
RTNBIT   TM    ZERO(R7),ZERO      SHOLD ROUTINE BE RUN ?
PATCH    DC    15F'0'             PATCH AREA                   @Y17XAUU
         SPACE 4
PLIST    DSECT
CTLFLG   DS    CL1                CONTROL PROGRAM FLAGS
RTNML    DS    CL1                MACRO LEVEL
RTNMID   DS    CL2                MACRO ID
NXTRTNA  DS    F                  ADDRESS OF NEXT ROUTINE
RTNNUM   DS    CL1                RTN NUMBER
REFNUMB  DS    CL1                REFNUM
RTNFLG   DS    CL1                ROUTINE FLAGS
RTNDCL   EQU   X'20'              DCL FLAG                     @XM05675
         SPACE 2
SPREF1   DSECT                    SECTION PREFACE PART 1
SECTID   DS    CL8                OLT SECTION ID
LEVEL    DS    CL2                OLT LEVEL
RTNDISP  DS    AL2                DISP TO ROUTINE #
         SPACE 3
SPREF2   DSECT                    SECTION PREFACE PART 2
RTN#     DS    CL1                ROUTINE NUMBER
         DS    CL8                FLAGS
RM       DS    CL2                ROUTINE MASK FLAGS
         DS    CL6                PLINK MANAGEMENT
REFNUM   DS    CL1                REFERENCE NUMBER
         EJECT
         EJECT                                                   S99528
         TAVTD                                                   S99528
         EJECT                                                   S99528
         IOBLOCKS                                                S99528
         EJECT                                                   S99528
UCB      DSECT                                                   S99528
         IEFUCBOB                                                S99528
         EJECT                                                   S99528
RESPL    RESPL                                                 @Y17XAUU
         EJECT
         OLTCB
TRMSLPL  DS    0F                 OP CONTROL PLIST             @Y17XAUU
         DC    X'0C'              FLAG START                   @Y17XAUU
         DC    AL3(0)             POINTER TO FIRST ELEMENT     @Y17XAUU
         DC    X'08'              FLAG LAST                    @Y17XAUU
         DC    AL3(0)             POINTER TO LAST ELEMENT      @Y17XAUU
STRTSAVE DS    7F                 SAVE AREA                    @Y17XAUU
RTNMSG   DS    0C                 ROUTINE MSG                  @Y17XAUU
         DC    C'IED346I '        MSG ID                       @Y17XAUU
RTNSID   DS    CL8                SECTION ID                   @Y17XAUU
         DC    C' - '             CHARACTER 'DASH'             @Y17XAUU
         DC    C'MI ROUTINE BYPASSED - MI NOT SPECIFIED IN TRM'
*        MSG GENERATED WHEN MI NOT SPECIFIED                   @Y17XAUU
RTNMSGE  DS    0C                 END OF MESSAGE               @Y17XAUU
RTNMSGA  DS    0C                 ROUTINE MESSAGE              @Y17XAUU
         DC    C'IED239I '        MSG ID                       @Y17XAUU
RTNSIDA  DS    CL8                SECTION ID                   @Y17XAUU
         DC    C' - '             CHAR DASH                    @Y17XAUU
         DC    C'ROUTINE BYPASSED - CONTROL AND TEST TERMINALS ON SAME *
               LINE'                                           @Y17XAUU
RTNMSGAE DS    0C                 END OF MESSAGE               @Y17XAUU
RTNFLAG  DS    XL1                ROUTINE CONTROL FLAG         @Y17XAUU
         END
