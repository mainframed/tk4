         TITLE 'IEDQWH - NUMERIC TRM HANDLER'
IEDQWH   CSECT                                                   S22024
* CHANGE ACTIVITY AS FOLLOWS:                                    S22024
* A002600-007500,A096600-097200,C202000-204000,C390600,C392000   S22024
* C508000,C518000                                                S22024
***********************************************************************
* NUMERIC TRM HANDLER                                                 *
* DESCRIPTIVE NAME: NUMERIC TRM HANDLER                          S22024
*                                                                S22024
* TITLE: IEDQWH - NUMERIC TRM HANDLER                            S22024
*                                                                     *
* MODULE NAME:  IEDQWH                                                *
*                                                                     *
*                                                                     *
* STATUS:      CHANGE LEVEL 1                                         *
* ENTRY POINT:                                                        *
*                                                                     *
*       IEDQWH - CALLED BY IEDQWC UPON RECOGNITION OF SEVEN 9'S.      *
*                                                                     *
* FUNCTIONS:                                                          *
*                                                                     *
*       THE PURPOSE OF THIS MODULE IS TO PROCESS A TRM FROM A NUMERIC *
*       ENTRY TERMINAL.                                               *
*                                                                     *
*       THIS MODULE TAKES THE INITIAL TRM THAT IS PASSED TO IT BY     *
*       IEDQWC.  THE INITIAL TRM IS ALREADY TRANSLATED FROM THE       *
*       LINE CODE AND THE SEVEN 9'S HAVE BEEN STRIPPED OFF.           *
*                                                                     *
*                                                                     *
*       THE MODULE DETERMINES THE LENGTH OF THE MESSAGE RECEIVED.  IF *
*       IT IS ODD THE LAST CHARACTER IS SAVED.  THE TRM IS THEN       *
*       TRANSLATED FROM THE SPECIAL NUMBERIC CODE TO EBCDIC.  THE     *
*       TRANSLATED TRM IS SEARCHED FOR AN END-OF-TRM CHARACTER (M).   *
*       IF AN END-OF-TRM IS FOUND, CONTROL IS PASSED BACK TO IEDQWC   *
*       FOR TRM VALIDITY CHECKING.                                    *
*                                                                     *
*       IF NO END-OR-TRM CHARACTER IS FOUND, THE PROGRAM ISSUES A READ*
*       TO THE TERMINAL FOR MORE OF THE TRM.  THE NEW MESSAGE IS      *
*       TRANSLATED FROM LINE CODE TO NUMERIC AND FROM NUMERIC TO      *
*       EBCDIC.  THIS PROCESS IS REPEATED UNTIL AN END-OF-TRM         *
*       CHARACTER IS FOUND.                                           *
*                                                                     *
* INPUT:        REGISTERS 02,13,14,15 CONTAIN THE FOLLOWING VALUES:   *
*                                                                     *
*                         02-OLTCB POINTER                            *
*                         13-CALLING MODULE'S SAVE AREA ADDRESS       *
*                         14-RETURN ADDRESS                           *
*                         15-ENTRY POINT ADDRESS                      *
*                                                                     *
*                                                                     *
* OUTPUT:       REGISTERS 01,15 CONTAIN THE FOLLOWING VALUES:         *
*                                                                     *
*                         01-PARAMETER LIST ADDRESS                   *
*                                                                     *
*                                                                     *
* EXTERNAL ROUTINES:                                                  *
*                                                                     *
*       IEDQWK- MESSAGES                                              *
*                                                                     *
* EXIT POINTS:  XCTL AFTER 'EXIT'     TO IEDQWC                       *
*               XCTL AFTER 'TRMERR'   TO IEDQWE                       *
*                                                                     *
* TABLES/WORK AREAS:                                                  *
*                                                                     *
*       CVT, OLTCB, AVT, DCB, TERMNAME TABLE                          *
*                                                                     *
* ATTRIBUTES:                                                         *
*                                                                     *
*       ENABLED, PROBLEM PROGRAM, TRANSIENT                           *
*                                                                S22024
* COPYRIGHT='NONE'                                               S22024
***********************************************************************
         EJECT
*
*              INITIALIZATION
*
*
R0       EQU   0                       REG 0                     S99528
R1       EQU   1                       REG 1                     S99528
R2       EQU   2                       REG 2                     S99528
R3       EQU   3                       REG 3                     S99528
R4       EQU   4                       REG 4                     S99528
R5       EQU   5                       REG 5                     S99528
R6       EQU   6                       REG 6                     S99528
R7       EQU   7                       REG 7                     S99528
R8       EQU   8                       REG 8                     S99528
R9       EQU   9                       REG 9                     S99528
R10      EQU   10                      REG 10                    S99528
R11      EQU   11                      REG 11                    S99528
R12      EQU   12                      REG 12                    S99528
R13      EQU   13                      REG 13                    S99528
R14      EQU   14                      REG 14                    S99528
R15      EQU   15                      REG 15                    S99528
PARMREG0 EQU   R0                      MSG PARM                  S99528
PARMREG1 EQU   R1                      MSG PARM                  S99528
ZERO     EQU   0                       ZERO                      S99528
ONE      EQU   1                       ONE                       S99528
PLUS     EQU   X'4E'                   BACK SPACE CHAR           S99528
EOM      EQU   X'5D'                   END OF TRM CHAR           S99528
         EJECT
         SPACE 2
         USING IEDQWH,R15              ADDRESSIBILIYT            S99528
IEDQWH   IEDHJN IEDQWHH,HJN            NAME AND HJN              S99528
         DROP  R15                     IEDQWH                    S99528
         SAVE  (14,12)                 SAVE REGS                 S99528
         LR    R6,R15                   LOAD BASE
         USING IEDQWH,R6
         USING TOTOLTCB,R2             TOTE CONRROL BLOCK        S22024
         LA    R3,TOTSAVE4             MY SAVE ADDRESS           S22024
         ST    R3,8(R13)                STORE IN CALLER'S SAVE
         ST    R13,4(R3)                SAVE CALLER'S
         LR    R13,R3                   SET R13 TO SAVE AREA
         SR    R3,R3                   CLEAR COUNT
         LA    R4,TOTTRMBF             POINT TO DATA FROM BUFFER S99528
         BAL   R9,ENDSRCH              GO GET LENGTH OF MSG
         LR    R8,R3                   TRANSFER TO R8
         N     R8,ANDFLD               ODD NUMBER?
         BC    8,EVEN                  NO BRANCH
         OI    EOFLG,ODD               TURN ON ODD FLAG
         BCTR  R3,0                    DECREMENT COUNT
         LA    R10,0(R3,R4)            FIND LAST CHARACTER ADDR
         MVC   ODDSAVE,0(R10)          SAVE LAST CHARACTER
EVEN     EQU   *
         BAL   R9,NUMTRAN              GO TRANSLATE FROM NUM CODE
         LA    R12,TOTTRMBF            DATA IN BUFFER            S99528
         SRL   R3,1                    DIVIDE BY 2
         LTR   R3,R3                   ANY REMAINDER             S99528
         BZ    NUM002                  NO BRANCH                 S99528
         BCTR  R3,0                    SUBTRACT 1
NUM001   EX    R3,MOVE                 MOVE TO TRMWRK
         LA    R3,1(R3)                RESTORE R3
         BAL   R9,ENDHUNT              GO SEE IF DONE
         LA    R12,0(R3,R12)           BUMP POINTER
NUM002   EQU   *
*********
*********      ENTER NEXT SEGMENT
*********
         IEDQMSG MSGID=099,FUNCT=REP,INBUF=TOTINBUF,INCNT=80,    S99528C
               LINK=YES                ENTER NEXT SEGMENT        S99528
         TM    TOTFLG06,TOTCANCL       WAS AN ERROR FOUND        S22024
         BO    GETOUT              YES  TERMINATE TEST
         LA    R4,TOTINBUF             PICK UP BUFFER ADDRESS    S22024
         SR    R3,R3                   CLEAR REG                 S99528
         XC    WORK4(100),WORK4        CLEAR WORK AREA
         SR    R3,R3                   CLEAR
         BAL   R9,ENDSRCH              GO GET MSEESAGE LENGTH
         LR    R8,R3                   TRANSFER TO R8
         N     R8,ANDFLD               ODD NUMBER?
         BC    8,NUM100                NO BRANCH
         TM    EOFLG,ODD               LAST MSG ODD?
         BZ    NUM102                  NO BRANCH
         LA    R3,1(R3)                ADD ONE TO COUNT
         MVC   WORK4(1),ODDSAVE        MOVE SAVED CHARACTER
         MVC   WORK4+1(99),0(R4)       MOVE REST OF MESSAGE
         NI    EOFLG,X'00'             CLEAR ODD FLAAG
         B     NUM103                  EXIT
NUM100   EQU   *
         TM    EOFLG,ODD               LAST MESSAGE ODD?
         BO    NUM101                  YES BRANCH
         MVC   WORK4,0(R4)             MOVE MSG TO WORK AREA
         B     NUM103                  EXIT
NUM101   EQU   *
         MVC   WORK4(1),ODDSAVE        MOVE SAVED CHAR TO WORK
         BCTR  R3,0                    DECREMENT COUNT
         LA    R10,0(R3,R4)            POINT TO LAST CHAR IN MSG
         MVC   ODDSAVE,0(R10)          SAVE IT
         LA    R3,1(R3)                RESTORE COUNT
         MVC   WORK4+1(99),0(R4)       MOVE REST OF MESSAGE
         B     NUM103                  EXIT
NUM102   EQU   *
         BCTR  R3,0                    DECREMENT REG             S99528
         LA    R10,0(R3,R4)            GET ADDR OF LAST CHARACTER
         MVC   ODDSAVE,0(R10)          SAVE LAST CHAR
         BCTR  R3,0                    DECREMENT REG             S99528
         EX    R3,MOVE2                MOVE EVEN NUM CHARS TO WORKAREA
         LA    R3,1(R3)                RESTORE COUNT
         OI    EOFLG,ODD               TURN ON ODD FLAG
NUM103   EQU   *
         LA    R4,WORK4                SET UP POINTER
         BAL   R9,NUMTRAN              GO TRAN FROM NUM CODE
         SRL   R3,1                    DIVIDE BY 2
         LTR   R3,R3                   ZERO                      S99528
         BZ    NUM002                  YES BRANCH                S99528
         LA    R15,0(R3,R15)           GET PROJECTED END OF MESSAGE
         LA    R14,TOTOLTID            END OF REPLY BUFFER       S99528
         CR    R15,R14                 IS BUFFER LONG ENOUGH?
         BL    NUM003                    YES
         SR    R14,R15                 COUNT BYTES OF AVAILABLE SPACE
         BCTR  R14,0                   REDUCE BY 1 FOR EXECUTE
         EX    R14,MOVE                MOVE WHAT THERE IS ROOM FOR
         B     EXIT                    EXIT TO TRM ANALYSIS
NUM003   EQU   *
         BCTR  R3,0                    DECREMENT REG             S99528
         EX    R3,MOVE                 MOVE TO TRM WRK
         LA    R3,1(R3)                RESTORE R3
         BAL   R9,ENDHUNT              GO FIND END OF SEGMENT    S99528
         LA    R12,0(R3,R12)           BUMP POINTER
         B     NUM002                  GO READ AGAIN
MOVE2    MVC   WORK4(0),0(R4)          MOVE MSG TO WORK AREA
MOVE     MVC   ZERO(ZERO,R12),WORK3    TRM TO WORK AREA          S22024
EXIT     EQU   *
         OI    TOTFLG04,TOTNUMDV       TURN ON FLAG FOR TRM ANAL S22024
         L     R13,4(R13)              PICK UP CALLERS SAVE AREA
         LM    R14,R1,12(R13)          RESTORE REGS
         XCTL  (2,12),EP=IEDQWC1       GO SCAN TRM               S99528
GETOUT   EQU   *
         L     R13,4(R13)              PICK UP CALLERS SAVE AREA
         LM    R14,R1,12(R13)          RESTORE REGS
         XCTL  (2,12),EP=IEDQWE
*
         EJECT
*
*        ROUTINE TO TRANSLATE MESSAGE FROM NUMERIC CODE.
*        R3=COUNT, R4=MESSAGE
*
NUMTRAN  EQU   *
         LTR   R3,R3                   ANY COUNT GIVEN?
         BCR   8,R9                    NO RETURN
         XC    WORK3,WORK3             CLEAR WORK AREA
         LR    R5,R4                   PICK UP POINTER
         LA    R7,WORK3                PICK UP WORK POINTER'
         LR    R10,R3                   PICK UP COUNT
PACKIT   XC    WORK1,WORK1             CLEAR WORK AREA
         PACK  WORK1(2),0(2,R5)        PACK 2 BYTES
         LH    R8,WORK1                GET PACKED HALF WORD      S99528
         SRL   R8,4                    GET RID OF SIGN
         STC   R8,0(R7)                STORE IN WORK AREA
         LA    R7,1(R7)                BUMP POINTERS
         LA    R5,2(R5)                MSG POINTER PLUS TWO      S99528
         BCTR  R10,0                   DECREMENT COUNT           S99528
         BCT   R10,PACKIT              PACK AGAIN IF ANY         S99528
         BCTR  R3,0                    DECREMENT COUNT           S99528
         EX    R3,TRAN2                TRANS LATE
         LA    R3,1(R3)                RESTORE R3
         BR    R9                      RETURN
TRAN2    TR    WORK3(0),TRANTAB        TRANSLATE TRM SEGMENT     S99528
         EJECT
*                                                               *
*              TRANSLATE TABLE FOR NUMERIC DEVICES                    *
*                                                               *
TRANTAB  DC    CL16'0123456789'
         DC    CL16'+-,=.*()''/'
         DC    CL16'ABCDEFGHI '
         DC    CL16'          '
         DC    CL16'JKLMNOPQR '
         DC    CL48'          '
         DC    CL10'STUVWXYZ  '
         EJECT
*
*              ROUTINE TO FIND THE LENGTH OF THE MESSAGE
ENDSRCH  EQU   *
*
*        R3    COUNT REG
*        R4    MESSAGE
*
         LR    R7,R4                   PICK UP MESSAGE
ENDSRCH1 EQU   *
         TM    0(R7),X'F0'             END OF MESSAGE?
         BNO   ENDSRCH9                YES BRANCH
         LA    R3,1(R3)                BUMP COUNTER
         LA    R7,1(R7)                BUMP POINTER
         B     ENDSRCH1                KEEP LOOKING FOR EOM      S99528
         SPACE
ENDSRCH9 EQU   *
         BR    R9                      RETURN
         EJECT
*
*        ROUTINE TO SEE IF END OF TRM
*
ENDHUNT  EQU   *
         LR    R10,R3                   PICK UP COUNT
         LR    R4,R12                  PICK UP POINTER
END001   CLI   0(R4),C')'              END OF MESSAGE
         BE    SPACKBK                 CHECK OOR BACKSPACE CHARS S99528
         LA    R4,1(R4)                BUMP POINTER
         BCT   R10,END001              FIND END OF MSG           S99528
         BR    R9                      RETURN                    S99528
         EJECT
SPACKBK  SR    R8,R8                   CLEAR REG                 S99528
         SR    R5,R5                   CLEAR REG                 S99528
         LA    R3,TOTTRMBF             START OF TRM              S99528
         LA    R4,TOTTRMND             END OF TRM                S99528
         LR    R7,R3                   SAVE START OF TRM         S99528
NUMOFCH  CLI   ZERO(R3),EOM            END OF TRM                S99528
         BE    BACKSP                  SEARCH OOR BACHSPACE      S99528
         LA    R5,ONE(R5)              NUM OF CHAR IN TRM        S99528
         LA    R3,ONE(R3)              BUMP TRM POINTER          S99528
         B     NUMOFCH                 CONT SEARCH               S99528
BACKSP   EQU   *                       FIND BACKSPACE CHARS      S99528
         LR    R3,R7                   START OF TRM              S99528
         LR    R9,R5                   SAVE TRM LEMGTH           S99528
BACKCOMP CLI   ZERO(R3),PLUS           BACKSPACE CHARH           S99528
         BE    BACSPTRM                GO REMOVE BACK SPACE CHAR S99528
*                                      AND PRECEEDING CHAR       S99528
         LA    R3,ONE(R3)              PUMP TRM POINTER          S99528
         LA    R8,ONE(R8)              BUMP NUMBER CHAR PTR      S99528
         CR    R8,R5                   END OF TRM                S99528
         BE    PRINTTRM                YES BRANCH                S99528
         B     BACKCOMP                LOOK FOR MORE BK SP CHARS S99528
BACSPTRM EQU   *                       DELETE CHARS FROM TRM     S99528
         LR    R10,R3                  GET NEXT                  S99528
         LA    R10,ONE(R10)            GOOD CHARACTER            S99528
         BCTR  R3,ZERO                 DECREMENT POINTER         S99528
         SR    R9,R8                   NUMBER OF CHARS TO MOVE   S99528
         EX    R9,COMPTRM              RE FORMAT TRM             S99528
         B     SPACKBK                 ANY MORE BACKSP           S99528
PRINTTRM EQU   *                       PRINT THE TRM             S99528
         EX    R5,MOVETRM              MOVE TRM TO BUF           S99528
         STC   R5,MSGLEN               LENGTH OF MSG             S99528
MSGTRM   IEDQMSG OTBUF=TRM,OTCNT=10,FUNCT=(CEC,PRT),RET=EXIT,          C
               PREG0=R0,PREG1=R1                                 S99528
MOVETRM  MVC   TRM(0),TOTTRMBF+1       TRM TO BUFFER             S99528
MSGLEN   EQU   MSGTRM+12               LENGTH OF TRM             S99528
COMPTRM  MVC   ZERO(ZERO,R3),ZERO(R10) REMOVE BLANKS FROM TRM    S99528
         EJECT
WORK1    DS    2F                      WORK AREA                 S99528
WORK2    DS    F                       WORK AREA                 S99528
TRANAD   DS    F                       TRANS WORK AREA           S99528
ANDFLD   DC    X'00000001'             ODD TEST                  S99528
OFFSET   DS    H                       OFFSET                    S99528
TRMWRK   DS    CL72                    TRM WORK AREA             S99528
WORK3    DS    CL256                   WORK AREA                 S99528
WORK4    DS    CL100                   WORK AREA                 S99528
ODD      EQU   X'80'                   ODD TEST                  S99528
ODDSAVE  DS    CL1                     ODD BYTE SAVE             S99528
EOFLG    DC    X'00'                   FLAG BYTE                 S99528
TRM      DC    CL256' '                TRM PRINT                 S99528
PATCH    DC    CL200' '                PATCH AREA
         LTORG
         EJECT
         TQCBD
         EJECT
         DCBD  DSORG=TX
         EJECT
         TSCBD
         EJECT
         TPRFD
         EJECT
         TLCBD
         EJECT
CVT      DSECT
         CVT
         EJECT
         TTNTD
         EJECT
UCB      DSECT
         IEFUCBOB
         EJECT
         TAVTD
         EJECT
         EJECT
TOTNPERM EQU   *
         OLTCB                         TOTE CONTROL BLOCK        S22024
         EJECT
         TTRMD
         END
