QWQ      TITLE 'IEDQWQ - TOTE CECOM SERVICE MODULE'
IEDQWQ   CSECT
*A000000-999999                                                @Y16X5U0
*** THIS MODULE IS BEING RESEQUENCED AS OF TCAM 10.0           @Y17XAUU
* CHANGE ACTIVITY AS FOLLOWS:
*A096400,687884                                                 SA41592
*C688190-688210,688220-688320                                   SA41592
*D687948-687956                                                 SA41592
*A624000                                                        SA41598
*A120400,528000,625000,705000                                   SA41606
*C423000,540000,555000-558000,625000-630000                     SA41601
*D597000                                                        SA41601
*A687492-687494                                                 SA41601
*A687634                                                        SA41609
*C687604                                                        SA41609
*A687496-687498                                                 SA41613
*A014000,097100-097160,435600-437400,687471-687478               S22024
*A689562-689574,689630,726850                                    S22024
*C695900,728300                                                  S22024
*D689520                                                         S22024
*C384450,728914                                                  S05331
*D591000-600000                                                 SA64786
*D436800-437400                                                 SA64786
*A690800-690900                                                 SA64786
*C687891                                                       @YA06896
*A137000,279000,288000,327000,437700,486000,686600,687889,     @Y17XAUU
*A689280,689300,689420,689500,845000                           @Y17XAUU
*C691500,689500-689540,689300                                  @Y17XAUU
* A584000                                                      @G36XRUV
* C585000                                                      @G36XRUV
IEDQW37  EQU   *                                                 S99528
         ENTRY IEDQW37
         SPACE 4
**************************************************************** S99528
*                                                              * S99528
* MODULE NAME = IEDQWQ -ALIAS IEDQW37 (TCAM,TOTE)              @Y17XAUU
*                                                              *
* DESCRIPTIVE NAME = CECOM SERVICE MODULE                      *
*                                                              *
* COPYRIGHT = NONE                                             *
*                                                              *
* STATUS --                                                    * S99528
*    VERSION 10.0                                              @Y17XAUU
*                                                              * S99528
* FUNCTIONS --                                                 * S99528
*    THE PURPOSE OF THIS MODULE IS TO SERVICE REQUESTS FOR     * S99528
*    COMMUNICATION WITH THE CONTROL TERMINAL.                  * S99528
*                                                              * S99528
*    THE CECOM SERVICE MODULE PROCESSES THE CALLING ROUTINES   * S99528
*    PARAMETER LIST AND ROUTES OUTPUT REQUESTS TO THE CONTROL  * S99528
*    TERMINAL VIA THE ACCESS MANAGER ROUTINE. IF THE CONTROL   * S99528
*    TERMINAL IS THE SYSTEM CONSOLE ALL REPLIES ARE PLACED IN  * S99528
*    THE INPUT BUFFER OF THE OLTCB.  IF THE CONTROL TERMINAL   * S99528
*    IS A REMOTE TERMINAL IEDQWQ OBTAINS THE INPUT TEXT FROM   * S99528
*    THE TCAM BUFFER. THE BUFFER IS THEN RETURNED TO TCAM. IN  * S99528
*    ALL CASES INVOLVING REPLIES IEDQWQ TRANSLATES THE REPLY   * S99528
*    MESSAGE TO UPPER CASE AND MOVES IT TO THE CALLING ROUTINE * S99528
*    INPUT BUFFER.                                             * S99528
*                                                              * S99528
*    AT THE COMPLETION OF THIS ROUTINE, ONE OF THE FOLLOWING   * S99528
*    RETURN CODES IS PLACED IN REGISTER 15:                    * S99528
*         0 - GOOD RETURN                                      * S99528
*         4 - UNSUPPORTED FUNCTION OR MACRO LEVEL              * S99528
*         16 - FOR A 3705 PRE SNA TEST DEVICE, THE CONTROL     @Y17XAUU
*              TERMINAL EQU THE TEST TERMINAL OR IS ON THE     @Y17XAUU
*              SAME LINE AS THE TEST DEVICE. THE 3705 LINE     @Y17XAUU
*              IS STILL IN TEST MODE WHEN A CECOM WITH         @Y17XAUU
*              REPLY EXPECTED IS ISSUED BY THE OLT(REPLIES     @Y17XAUU
*              ARE NOT PERMITTED AT THIS TIME).                @Y17XAUU
*                                                              * S99528
* ENTRY POINTS --                                              * S99528
*    IEDQWQ - CALLED WHEN A CECOM MACRO IS ISSUED BY THE       * S99528
*    ON-LINE TEST PROGRAM. IEDQW37 IS AN ALIAS FOR THIS MODULE.* S99528
*                                                              @Y17XAUU
* PURPOSE -- SEE FUNCTION                                      @Y17XAUU
*                                                              @Y17XAUU
* MODULE TYPE -- PROCEDURE                                     @Y17XAUU
*                                                              @Y17XAUU
* MODULE SIZE -- 2K MAXIMUM                                    @Y17XAUU
*                                                              @Y17XAUU
* LINKAGE -- BALR R14,R15 FROM IEDQWA                          @Y17XAUU
*                                                              @Y17XAUU
* MACROS -- SAVE, IEDHJN, AQCTL, WAIT                          @Y17XAUU
*                                                              * S99528
* INPUT --                                                     * S99528
*    REGISTERS 01,02,03,13,14,15 CONTAIN THE FOLLOWING VALUES: * S99528
*                                                              * S99528
*         01 - CECOM PARAMETER LIST ADDRESS                    * S99528
*         02 - OLTCB AND SCT POINTER                           * S99528
*         03 - OLT SECTION PREFACE ADDRESS                     * S99528
*         13 - CALLER'S SAVE AREA ADDRESS                      * S99528
*         14 - RETURN ADDRESS                                  * S99528
*         15 - IEDQWQ ENTRY POINT ADDRESS                      * S99528
*                                                              * S99528
* OUTPUT --                                                    * S99528
*    REGISTER 15 CONTAINS THE RETURN CODE.                     * S99528
*    TOTRTCOD CONTAINS THE RETURN CODE.                        * S99528
*    THE OLT INPUT BUFFER CONTAINS THE REPLY IF IT IS EXPECTED.* S99528
*                                                              * S99528
* EXTERNAL REFERENCES --                                       * S99528
*    IEDQWO - ACCESS MANAGER ROUTINE TO HANDLE                 * S99528
*             INPUT/OUTPUT REQUESTS.                           * S99528
*                                                              * S99528
* EXITS,NORMAL --                                              * S99528
*    CONTROL IS PASSED TO THE CALLING PROGRAM IF THE RETURN    * S99528
*    CODE IS ZERO AND THE TRACE FUNCTION IS NOT REQUESTED.     * S99528
*                                                              * S99528
* EXITS,ERROR --                                               * S99528
*    CONTROL IS PASSED TO IEDQWM2 (TRACE PROGRAM) IF ANY OF    * S99528
*    CONDITIONS HOLD.                                          * S99528
*                                                              * S99528
*         THE TRACE FUNCTION IS REQUESTED.                     * S99528
*         THE RETURN CODE FUNCTION IS REQUESTED AND THE        * S99528
*           RETURN CODE IS NONZERO.                            * S99528
*         THE MACRO LEVEL OF THE REQUEST IS WRONG.             * S©9528
*                                                              * S©9528
* TABLES/WORK AREA --                                          * S99528
*    OLTCB,LCB,SCB,PRF,AVT,IOBLOCKS,UCB                        * S99528
*                                                              * S99528
* ATTRIBUTES --                                                * S99528
*    ENABLED, PROBLEM PROGRAM MODE, RESIDENT, REENTRANT        @Y17XAUU
*                                                              * S99528
* NOTES -- SEE BELOW                                           @Y17XAUU
*                                                              * S99528
*  RESTRICTIONS--NONE                                          @Y17XAUU
*                                                              @Y17XAUU
*  REGISTERS CONVENTION--SEE REGISTERS ASSIGNMENT              @Y17XAUU
*                                                              @Y17XAUU
*  DEPENDENCIES--EBCDIC CHARACTER CODE SET                     @Y17XAUU
*                                                              @Y17XAUU
*  PATCH LABEL--PATCH                                          @Y17XAUU
*                                                              @Y17XAUU
**************************************************************** S99528
         EJECT
*                                                                     *
*        E Q U A T E S
*                                                                S99528
R0       EQU   0                  REGISTER 0                     S99528
R1       EQU   1                  REGISTER 1                     S99528
R2       EQU   2                  REGISTER 2                     S99528
R3       EQU   3                  REGISTER 3                     S99528
R4       EQU   4                  REGISTER 4                     S99528
R5       EQU   5                  REGISTER 5                     S99528
R6       EQU   6                  REGISTER 6                     S99528
R7       EQU   7                  REGISTER 7                     S99528
R8       EQU   8                  REGISTER 8                     S99528
R9       EQU   9                  REGISTER 9                     S99528
R10      EQU   10                 REGISTER 10                    S99528
R11      EQU   11                 REGISTER 11                    S99528
R12      EQU   12                 REGISTER 12                    S99528
R13      EQU   13                 REGISTER 13                    S99528
R14      EQU   14                 REGISTER 14                    S99528
R15      EQU   15                 REGISTER 15                    S99528
         SPACE
ONE      EQU   1                  VALUE OF 1                     S99528
TWO      EQU   2                  VALUE OF 2                     S99528
THREE    EQU   3                  VALUE OF 3                     S99528
FOUR     EQU   4                  VALUE OF 4                     S99528
EIGHT    EQU   8                  VALUE OF 8                     S99528
NINE     EQU   9                  VALUE OF 9                     S99528
SIXTN    EQU   16                 VALUE OF 16                    S99528
MI       EQU   X'80'              MANUAL INTERVENTION            S99528
TERM     EQU   X'40'              LAST ROUTINE                   S99528
ZERO     EQU   0                  VALUE OF ZERO                  S99528
TEN      EQU   10                 VALUE OF TEN                   S99528
TWELVE   EQU   12                 VALUE OF TWELVE                S99528
MINUS    EQU   X'FF'              MASK OF 255                    S99528
LIMIT    EQU   80                 MAXLENGTH OF CECOM MSG ALLOWED S99528
TPOSTE   EQU   X'80'              END OF REQ. ELEMENT           SA41592
TRMTERM  EQU   X'10'              SEC. CONTROL TERMINAL         SA41592
TPOSTA   EQU   X'0C'              FIRST ELEMENT                 SA41592
XFF      EQU   X'FF'              MASK ALL BITS                  S22024
RQINIT   EQU   X'0C'              START OF LIST                  S22024
RQEND    EQU   X'80'              END OF LIST                    S22024
         SPACE
D0       EQU   0                  DISP OF 0                      S99528
D1       EQU   1                  DISP OF 1                      S99528
D2       EQU   2                  DISP OF 2                      S99528
D3       EQU   3                  DISP OF 3                      S99528
D4       EQU   4                  DISP OF 4                      S99528
D5       EQU   5                  DISP OF 5                      S99528
D6       EQU   6                  DISP OF 6                      S99528
A        EQU   X'C1'              CHARACTER A                    S99528
TPAR1    EQU   X'0C'              TPOST FLAG 1                   S99528
TPAR2    EQU   X'80'              TPOST FLAG 2                   S99528
D7       EQU   7                  DISP OF 7                      S99528
D8       EQU   8                  DISP OF 8                      S99528
D9       EQU   9                  DISP OF 9                      S99528
D10      EQU   10                 DISP OF 10                     S99528
D11      EQU   11                 DISP OF 11                     S99528
D12      EQU   12                 DISP OF 12                     S99528
D13      EQU   13                 DISP OF 13                     S99528
D14      EQU   14                 DISP OF 14                     S99528
D15      EQU   15                 DISP OF 15                     S99528
D16      EQU   16                 DISP OF 16                     S99528
D17      EQU   17                 DISP OF 17                     S99528
D18      EQU   18                 DISP OF 18                     S99528
D19      EQU   19                 DISP OF 19                     S99528
D20      EQU   20                 DISP OF 20                     S99528
D24      EQU   24                 DISP OF 24                     S99528
D28      EQU   28                 DISP OF 28                     S99528
D32      EQU   32                 DISP OF 32                     S99528
D36      EQU   36                 DISP OF 36                     S99528
D40      EQU   40                 DISP OF 40                     S99528
D79      EQU   79                 DISP OF 79                    SA41901
ANR      EQU   X'1E'              ANR LOCAL                      S99528
XF0      EQU   X'F0'              LCB FLAG MASK                  S99528
MACLEV   EQU   X'02'              MACRO LEVEL FOR THIS MODULE    S99528
BADLEV   EQU   X'04'              UNSUPT'D FTN RETURN CODE       S99528
OTWTOR   EQU   X'E0'              REQ. FOR OUTSTANDING WTOR      S99528
CECNT    EQU   62                 MAXIMUM INPUT COUNT            S99528
LINBUF   EQU   X'50'              LENGTH OF OUTPUT BUFFER        S99528
BLANKA   EQU   X'40'              BLANK CHARACTER                S99528
MULBUF   EQU   X'14'              MULTIPLE BUFFER PREFIX         S22024
PREFIX   EQU   C'I'               MSG PREFIX                     S22024
DECL     EQU   C'I'               MSG SUFFIX (NO REPLY)          S22024
REPLY    EQU   C'D'               MSG SUFFIX (REPLY)             S22024
RQPRI    EQU   X'DC'              POST PRIORITY TO OP CTL      @Y17XAUU
RQELNG   EQU   X'18'              POST ELEMENT LENGTH          @Y17XAUU
DISREG2  EQU   28                 DISPLACEMENT TO REG 2        @Y17XAUU
LUTYPE   EQU   X'1C'              INPUT IS FROM A LU           @Y17XAUU
ERRCD16  EQU   X'10'              ERROR RETURN CODE - LINE IN  @Y17XAUU
*                                 TEST MODE FOR A CECOM WITH   @Y17XAUU
*                                 REPLY EXPECTED               @Y17XAUU
         EJECT
         USING IEDQWQ,R15
*
IEDQWQ    IEDHJN STARQ,HJN                                       S99528
*
         DROP  R15
         USING IEDQWQ,R4
*
         USING TOTOLTCB,R2
         SPACE
         SAVE  (14,12)                                           S99528
         SPACE
         LR    R4,R15             SET BASE REGISTER              S99528
         LA    R15,TOTSAVE1       GET ADDR. OF OWN SAVE AREA     S99528
         ST    R15,D8(R13)        PUT ADDR. IN CALLER'S SAVE     S99528
         ST    R13,D4(R15)        SAVE ADDR OF CALLER'S SAVE     S99528
         LR    R13,R15            POINT REG 13 TO MY SAVE AREA   S99528
         SPACE
         NI    TOTFLG05,MINUS-TOTEXIOF-TOTCECOM CLEAR ACCESS     S99528
         NI    TOTFLG05,MINUS-TOTPRINT-TOTREPLY FLAGS            S99528
         LR    R11,R1             SAVE P-LIST ADDR               S99528
         LA    R5,D17(R1)         POINT TO CECOM FLAG            S99528
         CLI   D1(R1),MACLEV      CORRECT MACRO LEVEL            S99528
         BE    CEC001             YES                            S99528
         SPACE
         LA    R15,BADLEV         SET FTN NOT AVAIL RET CODE     S99528
         STC   R15,TOTRTCOD       SAVE RETURN CODE               S99528
         SPACE
CE005    EQU   *                                               @Y17XAUU
         L     R13,D4(R13)        RESTORE CALLER'S SAVE AREA ADR S99528
         L     R14,D12(R13)       RESTORE RETURN ADDR            S99528
         L     R15,TOTRESPL       GET RESPL ADDRESS            @Y17XAUU
         USING RESPL,R15          SET RESPL ADDRESSABILITY     @Y17XAUU
         L     R15,RESPLM2        GET ADDRESS OF TRACE ROUTINE @Y17XAUU
         LM    R2,R12,DISREG2(R13) RESTORE CALLER'S REG        @Y17XAUU
         DROP  R15                                             @Y17XAUU
         BR    R15                EXIT TO IEDQWM2              @Y17XAUU
         SPACE 4
CEC001   EQU   *                                                 S99528
         TM    D0(R5),OTWTOR      REQUEST FOR OUTSTANDING WTOR   S99528
         BZ    CEC003             BR IF NO                       S99528
         SPACE
CEC002   EQU   *                                                 S99528
         OI    TOTFLG09,TOTMACFT  SET UNSUPPORTED MACRO FTN FLAG S99528
         LA    R15,BADLEV         SET FUNCTION NOT AVAIL RET CODES99528
         STC   R15,TOTRTCOD       SAVE RETURN CODE               S99528
         B     CE005              REPORT ERROR                 @Y17XAUU
         SPACE 4
CEC003   EQU   *                                                 S99528
         MVI   TOTRTCOD,ZERO      SET RETURN CODE                S99528
         SPACE
CEC004   EQU   *                                                 S99528
         SR    R15,R15            CLEAR RETURN CODE REG.         S99528
         OI    TOTFLG05,TOTCECOM  SET CECOM REQUEST FLAG         S99528
         L     R10,D4(R1)         GET OUT MSG ADDR               S99528
         L     R9,D8(R1)          GET OUT MSG COUNT              S99528
         LTR   R9,R9              ZERO COUNT                     S99528
         BZ    CEC054             YES                            S99528
         SPACE
         LA    R5,LIMIT           GET MAX NO. BYTES ALLOWED      S99528
         CR    R9,R5              IS COUNT > LIMIT               S99528
         BNH   CEC008             BRANCH IF NO                   S99528
         SPACE
         LR    R9,R5              SET COUNT = LIMIT              S99528
         SPACE
CEC008   EQU   *                                                 S99528
         MVI   TOTOTBUF,BLANKA    CLEAR OUTPUT                   S22024
         MVC   TOTOTBUF+D1(D79),TOTOTBUF BUFFER                  S22024
         L     R3,TOTOLTEN        START OF OLT                   S22024
         USING PSPREF,R3                                         S22024
         MVC   MSGIDSV(D2),$MSGIDAC  SAVE OLT COMPONENT ID       S05331
         LH    R14,RTNDISP        DISP TO RTN#                   S22024
         AR    R3,R14             POINT TO RTN#                  S22024
         DROP  R3                                                S22024
         USING SPREF,R3                                          S22024
         TM    #FLAG,#MSGIDRQ     MSGID PROVIDED                 S22024
         BZ    CEC010             NO                             S22024
         DROP  R3                                                S22024
*        BUILD OLT UNIQUE MESSAGE COMPONENT ID                   S22024
         MVI   TOTOTBUF,PREFIX    MSG PREFIX                     S22024
         MVC   TOTOTBUF+D1(D2),MSGIDSV OLT COMPONENT ID          S22024
         MVC   TOTOTBUF+D3(D3),D0(R10) MSG NUMBER                S22024
         MVI   TOTOTBUF+D6,DECL   SET MSG SUFFIX (NO REPLY)      S22024
         L     R3,D16(R11)        GET XPTD IN MSG COUNT          S22024
         LTR   R3,R3              REPLY EXPECTED                 S22024
         BZ    CEC009             NO                             S22024
         MVI   TOTOTBUF+D6,REPLY  SET MSG SUFFIX (REPLY)         S22024
CEC009   DS    0H                 BOUNDARY ALLIGNMENT            S22024
         LA    R9,D5(R9)          ADJUST MSG COUNT               S22024
         STC   R9,TOTOTCNT        SET OUTPUT COUNT
         L     R9,D8(R1)          OUTMSG COUNT                   S22024
         S     R9,F4              ADJUST FOR MOVE                S22024
         C     R9,F0              EX COUNT < 0                   S22024
         BL    CEC054             YES - NO DATA PRESENT          S22024
         EX    R9,MSGMOVE1        MOVE MSG TO OUTPUT BUFFER      S22024
         B     CEC011             CONTINUE                       S22024
CEC010   DS    0H                 BOUNDARY ALLIGNMENT            S22024
         STC   R9,TOTOTCNT        PUT OUT MSG CNT IN OLTCB       S99528
         EX    R9,CEC080          MOVE OUT MSG TO OLTCB          S99528
CEC011   DS    0H                 BOUNDARY ALLIGNMENT            S22024
         SPACE
         L     R9,D12(R1)         GET IN MSG ADDR                S99528
         LTR   R9,R9              ANY IN MSG                     S99528
         BZ    CEC012             BRANCH IF NO                   S99528
         SPACE
         L     R10,D16(R11)       GET XPTD IN MSG COUNT          S99528
         LTR   R10,R10            IS COUNT ZERO                  S99528
         BZ    CEC012             BRANCH IF NO                   S99528
         SPACE
         OI    TOTFLG05,TOTREPLY  SET REPLY FLAG                 S99528
         MVI   TOTINBUF,BLANKA    CLEAR INPUT BUFFER            SA41601
         MVC   TOTINBUF+D1(D79),TOTINBUF                        SA41601
         SPACE
*                                                                S99528
*        S E T  U P  F O R  S T A R T  L I N E                   S99528
*                                                                S99528
         TM    TOTFLG09,TOTCTCON  CT EQU SYSCON                  S22024
         BO    CEC012             YES                            S22024
         NI    TOTFLG09,XFF-TOTLNCON TURN OFF LINE CONN FLAG     S22024
         TM    TOTFLG02,TOTTSST   CT LINE IN TEST MODE         @Y17XAUU
         BZ    CEC0EX             NO - CONTINUE                @Y17XAUU
         LA    R15,ERRCD16        SET ERROR RETURN CODE        @Y17XAUU
         STC   R15,TOTRTCOD       SAVE RETURN CODE             @Y17XAUU
         B     CEC054             EXIT                         @Y17XAUU
CEC0EX   EQU   *                                               @Y17XAUU
         MVI   TOTOVBCD,LINESTRT  SET VERB CODE FOR START LINE @Y17XAUU
*                                                                S99528
         BAL   R8,STRTLINE        START CT LINE                  S22024
         SPACE
CEC012   EQU   *                                                 S99528
         TM    $ERROPT,$NOCNTRL   NO CONTROL PRINT SELECTED ?    S99528
         BZ    CEC016             NO                             S99528
         TM    TOTFLG05,TOTREPLY  YES - IS REPLY TO CECOM SET    S99528
         BZ    CEC050             NO - BYPASS PRINT              S99528
         SPACE
CEC016   EQU   *                                                 S99528
         L     R15,TOTRESPL       GET RESPL ADDRESS            @Y17XAUU
         USING RESPL,R15          SET RESPL ADDRESSABILITY     @Y17XAUU
         L     R15,RESPLWO        GET I/O ACCESS ROUTINE ADDR  @Y17XAUU
         DROP  R15                                             @Y17XAUU
         BALR  R14,R15            WRITE MESSAGE                @Y17XAUU
         SPACE
         TM    TOTFLG09,TOTCTCON  CT = SYSCON                    S99528
         BZ    CECREM             NO - ASSUME REMOTE             S99528
*                                                                S99528
         TM    TOTFLG05,TOTREPLY  REPLY REQUESTED                S99528
         BO    CEC022             BRANCH IF YES                  S99528
         B     CEC050             NO - RETURN                    S99528
         SPACE
CEC022   EQU   *                                                 S99528
         LA    R5,LINBUF          GET LENGTH OF IN MSG BUFFER    S99528
         LR    R3,R5              SET MSG COUNTER                S99528
         LA    R6,TOTINBUF        GET ADDR OF IN BUFFER          S99528
         BCTR  R5,R0              SUBTRACT ONE FROM LENGTH       S99528
         CLI   D0(R6),BLANKA      ANY REPLY READ IN             SA41601
         BNE   CEC026             YES                           SA41601
         MVI   D0(R9),ZERO        ZERO DATA COUNT               SA41601
         B     CEC054             RETURN                        SA41601
CEC026   EQU   *                                                SA41601
         AR    R6,R5              POINT TO LAST BYTE             S99528
         SPACE
CEC028   EQU   *                                                 S99528
         CLI   D0(R6),BLANKA      END OF MESSAGE                SA41601
         BNE   CEC030             YES - BRANCH                   S99528
         BCTR  R3,R0              DECREMENT MSG COUNTER          S99528
         BCTR  R6,R0              POINT TO NEXT POSITION         S99528
*                                                                S99528
         B     CEC028             CHECK NEXT CHAR               SA41601
         SPACE
CEC030   EQU   *                                                 S99528
         LA    R6,TOTINBUF        POINT TO INPUT BUFFER          S99528
         EX    R5,CEC082          XLATE TO UPPER CASE            S99528
         SPACE
CEC040   EQU   *                                                 S99528
         MVI   D0(R9),ZERO        CLEAR IN COUNT POSITION        S99528
         L     R5,D16(R11)        GET XPTD IN MSG COUNT          S99528
         LTR   R5,R5              ZERO COUNT                     S99528
         BZ    CEC054             YES                            S99528
         SPACE
CEC042   EQU   *                                                 S99528
         CL    R5,K62             MAXIMUM COUNT EXCEEDED         S99528
         BNH   CEC045             NO                             S99528
         LA    R5,CECNT           SET COUNT TO MAXIMUM           S99528
         SPACE
CEC045   EQU   *                                                 S99528
         BCTR  R5,R0              DECREMENT FOR EX               S99528
         EX    R5,CEC090          CLEAR OLT INPUT BUFFER        SA41601
         LA    R5,D1(R5)          RESTORE XPTD IN COUNT         SA41601
         CR    R3,R5              XPTD COUNT < ACTUAL COUNT     SA41601
         BNH   CEC047             NO - USE LESSER COUNT         SA41601
         LR    R3,R5              GET LESSER COUNT              SA41601
CEC047   EQU   *                                                SA41601
         STC   R3,D0(R9)          ACTUAL INPUT COUNT            SA41601
         BCTR  R3,R0              DECREMENT FOR EX              SA41601
         EX    R3,CEC084          MOVE RCVD MSG TO OLT          SA41601
         SPACE
CEC050   EQU   *                                                 S99528
CEC054   EQU   *                                                 S99528
         L     R13,D4(R13)        RESTORE CALLER'S SAVE AREA     S99528
         L     R14,D12(R13)       RESTORE RETURN ADDR            S99528
         TM    $OLTFLGS,$TRACE    TRACE OPTION SET               S99528
         BO    CEC060             YES                            S99528
         LM    R0,R12,D20(R13)    RESTORE REG                    S99528
         SR    R15,R15            CLEAR RET CODE REGISTER        S99528
         BR    R14                RETURN TO CALLER               S99528
*                                                                S99528
*        REQUEST IEDQWM2 TO PROVIDE TRACE FUNCTION               S99528
*                                                                S99528
CEC060   EQU   *                                                 S99528
         LM    R0,R1,D20(R13)     RESTORE REGISTERS 0-1          S99528
         L     R15,TOTRESPL       GET RESPL ADDRESS            @Y17XAUU
         USING RESPL,R15          SET RESPL ADDRESSABILITY     @Y17XAUU
         L     R15,RESPLM2        GET TRACE ROUTINE ADDRESS    @Y17XAUU
         DROP  R15                                             @Y17XAUU
         LM    R2,R12,DISREG2(R13) RESTORE REGISTERS           @Y17XAUU
         BR    R15                EXIT TO IEDQWM2              @Y17XAUU
         SPACE 4
CECREM   EQU   *                                                 S99528
         TM    TOTFLG05,TOTREPLY  REPLY EXPECTED?                S99528
         BZ    CEC050             NO                             S99528
         STM   R5,R8,WORK4        SAVE REGISTERS                 S99528
         SPACE
EMSG01D  EQU   *                                                 S22024
         SPACE 2
         XC    TOTOTECB(D4),TOTOTECB CLEAR ECB                   S99528
         MVI   TOTINBUF,BLANKA    CLEAR INPUT                   SA41601
         MVC   TOTINBUF+D1(D79),TOTINBUF  BUFFER                SA41601
         OI    TOTFLG03,TOTREPEX       TURN ON REPLY EXPECTED   SA41613
*                                        BIT                    SA41613
*                                                                S99528
*        WAIT FOR REPLY TO COME IN                               S99528
*                                                                S99528
         WAIT  ECB=TOTOTECB       ISSUE WAIT MACRO               S.9528
         SPACE
*                                                                S99528
*        THE FOLLOWING CODE RETRIVES A RESPONSE FROM             S99528
*        A TCAM BUFFER.  THE FIRST CHARACTER OF THE MESSAGE      S99528
*        IS THE CONTROL TERMINAL I. D.                           S99528
*                                                                S99528
         L     R6,TOTOLTMQ        GET ADDR OF TCAM BFR UNIT      S99528
         USING IEDQPRF,R6                                        S99528
         LA    R3,PRFSUNIT        GET START OF FIRST BFR UNIT    S99528
         AH    R3,PRFSCAN         POINT TO MSG ID                S99528
         LA    R7,D2              GET SIZE OF ID FIELD          SA41609
         LH    R10,PRFSIZE        GET TOTAL NUMBER OF DATA BYTES S99528
*                                   FROM THE BUFFER              S99528
         SH    R10,PRFSCAN        SUBTRACT RESERVE CHARACTERS    S99528
*                                   TO GET TOTAL NUMBER OF BYTES S99528
*                                   IN THE REPLY                 S99528
         SR    R10,R7             DECREMENT DATA COUNT BY SIZE  SA41609
*                                   OF ID FIELD(2)              SA41609
         SR    R7,R7              CLEAR INMSG COUNTER           SA41609
         LR    R8,R3              GET MSG POINTER
MSGLOOP  EQU   *                                                 S99528
         CLI   D2(R8),BLANKA      END OF MESSAGE?                S99528
         BL    EXMOVE             YES                            S99528
         LA    R7,D1(R7)          INCREMENT CHAR COUNTER         S99528
         LA    R8,D1(R8)          POINT TO NEXT CHAR             S99528
         BCT   R10,MSGLOOP        GO TEST NEXT BYTE              S99528
         SPACE
EXMOVE   EQU   *                                                 S99528
         STC   R7,D0(R9)          PUT MSG COUNT IN OLT BUFFER    S99528
         LTR   R7,R7              ANY RESPONSE?                  S99528
         BZ    EMSG11             NO                             S99528
         CL    R7,EIGHTY          MAXIMUM CHAR. EXCEEDED?        S99528
         BH    MAXMSG             YES - TRUNCATE IT              S99528
         BCTR  R7,R0              DECREMENT FOR EX               S99528
         B     MOVE               MOVE RESPONSE TO OLT           S99528
         SPACE
MAXMSG   EQU   *                                                 S99528
         LA    R7,LINBUF-ONE      SET MAX COUNT MINUS ONE        S99528
MOVE     EQU   *                                                 S99528
         EX    R7,RESPONSE        MOVE RESPONSE TO OLTCB         S99528
         SPACE
EMSG11   EQU   *                                                 S99528
*                                                                S99528
*        POST BUFFER LCB TO TCAM (BD)                            S99528
*                                                                S99528
         L     R12,TOTAVTPT       GET AVT ADDR                   S99528
         USING IEDQAVTD,R12
         LA    R5,AVTBFRTB        BFR RETURN QCB ADDR           SA41592
         ST    R5,WORK2           SAVE IT                       SA41592
         CLI   PRFKEY,TRMTERM     REQUEST FROM A SECONDARY      SA41592
*                                 OPERATOR CONTROL TERMINAL     SA41592
         BE    EMSGSCT            YES - BRANCH                  SA41592
         TM    PRFKEY,LUTYPE      LU ENTRY                     @Y17XAUU
         BE    EMSGSCT            YES - BRANCH                 @Y17XAUU
         CLI   PRFKEY,MULBUF      MULTIPLE BUFFERS PREFIX        S22024
         BE    EMSGSCT                  YES - POST BUFFER ONLY @YA06896
         L     R7,PRFLCB-ONE      GET REQ LINE LCB ADDR          S99528
         USING IEDQLCB,R7
         MVC   LCBLSPCI(D3),TOTOLTMQ+ONE PUT IN BUFFER ADDR      S99528
         MVI   LCBPRI,PRILCB      SET PRIORITY                   S99528
         L     R8,LCBSCBA-ONE     GET SCB ADDR                   S99528
         LA    R8,D0(R8)          CLEAR HIGH ORDER BYTE          S99528
         USING IEDQSCB,R8
         MVC   SCBDESTQ(D3),WORK2+ONE PUT IN SCB                 S99528
         L     R5,SCBMACR-ONE     GET ADDR OF BFR HDR ENTRY      S99528
         LA    R5,D0(R5)          CLEAR HI ORDER BYTE            S99528
         LA    R3,AVTINOUT        INEND/OUTEND PLIST ADDR        S99528
         ST    R3,DPARM           SAVE IT IN WORK AREA           S99528
         MVC   SCBMACR(D3),DPARM+D1 PUT IN SCB                   S99528
         L     R8,AVTMSGS-ONE                                    S99528
         LA    R8,D0(R8)          CLEAR HI ORDER BYTE            S99528
         L     R8,D0(R8)          GET QCB ADDR                   S99528
         ST    R8,WORK2           SAVE QCB ADDR FOR BD           S99528
         L     R8,LCBQCBA-D1      GET QCB ADDR                   S99528
         CLI   LCBRSKEY,ANR       TERMINAL = ANR                 S99528
         BNE   EMSG15             NO                             S99528
         MVI   LCBTSTSW,XF0       SET LCB FLAG FOR ANR LOCAL     S99528
         DROP  R8
EMSG15   EQU   *                                                 S99528
         MVC   LCBQCBA(D3),WORK2+ONE PUT QCB ADDR FOR BD IN LCB  S99528
*                                                                S99528
         ST    R7,TPOSTPAR        PUT LCB ADDR IN P-LIST         S99528
         ST    R7,TPOSTPAR+D4     PUT LCB ADDR IN P-LIST AGAIN   S99528
         MVI   TPOSTPAR,TPAR1     SET                            S99528
         MVI   TPOSTPAR+D4,TPAR2  FLAGS                          S99528
         LA    R1,TPOSTPAR        GET ADDRESS OF TPOST PLIST     S99528
         B     EMSG18             EXIT                          SA41592
         SPACE 2
*************************************************************** SA41592
*        REQUEST IS FROM SECONDARY OPERATOR CONTROL TERMINAL  * SA41592
*************************************************************** SA41592
EMSGSCT  EQU   *                                                SA41592
         ST    R5,PRFKEY          QCB ADDR IN BUFFER            SA41592
         MVI   PRFPRI,PRIBFRTB    PRIORITY                      SA41592
         L     R5,TOTOLTMQ        BUFFER ADDR                   SA41592
         ST    R5,TPOSTPAR+D4     SET UP POST                   SA41592
         MVC   TPOSTPAR+D1(D3),TPOSTPAR+D5 ELEMENT              SA41592
         MVI   TPOSTPAR,TPOSTA    SHOW FIRST ELEMENT            SA41592
         MVI   TPOSTPAR+D4,TPOSTE SHOW LAST BUFFER              SA41592
         LA    R1,TPOSTPAR        POINT TO PLIST                SA41592
*                                                               SA41592
         DROP  R7                                               SA41592
         DROP  R6                                               SA41592
         DROP  R12                                              SA41592
*                                                               SA41592
         SPACE
EMSG18   EQU   *                                                SA41592
         AQCTL                    POST BUFFER                   SA41592
         BAL   R5,CECSTOP         GO CHECK FOR STOPLINE          S99528
         SPACE 2
         LM    R5,R8,WORK4        RESTORE REGISTER               S99528
         B     CEC022             EXIT TO MOVE MSG TO OLT INBUF  S99528
         EJECT
**************************************************************** S99528
*                                                              * S99528
*        START LINE VIA TCAM'S OP-CONTROL FACILITY             * S99528
*                                                              * S99528
**************************************************************** S99528
STRTLINE DS    0H                                                S99528
         STM   R6,R12,STRTSAVE     SAVE REGS DURING STARTLINE    S99528
*                                                                S99528
         L     R6,TOTAVTPT        GET ADDRESS OF AVT             S99528
         USING IEDQAVTD,R6                                       S99528
         LA    R10,AVTOPCOB        GET POINTER TO OP CNTRL QCB   S99528
         ST    R10,TOTOKEY         PUT IN PARAMETER LIST       @Y17XAUU
         MVI   TOTOPRI,RQPRI       SET PRIORITY                @Y17XAUU
         MVI   TOTOELNG,RQELNG    SET ELEMENT LENGTH           @Y17XAUU
         DROP  R6                                                S99528
         MVC   TOTORLN(D1),TOTCTRLN CT RLN                     @Y17XAUU
         SR    R6,R6                                           @G36XRUV
         ICM   R6,3,TOTCTUCB      CT UCB ADDR                  @G36XRUV
         LTR   R6,R6               IS THERE A VALID UCB ADDRESS  S99528
         BZ    STRT5              NO - DONT ISSUE STARTLINE      S99528
         USING UCB,R6                                            S99528
         MVC   TOTOLADR(D8),BLANK BLANK OUT NAME FIELD         @Y17XAUU
         MVC   TOTOLADR(THREE),UCBNAME GET UNIT NAME           @Y17XAUU
         DROP  R6                                                S99528
         LA    R1,TOTOTECB         GET TOTE ECB                  S99528
         ST    R1,TOTOECBA         PUT IN PARAMETER LIST       @Y17XAUU
         XC    TOTOLINK(THREE),TOTOLINK CLEAR REQ ELEM LINK FLD@Y17XAUU
         XC    TOTORTCD,TOTORTCD  CLEAR RETURN CODE            @Y17XAUU
         XC    TOTOTECB(FOUR),TOTOTECB CLEAR TOTE ECB            S99528
         LA    R1,TOTPLIST        ADDR OF ELEMENT                S22024
         ST    R1,TRMSLPL         PUT IN PLIST                   S22024
         ST    R1,TRMSLPL+D4      PUT IN PLIST                   S22024
         MVI   TRMSLPL,RQINIT     SET INITIAL ELEMENT FLAG       S22024
         MVI   TRMSLPL+D4,RQEND   SET LAST ELEMENT FLAG          S22024
         LA    R1,TRMSLPL         ADDR OF PLIST                  S22024
         AQCTL                     POST ELEMENT TO OPERATOR CNTL S99528
*                                                                S99528
         WAIT  ECB=TOTOTECB                                      S99528
*                                                                S99528
STRT5    EQU   *
         LM    R6,R12,STRTSAVE     RESTORE REGS FROM STARTLINE   S99528
         BR    R8                  RETURN TO CALLER              S99528
         SPACE
CECSTOP  EQU   *                                                 S99528
         TM    TOTCTFLG,TOTCTORN  CT ON 3705                   @Y17XAUU
         BO    D0(R5)             YES - BYPASS CHECK           @Y17XAUU
         CLI   TOTOVBCD,LINESTRT  WAS START LINE ISSUED?        SA64786
         BNE   CECST4             NO, RETURN                    SA64786
         ST    R5,CECSAVE         SAVE RETURN ADDRESS            S99528
         MVI   TOTOVBCD,LINESTOP  SET VERB CODE FOR STOP LINE  @Y17XAUU
         CLI   TOTORTCD,ZERO      CT LINE INIT STOPPED           S22024
         BNE   CECST4             NO - RETURN                    S22024
         BAL   R8,STRTLINE        STOP CT LINE                   S22024
         L     R5,CECSAVE         GET RETURN ADDRESS             S99528
CECST4   EQU   *                                                 S99528
         BR    R5                 RETURN                         S99528
         SPACE 1
CEC080   MVC   TOTOTBUF(ZERO),ZERO(R10)  MOVE MESSAGE TO         S99528
*                                          OUTPUT BUFFER         S99528
MSGMOVE1 MVC   TOTOTBUF+D8(ZERO),D3(R10) OUTPUT MSG TO OLTCB     S22024
CEC082   OC    D0(D0,R6),BLANK    XLATE RCVD MSG TO UPPER CASE   S99528
CEC084   MVC   D1(D0,R9),TOTINBUF MOVE RCVD MSG TO OLT BUFFER    S99528
CEC090   XC    D0(D0,R9),D0(R9)   CLEAR OLT INPUT BUFFER        SA41601
         SPACE
         SPACE
         SPACE
PATCH    DS    CL50               MAITENANCE AREA.               S99528
         SPACE 2
EIGHTY   DC    F'80'              VALUE OF EIGHTY                S99528
F0       DC    F'0'               VALUE OF ZERO                  S22024
F4       DC    F'4'               VALUE OF 4                    S22024
K62      DC    F'62'              VALUE OF SIXTY TWO             S99528
MSGMOVE  MVC   TOTOTBUF(ZERO),ZERO(R7)  MOVE MESSAGE TO          S99528
*                                         OUTPUT BUFFER          S99528
RESPONSE MVC   TOTINBUF(D1),D2(R3) MOVE RCVD MSG TO OLTCB BUFFER S99528
KLEAR    DC    H'0100'            SCB MASK SETTING               S99528
BLANK    DC    80XL1'40'          BLANK FIELD                    S99528
*                                                                S99528
LINESTRT EQU   X'40'               VERB CODE FOR START LINE      S99528
LINESTOP EQU   X'41'               VERB CODE FOR STOP  LINE      S99528
TERMSTRT EQU   X'45'               VERB CODE FOR START TERMINAL  S99528
TERMSTOP EQU   X'47'               VERB CODE FOR STOP  TERMINAL  S99528
*                                                                S99528
         SPACE 2
PSPREF   DSECT                                                   S22024
SECTID   DS    CL8                SECTION ID                     S22024
LEVEL    DS    CL2                LEVEL                          S22024
RTNDISP  DS    AL2                DISPLACEMENT TO RTN#           S22024
INST     DS    A                  POINTER TO FIRST EXEC INST.    S22024
MSGID    DS    CL2                OLT COMPONENT ID               S22024
         SPACE 2
SPREF    DSECT                                                   S22024
RTN#     DS    CL1                RTN NUMBER                     S22024
         DS    CL15               SECTION PREFACE FLAGS/DATA     S22024
#FLAG    DS    CL1                FLAGS                          S22024
#MSGIDRQ EQU   X'10'              MESSAGE ID PROVIDED            S05331
         EJECT
         TQCBD                                                   S99528
         EJECT
         TPRIOR                                                  S99528
         SPACE 4
         EJECT
         TLCBD
         EJECT
         TSCBD
         EJECT
         TPRFD
         EJECT
         EJECT                                                   S99528
         TAVTD                                                   S99528
         EJECT                                                   S99528
         IOBLOCKS                                                S99528
         EJECT                                                   S99528
UCB      DSECT                                                   S99528
         IEFUCBOB                                                S99528
         EJECT
RESPL    RESPL                                                 @Y17XAUU
         EJECT
         OLTCB                                                   S99528
TRMSLPL  DS    0F                 OP CONTROL PLIST             @Y17XAUU
         DC    X'0C'              FLAG START                   @Y17XAUU
         DC    AL3(0)             POINTER TO FIRST ELEMENT     @Y17XAUU
         DC    X'08'              FLAG LAST                    @Y17XAUU
         DC    AL3(0)             POINTER TO LAST ELEMENT      @Y17XAUU
TPOSTPAR DS    2F                 TPOST PLIST FOR BFR RETURN   @Y17XAUU
STRTSAVE DS    7F                 SAVE AREA FOR START LINE     @Y17XAUU
DPARM    DS    F                  WORK AREA                    @Y17XAUU
WORK2    DS    F                  WORK AREA                    @Y17XAUU
WORK4    DS    4F                 REG SAVE AREA                @Y17XAUU
CECSAVE  DS    F                  SAVE AREA                    @Y17XAUU
MSGIDSV  DS    H                  OLT COMPONENT ID SAVE AREA   @Y17XAUU
PRIRTCD  DS    C                  RETURN CODE SAVE AREA        @Y17XAUU
SECRTCD  DS    C                  RETURN CODE SAVE AREA        @Y17XAUU
         END
