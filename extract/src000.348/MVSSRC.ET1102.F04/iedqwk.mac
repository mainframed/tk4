         TITLE 'MESSAGE MODULE 1 - IEDQWK'
IEDQWK   CSECT
*A000000-999999                                                @Y16X5U0
* CHANGE ACTIVITY AS FOLLOWS:                                    S22024
*A148000,671200,688000                                          SA41592
*D674800-675200                                                 SA41592
*A471800,952020                                                 SA41593
*C479000-480200,486200-487400,493400-494600,500600,927000       SA41593
*D682000-682800                                                 SA41593
*C666700,666742-666746                                          SA41609
*A665440-665460                                                 SA41613
*A000830,001280-001920,020600-020800,665210-665280               S22024
*C007600,376000                                                  S22024
*D424000                                                         S22024
*A671220-671240                                                 SA64778
*A512000,520000,207200,671500                                  @Y17XAUU
*C356000,358000,656000,664000                                  @Y17XAUU
*   TOTE ALIAS                                                 @OY13283
****************************************************************
*                                                              * S22024
* MODULE NAME = IEDQWK (TCAM,TOTE)                             @Y17XAUU
*                                                              * S22024
* DESCRIPTIVE NAME = MESSAGE MODULE 1                          * S22024
*                                                              * S22024
* COPYRIGHT = NONE                                             * S22024
*                                                              * S22024
* STATUS --                                                    *
*    VERSION 10.0                                              @Y17XAUU
*                                                              *
* FUNCTIONS --                                                 *
*    THE PURPOSE OF THIS MODULE IS TO PROVIDE BOTH INPUT AND   *
*    OUTPUT COMMUNICATION BETWEEN TOTE AND THE OPERATOR.       *
*                                                              *
*    IEDQWK PERFORMS A XCTL TO THE SUBMODULE IEDQWL, IEDQWL1,  * S22024
*    IEDQWL2, OR IEDQWL3 TO MOVE MESSAGES AND BYTES TO THE     *
*    OLTCB IF A MESSAGE ID IS GIVEN IN THE PARAMETER LIST      *
*    PASSED. IF THE ADDRESS OF THE OUTPUT BUFFER AND BYTE      *
*    COUNT IS GIVEN IN THE PARAMETER LIST, IEDQWK PERFORM      *
*    THE MOVE.                                                 *
*                                                              *
*    THE MODULE THEN PERFORM A LINK TO THE ACCESS MANAGER,     *
*    IEDQWO, TO PRINT THE MESSAGE. IF INPUT IS EXPECTED FROM   *
*    A REMOTE TERMINAL, A WAIT IS PERFORMED AND THE INPUT DATA *
*    IS MOVE FROM THE TCAM BUFFER INTO THE OLTCB.              *
*                                                              *
*    RETURN IS MADE TO THE CALLING MODULE VIA REGISTER 14.     *
*                                                              *
* ENTRY POINTS --                                              *
*    IEDQWK - CALLED BY ANY TOTE MODULE (EXCEPT TOTE SERVICE   *
*    MODULES) REQUIRING MESSAGE COMMUNICATION.                 *
*                                                              * S22024
*    IEDQWK1 - ENTRY POINT FROM ALL IEDQWL SUBMODULES          * S22024
*                                                              *
* INPUT                                                        *
*    REGISTERS 01,02,13,14,15 CONTAIN THE FOLLOWING VALUES:    *
*                                                              *
*    01 - MESSAGE PARAMETER LIST ADDRESS                       *
*    02 - OLTCB BASE POINTER                                   *
*    13 - CALLER'S SAVE AREA ADDRESS                           *
*    14 - RETURN ADDRESS                                       *
*    15 - IEDQWK ENTRY POINT ADDRESS                           *
*                                                              *
* OUTPUT --                                                    *
*    THE MESSAGE INPUT BUFFER IN THE OLTCB CONTAINS A REPLY    *
*    IF ONE IS EXPECTED.                                       *
*                                                              *
* EXTERNAL REFERENCES --                                       *
*    IEDQWL  - TOTE MESSAGE SUB-MODULE 1                       *
*    IEDQWL1 - TOTE MESSAGE SUB-MODULE 2                       *
*    IEDQWL2 - TOTE MESSAGE SUB-MODULE 3                       *
*    IEDQWL3 - TOTE MESSAGE SUB-MODULE 4                       *
*    IEDQWO  - TOTE ACCESS MODULE                              *
*                                                              *
* EXITS,NORMAL --                                              *
*    CONTROL IS RETURNED TO THE CALLING MODULE.                *
*                                                              *
* EXITS,ERROR --                                               *
*    NONE                                                      *
*                                                              *
* TABLES/WORK AREAS --                                         *
*    OLTCB, TCAM BUFFERS, MESSAGE PARAMETER LIST, LCB, SCB     *
*    AVT, QCB                                                  *
*                                                              *
* ATTRIBUTES --                                                *
*    ENABLED, PROBLEM PROGRAM MODE, TRANSIENT                  *
*                                                              *
* NOTES --                                                     *
*    NONE                                                      *
*                                                              *
****************************************************************
         EJECT
*                                                              *
*        E Q U A T E S                                         *
*                                                              *
R0       EQU   0                  REG 0
R1       EQU   1                  REG 1
R2       EQU   2                  REG 2
R3       EQU   3                  REG 3
R4       EQU   4                  REG 4
R5       EQU   5                  REG 5
R6       EQU   6                  REG 6
R7       EQU   7                  REG 7
R8       EQU   8                  REG 8
R9       EQU   9                  REG 9
R10      EQU   10                 REG 10
R11      EQU   11                 REG 11
R12      EQU   12                 REG 12
R13      EQU   13                 REG 13
R14      EQU   14                 REG 14
R15      EQU   15                 REG 15
         SPACE
D0       EQU   0                  DISP OF 0
D1       EQU   1                  DISP OF 1
D2       EQU   2                  DISP OF 2
D3       EQU   3                  DISP OF 3
D4       EQU   4                  DISP OF 4
D5       EQU   5                  DISP OF 5
D6       EQU   6                  DISP OF 6
D7       EQU   7                  DISP OF 7
D8       EQU   8                  DISP OF 8
D9       EQU   9                  DISP OF 9
D10      EQU   10                 DISP OF 10
D11      EQU   11                 DISP OF 11
D12      EQU   12                 DISP OF 12
D13      EQU   13                 DISP OF 13
D14      EQU   14                 DISP OF 14
D15      EQU   15                 DISP OF 15
D16      EQU   16                 DISP OF 16
D17      EQU   17                 DISP OF 17
D18      EQU   18                 DISP OF 18
D19      EQU   19                 DISP OF 19
D20      EQU   20                 DISP OF 20
D24      EQU   24                 DISP OF 24
D28      EQU   28                 DISP OF 28
D32      EQU   32                 DISP OF 32
D36      EQU   36                 DISP OF 36
D40      EQU   40                 DISP OF 40
D80      EQU   80                 DISP OF 80
TPOSTA   EQU   X'0C'              FIRST ELEMENT                 SA41592
TPOSTE   EQU   X'80'              END OF REQ. ELEMENT           SA41592
TRMTERM  EQU   X'10'              SEC. CONTROL TERMINAL         SA41592
         SPACE
*
MULTDROP EQU   X'10'              MULTIDROP LINE FLAG
ANR      EQU   X'1E'              ANR LOCAL
XF0      EQU   X'F0'              LCB FLAG MASK
         SPACE 2
XFF      EQU   X'FF'              MASK FIELD
CANCEL   EQU   X'14'              CANCEL MSG ID
MSPTR    EQU   X'80'              MSG ADDR PROVIDED
L49      EQU   49                 COMPARE VALUE
L79      EQU   79                 COMPARE VALUE
L99      EQU   99                 COMPARE VALUE
L150     EQU   150                COMPARE VALUE
D79      EQU   79                 DISP OF 79
BLANK    EQU   X'40'              BLANK CHAR
KA       EQU   X'C1'              CHAR A
CVT      EQU   16                 CVT ADDR LOC
RQPOST   EQU   X'0C'              POST FLAG
RQEND    EQU   X'80'              LAST ELEMENT FLAG
CARR     EQU   X'10'              DPRINT CARR CONTROL
MULBUF   EQU   X'14'              MULTIPLE BUFFER PREFIX         S22024
LUTYPE   EQU   X'1C'              LU INPUT                     @Y17XAUU
         EJECT
         USING IEDQWK,R15
         USING TOTOLTCB,R2
         SPACE
IEDQWK   IEDHJN STARK,HJN
         SPACE
         SAVE  (14,12)            SAVE CALLER'S REG
         SPACE
         DROP  R15
         LR    R4,R15             RESET BASE REGISTER
         USING IEDQWK,R4
         LA    R15,TOTSAVE1       GET ADDR OF MY SAVE AREA
         ST    R13,D4(R15)        SAVE CALLER'S SAVE AREA ADDR
         ST    R15,D8(R13)        SAVE MY SAVE AREA ADDR
         LR    R13,R15            SAVE IT AGAIN
         SPACE
         LR    R10,R1             GET PARAMETER LIST ADDRESS
         USING PLIST,R10
         ST    R0,TOTSMGRT        SAVE RETURN PTR               SA41593
         SPACE
         SPACE
         NI    TOTFLG07,XFF-TOTMSCEC-TOTMSREP-TOTMSPRT-TOTMSSRC
*                                 CLEAR FUNCTION FLAGS         @Y17XAUU
         LH    R5,D2(R1)          GET FLAG BITS
         SRL   R5,D12             ADJUST FOR OLTCB FLAG BYTE
         STC   R5,FLAG            PUT IN WORK AREA
         OC    TOTFLG07(D1),FLAG  SET FTN FLAG
*
         TM    TOTFLG06,TOTNPERR  HAS A PERMANENT RRROR OCCURED?
         BO    EMSG00               NO                           S22024
         NI    TOTFLG07,XFF-TOTMSPRT CLR PRINT FLAG
         CLI   MSGID+D1,CANCEL    CANCEL MSG REQUEST
         BE    EMSG00             YES - SEND IT
         B     EMSG02             NO - RETURN TO CALLER
EMSG00   EQU   *
         XC    TOTOTBUF(D80),TOTOTBUF CLEAR OUTPUT BFR
         TM    MSGID,MSPTR        MSG ADDR PROVIDED IN P-LIST
         BZ    EMSG00A            NO - CALL SUBMODULE
         SR    R6,R6              CLEAR REG 6
         IC    R6,MSGCT1          GET MESSAGE LENGTH
         MVC   OUTMSG(D4),MSGADR-D1 PUT MSG ADDR ON F-WD BNDY
         L     R7,OUTMSG          GET MSG ADDR
         STC   R6,TOTOTCNT        PUT BYTE COUNT INTO OLTCB
         BCTR  R6,R0              DECREMENT COUNT FOR EX
         EX    R6,MSGMOVE         MOVE MESSAGE TO OLTCB
         B     EMSG00M            EXIT TO SEND MESSAGE
         SPACE 2
EMSG00A  EQU   *
         L     R13,D4(R13)        RESTORE SAVE AREA POINTER     SA41593
         LM    R14,R1,D12(R13)    RESTORE REG 14-1              SA41593
         SR    R5,R5              CLEAR REG 5
         SR    R6,R6              CLEAR REG 6
         LH    R5,MSGID           GET MESSAGE ID
         LA    R6,L49             SET COMPARAND
         CLR   R5,R6              GO IEDQWL ?
         BH    EMSG00C            NO - CONTINUE CHECK
         SPACE
*
*        LINK TO IEDQWL SUBMODULE TO GET MESSAGE
*
EMSG00B  EQU   *
         XCTL  (2,12),EP=IEDQWL   GET MESSAGE                   SA41593
         SPACE 2
EMSG00C  EQU   *
         LA    R6,L79             SET COMPARAND
         CLR   R5,R6              GO TO IEDQWL1 ?
         BH    EMSG00E            NO - CONTINUE CHECK
         SPACE
*
*        LINK TO IEDQWL1 SUBMODULE TO GET MESSAGE
*
         XCTL  (2,12),EP=IEDQWL1  GET MESSAGE                   SA41593
         SPACE 2
EMSG00E  EQU   *
         LA    R6,L99             SET COMPARAND
         CLR   R5,R6              GO TO IEDQWL2 ?
         BH    EMSG00H            NO - CONTINUE CHECK
         SPACE
*
*        LINK TO IEDQWL2 SUBMODULE TO GET MESSAGE
*
         XCTL  (2,12),EP=IEDQWL2  GET MESSAGE                   SA41593
         SPACE 2
EMSG00H  EQU   *
         LA    R6,L150            SET COMPARAND
         CLR   R5,R6              GO TO IEDQWL3 ?
         BH    EMSG032            NO - MSG NOT FOUND - RETURN
         SPACE
*
*        LINK TO IEDQWL3 SUBMODULE TO GET MESSAGE
*
         XCTL  (2,12),EP=IEDQWL3  GET MESSAGE                   SA41593
         SPACE 2
IEDQWK1  EQU   *                  RETURN POINT FROM L'S MOD     SA41593
         ENTRY IEDQWK1                                          SA41593
         DROP  R4                                               SA41593
         USING IEDQWK1,R15                                      SA41593
         SAVE  (14,12),,IEDQWK1   SAVE REG                      SA41593
         L     R4,ENTRYPT         IEDQWK ENTRY POINT            SA41593
         DROP  R15                                              SA41593
         USING IEDQWK,R4                                        SA41593
         LA    R15,TOTSAVE1       GET ADDR OF MY SAVE AREA      SA41593
         ST    R13,D4(R15)        SAVE CALLER'S SAVE AREA ADDR  SA41593
         ST    R15,D8(R13)        SAVE MY SAVE AREA ADDR        SA41593
         LR    R13,R15            SAVE IT AGAIN                 SA41593
         SPACE 2
EMSG00M  EQU   *
*
         NI    TOTFLG05,XFF-TOTCECOM-TOTREPLY CLR ACCESS FLAGS
         NI    TOTFLG05,XFF-TOTPRINT-TOTSRCE  CLR ACCESS FLAGS @Y17XAUU
*
         TM    TOTFLG07,TOTMSSRC  SOURCE REQUEST               @Y17XAUU
         BZ    EMSG00P            NO - CONTINUE                @Y17XAUU
         NI    TOTFLG07,XFF-TOTMSSRC RESET SOURCE FLAG         @Y17XAUU
         OI    TOTFLG05,TOTSRCE   SEND MSG TO SOURCE FLAG      @Y17XAUU
         B     EMSG01             SEND MESSAGE                 @Y17XAUU
EMSG00P  EQU   *                                               @Y17XAUU
         TM    TOTFLG09,TOTPRECT  PRINTER EQU CONT TERMINAL
         BZ    EMSG001              NO
         TM    TOTFLG07,TOTMSPRT+TOTMSCEC BOTH PRINT AND CECOM
*                                   YES: IS REQUEST FOR BOTH PRINT
*                                     AND CECOM?
         BNO   EMSG001                NO
         NI    TOTFLG07,XFF-TOTMSPRT YES - CLR PRINT FLAG
EMSG001  EQU   *
         B     EMSG022            CONTINUE PROCESSING
EMSG008  EQU   *
         TM    TOTFLG07,TOTMSCEC+TOTMSREP CECOM/REPLY
         BZ    EMSG02                   NO
         NI    TOTFLG05,XFF-TOTEXIOF-TOTPRINT CLR ACCESS FLAGS
         NI    TOTFLG05,XFF-TOTCECOM-TOTREPLY CLR ACCESS FLAGS
         OI    TOTFLG05,TOTCECOM  SET CECOM FLAG
         TM    TOTFLG07,TOTMSREP  REPLY EXPECTED
         BZ    EMSG01               NO
         OI    TOTFLG05,TOTREPLY  SET REPLY FLAG
         TM    TOTFLG09,TOTCTCON  CONTROL TERMINAL = SYSCON ?
         BZ    EMSG01             NO
         MVI   TOTINBUF,BLANK     BLANK
         MVC   TOTINBUF+D1(D79),TOTINBUF INPUT BUFFER
EMSG01   EQU   *
         L     R15,TOTRESPL       GET RESPL ADDRESS            @Y17XAUU
         USING RESPL,R15          SET RESPL ADDRESSABILITY     @Y17XAUU
         L     R15,RESPLWO        GET ADDRESS OF ACCESS RTN    @Y17XAUU
         BALR  R14,R15            CALL IEDQWO TO WRITE MSG     @Y17XAUU
         DROP  R15                                             @Y17XAUU
         TM    TOTFLG09,TOTCTCON  CONTROL TERMINAL = SYSCON ?
         BO    EMSG032            YES - BRANCH
         TM    TOTFLG05,TOTREPLY  REPLY EXPECTED ?
         BZ    EMSG032            NO - BR
         XC    TOTOTECB(D4),TOTOTECB CLEAR TECB
         MVI   TOTINBUF,BLANK     BLANK
         MVC   TOTINBUF+D1(D79),TOTINBUF INPUT BUFFER
         OI    TOTFLG03,TOTREPEX       TURN ON REPLY EXPECTED   SA41613
*                                        BIT                    SA41613
*
*        WAIT FOR REPLY TO COME IN
*
         WAIT  ECB=TOTOTECB
         SPACE
*                                                              *
*        THE FOLLOWING CODE RETRIEVES A RESPONSE FROM A TCAM   *
*        BUFFER. THE FIRST PAIR OF CHARACTERS IN THE MESSAGE   *
*        IS THE CONTROL TERMINAL ID.                           *
*                                                              *
         L     R6,TOTOLTMQ        GET ADDR OF TCAM BFR UNIT
         USING IEDQPRF,R6
         LA    R3,PRFSUNIT        GET START OF FIRST BFR UNIT
         AH    R3,PRFSCAN         POINT TO MSG ID
         LA    R7,D2              GET SIZE OF ID FIELD          SA41609
         LH    R9,PRFSIZE         GET TOTAL NUMBER OF DATA BYTES
*                                   FROM THE BUFFER
         SH    R9,PRFSCAN         SUBTRACT RESERVE CHARACTERS TO
*                                   GET TOTAL NUMBER OF BYTES IN
*                                   THE REPLY
         SR    R9,R7              DECREMENT DATA COUNT BY SIZE  SA41609
*                                   OF ID FIELD(2)              SA41609
         SR    R7,R7              CLEAR INMSG COUNTER           SA41609
         LR    R8,R3              GET MSG POINTER
MSGLOOP  EQU   *
         CLI   D2(R8),BLANK       END OF MESSAGE?                S99528
         BL    EXMOVE             YES
         LA    R7,D1(R7)          INCREMENT CHAR COUNTER
         LA    R8,D1(R8)          POINT TO NEXT CHAR
         BCT   R9,MSGLOOP         TEST NEXT CHARACTER
EXMOVE   EQU   *
         LTR   R7,R7              ANY RESPONSE?
         BZ    EMSG11             NO
         CL    R7,EIGHTY          MAXIMUM CHAR. EXCEEDED?
         BH    MAXMSG             YES  - TRUNSCATE IT
         BCTR  R7,R0              DECREMENT FOR EX
         B     MOVE               MOVE REPLY TO OLTCB
MAXMSG   EQU   *
         LA    R7,D79             SET MAX COUNT MINUS ONE
MOVE     EQU   *
         EX    R7,RESPONSE        MOVE RESPONSE TO OLTCB IN BUF
EMSG11   EQU   *
         SPACE 2
*
*        POST BUFFER LCB TO TCAM (BD)
*
         L     R12,CVT            GET CVT ADDR
         L     R12,AVTCVTPT(R12)  GET TCAM DISPATCHER ADDR
         L     R12,D0(R12)        GET AVT ADDR
         USING IEDQAVTD,R12
         TM    AVTBIT1,AVTCLOSN   TCAM CLOSEDOWN IN PROGRESS?   SA64778
         BO    EMSG032            YES - RETURN                  SA64778
         LA    R5,AVTBFRTB        BFR RETURN QCB ADDR           SA41592
         ST    R5,WORK2           SAVE IT                       SA41592
         CLI   PRFKEY,TRMTERM     REQUEST FROM A SECONDARY      SA41592
*                                 OPERATOR CONTROL TERMINAL     SA41592
         BE    EMSGSCT            YES - BRANCH                  SA41592
         CLI   PRFKEY,LUTYPE      LU RESPONSE                  @Y17XAUU
         BE    EMSGSCT            YES - POST BUFFER ONLY       @Y17XAUU
         CLI   PRFKEY,MULBUF      MULTIPLE BUFFERS               S22024
         BE    EMSGSCT            YES - POST BUFFER ONLY         S22024
         SPACE
         L     R7,PRFLCB-D1       GET LCB ADDR
         USING IEDQLCB,R7
         MVC   LCBLSPCI(D3),TOTOLTMQ+1 PUT IN BUFFER ADDR
         MVI   LCBPRI,PRILCB      SET PRIORITY
         L     R8,LCBSCBA-D1      GET SCB ADDR
         LA    R8,D0(R8)          CLR HI ORDER BYTE
         USING IEDQSCB,R8
         MVC   SCBDESTQ(D3),WORK2+D1 PUT IT IN SCB
         L     R5,SCBMACR-D1      GET ADDR OF BFR HDR ENTRY
         LA    R5,D0(R5)          CLEAR HIGH ORDER BYTE
         LA    R3,AVTINOUT        INEND/OUTEND PLIST ADDR
         ST    R3,DPARM           SAVE IT IN WORK AREA
         MVC   SCBMACR(D3),DPARM+D1  PUT IN SCB
         DROP  R8
         L     R8,AVTMSGS-D1      VCON TABLE
         LA    R8,D0(R8)          CLR HI ORDER BYTE
         L     R8,D0(R8)          BD QCB ADDRESS
         ST    R8,WORK2           SAVE QCB ADDR FOR BD
         L     R8,LCBQCBA-D1      GET QCB ADDR
         CLI   LCBRSKEY,ANR       TERMINAL = ANR
         BNE   EMSG15             NO
         MVI   LCBTSTSW,XF0       SET LCB FLAG FOR ANR LOCAL
EMSG15   EQU   *
         MVC   LCBQCBA(D3),WORK2+D1 PUT QCB ADDR IN LCB
*
         ST    R7,TPOSTPAR        PUT LCB ADDR IN PLIST
         ST    R7,TPOSTPAR+D4     PUT LCB ADDR IN PLIST
         MVI   TPOSTPAR,RQPOST    SET REQUEST FLAGS
         MVI   TPOSTPAR+D4,RQEND  SHOW END OF ELEMENT
         LA    R1,TPOSTPAR        GET ADDR OF TPOST PLIST
*
         AQCTL                    TPOST BUFFER TO BD
*
         SPACE 2
         B     EMSG032            EXIT
         EJECT
*************************************************************** SA41592
*        REQUEST IS FROM SECONDARY OPERATOR CONTROL TERMINAL  * SA41592
*************************************************************** SA41592
EMSGSCT  EQU   *                                                SA41592
         ST    R5,PRFKEY          QCB ADDR IN BUFFER            SA41592
         MVI   PRFPRI,PRIBFRTB    PRIORITY                      SA41592
         L     R5,TOTOLTMQ        BUFFER ADDR                   SA41592
         ST    R5,TPOSTPAR+D4     SET UP POST                   SA41592
         MVC   TPOSTPAR+D1(D3),TPOSTPAR+D5 ELEMENT              SA41592
         MVI   TPOSTPAR,TPOSTA    SHOW FIRST ELEMENT            SA41592
         MVI   TPOSTPAR+D4,TPOSTE SHOW LAST BUFFER              SA41592
         LA    R1,TPOSTPAR        POINT TO PLIST                SA41592
*                                                               SA41592
         AQCTL                    POST BUFFER                   SA41592
*                                                               SA41592
         DROP  R7                                               SA41592
         DROP  R6                                               SA41592
         DROP  R12                                              SA41592
EMSG02   EQU   *
         B     EMSG032            EXIT
EMSG022  EQU   *
         MVC   TRMBUF(D80),TOTOTBUF SAVE OUTPUT DATA             S22024
         MVC   TRMCNT(D1),TOTOTCNT SAVE DATA COUNT               S22024
         TM    TOTFLG07,TOTMSPRT  DPRINT
         BZ    EMSG03               NO
         NI    TOTFLG05,XFF-TOTCECOM-TOTREPLY CLR CECOM/REPLY
         OI    TOTFLG05,TOTPRINT  SET DPRINT
         MVZ   TOTFLG08,CARR      MOVE IN CARRIAGE CONTROL
*
         L     R15,TOTRESPL       GET RESPL ADDRESS            @Y17XAUU
         USING RESPL,R15          SET RESPL ADDRESSABILITY     @Y17XAUU
         L     R15,RESPLWO        GET ACCESS ROUTINE ADDRESS   @Y17XAUU
         BALR  R14,R15            CALL IEDQWO TO WRITE MSG     @Y17XAUU
         DROP  R15                                             @Y17XAUU
*
EMSG03   EQU   *
         MVC   TOTOTBUF(D80),TRMBUF RESTORE OUTPUT DATA          S22024
         MVC   TOTOTCNT(D1),TRMCNT RESTORE DATA COUNT            S22024
         B     EMSG008            CHECK FOR CECOM
EMSG032  EQU   *
         OC    TOTINBUF(D80),BLANKS CONVERT TO UPPER CASE
         L     R13,D4(R13)        CALLER'S SAVE AREA ADDR
         L     R14,D12(R13)       CALLER'S RETURN ADDR
         L     R0,TOTSMGRT        RESTORE REG 0                 SA41593
         LM    R1,R12,D24(R13)    CALLER'S REG                  SA41593
         BR    R14                RETURN
         EJECT
BLANKS   DC    80XL1'40'          OR FIELD
FLAG     DC    X'00'              WORK AREA
PATCH    DS    XL80               PATCH AREA
TRMBUF   DS    XL80               OUTPUT MSG SAVE AREA           S22024
TRMCNT   DS    XL1                OUTPUT COUNT SAVE AREA         S22024
         SPACE 2
TPOSTPAR DC    2F'0'              TPOST PLIST FOR BFR RETURN
ENTRYPT  DC    A(IEDQWK)          ENTRY POINT ADDR FOR IEDQWK   SA41593
         SPACE
WORK2    DC    F'0'               WORK AREA
DPARM    DS    F                  WORK AREA
EIGHTY   DC    F'80'              VALUE OF EIGHTY
OUTMSG   DC    F'0'               WORK AREA
X0100    DC    X'0100'            MASK SETTING
MSGMOVE  MVC   TOTOTBUF(D0),D0(R7) MOVE MESSAGE TO OUTPUT BUFFER
RESPONSE MVC   TOTINBUF(D0),D2(R3) MOVE REPLY TO INPUT BUFFER
         SPACE 4
PLIST    DSECT
MSGID    DS    XL2                MSG ID
MSGFLG   DS    XL1                MSG FLAG
MSGCT    DS    XL1                MSG IN COUNT
MSGCT1   DS    XL1                MSG OUT COUNT
MSGADR   DS    XL3                MSG OUTPUT ADDR
         EJECT
         TQCBD
         EJECT
         TPRIOR
         EJECT
         TLCBD
         EJECT
         TSCBD
         EJECT
         TAVTD
         EJECT
         EJECT
         EJECT
         OLTCB
         EJECT
         TPRFD
         EJECT
RESPL    RESPL                                                 @Y17XAUU
         SPACE 2
         END
