         TITLE 'IEDQWID - CONFIGURATOR DELETE SCHEDULER'
IEDQWID  CSECT                                                   S22024
*CHANGE ACTIVITY AS FOLLOWS:
*D774500                                                         Y06330
*A775700                                                         Y06330
*D694000                                                        SA64780
*A455000                                                        SA64780
*A404000,626000,628000,654000,656000                           @YA12402
**************************************************************** S22024
*                                                              * S22024
* TITLE:                                                       * S22024
*                                                              * S22024
*    'IEDQWID - TOTE CONFIGURATOR DELETE/CHANGE SCHEDULER'     * S22024
*                                                              * S22024
* MODULE NAME:                                                 * S22024
*                                                              * S22024
*    IEDQWID                                                   * S22024
*                                                              * S22024
* DESCRIPTIVE NAME:                                            * S22024
*                                                              * S22024
*    TOTE CONFIGURATOR DELETE/CHANGE SCHEDULER                 * S22024
*                                                              * S22024
* COPYRIGHT:                                                   * S22024
*                                                              * S22024
*    'NONE'                                                    * S22024
*                                                              * S99528
* STATUS:                                                      * S99528
*                                                              * S99528
*    CHANGE LEVEL 5                                             Y06330*
*                                                              * S99528
* FUNCTION:                                                    * S99528
*    THIS MODULE HANDLES THE DELETION OF CONFIGURATION DATA    * S99528
*    FROM THE LOCAL AND REMOTE CONFIGURATION DATA SETS.  IT    * S99528
*    ALSO HANDLES CHANGE REQUESTS.                             * S99528
*                                                              * S99528
*    FOR 'DELETE' REQUESTS, THIS MODULE PROMPTS FOR THE LINE   * S99528
*    ADDRESS OR SYMBOLIC TERMINAL NAME.  FOR A LINE ADDRESS    * S99528
*    THIS MODULE DETERMINES IF THERE IS A VALID UCB.  VALID    * S99528
*    UCB'S ARE CHECKED FOR A DEVICE CLASS OF 10 OR 40.  FOR    * S99528
*    AN INVALID LINE ADDRESS AN ERROR MESSAGE IS SENT TO THE   * S99528
*    CONTROL TERMINAL.  FOR TERMINALS, IEDQWI8 IS LOADED TO    * S99528
*    DETERMINE IF THE SYMBOLIC NAME IS A VALID TCAM TERMINAL.  * S99528
*                                                              * S99528
*    FOR VALID ENTRIES, THE CDS IS DELETED FROM THE PDS        * S99528
*    DIRECTORY.  IF A MEMBER TO DELETE IS NOT FOUND IN THE     * S99528
*    DIRECTORY, A MESSAGE IS SENT TO THE CONTROL TERMINAL.     * S99528
*                                                              * S99528
*    FOR 'CHANGE' REQUESTS, THE SAME VALIDITY CHECKING         * S99528
*    DESCRIBED ABOVE IS PERFORMED.  IF THE ENTRY IS VALID,     * S99528
*    THIS MODULE ATTEMPTS TO FIND THE CDS RECORD.  IF A CDS    * S99528
*    RECORD DOES NOT EXIST, A MESSAGE IS SENT TO THE CONTROL   * S99528
*    TERMINAL.  OTHERWISE THE CDS RECORD IS PASSED TO MODULE   * S99528
*    IEDQWIA WHICH DETERMINES THE CHARACTERISTICS AND PASSES   * S99528
*    THE INFORMATION TO A UNIT CONFIGURATOR FOR FURTHER        * S99528
*    UPDATING.                                                 * S99528
*                                                              * S22024
* ENTRY POINTS:                                                * S99528
*                                                              * S99528
*         IEDQWID - RECEIVES CONTROL FROM IEDQWI VIA A 'XCTL'  * S99528
*                   WHEN AN 'DELETE' FUNCTION IS REQUESTED;    * S99528
*                                                              * S99528
*                 - RECEIVES CONTROL FROM IEDQWID OR IEDQWIA   * S99528
*                   VIA A 'XCTL' WHEN A 'CHANGE' FUNCTION IS   * S99528
*                   REQUESTED.                                 * S99528
*                                                              * S99528
* INPUT:                                                       * S99528
*                                                              * S99528
*    THE FOLLOWING REGISTERS ARE USED AS INPUT:                * S99528
*                                                              * S99528
*    02 - OLTCB POINTER;                                       * S99528
*    13 - SAVE AREA ADDRESS;                                   * S99528
*    15 - ENTRY POINT ADDRESS/RETURN CODE.                     * S99528
*                                                              * S99528
*    THE FOLLOWING OLTCB FIELD ARE USED AS INPUT:              * S99528
*                                                              * S99528
*    CDSWORK - 256 BYTE AREA TO CREATE CNFG RECORDS FOR ADD;   * S99528
*            - 256 BYTE AREA CONTAINING CDS RECORD FOR CHANGE; * S99528
*    CDSINPUT-  80 BYTE AREA TO PASS INFO BETWEEN MODULES;     * S99528
*    CDSBLDL -  40 BYTE AREA USED AS BLDL LIST FOR PDS;        * S99528
*    CDSCESD -  56 BYTE AREA CONTAINING CDS CESD RECORD;       * S99528
*    CDSCNTL -  20 BYTE AREA CONTAINING CDS CNTL RECORD;       * S99528
*    CDSFNFLG- FUNCTION FLAG: X'04' DELETE;                    * S99528
*                             X'16' CHANGE TERMINAL;           * S99528
*                             X'26' CHANGE LINE;               * S99528
*    CDSRETFL- RETURN CODE: X'00' VALID                        * S99528
*                           X'01' INVALID                      * S99528
*    TOTINBUF- 80 BYTE AREA FOR INPUT MESSAGES;                * S99528
*    TOTOTBUF- 80 BYTE AREA FOR OUTPUT MESSAGES;               * S99528
*    TOTSAVES- SAVE AREA FOR USE BY THIS MODULE.               * S99528
*                                                              * S99528
* OUTPUT:                                                      * S99528
*                                                              * S99528
*    THE FOLLOWING REGISTERS ARE USED FOR OUTPUT:              * S99528
*                                                              * S99528
*    02 - OLTCB POINTER;                                       * S99528
*    05 - UCB ADDRESS;                                         * S99528
*    13 - SAVE AREA ADDRESS;                                   * S99528
*    15 - ENTRY POINT ADDRESS.                                 * S99528
*                                                              * S99528
*    THE FOLLOWING OLTCB FIELDS ARE USED FOR OUTPUT:           * S99528
*                                                              * S99528
*    CDSWORK - 256 BYTE AREA TO CREATE CDS RECORD FOR ADD;     * S99528
*            - 256 BYTE AREA CONTAINING CDS RECORD FOR CHANGE; * S99528
*    CDSINPUT-  80 BYTE AREA CONTAINING LINE/TERMINAL DATA;    * S99528
*    CDSRETFL- RETURN CODE: X'00' VALID                        * S99528
*                           X'01' INVALID                      * S99528
* EXTERNAL ROUTINES:                                           * S99528
*                                                              * S99528
*         IEDQWIA - ADD SCHEDULER                              * S99528
*         IEDQWI5U- TCU/ADAPTER TYPE MODULE                    * S99528
*                                                              * S99528
*         IEDQWI8 - TERMINAL VERIFICATION MODULE               * S99528
*                                                              * S99528
*         IEDQWK  - MESSAGE MODULE                             * S99528
*                                                              * S99528
* EXITS, NORMAL:                                               * S99528
*         IEDQWI1 - XCTL AFTER 'EXIT' WHEN DELETE COMPLETE     * S99528
*         IEDQWIA - XCTL AFTER 'GETCHNG' FOR MORE CHANGES      * S99528
*         IEDQWI5U- BALR AFTER 'ADAPTYPE' FOR LINE ADD         * S99528
*                                                              * S99528
*         IEDQWI8 - BALR AFTER 'VALIDTER' FOR TERMINAL ADD     * S99528
*                                                              * S99528
* EXITS, ERROR                                                 * S99528
*                                                              * S99528
*         IEDQWI1 - XCTL AFTER 'EXIT' FOR CONFG TERMINATION    * S99528
*         ERROR RECOVERY - RETURN AFTER 'BADCDS' FOR SYNDAD RET* S99528
*                                                              * S99528
* TABLES/WORK AREAS:                                           * S99528
*                                                              * S99528
*    CDSWORK - WORK AREA USED TO PASS INFO BETWEEN MODULES AND * S99528
*              CREATE CDS RECORDS.                             * S99528
*                                                              * S99528
* CHARACTER CODE DEPENDENCY:                                   * S99528
*                                                              * S99528
*    NONE.                                                     * S99528
*                                                              * S99528
* NOTES:                                                       * S99528
*                                                              * S99528
*    NONE                                                      * S99528
*                                                              * S99528
**************************************************************** S99528
         EJECT
*              EQUATES
*
PARMREG0 EQU   0                       PARAMETER REGISTER        S99528
PARMREG1 EQU   1                       PARAMETER REGISTER        S99528
CUUADR   EQU   2                       OFFSET TO UNIT ADDRESS    S99528
R2       EQU   2                       OLTCB POINTER             S99528
BASREG   EQU   8                       IEDQWID BASE REGISTER     S99528
SAVEAREA EQU   13                      POINTER TO SAVE AREA      S99528
NEXTLOC  EQU   1                       OFFSET TO NEXT BYTE       S99528
NO       EQU   X'D5'                   NO RESPONSE               S99528
YES      EQU   X'E8'                   YES RESPONSE              S99528
GOODOPEN EQU   X'10'                   TEST FOR GOODOPEN         S99528
UAPTR    EQU   5                       UNIT ADDR POINTER         S99528
OPENFLAG EQU   48                      OFFSET TO DCB OPEN FLAGS  S99528
CDS40DCB EQU   7                       LOCAL DATA SET DCB PTR    S99528
CDS42DCB EQU   7                       REMOTE DATA SET DCB PTR   S99528
INPUTLEN EQU   78                      LEN OF CONFIG DATA        S99528
CDSLEN   EQU   255                     LEN OF CDS RECORD         S99528
GRAPHCLS EQU   X'10'                   GRAPHICS DEVICE CLASS     S99528
TCUCLASS EQU   X'40'                   TCU CLASS                 S99528
CHANGE   EQU   X'06'                   CHANGE REQUEST            S99528
TERMFLAG EQU   X'16'                   CHANGE TERMINAL REQUEST   S99528
LINEFLAG EQU   X'26'                   CHANGE LINE     REQUEST   S99528
DEVPTMSK EQU   X'40'                   DEVICE PROTECT MASK       S99528
CDSDPMSK EQU   X'20'                   DEV PROTECT CHK           S99528
NUMENTRY EQU   X'F0'                   NUMERIC ENTRY (LINE)      S99528
UCBDEVCL EQU   18                      OFFSET TO DEVICE CLASS    S99528
LEFTCLR  EQU   X'0F'                   MASK TO CLEAR LEFT BITS   S99528
SAVEADDR EQU   15                      SAVE ADDR PTR             S99528
RETCODE  EQU   15                      RETURN CODE REGISTER      S99528
ENTRYPT  EQU   15                      ENTRY POINT ADDRESS       S99528
LEN      EQU   2                       LEN OF UNIT ADR FLD       S99528
UNITLEN  EQU   3                       LEN OF UNIT ADDRESS       S99528
CUUPTR   EQU   4                       PTR TO UNIT ADR IN UCB    S99528
PREVSAVE EQU   4                       OFFSET TO PREV SAVE       S99528
GETOUT   EQU   X'01'                   TERMINATE FLAG            S99528
ZERO     EQU   X'00'                   ZERO CONSTANT             S99528
NAMELEN  EQU   8                       LEN OF SYMBOLIC NAME      S99528
R13      EQU   13                      REGISTER 13               S99528
R14      EQU   14                      RETURN ADDR REGISTER      S99528
R15      EQU   15                      ENTRY POINT REGISTER      S99528
UCBREG   EQU   5                       UCB POINTER               S99528
WORKREG6 EQU   6                       WORK REGISTER             S99528
WORKREG8 EQU   8                       WORK REGISTER             S99528
RETURN   EQU   9                       RETURN ADDRESS            S99528
CVT      EQU   16                      PTR TO CVT                S99528
UCBPTR   EQU   X'28'                   OFFSET TO UCB             S99528
UCBDVCLS EQU   18                      OFFSET TO UCB CLASS       S99528
WORKREG5 EQU   5                       WORK REGISTER             S99528
CHAR4    EQU   4                       CHAR MOVE CONST OF 4      S99528
CONCSELF EQU   X'E0'                   CONCENTRATOR ENTRY NAMED  S22024
SPECIAL  EQU   X'F0'                EXTENSIOT CDS INDICATOR      S22024
L4       EQU   4                   LENGTH FOR MOVE               S22024
BLANK    EQU   X'40'                    BLANK CHARACTER        @YA12402
D1       EQU   1                        CONSTANT 1             @YA12402
D3       EQU   3                        CONSTANT 3             @YA12402
D4       EQU   4                        CONSTANT 4             @YA12402
D13      EQU   13                       CONSTANT 13            @YA12402
         EJECT
**************************************************************** S99528
*                                                              * S99528
*              INITIALIZATION                                  * S99528
*                                                              * S99528
**************************************************************** S99528
         SAVE  (14,12)                 SAVE REGISTERS            S99528
         LR    WORKREG8,R15            LOAD BASE                 S99528
         USING IEDQWID,WORKREG8        USE REG 8 FOR PRGM BASE   S99528
         USING TOTOLTCB,R2             USE REG 2 FOR OLTCB       S99528
IEDQWID  IEDHJN QWIDHJN,HJN            SET UP HJN                S99528
         LA    R15,SVAREA              LOAD ADDR OF OWN SAVE ARE S99528
         ST    R15,8(R13)              STORE IN CALLERS          S99528
         ST    R13,4(R15)              STORE CALLERS IN MINE     S99528
         LR    SAVEAREA,SAVEADDR       PLACE SAVE ADDR IN SAVE   S99528
         EJECT
**************************************************************** S99528
*                                                              * S99528
*        C H A N G E / D E L E T E   L O G I C                 * S99528
*                                                              * S99528
**************************************************************** S99528
PROCDEL  EQU   *                       CLEAR CDS AREA            S99528
         CLOSE (CDS40,,CDS42)          INSURE CLOSE             SA64780
         MVI   CDSREC,ZERO             ZERO OUT FIRST LOCATION   S99528
         MVC   CDSREC+NEXTLOC(CDSLEN),CDSREC  ZERO OUT REST      S99528
         MVC   CDSINPUT(INPUTLEN),CDSREC  ZERO OUT WORK AREA     S99528
*                                      ENTER LINE ADDR OR SYMBOL S99528
         IEDQMSG MSGID=103,FUNCT=REP,INBUF=CEMSG,INCNT=8,              X
               LINK=YES                                          S99528
         TM    CEMSG,NUMENTRY          WAS ENTRY A LINE ADDR?    S99528
         BO    DELLINE                 YES; GO TO DELETE LINE    S99528
         EJECT
**************************************************************** S99528
*                                                              * S99528
*        D E L E T E   T E R M I N A L   P R O C E D U R E     * S99528
*                                                              * S99528
**************************************************************** S99528
DELTERM  EQU   *                       DELETE TERMINAL LOGIC     S99528
         MVC   CDSSYMBL(NAMELEN),CEMSG MOVE SYMBOLIC NAME TO CDS S99528
         LOAD  EP=IEDQWI8              VERIFY THAT NAME VALID    S99528
         LR    ENTRYPT,PARMREG0        GET ENTRYPT TO IEDQWI8    S99528
         BALR  RETURN,ENTRYPT          GO SEARCH TCAM TERMINALS  S99528
         DELETE EP=IEDQWI8             DELETE IEDQWI8            S99528
         CLI   CDSRETFL,SPECIAL    IS THIS AN EXTENSION CDS?     S22024
         BE    GOODNAM1            YES SET UP FOR IT             S22024
         CLI   CDSRETFL,ZERO           WAS SYMBOLIC NAME FOUND   S99528
         BE    GOODNAME                YES                       S99528
         CLI   CDSRETFL,CONCSELF       WAS CONCENTRATOR CONFIG   S22024
*                                        REQUESTED?              S22024
         BE    CHKFNFLG                YES, INVALID REQUEST - NO S22024
*                                        CONFIG ALLOWED          S22024
         MVI   CDSRETFL,ZERO           RESET RETURN FLAG         S99528
BADNAME  EQU   *                       INVALID TERMINAL NAME     S99528
         IEDQMSG MSGID=132,FUNCT=CEC,RET=CHKFNFLG                S99528
CONTPROC EQU   *                       DO YOU WISH TO CONTINUE?  S99528
         CLOSE CDS40                   CLOSE LOCAL DATA SET      S99528
         CLOSE CDS42                   CLOSE REMOTE DATA SET     S99528
         IEDQMSG MSGID=125,FUNCT=REP                             S99528
         CLI   CEMSG,YES                                         S99528
         BE    CHKFNFLG                YES RESPONSE              S99528
         CLI   CEMSG,NO                                          S99528
         BE    CLEANUP                 DO NOT CONTINUE           S99528
CONTPRC1 EQU   *                       INVALID RESPONSE          S99528
         IEDQMSG MSGID=079,FUNCT=CEC,RET=CONTPROC                S22024
CHKFNFLG EQU   *                       CHECK TYPE OF FUNCTION    S99528
         MVI   CDSRETFL,ZERO           RESET RETURN FLAG         S99528
         TM    CDSFNFLG,CHANGE         CHECK FUNCTION            S99528
         BM    DELMORE                 DELETE FUNCTION BRANCH    S99528
CHNGMORE EQU   *                       ARE THERE MORE CHANGES?   S99528
         IEDQMSG MSGID=121,FUNCT=REP                             S99528
         CLI   CEMSG,NO                MORE CHANGES?             S99528
         BE    EXIT                    NO, GO TO EXIT            S99528
         CLI   CEMSG,YES               MORE CHANGES?             S99528
         BNE   CONTPRC1                INVALID RESPONSE          S99528
         B     PROCDEL                 YES, GO GET NAME OR LINE  S99528
DELMORE  EQU   *                       OTHER DELETES?            S99528
         IEDQMSG MSGID=122,FUNCT=REP                             S99528
         CLI   CEMSG,YES               WAS RESPONSE YES          S99528
         BE    PROCDEL                 YES                       S99528
         CLI   CEMSG,NO                WAS RESPONSE NO           S99528
         BE    EXIT                    BRANCH TO EXIT            S99528
         IEDQMSG MSGID=079,FUNCT=CEC,RET=DELMORE                 S22024
*                                                                S22024
GOODNAM1 EQU   *                   BRANCH POINT                  S22024
         MVI   EXTENIP,SPECIAL     INDICATE EXTENSION CONFIGU    S22024
         MVC   LINEADDR(NAMELEN),CDSSYMBL MOVE IN SYMBOLIC NAME  S22024
         B     CHNGLINE             CONTINUE CONFIG RUN          S22024
GOODNAME EQU   *                                                 S99528
         MVC   CDSIDSAV(LEN),CDSREC SAVE RSID BEFORE READ COVERS S22024
         TM    CDSFNFLG,CHANGE         CHECK FUNCTION            S99528
         BM    CHNGTERM                DELETE, GO CHK DEV PRTCT  S99528
         MVI   CDSFNFLG,TERMFLAG       SET CHNG TERM FLG         S99528
         B     CHNGTERM                BRANCH CHNG TERM PROC     S99528
OPENC42  OPEN  (CDS42,(OUTPUT))        OPEN REMOTE DATA SET      S99528
         LA    CDS42DCB,CDS42          WAS OPEN SUCCESSFUL       S99528
         TM    OPENFLAG(CDS42DCB),GOODOPEN  TEST OPEN FLAG       S99528
         BZ    OPENERR                 OPENERR                   S99528
         MVC   CDSMEMBR(NAMELEN),CDSSYMBL  MOVE NAME TO BLDL LIS S99528
         STOW  CDS42,CDSBLDL,D         DELETE TERMINAL           S99528
CHKRETRN EQU   *                                                 S22024
         CH    RETCODE,SUCCESS         WAS DELETE SUCCESSFUL?    S22024
         BNE   CHKDIRCT                NO                        S99528
         IEDQMSG MSGID=115,FUNCT=CEC,RET=DELMORE 'ENTRY DELETED' S99528
CHKDIRCT CH    RETCODE,NOENTRY         WAS ENTRY MISSING FROM DI S99528
         BE    ENTRYMIS                YES                       S99528
*                                      UNABLE TO DELETE          S99528
         IEDQMSG MSGID=134,FUNCT=CEC,RET=CHKFNFLG                S99528
ENTRYMIS EQU   *                       ENTRY NOT FOUND           S99528
         IEDQMSG MSGID=137,FUNCT=CEC,RET=CHKFNFLG                S99528
         EJECT
**************************************************************** S99528
*                                                              * S99528
*              D E L E T E   L I N E   P R O C E D U R E       * S99528
*                                                              * S99528
**************************************************************** S99528
DELLINE  EQU   *                                                 S99528
UCBLOOK  L     WORKREG5,CVT(PARMREG0)  PICK UP CVT POINTER       S99528
         L     WORKREG6,UCBPTR(WORKREG5)  POINTER TO 2 BYTE UCB  S99528
UCBLOOK1 LH    UCBREG,0(WORKREG6)      LOAD UCB ADDRESS          S99528
         LA    WORKREG6,2(WORKREG6)    POINT TO NEXT UCB ADDRESS S99528
         LTR   UCBREG,UCBREG                                     S99528
         BZ    UCBLOOK1                PADDING;IGNORE ENTRY      S99528
         BC    4,UCBEND                END OF LIST; NO MATCH     S99528
         CLI   CEMSG+D3,BLANK           IS 4TH CHAR BLANK?     @YA12402
         BE    DIGIT3                   YES                    @YA12402
         CLI   CEMSG+D4,BLANK           MORE THAN 4 CHAR?      @YA12402
         BNE   UCBEND                   YES INVALID LINE ADDR  @YA12402
         CLC   CEMSG+D1(D3),D13(UCBREG) ADJUST FOR ZERO        @YA12402
         B     UCBMATCH                 GO TAKE APPRO. BRANCH  @YA12402
DIGIT3   EQU   *                                               @YA12402
         CLC   CEMSG(3),13(UCBREG)     CUU MATCH?                S99528
UCBMATCH EQU   *                                               @YA12402
         BNE   UCBLOOK1                NO; GO CHECK NEXT UCB ENT S99528
         CLI   UCBDEVCL(UCBREG),TCUCLASS  CLASS EQUAL 40         S99528
         BE    GOODUCB                 VALID DEVICE CLASS        S99528
         CLI   UCBDEVCL(UCBREG),GRAPHCLS  CLASS EQUAL 10         S99528
         BE    GOODUCB                 VALID DEVICE CLASS        S99528
*                                      UNSUPPORTED DEVICE TYPE   S99528
         IEDQMSG MSGID=143,FUNCT=CEC,RET=CHKFNFLG                S99528
UCBEND   EQU   *                       INVALID LINE ADDR.        S99528
         IEDQMSG MSGID=141,FUNCT=CEC,RET=CHKFNFLG                S22024
GOODUCB  EQU   *                                                 S99528
         MVC   CDSUNIT+CUUADR(LEN),CUUPTR(UCBREG) MOVE VA TO CDS S99528
         NI    CDSUNIT+CUUADR,LEFTCLR  CLEAR FLAG BITS           S99528
         MVC   CDSCESNM(NAMELEN),MODPREFX  MOVE LINE ADDR PREFX  S99528
         CLI   CEMSG+D3,BLANK           IS 4TH CHAR BLANK?     @YA12402
         BE    MOVE3                    YES  3 CHAR CUU        @YA12402
         MVC   CDSCESNM+UAPTR(UNITLEN),CEMSG+D1  ADJUST PTR    @YA12402
         B     MOVEDON                  MOVE FINISHED          @YA12402
MOVE3    EQU   *                                               @YA12402
         MVC   CDSCESNM+UAPTR(UNITLEN),CEMSG  MOVE LINE ADDR.    S99528
MOVEDON  EQU   *                                               @YA12402
         MVC   CDSMEMBR(NAMELEN),CDSCESNM  MOVE NAME TO BLDLLST  S99528
         TM    CDSFNFLG,CHANGE         CHECK FUNCTION            S99528
         BM    CHNGLINE                GO CHANGE LINE            S99528
         MVI   CDSFNFLG,LINEFLAG       SET CHANGE LINE FLAG      S99528
         B     CHNGLINE                BRANCH CHANGE LINE PROC   S99528
GOODLINE EQU   *                                                 S99528
         OPEN  (CDS40,(OUTPUT))        OPEN LOCAL DATA DET       S99528
         LA    CDS40DCB,CDS40          WAS OPEN SUCCESSFUL       S99528
         TM    OPENFLAG(CDS40DCB),GOODOPEN                       S99528
         BZ    OPENERR                 OPEN ERROR                S99528
         STOW  CDS40,CDSBLDL,D         DELETE LINE FROM PDS      S99528
         B     CHKRETRN                GO CHK FUNCTION           S99528
         EJECT
**************************************************************** S99528
*                                                              * S99528
*          C H A N G E   T E R M I N A L   P R O C E D U R E   * S99528
*                                                              * S99528
**************************************************************** S99528
CHNGTERM EQU   *                                                 S99528
         OPEN  (CDS42,(INPUT))         OPEN REMOTE DATA SET      S99528
         LA    CDS42DCB,CDS42                                    S99528
         TM    OPENFLAG(CDS42DCB),GOODOPEN  OPEN SUCCESSFUL?     S99528
         BZ    OPENERR                 NO; OPEN ERROR            S99528
         MVC   MODNAME(NAMELEN),CDSSYMBL GET SYMBL TERM NAME     S99528
         BLDL  CDS42,BLDLLIST                                    S99528
         LTR   RETCODE,RETCODE         CHECK RETURN CODE         S99528
         BNZ   FINDERR                 ERROR                     S99528
GETCDS42 EQU   *                                                 S99528
         MVC   CDSPOINT(CHAR4),USERTTR GET CDS TTR-C             S99528
         POINT CDS42,CDSPOINT          POINT TO CDS DATA         S99528
         READ  CDSDECB,SF,CDS42,CDSREC,256,MF=E  GET CDS DATA    S99528
         CHECK CDSDECB                 WAIT FOR I/O COMPLETION   S99528
         CLOSE CDS42                   CLOSE REMOTE DATA SET     S99528
         MVC   CDSREC(LEN),CDSIDSAV PUT RSID BACK IN DATA        S22024
         TM    CDSSIGNL,CDSDPMSK       IS DEV PROTECT BIT ON     S99528
         BZ    NO42PTCT                DEV PROTECT BIT OFF       S99528
         TM    TOTFLG09,TOTCTCON       IS SYSCON THE CNTL TERM   S99528
         BZ    DEVPRTCT                SYSCON IS NOT CNTL TERM   S99528
         OI    CDSFNFLG,DEVPTMSK       SET DEV PROTECT BIT ON    S99528
NO42PTCT TM    CDSFNFLG,CHANGE         IS THIS A CHANGE REQUEST  S99528
         BO    GETCHNG                 YES, PASS CDS INFO ON     S99528
         B     OPENC42                 NO, GO DELETE CDS ENTRY   S99528
         EJECT
**************************************************************** S99528
*                                                              * S99528
*              C H A N G E   L I N E   P R O C E D U R E       * S99528
*                                                              * S99528
**************************************************************** S99528
CHNGLINE EQU   *                                                 S99528
         OPEN  (CDS40,(INPUT))         OPEN LOCAL DATA SET       S99528
         LA    CDS40DCB,CDS40          GET DCB ADDRESS           S99528
         TM    OPENFLAG(CDS40DCB),GOODOPEN  OPEN SUCCESSFUL?     S99528
         BZ    OPENERR                 NO; OPEN ERROR            S99528
         MVC   MODNAME(NAMELEN),LINEADDR    GET LINE CDS NAME    S99528
         BLDL  CDS40,BLDLLIST          FIND ENTRY                S99528
         LTR   RETCODE,RETCODE         CHECK RETURN CODE         S99528
         BNZ   FINDERR                 ERROR                     S99528
GETCDS40 EQU   *                                                 S99528
         MVC   CDSPOINT(CHAR4),USERTTR GET CDS TTR-C             S99528
         POINT CDS40,CDSPOINT          POINT TO CDS DATA         S99528
         READ  CDSDECB,SF,CDS40,CDSREC,256,MF=E  GET CDS DATA    S99528
         CHECK CDSDECB                 WAIT FOR I/O              S99528
         CLOSE CDS40                   CLOSE LOCAL DATA SET      S99528
         CLI   EXTENIP,SPECIAL     IS EXTENSION CONFIG IN PROCES S22024
         BE    NO40PTCT            YES CONTINUE                  S22024
         TM    CDSSIGNL,CDSDPMSK       IS DEV PROTECT BIT ON     S99528
         BZ    NO40PTCT                DEV PROTECT BIT OFF       S99528
         TM    TOTFLG09,TOTCTCON       IS SYSCON THE CNTL TERM   S99528
         BZ    DEVPRTCT                SYSCON IS NOT CNTL TERM   S99528
         OI    CDSFNFLG,DEVPTMSK       SET DEVICE PROTECT BIT ON S99528
NO40PTCT TM    CDSFNFLG,CHANGE         IS THIS A CHANGE REQUEST
         BO    ADAPTYPE                YES, PASS CDS INFO ON
         B     GOODLINE                NO, GO DELETE CDS ENTRY
ADAPTYPE EQU   *                                                 S99528
         CLI   EXTENIP,SPECIAL     IS EXTENSION CONFIG IN PROCES S22024
         BE    SPECNAME            YES CONTINUE                  S22024
         MVI   CDSRETFL,ZERO      ZERO OUT RETURN CODE           Y06330
         MVI   CDSCLASS,TCUCLASS       MOVE CLASS TO CDS         S99528
         LOAD  EP=IEDQWI5U             LOAD MODULE               S99528
         LR    ENTRYPT,PARMREG0        GET ENTRY PT OF MODULE    S99528
         BALR  RETURN,ENTRYPT          BRANCH TO MODULE          S99528
         DELETE EP=IEDQWI5U            DELETE MODULE             S99528
         CLI   CDSRETFL,ZERO           CHECK RETURN CODE         S99528
         BNE   CHKFNFLG                BRANCH IF RETURN IS BAD   S99528
         B     GETCHNG                 GO GET CHNG REC           S99528
SPECNAME MVC   CDSTCUTY(L4),TCU3705C MOVE IN NAME OF SPEC MODULE S22024
         B     GETCHNG             CONTINUE PROCESSING           S22024
         EJECT
**************************************************************** S99528
*                                                              * S99528
*                       E X I T   P R O C E D U R E S          * S99528
*                                                              * S99528
**************************************************************** S99528
CLEANUP  EQU   *                       SET CLEANUP FLAG & EXIT   S99528
         MVI   CDSRETFL,GETOUT         SET FLAG                  S99528
EXIT     EQU   *                                                 S99528
         CLOSE CDS40                   CLOSE LOCAL CONFIG DATA   S99528
         CLOSE CDS42                   CLOSE REMOTE CNFIG DATA   S99528
         L     SAVEAREA,PREVSAVE(SAVEAREA)  ADDR.OF CALLERS      S99528
         LM    R14,R2,12(SAVEAREA)     RESTORE                   S99528
         XCTL  (2,12),EP=IEDQWI1       TRANSFER CONTROL          S99528
GETCHNG  EQU   *                                                 S99528
         L     SAVEAREA,PREVSAVE(SAVEAREA)  ADDR. OF CALLERS     S99528
         LM    R14,R2,12(SAVEAREA)     RESTORE REGISTERS         S99528
         XCTL  (2,12),EP=IEDQWIA       TRANSFER CONTROL          S99528
         EJECT
**************************************************************** S99528
*                                                              * S99528
*        I / O   E R R O R   A N A L Y S I S   L O G I C       * S99528
*                                                              * S99528
**************************************************************** S99528
OPENERR  EQU   *                       OPEN ERROR PROCEDURE      S99528
         IEDQMSG MSGID=129,FUNCT=CEC,RET=CONTPROC                S99528
BADCDS   EQU   *                                                 S99528
         IEDQMSG MSGID=128,FUNCT=CEC                             S99528
         OI    CDSRETFL,GETOUT         SET TERMINATE FLAG        S99528
         B     EXIT                    GET OUT                   S99528
FINDERR  EQU   *                                                 S99528
*                                      DEVICE NOT CONFIGURED     S99528
         IEDQMSG MSGID=144,FUNCT=CEC,RET=CHKFNFLG                S99528
DEVPRTCT EQU   *                       DEL/MOD NOT PREMITTED     S99528
         IEDQMSG MSGID=117,FUNCT=CEC,RET=CHKFNFLG                S99528
         READ  CDSDECB,SF,MF=L                                   S99528
BASEADDR DC    AL4(IEDQWID)            BASE ADDRESS
         EJECT
PATCH    DS    0F                      MAINTENANCE AREA FOR      S99528
         DS    CL100                   TEMPERARY FIXES           S99528
BLDLLIST DC    XL2'0001'               NUMBER OF LIST ENTRIES    S99528
         DC    XL2'003A'               LENGTH OF EACH ENTRY      S99528
MODNAME  DC    XL8'0'                  MODULE NAME
         DC    XL6'0'                  TTR-C
USERTTR  DC    XL4'0'                  USER TTR-C
         DC    XL48'0'                 ADDITIONAL DIRECTORY
CDSPOINT DS    0D
         DC    XL8'0'                  POINTER TO CDS DATA       S99528
MODPREFX DC    CL8'G0000000'           MODULE PREFIX             S99528
         DS    0H                                                S99528
NOENTRY  DC    X'0008'                 NO ENTRY FOUND            S99528
SUCCESS  DC    X'0000'                 GOOD RETURN CODE FROM STOWS22024
EXTENIP  DC    X'00'               EXTENSION IN PROCESS FLAG     S22024
CDSIDSAV DC    X'0000'             SAVE AREA FOR RSID            S22024
TCU3705C DC    C'3705'             INSETT FOR UCA MODULE NAME    S22024
         EJECT
CDS40    DCB   DDNAME=OLTLIB,DSORG=PO,RECFM=U,MACRF=(R,W),       Y06330X
               SYNAD=BADCDS                                      Y06330
CDS42    DCB   DDNAME=CDSLIB,DSORG=PO,RECFM=U,MACRF=(R,W),       Y06330X
               SYNAD=BADCDS                                      Y06330
         OLTCB CONFG=YES
CEMSG    EQU   TOTINBUF
LINEADDR EQU   CDSMEMBR
SVAREA   EQU   TOTSAVE5
         END
