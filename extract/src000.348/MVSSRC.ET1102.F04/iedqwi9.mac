         TITLE 'IEDQWI9 CONFIGURATOR SUB-MODULE 5'
IEDQWI9  CSECT                                                   S21903
*A000000-999999                                                @Y16X5U0
*D879000                                                       @OY20528
*A867000                                                       @OY20528
* CHANGE ACTIVITY AS FOLLOW:                                     S21903
*C003000-004000                                                  S21903
*D136000,144000                                                  S21903
*A207350,207400-207420,207870-207940,718300-718600              SA41605
*C308000-351000                                                 SA41605
*A207390,207450-207470,207901-207907,207950-207999,224000        S22024
*A258410-258540,284400-285800,292600-293200,418000,438000,446000 S22024
*A454000,466000,482300-483500,484500-485500,495000,514400-515900 S22024
*A558100,569020-569160,570420-570540,613000,701200-703000,714000 S22024
*A719700-721100,734100-736800,738400-738500,738986-738988        S22024
*C006400,009200-013400,146000-191000,218000,234000,246440,246520 S22024
*C288000,292000,304000,364000,366000,384000,392000,400000,408000 S22024
*C416000,428000,432000,436000,444000,452000,460000,476000,522000 S22024
*C532000,542000,552000,558000,567400,578000,612000,616000,620000 S22024
*C624000,628000,632000,636000,644000,652000,656000,660000,664000 S22024
*C666500,667500,668500,676000,692000                             S22024
*D192000-206000,256000-258400,560000                             S22024
*A207200,564600,564800,563200                                  @YA12402
**************************************************************** S21903
*                                                              * S21903
*                                                              * S21903
* MODULE NAME:                                                 * S21903
*                                                              * S21903
*    IEDQWI9 (TCAM,TOTE)                                       @Y17XAUU
*                                                              * S21903
* DESCRIPTIVE NAME:                                            * S21903
*                                                              * S21903
*    TOTE CONFIGURATOR SUB-MODULE                              * S21903
*                                                              * S21903
* COPYRIGHT:                                                   * S21903
*                                                              * S21903
*    'NONE'                                                    * S21903
*                                                              * S21903
* STATUS:                                                      * S99528
*                                                              * S99528
*    VERSION 10.0                                              @Y17XAUU
*                                                              * S99528
* FUNCTION:                                                    * S99528
*                                                              * S99528
*    THIS MODULE DETERMINES THE LINE ADDRESS AND TERMINAL TYPE.* S22024
*    IF THE LINE IS NOT OPENED, THE LINE ADDRESS IS PROMPTED   * S22024
*    FOR AND THE CORRECT UCB IS THEN FOUND.  THE POLLING       * S22024
*    CHARACTERS AND, IF NECESSARY, THE CPU ID IS MOVED INTO    * S22024
*    THE CDS FOR THE TERMINAL BEING CONFIGURED.  THE LINE      * S22024
*    CONNECTION CODE IS DETERMINED AND MOVED INTO THE CDS.     * S22024
*    IF THE LINE IS BI-SYNCH THE TRANSLATION CODE OF THE       * S22024
*    TERMINAL IS PROMPTED FOR.                                 * S22024
*                                                              * S99528
* ENTRY POINT:                                                 * S99528
*                                                              * S99528
*         IEDQWI9 - CALLED BY ADD SCHEDULER(IEDQWIA)           @Y17XAUU
*                                                              @Y17XAUU
* MODULE TYPE: PROCEDURE                                       @Y17XAUU
*                                                              @Y17XAUU
* MODULE SIZE: SEE LISTING                                     @Y17XAUU
*                                                              @Y17XAUU
* PROCESSOR: ASSEMBLER XF                                      @Y17XAUU
*                                                              * S99528
* MACROS: IEDQMSG,IEDHJN,SAVE,DELETE,LOAD,TCONV                @Y17XAUU
*                                                              @Y17XAUU
* INPUT:                                                       * S99528
*                                                              * S99528
*    REGISTERS 02,11,14,15 CONTAIN THE FOLLOWING VALUES:       * S99528
*                                                              * S99528
*    02-OLTCB POINTER                                          * S99528
*    11-CALLING ROUTINE WORK AREA                              * S99528
*    14-RETURN ADDRESS                                         * S99528
*    15-ENTRY POINT ADDRESS                                    * S99528
*                                                              * S99528
* OUTPUT:                                                      * S99528
*                                                              * S99528
*    REGISTER 1 CONTAINS THE FOLLOWING VALUE:                  * S99528
*                                                              * S99528
*    01-PARAMETER LIST ADDRESS                                 * S99528
*                                                              * S99528
* EXTERNAL REFERENCES:                                         * S99528
*                                                              * S99528
*         IEDQWK-MESSAGE MODULE                                * S99528
*         IEDQWI5U-TCU/ADAPTER TYPE MODULE                     * S99528
*         IEDQWI7-LINE TYPE MODULE                             * S99528
*         IEDIAP04-FIND NETWORK ADDRESS                        @Y17XAUU
*         IEDIAP06-FIND 3705 LCB ADDRESS                       @Y17XAUU
*                                                              * S99528
* EXITS-NORMAL:                                                * S99528
*                                                              * S99528
*         BR AFTER 'PLRET' TO IEDQWIA OR IEDQWI9               * S99528
*                                                              * S99528
* EXITS-ERROR:                                                 * S99528
*                                                              * S99528
*         NONE                                                 * S99528
*                                                              * S99528
* TABLES/WORK AREAS:                                           * S99528
*                                                              * S99528
*    OLTCB                                                     * S99528
*    CDS WORK AREA                                             * S99528
*                                                              * S99528
* ATTRIBUTES:                                                  * S99528
*                                                              * S99528
*    ENABLED,PROBLEM PROGRAM MODE,TRANSIENT                    * S99528
*                                                              * S99528
* NOTES: SEE BELOW                                             @Y17XAUU
*                                                              * S99528
*  DEPENDENCIES - EBCDIC CHARACTER CODE SET                    @Y17XAUU
*                                                              @Y17XAUU
*  RESTRICTIONS-- NONE                                         @Y17XAUU
*                                                              @Y17XAUU
*  REGISTERS CONVENTION--                                      @Y17XAUU
*                                                              @Y17XAUU
*  PATCH LABEL -- PATCH                                        @Y17XAUU
*                                                              * S99528
***********************************************************************
         EJECT
*              EQUATES
*
PARMREG0 EQU   0                       PARAMETER REGISTER        S99528
PARMREG1  EQU  1                       PARAMETER REGISTER        S99528
R2       EQU   2                       OLTCB POINTER             S99528
BASEREG  EQU   3                       PROGRAM BASE REG          S99528
CALLERS  EQU   8                       OFFSET TO CALLER SAVE     S99528
OWN      EQU   4                       OFFSET TO OWN SAVE        S99528
UCBPTREG EQU   10                      POINTER TO UCB            S99528
AREAADDR EQU   12                      AREA ADDRESS              S99528
SAVEAREA EQU   13                      POINTER TO SAVE AREA      S99528
SAVEADDR EQU   15                      SAVE AREA ADDRESS         S99528
RETCODE  EQU   15                      RETURN CODE               S99528
ENTRYPT  EQU   15                      ENTRY POINT ADDRESS       S99528
CUUADR   EQU   2                       CUU ADDRESS               S99528
LEN      EQU   2                       LENGTH                    S99528
CUUPTR   EQU   4                       POINTER TO CUU            S99528
LEFTCLR  EQU   X'0F'                   MASK TO CLEAR LEFT BITS   S99528
TERMCLAS EQU   X'42'                   CDS TERMINAL CLASS        S99528
ZERO     EQU   X'00'                   ZERO TEST                 S99528
LINEOPEN EQU   X'10'                   OPEN LINE INDICATOR       S99528
EBCDIC   EQU   X'C5'                   EBCDIC RESPONSE INDICATOR S99528
USASCII  EQU   X'C1'              ASCII RESPONSE INDICATOR       S22024
TRANSCOD EQU   X'E3'                   TRANSCOD RESPON INDICATOR S99528
YES      EQU   X'E8'                   YES RESPONSE TEST         S99528
NO       EQU   X'D5'                   NO  RESPONSE TEST         S99528
CDSEBCDC EQU   X'07'                   EBCIDIC CODE              S99528
CDSTRANS EQU   X'08'                   TRANS CODE                S99528
CDSUSASC EQU   X'05'                   USASC CODE                S99528
TYPELEN  EQU   4                       LENGTH OF TYPE FIELD      S99528
R0       EQU   0                       WORK REGISTER           @Y17XAUU
R1       EQU   1                       WORK REGISTER             S22024
WORKREG4 EQU   4                       WORK REGISTER             S22024
WORKREG5 EQU   5                       WORK REGISTER             S22024
UCBREG   EQU   5                       UCB ADDRESS               S22024
WORKREG6 EQU   6                       WORK REGISTER             S22024
WORKREG7 EQU   7                       WORK REGISTER 7           S22024
WORKREG8 EQU   8                       WORK REGISTER             S22024
RETURN   EQU   9                       RETURN ADDR FROM SUB-     S22024
*                                        ROUTINE                 S22024
R10      EQU   10                      WORK REGISTER             S22024
R12      EQU   12                      REGISTER 12               S22024
R13      EQU   13                      WORK REGISTER             S22024
R14      EQU   14                      RETURN ADDRESS            S22024
R15      EQU   15                      WORK REGISTER             S22024
CVT      EQU   16                      ADDRESS OF CVT            S22024
UCBPTR   EQU   X'28'                   OFFSET TO UCB TABLE       S22024
BSCTERM  EQU   X'10'                   BI SYNC TERMINAL INDICTR  S99528
CDSSTDBS EQU   X'80'                   RFT CONFIG FLAG           S99528
BLANK    EQU   X'40'                    BLANK CHARACTER        @YA12402
D0       EQU   0                        DISPLACEMENT 0           S99528
D1       EQU   1                        DISPLACEMENT 1           S99528
D2       EQU   2                       DISPLACEMENT 2           SA41605
D3       EQU   3                        DISPLACEMENT 3           S99528
D4       EQU   4                       CONSTANT DISPLACEMENT 4   S22024
D8       EQU   8                       DISPLACEMENT 8           SA41605
D9       EQU   9                       CONSTANT DISPLACEMENT    SA41605
D11      EQU   11                       DISPLACEMENT 11          S99528
D13      EQU   13                      CONSTANT DISPLACEMNT 13   S22024
D17      EQU   17                      CONSTANT DISPLACEMENT 17  S22024
D28      EQU   28                  CONSTANT DISPLACEMENT 28      S22024
D48      EQU   48                      CONSTANT DISPLACEMENT 48  S22024
CONCTERM EQU   X'80'                    BIT SETTING INDICATING   S99528
*                                         CONCENTRATOR ENTRY     S99528
R4       EQU   4                        WORK REGISTER            S99528
R5       EQU   5                        WORK REGISTER            S99528
R6       EQU   6                  WORK REGISTER                  S99528
R7       EQU   7                        WORK REGISTER            S99528
R9       EQU   9                        WORK REGISTER            S99528
R11      EQU   11                      WORK REGISTER            SA41605
LISTEND  EQU   X'FE'                   END OF INVITATION LIST   SA41605
*                                        INDICATOR              SA41605
INACT    EQU   X'40'                   INACTIVE LIST SCAN       SA41605
SAD1     EQU   X'01'                   SADONE INDICATION IN UCB  S22024
SAD3     EQU   X'03'                   SADTHREE INDICATION IN UCBS22024
LGBFLAG  EQU   X'80'                   BIT SETTING INDICATING    S22024
*                                        LGB FOR 3705            S22024
OPEN     EQU   X'10'                   BIT SETTING INDICATING    S22024
*                                        OPEN DCB                S22024
ERROR    EQU   X'01'                   ERROR BIT SETTING         S22024
EOT      EQU   X'80'                   BIT SETTING IN INVIT     SA41605
*                                        LIST INDICATION EOT IS SA41605
*                                        SPECIFIED IN LIST      SA41605
NOACT    EQU   X'80'                   NO ACTIVE ENTRIES IN LISTSA41605
BFDLAY   EQU   X'02'                   BUFFER DELAY INDICATOR    S22024
RNLU     EQU   X'6F'                   3705 SNA LU INDICATOR   @YM07722
RNBISYN  EQU   X'9F'                   3705 BI SYNCH TERMINAL    S22024
*                                        INDICATOR               S22024
RNSS     EQU   X'0F'                   3705 START/STOP TERMINAL  S22024
*                                        INDICATOR               S22024
RNLINE   EQU   X'FF'                   3705 COMMUNICATION LINE   S22024
*                                        INDICATOR               S22024
RNTRM    EQU   X'20'                   3705 TERMINAL OR LINE -   S22024
*                                        BYPASS POLL CHARS GET   S22024
DCBNIDLE EQU   X'02'                   3705 ACTIVE BIT IN DCB    S22024
         EJECT
**************************************************************** S99528
*
*              INITIALIZATION
*
**************************************************************** S99528
         SAVE  (14,12)                 SAVE CALLER'S REGISTERS   S22024
         LR    BASEREG,ENTRYPT         LOAD BASE REGISTER        S99528
         USING IEDQWI9,BASEREG         ESTAB PGM BASE            S99528
         USING TOTOLTCB,R2             ESTAB OLTCB BASE          S99528
IEDQWI9  IEDHJN IEDQWI99,HJN           SET DATE AND MODULE NAME  S22024
         LA    SAVEADDR,QWI9SAVE       LOAD OWN SAVE AREA ADDR   S99528
         ST    SAVEADDR,CALLERS(SAVEAREA)   STORE ADDR.IN CALLER S99528
         ST    SAVEAREA,OWN(SAVEADDR)  STORE CALLERS IN QWI9 SAV S99528
         LR    SAVEAREA,SAVEADDR       GET SAVE ADDR             S99528
         MVI   OPENFLAG,LINEOPEN       INIT OPEN LINE INDICATOR  S99528
         CLC   CDSTCUTY,TCU7770        IS TCU A 7770?            S22024
         BE    PLRET                   BR IF TCU IS 7770         S99528
         EJECT
**************************************************************** S99528
*
*  ROUTINE TO FIND INVITATION LIST ADDRESS
*
**************************************************************** S99528
INVITE   L     WORKREG6,TERTABLE        PICK UP TERMINAL TABLE ADDR
         USING IEDQTRM,WORKREG6   SET TTE ADDRESSABILITY       @Y17XAUU
         TM    TRMDEVFL+D1,TRMCONC  TERMINAL ATTACHED TO       @Y17XAUU
*                                         A CONCENTRATOR?        S99528
         BZ    CONTINUE                 NO, BYPASS CONCENTRATOR  S99528
*                                         SET UP                 S99528
         L     WORKREG8,TRMDESTQ-D1 GET QCB ADDRESS            @Y17XAUU
         DROP  WORKREG6                                        @Y17XAUU
         USING IEDQQCB,WORKREG8                                  S99528
         LA    WORKREG5,QCBDSFLG        GET ADDRESS OF QCB       S99528
         AH    WORKREG5,QCBEXTO         ADD OFFSET TO QCB        S99528
*                                         EXTENSION              S99528
         DROP  WORKREG8                                          S99528
         L     R1,D0(WORKREG5)         GET ADDRESS OF CONCEN-    S22024
*                                         TRATOR TERMINAL ENTRY  S99528
         ST    R1,CONCTTE              SAVE TERMINAL ADDRESS     S22024
         L     WORKREG7,TOTAVTPT        GET AVT ADDRESS          S99528
         USING IEDQAVTD,WORKREG7                                 S99528
         L     R14,AVTRNMPT             GET ADDRESS OF TERMNAME  S99528
*                                         TABLE                  S99528
         DROP  WORKREG7                                          S99528
         USING IEDQTNTD,R14                                      S99528
         LA    R7,TNTFIRST              GET ADDRESS OF FIRST     S99528
*                                         ENTRY IN TABLE         S99528
         SR    R9,R9                    CLEAR REGISTER 9         S99528
         IC    R9,TNTENLEN              GET NAME FIELD LENGTH    S99528
         LA    R4,D3(R9)                GET LENGTH OF ONE ENTRY  S99528
         LH    R5,TNTLEN                GET NUMBER OF ENTRIES    S99528
         AR    R7,R9                    GO TO FIRST TTE ADDRESS  S99528
         LA    R10,D1                   SET COUNTER TO ONE       S99528
         DROP  R14                                               S99528
CONC     EQU   *                                                 S99528
         CLC   D0(D3,R7),CONCTTE+D1     CONCENTRATOR ENTRY       S99528
*                                         FOUND?                 S99528
         BE    FOUND                    YES, CONTINUE PROCESSING S99528
         LA    R10,D1(R10)              INCREMENT COUNTER BY ONE S99528
         AR    R7,R4                    GET NEXT TTE ADDRESS     S99528
         BCT   R5,CONC                  CHECK NEXT ENTRY         S99528
         B     PLRET                    ENTRY NOT FOUND - EXIT   S99528
FOUND    EQU   *                                                 S99528
         STH   R10,WORK2                SAVE TERMNAME TABLE      S99528
*                                         OFFSET                 S99528
         L     R6,CONCTTE               GET CONCENTRATOR TERMINALS99528
*                                         ENTRY ADDRESS          S99528
CONTINUE EQU   *                                                 S99528
         USING IEDQTRM,WORKREG6   SET TTE ADDRESSABILITY       @Y17XAUU
         LR    WORKREG8,WORKREG6  COPY TTE ADDRESS             @YM07384
         L     WORKREG6,TRMDESTQ-D1 GET QCB ADDRESS            @YM07384
         DROP  WORKREG6                                        @YM07384
         USING IEDQQCB,WORKREG6   SET QCB ADDRESSABILITY       @YM07384
         SLR   WORKREG7,WORKREG7  CLEAR REG                    @YM07384
         IC    WORKREG7,QCBRELLN  GET RLN                      @YM07384
         L     WORKREG6,QCBDCBAD-D1 GET DCB ADDRESS            @YM07384
         DROP  WORKREG6                                        @YM07384
         USING IHADCB,WORKREG6    SET DCB ADDRESSABILITY       @YM07384
         TM    DCBDSORG,TRMLGB    LGB VS. DCB                  @YM07384
         BZ    DCBFOUND           NO - ALREADY HAVE DCB        @YM07384
         OI    POLLFLAG,RNTRM     SHOW 3705 RESOURCE           @YM07384
         LA    WORKREG7,D1        SET 3705 RLN                 @YM07384
         L     R4,TOTAVTPT        GET AVT ADDRESS              @YM07384
         USING IEDQAVTD,R4        SET AVT ADDRESSABILITY       @YM07384
NCPLOOP  DS    0H                                              @YM07384
         USING IEDNTRM,WORKREG8   RESET TTE ADDRESSABILITY     @YM07384
         LA    R14,TRMPRFSZ       GET TTE PREFIX SIZE          @YM07384
         SLR   WORKREG8,R14       POINT TO TTE PREFIX          @YM07384
         CLI   TRMTYPE,TRMLNCP    NCP ENTRY                    @YM07384
         BE    NCPFIND            YES                          @YM07384
         LH    PARMREG1,TRMCOHRT  GET COHORT POINTER           @YM07384
         LTR   PARMREG1,PARMREG1  ENTRY IN HIERARCHY           @YM07415
         BZ    NCPROMPT           NO - GO FIND 3705 DCB ADDRE  @YM07415
         L     R15,AVTRNMPT       TERM NAME TABLE ADDRESS      @YM07384
         BALR  R14,R15            FIND TTE ADDRESS             @YM07384
         LR    WORKREG8,PARMREG1  COPY THIS ENTRY              @YM07384
         B     NCPLOOP            CHECK THIS ENTRY             @YM07384
NCPFIND  DS    0H                                              @YM07384
         L     WORKREG6,TRMDESTQ-D1 GET QCB ADDRESS            @YM07384
         USING IEDQQCB,WORKREG6   SET QCB ADDRESSABILITY       @YM07384
         L     WORKREG6,QCBDCBAD-D1 GET 3705 DCB ADDRESS       @YM07384
         DROP  WORKREG6                                        @YM07384
         USING IHADCB,WORKREG6    SET DCB ADDRESSABILITY       @YM07384
         TM    TRMBYTE2,TRMRSACT  3705 ACTIVE                  @YM07384
         BZ    BYPASS             NO                           @YM07384
         TM    TRMBYTE2,TRMINPG   ACT DEACT IN PROGRESS        @YM07384
         BO    BYPASS             YES                          @YM07384
DCBFOUND EQU   *                                                 S22024
         TM    DCBOFLGS,OPEN           IS LINE OPEN?             S22024
         BZ    BYPASS                  NO, GO PROMPT FOR LINE    S22024
*                                        ADDRESS                 S22024
         L     WORKREG8,DCBDEBAD       GET DEB ADDRESS           S22024
GETDEB   EQU   *                                                 S22024
         USING DEBNMSUB,WORKREG8
         SR    R15,R15            GET NUMBER
         IC    R15,DEBNMEXT         OF EXTENTS
         CR    R15,WORKREG7       IS LINE OPENED?
         BL    BYPASS               NO
GETUCBAD EQU   *                       LINE IS OPEN, GET UCB ADD S99528
         BCTR  WORKREG7,0              CONVERT RLN SCALE         S99528
         SLL   WORKREG7,2              GET RLN BYTE OFFSET       S99528
         L     UCBREG,DEBUCBAD(WORKREG7)    GET UCB ADDR.FOR RLN S99528
         DROP  WORKREG6
         DROP  WORKREG8
         B     UCBMOVE                 GO GET LINE INFO          S99528
GETSCT   L     WORKREG8,48(WORKREG6)   PICK UP SCT ADDR          S99528
         ST    WORKREG8,SCTADD          SAVE IT
         L     WORKREG6,60(WORKREG7,WORKREG6)  PICK UP INVITATION LIST
         ST    WORKREG6,INVADDR                ADDR AND SAVE
         EJECT
**************************************************************** S99528
*
*   ROUTINE TO GET POLLING CHARACTERS FROM INVITATION LIST.
*
**************************************************************** S99528
         SPACE
         LTR   WORKREG6,WORKREG6       IS THERE AN INVITATION    S22024
*                                        LIST ADDRESS?           S22024
         BZ    SCTROUT                 YES, NO POLLING CHARS     S22024
         TM    POLLFLAG,RNTRM          IS DEVICE ON 3705?        S22024
         BO    LINEONRN                YES, NO POLL CHARS      @Y17XAUU
         SR    WORKREG7,WORKREG7
         LA    WORKREG4,D1             SET COUNTER TO 1
         LH    WORKREG7,WORK2     PICK UP TERMNAME TABLE INDEX
INVSRCH  EQU   *                                                 S22024
         SH    WORKREG6,TWO            SUBTRACT 2 FROM START OF  S22024
*                                        LIST                    S22024
         CH    WORKREG7,0(WORKREG6)    IS THIS ENTRY FOR TERM?
         BE    INVFIND                 YES BRANCH
         LA    WORKREG4,1(WORKREG4)    NO BUMP COUNTER
         B     INVSRCH                 GO LOOK AGAIN
INVFIND  EQU   *
         L     R5,INVADDR              RESET INVITE ADDR         S22024
         SR    WORKREG7,WORKREG7
         SR    R10,R10                 CLEAR REG 10             SA41605
         SR    WORKREG6,WORKREG6       CLEAR REGISTER 6         SA41605
         IC    WORKREG6,D2(R5)         PICK UP NUMBER OF BYTES  SA41605
*                                        PER ENTRY              SA41605
         IC    WORKREG7,D1(R5)         PICK UP NUMBER OF ACTIVE SA41605
*                                        ENTRIES                SA41605
         LTR   WORKREG7,WORKREG7       ARE THERE ANY ACTIVE     SA41605
*                                        ENTRIES?               SA41605
         BNZ   ACT1                    YES, CONTINUE            SA41605
         OI    POLLFLAG,NOACT          SET NO ACTIVE ENTRIES    SA41605
*                                        INDICATOR              SA41605
         B     INACT2                  GO CONTINUE SEARCH       SA41605
ACT1     EQU   *                                                SA41605
         LA    R11,D1                  SET REG 11 TO 1          SA41605
         IC    R10,D2(R5)              GET NUMBER OF BYTES IN   SA41605
*                                        A ENTRY                SA41605
         SR    R10,R11                 GET OFFSET TO TNT INDEX  SA41605
*                                        POINTER                SA41605
NOINV1   EQU   *                                                SA41605
         LA    R5,D8(R5)               POINT PAST CONTROL WORD  SA41605
NOINV    EQU   *                                                SA41605
         SR    R1,R1                   CLEAR REGISTER 1         SA41605
         IC    R1,D0(R10,R5)           GET TNT OFFSET INDEX     SA41605
         CR    WORKREG4,R1             IS THIS THE CORRECT      SA41605
*                                           ENTRY?              SA41605
         BE    ACTIVE                  YES, GO GET POLLING      SA41605
*                                        CHARACTERS             SA41605
         AR    R10,WORKREG6            POINT TO NEXT TNT INDEX  SA41605
*                                        FIELD                  SA41605
         SR    WORKREG7,R11            DECREMENT COUNT BY 1     SA41605
         LTR   WORKREG7,WORKREG7       HAVE ALL ACTIVE ENTRIES  SA41605
*                                        BEEN EXAMINED?         SA41605
         BZ    INACT2                  YES, GO SEARCH INACTIVE  SA41605
*                                        LIST                   SA41605
         AR    R10,WORKREG6            POINT TO NEXT TNT INDEX  SA41605
         B     NOINV                   CONTINUE SEARCH          SA41605
INACT2   EQU   *                                                SA41605
         TM    POLLFLAG,INACT          IS END OF INACTIVE       SA41605
*                                        LIST SCAN              SA41605
         BO    SCTROUT                 YES, GO GET LINE CONN    SA41605
         L     R5,INVADDR              GET INVITATION LIST ADDR SA41605
         SR    WORKREG7,WORKREG7       CLEAR REGISTER 7         SA41605
         SR    R11,R11                 CLEAR REG 11             SA41605
         IC    WORKREG7,D0(R5)         GET TOTAL NUMBER OF      SA41605
*                                        ENTRIES                SA41605
         IC    R11,D1(R5)              GET TOTAL NUMBER OF      SA41605
*                                        ACTIVE ENTRIES         SA41605
         SR    WORKREG7,R11            GET NUMBER OF INACTIVE   SA41605
*                                        ENTRIES                SA41605
         LTR   WORKREG7,WORKREG7       WERE ALL ENTRIES ACTIVE? SA41605
         BZ    SCTROUT                 YES, GO SET LINE CONN    SA41605
INACT1   EQU   *                                                SA41605
         TM    D3(R5),EOT              IS EOT SPECIFIED IN LIST SA41605
         BZ    NOEOT                   BRANCH AROUND INCREMENT  SA41605
         LA    R11,D1                  GET INCREMENT OF 1       SA41605
         AR    R10,R11                 INCREMENT OFFSET COUNT   SA41605
*                                        BY 1                   SA41605
NOEOT    EQU   *                                                SA41605
         TM    POLLFLAG,NOACT          ALL ENTRIES INACTIVE?    SA41605
         BNO   INACT5                  NO, ADD 1 TO OFFSET      SA41605
         LA    R1,D8                   SET REG 1 TO 8           SA41605
         B     INACT3                  BYPASS INCREMENT OF 9    SA41605
INACT5   EQU   *                                                SA41605
         LA    R1,D9                   SET REG1 TO 9            SA41605
INACT3   EQU   *                                                SA41605
         AR    R1,R5                   POINT PAST CONTROL WORD  SA41605
         AR    R1,R10                  POINT TO NEXT BYTE TO    SA41605
*                                        BE EXAMINED            SA41605
         CLI   D0(R1),LISTEND          END OF ACTIVE ENTRIED?   SA41605
         BE    BUMPCNT                 YES, GO INCREMENT OFFSET SA41605
         B     SCTROUT                 GO SET LINE CONN         SA41605
BUMPCNT  EQU   *                                                SA41605
         AR    R10,R11                 INCREMENT OFFSET COUNT   SA41605
*                                        BY 1                   SA41605
         AR    R10,WORKREG6            POINT TO TNT INDEX       SA41605
         OI    POLLFLAG,INACT          SET INDICATOR TO INACT-  SA41605
*                                        IVE LIST SCAN          SA41605
         TM    POLLFLAG,NOACT          ALL ENTRIES INACTIVE?    SA41605
         BNO   INACT4                  NO, BYPASS DECREMENT     SA41605
         SR    R10,R11                 DECREMENT COUNT BY 1     SA41605
INACT4   EQU   *                                                SA41605
         B     NOINV1                  GO CONTINUE SEARCH       SA41605
ACTIVE   EQU   *                                                SA41605
         BCT   WORKREG6,ACTIVE1        DECREMENT NUMBER OF
*                                        BYTES PER ENTRY BY 1   SA41605
ACTIVE1  EQU   *                                                SA41605
         SR    R10,WORKREG6            GET OFFSET TO POLL CHARS SA41605
         AR    R5,R10                  GET ADDRESS OF POLL CHARSSA41605
         STC   WORKREG6,CDS#POLL       STORE IN CDS INPUT AREA   S99528
         LTR   WORKREG6,WORKREG6       TEST FOR ZERO
         BZ    SCTROUT                 GO TO SET ROUTINE         S99528
         BCTR  WORKREG6,0              DECREMENT COUNT BY ONE    S99528
         STC   WORKREG6,POLLMOVE+1      STORE IN MOVE INSTRUCTION
POLLMOVE MVC   CDSPOLL,D0(R5)          MOVE POLL CHARS TO CDS    S22024
         LA    WORKREG6,D1(WORKREG6)   RESTORE BYTE COUNT        S22024
         EJECT
***********************************************************************
*                                                                     *
* ROUTINE TO DETERMINE LINE CONNECTION CODE AND MOVE IT TO CDS * S99528
*                                                                     *
***********************************************************************
SCTROUT  EQU   *
         CLI   CDSADAP,CDSSDA1         IS ADAPTER TYPE SDA I?    S22024
         BE    CPUID                   GET CPU ID                S99528
         CLI   CDSADAP,CDSSDA2         IS ADAPTER TYPE SDA II?   S22024
         BE    CPUID                   GET CPU ID                S99528
         CLC   CDSTCUTY,TCU2702        IS TCU TYPE 2702?         S22024
         BE    LNCN02                  GET LINE CONN CODE        S99528
         MVI   CDSCONN,CDSDISEN        SET LINE CONNECTION       S22024
         B     CPUID                   GET CPU ID                S99528
LNCN02   EQU   *                                                 S22024
         TM    D17(UCBREG),SAD3        DOES UCB SPECIFY SAD 3?   S22024
         BO    SAD07                   GET CDSCONN               S99528
         BZ    SAD04                   GET CDS CONN              S99528
         TM    D17(UCBREG),SAD1        DOES UCB SPECIFIY SAD 1?  S22024
         BZ    SAD06                   GET CDSCONN               S22024
SAD05    EQU   *                                                 S22024
         MVI   CDSCONN,CDSDSAD1        SET LINE CONNECTION CODE  S22024
         B     CPUID                   GET CPU ID                S99528
SAD06    EQU   *                                                 S22024
         MVI   CDSCONN,CDSDSAD2        SET LINE CONNECTION CODE  S22024
         B     CPUID                   GET CPU ID                S99528
SAD07    EQU   *                                                 S22024
         MVI   CDSCONN,CDSDSAD3        SET LINE CONNECTION CODE  S22024
         B     CPUID                   GET CPU ID                S99528
SAD04    EQU   *                                                 S22024
         MVI   CDSCONN,CDSDSAD0        SET LINE CONNECTION CODE  S22024
         EJECT
**********************************************************************
*        GET CPU ID AND MOVE TO CDS                            * S22024
***********************************************************************
*
CPUID    EQU   *
         L     R7,TERTABLE             GET ADDR OF TERM TABLE    S22024
         USING IEDQTRM,R7              ESTABLISH ADDRESSIBILITY  S22024
         TM    TRMDEVFL,BFDLAY         IS BUFFER DELAY SPECIFIED S22024
         BO    CPUID1                  YES, THEREFORE THERE ARE  S22024
*                                        NO CPU ID CHARS         S22024
         DROP  R7                                              @Y17XAUU
         L     WORKREG6,INVADDR        GET INV LIST ADR.
         LTR   WORKREG6,WORKREG6       IS THERE AN INVITATION    S22024
         BZ    UNDEF              NO - NO CPU ID DEFINED       @Y17XAUU
         TM    POLLFLAG,RNTRM          IS DEVICE ON 3705?        S22024
         BO    UNDEF                   YES, NO CPU ID            S22024
         L     WORKREG6,4(WORKREG6)    GET ADR OF CPUID FIELD.
         LTR   WORKREG6,WORKREG6       ADDR = 0?
         BZ    UNDEF                   BRANCH IF YES.
         MVC   CDS#ID(1),0(WORKREG6)   PUT # ID CHARS IN CDS     S99528
         MVC   CDSCPUID(15),1(WORKREG6) PUT CPU ID CHARS IN CDS  S99528
CPUID1   EQU   *                       BRANCH TARGET             S22024
         EJECT
***********************************************************************
*                                                                     *
*        ROUTINE TO CHECK IF TERMINAL IS BI-SYNC.  IF IT IS           *
*        BI-SYNC THE SPECIAL CHARACTERISTICS TABLE IS SEARCHED        *
*        FOR OLT SOH% TO FIND THE LINE CODE OF THE ADAPTER.           *
*                                                                     *
***********************************************************************
         SPACE 2
UNDEF    EQU   *
         CLI   CDSADAP,CDSSDA2         IS LINE BI-SYNC?          S99528
         BNE   TERTYPE                 NO SKIP ROUTINE
         CLI   OPENFLAG,LINEOPEN       IS LINE OPEN              S99528
         BNE   CODPRMPT                NO PROMPT FOR INFO        S99528
         L     WORKREG6,SCTADD         GET SCT ADDR
         CLI   D13(WORKREG6),D0        IS THERE AN OLT OFFSET?   S22024
         BE    TERTYPE                 NO EXIT
         SR    WORKREG7,WORKREG7       CLEAR R7
         IC    WORKREG7,13(WORKREG6)   PICK UP OFFSET
         LA    WORKREG7,1(WORKREG7,WORKREG6) GET ADDR OF OLT CHARS
         CLC   D0(D2,WORKREG7),CODEEBC IS CODE EBCDIC?           S22024
         BNE   UNDEF1                  NO BRANCH
SETEBCDC MVI   CDSCODE,CDSEBCDC        SET CODE TO EBCDIC        S99528
         B     BYSNTYPE                CHECK TYPE TERMINAL       S99528
UNDEF1   EQU   *
         CLC   D0(D2,WORKREG7),CODETRAN  IS CODE TRANSCODE?      S22024
         BNE   UNDEF2                  NO BRANCH
SETTRANS MVI   CDSCODE,CDSTRANS        SET CODE TO TRANSCODE     S99528
         B     BYSNTYPE                CHECK TYPE TERMINAL       S99528
UNDEF2   EQU   *
         CLC   D0(D2,WORKREG7),CODEASC IS LINE USASC?            S22024
         BNE   TERTYPE                 NO BRANCH
SETASCII MVI   CDSCODE,CDSUSASC        SET CODE TO USASCII       S99528
         B     BYSNTYPE                CHECK TYPE TERMINAL       S99528
CODPRMPT EQU   *                       PROMPT FOR LINE ADAPT COD S99528
         IEDQMSG MSGID=107,FUNCT=REP                             S99528
         CLI   CEMSG,EBCDIC            IS LINE CODE EBCDIC       S99528
         BE    SETEBCDC                YES                       S99528
         CLI   CEMSG,USASCII           IS LINE CODE EBCDIC       S99528
         BE    SETASCII                YES                      S99528
         CLI   CEMSG,TRANSCOD          IS LINE CODE EBCDIC       S99528
         BE    SETTRANS                YES                       S99528
CODPRMT1 EQU   *                                                 S22024
         IEDQMSG MSGID=079,FUNCT=CEC                             S22024
         IEDQMSG MSGID=125,FUNCT=REP   WISH TO CONTINUE          S99528
         CLI   CEMSG,YES               YES RESPONSE              S99528
         BE    CODPRMPT                CONTINUE PROMPT           S99528
         CLI   CEMSG,NO                NO  RESPONSE              S99528
         BE    CLEANUP                 GO CLEANUP                S99528
         B     CODPRMT1                INVALID RESPONSE          S99528
         EJECT
************************************************************** @YM07415
*                                                            * @YM07415
*        ROUTINE TO FIND 3705 DCB ADDRESS WHEN THE 3705 IS   * @YM07415
*        IN AN INACTIVE STATE.                               * @YM07415
*                                                            * @YM07415
************************************************************** @YM07415
NCPROMPT DS    0H                 BOUNDARY ALLIGNMENT          @YM07415
*        3705 NOT ACTIVE MESSAGE                               @YM07415
         IEDQMSG MSGID=086,FUNCT=CEC                           @YM07415
         SPACE
NCPLOOP1 DS    0H                 BOUNDARY ALLIGNMENT          @YM07415
*        ENTER SYMBOLIC NAME OF NCP TO WHICH RESOURCE IS       @YM07415
*        ATTACHED                                              @YM07415
         IEDQMSG MSGID=124,FUNCT=REP,INBUF=CEMSG,INCNT=8       @XM05674
         SPACE
         MVC   NCPNAME(D8),CEMSG  SAVE INPUT REPLY             @XM05674
         CLC   NCPNAME(D8),NULL   NAME ENTERED                 @YM07415
         BE    BADNAME            NO - WRITE ERROR MESSAGE     @YM07415
         L     R4,TOTAVTPT        GET AVT ADDRESS              @YM07415
         USING IEDQAVTD,R4        SET AVT ADDRESSABILITY       @YM07415
         SLR   R9,R9              CLEAR REG                    @YM07415
         LA    R6,NCPNAME         GET ADDRESS OF NCP NAME      @YM07415
BLNKTEST DS    0H                 BOUNDARY ALLIGNMENT          @YM07415
         CLI   D0(R6),BLANK       END OF NAME                  @YM07415
         BE    BLNKFIND           YES - LEN IS IN REG 9        @YM07415
         LA    R9,D1(R9)          INCREMENT NAME LEN COUNT     @YM07415
         LA    R6,D1(R6)          POINT TO NEXT CHARACTER      @YM07415
         C     R9,F8              SEARCH EXHAUSTED             @YM07415
         BE    BLNKFIND           YES - LEN IS IN REG 9        @YM07415
         B     BLNKTEST           CHECK NEXT CHARACTER         @YM07415
BLNKFIND DS    0H                 BOUNDARY ALLIGNMENT          @YM07415
         STH   R9,LENGTH          SAVE NAME LENGTH             @YM07415
         LA    R1,NCPNAME         GET NCP NAME                 @YM07415
         ALR   R1,R9              POINT TO END OF NAME         @YM07415
         CL    R9,F8              LENGTH AT MAXIMUM            @YM07415
         BE    TERSCH1            YES - EXIT                   @YM07415
         L     R15,F8             GET MAXIMUM LENGTH ALLOWED   @YM07415
         SLR   R15,R9             GET PAD COUNT                @YM07415
         EX    R15,BLMOVE         PAD THE NAME WITH BLANKS     @YM07415
TERSCH1  DS    0H                 BOUNDARY ALLIGNMENT          @YM07415
         L     R15,AVTMSGS-D1     VCON TABLE ADDRESS           @YM07415
         L     R15,D8(R15)        GET ADDRESS OF A1 ROUTINE    @YM07415
         LH    R0,LENGTH          GET NAME LENGTH              @YM07415
         LA    R1,NCPNAME         GET ADDRESS ON NAME          @YM07415
         BAL   R14,D4(R15)        VALIDATE NAME                @YM07415
         LTR   R15,R15            NAME VALID                   @YM07415
         BZ    BADNAME            NO - ERROR                   @YM07415
         SPACE
         LR    R1,R15             GET TTCIN                    @YM07415
         L     R15,AVTRNMPT       TERM NAME TABLE ADDRESS      @YM07415
         BALR  R14,R15            FIND TTE ADDRESS             @YM07415
         LA    R15,TRMPRFSZ       SET TTE PREFIX SIZE          @YM07415
         SLR   R1,R15             POINT TO TTE PREFIX          @YM07415
         USING IEDNTRM,R1         SET TTE ADDRESSABILITY       @YM07415
         TM    TRMSTATE,TRMPREF   3705 RESOURCE                @YM07415
         BZ    BADNAME            NO - ERROR                   @YM07415
         CLI   TRMTYPE,TRMLNCP    NCP ENTRY                    @YM07415
         BNE   BADNAME            NO - ERROR                   @YM07415
         L     R6,TRMDESTQ-D1     GET QCB ADDRESS              @YM07415
         DROP  R1                                              @YM07415
         USING IEDQQCB,R6         SET QCB ADDRESSABILITY       @YM07415
         L     R6,QCBDCBAD-D1     GET DCB ADDRESS              @YM07415
         DROP  R6                                              @YM07415
         USING IHADCB,R6          SET DCB ADDRESSABILITY       @YM07415
         L     WORKREG8,DCBDEBAD  GET DEB ADDRESS              @YM07415
         LA    R7,D1              SET RLN                      @YM07415
         B     GETDEB             CONTINUE CONFIG PROCESSING   @YM07415
         SPACE
BADNAME  DS    0H                 BOUNDARY ALLIGNMENT          @YM07415
*        INVALID NCP ENTRY                                     @YM07415
         IEDQMSG MSGID=126,FUNCT=CEC                           @YM07415
         SPACE
RESPN    DS    0H                 BOUNDARY ALLIGNMENT          @YM07415
         IEDQMSG MSGID=125,FUNCT=REP WISH TO CONTINUE          @YM07415
         CLI   CEMSG,YES          YES                          @YM07415
         BE    NCPLOOP1           PROMPT FOR NCP NAME AGAIN    @YM07415
         CLI   CEMSG,NO           NO                           @YM07415
         BE    CLEANUP            CLEANUP AND GET OUT          @YM07415
         SPACE
*        INVALID RESPONSE                                      @YM07415
         IEDQMSG MSGID=079,FUNCT=CEC,RET=RESPN                 @YM07415
         EJECT
**************************************************************** S99528
*                                                              * S99528
*     ROUTINE TO PROMPT FOR LINE ADDRESS WHEN LINE NOT OPENED *  S99528
*     THE ADDRESS OF THE UCB IS FOUND AND PLACED IN UCBREG(5).   S99528
*     ADAPTER IS PLACED IN CDSADAP. LINE CONNECTION CODE IN    * S99528
*     CDSCONN.                                                 * S99528
*                                                              * S99528
**************************************************************** S99528
BYPASS   EQU   *                                                 S99528
         MVI   OPENFLAG,ZERO           SET LINE CLOSED INDICATOR S99528
*                                      SET UP WORKREG7           S99528
         BCTR  WORKREG7,0              CONVERT RLN SCALE         S99528
         SLL   WORKREG7,2              GET RLN BYTE OFFSET       S99528
         IEDQMSG MSGID=118,FUNCT=CEC                             S99528
BYPASS1 IEDQMSG MSGID=119,FUNCT=REP,INBUF=CEMSG,INCNT=4,               X
               LINK=YES                                        @YA12402
UCBLOOK  L     WORKREG5,CVT(PARMREG0)  PICK UP CVT POINTER       S99528
         L     UCBPTREG,UCBPTR(WORKREG5)  6OAD PTR TO UCB TABLE  S99528
UCBLOOK1 LH    UCBREG,0(UCBPTREG)      LOAD UCB ADDR.            S99528
         LA    UCBPTREG,2(UCBPTREG)    POINT TO NEXT UCB         S99528
         LTR   UCBREG,UCBREG           UCB REG ZERO              S99528
         BZ    UCBLOOK1                PADDING; IGNORE ENTRY     S99528
         BC    4,UCBEND                END OF LIST; NO MATCH     S99528
         CLI   CEMSG+D3,BLANK           IS 4TH CHAR BLANK?     @YA12402
         BE    DIGIT3                   YES                    @YA12402
         CLI   CEMSG+D4,BLANK           MORE THAN 4 CHAR?      @YA12402
         BNE   UCBEND                   YES, INVALID LINE ADDR @YA12402
         CLC   CEMSG+D1(D3),D13(UCBREG) ADJUST FOR LEADING ZERO@YA12402
         B     UCBMATCH                 GO TAKE APPRO. BRANCH  @YA12402
DIGIT3   EQU   *                                               @YA12402
         CLC   CEMSG(3),13(UCBREG)     CUU MATCH?                S99528
UCBMATCH EQU   *                                               @YA12402
         BNE   UCBLOOK1                GO CHECK NEXT UCB ENTRY   S99528
UCBMOVE  MVC   CDSUNIT+CUUADR(LEN),CUUPTR(UCBREG) MOVE HEX CUU   S99528
         NI    CDSUNIT+CUUADR,LEFTCLR  CLEAR FLAG BITS           S99528
         B     CONTPRC2                GO TO CONTINUE PROCED     S99528
UCBEND   EQU   *                       INVALID LINE ADDRESS      S99528
         IEDQMSG MSGID=141,FUNCT=CEC   PRINT MSG                 S99528
CONTPROC IEDQMSG MSGID=125,FUNCT=REP   WISH TO CONTINUE?         S99528
         CLI   CEMSG,YES               YES                       S99528
         BE    BYPASS1                 GO TO BYPASS              S99528
         CLI   CEMSG,NO                NO                        S99528
         BE    CLEANUP                 GO TO CLEANUP GET OUT     S99528
CONTPRC1 EQU   *                       INVALID RESPONSE          S99528
         IEDQMSG MSGID=079,FUNCT=CEC,RET=CONTPROC                S22024
CONTPRC2 EQU   *                                                 S99528
         MVI   CDSCLASS,TERMCLAS       MOVE CLASS TO CDS         S99528
         LOAD  EP=IEDQWI5U             LOAD MODULE TO GET        S99528
         LR    ENTRYPT,PARMREG0             TCU TYPE             S99528
         BALR  RETURN,ENTRYPT               ADAPTER TYPE         S99528
         DELETE EP=IEDQWI5U            DELETE MODULE             S99528
         CLI   CDSRETFL,ZERO           WAS RETURN GOOD?          S99528
         BNE   CLEANUP                 NO, GET OUT               S99528
         CLI   CDSADAP,RNLU        SNA LU ENTRY                @YM07722
         BE    CONTPRC4            YES - GET RESOURCE ID       @YM07722
         CLI   CDSADAP,RNBISYN     BI-SYNCH ON 3704/3705         S22024
         BE    CONTPRC4            YES  GET RESOURCE ID          S22024
         CLI   CDSADAP,RNSS        START/STOP DEV ON 3704/3705   S22024
         BE    CONTPRC4            YES  GET RESOURCE ID          S22024
         CLI   CDSADAP,RNLINE      IS IT A COMM LINE ON 3704/3705S22024
        BE    CONTPRC4            YES  GET RESOURCE ID          S22024
CONTPRC3 EQU   *                   ESTABLISH BRANCH POINT        S22024
         CLI   CDSADAP,RNLU        SNA LU ENTRY                @YM07722
         BE    LINEONRN            YES, PROCESS                @YM07722
*                                                                S22024
         LOAD  EP=IEDQWI7              LOAD MODULE TO GET        S99528
         LR    ENTRYPT,PARMREG0             LINE TYPE            S99528
         BALR  RETURN,ENTRYPT               LINE CONNECTION CODE S99528
         DELETE EP=IEDQWI7             DELETE MODULE             S99528
         CLI   CDSRETFL,ZERO           WAS RETURN GOOD?          S99528
         BNE   CLEANUP                 NO, GET OUT               S99528
         B     GETSCT                  GO GET SCT ADDRESSS       S99528
CONTPRC4 EQU   *                   ESTABLISH BRANCH POINT        S22024
         CLI   OPENFLAG,ZERO       IS LINE OPEN                  S22024
         BNE   CONTPRC3            YES SO DONT ASK FOR RSID      S22024
*                                  ALREADY HAVE IT IN TTE        S22024
NETWADR  EQU   *                                               @Y17XAUU
         IEDQMSG MSGID=108,FUNCT=REP,INBUF=CEMSG,INCNT=4         S22024
         TCONV FROM=CEMSG,TO=CDSUNIT,COUNT=4,TYPE=EH             S22024
         B     CONTPRC3            NOW HAVE RSID CONTINUE        S22024
         EJECT
***********************************************************************
*                                                                     *
*  ROUTINE TO ASK CE TERMINAL TYPE, VERIFY IT, PUT IT INTO CDS * S22024
*  AND PLINK DEVICE CONFIGURATOR                                      *
*                                                                     *
***********************************************************************
         SPACE 2
TERTYPE  IEDQMSG MSGID=112,FUNCT=CEC   TERMINAL TYPES SUPPORTED  S99528
         IEDQMSG MSGID=113,FUNCT=CEC   ARE 1030,1050,1060,2740,  S99528
         MVI   TYPEFLAG,ZERO           SET START STOP INDICATOR  S99528
         LA    WORKREG7,3              SET UP ERROR COUNTER      S99528
         IEDQMSG MSGID=106,FUNCT=REP                             S99528
TERTYPE1 EQU   *                                                 S22024
         CLC   CEIN(D4),TERM1030       1030 SPECIFIED?           S22024
         BE    TYPEFIND                GO LOAD TYPE INTO CDS     S9952Y
         CLC   CEIN(D4),TERM1050       1050 SPECIFIED?           S22024
         BE    TYPEFIND                GO LOAD TYPE INTO CDS     S99528
         CLC   CEIN(D4),TERM1060       1060 SPECIFIED?           S22024
         BE    TYPEFIND                GO LOAD TYPE INTO CDS     S99528
         CLC   CEIN(D4),TERM2260       2260 SPECIFIED?           S22024
         BE    TYPEFIND                GO LOAD TYPE INTO CDS     S99528
         CLC   CEIN(D4),TERM2265       2265 SPECIFIED?           S22024
         BE    TYPEFIND                GO LOAD TYPE INTO CDS     S99528
         CLC   CEIN(D4),TERM2740       2740 SPECIFIED?           S22024
         BE    TYPEFIND                GO LOAD TYPE INTO CDS     S99528
         CLC   CEIN(D4),TERM2741       2741 SPECIFIED?           S22024
         BE    TYPEFIND                GO LOAD TYPE INTO CDS     S99528
         CLC   CEIN(D4),TERM2760       2760 SPECIFIED?           S22024
         BE    TYPEFIND                GO SET UNIT TYPE          S99528
         CLC   CEIN(D4),LINE3700       3705 RESOURCE           @Y17XAUU
         BE    TYPEFIND                GO SET UNIT TYPE        @Y17XAUU
         BCT   WORKREG7,ERROR2     DECREMENT COUNTER             S99528
         B     ERROR3                  BRANCH IF THIRD ERROR     S99528
BYSNTYPE MVI   TYPEFLAG,BSCTERM        SET BI SYNC INDICATOR     S99528
         IEDQMSG MSGID=114,FUNCT=CEC   BI SYNC TERMS SUPPORTED   S99528
         LA    WORKREG7,3              SET UP ERROR COUNTER      S99528
         IEDQMSG MSGID=106,FUNCT=REP   ENTER TERM TYPE           S99528
         CLC   CEIN(D4),TERM1130       1130 SPECIFIED?           S22024
         BE    STD2700                 GO SET STD RFT CNFG BIT   S99528
         CLC   CEIN(D4),TERM2020       MOD 20 SPECIFIED?         S22024
         BE    STD2700                 GO SET STD RFT CNFG BIT   S99528
         CLC   CEIN(D4),TERMS360       SYSTEM 360 SPECIFIED?     S22024
         BE    STD2700                 GO SET STD RFT CNFG BIT   S99528
         CLC   CEIN(D4),TERM2770       2770 SPECIFIED?           S22024
         BE    STD2700                 GO SET STD RFT CNFG BIT   S99528
         CLC   CEIN(D4),TERM2780       2780 SPECIFIED?           S22024
         BE    STD2700                 YES, GET STD BI SYNC CNFG S99528
         CLC   CEIN(D4),TERM3270       3270 SPECIFIED?           S22024
         BE    TYPEFIND                YES                       S99528
         CLC   CEIN(D4),TERM3670       3670 SPECIFIED?           S22024
         BE    STD2700                 GO SET STD RFT CNFG BIT   S99528
         CLC   CEIN(D4),LINE3700       3705 RESOURCE           @Y17XAUU
         BE    LINEONRN                YES - PROCESS           @Y17XAUU
         BCT   WORKREG7,ERROR2     DECREMENT COUNTER
ERROR3   EQU   *                       TOO MANY TRIES, GET OUT   S99528
         OI    CDSRETFL,ERROR          SET ERROR FLAG            S22024
         IEDQMSG MSGID=142,FUNCT=CEC,RET=PLRET                   S99528
ERROR2   IEDQMSG MSGID=142,FUNCT=CEC                             S99528
ERROR4   EQU   *                                                 S22024
         IEDQMSG MSGID=125,FUNCT=REP                             S99528
         CLI   CEMSG,YES               YES                       S99528
         BE    CHKDEV                  GO CHECK DEVICE TYPE      S22024
         CLI   CEMSG,NO                IS ANSWER NO?             S22024
         BE    CLEANUP                 YES, GO TO EXIT           S22024
         B     ERROR4                  GO ASK QUESTION AGAIN     S22044
CHKDEV   EQU   *                                                 S22024
         CLI   TYPEFLAG,ZERO           IS TERMINAL START STOP    S99528
         BE    TERTYPE                 START STOP DEVICE         S99528
         B     BYSNTYPE                BI SYNC                   S99528
CLEANUP  EQU   *                                                 S99528
         OI    CDSRETFL,ERROR          SET INVALID RETURN FLAG   S22024
         B     PLRET                   GO RESTORE REGISTERS      S99528
STD2700  OI    CDSFNFLG,CDSSTDBS       SET 2700 FLG ON           S99528
TYPEFIND EQU   *                                                 S99528
         MVC   CDSTERM(TYPELEN),CEMSG  MOVE TERM TYPE TO CDS     S99528
         B     PLRET               RETURN TO CALLER              S22024
*                                                                S22024
#EXIT    DS    0H                  ALIGN FOR INSTRUCTION         S22024
         L     R15,$TABLE          GET START OF TABLE            S22024
         L     R15,D28(R15)        BUMP BY 28                    S22024
         BR    R15                 GO TO MACRO HANDLER           S22024
*                                                                S22024
LINEONRN EQU   *                   ESTABLISH BRANCH POINT        S22024
         MVC   CDSTERM(TYPELEN),LINE3700 MOVE IN SPECIAL NAME    S22024
*                                  FOR 3705 LINE CONFIGURATION   S22024
PLRET    EQU   *                                                 S99528
         L     SAVEAREA,OWN(SAVEAREA)  GET OLD SAVE AREA ADDR    S99528
         LM    R14,R12,12(SAVEAREA)    RESTORE REG               S99528
         BR    RETURN                  RETURN                    S99528
         EJECT
QWI9SAVE DS    18F                     SAVE AREA                 S99528
OPENFLAG DC    CL1'0'                  OPEN LINE FLAG            S99528
TYPEFLAG DC    CL1'0'                  TERM TYPE FLAG            S99528
POLLFLAG DC    X'00'                   FLAG USED WHEN SCANNING  SA41605
*                                        FOR POLL CHARACTERS    SA41605
PATCH    DC    CL50' '                 MAINTAINENCE AREA         S99528
LINE3700 DC    C'3700'             NAME OF 3705 LINE CONFIG      S22024
*                                  MODULE FOR COMM LINES         S22024
*                                                                S22024
         DS    0F
INVADDR  DS    F                       INVIT LIST                S99528
SCTADD   DS    F                       SCT ADDR                  S99528
CONCTTE  DS    F                        ADDRESS OF CONCENTRATOR  S99528
*                                         TERMINAL ENTRY         S99528
         SPACE 1
*                                                              * S22024
*        SUPPORTED TERMINAL TYPES                              * S22024
*                                                              * S22024
TERM1030 DC    C'1030'                 1030 TERMINAL             S22024
TERM1050 DC    C'1050'                 1050 TERMINAL             S22024
TERM1060 DC    C'1060'                 1060 TERMINAL             S22024
TERM2260 DC    C'2260'                 2260 TERMINAL             S22024
TERM2265 DC    C'2265'                 2265 TERMINAL             S22024
TERM2740 DC    C'2740'                 2740 TERMINAL             S22024
TERM2741 DC    C'2741'                 2741 TERMINAL             S22024
TERM2760 DC    C'2760'                 2760 TERMINAL             S22024
TERM1130 DC    C'1130'                 1130 TERMINAL             S22024
TERM2020 DC    C'2020'                 2020 TERMINAL             S22024
TERMS360 DC    C'S360'                 S360 TERMINAL             S22024
TERM2770 DC    C'2770'                 2770 TERMINAL             S22024
TERM2780 DC    C'2780'                 2780 TERMINAL             S22024
TERM3270 DC    C'3270'                 3270 TERMINAL             S22024
TERM3670 DC    C'3670'                 3670 TERMINAL             S22024
         SPACE 1
CODEASC  DC    X'0125'                 USASC INDICATION IN SCT   S22024
CODETRAN DC    X'002C'                 TRANSCODE INDICATION IN   S22024
*                                        SCT                     S22024
CODEEBC  DC    X'016C'                 EBCDIC CODE INDICATION IN S22024
*                                        SCT                     S22024
TCU2702  DC    C'2702'                 2702 TCU TYPE             S22024
TCU7770  DC    C'7770'                 TCU TYPE OF 7770          S22024
TWO      DC    H'2'                    ARITHMETIC VALUE 2        S22024
         SPACE
F8       DC    F'8'               VALUE OF 8                   @Y17XAUU
NCPNAME  DS    CL8                PLACE FOR NCP SYM NAME       @YM07415
NULL     DC    8XL1'40'           BLANK FIELD                  @YM07415
LENGTH   DS    H                  LENGTH OF SYM NAME           @YM07415
BLMOVE   MVC   D0(D0,R1),NULL     PAD NAME FIELD               @YM07415
         EJECT
         TTRMD
         EJECT
DCBDSECT DCBD  DSORG=TQ
         OLTCB CONFG=YES               OLTCB DSECT               S99528
TERTABLE DS    F                        SAVE TTE ADDRESS       @OY20528
         TDEBD
         EJECT
         TLCBD                                                 @Y17XAUU
         EJECT
         TQCBD                                                   S99528
         EJECT
         TAVTD                                                   S99528
         EJECT
         TTNTD                                                   S99528
         EJECT
         TLGBD                                                   S22024
WORK2    EQU   CDSCODE                 PTR TO WORK AREA          S99528
CEMSG    EQU   TOTINBUF                EQU TO INPUT MESSAGE      S99528
CEIN     EQU   TOTINBUF                EQU TO INPUT MESSAGE      S99528
         END
         EJECT
         TDEBD
         END
