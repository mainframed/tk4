QWI7     TITLE 'IEDQWI7 CONFIGURATOR SUBMODULE 3'
IEDQWI7  CSECT                                                   S22024
*CHANGE ACTIVITY AS FOLLOWS:
**************************************************************** S22024
*                                                              * S22024
* TITLE --                                                     * S22024
*                                                              * S22024
*    'IEDQWI7 - TOTE CONFIGURATOR SUB-MODULE'                  * S22024
*                                                              * S22024
* MODULE NAME --                                               * S22024
*                                                              * S22024
*    IEDQWI7                                                   * S22024
*                                                              * S22024
* DESCRIPTIVE NAME --                                          * S22024
*                                                              * S22024
*    TOTE CONFIGURATOR SUB-MODULE                              * S22024
*                                                              * S22024
* COPYRIGHT --                                                 * S22024
*                                                              * S22024
*    'NONE'                                                    * S22024
*                                                              * S99528
* STATUS --                                                    * S99528
*                                                              * S99528
*    CHANGE LEVEL 2                                            * S22024
*                                                              * S99528
* FUNCTION --                                                  * S99528
*                                                              * S99528
*    THIS MODULE DETERMINES LINE TYPE AND LINE TRANSLATION     * S99528
*    CODE FOR THE LINE                                         * S99528
*                                                              * S99528
* ENTRY POINT --                                               * S22024
*                                                              * S99528
*    IEDQWI7 - CALLED BY ADD SCHEDULER (IEDQWIA) OR BY         * S99528
*              TERMINAL FEATURES MODULE.                       * S99528
*                                                              * S99528
* INPUT --                                                     * S99528
*                                                              * S99528
*    REGISTERS 02,05,11,13,14,15 CONTAIN THE FOLLOWING VALUES: * S99528
*                                                              * S99528
*         02 - OLTCB POINTER                                   * S99528
*         05 - UCB ADDRESS                                     * S99528
*         11 - CALLING ROUTINE WORK AREA                       * S99528
*         13 - SAVE AREA ADDRESS                               * S99528
*         14 - RETURN ADDRESS                                  * S99528
*         15 - ENTRY POINT ADDRESS                             * S99528
*                                                              * S99528
* OUTPUT --                                                    * S99528
*                                                              * S99528
*    REGISTER 01 CONTAINS THE FOLLOWING VALUE:                 * S99528
*                                                              * S99528
*         01 - PARAMETER LIST ADDRESS                          * S99528
*                                                              * S99528
* EXTERNAL REFERENCES --                                       * S99528
*                                                              * S99528
*    IEDQWK - MESSAGE MODULE                                   * S99528
*                                                              * S99528
* EXITS,NORMAL --                                              * S99528
*                                                              * S99528
*    BR AFTER 'CODEEND' TO IEDQWI9 OR IEDQWIA                  * S99528
*                                                              * S99528
* EXITS,ERROR --                                               * S99528
*                                                              * S99528
*    NONE                                                      * S99528
*                                                              * S99528
* TABLES/WORK AREAS --                                         * S99528
*                                                              * S99528
*    CDS WORK AREA                                             * S99528
*                                                              * S99528
* ATTRIBUTES --                                                * S99528
*                                                              * S99528
*    ENABLED, PROBLEM PROGRAM MODE, TRANSIENT                  * S99528
*                                                              * S99528
* CHARACTER CODE DEPENDENCY --                                 * S99528
*                                                              * S99528
*    NONE                                                      * S99528
*                                                              * S99528
* NOTES --                                                     * S99528
*                                                              * S99528
*    NONE                                                      * S99528
*                                                              * S99528
**************************************************************** S99528
         EJECT
*
*        EQUATES
*
         SPACE
R2       EQU   2                       POINTER TO OLTCB          S99528
UCBREG   EQU   5                       UCB ADDRESS               S22024
RETURN   EQU   9                       RETURN ADDRESS            S22024
R12      EQU   12                      REGISTER 12               S22024
R14      EQU   14                      RETURN ADDRESS            S22024
PARMREG0 EQU   0                       PARAMETER REG 0           S99528
PARMREG1 EQU   1                       PARAMETER REG 1           S99528
BASREG   EQU   3                       BASE REGISTER             S99528
OLTCBBAS EQU   11                      POINTEF TO OLTCB          S99528
SAVEAREA EQU   13                      POINTER TO SAVE AREA      S99528
SAVEADDR EQU   15                      SAVE AREA ADDR            S99528
PREVSAVE EQU   4                       OFFSET TO PREV SAVE       S99528
ENTRYPT  EQU   15                      ENTRY POINT ADDR          S99528
CALLERS  EQU   8                       OFFSET TO CALLERS SAVE    S99528
OWN      EQU   4                       OFFSET TO OWN SAVE        S99528
BRNCHADR EQU   8                       BRANCH TABLE ADDR         S99528
NO       EQU   X'D5'                   NO RESPONSE TEST          S99528
YES      EQU   X'E8'                   YES REPONSE TEST          S99528
BRNCHTBL EQU   1                       POINTER TO BRANCH TABLE   S99528
T2701SD2 EQU   X'03'                   SDA II TEST               S99528
T2703SD1 EQU   X'02'                   SDA I TEST                S99528
T2845LOC EQU   X'01'                   2845 LOCAL TEST           S99528
ZERO     EQU   X'00'                   ZERO TEST                 S99528
UCBOPT   EQU   17                      OFFSET IN UCB TO CHARACT- S22024
*                                        ERISTICS BYTE           S22024
SDAI     EQU   X'70'                   SYNCHRONOUS ADAPTER 1     S22024
SDAII    EQU   X'90'                   SYNCHRONOUS ADAPTER 2     S22024
SWTCHED  EQU   X'90'                   SWTCHED LINE TEST MASK    S99528
MULTIPT  EQU   X'08'                   MULTIPT LINE TEST MASK    S99528
UCBTYP   EQU   16                 CONSTANT DISPLACEMENT          S99528
BISMULTI EQU   X'07'              BISYNCH MULTIPOINT BIT SETTING S99528
         EJECT
*
*              INITIALIZATION
*
         SAVE  (14,12)                 SAVE CALLER'S REGISTERS   S22024
         LR    BASREG,ENTRYPT          LOAD BASEREG              S99528
         USING IEDQWI7,BASREG          SET UP BASE               S99528
         USING TOTOLTCB,R2             SET UP OLTCB BASE         S99528
IEDQWI7  IEDHJN QWI7HJN,HJN            SET DATE AND MODULE NAME  S22024
         LA    SAVEADDR,QWI7SAVE       LOAD ADDR.OF OWN SAVE ARE S99528
         ST    SAVEADDR,CALLERS(SAVEAREA)   STORE ADDR.IN CALLER S99528
         ST    SAVEAREA,OWN(SAVEADDR)  STORE CALLERS IN QWI7 SAV S99528
         LR    SAVEAREA,SAVEADDR       GET SAVE ADDR             S99528
***********************************************************************
*                                                                     *
* ROUTINE TO DETERMINE LINE TYPE (SWITCHED,LEASED,LOCAL) AND MOVE     *
* TO CDS                                                              *
*                                                                     *
***********************************************************************
         SPACE
LEAPTPT  EQU   X'01'                   PT-TO-PT CDSLINE FLAG     S22024
LEAMULT  EQU   X'02'                   LEASED MULTIPOINT CDSLINE S22024
*                                        FLAG                    S22024
SWNOAUTO EQU   X'03'                   SWITCHED NO AUTODIAL      S22024
*                                        CDSLINE FLAG            S22024
SWAUTO   EQU   X'04'                   SWITCHED AUTODIAL CDSLINE S22024
*                                        FLAG                    S22024
LOCAL    EQU   X'05'                   LOCAL CDSLINE FLAG        S22024
AUTODIAL EQU   X'80'                   AUTODIAL BIT IN UCB       S22024
         SPACE
LINETYPE EQU   *                       GET LINE TYPE             S99528
         CLC   CDSTCUTY,TCU2848        IS TCU A 2848?            S22024
         BE    LOCALSET                YES                       S99528
         CLC   CDSTCUTY,TCU7770        IS TCU A 7770?            S22024
         BE    LOCALSET                YES                       S99528
         SPACE
         TM    UCBOPT(UCBREG),SWTCHED  IS LINE SWITCHED          S99528
         BM    AUTOCHK             BRANCH IF THE LINE IS SWITCH  S99528
         BO    AUTOCHK                 BRANCH IF LINE IS SWITCH  S99528
         TM    UCBOPT(UCBREG),MULTIPT  IS LINE MULTIPOINT        S99528
         BO    LEASEDM                 MULTIPOINT BRANCH         S99528
         TM    UCBTYP(UCBREG),BISMULTI  IS LINE BISYNCH MULTI-PT?S99528
         BO    LEASEDM            YES, GO SET BIT                S99528
LEASED   MVI   CDSLINE,LEAPTPT         SET TO LEASED POINT-TO-PO S99528
         B     TYPEMOVE                GO MOVE TYPE TO CDS       S99528
LEASEDM  MVI   CDSLINE,LEAMULT         SET TO LEASED MUTIPOINT   S99528
         B     TYPEMOVE                GO MOVE TYPE TO CDS       S99528
AUTOCHK  TM    UCBOPT(UCBREG),AUTODIAL  IS LINE AUTODIAL?
         BO    AUTOSET                 SET AUTO DIAL SWITCH      S99528
         MVI   CDSLINE,SWNOAUTO        SET TO SWITCHED NO AUTODI S99528
         B     TYPEMOVE                GO MOVE TYPE TO CDS       S99528
AUTOSET  MVI   CDSLINE,SWAUTO          SET TO SWITCHED AUTODIAL  S99528
         B     TYPEMOVE                GO MOVE TYPE TO CDS       S99528
LOCALSET MVI   CDSLINE,LOCAL           SET LOCAL FLAG            S99528
         B     RESTORE                 GO RESTORE REG            S99528
TYPEMOVE EQU   *        MOVE LINE CONNECTION CODE TO CDS WORK AR S99528
TCUSDAII CLI   CDSADAP,SDAII           IS TCU A SDA II?          S99528
         BNE   TCUSDAI                 NO                        S99528
         MVI   CDSCONN,T2701SD2        MOVE X'03' TO CDSCONN     S99528
         B     RESTORE                 GO RESTORE REG            S99528
TCUSDAI  CLI   CDSADAP,SDAI            IS TCU A SDA I            S99528
         BNE   TCU2845A                NO                        S99528
         MVI   CDSCONN,T2703SD1        MOVE X'02' TO CDSCONN     S99528
         B     RESTORE                 GO RESTORRE REG           S99528
TCU2845A CLC   CDSTCUTY,T2845          IS TCU A 2845             S99528
         BNE   TCUOTHER                NO                        S99528
         MVI   CDSCONN,T2845LOC        MOVE X'01' TO CDSCONN     S99528
         B     RESTORE                 GO RESTORE REG            S99528
TCUOTHER MVI   CDSCONN,ZERO            MOVE X'00' TO CDSCONN     S99528
RESTORE  EQU   *                       RESTORE RESTORE REGISTERS S99528
         L     SAVEAREA,PREVSAVE(SAVEAREA)  RESTORE REG          S99528
         LM    R14,R12,R12(SAVEAREA)    RESTORE REGISTERS        S99528
         BR    RETURN                  RETURN                    S99528
         EJECT
TCU2848  DC    CL4'2848'               2848 TEST CONSTANT        S22024
TCU7770  DC    CL4'7770'               7770 TEST CONSTANT        S22024
T2845    DC    CL4'2845'               2845 TEST CONSTANT        S22024
QWI7SAVE DS    18F                     SAVE AREA                 S99528
PATCH    DC    CL50' '                 MAINTENANCE AREA          S22024
         EJECT
*              O L T C B   D S E C T                             S99528
         OLTCB CONFG=YES               OLTCB DSECT               S99528
CEMSG    EQU   TOTINBUF                EQU TO INPUT MESSAGE      S99528
         END
