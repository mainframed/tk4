         TITLE 'IGCMD10D - 3705 IPL - INITIALIZATION MODULE'
IGCMD10D CSECT
*-000000-999999                                                @X26X5R0
*D148000-171500                                                @YA03836
*A210000                                                       @YA03836
*C066000                                                       @Y17XARS
*A066000                                                       @Y17XARS
*A080000                                                       @Y17XARS
*D081000                                                       @Y17XARS
*C085000                                                       @Y17XARS
*C087000                                                       @Y17XARS
*A088500                                                       @Y17XARS
*C089000                                                       @Y17XARS
*C095000                                                       @Y17XARS
*C098000                                                       @Y17XARS
*A114000                                                       @Y17XARS
*C117000-122000                                                @Y17XARS
*C128000                                                       @Y17XARS
*C135000                                                       @Y17XARS
*C141000                                                       @Y17XARS
*C220000                                                       @Y17XARS
*C272000                                                       @Y17XARS
*C273500                                                       @Y17XARS
*C276000                                                       @Y17XARS
*C283000                                                       @Y17XARS
*C297000                                                       @Y17XARS
*C297500                                                       @Y17XARS
*A297500                                                       @Y17XARS
*C117000,A118000,A398000,D120000,D128000                       @Y17XARS
*A297500                                                       @OY19803
         SPACE
************************START OF SPECFICATIONS*************************
*                                                                     *
* MODULE NAME = IGCMD10D (TCAM, OPERATOR CONTROL)                     *
*                                                                     *
* DESCRIPTIVE NAME = 3705 IPL INITIALIZATION MODULE                   *
*                                                                     *
* COPYRIGHT = NONE                                                    *
*                                                                     *
* STATUS = VERSION 10.0                                               *
*                                                                     *
*    FUNCTION = IGCMD10D IS THE 1ST LOAD OF IPL PROPER. IT GAINS      *
*    CONTROL FROM THE COMMON IPL/DUMP MODULE IGCMH10D WHO OBTAINS A   *
*    WORKAREA.  IGCMD10D INITIALIZES THE WORKAREA AND CHECKS THE 3705 *
*    TTE FOR THE IPL LOAD MODULE NAME. IF NOT PRESENT IT XCTLS TO     *
*    IGCMI10D TO RESPOND IED163I 'IPL TERMINATED LOAD MODULE NAME NOT *
*    IN 3705 TTE. IF THE IPL LOAD MODULE NAME IS PRESENT              *
*    THE WORKAREA IS SETUP WITH THE LOAD MODULE NAME AND THE DECB TYPE*
*    VALUE. THE 3705 TTE IS NOW CHECKED TO SEE IF INITIAL TEST WAS    *
*    SPECIFIED AND THE INDICATION YES OR NO IS SET IN THE WORKAREA.   *
*    THE BLDLLIST IS SETUP IN THE WORKAREA AND THE DIAGNOSTIC MAXI AND*
*    MINI PHASE 1 NAMES ARE MOVED IN.   THE IOB IN THE 3705 LCB IS    *
*    INITIALIZED FOR CHANNEL I?O AND IPL REQUIRED, AND THE UCB        *
*    NAME ARE SETUP IN THE WORKAREA. NEXT THE 3705 UCB IS CHECKED FOR *
*    A MINI OR MAXI CHANNEL ADAPTER. IF MINI THE NO CHAINING BIT IS   *
*    SET IN THE CCW AND THE PHASE VECTOR TABLE IS SETUP WITH THE      *
*    MINI OR MAXI PHASE 1 ADDRESS. NEXT THE MINI OR MAXI PHASE 1      *
*    ROUTINES ARE LOADED INTO CORE AND THE ADDRESS OF PHASE 1 IS      *
*    INSERTED INTO THE PHASE TABLE.  NEXT THE PHASE 1 ROUTINE IS      *
*    LOADED AND ITS ADDRESS IS INSERTED INTO THE PHASE TABLE.  THE    *
*    LENGTH OF PHASE 2 IS INSERTED INTO PHASE 1. IF INITIAL TEST WAS  *
*    SELECTED, THE MEMBER NAME IFL3705D IS MOVED INTO THE BLDLLIST    *
*    AND A BLDL IS ISSUED.   IF THE BLDL FAILS, AN XCTL               *
*    IS DONE TO IGCMH10D TO WRITE OUT IPL TERMINATED -                *
*    BLDL FAILED. OTHERWISE THE 'FIND' ROUTINE IS ENTERED.            *
*    NEXT A TEST IS MADE TO SEE IF PART 2 IS BEING LOADED, IF YES,    *
*    XCTL IS ISSUED TO IGCME10D, OTHERWISE XCTL TO IGCMF10D.          *
*    IF INITIAL TEST IS NOT TO BE DONE, OR FOLLOWING INITIAL TEST     *
*    EXECUTION, A BIT IS SET IN PHASE 1 TO INDICATE INITIAL TEST WILL *
*    NOT BE LOADED AND INITIAL TEST IS SET IN THE WORKAREA.  THE      *
*    ADDRESS OF THE DCB CONTAINING THE LOAD MODULE IS OBTAINED AND    *
*    OPENED.  IF OPEN FAILS, AN XCTL TO IGCMI10D TO RESPOND IED165I   *
*    IPL TERMINATED.  OTHERWISE, THE LOAD MODULE NAME IS MOVED INTO   *
*    BLDLLIST AND A BLDL IS DONE.  IF BLDL FAILS, MESAGE IED165 IS    *
*    SENT AS DESCRIBED EARLIER.  OTHERWISE A FIND IS DONE AND THEN    *
*    XCTL IS DONE TO IGCMF10D (IF NOT ENTERED FROM IGCMF10D), OR TO   *
*    IGCME10D WITH 1 IN REG0                                          *
*                                                                     *
* NOTES = SEE BELOW                                                   *
*                                                                     *
*    DEPENDENCIES = EBCDIC CHARACTER CODE DEPENDENCIES - CORRECTABLE  *
*                   BY REASSEMBLY                                     *
*                                                                     *
*    RESTRICTIONS = NONE                                              *
*                                                                     *
*    REGISTER CONVENTIONS = R1 = OPCAVT                               *
*                           R3 = CURRENT ELEMENT                      *
*                           R4 = TCAM AVT                             *
*                           R7 = COMMON IPL/DUMP WORKAREA             *
*                           R5 = IPL WORKAREA 1                       *
*                           R6 = IPL WORKAREA 2                       *
*                           R11 = TRAILER                             *
*                                                                     *
*    PATCH LABEL = NONE                                               *
*                                                                     *
* MODULE TYPE = PROCEDURE                                             *
*                                                                     *
*    PROCESSOR = ASSEMBLER XF
*                                                                     *
*    MODULE SIZE =                                                    *
*                                                                     *
*    ATTRIBUTES = SERIALLY REUSABLE,REENTRANT,TRANSIENT TYPE 4,       *
*                 SUPERVISOR MODE                                     *
*                                                                     *
* ENTRY POINT = IGCMD10D                                              *
*                                                                     *
*    PURPOSE = SEE FUNCTION                                           *
*                                                                     *
*    LINKAGE = XCTL SF=(E,OPCXCTL)                                    *
*                                                                     *
* INPUT = R1 = OPCAVT ,OPCOPCE CONTAINS ADDRESS OF CURRENT ELEMENT    *
* OCTRUNIT CONTAINS ADDRESS OF A TRAILER ELEMENT, THE FIRST 4 BYTES   *
* OF OCMODNME CONTAINS THE ADDRESS OF THE COMMON IPL/DUMP WORKAREA    *
*                                                                     *
* OUTPUT = R0 = 0 XCTL TO IGCME10D TO READ RECORD, R1 = OPCAVT ADDRESS*
* AND OCTRTRN SET XCTL TO IGCME10D.  R1 = OPCAVT XCTL TO IGCMF10D     *
* TO ISSUE IPL COMMAND                                                *
*                                                                     *
* EXIT-NORMAL = IGCME10D IGCMF10D                                     *
*                                                                     *
* EXIT-ERROR = IGCMI10D                                               *
*                                                                     *
* EXTERNAL REFERENCES = SEE BELOW                                     *
*                                                                     *
*    ROUTINES = IEDQCA  OPEN ROUTINE                                  *
*               FIND ROUTINE                                          *
*                                                                     *
*    DATA AREAS = IPL/DUMP COMMON WORKAREA                            *
*                 IPL WORKAREA 1                                      *
*                 IPL WORKAREA 2                                      *
*                                                                     *
*    CONTROL BLOCKS = AVT, DCB, LCB, TTE, OPCAVT, OPCE, UCB, DECB,    *
*    IOB, ECB                                                         *
*                                                                     *
* TABLES = NONE                                                       *
*                                                                     *
* MACROS = LOAD XCTL BLDL                                             *
*                                                                     *
* CHANGE ACTIVITY = NONE                                              *
*                                                                     *
*************************END OF SPECIFICATIONS*************************
         EJECT
         SPACE
         USING IEDQOPCD,R2              OPCAVT BASE
         USING IEDQOPCE,R3              OPCE  BASE
         USING IEDQAVTD,R4              ADDRESSABILITY FOR AVT
         USING IEDCOM,R7                COMMON AREA BASE       @Y17XARS
         USING IEDOCTR,RB               TRAILER ADDRESSABILITY @Y17XARS
         SPACE 2
         BALR  RC,0                     ESTABLISH
         USING *,RC                     ADDRESSABILITY
IGCMD10D IEDHJN  SKIPID
         SPACE 5
         LR    R2,R1                    SET BASE FOR OPCAVT
         L     R3,OPCCOPCE              ADDR CURRENT ELEMENT
         L     R4,OPCAVTPT              BASE FOR AVT
         L     R7,OCMODNME              GET BASE FOR COMMON AREA
         LA    R5,COMCCWS               SET BASE FOR LWKAREA
         LA    R6,DATAOFF(R5)           SET BASE FOR DATADS
         L     RB,OCTRUNIT              SET BASE FOR TRAILER   @Y17XARS
         TM    OCTRSW,OCTRINMF          REENTERED FROM MF?     @Y17XARS
         BO    BLDL4LM                  YES- DO BLDL AND FIND FOR L.M.
         L     R9,OCTRDCB               GET DCB ADDR OF 3705   @Y17XARS
         ST    R9,FOUR(R5)              STORE IN LWKAREA
         L     R8,OCTRUCB               GET UCB FOR 3705       @Y17XARS
         ST    R8,EIGHT(R5)             STORE IN LWKAREA
         LA    R0,MSG163                ERROR MSG NUMBER
         USING IEDNCP,R8                SET BASE FOR DEVICE    @Y17XARS
*                                       DEP. SECTION OF TTE    @Y17XARS
         L     R8,OCTRDVD               LOAD BASE              @Y17XARS
         CLI   NCPTXID,BLANK            BLANKS-- NO NAME ?     @Y17XARS
         BE    OUT                      YES, EXIT TO MI TO FREE CORE
GETNAME  EQU   *
         MVC   TWELVE(EIGHT,R5),NCPTXID  MOVE NAME INTO L-WRK  @Y17XARS
         MVC   DECBTYPE(TWO,R5),TYPE    INSERT TYPE VALUE IN DECB
         USING DATADS,R6
         LA    R6,DATAOFF(R5)           INSERT ADDR DATADS IN BASE REG.
         TM    NCPFLAG1,NCPBGUP         WAS BRINGUP SPECIFIED  @Y17XARS
         BNO   NOBRNGUP                 NO
         OI    ZERO(R5),SETBRGUP        YES  SET BIT IN C-WRAREA
NOBRNGUP EQU   *
*        THIS SECTION OF CODE INITIALIZES THE DATA DSECT USED BY
*        VARIOUS LOADS OF IPL TO SAVE AND MANIPULATE DATA. THE USING
*        STATEMENT (USING DATADS,REGISTER) SETS UP ADDRESSABILITY TO
*        THE 2ND WORKAREA AND PREFIXED BY THE 1ST WORKAREA.           *
         SPACE 3
SETUP    EQU   *
         MVC   N256HW(EIGHT),TN256HW    MOVE VALUES TO LWKAREA
         MVC   MAXLIN(EIGHT),TMAXLIN    SET UP BLDLLIST
         MVI   MEMBER,AVTEBLNK          INSER A BLANK
         MVC   MEMBER+ONE(SEVEN),MEMBER PROPAGATE BLANKS
         USING IEDQLCB,R7               LCB ADDRESSABILITY
         L     R7,OCTRLCB               GET ADDRESS OF 3705 LCB@Y17XARS
         MVC   LCBCCW3(SIXTEEN),TRNCCWLN INSERT CHANNEL PROG   @Y17XARS
*                                       IN LCB        @Y17XARS
         USING IEDQCCW,R1               CCW STRUC ADDRESSABIL  @Y17XARS
         LA    R1,LCBCCW2               SENSE COMMAND FIELD    @Y17XARS
         MVC   LCBCCW2(EIGHT),SENOPCD   INSERT SENSE CHAN PROG @Y17XARS
         LA    R3,LCBSENS0              ADDRESS IOB SENSE      @Y17XARS
         STCM  R3,7,CCWADDR             ADDRESS                @Y17XARS
         DROP  R1
         LA    R3,LCBCCW2               ASSUME START ON SENSE  @Y17XARS
         TM    OCTRSW,OCTRBLST          IPL WITHOUT SENSE?     @Y17XARS
         BNO   SETUP2                   NO, START ON SENSE@Y17XARS
         LA    R3,LCBCCW3               YES, START ON IPL      @Y17XARS
SETUP2   EQU   *                                               @Y17XARS
         STCM  R3,7,LCBSTART            STORE CHAN PROG START ADDRESS
*                                       IN CSW                 @Y17XARS
         SPACE 3
         SPACE 3
LOADPHAS EQU   *
         OI    SWITCH,ONIPL             INDICATE IPL REQUIRED
         SPACE
INIT50   EQU   *
         SPACE 3
*    REGISTER 5 POINTS TO THE LOCAL WORKAREA AND REGISTER 6 POINTS    *
*    TO THE COMMON WORKAREA                                           *
         SPACE 3
         L     R3,OCTRUCB               GET PTR TO UCB FROM    @Y17XARS
*                                       LOCAL WORKAREA
         MVC   RNADR(THREE),UCBNAME(R3)   SAVE UCB NAME IN C-WK
INIT60   EQU   *
         CLI   UCBCA(R3),MINICA         DOES THIS 3705 HAVE A MIMI CA
         BNE   INIT62                   NO ASSUME MAXI
         OI    CASW,ONCA           INDICATE MINI FOR BRING UP.
         NI    LCBCCW3+FOUR,ONES-CHAIN  DON'T CHAIN FOR MINI   @Y17XARS
         LA    R7,MINIVT                YES ADJUST PHASE VECTOR
         B     INIT62AA                 CONTINUE
INIT62   EQU   *
         LA    R7,MAXIVT                GET MAXI PHASE V.T.
INIT62AA EQU   *
         ST    R7,PHASPTR               SAVE THE PHASE PTR
         SPACE 5
         SPACE 3
PHASLOAD EQU   *
         TM    SWITCH2,ONCA             MINI CHANNEL ?
         BO    INIT76                   YES GO SET MINI
         MVC   MEMBER(EIGHT),MXIBUPH    MODULE NAME
         BAL   RE,BLDLSET               ISSUE BLDL FOR MODULE
         MVC   MEMBER(EIGHT),MXIBUPH+EIGHT MODULE NAME
         BAL   RE,BLDLSET               ISSUE BLDL FOR MODULE
         MVC   MAXIBUPH(SIXTEEN),MXIBUPH INSERT NAME PHASE 1 AND 2
*                                       FOR MAXI C.A.
         LA    RA,MAXIBUPH              NO -- SET MAXI
         B     INIT77                   CONTINUE
INIT76   EQU   *
         MVC   MEMBER(EIGHT),TMIN       MODULE NAME
         BAL   RE,BLDLSET               ISSUE BLDL FOR MODULE
         MVC   MEMBER(EIGHT),TMIN+EIGHT MODULE NAME
         BAL   RE,BLDLSET               ISSUE BLDL FOR MODULE
         MVC   MAXIBUPH(SIXTEEN),TMIN   INSERT NAME OF PHASE 1 AND 2
*                                       FOR MINI C.A.
         LA    RA,MAXIBUPH              SET ADDRESS OF MINI PHASE 1
INIT77   EQU   *
         LOAD  EPLOC=(RA)
         SH    R0,HFOUR                 SET BACK TO START
         ST    R0,ZERO(R7)               PUT ADDRESS IN PHASE TABLE
         LR    R1,R0
         SPACE 5
         NI    RNSWITCH(R1),ONES-SIXTBIT SET 18 BIT FLAG IN PHASE 1
         LA    RA,MAXIBUPH+EIGHT        GET ADDRESS OF PHASE 2
         LOAD  EPLOC=(RA)
         ST    R0,FOUR(R7)              SET ADDR OF PHASE 2
         L     R1,ZERO(R7)              ADDRESS OF PHASE 1
         LR    R7,R0
         MVC   RNPH2D(TWO,R1),TWO(R7)    SET PHASE 2 LENGTH IN
*                                       BRING-UP PHASE 1
         TM    ZERO(R5),SETBRGUP        BRING-UP REQUESTED
         BNO   INIT80                   NO; DO BLDL AND FIND FOR L.M.
         XC    MEMBER(FOURTY),MEMBER    ZERO OUT BLDL LIST     @YA03836
         MVC   MEMBER(EIGHT),DIAGMEM    MOVE IN ITEST PART 1   @YA03836
GETDCB   EQU   *                                               @YA03836
         BAL   RE,BLDLPHS1              OPEN ITEST DCB         @YA03836
         BAL   RE,BLDLSET               ISSUE BLDL FOR BRINGUP @YA03836
         LA    RF,RALTTRK               LOAD AREA ADDRESS      @YA03836
         MVC   ZERO(FOUR,R1),ZERO(RF)   MOVE RELAD TO DCB      @YA03836
         USING IEDCOM,R7                                       @YM07698
         L     R3,OPCCOPCE              POINT TO OPCE          @YM07698
         L     R7,OCMODNME              COMMON WA BASE         @YM07698
         MVC   COMTTRZ(THREE),RALTTRK   SAVE TTR FOR POINT     @YM07698
         MVI   COMTTRZ+THREE,ZERO       CLEAR Z FIELD          @YM07698
         LR    R0,R1                    INDICATE C TYPE FIND   @YA03836
         L     RF,EIGHT4(R0,R1)         LOAD FIND ROUTINE ADDR @YA03836
         BAL   RE,FOUR(R0,RF)           LINK TO FIND ROUTINE   @YA03836
         CLC   MEMBER(EIGHT),DIAGMEM+EIGHT LOADING PART 2      @YA03836
         BE    GOTOME                   YES, EXIT TO SEND IT   @YA03836
         SPACE 3
INIT78   EQU   *
         SPACE 3
*    XCTL TO THE WRITE MODULE TO ISSUE A SENSE CMD. LOAD PHASE 1      *
*    THEN WRITE WILL XCTL TO IGCME10D TO READ BRING-UP RECORDS. AFTER *
*     BRING-UP FINISHES SEND PHASE 2. THEN SEND L.M. IPL REQ. WILL    *
*    BE SET. OPCCOPCE POINTS TO A REAL OPCE OR AN LCB                 *
         SPACE 3
GOTOWRTE EQU   *
         MVC   OPCMODID,XCTLWRTE        XCTL TO IFLWRITE RTN   @Y17XARS
EXIT     EQU   *
         LR    R1,R2
         XCTL  SF=(E,OPCXCTL)
         SPACE  3
*    OPCCOPCE POINTS TO AN OPCE OR A L-WKAREA. IF AN OPCE HIGH ORDER  *
*    BIT ONLY IS ON. IF L-WKAREA THE TWO HIGH ORDER BITS ARE ON.      *
BLDL4LM  EQU   *
         USING IEDQOPCE,R3              ADDRESSABILITY FOR OPCE
         CLI   DIAGIND,CHARN            INITIAL TEST PART 2 REQD.
         BNE   OVER                     NO CONTINUE
         MVC   MEMBER(EIGHT),DIAGMEM+EIGHT MOVE IN INITIAL TEST
*                                       PART 2
         MVI   DIAGIND,AVTEZERO         CLEAR INITIAL TEST INDICATOR
         B     GETDCB                   BRANCH TO OPEN DCBS
INIT80   EQU   *                        LINK TO INPUT RTN
         OI    NOBRUP(R1),ON            INDICATE BRING-UP WILL NOT BE
*                                       LOADED
OVER     EQU   *
         MVI   DIAGIND,CHARN            NO INITIAL TEST
         XC    TXTLNGTH(TEN),TXTLNGTH   ZERO L.M. INDICATORS
         SPACE 5
         L     R1,OPCIPLAD              GET DCB
         LR    R3,RB                    SAVE TRAILER           @YM04661
         BAL   RE,SAVEADDR              OPEN THE DCB             S22024
         LR    RB,R3                    RESTORE TRAILER        @YM04661
         XC    MEMBER(FOURTY),MEMBER    ZERO OUT BLDLLIST
         MVC   MEMBER(EIGHT),TWELVE(R5)  SET MEMBER NAME
         SPACE 5
         BAL   RE,BLDLSET               ISSUE BLDL AND DETERMINE
*                                       WHERE THE MODULE RESIDES
INIT85   EQU   *                        BLDL SUCCESSFUL NOW DO FIND
         TM    C,ALIASIND               IS THIS AN ALIAS?
         BNO   INIT86                   NO NORMAL
         TM    ATTRIB,RENREUS           IS ALIAS RENT OR REUS?
         BZ    INIT86                   NO NORMAL
         MVC   ENTRYPT+ONE(THREE),EPAALS GET THE ENTRY POINT
         B     INIT87                   CONTINUE
INIT86   EQU   *                        MEMBER NAME
         MVC   ENTRYPT+ONE(THREE),EPAMEM GET THE ENTRY POINT
INIT87   EQU   *
         LA    RF,RALTTRK               GET AREA ADDRESS
         MVC   ZERO(FOUR,R1),ZERO(RF)   MOVE RELAD TO DCB
         L     R3,OPCCOPCE              OPCE ADDRESS           @YM07698
         L     R7,OCMODNME              COMMON WA BASE         @YM07698
         MVC   COMTTRZ(THREE),RALTTRK   SAVE TTR FOR POINT     @YM07698
         MVI   COMTTRZ+THREE,ZERO       CLEAR Z FIELD          @YM07698
         LR    R0,R1
         L     RF,EIGHT4(R0,R1)
         BAL   RE,FOUR(R0,RF)           BRANCH                   S22024
** RETURN CODE ALWAYS ZERO FOR C-TYPE FIND  ***
****************************************************************
         SPACE  3
*    XCTL TO IGCME10D TO READ THE L.M. RECORDS IF NOTFIRST SET        *
         SPACE  3
         TM    OCTRSW,OCTRINMF          ENTERED FROM IFLWRITE  @Y17XARS
         BNO   GOTOWRTE                 NO PREPARE TO XCTL TO IFLWRITE
         NI    OCTRSW,ONES-OCTRINMF     TURN OFF REENTERED FLAG@Y17XARS
GOTOME   EQU   *
         LA    R0,ONE                   CONTROL CAME FROM 1ST LOAD
         MVC   OPCMODID,XCTLINPT        XCTL TO IFLINPUT       @Y17XARS
         B     EXIT                     PREPARE TO EXIT
         DROP  R7                                              @YM04692
         SPACE  3
OUT      EQU   *
NORMAL   EQU   *
         MVC   OPCMODID,XCTLMI          GO TO COMMON EXIT RTNE @Y17XARS
         B     EXIT                     BRANCH TO EXIT           S22024
         SPACE 3
*    XCTL TO IGCMF10D TO SEND PHASE 1 AND PHASE 2 IF INITIAL          *
*    TEST IS NOT TO BE LOADED, OTHERWISE SEND INITIAL TEST, THEN      *
*    PHASE 2. THE NCP IS NOW SENT OVER.                               *
         SPACE 3
BLDLSET  EQU   *                        SET UP FOR BLDL SVC
         LA    R0,BLDLIST               ADDR OF BLDL LIST IN C-WKAREA
         L     R1,TWENFOUR(R5)          GET ADDR OF DCB FOR 3705 IPL
         BLDL  (1),(0)
         LTR   RF,RF                    SUCCESSFUL ?
         L     R1,TWENFOUR(R5)          PICK UP DCB ADDR AGAIN
         BCR   EIGHT,RE                 YES- CONTINUE
         OI    OCTRRTN,OCTRERI          SET IPL ERROR FLAG     @Y17XARS
         L     R3,OPCCOPCE              RESTORE OPCE ADDR      @OY19803
         MVI   OCSWITCH,OCIPLC          SET OCSWITCH TO IPL CMD@Y17XARS
         LA    R0,MSG165                NO - GET ERROR MSG NUMBER
         B     OUT                      PREPARE TO EXIT
         SPACE 3
BLDLPHS1 EQU   *
         L     R1,OPCIPLAD              GET DCB
         LA    R1,DCBOFF(R1)            2ND DCB ADDRESS
         SPACE 3
SAVEADDR EQU   *
         ST    R1,TWENFOUR(R5)          SAVE DCB JUST FOUND
         LA    RD,REGSAVE(R5)           GET ADDR OF REG SAVEAREA
         LR    RA,R1                    SET UP REG 1 WITH DCB ADDR
         L     RF,OPCOPTLK              ADDR OF GETBLDL LIST
         LA    RF,ADDROPEN(RF)          PICKUP ADDR TO ADDR OF OPENCODE
*                                       IN IEDQCA RTN
         L     RF,ZERO(RF)              ADDR OF OPEN CODE
         BR    RF                       BRANCH TO OPEN CODE IN IEDQCA
*                                       TO DETERMINE IF THE DCB FOR
*                                       IPL PROCESSING HAS BEEN OPENED
         SPACE 3
         SPACE
         SPACE
         EJECT
*************  LOCAL DATA FOR INIT ONLY ************************
         SPACE
         SPACE 5
DIAGMEM  DC    C'IFL3705D'              INITIAL TEST PART 1
         DC    C'IFL3705E'              INITIAL TEST PART 2
         SPACE 5
         SPACE 5
DELAY    EQU   X'32'                    OFFSET INTO WKAREA FOR TIME
*                                       TO WAIT
*                                       TO WAIT FOR BOTH PARTS
*                                       OF INITIAL TEST TO RUN
TIME1    DC    H'0015'                  TIME FOR PART 1 TO RUN
         SPACE 5
TMIN     DC    C'IFL3705A'              PHASE 1 RAS
         DC    C'IFLLD1P2'              PHASE 2 NCP SUPPORT GROUP
MXIBUPH  DC    C'IFL3705B'              PHASE 1 RAS
         DC    C'IFLLD2P2'              PHASE 2 NCP SUPPORT GROUP
         SPACE 5
CVTLOC   EQU   X'10'                    OFFSET TO CVT ADDRESS
NIDLE    EQU   X'02'                    TO SET 3705 IDLE
IPLED    EQU   X'08'                    TO SET 3705 NOT IPLED
TYPE     DC    X'8080'                  TYPE FIELD VALUE
DCBOFF   EQU   X'58'                    SIZE OF A DCB
CLNUPPRI EQU   X'88'                    CLEAN UP INDICATOR TO MH
HFOUR    DC    H'4'                     CONSTANT FOUR
DECBTYPE EQU   60                       OFFSET IN L-WKAREA TO DECB
*                                       TYPE FIELD
RNPH2D   EQU   6                        DISP IN BU PH1 FOR PH2 LEN
LEN      EQU   256                      LENGTH IN MVC OF ZEROS
AUTOIPL  EQU   X'DF'                    PRIORITY OF AUTO-IPL ELEMENT
HALFTWO  DC    H'2'                     TWO                      S22024
ALLLEFT  EQU   235                      LENGTH REMAINING AFTE 1ST M5C
TN256HW  DC    H'256'                   MAXIMUM MVC COUNT
TINBUFSZ DC    H'1024'                  SIZE OF INPUT BUFFER
         DC    H'512'                   SIZE OF OUTPUT BUFFER
TMAXLIN  DC    H'52'                    MAXIMUM LINES PER PAGE
         DC    H'4'                     FOUR CONSTANT
         DC    H'1'                     ONE ENTRY IN THIS LIST
         DC    H'58'                    EACH ENTRY IS 58 BYTES LONG
TLINECNT DC    H'0'                     LINE COUNT FOR OUTPUT
TEC      EQU   X'EC'                    PHASE VECTOR TABLE PTR.
TPHASCNT DC    H'8'                     NUM OF PHASES TIMES 4
TDIAGIND DC    C'Y6'                    INITIAL-TEST INDICATOR
         DC    C'Y6'                    DEFAULT DIAG VALUE
TFLAGS   EQU   X'60'                    FLAGS IN CCW
*                                                              @Y17XARS
*              SENSE CCW                                       @Y17XARS
*                                                              @Y17XARS
SENOPCD  DC    AL1(CCWSENSE)            SENSE OP CODE          @Y17XARS
SENADDR  DC    AL3(0)                   ADDRESS                @Y17XARS
SENFLG   DC    AL1(CCWCC)               FLAGS                  @Y17XARS
         DC    AL1(0)                   UNUSED                 @Y17XARS
SENCNT   DC    AL2(1)                   DATA COUNT             @Y17XARS
*                                                              @Y17XARS
TRNCCWLN DC    AL1(0)                   ZERO                     S22024
RNCCWAD  DC    AL3(0)                   ZERO                     S22024
         DC    X'60'                    HEX 60                   S22024
         DC    AL1(0)                   ZERO                     S22024
RNCCWLN  DC    AL2(1025)                CCW LEN                  S22024
NOPCCW   DC    AL1(3)                   NOP CCW                  S22024
         DC    AL3(0)                   ZERO                     S22024
         DC    X'20'                    HEX 20                   S22024
         DC    AL3(1)                   ONE                      S22024
TIOBRN   EQU   X'42'                    HEX 42                   S22024
THIRTEEN EQU   13                       UCBNAME OFFSET IN UCB
WKSIZE   DC    X'0001EC'                LEN OF L-WK AND C-WK
LCBPRIO  EQU   X'E2'                    UNIQUE PRIORITY FOR 3705 LCB
*                                       PASSED FROM ERP OR OPEN
ERPCMD   EQU   X'01'                    CMD CAME IN FROM ERP OR OPEN
BLANK    EQU   X'40'                    BLANK                    S22024
THIRTY2  EQU   32                       OFFSET IN DEB TO DCB ADDR
TWENFOUR EQU   24                       OFFSET IN WKAREA TO SAVE LCB
HSEVEN   DC    H'7'                     SEVEN                    S22024
LCB      EQU   X'E1'                    INDICATES A REDISPATCHED CMD
NOBRUP   EQU   X'15'                    OFFSET IN PHASE 1 CODE TO
*                                      SWITCHES
ON       EQU   X'02'                    BRING-UP NOT REQUESTED
MSG164   EQU   164                      CORE NOT AVAIL FOR 3705 IPL CMD
MSG18    EQU   18                       INVALID CMD
TICLAST  DC    X'08000002'              TIC AND LAST UNIT INDICATORS
MSG163   EQU   163                      LOAD MOD NAME NOT FOUND IN
*                                       THE 3705 DCB
MSG165   EQU   165                      BLDL FAILED
TWENTY   EQU   20                       BIT IN DCB FOR BRING-UP
IOBOFF   DC    H'32'                    OFFSET IN LCB TO IOB
ADDROPEN EQU   X'48'                    OFFSET TO ADDRESS OF OPEN CODE
         SPACE 5
*        IPL   EQU
*      INPROG    EQU
          SPACE 5
FOXZERO  EQU   X'F0'                    MASK TO INDICATE HARDWARE ADDR
MSG17    EQU   17                       DCB NOT OPENED ERROR MSG
RETRY    EQU   X'05'                    NUMBER OF TIMES TO RETRY OPER.
EIGHTEEN EQU   X'F8'                    MASK FOR 1, BIT MACHINE
REGSAVE  EQU   96                       OFFSET IN L-WK OF REG SAVEAREA
CHAIN    EQU   X'40'                    CCW CHAINING FLAG
UCBNAME  EQU   13                       UCB NAME FIELD DISP
FOURTY   EQU   40                       OFFSET IN TCB TO JOBLIB DCB
FOURTY4  EQU   X'44'                    OFFSET IN DEB TO UCB ADDR
EIGHT4   EQU   84                       FIND RTN
XCTLINPT DC    C'ME'                    MOD ID FOR IFLINPUT RTN
BRNGUP   EQU   X'20'                    BRING UP SPECIFIED
SETBRGUP EQU   X'05'                    INDICATES BRING-UP SPECIFIED
TWENTY8  EQU   28                       UCBNAME SAVE OFFSET IN L-WKAR
APPVCTAB EQU   X'14'                    OFFSET IN CVT TO IOS APPEN.
*                                       VECTOR TABLE
LCBSAVE  EQU   88                       SAVEAREA IN L-WKAREA FOR LCB
RNAPPTAB EQU   28                       OFFSET IN DEB TO 3705 APPEN
*                                       VECTOR TABLE
LINK     EQU   X'01'                    MASK TO CHK LINKLIB FOR
*                                       RESIDENCY
STEPJOB  EQU   X'02'                    MASK TO CHK STEP OR JOB LIB FOR
*                                       RESIDENCY
THIRTY   EQU   X'30'                    DCBOFLAGS
THIRTONE EQU   X'31'                    NO SYS ERROR RTN FLAG
REALOPCE EQU   X'08'                    INDICATES OPCE AND NOT LCB
NOTFIRST EQU   X'80'                    IF SET IN ELEMENT POINTED TO BY
*                                       OPCCOPCE NOT 1ST TIME ENTERED
RDISP    EQU   X'08'                    CMD WAS REDISPATCHED
XCTLMI   DC    C'MI'                    ID OF COMMON EXIT RTNE @Y17XARS
MODID    DC    C'MD'                    INDICATES AN IPL CMD
XCTL00ID DC    C'00'                    ID OF IGC0010D ROUTINE
TWENSIX  EQU   26                       NUMBER OF BYTES TO MOVE
SIXTEEN  EQU   16                       NUM OF BYTES TO MOVE
TXRDCB   EQU   X'04'                    3705 DCB MASK
MSG173   EQU   173                      ERROR MSG NUMBER FOR 3705
CESDOFF  EQU   40                       OFFSET IN L-WK TO CESD TABLE
OPCE2ND  EQU   X'C0'                    OPCE PASSED FROM IFLWRITE RTN
DATAOFF  EQU   184                      OFFSET TO C-WKAREA
MSG168   EQU   168                      MSG NO 168               S22024
         SPACE
XCTLWRTE DC    C'MF'                    ID FOR IFLWRITE MODULE
PATCH    B     PATCH                    START OF PATCH AREA
         ORG   IGCMD10D+X'3FF'          SET MODULE LENGTH TO X'400'
         DC    X'00'
         EJECT
DATADS   DSECT
****************************************************************
*        REGISTER AND NUMBER EQUATES
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
RA       EQU   10
RB       EQU   11
RC       EQU   12
RD       EQU   13
RE       EQU   14
RF       EQU   15
         SPACE
         SPACE
ZERO     EQU   0
ONE      EQU   1
TWO      EQU   2
THREE    EQU   3
FOUR     EQU   4
FIVE     EQU   5
SIX      EQU   6
SEVEN    EQU   7
EIGHT    EQU   8
NINE     EQU   9
TEN      EQU   10
ELEVEN   EQU   11
TWELVE   EQU   12
         SPACE 2
****************************************************************
**    MISCELLANEOUS  EQUATES  AND CONSTANTS                   **
         SPACE
BIT10    EQU   X'10'                    TEST FOR OPEN IN DCB
IFLGSD   EQU   49                       DCB FLAGS DISPLACEMENT
NOERRTN  EQU   X'0C'                    INDICATE NO SYS ERR RTNS
UCBCA    EQU   19                       UCB DISP OF CA INDICATORS
MINICA   EQU   X'15'                    MIMI CA INDIC IN UCB
         SPACE
ABORTVAL EQU   12                       ABORT RETURN CODE THRESHOLD
RETCODE  DC    H'0'                RETURN CODE.
MAXRC    DC    H'0'          MAXIMUM RETURN CODE SET.
RNDDN    DC    C'        '    RN DDNAME
RNADR    DC    C'   '              UNIT ADDRESS AREA.
ENTRYPT  DC    F'0'           ENTRY POINT FOR LOAD.
ARADDR   EQU   12             DECB AREA ADDR. DISP.
RNSWITCH EQU   X'14'         DISP OF SWITCH IN DIAG PHASE 1.
SIXTBIT  EQU   X'01'         SIXTEEN BIT INDICATOR.
ALIASIND EQU   X'80'          THIS IS AN ALIAS-INDICATOR.  BLDL
RENREUS  EQU   X'C0'          REENTRANT & REUSABLE         BLDL
ABORT    EQU   X'03'          ABORT SENSE INDICATORS.
         DS    0H
TXTLNGTH DC    H'0'           LENGTH OF TEXT RECORD
         DS    0F
TOTLNGTW DC    AL1(0)         TOTAL LENGTH OF DATA
TOTLNGTH DC    AL3(0)         TRANSFERRED
         DS    0F
TXTADDRW DC    AL1(0)         ADDRESS (REL TO ZERO) OF
TXTADDR  DC    AL3(0)         THIS TEXT RECORD.
         SPACE
N256HW   DC    H'256'         MAXIMUM MVC COUNT
INBUFSZ  DC    H'1024'        SIZE OF INPUT BUFFER
OUTBFSZ  DC    H'512'         SIZE OF OUTPUT BUFFER.
DOUBLE   DC    D'0'           DOUBLE WORD WORK AREA
LINECNT  DC    H'0'           LINE COUNT FOR OUTPUT
MAXLIN   DC    H'52'          MAXIMUM LINES PER PAGE.
N4H      DC    H'4'           FOUR CONSTANT
         SPACE
**   BLDL  LIST  FOR  LOAD  MODULE  DATA  SET                 **
         DS    0H
BLDLIST  DC    H'1'           ONE ENTRY IN THIS LIST.
         DC    H'58'          EACH ENTRY IS 58 BYTES LONG
MEMBER   EQU   *              MEMBER NAME PLACE HERE.
         DS    CL8                      CHAR 8                   S22024
RALTTRK  DC    4X'00'              USED IN FIND
*
Z        DC    X'00'          FLAG BYTE
C        DC    X'00'          FLAG BYTE
         ORG   MEMBER+22
ATTRIB   EQU   *              ATTRIBUTE FIELD
         ORG   MEMBER+29
EPAMEM   EQU   *              ENTRY PT IF MEMBER DIR.ENTRY.
         ORG   MEMBER+43
EPAALS   EQU   *              ENTRY PT IF ALIAS DIR.ENTRY.
         ORG   MEMBER+58
         SPACE
MAXIVT   EQU   *
         DC    V(CXWMAXI1)         ADDRESS OF MAXI PHASE 1
         DC    V(CXWMAXI2)              ADDRESS OF MAXI PHASE 2
MINIVT   EQU   *
         SPACE
         DC    V(CXWMINI1)         ADDRESS OF MINI PHASE 1
         DC    V(CXWMINI2)         ADDRESS OF MINI PHASE 2..
         SPACE
PHASPTR  DC    A(MAXIVT)           PHASE VECTOR TABLE PTR..
PHASCNT  DC    H'8'                NUMBER OF PHASES TIMES FOUR..
MAXIPH1  DC    V(CXWMAXI1)         ADDRESS OF MAXI PHASE 1.
MINIPH1  DC    V(CXWMINI1)         ADDRESS OF MINI PHASE 1.
MAXIBUPH DC    CL8'IFL3705B'       NAME OF DIAG MAXI PHASE 1.
MINIBUPH DC    CL8'IFL3705A'       NAME OF DIAG MINI PHASE 1.
         SPACE
*                               *                              *
****************************************************************
***      SWITCHES AND TEST EQUATES                            **
         SPACE
SWITCH   EQU   *         * SWITCH BYTE *
         SPACE
SW1      EQU   *          INDICATES ONE PARM PROCESSED
ON1      EQU   X'80'          TURN ON SW1
OFF1     EQU   X'7F'          TURN OFF SW1
         SPACE
TXTSW    EQU   *            INDICATES TEXT RECORD IN NEXT BUFFER
ONTXT    EQU   X'40'          TURN ON TXTSW
OFFTXT   EQU   X'BF'          TURN OFF TXTSW
         SPACE
LASTSW   EQU   *            INDICATES LAST TEXT RECORD
ONLAST   EQU   X'20'          TURN ON LASTSW
         SPACE
ENDIN    EQU   *            INDICATES INPUT BUFFER IS EMPTY.
ONEND    EQU   X'10'          TURN ON ENDIN.
OFFEND   EQU   X'EF'          TURN OFF ENDIN.
         SPACE
IPLSW    EQU   *            INDICATES BOOTSTRAP IS TO BE SENT
ONIPL    EQU   X'08'          TURN ON IPLSW.
OFFIPL   EQU   X'F7'          TURN OFF IPLSW.
         SPACE
IOPEND   EQU   *            INDICATES AN EXCP IS OUTSTANDING.
ONIO     EQU   X'04'          TURN ON IOPEND
OFFIO    EQU   X'FB'          TURN OFF IOPEND
         SPACE
ERRENT   EQU   *            INDICATES ERROR RTN HAS BEEN ENTERED
ONENT    EQU   X'02'          TURN ON ERRENT.
         SPACE
BUFFERSW EQU   *                 INDICATES PRIMING OF BUFFERS..
BUFSW    EQU   X'01'                    HEX 01                   S22024
         SPACE
ONES     EQU   X'FF'          USED IN SWITCH MANIPULATIONS.
         SPACE
         DC    AL1(0)      * ACTUAL SWITCH BYTE *
         SPACE 2
SWITCH2  EQU   *
CASW     EQU   *
         SPACE
LOADFLG  EQU   X'80'          LOADMOD KEYWORD PROCESSED.
UNITFLG  EQU   X'40'          UNIT    KEYWORD PROCESSED.
DIAGFLG  EQU   X'20'          DIAG    KEYWORD PROCESSED.            BUP
DIAGOK   EQU   X'10'          BRING-UP COMPLETED INDICATOR.         BUP
ONCA     EQU   X'01'               ON INDICATING MINI CA.
COMPLETE EQU   X'02'          LAST WRITE JUST ISSUED.
NXTPHS   EQU   X'04'          NEXT PHASE REQUEST.
         SPACE
         DC    AL1(0)          * ACTUAL SWITCH2 BYTE *
         SPACE
DIAGIND  DC    C'Y6'              INITIAL-TEST INDICATOR.
DIAGDEF  DC    C'Y6'               DEFAULT DIAG VALUE.
CHARN    EQU   C'N'           NO BRING-UP INDICATOR.                BUP
         SPACE 2
WARN     DC    AL1(0)        BYTE USED FOR WARNING INDICATORS
RNFULL   EQU   X'80'            RN IS FULL WARNING
         SPACE
GOODCODE EQU   X'7F'          NORMAL ECB COMPLETION CODE.
PERMERR  EQU   X'41'          PERMANENT ERROR ECB CODE.
         SPACE 2
         SPACE
RNEND    EQU   X'C0'        * TEMPORARY - WILL NEED TO BE *
*                               MORE EXTENSIVE.
****************************************************************
         SPACE
**********  CONTROL BLOCKS FOR ASSOCIATED DATA SETS  ***********
         SPACE
ECBRN    DC    F'0'       **  ECB FOR EXCP'S TO 27RN.
         SPACE
IOBRN    DS    0F         **  IOB FOR EXCP'S TO 27RN.
         DC    X'42'         -COMMAND CHAINING & NOT RELATED.
         DC    AL1(0)        -FLAGS
IOBSENSE DC    AL2(0)        -SENSE BYTES
         DC    AL1(0)        -ECB CODE
         DC    AL3(0)                   ECB ADDRESS
         DC    AL1(0)        -FLAGS
         DC    AL3(0)        -CSW COMMAND ADDRESS.
IOBCSWS  DC    AL2(0)        -CSW STATUS BYTES
         EJECT
IOBCSWC  DC    AL2(0)        -CSW BYTE COUNT.
         DC    AL1(0)        -SIO CODE
         DC    AL3(0)                   CHANNEL PROGRAM ADDRESS
         DC    AL1(0)        -RESERVED
         DC    AL3(0)                   DCB ADDRESS
         DC    AL1(0)        -REPOSITION MODIFIER
         DC    AL3(0)        -RESTART ADDRESS.
         DC    AL2(0)        -BLOCK COUNT INCREMENT N/A.
         DC    AL2(0)        -ERROR COUNTS.
         DC    AL1(0)        -EXTENT 0 OF DEB.
         DC    AL3(0)        -BBCCHHR
         DC    AL4(0)        -(SAME)
* END OF IOB DEFINITION  ***************************************
         EJECT
         TAVTD
         EJECT
         TCCWD                                                 @Y17XARS
         EJECT                                                 @Y17XARS
         TOPCAVTD
         EJECT
         TOPCED
         EJECT
         TLCBD
         EJECT
         TCOMD
         TCOMIPLD
         EJECT
         TNCPID                                                @Y17XARS
         EJECT
         TOCTRD                                                @Y17XARS
         EJECT
         END
