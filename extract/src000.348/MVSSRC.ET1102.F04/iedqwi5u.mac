         TITLE 'IEDQWI5U - CONFIGURATOR SUB MODULE 2U'
IEDQWI5U CSECT
*CHANGE ACTIVITY AS FOLLOWS:
**************************************************************** S22024
*                                                              * S22024
* MODULE NAME:                                                 * S22024
*                                                              * S22024
*    IEDQWI5U (TCAM,TOTE)                                      @Y17XAUU
*                                                              * S22024
* DESCRIPTIVE NAME:                                            * S22024
*                                                              * S22024
*    TOTE CONFIGURATOR SUB-MODULE                              * S22024
*                                                              * S22024
* COPYRIGHT:                                                   * S22024
*                                                              * S22024
*    'NONE'                                                    * S22024
*                                                              *
* STATUS:                                                      * S21903
*                                                              *
*    VERSION 10.0                                              @Y17XAUU
*                                                              *
* FUNCTION:                                                    * S21903
*                                                              *
*    THIS MODULE FIRST DETERMINES WHETHER THE ADDRESS IS FOR   * S21903
*    COMMUNICATION OR GRAPHIC DEVICES. NEXT THE MODULE GETS    * S21903
*    THE TCU ADAPTER TYPE. THIS INFORMATION IS THEN PLACED     * S21903
*    INTO THE CDS WORKAREA.                                    * S21903
*                                                              *
* ENTRY POINTS:                                                * S21903
*                                                              *
*         IEDQWI5U - RECEIVES CONTROL FROM IEDQWIA VIA  A      * S21903
*                    'LOAD' WHEN AN 'ADD' FUNCTION IS          * S21903
*                    REQUESTED;                                * S21903
*                                                              *
*                    RECEIVES CONTROL FROM IEDQWID VIA A       * S22024
*                    'LOAD' WHEN A 'CHANGE' FUNCTION IS        * S21903
*                    REQUESTED;                                * S21903
*                                                              *
*                    RECEIVES CONTROL FROM IEDQWI9 WHEN AN     * S21903
*                    'ADD TERMINAL' FUNCTION IS REQUESTED      * S21903
*                                                              *
* INPUT:                                                       *
*                                                              *
*    THE FOLLOWING REGISTER(S) IS(ARE) USED FOR INPUT:         * S21903
*                                                              *
*    02-OLTCB POINTER                                          * S21903
*    13-SAVE AREA ADDRESS                                      * S21903
*    14-RETURN ADDRESS                                         * S21903
*    15-ENTRY POINT ADDRESS                                    * S21903
*                                                              *
* OUTPUT:                                                      * S21903
*                                                              *
*    THE FOLLOWING REGISTER(S) IS(ARE) USED FOR OUTPUT:        * S21903
*                                                              *
*    01-PARAMETER LIST ADDRESS                                 * S21903
*                                                              *
* EXTERNAL REFERENCES:                                         * S21903
*         IEDQWQ - CECOM SERVICE MODULE                        * S21903
*         IEDQWR - PLINK SERVICE MODULE                        * S21903
*                                                              *
* EXIT POINTS:                                                 * S21903
*                                                              *
*    BR AT 'ADAPTER' TO IEDQWIA                                * S21903
*    BR AT 'ADAPTER' TO IEDQWI9                                * S21903
*                                                              *
* TABLES/WORKAREAS:                                            * S21903
*                                                              *
*    CDSWORK-WORK AREA USED TO PASS INFO BETWEEN MODULES       * S21903
*            AND CREATE CDS RECORDS                            * S21903
*                                                              *
* ATTRIBUTES:                                                  * S21903
*                                                              *
*    ENABLED, PROBLEM PROGRAM MODE, TRANSIENT                  * S21903
*                                                              *
* CHARACTER CODE DEPENDENCIES:                                 * S21903
*                                                              *
*    NONE                                                      * S21903
*                                                              *
* NOTES:                                                       * S21903
*                                                              *
*    NONE                                                      * S21903
*                                                              *
****************************************************************
         EJECT
**************************************************************** S99528
*                                                              *
*              EQUATES
*
**************************************************************** S99528
PARMREG0 EQU   0                       PARAMETER REG 0           S99528
PARMREG1 EQU   1                       PARAMETER REG 1           S99528
R2       EQU   2                       OLTCB POINTER             S99528
R12      EQU   12                      REGISTER 12               S99528
R14      EQU   14                      RETURN ADDR REG           S99528
WORKREG0 EQU   0                       WORK REGISTER             S21903
WORKREG1 EQU   1                       WORK REGISTER             S21903
WORKREG2 EQU   2                       WORK REGISTER             S21903
WORKREG3 EQU   3                       WORK REGISTER             S21903
WORKREG4 EQU   4                       WORK REGISTER             S99528
UCBCHADD EQU   4                       OFFSET TO UCB ADDR        S99528
UCBREG   EQU   5                       UCB POINTER               S99528
WORKREG6 EQU   6                       WORK REGISTER             S21903
WORKREG7 EQU   7                       WORK REGISTER             S21903
RTRM     EQU   8                       TTE BASE REG.           @YM07722
ERRORCNT EQU   7                       ERROR COUNT               S99528
BASEREG  EQU   3                       BASE REGISTER             S99528
PREVSAVE EQU   4                       OFFSET TO PREV SAVE       S99528
OWN      EQU   4                       OFFSET TO OWN SAVE        S99528
CALLERS  EQU   8                       OFFSET TO CALLERS SAVE    S99528
RETURN   EQU   9                       RETURN ADDR REG           S21903
SAVEAREA EQU   13                      POINTER TO SAVE AREA      S99528
SAVEADDR EQU   15                      SAVE AREA ADDR            S99528
RETCODE  EQU   15                      RETURN CODE               S99528
ENTRYPT  EQU   15                      ENTRY PT ADDR             S99528
EXITFLG  EQU   X'01'                   EXIT FLAG                 S99528
ONECHAR  EQU   1                       MOVE CONST                S99528
HIORDBIT EQU   X'03'                   HIGH ORD BIT MASK         S99528
LOWORBIT EQU   X'F0'                   LOW ORD BIT MASK          S21903
AR145CNV EQU   X'04'                   CONVERT TO AR145 ARCH     S99528
CVT      EQU   16                      CVT OFFSET                S21903
UCBPTR   EQU   X'28'                   UCB POINTER               S21903
UCBDVCLS EQU   18                      PTR TO DEV CLASS IN UCB   S21903
WORKREG5 EQU   5                       WORK REGISTER             S21903
GRAPHIC  EQU   X'10'                   GRAPHIC DEV FLAG          S21903
COMMEQ   EQU   X'40'                   COMM EQUIP FLAG           S21903
T2848    EQU   X'06'                   2848 DEV FLAG             S21903
T2701    EQU   X'02'                   2701 TCU                  S21903
T2702    EQU   X'01'                   2702 TCU                  S21903
T2703    EQU   X'03'                   2703 TCU                  S21903
T7770    EQU   X'07'                   7770                      S21903
UCBADTYP EQU   19                      OFFSET TO ADAP TYPE       S21903
UCBOPT   EQU   17                      OFFSET TO OPTION BYTE     S21903
XCU7770  EQU   X'00'                   7770 TCU                  S99528
D1       EQU   1                  DISPLACEMENT 1                 S22024
D4       EQU   4                  DISPLACEMENT 4                 S22024
X0F      EQU   X'0F'              USED TO CLEAR BITS             S22024
T3705    EQU   X'05'              UCB SETTING FOR 3705           S22024
RNLU     EQU   X'6F'              SNA LU INDICATOR             @YM07722
RNBISYN  EQU   X'9F'                   3705 BI SYNCH TERMINAL    S22024
*                                        INDICATOR               S22024
RNSS     EQU   X'0F'                   3705 START/STOP TERMINAL  S22024
*                                        INDICATOR               S22024
RNLINE   EQU   X'FF'               3704/3705 COMMUNICATION       S22024
*                                  LINE INDICATOR                S22024
ANSBISYN EQU   C'B'                ANSWER FOR BI SYNCH DEVICE    S22024
ANSSS    EQU   C'S'                ANSWER FOR START STOP DEVICE  S22024
ANSLINE  EQU   C'L'                ANSWER FOR COMMUNICATION LINE S22024
TCUCLASS EQU   X'40'               CONTROL UNIT CLASS IN CDS     S22024
         EJECT
*
*              INITIALIZATION
*
         SAVE  (14,12)                 SAVE CALLER'S REGISTERS   S22024
         LR    BASEREG,ENTRYPT         LOAD BASE REGISTERS       S99528
         USING IEDQWI5U,BASEREG        ESTAB BASE REG            S99528
         USING TOTOLTCB,R2             ESTAB POINTER TO OLTCB    S99528
IEDQWI5U IEDHJN QI5U,HJN               SET DATE AND MODULE NAME  S22024
         LA    SAVEADDR,QWIASAVE       LOAD ADDR.OF OWN SAVE ARE S99528
         ST    SAVEADDR,CALLERS(SAVEAREA)   STORE ADDR.IN CALLER S99528
         ST    SAVEAREA,OWN(SAVEADDR)       STORE CALLERS IN WIA S99528
         LR    SAVEAREA,SAVEADDR       GET SAVE ADDR             S21903
         SPACE 2
         EJECT
***********************************************************************
*                                                                     *
* ROUTINE TO FIND AND VERIFY TCU.                                     *
*                                                                     *
***********************************************************************
         SPACE
TCULOOK  EQU   *
         CLI   UCBDVCLS(UCBREG),GRAPHIC      GRAPHIC DEVICE?
         BE    TCU2848                 YES                       S21903
         CLI   UCBDVCLS(UCBREG),COMMEQ       COMMUNICATION EQUIPMENT?
         BNE   TCUERROR                 WRONG CLASS              S99528
TCULOOK1 EQU   *
         MVC   SADWORK(D1),UCBADTYP(UCBREG)  MOVE TYPE FIELD     S22024
         NI    SADWORK,X0F        CLEAR HIGH ORDER NIBBLE        S22024
         CLI   SADWORK,T3705      3705 CONTROL UNIT?             S22024
         BE    TCU3705            YES, BRANCH                    S22024
         TM    UCBADTYP(UCBREG),T2703        TCU 2703?
         BO    TCU2703                 YES                       S21903
         TM    UCBADTYP(UCBREG),T2701   TCU 2701?
         BO    TCU2701                 YES                       S21903
         TM    UCBADTYP(UCBREG),T2702        TCU 2702?
         BO    TCU2702                 YES                       S21903
         CLI   UCBADTYP(UCBREG),XCU7770     TCU 7770?            S99528
         BE    TCU7770                      YES                  S99528
*
TCUERROR EQU   *                       TCU ERROR                 S99528
         IEDQMSG MSGID=143,FUNCT=CEC,INBUF=CEMSG,INCNT=3,LINK=YES 99528
         MVI   CDSRETFL,EXITFLG        MOVE EXIT FLG TO RETFLG   S99528
         B     ADAPTER                 BRANCH TO ADAPTER         S99528
         SPACE
TCU2701  OI    FLAGBYTE,T2701          TURN ON 2701 FLAG         S21903
         MVC   CDSTCUTY,TCU2701C       PLUG TCU IN CDS           S22024
         BAL   RETURN,ADAPSRCH         GO GET ADAPTER TYPE       S21903
         B     ADAPTER                 TO RESTORE REGISTERS      S21903
         SPACE
TCU2702  OI    FLAGBYTE,T2702          TURN ON 2702 FLAG         S21903
         MVC   CDSTCUTY,TCU2702C       PLUG TCU IN CDS           S22024
         BAL   RETURN,ADAPSRCH         GO GET ADAPTER TYPE       S21903
         MVC   SADWORK,UCBOPT(UCBREG)  MOVE OPTION BYTE TO WORK AREA
         NI    CDSCONN,HIORDBIT        STRIP UNWANTED BITS       S99528
         OI    CDSCONN,AR145CNV        ADD 4 TO ADJUST VALUE     S99528
         B     ADAPTER                 TO RESTORE REGISTERS      S21903
         SPACE
TCU2703  OI    FLAGBYTE,T2703          TURN ON 2703 FLAG         S21903
         MVC   CDSTCUTY,TCU2703C       PLUG TCU IN CDS           S22024
         BAL   RETURN,ADAPSRCH         GO GET ADAPTER TYPE       S21903
         B     ADAPTER                 TO RESTORE REGISTERS      S21903
         SPACE
TCU7770  OI    FLAGBYTE,T7770          TURN ON 7770 FLAG.
         MVC   CDSTCUTY,TCU7770C       SET TYPE EQUAL 7770       S22024
         B     ADAPTER                 TO RESTORE REGISTERS      S21903
*
TCU2848  OI    FLAGBYTE,T2848          TURN ON 2848 LOCAL FLAG   S21903
         MVC   CDSTCUTY,TCU2848C       SET TCU TYPE TO 2848      S22024
         BAL   RETURN,ADAPSRCH         GO GET ADAPTER TYPE       S21903
         B     ADAPTER            GO RESTORE REGISTERS           S22024
         SPACE 1
TCU3705  EQU   *                                                 S22024
         MVC   CDSTCUTY(D4),TCU3705C   PLUG TCU NAME IN CDS      S22024
         BAL   RETURN,ADAPSRC1     GO GET ADAPTER TYPE           S22024
         SPACE 1
ADAPTER  EQU   *                       RESTORE REGISTERS         S21903
         L     SAVEAREA,PREVSAVE(SAVEAREA)     RESTORE           S99528
         LM    R14,R12,R12(SAVEAREA)   RESTORE REGS              S99528
         BR    RETURN                  RETURN TO CALLER          S99528
         EJECT
**************************************************************** S21903
*                                                              * S21903
* ROUTINE SEARCHES FOR ADAPTER TYPE AND PLUGS IT INTO THE      * S21903
* CDS WORK AREA                                                * S21903
*                                                              * S21903
**************************************************************** S21903
         SPACE 2
ADAPSRCH EQU   *                       GET ADAP TYPE             S99528
         MVC   CDSADAP(ONECHAR),UCBADTYP(UCBREG) MOVE ADAPTER TY S99528
         NI    CDSADAP,LOWORBIT        CLEAR UNWANTED BITS       S99528
         BR    RETURN                  RETURN TO CALLER          S99528
*                                                                S22024
ADAPSRC1 DS    0H                  ALIGN FOR INSTRUCTION         S22024
         CLI   CDSCLASS,TCUCLASS   ARE WE CONFIGING A LINE?      S22024
         BE    ADAPSRC4            YES BYPASS TERMINAL QUESTION  S22024
         L     RTRM,TERTABLE       GET TTE ADDRESS             @YM07722
         LA    WORKREG1,TRMPRFSZ   GET TTE PREFIX SIZE         @YM07722
         SLR   RTRM,WORKREG1       POINT TO TTE PREFIX         @YM07722
         USING IEDNTRM,RTRM        SET TTE ADDRESSABILITY      @YM07722
         TM    TRMSTATE,TRMPREF    3705 RESOURCE               @YM07722
         BZ    CONT                NO, CONTINUE                @YM07722
         CLI   TRMTYPE,TRMLUNT     LU ENTRY                    @YM07722
         BE    ADAPSRC6            YES, PROCESS                @YM07722
CONT     DS    0H                  BOUNDARY ALLIGNMENT         @YM07722
*                                  ASK IF IT IS A BI-SYNCH TERM  S22024
         IEDQMSG MSGID=104,FUNCT=REP,INCNT=3                     S22024
         BE    ADAPSRC2            YES CONTINUE                  S22024
         BE    ADAPSRC3            YES CONTINUE                  S22024
         CLI   TOTINBUF,ANSBISYN       IS DEVICE BISYNC          S22024
         BE    ADAPSRC2                YES, CONTINUE             S22024
         CLI   TOTINBUF,ANSSS          IS DEVICE START/STOP      S22024
         BE    ADAPSRC3                YES, CONTINUE             S2024
         CLI   TOTINBUF,ANSLINE        IS DEVICE COMM LINE       S2024
         BE    ADAPSRC5                YES, CONTINUE             S22024
         B     ADAPSRC1            BAD REPLY ASK AGAIN           S22024
*                                                                S22024
ADAPSRC2 EQU   *                                                 S22024
         MVI   CDSADAP,RNBISYN         MOVE IN BI SYNCH INDICATORS22024
         BR    RETURN              RETURN TO CALLER              S22024
ADAPSRC3 EQU   *                                                 S22024
         MVI   CDSADAP,RNSS            MOVE IN START/STOP        S22024
*                                        INDICATOR               S22024
ADAPSRC4 DS    0H                  ALIGN FOR INSTRUCTION         S22024
         BR    RETURN              RETURN TO CALLER              S22024
ADAPSRC5 DS    0H                  ALIGN FOR INSTRUCTION         S22024
         MVI   CDSADAP,RNLINE      MOVE IN 3704/3705 LINE        S22024
*                                  INDICATOR                     S22024
         BR    RETURN              RETURN TO CALLER              S22024
         SPACE
ADAPSRC6 DS    0H                  BOUNDARY ALLIGNMENT         @YM07722
         MVI   CDSADAP,RNLU        SET LU INDICATOR            @YM07722
         BR    RETURN              RETURN TO CALLER            @YM07722
         EJECT
FLAGBYTE DC    X'00'                   FLAG BYTE                 S99528
SADWORK  DS    CL1                     WORK AREA                 S99528
QWIASAVE DS    18F                     SAVE AREA                 S99528
TCU2701C DC    C'2701'                 CDS NAME FOR 2701         S22024
TCU2702C DC    C'2702'                 CDS NAME FOR 2702         S22024
TCU2703C DC    C'2703'                 CDS NAME FOR 2703         S22024
TCU7770C DC    C'7770'                 CDS NAME FOR 7770         S22024
TCU2848C DC    C'2848'                 CDS NAME FOR 2848         S22024
TCU3705C DC    C'3705'                 CDS NAME FOR 3705         S22024
PATCH    DC    100X'0'            MAINTENANCE AREA               S22024
         EJECT
         OLTCB  CONFG=YES              OLTCB DSECT               S99528
CEMSG    EQU   TOTINBUF                EQU TO INPUT MESSAGE      S99528
CEIN     EQU   TOTINBUF                EQU TO INPUT MESSAGE      S99528
TERTABLE EQU   CDS#ID+12          TTE ADDRESS                  @YM07722
         EJECT
         TTRMD                                                 @YM07722
         END
, CONTINUE             S22024 Äf
DEVICE START/STOP      S22024 Äh
, CONTINUE             S2024 Ä
DEVICE COMM LINE       S2024 Äk
, CONTINUE             S22024 Äm
LY ASK AGAIN           S22024 Äo
                       S22024 •
                       S22024 •¶
E IN BI SYNCH INDICATORS22024 •ß
TO CALLER              S22024 •©
                       S22024 •
E IN START/STOP        S22024 •
NDICATOR               S22024 •≤
OR INSTRUCTION         S22024 •

TO CALLER              S22024 •
OR INSTRUCTION         S22024 •
 3704/3705 LINE        S22024 •Ω
OR                     S22024 •
TO CALLER              S22024 •¬
                              •

Y ALLIGNMENT         @YM07722 •
INDICATOR            @YM07722 •
TO CALLER            @YM07722 •√
                              •»
G BYTE                 S99528 ﬁ
K AREA                 S99528 ﬁﬁ
E AREA                 S99528 ê 
 NAME FOR 2701         S22024 ê-
 NAME FOR 2702         S22024 êé
 NAME FOR 2703         S22024 ê›
 NAME FOR 7770         S22024 ê•
 NAME FOR 2848         S22024 êﬂ
 NAME FOR 3705         S22024 ê„
NCE AREA               S22024 êÁ
                              ê
CB DSECT               S99528 ﬂ
 TO INPUT MESSAGE      S99528 ﬂÁ
 TO INPUT MESSAGE      S99528 ‡&
ESS                  @YM07722 ‡Á
                              ‚
                     @YM07722 ‚ 
