QWP      TITLE 'IEDQWP - TOTE DPRINT MODULE (1 OF 3)'
IEDQWP   CSECT
*A000000-999999                                                @Y16X5U0
* CHANGE ACTIVITY AS FOLLOWS:                                    S22024
*A077600,094100,130200                                          SA41596
*A079600,103290,328000,416810,478100,520000                    @Y17XAUU
*C487000-4885000                                               @Y17XAUU
IEDQW39  EQU   *                                                 S99528
         ENTRY IEDQW39
         USING IEDQWP,R15
         SPACE
IEDQWP   IEDHJN STARP,HJN         MODULE ID                      S99528
         SPACE 4
**************************************************************** S99528
*
* MODULE NAME = IEDQWP (TCAM,TOTE)                             @Y17XAUU
*                                                              * S22024
* DESCRIPTIVE NAME = TOTE DPRINT MODULE 1                      * S22024
*                                                              * S22024
* COPYRIGHT = NONE                                             * S22024
*                                                              * S22024
* STATUS --                                                    * S99528
*    VERSION 10.0                                              @Y17XAUU
*                                                              * S99528
* FUNCTIONS --                                                 * S99528
*    THE PURPOSE OF THIS ROUTINE IS TO SERVICE THE DPRINT MACRO* S99528
*    BY FORMATTING THE OUTPUT MESSAGES.                        @Y17XAUU
*                                                              * S99528
*    THE CALLING ROUTINE'S DPRINT PARAMETER LIST IS EXAMINED.  * S99528
*    IF A HEADER ANDOR DESCRIPTION LINES ARE REQUIRED, THE     * S99528
*    LINES ARE FORMATTED ONE AT A TIME AND IEDQWO (ACCESS      * S99528
*    MANAGER) IS CALLED TO ROUTE THE FORMATTED LINES TO THE    * S99528
*    PROPER OUTPUT DEVICE.                                     * S99528
*                                                              * S99528
*    IF RESULTS ARE REQUIRED, THE CAW, CCW, AND CC LINES ARE   * S99528
*    FORMATTED AND PASSED TO IEDQWO. CONTROL IS THEN           * S99528
*    TRANSFERRED TO IEDQWP1 (DPRINT SERVICE MODULE 1) TO       * S99528
*    CONTINUE PROCESSING THE DPRINT MACRO.                     * S99528
*                                                              * S99528
*    IF NO RESULTS LINES ARE REQUIRED, THE COMMENT LINES ARE   * S99528
*    FORMATTED (IF REQUIRED) AND PASSED TO IEDQWO. CONTROL IS  * S99528
*    THEN RETURNED TO THE CALLING ROUTINE.                     * S99528
*                                                              * S99528
*    AT THE COMPLETION OF THIS ROUTINE REGISTER 15 WILL        * S99528
*    CONTAIN A RETURN CODE OF 0.                               * S99528
*                                                              * S99528
* ENTRY POINTS --                                              * S99528
*    IEDQWP - CALLED WHEN A DPRINT MACRO IS ISSUED BY THE OLT. * S99528
*                                                              @Y17XAUU
* PURPOSE -- SEE FUNCTION                                      @Y17XAUU
*                                                              @Y17XAUU
* LINKAGE -- BALR R14,R15 FROM IEDQWA                          @Y17XAUU
*                                                              @Y17XAUU
* MODULE TYPE -- PROCEDURE                                     @Y17XAUU
*                                                              @Y17XAUU
* MODULE SIZE -- 2K MAXIMUM                                    @Y17XAUU
*                                                              @Y17XAUU
* PROCESSOR -- ASSEMBLER                                       @Y17XAUU
*                                                              @Y17XAUU
* MACROS -- IEDHJN,SAVE                                        @Y17XAUU
*                                                              * S99528
* INPUT --                                                     * S99528
*    REGISTERS 01,02,03,13,14,15 CONTAIN THE FOLLOWING VALUES: * S99528
*                                                              * S99528
*    01 -DPRINT PARAMETER LIST ADDR                            * S99528
*    02 - OLTCB POINTER                                        * S99528
*    03 - SECTION PREFACE ADDRESS                              * S99528
*    13 - SAVE AREA ADDRESS                                    * S99528
*    14 - RETURN ADDRESS                                       * S99528
*    15 - ENTRY POINT ADDRESS                                  * S99528
*                                                              * S99528
* OUTPUT --                                                    * S99528
*    NONE                                                      * S99528
*                                                              * S99528
* EXTERNAL REFERENCES --                                       * S99528
*    IEDQWO - ACCESS MANAGER - TO ROUTE OUTPUT MESSAGE LINE    * S99528
*                                                              * S99528
* EXITS,NORMAL --                                              * S99528
*    BR   TO IEDQWP1 OR RETURN TO CALLER                       * S99528
*                                                              * S99528
* EXITS,ERROR --                                               * S99528
*    NONE                                                      * S99528
*                                                              * S99528
* TABLES/WORK AREAS --                                         * S99528
*    OLTCB, SECTION PREFACE                                    * S99528
*                                                              * S99528
* ATTRIBUTES --                                                * S99528
*    ENABLED,PROBLEM PROGRAM MODE, RESIDENT, REENTRANT         @Y17XAUU
*                                                              * S99528
* NOTES -- SEE BELOW                                           @Y17XAUU
*                                                              @Y17XAUU
*  DEPENDENCIES --                                             @Y17XAUU
*                                                              @Y17XAUU
*    EBCDIC CHARACTER CODE                                     @Y17XAUU
*                                                              @Y17XAUU
*    MAX NO. OF BYTES IN A DESCRIPTION LINE OR COMMENT         * S99528
*    LINE(MSGE) IS 80. LINES OF GREATER LENGTH ARE TRUNCATED.  * S99528
*                                                              * S99528
*    MAX NO. OF SENSE BYTES IS 20. IFNO. OF BYTES IS           * S99528
*    GREATER, PRINT LINE IS TRUNCATED.                         * S99528
*                                                              @Y17XAUU
*  RESTRICTIONS -- NONE                                        @Y17XAUU
*                                                              @Y17XAUU
*  REGISTERS CONVENTION -- SEE REGISTERS ASSIGNMENT            @Y17XAUU
*                                                              @Y17XAUU
*  PATCH LABEL -- PATCH                                        @Y17XAUU
*                                                              * S99528
**************************************************************** S99528
         EJECT                                                 * S99528
*                                                              * S99528
*        EQUATES                                               * S99528
*                                                              * S99528
R0       EQU   0                  REG 0                        * S99528
R1       EQU   1                  REG 1                        * S99528
R2       EQU   2                  REG 2                        * S99528
R3       EQU   3                  REG 3                        * S99528
R4       EQU   4                  REG 4                        * S99528
R5       EQU   5                  REG 5                        * S99528
R6       EQU   6                  REG 6                        * S99528
R7       EQU   7                  REG 7                        * S99528
R8       EQU   8                  REG 8                        * S99528
R9       EQU   9                  REG 9                        * S99528
R10      EQU   10                 REG 10                       * S99528
R11      EQU   11                 REG 11                       * S99528
R12      EQU   12                 REG 12                       * S99528
R13      EQU   13                 REG 13                       * S99528
R14      EQU   14                 REG 14                       * S99528
R15      EQU   15                 R15                          * S99528
         SPACE
BYPASS   EQU   X'80'              CC PRINTED FLAG                S99528
XC6      EQU   X'C6'              EBCDIC F                       S99528
D0       EQU   0                  DISP OF 0                      S22024
D1       EQU   1                  DISP OF 1                      S22024
D2       EQU   2                  DISP OF 2                      S22024
D8       EQU   8                  DISP OF 8                      S22024
D11      EQU   11                 DISP OF 11                     S22024
D27      EQU   27                 DISP OF 27                     S22024
D37      EQU   37                 DISP OF 37                     S22024
D44      EQU   44                 DISP OF 44                     S22024
D46      EQU   46                 DISP OF 46                     S22024
D4       EQU   4                  DISP OF 4                      S22024
D28      EQU   28                 DISP OF 28                     S22024
PDESC    EQU   X'80'              DESCRIPTION LINES PRESENT FLG  S22024
XFF      EQU   X'FF'              MASK ALL                       S22024
D3       EQU   3                  DISP OF 3                      S22024
D6       EQU   6                  DISPLACEMENT OF 6              S22024
D14      EQU   14                 DISP OF 14                     S22024
D17      EQU   17                 DISP OF 17                     S22024
D23      EQU   23                 DISP OF 23                     S22024
D33      EQU   33                 DISP OF 33                     S22024
D9       EQU   9                  DISP OF 9                     SA41596
DASH     EQU   X'60'              DASH CHAR                     SA41596
DISREG2  EQU   28                 DISPLACEMENT TO REG 2        @Y17XAUU
         EJECT
         DROP  R15
         USING IEDQWP,R4
         USING TOTOLTCB,R2
         USING SPREF1,R3          SECTION PREFACE DSECT PART 1
         USING SPREF2,R9          SECTION PREFACE DSECT PART 2
         SPACE
         SAVE  (14,12)
         LR    R4,R15             SET BASE REG.
         LA    R15,TOTSAVE1       GET ADDR OF OWN SAVE AREA
         ST    R15,8(R13)         PUT ADDR IN CALLER'S SAVE AREA.
         SPACE
         ST    R13,4(R15)         SAVE ADDR OF CALLER'S SAVE AREA.
         LR    R13,R15            SET REG 13 TO MY SAVE
         SPACE
         CLI   1(R1),X'02'        CORRECT MACRO LEVEL ?
         BNE   DPR004             NO.
         L     R3,TOTOLTEN        SECTION PREFACE BASE          SA41596
         MVC   TOTSERV(END-START),START SET UP WORK AREA       @Y17XAUU
         SPACE
         L     R12,4(R1)          GET ADDR OF DPRINT CONTROL WORD.
         SR    R9,R9
         LH    R9,RTNDISP         GET DISPLACEMENT
         AR    R9,R3              ESTABLISH BASE REG
         ST    R9,PREFDS          SAVE ADDRESS
         BAL   R10,DPRCLR         FILL PRINT BUFFER WITH BLANKS.
         SPACE
         LA    R10,EPINDX         EP MASK CHECK FIELD ADDR       S22024
         SR    R14,R14            CLEAR INDEX REG                S22024
         IC    R14,TOTFLG08       GET LEVEL CNTL FLAG (BITS 4-7) S22024
         SLL   R14,D28            CLEAR                          S22024
         SRL   R14,D28            UNWANTED BITS                  S22024
         AR    R10,R14            POINT TO EP LEVEL CTL FLAG     S22024
         MVC   EPSAVE(D1),D0(R10) SAVE IT                        S22024
         SPACE
         TM    0(R12),X'02'       IS THIS AN ERROR PRINT ?
         BZ    DPR005             BRANCH IF NO.
         OI    $ERROPT,$ASTERIK   TURN ON FLAG TO PRINT '*'      S22024
         SPACE
         TM    $ERROPT,$NERRPRT   NO ERROR PRINT                 S22024
         BO    DPR500             BRANCH IF YES - SKIP PRINT - RETURN.
         B     DPR007             BRANCH.
         SPACE
DPR004   EQU   *
         LA    R15,4              SET FUNCT. NOT AVAIL. RET CODE
         STC   R15,TOTRTCOD       SAVE RETURN CODE
         L     R13,4(R13)         GET CALLER'S SAVE AREA ADDR
         L     R14,12(R13)        RESTORE REG 14
         L     R1,24(R13)         RESTORE REG 1
         SPACE
         USING RESPL,R5           SET RESPL ADDRESSABILITY     @Y17XAUU
         L     R5,TOTRESPL        GET RESPL ADDRESS            @Y17XAUU
         L     R15,RESPLM2        GET TRACE ROUTINE ADDRESS    @Y17XAUU
         LM    R2,R12,DISREG2(R13) RESTORE REGISTERS           @Y17XAUU
         BR    R15                EXIT TO TRACE ROUTINE        @Y17XAUU
         DROP  R5                                              @Y17XAUU
         SPACE
DPR005   EQU   *                                                 S22024
         TM    $ERROPT,$NOCNTRL   NO CONTROL PRINT               S22024
         BO    DPR500             BRANCH IF YES - SKIP PRINT - RETURN.
         EJECT
         SPACE 2
***********************************************************************
*        FORMS CONTROL                                                *
***********************************************************************
         SPACE
DPR007   SR    R7,R7              CLEAR R7.
         IC    R7,0(R12)          GET FORMS CONTROL BITS.
         SRL   R7,5               POSITION BITS TO
         SLL   R7,4                LOWER ORDER.
         STC   R7,WRK             STORE IN WORK LOCATION
         OC    TOTFLG08(D1),WRK   FORM CONTROL BITS              S22024
         SPACE 2
***********************************************************************
*        HEADER                                                       *
***********************************************************************
         SPACE
         TM    0(R12),X'10'       PRINT HEADER ?
         BZ    DPR030             BRANCH IF NO.
         SPACE
         TM    0(R12),X'02'       ERROR HEADER ?
         BZ    DPR010             BRANCH IF NO.
         MVI   PB,C'*'            PREFIX '*' ON HEADER.
         SPACE
DPR010   EQU   *
         MVC   PB+1(8),TOTCROLT   GET SECTION ID
         MVI   PB+D9,DASH         INSERT A DASH                 SA41596
         SPACE
         MVC   PB+10(2),LEVEL     GET OLT LEVEL
         SPACE
         MVC   PB+D14(D3),DPRTN   PUT 'RTN' IN PRINT BUFFER      S22024
         L     R9,PREFDS
         SR    R7,R7              CLEAR R7.
         IC    R7,RTN#            GET ROUTINE NUMBER.
         CVD   R7,WRK             CONVERT TO DECIMAL.
         SPACE
         LA    R13,WRK+6          GET ADDR OF RTN NO.
         LA    R14,WRK            GET OUTPUT ADDR.
         LA    R15,2              GET COUNT
         BAL   R10,DPRCNV         CONVERT RTN NO. TO EBCDIC.
         MVC   PB+18(3),WRK       PUT RTN NO. IN PRINT BUFFER.
         LA    R7,$PDEVADR        CDS BASE                       S22024
         USING CDSADR,R7                                         S22024
         TM    CDSFLG,SNPRES+CASNPRES SYMBOLIC NAME PRESENT      S22024
         BO    DPR017             YES                            S22024
         SPACE
         LA    R13,9(R12)         GET ADDR OR DEVICE ADDR. ***********
         CLC   D0(D2,R13),H0      DEVICE ADDR SUPPLIED           S22024
         BNE   DPR015             BRANCH IF YES.
         SPACE
         L     R13,12(R1)         GET TECB ADDR
         LTR   R13,R13            TECB ADDR GIVEN
         BZ    DPR020             NO
         LA    R13,10(R13)        POINT TO DEVICE ADDR
         CLC   D0(D2,R13),H0      DEVICE ADDR SUPPLIED           S22024
         BE    DPR020             BRANCH IF NO.
DPR015   EQU   *
         MVC   PB+D23(D3),DPDEV   PUT 'DEV' IN PRINT BUFFER      S22024
         LA    R14,PB+27          GET OUTPUT ADDR FOR CONVERT.
         LA    R15,2              GET COUNT.
         BAL   R10,DPRCNV         CONVERT DEV ADDR TO EBCDIC.
         B     DPR020             CHECK FOR REFNUM               S22024
         SPACE
DPR017   DS    0H                 BOUNDARY ALLIGNMENT            S22024
         MVC   PB+D23(D3),DPDEV   PUT 'DEV' IN PRINT BUF         S22024
         MVC   PB+D27(D8),CONFGNM SYMBOLIC NAME TO HEADER        S22024
         DROP  R7                                                S22024
         SPACE
DPR020   TM    0(R12),X'01'       IS A REFNUM TO BE PRINTED.
         BZ    DPR025             BRANCH IF NO.
         SPACE
         MVC   PB+D37(D6),DPREF   PUT 'REFNUM' IN PRINT BUFFER   S22024
         L     R9,PREFDS
         MVC   WRK(1),REFNUM      MOVE 1ST 2 DIGITS TO WORK AREA.
         MVC   WRK+1(2),7(R12)    MOVE LAST 3 DIGITS TO WORK AREA
         LA    R13,WRK            GET INPUT ADDR FOR CONVERT.
         LA    R14,WRK+8          GET OUTPUT ADDR FOR CONVERT.
         LA    R15,3              GET COUNT.
         BAL   R10,DPRCNV         CONVERT REFNUM TO EBCDIC.
         MVC   PB+D44(D2),WRK+D8  PUT REFNUM IN PRINT BUFFER     S22024
         MVC   PB+D46(D3),WRK+D11 PUT REFNUM IN PRINT BUFFER     S22024
         DROP  R9
         SPACE
DPR025   BAL   R10,DPRPNT         PRINT THE HEADER.
         SPACE 2
***********************************************************************
*        DESCRIPTION                                                  *
***********************************************************************
         SPACE
DPR030   EQU   *                                                 S22024
         TM    EPSAVE,DESC        PRINT DESCRIPTION LINE         S22024
         BZ    DPR045             BRANCH IF NO                   S22024
         TM    D2(R12),PDESC      PRINT DESCRIPTION LINE         S22024
         BZ    DPR045             BRANCH IF NO.
         SPACE
         SR    R7,R7              CLEAR R7.
         IC    R7,16(R12)         GET NO. OF DESC. LINES.
         LTR   R7,R7              NO. LINES = 0 ?
         BZ    DPR045             BRANCH IF YES
         SPACE
         LA    R11,60(R1)         GET ADDR OF DESC. LINE 1.
         LA    R8,23(R12)         GET ADDR OF COUNT.
         SPACE
DPR035   SR    R6,R6              CLEAR R6.
         IC    R6,0(R8)           GET COUNT.
         LTR   R6,R6              COUNT = 0 ?
         BZ    DPR040             BRANCH YES.
         LH    R5,MAXCNT          GET MAX NO. BYTES ALLOWED.
         CR    R6,R5              COUNT GREATER THAN MAX ?
         BNH   DPR037             BRANCH IF NO.
         LR    R6,R5              SET COUNT V MAX.
         SPACE
DPR037   EQU   *
         SPACE
         L     R5,0(R11)          GET DESC LINE ADDR.
         BCTR  R6,0               REDUCE COUNT FOR EXECUTE OF MVC.
         EX    R6,DPR605          MOVE DESC LINE TO PRINT BUFFER.
         BAL   R10,DPRPNT         PRINT DESCRIPTION LINE.
         SPACE
DPR040   LA    R11,4(R11)         GET ADDR OF DESC LINE 2 ADDR.
         LA    R8,1(R8)           GET ADDR OF COUNT.
         BCT   R7,DPR035          PROCESS LINE 2.
         SPACE 2
***********************************************************************
*        RESULTS                                                      *
***********************************************************************
         SPACE
DPR045   EQU   *
         TM    EPSAVE,RESULTS     TEST RESULTS LINES             S22024
         BZ    DPR085             BRANCH IF NO - EXIT TO QWP1    S22024
         SPACE 2
***********************************************************************
*        CAW  CCW  CC                                                 *
***********************************************************************
         SPACE
DPR050   TM    2(R12),X'40'       PRINT CCW ?
         BO    DPR052             BRANCH IF YES.
         TM    3(R12),X'04'       PRINT CONDITION CODE ?
         BZ    DPR085             BRANCH IF NO.
         LA    R11,1              SET NO CCW'S TO PRINT.
         B     DPR070             GO PRINT CC.
         SPACE
DPR052   DS    0H                 BOUNDARY ALLIGNMENT            S22024
         MVC   PB+D1(D3),DPCAW    PUT 'CAW' IN PRINT BUFFER      S22024
         SR    R11,R11            CLEAR R11.
         IC    R11,11(R12)        GET CCW COUNT.
         LA    R8,1               SET R8 FOR CCW#.
         L     R7,20(R1)
         SR    R5,R5              CLEAR R5.
         IC    R5,12(R12)         GET REL. NO. OF FAILING CCW
         LA    R6,1               SET R6 = 1.
         CR    R6,R5              IS THIS CCW = FAILING CCW ?
         BNE   DPR055             BRANCH IF NO.
         SPACE
         TM    1(R12),X'40'       CCW ERROR ?
         BZ    DPR055             BRANCH IF NO.
         SPACE
         MVI   PB+16,C'*'         PREFIX CCW WITH '*'.
         SPACE
DPR055   EQU   *
         LA    R13,20(R1)         GET ADDR PORTION OF CAW
         LA    R14,PB+6           GET OUTPUT ADR FOR CONVERT.
         LA    R15,4              GET COUNT.
         BAL   R10,DPRCNV         CONVERT CAW TO EBCDIC.
         SPACE
         NI    WRK+20,X'7F'       CLEAR CC PRINTED FLAG.
         SPACE
DPR060   LR    R13,R7             GET ADR OF CCW.
         LA    R14,WRK            GET OUTPUT ADDR FOR CONVERT.
         LA    R15,8              GET COUNT.
         BAL   R10,DPRCNV         CONVERT CCW TO EBCDIC.
         SPACE
         CR    R6,R5              IS THIS CCW = FAILING CCW ?
         BNE   DPR065             BRANCH IF NO.
         SPACE
         MVI   PB+16,C'*'         PREFIX CCW WITH '*'.
         SPACE
DPR065   DS    0H                 BOUNDARY ALLIGNMENT            S22024
         MVC   PB+D17(D3),DPCCW   PUT 'CCW' IN PRINT BUFFER      S22024
         MVC   PB+23(2),WRK       MOVE CMD CODE TO PRINT BUFFER.
         MVC   PB+26(6),WRK+2     MOVE DATA ADR TO PRINT BUFFER.
         MVC   PB+33(2),WRK+8     MOVE FLAGS TO PRINT BUFFER.
         MVC   PB+36(2),WRK+10    MOVE UNUSED TO PRINT BUFFER.
         MVC   PB+39(4),WRK+12    MOVE BYTE CNT TO PRINT BUFFER.
         SPACE
         STC   R8,WRK+30          STORE CURRENT CCW #.
         LA    R13,WRK+30         GET ADR OF INPUT.
         LA    R14,WRK            GET OUTPUT ADR FOR CONVERT.
         LA    R15,1              GET COUNT.
         BAL   R10,DPRCNV         CONVERT CCW # TO EBCDIC.
         SPACE
         MVC   PB+20(1),WRK+1     MOVE CCW # TO PRINT BUFFER.
         CLI   WRK,C'0'           DOES CCW # HAVE LEADING 0 ?
         BE    DPR068             BRANCH IF NO.
         MVC   PB+20(2),WRK       PUT CCW # IN PB WITHOUT LEADING 0.
DPR068   EQU   *
         SPACE
         BAL   R10,DPRPNT         PRINT CCW LINE.
         SPACE
DPR070   EQU   *
         TM    DPFLAG,BYPASS      HAS CC BEEN PRINTED ?
         BO    DPR075             BRANCH IF YES.
         SPACE
         OI    DPFLAG,BYPASS      SET CC PRINTED FLAG
         TM    3(R12),X'04'       PRINT CC ?
         BZ    DPR072             BRANCH IF NO.
         SPACE
         MVC   PB+D1(D2),DPCC     PUT 'CC' IN PRINT BUFFER       S22024
         L     R9,12(R1)          GET TECB ADDR.
         MVC   PB+4(1),8(R9)      PUT CC IN PB.
         CLI   PB+4,X'FF'         CC OF X'FF' ?
         BNE   DPR072             NO - BRANCH
         MVI   PB+4,XC6           PUT F'S IN
         MVI   PB+5,XC6           BUFFER
         SPACE
DPR072   DS    0H                 BOUNDARY ALLIGNMENT            S22024
         C     R11,F1             ANY MORE CCW'S LEFT            S22024
         BNE   DPR075             BRANCH IF YES.
         SPACE
         TM    3(R12),X'04'       PRINT CC ?
         BZ    DPR085             BRANCH IF NO.
         SPACE
         BAL   R10,DPRPNT         PRINT CC.
         B     DPR085             GO PROCESS EXPD CSW.
         SPACE
DPR075   LA    R7,8(R7)           POINT TO NEXT CCW.
         LA    R6,1(R6)           INCREMENT CCW NO.
         LA    R8,1(R8)           INCREMENT EBCDIC CCW NO.
         BCT   R11,DPR060         GO PROCESS NEXT CCW.
         SPACE 2
***********************************************************************
*        EXIT TO IEDQWP1                                              *
***********************************************************************
         SPACE
DPR085   EQU   *
         L     R13,TOTSAVE1+D4    CALLER'S SAVE AREA             S22024
         L     R14,12(R13)        GET RETURN ADR.
         USING RESPL,R5           SET RESPL ADDRESSABILITY     @Y17XAUU
         L     R5,TOTRESPL        GET RESPL ADDRESS            @Y17XAUU
         L     R15,RESPLP1        GET IEDQWP1 ADDRESS          @Y17XAUU
         LM    R2,R12,DISREG2(R13) RESTORE REGISTERS           @Y17XAUU
         BR    R15                EXIT TO IEDQWP1              @Y17XAUU
         DROP  R5                                              @Y17XAUU
         SPACE 2
DPR500   EQU   *
         SPACE 2
***********************************************************************
*        RETURN TO CALLER                                             *
***********************************************************************
         SPACE
         TM    $OLTFLGS,$TRACE    TRACE OPTION SET ?
         BO    DPR530             YES
         SPACE
         L     R13,TOTSAVE1+D4    CALLER'S SAVE AREA             S22024
         LM    R14,R12,12(R13)    RESTORE CALLER'S REGS.
         SR    R15,R15            CLEAR RETURN CODE REG (R15).
         BR    R14                RETURN TO CALLER.
*
*        REQUEST IEDQWM2 TO PROVIDE TRACE FUNCTION
*
DPR530   EQU   *
         SR    R15,R15            CLEAR RETURN CODE REG
         STC   R15,TOTRTCOD       SAVE RETURN CODE
         L     R13,TOTSAVE1+D4    CALLER'S SAVE AREA ADDR        S22024
         L     R1,24(R13)         RESTORE REG 1
         L     R14,12(R13)        RESTORE REG 14
*
         USING RESPL,R5           SET RESPL ADDRESSABILITY     @Y17XAUU
         L     R5,TOTRESPL        GET RESPL ADDRESS            @Y17XAUU
         L     R15,RESPLM2        GET TRACE ROUTINE ADDRESS    @Y17XAUU
         LM    R2,R12,DISREG2(R13) RESTORE REGISTERS           @Y17XAUU
         BR    R15                EXIT TO TRACE ROUTINE        @Y17XAUU
         DROP  R5                                              @Y17XAUU
         EJECT
***********************************************************************
*        DPRCNV - HEX TO EBCDIC CONVERT                               *
*                                                                     *
*              CALL SEQ -  R13 = INPUT ADDRESS                        *
*                          R14 = OUTPUT ADDRESS                       *
*                          R15 = INPUT BYTE COUNT                     *
*                                                                     *
*                          BAL R10,DPRCNV                             *
***********************************************************************
         SPACE
DPRCNV   ST    R5,WRK+120         SAVE WORK REG R5.
         ST    R6,WRK+124         SAVE WORK REG R6.
         SPACE
         LTR   R15,R15            COUNT = 0 ?
         BZ    DPRCNV8            BRANCH IF YES.
         SPACE
         AR    R14,R15         ADD COUNT TO OUTPUT ADDR.
         AR    R14,R15            ADD COUNT AGAIN.
         AR    R13,R15            ADD COUNT TO INPUT ADDR.
         SR    R5,R5              CLEAR R5.
         SR    R6,R6              CLEAR R6.
         SPACE
DPRCNV4  DS    0H                 BOUNDAY ALLIGNMENT             S22024
         S     R14,F2             GET ADDR OF LAST BYTE OF OUTPUT
*                                                                S22024
         BCTR  R13,0              GET ADDR OF LAST BYTE OF INPUT.
         IC    R6,0(R13)          GET HEX BYTE.
         LR    R5,R6              PUT IN R5.
         SRL   R6,4               GET BITS 0-3 IN R6.
         N     R5,F15             GET BITS 4-7 IN R5             S22024
         IC    R6,DPREBC(R6)      CONVERT BITS 0-3 TO EBCDIC CHAR.
         IC    R5,DPREBC(R5)      CONVERT BITS 4-7 TO EBCDIC CHAR.
         STC   R6,0(R14)          STORE 1ST EBCDIC CHAR IN OUTPUT.
         STC   R5,1(R14)          STORE 2ND EBCDIC CHAR IN OUTPUT.
         BCT   R15,DPRCNV4        GET NEXT HEX BYTE.
DPRCNV8  L     R5,WRK+120         RESTORE WORK REG R5.
         L     R6,WRK+124         RESTORE WORK REG R6.
         SPACE
         BR    R10                RETURN TO CALLER.
         SPACE
DPREBC   DC    C'0123456789ABCDEF' HEX TO EBCDIC CONVERT TABLE.
         EJECT
***********************************************************************
*        PRINT LINE AND FILL PRINT BUFFER WITH BLANKS                 *
**********************************************************************
         SPACE
DPRPNT   EQU   *
         SPACE
         LA    R14,L'PB           GET LENGTH OF PRINT BUFFER.
         LA    R15,PB+79
         SPACE
DPRPNT5  EQU   *
         CLI   0(R15),C' '        IS THIS CHAR A BLANK ?
         BNE   DPRPNT10           BRANCH IF NO.
         BCT   R14,DPRPNT8        SUBTRACT 1 FROM COUNT.
         B     DPRPNT10           BUFFER IS ALL BLANKS - SET CNT = 0.
         SPACE
DPRPNT8  EQU   *
         BCT   R15,DPRPNT5        CHECK NEXT CHAR.
DPRPNT10 EQU   *
         STC   R14,TOTOTCNT       OUTPUT COUNT                   S22024
         NI    TOTFLG05,XFF-TOTCECOM-TOTREPLY-TOTEXIOF           S22024
         OI    TOTFLG05,TOTPRINT  SET DPRINT FLAG                S22024
         LA    R13,TOTSAVE1       MY SAVE AREA                   S22024
         USING RESPL,R5           SET RESPL ADDRESSABILITY     @Y17XAUU
         L     R5,TOTRESPL        GET RESPL ADDRESS            @Y17XAUU
         L     R15,RESPLWO        GET I/O ROUTINE ADDRESS      @Y17XAUU
         BALR  R14,R15            CALL I/O ROUTINE             @Y17XAUU
         DROP  R5                                              @Y17XAUU
DPRCLR   MVI   PB,C' '            PUT BLANK IN PRINT BUFFER.
         MVC   PB+1(L'PB-1),PB    FILL BUFFER WITH BLANKS.
         BR    R10                RETURN.
         EJECT
         DS    0F
MAXCNT   DC    H'80'              MAX NO. BYTES IN DESC AND MSGE LINE.
DPR600   MVC   PB(0),0(R11)       MOVE MSGE TO PRINT BUFFER.
DPR605   MVC   PB(0),0(R5)        MOVE DESC LINE TO PRINT BUFFER.
PATCH    DS    CL60               MAINTENANCE AREA.
START    DS    0F *************** START OF DATA TO BE MOVED    @Y17XAUU
QREFDS   DC    F'0'               SAVE AREA                    @Y17XAUU
QPFLAG   DC    X'00'              CONTROL FLAG                 @Y17XAUU
QPSAVE   DC    XL1'0'             EP LEVEL CONTROL FIELD       @Y17XAUU
END      DS    0C *************** END OF DATA TO BE MOVED      @Y17XAUU
         SPACE
EPINDX   DS    0CL4               EP MASK TABLE                  S22024
EP0      DC    X'80'              PRINTER HEADER                 S22024
EP1      DC    X'E0'              PRINT HEADER/DESC/MSG          S22024
EP2      DC    X'90'              PRINT HEADER/RESULTS           S22024
EP3      DC    X'F0'              PRINT ALL                      S22024
*                                                                S22024
HDR      EQU   X'80'              HEADER LINE                    S22024
DESC     EQU   X'40'              DESCRIPTION LINES              S22024
MSG      EQU   X'20'              MESSAGES LINES                 S22024
RESULTS  EQU   X'10'              RESULTS                        S22024
*                                                                S22024
         LTORG
F1       DC    F'1'               VALUE OF 1                     S22024
F2       DC    F'2'               VALUE OF 2                     S22024
F15      DC    F'15'              VALUE OF 15                    S22024
H0       DC    H'0'               VALUE OF 0                     S22024
DPREF    DC    C'REFNUM'          CHAR CONSTANT                  S22024
DPCC     DC    C'CC'              CHAR CONSTANT                  S22024
DPRTN    DC    C'RTN'             CHAR CONSTANT                  S22024
DPDEV    DC    C'DEV'             CHAR CONSTANT                  S22024
DPCAW    DC    C'CAW'             CHAR CONSTANT                  S22024
DPCCW    DC    C'CCW'             CHAR CONSTANT                  S22024
         EJECT
***********************************************************************
*        SECTION PREFACE DUMMY SECTION                                *
***********************************************************************
         SPACE
SPREF1   DSECT
SECTID   DS    CL8                OLT SECTION ID
LEVEL    DS    CL2                OLT LEVEL
RTNDISP  DS    AL2                DISP TO RTN NUM
SPREF2   DSECT
RTN#     DS    XL1                ROUTINE NUMBER
         DS    XL8                FLAGES
RM       DS    XL2                ROUTINE MASK
         DS    XL6                PLINK MANAGEMENT
REFNUM   DS    CL1                REFERENCE NUMBER
         SPACE 4
CDSADR   DSECT                    CDS DSECT                      S22024
CUUADDR  DS    A                  UNIT ADDR                      S22024
         DS    CL5                UNUSED                         S22024
CDSFLG   DS    CL1                FLAG                           S22024
SNPRES   EQU   X'08'              SYNBOLIC NAME PRESENT          S22024
CASNPRES EQU   X'02'              CUSTOMER ASSIGNED NAME         S22024
         DS    CL2                UNUSED                         S22024
CONFGNM  DS    CL8                SYMBOLIC NAME                  S22024
         EJECT
RESPL    RESPL                                                 @Y17XAUU
         EJECT
         OLTCB                                                   S22024
PREFDS   DC    F'0'               SAVE AREA                    @Y17XAUU
DPFLAG   DC    X'00'              CONTROL FLAG                 @Y17XAUU
EPSAVE   DC    X'00'              EP LEVEL CONTROL FIELD       @Y17XAUU
         SPACE 2
WRK      EQU   TOTWKSPC           WORK AREA                      S22024
PB       EQU   TOTOTBUF           OUTPUT BUFFER                  S22024
         END
