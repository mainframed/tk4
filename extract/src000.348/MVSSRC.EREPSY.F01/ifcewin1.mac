***********************************************************************
*
* STATUS:
*   VERSION NONE
*
* FUNCTION:
*   EDIT TABLE IFCEWIN1 PROVIDES DEVICE SUPPORT FOR ALL 3340 DEVICES.
*   IT IS LOADED BY IFCEEDIT. IT PROVIDES THE DIRECTION FOR OUTPUTING
*   THE HEADER INFORMATION AND SENSE BYTES 0 - 7. IT ALSO DETERMINES
*   THE FORMAT OF THE SENSE BYTES AND THE NEXT ROUTINE TO BE LOADED.
*                                                              @D3505GB
* ENTRY POINT:                                                 @D3505GB
*   NOT APPLICABLE                                             @D3505GB
*                                                              @D3505GB
* INPUT:                                                       @D3505GB
*   REGISTER                                                   @D3505GB
*            2  POINTER TO START OF OBR RECORD                 @D3505GB
*            3  POINTER TO CURRENT POSITION IN PRINT BUFFER    @D3505GB
*            4  POINTER TO INPUT DATA ITEM                     @D3505GB
*            14 RETURN POINT IN CALLING ROUTINE                @D3505GB
*                                                              @D3505GB
* OUTPUT:                                                      @D3505GB
*   REGISTER                                                   @D3505GB
*            15 SET TO 0 TO SUPPRESS PRINTING OF CURRENT LINE  @D3505GB
*                                                              @D3505GB
* REGISTERS USED:                                              @D3505GB
*            5 - 8  WORK REGISTERS                             @D3505GB
*                                                              @D3505GB
* EXTERNAL REFERENCES:                                         @D3505GB
*   NONE                                                       @D3505GB
*                                                              @D3505GB
* EXITS - NORMAL:                                              @D3505GB
*   TO IFCEEDIT VIA REGISTER 14                                @D3505GB
*                                                              @D3505GB
* EXITS - ERROR:                                               @D3505GB
*   TO IFCEEDIT VIA REGISTER 14 WITH A RETURN CODE OF 00 IN REG 15
*                                                              @D3505GB
* TABLE/WORK AREAS:                                            @D3505GB
*   R3 PRINT BUFFER                                            @D3505GB
*                                                              @D3505GB
* ATTRIBUTES:                                                  @D3505GB
*   PROBLEM PROGRAM                                            @D3505GB
*   NON - RESIDENT                                             @D3505GB
*   SERIALLY REUSABLE                                          @D3505GB
*                                                              @D3505GB
* NOTES:                                                       @D3505GB
*   THIS PROGRAM WILL RUN ON MFT,MVT,VS1,VS WITH 370 CPU.      @D3505GB
*                                                              @D3505GB
*********************************************************************
         TITLE 'I F C E W I N 1  EDIT TABLE FOR 3340 SNB''S 0-7 AND FMTC
                1 8-23'
         GBLC  &DSECT,&CSECT
IFCEWIN1 CSECT
&CSECT   SETC  'IFCEWIN1'
&DSECT   SETC  'WIN1'
*
*        SENSE 0-7 AND 8-23 FOR FORMAT 1
*
WIN1     DSECT
         DSGEN (CLASRC,8),(SYSREL,8),(SWITCHES,8),(SW1,8)
         DSGEN (SW2,8),(SW3,8),(SPARE1,16)
         DSGEN (DAY,32),(HOUR,32)
         DSGEN (SPARE2,8),(CPUSERL,24),(CPUID,16),(SPARE3,16)
         DSGEN (JOBID,64)
         DSGEN (FAILCCW,64)
         DSGEN (CSW,64)
         DSGEN (DEVDPD,8),(SECCUA,24),(DEVTYP1,16),(DEVTYP2,16)
         DSGEN (SDRCNT,8),(CUA,24),(IORET,16),(SBCNT,16)
         DSGEN (VOLLABL,48),(SPARE4,16)
         DSGEN (LASTSK,64)
         DSGEN (HOMEADR,32),(SPARE5,32)
         DSGEN (SNB,1,0,23,0,7)
         EJECT
***********************************************************************
*                                                                     *
*                                                                     *
*              THIS ENTIRE MODULE IS IN SUPPORT OF  XL03130           *
*                                                                     *
*                                                                     *
***********************************************************************
         SPACE
IFCEWIN1 CSECT
         USING *,R9
         SPACE
R0       EQU   0                       R
R1       EQU   1                        E
R2       EQU   2                         G
R3       EQU   3                          I
R4       EQU   4                           S
R5       EQU   5                            T
R6       EQU   6                             E
R7       EQU   7                              R
R8       EQU   8
R9       EQU   9                                E
R10      EQU   10                                Q
R11      EQU   11                                 U
R12      EQU   12                                  A
R13      EQU   13                                   T
R14      EQU   14                                    E
R15      EQU   15                                     S
         SPACE 2
L0       EQU   0                       EQUATES
L1       EQU   1
L2       EQU   2
L3       EQU   3                               FOR
L4       EQU   4
L5       EQU   5
L6       EQU   6                                   LENGTHS
L7       EQU   7
L8       EQU   8
L9       EQU   9                               AND
L10      EQU   10
L11      EQU   11
L12      EQU   12                                  DISPLACEMENTS
L13      EQU   13
L14      EQU   14
L15      EQU   15
L24      EQU   24
         EJECT
H10      EQU   X'10'                   H
H20      EQU   X'20'                    E
H28      EQU   X'28'                                           @D3505GB
H30      EQU   X'30'                     X
H38      EQU   X'38'                                           @D3505GB
H3A      EQU   X'3A'                                           @D3505GB
H40      EQU   X'40'
H50      EQU   X'50'
H08      EQU   X'08'                        VALUES
H80      EQU   X'80'
         EJECT
         DC    A(ENTRIES)
         DC    CL8'IFCEWIN1'
XR9PTR   DC    F'0'
XLABELP  DC    F'0'
         SPACE 2
*
*        ALL THE FOLLOWING ROUTINES ARE EXECUTED
*              VIA THE 'ROUTINE' MACRO - NOTE: R3=ADR OF PRINT POSITION
*                                              R4=ADR OF DATA IN RECORD
         SPACE
*      SET HEX - PRINTS ONE CHARACTER HEX
SETHEX   STM   R5,R8,XSAVE             SAVE REGS
         LA    R8,CHARS                LOAD ADDR OF CHAR. TABLE
         IC    R7,L0(R4)               INSERT BYTE TO BE CONVERTED
         SLL   R7,L24                  SHIFT TO HIGH ORDER BYTE
         SR    R6,R6                   CLEAR REG
         SLDL  R6,L4                   SHIFT IN 4 BITS
         AR    R6,R8                   ADD ADDR TO OFFSET
         MVC   L0(L1,R3),L0(R6)        MOVE CHARACTER TO PRINT LINE
         SR    R6,R6                   CLEAR REG
         SLDL  R6,L4                   SHIFT IN NEXT 4 BITS
         AR    R6,R8                   ADD ADDR OT OFFSET
         MVC   L1(L1,R3),L0(R6)        MOVE CHAR TO PRINT LINE
         B     RETURN                  RETURN
*
*      THE FOLLOWING SETS BINARY DIGITS TO PRINTABLE HEX
*
SETBIN   STM   R5,R8,XSAVE             SAVE REGS
         IC    R7,L0(R4)               INSERT BYTE
         SLL   R7,L24                  SHIFT BYTE TO HIGH ORDER
         LR    R8,R3                   LOAD ADDR OF PRINT POSITION
         LA    R5,L8                   LOAD LOOP COUNT
SETBINA  SR    R6,R6                   CLEAR REG
         SLDL  R6,L1                   SHIFT ONE BIT TO REG
         STC   R6,L0(R8)               STORE BIT IN PRINT POSITION
         MVZ   L0(L1,R8),CHAR0         MOVE 'F' IN FRONT OF BIT
         LA    R8,L1(R8)               BUMP PRINT POS PTR
         BCT   R5,SETBINA              BR TO DO NEXT BIT
         B     RETURN                  RETURN
         SPACE
SETBIN2  STM   R5,R8,XSAVE             SAVE REGS
         IC    R7,L0(R4)               INSERT BYTE
         SLL   R7,L24                  SHIFT IT TO HIGH ORDER
         LR    R8,R3                   LOAD PRINT POSITION ADDR
         LA    R5,L8                   LOAD LOOP COUNT
         S     R8,ELEVEN               SUBTRACT FROM PRINT POSITION
         B     SETBINA                 BR TO CONVERT
         EJECT
*
*      CHECK FORMAT OF RECORD - IF ITS FORMAT 1,CONTINUE
*                             - IF ITS FORMAT 0 OR 2-5 CALL A NEW MOD
*
CHKFMT   STM   R5,R8,XSAVE             SAVE REGS
         SR    R15,R15                 DONT PRINT THIS LINE
         SR    R5,R5                   CLEAR REG
         TM    L0(R4),H50              IS IT FORMAT 5
         BO    CALLFMT5                YES - BR
         SPACE
         TM    L0(R4),H40              IS IT FORMAT 4
         BO    CALLFMT4                YES - BR
         TM    L0(R4),H38              IS THIS FORMAT 3        @D3505GB
         BO    CALLCUT3                YES                     @D3505GB
         SPACE
         TM    L0(R4),H30              IS IT FORMAT 3
         BO    CALLFMT3                YES - BR
         SPACE
         TM    L0(R4),H28              IS THIS FORMAT 2
         BO    CALLCUT2                YES                     @D3505GB
         TM    L0(R4),H20              IS IT FORMAT 2
         BO    CALLFMT2                YES - BR
         SPACE
         TM    L0(R4),H10              IS IT FORMAT 1
         BO    RETURN                  YES - BR TO RETURN - CONTINUE
         TM    L0(R4),H08              CONNECTED TO A 3880     @D3505GB
         BO    CALLCUT0                                        @D3505GB
         B     CALLFMT0                BRANCH - ITS FORMAT 0
CALLCUT3 TM    L1(R4),H08              IF BYTE 8 (8,4) IS ON, ERROR IS
         BO    MICRO                   MICRO CODE SENSED       @D3505GB
         CLI   0(R4),H3A                                       @D3505GB
         BE    MICRO                   ONLY EXECPTION          @D3505GB
         LA    R5,FECUTH3-L4           CALL IFCENFH3 FOR FORMAT 3
         ST    R5,XR9PTR                                       @D3505GB
         B     RETURN                                          @D3505GB
MICRO    LA    R5,FECUTM3-L4           CALL IFCENFM3 FOR FORMAT 3
         ST    R5,XR9PTR                                       @D3505GB
         B     RETURN                                          @D3505GB
         SPACE 2
CALLFMT0 LA    R5,FE0-L4               LOAD ADDR OF MODULE NAME
         ST    R5,XR9PTR               STORE ADDR FOR EREP
         B     RETURN                  RETURN TO CALL MODULE
         SPACE
CALLFMT2 LA    R5,FE2-L4               LOAD ADDR OF MODULE NAME
         ST    R5,XR9PTR               STORE ADDR FOR EREP
         B     RETURN                  RETURN TO CALL MODULE
         SPACE
CALLFMT3 LA    R5,FE3-L4               LOAD ADDR OF MODULE NAME
         ST    R5,XR9PTR               STORE ADDR FOR EREP
         B     RETURN                  RETURN TO CALL MODULE
         SPACE
CALLFMT4 LA    R5,FE4-L4               LOAD ADDR OF MODULE NAME
         ST    R5,XR9PTR               STORE ADDR FOR EREP
         B     RETURN                  RETURN TO CALL MODULE
         SPACE
CALLFMT5 LA    R5,FE5-L4               LOAD ADDR OF MODULE NAME
         ST    R5,XR9PTR               STORE ADDR FOR EREP
         B     RETURN                  RETURN TO CALL MODULE
         SPACE
CALLCUT0 LA    R5,FECUT0-L4            CALL IFCECUT0 FOR FORMAT 0
         ST    R5,XR9PTR                                       @D3505GB
         B     RETURN                                          @D3505GB
         SPACE
CALLCUT2 LA    R5,FECUT2-L4            CALL IFCENFP2 FOR FORMAT 2
         ST    R5,XR9PTR                                       @D3505GB
         B     RETURN                                          @D3505GB
         SPACE
         EJECT
FE0      DC    X'FE'
         DC    CL8'IFCEMER0'           FORMAT 0 EDIT TABLE
         SPACE
FE2      DC    X'FE'
         DC    CL8'IFCEMER2'           FORMAT 2 EDIT TABLE
         SPACE
FE3      DC    X'FE'
         DC    CL8'IFCEMER3'           FORMAT 3 EDIT TABLE
         SPACE
FE4      DC    X'FE'
         DC    CL8'IFCEMER4'           FORMAT 4 EDIT TABLE
         SPACE
FE5      DC    X'FE'
         DC    CL8'IFCEMER5'           FORMAT 5 EDIT TABLE
         SPACE
FECUT0   DC    X'FE'                                           @D3505GB
         DC    CL8'IFCECUT0'           FORMAT 0 EDIT TABLE
FECUT2   DC    X'FE'                                           @D3505GB
         DC    CL8'IFCENFP2'           FORMAT 2 EDIT TABLE
FECUTH3  DC    X'FE'                                           @D3505GB
         DC    CL8'IFCENFH3'           FORMAT 3 EDIT TABLE
FECUTM3  DC    X'FE'                                           @D3505GB
         DC    CL8'IFCENFM3'           FORMAT 3 EDIT TABLE
*
*      THE FOLLOWING MOVES A UNIQUE LABEL FOR BYTE 8,BIT 5
*        DEPENDING ON THE SETTING OF BYTE 19 BIT 0.
*
         SPACE
SETLAB   S     R3,TOCOLONE             SUBTRACT TO PT TO COL ONE XM3751
         TM    L0(R4),H80              IS THE R/W BIT ON         XM3751
         BO    MOVEWRSN                YES - BR TO MOVE LABEL    XM3751
         MVC   L0(L11,R3),MODA         MOVE LABEL                XM3751
         BR    R14                     RETURN                    XM3751
         SPACE
MOVEWRSN MVC   L0(L11,R3),WRSN         MOVE LABEL                XM3751
         BR    R14                     RETURN                    XM3751
         SPACE 2
*
*      THE FOLLOWING MOVES A UNIQUE LABEL FOR BYTE 8,BIT 6
*        DEPENDING ON THE SETTING OF BYTE 19 BIT 0
*
SETLAB1  S     R3,TOCOLONE             SUBTRACT TO PT TO COLUMN ONE
         TM    L0(R4),H80              IS THE R/W BIT ON
         BO    MOVEINMK                YES - BR TO MOVE LABEL
         MVC   L0(L11,R3),BUSY         MOVE LABEL
         BR    R14                     RETURN
         SPACE
MOVEINMK MVC   L0(L11,R3),INMK         MOVE LABEL
         BR    R14                     RETURN
         EJECT
*
*      THE FOLLOWING MOVES A UNIQUE LABEL FOR BYTE 8,BIT 7
*        DEPENDING ON THE SETTING OF BYTE 19 BIT 0
*
SETLAB2  S     R3,TOCOLONE             SUBTRACT TO PT TO COLUMN ONE
         TM    L0(R4),H80              IS THE R/W BIT ON
         BO    MOVEACTK                YES - BR
         MVC   L0(L11,R3),SSCM         MOVE LABEL
         BR    R14                     RETURN
         SPACE
MOVEACTK MVC   L0(L11,R3),ACTK         MOVE LABEL
         BR    R14                     RETURN
         SPACE 2
*
*      THE FOLLOWING RESTORES REGISTERS USED BY ROUTINES IN THIS
*        MODULE AND RETURNS TO EREP0
*
RETURN   LM    R5,R8,XSAVE             RESTORE REGS
SKIPPER  BR    R14                     RETURN TO EREP
         SPACE 5
*
*      THE FOLLOWING STORAGE CONSTANTS ARE USED IN THE
*        PREVIOUS ROUTINES
*
         SPACE
         DS    0F
XSAVE    DC    5F'0'                   REG SAVE AREA
         SPACE
ELEVEN   DC    F'11'
TOCOLONE DC    F'132'
         SPACE
CHARS    DC    C'0123456789ABCDEF'     HEX CHARACTER TABLE
CHAR0    DC    C'0'
         SPACE
MODA     DC    CL11'MOD ATTENTN'                                 XM3751
WRSN     DC    CL11'I WR SENSE'                                  XM3751
BUSY     DC    CL11'BUSY'
INMK     DC    CL11'INDEX MARK'
SSCM     DC    CL11'SK/SCTR COM'
ACTK     DC    CL11'ACTIVE TRK'
         EJECT
*                                                                     *
*   E D I T  T A B L E  F O R  S E N S E  B Y T E S  0 - 7            *
*                                                                     *
*        O N  A  3 3 4 0 ( W I N C H E S T E R )                      *
*                                                                     *
ENTRIES  DS    0F
         DC    XL4'00010001'
         DC    XL4'00010001'
         DC    A(LABELS)               LABEL POINTER
         DC    H'0'
         SPACE
*
*        SKIP TO CHANNEL 1
*
         DC    X'01'
         DC    X'000B0B00'
         SPACE 2
         LINE  (12,15),(3,15),SKIP=1
         ROUTINE   SNB00,SETHEX
         ROUTINE   SNB10,SETHEX
         ROUTINE   SNB20,SETHEX
         ROUTINE   SNB30,SETHEX
         ROUTINE   SNB40,SETHEX
         ROUTINE   SNB50,SETHEX
         ROUTINE   SNB60,SETHEX
         ROUTINE   SNB70,SETHEX
         LABEL 'BYTE 0','BYTE 1','BYTE 2','BYTE 3','BYTE 4','BYTE 5','BC
               YTE 6','BYTE 7'
         LINE  ,(1,1),SKIP=2
         ROUTINE   SPARE1,SKIPPER
         LABEL '----------UNIT CHECK DESCRIPTION-----------  RESTART CMC
               WD   PHYSICAL ID    LOW CYLINDER   HI CYL/LOG TRK FORMATC
               /MSG'
         LINE  (13,15),(1,15),SKIP=1
         BIN   (SNB00),(SNB10),(SNB20)
         ROUTINE   SNB30,SETBIN2
         BIN   (SNB40),(SNB50),(SNB60),(SNB70)
         LABEL 'COMMAND REJ','PERM ERROR','RPS PRESENT',' ','DRIVE A','C
               CYL 128','        ','FORMAT  8'
         LINE  (13,15),(1,15)
         BIN   (SNB01),(SNB11),(SNB21),(SNB31)
         BIN   (SNB41),(SNB51),(SNB61),(SNB71)
         LABEL 'INTERVN REQ','INV TRK FMT','CORRECTABLE','             C
               ','DRIVE B','CYL  64','CYL   512','FORMAT  4'
         LINE
         BIN   (SNB02),(SNB12),(SNB22),(SNB32)
         BIN   (SNB42),(SNB52),(SNB62),(SNB72)
         LABEL 'BUS OUT PAR','END OF CYL',' ','             ','DRIVE C'C
               ,'CYL  32','CYL   256','FORMAT  2'
         LINE
         BIN   (SNB03),(SNB13),(SNB23),(SNB33)
         BIN   (SNB43),(SNB53),(SNB63),(SNB73)
         LABEL 'EQUIPMNT CK',' ','ENV DATA PR','             ','DRIVE DC
               ','CYL  16',' ','FORMAT  1'
         LINE
         BIN   (SNB04),(SNB14),(SNB24),(SNB34)
         BIN   (SNB44),(SNB54),(SNB64),(SNB74)
         LABEL 'DATA CHECK','NO REC FND','COMPAT MODE',        @X12827WC
               '             ','DRIVE E',                      @X12827WC
               'CYL   8','LOG TRK 8','MESSAGE 8'
         LINE
         BIN   (SNB05),(SNB15),(SNB25),(SNB35)
         BIN   (SNB45),(SNB55),(SNB65),(SNB75)
         LABEL 'OVERRUN','FILE PROTCT','FIXED HEAD',           @X12827WC
               '             ','DRIVE F',                      @X12827WC
               'CYL   4','LOG TRK 4','MESSAGE 4'
         LINE
         BIN   (SNB06),(SNB16),(SNB26),(SNB36)
         BIN   (SNB46),(SNB56),(SNB66),(SNB76)
         LABEL 'TRK COND CK','WRT INHIBIT','DATA MOD SZ','             C
               ','DRIVE G','CYL   2','LOG TRK 2','MESSAGE 2'
         LINE  ,,SKIP=3
         BIN   (SNB07),(SNB17),(SNB27),(SNB37)
         BIN   (SNB47),(SNB57),(SNB67),(SNB77)
         LABEL 'SEEK CHECK','OP INCOMPLT','DATA MOD SZ','             'C
               ,'DRIVE H','CYL   1','LOG TRK 1','MESSAGE 1'
         LINE  ,,SKIP=1
         ROUTINE   SNB70,CHKFMT
         LABEL ' '
         EJECT
*
*   E D I T  T A B L E  F O R  S E N S E  B Y T E S  8 - 2 3
*
*        F O R  F O R M A T  1
*
         LINE  (12,15),(3,15),SKIP=1
         ROUTINE   SNB80,SETHEX
         ROUTINE   SNB90,SETHEX
         ROUTINE   SNB100,SETHEX
         ROUTINE   SNB110,SETHEX
         ROUTINE   SNB120,SETHEX
         ROUTINE   SNB130,SETHEX
         ROUTINE   SNB140,SETHEX
         ROUTINE   SNB150,SETHEX
         LABEL 'BYTE 8','BYTE 9','BYTE 10','BYTE 11','BYTE 12','BYTE 13C
               ','BYTE 14','BYTE 15'
         LINE  ,(1,1),SKIP=2
         ROUTINE   SPARE1,SKIPPER
         LABEL 'DRIVE STATUS   DRIVE CK/STAT  SEQUENCE CNTRL LOAD SW STC
               ATUS R/W SAFETY     CTL INT B OUT  CTL INT B IN   CTL INC
               T TAG B'
         LINE  (13,15),(1,15),SKIP=1
         BIN   (SNB80),(SNB90),(SNB100),(SNB110)
         BIN   (SNB120),(SNB130),(SNB140),(SNB150)
         LABEL 'CNTRLR CK','DM LD SW L','DM SIZE CK ','DR START SW','MUC
               LTI-HD CK','BIT 0','BIT 0'
         LINE
         BIN   (SNB81),(SNB91),(SNB101),(SNB111)
         BIN   (SNB121),(SNB131),(SNB141),(SNB151)
         LABEL 'INTRFACE CK','SECT COM CK','MOD LATCH 4','DM PRES  SW',C
               'CAP/ENAB CK','BIT 1','BIT 1'
         LINE
         BIN   (SNB82),(SNB92),(SNB102),(SNB112)
         BIN   (SNB122),(SNB132),(SNB142),(SNB152)
         LABEL 'DRIVE CHECK','MOT AT SP L','MOD LATCH 2',      @M3787  C
               'COVER LK SW','WRITE OVRUN','BIT 2','BIT 2'     @M3787
         LINE
         BIN   (SNB83),(SNB93),(SNB103),(SNB113)
         BIN   (SNB123),(SNB133),(SNB143),(SNB153)
         LABEL 'R/W CHECK','AIR BT SW L','MOD LATCH 1',        @M3787  C
               'DM UNLD  SW','INDEX CHECK','BIT 3','BIT 3'     @M3787
         LINE
         BIN   (SNB84),(SNB94),(SNB104),(SNB114)
         BIN   (SNB124),(SNB134),(SNB144),(SNB154)
         LABEL 'ON LINE','WRT ENABLE','CHECK LATCH',           @M4330  C
               'DM LOAD  SW','R/W INTR CK','BIT 4','BIT 4'      XM3751
         LINE
         BIN   (SNB85),(SNB95),(SNB105),(SNB115)
         BIN   (SNB125),(SNB135),(SNB145),(SNB155)
         ROUTINE   SNB190,SETLAB                                 XM3751
         LABEL '',' ','SEQUENCE CK','AIR BELT SW','CONTROL CK','BIT 5',C
               'BIT 5'                                           XM3751
         LINE  (13,15),(1,15)
         BIN   (SNB86),(SNB96),(SNB106),(SNB116)
         BIN   (SNB126),(SNB136),(SNB146),(SNB156)
         ROUTINE   SNB190,SETLAB1
         LABEL '','MODULE SZ 2','BIAS DSB SW','CARR HOME','TRANSINT CK'C
               ,'BIT 6','BIT 6'
         LINE  ,,SKIP=3
         BIN   (SNB87),(SNB97),(SNB107),(SNB117)
         BIN   (SNB127),(SNB137),(SNB147),(SNB157)
         ROUTINE   SNB190,SETLAB2
         LABEL '','MODULE SZ 1','ODD TRACK  ','MT AT SP SW',   @XM3794 C
               'WRT CURT CK','BIT 7','BIT 7'                   @XM3794
         LINE  (12,15),(3,15),SKIP=1
         ROUTINE   SNB160,SETHEX
         ROUTINE   SNB170,SETHEX
         ROUTINE   SNB180,SETHEX
         ROUTINE   SNB190,SETHEX
         ROUTINE   SNB200,SETHEX
         ROUTINE   SNB210,SETHEX
         ROUTINE   SNB220,SETHEX
         ROUTINE   SNB230,SETHEX
         LABEL 'BYTE 16','BYTE 17','BYTE 18','BYTE 19','BYTE 20','BYTE C
               21','BYTE 22','BYTE 23'
         LINE  ,(1,15),SKIP=2
         ROUTINE   SPARE1,SKIPPER
         LABEL 'ACCESS STATUS','CNTRLR CHKS','MICRO DETCD ER','STATUS',C
               'INTERFACE CKS',' ','-----ERROR SYMPTOM CODE-----'
         LINE  (13,15),(1,15),SKIP=1
         BIN   (SNB160),(SNB170),(SNB180),(SNB190),(SNB200)
         ROUTINE   SNB210,SETBIN2
         ROUTINE   SNB220,SETBIN2
         ROUTINE   SNB230,SETBIN2
         LABEL 'TIME-OUT CK','PLO CHECK',' ','SET R/W',                C
               'CTL TAG CK'                                      XM3751
         LINE
         BIN   (SNB161),(SNB171),(SNB181),(SNB191),(SNB201)
         LABEL 'OVRSHOOT CK','NO PLO INPT',' ',' ','CTL B/O CK'  XM3751
         LINE
         BIN   (SNB162),(SNB172),(SNB182),(SNB192),(SNB202)
         LABEL 'ACC OFF TRK','SER/DES CK ',' ',' ',            @XM3794 C
               'DEV SEL CK '                                   @XM3794
         LINE
         BIN   (SNB163),(SNB173),(SNB183),(SNB193),(SNB203)
         LABEL 'TRK CROSSNG','GAP CNTR CK',' ',' ','DEV B/I CK'  XM3751
         LINE
         BIN   (SNB164),(SNB174),(SNB184),(SNB194),(SNB204)
         LABEL 'SERVO LATCH','WRT DATA CK','MESSAGE  8',' ','CTL B/I CKC
               '
         LINE
         BIN   (SNB165),(SNB175),(SNB185),(SNB195),(SNB205)
         LABEL 'LIN MOD LAT','MONITOR CK','MESSAGE  4',' ',            C
               'I WRT FAIL '                                     XM4371
         LINE
         BIN   (SNB166),(SNB176),(SNB186),(SNB196),(SNB200)
         LABEL 'CONTROL LAT','ECC CHECK','MESSAGE  2',' ','DEV B/O CK'
         LINE  ,,SKIP=1
         BIN   (SNB167),(SNB177),(SNB187),(SNB197),(SNB207)
         LABEL 'WAIT LATCH','ECC ZEROES','MESSAGE  1',' ','DEV TAG CK'
         LINE
         ROUTINE   SPARE1,SKIPPER
         LABEL ' '
         SPACE 2
         DC    X'FF'
         DC    XL2'0070'
         DC    XL2'0018'
         DC    XL2'0000'
*
LABELS   DS    0F
         DC    C'$'
         LSTART
         SPACE 2
         END
