         TITLE 'IFCEXXX9 - EDIT TABLE - LINKNAME=IFCE2250, ALIASES=IFCEC
               2260,IFCE2280,IFCE2282,IFCE3505,IFCE3525'
         SPACE 2
         GBLC  &DSECT,&CSECT
IFCEXXX9 CSECT
&DSECT   SETC  'XXX9'
&CSECT   SETC  'IFCEXXX9'
*
*              EDIT TABLE FOR OBR REC
*
*              DEVICE CODE=1002   DEV TYPE=2250   ALIAS=IFCE2250
*                          1003            2260         IFCE2260
*                          1005            2280         IFCE2280
*                          1006            2282         IFCE2282
*                          0806            3505         IFCE3505 S21021
*                          080C            3525         IFCE3525 S21021
*
*
XXX9     DSECT
         DSGEN (XDUM1,8),(XRELN,8),(XDUM2,8),                    S21021X
               (SW2B0,1),(SW2B1,1),(SW2B2,1),(SW2B3,1),          S21021X
               (SW2B4,1),(SW2B5,1),(SW2B6,1),(SW2B7,1)           S21021
         DSGEN (XDUM3,32)                         SPARES
         DSGEN (XDATE,32)                         DATE
         DSGEN (XTIME,32)                         TIME
         DSGEN (XDUM4,8),(XSER,24)           SERIAL NO
         DSGEN (XMODEL,16),(XDUM5,16)        MODEL NUMBER & MCLEN
         DSGEN (XPGN1,32)                    PROGRAM
         DSGEN (XPGN2,32)                            NAME
         DSGEN (XFCCWCC,8),(XFCCWDA,24)  FAILING CCW
         DSGEN (XFCCWFL,8),(XZERO,8),(XFCCWCT,16)    CCW
         DSGEN (XCSWK,8),(XCSWCA,24)         CHAN  STATUS
         DSGEN (XCSWATTN,1),(XCSWSM,1),(XCSWCUE,1),      WORD  CSW     C
               (XCSWBSY,1),(XCSWCE,1),(XCSWDE,1),(XCSWUC,1),(XCSWUE,1),C
               (XCSWPCI,1),(XCSWSILI,1),(XCSWPC,1),(XCSWPCHK,1),       C
               (XCSWCDC,1),(XCSWCCC,1),(XCSWICC,1),(XCSWCH,1),         C
               (XCSWCT,16)
         DSGEN (XDUMA,8),(XCUA2,24)                              S21021
         DSGEN (XDEVT1,16),(XDEVT2,16)       DEVICE TYPE
         DSGEN (XDUM6,8),(XCUA,24)           DUMMY AND CUA
         DSGEN (XDUM7,16),(XDUM8,16)         RTRY AND SPARE
         DSGEN (XDUM9,32)                    VOLUME
         DSGEN (XDUM10,16),(XTCAM,8),(XDUM10A,8)
         DSGEN (XDUM11,32)                   LAST
         DSGEN (XDUM12,32)                        SEEK
         DSGEN (XDUM13,32)                   HOME
         DSGEN (XDUM14,32)                        ADDRESS
         DSGEN (SDR1,8),(SDR2,8),(SDR3,8),(SDR4,8),(SDR5,8),     S21021X
               (SDR6,8),(SDR7,8),(SDR8,8)                        S21021
         DSGEN (SDR9,8),(SDRA,8),                                S21021X
               (XB0CMDR,1),(XB0BIT1,1),(XB0BUSC,1),              S21021C
               (XB0BIT3,1),(XB0DCHK,1),(XB0BIT5,1),(XB0BRUN,1),        C
               (XB0BIT7,1),                                            C
               (XB1LPD,1),(XB1EOS,1),(XB1CMODE,1),(XB1BIT3,1),         C
               (XB1BIT4,1),(XB1BIT5,1),(XB1BIT6,1),(XB1BIT7,1)
         DSGEN (XB2BIT0,1),(XB2BAR15,1),(XB2BAR14,1),(XB2BAR13,1),     C
               (XB2BAR12,1),(XB2BAR11,1),(XB2BAR10,1),(XB2BAR9,1),     C
               (XB3BAR8,1),(XB3BAR7,1),(XB3BAR6,1),(XB3BAR5,1),        C
               (XB3BAR4,1),(XB3BAR3,1),(XB3BAR2,1),(XB3BAR1,1),      - C
               (XBDUM18,16)
EXPNOBR  DSECT                                                   S21021
EXPNHDRA DS CL52                                                 S21021
EXPNDEVA DS CL36                                                 S21021
EXPNSDRA DS CL10                                                 S21021
EXPNSNSA DS CL6                                                  S21021
*                                                                S21021
NORMOBR DSECT                                                    S21021
NORMHDRA DS CL64                                                 S21021
NORMSDRA DS CL10                                                 S21021
NORMSNSA DS CL6                                                  S21021
*                                                                S21021
SHRTOBR DSECT                                                    S21021
SHRTHDRA DS CL24                                                 S21021
SHRTDEVA DS CL8                                                  S21021
SHRTSDRA DS CL10                                                 S21021
*                                                                S21021
REDPIOBR DSECT                                                   S21088
REDPIHDR DS    CL64                                              S21088
REDPISNS DS    CL6                                               S21088
*
*        END OF DSECT    104 BYTES TOTAL
*
IFCEXXX9 CSECT
         USING *,R9
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
L0       EQU   0
L1       EQU   1
L2       EQU   2
L3       EQU   3
L4       EQU   4
L5       EQU   5
L6       EQU   6
L7       EQU   7
L8       EQU   8
L9       EQU   9
L10      EQU   10
L11      EQU   11
L12      EQU   12
L13      EQU   13
L14      EQU   14
L15      EQU   15
L16      EQU   16
L17      EQU   17
L18      EQU   18
L19      EQU   19
L20      EQU   20
L21      EQU   21                                                S21021
L24      EQU   24
L28      EQU   28
L29      EQU   29                                                S21021
L31      EQU   31                                                S21021
L34      EQU   34                                                S21021
L36      EQU   36                                                S21021
L40      EQU   40                                                S21021
L44      EQU   44                                                S21021
L46      EQU   46                                                S21021
L64      EQU   64                                                S21021
L104     EQU   104                                               S21021
L120     EQU   120                                               S21021
HEX08    EQU   X'08'                                             S21021
HEX10    EQU   X'10'                                             S21021
HEX20    EQU   X'20'                                             S21021
HEX44    EQU   X'44'                                             S21021
HEX46    EQU   X'46'                                             S21021
HEX50    EQU   X'50'                                             S21021
XFF      EQU   X'FF'                                             S21021
A        EQU   C'A'                                              S21021
B        EQU   C'B'                                              S21021
         USING XXX9,R2                                           S21021
*
HEX05    EQU   X'05'
         DC    A(ENTRIES)               PTR TO EDIT ENTRIES
         DC    CL8'IFCE2260'
XR9PTR   DC    F'0'                     PTR TO NXT EDIT-4        S21021
XLABELP  DC    F'0'
*        SPECIAL ROUTINE SECTION
*
SKIPPER  BR    R14                      NOOP RTRN
*
*
SPACER1  LA    R3,L1(R3)                SPACE ONE BLANK          S21021
         BR    R14                      RETURN
*
*
*        SET BINARY
*              R4 HAS PTR TO CHAR
*
SETBIN   STM   R5,R8,XSAVE              SAVE REGS
         IC    R7,L0(R4)                PICK UP CHAR TO PRINT
         SLL   R7,L24                   MOVE IT TO HI OF R7
         LR    R8,R3                    PRINT POS POINTER
         LA    R5,L8                    LOOP COUNT
SETBINA  SR    R6,R6                    CLEAR REG 6
         SLDL  R6,L1                    MOVE BIT TO R6
         STC   R6,L0(R8)                PUT IT INTO PTINT LINE
         MVZ   L0(L1,R8),CHAR0          MOVE ON PRORPER ZONE
         LA    R8,L1(R8)                BUMP POINTER TO NEXT POS
         BCT   R5,SETBINA               LOOP TILL 8 PRINTED
         B     RETURN                   RETURN
*
*
*        SET PROGRAM NAME  R4 POINTS TO NAME FIELD
*
SETPGMN  DS    0H                                                S21021
         TM    SW2B0,HEX20              SHORT RECORD             S21021
         BCR   1,R14                    YES, NO JOB ID           S21021
         MVC   0(L12,R3),JOBID          MOVE IN LABEL            S21021
         MVC   L13(L8,R3),XPGN1         MOVE IN NAME             S21021
         BR    R14                                               S21021
NONAME   TM    L0(R4),HEX20             SHORT RECORD?            S21021
         BCR   14,R14                   NO, LET HEX JOBNAME PRNT S21021
         ST    R5,XSAVE                 SAVE REG                 S21021
         LA    R5,L44                   BACK UP BUFFER PNTR      S21021
         SR    R3,R5                                             S21021
         MVC   0(L17,R3),BLANKS         CLEAR OUT HEX JOBNAME    S21021
         L     R5,XSAVE                 RESTORE REG              S21021
         BR    R14                                               S21021
*
*
*        SET HEX  SAME AS BIN EXCPT PRINTS 1 CHAR HEX
*
SETHEX   STM   R5,R8,XSAVE              SAVE REGS
         LA    R8,CHARS                 CHAR TABLE POINTER
         IC    R7,L0(R4)                PICK UP CHAR
         SLL   R7,L24                   MOVE IT TO HI OF R7
         SR    R6,R6                    CLEAR REGS
         SLDL  R6,L4                    MOVE FOUR BITS
         AR    R6,R8                    ADD OFFSET & PTR
         MVC   L0(L1,R3),L0(R6)         MOVE CHAR TO LINE
         SR    R6,R6                    CLEAR AGAIN
         SLDL  R6,L4                    MOVE LAST 4 BOTS
         AR    R6,R8                    COMPUTE OFFSET
         MVC   L1(L1,R3),L0(R6)         MOVE LAST CHAR
         B     RETURN
*
*
*
SETCONV  TM    L0(R4),HEX02             HAS RECORD BEEN CONVERTED KEY32
         BCR   ON,R14                   YES RETURN
         SR    R15,R15                  NO CLEAR R15 FOR NO PRINT OF
         BR    R14                      LINE BR-14
ON       EQU   1
HEX02    EQU   X'02'
*                                       SET DEVICE TYPE FIELD
*
SETDEVTY STM   R5,R8,XSAVE              SAVE REGS    R3 PTS TO BUFF POS
         LA    R5,L6                    NO OF DEVICES            S21021
         LA    R6,DEVTABLE
SETDEVLP CLC   L0(L2,L4),L0(R6)         COMPARE TYPES
         BE    SDEVFND                  BR-EQUAL
         LA    R6,L6(R6)                UP TO NXT DEV TYPE
         BCT   R5,SETDEVLP              LOOP
         MVC   L0(L4,R3),NONE           NONE FOUND USE NONE
         B     RETURN
SDEVFND  MVC   L0(L4,R3),L2(R6)         MOVE DEV NAME TO LINE
         B     RETURN
*
*
*                             CHECK ON CPU AND SERIAL NO
SETCPUNO STM   R5,R8,XSAVE
         SR    R5,R5               CLEAR R5
         IC    R5,L0(R4)           PICK UP MODEL NO
         SLL   R5,L8               IN TO
         IC    R5,L1(R4)           R5
         LTR   R5,R5                    TEST FOR ZERO MODEL      S21021
         LR    R5,R3                    POINT TO DATA FIELD      S21021
         BNZ   SETSER                   BRANCH IF MODEL NOT 0    S21021
         LA    R6,L46                   BACK UP TO SAY 'NONE'    S21021
         SR    R5,R6                    BACKUP BUFFER            S21021
         MVC   L0(L29,R5),UNIVERS       SAY NONE, MODEL UNIVERSALS21021
         B     RETURN                   RETURN  CONTROL
SETSER   LA    R6,L36                   SET UP TO ADD 'SERIAL NO.S21021
         SR    R5,R6                                             S21021
         MVC   0(L10,R5),SERNO                                   S21021
         B     RETURN                                            S21021
*
*        SET DATE
*
SETDATE  CLC   L1(L3,R4),ZERO           IS DATE ZERO
         BE    SETDTE                   BR-YES
         UNPK  XSAVE(L8),L1(L3,R4)      UNPACK DATE
         MVC  L5(L2,R3),XSAVE+L3  MOVE YEAR
         MVC   L0(L3,R3),XSAVE+L5       MOVE DAY
         BR    R14                      RETURN
SETDTE   MVC   L0(L4,R3),NONE           SET DATE TO NONE
         BR    R14
ZERO     DC    XL4'00'
*
*
SETLABEL STM   R5,R8,XSAVE              SAVE REGS
         CLC   L0(L2,R4),REDLDEV        IS IT REDLAKE            S21088
         BE    REDPIRT                  YES - BRANCH             S21088
         CLC   L0(L2,R4),PIKLDEV        IS IT PIKELAKE           S21088
         BE    REDPIRT                  YES - BRANCH             S21088
         MVI   RECSIZE+L1,HEX50                                  S21088
         L     R5,XLABELP               PICK UP LABEL PTR
         CLI   L1(R4),HEX03             TEST TOR 2250
         BL    RETURN                   BR-YES
         BE    SET2260                  EQUAL IS 2260
         CLI   L1(R4),HEX05             TEST FOR 2282
         BL    SET2280                  LOEW IS 2282
SET2282  LA    R6,CLABEL1               POINT TO 2282 LABELS
         LA    R7,L8        LOAD COUNT INTO REG SEVEN
         B     SETLBLMV                 MOVE LABELS
SET2280  MVC   CLABEL1A(L10),CLABEL2    SET 2280 LABEL
         B     SET2282                  LOOP
SET2260  LA    R6,CLABEL3               SET POINTER TO 2260
         LA    R7,L8    LOAD COUNT INTO REG SEVEN...
SETLBLMV MVC   L0(L20,R5),L0(R6)        MOVE TWO LABELS
         LA    R5,L40(R5)               UP POINTER
         LA    R6,L20(R6)               UP LABEL POINTER
         BCT   R7,SETLBLMV              LOOP
         B     RETURN
REDPIRT  LA    R5,REDPIKLB              GET RED/PIK PTR.         S21088
         ST    R5,XLABELP               AND PUT IN               S21088
         MVI   RECSIZE+L1,HEX46                                  S21088
         B     RETURN                                            S21088
*
RETURN   LM    R5,R8,XSAVE              RESTORE REG
         BR    R14                      RETURN
HEX03    EQU   X'03'
HEX18    EQU   X'18'
*
SPACER16 LA    R3,L16(R3)                                        S21021
         BR    R14                                               S21021
SDRCONVL STM   R5,R8,XSAVE              SAVE REGS                S21021
         SR    R6,R6                    CLEAR REG                S21021
         IC    R6,L0(R4)                PICK UP COUNTER IN QUEST S21021
         SRL   R6,L4                    DROP OFF 4 LOW BITS      S21021
         B     SDRCONVT                                          S21021
SDRCONVR STM   R5,R8,XSAVE              SAVE REGS                S21021
         IC    R6,L0(R4)                PICK UP CHAR             S21021
         SLL   R6,L28                                            S21021
         SRL   R6,L28                   DROP OFF HI ORDER BITS   S21021
SDRCONVT CVD   R6,WORK                  CONVERT IT               S21021
         UNPK  L0(L3,R3),WORK+L5(L3)    TO DECIMAL               S21021
         MVZ   L2(L1,R3),L0(R3)         PUT ON ZONE              S21021
         B     RETURN                                            S21021
*                                                                S21021
RESETR2  L     R2,SAVE2                 PICK UP SAVED REC POINTERS21021
         BR    R14                                               S21021
*                                                                S21021
TESTMP   TM    L0(R4),HEX10             IS IT A MP RECODR        S21021
         BZ    NOMP                                              S21021
         MVC   0(L20,R3),MPCPU          SHOW MULTIPROC           S21021
         TM    L0(R4),HEX08             IS IT CPUA / CPUB        S21021
         BO    MPB                      BR IT IS B               S21021
         MVI   L21(R3),A                MOVE 'A' TO PRINT LINE   S21021
NOMP     BR    R14                                               S21021
MPB      MVI   L21(R3),B                MOVE 'B' TO PRINT LINE   S21021
         BR    R14                                               S21021
NOPRT    SR    R15,R15                  STOP PRINTING            S21021
         BR    R14                                               S21021
*                                                                S21021
*                                                                S21021
TESTSHRT TM    SW2B0,HEX20              IS IT A SHORT REC        S21021
         BZ    SKIPPER                  BR-NO                    S21021
         BCTR  R3,R0                    BACKUP ONE DATA SPACE    S21021
         MVC   0(L6,R3),BLANKS          CLEAR OUT HEX 2NDARY CUA S21021
         MVC   XR9PTR(L8),SHRTADCN      POINT TO STAT EDIT SECT  S21021
         BR    R14                                               S21021
*                                                                S21021
EXPNDRTN STM   R5,R8,XSAVE                                       S21021
         ST    R2,SAVE2                 SAVE R2 FOR LATER        S21021
         LA    R5,OBRAREA               PICK UP EXPANDED AREA    S21021
         USING EXPNOBR,R5                                        S21021
         USING NORMOBR,R4                                        S21021
         XC    EXPNOBR(L104),EXPNOBR    CLEAR AREA               S21021
         CLC   XDEVT2(L2),REDLDEV       IS IT 3505?              S21088
         BE    REDPIK                   YES - BRANCH             S21088
         CLC   XDEVT2(L2),PIKLDEV       IS IT 3525?              S21088
         BE    REDPIK                   YES - BRANCH             S21088
         TM    SW2B0,HEX20              IS IT A SHORT REC        S21021
         BO    MOVESHRT                 BR-YES                   S21021
         TM    XDUM1,HEX02              IS IT A CONVERTED REC    S21021
         BO    MOVEALL                                           S21021
         MVC   EXPNHDRA(L64),NORMHDRA   EXPAND RECORD TO CONVERTES21021
         MVC   EXPNSDRA(L10),NORMSDRA                            S21021
         MVC   EXPNSNSA(L6),NORMSNSA                             S21021
EXPNDRET LR    R2,R5                    SWAP POINTERS            S21021
         B     RETURN                                            S21021
MOVEALL  MVC   EXPNOBR(L104),NORMOBR    MOVE ENTIRE RECORD       S21021
         B     EXPNDRET                                          S21021
         USING SHRTOBR,R4                                        S21021
MOVESHRT MVC   EXPNHDRA(L24),SHRTHDRA   MOVE HEADER              S21021
         MVC   EXPNDEVA(L8),SHRTDEVA                             S21021
         MVC   EXPNSDRA(L10),SHRTSDRA                            S21021
         B     EXPNDRET                                          S21021
         USING REDPIOBR,R4                                       S21088
REDPIK   MVC   EXPNHDRA(L64),REDPIHDR   MOVE HEADER              S21088
         MVC   EXPNSNSA(L6),REDPISNS    MOVE SENSE               S21088
         B     EXPNDRET                 RETURN                   S21088
         DROP  R4,R5                                             S21021
*                                                                S21021
RECTYPE  STM   R5,R8,XSAVE                                       S21021
         LR    R5,R3                    SET UP DATA SLOT FOR TYPES21021
         LA    R6,L31                                            S21021
         SR     R5,R6                                            S21021
         TM    0(R4),HEX20              SHORT RECORD?            S21021
         BO    RECSHORT                                          S21021
         MVC   0(L10,R5),UNITCK         INSERT DATA 'UNIT CHECK' S21021
         B     RETURN                                            S21021
RECSHORT MVC   0(L10,R5),OVFL           DATA = 'OVERFLOW'        S21021
         B     RETURN                                            S21021
SETSTLBL DS    0H                       SET LABELS FOR STAT COUNTS21088
         STM   R5,R8,XSAVE              SAVE REGS                S21088
         LA    R6,L2                    SET BCT COUNT            S21088
         L     R5,XLABELP               GET CURRENT LABEL PNTR   S21088
         LA    R7,L6                    NUMBER OF LABEL LISTS    S21088
         LA    R8,STLBLIST              POINT TO FIRST LABEL ENTRS21088
STLBCOMP CLC   XDEVT2(L2),0(R8)         IS THIS THE DEVICE TYPE? S21088
         BE    STLBLTFD                 YES, GO MOVE LABELS      S21088
         LA    R8,L18(R8)          NO, BUMP POINTER              S21088
         BCT   R7,STLBCOMP              REPEAT NUMBER OF LISTS   S21088
         LA    R8,STLBDFLT              NOT FOUND, USE DEFAULT   S21088
STLBLTFD DS    0H                  LABELS FOUND                  S21088
         LA    R8,L2(R8)                USE LABELS               S21088
         LA    R7,L16(R8)               DETERMINE END POINT      S21088
         ST    R7,STLBLEND              SAVE FOR COMPARE         S21088
STLABLP  DS    0H                                                S21088
         LA    R6,L2                    RESET BCT                S21088
         TM    SW2B0,HEX20              IS THIS A SHORT RECORD   S21088
         BO    STLBSHRT                 YES, NO UNIT STATUS      S21088
         LA    R5,L34(R5)               SKIP UNIT STATUS LABELS  S21088
STLBSHRT DS    0H                                                S21088
         CLI   0(R8),XFF                IS THIS 'NOT USED'       S21088
         BE    STLBLNUS                 YES, LABEL = 'NOT USED'  S21088
         SR    R7,R7                    CLEAR R7                 S21088
         IC    R7,0(R8)                 GET INDEX TO LABEL       S21088
         MH    R7,HW13                  MULTIPLY TO GET LABEL    S21088
         A     R7,SLTABAD               PT TO LABEL IN TABLE     S21088
         MVC   L0(L13,R5),L0(R7)        MOVE IN LABEL            S21088
         B     STLBL001                                          S21088
STLBLNUS DS    0H                                                S21088
         MVC   L0(L13,R5),STLBNTUS      'NOT USED' LABEL         S21088
STLBL001 DS    0H                                                S21088
         LA    R5,L14(R5)               BUMP R5                  S21088
         LA    R8,L1(R8)                POINT TO NEXT INDEX      S21088
         BCT   R6,STLBSHRT              DO AGAIN                 S21088
         C     R8,STLBLEND              AT END OF LIST?          S21088
         BNE   STLABLP                  NO, CONTINUE             S21088
         B     RETURN                                            S21088
SLTABAD  DC    A(SLTABL)                                         S21088
SLTABL   DS    0C                                                S21088
         DC    C'TEMPY READS  '         00                       S21088
         DC    C'TEMPY WRITES '         01                       S21088
         DC    C'INTRVN REQD  '         02                       S21088
         DC    C'BUS OUT CHK  '         03                       S21088
         DC    C'EQUIP CHK    '         04                       S21088
         DC    C'CHAN DATA CHK'         07                       S21088
STLBNTUS DC    C'NOT USED     '                                  S21088
STLBLIST DS    0C                                                S21088
         DC    X'1005'                  2280                     S21088
         DC    X'0001020304FFFFFFFFFFFFFFFFFFFF05'               S21088
         DC    X'1006'                  2282                     S21088
         DC    X'0001020304FFFFFFFFFFFFFFFFFFFF05'               S21088
         DC    X'1002'                  2250                     S21088
         DC    X'0001020304FFFFFFFFFFFFFFFFFFFF05'               S21088
         DC    X'1003'                  2260                     S21088
         DC    X'0001020304FFFFFFFFFFFFFFFFFFFF05'               S21088
         DC    X'0806'                  3505                     S21088
         DC    X'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF'               S21088
STLBDFLT DS    0C                                                S21088
         DC    X'080C'                  3525                     S21088
         DC    X'FFFFFFFFFFFFFFFFFFFFFFFFFFFFFFFF'               S21088
STLBLEND DC    F'0'                                              S21088
WORK     DS    D                                                 S21021
SAVE2    DS    F                                                 S21021
SHRTADCN DC    A(SHRTOBRS-L4)                                    S21021
         DC    A(SHRTOBRL)                                       S21021
OBRAREA  DS    CL104                                             S21021
BLANKS   DC    CL25' '                                           S21021
HW21     DC    H'21'                                             S21021
HW13     DC    H'13'                                             S21021
UNITCK   DC    C'UNIT CHECK'                                     S21021
OVFL     DC    C'OVERFLOW  '                                     S21021
SERNO    DC    C'SERIAL NO.'                                     S21021
MPCPU    DC    C'MULTIPROCESSOR - CPU'                           S21021
UNIVERS  DC    C'UNIVERSAL   SERIAL NO. NONE  '                  S21021
JOBID    DC    C'JOB IDENTITY'                                   S21021
         DS    0F
XSAVE    DC    5F'0'
NONE     DC    CL6'NONE  '
CHARS    DC    C'0123456789ABCDEF'
CHAR0    DC    C'0'
DEVTABLE DC    X'1002'
         DC              C'2250'
         DC    X'1003'
         DC              C'2260'
         DC    X'1005'
         DC              C'2280'
         DC    X'1006'
         DC              C'2282'
REDLDEV  DC    X'0806'                                           S21088
         DC              C'3505'                                 S21088
PIKLDEV  DC    X'080C'                                           S21088
         DC              C'3525'                                 S21088
CLABEL1  DC    CL10'CMND REJ *'
         DC    CL10'RD CT CHK*'
         DC    CL10'INTV REQD*'
         DC    CL10'FILM  LOW*'
         DC    CL10'BUS O CHK*'
         DC    CL10'FORCD R G*'
         DC    CL10'EQUIP CHK*'
CLABEL1A DC    CL10'FILM MOT *'
         DC    CL10'DATA CHK *'
         DC    CL10'BIT  4   *'
         DC    CL10'BIT  5   *'
         DC    CL10'O/P CHECK*'
         DC    CL10'BIT  6   *'
         DC    CL10'I/P CHECK*'
         DC    CL10'ILLEG SEQ*'
         DC    CL10'GRAPH CHK*'
CLABEL2  DC    CL10'BIT  3   *'
CLABEL3  DC    CL10'CMND REJ *'
         DC    CL10'BIT  0   *'
         DC    CL10'INTV REQD*'
         DC    CL10'BIT  1   *'
         DC    CL10'BUS O CHK*'
         DC    CL10'BIT  2   *'
         DC    CL10'EQUIP CHK*'
         DC    CL10'BIT  3   *'
         DC    CL10'BIT  4   *'
         DC    CL10'BIT  4   *'
         DC    CL10'BIT  5   *'
         DC    CL10'BIT  5   *'
         DC    CL10'BIT  6   *'
         DC    CL10'BIT  6   *'
         DC    CL10'BIT  7   *'
         DC    CL10'BIT  7   *'
TCAMEND  DC    X'FE'                    NEXT TABLE END INDICATOR
         DC    CL8'IFCEXXXX'            TCAM TABLE NAME
*
HEX0F    EQU   X'0F'
*                       TABLE CONSTANTS
ENTRIES  DS    0F
         DC    XL4'00010001'            FORMAT1
         DC    XL4'00010001'            FORMAT2
         DC    A(LABELS)                LABEL PTR
         DC    H'0'                     DISP
         DC    X'02'                                             S21021
         DC    X'000B8B00'
         DC    AL.12(XDUM1-XXX9),XL.4'D'  = ROUTINE XDUM1,       S21021
         DC    AL.4(0),AL.12(EXPNDRTN-IFCEXXX9)   EXPNDRTN       S21021
         LINE
         LABEL      ' '
*
         LINE  (55,23),(1,35),SKIP=1                             S21021
         ROUTINE  SW2B0,RECTYPE                                  S21021
         HEX   XMODEL,0,2B                                       S21021
         HEX   XSER,0,3B                                         S21021
         ROUTINE XMODEL,SETCPUNO                                 S21021
         LABEL '---RECORD ENTRY TYPE -','   SOURCE - OUTBOARD',  S21021X
               'MODEL-'                                          S21021
         LINE  (16,1),(4,1)                                      S21021
         SYSRELN    XRELN,4                                    @D35UGFD
         LABEL      ''                                         @D35UGFD
         LINE  (69,1),(31,19)                                    S21021
         ROUTINE XPGN1,SETPGMN                                   S21021
         LABEL 'DAY YEAR','HH MM SS.TH'                          S21021
         LINE  (31,19),(25,19)                                   S21021
         ROUTINE XDATE,SETDATE                                   S21021
         HEX   XTIME,1,4B                                        S21021
         ROUTINE  XDUM1,SKIPPER                                  S21021
         HEX   XPGN1,B6,4B                                       S21021
         HEX   XPGN2,B16,4B                                      S21021
         ROUTINE  SW2B0,NONAME                                   S21021
         LABEL 'DATE-','TIME-'                                   S21021
         LINE  (1,1),(1,1)                                       S21021
         ROUTINE SW2B0,TESTMP                                    S21021
         LABEL ''                                                S21021
         LINE  ,(1,1),SKIP=2                                     S21021
         ROUTINE XDUM1,SETCONV                                   S21021
         LABEL 'OBR RECORD CONVERTED TO THE STANDARD FORMAT'     S21021
         LINE  (34,1),(2,1),SKIP=1                               S21021
         ROUTINE XDEVT2,SETDEVTY                                 S21088
         LABEL 'DEVICE TYPE'                                     S21021
         LINE
         HEX   XCUA,B0,3B                                        S21021
         LABEL 'PRIMARY   CHANNEL UNIT ADDRESS  '                S21021
         LINE
         HEX   XCUA2,B0,3B                                       S21021
         ROUTINE  XDUM1,TESTSHRT                                 S21021
         LABEL 'ALTERNATE CHANNEL UNIT ADDRESS  '                S21021
         LINE
         LABEL ' '                                               S21021
         LINE  (1,1),(23,38)                                     S21021
         LABEL 'CC   CA   FL     CT','K    CA   US CS  CT'       S21021
         LINE  (23,3),(5,51),SKIP=2                              S21021
         HEX   XFCCWCC,0,1B                                      S21021
         HEX   XFCCWDA,0,3B                                      S21021
         ROUTINE  XDUM1,SPACER1                                  S21021
         HEX   XFCCWFL,0,1B                                      S21021
         HEX   XZERO,0,1B                                        S21021
         HEX   XFCCWCT,0,2B                                      S21021
         ROUTINE  XDUM1,SPACER16                                 S21021
         HEX   XCSWK,0,1B                                        S21021
         HEX   XCSWCA,0,3B                                       S21021
         ROUTINE  XDUM1,SPACER1                                  S21021
         ROUTINE XCSWATTN,SETHEX                                 S21021
         ROUTINE XCSWPCI,SETHEX                                  S21021
         HEX   XCSWCT,0,2B                                       S21021
         LABEL 'FAILING CCW','CSW'                               S21021
         LINE  ,(5,28)                                           S21021
         ROUTINE    XDUM1,SKIPPER
         LABEL 'UNIT STATUS','CHANNEL STATUS',                   S21021X
               'STATISTICAL DATA','STATISTICAL DATA'             S21021
*
*                     CSW DETAIL  OF US & CS FIELDS
*
         LINE  (20,28),(3,28),SKIP=1                             S21021
         BIN        XCSWATTN
         BIN        XCSWPCI
         ROUTINE    SDR1,SDRCONVL                                S21021
         ROUTINE    SDR1,SDRCONVR                                S21021
         ROUTINE  XDUM1,SETSTLBL                                 S21088
         LABEL 'ATTENTION       ','PRGM-CTLD TRPT  ',            S21021X
               '             ','             '                   S21021
         LINE
         BIN        XCSWSM
         BIN        XCSWSILI
         ROUTINE    SDR2,SDRCONVL                                S21021
         ROUTINE    SDR2,SDRCONVR                                S21021
         LABEL 'STATUS MODIFIER ','INCORRECT LENGTH',            S21021X
               '             ','             '                   S21021
         LINE
         BIN        XCSWCUE
         BIN        XCSWPC
         ROUTINE    SDR3,SDRCONVL                                S21021
         ROUTINE SDR3,SDRCONVR                                   S21021
         LABEL 'CONTROL UNIT END','PROGRAM CHECK   ',            S21021X
               '             ','             '                   S21021
         LINE
         BIN        XCSWBSY
         BIN        XCSWPCHK
         ROUTINE SDR4,SDRCONVL                                   S21021
         ROUTINE SDR4,SDRCONVR                                   S21021
         LABEL 'BUSY            ','PROTECTION CHECK',            S21021X
               '             ','             '                   S21021
         LINE
         BIN        XCSWCE
         BIN        XCSWCDC
         ROUTINE SDR5,SDRCONVL                                   S21021
         ROUTINE SDR5,SDRCONVR                                   S21021
         LABEL 'CHANNEL END     ','CHAN DATA CHECK ',            S21021X
               '             ','             '                   S21021
         LINE
         BIN        XCSWDE
         BIN        XCSWCCC
         ROUTINE SDR6,SDRCONVL                                   S21021
         ROUTINE SDR6,SDRCONVR                                   S21021
         LABEL 'DEVICE END      ','CHAN CTL CHECK  ',            S21021X
               '             ','             '                   S21021
         LINE
         BIN        XCSWUC
         BIN        XCSWICC
         ROUTINE SDR7,SDRCONVL                                   S21021
         ROUTINE SDR7,SDRCONVR                                   S21021
         LABEL 'UNIT CHECK      ','I/F CTL CHECK   ',            S21021X
               '             ','             '                   S21021
         LINE       ,,SKIP=2
         BIN        XCSWUE
         BIN        XCSWCH
         ROUTINE SDR8,SDRCONVL                                   S21021
         ROUTINE SDR8,SDRCONVR                                   S21021
         LABEL 'UNIT EXCEPTION  ','CHAINING CHECK  ',            S21021X
               '             ','             '                   S21021
*
         LINE
         ROUTINE    XDUM1,SKIPPER
         LABEL      'SENSE BYTE DATA'
*
         LINE  (15,20),(1,20),SKIP=2                             S21021
         ROUTINE  XB0CMDR,SETHEX                                 S21021
         ROUTINE  XB1LPD,SETHEX                                  S21021
         ROUTINE  XB2BIT0,SETHEX                                 S21021
         ROUTINE  XB3BAR8,SETHEX                                 S21021
         LABEL      'BYTE 0','BYTE 1','BYTE 2','BYTE 3'
*                                       STATUS BYTE
         LINE  (15,20),(1,20),SKIP=1                             S21021
         BIN        XB0CMDR
         BIN        XB1LPD
         BIN        XB2BIT0
         BIN        XB3BAR8
         ROUTINE    XDEVT2,SETLABEL          SET LABELS FOR DIFF DEV
         LABEL      'CMND REJ ','LP DETECT','BIT  0   ','BAR  8   '
         LINE
         BIN        XB0BIT1
         BIN        XB1EOS
         BIN        XB2BAR15
         BIN        XB3BAR7
         LABEL      'BIT  1   ','END O SEQ','BAR 15   ','BAR  7   '
         LINE
         BIN        XB0BUSC
         BIN        XB1CMODE
         BIN        XB2BAR14
         BIN        XB3BAR6
         LABEL      'BUS O CHK','CHAR MODE','BAR 14   ','BAR  6   '
         LINE
         BIN        XB0BIT3
         BIN        XB1BIT3
         BIN        XB2BAR13
         BIN        XB3BAR5
         LABEL      'BIT  3   ','BIT  3   ','BAR 13   ','BAR  5   '
         LINE
         BIN        XB0DCHK
         BIN        XB1BIT4
         BIN        XB2BAR12
         BIN        XB3BAR4
         LABEL      'DATA CHK ','BIT  4   ','BAR 12   ','BAR  4   '
         LINE
         BIN        XB0BIT5
         BIN        XB1BIT5
         BIN        XB2BAR11
         BIN        XB3BAR3
         LABEL      'BIT  5   ','BIT  5   ','BAR 11   ','BAR  3   '
         LINE
         BIN        XB0BRUN
         BIN        XB1BIT6
         BIN        XB2BAR10
         BIN        XB3BAR2
         LABEL      'BUFF RUNN','BIT  6   ','BAR 10   ','BAR  2   '
         LINE  ,,SKIP=2                                          S21021
         BIN        XB0BIT7
         BIN        XB1BIT7
         BIN        XB2BAR9
         BIN        XB3BAR1
         ROUTINE    XDUM1,RESETR2                                S21021
         LABEL      'BIT  7   ','BIT  7   ','BAR  9   ','BAR  1   '
         LINE  ,,SKIP=1                                          S21021
         LABEL ''                                                S21021
         DC    X'FF'                    END OF TABLE
RECSIZE  DC    X'0000'                  SIZE OF REC- VARIES      S21021
         DC    XL2'0018'
         DC    XL2'0018'                                         S21021
*                                                                S21021
*                                       LABELS FOR 3505,3525     S21021
*                                                                S21021
REDPIKLB LABEL 'CMND REJ','PERM ERROR','BIT 0','BIT 0'           S21021
         LABEL 'INTV REQD','AUTO RETRY','BIT 1','BIT 1'          S21021
         LABEL 'BUS O CHK','MOTION MALF','BIT 2','BIT 2'         S21021
         LABEL 'EQUIP CHK','RAIC','BIT 3','BIT 3'                S21021
         LABEL 'DATA CHK','BIT 4','BIT 4','BIT 4'                S21021
         LABEL 'BIT 5','BIT 5','BIT 5','BIT 5'                   S21021
         LABEL 'AB FORM RST','BIT 6','BIT 6','BIT 6'             S21021
         LABEL 'PERM ERR-PEK','BIT 7','BIT 7','BIT 7'            S21021
SHRTOBRL LABEL      ''                                           S21021
SHRTOBRS LINE  ,(1,1),SKIP=2                                     S21021
         ROUTINE    XDUM1,SKIPPER                                S21021
         LABEL      'STATISTICAL DATA'                           S21021
*                                                                S21021
         LINE       (21,30),(1,30),SKIP=1                        S21021
         ROUTINE    SDR1,SDRCONVL                                S21021
         ROUTINE    SDR1,SDRCONVR                                S21021
         ROUTINE  XDUM1,SETSTLBL                                 S21088
         LABEL '             ','             '                   S21021
*                                                                S21021
         LINE
         ROUTINE    SDR2,SDRCONVL                                S21021
         ROUTINE    SDR2,SDRCONVR                                S21021
         LABEL '             ','             '                   S21021
         LINE
         ROUTINE    SDR3,SDRCONVL                                S21021
         ROUTINE SDR3,SDRCONVR                                   S21021
         LABEL '             ','             '                   S21021
         LINE
*                                                                S21021
         ROUTINE SDR4,SDRCONVL                                   S21021
         ROUTINE SDR4,SDRCONVR                                   S21021
         LABEL '             ','             '                   S21021
         LINE
         ROUTINE SDR5,SDRCONVL                                   S21021
         ROUTINE SDR5,SDRCONVR                                   S21021
         LABEL '             ','             '                   S21021
         LINE
         ROUTINE SDR6,SDRCONVL                                   S21021
         ROUTINE SDR6,SDRCONVR                                   S21021
         LABEL '             ','             '                   S21021
         LINE
         ROUTINE SDR7,SDRCONVL                                   S21021
         ROUTINE SDR7,SDRCONVR                                   S21021
         LABEL '             ','             '                   S21021
         LINE
         ROUTINE SDR8,SDRCONVL                                   S21021
         ROUTINE SDR8,SDRCONVR                                   S21021
         LABEL '             ','             '                   S21021
         LINE
         ROUTINE    XDUM1,RESETR2                                S21021
         LABEL      ''                                           S21021
*                                                                S21021
         DC         X'FF'                                        S21021
         DC         X'0028'                                      S21021
         DC         X'0018'                                      S21021
         DC    XL2'0018'                                         S21021
LABELS   DS    0F
         DC    C'$'
         LSTART
         END
