         TITLE 'IFCET040 - 8809 MDR EDIT'
         GBLC  &CSECT,&DSECT
***********************************************************************
**                                                                   **
**                                                                   **
** MODULE NAME:   IFCET040                                           **
**                                                                   **
** DESCRIPTIVE-NAME: DETAILED EDITED STATISTICAL REPORT FOR MDR      **
**                   RECORDS FOR THE 8809                            **
**                                                                   **
** COPYRIGHT:  NONE                                                  **
**                                                                   **
** STATUS:  N/A                                                      **
**                                                                   **
** FUNCTION:   PROVIDES A DETAILED EDITED STATISTICAL REPORT FOR     **
**             ANY MDR RECORDS                                       **
**                                                                   **
** NOTES:                                                            **
**                                                                   **
**                                                                   **
**   DEPENDENCIES : THIS MODULE WAS ASSEMBLED USING THE EBCDIC       **
**                  CHARACTER SET.  IT MUST BE RE-ASSEMBLED IF       **
**                  ANOTHER CHARACTER SET IS TO BE USED AT EXECUTION **
**                                                                   **
**   RESTRICTIONS: N/A                                               **
**                                                                   **
**   REGISTER-CONVENTIONS :                                          **
**         REG 2 :  POINTER TO THE MDR RECORD                        **
**         REG 3 :  POINTER TO THE PRINT BUFFER                      **
**         REG 4 :  POINTER TO THE DATA ITEM REFERENCED IN 'ROUTINE' **
**         REG 14:  RETURN POINT FROM 'ROUTINE'                      **
**         REG 15:  SET TO ZERO TO SUPPRESS PRINTING LINE            **
**                                                                   **
**   PATCH-LABEL:  PATCH                                             **
**                                                                   **
** MODULE TYPE:  DATA                                                **
**                                                                   **
**   PROCESSOR:  370 ASSEMBLER                                       **
**                                                                   **
**   MODULE-SIZE:                                                    **
**                                                                   **
**   ATTRIBUTES:  SERIALLY RE-USABLE                                 **
**                                                                   **
** ENTRY-POINT:    N/A                                               **
**                                                                   **
**                                                                   **
**   LINKAGE:  N/A                                                   **
**                                                                   **
** INPUT:  N/A                                                       **
**                                                                   **
** OUTPUT:  N/A                                                      **
**                                                                   **
** EXIT-NORMAL:  N/A                                                 **
**                                                                   **
** EXIT-ERROR :  N/A                                                 **
**                                                                   **
** EXTERNAL-REFERENCES:                                              **
**                                                                   **
**   ROUTINES:  NONE                                                 **
**                                                                   **
**   DATA-AREAS:  NONE                                               **
**                                                                   **
**   CONTROL-BLOCKS:  NONE                                           **
**                                                                   **
** TABLES:  NONE                                                     **
**                                                                   **
** MACROS:  EDIT TABLE MACROS                                        **
**                                                                   **
***********************************************************************
         PROLOG NAME=IFCET040
         DSGEN (KEY,8),(LEVEL,8),(INDSW,8),(DEPSW,8),(RSVD1,16)
         DSGEN (RECNUM,8),(RSVD2,8),(DATE,32),(TIME,32),(VERSION,8)
         DSGEN (CPUSER,24),(CPUID,16),(MCEL,16)
DATAXXX  EQU   *
         DSGEN (CUAAD,16),(VOLSER,48),(BLKLNG,16)
         DSGEN (SENS02,24),(SENS03,8),(SENS46,24),(SENS07,8)
         DSGEN (SENMTE,8),(SENEDC,8),(SENSRC,8),(SENRBF,8)
         DSGEN (SENENV,8),(SENNOP,8),(SENCRE,8),(SENSKE,8)
         DSGEN (SETRK4,8),(SETRK5,8),(SETRKP,8),(SENVEL,8)
         DSGEN (SENTRE,8),(SENTWE,8),(SENROP,16),(SENWOP,16)
         DSGEN (SENS26,8),(SENORN,8),(SEEGAP,16),(SERRTY,16)
         EJECT
&CSECT   CSECT
***********************************************************************
*                        SPECIAL ROUTINES                             *
***********************************************************************
WORKX    DS    D                   DOUBLEWORD WORK AREA
WORKY    DS    F                   FULLWORD WORK AREA
XSAVE    DS    4F
NONE     DC    C'NONE  '
SETDATE  UNPK  WORKX(L5),DATE+1(L3) UNPACK DATE
         MVC   L5(L2,R3),WORKX     MOVE IN YEAR
         MVC   L0(L3,R3),WORKX+2   MOVE IN DAY
         BR    R14
SETVOL1  MVC   L0(L6,R3),VOLSER    GET VOLUME SERIAL
         BR    R14
SETTYPE  CLI   KEY,EOD             EOD OR VOLUME DISMOUNT RECORD ?
         BE    EODB                YES. . .
         CLI   KEY,SDROVFL         STATISTICAL COUNTER OVERFLOW RECORD?
         BE    SDROVFB             YES. . .
         MVC   0(21,R3),UKNM       UNKNOWN TYPE?
         BR    R14                 RETURN
EODB     MVC   0(21,R3),EODM
         BR    R14
SDROVFB  MVC   0(21,R3),SDROVFM
         BR    R14
EOD      EQU   X'90'
SDROVFL  EQU   X'91'
UKNM     DC    CL21'UNKNOWN'
EODM     DC    CL21'VOL DISMOUNT/EOD'
SDROVFM  DC    CL21'SDR OVERFLOW'
MOD3     SH    R3,TOCOL15          MOVE POINTER TO COL15
         CLI   VERSION,MOD3VER     IS IT MODEL 3?
         BNER  R14                 NO . . .RETURN
         MVC   L4(L2,R3),MOD3MSG
         BR    R14                 RETURN
MOD3VER  EQU   X'01'
MOD3MSG  DC    CL2'-3'
TOCOL15  DC    H'17'
* SETSYS REMOVED FOR SYSRELN CHANGES
* SYSDATAREMOVED FOR SYSRELN CHANGES
UNK      DC    XL2'FFFF'
         DC    C'UNKNWN'
XSAVE1   DC    3F'0'
*SETLEVELREMOVED FOR SYSRELN CHANGES
CONVT    CVD   R5,WORKX            CONVERT TO DECIMAL
         UNPK  L0(L2,R3),WORKX+6(2) UNPACK LEVEL
         OI    1(R3),ZONE          SET ZONE FOR PRINT
         L     R5,WORKF            RESTORE CONTENTS
         LM    R5,R6,WORKX         RESTORE
         BR    R14
         DS    0F
LVLMSK   DC    F'31'               RELEASE LEVEL MASK
ZONE     EQU   X'F0'               ZONE BIT MASK
DOSADJ   DC    F'26'               DOS ADJUSTMENT NUMBER
WORKF    DC    F'0'
RECTYPE  TM    KEY,OBRMSK          OBR RECORD?
         BNOR  R14                 NO, CONTINUE PROCESSING
         MVC   XR9PTR,ADDRMDR      CHANGE TO LOAD OBR EDIT
         SR    R15,R15             DO NOT PRINT LINE
         BR    R14
ADDRMDR  DC    A(MDR-4)
OBRMSK   EQU   X'30'
         EJECT
MDR      ETCALL   IFCE8006
         SPACE 3
         ENTRIES
         LINE  (1,1),(1,1),SKIP=E
         ROUTINE  KEY,RECTYPE
         LABEL  ''
         LINE  ,(48,1),SKIP=2
         LABEL '--- I/O DEVICE EDITING ---'
         SPACE 2
         LINE  (14,1),(1,1),SKIP=2
         ROUTINE DEPSW,SETTYPE
         LABEL 'RECORD TYPE  '
         SPACE 2
         LINE  (11,17),(1,20),SKIP=0
         HEX   (CPUID,0,2B)
         ROUTINE VERSION,MOD3
         HEX   (CPUSER,0,3B)
         LABEL 'CPU MODEL','SERIAL',,,'  DAY YEAR            HH MM SS'
         SPACE 2
         LINE  (83,20),(75,20),SKIP=0
         ROUTINE DATE,SETDATE
         HEX   (TIME,1,3B)
         LABEL 'DATE','TIME'
         SPACE 2
         LINE  (1,21),(8,1),SKIP=2
         SYSRELN LEVEL,8
         LABEL ''
         SPACE 2
         LINE  (34,23),(1,44),SKIP=2
         HEX   (CUAAD,0,2B)
         LABEL 'NORMALIZED CHANNEL/UNIT ADDRESS','DEVICE TYPE 8809'
         SPACE 2
         LINE  ,
         LABEL ''
         SPACE 2
         LINE  ,(1,1),SKIP=3
         LABEL 'DEVICE DEPENDENT DATA:'
         SPACE 2
         LINE  (16,1),(1,1),SKIP=2
         ROUTINE VOLSER,SETVOL1
         LABEL 'VOLUME SERIAL'
         SPACE 2
         LINE  (16,1),(1,1),SKIP=2
         CONVT (BLKLNG,2,4)
         LABEL 'BLOCK LENGTH'
         SPACE 2
         LINE  ,
         LABEL ''
         SPACE 2
         LINE  ,(1,1),SKIP=3
         LABEL 'STATISTICAL COUNTERS:'
         SPACE 2
         LINE  ,(8,1),SKIP=1
         LABEL 'TEMP              OPERATIONS        OVER      ERASE    *
                 READ'
         SPACE 2
         LINE  ,(4,1),SKIP=2
         LABEL 'RDS       WRTS        RD       WRT      RUNS       GAPS*
                   RETRIES'
         SPACE 2
         LINE  (4,10),,SKIP=2
         CONVT (SENTRE,1,3)
         CONVT (SENTWE,1,3)
         CONVT (SENROP,2,5)
         CONVT (SENWOP,2,5)
         CONVT (SENORN,1,3)
         CONVT (SEEGAP,2,5)
         CONVT (SERRTY,2,5)
         LABEL ''
         SPACE 2
         LINE  ,
         LABEL ''
         SPACE 2
         LINE  ,(1,1),SKIP=3
         LABEL 'SENSE BYTE COUNTERS:'
         SPACE 2
         LINE  ,(3,11),SKIP=2
         LABEL 'MTE','EDC','SRC','RBF','ENV','NOP','CRE',              *
               'SKFN','TRK 4','TRK 5','TRK P','VEL'
         SPACE 2
         LINE  (3,11),,SKIP=3
         CONVT (SENMTE,1,3),(SENEDC,1,3),(SENSRC,1,3),(SENRBF,1,3)
         CONVT (SENENV,1,3),(SENNOP,1,3),(SENCRE,1,3),(SENSKE,1,3)
         CONVT (SETRK4,1,3),(SETRK5,1,3),(SETRKP,1,3),(SENVEL,1,3)
         LABEL ''
         SPACE 2
         LINE  ,
         LABEL ''
         ETEPILOG RLEN=65
         END
