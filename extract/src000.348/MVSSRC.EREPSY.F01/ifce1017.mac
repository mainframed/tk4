         GBLC  &CSECT,&DSECT
         TITLE 'IFCE1017 -- EDIT TABLE FOR 1017'
***********************************************************************
**                                                                   **
**                                                                   **
**  MODULE NAME:   IFCE1017                                          **
**                                                                   **
**  DESCRIPTIVE-NAME:   EDIT TABLE FOR 1017 (DEVICE CODE 0831)       **
**                                                                   **
**  COPYRIGHT:  NONE                                                 **
**                                                                   **
**  STATUS:    N/A                                                   **
**                                                                   **
**  FUNCTION:   PROVIDES DEVICE EDIT SUPPORT FOR 1017 OBR RECORDS    **
**                                                                   **
**  NOTES:                                                           **
**                                                                   **
**    DEPENDENCIES:   THIS MODULE WAS ASSEMBLED USING THE EBCDIC     **
**                  CHARACTER SET.  IT MUST BE RE-ASSEMBLED IF       **
**                  ANOTHER CHARACTER SET IS TO BE USED AT EXECUTION **
**                                                                   **
**    RESTRICTIONS:   N/A                                            **
**                                                                   **
**    REGISTER-CONVENTIONS:                                          **
**          REG 2  = POINTER TO THE OBR RECORD                       **
**          REG 3  = POINTER TO THE PRINT BUFFER                     **
**          REG 4  = POINTER TO DATA ITEM REFERENCED IN 'ROUTINE'    **
**          REG 14 = RETURN POINT FROM 'ROUTINE'                     **
**          REG 15 = SET TO ZERO TO SUPPRESS PRINTING LINE           **
**                                                                   **
**    PATCH-LABEL:  PATCH                                            **
**                                                                   **
**  MODULE TYPE:    DATA                                             **
**                                                                   **
**    PROCESSOR:    370 ASSEMBLER                                    **
**                                                                   **
**    MODULE-SIZE:                                                   **
**                                                                   **
**    ATTRIBUTES:   SERIALLY RE-USABLE                               **
**                                                                   **
**  ENTRY-POINT:    N/A                                              **
**                                                                   **
**    LINKAGE:      N/A                                              **
**                                                                   **
**  INPUT:     N/A                                                   **
**                                                                   **
**  OUTPUT:    N/A                                                   **
**                                                                   **
**  EXIT-NORMAL:     N/A                                             **
**                                                                   **
**  EXIT-ERROR:      N/A                                             **
**                                                                   **
**  EXTERNAL-REFERENCES:                                             **
**                                                                   **
**    ROUTINES:        NONE                                          **
**                                                                   **
**    DATA-AREAS:      NONE                                          **
**                                                                   **
**    CONTROL-BLOCKS:  NONE                                          **
**                                                                   **
**    TABLES:          NONE                                          **
**                                                                   **
**    MACROS:    EDIT TABLE MACROS                                   **
**                                                                   **
***********************************************************************
         EJECT
         PROLOG NAME=IFCE1017
         DSGEN (KEY,8),(LEVEL,8),(INDSW,8),(DEPSW,8),(RSVD1,16)
         DSGEN (RECNUM,8),(RSVD2,8),(DATE,32),(TIME,32),(VERSION,8)
         DSGEN (CPUSER,24),(CPUID,16),(MCEL,16)
DATAXXXX EQU   *
**      OVERLAY FOR LONG OBR (UNIT CHECK) OK
         DSGEN (TASKID,64)
         DSGEN (CCWCC,8),(CCWDA,24),(CCWFL,8),(CCWUN,8),(CCWCT,16)
         DSGEN (CSWK,8),(CSWCA,24),(CSWUS,8),(CSWCS,8),(CSWCT,16)
         ORG   CSWUS
         DSGEN (CSWUSB,1,0,7),(CSWCSB,1,0,7)
         ORG
         DSGEN (RSVD3,16),(FCUA,16)
         DSGEN (PUBDTC,8),(RSVD4,24),(SDRLNG,8)
         DSGEN (RSVD5,8),(NCUA,16)
         DSGEN (IORETRY,16),(SNSLNG,16)
         DSGEN (SDRBYT,8,1,10),(SNB0,1,0,7)
         DSGEN (SNB,8,EQU,SNB00),(SIOCNT,32)
**      OVERLAY FOR SHORT OBR (EOD, SVC REQ, CTR OFLO)
         ORG   DATAXXXX
         DSGEN (SXTYP,32),(SXSDRLN,8),(SXRSVD,8),(SXNCUA,16)
         DSGEN (SXSDR,4,1,20),(SXSIOCT,32)
         EJECT
&CSECT   CSECT
***********************************************************************
**                                                                   **
**                        SPECIAL ROUTINES                           **
**                                                                   **
**===================================================================**
L26      EQU   26
WORKX    DS    D                   DOUBLEWORD WORK AREA
WORKY    DS    F                   FULLWORD WORK AREA
SETTID   DS    0H
         TM    DEPSW,SHORTOBR      SHORT OBR RECORD?
         BOR   R14                 YES...RETURN
         MVC   L0(L15,R3),TASKIDM  MOVE IN 'TASK ID' LABEL
         MVC   L16(L8,R3),TASKID   MOVE IN TASK ID
         BR    R14                 RETURN
TASKIDM  DC    CL15'TASK IDENTITY -'
         SPACE 3
SETDATE  UNPK  WORKX(L5),DATE+1(L3) UNPACK DATE
         MVC   L6(L2,R3),WORKX     MOVE IN YEAR
         MVC   L0(L3,R3),WORKX+2   MOVE IN DAY
         SPACE 1
SETTYPE  DS    0H
         ST    R5,WORKY            SAVE REG
         LA    R5,67               BACK UP POINTER
         LNR   R5,R5               .  TO COLUMN 15
         AR    R5,R3
         CLI   DEPSW,UCHK          UNIT CHECK RECORD?
         BE    UCHKB               YES...
         CLI   DEPSW,EOD           END OF DAY RECORD?
         BE    EODB                YES...
         CLI   DEPSW,COVFL         COUNTER OVERFLOW RECORD?
         BE    COVFLB              YES...
         CLI   DEPSW,SVCREQ        SVC REQUESTED?
         BE    SVCREQB             YES...
         SPACE 1
         MVC   0(13,R5),UKNM       UNKNOWN TYPE
RSTREG   L     5,WORKY             RESTORE REG
         BR    R14                 RETURN
         SPACE 1
UCHKB    MVC   0(13,R5),UCHKM      UNIT CHECK TYPE
         B     RSTREG
         SPACE 1
EODB     MVC   0(13,R5),EODM       EOD TYPE
         B     RSTREG
         SPACE 1
COVFLB   MVC   0(13,R5),COVFLM     COUNTER OVERFLOW TYPE
         B     RSTREG
         SPACE 1
SVCREQB  MVC   0(13,R5),SVCREQM    SVC REQUESTED
         B     RSTREG
         SPACE 2
UCHK     EQU   X'00'
EOD      EQU   X'A0'
COVFL    EQU   X'60'
SVCREQ   EQU   X'22'
UKNM     DC    CL13'UNKNOWN'
UCHKM    DC    CL13'UNIT CHECK'
EODM     DC    CL13'END OF DAY'
COVFLM   DC    CL13'COUNTER OFLO'
SVCREQM  DC    CL13'SVC REQUESTED'
         SPACE 3
* SETLEVEL REMOVED FOR SYSRELN CHANGES
ZONE     EQU   X'F0'
         SPACE 3
MOD3     SH    R3,TOCOL15          MOVE POINTER TO COL 15
         CLI   VERSION,MOD3VER     IS IT MODEL 3?
         BNER  R14                 NO...RETURN
         MVC   L0(L2,R3),MOD3MSG
         BR    R14                 RETURN
MOD3VER  EQU   X'01'
MOD3MSG  DC    CL2'-3'
TOCOL15  DC    H'9'
         SPACE 3
MOVAHEAD SR    R4,R2               GET DISPLACEMENT
         AR    R3,R4               ADD TO POINTER
         BR    R14                 RETURN
         SPACE 3
XCTR1    ST    R5,WORKY            SAVE REG
         IC    R5,0(R4)            GET COUNTER
         N     R5,CNTRM1           MASK OUT UNWANTED PART
         SRL   R5,L3               SHIFT INTO PLACE
         LA    R5,CHARS(R5)        INDEX INTO CHARACTERS
         MVC   L0(L2,R3),0(R5)     MOVE IN CARACTERS
         L     R5,WORKY            RESTORE REG
         BR    R14
CHARS    DC    C' 0 1 2 3 4 5 6 7 8 9101112131415'
CNTRM1   DC    F'240'
CNTRM2   DC    F'15'
         SPACE 3
XCTR2    ST    R5,WORKY            SAVE REG
         IC    R5,0(R4)            GET COUNTER
         N     R5,CNTRM2           MASK OUT UNWANTED PART
         SLL   R5,L1               SHIFT INTO PLACE
         LA    R5,CHARS(R5)        INDEX INTO CHARACTERS
         MVC   L0(L2,R3),0(R5)     MOVE IN CARACTERS
         L     R5,WORKY            RESTORE REG
         BR    R14
         SPACE 3
SHORT1   DS    0H
         XC    SFLAG(L1),SFLAG     CLEAR SHORT INDICATOR
         XR    R15,R15             CLEAR FOR NO PRINT
         TM    DEPSW,SHORTOBR      SHORT RECORD?
         BZR   R14                 NO...
         MVC   XR9PTR(8),SHRTB1A   BRANCH IN TABLE
         BR    R14                 RETURN
         SPACE 2
SHORT2   DS    0H
         XR    R15,R15             CLEAR FOR NO PRINT
         TM    SFLAG,SHORTOBR      SHORT RECORD?
         BZR   R14                 NO...
         MVC   XR9PTR(8),SHRTB2A   BRANCH IN TABLE
         AH    R2,SDRADJ           ADJUST FOR SHORT REC SDR CTRS
         BR    R14                 RETURN
         SPACE 2
SHORT3   DS    0H
         MVC   XR9PTR(8),SHRTB3A
         XR    R15,R15             CLEAR FOR NO PRINT
         SH    R2,SDRADJ           READJUST RECORD POINTER
         OI    SFLAG,SHORTOBR      INDICATE SHORT RECORD
         BR    R14
         SPACE 2
SHORTOBR EQU   X'20'
SHRTB1A  DC    A(SHRTB1-4),A(SHRTB1X-1)
SHRTB2A  DC    A(SHRTB2-4),A(SHRTB2X-1)
SHRTB3A  DC    A(SHRTB3-4),A(SHRTB3X-1)
SDRADJ   DC    H'32'
SFLAG    DC    X'00'
         SPACE 3
**-------------------------------------------------------------------**
**                            PATCH AREA                             **
         DC    S(PATCH)
PATCH    DC    XL50'0'
**-------------------------------------------------------------------**
         EJECT
         ENTRIES  PAGE
         LINE  ,(48,1),SKIP=2
         LABEL '--- I/O DEVICE EDITING ---'
         SPACE 2
         LINE  (23,1),(82,20),SKIP=1
         ROUTINE TASKID,SETTID
         LABEL 'DAY  YEAR','HH MM SS'
         SPACE 2
         LINE  (82,20),(1,10),SKIP=2
         ROUTINE DATE,SETDATE
         HEX   (TIME,1,3B)
         LABEL 'RECORD TYPE - ',,,,,,,'    DATE -',,'    TIME -'
         SPACE 2
         LINE  (11,13),(1,20),SKIP=1
         HEX   (CPUID,0,2B)
         ROUTINE VERSION,MOD3
         HEX   (CPUSER,0,3B)
         LABEL 'CPU MODEL','SERIAL'
         SPACE 2
         LINE  (19,1),(1,1),SKIP=2
         SYSRELN LEVEL,2
         LABEL ''
         SPACE 2
         LINE  ,
         ROUTINE DEPSW,SHORT1
         LABEL ''
         SPACE 2
         LINE  (31,23),(1,40),SKIP=2
         HEX   (FCUA,0,2B)
         LABEL 'FAILING CHANNEL/UNIT ADDRESS','DEVICE TYPE  1017'
         SPACE 2
         LINE  ,(15,68),SKIP=1
         LABEL 'CC   DA    FL     CT','K    CA    US CS  CT'
         SPACE 2
         LINE  (15,3),(1,76),SKIP=2
         HEX   (CCWCC,0,1B),(CCWDA,0,3B)
         ROUTINE &DSECT+2,MOVAHEAD
         HEX   (CCWFL,0,1B),(CCWUN,0,1B),(CCWCT,0,2B)
         ROUTINE &DSECT+45,MOVAHEAD
         HEX   (CSWK,0,1B),(CSWCA,0,3B)
         ROUTINE &DSECT+2,MOVAHEAD
         HEX   (CSWUS,0,1B),(CSWCS,0,1B),(CSWCT,0,2B)
         LABEL 'FAILING CCW','CSW'
         SPACE 2
         LINE  ,(15,55),SKIP=1
         LABEL '--- UNIT STATUS ---','--- CHANNEL STATUS ---'
         SPACE 2
         LINE  (19,28),(1,28)
         BIN   (CSWUSB0),(CSWUSB4),(CSWCSB0),(CSWCSB4)
         LABEL 'ATTENTION','CHANNEL END','PRGM-CTLD IRPT','CHAN DATA CH*
               ECK'
         SPACE 2
         LINE  ,
         BIN   (CSWUSB1),(CSWUSB5),(CSWCSB1),(CSWCSB5)
         LABEL 'STATUS MODIFIER','DEVICE END','INCORRECT LENGTH','CHAN *
               CTL CHECK'
         SPACE 2
         LINE  ,
         BIN   (CSWUSB2),(CSWUSB6),(CSWCSB2),(CSWCSB6)
         LABEL 'CONTROL UNIT END','UNIT CHECK','PROGRAM CHECK','I/F CTL*
                CHECK'
         SPACE 2
         LINE  ,,SKIP=2
         BIN   (CSWUSB3),(CSWUSB7),(CSWCSB3),(CSWCSB7)
         LABEL 'BUSY','UNIT EXCEPTION','PROTECTION CHECK','CHAINING CHE*
               CK'
         SPACE 2
         LINE  (26,27),(1,34),SKIP=2
         CONVT (IORETRY,2,5),(SIOCNT,4,8)
         LABEL 'NUMBER OF I/O RETRIES -','NUMBER OF SIOS -'
         SPACE 2
SHRTB3   LINE  ,,SKIP=1
SHRTB3X  LABEL 'SDR COUNTERS'
         SPACE 2
         LINE  (20,25),(5,25)
         ROUTINE SDRBYT1,XCTR1
         ROUTINE SDRBYT1,XCTR2
         LABEL 'TEMPY READ','TEMPY WRITE'
         SPACE 2
         LINE  ,
         ROUTINE SDRBYT2,XCTR1
         ROUTINE SDRBYT3,XCTR1
         LABEL 'INTRVN REQD','EQUIP CHECK'
         SPACE 2
         LINE  ,
         ROUTINE SDRBYT2,XCTR2
         ROUTINE SDRBYT8,XCTR2
         LABEL 'BUS OUT CHK','CHAN DATA CHK'
         SPACE 2
         LINE  ,,SKIP=2
         ROUTINE SDRBYT4,XCTR2
         LABEL 'BROKEN TAPE'
         SPACE 2
         LINE  ,,SKIP=1
         ROUTINE DEPSW,SHORT2
         LABEL ''
         SPACE 2
         LINE  ,(1,1)
         LABEL 'SENSE BYTE DATA'
         SPACE 2
         LINE  ,(5,1),SKIP=1
         LABEL 'BYTE   0'
         SPACE 2
         LINE  (5,1),,SKIP=2
         BIN   (SNB,0,8)
         LABEL ''
         SPACE 2
         LINE  ,,SKIP=1
         LABEL 'BYTE   0'
         SPACE 2
         LINE  (20,1)
         BIN   SNB00
         LABEL 'COMMAND REJECT'
         SPACE 2
         LINE  ,
         BIN   SNB01
         LABEL 'INTRVN REQD'
         SPACE 2
         LINE  ,
         BIN   SNB02
         LABEL 'BUS OUT CHK'
         SPACE 2
         LINE  ,
         BIN   SNB03
         LABEL 'EQUIP CHECK'
         SPACE 2
         LINE  ,
         BIN   SNB04
         LABEL 'DATA CHECK'
         SPACE 2
         LINE  ,
         BIN   SNB05
         LABEL 'OVERRUN'
         SPACE 2
         LINE  ,
         BIN   SNB06
         LABEL 'BIT 6'
         SPACE 2
         LINE  ,,SKIP=2
         BIN   SNB07
         LABEL 'BROKEN TAPE'
         SPACE 2
SHRTB2   LINE  ,,SKIP=1
SHRTB2X  LABEL ''
         SPACE 2
         ETDUMP RLEN=4F
         SPACE 2
SHRTB1   LINE  (34,20),(1,40),SKIP=1
         HEX   (SXNCUA,0,2B)
SHRTB1X  LABEL 'NORMALIZED CHANNEL/UNIT ADDRESS','DEVICE TYPE  1017'
         SPACE 2
         LINE  (18,1),(1,1),SKIP=2
         CONVT (SXSIOCT,4,8)
         LABEL 'NUMBER OF SIOS -'
         SPACE 2
         LINE  ,
         ROUTINE DEPSW,SHORT3
         LABEL ''
         SPACE 2
         ETEPILOG NODUMP
         END
