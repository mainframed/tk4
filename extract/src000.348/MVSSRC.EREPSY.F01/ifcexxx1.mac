         TITLE 'IFCEXXX1 - EDIT TABLE - LINKNAME=IFCE2540, ALIASES=IFCEC
               1442,IFCE2501,IFCE2520,IFCE2495'
         SPACE 2
         GBLC  &DSECT,&CSECT
IFCEXXX1 CSECT
&CSECT   SETC  'IFCEXXX1'
&DSECT   SETC  'XXX1'
*
*              EDIT TABLE FOR OBR REC
*
*        DEVICE CODE=0801  DEV TYPE= 2540    ALIAS= IFCE2540
*                    0802            2540           IFCE2540
*                    0803            1442           IFCE1442
*                    0804            2501           IFCE2501
*                    0805            2520           IFCE2520
*                    0818            2495           IFCE2495
*                    0806            2520           IFCE2520
*
*
*
*
*
*
XXX1     DSECT
         DSGEN (XDUM1,8),(XRELN,8),(XDUM2,8),                    S21021X
               (SW2B0,1),(SW2B1,1),(SW2B2,1),(SW2B3,1),          S21021X
               (SW2B4,1),(SW2B5,1),(SW2B6,1),(SW2B7,1)           S21021
         DSGEN (XDUM3,32)                         SPARES
         DSGEN (XDATE,32)                         DATE
         DSGEN (XTIME,32)                         TIME
         DSGEN (XDUM4,8),(XSER,24)           SERIAL NO
         DSGEN (XMODEL,16),(XDUM5,16)        MODEL NUMBER & MCLEN
         DSGEN (XPGN1,32)                    PROGRAM
         DSGEN (XPGN2,32)                            NAME
         DSGEN (XFCCWCC,8),(XFCCWDA,24)  FAILING CCW
         DSGEN (XFCCWFL,8),(XZERO,8),(XFCCWCT,16)    CCW
         DSGEN (XCSWK,8),(XCSWCA,24)         CHAN  STATUS
         DSGEN (XCSWATTN,1),(XCSWSM,1),(XCSWCUE,1),      WORD  CSW     C
               (XCSWBSY,1),(XCSWCE,1),(XCSWDE,1),(XCSWUC,1),(XCSWUE,1),C
               (XCSWPCI,1),(XCSWSILI,1),(XCSWPC,1),(XCSWPCHK,1),       C
               (XCSWCDC,1),(XCSWCCC,1),(XCSWICC,1),(XCSWCH,1),         C
               (XCSWCT,16)
         DSGEN (XDUMA,8),(XCUA2,24)                              S21021
         DSGEN (XDEVT1,16),(XDEVT2,16)       DEVICE TYPE
         DSGEN (XDUM6,8),(XCUA,24)           DUMMY AND CUA
         DSGEN (XDUM7,16),(XDUM8,16)         RTRY AND SPARE
         DSGEN (XDUM9,32)                    VOLUME
         DSGEN (XDUM10,32)                          SER
         DSGEN (XDUM11,32)                   LAST
         DSGEN (XDUM12,32)                        SEEK
         DSGEN (XDUM13,32)                   HOME
         DSGEN (XDUM14,32)                        ADDRESS
         DSGEN (SDR1,8),(SDR2,8),(SDR3,8),(SDR4,8),(SDR5,8),     S21021X
               (SDR6,8),(SDR7,8),(SDR8,8)                        S21021
         DSGEN (SDR9,8),(SDRA,8),(XB0CMDR,1),(XB0INTRQ,1),       S21021X
               (XB0BUSC,1),(XB0ECHK,1),(XB0DCHK,1),                    C
               (XB0BIT5,1),(XB0UCMD,1),(XB0BIT7,1),                    C
               (XDUM18,8)
         DSGEN (XDUM19,32)
EXPNOBR  DSECT                                                   S21021
EXPNHDRA DS CL52                                                 S21021
EXPNDEVA DS CL36                                                 S21021
EXPNSDRA DS CL10                                                 S21021
EXPNSNSA DS CL6                                                  S21021
*                                                                S21021
NORMOBR DSECT                                                    S21021
NORMHDRA DS CL64                                                 S21021
NORMSDRA DS CL10                                                 S21021
NORMSNSA DS CL6                                                  S21021
*                                                                S21021
SHRTOBR DSECT                                                    S21021
SHRTHDRA DS CL24                                                 S21021
SHRTDEVA DS CL8                                                  S21021
SHRTSDRA DS CL10                                                 S21021
*                                                                S21021
*
*        END OF DSECT    104 BYTES TOTAL
*
IFCEXXX1 CSECT
         USING *,R9
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
L0       EQU   0
L1       EQU   1
L2       EQU   2
L3       EQU   3
L4       EQU   4
L5       EQU   5
L6       EQU   6
L7       EQU   7
L8       EQU   8
L9       EQU   9
L10      EQU   10
L11      EQU   11
L12      EQU   12
L13      EQU   13
L14      EQU   14
L15      EQU   15
L16      EQU   16
L17      EQU   17
L18      EQU   18
L19      EQU   19
L20      EQU   20
L21      EQU   21                                                S21021
L22      EQU   22                                                S21021
L24      EQU   24
L26      EQU   26
L28      EQU   28
L29      EQU   29                                                S21021
L30      EQU   30                       LENGTH OF THIRTY       @YM6459P
L31      EQU   31                                                S21021
L34      EQU   34                                                S21021
L36      EQU   36                                                S21021
L44      EQU   44                                                S21021
L46      EQU   46                                                S21021
L52      EQU   52
L64      EQU   64                                                S21021
L104     EQU   104                                               S21021
L120     EQU   120                                               S21021
DMP80    EQU   80                       NORMAL DUMP LENGTH
DMP104   EQU   104                      DUMP LENGTH CONVERTED
HEX08    EQU   X'08'                                             S21021
HEX16    EQU   X'16'                    3890 CODE              @YM6459P
HEX10    EQU   X'10'                                             S21021
HEX20    EQU   X'20'                                             S21021
A        EQU   C'A'                                              S21021
B        EQU   C'B'                                              S21021
         USING XXX1,R2                                           S21021
*
         DC    A(ENTRIES)               PTR TO EDIT ENTRIES
         DC    CL8'IFCEXXX1'            TABLE NAME
XR9PTR   DC    F'0'                     PNTR TO TABLE ENTRY-4    S21021
XLABELP  DC    F'0'
*        SPECIAL ROUTINE SECTION
*
SKIPPER  BR    R14                      NOOP RTRN
*
*
SPACER1  LA    R3,L1(R3)                SPACE ONE BLANK          S21021
         BR    R14                      RETURN
*
*
*        SET BINARY
*              R4 HAS PTR TO CHAR
*
SETBIN   STM   R5,R8,XSAVE              SAVE REGS
         IC    R7,L0(R4)                PICK UP CHAR TO PRINT
         SLL   R7,L24                   MOVE IT TO HI OF R7
         LR    R8,R3                    PRINT POS POINTER
         LA    R5,L8                    LOOP COUNT
SETBINA  SR    R6,R6                    CLEAR REG 6
         SLDL  R6,L1                    MOVE BIT TO R6
         STC   R6,L0(R8)                PUT IT INTO PTINT LINE
         MVZ   L0(L1,R8),CHAR0          MOVE ON PRORPER ZONE
         LA    R8,L1(R8)                BUMP POINTER TO NEXT POS
         BCT   R5,SETBINA               LOOP TILL 8 PRINTED
         B     RETURN                   RETURN
*
SETSTAT1 CLC   0(L2,R4),DEVT2495        IS THIS A 2495?          S21021
         BNE   SETSTAT2                 NO, GO SET 'NOT USED'    S21021
         MVC   STLABSHT(L'POSCHK),POSCHK   SET UP SHORT STAT LABSS21021
         MVC   STLABLNG+L22(L'POSCHK),POSCHK  LONG STAT LABEL    S21021
         B     SETSTAT4
SETSTAT2 MVC   STLABSHT(L'NOTUSED),NOTUSED   SHORT STAT LABEL    S21021
         MVC   STLABLNG+L22(L'NOTUSED),NOTUSED   LONG STAT LABEL S21021
SETSTAT3 CLC   L0(L2,R4),DEVT2540       TEST FOR 2540
         BH    SETSTAT4                 BR NO
         MVC   STATLB40+L41(L'UNUSUAL),UNUSUAL    SET 2540 LABEL
         MVC   STATSH40+L10(L'UNUSUAL),UNUSUAL    SET 2540 LABEL
         BR    R14
SETSTAT4 MVC   STATLB40+L41(L'OVERRUN),OVERRUN                  XL03123
         MVC   STATSH40+L10(L'OVERRUN),OVERRUN                  XL03123
         BR    R14
*
*        SET PROGRAM NAME  R4 POINTS TO NAME FIELD
*
SETPGMN  DS    0H                                                S21021
         TM    SW2B0,HEX20              SHORT RECORD             S21021
         BCR   1,R14                    YES, NO JOB ID           S21021
         MVC   0(L12,R3),JOBID          MOVE IN LABEL            S21021
         MVC   L13(L8,R3),XPGN1         MOVE IN NAME             S21021
         BR    R14                                               S21021
NONAME   TM    L0(R4),HEX20             SHORT RECORD?            S21021
         BCR   14,R14                   NO, LET HEX JOBNAME PRNT S21021
         ST    R5,XSAVE                 SAVE REG                 S21021
         LA    R5,L44                   BACK UP BUFFER PNTR      S21021
         SR    R3,R5                                             S21021
         MVC   0(L17,R3),BLANKS         CLEAR OUT HEX JOBNAME    S21021
         L     R5,XSAVE                 RESTORE REG              S21021
         BR    R14                                               S21021
*
*
*        SET HEX  SAME AS BIN EXCPT PRINTS 1 CHAR HEX
*
SETHEX   STM   R5,R8,XSAVE              SAVE REGS
         LA    R8,CHARS                 CHAR TABLE POINTER
         IC    R7,L0(R4)                PICK UP CHAR
         SLL   R7,L24                   MOVE IT TO HI OF R7
         SR    R6,R6                    CLEAR REGS
         SLDL  R6,L4                    MOVE FOUR BITS
         AR    R6,R8                    ADD OFFSET & PTR
         MVC   L0(L1,R3),L0(R6)         MOVE CHAR TO LINE
         SR    R6,R6                    CLEAR AGAIN
         SLDL  R6,L4                    MOVE LAST 4 BOTS
         AR    R6,R8                    COMPUTE OFFSET
         MVC   L1(L1,R3),L0(R6)         MOVE LAST CHAR
         B     RETURN
*
*
*
SETCONV  TM    L0(R4),HEX02             HAS RECORD BEEN CONVERTED KEY32
         BCR   ON,R14                   YES RETURN
         SR    R15,R15                  NO CLEAR R15 FOR NO PRINT OF
         BR    R14                      LINE BR-14
ON       EQU   1
HEX02    EQU   X'02'
*                                       SET DEVICE TYPE FIELD
*
SETDEVTY STM   R5,R8,XSAVE              SAVE REGS    R3 PTS TO BUFF POS
         LA    R5,L8                    NUMBER OF DEVICES       XL03123
         LA    R6,DEVTABLE
SETDEVLP CLC   L0(L2,L4),L0(R6)         COMPARE TYPES
         BE    SDEVFND                  BR-EQUAL
         LA    R6,L6(R6)                UP TO NXT DEV TYPE
         BCT   R5,SETDEVLP              LOOP
         MVC   L0(L4,R3),NONE           NONE FOUND USE NONE
         B     RETURN
SDEVFND  MVC   L0(L4,R3),L2(R6)         MOVE DEV NAME TO LINE
         B     RETURN
*
*
*                             CHECK ON CPU AND SERIAL NO
SETCPUNO STM   R5,R8,XSAVE
         SR    R5,R5               CLEAR R5
         IC    R5,L0(R4)           PICK UP MODEL NO
         SLL   R5,L8               IN TO
         IC    R5,L1(R4)           R5
         LTR   R5,R5                    TEST FOR ZERO MODEL      S21021
         LR    R5,R3                    POINT TO DATA FIELD      S21021
         BNZ   SETSER                   BRANCH IF MODEL NOT 0    S21021
         LA    R6,L46                   BACK UP TO SAY 'NONE'    S21021
         SR    R5,R6                    BACKUP BUFFER            S21021
         MVC   L0(L29,R5),UNIVERS       SAY NONE, MODEL UNIVERSALS21021
         B     RETURN                   RETURN  CONTROL
SETSER   LA    R6,L36                   SET UP TO ADD 'SERIAL NO.S21021
         SR    R5,R6                                             S21021
         MVC   0(L10,R5),SERNO                                   S21021
         B     RETURN                                            S21021
*
*        SET DATE
*
SETDATE  CLC   L1(L3,R4),ZERO           IS DATE ZERO
         BE    SETDTE                   BR-YES
         UNPK  XSAVE(L8),L1(L3,R4)      UNPACK DATE
         MVC  L5(L2,R3),XSAVE+L3  MOVE YEAR
         MVC   L0(L3,R3),XSAVE+L5       MOVE DAY
         BR    R14                      RETURN
SETDTE   MVC   L0(L4,R3),NONE           SET DATE TO NONE
         BR    R14
ZERO     DC    XL4'00'
*
*
SETLABEL STM   R5,R8,XSAVE              SAVE REGS
         L     R5,XLABELP               PICK UP LABEL PTR
         CLI  L1(R4),HEX16              TEST FOR 3890          @YM6459P
         BE    SET3890                  YES-SET UP FOR 3890    @YM6459P
         CLI   L1(R4),HEX18             TEST FOR 2495
         BE    SET2495                  BR-YES
         CLI   L1(R4),HEX03             TEST FOR 2540
         BL    RETURN                   BR-YES
SET1442A MVC   L9(L20,R5),CLABEL1       LABELS 2501,2520,1442  @YM6459P
         B     RETURN
SET3890  MVC   L9(L30,R5),CLABEL3       SET LABELS 3890        @YM6459P
         B     RETURN                   RETURN                 @YM6459P
SET2495  MVC   L9(L20,R5),CLABEL2       SET LABELS 2495
*
RETURN   LM    R5,R8,XSAVE              RESTORE REG
         BR    R14                      RETURN
SPACER16 LA    R3,L16(R3)                                        S21021
         BR    R14                                               S21021
SDRCONVL STM   R5,R8,XSAVE              SAVE REGS                S21021
         SR    R6,R6                    CLEAR REG                S21021
         IC    R6,L0(R4)                PICK UP COUNTER IN QUEST S21021
         SRL   R6,L4                    DROP OFF 4 LOW BITS      S21021
         B     SDRCONVT                                          S21021
SDRCONVR STM   R5,R8,XSAVE              SAVE REGS                S21021
         IC    R6,L0(R4)                PICK UP CHAR             S21021
         SLL   R6,L28                                            S21021
         SRL   R6,L28                   DROP OFF HI ORDER BITS   S21021
SDRCONVT CVD   R6,WORK                  CONVERT IT               S21021
         UNPK  L0(L3,R3),WORK+L5(L3)    TO DECIMAL               S21021
         MVZ   L2(L1,R3),L0(R3)         PUT ON ZONE              S21021
         B     RETURN                                            S21021
*                                                                S21021
RESETR2  L     R2,SAVE2                 PICK UP SAVED REC POINTERS21021
         BR    R14                                               S21021
*                                                                S21021
TESTMP   TM    L0(R4),HEX10             IS IT A MP RECODR        S21021
         BZ    NOMP                                              S21021
         MVC   0(L20,R3),MPCPU          SHOW MULTIPROC           S21021
         TM    L0(R4),HEX08             IS IT CPUA / CPUB        S21021
         BO    MPB                      BR IT IS B               S21021
         MVI   L21(R3),A                MOVE 'A' TO PRINT LINE   S21021
NOMP     BR    R14                      RETURN                   S21021
MPB      MVI   L21(R3),B                MOVE 'B' TO PRINT LINE   S21021
         BR    R14                      RETURN                   S21021
NOPRT    SR    R15,R15                  STOP PRINTING            S21021
         BR    R14                                               S21021
*                                                                S21021
TESTSHRT TM    SW2B0,HEX20              IS IT A SHORT REC        S21021
         BZ    SKIPPER                  BR-NO                    S21021
         BCTR  R3,R0                    BACKUP ONE DATA SPACE    S21021
         BCTR  R3,R0                    BACKUP ONE DATA SPACE    S21021
         MVC   0(L6,R3),BLANKS          CLEAR OUT HEX 2NDARY CUA S21021
         MVC   XR9PTR(L8),SHRTADCN      MOVE IN NEW A POINTERS   S21021
         BR    R14                                               S21021
*                                                                S21021
EXPNDRTN STM   R5,R8,XSAVE                                       S21021
         ST    R2,SAVE2                 SAVE R2 FOR LATER        S21021
         LA    R5,OBRAREA               PICK UP EXPANDED AREA    S21021
         USING EXPNOBR,R5                                        S21021
         USING NORMOBR,R4                                        S21021
         XC    EXPNOBR(L104),EXPNOBR    CLEAR AREA               S21021
         TM    SW2B0,HEX20              IS IT A SHORT REC        S21021
         BO    MOVESHRT                 BR-YES                   S21021
         TM    XDUM1,HEX02              IS IT A CONVERTED REC    S21021
         BO    MOVEALL                                           S21021
         MVC   EXPNHDRA(L64),NORMHDRA   EXPAND RECORD TO CONVERTES21021
         MVC   EXPNSDRA(L10),NORMSDRA                            S21021
         MVC   EXPNSNSA(L6),NORMSNSA                             S21021
         MVI   DMPSIZE+L1,DMP80         DUMP 80 BYTES
EXPNDRET LR    R2,R5                    SWAP POINTERS            S21021
         B     RETURN                                            S21021
MOVEALL  MVC   EXPNOBR(L104),NORMOBR    MOVE ENTIRE RECORD       S21021
         MVI   DMPSIZE+L1,DMP104        DUMP 104 BYTES
         B     EXPNDRET                                          S21021
         USING SHRTOBR,R4                                        S21021
MOVESHRT MVC   EXPNHDRA(L24),SHRTHDRA   MOVE HEADER              S21021
         MVC   EXPNDEVA(L8),SHRTDEVA                             S21021
         MVC   EXPNSDRA(L10),SHRTSDRA                            S21021
         B     EXPNDRET                                          S21021
         DROP  R4,R5                                             S21021
*                                                                S21021
RECTYPE  STM   R5,R8,XSAVE                                       S21021
         LR    R5,R3                    SET UP DATA SLOT FOR TYPES21021
         LA    R6,L31                                            S21021
         SR     R5,R6                                            S21021
         TM    0(R4),HEX20              SHORT RECORD?            S21021
         BO    RECSHORT                                          S21021
         MVC   0(L10,R5),UNITCK         INSERT DATA 'UNIT CHECK' S21021
         B     RETURN                                            S21021
RECSHORT MVC   0(L10,R5),OVFL           DATA = 'OVERFLOW'        S21021
         B     RETURN                                            S21021
WORK     DS    D                                                 S21021
SAVE2    DS    F                                                 S21021
SHRTADCN DC    A(SHRTOBRS-L4)                                    S21021
         DC    A(SHRTOBRL)                                       S21021
OBRAREA  DS    CL104                                             S21021
BLANKS   DC    CL25' '                                           S21021
HW21     DC    H'21'                                             S21021
HW13     DC    H'13'                                             S21021
DEVT2495 DC    X'0818'                                           S21021
DEVT2540 DC    X'0802'
DEV3890  DC    X'0816'       SENECA                             XL03123
UNUSUAL  DC    C'UNUSL CMD SEQ'
L41      EQU   41
NOTUSED  DC    C'NOT USED     '                                  S21021
POSCHK   DC    C'POSITION CHK '                                  S21021
UNITCK   DC    C'UNIT CHECK'                                     S21021
OVFL     DC    C'OVERFLOW  '                                     S21021
SERNO    DC    C'SERIAL NO.'                                     S21021
MPCPU    DC    C'MULTIPROCESSOR - CPU'                           S21021
UNIVERS  DC    C'UNIVERSAL   SERIAL NO. NONE  '                  S21021
JOBID    DC    C'JOB IDENTITY'                                   S21021
OVERRUN  DC    CL13'OVERRUN'                                    XL03123
HEX03    EQU   X'03'
HEX18    EQU   X'18'
         DS    0F
XSAVE    DC    5F'0'
NONE     DC    CL6'NONE  '
CHARS    DC    C'0123456789ABCDEF'
CHAR0    DC    C'0'
CLABEL1  DC    CL10'OVERN CHK$'
         DC    CL10'BIT  6   $'
CLABEL2  DC    CL10'BIT  5   $'
         DC    CL10'POS CHECK$'
CLABEL3  DC    CL10'OVERN CHK$'                                @YM6459P
         DC    CL10'NOT INIT $'                                @YM6459P
         DC    CL10'RUNNING  $'                                @YM6459P
         DS    0F
DEVTABLE DC    X'0801'
         DC              C'2540'
         DC    X'0802'
         DC              C'2540'
         DC    X'0803'
         DC              C'1442'
         DC    X'0804'
         DC              C'2501'
         DC    X'0805'
         DC              C'2520'
         DC    X'0818'
         DC              C'2495'
         DC    X'0806'
         DC              C'2520'
         DC    X'0816'       SENECA                             XL03123
         DC              C'3890'                                XL03123
*
ENTRIES  DS    0F
         DC    XL4'00010001'            FORMAT1
         DC    XL4'00010001'            FORMAT2
         DC    A(LABELS)                LABEL PTR
         DC    H'0'                     DISP
*
*        SKIP TO CHANNEL 1
*
         DC    X'02'                                             S21021
         DC         X'000B8B00'         SKIP TO CHANNEL 1
         DC    AL.12(XDUM1-XXX1),XL.4'D'  = ROUTINE XDUM1,EXPNDRTS21021
         DC    AL.4(0),AL.12(EXPNDRTN-IFCEXXX1)       TLBL       S21021
*
*
         LINE  (55,23),(1,35),SKIP=1                             S21021
         ROUTINE  SW2B0,RECTYPE                                  S21021
         HEX   XMODEL,0,2B                                       S21021
         HEX   XSER,0,3B                                         S21021
         ROUTINE XMODEL,SETCPUNO                                 S21021
         LABEL '---RECORD ENTRY TYPE -','   SOURCE - OUTBOARD',  S21021X
               'MODEL-'                                          S21021
         LINE  (16,1),(4,1)                                      S21021
         SYSRELN XRELN,4                                       @D35UGFD
         LABEL      ''                                         @D35UGFD
         LINE  (69,1),(31,19)                                    S21021
         ROUTINE XPGN1,SETPGMN                                   S21021
         LABEL 'DAY YEAR','HH MM SS.TH'                          S21021
         LINE  (31,19),(25,19)                                   S21021
         ROUTINE XDATE,SETDATE                                   S21021
         HEX   XTIME,1,4B                                        S21021
         ROUTINE  XDUM1,SKIPPER                                  S21021
         HEX   XPGN1,B6,4B                                       S21021
         HEX   XPGN2,B16,4B                                      S21021
         ROUTINE  SW2B0,NONAME                                   S21021
         LABEL 'DATE-','TIME-'                                   S21021
         LINE  (1,1),(1,1)                                       S21021
         ROUTINE SW2B0,TESTMP                                    S21021
         LABEL ''                                                S21021
         LINE  ,(1,1),SKIP=2                                     S21021
         ROUTINE XDUM1,SETCONV                                   S21021
         LABEL 'OBR RECORD CONVERTED TO THE STANDARD FORMAT'     S21021
         LINE  (34,1),(2,1),SKIP=1                               S21021
         ROUTINE XDEVT2,SETDEVTY                                 S21021
         LABEL 'DEVICE TYPE'                                     S21021
         LINE  (34,1),(2,1)                                      S21021
         HEX   XCUA,B0,3B                                        S21021
         LABEL 'PRIMARY   CHANNEL UNIT ADDRESS  '                S21021
         LINE
         HEX   XCUA2,B0,3B                                       S21021
         ROUTINE  XDEVT2,SETSTAT1       SET LABEL FOR 2495       S21021
         ROUTINE  XDUM1,TESTSHRT        IF SHORT REC, GO TO STATSS21021
         LABEL 'ALTERNATE CHANNEL UNIT ADDRESS  '                S21021
         LINE
         LABEL ' '                                               S21021
         LINE  (1,1),(23,38)                                     S21021
         LABEL 'CC   CA   FL     CT','K    CA   US CS  CT'       S21021
         LINE  (23,3),(5,51),SKIP=2                              S21021
         HEX   XFCCWCC,0,1B                                      S21021
         HEX   XFCCWDA,0,3B                                      S21021
         ROUTINE  XDUM1,SPACER1                                  S21021
         HEX   XFCCWFL,0,1B                                      S21021
         HEX   XZERO,0,1B                                        S21021
         HEX   XFCCWCT,0,2B                                      S21021
         ROUTINE  XDUM1,SPACER16                                 S21021
         HEX   XCSWK,0,1B                                        S21021
         HEX   XCSWCA,0,3B                                       S21021
         ROUTINE  XDUM1,SPACER1                                  S21021
         ROUTINE XCSWATTN,SETHEX                                 S21021
         ROUTINE XCSWPCI,SETHEX                                  S21021
         HEX   XCSWCT,0,2B                                       S21021
         LABEL 'FAILING CCW','CSW'                               S21021
*
         LINE  ,(5,28)                                           S21021
         ROUTINE    XDUM1,SKIPPER
         LABEL 'UNIT STATUS','CHANNEL STATUS',                   S21021X
               'STATISTICAL DATA','STATISTICAL DATA'             S21021
*
*                     CSW DETAIL  OF US & CS FIELDS
*
         LINE  (20,28),(3,28),SKIP=1                             S21021
         BIN        XCSWATTN
         BIN        XCSWPCI
         ROUTINE    SDR1,SDRCONVL                                S21021
         ROUTINE    SDR1,SDRCONVR                                S21021
         LABEL 'ATTENTION','PRGM-CTLD TRPT',                     S21021X
               'TEMPY READS  ','TEMPY WRITES '                   S21021
         LINE
         BIN        XCSWSM
         BIN        XCSWSILI
         ROUTINE    SDR2,SDRCONVL                                S21021
         ROUTINE    SDR2,SDRCONVR                                S21021
         LABEL 'STATUS MODIFIER','INCORRECT LENGTH',             S21021X
               'INTRVN REQD  ','BUS OUT CHK  '                   S21021
         LINE
         BIN        XCSWCUE
         BIN        XCSWPC
         ROUTINE    SDR3,SDRCONVL                                S21021
         ROUTINE SDR3,SDRCONVR                                   S21021
STATLB40 LABEL 'CONTROL UNIT END','PROGREM CHECK',                     C
               'EQUIP CHK','UNUSL CMD SEQ'
         LINE
         BIN        XCSWBSY
         BIN        XCSWPCHK
         ROUTINE SDR4,SDRCONVL                                   S21021
         ROUTINE SDR4,SDRCONVR                                   S21021
STLABLNG LABEL 'BUSY','PROTECTION CHECK',                        S21021X
               'NOT USED     ','NOT USED'                        S21021
         LINE
         BIN        XCSWCE
         BIN        XCSWCDC
         ROUTINE SDR5,SDRCONVL                                   S21021
         ROUTINE SDR5,SDRCONVR                                   S21021
         LABEL 'CHANNEL END','CHAN DATA CHECK',                  S21021X
               'NOT USED','NOT USED'                             S21021
         LINE
         BIN        XCSWDE
         BIN        XCSWCCC
         ROUTINE SDR6,SDRCONVL                                   S21021
         ROUTINE SDR6,SDRCONVR                                   S21021
         LABEL 'DEVICE END','CHAN CTL CHECK',                    S21021X
               'NOT USED','NOT USED'                             S21021
         LINE
         BIN        XCSWUC
         BIN        XCSWICC
         ROUTINE SDR7,SDRCONVL                                   S21021
         ROUTINE SDR7,SDRCONVR                                   S21021
         LABEL 'UNIT CHECK','I/F CTL CHECK',                     S21021X
               'NOT USED','NOT USED'                             S21021
         LINE       ,,SKIP=2
         BIN        XCSWUE
         BIN        XCSWCH
         ROUTINE SDR8,SDRCONVL                                   S21021
         ROUTINE SDR8,SDRCONVR                                   S21021
         LABEL 'UNIT EXCEPTION','CHAINING CHECK',                S21021X
               'NOT USED','CHAN DATA CHK'                        S21021
*
         LINE  ,,SKIP=2                                          S21021
         ROUTINE    XDUM1,SKIPPER
         LABEL      'SENSE BYTE DATA'
*
         LINE  (11,19),(1,19),SKIP=2                             S21021
         ROUTINE  XB0CMDR,SETHEX                                 S21021
         LABEL      'BYTE 0'
*                                       STATUS BYTE
         LINE  ,,SKIP=1                                          S21021
         BIN        XB0CMDR
         LABEL      'CMND REJ'
         LINE
         BIN        XB0INTRQ
         LABEL      'INTV REQD'
         LINE
         BIN        XB0BUSC
         LABEL      'BUS O CHK'
         LINE
         BIN        XB0ECHK
         LABEL      'EQUIP CHK'
         LINE
         BIN        XB0DCHK
         ROUTINE    XDEVT2,SETLABEL          SET LABELS FOR DIFF DEV
         LABEL      'DATA CHK'
         LINE
         BIN        XB0BIT5
         LABEL      'BIT  5   '
         LINE
         BIN        XB0UCMD
         LABEL      'UCMD SEQ '
         LINE       ,,SKIP=3
         BIN        XB0BIT7
         LABEL      'BIT  7'
         ROUTINE    XDUM1,RESETR2                                S21021
         DC    X'FF'                    END OF TABLE
DMPSIZE  DC    XL2'0000'                SIZE TO DUMP, VARIES
        DC     XL2'0018'
         DC    X'0018'                                           S21021
         LINE  ,,SKIP=1                                          S21021
SHRTOBRL LABEL      ''                                           S21021
SHRTOBRS LINE  ,(1,1),SKIP=2                                     S21021
         ROUTINE    XDUM1,SKIPPER                                S21021
         LABEL      'STATISTICAL DATA'                           S21021
*                                                                S21021
         LINE       (21,30),(1,30),SKIP=1                        S21021
         ROUTINE    SDR1,SDRCONVL                                S21021
         ROUTINE    SDR1,SDRCONVR                                S21021
         LABEL 'TEMPY READS  ','TEMPY WRITES '                   S21021
*                                                                S21021
         LINE
         ROUTINE    SDR2,SDRCONVL                                S21021
         ROUTINE    SDR2,SDRCONVR                                S21021
         LABEL 'INTRVN REQD  ','BUS OUT CHK  '                   S21021
         LINE
         ROUTINE    SDR3,SDRCONVL                                S21021
         ROUTINE SDR3,SDRCONVR                                   S21021
STATSH40 LABEL 'EQUIP CHK','UNUSL CMD SEQ'
         LINE
*                                                                S21021
         ROUTINE SDR4,SDRCONVL                                   S21021
         ROUTINE SDR4,SDRCONVR                                   S21021
STLABSHT LABEL 'NOT USED     ','NOT USED'                        S21021
         LINE
         ROUTINE SDR5,SDRCONVL                                   S21021
         ROUTINE SDR5,SDRCONVR                                   S21021
         LABEL 'NOT USED','NOT USED'                             S21021
         LINE
         ROUTINE SDR6,SDRCONVL                                   S21021
         ROUTINE SDR6,SDRCONVR                                   S21021
         LABEL 'NOT USED','NOT USED'                             S21021
         LINE
         ROUTINE SDR7,SDRCONVL                                   S21021
         ROUTINE SDR7,SDRCONVR                                   S21021
         LABEL 'NOT USED','NOT USED'                             S21021
         LINE
         ROUTINE SDR8,SDRCONVL                                   S21021
         ROUTINE SDR8,SDRCONVR                                   S21021
         LABEL 'NOT USED','CHAN DATA CHK'                        S21021
         LINE
         ROUTINE    XDUM1,RESETR2                                S21021
         LABEL      ''                                           S21021
*                                                                S21021
         DC         X'FF'                                        S21021
         DC         X'0028'                                      S21021
         DC         X'0018'                                      S21021
         DC    X'0018'                                           S21021
*                                                                S21021
LABELS   DS    0F
         DC    C'$'
         LSTART
         END
