         TITLE 'IFCEI115 - (ALIAS IFCEI125) CCH EDIT TABLE FOR 115 AND *
               125 CPUS'
***********************************************************************
**                                                                   **
**                                                                   **
**  MODULE NAME:   IFCEI115    (ALIAS IFCEI125)                      **
**                                                                   **
**  DESCRIPTIVE-NAME:   EDIT TABLE FOR 0115 AND 0125 CPUS            **
**                                                                   **
**  COPYRIGHT:  NONE                                                 **
**                                                                   **
**  STATUS:     N/A                                                  **
**                                                                   **
**  FUNCTION:   PROVIDES EDIT SUPPORT FOR 0115 AND 0125 CCH RECORDS  **
**                                                                   **
**  NOTES:                                                           **
**                                                                   **
**    DEPENDENCIES:   THIS MODULE WAS ASSEMBLED USING THE EBCDIC     **
**                 CHARACTER SET.  IT MUST BE REASSEMBLED IF ANOTHER **
**                CHARACTER SET IS TO BE USED AT EXECUTION.         **
**                                                                   **
**    RESTRICTIONS:   N/A                                            **
**                                                                   **
**    REGISTER-CONVENTIONS:                                          **
**          REG 2  = POINTER TO THE CCH RECORD                       **
**          REG 3  = POINTER TO THE PRINT BUFFER                     **
**          REG 4  = POINTER TO DATA ITEM REFERENCED IN 'ROUTINE'    **
**          REG 14 = RETURN POINT FROM 'ROUTINE'                     **
**          REG 15 = SET TO ZERO TO SUPPRESS PRINTING LINE           **
**                                                                   **
**    PATCH-LABEL:  PATCH                                            **
**                                                                   **
**  MODULE TYPE:    DATA                                             **
**                                                                   **
**    PROCESSOR:    370 ASSEMBLER                                    **
**                                                                   **
**    MODULE-SIZE:                                                   **
**                                                                   **
**    ATTRIBUTES:   SERIALLY REUSABLE                                **
**                                                                   **
**  ENTRY-POINT:    N/A                                              **
**                                                                   **
**    LINKAGE:      N/A                                              **
**                                                                   **
**  INPUT:     N/A                                                   **
**                                                                   **
**  OUTPUT:    N/A                                                   **
**                                                                   **
**  EXIT-NORMAL:     N/A                                             **
**                                                                   **
**  EXIT-ERROR:      N/A                                             **
**                                                                   **
**  EXTERNAL-REFERENCES:                                             **
**                                                                   **
**    ROUTINES:        NONE                                          **
**                                                                   **
**    DATA-AREAS:      NONE                                          **
**                                                                   **
**    CONTROL-BLOCKS:  NONE                                          **
**                                                                   **
**    TABLES:          NONE                                          **
**                                                                   **
**    MACROS:    EDIT TABLE MACROS                                   **
**                                                                   **
***********************************************************************
         EJECT
         GBLC  &CSECT,&DSECT
         PROLOG NAME=IFCEI115
L35      EQU   35
L45      EQU   45
L64      EQU   64
         DSGEN (RECTYP,8),(LEVEL,8),(FLAG1,8),(FLAG2,8)
         DSGEN (RSVD1,16),(NOFN,8),(RSVD2,8)
         DSGEN (DATE,32),(TIME,32),(VERSION,8),(SERIAL,24)
         DSGEN (CPUID,16),(MCEL,16)
         DSGEN (JOBID,64),(CUU,16,1,8)
         DSGEN (CCWCC,8),(CCWDA,24),(CCWFL,8),(CCWXX,8),(CCWCT,16)
         DSGEN (CSWK,8),(CSWCA,24),(CSWUS,1,0,7),(CSWCS,1,0,7),(CSWCT,1*
               6)
         DSGEN (ECSW,1,0,3,0,7)
         DSGEN (ECSWX3,3,EQU,ECSW35),(ECSWX2,2,EQU,ECSW30)
         DSGEN (CSWUS,8,EQU,CSWUS0),(CSWCS,8,EQU,CSWCS0)
         DSGEN (DEVTP,32),(CHANID,8),(RSVD3,8),(CUA,16),(MPI,32)
INDEPLOG DS    CL120               INDEPENDENT LOGOUT
         EJECT
&CSECT   CSECT
***********************************************************************
**                        SPECIAL ROUTINES                           **
***********************************************************************
SETID    MVC   L0(L8,R3),L0(R4)
         BR    R14
SETDATE  UNPK  WORKX(L5),DATE+1(L3)
         MVC   L0(L3,R3),WORKX+2
         MVC   L6(L2,R3),WORKX
         BR    R14
WORKX    DC    D'0'
         SPACE 3
TYPCHAN  ST    R5,XSAVE            SAVE REG
         SRDL  R4,L64              CLEAR REGS
         TM    CHANID,X'F0'        THESE BITS ON?
         BNZ   USC                 YES...NG CHANNEL ID
         IC    R5,CHANID           GET CHANNEL ID
         SLL   R5,L2               TIMES 4
USC      LA    R5,CHANTBL(R5)      INDEX INTO TABLE
         IC    R4,L0(R5)           GET LENGTH OF MESSAGE
         BCTR  R4,R0               MINUS ONE FOR EXECUTE
         ICM   R5,L7,L1(R5)        GET ADDRESS OF MESSAGE
         EX    R4,MOVEMSG          MOVE IN THE MESSAGE
         L     R5,XSAVE            RESTORE REG
         BR    R14                 GO HOME
MOVEMSG  MVC   L0(L0,R3),L0(R5)
CHANTBL  DC    AL1(L'UNSUP),AL3(UNSUP)
         DC    AL1(L'MPX),AL3(MPX)
         DC    AL1(L'SEL),AL3(SEL)
         DC    AL1(L'BLKMPX),AL3(BLKMPX)
         DC    AL1(L'UNSUP),AL3(UNSUP)
         DC    AL1(L'UNSUP),AL3(UNSUP)
         DC    AL1(L'UNSUP),AL3(UNSUP)
         DC    AL1(L'UNSUP),AL3(UNSUP)
         DC    AL1(L'UNSUP),AL3(UNSUP)
         DC    AL1(L'UNSUP),AL3(UNSUP)
         DC    AL1(L'IFA),AL3(IFA)
         DC    AL1(L'UNSUP),AL3(UNSUP)
         DC    AL1(L'BYTMPX),AL3(BYTMPX)
         DC    AL1(L'NMT),AL3(NMT)
         DC    AL1(L'NDASD),AL3(NDASD)
         DC    AL1(L'NUR),AL3(NUR)
UNSUP    DC    C'UNSUPPORTED CHANNEL'
MPX      DC    C'MPX CHANNEL'
SEL      DC    C'SEL CHANNEL'
BLKMPX   DC    C'BLK MPX CHANNEL'
IFA      DC    C'INT FILE ADAPTER CHANNEL'
BYTMPX   DC    C'TRUE BYTE MPX CHANNEL'
NMT      DC    C'NATIVE MAGNETIC TAPE'
NDASD    DC    C'NATIVE DISK'
NUR      DC    C'CRT, CARD I/O, LINE PRINTER, COMMUNICATIONS ADAPTER (A*
               LL NATIVELY ATTACHED)'
         SPACE 3
CKIODEVS TM    MPI,X'80'           IO DEVICES INVALID?
         BO    IONG                ...INVALID...TELL HIM SO
         MVC   L0(L35,R3),IOOKMSG
         BR    R14
IONG     MVC   L0(L45,R3),IONGMSG
         MVC   XR9PTR(L4),DUMPADDR TERMINATE PROCESSING
         BR    R14                 RETURN
IOOKMSG  DC    C'I/O UNITS IN USE AT TIME OF FAILURE'
IONGMSG  DC    C'ACTIVE I/O UNITS AT TIME OF ERROR UNAVAILABLE'
DUMPADDR DC    A(HEXDUMP-4)
XSAVE    DC    F'0'
**----------------------------PATCH AREA-----------------------------**
         DC    S(PATCH)
PATCH    DC    5CL11'PATCH AREA '
         EJECT
         ENTRIES PAGE
         LINE  (1,1),(44,1),SKIP=3
         LABEL '--- CHANNEL CHECK DATA EDITING ---'
         SPACE 2
         LINE  (5,31),(1,10),SKIP=2
         HEX   (CPUID,0,2B),(SERIAL,0,3B)
         ROUTINE JOBID,SETID
         LABEL 'MOD',,'SERIAL NUMBER',,,'JOB IDENTITY -',,,            *
               'PROG IDENTITY - DOES NOT APPLY'
         SPACE 2
         LINE  (1,1),,SKIP=1
         ROUTINE CHANID,TYPCHAN
         LABEL ''
         SPACE 2
         LINE  (23,1),(1,1),SKIP=1
         HEX   (CUA,0,2B)
         LABEL 'CHANNEL/UNIT ADDRESS'
         SPACE 2
         LINE  ,(75,20),SKIP=1
         LABEL 'DAY  YEAR','HH MM SS'
         SPACE 2
         LINE  (75,20),(68,20),SKIP=1
         ROUTINE DATE,SETDATE
         HEX   (TIME,1,3B)
         LABEL 'DATE -','TIME -'
         SPACE 2
         LINE  ,(14,1),SKIP=1
         LABEL 'CC   DA    FL     CT'
         SPACE 2
         LINE  (14,6),(1,82)
         HEX   (CCWCC,0,1B),(CCWDA,B3,3B),(CCWFL,B1,1B),(CCWXX,B3,1B),(*
               CCWCT,B6,2B)
         LABEL 'FAILING CCW','K    CA    US CS  CT'
         SPACE 2
         LINE  (83,6),(75,1),SKIP=3
         HEX   (CSWK,0,1B),(CSWCA,B3,3B),(CSWUS,B1,1B),(CSWCS,B4,1B),(C*
               SWCT,B7,2B)
         LABEL 'CSW'
         SPACE 2
         LINE  ,(49,1),SKIP=2
         LABEL '--- ECSW ---'
         SPACE 2
         LINE  (83,1),(30,21),SKIP=1
         BIN   (ECSWX2,0,2)
         LABEL 'ERROR SOURCE','VALIDITY','TERM TYPE'
         SPACE 2
         LINE  (39,22),(32,8)
         BIN   (ECSW10),(ECSW20),(ECSW34)
         LABEL 'CPU',,'I/F ADDR',,,'DISCIN BIT'
         SPACE 2
         LINE
         BIN   (ECSW11),(ECSW23),(ECSWX3,0,3)
         LABEL 'CHAN',,'SEQ CODE',,,'SEQ CODE'
         SPACE 2
         LINE
         BIN   (ECSW12),(ECSW24)
         LABEL 'SCU',,'DEV STATUS'
         SPACE 2
         LINE
         BIN   (ECSW13),(ECSW25)
         LABEL 'SU',,'CCW ADDR'
         SPACE 2
         LINE
         BIN   (ECSW14),(ECSW26)
         LABEL 'CU',,'CHAN ADDR'
         SPACE 2
         LINE  (61,1),(48,1),SKIP=3
         BIN   (ECSW27)
         LABEL 'DEV ADDR'
         SPACE 2
         LINE  ,(15,53),SKIP=2
         LABEL '--- UNIT STATUS ---','--- CHANNEL STATUS ---'
         SPACE 2
         LINE  (20,26),(1,26),SKIP=1
         BIN   (CSWUS0),(CSWUS4),(CSWCS0),(CSWCS4)
         LABEL 'ATTENTION','CHANNEL END','PRGM-CTLD IRPT','CHAN DATA CH*
               ECK'
         SPACE 2
         LINE
         BIN   (CSWUS1),(CSWUS5),(CSWCS1),(CSWCS5)
         LABEL 'STATUS MODIFIER','DEVICE END','INCORRECT LENGTH','CHAN *
               CTL CHECK'
         SPACE 2
         LINE
         BIN   (CSWUS2),(CSWUS6),(CSWCS2),(CSWCS6)
         LABEL 'CONTROL UNIT END','UNIT CHECK','PROGRAM CHECK','I/F CTL*
                CHECK'
         SPACE 2
         LINE  ,,SKIP=2
         BIN   (CSWUS3),(CSWUS7),(CSWCS3),(CSWCS7)
         LABEL 'BUSY','UNIT EXCEPTION','PROTECTION CHECK','CHAINING CHE*
               CK'
         SPACE 2
         LINE  (1,1),,SKIP=1
         ROUTINE MPI,CKIODEVS
         LABEL ''
         SPACE 2
         LINE  (25,5),(1,1)
         HEX   (CUU1,0,2B),(CUU2,0,2B),(CUU3,0,2B),(CUU4,0,2B)
         HEX   (CUU5,0,2B),(CUU6,0,2B),(CUU7,0,2B),(CUU8,0,2B)
         LABEL 'CHANNEL/UNIT ADDRESSES'
         SPACE 2
HEXDUMP  ETEPILOG RLEN=C8
         END
