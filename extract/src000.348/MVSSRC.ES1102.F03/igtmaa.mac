TMAA     TITLE 'IGTMAA -- USER/LPA INTERFACE ROUTINE'
IGTMAA   CSECT
*/* * START OF SPECIFICATIONS ************************************** */
*/*                                                                  */
*/*   MODULE-NAME = IGTMAA                                           */
*/*                                                                  */
*/*   DESCRIPTIVE-NAME = VPSS USER/LPA INTERFACE ROUTINE             */
*/*                                                                  */
*/*   COPYRIGHT = NONE                                               */
*/*                                                                  */
*/*   STATUS = RELEASE 1, MODIFICATION LEVEL 0.                      */
*/*                                                                  */
*/*   FUNCTION = THIS ROUTINE BRANCHES TO THE RESIDENT ACCESS METHOD */
*/*       MODULE LOCATED IN THE LINK PACK AREA (MVS) OR THE          */
*/*       RAM (VS1).  ON THE INITIAL ENTRY, IGTMAA LINKS TO          */
*/*       IGTMAB TO LOCATE THE RESIDENT MODULE AND INITIALIZE THE    */
*/*       APUT AND APAT.  SUBSEQUENT CALLS USE A BRANCH ENTRY TO     */
*/*       THE INTERFACE ROUTINE.                                     */
*/*                                                                  */
*/*   NOTES =                                                        */
*/*     THIS ROUTINE MUST BE LINK-EDITED WITH AN ALIAS OF VPSS.      */
*/*                                                                  */
*/*     DEPENDENCIES =                                               */
*/*        THE NAME OF THE SUBSYSTEM TO PROCESS THESE REQUESTS       */
*/*        (NORMALLY VPSS) IS CONTAINED IN THE 4-CHARACTER           */
*/*        CONSTANT IGTMAA01.  THIS IS USED BY IGTMAB (FIRST CALL    */
*/*        TO IGTMAA) TO ESTABLISH THE CONTROL BLOCKS CONNECTING     */
*/*        THE USER WITH THE CORRECT SUBSYSTEM.  IGTMAA01 IS         */
*/*        DECLARED AS 'ENTRY' TO ALLOW THE USER TO MODIFY THIS      */
*/*        VALUE VIA AN 'EXTRN'.  THIS IS NOT REQUIRED IF THE        */
*/*        STANDARD SUBSYSTEM NAME (VPSS) IS USED OR THE             */
*/*        INSTALLATION MODIFIES THIS VALUE.  THIS FEATURE IS OF     */
*/*        USE WHEN TESTING AN ALTERNATE 3838 PROCESSING SUBSYSTEM.  */
*/*                                                                  */
*/*     RESTRICTIONS =  THIS ROUTINE MUST ASSEMBLE IDENTICALLY IN    */
*/*                     AN MVS AND A VS1 ENVIRONMENT SINCE IT IS     */
*/*                     LINK-EDITED WITH THE USER.                   */
*/*                                                                  */
*/*     REGISTER-CONVENTIONS = STANDARD VPSS CONVENTIONS             */
*/*                                                                  */
*/*     PATCH-LABEL = APPATCH                                        */
*/*                                                                  */
*/*   MODULE-TYPE = CSECT                                            */
*/*                                                                  */
*/*     PROCESSOR = VS/2 ASSEMBLER                                   */
*/*                                                                  */
*/*     MODULE SIZE = SEE END OF ASSEMBLER LISTING                   */
*/*                                                                  */
*/*     ATTRIBUTES = REUSABLE.  NOTE THAT THIS PROGRAM IS            */
*/*                  RE-ENTRANT EXCEPT FOR THE INITIAL CALL.         */
*/*                  HOWEVER MULTIPLE INITIAL CALLS ARE HANDLED      */
*/*                  BY IGTMAB.  THEREFORE IGTMAA WILL FUNCTION IN   */
*/*                  A RE-ENTRANT ENVIRONMENT.                       */
*/*                                                                  */
*/*   ENTRY-POINT = IGTMAA                                           */
*/*                                                                  */
*/*     PURPOSE = SAME AS FUNCTION                                   */
*/*                                                                  */
*/*     LINKAGE = ENTERED FROM USER'S PROGRAM.  ENTERED IN           */
*/*               THE USER'S KEY, PROBLEM STATE, UNLOCKED, TASK      */
*/*               MODE, ENABLED.                                     */
*/*                                                                  */
*/*     INPUT = THE FOLLOWING REGISTERS ARE INITIALIZED ON ENTRY --  */
*/*         REGISTER  1 - ADDRESS OF THE VPSS CALL PARAMETER LIST    */
*/*         REGISTER 13 - ADDRESS OF 72-BYTE SAVEAREA.               */
*/*         REGISTER 14 - RETURN ADDRESS                             */
*/*         REGISTER 15 - ADDRESS OF THIS ROUTINE (IGTMAA).          */
*/*                                                                  */
*/*     OUTPUT = ON THE INITIAL CALL TO IGTMAA (VPSS), THE APUT      */
*/*              AND APAT ARE CONTRUCTED VIA A LINK TO IGTMAA.       */
*/*              ON THIS CALL AND SUBSEQUENT CALLS, NO OTHER         */
*/*              OUTPUT IS PRODUCED BY THIS MODULE.                  */
*/*                                                                  */
*/*   ENTRY-POINT = IGTMAA01                                         */
*/*                                                                  */
*/*     PURPOSE = IDENTIFIES THE 4-CHARACTER NAME OF THE             */
*/*               SUBSYSTEM TO PROCESS THESE REQUESTS.               */
*/*               ALLOWS THE APPLICATION PROGRAM TO USE              */
*/*               A SUBSYSTEM OTHER THAN VPSS.  THIS VALUE           */
*/*               MUST BE CHANGED PRIOR TO THE FIRST CALL            */
*/*               TO VPSS (ENTRY POINT IGTMAA) IF AN ALTERNATE       */
*/*               SUBSYSTEM IS WANTED.                               */
*/*                                                                  */
*/*     LINKAGE = NON-EXECUTABLE ENTRY POINT.  SHOULD BE REFERENCED  */
*/*               VIA AN ADCON AND AN EXTRN STATEMENT.               */
*/*                                                                  */
*/*     INPUT = N/A.                                                 */
*/*                                                                  */
*/*     OUTPUT = N/A.                                                */
*/*                                                                  */
*/*   EXIT-NORMAL = CONTROL PASSES TO IGTMBA TO PERFORM THE          */
*/*                 REQUESTED FUNCTION.  THE FOLLOWING REGISTERS     */
*/*                 ARE SET ON EXIT:                                 */
*/*                 R0   - ADDRESS OF WORD CONTAINING ADDRESS OF     */
*/*                        THE APUT.                                 */
*/*                 R1   - ADDRESS OF THE USER'S PARAMETER LIST      */
*/*                        (SAME AS ON INPUT).                       */
*/*                 R2-R13 - UNCHANGED FROM INPUT.                   */
*/*                 R14  - RETURN ADDRESS TO APPLICATION PROGRAM     */
*/*                        (SAME AS ON INPUT TO IGTMAA).             */
*/*                 R15  - ADDRESS OF IGTMBA                         */
*/*                 CONTROL EXITS IN THE USER'S KEY, PROBLEM         */
*/*                 STATE, ENABLED, OWNING NO LOCKS (SAME AS INPUT). */
*/*                                                                  */
*/*   EXIT-ERROR  = IF UNABLE TO INITIALIZE THE APUT/APAT            */
*/*                 (NORMALLY SUBSYSTEM DOES NOT EXIST),             */
*/*                 CONTROL RETURNS TO THE CALLER.                   */
*/*                 THE FOLLOWING REGISTERS ARE PASSED TO THE        */
*/*                 USER --                                          */
*/*                 R0-R14 - UNCHANGED                               */
*/*                 R15    - CONTAINS A 12 (SET BY IGTMBA).          */
*/*                 CONTROL EXITS IN THE USER'S KEY, PROBLEM         */
*/*                 STATE, ENABLED, OWNING NO LOCKS (SAME AS INPUT). */
*/*                                                                  */
*/*   EXTERNAL-REFERENCES = THE FOLLOWING ROUTINES AND               */
*/*                         CONTROL BLOCKS ARE USED.                 */
*/*                                                                  */
*/*     ROUTINES =                                                   */
*/*         IGTMAB  -  USER/VPSS CONNECTION                          */
*/*         IGTMBA  -  VPSS FAST PATH PROCESSOR                      */
*/*                                                                  */
*/*     DATA-AREAS = NONE                                            */
*/*                                                                  */
*/*     CONTROL-BLOCKS =                                             */
*/*         PSA      - R                                             */
*/*         CVT      - R                                             */
*/*         APSV     - R, M                                          */
*/*                                                                  */
*/*   TABLES = NONE                                                  */
*/*                                                                  */
*/*   MACROS =                                                       */
*/*       IGTXEPLG                                                   */
*/*       LINK                                                       */
*/*       GETMAIN                                                    */
*/*       FREEMAIN                                                   */
*/*                                                                  */
*/*   CHANGE-ACTIVITY = NONE                                         */
*/*                                                                  */
*/*   MESSAGES = NONE                                                */
*/*                                                                  */
*/*   ABEND-CODES = NONE                                             */
*/*                                                                  */
*/* * END OF SPECIFICATIONS **************************************** */
         EJECT ,
         COPY  IGTXSYSO            DEFINE SYSTEM PARAMETERS
         EJECT ,
VPSS     DS    0D                  USER ENTRY POINT NAME
         ENTRY VPSS                * (ALIAS)
         SPACE 3
         USING IGTMAA,APENTRY      BASE REGISTER
         SPACE 3
         B     STARTUP             SKIP IDENTIFIER
         DC    YL1(COREIDE-*-1)    LENGTH OF ID
         DC    C'IGTMAA'           CSECT NAME
         DC    C' &SYSDATE'        ASSEMBLY DATE
COREIDE  EQU   *                   END OF ID
         SPACE 3
         CNOP  2,4                 FORCE HALFWORD (NON-FULL) ALIGNMENT
         SPACE 3
STARTUP  DS    0H                  START OF CODE
         L     APENTRY,ADDRINTF    ADDRESS OF INTERFACE ROUTINE
         BALR  R0,APENTRY          GOTO IT. R0 -> INTERFACE LIST
         SPACE 3
*
** INTERFACE TABLE POINTERS
** USED TO POINT TO COMMON USER CONTROL BLOCKS.
*
AAPUT    DC    A(*-*)              ADDRESS OF APUT.
*                                  * INITIALIZED BY IGTMAB
         DC    XL1'80'             END-OF-LIST INDICATOR
*                                  * MUST REMAIN ON
         DC    AL3(0)              NOT CURRENTLY USED
         SPACE 3
*
** FIELDS PASSED TO IGTMAB FOR INITIALIZATION
*
         DS    0A                  FULLWORD ALIGNMENT
AMNAME   DC    CL4'VPSS'           SUBSYSTEM NAME
ADDRINTF DC    A(LOCATE)           ADDRESS OF INTERFACE ROUTINE
*                                  * ON FIRST CALL, GOES TO 'LOCATE'
*                                  * WHICH LINKS TO IGTMBA TO RESET
*                                  * ADDRINTF TO THE PROPER VALUE.
USER     DC    A(*-*)              RESERVED FOR INSTALLATION USE
         SPACE 2
IGTMAA01 EQU   AMNAME,4,C'C'       DEFINE ENTRY POINT
         ENTRY IGTMAA01            * FOR SUBSYSTEM NAME
         EJECT ,
***********************************************************************
**                                                                   **
**       THIS CODE ENTERED ONLY ON INITIAL CALL TO VPSS.  IT         **
**       LINKS TO IGTMAB TO CONSTRUCT THE APUT AND APAT, AND TO      **
**       SET ADDRINTF TO THE ADDRESS OF THE INITIAL VPSS             **
**       PROCESSING ROUTINE IN THE LINK PACK AREA.  THE ADDRESS      **
**       OF THE CONSTRUCTED APUT IS PLACED AT LABEL 'AAPUT' FOR      **
**       USE BY FUTURE CALLS TO VPSS.                                **
**                                                                   **
**       ON RETURN FROM IGTMAB, CONTROL RETURN TO THE BEGINNING OF   **
**       THIS MODULE TO PROCESS THE USER'S REQUEST.  FUTURE CALLS    **
**       TO THIS MODULE CAUSE CONTROL TO BE PASSED DIRECTLY TO       **
**       THE INITIAL VPSS PROCESSING ROUTINE IN THE LINK PACK AREA   **
**       (BECAUSE OF THE MODIFICATION OF FIELD 'ADDRINTF').          **
**                                                                   **
***********************************************************************
         SPACE 3
LOCATE   DS    0H                  INITIAL CALL -- LINK TO IGTMAB
         USING LOCATE,APENTRY      BASE REGISTER
         USING APSV,APSVPTR        INPUT SAVEAREA POINTER
         STM   R14,R12,APSVRG14    SAVE INPUT REGISTERS
         LR    APCODE,APENTRY      ESTABLISH BASE IN
         DROP  APENTRY             NON-DYNAMIC REGISTER
         USING LOCATE,APCODE       TELL ASSEMBLER
         LA    R0,APSVLN+LINKLN    REQUIRED GETMAIN LENGTH
         GETMAIN R,LV=(0)          OBTAIN DYNAMIC STORAGE
         ST    R1,APSVNEXT         USE FIRST PART AS SAVEAREA
         ST    APSVPTR,APSVPREV-APSV(,R1) * CHAIN SAVEAREAS
         LR    APSVPTR,R1          * POINT TO NEW SAVEAREA
         L     TPTR,FLCCVT-FLC(*-*) ADDRESS OF CVT
         L     TPTR,CVTLINK-CVTMAP(,TPTR) LINKLIB DCB ADDRESS
         LINK  EP=IGTMAB,          GO TO USER/VPSS CONNECTION ROUTINE  *
               DCB=(TPTR),         * USE LINKLIB DCB (INCLUDES LPA)    *
               PARAM=(AAPUT,       * INPUT: ADDRESS OF APUT RETURNED   *
               AMNAME,             * * SUBSYSTEM NAME                  *
               ADDRINTF,           * * @ OF INTERFACE RTN RETURNED     *
               USER),              * * RESERVED FOR INSTALLATION MODS  *
               VL=1,               * FLAG END OF PARM LIST WITH X'80'  *
               SF=(E,APSV+APSVLN)  * LINK WORKAREA AFTER OS SAVEAREA
         LR    R1,APSVPTR          ADDRESS OF SAVEAREA FOR FREEMAIN
         L     APSVPTR,APSVPREV    POINT TO INPUT SAVEAREA
         ST    APRCODE,APSVRG15    SAVE IGTMAB RETURN CODE
         LA    R0,APSVLN+LINKLN    LENGTH TO FREEMAIN
         FREEMAIN R,LV=(0),A=(1)   FREE TEMPORARY STORAGE
         LM    R14,R12,APSVRG14    RELOAD INPUT REGISTERS
*                                  * R15 = RETURN CODE FROM IGTMBA
         DROP  APCODE              BASE REGISTER LOST
         LTR   APRCODE,APRCODE     IF NON-ZERO RETURN CODE
         BNZR  APRETURN            * RETURN TO CALLER WITHOUT
*                                  * PROCESSING REQUEST.
*                                  * INITIALIZATION FAILED.
         BALR  APENTRY,*-*         RE-ESTABLISH A BASE
         USING *,APENTRY           TELL ASSEMBLER
         L     APENTRY,BASEADDR    RELOAD INITIAL BASE ADDRESS
         BR    APENTRY             RETRY IGTMAA LOGIC
*                                  * THIS TIME CONTROL PASSES TO IGTMBA
*                                  * (INITIAL VPSS PROCESSING ROUTINE)
         SPACE 3
BASEADDR DC    A(IGTMAA)           RETRY ADDRESS
*                                  * (CSECT START)
         EJECT ,
********************************************************************* *
**                                                                    *
**       VPSS AND SYSTEM DSECTS                                       *
**                                                                    *
**       ALTHOUGH DIFFERENT MVS AND VS1 SYSTEM MACROS USED, THIS      *
**       CSECT MUST ASSEMBLE IDENTICALLY IN MVS AND VS1 SINCE IT      *
**       IS LINK-EDITED WITH THE USER (WHO MAY RUN ON BOTH SYSTEMS).  *
**                                                                    *
********************************************************************* *
         SPACE 3
LINK     DSECT ,                   SIZE OF LINK MACRO LIST
         LINK  EPLOC=*-*,DCB=*-*,SF=L FORMAT OF LINK MACRO
LINKLN   EQU   *-LINK              LENGTH OF LINK-MACRO PARAMETER LIST
         EJECT ,
         IGTXAPSV ,                SAVEAREA
         EJECT ,
         AIF   ('&VPSSSYS' EQ 'VS1').VS1GEN IF VS/1, SKIP MVS VERSIONS
         CVT   SYS=AOS2,DSECT=YES  COMMUNICATIONS VECTOR TABLE
         EJECT ,
         IHAPSA ,                  LOW STORAGE OVERLAY
         AGO   .DSECTOK            DSECTS GENERATED
.VS1GEN  ANOP  ,                   GENERATE VS1 DSECTS
         CVT   SYS=AOS1,DSECT=YES  COMMUNICATIONS VECTOR TABLE
         EJECT ,
         IHAFLC ,                  LOW STORAGE OVERLAY
.DSECTOK ANOP  ,                   DSECTS GENERATED
         EJECT ,
IGTMAA   CSECT ,                   RESUME PROGRAM
         IGTXEPLG ,                EXPAND REQUIRED VALUES
TPTR     EQU   R2                  TEMPORARY LINKLIB DCB POINTER
         END   ,
