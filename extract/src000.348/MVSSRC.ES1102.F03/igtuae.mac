TUAE     TITLE 'IGTUAE -- FREE DYNAMIC STORAGE'
IGTUAE   CSECT ,                   FREE CORE ROUTINE
*/* * START OF SPECIFICATIONS ************************************** */
*/*                                                                  */
*/*   MODULE-NAME = IGTUAE                                           */
*/*                                                                  */
*/*   DESCRIPTIVE-NAME = FREE DYNAMIC STORAGE                        */
*/*                      (IGTYFSTK MACRO SUPPORT)                    */
*/*                                                                  */
*/*   COPYRIGHT = NONE                                               */
*/*                                                                  */
*/*   STATUS = RELEASE 1, MODIFICATION LEVEL 0.                      */
*/*                                                                  */
*/*   FUNCTION = THIS ROUTINE RELEASES STORAGE PREVIOUSLY OBTAINED   */
*/*       FROM THE CURRENT APSM (USING EITHER IGTYGSV OR IGTYGSTK).  */
*/*       IF THE ENTIRE APSM IS NOW FREE, THE APSM AND RELATED       */
*/*       STORAGE IS FREEMAINED, AND THE PREVIOUS APSM MADE THE      */
*/*       CURRENT APSM.  THIS ROUTINE MAY NOT BE USED TO FREE THE    */
*/*       SUBROUTINE'S APSV (USE IGTYFSV).                           */
*/*       THIS ROUTINE SUPPORTS THE IGTYFSTK MACRO.                  */
*/*       STORAGE MANAGEMENT IS LIFO (LAST ACQUIRED STORAGE MUST     */
*/*       BE INITIALLY RELEASED STORAGE).                            */
*/*                                                                  */
*/*   NOTES =                                                        */
*/*     THIS MACRO SUPPORTS THE PARAMETERS REQUIRED FOR THE          */
*/*     FREEMAIN R,LV= MACRO.                                        */
*/*                                                                  */
*/*     DEPENDENCIES =                                               */
*/*        REGISTER 13 MUST POINT TO A VPSS EXTENDED SAVEAREA        */
*/*        (APSV) WHICH HAS BEEN CREATED BY IGTUAA, IGTUAE, OR       */
*/*        IGTUAF.                                                   */
*/*                                                                  */
*/*     RESTRICTIONS =                                               */
*/*        SINCE THIS ROUTINE MAY BE ENTERED IN EITHER PROBLEM OR    */
*/*        SUPERVISOR STATE AND IN ANY KEY, THIS PROGRAM MAY NOT     */
*/*        ISSUE PRIVLEDGED INSTRUCTIONS OR UPDATE SYSTEM CONTROL    */
*/*        BLOCKS.                                                   */
*/*        STORAGE MANAGEMENT IS LIFO (LAST ACQUIRED STORAGE MUST    */
*/*        BE INITIALLY RELEASED STORAGE).                           */
*/*                                                                  */
*/*     REGISTER-CONVENTIONS = STANDARD VPSS CONVENTIONS             */
*/*                                                                  */
*/*     PATCH-LABEL = APPATCH                                        */
*/*                                                                  */
*/*   MODULE-TYPE = CSECT                                            */
*/*                                                                  */
*/*     PROCESSOR = VS/2 ASSEMBLER                                   */
*/*                                                                  */
*/*     MODULE SIZE = SEE END OF ASSEMBLER LISTING                   */
*/*                                                                  */
*/*     ATTRIBUTES = RE-ENTRANT.                                     */
*/*                                                                  */
*/*   ENTRY-POINT = IGTUAE                                           */
*/*                                                                  */
*/*     PURPOSE = SAME AS FUNCTION                                   */
*/*                                                                  */
*/*     LINKAGE = ENTERED UNLOCKED, TASK MODE, ENABLED. MAY BE       */
*/*               IN ANY KEY AND EITHER PROBLEM OR SUPERVISOR        */
*/*               STATE.                                             */
*/*                                                                  */
*/*     INPUT = THE FOLLOWING REGISTERS ARE INITIALIZED ON ENTRY     */
*/*             (SET BY MACRO IGTYFSTK) --                           */
*/*         REGISTER  0 - BYTE-0 IGNORED. STORAGE RELEASED FROM      */
*/*                       SAME SUBPOOL AS APSV.                      */
*/*                       BYTES 1-3 CONTAIN THE LENGTH OF THE        */
*/*                       DYNAMIC STORAGE TO FREE.                   */
*/*         REGISTER  1 - IGNORED.                                   */
*/*         REGISTER 12 - ADDRESS OF THE APCT.                       */
*/*         REGISTER 13 - ADDRESS OF CURRENT APSV (VPSS EXTENDED     */
*/*                       SAVEAREA).                                 */
*/*         REGISTER 14 - RETURN ADDRESS                             */
*/*         REGISTER 15 - ADDRESS OF THIS ROUTINE (IGTUAE).          */
*/*                                                                  */
*/*     OUTPUT = THE REQUESTED STORAGE IS ADDED TO THE FREE SPACE    */
*/*              IN THE CURRENT APSM.  IF THE APSM IS NOW            */
*/*              COMPLETELY FREE, THE CURRENT APSM AREA IS FREED     */
*/*              AND THE PREVIOUS APSM MADE CURRENT.                 */
*/*                                                                  */
*/*   EXIT-NORMAL = THE FOLLOWING REGISTERS ARE RETURNED TO THE      */
*/*                 CALLER --                                        */
*/*                 REGISTER  0 - UNPREDICTABLE                      */
*/*                 REGISTER  1 - UNPREDICTABLE                      */
*/*                 REGISTER 2-13 - SAME AS ON INPUT                 */
*/*                 REGISTER 14 - RETURN ADDRESS                     */
*/*                 REGISTER 15 - UNPREDICTABLE                      */
*/*                                                                  */
*/*   EXIT-ERROR  = NONE.                                            */
*/*                                                                  */
*/*   EXTERNAL-REFERENCES = THE FOLLOWING ROUTINES AND               */
*/*                         CONTROL BLOCKS ARE USED.                 */
*/*                                                                  */
*/*     ROUTINES = NONE.                                             */
*/*                                                                  */
*/*     DATA-AREAS = NONE                                            */
*/*                                                                  */
*/*     CONTROL-BLOCKS =                                             */
*/*         APCT     - R                                             */
*/*         APSC     - R, M, D                                       */
*/*         APSM     - R, M, D                                       */
*/*         APSV     - R, M                                          */
*/*                                                                  */
*/*   TABLES = NONE                                                  */
*/*                                                                  */
*/*   MACROS =                                                       */
*/*       IGTXEPLG                                                   */
*/*       FREEMAIN                                                   */
*/*                                                                  */
*/*   CHANGE-ACTIVITY = NONE                                         */
*/*                                                                  */
*/*   MESSAGES = NONE                                                */
*/*                                                                  */
*/*   ABEND-CODES = NONE                                             */
*/*                                                                  */
*/* * END OF SPECIFICATIONS **************************************** */
         EJECT ,
         COPY  IGTXSYSO            DEFINE SCP CHARACTERISTICS
         SPACE 3
         USING APCT,APCTPTR        VPSS CONTROL TABLE
         USING APSV,APSVPTR        VPSS SAVE AREA
         USING IGTUAE,APENTRY      BASE REGISTER
         SPACE 5
         B     STARTUP             SKIP IDENTIFIER
         DC    YL1(COREIDE-*-1)    LENGTH OF ID
         DC    C'IGTUAE'           CSECT NAME
         DC    C' &SYSDATE'        ASSEMBLY DATE
COREIDE  EQU   *                   END OF ID
         SPACE 2
STARTUP  DS    0H                  START OF CODE
         L     APSCPTR,APSVAPSC    POINT TO STORAGE MANAGEMENT
         USING APSC,APSCPTR        * CONTROL WORD (APSC)
         AH    RCORESZ,EXTRA       ROUND REQUEST UP TO
         N     RCORESZ,ROUND       * DOUBLEWORD BOUNDARY
         SH    RCORESZ,APSCCRSZ    SET APSCCRSZ =
         LCR   RCORESZ,RCORESZ     * AMOUNT OF STORAGE
         STH   RCORESZ,APSCCRSZ    * NOW USED.
         CH    RCORESZ,CAPSCLN     IF MORE THAN APSC LEFT,
         BHR   APRETURN            * APSM NOT EMPTY, SO DONE
         EJECT ,
*
** CURRENT APSM EMPTY. MUST ISSUE FREEMAIN.
*
         L     APSCPTR,APSCAPSC    PREVIOUS APSC FOR THIS SAVEAREA
         ST    APSCPTR,APSVAPSC    MAKE THIS APSC CURRENT
         L     APSMPTR,APSVAPSM    POINT TO APSM HEADER
         USING APSM,APSMPTR        POINTING TO CURRENT APSM
         DROP  APENTRY             CHANGING BASE REGISTER CONTENT
*                                  NOTE: NO BASE REGISTER EXISTS
         L     RTEMPA,APSMAPSV     NEXT APSM OWNED BY THIS APSV
*                                  * (IF ANY)
         ST    RTEMPA,APSVFREE     SAVE IN APSV FOR SUBROUTINE EXIT
         L     RTEMPA,APSMPREV     PREVIOUS APSM
         ST    RTEMPA,APSVAPSM     MAKE IT NEW 'CURRENT' APSM
         SR    RTEMPB,RTEMPB       SET 'NEXT' FIELD IN NEW CURRENT
         ST    RTEMPB,APSMNEXT-APSM(,RTEMPA) * TO ZERO (END-OF-QUEUE)
         L     RCORESZ,APSMSIZE    SUBPOOL/SIZE OF APSM TO RELEASE
         FREEMAIN R,LV=(0),A=(1)   RELEASE APSM
         SPACE 2
         BR    APRETURN            DONE -- SO EXIT
         EJECT ,
********************************************************************* *
**                                                                    *
**       CONSTANTS                                                    *
**                                                                    *
********************************************************************* *
         SPACE 2
CAPSCLN  DC    Y(APSCLN)           LENGTH OF APSC USED TO DETERMINE
*                                  * IF APSM IS EMPTY.
EXTRA    DC    Y(7)                USED TO ROUND VALUE TO MULTIPLE
ROUND    DC    A(X'00007FF8')      * OF 8 (DOUBLEWORD) AND TO CLEAR
*                                  * THE SUBPOOL NUMBER
         SPACE 5
********************************************************************* *
**                                                                    *
**       DEFINE REQUESTED VPSS DSECTS                                 *
**                                                                    *
********************************************************************* *
         SPACE 2
         IGTXDSCT IGTXAPCT         CONTROL TABLE
         IGTXDSCT IGTXAPSC         STORAGE MANAGEMENT CONTROL WORD
         IGTXDSCT IGTXAPSM         STORAGE MANAGEMENT CONTROL BLOCK
         IGTXDSCT IGTXAPSV         SAVEAREA/WORKAREA
         EJECT ,
         IGTXEPLG ,                GENERATE DSECTS & COMMON VALUES
RCORESZ  EQU   R0                  REQUESTED DYNAMIC STORAGE SIZE
RTEMPB   EQU   R0                  TEMPORARY VALUE HOLDER
RTEMPA   EQU   R15                 TEMPORARY VALUE HOLDER
         SPACE 5
         END   ,
