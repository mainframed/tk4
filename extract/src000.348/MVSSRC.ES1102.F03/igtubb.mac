TUBB     TITLE 'IGTUBB -- CROSS MEMORY POST ROUTINE'
IGTUBB   CSECT ,                   POST SERVICE ROUTINE
*/* * START OF SPECIFICATIONS ************************************** */
*/*                                                                  */
*/*   MODULE-NAME = IGTUBB                                           */
*/*                                                                  */
*/*   DESCRIPTIVE-NAME = CROSS MEMORY POST ROUTINE (MVS)             */
*/*                      INTERPARTITION POST ROUTINE (VS1)           */
*/*                      (SUPPORTS IGTYPOST MACRO)                   */
*/*                                                                  */
*/*   COPYRIGHT = NONE                                               */
*/*                                                                  */
*/*   STATUS = RELEASE 1, MODIFICATION LEVEL 0.                      */
*/*                                                                  */
*/*   FUNCTION = THIS ROUTINE WILL POST THE SPECIFIED ECB            */
*/*       CONTAINED IN ANOTHER ADDRESS SPACE (OR PARTITION) WITH     */
*/*       A COMPLETION CODE OF 0.  IF AN ERROR OCCURS IN THE POST    */
*/*       PROCESSING, THE CALLER IS NOT NOTIFIED.                    */
*/*                                                                  */
*/*   NOTES =                                                        */
*/*                                                                  */
*/*     DEPENDENCIES =                                               */
*/*        THIS ROUTINE SUPPORTS THE IGTYPOST MACRO AND ASSUMES      */
*/*        A 'COMPARE AND SWAP' POST HAS BEEN ATTEMPTED.             */
*/*                                                                  */
*/*     RESTRICTIONS =                                               */
*/*        THE ECB MUST BE IN CSA OR SQA.  THIS ROUTINE ASSUMES      */
*/*        IT CAN REFERENCE THE STORAGE FROM ANY ADDRESS SPACE.      */
*/*                                                                  */
*/*     REGISTER-CONVENTIONS = STANDARD VPSS CONVENTIONS             */
*/*                                                                  */
*/*     PATCH-LABEL = APPATCH                                        */
*/*                                                                  */
*/*   MODULE-TYPE = CSECT                                            */
*/*                                                                  */
*/*     PROCESSOR = VS/2 ASSEMBLER                                   */
*/*                                                                  */
*/*     MODULE SIZE = SEE END OF ASSEMBLER LISTING                   */
*/*                                                                  */
*/*     ATTRIBUTES = RE-ENTRANT.                                     */
*/*                                                                  */
*/*   ENTRY-POINT = IGTUBB                                           */
*/*                                                                  */
*/*     PURPOSE = SAME AS FUNCTION                                   */
*/*                                                                  */
*/*     LINKAGE = ENTERED UNLOCKED, TASK MODE, ENABLED,              */
*/*               SUPERVISOR STATE.  IT MAY BE IN ANY KEY.           */
*/*                                                                  */
*/*     INPUT = THE FOLLOWING REGISTERS ARE INITIALIZED ON ENTRY     */
*/*             (SET BY MACRO IGTYPOST) --                           */
*/*         REGISTER  0 - ADDRESS OF THE ASCB FOR THE ADDRESS        */
*/*                       SPACE WAITING ON THE ECB.                  */
*/*         REGISTER  1 - ADDRESS OF THE ECB TO POST.                */
*/*         REGISTER 12 - ADDRESS OF THE APCT.                       */
*/*         REGISTER 13 - ADDRESS OF CURRENT APSV (VPSS EXTENDED     */
*/*                       SAVEAREA).                                 */
*/*         REGISTER 14 - RETURN ADDRESS                             */
*/*         REGISTER 15 - ADDRESS OF THIS ROUTINE (IGTUBB).          */
*/*                                                                  */
*/*     OUTPUT = THE ECB IS POSTED WITH A COMPLETION CODE OF 0.      */
*/*              (THE POST MAY ACTUALLY ONLY BE SCHEDULED WHEN       */
*/*              THE POST IS ISSUED.)                                */
*/*                                                                  */
*/*   EXIT-NORMAL = THE FOLLOWING REGISTERS ARE RETURNED TO THE      */
*/*                 CALLER --                                        */
*/*                 REGISTER  0 - UNPREDICTABLE                      */
*/*                 REGISTER  1 - UNPREDICTABLE                      */
*/*                 REGISTER 2-13 - SAME AS ON INPUT                 */
*/*                 REGISTER 14 - RETURN ADDRESS                     */
*/*                 REGISTER 15 - UNPREDICTABLE                      */
*/*                 CONTROL IS RETURNED IN SUPERVISOR STATE,         */
*/*                 TASK MODE, UNLOCKED, ENABLED AND IN THE KEY      */
*/*                 OF THE CALLER (SAME AS ON INPUT).                */
*/*                                                                  */
*/*   EXIT-ERROR  = NONE.                                            */
*/*                                                                  */
*/*   EXTERNAL-REFERENCES = THE FOLLOWING ROUTINES AND               */
*/*                         CONTROL BLOCKS ARE USED.                 */
*/*                                                                  */
*/*     ROUTINES = NONE.                                             */
*/*                                                                  */
*/*     DATA-AREAS =                                                 */
*/*         ECB                                                      */
*/*                                                                  */
*/*     CONTROL-BLOCKS =                                             */
*/*         PSA      - R                                             */
*/*         APCT     - R                                             */
*/*         APSV     - R, M                                          */
*/*                                                                  */
*/*   TABLES = NONE                                                  */
*/*                                                                  */
*/*   MACROS =                                                       */
*/*       IGTXEPLG                                                   */
*/*       POST                                                       */
*/*                                                                  */
*/*   CHANGE-ACTIVITY = NONE                                         */
*/*                                                                  */
*/*   MESSAGES = NONE                                                */
*/*                                                                  */
*/*   ABEND-CODES = NONE                                             */
*/*                                                                  */
*/* * END OF SPECIFICATIONS **************************************** */
         EJECT ,
         COPY  IGTXSYSO            DEFINE SCP CHARACTERISTICS
         SPACE 2
         USING IGTUBB,APENTRY      TELL ASSEMBLER OF BASE
         USING APCT,APCTPTR        AP CONTROL TABLE
         USING APSV,APSVPTR        SAVEAVEA OVERLAY
         SPACE 2
         B     STARTUP             SKIP IDENTIFIER
         DC    YL1(COREIDE-*-1)    LENGTH OF ID
         DC    C'IGTUBB'           CSECT NAME
         DC    C' &SYSDATE'        ASSEMBLY DATE
COREIDE  EQU   *                   END OF ID
         SPACE 2
STARTUP  DS    0H                  START OF CODE
         AIF   ('&VPSSSYS' NE 'MVS').SKIP1 ASCB EXISTS ONLY FOR MVS
*
** IF ASCB TO POST IS CURRENT, USE
** STANDARD POST MACRO INTERFACE.
*
         USING PSA,0               LOW CORE OVERLAY (ABSOLUTE)
         C     RASCBPTR,PSAAOLD    ECB IN CURRENT MEMORY ?
         BE    CURRENT             * YES--GO DO NORMAL POST
         SPACE 3
.SKIP1   ANOP  ,                   MVS ASCB CHECK PROCESSED
*
** BUILD CROSS-MEMORY POST PARAMETER LIST:
**   +00 - ADDRESS OF ECB
**   +04 - ADDRESS OF ASCB FOR WAITING TCB (MVS ONLY)
**   +08 - ADDRESS OF ERROR EXIT ROUTINE (BR_14 IN APCT) (MVS ONLY)
** USE INPUT SAVEAREA FOR THIS WORKAREA
*
         ST    RECBPTR,POSTECB     ADDRESS OF ECB
         AIF   ('&VPSSSYS' NE 'MVS').SKIP2 SKIP ASCB/ERRET IF NOT MVS
         ST    RASCBPTR,POSTASCB   ADDRESS OF ASCB
         LA    RTEMPA,APCTBR14     ERROR RETURN IS
         ST    RTEMPA,POSTERET     * SR 15,15 / BR 14
.SKIP2   ANOP  ,                   MVS ASCB/ERRET PROCESSED
         POST  MF=(E,POST)         ISSUE CROSS-MEMORY POST
         BR    APRETURN            RETURN TO CALLER
         AIF   ('&VPSSSYS' NE 'MVS').SKIP3 SKIP NORMAL POST IF NOT MVS
         EJECT ,
*
** ECB IN CURRENT ADDRESS SPACE. USE STANDARD POST MACRO.
*
CURRENT  EQU   *
         POST  (RECBPTR),0         ISSUE NORMAL POST (NOT X-MEMORY)
         BR    APRETURN            RETURN TO CALLER
.SKIP3   ANOP  ,                   MVS NORMAL POST PROCESSED
         EJECT ,
         AIF   ('&VPSSSYS' NE 'MVS').SKIP4 MVS DSECTS REQUIRED ?
         IHAPSA ,                  LOW CORE
IGTUBB   CSECT ,                   RESUME CSECT CODE
         EJECT ,
.SKIP4   ANOP  ,                   MVS REQUIRED DSECTS PROCESSED
         IGTXAPSV LIST=YES         SAVEAREA FORMAT
         SPACE 5
********************************************************************* *
**                                                                    *
**       CROSS-MEMORY POST OVERLAY (USE INPUT SAVEAREA)               *
**                                                                    *
********************************************************************* *
         SPACE ,
POST     EQU   APSVRG15+00,4,C'F'  MF=L FORMAT FOR CROSS-MEMORY POST
*                                  * (USE REG 15, 0, AND 1 SLOTS)
POSTECB  EQU   POST+00,4,C'A'      ADDRESS OF ECB TO POST
         AIF   ('&VPSSSYS' NE 'MVS').SKIP5 MVS EQUATES REQUIRED ?
POSTASCB EQU   POST+04,4,C'A'      ADDRESS OF ASCB FOR THE
*                                  * ADDRESS SPACE THAT CONTAINS
*                                  * THE ECB TO POST.
POSTERET EQU   POST+08,4,C'A'      ADDRESS OF ERROR-EXIT ROUTINE
*                                  * SET TO 'BR 14' ROUTINE
.SKIP5   ANOP  ,                   MVS REQUIRED EQUATES PROCESSED
         SPACE 2
IGTUBB   CSECT ,                   RESUME CSECT CODE
         EJECT ,
********************************************************************* *
**                                                                    *
**       VPSS DSECT REQUESTS                                          *
**                                                                    *
********************************************************************* *
         SPACE ,
         IGTXDSCT IGTXAPCT         CONTROL TABLE
         EJECT ,
         IGTXEPLG ,                VPSS DSECTS/REGISTERS
RTEMPA   EQU   R0                  TEMPORARY VALUE HOLDER (MVS ONLY)
RASCBPTR EQU   R0                  ADDRESS OF ASCB FOR ADDRESS
*                                  * SPACE CONTAINING ECB (MVS ONLY)
RECBPTR  EQU   R1                  ADDRESS OF ECB TO POST
         SPACE 5
         END   ,
