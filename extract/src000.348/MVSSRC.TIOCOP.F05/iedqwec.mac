         TITLE ' IEDQWEC  TOTE CONCENTRATOR MODULE'
IEDQWEC  CSECT                                                   S22024
* CHANGE ACTIVITY AS FOLLOWS:
* A000000-999999                                                SA41597
* A170600-171200,A210200-211400,C318000,C352000,C364000,C378000  S22024
* C398000,C402000,C406000,C410000,C426000,C430000,C434000        S22024
* C438000,C446000,C450000,C454000,C458000,C508000,C514000,C520000S22024
* C526000,C530000,C602000,C606000,A614000,C686000,C690000,C704000S22024
****************************************************************
* DESCRIPTIVE NAME:  TOTE CONCENTRATOR MODULE                  *
*                                                              *
* TITLE: IEDQWEC - TOTE CONCENTRATOR MODULE
*
* MODULE NAME: IEDQWEC                                         *
*                                                              *
* STATUS:      CHANGE LEVEL 0                                  *
*                                                              *
* ENTRY POINT: IEDQWEC CALLED BY IEDQWCZ OR IEDQWJZ FOR        *
*                      INITIAL SET UP                          *
*              IEDQWEC CALLED BY IEDQWE FOR CLEAN UP           *
*                                                              *
* FUNCTIONS:   THE PURPOSE OF THIS MODULE IS TO CHECK EACH     *
*              TEST DEVICE TO SEE IF IT IS ON A CONCENTRATOR.  *
*              IF THE TEST DEVICE IS ON A CONCENTRATOR         *
*              RECORD THE ORIGINAL STATUS OF THE CONCENTRATOR. *
*                                                              *
*              WHEN ENTERED FOR CLEAN UP - RESTORE THE         *
*              CONCENTRATOR TO THE ORIGINAL STATUS.            *
*                                                              *
* INPUT:  REGISTER 02,13,14,15 CONTAIN THE FOLLOWING VALUES:   *
*                                                              *
*               02-OLTCB POINTER                               *
*               13-SAVE AREA ADDRESS                           *
*               14-RETURN ADDRESS                              *
*               15-ENTRY POINT ADDRESS                         *
*                                                              *
* OUTPUT: REG 02-OLTCB                                         *
*                                                              *
* EXTERNAL ROUTINES: IEDQWK - TOTE MESSAGE MODULE              *
*                                                              *
* EXIT POINTS: XCTL TO IEDQWD AFTER INITIAL SET UP             *
*              BRANCH RETURN TO IEDQWA -TOTE MOTHER TASK       *
*               AFTER CLEAN UP.                                *
*                                                              *
* TABLES/WORK AREAS:                                           *
*                                                              *
*       OLTCB,TNT,TTE,QCB                                      *
*                                                              *
* ATTRIBUTES:                                                  *
*                                                              *
*       ENABLED,PROBLEM PROGRAM MODE,TRANSIENT                 *
*                                                              *
* COPYRIGHT='NONE'                                             *
*                                                              *
****************************************************************
         EJECT
****************************************************************
*                                                              *
*        REGISTER EQUATES AND EQUATES                          *
*                                                              *
****************************************************************
R0       EQU   0                        REG 0
R1       EQU   1                        REG 1
R2       EQU   2                        REG 2
R3       EQU   3                        REG 3
R4       EQU   4                        REG 4
R5       EQU   5                        REG 5
R6       EQU   6                        REG 6
R7       EQU   7                        REG 7
R8       EQU   8                        REG 8
R9       EQU   9                        REG 9
R10      EQU   10                       REG 10
R11      EQU   11                       REG 11
R12      EQU   12                       REG 12
R13      EQU   13                       REG 13
R14      EQU   14                       REG 14
R15      EQU   15                       REG 15
D0       EQU   0                        DISPLACEMENT 0
D1       EQU   1                        DISPLACEMENT 1
D4       EQU   4                        DISPLACEMENT 4
D8       EQU   8                        DISPLACEMENT 8
D12      EQU   12                       TWELVE
STRTTERM EQU   X'45'                    START TERMINAL VERB CODE
STOPTERM EQU   X'47'                    STOP TERMINAL CODE
HOLDTERM EQU   X'88'                    HOLD TERMINAL CODE
ACTVTERM EQU   X'46'                    ACTIVATE TERMINAL CODE
REALTERM EQU   X'87'                    REALEASE TERMINAL CODE
INACTERM EQU   X'48'                    INACTIVATE TERMINAL CODE
X40      EQU   X'40'                    BLANK
X0C      EQU   X'0C'                   START FLAG                S22024
X80      EQU   X'80'                   END FLAG                  S22024
D7       EQU   7                        SEVEN
D3       EQU   3                        THREE
D2       EQU   2                        TWO
RT0      EQU   X'00'                    RETURN CODE ZERO
RT8      EQU   X'08'                    RETURN CODE EIGHT
         EJECT
****************************************************************
*                                                              *
*        STANDARD LINKAGE                                      *
*                                                              *
****************************************************************
         SAVE  (14,12)                  SAVE REGS
         USING TOTOLTCB,R2              TOTE CONTROL BLOCK
         LR    R4,R15                   ESTABLISH BASE REG
         USING IEDQWEC,R4               IEDQWEC
         ST    R13,TOTSAVE4+D4          SAVE CALLERS SAVE ADDR
         LR    R12,R13                  MY SAVE
         LA    R13,TOTSAVE4             AREA ADDRESS
         ST    R13,D8(R12)              IN CALLERS SAVE AREA
IEDQWEC  IEDHJN IEDQWECC,HJN            NAME AND HJN
         MVI   TOTOLADR,X40            BLANK OUT                 S22024
         MVC   TOTOLADR+D1(D7),TOTOLADR  NAME                    S22024
         LA    R1,TOTPLIST             P LIST ADDR               S22024
         ST    R1,CBSLPL               TO SVC 102 FIRST          S22024
         ST    R1,CBSLPL+D4            AND LAST                  S22024
         MVI   CBSLPL,X0C              START                     S22024
         MVI   CBSLPL+D4,X80           END FLAGS                 S22024
         EJECT
         LA    R3,TOTTDTBL              TEST DEV TABLE ADDR
         LA    R5,TOTTDEND              END OF TABLE ADDRESS
         LA    R11,TOTCONTB             ADDRESS OF CONCENTRATOR
         TM    TOTTDTBL,TOTTDLIN        TEST DEVICES LINES
         BO    CONTROLC                 YES BRANCH
*                                       INITIAL STATUS TABLE
LOOP     EQU   *
         CR    R3,R5                    ALL ENTRIES PROCESSED
         BE    CONTROLC                 CKECK CONTROL TERM
         L     R8,D0(R3)                GET TNT ENTRY
         LA    R8,D0(R8)                CLEAR HI BYTE
         LTR   R8,R8                    ANY ADDRESS
         BZ    CONTROLC                 CKECK CONTROL TERM
CTAP     EQU   *
         BAL   R12,TNTALINE             GO GET TTE ADDRESS
         USING IEDQTRM,R6               TTE
         TM    TRMDEVFL+D1,TRMCONC      TERMINAL ON A CONC
         BZ    BUMPCONC                 NO BRANCH
         L     R6,TRMDESTQ-D1           GET QCB ADDRESS
         DROP  R6                       TTE
         USING IEDQQCB,R6               QCB
         LH    R7,QCBEXTO               OFFSET TO EXTENSION
         DROP  R6                       QCB
         AR    R6,R7                    GET ADDR OF
         USING IEDQQCBE,R6              QCBE
         L     R6,QCBECONC-D1           ADDR OF CONC TTE
         DROP  R6                       QCBE
         LA    R6,0(R6)                 CLEAR HI BYTE
*                                       CHECK TO SEE IF THIS
*                                       CONCENTRATOR ENTRY HAS
*                                       ALREADY BEEN PROCESSED
         LA    R9,CONTTES               SAVE AREA FOR TTES
CONC1    EQU   *
         L     R7,D0(R9)                GET TTE
         LTR   R7,R7                    IS THERE ONE
         BNZ   CONC2                    YES BRANCH
         ST    R6,D0(R9)                SAVE CURRENT TTE
         ST    R6,CONTTE                SAVE TTE
         B     CONC3                    CONTINUE
CONC2    EQU   *
         CLC   D0(D4,R7),D0(R6)         HAS CURRENT TTE ALREADY
*                                       BEEN PROCESSED
         BE    BUMPCONC                 YES BRANCH
         LA    R9,D4(R9)                BUMP SAVE POINTER
         B     CONC1                    LOOK AGAIN
*                                       THIS ROUTINE FINDS THE
*                                       NAME OF THE CONC FROM
*                                       THE TTE
CONC3    EQU   *
         L     R7,TOTAVTPT              GET AVT POINTER
         USING IEDQAVTD,R7              AVT
         LA    R1,AVTOPCOB              GET OP CONTROL QCB
         ST    R1,TOTOKEY              PUT IN P-LIST             S22024
         L     R7,AVTRNMPT              GET TNT TABLE ADDRESS
         USING IEDQTNTD,R7              TNT TABLE
         LH    R1,TNTLEN                GET NUMBER OF ENTRIES
         LA    R7,TNTFIRST              GET FIRST ENTRY ADDRESS
         DROP  R7                       TNT TABLE
         SR    R9,R9                    CLEAR REG
         IC    R9,TOTTTBEL              GET NAME LENGTH
         LA    R10,D3(R9)               LENGTH OF ONE ENTRY
         AR    R7,R9                    FIRST TTE ADDRESS
CONC4    EQU   *
         CLC   D0(D3,R7),CONTTE+D1      CONC ENTRY FOUND
         BE    CONC5                    YES BRANCH
         AR    R7,R10                   NEXT TTE ADDRESS
         BCT   R1,CONC4                 CHECK NEXT ENTRY
*
*
BAD      B     BSTOP                   SEND MESSAGE              S22024
CONC5    EQU   *
         SR    R7,R9                    POINT TO NAME
         BCTR  R9,R0                    DECREMENT FOR EX
         EX    R9,NAMEMOVE              NAME TO P-LIST
         LA    R1,TOTOTECB              GET TOTE ECB ADDRESS
         ST    R1,TOTOECBA             PUT IN P-LIST             S22024
         USING IEDQTRM,R6               TTE
         L     R6,TRMDESTQ-D1           GET QCB
         DROP  R6                       TTE
         USING IEDQQCB,R6               QCB
         SR    R1,R1                    CLEAR REG
         IC    R1,QCBRELLN              GET RLN
         STC   R1,TOTORLN              RLN TO P-LIST             S22024
         TM    TOTFLG02,TOTCONCU        ENTRY FOR CLEAN UP
         BO    CONCLUP                  YES BRANCH
****************************************************************
*
*       RECORD ORIGINAL STATUS OF CONCENTRATOR
*
****************************************************************
         TM    QCBSTAT,QCBTRMHO         CONC INITIALLY HELD
         BZ    CONCNOTH                 NO BRANCH
         MVI   TOTOVBCD,INACTERM       VERB TO INACTIVATE        S22024
         BAL   R12,CONCOPC              GO INACTIVATE CONC
         CLI   TOTORTCD,RT0            OK                        S22024
         BE    CONCSPT                  YES BRANCH
         CLI   TOTORTCD,RT8            CONC ALREADY INACTIVE     S22024
         BE    CONCARIA                 YES BRANCH
BAD1     B     BSTOP                   SEND MESSAGE              S22024
CONCARIA EQU   *                        THEREFOR CONC STOPPED
         OI    D0(R11),TOTCSTOP         SET STOPPED BIT
         B     BUMPCONC                 CONTINUE THRU DEVICES
CONCSPT  EQU   *                        THEREFOR CONC HELD
         OI    D0(R11),TOTCHELD         SET HELD BIT
         B     BUMPCONC                 CONTINUE THRU DEVICES
CONCNOTH EQU   *                        NOT HELD
         MVI   TOTOVBCD,INACTERM       VERB TO INACTIVATE        S22024
         BAL   R12,CONCOPC              GO INACTIVATE
         CLI   TOTORTCD,RT0            OK                        S22024
         BE    CONCSTPP                 YES BRANCH
         CLI   TOTORTCD,RT8            ALREADY INACTIVE          S22024
         BE    CONCINAC                 YES BRANCH
BAD2     B     BSTOP                   SEND MESSAGE              S22024
CONCINAC EQU   *                        THEREFOR INACTIVE
         OI    0(R11),TOTCINAC          SET INACTIVE BIT
CONCSTPP EQU   *
         MVI   TOTOVBCD,STOPTERM       VER TO STOP               S22024
         BAL   R12,CONCOPC              GO STOP CONC
         CLI   TOTORTCD,RT0            OK                        S22024
         BE    BUMPCONC                 YES BRANCH
         CLI   TOTORTCD,RT8            OK                        S22024
         BE    BUMPCONC                 YES BRANCH
BAD3     B     BSTOP                   SEND MESSAGE              S22024
BUMPCONC EQU   *                        BUMP ALL POINTERS
         LA    R11,D1(R11)              INIT STATUS TABLE PTR
         TM    CONCFLGS,CONCCTAP        ONLY CONTROL TERM BEEN
*                                       CHECKED
         BM    APCONCCK                 YES BRANCH
         TM    CONCFLGS,CONCAPCK        AP BEEN CHECKED
         BO    EXIT                     YES BRANCH
         LA    R3,D4(R3)                TEST DEVICE PTR
         B     LOOP                     CHECK ALL ENTRIES
         EJECT
****************************************************************
*                                                              *
*       IF ENTRY FOR CLEAN UP THIS SECTION RETURNS THE         *
*       CONCENTRATORS TO THEIR ORIGINAL STATUS                 *
*                                                              *
****************************************************************
CONCLUP  EQU   *
         TM    D0(R11),TOTCSTOP         CONC INIT STOPPED
         BO    BUMPCONC                 YES BRANCH NOTHING
*                                       NEED BE DONE
         TM    D0(R11),TOTCSTRT         CONC INIT STARTED
         BZ    STCONC                   YES BRANCH
         TM    D0(R11),TOTCHELD         CONC INIT HELD
         BO    CAVTCONC                 YES BRANCH
         MVI   TOTOVBCD,REALTERM       VERB TO REALEASE          S22024
         B     ORIGCONC                 BRANCH
CAVTCONC EQU   *
         MVI   TOTOVBCD,ACTVTERM       VERB TO ACTIVATE          S22024
         B     ORIGCONC                 BRANCH
STCONC   EQU   *
         MVI   TOTOVBCD,STRTTERM       VERB TO START             S22024
ORIGCONC EQU   *
         BAL   R12,CONCOPC              GO RETURN CONC
         CLI   TOTORTCD,RT0            OK                        S22024
         BE    BUMPCONC                 YES BRANCH
         CLI   TOTORTCD,RT8            OK                        S22024
         BE    BUMPCONC                 YES BRANCH
*
*       CAN NOT RETURN CONCENTRATOR TO ORIGINAL STATUS
*
BSTOP    EQU   *
         WTO   'IED154I TOTE CANNOT RETURN DEVICE          TO ORIGINAL C
               STATUS',ROUTCDE=10,DESC=7
MESSAGE  EQU   BSTOP+43                 CONC NAME
         B     BUMPCONC                 CONTINUE
EXIT     EQU   *                        EXIT ROUTINE
         L     R13,D4(R13)              GET CALLERS SAVE ADDR
         TM    TOTFLG02,TOTCONCU        ENTRY FOR CLEAN UP
         BO    CLUPEXIT                 YES BRANCH
         OI    TOTFLG02,TOTCONCU        CLEAN UP REQUIRED
         XCTL  (2,12),EP=IEDQWD         GO TO IEDQWD
CLUPEXIT EQU   *
*
*       RETURN TO RESIDENT MODULE
*
         LM    R14,R12,D12(R13)         RESTORE REGS
         SR    R15,R15                  GOOD RETURN
         BR    R14                      GO TO IEDQWA
TNTALINE EQU   *                        THIS ROUTINE ALINES
*                                       THE TTE ADDRESS
         LR    R9,R8                    SAVE TNT POINTER
         SR    R10,R10                  CLEAR REG
         IC    R10,TOTTTBEL             GET NAME LENGTH
         AR    R9,R10                   POINT TO TTE ADDRESS
         MVC   TTEADDR+D1(D3),D0(R9)    ALLINE ADDRESS
         L     R6,TTEADDR               GET TTE ADDRESS
         BR    R12                      RETURN
*
*       OP CONTROL INTERFACE ROUTINE
*
CONCOPC  EQU   *
         XC    TOTOLINK(D3),TOTOLINK   CLEAR LINK FIELD          S22024
         XC    TOTOTECB,TOTOTECB        CLEAR ECB
         MVI   TOTORTCD,D0             CLEAR RETURN CODE         S22024
         LA    R1,CBSLPL                GET P-LIST ADDRESS
         AQCTL                          POST ELE TO OP CONTROL
         WAIT  ECB=TOTOTECB             WAIT FOR COMPLETION
         MVC   MESSAGE(D8),TOTOLADR    MOVE NAME TO MESSAGE      S22024
         BR    R12                      RETURN
CONTROLC EQU   *
         OI    CONCFLGS,CONCCTCK        CKECK MADE FOR CT
         L     R8,TOTCRTNT              GET CONTROL TERM ENTRY
         LA    R8,D0(R8)                ZERO HI BYTE
         LTR   R8,R8                    IS THERE ONE
         BZ    APCONCCK                 NO BRANCH
         B     CTAP                     GO CHECK CONRROL TERM
APCONCCK EQU   *
         OI    CONCFLGS,CONCAPCK        INDICATE AP CHECK
         L     R8,TOTARTNT              GET AP ENTRY
         LA    R8,D0(R8)                ZERO HI BYTE
         LTR   R8,R8                    IS THERE ONE
         BZ    EXIT                     NO BRANCH
         B     CTAP                     GO CHECK AP
         EJECT
*       PARAMETER LIST FOR SVC 102
*
CBSLPL   DS    0F                       ALLINE
         DC    X'0C'                    START
         DC    AL3(0)                  FIRST                     S22024
         DC    X'80'                    FLAG END
         DC    AL3(0)                  LAST                      S22024
*
*
TTEADDR  DC    A(0)                     TTE ADDRESS
CONTTES  DC    40X'0'                   SAVE AREA FOR 10 CONC
*                                       TTES
CONTTE   DC    A(0)                     CURRENT TTE SAVE
NAMEMOVE MVC   TOTOLADR(D0),D0(R7)     NAME TO P-LIST            S22024
CONCFLGS DC    AL1(0)                   PATH FLAGS
CONCCTCK EQU   X'80'                    CONTROL TERM CHECK
CONCAPCK EQU   X'40'                    AP TERM CHECK
CONCCTAP EQU   X'C0'                    CT AND AP CHECK
PATCH    DS    CL100                    PATCH AREA
         EJECT
         TTNTD                          TNT
         EJECT
         TTRMD                          TTE
         EJECT
         TAVTD                          AVT
         EJECT
         TLCBD                          LCB
         EJECT
         CVT                            CVT
         EJECT
         TQCBD                          QCB
         TQCBED                         QCBE
         EJECT
         OLTCB                          OLTCB
         END
