         TITLE 'IEDQWC TRM ANALYSIS MODULE 1'
IEDQWC   CSECT
*A000000-999999                                                @Y16X5U0
* CHANGE ACTIVITY AS FOLLOWS:
*C840880                                                         S21903
*D834100-834600,836860-836880                                   SA51078
*C836820-836840                                                 SA51078
*A837000                                                        SA41597
*C836100                                                        SA41597
*D837440-837520                                                 SA41597
*A088860,813600                                                 SA41607
*A440600                                                        SA41606
*C824000                                                        SA41611
*D104200-104400,358560-363560                                   SA41614
*A151400-151600,580380,580425-580620,580780-581400              SA61759
*C477800                                                       @SA70654
*A147200-147400,379470-379960,394300-394500,408850-409450        S05331
*A410900-411200,690240-690800,766520-767020                      S05331
*C645400                                                         S05331
*A233100,409150,891600,466200,467600,469000,477400,581300      @Y17XAUU
*A597100,617400,646100,654500,657300,666400,690280,703500      @Y17XAUU
*A707000,710500,713300,723800                                  @Y17XAUU
*C166600                                                       @Y17XAUU
*D184100,795200-796600,798000-807100,816200-822500             @Y17XAUU
* A073500,237300,285900,402500,439600,770700,766640,766720     @G36XRUV
* C073500,237300,285900,403200,439600,766660,766740            @G36XRUV
*A321300                                                       @OZ27338
*C329000-329700                                                @OZ27338
*
         SPACE 2
         USING IEDQWC,R15
IEDQWC   IEDHJN STARC,HJN
         SPACE 2
****************************************************************
*                                                              *
* MODULE NAME = IEDQWC (TCAM,TOTE)                             @Y17XAUU
*                                                              *
* DESCRIPTIVE NAME = TRM ANALYSIS MODULE 1                     *
*                                                              *
* COPYRIGHT = NONE                                             *
*                                                              *
* STATUS --                                                    *
*                                                              *
*    VERSION 10.0                                              @Y17XAUU
*                                                              *
* FUNCTIONS --                                                 *
*                                                              *
*    THE PURPOSE OF THIS MODULE IS TO ANALYZE TRM'S AND TURN   *
*    OVER CONTROL TO THE APPROPRIATE MODULE FOR FURTHER        *
*    PROCESSING.                                               *
*                                                              *
*    THE TRM IS OBTAINED FROM THE OLTCB CONTROL BLOCK.         @Y17XAUU
*                                                              *
*    THE CONTROL TERMINAL ENTRY OF THE TRM IS ANALYZED. IF     *
*    THERE IS AN ERROR IN THE CONTROL TERMINAL ENTRY, THE      *
*    SOURCE IS NOTIFIED OF THE ERROR CONDITION AND THE         @Y17XAUU
*    SYSTEM OPERATOR IS INFORMED THAT A TRM WAS RECEIVED BUT   *
*    IGNORED BECAUSE OF AN ERROR. IF AN ERROR IS DETECTED IN   *
*    THE AP OPTION ON THE OPTION FIELD, AN ERROR BIT IS SET    *
*    FOR IEDQWC2 TO INTERROGATE. CONTROL IS THEN PASSED TO THE *
*    NEXT TRM ANALYSIS MODULE, IEDQWC1.                        *
*                                                              *
*    IF THE CONTROL TERMINAL OR ALTERNATE PRINTER IS A REMOTE  *
*    TERMINAL, A DUMMY QCB AND TTE IS SET UP FOR EACH. IF      *
*    ENOUGH IS UNAVAILABLE FOR THE ENTRIES, THE SYSTEM OPERATOR*
*    AND THE SOURCE ARE NOTIFIED AND THE TRM REJECTED.         @Y17XAUU
*                                                              *
*    IF ENTRY IS FROM A NUMERIC ONLY DEVICE, THE CONTROL       *
*    FIELD IS SET UP. THE CONTROL TERMINAL DEFAULTS TO THE     *
*    REQUEST TERMINAL. CONTROL IS PASSED TO THE NUMERIC TRM    *
*    ANALYSIS MODULE TO READ IN THE REMAINDED OF THE TRM.      *
*    UPON RETURN TO IEDQWC, THE TRM THAT WAS BUILT IS ANALYZED *
*    NORMALLY BY IEDQWC.                                       *
*                                                              *
*    THE OPTION FIELD OF THE TRM IS CHECKED FOR THE AP OPTION. *
*    IF THE AP OPTION IS SPECIFIED, THIS MODULE VERIFIES THE   *
*    NAME OF THE ALTERNATE PRINTER TO BE SYSCON, SYSOUT, OR A  *
*    VALID OPERAND TCAM TERMINAL.                              *
*                                                              *
* ENTRY POINTS --                                              *
*                                                              *
*    IEDQWC                                                    @Y17XAUU
*                                                              *
* LINKAGE -- XCTL'ED FROM IEDQWB1                              @Y17XAUU
*                                                              @Y17XAUU
* MACROS -- AQCTL                                              @Y17XAUU
*           DEQ                                                @Y17XAUU
*           ENQ                                                @Y17XAUU
*           IEDHJN                                             @Y17XAUU
*           IEDQMSG                                            @Y17XAUU
*           LINK                                               @Y17XAUU
*           POST                                               @Y17XAUU
*           SAVE                                               @Y17XAUU
*           WAIT                                               @Y17XAUU
*           WTO                                                @Y17XAUU
*           XCTL                                               @Y17XAUU
*                                                              @Y17XAUU
* PURPOSE -- SEE FUNCTION                                      @Y17XAUU
*                                                              @Y17XAUU
* MODULE SIZE -- 4K MAXIMUM                                    @Y17XAUU
*                                                              @Y17XAUU
* MODULE TYPE -- PROCEDURE                                     @Y17XAUU
*                                                              @Y17XAUU
* PROCESSOR -- ASSEMBLER XF                                    @Y17XAUU
*                                                              @Y17XAUU
* INPUT --                                                     *
*                                                              *
*    REGISTER 02,13,14,15 CONTAIN THE FOLLOWING VALUES:        *
*                                                              *
*    02 - OLTCB POINTER                                        *
*    13 - SAVE AREA ADDRESS                                    *
*    14 - RETURN ADDRESS                                       *
*    15 - ENTRY POINT ADDRESS                                  *
*                                                              *
* OUTPUT --                                                    *
*                                                              *
*    NONE                                                      *
*                                                              *
* EXTERNAL REFERENCES --                                       *
*                                                              *
*    IEDQWB - RESOURCE MANAGER                                 *
*    AVTUI  - TCAM BINARY SEARCH ROUTINE                       *
*    IEDQCA - TCAM OPERATOR CONTROL MODULE                     *
*    IEDQWK - TOTE MESSAGE MODULE                              *
*                                                              *
* EXITS,NORMAL --                                              *
*                                                              *
*    XCTL TO IEDQWB1 ON CONFIGURATOR,PROMPT AND NORMAL TRM     @G36XRUV
*    REQUESTS.                                                 @G36XRUV
*    XCTL TO IEDQWH WHEN TRM IS ENTERED FROM NUMERIC-ONLY      @G36XRUV
*    DEVICES.                                                  @G36XRUV
*    XCTL TO IEDQWF1 ON DEALLOCATION REQUESTS.                 @G36XRUV
*                                                              *
* EXITS,ERROR --                                               *
*    XCTL TO IEDQWB1 (AP ERROR)                                @Y17XAUU
*    XCTL TO IEDQWE (CT ERROR)
*    RETURN TO IEDQWA (CT ERROR)                               @Y17XAUU
*                                                              *
* TABLES/WORK AREA --                                          *
*                                                              *
*    CVT,AVT,TERMNAME TABLE,TERMINAL TABLE,OLTCB,LCB,DCB,SCB,  *
*    TCB,IEDQWC WORK AREA,DEB                                  *
*                                                              *
* ATTRIBUTES -- --                                             *
*                                                              *
*    ENABLED, PROBLEM PROGRAM MODE, TRANSIENT                  *
*                                                              *
* NOTE --                                                      *
*                                                              *
*  DEPENDENCIES -- EBCDIC CHARACTER CODE                       @Y17XAUU
*                                                              @Y17XAUU
*  RESTRICTIONS -- NONE                                        @Y17XAUU
*                                                              @Y17XAUU
*  REGISTER CONVENTIONS -- SEE REGISTER ASSIGNMENT             @Y17XAUU
*                                                              @Y17XAUU
*  PATCH LABEL -- PATCH
*                                                              *
****************************************************************
         EJECT
***********************************************************************
*                                                                     *
*        EQUATES                                                      *
*                                                                     *
***********************************************************************
         SPACE 2
R0       EQU   0                  REG 0
R1       EQU   1                  REG 1
R2       EQU   2                  OLTCB POINTER/SCT POINTER
R3       EQU   3                  REG 3
R4       EQU   4                  REG 4
R5       EQU   5                  REG 5
R6       EQU   6                  BASE REG
R7       EQU   7                  REG 7
R8       EQU   8                  REG 8
R9       EQU   9                  REG 9
R10      EQU   10                 REG 10
R11      EQU   11                 REG 11
R12      EQU   12                 REG 12
R13      EQU   13                 SAVE AREA POINTER
R14      EQU   14                 RETURN ADDR
R15      EQU   15                 ENTRY POINT ADDR
         SPACE 2
XFF      EQU   X'FF'              MASK SETTING
XF0      EQU   X'F0'              LCB MASK FOR ANR LOCAL
ANR      EQU   X'1E'              REQ TERM = ANR LO
ACTVBCD  EQU   X'46'              OP CONTROL ACTIVATE CMMD
ACTRTCD  EQU   X'08'              INVITATION LIST ACTIVE RT CD
HELDN    EQU   X'04'              HOLD MODE                     SA41607
D0       EQU   0                  DISP OF 0
D1       EQU   1                  DISP OF 1
D2       EQU   2                  DISP OF 2
D3       EQU   3                  DISP OF 3
D4       EQU   4                  DISP OF 4
D5       EQU   5                  DISP OF 5
D6       EQU   6                  DISP OF 6
D7       EQU   7                  DISP OF 7
D8       EQU   8                  DISP OF 8
D9       EQU   9                  DISP OF 9
D10      EQU   10                 DISP OF 10
D11      EQU   11                 DISP OF 11
D12      EQU   12                 DISP OF 12
D13      EQU   13                 DISP OF 13
D14      EQU   14                 DISP OF 14
D15      EQU   15                 DISP OF 15
D16      EQU   16                 DISP OF 16
D20      EQU   20                 DISP OF 20
D22      EQU   22                 DISP OF 22
D24      EQU   24                 DISP OF 24
D28      EQU   28                 DISP OF 28
D32      EQU   32                 DISP OF 32
ACVT     EQU   16                 POINTER TO CVT ADDRESS
PRETERM  EQU   X'52'              DISP TO 1ST ENTRY IN TABLE
TRMAX    EQU   72                 TRM BUFFER LENGTH
SLASH    EQU   X'61'              A / CHAR
COMMA    EQU   X'6B'              A , CHAR
D17      EQU   17                 DISP OF 17
D18      EQU   18                 DISP OF 18
D19      EQU   25                 DISP OF 25                     S22024
DCBNIDLE EQU   X'02'              3705 ACTIVE                    S22024
X40      EQU   X'40'              A BLANK CHAR
RNTERM   EQU   X'E0'              RN TERMINAL (IF OFF)           S22024
XE0      EQU   X'E0'              QCB MASK SETTING
RNLINE   EQU   X'80'              RN LINE CHECK                  S22024
TCAMDEB  EQU   X'44'              DEB BELONGS TO TCAM FLAG       S22024
DEFLEV   EQU   X'03'              DPRINT DEFAULT LEVEL
PARMREG0 EQU   0                  REG 0
PARMREG1 EQU   1                  REG 1
RQINIT   EQU   X'0C'              START OF LIST                  S22024
RQEND    EQU   X'80'              END OF LIST                    S22024
D72      EQU   72                 LENGTH OF TRM BUFFER
XBLANK   EQU   X'61'              A SLASH CHAR
TWO      EQU   2                  DISP OF TWO
FOUR     EQU   4                  DISP OF FOUR
TRMSCCT  EQU   X'10'              SECONDARY SYSTEM CONSOLE ENTRY ?
PROMPTFL EQU   X'01'              PROMPTER RQ FLAG
XFO      EQU   X'F0'              NUMERIC TESTER
X52      EQU   X'52'              1060 CT FLAG
X18      EQU   X'18'              DISP OF 24
X24      EQU   X'24'              DISP OF 36
X1C      EQU   X'1C'              DISP OF 28
X10      EQU   X'10'              DCB OPEN FLAG
X80      EQU   X'80'              LAST DUMMY ENTRY RQ FLAG
X14      EQU   X'14'              DISP OF 20
X07      EQU   X'07'              LINE STOPPED FLAG
DEQFLG   EQU   X'01'              DEQ REQUEST FLAG               S05331
ENQFLG   EQU   X'83'              ENQ REQUEST FLAG               S05331
HOLD     EQU   X'88'              'HOLD' OPERATOR CONTROL VERB CODE
UCB1060  EQU   X'52'              UCB SETTING FOR 1060 TERMINAL
ZERO     EQU   X'00'              CONSTANT VALUE
CONFIGFL EQU   X'02'              INDICATES CONFIGURATOR REQUEST
TRMMAX   EQU   256                MAXIMUM LENGTH OF TRM
LGBA     EQU   X'80'              LGB MAP                        S22024
X60      EQU   X'60'              MASK FIELD                    SA61759
TSONLY   EQU   X'40'              TSO ONLY SYSTEM CHECK         SA61759
         EJECT
***********************************************************************
*                                                                     *
*        PERFORM INITIALIZATION                                       *
*                                                                     *
***********************************************************************
         SPACE 1
         SPACE 2
         SAVE  (14,12)
         SPACE
         DROP  R15
         LR    R6,R15                   LOAD BASE
         USING TOTOLTCB,R2
         USING IEDQWC,R6
         LA    R3,TOTSAVE4        GET MY SAVE AREA ADDR
         ST    R3,D8(R13)         STORE IN CALLER'S SAVE AREA
         ST    R13,D4(R3)         SAVE MY SAVE AREA ADDR
         ST    R13,TOTRESSV       SAVE MOTHER TASK SAVE AREA ADDR
         LR    R13,R3                   SET R13 TO SAVE AREA
         SPACE 2
         L     R10,TOTAVTPT       GET AVT ADDR
         USING IEDQAVTD,R10                                    @Y17XAUU
         OI    $OLTFLGS,$RETCODE  SHOW RETURN CODE FTN SUPPORTED S22024
TRM000   EQU   *
*
*        SAVE PARAMETER PASSED BY IEDQWB1
*
         STH   R1,TRMSRCE         INDEX TO TERMNAME TABLE
         SRL   R1,D24             ADJUST TO GET FLAG BYTE
         STC   R1,NUMFLG          SAVE NUMERIC ENTRY FLAG BYTE
         SPACE
         MVC   TRMWRK(D72),TOTTRMBF MOVE TRM TO WORK AREA
         SPACE
         TM    NUMFLG,NUMEN       NUMERIC ENTRY
         BO    TRMNUH             YES PROCESS
         SPACE
TRMCHK6  EQU   *
         LA    R4,TRMWRK          PICK UP WORK AREA ADDRESS
         L     R3,AVTRNMPT        PICK UP ADDRESS OF TERMNAME TABLE
         ST    R3,TERNMTAB        SAVE IT
         B     TRMCHK8            GO CONTINUE TRM PROCESSING
         SPACE
         EJECT
***********************************************************************
*                                                                     *
*        PRE-PROCESSOR FOR NUMERIC ENTRY                              *
*                                                                     *
***********************************************************************
         SPACE 2
TRMNUH   EQU   *
         SR    R4,R4              CLEAR REG 4
         IC    R4,TOTTTBEL        GET MAX. NAME LENGTH
         LA    R4,D3(R4)          ADJUST FOR ONE ENTRY
         LH    R3,TRMSRCE         GET RELATIVE POS. OF TERM IN TT
         BCTR  R3,R0              DECREMENT BY 1
         STH   R3,TRMSRCE         SAVE IT
         MH    R4,TRMSRCE         GET DISP TO ENTRY
         L     R5,AVTRNMPT        GET ADDR OF TERMNAME TABLE
         LA    R5,PRETERM(R5)     POINT PASS PREFIX
         AR    R4,R5              POINT TO SYMBOLIC NAME OF RQ TER.
         SR    R5,R5              CLEAR REG 5
         IC    R5,TOTTTBEL        GET MAX. NAME LENGTH
         BCTR  R5,R0              DECREMENT BY ONE
         EX    R5,EXMSYM          MOVE NAME TO WORK AREA
TRMNUH1  EQU   *
         LA    R1,WORK1           POINT NAME
         MVC   TOTCTNAM(D8),WORK1 SAVE CT NAME IN OLTCB
         MVI   TOTCTNAM+D8,XBLANK SHOW END
         LA    R10,D0             INIT COUNTER
         SR    R3,R3              CLEAR REG 3
         IC    R3,TOTTTBEL        GET MAX. NAME LENGTH
         LA    R3,D1(R3)          INCREMENT BY 1
TRMNUH3  EQU   *
         CLI   D0(R1),X40         END OF NAME
         BE    TRMNUH7            YES
         BCT   R3,TRMNUH5         SET UP TO CHECK NEXT CHAR
         B     TRMNUH7            BRANCH TO PUT NAME LENGTH INTO
*                                   PARAMETER LIST
TRMNUH5  EQU   *
         LA    R1,D1(R1)          POINT TO NEXT CHAR
         LA    R10,D1(R10)        INCREMENT NAME COUNTER
         B     TRMNUH3            CHECK NEXT CHAR
TRMNUH7  EQU   *
         STC   R10,LENGTH         PUT NAME LENGTH IN P - LIST
         LA    R5,TRMWRK          POINT TO TRM
         OI    TOTFLG09,TOTPRECT  SET CT EQUALS PRINTER FLAG
         B     TRMCK14A           EXIT TO VERIFY NAME
EXMSYM   MVC   WORK1(D0),D0(R4)   MOVE RQ TERMINAL TO WORK AREA
         DROP  R10
         EJECT
***********************************************************************
*                                                                     *
*        CONTINUE NORMAL TRM PROCESSING                               *
*                                                                     *
***********************************************************************
         SPACE 1
         SPACE 2
TRMCHK8  EQU   *
         LA    R5,TRMWRK          POINT TO TRM
         OI    TOTFLG09,TOTPRECT  SET C.T. EQUALS PRINTER FLAG
TRMCHK11 EQU   *
         MVC   WORK1(D9),D0(R5)   MOVE CONTROL TERM TO WORK AREA
         CLC   WORK1(D7),SYSCONL  CONTROL TERM. EQU SYSTEM CON
         BNE   TRM11A             NO, BRANCH
         OI    TOTFLG09,TOTCTCON+TOTPRECT  TURN ON CONT TERM FLAG
         LA    R10,D7(R5)         POINT TO TEST DEVICE FIELD   @Y17XAUU
         LA    R3,TRMMAX          GET MAX TRM LENGTH ALLOWED   @Y17XAUU
TOTE1    DS    0H                 FIND END OF TEST DEV FIELD   @Y17XAUU
         CLI   D0(R10),SLASH      END REACHED                  @Y17XAUU
         BE    TOTE11             YES                          @Y17XAUU
         LA    R10,D1(R10)        BUMP TO NEXT FIELD           @Y17XAUU
         BCT   R3,TOTE1           CONTINUE SEARCH              @Y17XAUU
         B     TRMCK85            NO SLASH EQU ERROR           @Y17XAUU
TOTE11   DS    0H                 CHECK FOR DEALLOCATION REQ   @Y17XAUU
         CLC   D0(D7,R10),TOTE    DEALLOCATION TRM             @Y17XAUU
         BNE   TRMCK85            NO - CONTINUE                @Y17XAUU
         C     R3,TRMMAX          TEST DEVICE FIELD OMITTED    @Y17XAUU
         BE    TRMCK85            YES - ERROR                  @Y17XAUU
         L     R13,D4(R13)        RESTORE                      @Y17XAUU
         LM    R14,R0,D12(R13)    REGISTERS                    @Y17XAUU
         XCTL  (2,12),EP=IEDQWF1  PROCESS DEALLOCATION REQUEST @Y17XAUU
TRMCK85  DS    0H                                              @Y17XAUU
         LA    R10,D6             SET COUNTER REG FOR SYSCON
         MVC   WORK1+D6(D2),BLANK PAD WORK1 WITH BLANKS
         LA    R5,D1(R5,R10)      BUMP POINTER TO NEXT FIELD
         B     TRM045             BRANCH
TRM11CA  EQU   *
         L     R3,TOTFLG01        WORD CONTAINING TOTCONFG     @G36XRUV
CSLOOP1  DS    0H                                              @G36XRUV
         LR    R4,R3              COPY                         @G36XRUV
         O     R4,SETCONFG        SET CONFIG BIT IN REG        @G36XRUV
         CS    R3,R4,TOTFLG01     SET TOTCONFG                 @G36XRUV
         BNZ   CSLOOP1            BR IF BYTE HAS CHANGED       @G36XRUV
****************************************************************
*        EXIT TO IEDQWB1 TO CHECK FOR DEVICE PROTECT FEATURE   *
*        AND SEND ON LINE TESTING ACTIVE MESSAGE               *
****************************************************************
TRMEND   EQU   *
         TM    TOTFLG01,TOTCONFG+TOTPRMPT PROMPT OR CONFIG       S22024
         NI    TOTFLG04,XFF-TOTNUMDV CLEAR NUMERIC ENTRY FLAG   SA41614
         BNZ   TRMEND1            YES - ONE IS SET               S22024
         OI    TOTFLG02,TOTOLTRQ  SHOW ON-LINE TEST REQUEST      S22024
TRMEND1  EQU   *                                                 S22024
         L     R13,D4(R13)        PICK UP CALLERS SAVE AREA ADDRESS
         LM    R14,R0,D12(R13)    RESTORE REGS
         XCTL  (2,12),EP=IEDQWB1  CONTINUE TRM PROCESSING
TRM11A   EQU   *
         LA    R1,WORK1           FIND LENGTH OF CONTROL TERMINAL
         LA    R10,D0               IF IT IS LESS
         LA    R3,D9                THAN 8 CHARACTERS
TRMCHK12 EQU   *
         CLI   D0(R1),SLASH       END OF FIELD?
         BE    TRMCHK14                 YES BRANCH
         BCT   R3,TRMCHK13              GREATER THAN 8?
         B     TRMERROR                 YES GO TO ERROR ROUTINE
TRMCHK13 EQU   *
         LA    R1,D1(R1)          NO BUMP POINTER
         LA    R10,D1(R10)        BUMP COUNTER
         B     TRMCHK12                 GO LOOK AGAIN
TRMCHK14 EQU   *
         STC   R10,LENGTH         SAVE LENGTH
         LA    R9,D8              SET UP TO PAD WORK1 WITH BLANKS
         CR    R10,R9                  ALREADY 8 CHARS
         BE    TRMCK14A           YES
         SR    R9,R10                  GET NUMBER OF POSITIONS TO FILL
         LA    R1,WORK1                GET WORK1 ADDRESS
         BCTR  R9,D0              DECREMENT FOR MOVE
         AR    R1,R10                  POINT PAST NAME
         EX    R9,BLMOVE               MOVE IN BLANKS
TRMCK14A EQU   *
         MVC   RNAME(D8),WORK1    MOVE CT NAME TO ENQ LIST
         BAL   R9,NAMESRCH              GO COMPARE
         B     TRMFIND                  TERM NAME FOUND
         B     TRMERROR                 NO BRANCH TO ERROR ROUTINE
BLMOVE   MVC   D0(D0,R1),BLANK    GENERAL BLANK MOVE
TRMFIND  EQU   *
         MVC   TOTCROFF(D2),TRMTTCIN SAVE CT REAL TTCIN        @Y17XAUU
         MVC   WORK3(D8),WORK1    SAVE CONTROL TERMINAL NAME
         MVC   TRMCTSAV(D8),WORK1 SAVE IT AGAIN
         MVI   TRMRQFLG,CTREQ     SHOW C.T.REQUEST             @Y17XAUU
         SPACE
         BAL   R9,DEVCHK          VERIFY CTL TERMINAL EQU DEVICE S22024
         MVC   TOTCRTNT(D4),TRMTNT SAVE REAL CT TNT ADDR         S22024
         BAL   R9,UCBFIND         GO FIND UCB ADDR               S22024
*                                                                S22024
*        ERROR RETURN IS D0(R9); GOOD RETURN IS TO D4(R9)        S22024
*                                                                S22024
         B     NOOPEN             ERROR EXIT                     S22024
         STH   R8,TOTCTUCB        CT UCB ADDR TO OLTCB           S22024
         SPACE
         BAL   R9,DUMMYTNT        REQUEST DUMMY TNT
         MVC   TRMNAME(D8),WORK1  MOVE FOR READ COMPARE
         BAL   R9,LCBSCAN         FIND CT LCB ADDRESS
         ST    R11,TOTCTLCB-D1    PUT LCB ADDRESS IN OLTCB
         MVC   TOTCTRLN(D1),TRMRLN  PUT RLN OF CT IN OLTCB
         LA    R5,D1(R10,R5)      POINT TO NEXT FIELD            S22024
TRM045   EQU   *
         CLC   D0(D9,R5),PROMPT   PROMPT REQUEST?
         BE    TRM050             YES                            S22024
         CLC   D0(D9,R5),CONFIG   IS CONFIG REQUESTED?
         BNE   TRM11E                    NO
         OI    SSFLG,CONFIGFL     YES, SET CONFIG INDICATOR
         B     TRM15Y                      AND BRANCH
TRM050   DS    0H                 BOUNDARY ALLIGNMENT            S22024
         OI    SSFLG,PROMPTFL     SHOW PROMPT REQUEST            S22024
         L     R7,TOTFLG01        CONTAINS TOTPRMPT            @G36XRUV
CSLOOP2  DS    0H                                              @G36XRUV
         LR    R8,R7              COPY                         @G36XRUV
         O     R8,SETPRMPT        SET PROMPT FLAG IN REG       @G36XRUV
         CS    R7,R8,TOTFLG01     SET TOTPRMPT                 @G36XRUV
         BNZ   CSLOOP2            BR OF BYTE HAS CHANGED       @G36XRUV
         B     TRM15Y             ENQUEUE CT                     S22024
         EJECT
***********************************************************************
*                                                                     *
*        ROUTINE TO EVALUATE ALTERNATE PRINTER OPTION                 *
*                                                                     *
***********************************************************************
         SPACE 1
TRM11E   EQU   *
         TM    NUMFLG,NUMEN       NUMERIC ENTRY ?
         BO    TRM15Y             YES
TRM12    EQU   *
         CLI   D0(R5),X40         BLANK CHAR                    SA41606
         BNE   TRM12A             NO                            SA41606
         OI    TOTFLG03,TOTTDFER  SET TEST DEV ERROR FLAG       SA41606
         B     TRMPROMT           GO TO PROMPTER                SA41606
TRM12A   EQU   *                                                SA41606
         CLC   D0(D2,R5),SLASH2   TWO SLASHES ?
         BNE   TRM14              NO
TRM12D   DS    0H                 BOUNDARY ALLIGNMENT            S22024
         OI    TOTFLG03,TOTTDFER  SET TEST DEVICE ERROR FLAG
         B     TRMPROMT           GO TO PROMPTER
TRM14    EQU   *
         LR    R1,R5              SET UP TO FIND OPTIONS FIELD
         LA    R3,TRMMAX          SET TRM LENGTH MAXIMUM
         LA    R10,D2             SET COUNTER TO 2
TRM15B   EQU   *
         CLI   D0(R1),SLASH       END OF FIELD?
         BE    TRM15A             YES, BRANCH
         LA    R1,D1(R1)          BUMP POINTER
         BCT   R3,TRM15B          GO EXAMINE NEXT BYTE
         CL    R10,F2             ERROR IN TD FIELD              S22024
         BE    TRM12D             YES - EXIT                     S22024
         OI    TOTFLG03,TOTTSTER  ASSUME ERROR IN TEST FIELD     S22024
         B     TRMPROMT           GO TO PROMPTER                 S22024
TRM15A   EQU   *
         LA    R1,D1(R1)          BUMP POINTER
         BCT   R10,TRM15B         LOOK FOR END OF NEXT FIELD
TRM15E   EQU   *
         CLI   D0(R1),SLASH       END OF OPTION FIELD?
         BE    TRM15Y             YES - EXIT
TRM15EA  EQU   *
         CLC   D0(D3,R1),NAP      NAP OPTION SPECIFIED?
         BNE   TRM15EB            NO, CHECK FOR AP
         NI    $ERROPT,XFF-$ALTPRNT  TURN OFF AP OPTION
         OI    TOTFLG09,TOTPRECT  SET NO ALTERNATE PRINTER FLAG
         LA    R1,D2(R1)          POINT PAST NA
         B     TRM15F             GO CHECK NEXT FIELD
TRM15EB  EQU   *
         CLC   D0(D2,R1),AP       AP OPTION?
         BNE   TRM15F             NO
         OI    $ERROPT,$ALTPRNT    SET AP OPTION
         NI    TOTFLG09,XFF-TOTPRECT TURN OFF NO ALT PRNT FLAG
         B     TRM15D             BRANCH TO GET NEXT OPTION
TRM15F   EQU   *
         LA    R1,D1(R1)          BUMP POINTER
         BCT   R3,TRM15FB               LOOP IF NOT AT END     @OZ27338
         B     APCOM                    TRM TOO LONG           @OZ27338
TRM15FB  EQU   *
         CLI   D0(R1),COMMA       OPTION DELIMITER ?
         BE    TRM15FA                 YES BRANCH
         CLI   D0(R1),SLASH       END OF FIELD ?
         BNE   TRM15F             NO
         TM    $ERROPT,$ALTPRNT   HAS A TERMINAL BEEN ENTERED FOR AP?
         BZ    TRM15Y             NO
         B     TRM15H1            YES
TRM15FA  EQU   *
         LA    R1,D1(R1)          BUMP POINTER PAST COMMA
         BCT   R3,TRM15EA               GO LOOK AT NEXT OPTION @OZ27338
         B     APCOM              OPTION ERROR
TRM15D   EQU   *
         LA    R1,D2(R1)          POINT PAST AP
         LA    R10,D0             SET UP LENGTH COUNTER
         LR    R4,R1              SAVE R1
         LA    R3,D9              SET ERROR COUNTER
TRM15G   EQU   *
         CLI   D0(R1),COMMA       END OF AP OPTION ?
         BE    TRM15H                  YES BRANCH
         CLI   D0(R1),SLASH       END OF FIELD ?
         BE    TRM15H                  YES BRANCH
         LA    R10,D1(R10)        BUMP COUNTER BY 1
         LA    R1,D1(R1)          BUMP POINTER
         BCT   R3,TRM15G          LOOP UNTIL END OF AP FIELD
         B     APCOM              A.P. NAME TOO LONG
APMOVE   MVC   WORK1(D0),D0(R4)   GENERAL MOVE
TRM15H   EQU   *
         LTR   R10,R10            IS NAME FIELD PRESENT?
         BZ    APCOM                NO: GO TO PROMPTER
         STC   R10,LENGTH         SAVE LENGTH OF OPTION
         BCTR  R10,D0             DECREMENT COUNTER
         MVC   WORK1(D8),BLANK    FILL BUFFER WITH BLANKS
         EX    R10,APMOVE               MOVE NAME TO WORK AREA
         CLI   D0(R1),SLASH       END OF OPTION FIELD ?
         BE    TRM15H1            YES
         B     TRM15FB            NO
TRM15H1  EQU   *
         CLC   WORK1(D6),SYSCON   SYSTEM CONSOLE ?
         BNE   TRM15H2                   NO
         OI    TOTFLG09,TOTAPCON  SET ALT PRINTER EQU SYSCON   @YM0
         OI    TOTFLG09,TOTPRECT  ASSUME PRINTER EQU CT
         TM    TOTFLG09,TOTCTCON  CT EQU SYSTEM CONSOLE ?
         BO    TRM15Y                    YES
         NI    TOTFLG09,XFF-TOTPRECT CLEAR PRINTER EQU CT FLAG
         B    TRM15Y                  YES BRANCH
TRM15H2  EQU   *
         CLC   WORK1(D6),SYSOUT   SYSTEM OUTPUT DEVICE ?
         BNE   TRM15HA                   NO
         NI    TOTFLG09,XFF-TOTPRECT CLEAR PRINTER EQU CT FLAG
         OI    TOTFLG09,TOTAPOUT  SET PRINTER EQU SYSOUT
         B     TRM15Y                  YES BRANCH
TRM15HA  EQU   *
         MVC   RNAME(D8),WORK1    MOVE AP NAME TO ENQ LIST
         MVC   TOTAPNAM(D8),WORK1 MOVE AP NAME TO OLTCB
         BAL   R9,NAMESRCH             GO FIND ENTRY IN TERMNAME TABLE
         B     TRM15I                  BRANCH AROUND ERROR RETURN
         B     APCOM              ERROR
TRM15I   EQU   *
         MVC   TOTAROFF(D2),TRMTTCIN SAVE AP REAL TTCIN        @Y17XAUU
         MVI   TRMRQFLG,APREQ     SHOW A.P. REQUEST            @Y17XAUU
         BAL   R9,DEVCHK          VERIFY AP EQU A TERMINAL       S22024
         MVC   TOTARTNT(D4),TRMTNT SAVE REAL AP TNT ADDR         S22024
         BAL   R9,UCBFIND         GO FIND UCB ADDR               S22024
         B     APNOER             ERROR EXIT                     S22024
         STH   R8,TOTAPUCB        AP UCB ADDR TO OLTCB           S22024
         BAL   R9,DUMMYTNT        REQUEST DUMMY TNT
         TM    TRMFLG01,TRMCTEAP  CT EQU AP                      S22024
         BZ    TRM15I2            NO - PROCESS AP                S22024
         OI    TOTFLG09,TOTPRECT  SET CT EQU AP FLAG             S22024
         B     TRM15Y             BYPASS AP SETUP                S22024
TRM15I2  EQU   *                                                 S22024
         MVC   TRMNAME(D8),WORK1  MOVE FOR READ COMPARE
         BAL   R9,LCBSCAN         GET AP LCB ADDR
         ST    R11,TOTAPLCB-D1    PUT IT IN OLTCB
         MVC   TOTAPRLN(D1),TRMRLN PUT RLN FOR AP IN OLTCB       S22024
         USING UCB,R8
TRMAPENQ EQU   *
         ENQ   ,MF=(E,ENQLIST)         REQUEST RESOURCE
         LTR   R15,R15                 WAS IT ASSIGNED
         BZ    TRM15I4                   YES
         CLI   D3(R15),D8         WAS IT ALREADY ASSIGNED?
         BE    TRM15I4                   YES
         TM    RNAME,XF0          ENQ FAIL ON UCB NAME           S05331
         BNO   TRMAPX             NO - BYPASS TERMINAL NAME DEQ  S05331
*        DEQ AP TERMINAL NAME                                    S05331
         MVI   ENQLIST+D2,DEQFLG  SET DEQ FLAG                   S05331
         MVC   RNAME(D8),TOTAPNAM AP NAME TO PLIST               S05331
         XC    TOTAPNAM(D8),TOTAPNAM CLEAR AP NAME FIELD         S05331
         DEQ   ,MF=(E,ENQLIST)    DEQ AP NAME                    S05331
TRMAPX   DS    0H                 BOUNDARY ALLIGNMENT            S05331
         WTO   'IED128I ALTERNATE PRINTER REQUESTED BY OLT ALREADY IN U*
               SE',ROUTCDE=10,DESC=7
         B     APCOM              SHOW AP ERROR
TRM15I4  EQU   *
         TM    RNAME,XFO          HAS UCBNAME BEEN ENQ?
         BO    TRM15I4A           YES - EXIT
         MVC   RNAME(D8),BLANK    BLANK AP NAME FIELD            S22024
         MVC   RNAME(D3),UCBNAME  MOVE AP UCBNAME TO ENQ LIST
         B     TRMAPENQ           GO REQUEST RESOURCE
TRM15I4A EQU   *
         NI    TRMFLG04,XFF-CTRSID CLEAR CT FLAG                 S22024
         OI    TRMFLG04,APRSID    SHOW AP REQUEST                S22024
         BAL   R9,RNLINET         CHECK FOR RN TERM FOR L ENQ    S22024
         SPACE
         SPACE
         SPACE
         OI    TOTFLG09,TOTPRECT  SET PRINTER EQU CT
         CLC   TRMCTSAV(D8),TOTAPNAM CT = AP ?
         BE    TRM15Y             YES                            S22024
         NI    TOTFLG09,XFF-TOTPRECT NO - SET
         OI    TOTFLG09,TOTAPTRM  SET PRINTER EQU TERMINAL FLAG
         EJECT
***********************************************************************
*                                                                     *
*        FIND IF TRM WAS ENTERED FROM CONTROL TERMINAL                *
*                                                                     *
***********************************************************************
         SPACE 1
TRM15Y   EQU   *
         TM    TOTFLG04,TOTNUMDV  RETURN FROM NUMERIC HANDLER ?
         BO    TRM080             YES
         TM    TOTFLG09,TOTCTCON  CT EQU SYSTEM CONSOLE ?
         BO    TRM080             YES - BRANCH                   S22024
         SLR   R8,R8              CLEAR                        @G36XRUV
         ICM   R8,3,TOTCTUCB      GET CT UCB ADDR              @G36XRUV
         USING UCB,R8
         MVC   RNAME(D8),WORK3    GET CT NAME
TRMCTENQ EQU   *
         ENQ   ,MF=(E,ENQLIST)         REQUEST USE OF RESOURCE
         LTR   R15,R15                 WAS IT ASSIGNED?
         BZ    TRM15Y4                   YES
         CLI   D3(R15),D8         WAS IT PREVIOUSLY ASSIGNED?
         BE    TRM15Y4                   YES
         L     R11,TOTCRTNT       CT TNT ADDR                    S05331
         SR    R7,R7              CLEAR REG                      S05331
         IC    R7,TOTTTBEL        MAXIMUM NAME LENGTH            S05331
         AR    R11,R7             POINT TO TTE ADDR              S05331
         MVC   TNTENTRY+D1(D3),D0(R11) CT TTE ADDR               S05331
         L     R7,TNTENTRY        CT TTE ADDR                    S05331
         USING IEDNTRM,R7                                        S05331
         LA    R1,TRMPRFSZ        TTE PREFIX SIZE              @Y17XAUU
         SLR   R7,R1              GET ADDRESS OF TTE PREFIX    @Y17XAUU
         TM    TRMSTATE,TRMPREF   3705 MODE                    @Y17XAUU
         BZ    TRMCTX             NO                             S05331
         OI    TOTCTFLG,TOTCTORN  CT ON 3705                     S05331
         DROP  R7                                                S05331
TRMCTX   DS    0H                 BOUNDARY ALLIGNMENT            S05331
         WTO   'IED133I C.T. REQUESTED BY OLT ASSIGNED TO ANOTHER OLT',*
               ROUTCDE=10,DESC=7
         TM    TOTFLG05,TOTSRCON  TRM ENTERED FROM SYSCON      @Y17XAUU
         BO    TRMCTXE            YES - EXIT                   @Y17XAUU
         IEDQMSG MSGID=053,FUNCT=SRC    WRITE MSG TO SOURCE    @Y17XAUU
TRMCTXE  DS    0H                                              @Y17XAUU
         OI    TOTFLG06,TOTOTERM  SET TERMINATE FLAG             S05331
         L     R13,D4(R13)        CALLER'S SAVE AREA             S05331
         LM    R14,R1,D12(R13)    RESTORE REG 14-1               S05331
         XCTL  (2,12),EP=IEDQWE2  TERMINATE TRM                  S05331
TRM15Y4  EQU   *
         TM    RNAME,XFO          HAS UCBNAME BEEN ENQ?
         BO    TRM15Y4A           YES - EXIT
         MVI   RNAME,X40          BLANK RNAME
         MVC   RNAME+D1(D7),RNAME   FIELD
         MVC   RNAME(D3),UCBNAME  MOVE CT UCBNAME TO ENQ LIST
         B     TRMCTENQ           GO REQUEST RESOURCE
TRM15Y4A EQU   *
         NI    TRMFLG04,XFF-APRSID CLEAR AP FLAG                 S22024
         OI    TRMFLG04,CTRSID    SHOW CT REQUEST                S22024
         L     R11,TOTCRTNT       CT TNT ADDR                    S22024
         SR    R7,R7              CLEAR REG                      S22024
         IC    R7,TOTTTBEL        MAX NAME LENGTH                S22024
         AR    R11,R7             POINT TO TTE ADDR              S22024
         MVC   TNTENTRY+D1(D3),D0(R11) CT TTE ADDR               S22024
         L     R7,TNTENTRY        TTE ADDR                       S22024
         USING IEDQTRM,R7                                        S22024
         L     R7,TRMDESTQ-D1     CT QCB ADDR                    S22024
         LA    R7,D0(R7)          CLEAR FLAG BYTE                S22024
         ST    R7,TRMQCB          SAVE IT                        S22024
         DROP  R7                                                S22024
         BAL   R9,RNLINET         CHECK FOR RN TERM FOR L ENQ    S22024
         TM    TOTFLG09,TOTCTCON  CONTROL TERM = SYSCON ?
         BO    TRM080             YES
         MVC   WORK1(D3),UCBNAME  MOVE LINE ADDRESS TO WORK AREA
         DROP  R8
         BAL   R9,CTSTOP          VERIFY UCB NAME
TRM080   EQU   *
         TM    SSFLG,PROMPTFL+CONFIGFL PROMPT OR CONFIG REQUESTED
         BZ    TRM090                    NO
         TM    TOTFLG09,TOTCTCON  CT EQU SYSTEM CONSOLE
         BO    TRM090                    YES
         SLR   R8,R8              CLEAR                        @G36XRUV
         ICM   R8,3,TOTCTUCB      GET CONTROL TERM UCB ADDR    @G36XRUV
         USING UCB,R8
         CLI   UCBTBYT1,X52       1060?
         DROP  R8
         BNE   TRM090                    NO
         IEDQMSG MSGID=097,FUNCT=CEC,LINK=YES
TRM085   EQU   *                                                 S22024
         OI    TOTFLG06,TOTOTERM  SET TERMINATE FLAG
         L     R13,D4(R13)        GET CALLER'S SAVE AREA ADDR
         LM    R14,R1,D12(R13)    RESTORE REGISTERS 14-1
         XCTL  (2,12),EP=IEDQWE   TERMINATE TRM
TRM090   EQU   *
         OI    TOTFLG05,TOTNDMSG  TURN ON ENDED MESSAGE FLAG
         TM    SSFLG,PROMPTFL     RETURN TO PROMPT LOAD?
         BO    TRMPT                   YES BRANCH
         TM    SSFLG,CONFIGFL     RETURN TO CONFIG LOAD?
         BO    TRM11CA                 YES BRANCH
         TM    NUMFLG,NUMEN       NUMERIC ENTRY ?
         BO    TRM091             YES
         B     TRMEND             EXIT TO IEDQWB1
TRM091   EQU   *
         OI    TOTFLG04,TOTNUMDV  SET NUMERIC ENTRY FLAG        SA41614
         L     R13,D4(R13)        RESTORE SAVE REG              SA41614
         LM    R14,R1,D12(R13)    RESTORE REG 14-1              SA41614
         XCTL  (2,12),EP=IEDQWB1  EXIT TO IEDQWB1               SA41614
         EJECT
         OI    TOTFLG05,TOTNDMSG  SET FLAG TO SEND TERMINATE FLAG
         SPACE
**************************************************************** S22024
*                                                              * S22024
*        ROUTINE TO VERIFY THAT THE SYMBOLIC NAME ENTERED      * S22024
*        AS THE CONTROL TERMINAL OR ALTERNATE PRINTER IS A     * S22024
*        DEVICE AS OPPOSED TO A SUB CHANNEL HUNG OFF THE       * S22024
*        RN.                                                   * S22024
*                                                              * S22024
**************************************************************** S22024
DEVCHK   EQU   *                                                 S22024
         STM   R7,R11,TRMSAVE2    SAVE REGISTERS 7-11            S22024
         L     R11,TNTENTRY       GET TTE ADDR                   S22024
         LA    R7,TRMPRFSZ        TTE PREFIX SIZE              @Y17XAUU
         SLR   R11,R7             GET ADDRESS OF TTE PREFIX    @Y17XAUU
         USING IEDNTRM,R11                                       S22024
         TM    TRMSTATE,TRMPROC   VERIFY NOT PROCESS ENTRY     @Y17XAUU
         BO    DEVCHKE            BR IF YES                    @Y17XAUU
         TM    TRMSTATE,TRMPREF   3705 MODE                    @Y17XAUU
         BZ    DEVCHKE            NO - RETURN                    S22024
         CLI   TRMTYPE,TRMPUNT    PU ENTRY                     @Y17XAUU
         BE    DEVCHK8            YES - ERROR                  @Y17XAUU
         CLI   TRMTYPE,TRMLNCP    NCP ENTRY                    @Y17XAUU
         BE    DEVCHK8            YES - ERROR                  @Y17XAUU
         CLI   TRMTYPE,TRMSSCP    SSCP ENTRY                   @Y17XAUU
         BE    DEVCHK8            YES - ERROR                  @Y17XAUU
         TM    TRMSTATE,RNTERM    TERMINAL ATTACHED TO RN        S22024
         BZ    DEVCHKE            YES                            S22024
*                                                              * S22024
*        AN INVALID ENTRY HAS BEEN MADE IN THE AP/CT FIELD.    * S22024
*        IF CT ERROR NOTIFY SYSTEM OPERATOR VIA A 'WTO'.       * S22024
*        IF AP ERROR SET ERROR FLAG AND CONTINUE PROCESSING.   * S22024
*                                                              * S22024
DEVCHK8  DS    0H                 BOUNDARY ALLIGNMENT            S22024
         CLI   TRMRQFLG,CTREQ     CONTROL TERMINAL ERROR       @Y17XAUU
         BNE   APCOM              NO - ASSUME AP ERROR           S22024
         B     TRMERROR           TERMINATE REQUEST              S22024
         SPACE
DEVCHKE  EQU   *                                                 S22024
         TM    TRMSTATE,TRMPREF   3705 MODE                    @Y17XAUU
         BO    DEVCHKED           YES - BYPASS CHECK             S22024
         TM    TRMSTATE,XE0       TERMINAL ENTRY                 S22024
         BZ    DEVCHKED                 YES, VALID TERMINAL    @SA70654
         MVC   WRKFLG(D1),TRMSTATE      GET FLAG BYTE          @SA70654
         NI    WRKFLG,XE0               CLEAR UNWANTED FLAGS   @SA70654
         CLI   WRKFLG,RNLINE            270X DIAL TERMINAL     @SA70654
         BZ    DEVCHK8                  NO, ERROR              @SA70654
DEVCHKED DS    0H                 BOUNDARY ALLIGNMENT            22
         LM    R7,R11,TRMSAVE2    RESTORE REGISTERS              S22024
         BR    R9                 RETURN                         S22024
         DROP  R11                                               S22024
         SPACE 3
APCOM    EQU   *
         OI    TOTFLG03,TOTAPOER  YES - SHOW OPTION ERROR
         B     TRMPROMT           GO TO PROMPTER                 S22024
         SPACE
APNOER   EQU   *
         TM    TRMFLG01,TRMINACV  3705 INACTIVE                  S22024
         BO    APNOER4            YES                            S22024
         OI    TOTFLG03,TOTAPOER  SHOW AP OPTION ERROR
         NI    $ERROPT,XFF-$ALTPRNT CLEAR AP OPTION FLAG
         IEDQMSG MSGID=004,FUNCT=CEC  AP LINE NOT OPENED
         B     TRMPROMT           GOT TO PROMPTER                S22024
         SPACE
APNOER4  EQU   *                                                 S22024
         OI    TOTFLG03,TOTAPOER  SHOW AP ERROR                  S22024
         OI    TOTFLG09,TOTPRECT  CT EQU AP                      S22024
         IEDQMSG MSGID=010,FUNCT=CEC 3705 INACTIVE MSG           S22024
         B     TRMPROMT           GO TO PROMPTER                 S22024
         EJECT
TRMERROR EQU   *
         WTO   'IED127I OLT REQUEST REJECTED, CONTROL TERMINAL UNIDENTI*
               FIED',ROUTCDE=10,DESC=7
         TM    TOTFLG05,TOTSRCON  TRM ENTERED FROM SYSCON      @Y17XAUU
         BO    TRMERR2            YES - EXIT                   @Y17XAUU
         IEDQMSG MSGID=077,FUNCT=SRC    WRITE MSG TO SOURCE    @Y17XAUU
TRMERR2  EQU   *
         NI    TOTFLG06,XFF-TOTOTERM  CLEAR SECTION RESTART FLAG
         OI    TOTFLG06,TOTOTERM  SET TERMINATE FLAG
         L     R13,D4(R13)        PICK UP CALLERS SAVE AREA ADDRESS
         LM    R14,R12,D12(R13)   RESTORE CALLER'S REGISTERS
         SR    R15,R15            CLEAR RETURN CODE REG
         BR    R14                RETURN TO MOTHER
TRMPROMT EQU   *
         OI    SSFLG,PROMPTFL     TURN ON PROMPT FLAG
         B     TRM15Y             GO SEE IF I HAVE TO ENQUEUE
TRMPT    EQU   *
         B     TRMEND             EXIT TO IEDQWB1
*
         SPACE 2
         SPACE
         EJECT
CTSTOP   EQU   *
*
*        R8    HAS UCB ADDR IN IT
*
         ST    R11,REGSAVE        SAVE REGISTER 11
         L     R1,TOTAVTPT        GET AVT ADDR
         USING IEDQAVTD,R1
         L     R1,AVTTCB               GET ADDR OF TCB
         DROP  R1
         L     R1,D8(R1)          GET DEB CHAIN ADDRESS
AGAIN    EQU   *
         SR    R3,R3              CLEAR REGISTER 3
         IC    R3,D16(R1)         GET NUMBER OF EXTENTS
         LA    R5,D32(R1)         GET UCB CHAIN ADDRESS
         L     R4,X18(R1)         GET DCB ADDRESS
         MVC   MULT+D3(D1),X24(R4)  GET LCB LENGTH
         L     R12,D4(R1)         GET NEXT DEB
         USING IHADCB,R4
         TM    DCBDSORG+D1,TCAMDEB TCAM DEB                      S22024
         BZ    HERE1              NO, BRANCH
         DROP  R4
         LA    R7,D1              SET UP COUNTER
HERE     EQU   *
         L     R10,D0(R5)         GET UCB ADDRESS
         LA    R10,D0(R10)          FROM CHAIN
         LA    R8,D0(R8)          GET UCB ADDRESS THAT WAS PASSED
*                                   TO THE SUBROUTINE
         CR    R8,R10             UCB ADDRESSES MATCH?
         BE    THERE              YES, BRANCH
         LA    R5,D4(R5)          GET NEXT UCB ADDRESS
         LA    R7,D1(R7)          BUMP LINE COUNTER
         BCT   R3,HERE            CHECK NEXT UCB ADDR
HERE1    EQU   *
         LR    R1,R12                  SET R1 TO NEXT DEB
         B     AGAIN              EVALUATE NEXT DEB EXTENT
THERE    EQU   *
         LR    R11,R7                   SET UP MULTIPLICAND
         SR    R10,R10            CLEAR REGISTER 10
         M     R10,MULT                 MULT BY LINE NO.
         L     R7,X1C(R4)         PICK UP IOB ADDRESS
         L     R11,REGSAVE             RESTORE R11
         BR    R9                      RETURN
         EJECT
**************************************************************** S22024
*                                                              * S22024
*        ROUTINE TO SEARCH TERMINAL NAME TABLE TO VERIFY THAT  * S22024
*        SYMBOLIC ENTRY FOR CONTROL TERMINAL OR ALTERNATE      * S22024
*        PRINTER IS VALID.                                     * S22024
*                                                              * S22024
**************************************************************** S22024
*
NAMESRCH EQU   *
         SPACE
         L     R14,TOTAVTPT       GET AVT ADDR
         USING IEDQAVTD,R14
         L     R15,AVTRNMPT       GET ADDRESS OF TERMNAME TABLE
         ST    R15,TERNMTAB       SAVE IT
         MVC   PAREMLST(D1),AVTBSX  GET OFFSET TO BINARY SEARCH ADDRESS
         L     R15,AVTMSGS-D1     VCON TABLE ADDRESS
         L     R15,D8(R15)        LOAD ADDRESS OF A1 ROUTINE
         DROP  R14
         LA    R1,PAREMLST              SET R1
         LH    R0,TWO(R1)         LOAD CHAR STRING LEN
         L     R1,FOUR(R1)        LOAD ADDR OF CHARS
         BAL   R14,FOUR(R15)      BRANCH TO SCAN ROUTINE
         LTR   R15,R15            WAS NAME FOUND IN TABLE?
         BZ    D4(R9)             NAME NOT FOUND - ERROR
         SPACE
         STM   R7,R9,TRMSAV1      SAVE REG 7-9
         L     R8,TERNMTAB        SET START OF TABLE
         USING IEDQTNTD,R8
         LA    R7,TNTFIRST        POINT TO FIRST ENTRY IN TABLE
         SR    R9,R9              CLEAR REG 9
         IC    R9,TNTENLEN        GET MAX. LENGTH OF NAME FIELD
         LA    R9,D3(R9)          GET LENGTH OF 1 ENTRY
         DROP  R8
         BCTR  R15,D0             DECREMENT ENTRY REL. POSITION
         MR    R8,R15             GET DISPLACEMENT TO ENTRY DESIRED
         AR    R7,R9              GET DESIRED TNT ADDRESS
         ST    R7,TRMTNT          SAVE IT
         LA    R1,D1(R15)         RESTORE ENTRY REL. POSITION
         STH   R1,TRMTTCIN        SAVE TTCIN                   @Y17XAUU
         LM    R7,R9,TRMSAV1      RESTORE REG 7-9
         SPACE
         L     R15,TERNMTAB       GET ADDRESS OF TERMNAME TABLE
         BALR  R14,R15            GO FIND TTE ADDRESS
         ST    R1,TNTENTRY        SAVE TERMINAL ENTRY ADDRESS
         USING IEDQTRM,R1                                       SA61759
         TM    TRMDSORG,TRMLGB    LGB RATHER THAN A TTE          S22024
         BO    D4(R9)             YES - ERROR - NOT A TERMINAL   S22024
         L     R14,TOTAVTPT       GET AVT ADDRESS               SA61759
         USING IEDQAVTD,R14                                     SA61759
         MVC   WRKFLG(D1),AVTBIT1 GET ENVIRONMENTAL FLAG        SA61759
         NI    WRKFLG,X60         CLEAR UNWANTED BITS           SA61759
         CLI   WRKFLG,TSONLY      TSO ONLY SYSTEM               SA61759
         BE    TRMTSO             YES - TERMINATE TRM           SA61759
         DROP  R14                                              SA61759
         L     R1,TRMDESTQ-D1     DEST. QCB ADDR                SA61759
         DROP  R1                                               SA61759
         USING IEDQQCB,R1                                       SA61759
         TM    QCBFLAG,QCBTSSES   TSO TERMINAL                   S22024
         BO    TRMTSO             YES - TERMINATE TRM           SA61759
         TM    QCBDSFLG,QCBREUS+QCBNREUS+QCBCORE  ARE THERE     SA61759
*                                 ANY QUEUES USED               SA61759
         BZ    TRMTSO             NO - TERMINATE                SA61759
         DROP  R1                                               SA61759
         BR    R9                 RETURN TO CALLER
TRMTSO   DS    0H                 BOUNDARY ALLIGNMENT           SA61759
         WTO   'IED216I TRM CANCELLED - TOTE CANNOT USE TSO DEVICE',ROU*
               TCDE=10,DESC=7     TSO DEVICE REJECT MSG         SA61759
         TM    TOTFLG05,TOTSRCON  TRM ENTERED FROM SYSCON      @Y17XAUU
         BO    TRM085             YES - EXIT                   @Y17XAUU
         IEDQMSG MSGID=078,FUNCT=SRC    WRITE MSG TO SOURCE    @Y17XAUU
         B     TRM085             EXIT TO CLEANUP               SA61759
         EJECT
*
*
*        ROUTINE TO FIND UCB ADDR AFTER ENTRY IN TERMINAL TABLE HAS
*         BEEN FOUND
*
*
UCBFIND  EQU   *
         L     R8,TNTENTRY        GET TERMINAL ENTRY ADDRESS
         USING IEDQTRM,R8
         L     R8,TRMDESTQ-D1     GET QCB ADDRESS
         DROP  R8
         USING IEDQQCB,R8
         SR    R7,R7              CLEAR REGISTER 7
         IC    R7,QCBRELLN        GET RELATIVE LINE NUMBER
         STC   R7,TRMRLN          SAVE CT RLN
         SLL   R7,D2              MULTIPLY BY 4
         L     R8,QCBDCBAD-D1     GET DCB ADDRESS
         DROP  R8
         USING IHADCB,R8
         TM    DCBDSORG,LGBA      LGB VS. DCB                    S22024
         BZ    UCB0X              NO                             S22024
         DROP  R8                                                S22024
         L     R1,TNTENTRY        GET TERMINAL ENTRY ADDRESS   @Y17XAUU
         BAL   R14,NCPTTE         GET NCP TTE ADDRESS          @Y17XAUU
*                                                              @Y17XAUU
*        ON RETURN R1 CONTAINS ADDRESS OF NCP TTE PREFIX       @Y17XAUU
*                                                              @Y17XAUU
         USING IEDNTRM,R1         ESTABLISH ADDRESSABILITY     @Y17XAUU
         TM    TRMBYTE2,TRMRSACT  3705 ACTIVE                  @Y17XAUU
         BZ    UCB08              NO - ERROR                   @Y17XAUU
         TM    TRMBYTE2,TRMINPG   ACT/DEACT IN PROGRESS        @Y17XAUU
         BO    UCB08              YES - ERROR                  @Y17XAUU
         L     R1,TRMDESTQ-D1     GET NCP QCB ADDRESS          @Y17XAUU
         DROP  R1                                              @Y17XAUU
         USING IEDQQCB,R1         ESTABLISH ADDRESSABILITY     @Y17XAUU
         L     R8,QCBDCBAD-D1     GET DCB ADDRESS              @Y17XAUU
         DROP  R1                                              @Y17XAUU
         LA    R7,D4              SHOW RLN EQU 1                 S22024
UCB0X    EQU   *                                                 S22024
         USING IHADCB,R8                                         S22024
         TM    DCBOFLGS,X10       IS DCB OPEN?
         BZ    D0(R9)             NO, BRANCH
         L     R8,DCBDEBAD        GET DEB ADDRESS
         DROP  R8
         USING DEBNMSUB,R8
         SR    R15,R15            CLEAR REGISTER 15
         IC    R15,DEBNMEXT       GET NUMBER OF EXTENTS
         SLL   R15,D2             MULTIPLY BY 4
UCB04    EQU   *                                                 S22024
         CR    R15,R7             IS DESIRED LINE OPENED?
         BL    D0(R9)             NO
         L     R8,DEBUCBAD-D4(R7) GET UCB ADDRESS
         USING UCB,R8
         MVC   WORK1(D3),UCBNAME  MOVE UCB ID IN EBCDIC
         DROP  R8
         B     D4(R9)             RETURN
UCB08    EQU   *                                                 S22024
         OI    TRMFLG01,TRMINACV  SET 3705 INACTIVE FLAG         S22024
         B     D0(R9)             ERROR RETURN                   S22024
         EJECT
*                                                             *@Y17XAUU
*        ROUTINE TO FIND NCP TTE ADDRESS. INPUT: REG 1 =      *@Y17XAUU
*        TERMINAL TTE ADDRESS. OUTPUT: REG 1 = NCP TTE ADDRESS*@Y17XAUU
*        RETURN TO CALLER VIA REG 14.                         *@Y17XAUU
*                                                             *@Y17XAUU
NCPTTE   DS    0H                 BOUNDARY ALLIGHMENT          @Y17XAUU
         ST    R14,TRMSAVE3       SAVE RETURN ADDRESS          @YM08072
         LA    R8,TRMPRFSZ        GET TTE PREFIX SIZE          @Y17XAUU
         SLR   R1,R8              POINT TO TTE PREFIX          @Y17XAUU
         USING IEDNTRM,R1         ESTABLISH ADDRESSABILITY     @Y17XAUU
NCPLOOP  DS    0H                 FIND NCP TTE                 @Y17XAUU
         LH    R1,TRMCOHRT        GET COHORT POINTER           @Y17XAUU
         LTR   R1,R1              ENTRY IN HIERARCHY           @YM07415
         BZR   R9                 NO - ERROR RETURN            @YM07415
         L     R15,TERNMTAB       TTCIN TO TTE CONVERSION      @Y17XAUU
*                                 ROUTINE ADDRESS              @Y17XAUU
         BALR R14,R15             FIND TTE ADDRESS             @Y17XAUU
         SLR   R1,R8              POINT TO TTE PREFIX          @Y17XAUU
         CLI   TRMTYPE,TRMLNCP    NCP ENTRY                    @Y17XAUU
         BNE   NCPLOOP            NO - CHECK NEXT ENTRY        @Y17XAUU
         L     R14,TRMSAVE3       RESTORE RETURN ADDRESS       @YM08072
         BR    R14                RETURN TO CALLER             @Y17XAUU
         DROP  R1                                              @Y17XAUU
         EJECT
**************************************************************** S22024
*        ROUTINE TO GO TO IEDQWB2 TO CALCULATE THE AMOUNT OF   * S22024
*        OF CORE NEEDED TO BUILD DUMMY ENTRIES FOR A SPECIFIED * S22024
*        REMOTE TERMINAL. CONTROL IS PASS TO IEDQWB TO ATTAIN  * S22024
*        THE ACTUAL CORE.  CONTROL IS THEN RETURNED TO IEDQWB2 * S22024
*        TO COPY THE ENTRIES.                                  * S22024
**************************************************************** S22024
DUMMYTNT EQU   *                                                 S22024
         STM   R7,R11,TRMSAVE2    SAVE REGISTERS                 S22024
         LA    R12,KTRM           PASS PARAMETERS                S22024
         XC    TRMFLG01(D1),TRMFLG01 CLEAR CONTROL FLAG          S22024
*                                                                S22024
         LINK  EP=IEDQWB2         CALCULATE CORE                 S22024
         CL    R15,ERRCD8         SNA LU REJECT                @XM05678
         BE    DUMFAILU           YES-WRITE ERROR MESSAGE      @XM05678
         LTR   R15,R15            REQUEST SUCCESSFUL             S22024
         BNZ   DUMFAIL            NO - ERROR                     S22024
         TM    TRMFLG01,TRMCTEAP  CT EQU AP                      S22024
         BO    DUMOUT             RETURN                         S22024
*                                                                S22024
         L     R10,TOTRESPL       ADDR OF TOTE RES. CTL BLK      S22024
         USING RESPL,R10                                         S22024
         XC    TOTOTECB(D4),TOTOTECB CLEAR ECB                   S22024
         POST  RESTECB1           POST REQUEST TO IEDQWB         S22024
         WAIT  ECB=TOTOTECB       WAIT FOR REQUEST TO BE HONORED S22024
*                                                                S22024
         CLI   TOTWKSPC,XFF       REQUEST SUCCESSFUL             S22024
         BE    DUMFAIL            NO - ERROR                     S22024
*                                                                S22024
         OI    TRMFLG01,TRMQWB3   SHOW SECOND PASS               S22024
         LA    R12,KTRM           PASS PARAMETERS                S22024
         LINK  EP=IEDQWB2         COPY ENTRIES                   S22024
         LTR   R15,R15            REQUEST SUCCESSFUL             S22024
         BNZ   DUMFAIL            NO - ERROR                     S22024
*                                                                S22024
DUMOUT   EQU   *                                                 S22024
         LM    R7,R11,TRMSAVE2    RESTORE REGISTERS              S22024
         BR    R9                 RETURN                         S22024
         SPACE
DUMFAILU EQU   *                                               @XM05678
         WTO   'IED586I OLT REQUEST REJECTED, SNA LU NOT IN SSCP-LU SES*
               SION ONLY',ROUTCDE=10,DESC=7                    @XM05678
         TM    TOTFLG05,TOTSRCON  TRM ENTERED FROM SYSCON      @XM05678
         BO    DUMEND             YES - EXIT                   @XM05678
         IEDQMSG MSGID=093,FUNCT=SRC                           @XM05678
         B     DUMEND             TERMINATE TRM                @XM05678
         SPACE 2
DUMFAIL  EQU   *
         WTO   'IED215I OLT REQUEST REJECTED, UNABLE TO ALLOCATE RESOUR*
               CES FOR CT/AP',ROUTCDE=10,DESC=7
         TM    TOTFLG05,TOTSRCON  TRM ENTERED FROM SYSCON      @Y17XAUU
         BO    DUMEND             YES - EXIT                   @Y17XAUU
         IEDQMSG MSGID=080,FUNCT=SRC    WRITE MSG TO SOURCE    @Y17XAUU
         SPACE
DUMEND   EQU   *
         L     R13,D4(R13)        RESTORE CALLERS SAVE AREA ADDRESS
         LM    R14,R12,12(R13)    RESTORE CALLER'S REG
         SR    R15,R15            CLEAR REG
         BR    R14                RETURN
         SPACE
         EJECT
NOOPEN   EQU   *
         TM    TRMFLG01,TRMINACV  3705 INACTIVE                  S22024
         BZ    NOOP2              NO                             S22024
         WTO   'IED217I 3705 NOT ACTIVE',ROUTCDE=10,DESC=7       S22024
         TM    TOTFLG05,TOTSRCON  TRM ENTERED FROM SYSCON      @Y17XAUU
         BO    TRMERR2            YES - EXIT                   @Y17XAUU
         IEDQMSG MSGID=086,FUNCT=SRC    WRITE MSG TO SOURCE    @Y17XAUU
         B     TRMERR2            EXIT                           S22024
NOOP2    EQU   *                                                 S22024
         WTO   'IED130I OLT REQUEST REJECTED, CONTROL TERMINAL NOT OPEN*
               ED',ROUTCDE=10,DESC=7
         TM    TOTFLG05,TOTSRCON  TRM ENTERED FROM SYSCON      @Y17XAUU
         BO    TRMERR2            YES - EXIT                   @Y17XAUU
         IEDQMSG MSGID=092,FUNCT=SRC    WRITE MSG TO SOURCE    @Y17XAUU
         B     TRMERR2            BR TO SET UP FOR TERMINATION
         SPACE 4
LCBSCAN  EQU   *
         STM   R10,R12,TRMSAVE1   SAVE REG 10-12
         STM   R5,R9,TRMSAVE2     SAVE REG 5-9
         L     R5,TRMQCB          GET CT QCB ADDRESS
         USING IEDQQCB,R5
         LA    R5,D0(R5)          CLEAR HI ORDER BYTE
         L     R9,QCBDCBAD-D1     GET DCB ADDRESS
         USING IHADCB,R9
         TM    DCBDSORG,LGBA      LGB VS DCB                     S22024
         BZ    LCB0X              NO                             S22024
         DROP  R9                                                S22024
         L     R1,TNTENTRY        GET TERMINAL TTE ADDRESS     @Y17XAUU
         BAL   R14,NCPTTE         FIND NCP TTE ADDRESS         @Y17XAUU
         USING IEDNTRM,R1         ESTABLISH ADDRESSABILITY     @Y17XAUU
         L     R5,TRMDESTQ-D1     GET QCB ADDRESS              @Y17XAUU
         DROP  R1                                              @Y17XAUU
         L     R9,QCBDCBAD-D1     GET DCB ADDRESS              @Y17XAUU
         USING IHADCB,R9                                         S22024
LCB0X    EQU   *                                                 S22024
         L     R7,DCBDEBAD        GET DEB ADDR
         USING DEBNMSUB,R7
         SR    R8,R8              CLEAR REG 8
         IC    R8,DEBNMEXT        GET DEB NUMBER OF EXTENTS
         DROP  R7
         SR    R7,R7              CLEAR REG 7
         IC    R7,QCBRELLN        GET QCB'S STARTING RLN
         TM    TRMFLG01,TRM3705   3705 MODE                      S22024
         BZ    LCB04              NO                             S22024
         LA    R7,D1              SET RLN EQU 1                  S22024
LCB04    EQU   *                                                 S22024
         DROP  R5
         LR    R5,R7              COPY RLN FOR NEXT TEST
         BCTR  R5,D0              GET RLN MINUS 1
         SR    R8,R5              GET COUNT OF LCB'S TO SCAN
         SR    R12,R12            CLEAR REG 12
         IC    R12,DCBEIOBX       GET LCB SIZE
         LR    R11,R7             SAVE QCB'S STARTING RLN
         MR    R10,R12            GET STARTING LCB OFFSET
         AL    R11,DCBIOBAD       POINT TO IOB OF LCB
         DROP  R9
         S     R11,F32            ADJUST TO START OF LCB
         CLI   TRMRQFLG,CTREQ     C.T.REQUEST?                 @Y17XAUU
         BE    LCBSS4             YES
         LA    R5,TOTAPDFL        AP INIT STATUS FLAGS
         B     LCBSS8             BRANCH TO TEST LINE STATUS
LCBSS4   EQU   *
         LA    R5,TOTCTDFL        CT INIT STATUS FLAGS
LCBSS8   EQU   *
         TM    TRMFLG01,TRM3705   3705 MODE?                     S05331
         BZ    MODE0X             NO, BRANCH                     S05331
         L     R8,TNTENTRY        GET TERMINAL TTE ADDRESS     @Y17XAUU
         LA    R1,TRMPRFSZ        GET TTE PREFIX SIZE          @Y17XAUU
         SR    R8,R1              POINT TO TTE PREFIX          @Y17XAUU
         USING IEDNTRM,R8         ESTABLISH ADDRESSABILITY     @Y17XAUU
LINELOOP DS    0H                 FIND LINE TTE                @Y17XAUU
         LH    R1,TRMCOHRT        GET COHORT POINTER           @Y17XAUU
         L     R15,TERNMTAB       TTCIN TO TTE CONVERSION      @Y17XAUU
*                                 ROUTINE ADDRESS              @Y17XAUU
         BALR  R14,R15            FIND TTE ADDRESS             @Y17XAUU
         LR    R8,R1              GET TTE ADDRESS              @Y17XAUU
         LA    R1,TRMPRFSZ        GET TTE PREFIX SIZE          @Y17XAUU
         SR    R8,R1              POINT TO TTE PREFIX          @Y17XAUU
         CLI   TRMTYPE,TRMNSDLC   PRE SNA LINE ENTRY           @Y17XAUU
         BE    LINEFND            YES                          @Y17XAUU
         CLI   TRMTYPE,TRMSDLC    SNA LINE ENTRY               @Y17XAUU
         BNE   LINELOOP           CHECK NEXT ENTRY             @Y17XAUU
LINEFND  DS    0H                 LINE ENTRY FOUND             @Y17XAUU
         TM    TRMLINK,TRMLACTV   LINE STOPPED                 @Y17XAUU
         BO    LCBSSC             NO                           @Y17XAUU
         OI    D0(R5),TOTCTLST    SHOW LINE INITIALLY STOPPED    S05331
         B     LCBSSC             BRANCH                         S05331
         DROP  R8                 DROP QCB BASE                  S05331
MODE0X   EQU   *                                                 S05331
         USING IEDQLCB,R11
         TM    LCBSTAT1,X07       LINE STOPPED?
         BNZ   LCBSSC             NO
         OI    D0(R5),TOTCTLST    SHOW CT INIT. STOPPED
LCBSSC   EQU   *
         DROP  R11
         LM    R5,R9,TRMSAVE2     RESTORE REG 5-9
         L     R10,TRMSAVE1       RESTORE REG 10
         L     R12,TRMSAVE1+8     RESTORE REG 12
         BR    R9                 RETURN TO CALLER
         EJECT
**************************************************************** S22024
*        ROUTINE TO FIND THE SYMBOLIC NAME OF THE RN LINE      * S22024
*        FOR THE TERMINAL ATTACHED TO THE RN.                  * S22024
**************************************************************** S22024
RNLINET  EQU   *                                                 S22024
         STM   R7,R11,TRMSAVE2    SAVE CALLER'S REG              S22024
         L     R11,TNTENTRY       TTE ADDR                       S22024
         USING IEDNTRM,R11                                       S22024
         LA    R7,TRMPRFSZ        TTE PREFIX SIZE              @Y17XAUU
         SLR   R11,R7             TTE PREFIX ADDRESS           @Y17XAUU
         TM    TRMSTATE,TRMPREF   3705 RESOURCE                @Y17XAUU
         BZ    RNLINEND           NO                             S22024
         TM    TRMSTATE,XE0       RN TERMINAL                    S22024
         BNZ   RNLINEND           NO                             S22024
         LH    R7,TRMTTCIN        GET TERMINAL TTCIN
         TM    TRMFLG04,CTRSID    CT REQUEST                     S22024
         BZ    RN02               NO - ASSUME AP                 S22024
         OI    TOTCTFLG,TOTCTORN  CT ON 3705                     S22024
         STH   R7,TOTCROFF        CT REAL TTCIN                @Y17XAUU
         LA    R7,TOTCRSID        ADDRESS OF CT NETWORK ADDR   @Y17XAUU
         B     RN03               EXIT                           S22024
RN02     EQU   *                                                 S22024
         OI    TOTAPFLG,TOTAPORN  AP ON 3705                     S22024
         STH   R7,TOTAROFF        AP REAL TTCIN                @Y17XAUU
         LA    R7,TOTARSID        ADDRESS OF AP NETWORK ADDR   @Y17XAUU
RN03     EQU   *                                                 S22024
         L     R4,TOTAVTPT        GET AVT ADDRESS              @Y17XAUU
         USING IEDQAVTD,R4        SET AVT ADDRESSABILITY       @Y17XAUU
         L     R4,AVTSAVTP        GET SAVT ADDRESS             @Y17XAUU
         DROP  R4                                              @Y17XAUU
         USING IEDNSVTD,R4        SET SAVT ADDRESSABILITY      @Y17XAUU
         L     R15,SAVTTNTX       TTCIN TO NETWORK ADDR RTN    @Y17XAUU
         DROP  R4                                              @Y17XAUU
         LH    R1,TRMTTCIN        GET TERMINAL TTCIN           @Y17XAUU
         BALR  R14,R15            CALL CONVERSION ROUTINE      @Y17XAUU
         STH   R15,D0(R7)         SAVE NETWORK ADDRESS         @YM07278
         L     R10,TRMQCB         GET QCB ADDR                   S22024
         USING IEDQQCB,R10                                       S22024
         L     R10,QCBLGBAD-D1    LGB ADDR                       S22024
         LA    R10,D0(R10)        CLEAR HIGH ORDER BYTE          S22024
         DROP  R10                                               S22024
         USING IEDNLGB,R10                                       S22024
         TM    TRMFLG04,CTRSID    CT REQUEST                     S22024
         BZ    RN03F              NO                             S22024
         MVC   TOTCTRNL(D8),LGBNAME SAVE CT LINE GROUP NAME      S22024
         B     CHKTTE             EXIT                         @Y17XAUU
RN03F    EQU   *                                                 S22024
         MVC   TOTAPRNL(D8),LGBNAME SAVE AP LINE GROUP NAME
         DROP  R10                                             @Y17XAUU
CHKTTE   DS    0H                 FIND LINE ENTRY              @Y17XAUU
         LH    R1,TRMCOHRT        GET TTCIN OF NEXT ENTRY      @Y17XAUU
         LR    R9,R1              SAVE TTCIN                   @Y17XAUU
         L     R15,TERNMTAB       TTCIN TO TTE CONVERSION RTN  @Y17XAUU
         BALR  R14,R15            FIND TTE ADDRESS             @Y17XAUU
         LR    R11,R1             SET TTE BASE                 @Y17XAUU
         LA    R1,TRMPRFSZ        GET TTE PREFIX SIZE          @Y17XAUU
         SLR   R11,R1             POINT TO TTE PREFIX          @Y17XAUU
         CLI   TRMTYPE,TRMNSDLC   NON SNA LINE                 @Y17XAUU
         BE    LINETTE            YES - LINE TTE FOUND         @Y17XAUU
         CLI   TRMTYPE,TRMSDLC    SNA LINE                     @Y17XAUU
         BE    LINETTE            YES - LINE TTE FOUND         @Y17XAUU
         B     CHKTTE             CHECK NEXT ENTRY             @Y17XAUU
LINETTE  DS    0H                 FIND LINE SYMBOLIC NAME      @Y17XAUU
         BCTR  R9,R0              DECREMENT FOR MULTIPLY       @Y17XAUU
         L     R1,TERNMTAB        SET TNT BASE                 @Y17XAUU
         USING IEDQTNTD,R1        ESTABLISH ADDRESSABILITY     @Y17XAUU
         SR    R8,R8              CLEAR REGISTER               @Y17XAUU
         IC    R8,TNTENLEN        LENGTH OF NAME               @Y17XAUU
         LA    R8,D3(R8)          TOTAL LEN OF A TNT ENTRY     @Y17XAUU
         MR    R8,R8              OFFSET TO TNT ENTRY          @Y17XAUU
         LA    R10,TNTFIRST(R9)   LINE TNT ADDRESS             @Y17XAUU
         DROP  R1                                              @Y17XAUU
         SPACE
*                                                                S22024
*        ENQUEUE THE RN LINE SHARED                              S22024
*                                                                S22024
RN0C     EQU   *
         SR    R9,R9              CLEAR REG                      S22024
         IC    R9,TOTTTBEL        MAX NAME LENGTH                S22024
         BCTR  R9,R0              DECREMENT FOR EXECUTE          S22024
         EX    R9,RNENQ           MOVE RN LINE NAME TO ENQ LIST  S22024
         SPACE
         ENQ   ,MF=(E,ENQLIST)    ENQ RN LINE                    S22024
         LTR   R15,R15            WAS IT ASSIGNED                S05331
         BZ    RNLINEND           YES                            S05331
         CLI   D3(R15),D8         WAS IT PREVIOUSLY ASSIGNED     S05331
         BE    RNLINEND           YES                            S05331
*        DEQ TERMINAL NAME AND UCB - ERROR EXIT                  S05331
         TM    TRMFLG04,CTRSID    CT REQUEST                     S05331
         BZ    RN10               NO - ASSUME AP                 S05331
         SR    R8,R8              CLEAR                        @G36XRUV
         ICM   R8,3,TOTCTUCB      GET CT UCB ADDR              @G36XRUV
         LA    R7,WORK3           ADDR OF CT NAME                S05331
         B     RN14               CONTINUE PROCESSING            S05331
RN10     DS    0H                 BOUNDARY ALLIGNMENT            S05331
         SR    R8,R8              CLEAR                        @G36XRUV
         ICM   R8,3,TOTAPUCB      GET AP UCB ADDR              @G36XRUV
         LA    R7,TOTAPNAM        ADDR OF AP NAME                S05331
RN14     DS    0H                 BOUNDARY ALLIGNMENT            S05331
         USING UCB,R8                                            S05331
         MVI   ENQLIST+D2,DEQFLG  SET DEQ FLAG                   S05331
         MVC   RNAME(D8),D0(R7)   GET TERMINAL NAME              S05331
         DEQ   ,MF=(E,ENQLIST)    DEQ IT                         S05331
         MVI   RNAME,X40          BLANK NAME                     S05331
         MVC   RNAME+D1(D7),RNAME   FIELD                        S05331
         MVC   RNAME(D3),UCBNAME  GET UCB NAME                   S05331
         DEQ   ,MF=(E,ENQLIST)    DEQ IT                         S05331
         MVI   ENQLIST+D2,ENQFLG  RESTORE ENQ FLAG               S05331
         TM    TRMFLG04,CTRSID    CT REQUEST                     S05331
         BO    TRMCTX             YES - TERMINATE TRM            S05331
         B     APCOM              SHOW AP ERROR                  S05331
         SPACE
RNLINEND EQU   *                                                 S22024
         LM    R7,R11,TRMSAVE2    RESTORE REG                    S22024
         BR    R9                 RETURN                         S22024
RNENQ    MVC   RNAME(D0),D0(R10)  MOVE RN LINE NAME TO ENQ LIST
         EJECT
SETCONFG DS    0F                 USED TO SET TOTCONFG W/ C&S  @G36XRUV
         DC    AL1(TOTCONFG)                                   @G36XRUV
         DC    AL3(0)                                          @G36XRUV
SETPRMPT DS    0F                 USED TO SET TOTPRMPT W/ C&S  @G36XRUV
         DC    AL1(TOTPRMPT)                                   @G36XRUV
         DC    AL3(0)                                          @G36XRUV
TRMSAVE3 DS    A                        REGISTER SAVE AREA     @YM08072
ERRCD8   DC    F'8'               ERROR RETURN CODE            @XM05678
TRMCTSAV DC    8XL1'40'           CT NAME SAVE AREA              S22024
F2       DC    F'2'               VALUE OF 2                     S22024
OFFSET   DS    H                  3705 LINE TNT OFFSET           S22024
TRMTTCIN DC    H'0'               TTCIN SAVE AREA              @Y17XAUU
TMRQLADR DC    8CL1' '                  SYMBOLIC NAME FIELD
PATCH    DC    100XL1'0'          IEDQWC PATCH AREA            @Y17XAUU
         SPACE 2
**************************************************************** S22024
KTRM     DS    0F                 P-LIST PASSED TO QWB2          S22024
TERNMTAB DS    A                  TERMNAME TABLE ADDRESS         S22024
TNTENTRY DS    A                  TERMNAME ENTRY ADDRESS         S22024
TRMQCB   DS    A                  CT/AP QCB ADDRESS              S22024
TRMTNT   DS    A                  CT/AP TNT ADDRESS              S22024
TRMSAVE1 DS    3F                 REGISTERS SAVE AREA            S22024
TRMSAVE2 DS    5F                 REGISTERS SAVE AREA            S22024
TRMSAV1  DS    3F                 REGISTERS SAVE AREA            S22024
REGSAVE  DS    F                  WORK AREA                      S22024
F32      DC    F'32'              VALUE OF 32                    S22024
KPQCB    DS    A                  TRUE P-QCB ADDRESS           @YM08027
KMQCB    DS    A                  DUMMY M-QCB ADDRESS          @YM08027
CONCTTE  DS    A                  TRUE CONC TERM TTE ADDRESS   @YM08027
KDRQ     DS    A                  TRUE DRQ ADDRESS             @YM08027
KPQCBSZ  DS    H                  P-QCB SIZE                   @YM08027
KMQCBSZ  DS    H                  M-QCB SIZE                   @YM08027
KDRQSZ   DS    H                  DRQ SIZE                     @YM08027
TTETSZ   DS    CL1                CONC TERM TTE SIZE           @YM08027
TTECSZ   DS    CL1                CONC TTE SIZE                @YM08027
TRMRQFLG DC    X'00'              DUMMY TNT REQUEST FLAG         S22024
CTREQ    EQU   X'80'              CT REQUEST                   @Y17XAUU
APREQ    EQU   X'40'              AP REQUEST                   @Y17XAUU
PRREQ    EQU   X'20'              PRIMARY TEST DEVICE REQUEST  @Y17XAUU
SCREQ    EQU   X'10'              SECONDARY TEST DEV REQUEST   @Y17XAUU
TRMFLG01 DS    CL1                CONTROL FLAG                   S22024
CONC     EQU   X'80'              TERMINAL ATTACHED TO CONC      S22024
TRMSPAS  EQU   X'40'              PROCESS CONC TTE               S22024
TRMQWB3  EQU   X'20'              SECOND PASS FUNCTION           S22024
TRMDELY  EQU   X'08'              QCB IN TIME DELAY QUEUE        S22024
TRM3705  EQU   X'04'              3705 MODE                      S22024
TRMCTEAP EQU   X'02'              CT EQU AP                      S22024
TRMINACV EQU   X'01'              3705 INACTIVE                  S22024
WRKFLG   DS    CL1                WORK AREA                      S22024
**************************************************************** S22024
         SPACE
MULT     DC    F'0'               WORK AREA                    @Y17XAUU
WORK1    DC    8X'0'              PLACE FOR CT/AP NAME
WORK3    DS    CL8                SAVE AREA FOR CTL TERM. NAME
WORK4    DS    3F                 WORK AREA
F8       DC    F'8'               VALUE OF 8                     S22024
TRMSRCE  DS    H                  INDEX TO TERMINAME TABLE ENTRY
TRMNAME  DC    8C' '              PLACE FOR CT/AP NAME
TRMFLG   DC    X'00'              TRM FLAGS
NUMFLG   DC    X'00'              CONTROL FLAG
NUMEN    EQU   X'80'              NUMERIC ENTRY
         DS    0F                 USED FOR ALLIGNMENT
PAREMLST DS    0CL8               BINARY SEARCH PARM-LIST
         DS    CL1                OFFSET TO BINARY SEARCH ADDRESS
         DC    X'00'              FLAG BYTE 1
         DC    X'00'              FLAG BYTE 2
LENGTH   DS    CL1                LENGTH OF TERMINAL NAME
         DC    A(WORK1)           ADDRESS OF TERMINAL NAME
SSFLG    DC    X'00'              CONTROL BYTE
         DS    0H                 USED FOR ALLIGNMENT
BLANK    DC    8X'40'             OR FIELD
*
*
ENQLIST  ENQ   (QNAME,RNAME,S,8,STEP),RET=USE,MF=L  ENQUEUE PARAMETER  *
                                                      LIST FOR CT/AP
QNAME    DC    CL8'SYSZTOTE'      MAJOR ELEMENT                @Y17XAUU
RNAME    DC    8CL1' '            RESOURCE NAME FOR ENQUEUE
TRMRLN   DS    CL1                CT/AP RLN
SLASH2   DC    CL3'///'           THREE SLASHES
SYSOUT   DC    C'SYSOUT'          COMPARE FIELD
SYSCON   DC    C'SYSCON'          COMPARE FIELD
SYSCONL  DC    C'SYSCON/'         CT EQU SYSTEM CONSOLE
CONFIG   DC    C'//CONFIG/'       CONFIG COMPARE FIELD
PROMPT   DC    C'//PROMPT/'       PROMPT COMPARE FIELD
TOTE     DC    C'/TOTE//'         DEALLOCATION REQUEST         @Y17XAUU
TRMFLG04 DC    X'00'              FLAG BYTE                      S22024
CTRSID   EQU   X'80'              CT RSID INDICATOR              S22024
APRSID   EQU   X'40'              AP RSID INDICATOR              S22024
NAP      DC    C'NAP'             NO ALTERNATE PRINTER COMPARE FIELD
AP       DC    C'AP'              ALTERNATE PRINTER COMPARE FIELD
         EJECT
         TLGBD                                                   S22024
         EJECT
         TQCBED
         EJECT
         TDRQD
         EJECT
         TPRIOR
         EJECT
         TQCBD
         EJECT
DCBDSECT DCBD  DSORG=TX
         EJECT
         TSCBD
         EJECT
         TPRFD
         EJECT
         TLCBD
         EJECT
CVT      DSECT
         CVT
         EJECT
UCB      DSECT
         IEFUCBOB
         EJECT
         TAVTD
         EJECT
         TTRMD
         EJECT
         TDEBD
         EJECT
         TTNTD
         EJECT
         OLTCB
ECB      EQU   TOTOTECB
TRMWRK   EQU   TOTTRMBF+72
DUMMY    EQU   TOTWKSPC
         EJECT
RESPL    RESPL
         SPACE 2
         END
