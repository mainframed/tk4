         TITLE 'IEDQNA-RESIDENT CLOSEDOWN COMPLETION'
IEDQNA   CSECT
         SPACE 3
***********************************************************************
*                                                                     *
*TITLE:'IEDQNA'-RESIDENT CLOSEDOWN COMPLETION                         *
*                                                                     *
*STATUS:CHANGE LEVEL 0                                                *
*                                                                     *
*FUNCTION:THIS MODULE LINKS TO THE NONRESIDENT CLOSEDOWN COMPLETION   *
*   MODULE TO PROCESS A MCPCLOSE FUNCTION.WHEN CONTROL IS RETURNED,   *
*   THIS MODULE CHECKS THE RETURN CODE. IF 0,IT BRANCHES TO THE TCAM  *
*   DISPATCHER TO ALLOW DISK ACTIVITY TO CEASE.  IF 4,IT RESTORES     *
*   REGISTERS FROM REGISTER 13 AND RETURNS TO TO OS (THE ADDRESS IN   *
*   REGISTER 14).  IF 8,IT RESTORES THE  USERS REGISTERS FROM         *
*   AVTSAVE1 AND RETURNS TO THE USER CODE FOLLOWING THE READY MACRO.  *
*                                                                     *
*   THIS MODULE  ALSO GETS CONTROL EACH TIME A TCAM ATTACHED TASK     *
*   TERMINATES.  IT CHECKS THE TCB FOR THE ATTACHED TASK TO DETERMINE *
*   THE TYPE OF TERMINATION.  IF NORMAL, IT RETURNS TO THE ADDRESS    *
*   IN REGISTER 14. IF ABNORMAL,IT NEGATES REGISTER 1 AND LINKS TO    *
*   THE NON-RESIDENT CLOSEDOWN COMPLETION ROUTINE.
*                                                                     *
*ENTRY POINT:                                                         *
*                                                                     *
*        IEDQNA-CLOSEDOWN OF THE MCP                                  *
*        IEDQNA3-TERMINATION OF A TCAM ATTACHED TASK                  *
*                                                                     *
*INPUT-                                                               *
*   WHEN ENTERED AT IEDQNA                                            *
*   REGISTER 13 HAS THE ADDRESS OF AVTSAVE2                           *
*   REGISTER 15 HAS THE ADDRESS OFIEDQNA+2                            *
*   WHEN ENTERED AT IEDQNA3                                           *
*   REGISTER 1 HAS THE ADDRESS OF THE TCB OF THE TERMINATED TASK      *
*   REGISTER 13 HAS THE ADDRESS OF A SAVE AREA                        *
*   REGISTER 15 HAS THE ADDRESS OF IEDQNA3
*                                                                     *
*OUTPUT:NONE                                                          *
*                                                                     *
*EXTERNAL ROUTINES:                                                   *
*                                                                     *
*        'IEDQNA2'-CLOSES DOWN MCP AND ATTACHED TASKS                 *
*                  NOTIFIES THE USER OF ANY ABNORMAL TASK TERMINATION *
*                                                                     *
*EXITS-NORMAL:THIS MODULE CAN EXIT TO THREE LOCATIONS DEPENDING ON    *
*   THE RETURN CODE FROM IEDQNA2(DISCUSSED IN 'FUNCTIONS').           *
*   1.THE USER'S INSTRUCTION WHICH FOLLOWS READY                      *
*   2.THE ADDRESS IN REGISTER 14(OS ABEND ROUTINE)                    *
*   3.THE TCAM DISPATCHER AT DSPDISP                                  *
*                                                                     *
*EXITS-ERROR:NONE                                                     *
*                                                                     *
*TABLES/WORK AREAS                                                    *
*                                                                     *
*   AVT (AVTSAVE1,AVTSAVE2)                                           *
*   TCB                                                               *
*                                                                     *
*ATTRIBUTES:REENTERABLE                                               *
*                                                                     *
*NOTES:THE OPERATION OF THIS MODULE DEPENDS UPON AN INTERNAL REPRE-   *
*   SENTATION OF THE EXTERNAL CHARACTER SET WHICH IS EQUIVALENT TO    *
*   THE ONE USED AT ASSEMBLY TIME. THE CODING HAS BEEN ARRANGED SO    *
*   THAT REDEFINITION OF 'CHARACTER' CONSTANTS,BY REASSEMBLY, WILL    *
*   RESULT IN A CORRECT MODULE FOR THE NEW DEFINITIONS.               *
*                                                                     *
***********************************************************************
         SPACE 3
*REGISTER ASSIGNMENTS
         SPACE
R0       EQU   0
R1       EQU   1                        PARM REG FOR SUBROUTINE
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13                       ADDR AVTSAVE2
R14      EQU   14
R15      EQU   15                       BASE
         SPACE 3
*STCB
         SPACE
         DS    0F
         DC    X'4'
         DC    X'0'
         SPACE 3
*ENTRY FROM TCAM DISPATCHER
         SPACE
         USING *,R15
         B     QNA00
         SPACE 3
*ENTRY FROM OS TERMINATION ROUTINE
         SPACE
IEDQNA3  EQU   *
         USING *,R15
         USING IEDQTCB,R1               BASE FOR TCB
         TM    TCBFLGS,X'80'            CHECK TERMINATION
         BCR   8,R14                    RETURN TO OS IF NORMAL TERM
         STM   R14,R12,12(R13)          SAVE REGS IN OS SAVE AREA
         LNR   R1,R1                    NEGATE PARM REGISTER
         B     QNA10                    LINK TO IEDQNA2
         SPACE 3
         DC    CL8'IEDQNA'
         DC    CL8'5/6/70'
QNA00    EQU   *
         USING AVTSAVE2,R13
         LR    R1,R13                   PARM FOR RTN
QNA10    EQU   *
         BALR  R12,0                    SET NEW BASE REG
         USING *,R12
         DROP  R15
         LINK  EP=IEDQNA2
         SPACE 3
*USE RETURN CODE TO INDEX BRANCH TABLE
         SPACE
         USING IEDQDISP,R11             BASE FOR TCAM DISPATCHER
         B     QNA20(R15)               CHECK RETURN CODE
         SPACE
QNA20    EQU   *
         B     DSPDISP                  RETURN TO DISPATACHER
         B     QNA40                    RETURN TO OS TERMINATION
*        B     QNA30                    RETURN TO USER FOLLOWING READY
         SPACE
QNA30    EQU   *
         SPACE
         LA    R1,AVTSVSIZ              LENGTH OF SAVE AREA
         SR    R13,R1                   ADDRESS OF AVTSAVE1
         SPACE
QNA40    EQU   *
         RETURN (14,12),T
         SPACE 3
         PRINT NOGEN
         TAVTD
         TDISPD
IEDQTCB  DSECT
         ORG   IEDQTCB+29
TCBFLGS  DS    X                        TERMINATION FLAG
         END
