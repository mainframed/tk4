QNB      TITLE 'IEDQNB-APPLICATION PROGRAM CHECKPOINT INTERFACE'
         SPACE 3
*A000000-999999                                                @Y16X5C0
*C390000-408000                                                  Y01004
*A349500-350500                                                  Y01004
*D687000                                                       @Y17XAC2
*C688400                                                       @Y17XAC2
*D825000-828000                                                @Y17XAC2
*D834000                                                       @Y17XAC2
*A689100                                                       @Y17XAC2
*A837000                                                       @Y17XAC2
*A392000                                                       @Y17XAC2
***********************************************************************
*                                                                     *
*MODULE-NAME = IEDQNB (TCAM,CHECKPOINT/RESTART)                       *
*                                                                     *
*DESCRIPTIVE-NAME = APPLICATION PROGRAM CHECKPOINT INTERFACE          *
*                                                                     *
*COPYRIGHT = 'NONE'                                                   *
*                                                                     *
*STATUS: VERSION 10.0                                                 *
*                                                                     *
*FUNCTION:WHEN AN APPLICATION PROGRAM ISSUES A TCAM MACRO WHICH       *
*   CHANGES THE MCP ENVIRONMENT,THIS MODULE BUILDS A CHECKPOINT       *
*   REQUEST ELEMENT ,   TPOSTS IT TO THE READY QUEUE, AND WAITS       *
*   UNTIL THE CHECKPOINT HAS BEEN TAKEN. WHEN AN OPEN OR CLOSE IS     *
*   ISSUED IN AN APPLICATION PROGRAM,THIS MODULE DETERMINES IF AN     *
*   ENTRY IN THE CKREQ-TTR TABLE IS INVOLVED,AND IF SO,IT INVERTS     *
*   THE STATUS OF THE CKREQ-TTR ENTRY. (IE. IF A DESTINATION QUEUE    *
*   WHICH CAN BE CHECKPOINTED AS A RESULT OF A CKREQ MACRO IS         *
*   OPENED, ITS ENTRY IN THE CKREQ-TTR TABLE IS MADE ACTIVE; IF       *
*   CLOSED,ITS ENTRY IN THE CKREQ-TTR TABLE IS MADE INACTIVE.         *
*   INACTIVE ENTRIES CAN BE USED FOR OTHER DESTINATION QUEUES WHICH   *
*   ARE OPENED LATER.) IF SYNC=YES IS SPECIFIED ON THE DESTINATION    @
*   THEN THE QUEUE CAN BE CHECKPOINTED WITH CKREQ MACRO,SO IT IS      *
*   GIVEN AN ENTRY IN THE CKREQ-TTR TABLE                             *
*                                                                     *
*ENTRY POINTS:                                                        *
*                                                                     *
*        IEDQNB-FROM CKREQ MACRO                                      *
*        IEDQNB02-FROM TCHING MACRO                                   *
*        IEDQNB05-FROM OPEN OR CLOSE                                  *
*                                                                     *
*INPUT:THIS MODULE RECEIVES THE FOLLOWING INPUT AT THE INDICATED      *
*   ENTRY POINT:                                                      *
*                                                                     *
*   IEDQNB:NONE                                                       *
*   IEDQNB02:REGISTER 1 CONTAINS THE TERMNAME OFFSET OF THE TERMI-    *
*            NAL SPECIFIED IN THE TCHNG MACRO.                        *
*   IEDQNB05:REGISTER 1 CONTAINS THE ADDRESS OF THE DCB SPECIFIED     *
*            IN THE OPEN OR CLOSE MACRO.                              *
*                                                                     *
*OUTPUT:THIS MODULE BUILDS,IN THE PCB, THE FOLLOWING REQUEST ELE-     *
*   MENTS FOR THE INDICATED ENTRY POINTS:                             *
*                                                                     *
*   IEDQNB:OFFSER 0-KEY:X'60'                                         *
*                 1-ADDRESS OF CHECKPOINT QCB                         *
*                 4-PRIORITY                                          *
*                 5-LINK FIELD                                        *
*                 8-ADDRESS OF DEB CHAIN FOR APPLICATION PROGRAM      *
*                12-ADDRESS OF ECB FOR APPLICATION PROGRAM            *
*                                                                     *
*   IEDQNB02:     0-KEY:X'10'                                         *
*                 1-ADDRESS OF CHECKPOINT QCB                         *
*                 4-PRIORITY                                          *
*                 5-LINK FIELD                                        *
*                 8-OFFSET TO TERMNAME TABLE ENTRY                    *
*                10-UNUSED                                            *
*                12-ADDRESS OF ECB FOR APPLICATION PROGRAM            *
*                                                                     *
*                                                                     *
*   IEDQNB05:THIS ENTRY POINT INVERTS THE FIRST BIT OF THE CKREQ-     *
*            TTR TABLE ENTRY:                                         *
*                                                                     *
*            OFFSET 0-BIT0-ON,ENTRY IS ACTIVE
*                        1-ON,ENTRY HAS A DISK ERROR                  *
*                        2-7-UNUSED                                   *
*                   1-TTR OF DISK RECORD                              *
*                   4-OFFSET TO TERMNAME TABLE ENTRY                  *
*                                                                     *
*EXTERNAL ROUTINES:                                                   *
*                                                                     *
*        IEDQTNT-DETERMINES TERMNAME OFFSET                           *
*                                                                     *
*EXITS-NORMAL:THIS MODULE BRANCHES TO THE ADDRESS IN REGISTER 14.     *
*                                                                     *
*EXITS-ERROR:NONE                                                     *
*                                                                     *
*TABLES/WORK AREAS:                                                   *
*                                                                     *
*   AVT (AVTCKGET,AVTCKPTB,AVTCLRHI,AVTCVPT,AVTCKPR,AVTRNMPT          *
*   CHECKPOINT WORK AREA                                              *
*   DCB                                                               *
*   PCB                                                               *
*   DEB(APPLICATION PROGRAM)                                          *
*   LCB                                                               *
*   ACCESS METHOD WORK AREA                                           *
*   TERMNAME TABLE                                                    *
*   TERMINAL TABLE                                                    *
*                                                                     *
*ATTRIBUTES:REENTERABLE                                               *
*                                                                     *
*NOTES:THE OPERATION OF THIS MODULE DEPENDS UPON AN INTERNAL REPRE-   *
*   SENTATION OF THE EXTERNAL CHARACTER SET WHICH IS EQUIVALENT TO    *
*   THE ONE USED AT ASSEMBLY TIME. THE CODING HAS BEEN ARRANGED SO    *
*   THAT REDEFINITION OF 'CHARACTER' CONSTANTS,BY REASSEMBLY, WILL    *
*   RESULT IN A CORRECT MODULE FOR THE NEW DEFINITIONS.               *
*                                                                     *
***********************************************************************
         SPACE 3
IEDQNB   CSECT
         SPACE 3
*REGISTER ASSIGNMENTS
         SPACE
R0       EQU   0
R1       EQU   1                        INPUT PARM;TERMNAME OFFSET
R2       EQU   2                        ADDRESS OF CKPT WORK AREA
R3       EQU   3                        ADDRESS OF CKPT REQUEST ELM
R4       EQU   4                        ADDRESS OF PCB WORK AREA
R5       EQU   5                        ADDRESS OF DEB CHAIN
R6       EQU   6                        ADDRESS OF PCB
R7       EQU   7                        ADDRESS OF CKPT QCB
R8       EQU   8                        ADDRESS OF CVT; LCB
RAVT     EQU   9                        ADDRESS OF AVT
R10      EQU   10                       ADDRESS OF TCB
R11      EQU   11                       ADDRESS OF ACCESS WORKAREA
RPEWA    EQU   11                       ADDRESS OF PEWA        @ZM46809
R12      EQU   12                       ADDRESS OF CKREQ-TTR ENTRY
R13      EQU   13                       ADDRESS OF SAVE AREA
R14      EQU   14                       BRANCH REG,INDEX
R15      EQU  15
QNBNOCKR EQU   4                        RETURN CODE FOR NO CKPT    1019
CODE     EQU   8                        TOPCTL CODE              Y01004
ADDRESS  EQU   7                        MASK FOR ADDRESS         Y01004
SIXTEEN  EQU   16                       ECB DISP                 Y01004
         SPACE 3
         USING IHADCB,R1                BASE FOR DCB
         USING IEDQCKPD,R2              BASE FOR CKPT WORK AREA
         USING IEDQCRED,R3              BASE FOR CKPT REQUEST ELEMT
         USING IEDQDEB,R5               BASE FOR TCAM DEB
         USING IEDQAVTD,RAVT            BASE FOR AVT
         USING IEDQWRKA,R11             BASE FOR ACCESS METHOD WORK
         SPACE 3
*ENTRY POINT FOR CKREQ MACRO
         SPACE
         SAVE  (14,12)                  SAVE USERS REGS          Y01004
         BALR  R12,R0                   ESTABLISH                Y01004
         USING *,R12                    ADDRESSABILITY           Y01004
IEDQNB   IEDHJN AFTERHJN                                       @Y17XAC2
         SPACE 1                                                 Y01004
         L     R10,CVTPTR               ADDRESS OF CVT         @ZM46809
         L     R10,ZERO(R10)            ADDRESS OF OLD-NEW PTRS@ZM46809
         L     R10,CURRENT(R10)         ADDRESS OF CURRENT TCB @ZM46809
         L     R5,TCBDEB-IEDQTCB(R10)   ADDRESS OF DEB CHAIN   @ZM46809
         LA    R15,RC4                  SET RETURN CODE        @ZM46809
*BEGINNING OF LOOP THROUGH DEB CHAIN                           @ZM46809
         SPACE
QNB15    EQU   *                                               @ZM46809
         LA    R5,0(,R5)                CLEAR HIGH ORDER BYTE  @ZM46809
         LTR   R5,R5                    CHECK FOR END OF CHAIN @ZM46809
         BZ    QNB25                    BRANCH IF END          @ZM46809
         SPACE
         CLI   DEBTAMID,X'C0'           CHECK DEB TYPE         @ZM46809
         BE    QNB20                    BR IF TCAM INPUT DEB   @ZM46809
         L     R5,4(0,R5)               GET NEXT DEB IN CHAIN  @ZM46809
         B     QNB15                    LOOP                   @ZM46809
         SPACE
*END OF LOOP THROUGH DEB CHAIN                                 @ZM46809
         SPACE 3
QNB20    EQU   *                                               @ZM46809
         L     RPEWA,DEBPEWA-ONE        ADDRESS OF PEWA        @ZM46809
         XC    CKECB(4),CKECB           CLEAR ECB              @ZM46809
         LA    R1,CKPARM                PARAMETER LIST ADDRESS @ZM46809
         AQCTL                                                 @ZM46809
QNB25    EQU   *                                               @ZM46809
         RETURN (14,12),T,RC=(15)       RETURN TO USER         @ZM46809
         EJECT
*ENTRY POINT FROM TCHNG MACRO
         SPACE
         ENTRY IEDQNB02
IEDQNB02 EQU   *
         SPACE
         USING *,R15
         BAL   R0,QNB60                 BEGIN REQUEST ELEMENT
*                                       PROCESSING             @ZM46809
         MVI   CREKEY,CRECHNGT          KEY FOR TCHNG
         STH   R1,CREOFFS               OFFSET FOR TERMNAME IN MACR
         SPACE 3
*COMMON CODE FOR CKREQ,TCHNG
         SPACE
QNB30    EQU   *
         LA    R7,AVTCKPTB              ADDRESS OF CKPT QCB
         ST    R7,CREECB
         MVC   CREQCBAD(3),CREECB+1     MOVE QCB ADDR TO ELEMENT
         MVI   CRELINK,PRIAPCKP         SET PRIORITY
         LA    R1,CHKECB1               ADDRESS OF ECB         @ZM46809
         ST    R1,CREECB                PUT INTO REQ ELEMENT
         SPACE 3
*QPOST REQUEST ELEMENT TO READY QUEUE VIA SVC. POST MCP TCB
         SPACE
         STM   R3,R4,CHKLIST            ADDRESS OF PARMLIST    @ZM46809
         MVI   CHKLIST,X'0C'            KEY TO INDIC FUNCTION  @ZM46809
         LA    R1,CHKLIST               ADDRESS OF PARMLIST
         AQCTL                          QPOST SVC
         SPACE
         WAIT  ECB=CHKECB1              WAIT FOR CKPT          @ZM46809
         SR    R0,R0                    SET NORMAL RETURN CODE     1019
         SPACE
QNB35    EQU   *                                                   0616
         LR    R15,R0                   PICK UP RETURN CODE        1019
         RETURN (14,12),T,RC=(15)       RETURN TO APP PROG         1019
         SPACE 3
*ENTRY POINT FOR OPEN OR CLOSE FROM APPLICATION PROGRAM
         SPACE
         ENTRY IEDQNB05
IEDQNB05 EQU   *
         USING *,R15
         BAL   R0,QNB60                 GET PCB ADDRESS
         L     R5,DCBDEBAD              ADDRESS OF DEB
         LH    R1,DEBTAMOS              TERMNAME OFFSET
         N     R1,AVTCLRHI              CLEAR HIGH ORDER HALF WORD
         L     R15,AVTRNMPT             ADDRESS OF TERMNAME TABLE
         BALR  R14,R15                  GET TERMINAL OFFSET
         SPACE
         L     R15,16(0,R13)            RESTORE FROM SAVE AREA
         USING IEDQTRM,R1               BASE FOR TERM ENTRY
         TM    TRMSTATE,TRMHELDN        CHECK FOR SYNC=YES
         BZ    QNB90                    BRANCH IF SYNC=NO
         SR    R14,R14                  CLEAR
         IC    R14,CKPCKRQS             VALUE OF CKREQS ON INTRO   0616
         LTR   R14,R14                  CHECK FOR NO CKREQ
         BZ    QNB90
         LA    R14,CKPCKRNO(0,R14)      NUMBER OF CKREQ DISK RECRDS
         L     R12,CKPCTTRB             GET CKREQ-TTR TABLE
         SPACE 3
*BEGINNING OF LOOP THROUGH CKREQ-TTR TABLE-LOOKING FOR PROCESS
*ENTRY BEING OPENED OR CLOSED.
         SPACE
QNB40    EQU   *
         TM    CKPCTFLG(R12),CKPCTDER   CHECK FOR DISK ERROR
         BO    QNB42                    BRANCH IF DISK ERROR
         CLC   CKPCTTTR(2,R12),DEBTAMOS COMPARE OFFSETS
         BE    QNB89                    BRANCH IF EQUAL
         SPACE
QNB42    EQU   *
         LA    R12,CKPCTTRL(0,R12)       MOVE TO NEXT ENTRY
         BCT   R14,QNB40                SUBRRACT FROM ENTRY COUNT
         SPACE
*END OF LOOP THROUGH CKREQ-TTR TABLE
         SPACE 3
         B     QNB90
         SPACE 3
*SUBROUTINE USED TO LOCATE CURRENT TCB,AVT,DEB CHAIN, PCB, ACCESS
*WORK AREA,AND CKPT WORK AREA.  R14 IS BRANCH REG.
         SPACE
QNB60    EQU   *
         STM   R14,R12,12(R13)          SAVE REGS
         DROP  R15
         BALR  R12,0
         USING *,R12                    SET UP BASE
         SPACE
         LR    R14,R0                   MOVE BRANCH REG
         L     R8,16                    ADDRESS OF CVT
         L     R10,0(0,R8)
         L     R10,4(0,R10)             ADDRESS OF CURRENT TCB
         L     RAVT,AVTCVTPT(0,R8)      ADDRESS OF TCAM DISPATCHER
         L     RAVT,0(0,RAVT)           ADDRESS OF AVT
         L     R5,8(0,R10)              ADDRESS OF DEB CHAIN
         LA    R0,QNBNOCKR              SET RETURN CODE FOR NO CKPT101
         TM    AVTCKFLG,AVTCKTAC        TCAM CHCKPT ACTIVE     @Y17XAC2
         BZ    QNB90                    BRANCH IF NOT ACTIVE       1020
         L     R2,AVTCKAVT-1            GET CKPT AVT ADDRESS   @Y17XAC2
         L     R2,CAVTTCWA-IEDQCKAD(,R2)  GET TCAM CHECKPOINT  @Y17XAC2
*                                       WORK AREA ADDRESS      @Y17XAC2
         SPACE 3
*BEGINNING OF LOOP THROUGH DEB CHAIN
         SPACE
QNB65    EQU   *
         LA    R5,0(,R5)                CLEAR HIGH ORDER BYTE
         LTR   R5,R5                    CHECK FOR END OF CHAIN
         BZ    QNB90                    BRANCH IF END
         SPACE
         CLI   DEBTAMID,X'C0'           CHECK DEB TYPE             1014
         BE    QNB70                    BRANCH IF TCAM INPUT DEB   1014
         L     R5,4(0,R5)               GET NEXT DEB IN CHAIN
         B     QNB65                    LOOP
         SPACE
*END OF LOOP THROUGH DEB CHAIN
         SPACE 3
QNB70    EQU   *
         L     R4,DEBPEWA-ONE           PEWA ADDRESS           @ZM46809
         L     R4,PEWAWA-IEDQPEWA(,R4)  AIB WORKAREA ADDRESS   @ZM46809
         LA    R4,220(R4)               SPECIAL WORK AREA      @ZM46809
         LA    R3,CHKPARM1              ADDR OF REQUEST ELEMENT@ZM46809
         LA    R0,QNBMOVEL              ADDR OF LENGTH OF LIST
         ST    R0,CHKLEN                PUT IN PARM LIST       @ZM46809
         XC    CHKPARM1(20),CHKPARM1    CLEAR ECB AND REQ ELEM @ZM46809
         BR    R14                      RETURN
         SPACE
*END OF SUBROUTINE
         SPACE 3
*RETURN TO OPEN OR CLOSE EXECUTOR
         SPACE
QNB89    EQU   *
         DROP  R12
         USING IEDQNB05,R15
         LA    R0,QNBLEN                ADDRESS OF LN AND ZERO BYTE
         ST    R0,CHKLIST               ADDR OF ZERO BYTE      @ZM46809
         ST    R0,CHKLEN                ADDR OF HALF WORD LEN  @ZM46809
         ST    R12,CHKTARG              ADDR OF TARGET         @ZM46809
         MVI   CHKLIST,X'08'            FLAG                   @ZM46809
         LA    R1,CHKLIST               ADDR OF PARM           @ZM46809
         AQCTL                          MOVE ZERO TO FLAG BYTE IN      X
                                        CKREQ-TTR ENTRY-MARK INACTV
         SR    R0,R0                    SET NORMAL RETURN CODE     1019
         SPACE
QNB90    EQU   *
         LR    R15,R0                   PICK UP RETURN CODE        1019
         RETURN (14,12),T,RC=(15)       RETURN CODE FOR APP PROG   1019
         SPACE 3
         EJECT
*              CONSTANTS                                       @ZM46809
         DS    0F                       PARAMETER LIST         @ZM46809
CKPARM   DC    X'9C'                    CKREQ CODE FOR SVC 102 @ZM46809
         DC    AL3(CKECB)               ECB ADDRESS            @ZM46809
CKECB    DC    F'0'                     ECB FOR SVC 102        @ZM46809
         SPACE 2
CHKPARM1 DS    0F                       PARAMETER LIST         @ZM46809
         DS    4F                       SPECIAL AQCTL ELEM     @ZM46809
CHKECB1  DS    F                        ECB                    @ZM46809
CHKLIST  DS    0F                       AQCTL PARAMETER LIST   @ZM46809
CHKFROM  DS    F                        FROM ADDRESS           @ZM46809
CHKTARG  DS    F                        TARGET OF DATA         @ZM46809
CHKLEN   DS    F                        ADDRESS OF LENGTH      @ZM46809
         SPACE 2
*              EQUATES                                         @ZM46809
ZERO     EQU   0                        DISPLACEMENT           @ZM46809
ONE      EQU   1                        DISPLACEMENT           @ZM46809
CURRENT  EQU   4                        NEW TCB                @ZM46809
RC4      EQU   4                        NO DCBS OPEN RET CODE  @ZM46809
CVTPTR   EQU   16                       PTR TO CVT             @ZM46809
QNBLEN   DC    H'1'                     LENGTH FOR MOVE
QNBMOVEL DC    H'16'                    LENGTH TO BE MOVED BY SVC
         SPACE 3
         TAVTD
         TCAVTD                                                @Y17XAC2
         TCKPD 3330                     MERLIN VERSION             0421
         TPCBD
         TDEBAPD
         TACSMD
         DCBD  DSORG=TX
         TTRMD
         TPEWAD
         TPRIOR
         EJECT
         TTCBD                                                 @ZM46809
         END
