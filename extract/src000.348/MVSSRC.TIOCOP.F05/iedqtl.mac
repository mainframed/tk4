TL       TITLE 'D E V I C E  D E P E N D E N T  F L D  L O O K U P'
IEDQTL   CSECT
*A-000000-999999                                               @Y17XAPG
***********************************************************************
*                                                                     *
*   MODULE NAME = IEDQTL                                              *
*                                                                     *
*   DESCRIPTIVE NAME =  DEVICE DEPENDENT FIELD LOOK UP ROUTINE        *
*                                                                     *
*   COPYRIGHT = NONE                                                  *
*                                                                     *
*   STATUS = VERSION 10.0                                             *
*                                                                     *
*   FUNCTION = THIS SUBROUTINE IS LINKED TO BE TCAM MODULES WHICH     *
*              NEED TO FIND THE ADDRESS OF A PARTICULAR DEVICE        *
*              DEPENDENT FIELD OF A GIVEN TERMINAL ENTRY              *
*                  A MASK AND TTE ADDRESS IS PASSED TO THE ROUTINE    *
*              THE MASK DESCRIBES WHICH DEVICE DEPENDENT FIELD SHOULD *
*              SHOULD BE FOUND.  USING THE INPUTTED MASK AND THE      *
*              TRMDEVFL FIELD WITHIN THE TTE, THE ROUTINE WILL FIND   *
*              THE ADDRESS TO THE FIRST DEVICE DEPENDENT FIELD AND    *
*              SKIP OVER ANY DEVICE DEPENDENT FIELDS THAT LIE BEFORE  *
*              THE REQUESTED FIELD.                                   *
*   NOTES =                                                           *
*                                                                     *
*      DEPENDENCIES = NONE                                            *
*                                                                     *
*      RESTRICTIONS = NONE                                            *
*                                                                     *
*      REGISTER CONVENTIONS =  SEE DECLARES                           *
*                                                                     *
*      PATCH LABEL = NONE                                             *
*                                                                     *
*   MODULE TYPE =                                                     *
*                                                                     *
*      PROCESSOR = ASSEMBLER F                                        *
*                                                                     *
*      ATTRIBUTES =  REENTRANT                                        *
*                                                                     *
*   ENTRY POINT =  IEDQTL                                             *
*                                                                     *
*      LINKAGE =  ADDR OF ROUTINE APPEARS IN AVT                      *
*                                                                     *
*   INPUT = R0 EQUAL MASK FOR FIELD REQUESTED                         *
*           R1 EQUAL ADDR TO TTE                                      *
*   OUTPUT = R15 EQUAL ZERO IF FIELD DOES NOT EXIST                   *
*                OR    ADDR OF REQUESTED DEV DEP FIELD                *
*   EXIT-NORMAL = R14                                                 *
*                                                                     *
*   EXIT-ERROR =  R14                                                 *
*                                                                     *
*   EXTERNAL REFERENCES = NONE                                        *
*                                                                     *
*      ROUTINES = NONE                                                *
*                                                                     *
*      DATA AREAS = NONE                                              *
*                                                                     *
*      CONTROL BLOCKS = TTE                                           *
*                                                                     *
*   TABLES = NONE                                                     *
*                                                                     *
*   MACROS = NONE                                                     *
*                                                                     *
*   CHANGE ACTIVITY =                                                 *
*   A-000000-999999                                           @Y17XAPF*
*                                                                     *
***********************************************************************
         EJECT
DEVLEN   EQU   0                        DISPLACEMENT FOR DEVICE
*                                       FIELD LENGTH.
RBASE    EQU   15                       PROGRAM BASE
RDEVFLAD EQU   2                        DEVICE FIELD ADDRESS REG
RDEVFLD  EQU   1                        DEVICE FIELD SHIFT REGISTER
RETURN   EQU   14                       RETURN REGISTER
RMASK    EQU   0                        INPUT MASK CORRESSPONDING
RSAVE    EQU   13                       SAVE AREA POINTER
RWORK    EQU   3                        WORK REGISTER
         EJECT
* SAVE REGS 0-3 AND SET PROGRAM BASE.
         SPACE
         STM   RMASK,RWORK,12(RSAVE)    SAVE REGS              @YM02912
         USING IEDQTL,RBASE
IEDQTL   IEDHJN AFTERHJN,HJN
         SPACE
* LEFT ADJUST INPUT MASK.
         SPACE
         SLL   RMASK,16                 LEFT ADJUST
         SPACE
* IF REQUESTED FIELD NOT PRESENT....
         SPACE
         USING IEDQTRM,RDEVFLD          TRMBASE
         LH    RWORK,TRMDEVFL           DEVICE FIELD FLAGS
         SLL   RWORK,16                 LEFT ADJUST
         NR    RWORK,RMASK              TURN OFF UNWANTED FIELD BITS
         XR    RWORK,RMASK              DESIRED FIELD PRESENT?
         BZ    ELSEDO                   BRANCH YES TO ELSE.
         SPACE
* THEN SET RETURN CODE TO SO INDICATE.
         SPACE
         SLR   RDEVFLAD,RDEVFLAD        RETURN CODE = 0.
         B     EXIT                     BRANCH AROUND ELSE
         SPACE
* ELSE DO: SET FIRST DEVICE FIELD AT TRMOPNO.
         SPACE
ELSEDO   EQU   *
         LA    RDEVFLAD,TRMOPNO         OPTIONS ADDRESS
         SLR   RWORK,RWORK              CLEAR WORK REG FOR LATER USE
         SPACE
* IF OPTION FIELDS....
         SPACE
         TM    TRMSTATE,TRMOPTFN        OPTION FIELDS?
         BZ    NOPTIONS                 BRANCH NO
         SPACE
* THEN ADD LENGTH OF OPTION COUNT BYTE AND TABLE DISPLACEMENT FIELD
* PLUS THE NUMBER OF OPTION FIELD INDEXES.
         SPACE
         IC    RWORK,TRMOPNO            # OPTION INDEX BYTES.
         LA    RDEVFLAD,L'TRMOPNO+L'TRMOPTBL(RWORK,RDEVFLAD)
*                                       COUNT BYTE + TBL DISP FLD +
*                                       # OPTION FIELD INDEXES.
         SPACE
* LEFT ADJUST DEVICE FIELD IN R1 FOR TESTING.
         SPACE
NOPTIONS EQU   *
         LH    RDEVFLD,TRMDEVFL         DEVICE FIELD
         DROP  RDEVFLD
         SLL   RDEVFLD,16               LEFT ADJUST
         SPACE
* IF THIS IS ^ THE FIELD WE'RE AFTER....
         SPACE
CHKMSK   EQU   *
         LTR   RMASK,RMASK              THIS IT?
         BM    EXIT                     BRANCH YES
         SPACE
* THEN DO: IF CURRENT FIELD PRESENT....
         SPACE
         LTR   RDEVFLD,RDEVFLD          PRESENT?
         BNM   NOTPRES                  BRANCH NO
         SPACE
* THEN INCREMENT DEVICE FIELD ADDRESS BY CURRENT FIELD LENGTH.
         SPACE
         IC    RWORK,DEVLEN(RDEVFLAD)   DEVICE FLD LENGTH
         LA    RDEVFLAD,1(RWORK,RDEVFLAD)    NEXT FLD ADDR     @PI02911
         SPACE
* END THEN DO.
* ADVANCE TO NEXT DEVICE FIELD POSITION IN MASKS.
NOTPRES  EQU   *
         SPACE
         SLDL  RMASK,1                  BOTH MASKS SHIFT LEFT 1 TO
*                                       NEXT FIELD POSITION.
         SPACE
* GO BACK TO CHECK NEXT FIELD.
         SPACE
         B     CHKMSK
         SPACE
* END ELSE DO.
* SET RETURN REG AND RESTORE REGS.
         SPACE
EXIT     EQU   *
         LR    RBASE,RDEVFLAD           DEV FLD ADDR OR 0 RETURN
*                                       CODE IF NOT FOUND.
         LM    RMASK,RWORK,12(RSAVE)    RESTORE REGS.          @YM02912
         SPACE
* RETURN.
         SPACE
         BR    RETURN
         EJECT
         TTRMD
         END
