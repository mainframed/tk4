QNH      TITLE 'IEDQNH-BUILD INCIDENT CKPT DISK RECORD FOR CHNGT'
IEDQNH   CSECT
         SPACE 3
* CHANGE ACTIVITY  =  AS FOLLOWS :                             @OZ26495
*A752000,840000                                                @OZ26495
*C612000                                                       @OZ26495
***********************************************************************
*                                                                     *
*TITLE:'IEDQNH'-INCIDENT CHECKPOINT FOR A TCHNG MACRO                 *
*                                                                     *
*STATUS:CHANGE LEVEL 0                                                *
*                                                                     *
*FUNCTION:THIS MODULE BUILDS AN INCIDENT DISK RECORD WHEN THE         *
*   REQUEST ELEMENT ON THE CHECKPOINT QCB IS FROM A TCHNG MACRO       *
*   IN AN APPLICATION PROGRAM.                                        *
*                                                                     *
*ENTRY POINT:                                                         *
*                                                                     *
*        IEDQNH                                                       *
*                                                                     *
*INPUT:REGISTERS 2,3,9,12,14,15 CONTAIN THE FOLLOWING:                *
*                                                                     *
*   2-ADDRESS OF THE CHECKPOINT WORK AREA                             *
*   3-ADDRESS OF THE REQUEST ELEMENT THIS MODULE IS TO PROCESS        *
*   9-ADDRESS OF AVT                                                  *
*   12-BASE REGISTER FOR CHECKPOINT EXECUTOR-IEDQNF                   *
*   14-RETURN ADDRESS-IN IEDQNF                                       *
*   15-ENTRY POINT FOR THIS MODULE                                    *
*                                                                     *
*   THE FOLLOWING 16-BYTE REQUEST ELEMENT POINTED TO BY R3.           *
*                                                                     *
*   OFFSET 0-KEY:X'10'                                                *
*          1-ADDRESS OF CHECKPOINT QCB                                *
*          4-LINK FIEDL                                               *
*          8-OFFSET TO TERMNAME TABLE FOR TERMINAL ON TCHNG MACRO     *
*          10-UNUSED                                                  *
*          12-ADDRESS OF ECB FOR APPLICATION PROGRAM                  *
*                                                                     *
*OUTPUT:THE ABOVE REGISTERS 2-14 REMAIN UNCHANGED. (R15 MAY           *
*   CONTAIN A RETURN CODE.)                                           *
*                                                                     *
*   AN INCIDENT DISK RECORD IS BUILT IN A GETMAIN AREA. (ITS          *
*   ADDRESS IS IN CKPLDRB.)                                           *
*                                                                     *
*EXTERNAL ROUTINES:                                                   *
*                                                                     *
*        IEDQTNT-DETERMINES TERMINAL TABLE ADDRESS                    *
*                                                                     *
*EXITS-NORMAL:THIS MODULE RETURNS TO THE ADDRESS IN R14. R15 HAS AN   *
*   OFFSET FOR THE CHECKPOINT QUEUE MANAGER(72) OR THE INCIDENT       *
*   OVERFLOW ROUTINE(96)                                              *
*                                                                     *
*EXITS-ERROR:THIS MODULE RETURNS TO THE ADDRESS IN R14. R15 HAS AN    *
*   OFFSET FOR THE NO CORE ROUTINE(96).                               *
*                                                                     *
*TABLES/WORK AREAS:                                                   *
*                                                                     *
*   AVT(AVTCLRHI,AVTRNMPT,AVTOPTPT)                                   *
*   CHECKPOINT WORK AREA                                              *
*   TERMNAME TABLE                                                    *
*   TERMINAL TABLE                                                    *
*   OPTION TABLE                                                      *
*   OPTION LENGTH TABLE                                               *
*                                                                     *
*                                                                     *
*ATTRIBUTES:REENTERABLE                                               *
*                                                                     *
*NOTES:THE OPERATION OF THIS MODULE DEPENDS UPON AN INTERNAL REPRE-   *
*   SENTATION OF THE EXTERNAL CHARACTER SET WHICH IS EQUIVALENT TO    *
*   THE ONE USED AT ASSEMBLY TIME. THE CODING HAS BEEN ARRANGED SO    *
*   THAT REDEFINITION OF 'CHARACTER' CONSTANTS,BY REASSEMBLY, WILL    *
*   RESULT IN A CORRECT MODULE FOR THE NEW DEFINITIONS.               *
*                                                                     *
***********************************************************************
         SPACE 3
*REGISTER ASSIGNMENTS
         SPACE
R0       EQU   0
R1       EQU   1                        PARM REG OR WORK REG
R2       EQU   2                        ADDRESS OF CKPT WORK AREA
R3       EQU   3                        ADDRESS OF REQUEST ELEMENT
R4       EQU   4                        ADDRESS OF DISK RECORD
R5       EQU   5                        ADDRESS OF OPTION ENTRY
R6       EQU   6                        INDEX REG
R7       EQU   7                        OFFSET TO TERMNAME ENTRY
RTERM    EQU   8                        ADDRESS OF TERM ENTRY
RAVT     EQU   9                        ADDRESS OF AVT
R10      EQU   10                       LENGTH OF OPTION FIELD
R11      EQU   11                       COMPARAND
R12      EQU   12                       BASE REG FOR IEDQNF
R13      EQU   13                       SAVE AREA
R14      EQU   14                       RETURN REG
R15      EQU   15                       ENTRY POINT OR RETURN CODE
         SPACE 3
*STANDARD LINKAGE
         SPACE
         USING *,R15                    TEMP BASE
         B     QNH00
         DC    CL8'IEDQNH'
         DC    CL6'041671'
QNH00    EQU   *
         USING IEDQAVTD,RAVT
         USING IEDQCKPD,R2              BASE FOR CKPT WORK AREA
         USING IEDQCRED,R3              BASE FOR REQUEST ELEMENT
         SPACE
*CHECK FOR AN AVAILABLE INCIDENT DISK RECORD
         SPACE 3
         SR    R0,R0                    CLEAR
         IC    R0,CKPINCNO              GET NO. OF AVAIL INCIDENTS
         LTR   R0,R0                    CHECK THE NO.
         BZ    QNH70                    BRANCH IF NONE AVAILABLE
         SPACE
         BCTR  R0,0                     SUBTRACT ONE FROM COUNT
         STC   R0,CKPINCNO              RESTORE COUNT
         SPACE 3
*GET TERMINAL ENTRY ADDRESS
         SPACE
         LH    R7,CREOFFS               GET TERMNAME OFFSET
         N     R7,AVTCLRHI              CLEAR HIGH ORDER HALF WORD
         LR    R1,R7                    SET UP PARM FOR SUBRTN
         LR    R4,R14
         LR    R5,R15                   SAVE REG
         SPACE
         DROP  R15
         USING IEDQNH,R5                TEMPORARY BASE
         SPACE
         L     R15,AVTRNMPT             ADDRESS OF SUBROUTINE
         BALR  R14,R15                  GET ADDRESS OF TERM ENTRY
         SPACE 3
         USING IEDQTRM,RTERM            BASE FOR TERM ENTRY
         LR    RTERM,R1
         SPACE 3
*ISSUE GETMAIN TO BUILD RECORD
         SPACE
         LA    R14,CKPLDRB              ADDRESS OF ADDR LOCATION
         LH    R15,CKPINCLN             LENGTH OF INCIDENT RECORD
         GETMAIN EC,LV=(R15),A=(R14),MF=(E,CKPGETML)
         SPACE 3
         LTR   R15,R15                  CHECK RETURN CODE
         LR    R14,R4                   RESTORE REG
         LR    R15,R5                   RESTORE REG
         SPACE
         DROP  R5
         USING IEDQNH,R15
         SPACE
         BNZ   QNH60
         SPACE 3
*BUILD RECORD
         SPACE
         L     R4,CKPLDRB               GET ADDRESS OF GETMAIN AREA
         USING IEDQCDRD,R4              BASE FOR DISK RECORD
         MVI   CDRKEY,CDRCHNGT          PUT IN KEY FOR CHNGT RECORD
         STH   R7,CDROFFS               PUT IN TERMNAME OFFSET
         MVC   CDRSTAT,TRMSTATE         PUT IN TERM STATUS
         MVC   CDRSEQIN(4),TRMINSEQ     MOVE SEQ NUMBERS TO RCD    1008
         TM    TRMSTATE,TRMOPTFN        CHECK FOR OPTIONS
         BZ    NOOPTS                   BRANCH IF OPTION FIELDS@OZ26495
*                                        NOT USED              @OZ26495
         SPACE 3
*MOVE OPTION FIELD TO DISK RECORD (USES REGISTERS 1,4,5,6,7,8,10,
*11,13)
         SPACE
*SET UP FOR LOOP
         SPACE
         SR    R11,R11                  CLEAR
         IC    R11,TRMOPNO              COMPARAND
         L     R13,AVTOPTPT             ADDRESS OF OPTION TABLE
         L     R7,4(,R13)               ADDRESS OF OPT LENGTH TABLE
         LH    R1,TRMOPTBL              OFFST TO OPT THIS ENTRY
         N     R1,AVTCLRHI              CLEAR HIGH ORDER HALF WORD
         AR    R13,R1                   ADD OFFSET TO ADDRESS
         SR    R1,R1                    CLEAR
         SR    R10,R10                  CLEAR (LENGTH FOR MOVE)
         LA    R6,TRMOPT                ADDRESS OF FIRST OPTION OFF
         SPACE 3
*BEGINNING OF LOOP THROUGH OPTION TABLE OFFSETS FOR THIS TERM
         SPACE
QNH40    EQU   *
         LR    R5,R13                   INITIALIZE
         IC    R1,0(R6)                 OPTION FIELD OFFSET
         CLI   0(R6),X'FF'              CHECK FOR UNUSED ENTRY
         BE    QNH45                    BRANCH IF UNUSED
         AR    R5,R1                    ADDRESS OF OPTION FIED
         IC    R10,0(R7)                GET LENGTH OF THIS OP FIELD
         CLI   1(R7),X'D0'              CHECK OPTION TYPE          1111
         BH    QNH43                    BRANCH IF NOT ADDRESS TYPE 1111
         CLI   1(R7),X'80'              CHECK OPTION TYPE          1111
         BH    QNH45                    BRANCH IF ADDRESS TYPE     1111
QNH43    EQU   *                                                   1111
         EX    R10,QNHMOVE              MOVE OPT FIELD TO DISK RCD
         LA    R4,1(R4,R10)             ADD LENGTH MOVED TO BASE OF    X
                                        DISK RECORD
QNH45    EQU   *
         LA    R7,10(0,R7)              GET OPT LNGTH TABLE ENTRY
         LA    R6,1(0,R6)               ADD 1 TO INDEX
         BCT   R11,QNH40                SUBTRACT FROM NO. OF ENTRY
         SPACE
*END OF LOOP THROUGH OPTION TABLE OFFSETS
         SPACE 3                                               @OZ26495
         LA    R5,TRMOPT                START OF OPTION FIELDS @OZ26495
         SR    R10,R10                  CLEAR REG              @OZ26495
         IC    R10,TRMOPNO              NUMBER OF OPTIONS      @OZ26495
         AR    R5,R10                   START OF DEVICE FIELDS @OZ26495
         B     CKBUF                    CHECK BUFSIZE          @OZ26495
NOOPTS   EQU   *                                               @OZ26495
         SR    R10,R10                  CLEAR REG              @OZ26495
         LA    R5,TRMOPNO               START OF DEVICE FIELDS @OZ26495
CKBUF    EQU   *                                               @OZ26495
         TM    TRMDEVFL,X80             BUFSIZE SPECIFIED      @OZ26495
         BNO   CKDIAL                   NO, BRANCH             @OZ26495
         IC    R10,0(R5)                LENGTH OF BUFSIZE      @OZ26495
         LA    R5,ONE(R10,R5)           BUMP PAST BUFSIZE      @OZ26495
CKDIAL   EQU   *                                               @OZ26495
         TM    TRMDEVFL,X40             DIAL DIGITS SPECIFIED  @OZ26495
         BNO   CKADDR                   NO, BRANCH             @OZ26495
         IC    R10,0(R5)                LENGTH OF DIAL DIGITS  @OZ26495
         EX    R10,QNHMOVE              MOVE DIAL DIGITS TO RCD@OZ26495
         LA    R4,ONE(R10,R4)           BUMP PAST DIAL DIGITS  @OZ26495
         LA    R5,ONE(R10,R5)           BUMP PAST DIAL DIGITS  @OZ26495
CKADDR   EQU   *                                               @OZ26495
         TM    TRMDEVFL,X20             ADDR CHAR SPECIFIED    @OZ26495
         BNO   QNH50                    NO, BRANCH             @OZ26495
         IC    R10,0(R5)                LENGTH OF ADDR CHAR    @OZ26495
         EX    R10,QNHMOVE              MOVE ADDR CHAR TO RCD  @OZ26495
         LA    R4,ONE(R10,R4)           BUMP PAST ADDR CHAR    @OZ26495
         SPACE 3
*SETS RETURN CODE TO LOAD I/O QUEUE MANAGER RTN
         SPACE
QNH50    EQU   *
         LA    R15,CKPIOQM              OFFSET FOR RTN NAME
         BR    R14                      RETURN TO EXECUTOR
         SPACE 3
*NO GETMAIN CORE ALLOCATED. SET RETURN CODE FOR NO CORE RTN.
         SPACE
QNH60    EQU   *
         LA    R15,CKPNOCOR             OFFSET FOR RTN NAME
         BR    R14                      RETURN TO EXECUTOR
         SPACE 3
*THERE ARE NO MORE AVAILABLE INCIDENT DISK RECORDS SET RETURN CODE
*FOR NO INCIDENTS RTN. TO SET FOR TOTAL CKPT.
         SPACE
QNH70    EQU   *
         LA    R15,CKPNOINC             OFFSET FOR RTN NAME
         BR    R14                      RETURN TO EXECUTOR
         SPACE 3
QNHMOVE  MVC   CDROPTN(0),0(R5)         MOVE OPTIONS TO DISK RECORD
         SPACE 3
ONE      EQU   1                        CONSTANT               @OZ26495
X80      EQU   X'80'                    FLAG FOR BUFSIZE       @OZ26495
X40      EQU   X'40'                    FLAG FOR DIAL DIGITS   @OZ26495
X20      EQU   X'20'                    FLAG FOR ADDR CHAR     @OZ26495
         PRINT NOGEN
         TAVTD
         TCKPD 3330
         TTRMD
         END
