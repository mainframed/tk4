         TITLE ' IEDQOT01 - TCAM RESOURCE TERMINATION MANAGER '
IEDQOT01 CSECT
*A-000000-999999                                               @Y02X6B0
         SPACE 3
*  CHANGE ACTIVITY AS FOLLOWS
******************** MICROFICHE FLAGS *********************** SUPT CODE
*A596000,612000                                                 SA51771
*D548000-572000,576000-596000,597800,600000-612000               Y01004
*A572000,5984000                                                SA62368
*C304000                                                        SA62368
*D308000-316000,598400-599000,614000-668000,676000-692000       SA62368
*C292000-304000,597200                                           Y05330
*D060000-068000,232000-236000,244000,268000,7599600-672000,      Y05330
*D708000-712000,724000-732000                                    Y05330
*A778100                                                       @Z30AABG
*C418802                                                       @Z30AABG
*D761600                                                       @Z30AABG
*D345676                                                       @ZA02643
*C344160,344004-344616,345000-345336,345363-345372,345393      @ZA02601
*D287000,346395,346480-346780                                  @ZA02601
*A340000,344622,344760-344770,345480,547802,574000             @ZA02601
*C345417-345423,345503-345676,345710-345750,345860             @ZA02601
*C346170-346200,346340-346365,346450-346455                    @ZA02601
*C424802,587000,590000,602000,605000,607112-607126             @ZA02601
*C607450-607750,618000,626300-626600,628640-628992             @ZA02601
*A614000,758000,777000,788000,791200                           @ZA02601
*C637090-670000                                                @ZA02601
*C345315-345370                                                @ZA03055
*A346160                                                       @ZA04027
*C649090                                                       @ZA04027
*A547802,759500,778200,798000                                  @Z40X9BG
*C038000,346460,424802,593000                                  @Z40X9BG
*C346345-346390                                                @Z40X9BG
*D345427,345432                                                @ZA07067
*D345365-345367                                                @ZA07078
*A345370                                                       @ZA07078
*A345452,345480                                                @ZA07083
*C607550-607600                                                @OZ08086
*C670690                                                       @OZ07798
*A344180                                                       @OZ07816
*C344780-344820                                                @OZ07816
*D345048-345056,614300-614600                                  @OZ07816
*C758500                                                       @OZ09293
*C345437-345452                                                @OZ08374
*C346377                                                       @OZ08384
*A676290                                                       @OZ16542
*C656290                                                       @OZ17630
*A651490,652290,655490,656690,778100                           @OZ17630
*C346680                                                       @G36XRBB
*A541862                                                       @G36XRBB
*                                       PEWA DSECT CHG         @OZ27843
*A554102                                                       @OZ26963
*C554102-554312                                                @OZ29317
*A652690                                                       @OZ32424
         SPACE 3
************************START OF SPECFICATIONS*************************
*                                                                     *
*                                                                     *
* MODULE NAME = IEDQOT01                                              *
*                                                                     *
* DESCRIPTIVE NAME = RESOURCE TERMINATION MANAGER (RTM)               *
*                                                                     *
* COPYRIGHT = NONE                                                    *
*                                                                     *
* STATUS = LEVEL 9                                             @Z40X9BG
*                                                                     *
* FUNCTION = CLEAN UP TCAM RESOURCES                                  *
*                                                                     *
* NOTES = SEE BELOW                                                   *
*                                                                     *
*    DEPENDENCIES = NONE                                              *
*                                                                     *
*    RESTRICTIONS = NONE                                              *
*                                                                     *
*    REGISTER CONVENTIONS = R2  BRANCH AND LINK REGISTER              *
*                           R3  POINTER TO CVT                        *
*                           R5  POINTER TO RTM WORK AREA              *
*                           R6  POINTER TO TCX                        *
*                           R7  ADDRESS SPACE ID REGISTER             *
*                           R8  POINTER TO RMPL                       *
*                           R9  BASE REGISTER                         *
*                           R10 POINTER TO PEB                        *
*                           R11 POINTER TO AIB                        *
*                           R12 POINTER TO PEWA                       *
*                           R13 SAVE AREA POINTER                     *
*                                                                     *
*    PATCH LABEL = PATCH                                              *
*                                                                     *
* MODULE TYPE = PROCEDURE                                             *
*                                                                     *
*    PROCESSOR = ASSEMBLER XF                                         *
*                                                                     *
*    ATTRIBUTES = REENTRANT, SUPERVISOR STATE, KEY 0                  *
*                                                                     *
* ENTRY POINT = IEDQOT01                                              *
*                                                                     *
*    PURPOSE = SEE FUNCTION                                           *
*                                                                     *
*    LINKAGE = CONTROL PASSED TO THIS MODULE FROM IGC0001C VIA        *
*              A CALL MACRO                                           *
*                                                                     *
* INPUT = R1  LOADED WITH THE PARAMETER LIST ADDRESS                  *
*         R13 POINTS TO STANDARD SAVE AREA                            *
*         R14 CONTAINS RETURN ADDRESS                                 *
*         R15 CONTAINS ENTRY POINT OF THIS MODULE                     *
*                                                                     *
* OUTPUT = NONE                                                       *
*                                                                     *
* EXIT-NORMAL = RETURN TO IGC0001C VIA RETURN MACRO WITH R15 = 0      *
*                                                                     *
* EXIT-ERROR = NONE                                                   *
*                                                                     *
* EXTERNAL REFERENCES = SEE BELOW                                     *
*                                                                     *
*    ROUTINES = NONE                                                  *
*                                                                     *
*    DATA AREAS = RESOURCE MANAGER WORK AREA ADDRESSED BY             *
*                 RMPLRMWA.                                           *
*                                                                     *
*    CONTROL BLOCKS = AIB (APPLICATION INTERFACE BLOCK)               *
*                     AVT (TCAM'S ADDRESS VECTOR TABLE)               *
*                     CVT                                             *
*                     PEB (PROCESS EXTENSION BLOCK)                   *
*                     PEWA (PROCESS ENTRY WORK AREA)                  *
*                     PSA (PREFIXED SAVE AREA)                        *
*                     RECB (RESOURCE ELEMENT CONTROL BLOCK)           *
*                     RMPL (RESOURCE MANGER PARAMETER LIST)           *
*                     TCX (TCAM CVT EXTENSION)                        *
*                                                                     *
* TABLES = NONE                                                       *
*                                                                     *
* MACROS = CALLRTM                                                    *
*          FREEMAIN                                                   *
*          GETMAIN                                                    *
*          IOSGEN                                                     *
*          MODESET                                                    *
*          SETLOCK                                                    *
*          TCABEND                                                    *
*                                                                     *
* CHANGE ACTIVITY = NONE                                              *
*                                                                     *
*************************END OF SPECIFICATIONS*************************
         EJECT
*        REGISTER USAGE
*
RZERO    EQU   0                        WORK REGISTER            Y02027
RONE     EQU   1                        WORK REGISTER            Y02027
RBRANCH  EQU   2                        BRANCH REGISTER          Y02027
RCVT     EQU   3                        CVT DSECT BASE REGISTER
RPEBF    EQU   4                        POINTER TO PEB PRECEDING Y02027
*                                       THE CURRENT PEB          Y02027
RRTMWA   EQU   5                        POINTER TO RTM WORK AREA Y02027
RTCX     EQU   6                        THE TCX BASE REGISTER    Y02027
RASID    EQU   7                        ADDRESS SPACE ID REG     Y02027
RUCB     EQU   7                        UCB REGISTER             Y02027
RRMPL    EQU   8                        ADDR OF RESOURCE MANAGER Y02027
*                                       PARAMETER LIST           Y02027
RBASE    EQU   9                        IEDQOT01 BASE REGISTER   Y02027
RPEB     EQU   10                       PROCESS ENTRY BLOCK ADDR Y02027
RAIB     EQU   11                       AIB BASE REGISTER        Y02027
RTCB     EQU   11                       TCB BASE REGISTER        Y02027
RLOOKUP  EQU   11                       UCB LOOKUP TABLE POINTER Y02027
RUCBEXT  EQU   12                       UCB EXTENSION            Y02027
RPEWA    EQU   12                       BASE REG FOR PEWA        Y02027
RSAVE    EQU   13                       SAVE AREA POINTER        Y02027
RRECB    EQU   14                       RECB BASE REGISTER       Y02027
R15      EQU   15                       REGISTER 15              Y02027
         SPACE 3
*        REGISTER EQUATES USED ONLY FOR CROSS MEMORY POST        Y02027
         SPACE 1
RPOST9   EQU   9                        REG SAVE AREA FOR POST   Y02027
RPOST10  EQU   10                       COMPLETION CODE          Y02027
RPOST11  EQU   11                       ECB ADDRESS              Y02027
RPOST12  EQU   12                       ERRET ENTRY POINT        Y02027
RPOST13  EQU   13                       ASCB ADDRESS             Y02027
RPOST14  EQU   14                       RETURN ADDRESS           Y02027
RPOST15  EQU   15                       POST ENTRY POINT         Y02027
         SPACE 2
*
         USING CVT,RCVT                 CVT DSECT
         USING RTMWA,RRTMWA             WORK AREA DSECT          Y02027
         USING IEDQPEB,RPEB             PROCESS ENTRY BLOCK ADDR Y02027
         USING RMPL,RRMPL               RECOVERY PARAMETER LIST  Y02027
         USING IEDQTCX,RTCX             TCX DSECT                Y02027
         USING IEDQPEWA,RPEWA           PROCESS ENTRY WORK AREA  Y02027
         USING IEDQAIB,RAIB             AIB DSECT                Y02027
         EJECT
***************************************************************  Y02027
*                                                             *  Y02027
*        SET UP BASE REGISTER AND PARAMETER LIST              *  Y02027
*        ADDRESSABILITY                                       *  Y02027
*                                                             *  Y02027
***************************************************************  Y02027
         SAVE  (14,12)                  SAVE THE CALLER'S REGS   Y02027
         SPACE 2
         LR    RBASE,R15                SET BASE REGISTER        Y02027
         USING IEDQOT01,RBASE           ADDRESSABILITY           Y02027
         SPACE 2
IEDQOT01 IEDHJN START                   SET DATE                 Y02027
         SPACE 2
         L     RRMPL,ZERO(RONE)         LOAD THE PARAMETER LIST  Y02027
*                                       ADDRESS                  Y02027
         L     RRTMWA,RMPLRMWA          GET ADDR OF RTM WORK AREAY02027
         ST    RSAVE,SAVE13             SAVE CALLER'S REG 13 IN  Y02027
*                                       WORK AREA                Y02027
         SR    RCVT,RCVT                CLEAR FOR STORE        @ZA02601
         ST    RCVT,PTRSA               CLEAR SAVE AREA POINTER@ZA02601
         L     RCVT,CVTPTR              LOAD THE CVT ADDRESS
         EJECT
***************************************************************  Y02027
*                                                             *  Y02027
*        IF TCAM IS NOT IN THE SYSTEM, THEN SKIP IEDQOT01     *  Y02027
*                                                             *  Y02027
***************************************************************  Y02027
         ICM   RTCX,ADDR,CVTAQAVT+ONE   GET TCX ADDRESS          Y02027
         BZ    RETURN1                  IF TCX ADDR IS 0, THEN @ZA02601
*                                       BRANCH                   Y02027
         BAL   RBRANCH,GETSA            GET SAVE AREA          @OZ07816
         BAL   RBRANCH,LOCK             GET LOCAL & CMS LOCK   @OZ07816
         CLM   RTCX,ADDR,CVTAQAVT+ONE   TCX STILL VALID        @OZ07816
         BNE   RETURN                   NO, RETURN             @OZ07816
         SPACE 3                                                 Y02027
***************************************************************  Y02027
*                                                             *  Y02027
*        DETERMINE IF THE TERMINATING TASK OR MEMORY IS TCAM  *  Y02027
*                                                             *  Y02027
***************************************************************  Y02027
         TM    RMPLFLG1,RMPLTERM        IS THIS A MEMORY         Y02027
*                                         TERMINATION            Y02027
         BO    TASKTEST                 YES, BRANCH              Y02027
         CLC   RMPLTCBA+ONE(L3),TCXTCB+ONE DOES TCAM'S TCB ADDR  Y02027
*                                       MATCH THAT OF THE        Y02027
*                                       TERMINATING TASK         Y02027
         BNE   PROCTERM                 NO, BRANCH               Y02027
TASKTEST EQU   *                                                 Y02027
         CLC   RMPLASCB+ONE(L3),TCXASCB+ONE DOES TCAM'S ASCB ADDRY02027
*                                       MATCH THAT OF THE        Y02027
*                                       TERMINATING MEMORY OR    Y02027
*                                       THE MEMORY OF THE        Y02027
*                                       TERMINATING TASK         Y02027
         BNE   PROCTERM                 NO, BRANCH               Y02027
         SPACE 2
***************************************************************  Y02027
*                                                             *  Y02027
*        CHECK TCXRTM. IF THE FLAG IS SET, THEN IEDQOT01 HAS  *  Y02027
*        ALREADY BEEN CALLED FOR TCAM MCP TERMINATION  --     *  Y02027
*        RETURN TO CALLER. IF TCXRTM IS NOT SET, CONTINUE.    *  Y02027
*                                                             *  Y02027
***************************************************************  Y02027
         OI    TCXFLAG1,TCXMCPCD        INDICATE THAT TCAM IS  @ZA02601
*                                       TERMINATING AND THAT     Y02027
*                                       RTM HAS BEEN CALLED      Y02027
         OI    TCXFLAG1,TCXMCPCD        AGAIN FOR MP           @ZA02601
         EJECT
***************************************************************  Y02027
*                                                             *  Y02027
*        ISSUE TCABEND IF TSO IS ACTIVE                       *  Y02027
*                                                             *  Y02027
***************************************************************  Y02027
         ICM   RONE,ALL,TCXRPT          IS TSO ACTIVE            Y02027
         BZ    CHKPEB                   NO, BRANCH               Y02027
         TM    TCXFLAG1,TCXRTM          RECURRSION ON MCP ABEND@ZA02601
         BO    CHKPEB                   YES, BRANCH            @ZA02601
         BAL   RBRANCH,UNLOCK           RELEASE LOCKS          @OZ07816
         TCABEND ,                      ABEND TSO                Y02027
         BAL   RBRANCH,GETSA            GET SAVE AREA          @OZ07816
         BAL   RBRANCH,LOCK             GET LOCAL & CMS LOCK   @OZ07816
         CLM   RTCX,ADDR,CVTAQAVT+ONE   TCX STILL VALID        @OZ07816
         BNE   RETURN                   NO, RETURN             @OZ07816
         EJECT
***************************************************************@ZA02601
*                                                              @ZA02601
*        CHECK FOR APPLICATION WAITING ON MCP                  @ZA02601
*                                                              @ZA02601
***************************************************************@ZA02601
CHKPEB   EQU   *                                               @ZA02601
         LA    RAIB,TCXAIBC             PTR TO AIBS            @ZA02601
LOOPAIB  EQU   *                                               @ZA02601
         ICM   RAIB,ADDR,AIBCHAIN       GET AIB                @ZA02601
         BZ    CHKPEB1                  END OF CHAIN           @ZA02601
         TM    AIBSTATE-IEDQAIB(RAIB),ABEND AIB IN USE         @ZA02601
         BNO   LOOPAIB                  NO, SKIP TO NEXT       @ZA02601
         L     RRECB,AIBECBA-IEDQAIB(RAIB) GET OPEN/CLOSE FLAG @ZA02601
         CLM   RRECB,ALL,OPEN           OPEN IN PROGRESS       @ZA02601
         BNE   CLOSEAIB                 NO, CHECK CLOSE        @ZA02601
         MVI   RCBERR-IEDQRECB+AIBKEY-IEDQAIB(RAIB),NOTCAM     @ZA02601
*                                       SET TCAM NOT IN SYSTEM @ZA02601
         B     APPLPOST                 POST APPL OPEN         @ZA02601
CLOSEAIB EQU   *                                               @ZA02601
         CLM   RRECB,ALL,CLOS           CLOSE IN PROGRESS      @ZA02601
         BE    APPLPOST                 YES, POST APPL COMPLETE@ZA02601
         B     LOOPAIB                  CHECK NEXT AIB         @ZA02601
CHKPEB1  EQU   *                                               @ZA02601
         LA    RPEB,TCXPEBC             DUMMY FIRST PEB        @ZA02601
LOOPPEB  EQU   *                                               @ZA02601
         LTR   RPEBF,RPEB               COPY PEB ADDRESS       @ZA02601
         BZ    FREESA                   END OF PEB CHAIN       @ZA02601
         ICM   RPEB,ALL,PEBLINK         GET NEXT PEB           @ZA02601
         BZ    FREESA                   END OF PEB CHAIN       @ZA02601
         CLC   PEBASCB,QOT1             RTM ALREADY TRIED PEB  @ZA02601
         BNE   CALLRTM                  PEB NOT PROCESSED BY   @ZA02601
*                                       RTM                    @ZA02601
RMVPEB   EQU   *                                               @ZA02601
         BAL   RRECB,DLKPEWA            DELINK PEWA AND PEB    @ZA02601
         B     LOOPPEB                  CHECK NEXT PEB         @ZA02601
APPLPOST EQU   *                                               @ZA02601
         XC    AIBECBA-IEDQAIB(FOUR,RAIB),AIBECBA-IEDQAIB(RAIB) ZA02601
*                                       CLEAR SPECIAL FLAG     @ZA02601
***************************************************************  Y02027
*                                                             *  Y02027
*        SAVE REGISTERS AND SET UP FOR BRANCH ENTRY INTO      *  Y02027
*        CROSS MEMORY POST TO POST APPLICATION PROGRAM CLOSE  *  Y02027
*                                                             *  Y02027
***************************************************************  Y02027
         L     RONE,PTRSA               GET POINTER TO SAVE AREA Y02027
         STM   RZERO,R15,ZERO(RONE)     SAVE ALL REGISTERS       Y02027
         L     RPOST13,RCBASCB-IEDQRECB+AIBKEY-IEDQAIB(RAIB)   @ZA02601
*                                       GET ASCB ADDRESS       @ZA02601
         SR    RPOST10,RPOST10          CLEAR FOR COMP CODE    @ZA02601
         IC    RPOST10,RCBERR-IEDQRECB+AIBKEY-IEDQAIB(RAIB) SET@ZA02601
*                                       COMPLETION CODE        @ZA02601
         L     RPOST11,RCBPECB-IEDQRECB+AIBKEY-IEDQAIB(RAIB)   @ZA07078
*                                        ECB ADDRESS           @ZA07078
         ICM   RPOST11,HIGHORDR,HIBIT   SET CROSS MEM POST     @ZA07078
         LA    RPOST12,CVTBRET          LOAD BR 14 AS ERRET ADDR Y02027
         L     RPOST15,CVT0PT01         POST ENTRY PT IEA0PT01   Y02027
         LR    RPOST9,RONE              SAVE AREA ADDR           Y02027
         BALR  RPOST14,RPOST15          POST ECB                 Y02027
         SPACE 1
         LM    RZERO,R15,ZERO(RPOST9)   RESTORE REGISTERS        Y02027
         B     LOOPAIB                  CHECK NEXT AIB         @ZA02601
         EJECT
***************************************************************  Y02027
*                                                             *  Y02027
*        ISSUE CALLRTM FOR APPLICATION PROGRAM                *  Y02027
*                                                             *  Y02027
***************************************************************  Y02027
CALLRTM  EQU   *                                                 Y02027
         ICM   RTCB,ALL,PEBTCB          TCB ADDRESS            @ZA02601
         BZ    RMVPEB                   BRANCH NO TCB          @ZA02601
         TM    PEBFLAG,PEBTERM          APPL ALREADY TERMINTED @OZ08374
         BO    RMVPEB                   YES, DELINK PEB & PEWAS@OZ08374
         L     RASID,PEBASID            ASID FOR APPL          @OZ08374
         L     RSAVE,PTRSA              GET SAVE AREA ADDRESS  @OZ08374
         CLC   PEBASCB+1(L3),PSAAOLD+1-PSA(RZERO) IS A.P. A    @OZ08374
*                                       SUBTASK OF THE MCP     @OZ08374
         BNE   DUMP                     BR, NO                 @OZ08374
         TM    TCBFLGS-IEDQTCB(RTCB),TERM  IS A.P. TERMINATING @OZ08374
         BNZ   DUMPED                   YES,SET TERMINATING BIT@OZ08374
DUMP     EQU   *                                               @OZ08374
         TM    TCXFLAG1,TCXAPDMP        IS APPL DUMP REQUESTED @OZ08374
         BZ    NODUMP                   NO, DON'T REQUEST DUMP @OZ08374
         CALLRTM TYPE=ABTERM,COMPCOD=X'046',TCB=(RTCB),ASID=(RASID),   *
               DUMP=YES                                        @ZA07083
         B     DUMPED                   DUMP REQUESTED         @ZA07083
NODUMP   EQU   *                                               @ZA07083
         CALLRTM TYPE=ABTERM,COMPCOD=X'046',TCB=(RTCB),ASID=(RASID),   *
               DUMP=NO                                           Y02027
DUMPED   EQU   *                                               @ZA07083
         OI    PEBFLAG,PEBTERM          MARK APPL TERMINATED   @ZA02601
         B     LOOPPEB                  PROCESS NEXT PEB       @ZA02601
         EJECT                                                   Y02027
FREESA   EQU   *                                               @ZA02601
         TM    TCXFLAG1,TCXRTM          RECURRSION             @ZA02601
         BO    PEBFREE                  YES, BRANCH            @ZA02601
         OI    TCXFLAG1,TCXRTM          SET RECURRSION FLAG    @ZA02601
         TM    RMPLFLG1,RMPLTERM        IS THIS MCP MEMORY     @ZA02601
*                                       TERMINATION            @ZA02601
         BO    PEBFREE                  YES, FREE CNTRL BLOCKS @ZA02601
         ICM   RPEB,ALL,TCXPEBC         FIRST PEB ON CHAIN     @ZA02601
         BNZ   RETURN                   CHAIN NOT EMPTY        @ZA02601
PEBFREE  EQU   *                                               @ZA02601
         L     RPEB,TCXPEBC             FIRST PEB ON CHAIN     @ZA02601
         SR    RONE,RONE                CLEAR FOR CS           @ZA02601
PEBFREE1 EQU   *                                               @ZA02601
         CS    RPEB,RONE,TCXPEBC        REMOVE PEBS FROM CHAIN @ZA02601
         BNE   PEBFREE1                 RETRY IF FAILED        @ZA02601
FRPEBS   EQU   *                                               @ZA02601
         LTR   RPEB,RPEB                END OF CHAIN           @ZA02601
         BZ    FREEPEWA                 YES, BRANCH            @ZA02601
         LR    RONE,RPEB                COPY PEB ADDRESS       @ZA02601
         L     RPEB,PEBLINK             NEXT PEB               @ZA02601
         LA    RZERO,PEBSIZE            LENGTH OF PEB          @ZA02601
         BAL   RBRANCH,FREE231          FREE PEB               @ZA02601
         B     FRPEBS                   FREE NEXT PEB          @ZA02601
         EJECT
***********************************************************************
*                                                                     *
*        FREE PROCESS ENTRY WORK AREAS (PEWA'S)                  Y02027
*                                                                     *
***********************************************************************
FREEPEWA EQU   *                                                 Y02027
         SR    RONE,RONE                CLEAR FOR CS           @ZA02601
         L     RPEWA,TCXPEWAC           PEWA CHAIN             @ZA02601
FRPEWA1  EQU   *                                               @ZA02601
         CS    RPEWA,RONE,TCXPEWAC      REMOVE PEWAS FOR CHAIN @ZA02601
         BNE   FRPEWA1                  RETRY IF FAILED        @ZA02601
FRPEWA   EQU   *                                                 Y02027
         LTR   RPEWA,RPEWA              IS THERE A PEWA          Y02027
         BZ    FREEAIBS                 NO, BRANCH               Y02027
         LR    RONE,RPEWA               LOAD THE PEWA ADDRESS    Y02027
         ICM   RPEWA,ADDR,PEWALINK+ONE  GET ADDR OF NEXT PEWA    Y02027
         SR    RBRANCH,RBRANCH          CLEAR REG              @ZA02601
         IC    RBRANCH,TCXSCBSZ         SCB SIZE               @ZA02601
         LA    RBRANCH,PEWASIZE(RBRANCH) ADD PEWA SIZE         @ZA02601
         TM    PEWAOPT2-IEDQPEWA(RONE),PEWAIN  INPUT DCB       @ZA02601
         BNO   FRPEWAS                  NO, BRANCH             @ZA02601
         LA    RBRANCH,TRT(RBRANCH)     ADD TRT SIZE           @ZA02601
FRPEWAS  EQU   *                                               @ZA02601
         LA    RZERO,LCBCHAIN-IEDQLCB+FOUR ADJUST FOR LCB      @ZA02601
         SR    RONE,RZERO               SET ADDR FOR FREEMAIN  @ZA02601
         AR    RZERO,RBRANCH            SET LENGTH FOR FREEMAIN@ZA02601
         BAL   RBRANCH,FREE231          FREE PEWA IN SUBPOOL 231 Y02027
         SPACE 1                                                 Y02027
         B     FRPEWA                   TEST FOR ANOTHER PEWA    Y02027
         EJECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *  Y02027
*                                                             *  Y02027
*        THIS SECTION OF CODE WILL SEARCH THE TCX APPLICATION    Y02027
*        INTERFACE BLOCK (AIB) CHAIN AND FREE ANY AIB'S          Y02027
*        REMAINING ON THE CHAIN.                                 Y02027
*                                                             *  Y02027
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *  Y02027
FREEAIBS EQU   *                                                 Y02027
         TM    TCXFLAG1,TCXMCPCD        MCP TERMINATED         @ZA04027
         BNO   RETURN                   NO EXIT                @ZA04027
         SR    RONE,RONE                CLEAR FOR CS           @ZA02601
         L     RAIB,TCXAIBC             CHAIN OF AIBS          @ZA02601
FRAIBS1  EQU   *                                               @ZA02601
         CS    RAIB,RONE,TCXAIBC        REMOVE AIBS FROM CHAIN @ZA02601
         BNE   FRAIBS1                  RETRY IF FAILED        @ZA02601
FRAIBS   EQU   *                                                 Y02027
         LTR   RAIB,RAIB                IS THERE AN AIB          Y02027
         BZ    FREESRBS                 NO, BRANCH             @Z49X9BG
         LR    RONE,RAIB                COPY THE AIB ADDRESS     Y02027
         ICM   RAIB,ADDR,AIBCHAIN       GET ADDR OF NEXT AIB     Y02027
         LH    RZERO,TCXSZAIB           GET AIB SIZE             Y02027
         BAL   RBRANCH,FREE231          FREE AIB IN SUBPOOL 231  Y02027
         B     FRAIBS                   TEST FOR ANOTHER AIB   @ZA02601
         EJECT                                                 @Z40X9BG
************************************************************** @Z40X9BG
*                                                            * @Z40X9BG
*        FREE SRB'S USED FOR POST FUNCTION                   * @Z40X9BG
*                                                            * @Z40X9BG
************************************************************** @Z40X9BG
FREESRBS EQU   *                                               @Z40X9BG
         SLR   RZERO,RZERO              CLEAR FOR CS           @Z40X9BG
         L     RONE,TCXSRBS             GET PTR TO SRB'S       @Z40X9BG
FRSRB1   EQU   *                                               @Z40X9BG
         LTR   RONE,RONE                ANY SRB'S TO FREE      @Z40X9BG
         BZ    RESTAB                   NO, BRANCH             @Z40X9BG
         CS    RONE,RZERO,TCXSRBS       DECHAIN SRB'S          @Z40X9BG
         BNE   FRSRB1                   BRANCH IF DECHAIN      @Z40X9BG
*                                       UNSUCESSFUL            @Z40X9BG
         SPACE 2                                               @Z40X9BG
************************************************************** @Z40X9BG
*                                                            * @Z40X9BG
*        BRANCH ENTER FREEMAIN - ENTRY EXPECTS TCB ADDRESS   * @Z40X9BG
*        IN REG 4 AND ASCB ADDRESS IN REG 7. EXPANSION       * @Z40X9BG
*        DESTROYS REGS 3,14 AND 15 AND CODE USES REG 7.      * @Z40X9BG
*                                                            * @Z40X9BG
************************************************************** @Z40X9BG
FREES    EQU   *                                               @Z40X9BG
         LA    RPEB,SRBBLKSZ-SIZESRB(,RONE) POINT TO LAST      @Z40X9BG
*                                       SRB IN THE BLOCK       @Z40X9BG
         ICM   RPEB,ADDR,ONE(RPEB)      SAVE ADDR OF FIRST SRB @Z40X9BG
*                                       IN NEXT BLOCK          @Z40X9BG
         L     RSAVE,PTRSA              GET SAVE AREA POINTER  @Z40X9BG
         STM   RBRANCH,R15,ZERO(RSAVE)  SAVE REGISTERS         @Z40X9BG
         L     RPEBF,PSATOLD-PSA(,RZERO) SET TCB ADDRESS       @VS41904
         L     RASID,PSAAOLD-PSA(,RZERO) SET ASCB ADDRESS      @VS41904
         LA    RZERO,SRBBLKSZ           GET SRB BLOCK SIZE     @OZ08384
         FREEMAIN RU,LV=(0),A=(1),SP=SQA,KEY=KEY0,BRANCH=YES   @Z40X9BG
         LM    RBRANCH,R15,ZERO(RSAVE)  RESTORE REGISTERS      @Z40X9BG
         LTR   RONE,RPEB                POINT TO NEXT SRB BLK  @Z40X9BG
         BNZ   FREES                    FREE NEXT BLOCK OF SRBS@Z40X9BG
         EJECT
RESTAB   EQU   *                                               @ZA02601
         TM    TCXFLAG1,TCXMCPCD+TCXRTM MCP TERMINATED         @ZA02601
         BNO   RETURN                   NO EXIT                @ZA02601
         SPACE 2                                               @Z49X9BG
***************************************************************  Y02027
*                                                             *  Y02027
*        RESTORE ATTENTION TABLE POINTER SAVED BY TCAM OPEN   *  Y02027
*                                                             *  Y02027
***************************************************************  Y02027
         L     RONE,CVTIXAVL            GET POINTER TO IOCOM   @Z49X9BG
         USING IOCOM,RONE               IOCOM ADDRESSABILITY   @Z49X9BG
         L     RONE,IOCATTBL            GET ATTN TABLE POINTER   Y02027
         USING ATB,RONE                 ATTN TABLE ADDRESSABILITYY02027
         LA    RONE,TCAM(RONE)          INCREMENT ATTN TABLE ADDRY02027
         MVC   ATBRTN,TCXSATTN          RESTORE INITIAL VALUE OF Y02027
*                                       ATTN ROUTINE POINTER     Y02027
*                                       FOR TCAM                 Y02027
         SR    RBRANCH,RBRANCH          CLEAR FOR STH          @ZA02601
         STH   RBRANCH,ATBASID          CLEAR ASID VALUE       @ZA02601
         DROP  RONE                                            @ZA02601
         SPACE 4                                               @Z40X9BG
************************************************************** @Z40X9BG
*                                                            * @Z40X9BG
*        RESET TCAM'S PURGE ENTRY IN THE VECTOR OF IOS       * @Z40X9BG
*        DRIVERS (VOID)                                      * @Z40X9BG
*                                                            * @Z40X9BG
************************************************************** @Z40X9BG
         L     RONE,CVTIXAVL            GET POINTER TO IOCM    @Z40X9BG
         USING IOCOM,RONE               IOCOM ADDRESSABILITY   @Z40X9BG
         L     RONE,IOCVOID             GET ADDR OF VOID       @Z40X9BG
         USING VOID,RONE                VOID ADDRESSABILITY    @Z40X9BG
         LA    RONE,VOIEL*IOSTCMID(,RONE) GET ADDR OF TCAM'S   @G36XRBB
*                                       SLOT IN THE VOID       @Z40X9BG
         MVC   VOIPRG,VOIRSTR           RESET PURGE ADDR TO    @Z40X9BG
*                                       SAME NO-OP ADDRESS     @Z40X9BG
*                                       USED FOR RESTORE ADDR  @Z40X9BG
         DROP  RONE                                            @Z40X9BG
         EJECT                                                   Y02027
***************************************************************  Y02027
*                                                             *  Y02027
*        RESET TABS IN TCAM UCB'S                             *  Y02027
*                                                             *  Y02027
***************************************************************  Y02027
         USING UCB,RUCB                 UCB ADDRESSABILITY       Y02027
         USING UCBCMEXT,RUCBEXT         UCB EXTENSION            Y02027
*                                       ADDRESSABILITY           Y02027
         L     RLOOKUP,CVTILK2          LOAD THE ADDRESS OF THE  Y02027
*                                       UCB HALFWORD ADDRESS LISTY02027
*                                       SECTION IN THE UCB       Y02027
*                                       LOOKUP TABLE             Y02027
         SR    RUCB,RUCB                CLEAR FOR INSERT         Y02027
         SPACE 3
***************************************************************  Y02027
*                                                             *  Y02027
*        RESET THE ERROR TAB IN ANY NON-BISC UCB'S MODIFIED   *  Y02027
*        BY TCAM                                              *  Y02027
*                                                             *  Y02027
***************************************************************  Y02027
GETUCBAD EQU   *                                                 Y02027
         ICM   RUCB,HALF,ZERO(RLOOKUP)  GET THE ADDR OF THE      Y02027
*                                       FIRST/NEXT UCB           Y02027
         BZ    NEXTUCB                  IF ADDR IS 0, BRANCH     Y02027
         CLM   RUCB,HALF,ENDUCB         IS THIS THE END OF THE @Z30AABG
*                                       UCB LOOKUP TABLE         Y02027
         BE    FREEIOSB                 YES, BRANCH            @Z40X9BG
         L     RUCBEXT,UCBEXTPT         GET UCB EXTENSION ADDR   Y02027
         CLI   UCBETI,NONBISC           IS THIS A NON-BISC LINE  Y02027
         BNE   TESTBISC                 NO, BRANCH               Y02027
         LR    R15,RUCB                 SET UCB ADDR             Y02027
         IOSGEN TP,VAR=29,UCB=(R15)     RESET THE ERROR TAB      Y02027
         SPACE 3
         B     TESTATTN                 GO CHECK ATTN TAB        Y02027
         EJECT
***************************************************************  Y02027
*                                                             *  Y02027
*        RESET THE ERROR TAB IN ANY BISC UCB'S MODIFIED       *  Y02027
*        BY TCAM                                              *  Y02027
*                                                             *  Y02027
***************************************************************  Y02027
TESTBISC EQU   *                                                 Y02027
         CLI   UCBETI,BISC              IS THIS A BISC LINE      Y02027
         BNE   NEXTUCB                  NO, BRANCH               Y02027
         LR    R15,RUCB                 SET UCB ADDR             Y02027
         IOSGEN TP,VAR=2B,UCB=(R15)     RESET BISC ERROR TAB     Y02027
         EJECT
***************************************************************  Y02027
*                                                             *  Y02027
