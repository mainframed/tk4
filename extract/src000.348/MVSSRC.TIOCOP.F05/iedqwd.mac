         TITLE 'IEDQWD - TOTE DISPATCHER MODULE'
IEDQWD   CSECT
*A000000-999999                                                @Y16X5U0
*CHANGE ACTIVITY AS FOLLOWS:
*A378450-378650,405250-405650,669420-669480                     SA41599
*D203850-323850                                                  S21903
*A203850-263850                                                  S21903
*C000000-999999                                                  S22024
*A269080-269880                                                  S05331
*C817000                                                         S05331
*D309000                                                         S05331
*A122300-122600,804200-804800,821700-821900,822070-823890        Y06330
*A878020-878120                                                  Y06330
*822000-823000                                                   Y06330
*D653000                                                        SA64791
*A705000,746000                                                 SA64791
*A431300-431600,833200-833800,881300-881600                    @SA69637
*D882000                                                       @ZA06159
*C742000-745000,736000-737000                                  @ZA06159
*A170000                                                       @ZA06159
*A701000,441000,705000                                         @OY12959
*A427000,431000,569000,615000,697000,820000,861000             @Y17XAUU
*C702000,8220000                                               @Y17XAUU
* A016100,800800,820400                                        @G36XRUV
* C800800                                                      @G36XRUV
*
***********************************************************************
*                                                                     *
* MODULE NAME:  IEDQWD (TCAM,TOTE)                             @Y17XAUU
*                                                                     *
* DESCRIPTIVE NAME:  TOTE DISPATCHER                                  *
*                                                                     *
* COPYRIGHT:  'NONE'                                                  *
*                                                                     *
* STATUS:  VERSION 10.0                                        @Y17XAUU
*                                                                     *
* FUNCTION:                                                           *
*                                                                     *
*    THE PURPOSE OF THIS MODULE IS TO:                                *
*                                                                     *
*        1. SET THE NO CONTROL PRINT FLAG IN THE SCT IF 'NCP' IS      *
*           SPECIFIED IN THE TRM.                                     *
*        2. VERIFY THAT THE SYSOUT DCB IS OPEN IF 'APSYSOUT' IS       *
*           SPECIFIED IN THE TRM.                                     *
*        3. VERIFY THAT THE TEST DEVICE IS SUPPORTED - I.E. UCB       *
*           INDICATES COMMUNICATION OR GRAPHIC DEVICE                 *
*        4. EXCLUSIVELY ENQ ON TEST DEVICE TO PROTECT OTHER OLTS      *
*           FROM USING SAME DEVICES.  IF TEST DEVICE IS A REMOTE      *
*           DEVICE (I.E. SYMBOLIC NAME), A 'SHARE' ENQ IS ISSUED FOR  *
*           ITS UCB.                                                  *
*        5. VERIFY THAT TEST DEVICE IS CONFIGURED.  IF CDS INDICATES  *
*           THAT THE DEVICE IS SWITCHABLE OR SHAREABLE, THE SYSTEM    *
*           CONSOLE OPERATOR IS ASKED TO INSURE THAT THE CONTROL UNIT *
*           IS CONNECTED TO THE PROPER CPU.  IF CDS INDICATES THAT    *
*           DEVICE IS PROTECTED, THE SYSTEM CONSOLE OPERATOR IS       *
*           ASKED FOR PERMISSION TO TEST THE DEVICE.                  *
*        6. STOPS ALL TEST DEVICES TO PREVENT THEIR USE BY TCAM.      *
*                                                                     *
*    THE TRM IS CANCELLED IF:                                         *
*        1. DIAGMSG OPEN FAILS - MESSAGE IED236I                      *
*        2. DEVICE IS NOT SUPPORTED - MESSAGE IED212I                 *
*        3. EXCLUSIVE ENQ FAILS FOR TEST DEVICE - MESSAGE IED231I     *
*        4. I/O ERROR OCCURRED ON LOADING CONFIGURATION DATA - MESSAGE*
*           IED228I                                                   *
*        5. CONFIGURATION DATA SET CANNOT BE OPENED - MESSAGE IED230I *
*        6. NO CONFIGURATION DATA ('FIND' FAILED) - MESSAGE IED229I   *
*        7. CONTROL UNIT NOT CONNECTED TO PROPER CPU - MESSAGE IED226I*
*        8. SYSTEM OPERATOR WILL NOT ALLOW TEST ON PROTECTED DEVICE - *
*           MESSAGE IED329I                                           *
*        9. OPERATOR CONTROL RETURNS A BAD RETURN CODE TO STOP        *
*           LINE, STOP TERMINAL OR DEACTIVATE 3705 COMMAND - MESSAGE  *
*           IED221I                                                   *
*                                                                     *
* ENTRY POINT:  IEDQWD                                                *
*                                                              @Y17XAUU
* PURPOSE: SEE FUNCTION                                        @Y17XAUU
*                                                              @Y17XAUU
* MODULE TYPE: PROCEDURE                                       @Y17XAUU
*                                                              @Y17XAUU
* MODULE SIZE: 4K MAXIMUM                                      @Y17XAUU
*                                                              @Y17XAUU
* PROCESSOR: ASSEMBLER XF                                      @Y17XAUU
*                                                                     *
* EXTERNAL ROUTINES: IEDQTNT - FIND TTE ADDRESS                @Y17XAUU
*                                                              @Y17XAUU
* INPUT:        REGISTERS 02,13,14,15 CONTAIN THE FOLLOWING VALUES:   *
*                                                                     *
*                         02-OLTCB POINTER                            *
*                         13-SAVE AREA ADDRESS                        *
*                         14-RETURN ADDRESS                           *
*                         15-ENTRY POINT ADDRESS                      *
*                                                                     *
* MACROS: AQCTL                                                @Y17XAUU
*         CLOSE                                                @Y17XAUU
*         DELETE                                               @Y17XAUU
*         ENQ                                                  @Y17XAUU
*         FIND                                                 @Y17XAUU
*         IEDHJN                                               @Y17XAUU
*         IEDQMSG                                              @Y17XAUU
*         LINK                                                 @Y17XAUU
*         LOAD                                                 @Y17XAUU
*         OPEN                                                 @Y17XAUU
*         WAIT                                                 @Y17XAUU
*         WTO                                                  @Y17XAUU
*         WTOR                                                 @Y17XAUU
*         XCTL                                                 @Y17XAUU
*                                                              @Y17XAUU
* EXIT POINTS:  XCTL AFTER 'DISPEXIT' TO IEDQWE                       *
*                                                                     *
* TABLES/WORK AREAS:                                                  *
*                                                                     *
*    OLTCB,UCB,DEB,DCB,QCB,CDS,CVT,LCB,TTE,TNT,AVT                    *
*                                                                     *
* ATTRIBUTES:                                                         *
*                                                                     *
*       ENABLED, PROBLEM PROGRAM MODE, TRANSIENT.                     *
*                                                                     *
* NOTES: SEE BELOW                                             @Y17XAUU
*                                                              @Y17XAUU
*  DEPENDENCIES: EBCDIC CHARACTER CODE                         @Y17XAUU
*                                                              @Y17XAUU
*  RESTRICTIONS: NONE                                          @Y17XAUU
*                                                              @Y17XAUU
*  REGISTER CONVENTIONS: SEE REGISTERS ASSIGNMENT              @Y17XAUU
*                                                              @Y17XAUU
*  PATCH LABEL: PATCH                                          @Y17XAUU
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
***********************************************************************
*
*
*        REGISTER EQUATES
*
***********************************************************************
***********************************************************************
R0       EQU   0                       REG 0                     S99528
R1       EQU   1                       REG 1                     S99528
R2       EQU   2                       REG 2                     S99528
R3       EQU   3                       REG 3                     S99528
R4       EQU   4                       REG 4                     S99528
R5       EQU   5                       REG 5                     S99528
R6       EQU   6                       REG 6                     S99528
R7       EQU   7                       REG 7                     S99528
R8       EQU   8                       REG 8                     S99528
R9       EQU   9                       REG 9                     S99528
RSIB     EQU   9                  SIB BASE                     @Y17XAUU
R10      EQU   10                      REG 10                    S99528
R11      EQU   11                      REG 11                    S99528
R12      EQU   12                      REG 12                    S99528
R13      EQU   13                      REG 13                    S99528
R14      EQU   14                      REG 14                    S99528
R15      EQU   15                      REG 15                    S99528
PARMREG0 EQU   R0                      MSG PARM                  S99528
PARMREG1 EQU   R1                      MSG PARM                  S99528
Y        EQU   X'E8'                   YES                       S99528
TPCLASS  EQU   X'40'              TP DEVICE CLASS
GRAPHIC  EQU   X'10'              DISPLAY DEVICE CLASS
TYPE2260 EQU   X'03'              2260 DEVICE TYPE
ANR      EQU   X'09'                   3270                      S99528
ANRP     EQU   X'0A'                   3284                      S99528
ANRPP    EQU   X'0B'                   3286                      S99528
DEVTBEND EQU   C' '                    BLANK                     S99528
XFF      EQU   X'FF'              MASK BYTE                    @Y17XAUU
ADRMSK   EQU   7                       ADDRESS MASK            @Y17XAUU
RELEASE  EQU   X'87'              RELEASE TERMINAL VERB CODE   @Y17XAUU
LINESTOP EQU   X'41' .       STOP LINE VERB CODE
TERMSTRT EQU   X'45' .       START TERMINAL VERB CODE
TERMSTOP EQU   X'47' .       STOP TERMINAL VERB CODE
TERMNACT EQU   X'48'                   INACTIVATE TERMINAL VERB  S22024
*                                        CODE                    S22024
DEACT    EQU   X'54'                   DEACTIVATE VERB CODE      S22024
TERMHOLD EQU   X'88'                   HOLD TERMINAL VERB CODE   S22024
SWPRI    EQU   X'50'                   SWITCH TO PRIMARY         Y06330
SWBCK    EQU   X'51'                   SWITCH TO BACKUP          Y06330
LGB      EQU   X'80'                   DSORG BIT SETTING FOR LGB S22024
D0       EQU   0                       CONSTANT ZERO             S22024
D1       EQU   1                       DISP ONE                  S22024
D2       EQU   2                       DISPLACEMENT 2            S22024
D3       EQU   3                       DISP THREE                S22024
D4       EQU   4                       DISP FOUR                 S22024
D5       EQU   5                       DISPLACEMENT 5            S22024
D6       EQU   6                       DISPLACEMENT 6            S22024
D7       EQU   7                       DISPLACEMENT 7            S22024
D8       EQU   8                       DISPLACEMENT 8            S22024
D9       EQU   9                       CONSTANT DISPLACEMENT     S22024
D12      EQU   12                      DISPLACEMENT 12           S22024
D13      EQU   13                      DISPLACEMENT 13
D16      EQU   16                      DISPLACEMENT 16           S22024
D18      EQU   18                      DISPLACEMENT 18           S22024
D19      EQU   19                      DISPLACEMENT 19           S22024
D24      EQU   24                      DISPLACEMENT 24           S22024
D32      EQU   32                      DISPLACEMENT 32           S22024
D40      EQU   40                      CONSTANT DISPLACEMENT 40  S22024
D41      EQU   41                      CONSTANT DISPLACEMENT 41  S22024
D42      EQU   42                      CONSTANT DISPLACEMENT 42  S22024
TCUCLASS EQU   X'40'                   CLASS BYTE IN CDS FOR TCU S22024
D48      EQU   48                      CONSTANT DISPLACEMENT     S22024
X0F      EQU   X'0F'                   USED TO CLEAR BITS        S22024
XF0      EQU   X'F0'                   USED TO CLEAR BITS        S22024
MAINONLY EQU   X'40'                   BIT SETTING IN QCB        S22024
*                                        INDICATING MAIN         S22024
*                                        STORAGE QUEUEING ONLY   S22024
XE0      EQU   X'E0'                   USED TO CLEAR BITS        S22024
OPEN     EQU   X'10'                   BIT SETTING IN DCB IF OPENS22024
GOOD     EQU   X'00'                   GOOD RETURN CODE FROM     S22024
*                                      OPERATOR CONTROL          S22024
ALREADY  EQU   X'08'                   RETURN CODE FROM OP CNTRL S22024
*                                        INDICATING REQUEST      S22024
*                                        ALREADY DONE            S22024
X20      EQU   X'20'              USED TO FIND LCB               S22024
X44      EQU   X'44'                   BIT SETTING IN CDS        S22024
*                                        INDICATING SHARED OF    S22024
*                                        SWITCHABLE DEVICE       S22024
X80      EQU   X'80'                   BIT SETTING IN TERMINAL   S22024
*                                        ENTRY INDICATING LINE   S22024
*                                        ENTRY                   S22024
BLANK    EQU   X'40'                   BLANK CHARACTER           S22024
RN3705   EQU   X'05'                   BIT INDICATING 3705 UCB   S22024
RNUCB    EQU   X'80'                   BIT SETTING INDICATING    S22024
*                                        3705 UCB TEST           S22024
OPCKEY   EQU   X'00'                   OP CONTROL KEY FIELD      S22024
OPCPRI   EQU   X'DC'                   OP CONTROL PRIORITY       S22024
OPCRTCD  EQU   X'00'                   OP CONTROL RETURN CODE    S22024
OPCELNG  EQU   X'18'                   OP CONTROL ELEMENT LENGTH S22024
START102 EQU   X'0C'                   ACTION CODE FOR SVC 102   S22024
LAST102  EQU   X'80'                   LAST ELEMENT IN LIST      S22024
DISKQ    EQU   X'30'                    MASK FOR DISK QUEUES   @ZA06159
         EJECT
***********************************************************************
***********************************************************************
*                                                                     *
*       SATISFY SYSTEM LINKAGE CONVENTIONS                            *
*                                                                     *
***********************************************************************
***********************************************************************
         USING TOTOLTCB,R2             TOTE CONTROL BLOCK        S22024
         USING IEDQWD,R4
         STM   R14,R12,D12(R13)        SAVE CALLER'S REGISTERS   S22024
         LR    R4,R15             SET UP BASE
         ST    R13,TOTSAVE4+D4         POINT TO CALLERS SAVE     S22024
         LR    R12,R13                 SAVE CALLERS SAVE         S99528
         LA    R13,TOTSAVE4            POINT TO MY SAVE AREA     S22024
         ST    R13,D8(R12)             SAVE POINTER TO           S22024
*                                        DISPATCHERS SAVE AREA   S22024
IEDQWD   IEDHJN IEDQWDD,HJN
         EJECT
***********************************************************************
***********************************************************************
*                                                                     *
*****          CLEAR TEST DEVICE ENTRIES IN OLTCB          ******S99528
*                                                                     *
***********************************************************************
***********************************************************************
         XC    TOTPRIBK(TOTSCIBK-TOTPRIBK),TOTPRIBK  CLEAR       S22024
*                                                      PRIMARY   S22024
*                                                      FIELD     S22024
         XC    TOTSCIBK(TOTTRMND-TOTSCIBK),TOTSCIBK  CLEAR       S22024
*                                                      SECONDARY S22024
*                                                      FIELD     S22024
         L     R14,TOTAVTPT            GET AVT ADDRESS         @Y17XAUU
         USING IEDQAVTD,R14            SET AVT ADDRESSABILITY  @Y17XAUU
         L     R1,AVTRNMPT             GET TTE CONVERSION RTNA @Y17XAUU
         ST    R1,TERNMTAB             SAVE IT                 @Y17XAUU
         LA    R1,AVTOPCOB             GET OPCONTROL QCB ADDR  @Y17XAUU
         STCM  R1,ADRMSK,TOTOQCBA      SAVE IT IN POST PLIST   @Y17XAUU
         DROP  R14                                             @Y17XAUU
         EJECT
***********************************************************************
***********************************************************************
*
**             SET NO CONTROL PRINT FLAG IF REQUIRED
*
***********************************************************************
***********************************************************************
DISP005  EQU   *
         TM    TOTFLG03,TOTIMNCP       WAS NCP IN TRM
         BZ    SYSOUT                  NO BRANCH                 S99528
         OI    $ERROPT,$NOCNTRL        SET NCP FLAG
SYSOUT   EQU   *                                                 S22024
         TM    TOTFLG09,TOTAPOUT       APSYSOUT REQUESTED?       S22024
         BZ    DISP19B                 NO BRANCH                 S99528
         L     R1,TOTAPDCB             GET AP DCB                S21903
         LTR   R1,R1                   IS THERE ONE              S21903
         BNZ   DISP19B                 YES BRANCH                S21903
         IEDQMSG MSGID=050,FUNCT=CEC   AP NOT OPEN MSG           S99528
         B     CANCELL                 GO CANCEL TRM             S99528
         EJECT
***********************************************************************
***********************************************************************
*
*        DATA PROTECTION CHECKS
*
***********************************************************************
***********************************************************************
DISP19B  EQU   *
         LA    R5,TOTTDTBL             DEVICE TABLE ADDR         S99528
         MVC   SAVENT(D4),TOTTDTBL     SAVE TEST DEVICE ENTRY  @Y17XAUU
UCBADDR  EQU   *                                                 S22024
         TM    D0(R5),TOTTDORN         IS TEST ENTRY ON 3705?    S22024
         BO    TNTENTRY                YES, BRANCH               S22024
         TM    D0(R5),TOTTDTRM         IS TEST ENTRY A TERMINAL? S22024
         BO    TNTENTRY                YES, BRANCH               S22024
         L     R6,D0(R5)               GET UCB ADDRESS           S22024
         LTR   R6,R6                   LAST ENTRY?               S22024
         BZ    EXIT                    YES, BRANCH TO EXIT       S22024
         CLI   D18(R6),TPCLASS         IS IT TP DEVICE?          S22024
         BNE   TESTGR                  NO, GO TEST FOR GRAPHICS  S22024
         MVC   TOTWKSPC(D1),D19(R6)    GET CONTROL UNIT FIELD    S22024
*                                        FROM UCB                S22024
         NI    TOTWKSPC,X0F            CLEAR HIGH ORDER NIBBLE   S22024
         CLI   TOTWKSPC,RN3705         IS THIS A 3705 UCB?       S22024
         BNE   DISP22                  NO, 270X UCB - OK FOR TESTS22024
         OI    RNFLAG,RNUCB            SET BIT INDICATING RN UCB S22024
         B     DISP22                  OK FOR TEST, BRANCH       S22024
TESTGR   EQU   *                                                 S22024
         CLI   D18(R6),GRAPHIC         IS IT GRAPHIC DEVICE?     S22024
         BNE   DISP21                  NO: DEVICE NOT TESTABLE
         CLI   D19(R6),TYPE2260        IS IT 2260?               S22024
         BE    DISP22                  YES:OK FOR TEST
         CLI   D19(R6),ANR             ANR (3270)?               S22024
         BE    DISP22                  YES BR                    S99528
         CLI   D19(R6),ANRP            ANR PRINTER (3284)?       S22024
         BE    DISP22                  YES BR                    S99528
         CLI   D19(R6),ANRPP           ANR PRINTER (3286)?       S22024
         BE    DISP22                  YES BR                    S99528
         SPACE 2
*                                                                S22024
*        ISSUE DEVICE NOT SUPPORTED MESSAGE                      S22024
*                                                                S22024
         SPACE 2
DISP21   EQU   *
         IEDQMSG MSGID=007,FUNCT=CEC,LINK=YES  NOT SUPPORTED     S22024
         SPACE 2
CANCELL  EQU   *                                                 S22024
         TM    D0(R5),TOTTDORN+TOTTDTRM  IS TEST DEVICE ON A 3705S05331
         BNZ   CANCELL1                YES, BRANCH               S05331
         L     R6,D0(R5)               GET UCB ADDRESS           S05331
         USING UCB,R6                  SET UP BASE FOR UCB       S05331
         MVC   TOTWKSPC(D1),UCBTBYT4   GET UCB TYPE FIELD        S05331
         NI    TOTWKSPC,X0F            CLEAR HIGH ORDER NIBBLE   S05331
         CLI   TOTWKSPC,RN3705         IS THIS A 3705 UCB?       S05331
         BNE   CANCELL1                NO, BYPASS CLEAR CODE     S05331
         OI    D0(R5),TOTTNIPL         SET FLAG SAYING NO IPL    S05331
         DROP  R6                      DROP UCB BASE             S05331
CANCELL1 EQU   *                                                 S05331
         OI    TOTFLG06,TOTOTERM+TOTCANCL    SET CANCEL AND      S22024
*                                              TERMINATE BITS    S22024
         OI    TOTFLG06,TOTCBOPN       SET CANCEL BEFOR OPEN     S99528
         B     EXIT                    BRANCH TO EXIT ROUTINE    S22024
         EJECT
DISP22   EQU   *
         USING UCB,R6                  UCB                       S99528
         MVC   RNAME(D3),UCBNAME       MOVE NAME TO PARM LIST    S22024
         DROP  R6                      UCB                       S99528
ENQ1     EQU   *                                                 S22024
         ENQ   ,MF=(E,ENQLIST)         REQUEST EXCLUSIVE USE     S22024
         LTR   R15,R15                 HAS DEVICE BEEN ASSIGNED  S22024
*                                        TO TOTE?                S22024
         BZ    DISP234                 YES BRANCH                S99528
         CLI   D3(R15),ALREADY         RETURN CODE 8?            S22024
         BE    DISP234                 OK BRANCH                 S99528
         SPACE 2
***      REENTER TRM LATER: RESOURCE IN UNSHAREABLE STATE
***
         SPACE 2
BUSY     EQU   *                                                 S22024
         IEDQMSG MSGID=044,FUNCT=CEC   ISSUE ERROR MESSAGE       S22024
         B     CANCELL                 BRANCH                    S99528
         EJECT
**************************************************************** S22024
*                                                              * S22024
*        THE FOLLOWING CODE OPENS THE CORRECT CONFIGURATION    * S22024
*        DATA SET AND VERIFIES THAT THE TEST DEVICE IS         * S22024
*        CONFIGURED.  TESTS ARE MADE TO VERIFY THAT THE DEVICE * S22024
*        IS CONNECTED TO THE PROPER CPU IF IT IS SWITCHABLE    * S22024
*        AND TO GET PERMISSION TO TEST A PROTECTED DEVICE.     * S22024
*                                                              * S22024
**************************************************************** S22024
         SPACE 2
DISP234  EQU   *                                                 S99528
         TM    D0(R5),TOTTDORN         IS TEST ENTRY ON 3705?    S22024
         BO    TERMNAME                YES, BRANCH               S22024
         TM    D0(R5),TOTTDTRM         IS TEST ENTRY A TERMINAL? S22024
         BO    TERMNAME                YES, BRANCH               S22024
         MVC   CDSNAME+D5(D3),RNAME    MOVE MEMBER NAME FOR LINE S22024
         B     MEMBFIND                BRANCH                    S99528
TERMNAME EQU   *                                                 S22024
         MVC   CDSNAME,RNAME           SYMBOLIC NAME FOR LINE    S22024
*                                        OR TERMINAL             S22024
MEMBFIND EQU   *                                                 S22024
         MVI   RNAME,BLANK             BLANK OUT                 S22024
         MVC   RNAME+D1(D7),RNAME        NAME                    S22024
         OPEN  OLT                     OPEN DATA SET CONTAINING  S22024
*                                        CONFIGURATION DATA      S22024
         TM    OLT+X'30',X'10'         OPEN OK                   S99528
         BZ    CANOTOPN                NO BRANCH                 S99528
         FIND  OLT,CDSNAME,D           SEE IF MEMBER EXIST       S99528
         LTR   R15,R15                 FIND SUCCESSFUL           S99528
         BZ    NOWLOAD                 YES BRANCH                S99528
         LA    R11,4                   CK RETURN CODE            S99528
         CR    R11,R15                 RETURN CODE EQ 4          S99528
         BE    NONAMEMG                YES BRANCH                S99528
***
***      I/O ERROR LOADING CONFIGURATION DATA
***
         IEDQMSG MSGID=041,FUNCT=CEC                             S99528
         B     CANCELL                 GO STOP TOTE
CANOTOPN EQU   *                  MESSAGE                        S99528
***
***      CONFIGURATION DATA SET CANNOT BE OPENED
***
         IEDQMSG MSGID=043,FUNCT=CEC
         B     CANCELL            CANCEL TRM                     S99528
NONAMEMG EQU   *
         TM    SAVENT,TOTTDORN               3705 RESOURCE     @Y17XAUU
         BZ    NONACON                 NO - ISSUE ERROR MSG    @Y17XAUU
         L     R1,SAVENT               GET TEST DEVICE ENTRY   @Y17XAUU
         LA    R1,D0(R1)               CLEAR FLAG BYTE         @Y17XAUU
         SLR   R14,R14                 CLEAR REG               @Y17XAUU
         IC    R14,TOTTTBEL            GET LEN OF NAME         @Y17XAUU
         AR    R1,R14                  POINT TO TTE ADDRESS    @Y17XAUU
         ICM   R1,ADRMSK,D0(R1)        GET TTE ADDRESS         @Y17XAUU
         LA    R14,TRMPRFSZ            GET TTE PREFIX SIZE     @Y17XAUU
         SLR   R1,R14                  POINT TO TTE PREFIX     @Y17XAUU
         USING IEDNTRM,R1              TTE ADDRESSABILITY      @Y17XAUU
         CLI   TRMTYPE,TRMLUNT         LU ENTRY                @Y17XAUU
         BNE   NONAM04                 YES - CHECK NEXT ENTRY  @Y17XAUU
         L     R15,TOTAVTPT            GET AVT ADDRESS         @Y17XAUU
         USING IEDQAVTD,R15            SET AVT ADDRESSABILITY  @Y17XAUU
         SLR   R11,R11                 CLEAR REGISTER          @Y17XAUU
         IC    R11,TRMCHCIN            GET INDEX INTO DCT      @Y17XAUU
         BCTR  R11,R0                  ADJUST INDEX            @Y17XAUU
         MH    R11,AVTDCTLN            TRUE OFFSET INTO DCT    @Y17XAUU
         AL    R11,AVTCSTCS            POINT TO CORRECT ENTRY  @Y17XAUU
         USING IEDDCT,R11              SET DCT ADDRESSABILITY  @Y17XAUU
         TM    DCTBYTE5,DCTL3270       3270 LU                 @ZM47850
         BO    NONACON                 YES - SEND ERROR MSG    @Y17XAUU
         B     TRMNOTRJ                BYPASS ERROR MSG        @YM08563
         DROP  R11                                             @Y17XAUU
         DROP  R15                                             @Y17XAUU
NONAM04  DS    0H                      BOUNDARY ALLIGNMENT     @Y17XAUU
         CLI   TRMTYPE,TRMSDLC         SDLC LINK               @Y17XAUU
         BE    TRMNOTRJ                YES - CHECK NEXT ENTRY  @Y17XAUU
         CLI   TRMTYPE,TRMNSDLC        NON-SDLC LINK           @Y17XAUU
         BE    TRMNOTRJ                YES - CHECK NEXT ENTRY  @Y17XAUU
         DROP  R1                                              @Y17XAUU
NONACON  DS    0H                      BOUNDARY ALLIGNMENT     @Y17XAUU
***
***      TRM REJECTED: NO CONFIGURATION DATA
***
         IEDQMSG MSGID=042,FUNCT=CEC                             S99528
         B     CANCELL                 CANCEL TRM                S99528
NOWLOAD  EQU   *                                                 S99528
         LOAD  EPLOC=CDSNAME           CDS TO CORE             @YM08389
         LR    R11,R0                  GET CDS ADDRESS           S99528
         XC    TOTOTECB,TOTOTECB       CLEAR ECB                 S99528
         B     DEVPROT                 GO CHECK FOR DEV PROTECT  S99528
SWABLE   EQU   *                                                 S99528
         TM    D9(R11),X44             IS CONTROL UNIT SWITCHABLES22024
*                                        AND/OR SHARED?          S22024
         BZ    MSGNOTND                NO BRANCH                 S99528
         XC    TOTOTECB,TOTOTECB       CLEAR ECB                 S99528
         XC    ANSWER,ANSWER           CLEAR ANSWER              S99528
         CLI   D6(R11),TCUCLASS        IS THIS A TCU CDS?        S22024
         BNE   MOVENAME                NO, MOVE ENTIRE CDS NAME  S22024
*                                        INTO MESSAGE            S22024
         SPACE 2
***
***      MESSAGE-WTO TO ASK OPERATOR TO INSURE DEVICE IS HOOKED TO THIS
***
         SPACE 2
         MVI   A270XSW+D40,BLANK       BLANK OUT NAME            S22024
         MVC   A270XSW+D41(D7),A270XSW+D40  FIELD IN MESSAGE     S22024
         MVC   A270XSW+D40(D3),CDSNAME+D5   MOVE NAME INTO MSG   S22024
         B     SWABLEQ                 GO ASK THE QUESTION       S22024
MOVENAME EQU   *                                                 S22024
         MVC   A270XSW+D40(D8),CDSNAME   MOVE NAME INTO MESSAGE  S22024
SWABLEQ  EQU   *                                                 S22024
         WTOR  ,ANSWER,3,TOTOTECB,MF=(E,A270XSW)                 S99528
         DELETE EPLOC=CDSNAME          DELETE CDS                S99528
         WAIT  ECB=TOTOTECB            WAIT FOR ANSWER           S99528
         OI    ANSWER,BLANK            MAKE REPLY UPPER CASE     S22024
         CLI   ANSWER,Y                ANSWER YES                S99528
         BE    TRMNOTRJ                YES BR                    S99528
         SPACE 2
***                                                              S22024
***      TRM REJECTED: C.U. NOT CONNECTED TO PROPER CPU
***                                                              S22024
         SPACE 2
         IEDQMSG MSGID=039,FUNCT=CEC                             S99528
         B     CANCELL                 BRANCH                    S99528
DEVPROT  EQU   *                                                 S99528
         TM    10(R11),X'20'           DEVICE PROTECTED?         S99528
         BO    PROTECT                 YES, BRANCH               S99528
         B     SWABLE                  NO, BRANCH                S99528
PROTECT  EQU   *                                                 S99528
         CLI   D6(R11),TCUCLASS        IS THIS A TCU CDS?        S22024
         BNE   MOVEALL                 NO, MOVE IN ENTIRE CDS    S22024
*                                        NAME                    S22024
         MVI   PROTDEV+D41,BLANK       BLANK OUT NAME FIELD      S22024
         MVC   PROTDEV+D42(D7),PROTDEV+D41  IN MESSAGE           S22024
         MVC   PROTDEV+D41(D3),CDSNAME+D5  MOVE IN CUU NAME      S22024
*                                            WITHOUT PREFIX      S22024
         B     PROTECTQ                GO ASK QUESTION           S22024
MOVEALL  EQU   *                                                 S22024
         MVC   PROTDEV+41(8),CDSNAME   MOVE NAME INTO MESSAGE    S99528
PROTECTQ EQU   *                                                 S22024
         WTOR  ,ANSWER,3,TOTOTECB,MF=(E,PROTDEV)                 S99528
         WAIT  ECB=TOTOTECB            WAIT FOR ANSWER           S99528
         OI    ANSWER,X'40'            MAKE REPLY UPPER CASE     S99528
         CLI   ANSWER,Y                'YES' REPLY?              S99528
         BE    SWABLE                  IF SO BRANCH              S99528
         MVC   DEVPRONA,CDSNAME        MOVE NAME TO MESSAGE      S99528
         IEDQMSG FUNCT=(CEC,PRT),OTBUF=PROTMSG,OTCNT=64,               C
               PREG0=R0,PREG1=R1,RET=CANCELL                     S99528
MSGNOTND EQU   *                                                 S99528
         DELETE EPLOC=CDSNAME          DELETE CDS                S99528
TRMNOTRJ EQU   *                                                 S22024
         L     R6,D0(R5)               GET TEST DEVICE ENTRY     S22024
         BAL   R12,STOPDEV             GO STOP TEST DEVICE       S22024
         TM    D0(R5),TOTTDORN         IS TEST ENTRY ON 3705?    S22024
         BO    UCBLOOK                 YES, GO FIND 3705 UCB     S22024
         TM    D0(R5),TOTTDTRM         IS TEST ENTRY A TERMINAL? S22024
         BO    UCBLOOK                 YES, GO FIND UCB FOR LINE S22024
         LA    R5,4(R5)                GET NEXT UCB              S99528
         LA    R7,TOTTDEND             GET ADDRESS OF END OF     S22024
*                                        TABLE                   S22024
         CR    R5,R7                   END OF TABLE              S99528
         BNE   UCBADDR                 NO GO GET NEXT ENTRY      S22024
         B     EXIT                    YES, BRANCH TO EXIT       S22024
         EJECT
**************************************************************** S22024
*                                                              * S22024
*        THIS SECTION OF CODE PROCESSES A TEST ENTRY THAT IS   * S22024
*        A SYMBOLIC ENTRY - I.E. A TERMINAL ON A 270X OR A     * S22024
*        LINE OR TERMINAL ON A 3705.                           * S22024
*                                                              * S22024
**************************************************************** S22024
         SPACE 2
TNTENTRY EQU   *                                                 S22024
         L     R6,D0(R5)               GET ADDRESS OF TNT        S22024
         LTR   R6,R6                   LAST ENTRY IN TABLE?      S22024
         BZ    EXIT                    YES, BRANCH TO EXIT       S22024
         TM    D0(R5),TOTTDLIN         IS TEST DEVICE A LINE?    S22024
         BO    ENQONE                  YES, BYPASS LINE ENQ      S22024
         TM    D0(R5),TOTTDORN         IS TERMINAL ON A 3705?    S22024
         BNO   ENQONE                  NO, BYPASS SEARCH FOR     S22024
*                                        LINE NAME               S22024
         SR    R3,R3                   CLEAR REGISTER 3          S22024
         IC    R3,TOTTTBEL             GET MAXIMUM NAME LENGTH   S22024
         LR    R11,R6                  SAVE TNT ADDRESS          S22024
         AR    R11,R3                  GET ADDR OF TTE ADDR      S22024
         MVC   TTEADDR+D1(D3),D0(R11)  MOVE TERMINAL ENTRY ADDR  S22024
         L     R8,TTEADDR              LOAD TERMINAL ENTRY ADDR  S22024
         LA    R9,TRMPRFSZ             GET TTE PREFIX SIZE     @Y17XAUU
         SLR   R8,R9                   POINT TO TTE PREFIX     @Y17XAUU
         USING IEDNTRM,R8              SET TTE ADDRESSABILITY  @Y17XAUU
         TM    TRMBYTE2,TRMGPACT       GEN POLL ACTIVE         @Y17XAUU
         BO    IED278I                 YES BRANCH              @SA69637
         L     R12,TOTAVTPT            GET AVT ADDRESS         @Y17XAUU
         USING IEDQAVTD,R12            SET AVT ADDRESSABILITY  @Y17XAUU
LINELOOP EQU   *                                               @Y17XAUU
         LH    R1,TRMCOHRT             GET COHORT POINTER      @Y17XAUU
         STH   R1,OFFSET               SAVE IT                 @Y17XAUU
         L     15,AVTRNMPT             TTCIN TO TTE CONVERSION @Y17XAUU
*                                      ROUTINE ADDRESS         @Y17XAUU
         BALR  R14,R15                 GET TTE ADDRESS         @Y17XAUU
         LR    R8,R1                   ET TTE ADDRESS          @Y17XAUU
         LA    R1,TRMPRFSZ             GET TTE PREFIX SIZE     @Y17XAUU
         SLR   R8,R1                   POINT TO TTE PREFIX     @Y17XAUU
         CLI   TRMTYPE,TRMNSDLC        PRE SNA LINE ENTRY      @Y17XAUU
         BE    LINEFND                 YES                     @Y17XAUU
         CLI   TRMTYPE,TRMSDLC         SNA LINE ENTRY          @Y17XAUU
         BNE   LINELOOP                NO - CHECK NEXT ENTRY   @Y17XAUU
LINEFND  EQU   *                                               @Y17XAUU
         L     R1,AVTRNMPT             GET TERMNAME TABLE ADDR @Y17XAUU
         DROP  R12                                             @Y17XAUU
         USING IEDQTNTD,R1             SET TNT ADDRESSABILITY  @Y17XAUU
         LA    R1,TNTFIRST             GET ADDRESS OF FIRST    @Y17XAUU
*                                      ENTRY                   @Y17XAUU
         DROP  R1                                              @Y17XAUU
         LA    R7,D3                   GET LEN OF TTE ADDRESS  @Y17XAUU
         AR    R7,R3                   GET LEN ON TNT ENTRY    @Y17XAUU
         LH    R11,OFFSET              GET LINE TTCIN          @Y17XAUU
         BCTR  R11,R0                  ADJUST OFFSET           @Y17XAUU
         MR    R10,R7                  GET OFFSET TO LINE TNT  @Y17XAUU
         AR    R1,R11                  GET LINE TNT ADDRESS    @Y17XAUU
         LR    R6,R1                   COPY TNT ADDRESS        @Y17XAUU
         BCTR  R3,R0                   DECREMENT COUNT BY 1      S22024
         CLI   TRMTYPE,TRMSDLC         SNA LINK                @Y17XAUU
         BNE   LN004                   NO - SET ENQ PLIST      @Y17XAUU
         LA    R1,ENQLIST1             ENQ SHARED ON SNA LINK  @Y17XAUU
         B     LN008                   ENQ LINE                @Y17XAUU
LN004    DS    0H                      BOUNDARY ALLIGNMENT     @Y17XAUU
         LA    R1,ENQLIST              ENQ EXCLU ON PRE SNA    @Y17XAUU
*                                      LINK                    @Y17XAUU
LN008    DS    0H                      BOUNDARY ALLIGNMENT     @Y17XAUU
         LA    R8,TNTNAME              GET TNT POINTER           S22024
         EX    R3,TNTMOVE              MOVE NAME                 S22024
         MVC   RNAME(D8),TNTNAME       MOVE NAME INTO ENQ LIST   S22024
         ENQ   ,MF=(E,(1))             ENQ 3705 COMM LINE      @Y17XAUU
         MVI   RNAME,BLANK             BLANK OUT                 S22024
         MVC   RNAME+D1(D7),RNAME        NAME FIELD              S22024
         LTR   R15,R15                 ASSIGNED OK?              S22024
         BZ    ENQONE                  YES, CONTINUE             S22024
         CLI   D3(R15),ALREADY         ALREADY DONE BY TASK      S22024
         BNE   BUSY                    NO, BAD RETURN, BUSY      S22024
ENQONE   EQU   *                                                 S22024
         SR    R3,R3                   CLEAR REG                 S99528
         IC    R3,TOTTTBEL             GET NAME LENGTH           S99528
         TM    OLT+D48,OPEN            IS CDS DCB ALREADY OPEN?  S22024
         BO    NOMOVE                  BRANCH AROUND MOVE OF NAMES22024
         MVC   OLT+40(8),CDSLIB        NEW NAME DD               S99528
NOMOVE   EQU   *                                                 S22024
         BCTR  R3,0                    DECREMENT FOR EX          S99528
TNTADDR  L     R6,0(R5)                GET TNT ADDR              S99528
         LA    R8,TNTNAME              TNT POINTER               S99528
         EX    R3,TNTMOVE              MOVE NAME                 S99528
         MVC   RNAME(8),TNTNAME        MOVE NAME TO DCB          S99528
         B     ENQ1                    BRANCH                    S99528
         EJECT
**************************************************************** S22024
*                                                              * S22024
*        THIS ROUTINE FINDS THE UCB FOR A GIVEN TERMNAME TABLE * S22024
*        ENTRY.                                                * S22024
*                                                              * S22024
**************************************************************** S22024
         SPACE 2
UCBLOOK  EQU   *                                                 S22024
         LR    R9,R6                   SAVE POINTER              S22024
         SR    R10,R10                 CLEAR REG                 S99528
         IC    R10,TOTTTBEL            GET NAME LENGTH           S99528
         AR    R9,R10                  TTE ADDRESS               S99528
         MVC   TTEADDR+1(3),0(R9)      ALLINE ADDRESS            S99528
         L     R8,TTEADDR              GET TTE ADDRESS           S99528
         USING IEDQTRM,R8              TTE                       S99528
         L     R9,TRMDESTQ-D1          GET QCB ADDR              S22024
         USING IEDQQCB,R9              SET UP ADDRESSIBILITY FOR S22024
*                                        QCB                     S22024
         SR    R7,R7                   CLEAR REGISTER 7          S22024
         TM    TRMSTATE,TRMPREF        IS THIS A 3705 TERMINAL?@Y17XAUU
         BZ    GETRLN                  NO, GO GET RLN FROM QCB   S22024
         LA    R7,D1                   SET RLN TO 1 FOR 3705     S22024
         B     MULTRLN                 GO MULTIPLY RLN BY 4      S22024
GETRLN   EQU   *                                                 S22024
         IC    R7,QCBRELLN             GET RLN                   S22024
MULTRLN  EQU   *                                                 S22024
         SLL   R7,2                    MULT BY 4                 S99528
         L     R8,QCBDCBAD-1           GET DCB ADDRESS           S99528
         DROP  R8                      DROP TERMINAL ENTRY BASE  S22024
         DROP  R9                      DROP QCB BASE             S22024
         USING IHADCB,R8               DCB                       S99528
         TM    DCBDSORG,LGB            IS THIS A LGB?            S22024
         BZ    DCBFIND                 NO, A DCB, BRANCH         S22024
         DROP  R8                      DROP DCB BASE             S22024
         L     1,TTEADDR               GET TTE ADDRESS         @Y17XAUU
         L     R12,TOTAVTPT            GET AVT ADDRESS         @Y17XAUU
         USING IEDQAVTD,R12            SET AVT ADDRESSABILITY  @Y17XAUU
         BAL   R14,NCPTTE              GET NCP TTE ADDRESS     @Y17XAUU
*                                                              @Y17XAUU
*        ON RETURN R1 CONTAINS ADDRESS OF NCP TTE PREFIX       @Y17XAUU
*                                                              @Y17XAUU
         USING IEDNTRM,R1              SET TTE ADDRESSABILITY  @Y17XAUU
         L     R1,TRMDESTQ-D1          GET QCB ADDRESS         @Y17XAUU
         DROP  R1                                              @Y17XAUU
         USING IEDQQCB,R1              SET QCB ADDRESSABILITY  @Y17XAUU
         L     R8,QCBDCBAD-D1          GET 3705 DCB ADDRESS    @Y17XAUU
         DROP  R1                                              @Y17XAUU
DCBFIND  EQU   *                                                 S22024
         USING IHADCB,R8               SET UP BASE REG FOR DCB   S22024
         TM    DCBOFLGS,X'10'          DCB OPEN                  S99528
         BZ    NOLINEOP                NO, BRANCH                S22024
         ST    R8,DCBADDS              SAVE DCB ADDRESS          S99528
         L     R8,DCBDEBAD             GET DEB ADDRESS           S99528
         DROP  R8                      DCB                       S99528
         USING DEBNMSUB,R8             DEB                       S99528
         SR    R15,R15                 CLEAR REG                 S99528
         IC    R15,DEBNMEXT            GET # OF EXTENTS          S99528
         ST    R15,NOOFEXTS            SAVE NUMBER               S99528
         SLL   R15,2                   MULT BY 4                 S99528
         CR    R15,R7                  LINE OPEN                 S99528
         BL    NOLINEOP                NO, BRANCH                S22024
         L     R8,DEBUCBAD-4(R7)       GET UCB ADDRESS           S99528
         USING UCB,R8                  UCB                       S99528
         MVC   RNAME(3),UCBNAME        MOVE NAME TO P-LIST       S99528
         DROP  R8                      UCB                       S99528
         ENQ   ,MF=(E,ENQLIST1)        UCB NAME                  S99528
         MVI   RNAME,X'40'             BLANK OUT                 S99528
         MVC   RNAME+1(7),RNAME        NAME                      S99528
         LTR   R15,R15                 ASSIGNED OK               S99528
         BZ    BUMPPTR                 YES BRANCH                S99528
         CLI   3(R15),X'08'            ALREADY DONE BY TASK      S99528
         BE    BUMPPTR                 OK BRANCH                 S99528
         B     BUSY                    BRANCH                    S99528
BUMPPTR  EQU   *                                                 S99528
         LA    R5,4(R5)                BUMP DEV TABLE PTR        S99528
         LA    R7,TOTTDEND             END OF TABLE ADDRESS      S99528
         CR    R5,R7                   END OF TABLE              S99528
         BNE   TNTENTRY                NO, GO GET NEXT ENTRY     S22024
         B     EXIT                    GET OUT                   S22024
*                                                                S22024
*        TRM REJECTED - SIZE OF DATA TO BE TRANSFERRED IS        S22024
*        GREATER THAN MAXIMUM DATA SIZE THE 3705 CAN HANDLE      S22024
*                                                                S22024
DATAERR  EQU   *                                                 S22024
         IEDQMSG MSGID=013,FUNCT=CEC,RET=CANCELL,PREG0=R0,PREG1=R1
*                                                                S22024
*        TRM REJECTED - TEST LINE NOT OPEN                       S22024
*                                                                S22024
NOLINEOP EQU   *                                                 S22024
         IEDQMSG MSGID=038,FUNCT=CEC,RET=CANCELL,PREG0=R0,PREG1=R1
         EJECT
***********************************************************************
*                                                                     *
*       EXIT ROUTINE                                                  *
*                                                                     *
***********************************************************************
         SPACE 2
EXIT     EQU   *                                                 S22024
         TM    OLT+X'30',X'10'         DCB OPEN                  S99528
         BZ    DISPEXIT                NO BRANCH                 S99528
         CLOSE OLT                     LOT2LIB OR CDSLIB         S99528
DISPEXIT EQU   *
         L     R13,4(R13)           TRANSFER CONTROL
         L     R14,12(R13)          TO TEST CONTROL
         XCTL  (2,12),EP=IEDQWE     MODULE
         EJECT
************************************************************** @Y17XAUU
*        ROUTINE TO FIND NCP TTE ADDRESS. INPUT: REG 1 =     * @Y17XAUU
*        TERMINAL TTE ADDRESS. OUTPUT: REG 1 = NCP TTE       * @Y17XAUU
*        ADDRESS. RETURN TO CALLER VIA REG 14.               * @Y17XAUU
************************************************************** @Y17XAUU
NCPTTE   EQU   *                                               @Y17XAUU
         ST    R14,TRMSAVE2            SAVE RETURN ADDRESS     @Y17XAUU
         LR    R8,R1                   GET TERMINAL TTE ADDRESS@Y17XAUU
         LA    R1,TRMPRFSZ             GET TTE PREFIX SIZE     @Y17XAUU
         SLR   R8,R1                   POINT TO TTE PREFIX     @Y17XAUU
         USING IEDNTRM,R8              SET TTE ADDRESSABILITY  @Y17XAUU
NCPLOOP  EQU   *                                               @Y17XAUU
         LH    R1,TRMCOHRT             GET COHORT POINTER      @Y17XAUU
         L     15,AVTRNMPT             TTCIN TO TTE CONVERSION @Y17XAUU
*                                      ROUTINE ADDRESS         @Y17XAUU
         BALR  R14,R15                 FIND TTE ADDRESS        @Y17XAUU
         LR    R8,R1                   GET TTE ADDRESS         @Y17XAUU
         LA    R1,TRMPRFSZ             GET TTE PREFIX SIZE     @Y17XAUU
         SLR   R8,R1                   POINT TO TTE PREFIX     @Y17XAUU
         CLI   TRMTYPE,TRMLNCP         NCP ENTRY               @Y17XAUU
         BNE   NCPLOOP                 NO - CHK NEXT ENTRY     @Y17XAUU
         LR    R1,R8                   SAVE NCP TTE ADDRESS    @Y17XAUU
         L     R14,TRMSAVE2            STORE RETURN ADDRESS    @Y17XAUU
         BR    R14                     RETURN TO CALLER        @Y17XAUU
         DROP  R8                                              @Y17XAUU
         DROP  R12                RELEASE AVT BASE             @YM07721
         EJECT
**************************************************************** S22024
*                                                              * S22024
*        THIS ROUTINE STOPS THE TEST DEVICE.  IF THE DEVICE IS * S22024
*        A TERMINAL, A 'STOP TERMINAL' OPERATOR CONTROL COMMAND* S22024
*        IS ISSUED.  IF THE DEVICE IS A 3705, A 'DEACTIVATE'   * S22024
*        COMMAND IS ISSUED.  IF THE DEVICE IS A LINE, A 'STOP  * S22024
*        LINE' COMMAND IS ISSUED.                              * S22024
*                                                              * S22024
**************************************************************** S22024
         SPACE 2
STOPDEV  EQU   *                                                 S22024
         STM   R5,R6,WORKAREA          SAVE REGISTERS            S22024
         L     R3,TOTAVTPT             GET ADDRESS OF AVT        S22024
         USING IEDQAVTD,R3             SET UP ADDRESSIBILITY     S22024
*                                        FOR AVT                 S22024
         LA    R1,AVTOPCOB             GET OPERATOR CONTROL      S22024
*                                        QCB ADDRESS             S22024
         ST    R1,TOTOKEY              PUT ADDRESS INTO ELEMENT  S22024
         MVI   TOTOPRI,OPCPRI          SET OP CONTROL PRIORITY   S22024
         MVI   TOTOKEY,OPCKEY          SET KEY FIELD TO ZERO     S22024
         MVI   TOTOELNG,OPCELNG        SET ELEMENT LENGTH        S22024
         XC    TOTOLINK,TOTOLINK       CLEAR LINK FIELD          S22024
         MVI   TOTORTCD,OPCRTCD        ZERO OUT RETURN CODE FLD  S22024
         MVI   TOTOLADR,BLANK          MOVE BLANK INTO NAME FLD  S22024
         MVC   TOTOLADR+D1(D7),TOTOLADR  BLANK OUT FIELD         S22024
         XC    TOTOTECB,TOTOTECB       CLEAR ECB                 S22024
         LA    R1,TOTOTECB             GET ECB ADDRESS           S22024
         ST    R1,TOTOECBA             PUT ECB ADDRESS INTO      S2024
*                                        ELEMENT                 S22024
         TM    D0(R5),TOTTDORN         IS TEST DEVICE ON 3705?   S22024
         BO    RNDEVICE                YES, BRANCH AROUND DEB    S22024
*                                        SEARCH                  S22024
         TM    D0(R5),TOTTDTRM         IS TEST DEVICE TERMINAL?  S22024
         BO    RNDEVICE                BRANCH TO STOP TERMINAL   S22024
         MVC   TOTOLADR(D3),D13(R6)    MOVE UCB NAME INTO OP     S22024
*                                        CONTROL ELEMENT         S22024
         L     R1,AVTTCB               GET TCAM TCB ADDRESS      S22024
         L     R1,D8(R1)               GET DEB CHAIN ADDRESS     S22024
         USING DEBNMSUB,R1             SET UP ADDRESSIBILITY FOR S22024
*                                        DEB                     S22024
STOPDEV1 EQU   *                                                 S22024
         LA    R1,D0(R1)               CLEAR HIGH ORDER BYTE     S22024
         SR    R10,R10                 CLEAR LOOP COUNTER        S22024
         IC    R10,DEBNMEXT            INITIALIZE LOOP COUNTER   S22024
*                                        TO NUMBER OF EXTENTS    S22024
         LA    R8,DEBUCBAD             GET UCB CHAIN ADDRESS     S22024
         L     R11,DEBDEBAD            GET ADDRESS OF NEXT DEB   S22024
         L     R9,DEBDCBAD             GET DCB ADDRESS           S22024
         DROP  R1                      DROP DEB BASE REGISTER    S22024
         USING IHADCB,R9               SET UP ADDRESSIBILITY FOR S22024
*                                        DCB                     S22024
         TM    DCBDSORG+D1,X44         IS THIS A TCAM DEB?       S22024
         BZ    STOPDEV3                NO: GET NEXT DEB          S22024
         LA    R7,D1                   INITIALIZE RELATIVE LINE  S22024
         DROP  R9                      DROP DCB BASE             S22024
*                                        NUMBER COUNTER TO ONE   S22024
STOPDEV2 EQU   *                                                 S22024
         L     R9,D0(R8)               GET UCB ADDRESS           S22024
         LA    R9,D0(R9)               CLEAR HIGH ORDER BYTE     S22024
         LA    R6,D0(R6)               CLEAR HIGH ORDER BYTE FOR S22024
*                                        COMPARE                 S22024
         CR    R6,R9                   CORRECT UCB FOUND?        S22024
         BE    STOPDEV4                YES, END OF SEARCH        S2024
         LA    R7,D1(R7)               BUMP RELATIVE LINE NUMBER S22024
         LA    R8,D4(R8)               GET NEXT UCB ADDRESS      S22024
         BCT   R10,STOPDEV2            LOOP TILL END OF UCB CHAINS22024
STOPDEV3 EQU   *                                                 S22024
         LR    R1,R11                  POINT R1 TO NEXT DEB      S22024
         LA    R11,D0(R11)             CLEAR HIGH BYTE           S22024
         LTR   R11,R11                 IS THTER ANOTHER          S22024
         BZ    D0(R12)                 NO BRANCH                 S22024
         B     STOPDEV1                GET NEXT UCB CHAIN        S22024
STOPDEV4 EQU   *                                                 S22024
         LM    R5,R6,WORKAREA          RESTORE REGISTERS 5 & 6   S22024
         STC   R10,TOTORLN             PUT RELATIVE LINE NUMBER  S22024
*                                        INTO OP CONTROL ELEMENT S22024
         TM    RNFLAG,RNUCB            IS THIS AN RN UCB TEST?   S22024
         BNO   STOPLINE                NO, GO ISSUE STOP LINE    S22024
*                                        COMMAND                 S22024
         MVI   TOTOVBCD,DEACT          MOVE DEACTIVATE VERB CODE S22024
*                                        INTO OP CONTROL ELEMENT S22024
         B     POSTELEM                GO POST ELEMENT           S22024
STOPTERM EQU   *                                                 S22024
         L     R8,TTEADDR              GET TTE ADDRESS           S22024
         LA    R10,TRMPRFSZ            GET TTE PREFIX SIZE     @Y17XAUU
         SLR   R8,R10                  GET TTE PREFIX ADDRESS  @Y17XAUU
         USING IEDNTRM,R8              SET TTE ADDRESSABILITY  @Y17XAUU
         L     R10,TRMDESTQ-D1         GET QCB ADDRESS         @OY12959
         USING IEDQQCB,R10             SET QCB ADDRESSABILITY  @OY12959
         MVC   TOTWKSPC(D1),QCBDSFLG   GET QCB FLAG BYTE       @OY12959
         DROP  R10                                             @OY12959
         TM    TRMSTATE,TRMPREF        TEST DEVICE ON A 3705   @Y17XAUU
         BNO   NOT3705                 NO, GO FIND LCB           S22024
         TM    TRMBYTE0,TRMINPUT       TERM CAPABLE OF INPUT   @OY12959
         BZ    HOLDTERM                NO - OUTPUT ONLY        @OY12959
         TM    TRMBYTE0,TRMDIAL        IS TERMINAL DIAL?       @Y17XAUU
         BNO   STOPBOTH                NO, GO ISSUE STOP TERM    S22024
*                                        COMMAND                 S22024
HOLDTERM EQU   *                                                 S22024
         TM    TOTWKSPC,DISKQ          DISK QUEUE              @OY12959
         BZ    D0(R12)                 NO - MAIN STORAGE       @OY12959
         CLC   AVTAS(D4),AVTFZERO      IS HOLD IN THE SYSTEM?   SA64791
         BE    D0(R12)                 NO, DO NOT ISSUE HOLD    SA64791
         MVI   TOTOVBCD,TERMHOLD       SET VERB CODE TO HOLD     S22024
*                                        TERMINAL                S22024
         B     POSTELEM                GO POST ELEMENT TO OP     S22024
*                                        CONTROL                 S22024
NOT3705  EQU   *                                                 S22024
         L     R10,TRMDESTQ-D1         GET QCB ADDRESS           S22024
         USING IEDQQCB,R10             SET UP ADDRESSIBILITY FOR S22024
*                                        QCB                     S22024
         DROP  R8                      DROP TTE BASE             S22024
         MVC   TOTWKSPC(D1),QCBDSFLG   GET QCB FLAG BYTE         S22024
         NI    TOTWKSPC,XF0            CLEAR LOW ORDER NIBBLE    S22024
         L     R7,QCBDCBAD-D1          GET DCB ADDRESS           S22024
         USING IHADCB,R7               SET UP ADDRESSIBILITY FOR S22024
*                                      DCB                       S22024
         SR    R9,R9                   CLEAR REGISTER 9          S22024
         IC    R9,QCBRELLN             GET RELATIVE LINE NUMBER  S22024
         DROP  R10                     DROP QCB BASE             S22024
         SR    R10,R10                 CLEAR REGISTER 10         S22024
         IC    R10,DCBEIOBX            GET LCB SIZE FROM DCB     S22024
         MR    R8,R10                  GET STARTING LCB OFFSET   S22024
         AL    R9,DCBIOBAD             GET STARTING ADDRESS OF   S22024
*                                        LCB                     S22024
         LA    R10,X20                 GET DECREMENT FOR LCB     S22024
*                                      START                     S22024
         SR    R9,R10                  GET ADDRESS OF LCB        S22024
         USING IEDQLCB,R9              SET UP ADDRESSIBILITY FOR S22024
*                                        LCB                     S22024
         TM    LCBSTAT2,LCBDIAL        IS THIS A DIAL LCB?       S22024
         BNO   CHKMO                   NO, GO SEE IF MAIN STORAGES22024
*                                        ONLY QUEUING            S22024
         TM    TOTWKSPC,DISKQ           DISK QUEUES?           @ZA06159
         BNZ   HOLDTERM                 GO ISSUE HOLD CMD      @ZA06159
         B     D0(R12)                 TERMINAL IS DIAL AND WITH S22024
*                                        MO QUEUING SO NO OP     S22024
*                                        CONTROL COMMAND ISSUED  S22024
CHKMO    EQU   *                                                 S22024
         L     R8,DCBDEBAD             GET DEB ADDRESS         @OY12959
         CLI   DEBOPATB-DEBNMSUB(R8),D3 OUTPUT ONLY            @OY12959
         BE    HOLDTERM                YES                     @OY12959
         TM    TOTWKSPC,DISKQ           DISK QUEUES?           @ZA06159
         BZ    INACT                    ONLY INACTIVATE TERM   @ZA06159
STOPBOTH EQU   *                                                 S22024
         CLC   AVTAS(D4),AVTFZERO      IS HOLD IN THE SYSTEM?   SA64791
         BE    INACT                   NO, DO NOT ISSUE HOLD    SA64791
         DROP  R3                      DROP AVT BASE REGISTER   SA64791
         MVI   TOTOVBCD,TERMSTOP       GET STOP TERMINAL VERB    S22024
*                                        CODE (V OFFTP,B)        S22024
         B     POSTELEM                GO POST ELEMENT TO OP     S22024
*                                        CONTROL                 S22024
INACT    EQU   *                                                 S22024
         MVI   TOTOVBCD,TERMNACT       GET INACTIVATE VERB CODE  S22024
*                                        (V OFFTP,E)             S22024
         B     POSTELEM                GO POST ELEMENT TO OP     S22024
*                                        CONTROL
         DROP  R9                      DROP LCB BASE             S22024
         DROP  R7                      DROP DCB BASE             S22024
RNDEVICE EQU   *                                                 S22024
         LR    R9,R6                   SAVE TNT ADDRESS          S22024
         SR    R10,R10                 CLEAR REGISTER 10         SI2024
         IC    R10,TOTTTBEL            GET MAXIMUM NAME LENGTH   S22024
         BCTR  R10,R0                  DECREMENT COUNT BY 1      S22024
         EX    R10,SYMMOVE             GO MOVE NAME INTO P-LIST  S22024
         LA    R10,D1(R10)             RESTORE LENGTH OF NAME    S22024
         AR    R9,R10                  GET TERMINAL ENTRY ADDR   S22024
         MVC   TTEADDR+D1(D3),D0(R9)  SAVE TTE ADDRESS           S22024
         L     R8,TTEADDR              GET TTE ADDRESS           S22024
         LR    R10,R8                  COPY TTE ADDRESS        @Y17XAUU
         USING IEDQTRM,R8              SET UP ADDRESSIBILITY FOR S22024
*                                        TERMINAL ENTRY          S22024
         L     R8,TRMDESTQ-D1          GET QCB ADDRESS           S22024
         DROP  R8                      DROP TTE BASE             S22024
         USING IEDQQCB,R8              SET UP ADDRESSIBLITY FOR  S22024
*                                      QCB                       S22024
         MVC   TOTORLN(D1),QCBRELLN    GET RELATIVE LINE NUMBER  S22024
         TM    D0(R5),TOTTDTRM         IS DEVICE A TERMINAL?     S22024
         BO    BACKUP                  YES, BYPASS INACTIVATE- @YM08136
*                                      ALREADY DONE BY QWC1/QWJ
STOPLINE EQU   *                                                 S22024
         MVI   TOTOVBCD,LINESTOP       MOVE STOP LINE VERB CODE  S22024
*                                      INTO ELEMENT              S22024
         TM    D0(R5),TOTTDORN         IS DEVICE ON A 3705?      S22024
         BNO   POSTELEM                NO, DO NOT GET GROUP NAME S22024
         LA    R1,TRMPRFSZ             GET TTE PREFIX SIZE     @XM05679
         SLR   R10,R1                  POINT TO TTE PREFIX     @XM05679
         USING IEDNTRM,R10             SET TTE ADDRESSABILITY  @XM05679
         CLI   TRMTYPE,TRMNSDLC        NON SDLC LINK           @XM05679
         BE    POSTE                   YES                     @XM05679
         MVI   TOTOVBCD,TERMHOLD       SET HOLD TERM VERB CODE @XM05679
         B     POSTELEM                GO ISSUE COMMAND        @XM05679
POSTE    EQU   *                                               @XM05679
         L     R8,QCBLGBAD-D1          GET LGB ADDRESS TO GET    S22024
         DROP  R8                      LINE GROUP NAME           S22024
         USING IEDNLGB,R8              FOR STOP LINE ON 3705     S22024
         MVC   TOTOLADR(D8),LGBNAME    GROUP NAME TO PLIST     @Y17XAUU
         DROP  R8                                              @Y17XAUU
POSTELEM EQU   *                                                 S22024
         LA    R1,TOTPLIST             GET ADDRESS OF OP CONTROL S22024
*                                        PARAMETER LIST          S22024
         ST    R1,ELEM1                STORE ADDRESS IN 1ST WORD S22024
*                                        OF SVC 102 POST LIST    S22024
         ST    R1,ELEM2                STORE ADDRESS INTO 2ND    S22024
*                                        WORD OF SVC 102 POSTLISTS22024
         MVI   ELEM1,START102          MOVE IN ACTION CODE       S22024
         MVI   ELEM2,LAST102           MOVE IN LAST ELEMENT      S22024
*                                        INDICATOR               S22024
         LA    R1,SLPL                 GET ADDRESS OF OPERATOR   S22024
*                                        CONTROL PARAMETER LIST  S22024
         TM    TOTFLG04,TOTCLOSE       TCAM CLOSE DOWN IN        S22024
*                                        PROGRESS?               S22024
         BO    EXIT                    YES, BRANCH TO EXIT       S2024
         AQCTL                         ISSUE OP CONTROL COMMAND  S22024
         WAIT  ECB=TOTOTECB            WAIT FOR FUNCTION         S22024
         LM    R5,R6,WORKAREA          RESTORE REGISTERS 5 & 6   S22024
         CLI   TOTOVBCD,SWPRI          SW TO PRIMARY             Y06330
         BE    SWCHECK                 YES BRANCH                Y06330
         CLI   TOTOVBCD,SWBCK          SW REQUEST                Y06330
         BE    SWCHECK                 YES BRANCH                Y06330
         CLI   TOTORTCD,GOOD           WAS REQUEST SUCCESSFUL?   S22024
         BE    BACKUP                  YES, GO SEE IF DEVICE IS  S22024
*                                        IN BACKUP MODE          S22024
         CLI   TOTORTCD,ALREADY        ALREADY STOPPED?          S22024
         BE    BACKUP                  YES, GO SEE IF DEVICE IS  S22024
*                                        IN BACKUP MODE          S22024
         CLI   TRMTYPE,TRMSDLC         SDLC LINK               @Y17XAUU
         BNE   STOPERR                 NO - WRITE ERROR MSG    @Y17XAUU
         MVI   TOTOVBCD,RELEASE        SET RELEASE TERM VERB CD@Y17XAUU
         XC    TOTORTCD,TOTORTCD       CLEAR RETURN CODE       @Y17XAUU
         XC    TOTOLINK(D3),TOTOLINK   CLEAR LINK FIELD        @Y17XAUU
         XC    TOTOTECB(D4),TOTOTECB   CLEAR ECB               @Y17XAUU
         LA    R1,TOTOTECB             GET ECB ADDRESS         @Y17XAUU
         ST    R1,TOTOECBA             PUT ECB ADDRESS IN PLIST@Y17XAUU
         LA    R1,SLPL                 GET PLIST ADDRESS       @Y17XAUU
         AQCTL                         RELEASE THE TERMINALS   @Y17XAUU
*                                      ON THE SDLC LINK        @Y17XAUU
         WAIT  ECB=TOTOTECB            WAIT FOR COMPLETION     @Y17XAUU
         DROP  R10                                             @YM07444
STOPERR  DS    0H                      BOUNDARY ALLIGNMENT     @Y17XAUU
         SPACE 2
***                                                              S22024
***      SEND STOP DEVICE FAILED MESSAGE                         S22024
***                                                              S22024
         MVC   DEVSTOP,TOTOLADR        MOVE NAME INTO MESSAGE    S22024
         IEDQMSG FUNCT=CEC,OTBUF=STOPFAIL,OTCNT=55,PREG0=R0,PREG1=R1,  *
               RET=CANCELL1
         SPACE 1
BACKUP   EQU   *                                                 S22024
         L     R8,TTEADDR              GET TTE ADDRESS           S22024
         LA    R1,TRMPRFSZ             GET TTE PREFIX SIZE     @Y17XAUU
         SLR   R8,R1                   POINT TO TTE PREFIX     @Y17XAUU
         USING IEDNTRM,R8              SET TTE ADDRESSABILITY  @Y17XAUU
         TM    TRMSTATE,TRMPREF        3705 RESOURCE           @Y17XAUU
         BNO   D0(R12)                 NO, RETURN TO CALLER      S22024
         CLI   TRMTYPE,TRMSDLC         SDLC LINK TEST          @Y17XAUU
         BE    LUCHK                   YES - SCAN LU'S         @Y17XAUU
         TM    $TOTFLG1,$TERMSEC       TRM REQUEST TEST IN       Y06330
*                                        BACKUP MODE (IE.TAP)    Y06330
         BO    IED272I                 YES, GO ISSUE ERROR MSG   Y06330
         TM    TRMBYTE1,TRMSWBCK       IS TERMINAL IN SWITCHED @Y17XAUU
*                                        BACKUP MODE?            Y06330
         BZ    D0(R12)                 NO, NO COMMAND NEEDED     Y06330
         MVI   TOTOVBCD,SWPRI          MOVE SWITCH TO PRIMARY    Y06330
*                                        VERB CODE IN P-LIST     Y06330
         DROP  R8                                              @Y17XAUU
CLRFIELD EQU   *                                                 Y06330
         MVI   TOTOKEY,OPCKEY          CLEAR KEY                 Y06330
         MVI   TOTORTCD,OPCRTCD        CLEAR RT CODE             Y06330
         XC    TOTOTECB,TOTOTECB       CLEAR ECB                 Y06330
         XC    TOTOLINK,TOTOLINK       CLEAR LINK FIELD          Y06330
         OI    D0(R5),TOTTSWPM         INITIAL STATUS IS BACKUP  Y06330
         B     POSTELEM                GO TO OP CONTROL          Y06330
SWCHECK  EQU   *                                                 Y06330
         CLI   TOTORTCD,GOOD           REQUEST SUCESSFUL         Y06330
         BE    D0(R12)                 YES RETURN                Y06330
         MVC   TERMINAL(D8),TOTOLADR   NAME TO MSG               Y06330
         CLI   TOTOVBCD,SWPRI          VERB SW TO PRIMARY        Y06330
         BNE   IED240I                 NO BRANCH                 Y06330
         MVC   BACKUPP(D7),PRIMARY     PROCESS MSG               Y06330
IED240I  EQU   *                                                 Y06330
         IEDQMSG FUNCT=CEC,OTBUF=BCKUPFAL,OTCNT=52,PREG0=R0,PREG1=R1,  C
               RET=CANCELL             CAN NOT MAKE SW REQUEST   Y06330
         SPACE 2
***      ISSUE MESSAGE SAYING BACKUP TEST NOT YET SUPPORTED      Y06330
***      ISSUE MESSAGE SAYING BACKUP TEST NOT YT
IED272I  EQU   *                                                 Y06330
         IEDQMSG MSGID=014,FUNCT=CEC,PREG0=R0,                   Y06330*
               PREG1=R1,RET=CANCELL                              Y06330
         BO    NOTRM                   YES, CANCEL TRM           S22024
         B     D0(R12)                 NO, RETURN TO CALLER      S22024
*                                                                S22024
*        TEST DEVICE IN SWITCHED BACKUP MODE - TRM CANCELLED     S22024
*                                                                S22024
NOTRM    EQU   *                                                 S22024
         IEDQMSG MSGID=012,FUNCT=CEC,RET=CANCELL,PREG0=R0,PREG1=R1
SYMMOVE  MVC   TOTOLADR(D0),D0(R9)     MOVE SYMBOLIC NAME TO     S22024
*                                        OPERATOR CONTROL PARM   S22024
*                                        LIST                    S22024
IED278I  EQU   *                       IED278I                 @SA69637
***      IED278I TRM CANCELLED - TEST DEVICE IS BEING GEN POLLEDSA69637
         IEDQMSG FUNCT=CEC,OTBUF=GPOLLREJ,OTCNT=59,PREG0=R0,           C
               PREG1=R1,RET=CANCELL    REJ TRM                 @SA69637
         EJECT
LUCHK    DS    0H                      BOUNDARY ALLIGNMENT     @Y17XAUU
************************************************************** @Y17XAUU
*                                                            * @Y17XAUU
*        VERIFY ALL LU'S ON THE SDLC LINK ARE NOT IN SESSION * @Y17XAUU
*        IF ANY ARE IN SESSION TERMINATE TRM PROCESSING AND  * @Y17XAUU
*        TURN OFF TRMSTOTE FLAG IN EACH LU TTE AND LINE TTE. * @Y17XAUU
*        ISSUE RELEASE FOR SDLC LINK.                        * @Y17XAUU
*                                                            * @Y17XAUU
************************************************************** @Y17XAUU
         L     R14,TOTAVTPT            GET AVT ADDRESS         @Y17XAUU
         USING IEDQAVTD,R14            SET AVT ADDRESSABILITY  @Y17XAUU
         L     R14,AVTSAVTP            GET SAVT ADDRESS        @Y17XAUU
         DROP  R14                                             @Y17XAUU
         USING IEDNSVTD,R14            SET SAVT ADDRESSABILITY @Y17XAUU
         MVC   SSCPTCIN(D2),SAVTSCPT   SAVE SSCT TTCIN         @Y17XAUU
         DROP  R14                                             @Y17XAUU
         SPACE
         L     R1,TTEADDR              GET LINE TTE ADDRESS    @Y17XAUU
         LA    R8,TRMPRFSZ             GET TTE PREFIX SIZE     @Y17XAUU
         SLR   R1,R8                   POINT TO TTE PREFIX     @Y17XAUU
         USING IEDNTRM,R1              SET TTE ADDRESSABILITY  @Y17XAUU
         LH    R1,TRMSUBST             GET SUBSTRUCT POINTER   @Y17XAUU
         LTR   R1,R1                   ANY TERMINALS ON LINE   @Y17XAUU
         BZ    LUSTOP                  GO ISSUE STOPLINE       @Y17XAUU
         SPACE
LULOOP   DS    0H                      BOUNDARY ALLIGNMENT     @Y17XAUU
         L     R15,TERNMTAB            GET TTE CONVERSION RTNA @Y17XAUU
         BALR  R14,R15                 GO FIND TTE ADDRESS     @Y17XAUU
         SLR   R1,R8                   POINT TO TTE PREFIX     @Y17XAUU
         CLI   TRMTYPE,TRMSDLC         END OF CHAIN            @Y17XAUU
         BE    LUSTOP                  YES - GO ISSUE STOPLINE @Y17XAUU
         ICM   RSIB,ADRMSK,TRMSIBPT    GET SIB ADDRESS         @Y17XAUU
         BZ    LURL08                  NO SESSION ACTIVE       @Y17XAUU
         USING IEDSIBD,RSIB            SET SIB ADDRESSABILITY  @Y17XAUU
         CLC   SSCPTCIN(D2),SIBINDEX   SSCP-LU SESSION ONLY    @Y17XAUU
         BNE   LURLSE                  NO - ERROR PROCESSING   @Y17XAUU
LURL08   DS    0H                      BOUNDARY ALLIGNMENT     @Y17XAUU
         LH    R1,TRMCOHRT             GET COHORT POINTER      @Y17XAUU
         B     LULOOP                  CHECK NEXT ENTRY        @Y17XAUU
LUSTOP   DS    0H                      BOUNDARY ALLIGNMENT     @Y17XAUU
         MVI   TOTOVBCD,LINESTOP       SET STOPLINE VERB CODE  @Y17XAUU
         XC    TOTORTCD(D1),TOTORTCD   CLEAR RETURN CODE       @Y17XAUU
         XC    TOTOLINK(D3),TOTOLINK   CLEAR LINK FIELD        @Y17XAUU
         XC    TOTOTECB(D4),TOTOTECB   CLEAR ECB               @Y17XAUU
*                                                              @ZM46755
*        SET LGBNAME IN PARM LIST                              @ZM46755
         L     R8,TRMDESTQ-D1          QCB ADDR                @ZM46755
         USING IEDQQCB,R8                                      @ZM46755
         L     R8,QCBLGBAD-D1          LGB ADDR                @ZM46755
         DROP  R8                                              @ZM46755
         USING IEDNLGB,R8                                      @ZM46755
         MVC   TOTOLADR(D8),LGBNAME    GROUP NAME TO PLIST     @ZM46755
         DROP  R8                                              @ZM46755
         LA    R1,SLPL                 POINT TO PLIST          @Y17XAUU
         AQCTL                         ISSUE STOPLINE          @Y17XAUU
         WAIT  ECB=TOTOTECB            WAIT FOR COMPLETION     @Y17XAUU
         CLI   TOTORTCD,GOOD           GOOD RETURN             @ZM46755
         BE    D0(R12)                 YES, CHECK NEXT ENTRY   @ZM46755
         SPACE
LURLSE   DS    0H                      BOUNDARY ALLIGNMENT     @Y17XAUU
         MVC   DEVSTOP,TOTOLADR        MOVE NAME TO MESSAGE    @Y17XAUU
*        STOP FAILED FOR TEST DEVICE                           @Y17XAUU
         IEDQMSG FUNCT=CEC,OTBUF=STOPFAIL,OTCNT=55             @Y17XAUU
         L     R1,TTEADDR              GET LINE TTE ADDRESS    @Y17XAUU
         LA    R8,TRMPRFSZ             GET TTE PREFIX SIZE     @Y17XAUU
         SLR   R1,R8                   POINT TO TTE PREFIX     @Y17XAUU
         USING IEDNTRM,R1              SET TTE ADDRESSABILITY  @Y17XAUU
         LH    R1,TRMSUBST             GET SUBSTRUCT POINTER   @Y17XAUU
         LTR   R1,R1                   ANY TERMINALS ON LINE   @Y17XAUU
         BZ    LURLSEND                NO - ISSUE RELEASE      @Y17XAUU
LULOOP1  DS    0H                      BOUNDARY ALLIGNMENT     @Y17XAUU
         L     R15,TERNMTAB            GET TTE CONVERSION RTNA @Y17XAUU
         BALR  R14,R15                 GO FIND TTE ADDRESS     @Y17XAUU
         SLR   R1,R8                   POINT TO TTE PREFIX     @Y17XAUU
         L     R14,TRMBYTE2            WORD CONTAINS TRMSTOTE  @G36XRUV
CSLOOP1  DS    0H                                              @G36XRUV
         LR    R15,R14                 COPY                    @G36XRUV
         N     R15,OFFSTOTE            TURN OF TOTE BIT IN REG @G36XRUV
         CS    R14,R15,TRMBYTE2        RESET TRMSTOTE          @G36XRUV
         BNZ   CSLOOP1                 BR IF WORD ALTERED      @G36XRUV
         CLI   TRMTYPE,TRMSDLC         END OF CHAIN            @Y17XAUU
         BE    LURLSEND                YES - ISSUE RELEASE     @Y17XAUU
         LH    R1,TRMCOHRT             GET COHORT POINTER      @Y17XAUU
         B     LULOOP1                 CHECK NEXT ENTRY        @Y17XAUU
         DROP  R1                                              @Y17XAUU
LURLSEND DS    0H                      BOUNDARY ALLIGNMENT     @Y17XAUU
         MVI   TOTOVBCD,RELEASE        SET RELEASE VERB CODE   @Y17XAUU
         XC    TOTORTCD(D1),TOTORTCD   CLEAR RETURN CODE       @Y17XAUU
         XC    TOTOLINK(D3),TOTOLINK   CLEAR LINK FIELD        @Y17XAUU
         XC    TOTOTECB(D4),TOTOTECB   CLEAR ECB FOR WAIT      @Y17XAUU
         LA    R1,SLPL                 POINT TO PLIST          @Y17XAUU
         AQCTL                         POST REQUEST            @Y17XAUU
         WAIT  ECB=TOTOTECB            WAIT FOR COMPLETION     @Y17XAUU
         SPACE
         B     CANCELL1                CANCEL TRM              @Y17XAUU
         EJECT
         DS    0F
TERNMTAB DS    A                       SAVE AREA               @Y17XAUU
EIGHTSAV DC    AL4(0)                  SAVE REG EIGHT            S99528
ENQLIST  ENQ   (QNAME,RNAME,E,8,STEP),RET=USE,MF=L
ENQLIST1 ENQ   (QNAME,RNAME,S,8,STEP),RET=USE,MF=L
QNAME    DC    CL8'SYSZTOTE'           MAJOR NAME              @Y17XAUU
RNAME    DC    8CL1' '                 MINOR NAME                S99528
***
***                                    PL FOR SVC 102
SLPL     DS    0F                                                S99528
ELEM1    DS    F                       POINTER TO 1ST ELEMENT    S22024
ELEM2    DS    F                       POINTER TO LAST ELEMENT   S22024
OFFSTOTE DS    0F                      TURNS OFF TRMSTOTE W/C&S@G36XRUV
         DC    XL1'FF'                                         @G36XRUV
         DC    AL1(255-TRMSTOTE)                               @G36XRUV
         DC    XL2'FFFF'                                       @G36XRUV
         DS    0D                                                S99528
CDSNAME  DC    CL8'G0000000'           CDSNAME                   S99528
DCBADDS  DC    A(0)                    DCB SAVE AREA             S99528
NOOFEXTS DC    A(0)                    NUMBER EXTENTS SAVE AREA  S99528
RESTORE  DC    X'00'                   FLAG                      S99528
NFIVESIX EQU   X'80'                   DO NOT RESTORE            S99528
CLEARFLG EQU   X'00'                   CLEAR                     S99528
OLT      DCB   DDNAME=OLTLIB,DSORG=PO,MACRF=(R,W),LRECL=256,RECFM=U,   X
               BLKSIZE=256                                       S99528
         DS    0F                                                S99528
CDSLIB   DC    CL8'CDSLIB  '           DDNAME                    S99528
TNTNAME  DC    CL8'        '           TNT NAME                  S99528
TNTMOVE  MVC   0(0,R8),0(R6)           MOVE TNT NAME             S99528
         DS    0F                                                S99528
TTEADDR  DC    A(0)                    TTE ADDRESS               S99528
TRMSAVE2 DS    F                       REGISTER SAVE AREA      @Y17XAUU
RNFLAG   DC    X'00'                   FLAG BYTE                 S22024
SSCPTCIN DS    H                       SSCP TTCIN SAVE AREA    @Y17XAUU
ONE      DC    H'1'                    CONSTANT 1                S22024
OFFSET   DS    H                       TERMNAME TABLE OFFSET     S22024
RSIDSAVE DS    H                       RSID SAVE AREA            S22024
WORKAREA DS    2F                      REGISTER SAVE AREA        S22024
A270XSW  WTOR  'IED148D IS C. U. FOR DEVICE XXXXXXXX CONNECTED ONLY TO *
               THIS SYSTEM?',ANSWER,3,,MF=L  DEVICE SWITCHED OR  S22024
*                                              SHARED MESSAGE    S22024
ANSWER   DC    X'000000'               YES OR NO                 S99528
PROTDEV  WTOR  'IED328D CAN PROTECTED DEVICE          BE TESTED?',     *
               ANSWER,3,,MF=L                                    S99528
PROTMSG  DC    CL64'IED329I SYSTEM OPERATOR WILL NOT ALLOW TEST ON PROT*
               ECTED DEVICE '          MESSAGE                   S22024
DEVPRONA DC    CL8'        '           NAME FOR MESSAGE
*  MESSAGE ISSUED WHEN BAD RETURN CODE RECEIVED FROM OP CONTROL  S22024
STOPFAIL DC    CL47'IED221I TRM CANCELLED - STOP FAILED FOR DEVICE '
DEVSTOP  DC    CL8'        '           NAME FOR MESSAGE          S22024
BCKUPFAL EQU   *                                                 Y06330
         DC    C'IED240I TERMINAL'     MESSAGE                   Y06330
TERMINAL DC    C'XXXXXXXX'             TERMINAL NAME             Y06330
         DC    C'CANNOT BE SWITCHED TO'  MESSAGE                 Y06330
BACKUPP  DC    C'BACK UP'              MESSAGE                   Y06330
PRIMARY  DC    C'PRIMARY'              MESSAGE                   Y06330
SAVENT   DS    F                  TNT ADDRESS SAVE AREA        @Y17XAUU
BTUSIZE  DC    H'14'                   SIZE OF BTU               S22024
HDRSIZE  DC    H'31'                   SIZE OF HEADER PAD        S22024
DATASIZE DC    H'512'                  MAXIMUM DATA SIZE         S22024
OLTTCBSZ DC    H'23'                   SIZE OF OLTTCB            S22024
INITSTAT DS    F                       TEST DEVICE TABLE ENTRY  SA41599
*                                        SAVE AREA (INITIAL     SA41599
*                                        STATUS IN FIRST BYTE)  SA41599
GPOLLREJ DC    C'IED278I TRM CANCELLED - TEST DEVICE IS BEING GENERAL PC
               OLLED'                  MESSAGE                 @SA69637
         EJECT
         TDCTD                                                 @Y17XAUU
         EJECT
         OLTCB                         TOTE CONTROL BLOCK        S22024
         EJECT
         TLGBD                         LINE GROUP CONTROL BLOCK  S22024
*
DCHBNAME EQU   7
         EJECT
UCB      DSECT
         IEFUCBOB
         EJECT
         TAVTD
         EJECT
         TTNTD
         EJECT
         TTRMD
         EJECT
         TPRFD
         EJECT
         TLCBD
         EJECT
DCBDSECT DCBD  DSORG=TX
         EJECT
CVT      DSECT
         CVT
         EJECT
         TQCBD
         EJECT
         TDEBD
         EJECT
         TSIBD                                                 @Y17XAUU
         END
