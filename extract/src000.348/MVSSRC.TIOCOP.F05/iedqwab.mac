         TITLE 'IEDQWAB  $DTIME SERVICE MODULE OF TCAM/TOTE'
IEDQWAB  CSECT
*A000000-999999                                                @Y16X5U0
*CHANGE ACTIVITY AS FOLLOWS:
*A653000-655000,666000-670000,877000-879000                      S05331
*C588000-598000,784000,875000                                    S05331
*D602000                                                         S05331
*A476000,756000,885000                                         @Y17XAUU
**************************************************************** S22024
*                                                              * S22024
* MODULE NAME: IEDQWAB (TCAM,TOTE)                             @Y17XAUU
*                                                              * S99528
* DESCRIPTIVE NAME:                                            * S22024
*    $DTIME SERVICE MODULE OF TCAM/TOTE                        * S22024
*                                                              * S22024
* COPYRIGHT:                                                   * S22024
*    'NONE'                                                    * S22024
*                                                              * S22024
* STATUS: VERSION 10.0                                         @Y17XAUU
*                                                              * S99528
* FUNCTIONS:                                                   * S99528
*              THIS MACRO RETURNS THE TIME OF DAY, IN PACKED   * S99528
*              DECIMAL, TO THE UNIT TEST IN REGISTER 1.        * S99528
*                                                              * S99528
* REGISTER 1 CONTENTS:                                         * S99528
*                                                              * S99528
*              BYTE 0  1  2  3                                 * S99528
*                   HH MM SS TH                                * S99528
*                                                              * S99528
*              HH  HOURS, IN 24 HOUR CLOCK SYSTEM              * S99528
*              MM  MINUTES                                     * S99528
*              SS  SECONDS                                     * S99528
*              T   TENTHS OF SECONDS                           * S99528
*              H   HUNDREDTHS OF SECONDS                       * S99528
*                                                              * S99528
* ENTRY POINT: IEDQWAB                                         * S99528
*                                                              * S99528
*              IEDQWAB - CALLED WHEN A $DTIME MACRO IS ISSUED  * S99528
*                        BY A UNIT TEST.                       * S99528
*                                                              * S99528
* INPUT:       REG 1 = $DTIME PLIST                            @Y17XAUU
*              REG 2 = OLTCB ADDRESS                           @Y17XAUU
*              REG 13 = CALLER SAVE AREA ADDRESS               @Y17XAUU
*              REG 14 = RETURN ADDRESS                         @Y17XAUU
*              REG 15 = IEDQWAB ENTRY ADDRESS                  @Y17XAUU
*                                                              * S99528
* OUTPUT:      REGISTER 1 CONTAINS THE TIME OF DAY IN PACKED   * S99528
*              DECIMAL.                                        * S99528
*                                                              * S99528
* EXTERNAL ROUTINE:   NONE                                     * S99528
*                                                              * S99528
* EXIT POINTS (NORMAL) :                                       * S99528
*               BR 14 AT 'DTIM002'                             @Y17XAU2
*                                                              * S99528
* EXIT POINTS (ERROR) :                                        * S99528
*    NONE                                                      * S99528
*                                                              * S99528
* TABLES/WORK AREAS:  NONE                                     * S99528
*                                                              * S99528
* ATTRIBUTES:                                                  * S99528
*              ENABLED, PROBLEM PROGRAM MODE, RESIDENT,        @Y17XAU2
*              RE-ENTRANT                                      @Y17XAU2
*                                                              * S99528
* NOTES = SEE BELOW                                            @Y17XAUU
*                                                              * S99528
*  DEPENDENCIES - EBCDIC CHARACTER CODE                        @Y17XAUU
*                                                              @Y17XAUU
*  REGISTERS CONVENTION - SEE REGISTER ASSIGNMENT              @Y17XAUU
*                                                              @Y17XAUU
*  PATCH LABEL - PATCH                                         @Y17XAUU
*                                                              @Y17XAUU
*  RESTRICTIONS - NONE                                         @Y17XAUU
*                                                              @Y17XAUU
* NOTES:       NONE                                            * S99528
*                                                              * S99528
**************************************************************** S99528
         EJECT
**************************************************************** S22024
*                                                                S99528
*        EQUATES TO ALLOW SYMBOLIC NAMING OF GENERAL REGISTERS   S99528
*                                                                S99528
*                                                                S99528
**************************************************************** S22024
         SPACE 1
R0       EQU   0                   R                             S99528
R1       EQU   1                    E                            S99528
R2       EQU   2                     G                           S99528
R3       EQU   3                      I                          S99528
R4       EQU   4                       S                         S99528
R5       EQU   5                        T                        S99528
R6       EQU   6                         E                       S99528
R7       EQU   7                          R                      S99528
R8       EQU   8                           S                     S99528
R9       EQU   9                   E                             S99528
R10      EQU   10                   Q                            S99528
R11      EQU   11                    U                           S99528
R12      EQU   12                     A                          S99528
R13      EQU   13                      T                         S99528
R14      EQU   14                       E                        S99528
R15      EQU   15                        D                       S99528
DISREG2  EQU   28                  DISPLACEMENT TO REG 2       @Y17XAUU
         EJECT
         USING IEDQWAB,R4                                        S99528
         USING TOTOLTCB,R2                                       S22024
         SAVE  (14,12)            SAVE CALLER'S REGISTERS        S22024
         LR    R4,R15             LOAD BASE REGISTER             S22024
IEDQWAB  IEDHJN DTIM000,HJN                                      S99528
         LA    R12,TOTSAVE1        PICK UP SAVE AREA ADDRESS     S99528
         ST    R12,OFF8(R13)       SAVE IT IN CALLERS SAVE AREA  S99528
         ST    R13,TOTSAVE1+OFF4   SAVE CALLERS SAVE REG         S99528
         LR    R13,R12            SET UP SAVE AREA REG           S99528
         CLI   1(R1),X'02'         LEVEL 2 MACRO ID ?            S99528
         BE    DTIM001             YES                           S99528
*                                                                S99528
         LA    R15,OFF4           SET RETURN CODE TO 4           S05331
         B     DTEXIT             BRANCH TO EXIT ROUTINE         S05331
DTIM001  EQU   *                                                 S99528
*                                                                S99528
         TIME  DEC                 GET TIME AND DATE             S99528
         ST    R0,DTIM003          SAVE TIME                     S99528
*                                                                S99528
*                                                                S99528
         LR    R1,R0               PUT TIME IN REG 1             S99528
         SR    R15,R15             CLEAR RETURN CODE REGISTER    S99528
         XC    TOTRTCOD,TOTRTCOD   ZERO RETURN CODE              S99528
DTEXIT   EQU   *                                                 S05331
         STC   R15,TOTRTCOD       PUT RETURN CODE IN OLTCB       S05331
         TM    $OLTFLGS,$TRACE     IS TRACE REQUESTED            S99528
         BO    DTIM002A            YES GO TO TRACE MODULE        S99528
         TM    $RETMASK,XFF       ANY RETURN CODES SET BY OLT?   S05331
         BZ    DTNORM             NO, RETURN TO CALLER           S05331
         CLI   TOTRTCOD,ZERO      IS RETURN CODE ZERO            S05331
         BNE   DTIM002A           NO, GO TO IEDQWM2              S05331
DTNORM   EQU   *                                                 S05331
         L     R13,OFF4(R13)       RESTORE CALLERS SAVE REG      S99528
         L     R14,OFF12(R13)      RESTORE R14                   S99528
         MVC   OFF24(LEN4,R13),DTIM003  PUT TIME IN REG 1        S99528
*                                        OF CALLER               S99528
         LM    R0,R12,20(R13)      RESTORE CALLERS REGISTERS     S99528
DTIM002  BR    R14                 RETURN                        S99528
*                                                                S99528
*                                                                S99528
DTIM002A DS    0H                                                S99528
         L     R13,OFF4(R13)       RESTORE CALLERS SAVE REG      S99528
         L     R14,OFF12(R13)      RESTORE R14                   S99528
         MVC   OFF24(LEN4,R13),DTIM003  PUT TIME IN REG 1        S99528
*                                        OF CALLER               S99528
         USING RESPL,R5            SET UP ADDRESSBILTY OF RESPL@Y17XAU2
         L     R5,TOTRESPL         FIND START OF RESPL         @Y17XAU2
         L     R15,RESPLM2         GET ENTRY POINT OF IEDQWM2  @Y17XAU2
         LM    R2,R12,DISREG2(R13) RESTORE REG                 @Y17XAUU
         BR    R15                 GO TO IEDQWM2               @Y17XAU2
         DROP  R5                                              @Y17XAU2
         EJECT                                                   S99528
PATCH    DC    100C' '             PATCH AREA                    S05331
         EJECT
**************************************************************** S99528
*                                                              * S99528
*        OFFSETS   AND   EQUATES                               * S99528
*                                                              * S99528
*                                                              * S99528
**************************************************************** S99528
OFF4     EQU   4                   OFFSET TO POINT TO 2ND WORD   S99528
OFF8     EQU   8                   OFFSET TO POINT TO 3RD WORD   S99528
OFF12    EQU   12                  OFFSET TO POINT TO 4TH WORD   S99528
OFF24    EQU   24                  OFFSET TO POINT TO 6TH WORD   S99528
LEN4     EQU   4                   LENGTH FOR FULLWORD MOVE      S99528
XFF      EQU   X'FF'               USED TO COMPARE FOR ANY BITS  S05331
*                                    ON                          S05331
ZERO     EQU   0                   CONSTANT VALUE 0              S05331
         EJECT                                                 @Y17XAU2
RESPL    RESPL                                                 @Y17XAU2
         EJECT                                                 @Y17XAU2
         OLTCB                                                   S22024
DTIM003  DS    F .                 SAVE AREA                   @Y17XAUU
         END
