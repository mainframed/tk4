         TITLE 'IEDQWC2 TRM ANALYSIS MODULE 3'
IEDQWC2  CSECT
* CHANGE ACTIVITY AS FOLLOWS:
*A836000                                                        SA41597
*A139200-139400,150800-151400,424900-425160,431280-432000        S22024
*A432490,432910-432960,730010-730040,743000,770460-771120        S22024
*A833100-833700,836700,856393-856396,884060-885740               S22024
*A908550,909106-909107,909195                                    S22024
*C004000,009000,712000-725000,744000                             S22024
*C813000,814000-815000,820000,832000,854040-854060               S22024
*D670000,726000,731350,735700,737700,812100-812500               S22024
**************************************************************** S22024
*                                                              *
* TITLE = TRM ANALYSIS MODULE 3                                *
*                                                              *
* MODULE NAME = IEDQWC2                                        *
*                                                              *
* DESCRIPTIVE NAME = TRM ANALYSIS MODULE 3                     *
*                                                              *
* COPYRIGHT = NONE                                             *
*                                                              *
* STATUS --                                                    *
*                                                              *
*    CHANGE LEVEL 2                                            * S22024
*                                                              *
*                                                              *
* FUNCTIONS --                                                 *
*                                                              *
*    THE PURPOSE OF THIS MODULE IS TO VERIFY THE TEST AND      *
*    OPTION FIELDS OF THE TEST REQUEST MESSAGE (TRM).          *
*                                                              *
*    THE TEST FIELD IS VERIFIED FOR VALID SYNTAX. IF AN        *
*    ERROR IS FOUND, CONTROL IS PASSED TO THE PROMPTER         *
*    MODULE (IEDQWJ).                                          *
*                                                              *
*    THE OPTION FIELD IS VERIFIED FOR VALID SYNTAX AND         *
*    REQUESTED OPTIONS ARE SET IN THE OLTCB. IF AN ERROR       *
*    IS FOUND IN THE OPTION FIELD, CONTROL IS PASSED TO        *
*    THE PROMPTER MODULE (IEDQWJ).                             *
*                                                              *
*    THIS MODULE ALSO CHECKS THE OPTION FIELD TO SEE IF        *
*    NCM WAS SPECIFIED. IF NCM WAS SPECIFIED THE MODULE        *
*    ASKS THE SYSTEM OPERATOR FOR PERMISSION TO USE THE        *
*    LINE(S) IN NCM MODE. IF PERMISSION IS NOT GRANTED, THE    *
*    CONTROL TERMINAL IS NOTIFIED AND THE TRM REJECTED.        *
*                                                              *
*                                                              *
* ENTRY POINTS --                                              *
*                                                              *
*    IEDQWC2 - CALLED BY IEDQWC1 TO ANALYZE THE TEST AND       *
*    OPTION FIELDS.                                            *
*                                                              *
*                                                              *
* INPUT --                                                     *
*                                                              *
*    REGISTERS 02,13,14,15 CONTAIN THE FOLLOWING VALUES:       *
*                                                              *
*    02 - OLTCB POINTER                                        *
*    13 - CALLER'S SAVE AREA ADDRESS                           *
*    14 - RETURN ADDRESS                                       *
*    15 - ENTRY POINT ADDRESS
*                                                              *
*                                                              *
* OUTPUT --                                                    *
*                                                              *
*    NONE                                                      *
*                                                              *
*                                                              *
* EXTERNAL REFERENCES --                                       *
*                                                              *
*    LINK TO IEDQWK (MESSAGE MODULE)                           *
*                                                              *
*                                                              *
* EXITS,NORMAL --                                              *
*                                                              *
*    XCTL TO IEDQWE (TEST CONTROL MODULE) WHENEVER             *
*    NCM IS REJECTED                                           *
*                                                              *
*                                                              *
* EXITS,ERROR --                                               *
*                                                              *
*    XCTL TO IEDQWJ (PROMPTER MODULE)                          *
*                                                              *
*                                                              *
* TABLES/WORK AREAS --                                         *
*                                                              *
*    UCB,DEB,DCB,QCB,OLTCB                                     *
*                                                              *
*                                                              *
* ATTRIBUTES --                                                *
*                                                              *
*    ENABLED, PROBLEM PROGRAM MODE, TRANSIENT                  *
*                                                              *
*                                                              *
* NOTES --                                                     *
*                                                              *
*    NONE                                                      *
*                                                              *
***********************************************************************
         EJECT
*
*              INITIALIZATION
*
*
D0       EQU   0                  DISP OF 0
D1       EQU   1                  DISP OF 1
D2       EQU   2                  DISP OF 2
D3       EQU   3                  DISP OF 3
D4       EQU   4                  DISP OF 4
D8       EQU   8                  DISP OF 8
D12      EQU   12                 DISP OF 12
D16      EQU   16                 DISP OF 16
D17      EQU   17                 DISP OF 17
D20      EQU   20                 DISP OF 20
D24      EQU   24                 DISP OF 24
D40      EQU   40                 DISP OF 40                     S22024
TRMEND   EQU   72                 MAXIMUM LENGTH OF TRM
BLANK    EQU   X'40'              A BLANK
RTNSFLG  EQU   X'80'              END OF TEST FIELD
RTNDFLG  EQU   X'40'              INCLUSIVE ROUTINES
RTNCFLG  EQU   X'20'              SEPARATE ROUTINE ENTRY
RTNNFLG  EQU   X'10'              SECOND PASS FLAG
RTNIFLG  EQU   X'08'              INCLUSIVE RTN LOOP FLAG
SLASH    EQU   X'61'              SLASH
COMMA    EQU   X'6B'              COMMA
DASH     EQU   X'60'              DASH
F0       EQU   X'F0'              EBCDIC 0
F9       EQU   X'F9'              EBCDIC 9
INCLUS   EQU   X'80'              INCLUSIVE ROUTINES
PARMREG0 EQU   0                  REG 0
PARMREG1 EQU   1                  REG 1
R0       EQU   0                  REG 0
R1       EQU   1                  REG 1
R2       EQU   2                  REG 2
R3       EQU   3                  REG 3
R4       EQU   4                  REG 4
R5       EQU   5                  REG 5
R6       EQU   6                  REG 6
R7       EQU   7                  REG 7
R8       EQU   8                  REG 8
R9       EQU   9                  REG 9
R10      EQU   10                 REG 10
R11      EQU   11                 REG 11
R12      EQU   12                 REG 12
R13      EQU   13                 REG 13
R14      EQU   14                 REG 14
R15      EQU   15                 REG 15
XFF      EQU   X'FF'              MASK FIELD
EQUAL    EQU   X'7E'              = CHAR
END      EQU   X'80'              CONTROL BIT
LEV0     EQU   X'F0'              EP OPTION LEVEL 0              S22024
LEV3     EQU   X'F3'              EP OPTION LEVEL 3              S22024
D7       EQU   7                  DISP OF 7
D29      EQU   29                 DISP OF 29
D33      EQU   33                 DISP OF 33
D5       EQU   5                  DISP OF 5
D9       EQU   9                  DISP OF 9
TRMLEN   EQU   72                 TRM BUFFER SIZE
OLTPRFX  EQU   C'T'               TEST SECTION PREFIX (DEFAULT)
KA       EQU   C'A'               CHAR A
KZ       EQU   C'Z'               CHAR Z
X0F      EQU   X'0F'              MASK FIELD
TLDF     EQU   X'02'              TL DEFAULT COUNT
ELDF     EQU   X'02'              EL DEFAULT COUNT
F5       EQU   X'F5'              VALUE OF 5
EXTLEN   EQU   54                 EXT BFR SIZE
TDMAX    EQU   11                 MAX + 1 TEST DEVICE COUNT      S22024
YES      EQU   X'E8'              CHAR Y
TYPE1CA  EQU   X'15'              UCB ID 3705 TYPE 1 CA          S22024
TYPE2CA  EQU   X'25'              UCB ID OF 3705 TYPE 2 CA       S22024
         SPACE 2
         USING IEDQWC2,R15
         USING TOTOLTCB,R2
IEDQWC2  IEDHJN STARC2,HJN
         DROP  R15
         SPACE
         SAVE  (14,12)            SAVE CALLER'S REG.
         LR    R6,R15                   LOAD BASE
         USING IEDQWC2,R6
         LA    R3,TOTSAVE4        GET MY SAVE AREA ADDR
         ST    R3,D8(R13)         SAVE IN CALLER'S SAVE
         ST    R13,D4(R3)         SAVE CALLER'S
         LR    R13,R3                   SET R13 TO SAVE AREA
         SPACE
         MVI   TOTCROLT,BLANK     BLANK CURRENT
         MVC   TOTCROLT+D1(D7),TOTCROLT OLT DEVICE FIELD
         MVI   TOTOLTTB,BLANK     BLANK OLT
         MVC   TOTOLTTB+D1(D29),TOTOLTTB SUFFIX TABLE
*
*
*              FIND THE TESTS FIELD
*
*
         LA    R3,TOTTRMBF        POINT TO TRM BUFFER
         MVC   TRMWRK(TRMLEN),D0(R3) TRM TO WORK AREA
         LA    R1,TRMWRK               SET UP POINTER
         LA    R5,TRMWRK+TRMEND   POINT TO END OF TRM BUFFER
         LA    R10,D2             SET UP COUNTER
FLDHUNT  EQU   *
         CLI   D0(R1),SLASH       END OF FIELD
         BE    FLDFIND                 YES BRANCH
         LA    R1,D1(R1)          BUMP POINTER
         CR    R1,R5              END OF BUFFER ?
         BE    TEST1              YES - ERROR
         B     FLDHUNT                 GO LOOK AGAIN
FLDFIND  EQU   *
         LA    R1,D1(R1)          BUMP PASS SLASH
         BCT   R10,FLDHUNT        CHECK FOR SLASH AGAIN
TESFLD   EQU   *
*                                      R1 HAS ADDR OF TEST FIELD
         SR    R10,R10            CLEAR REG 10
         LA    R10,TRMLEN         LENGTH OF TRM
         MVI   TOTCROLT,OLTPRFX   INIT TEST PREFIX
         CLI   D0(R1),KA          CHECK FOR ALPHA PREFIX
         BL    TEST1              BR LOW - ERROR
         CLI   D0(R1),KZ          CHECK ALPHA RANGE
         BH    TEST0              BR HIGH - CHECK FOR NUMERIC
         MVC   TOTCROLT(D1),D0(R1) PUT ALPHA IN OLT TABLE
         LA    R1,D1(R1)          INCREMENT POINTER
TEST0    EQU   *
         MVC   WORK1(D4),D0(R1)   MOVE TEST TO WORK AREA
         NC    WORK1(D4),KF0      CLEAR LOW ORDER 4 BYTES
         CLC   WORK1(D4),KF0      ALL NUMERIC?
         BNE   TEST1                   NO BRANCH
TEST2    EQU   *
         MVC   TOTCROLT+D1(D4),D0(R1) MOVE IN NUMERIC ID
         LA    R1,D4(R1)          BUMP PASS DEVICE TYPE
         SPACE
         SR    R4,R4              CLEAR REG 4
         LR    R5,R1              POINT TO TEST SUFFIX IN TRM
         LA    R12,TOTOLTTB       POINT TO START OF OLT SUFFIX TABLE
         SPACE
TEST2E   EQU   *
         CLI   D0(R1),KA          VALUE LESS THAN A
         BL    TEST1              ERROR - NOT AN ALPHA
         CLI   D0(R1),KZ          VALUE GREATER THAN Z
         BH    TEST1              ERROR - NOT AN ALPHA
         SPACE
         BAL   R9,ALPCHK               CHECK FOR VALID ALPHA FIELD
         CLI   D0(R1),COMMA       COMMA DELIMITER
         BNE   TEST4                     NO
         TM    D1(R1),F0          YES - NEXT FIELD NUMERIC
         BNO   TEST4                       NO
         LA    R9,NUMCHK               SET UP FOR RETURN
NUMCHK   EQU   *                       CHECK VALIDITY OF FIELD
         LA    R1,D1(R1)          BUMP PASS DELIMITER
         SR    R10,R10                 CLEAR COUNTER
NUMCHK1  EQU   *
         CLI   D0(R1),F0          CHAR LESS THAN ZERO
         BL    TEST1              YES - ERROR
         CLI   D0(R1),F9          CHAR GREATER THAN NINE ?
         BH    TEST1              YES - ERROR
         B     RTNCHK             VERIFY ROUTINE NUMBER*
TEST4    EQU   *
         LA    R1,D1(R1)          BUMP PASS DELIMITER
         LR    R5,R1              POINT TO ALPHA SUFFIX
         BAL   R9,ALPCHK               CHECK FOR VALID ALPHA FIELD
         B     TEST4                   CONTINUE IN LOOP
ALPCHK   EQU   *
         SR    R10,R10                 CLEAR COUNTER
ALPCHK1  EQU   *
         CLI   D0(R1),SLASH       END OF TEST FIELD
         BE    TEST6                     YES
         CLI   D0(R1),COMMA       COMMA DELIMITER
         BE    TEST7                     YES
         CLI   D0(R1),DASH        HYPHEN DELIMITER
         BE    TEST7                     YES
         CLI   D0(R1),KA          CHAR A OR GREATER
         BL    TEST1                     NO
         CLI   D0(R1),KZ          CHAR Z OR LESS
         BH    TEST1                     NO
         LA    R10,D1(R10)        BUMP COUNTER
         LA    R1,D1(R1)          BUMP CHAR POINTER
         B     ALPCHK1                 CONTINUE IN LOOP
TEST6    EQU   *
         LA    R9,OPTCHK               GET EXIT ADDRESS
TEST7    EQU   *
         CH    R10,H1             ONE OR LESS CHAR
         BL    TEST1                     NO
         CH    R10,H3             THREE OF LESS CHAR
         BH    TEST1                     NO
         BCTR  R10,R0             DECREMENT FOR EX
         EX    R10,ALPSUFX        MOVE CHAR TO SUFFIX TABLE
         LA    R12,D3(R12)        NEXT SLOT IN SUFFIX TABLE
         CLI   D0(R1),DASH        INCLUSIVE TEST
         BNE   TEST7A             NO
         OI    TOTFLG03,TOTTSINC  TURN ON INCLUSIVE FLAG
         LA    R1,D1(R1)          POINT PASS DELIMITER
         LR    R5,R1              POINT TO ALPHA SUFFIX
         SR    R11,R11            CLEAR REG 11
         LA    R12,TOTOLTTB       POINT TO START OF SECTION ID TABLE
TESTAA   EQU   *
         MVC   WORK(D1),D0(R1)    GET NEXT FIELD
         CLI   WORK,KA            CHECK FOR INCLUSIVE ALPHA
         BL    TEST1              BR LOW - ERROR
         CLI   WORK,KZ            CHECK LIMIT
         BH    TEST1              BR HIGH - ERROR
         LA    R7,TOTOLTTB+D33    END OF TABLE
         SPACE
TESTA1   EQU   *
         CR    R12,R7             END OF TABLE?
         BE    TEST1              YES
         CLC   D0(D3,R12),BLK3
         BE    ALPCHK             YES
         LA    R12,D3(R12)        POINT TO NEXT SLOT
         B     TESTA1             CHECK NEXT SLOT
         SPACE
ALPSUFX  MVC   D0(D1,R12),D0(R5)  SECT SUFFIX TO TABLE
         SPACE
TEST7A   EQU   *
         BR    R9                      EXIT
TEST1    EQU   *
         OI    TOTFLG03,TOTTSTER  SET TEST ID ERROR FLAG
TEST1A   EQU   *
         L     R13,D4(R13)        CALLER'S SAVE
         LM    R14,R1,D12(R13)    CALLER'S REG
         XCTL  (2,12),EP=IEDQWJ   ERROR EXIT
*
*
*        CHECK OPTIONS FIELD FOR VALID OPTIONS AND TURN ON
*        OPTION BITS IN OLTCB.  R1 HAS ADDR OF SLASH
*        PRECEEDING THE OPTION FIELD
*
*
OPTCHK   EQU   *
         TM    TOTFLG03,TOTTSINC  INCLUSIVE TEST SECTIONS ?
         BZ    OPTCHK0            NO - CONTINUE
         CLC   TOTOLTTB(D3),TOTOLTTB+D3 A GRT/EQ B ?
         BNL   TEST1              YES - ERROR
OPTCHK0  EQU   *
         TM    TOTFLG03,TOTAPOER   AP OPTION ERROR ?
         BO    OPTERR             YES
         LA    R1,D1(R1)          POINT PASS SLASH
         CLI   D0(R1),SLASH       END OF FIELD
         BE    OPTEND                  YES BRANCH
OPTCHK1  EQU   *
         CLC   D0(D2,R1),TL       TEST LOOP?
         BE    TLOPT              YES BRANCH
         CLC   D0(D3,R1),NTL      NO TEST LOOP?
         BE    NTLOPT             YES BRANCH
         CLC   D0(D2,R1),EL       ERROR LOOP?
         BE    ELOPT              YES BRANCH
         CLC   D0(D3,R1),NEL      NO ERROR LOOP?
         BE    NELOPT             YES BRANCH
         CLC   D0(D2,R1),CP       CONTROL PRINT?
         BE    CPOPT              YES BRANCH
         CLC   D0(D3,R1),NCP      NO CONTROL PRINT?
         BE    NCPOPT             YES BRANCH
         CLC   D0(D3,R1),NMI      NO MANUAL INTERVENTION?
         BE    NMIOPT             YES BRANCH
         CLC   D0(D2,R1),MI       MANUAL INTERVENTION?
         BE    MIOPT              YES BRANCH
         CLC   D0(D2,R1),AP       ALTERNATE PRINTER?
         BE    APOPT              YES BRANCH
         CLC   D0(D3,R1),NAP      NO ALTERNATE PRINTER?
         BE    NAPOPT             YES BRANCH
         CLC   D0(D2,R1),EP       ERROR PRINT?
         BE    EPOPT              YES BRANCH
         CLC   D0(D3,R1),NEP      NO ERROR PRINT?
         BE    NEPOPT             YES BRANCH
         CLC   D0(D2,R1),CM       CONCURRENT MODE?
         BE    CMOPT              YES BRANCH
         CLC   D0(D3,R1),NCM      NON-CONCURRENT MODE?
         BE    NCMOPT             YES BRANCH
         CLC   D0(D3,R1),EXT      EXTERNAL DATA?
         BE    EXTOPT             YES BRANCH
         CLC   D0(D2,R1),BK       BREAK OPTION                   S22024
         BE    BKOPT              YES BRANCH                     S22024
         CLC   D0(D3,R1),NBK      NO BREAK OPTION                S22024
         BE    NBKOPT             YES BRANCH                     S22024
         CLC   D0(D2,R1),TR       TRACE?
         BE    TROPT              YES BRANCH
         CLC   D0(D3,R1),NTR      NO TRACE?
         BE    NTROPT             YES
         CLC   D0(D2,R1),NP       NO PRINT OPTION                S22024
         BE    NPOPT              YES                            S22024
         CLC   D0(D3,R1),TTP      TEST PRIMARY PATH              S22024
         BE    TTPOPT             YES                            S22024
         CLC   D0(D3,R1),TAP      TEST ALTERNATE PATH            S22024
         BE    TAPOPT             YES                            S22024
*
*        OPTION FIELD IN ERROR
*
OPTERR   EQU   *
         OI    TOTFLG03,TOTOPTER  SET OPTION ERROR BIT
         B     TEST1A             EXIT TO PROMPTER
         SPACE
TTPOPT   DS    0H                 BOUNDARY ALLIGNMENT            S22024
         NI    $TOTFLG1,XFF-$TERMSEC TEST PRIMARY PATH           S22024
         LA    R1,D3(R1)          POINT PASS OPTION              S22024
         B     SLCHK              CHECK NEXT ENTRY               S22024
         SPACE
TAPOPT   DS    0H                 BOUNDARY ALLIGNMENT            S22024
         OI    $TOTFLG1,$TERMSEC  TEST ALTERNATE PATH            S22024
         LA    R1,D3(R1)          POINT PASS OPTION              S22024
         B     SLCHK              CHECK NEXT ENTRY               S22024
         SPACE
BKOPT    DS    0H                 BOUNDARY ALLINMENT             S22024
         NI    TOTFLG09,XFF-TOTNOBRK CLEAR NO BREAK FLAG         S22024
         LA    R1,D2(R1)          POINT PASS OPTION              S22024
         B     SLCHK              CHECK NEXT ENTRY               S22024
         SPACE
NBKOPT   DS    0H                 BOUNDARY ALLIGNMENT            S22024
         OI    TOTFLG06,TOTNCMFG  SHOW NON CONCURRENT MODE       S22024
         OI    TOTFLG09,TOTNOBRK  SET NO BREAK FLAG              S22024
         LA    R1,D3(R1)          POINT PASS OPTION              S22024
         B     SLCHK              CHECK NEXT OPTION              S22024
         SPACE
TROPT    EQU   *
         OI    $OLTFLGS,$TRACE    TURN ON TRACE BIT
         LA    R1,D2(R1)          POINT PASS OPTION
         B     SLCHK              CHECK NEXT ENTRY
         EJECT
         SPACE
NTROPT   EQU   *
         NI    $OLTFLGS,XFF-$TRACE TURN OFF TRACE OPT
         LA    R1,D3(R1)          POINT PASS OPTION
         B     SLCHK              CHECK NEXT ENTRY
         SPACE 2
NPOPT    EQU   *                                                 S22024
         OI    TOTFLG03,TOTIMNCP  SHOW NCP OPTION                S22024
         OI    $ERROPT,$NERRPRT   SHOW NEP OPTION                S22024
         LA    R1,D2(R1)          POINT PASS OPTION              S22024
         B     SLCHK              CHECK NEXT ENTRY               S22024
         SPACE 2
TLOPT    EQU   *
         OI    $ERROPT,$LOOPTST   TURN ON TL FLAG
         LA    R10,D0             SET COUNTER
         LA    R1,2(R1)                POINT PAST TL
         LR    R3,R1                   SAVE POINTER
TLOPT3   EQU   *
         CLI   D0(R1),COMMA       END OF ENTRY
         BE    TLOPT1                  YES BRANCH
         CLI   D0(R1),SLASH       END OF FIELD
         BE    TLOPT2                  YES BRANCH
         LA    R1,D1(R1)          BUMP POINTER
         LA    R10,D1(R10)        BUMP COUNTER
         B     TLOPT3                  GO LOOK AGAIN
TLOPT1   EQU   *
TLOPT1A  LTR   R10,R10                   ANY NUMBERS GIVEN?
         BZ    TLOPT4                  NO BRANCH
         CLI   D0(R3),F0          1ST NUMERIC ZERO
         BNE   TRM202                    NO
         TM    D1(R3),F0          YES - IS LOOP COUNT ZERO
         BNO   OPTERR                      YES: ERROR
TRM202   EQU   *
         XC    WORK1,WORK1
         XC    WORK2,WORK2             CLEAR WORK AREAS
         BCTR  R10,R0             DECREMENT COUNT
         LA    R7,D3              SET COMPARAND
         CR    R10,R7                   MORE THAN 4 DIGITS?
         BH    OPTERR                  YES BRANCH
         EX    R10,MOVE                MOVE NUMERICS TO WORKAREA
         EX    R10,AND                 CLEAR NUMERICS
         EX    R10,COMPARE             ALL VALID NUMERICS?
         BNE   OPTERR                    NO
         LA    R7,D7              SET COUNT
         SR    R7,R10                   SUBTRACT NO. OF DIGITS
         LA    R8,WORK2(R7)            SET UP REG FOR MOVE
         EX    R10,MOVETL              MOVE LOOP COUNT TO WORK AREA
         PACK  WORK1+D5(D3),WORK2+D4(D4) PACK NUMBER
         OI    WORK1+D7,X0F       MAKE SURE NUMBER IS +
         SR    R10,R10
         CVB   R10,WORK1
         STH   R10,TOTTLCNT       PUT TL COUNT IN OLTCB
TLOPT5   TM    TRMFLG,END              END OF FIELD?
         BO    OPTEND                  YES BRANCH
         LA    R1,D1(R1)          POINT PASS DELIMITER
         B     OPTCHK1            GO EXAMINE THE NEXT OPTION FIELD
TLOPT2   EQU   *
         OI    TRMFLG,END              TURN ON END FLAG
         B     TLOPT1A            GO SEE IF TEST LOOP COUNT SPECIFIED
TLOPT4   EQU   *
         LA    R10,TLDF
         STH   R10,TOTTLCNT       PUT TL DEFAULT COUNT IN OLTCB
         B     TLOPT5             CHECK FOR NEXT OPTION
         SPACE 2
NTLOPT   EQU   *
         NI    $ERROPT,X'FF'-$LOOPTST  TURN OFF TL FLAG
         LA    R1,D3(R1)          POINT PAST OPTION
         B     SLCHK              GO CHECK FOR NEXT OPTION FIELD
         SPACE 2
MOVETL   MVC   D0(D0,R8),D0(R3)   MOVE TL OPTION COUNT
MOVE     MVC   TOTWKSPC(D0),D0(R3) MOVE TL OPT COUNT
AND      NC    TOTWKSPC(D0),MASK0 MASK OUT UNWANTED BIT
COMPARE  CLC   TOTWKSPC(D0),MASK0 NUMERIC
         SPACE 2
NELOPT   EQU   *
         NI    $ERROPT,XFF-$LOOPERR TURN OFF ERROR LOOP FLAG
         LA    R1,D3(R1)          POINT PASS OPTION
         B     SLCHK              CHECK FOR NEXT OPTION
NCMOPT   EQU   *
         OI    TOTFLG06,TOTNCMFG  SET NON-CONCURRENT MODE FLAG
         LA    R1,D3(R1)          POINT PASS NCM
         B     SLCHK              CHECK NEXT OPTION
         SPACE
NAPOPT   EQU   *
         NI    $ERROPT,XFF-$ALTPRNT TURN OFF AP OPTION FLAG
         LA    R1,3(R1)                 POINT PASS NAP
         SPACE 2
SLCHK    EQU   *
         CLI   D0(R1),SLASH       END OF FIELD
         BE    OPTEND                  YES BRANCH
         CLI   D0(R1),COMMA       END OF ENTRY
         BNE   OPTERR                  OPTION FIELD ERROR
         LA    R1,D1(R1)          POINT PASS COMMA
         B     OPTCHK1            CHECK NEXT OPTION
         SPACE 2
ELOPT    EQU   *
         OI    $ERROPT,$LOOPERR   TURN ON LOOP ON ERROR FLAG
         LA    R10,D0             FIND IF NUMBER GIVEN
         LA    R1,D2(R1)          POINT PASS EL
         LR    R3,R1                   SAVE POINTER
ELOPT3   EQU   *
         CLI   D0(R1),COMMA       END OF ENTRY
         BE    ELOPT1                  YES BRANCH
         CLI   D0(R1),SLASH       END OF FIELD
         BE    ELOPT2                  YES BRANCH
         LA    R10,D1(R10)        BUMP COUNTER
         LA    R1,D1(R1)          BUMP POINTER
         B     ELOPT3                  GO LOOK AGAIN
ELOPT1   EQU   *
ELOPT1A  LTR   R10,R10                   ANY NUMBERS GIVEN?
         BZ    ELOPT4                  NO BRANCH
         CLI   D0(R3),F0          1ST NUMERIC ZERO
         BNE   TRM204                    NO
         TM    D1(R3),F0          YES - AND SECOND
         BNO   OPTERR                      LOOP COUNT IS 0
TRM204   EQU   *
         XC    WORK1,WORK1
         XC    WORK2,WORK2             CLEAR WORK AREAS
         BCTR  R10,R0             DECREMENT COUNT BY ONE
         LA    R7,D3              SET COMPARAND
         CR    R10,R7                   GT 4?
         BH    OPTERR                  YES BRANCH
         EX    R10,MOVE                MOVE LOOP COUNT TO WORKAREA
         EX    R10,AND                 CLEAR NUMERICS
         EX    R10,COMPARE             ALL VALID NUMERICS?
         BNE   OPTERR                    NO: GO TO PROMPTER
         LA    R7,D7              SET COUNT
         SR    R7,R10                   SUBTRACT NO OF DIGITS
         LA    R8,WORK2(R7)            SET UP REG FOR MOVE
         EX    R10,MOVETL              MOVE LOOP COUNT TO WORK AREA
         PACK  WORK1+D5(D3),WORK2+D4(D4) PACK THE NUMBER
         OI    WORK1+D7,X0F       MAKE SURE NUMBER IS +
         SR    R10,R10
         CVB   R10,WORK1                CONVERT NUMBER
         STH   R10,$ERRLPCT       SET LOOP ON ERROR COUNT
ELOPT5   TM    TRMFLG,END              END OF FIELD
         BO    OPTEND                  YES BRANCH
         LA    R1,D1(R1)          BUMP PAST DELIMITER
         B     OPTCHK1            GO CHECK FOR NEXT OPTION
ELOPT2   EQU   *
         OI    TRMFLG,END
         B     ELOPT1A                 TURN ON END FLAG AND BRANCH
ELOPT4   EQU   *
         LA    R10,ELDF           SET EL DEFAULT COUNT
         STH   R10,$ERRLPCT       SET LOOP ON ERROR COUNT (DEFAULT)
         B     ELOPT5             CHECK FOR NEXT OPTION
         SPACE 2
         SPACE
NCPOPT   EQU   *
         LA    R1,D3(R1)          BUMP POINTER
         OI    TOTFLG03,TOTIMNCP  SET NCP INTERMEDIATE FLAG
         SPACE 2
         B     SLCHK              CHECK FO NEXT OPTION
NEPOPT   EQU   *
         OI    $ERROPT,$NERRPRT   TURN ON NEP FLAG
         LA    R1,D3(R1)          POINT PASS NEP
         B     SLCHK              CHECK FOR NEXT OPTION
         SPACE 2
APOPT    EQU   *
         LA    R1,D2(R1)          BUMP POINTER
         LA    R9,D9              SET COUNTER
APOPT1   EQU   *
         CLI   D0(R1),SLASH       END OF FIELD
         BE    OPTEND                  YES BRANCH
         CLI   D0(R1),COMMA       END OF ENTRY
         BE    APOPT2                  YES BRANCH
         LA    R1,D1(R1)          BUMP POINTER
         BCT   R5,APOPT1          CHECK FOR END OF AP NAME
         B     OPTERR             ERROR
         SPACE 2
APOPT2   EQU   *
         LA    R1,D1(R1)          BUMP POINTER
         B     OPTCHK1            CHECK NEXT OPTION
         SPACE 2
CPOPT    EQU   *
         NI    TOTFLG03,XFF-TOTIMNCP TURN OFF NCP INTERMED. FLG
         LA    R1,D2(R1)          BUMP POINTER
         B     SLCHK              CHECK FOR NEXT OPTION
         SPACE 2
EPOPT    EQU   *
         LA    R1,2(R1)                BUMP POINTER PAST EP
         CLI   D0(R1),COMMA       DELIMITER                      S22024
         BE    EPOPT4             YES                            S22024
         CLI   D0(R1),SLASH       DELIMITER                      S22024
         BE    EPOPT4             YES                            S22024
         SPACE
         CLI   D0(R1),LEV0        LEVEL LESS THAN 0              S22024
         BL    OPTERR             YES - ERROR                    S22024
         CLI   D0(R1),LEV3        LEVEL GREATER THAN 3           S22024
         BH    OPTERR             YES - ERROR                    S22024
         SPACE
         MVC   WORK1(D1),D0(R1)   LEVEL VALUE TO WORK AREA       S22024
EPOPT2   EQU   *                                                 S22024
         NI    WORK1,X0F          CLEAR UNWANTED BITS            S22024
         MVC   TOTFLG08(D1),WORK1 SET EP LEVEL FOR DPRINT        S22024
         LA    R1,D1(R1)          BUMP POINTER
         B     SLCHK                   YES GO LOOK FOR NEXT OPTION
         SPACE
EPOPT4   EQU   *                                                 S22024
         MVI   WORK1,LEV3         SET DEFAULT LEVEL              S22024
         B     EPOPT2             SET VALUE IN OLTCB             S22024
         SPACE 2
CMOPT    EQU   *
         NI    TOTFLG06,XFF-TOTNCMFG SHOW CONCURRENT MODE
         LA    R1,2(R1)           POINT PASS CM
         B     SLCHK              CHECK FOR NEXT OPTION
         SPACE 2
EXTOPT   EQU   *
         SR    R5,R5              INITIALIZE DATA COUNTER
         CLI   D3(R1),EQUAL       = CHAR DELIMITER
         BNE   OPTERR             NO, ERRROR
         LA    R15,EXTLEN         DATA COUNT LENGTH
         LA    R4,D4(R1)          POINT TO DATA FIELD
EXTOPT1  EQU   *
         CLI   D0(R4),SLASH       / CHAR DELIMITER
         BE    EXTOPT2            YES
         LA    R5,D1(R5)          INCREMENT CHAR COUNTER
         LA    R4,D1(R4)          POINT TO NEXT CHAR
         CR    R5,R15             MAXIMUM NO. OF CHAR ?
         BH    OPTERR             TOO MANY CHAR
         B     EXTOPT1            CHECK NEXT CHAR
         SPACE 2
EXTOPT2  EQU   *
         LTR   R5,R5
         BZ    OPTERR             ERROR
         STC   R5,TOTEXT          MAKE COUNT FIRST CHAR. OF BUFFER
         BCTR  R5,R0              DECREMENT FOR MOVE
         EX    R5,MOVEXT          MOVE EXT DATA TO BFR
         LA    R1,D5(R5,R1)       POINT TO DELIMITER
         B     SLCHK              CHECK NEXT OPTION
         SPACE 2
NMIOPT   EQU   *
         NI    $OLTFLGS,XFF-$MANINTV SHOW NO MI
         LA    R1,D3(R1)          POINT PASS NMI
         B     SLCHK              CHECK NEXT OPTION
         SPACE 2
MIOPT    EQU   *
         OI    $OLTFLGS,$MANINTV  SHOW MI
         LA    R1,D2(R1)          POINT PASS MI
         B     SLCHK              CHECK NEXT OPTION
         SPACE 2
         SPACE 2
OPTEND   EQU   *
         TM    TOTFLG06,TOTNCMFG  IS NCM REQUESTED?
         BZ    TRM259                    NO
         BAL   R9,RNCHK           DETERMINE IF TD ON 3705        S22024
         LA    R7,NCMMSG2         GET START OF DEVICE FLD OF MSG S22024
         LA    R9,TDMAX           MAX TEST DEV COUNT
         LA    R8,TOTTDTBL        GET START OF TEST DEVICE FIELD
         TM    TOTFLG10,TOTTERMS  TERMINAL TEST ?
         BO    TRMTNCM            YES - BRANCH
         MVC   WORK3(D4),D0(R8)   UCB ADDR
         MVI   WORK3,D0           CLEAR FLAG
         B     TRM2506A           GET NEXT ENTRY
TRM2506  EQU   *
         MVC   WORK3(D4),D0(R8)   SAVE ENTRY
         CLC   D0(D4,R8),K0       ANY MORE ENTRIES
         BE    TRM2508                   NO
         MVI   D0(R7),COMMA       SET DELIMITER
         LA    R7,D1(R7)          BUMP PASS COMMA
         TM    TOTFLG10,TOTTERMS  TERMINAL TEST ?
         BO    TRMTNCM            YES - BRANCH
TRM2506A EQU   *
         BCT   R9,TRM2507              END OF TABLE?
         B     TRM2508                   YES
TRM2507  EQU   *
         L     R12,WORK3          GET UCB ADDR
         USING UCB,R12
         MVC   D0(D3,R7),UCBNAME  UCB EBCDIC NAME
         TM    TOTFLG10,TOTTERMS   IS THIS A TERMINAL TEST       S22024
         BO    TRM25072            YES BYPASS THIS MIX TEST      S22024
*                                  NO TEST FOR MIXED 270X / 3705 S22024
         CLI   UCBTBYT4,TYPE1CA    3704/5 TYPE 1 CA              S22024
         BE    TRM25071            YES SET PROPER BIT            S22024
         CLI   UCBTBYT4,TYPE2CA    3704/5 TYPE 2 CA              S22024
         BE    TRM25071            YES SET PROPER BIT            S22024
         OI    TOTFLG02,TOTDEV0X   CHANNEL TEST ON 2701,2,3      S22024
         B     TRM25072            CONTINUE CHECKING TCU'S       S22024
TRM25071 DS    0H                  ALIGN ON HALFWORD             S22024
         OI    TOTFLG02,TOTDEVRN   CHANNEL TEST ON 3704,5        S22024
TRM25072 DS    0H                  ALIGN ON HALFWORD             S22024
         DROP  R12
         LA    R7,D3(R7)          BUMP TO NEXT FIELD
         LA    R8,D4(R8)          POINT TO NEXT UCB ADDR
         B     TRM2506                 GO GET NEXT ENTRY
TRM2508  EQU   *
         TM    TOTFLG02,TOTDEV0X+TOTDEVRN ARE BOTH TCU BITS ON   S22024
         BO    TRM25042                    YES PRINT ERROR       S22024
         TM    TOTFLG02,TOTDEVRN   IS TEST FOR 3704/3705         S22024
         BO    TRM25043            YES ASK FOR PERMISSION        S22024
*                                  NO  ASK TO USE 270X LINES     S22024
         WTO   'IED224D CAN OLT HAVE EXCLUSIVE USE OF CONTROL UNIT ON A*
               DDRESS(ES) ',ROUTCDE=10,DESC=7                    S22024
TRM25041 DS    0H                  ALIGN ON HALFWORD             S22024
         XC    ECB(4),ECB              CLEAR ECB FOR WTOR
WTORNCM  WTOR  '                                                     ',*
               ANSWER,3,ECB,ROUTCDE=(8,10),DESC=2                S22024
         STIMER REAL,NOANS,DINTVL=TIMOUT    SET TIMER FOR TIMEOUT
         WAIT  ECB=ECB                 WAIT FOR REPLY
         TTIMER CANCEL                 CANCEL TIMER
         OI    ANSWER,BLANK       XLATE ANS TO CAP
         CLI   ANSWER,YES         YES
         BE    TRM259                    YES
         IEDQMSG MSGID=051,FUNCT=CEC,LINK=YES NCM REJECTED       S22024
         B     GETOUT             EXIT
TRM25042 DS    0H                  ALIGN ON HALFWORD             S22024
         IEDQMSG MSGID=001,FUNCT=CEC   INVALID MIX OF TCU TYPES  S22024
         B     GETOUT              CLEAN UP AND RETURN           S22024
TRM25043 DS    0H                  ALIGN ON HALFWORD
         WTO   'IED235D CAN OLT HAVE EXCLUSIVE USE OF 3705 ON ADDRESS(E*
               S) ',ROUTCDE=10,DESC=7                            S22024
         B     TRM25041            PRINT REST OF MESSAGE         S22024
TRM259   EQU   *
         L     R13,D4(R13)        CALLER'S SAVE AREA ADDR
         TM    TOTFLG02,TOTCONMD  TD ON CONC                    SA41597
         BZ    PMT260             NO                            SA41597
         LM    R14,R1,D12(R13)    CALLER'S REG                  SA41597
         XCTL  (2,12),EP=IEDQWEC  YES - EXIT TO IEDQWC          SA41597
         SPACE
PMT260   EQU   *                                                SA41597
         LM    R14,R1,D12(R13)    CALLER'S REG
         XCTL  (2,12),EP=IEDQWD   NORMAL EXIT
         EJECT
GETOUT   EQU   *
         XC    TOTTDTBL(D40),TOTTDTBL CLEAR TD TABLE
         OI    TOTFLG06,TOTOTERM  SET TERMINATE FLAG
         L     R13,D4(R13)        CALLER'S SAVE AREA ADDR
         LM    R14,R1,D12(R13)    CALLER'S REG
         XCTL  (2,12),EP=IEDQWE        GO TO IEDQWE
NCMMSG2  EQU   WTORNCM+16         STARTING ADDR FOR CUU ADDR IN  S22024
*                                   MESSAGE                      S22024
         EJECT
TRMTNCM  EQU   *   R8 POINTS TO TEST ENTRY
         STM   R7,R12,TRMSAVE1    SAVE WORK REG
         L     R9,TRMNCMCT        GET  ENTRY COUNTER
         LA    R9,D1(R9)          INCREMENT BY ONE
         CL    R9,TRMTEN          END OF TABLE ?
         BH    TRMNCM1            YES
         ST    R9,TRMNCMCT       SAVE COUNTER
         L     R12,D0(R8)         GET TNT ADDR
         LA    R12,D0(R12)         CLEAR HIGH ORDER BYTE
         LTR   R12,R12            LAST ENTRY ?
         BZ    TRMNCM1            YES - BRANCH
         SR    R10,R10            CLEAR REG
         IC    R10,TOTTTBEL       GET NAME LENGTH
         AR    R12,R10            GET ADDR OF TTE ADDR
         MVC   TTEADDR+D1(D3),D0(R12)   ALLIGN ADDRESS
         L     R11,TTEADDR        GET TTE ADDR
         USING IEDQTRM,R11
         L     R11,TRMDESTQ-D1    GET QCB ADDR
         DROP  R11
         USING IEDQQCB,R11
         SR    R7,R7              CLEAR REG
         IC    R7,QCBRELLN        GET RLN
         SLL   R7,D2              MULT BY 4
         L     R11,QCBDCBAD-D1    GET DCB ADDR
         DROP  R11
         USING IHADCB,R11
         L     R11,DCBDEBAD       GET DEB ADDR
         DROP  R11
         USING DEBNMSUB,R11
         SR    R15,R15            CLEAR REG
         IC    R15,DEBNMEXT       GET # OF EXTENTS
         SLL   R15,D2             MULT BY 4
         L     R11,DEBUCBAD-D4(R7)  GET UCB ADDR
         ST    R11,WORK3          SAVE IT
         LM    R7,R12,TRMSAVE1    RESTORE CALLER'S REG
         B     TRM2507            RETURN
TRMNCM1  EQU   *
         BCTR  R7,R0              DECREMENT COUNT BY 1
         MVI   D0(R7),BLANK       BLANK OUT CHARACTER
         LM    R7,R12,TRMSAVE1    RESTORE CALLER'S REG
         B     TRM2508            RETURN
         EJECT
RTNCHK   EQU   *                  SET ROUTINE MASKS IN SCT
         TM    TOTFLG03,TOTTSINC  INCLUSIVE TEST ?
         BO    TEST1              YES - ERROR
*
         OI    $EXECFLG,$RTNSLCT  SHOW THAT ROUTINES SELECTED
         STM   R7,R12,SAVAREA     SAVE REGISTERS
RTN2     EQU   *
         TM    RTNFLG,RTNSFLG     END OF TEST FIELD ?
         BO    RTNEND             YES
         XC    RTNFLG,RTNFLG      CLEAR FLAG BYTE
         LR    R7,R1              POINT TO FIRST DIGIT
RTN3     EQU   *
         LA    R8,D0              INIT. DIGIT COUNTER
RTN4     EQU   *
         CLI   D0(R7),SLASH       SLASH DELIMITER ?
         BE    RTNSLH             YES
         CLI   D0(R7),DASH        INCLUSIVE TEST ?
         BE    RTNDSH             YES
         CLI   D0(R7),COMMA       COMMA DELIMITER ?
         BE    RTNCMA             YES
         CLI   D0(R7),F0          DIGIT LESS THAN 0 ?
         BL    TEST1              YES - ERROR
         CLI   D0(R7),F9          DIGIT GREATER THAN 9 ?
         BH    TEST1              YES - ERROR
         LA    R7,D1(R7)          POINT TO NEXT CHAR
         LA    R8,D1(R8)          INCREMENT CHAR COUNTER
         B     RTN4               CHECK NEXT CHAR
RTNSLH   EQU   *
         OI    RTNFLG,RTNSFLG     SHOW END OF TEST FIELD
         B     RTN6               EXIT
RTNDSH   EQU   *
         TM    RTNFLG,RTNDFLG     INCLUSIVE ROUTINES ?
         BO    RTN6               YES
         OI    RTNFLG,RTNDFLG     SHOW INCLUSIVE ROUTINES
         B     RTN6               EXIT
RTNCMA   EQU   *
         OI    RTNFLG,RTNCFLG     SHOW SEPARATE ENTRIES
RTN6     EQU   *
         LTR   R8,R8              ANY DIGITS ?
         BZ    TEST1              NO - ERROR
         CL    R8,K3              THREE DIGITS OR LESS           S22024
         BH    TEST1              NO - ERROR                     S22024
         CL    R8,K3              NUMBER OF DIGITS = 3           S22024
         BNE   RTN8               NO - BYPASS MAXIMUM CHECK      S22024
         SR    R7,R8              POINT TO FIRST DIGIT           S22024
         CLC   D0(D3,R7),RTNMAX   RTN NUMBER > MAXIMUM           S22024
         BH    TEST1              YES - RTN # TOO LARGE          S22024
         AR    R7,R8              RESET POINTER                  S22024
RTN8     DS    0H                 BOUNDARY ALLIGNMENT            S22024
         BCTR  R8,R0              DECREMENT COUNT FOR EX         S22024
         EX    R8,PACK            PACK THE NUMBER                S22024
         CVB   R9,DEC             CONVERT IT TO BINARY           S22024
         TM    RTNFLG,RTNNFLG     INCLUSIVE SECOND TEST          S22024
         BO    RTN12              YES
         ST    R9,DIG1            SAVE NUMBER
         TM    RTNFLG,RTNDFLG     INCLUSIVE ROUTINES ?
         BZ    RTN15              NO
         OI    RTNFLG,RTNNFLG     YES - SET SECOND PASS FLAG
         LA    R1,D1(R7)          POINT PASS DELIMITER
         LR    R7,R1              SAVE POINTER
         B     RTN3               GO FIND UPPER LIMIT
RTN12    EQU   *
         ST    R9,DIG2            SAVE UPPER LIMIT
         CLC   DIG1(D4),K256      NUMBER GREATER THAN MAX. ?
         BH    TEST1              YES - ERROR
         CLC   DIG2,K256          NUMBER GREATER THAN MAX. ?
         BH    TEST1              YES - ERROR
         CLC   DIG1(D4),DIG2      1ST NUMBER GRT/EQ TO 2ND NUMBER ?
         BNL   TEST1              YES - ERROR
RTN15    EQU   *                  SET ROUTINE MASK BITS
         L     R9,DIG1            GET ROUTINE NUMBER
         CL    R9,K0              RTN NUMBER EQU ZERO
         BE    TEST1              YES - ERROR
         CLC   DIG1(D4),K16       SET MASK BETWEEN 1 AND 16
         BH    RTN24              NO - CHECK NEXT FIELDS
*        SET MASK FOR ROUTINES 1-16
         LA    R12,$RT0108        POINT TO RTN MASK FIELD
         LA    R11,TESET          POINT TO SET MASK TABLE
RTN17    EQU   *
         S     R9,K8              DECREMENT BY 8
         BNP   RTN19              IF RESULT NEG. , EXIT
         LA    R12,D1(R12)        POINT TO SECOND BYTE
         B     RTN19A             GO SET MASK
RTN19    EQU   *
         L     R9,DIG1            GET RTN NUMBER
RTN19A   EQU   *
         BCTR  R9,R0              DECREMENT FOR MOVE
RTN20    EQU   *
         AR    R9,R11             POINT TO MASK SETTING
         OC    D0(D1,R12),D0(R9)  SET MASK
RTN21    EQU   *
         TM    RTNFLG,RTNIFLG     INCLUSIVE LOOP ?
         BO    RTN22            YES
         LA    R7,D1(R7)          POINT PASS DELIMITER
         LR    R1,R7              RESET TRM PTR
RTN22    EQU   *
         TM    RTNFLG,RTNDFLG     INCLUSIVE ROUTINES
         BZ    RTN2               NO CONTINUE
         OI    RTNFLG,RTNIFLG     SET INCLUSIVE LOOP FLAG
         CLC   DIG1(D4),DIG2      END OF INCLUSIVE SET ?
         BE    RTN2               YES CONTINUE
         L     R9,DIG1            GET FIRST NUMBER
         LA    R9,D1(R9)          INCREMENT BY 1
         ST    R9,DIG1            REPLACE NUMBER
         B     RTN15              EVALUATE THIS ROUTINE NUMBER
RTN24    EQU   *                  SET MASK FOR ROUTINES 17-256
         L     R9,DIG1            GET FIRST NUMBER
         LA    R12,$R025032       POINT TO SECOND MASK FIELD
         LA    R8,D24             INIT COMPARAND
         LA    R11,TESET          POINT TO SET MASK TABLE
RTN28    EQU   *
         CR    R9,R8              IS FLAG IN PRIOR BYTE ?
         BNH   RTN32              YES - EXIT
         LA    R8,D8(R8)          INCREMENT COMPARAND
         LA    R12,D1(R12)        POINT TO NEXT MASK BYTE
         B     RTN28              CHECK NEXT BYTE
RTN32    EQU   *                  MASK FIELD FOUNT
         BCTR  R12,R0             ADJUST TO CORRECT BYTE
         LA    R9,D8(R9)          ADJUST RTN NUMBER
         SR    R9,R8              ADJUST INDEX INTO MASK TABLE
         BCTR  R9,R0              DECREMENT FOR INDEX INTO MASK TABLE
         B     RTN20              GO SET MASK
RTNEND   EQU   *
         BCTR  R1,R0              ADJUST TRM POINTER
         LM    R7,R12,SAVAREA     RESTORE REG.
         B     OPTCHK             CHECK OPTION FIELD
         EJECT
NOANS    EQU   *
         STM   R14,R12,D12(R13)   SAVE CALLER'S REG
         LR    R4,R15                  GET MY BASE
         USING NOANS,R4
         LA    R12,TRMSAV2             GET MY SAVE
         ST    R13,D4(R12)        SAVE HIS SAVE ADDR
         ST    R12,D8(R13)        SAVE MY SAVE ADDR
         LR    R13,R12                 SET SAVE POINTER
         POST  ECB                     POST WTOR ECB
         L     R13,D4(R13)        GET CALLER'S SAVE
         LM    R14,R12,D12(R13)   RESTORE CALLER'S REG
         BR    R14                     RETURN TO CALLER
         DROP  R4
         SPACE 2
**************************************************************** S22024
*        ROUTINE TO DETERMINE IF TEST DEVICE IS ON A 3705.     * S22024
*        IF SO , THE NCM OPTION IS TURN OFF.                   * S22024
**************************************************************** S22024
RNCHK    EQU   *                                                 S22024
         STM   R7,R9,TRMSAVE1     SAVE REGISTERS                 S22024
         LA    R7,TOTTDTBL        POINT TO TEST DEVICE TABLE     S22024
         LA    R8,TOTTDEND        END OF TABLE                   S22024
RNCHK2   EQU   *                  S22024
         CR    R7,R8              END OF TABLE                   S22024
         BE    RNCHKE             YES - EXIT (NO 3705 TD)        S22024
         L     R9,D0(R7)          GET TD ENTRY                   S22024
         LTR   R9,R9              ANY MORE ENTRIES               S22024
         BZ    RNCHKE             NO - EXIT (NO 3705 TD)         S22024
         SPACE
         TM    D0(R7),TOTTDORN    TD ON 3705                     S22024
         BO    RNCHKNCM           YES                            S22024
         LA    R7,D4(R7)          POINT TO NEXT ENTRY            S22024
         B     RNCHK2             CHECK IT                       S22024
         SPACE
RNCHKE   EQU   *                                                 S22024
         LM    R7,R9,TRMSAVE1     RESTORE REGISTERS              S22024
         BR    R9                 RETURN                         S22024
         SPACE
RNCHKNCM EQU   *                  S22024
         NI    TOTFLG06,XFF-TOTNCMFG SET CM                      S22024
         IEDQMSG MSGID=008,FUNCT=CEC NCM REJECTED                S22024
         B     TRM259             EXIT                           S22024
         EJECT
WORK1    DS    D                  WORK AREA
WORK2    DS    D                  WORK AREA
WORK3    DS    F                  WORK AREA
SAVAREA  DS    18F                SAVE AREA
THREE    DC    F'3'               VALUE OF 3
WORK     DC    F'0'               WORK AREA
TRMFLG   DC    X'00'              CONTROL FLAG
TRMWRK   DS    CL72               TRM BUFFER WORK AREA
TRMSAV2  DC    18F'0'             TIMER COMPLETE SAVE AREA
TRMNCMCT DC    F'0'               TEST ENTRY COUNTER SAVE
TRMSAVE1 DS    5F                 SAVE AREA
TRMTEN   DC    F'10'              VALUE OF TEN
TTEADDR  DC    F'0'               WORK AREA
NOUCB    DC    H'0'               COMPARE OPERAND
         DS    0D
TIMOUT   DC    X'F0F0F0F3F0F0F0F0' TIMER FOR TIMEOUT
ANSWER   DC    CL3' '             REPLY BUFFER
ECB      DC    F'0'               ECB WORD
DEC      DC    D'0'               WORK AREA
DIG1     DC    F'0'               WORK AREA
DIG2     DC    F'0'               WORK AREA
K0       DC    F'0'               VALUE OF ZERO
K1       DC    F'1'               VALUE OF 1
K3       DC    F'3'               VALUE OF 3
K8       DC    F'8'               VALUE OF 8
K16      DC    F'16'              VALUE OF 16
K248     DC    F'248'             VALUE OF 248
K256     DC    F'256'             VALUE OF 256
H1       DC    H'1'               VALUE OF 1
H3       DC    H'3'               VALUE OF 3
MASK0    DC    4XL1'F0'           AND/COMPARAND FIELD
BLK3     DC    3XL1'40'           COMPARAND FIELD
RTNFLG   DC    X'00'              ROUTINE CONTROL FLAG
TESET    DC    X'8040201008040201' SET RTN MASK TABLE
KF0      DC    4CL1'0'            MASK FIEDL
TTP      DC    C'TPP'             TEST PRIMARY PATH              S22024
TAP      DC    C'TAP'             TEST ALTERNATE PATH            S22024
TL       DC    C'TL'              TEST LOOP
NTL      DC    C'NTL'             NO TEST LOOP
EL       DC    C'EL'              ERROR LOOP
NEL      DC    C'NEL'             NO ERROR LOOP
CP       DC    C'CP'              CONTROL PRINT
NCP      DC    C'NCP'             NO CONTROL PRINT
NMI      DC    C'NMI'             NO MANUAL INPUT
MI       DC    C'MI'              MANUAL INPUT
AP       DC    C'AP'              ALTERNATE PRINTER
NAP      DC    C'NAP'             NO ALTERNATE PRINTER
EP       DC    C'EP'              ERROR PRINT
NEP      DC    C'NEP'             NO ERROR PRINT
CM       DC    C'CM'              CONCURRENT MODE
NCM      DC    C'NCM'             NON-CONCURRENT MODE
EXT      DC    C'EXT'             EXTERNAL DATA
TR       DC    C'TR'              TRACE FUNCTION
NTR      DC    C'NTR'             NO TRACE FUNCTION
NBK      DC    C'NBK'             NO BREAK OPTION                S22024
BK       DC    C'BK'              BREAK OPTION                   S22024
RTNMAX   DC    C'255'             MAXIMUM VALID RTN NUMBER       S22024
NP       DC    C'NP'              NO PRINT
PACK     PACK  DEC(8),D0(D0,R1)   CONVERT FROM ZONE TO DEC
MOVEXT   MVC   TOTEXT+D1(D1),D4(R1) MOVE EXT DATA TO OLTCB
PATCH    DC    CL200' '           MAINTENANCE AREA
         EJECT
         TQCBD
         EJECT
         DCBD  DSORG=TX
         EJECT
         TTRMD
         EJECT
         TDEBD
         EJECT
         OLTCB
         EJECT
         TPRFD
         EJECT
UCB      DSECT
         IEFUCBOB
         SPACE 2
         END
