         TITLE 'IEDQNA2-NONRESIDENT CLOSEDOWN COMPLETION'
IEDQNA2  CSECT
*A255000,666000                                                @ZA02080
*A696000                                                       @ZA03128
*A696000                                                       @XA09997
*   PACKAGING PROBLEM                                          @OZ09954
*A860800                                                       @OZ26164
*C858070-859540                                                @OZ26164
* DUMMY APAR                                                   @OZ27328
         SPACE 3
***********************************************************************
*                                                                     *
*TITLE:'IEDQNA2'-NONRESIDENT CLOSEDOWN COMPLETION                     *
*                                                                     *
*STATUS:CHANGE LEVEL 9                                                *
*D478200,479000                                                  A44022
*                                                                     *
*FUNCTION:THIS MODULE WAITS FOR THE COMPLETION OF ALL DISK ACTIVITY   *
*   IN THE MCP;IT SETS A CLOSEDOWN COMPLETION BIT IN THE ENVIRON-     *
*   MENT CHECKPOINT REQUEST ELEMENT;IT POSTS ECBS FOR ATTACHED        *
*   TASKS;IT WAITS FOR TERMINATION OF THE ATTACHED TASKS;IT           *
*   DETACHES THE TASKS.                                               *
*                                                                     *
*   IF REGISTER 1 IS NEGATIVE,THIS ROUTINE IS GETTING CONTROLAFTER    *
*   THE ABNORMAL TERMINATION OF A TCAM ATTACHED TASK.  THIS MODULE    *
*   DETERMINES WHICH TASK HAS ABENDED AND NOTIFIES THE USER VIA A     *
*   WTO TO THE SYSTEM CONSOLE.                                        *
*                                                                     *
*ENTRY POINT:                                                         *
*                                                                     *
*        IEDQNQ2                                                      *
*INPUT:                                                               *
*   IF REGISTER 1 IS POSITIVE,IT HAS THE ADDRESS OF AVTSAVE2.         *
*                                                                     *
*   IF REGISTER 1 IS NEGATIVE, IT HAS THE ADDRESS OF THE TCB FOR      *
*        THE ABENDED TASK.                                            *
*                                                                     *
*OUTPUT:NONE                                                          *
*                                                                     *
*EXTERNAL ROUTINES:DSPPOSTR                                           *
*                                                                     *
*EXITS-NORMAL:                                                        *
*                                                                     *
*   1.IF THERE IS ANY DISK ACTIVITY IN THE MCP,THIS MODULE EXITS      *
*   TO IEDQNA WITH 0 IN REGISTER 15.  IF TCAM IS CLOSED DOWN,IT EXITS *
*   TO IEDQNA WITH 8 IN REGISTER 15.  IF AN ATTACHED HAS ABENDED, IT  *
*   EXITS TO IEDQNA, WITH 4 IN REGISTER 15.                           *
*                                                                     *
*EXITS-ERROR:NONE                                                     *
*                                                                     *
*TABLES/WORK AREAS:                                                   *
*                                                                     *
*   AVT(AVTIOBR,AVTIOBN,AVTOSECB,AVTCKELF,AVTCKECA,AVTOPECA,          *
*       AVTINECA,AVTOLECA,AVTCKPTB,AVTOPCOB,AVTINSQB,AVTOLTQB,        *
*       AVTOCGET)                                                     *
*   IOSBE - DISK QUEUES                                        @VS41905
*   DCB                                                               *
*   DEB                                                               *
*                                                                     *
*ATTRIBUTES:SERIALLY REUSABLE                                         *
*                                                                     *
*NOTES:THE OPERATION OF THIS MODULE DEPENDS UPON AN INTERNAL REPRE-   *
*   SENTATION OF THE EXTERNAL CHARACTER SET WHICH IS EQUIVALENT TO    *
*   THE ONE USED AT ASSEMBLY TIME. THE CODING HAS BEEN ARRANGED SO    *
*   THAT REDEFINITION OF 'CHARACTER' CONSTANTS,BY REASSEMBLY, WILL    *
*   RESULT IN A CORRECT MODULE FOR THE NEW DEFINITIONS.               *
*                                                                     *
***********************************************************************
         SPACE 3
*REGISTER ASSIGNMENTS
         SPACE
R0       EQU   0                        TEMPORARY REG
R1       EQU   1                        PARAMETER REG
R2       EQU   2
R3       EQU   3
R4       EQU   4                        BASE FOR TCB
R5       EQU   5                        BASE FOR DEB
R6       EQU   6                        BASE FOR DCB
R7       EQU   7                        BASE FOR LCB
R8       EQU   8                        INDEX REG-NUMBER OF IOB'S
R9       EQU   9                        INDEX FOR LCB LIST-LCB LENGTH
R10      EQU   10                       BASE FOR IOB
R11      EQU   11                       INCREMENT REG-LENGTH OF IOB
R12      EQU   12                       BASE REG
R13      EQU   13                       SAVE REG-POINTS TO AVTSAVE2
R14      EQU   14                       RETURN REG
R15      EQU   15                       BRANCH REG
         SPACE
         SPACE 3
*STANDARD LINKAGE
         SPACE
         USING *,R15
IEDQNA2  IEDHJN ID                                             @ZA02080
         LTR   R1,R1                    CHECK TYPE OF PARAMETER
         BM    QNA70                    BRANCH IF A TCB ADDRESS
         LR    R13,R1                   ADDRESS OF AVTSAVE2
         USING AVTSAVE2,R13
         STM   R14,R12,AVTSAVE2+12      SAVE REGS
         LR    R12,R15                  SET UP BASE
         DROP  R15
         USING IEDQNA2,R12
         SPACE 3
*ALL LINE ACTIVITY IS NOW STOPPED. NOW CHECK FOR ANY DISK ACTIVITY IN
*THE MCP. GET IOSBE'S FROM AVT AND CHECK FOR BUSY. IF THERE
*ARE ANY INCOMPLETE, THIS ROUTINE ISSUES OS WAIT ON THE I/O,THEN EXITS
*TO DISPATCHER QPOSTING ITS OWN QCB TO ITSELF WITH LOWEST PRIORITY.
         SPACE
         TM    AVTBIT1,AVTDISKN         CHECK FOR DISK=YES         1119
         BZ    QNA52                    BRANCH IF DISK=NO          1119
         L     R10,AVTIOBR              ADDR OF REUS IOSBE'S   @VS41905
         L     R5,AVTADEBR              GET ADDR OF DEB        @VS41905
QNA40    EQU   *
         LR    R3,R10                   SAVE ADDRESS FOR LATER COMPARE
         USING IOSBE,R10                BASE FOR IOSBE         @VS41905
         SPACE 3
*INITIALIZE FOR IOSBE LOOP                                     @VS41905
         SPACE
         LTR   R10,R10                  CHECK FOR NO IOSBE'S   @VS41905
         BZ    QNA50                    BRANCH IF THERE ARE NONE
         USING IEDQDEB,R5
         XR    R8,R8                    CLEAR INDEX REG
         IC    R8,DEBNMEXT              GET NUMBER OF IOSBE'S  @VS41905
         LA    R11,IOSBELEN             GET LENGTH OF IOSBE    @VS41905
         SPACE 3
*BEGINNING OF LOOP THROUGH IOSBE'S                             @VS41905
         SPACE
QNA44    EQU   *
         TM    IOSBEFLG,IOSBERDY        IS IOSBE IN READY STATE@VS41905
*                                       (I.E., NOT BUSY)       @VS41905
         BO    QNA48                    YES, BRANCH            @VS41905
         SPACE 3
*IOSBE BUSY - WAIT FOR I/O TO COMPLETE                         @VS41905
         SPACE
         WAIT  ECB=AVTOSECB             WAIT FOR I/O
         SPACE
         LA    R1,AVTCLOSB              ADDRESS OF CLOSEDOWN COMP'S QCB
         L     R11,AVTEA                ADDRESS OF DISPATCHER
         USING IEDQDISP,R11             BASE FOR DISPATCHER
         LA    R15,DSPPOSTR             ADDRESS OF POST ENTRY
         BALR  R14,R15                  EXIT TO DISPATCHER,POST QCB
         LA    R15,0                   SET RETURN CODE FOR DSPDISP
         B     QNA60                    RETURN TO RESIDENT RTN
QNA48    EQU   *
         AR    R10,R11                  MOVE TO NEXT IOSBE     @VS41905
         BCT   R8,QNA44                 SUBTRACT FROM COUNT AND BRANCH T
                                        BEGINNING OF LOOP
         SPACE
*END OF LOOP THROUGH IOSBE'S                                   @VS41905
         SPACE 3
QNA50    EQU   *
         L     R10,AVTIOBN              GET ADDR OF NON-REUS   @VS41905
*                                       DISK IOSBE'S           @VS41905
         L     R5,AVTADEBN              GET ADDR OF DEB        @VS41905
         CR    R3,R10                   WHICH IOSBE'S WERE     @VS41905
*                                       PROCESSED              @VS41905
         BNE   QNA40                    BRANCH IF 1ST LIST(REUSABLE) WAS
                                        JUST PROCESSED
         SPACE 3
*ALL DISK ACTIVITY HAS STOPPED. NOW MAKE SURE ALL ATTACHED TASKS HAVE
*TERMINATED BY POSTING THEIR TCAM ECB'S AND ISSUING OS WAIT ON THEIR
*OS ECB'S.
         SPACE
QNA52    EQU   *                                                   1119
QNA53    EQU   *                                                   0106
         OI    AVTCKELF,AVTCCLCN        TURN ON BIT FOR CKPT
         POST  AVTOPCOB+4               OPERATOR  CONTROL
         POST  AVTCKPTB+4               CHECKPOINT
         POST  AVTOLTQB+4               ON LINE TEST
         POST  AVTCWEC1                 POST ECB FOR COMMON WRITE
         LA    R1,AVTOPECA              ADDRESS OF OP. CONTROL ECB
         LA    R2,AVTCKECA              ADDRESS OF CHECKPOINT ECB
         LA    R3,AVTOLECA              ADDRESS OF ON-LINE-TEST
         LA    R4,AVTCWECA              ADDRESS OF COMMON WRITE RTN
         STM   R1,R4,QNAECBLS           PUT ADDRESS IN ECB LIST
         OI    QNAEND,X'80'             INDICATE END OF LIST
         SPACE 3
         WAIT  4,ECBLIST=QNAECBLS
         SPACE 3
*DETACH THE ATTACHED TASKS: CHECKPOINT, OPERATOR CONTROL, ON-LINE-
*TEST,COMMON WRITE ROUTINE. THE ADDRESS OF THE TCBS ARE IN THE AVT.
         SPACE
         NC    AVTCKTCB(4),AVTCKTCB     CHECK FOR CHECKPOINT       0317
         BZ    QNA55                    BRANCH IF NOT ATTACHED     0317
         DETACH AVTCKTCB
QNA55    EQU   *
         DETACH AVTOCTCB
         NC    AVTOLTCB(4),AVTOLTCB     CHECK FOR TOTE SPECIFIED   0317
         BZ    QNA56                    BRANCH IF NOT ATTACHED     0317
         DETACH AVTOLTCB
QNA56    EQU   *
         NC    AVTCWTCB(4),AVTCWTCB      CHECK FOR COMWRITE        0317
         BZ    QNA57                    BRANCH IF NOT ATTACHED     0317
         DETACH AVTCWTCB                DETACH COMMON WRITE
QNA57    EQU   *
         SR    R0,R0                    CLEAR                      1203
         CLI   AVTCLOSB+8,X'FF'         CHECK FOR CONSOLE ID       1203
         BE    QNA58                    BRANCH IF NOT FROM CONSOLE 1203
         IC    R0,AVTCLOSB+8            GET CONSOLE ID             1203
         OI    QNAMSG+2,X'40'           MCSFLAG=REG0 BIT IS ON     1203
QNA58    EQU   *                                                   1203
         WTO   MF=(E,QNAMSG)            CLOSEDOWN REQUEST DID NOT  1203X
                                        COME FROM CONSOLE          1203
QNA59    EQU   *                                                   1203
         LA    R15,8                    SET RETURN CODE FOR USER
         SPACE 3
*RETURN TO THE RESIDENT PORTION OF CLOSEDOWN COMPLETION
         SPACE
QNA60    EQU   *
         RETURN (14,12),T,RC=(15)
         SPACE 3
*DETERMINE THE ATTACHED TASK THAT HAS ABENDED
         SPACE
QNA70    EQU   *
         LR    R11,R15                  SET NEW BASE REG
         USING IEDQNA2,R11
         DROP  R12
         LPR   R1,R1                    REG IS NEGATIVE
         LA    R1,0(,R1)                CLEAR HIGH ORDER BYTE
        LA    R10,10                   COUNT FOR TIMER LOOP       1209
         SPACE 3
*BEGINNING OF STIMER LOOP WAITING FOR OPEN TO INITIALIZE TCAM WORD 1209
*IN CVT
         SPACE
QNA71    EQU   *                                                   1209
         L     R9,16                    GET CVT ADDRESS
         L     R9,AVTCVTPT(,R9)
         LA    R9,0(,R9)                CLEAR HIGH ORDER BYTE      1111
         LTR   R9,R9                    CHECK FOR LOADED DISPATCHER1111
         BNZ   QNA73                    BRANCH IF OPEN HAS BEEN IN 1209
         STIMER WAIT,BINTVL=QNAINTVL    WAIT FOR 10 SECONDS        1209
         BCT   R10,QNA71                SUB FROM INDEX,CHECK AGAIN 1209
         B     QNA78                    GIVE UP ON OPEN -DEFAULT   1209
         SPACE
*END OF STIMER LOOP
         SPACE 3
QNA73    EQU   *                                                   1209
         L     R9,0(,R9)                GET AVT ADDRESS
         DROP  R13
         USING IEDQAVTD,R9              BASE FOR AVT
         C     R1,AVTCKTCB              COMPARE CKPT TCB ADDRESS
         BNE   QNA75                    BRANCH IF NOT CHECKPOINT
         NI    AVTCKFLG,X'FF'-AVTCKTAC-AVTCKNAC IND CHECKPOINT @ZM47551
*                                       NO LONGER ACTIVE       @ZM47551
         LA    R1,QNAMSG1               ADDRESS OF ERROR MSG
         B     QNA90                    ISSUE ERROR MSG
QNA75    EQU   *
         C     R1,AVTOCTCB              COMPARE WITH TCB FOR           X
                                        OPERATOR CONTROL
         BNE   QNA80                    BRANCH IF NOT OPERATOR CNTL
QNA78    EQU   *                                                   1111
         LA    R1,QNAMSG2               ADDRESS OF ERROR MSG
         XC    AVTAFE10(AVTCWINT-AVTAFE10),AVTAFE10 CLEAR THE  @ZA02080
*                                         COMWRITE POINTERS    @ZA02080
         B     QNA90                    ISSUE MSG
QNA80    EQU   *
         C     R1,AVTOLTCB              COMPARE TCB ADDRESS FOR TOTE
         BNE   QNA85                    BRANCH IF NOT TOTE
         LA    R1,QNAMSG3               ADDRESS OF TOTE MSG        0225
         MVI   AVTOLTST,AVTEZERO        CLEAR TOTE PARM TO ZERO    0225
         B     QNA90                    ISSUE ERROR MSG
QNA85    EQU   *
         LA    R1,QNAMSG4               ADDRESS OF MSG FOR COMWRITE
         NI    AVTCWFL1,X'FF'-AVTCOMWN  INDICATE COMWRT NOT ACTIVE
         MVI   AVTCWTS1,AVTEFF          MAKE COMWRITE'S...     @ZA03128
         MVI   AVTCWTS2,AVTEFF          ... ECB'S UNAVAILABLE  @ZA03128
         SPACE 3
*ISSUE WTO TO NOTIFY USER OF ABEND
         SPACE
QNA90    EQU   *
         WTO   MF=(E,(1))
         LA    R15,4                    RETURN CODE -TO OS
         BR    R14                      RETURN TO RESIDENT MODULE      X
                                        WITHOUT RESTORING REGS
         SPACE 3
*ECB LIST USED TO WAIT ON TERMINATION OF ATTACHED TASKS
         SPACE
QNAECBLS DS    0F
         DC    F'0'
         DC    F'0'
         DC    F'0'
QNAEND   DS    0F
         DC    F'0'
         SPACE 3
QNACODE  DC    X'80042000'              ABEND COMPLETION FOR TOTE  0916
QNAINTVL DC    XL4'3E8'                 BINARY INTERVAL OF 10.00SEC1209
         SPACE 3
QNAMSG1  WTO   'IED096I CHECKPOINT NO LONGER ACTIVE',MF=L,             X
               ROUTCDE=(2,11),DESC=4
QNAMSG2  WTO   'IED096I OPERATOR CONTROL NO LONGER ACTIVE',MF=L,       X
               ROUTCDE=(2,11),DESC=4
QNAMSG3  WTO   'IED096I TOTE NO LONGER ACTIVE',MF=L,               0225X
               ROUTCDE=(2,11),DESC=4
QNAMSG4  WTO   'IED096I COMWRITE NO LONGER ACTIVE',MF=L,               X
               ROUTCDE=(2,11),DESC=4
QNAMSG   WTO   'IED097I TCAM IS CLOSED DOWN',ROUTCDE=(2,11),DESC=5,    X
               MF=L                                                1203
         SPACE 3
         TTSID
         EJECT                                                 @VS41905
         TAVTD
         TDISPD
         TTCBD
         SPACE 3
*DSECT FOR DEB
         SPACE
         TDEBD                                                 @VS41905
         DCBD  DSORG=TQ
         TLCBD
         EJECT                                                 @VS41905
         TIOSBED                                               @OZ26164
         SPACE 4                                               @VS41905
***********************************************************************
*                *                                                    *
*    IOSBEENT    *                      IOSBEIOS                      *
*                *                                                    *
***********************************************************************
*                *                                                    *
*    IOSBEFLG    *                      IOSBECPB                      *
*                *                                                    *
***********************************************************************
*                                  *                                  *
*            IOSBEXCC              *             IOSBERET             *
*                                  *                                  *
***********************************************************************
*                *                                                    *
*    IOSBERET    *                      IOSBENEW                      *
*     (CONT)     *                                                    *
***********************************************************************
*                *                                                    *
*    IOSBELCK    *                                                    *
*                *                                                    *
***********************************************************************
*                                                                     *
*                                                                     *
*                                                                     *
***********************************************************************
         END
