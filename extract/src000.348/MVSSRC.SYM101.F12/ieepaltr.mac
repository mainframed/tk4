IEEPSTP1 CSECT
***********************************************************************
*MODULE NAME= IEEPALTR
*  CSECT-NAME= IEEPSTP1
*
*DESCRIPTIVE NAME= SVC 110 ATTACH ROUTINE
*
*COPYRIGHT= N/A
*
*STATUS= RELEASE 02.0
*
*FUNCTION= ATTACH THE SVC 110
*  OPERATION= AFTER SAVING THE RETURN REGISTER (REGISTER 14) AND THE
*             PARAMETER LIST POINTER (REGISTER 1), IEEPALTR LOADS THE
*             STAE CREATION ROUTINE (IEECB860). IT THEN DETERMINES
*             THE COMMAND BEING EXECUTED AND THEN SAVES THE COMMAND
*             FOR THE ESTAE EXIT.  THE STAE CREATION ROUTINE IS
*             INVOKED AND THEN DELETED.  IF THE STAE ENVIRONMENT WAS
*             NOT CREATED SUCCESSFULLY, A RETURN TO THE CALLER IS MADE.
*             IF THE STAE ENVIRONMENT WAS SUCCESSFULLY CREATED, THE
*             CSCB ADDRESS IS PASSED FOR THE SVC 110 CALL.
*
*NOTES
*  DEPENDENCIES= LOAD, GETMAIN, FREEMAIN, DELETE, SVC 110
*  RESTRICTIONS= NONE
*  REGISTER-CONVENTIONS= STANDARD
*  PATCH-LABEL= FIXAREA
*
*MODULE TYPE= CSECT
*  PROCESSOR= ASSEM
*  MODULE SIZE= X'118'
*  ATTRIBUTES= READ-ONLY, AND REENTRANT
*
*ENTRY POINT= IEEPALTR
*  PURPOSE= SAME AS FUNCTION
*  LINKAGE= FROM IEEVWAIT VIA ATTACH
*  INPUT= REGISTER 1 CONTAINING THE PARAMETER LIST POINTER
*         REGISTER 14 CONTAINING THE RETURN ADDRESS
*  REGISTERS SAVED= REGISTERS 1 AND 14
*  REGISTER USAGE= R0     LENGTH/SP OF PARAMETER AREA
*                  R1     PARAMETER LIST REGISTER AND WORK REGISTER
*                  R2     UNUSED
*                  R3     UNUSED
*                  R4     STORAGE FOR RETURN ADDRESS
*                  R5     STAE ROUTINE ADDRESS
*                  R6     UNUSED
*                  R7     IEECHAIN REGISTER
*                  R8     COMMAND NAME
*                  R9     BASE REGISTER
*                  R10    WORK REGISTER
*                  R11    UNUSED
*                  R12    UNUSED
*                  R13    UNUSED
*                  R14    RETURN ADDRESS
*                  R15    WORK REGISTER
*  REGISTERS RESTORED= REGISTER 14
*
*EXIT - NORMAL= ATTACH TO SVC 110
*  CONDITIONS= WHENEVER THE STAE CREATION ROUTINE WAS SUCCESSFUL
*  OUTPUT= REGISTER 1 CONTAINING THE CSCB ADDRESS
*          REGISTER 15 CONTAINING ZEROES, INDICATING FIRST TIME ENTRY
*  RETURN CODES= NONE
*
*EXIT - ERROR= RETURN TO CALLER
*  CONDITIONS= WHENEVER THE STAE CREATION ROUTINE WAS NOT SUCCESSFUL
*  OUTPUT= NONE
*  RETURN CODES= NONE
*
*EXTERNAL REFERENCES= SEE BELOW
*  ROUTINES= LOADS IEECB860
*            DELETES IEECB860
*            SVC 110 CALL
*  DATA AREAS= PARMLIST (CONTAINS PERTINENT DATA)
*  CONTROL BLOCKS= IEECHAIN - R
*
*TABLES= NONE
*
*MACROS= DELETE, FREEMAIN, GETMAIN, LOAD, AND SVC 110
*
*CHANGE ACTIVITY= Y02669
*
*MESSAGES= NONE
*
*ABEND CODES= NONE
*
***********************************************************************
         EJECT
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
DPLYCK   EQU   46                  DISPLAY C,K VERB CODE         Y02669
DPLYPFK  EQU   54                  DISPLAY PFK VERB CODE         Y02669
DPLYUNIT EQU   82                  DISPLAY UNIT VERB CODE        Y02669
         EJECT
* D 003220,004525-004585,005125-005140,009339-009439             Y02669
* C 004316,004375                                                Y02669
* D 003389-003390,004333-004374,005189-005198,009483-009495      Y02669
         BALR  R9,R0
         USING *,R9
POINT1   EQU   *                                                 Y02669
IEEPALTR MODID BRANCH=YES                                        Y01886
         USING CHAIN,R7                                          Y01043
         LR    R4,R14              SAVE RETURN REG             @YM02760
         LR    R7,R1               SAVE PARAMETER POINTER        Y01043
         LOAD  EP=IEECB860         LOAD THE STAE ROUTINE         Y01043
         LR    R5,R0               SAVE ADDRESS OF ROUTINE       Y01043
         L     R0,PARMAR           LENGTH/SP OF PARM AREA        Y01043
         GETMAIN R,LV=(0)          GET PARM AREA                 Y01043
         USING PARMLIST,R1         BASE PARM AREA                Y01043
         ST    R4,RTNADDR          SAVE RETURN ADDRESS         @YM02760
         ST    R7,CSCBPASS         SAVE/PASS CSCB ADDRESS        Y01043
         TM    CHTYPE,CHDSTAT      THIS STATUS DISPLAY           Y01043
         BNO   EXIT                NO - RETURN                   Y02669
         CLI   CHVCD,DPLYCK        IS THIS DISPLAY C,K COMMAND?  Y02669
         BNE   NEXT1               NO - CONTINUE CHECK           Y02669
         LA    R8,DISPLAYC         INDICATE DISPLAY C,K COMMAND  Y02669
         B     SET                 SET UP PARAMETER AREA         Y02669
NEXT1    EQU   *                                                 Y02669
         CLI   CHVCD,DPLYUNIT      IS THIS DISPLAY UNIT COMMAND? Y02669
         BNE   NEXT2               NO - CONTINUE CHECK           Y02669
         LA    R8,DISPLAYU         INDICATE DISPLAY UNIT COMMAND Y02669
         B     SET                 SET UP PARAMETER AREA         Y02669
NEXT2    EQU   *                                                 Y02669
         CLI   CHVCD,DPLYPFK       IS THIS DISPLAY PFK COMMAND?  Y02669
         BNE   SET                 NO - CONTINUE                 Y02669
         LA    R8,DISPLAYP         INDICATE DISPLAY PFK COMMAND  Y02669
SET      EQU   *                                                 Y02669
         ST    R8,COMMAND          SET PTR LGTH/NAME OF COMMAND  Y01043
         LR    R15,R5              SET ADDR OF STAE ROUTINE      Y01043
         BALR  R14,R15             INVOKE STAE CREATION          Y01043
         LR    R10,R15             SAVE RETURN CODE              Y01043
         BALR  R9,R0               RESET BASE ADDRESS            Y01043
POINT2   EQU   *                                                 Y02669
         LA    R7,POINT2-POINT1    RE-ESTABLISH                  Y02669
         SR    R9,R7                    ADDRESSABILITY           Y02669
         L     R7,CSCBPASS         RESET CSCB ADDRESS            Y01043
         L     R4,RTNADDR          RESET RETURN ADDRESS          Y01043
         L     R0,PARMAR           SET LENGTH/SP OF PARM AREA    Y01043
         FREEMAIN R,LV=(0),A=(1)   FREE AREA                     Y01043
         DELETE EP=IEECB860        GET RID OF STAE ROUTINE       Y02669
         LTR   R10,R10             STAE CREATED SUCCESSFULLY     Y01043
         BNZ   EXIT                NO, DON'T CONTINUE            Y02669
         LR    R1,R7               PASS THE CSCB ADDRESS         Y01043
         SR    R15,R15             INDICATE FIRST ENTRY           21002
         SVC   110                 ISSUE SVC FOR COMMAND          21002
EXIT     EQU    *                                                Y02669
         LR    R14,R4              RESTORE RETURN ADDRESS         21002
         BR    R14                 RETURN TO CALLER               21002
         EJECT
         DS    0F                                                Y01043
PARMAR   DC    AL1(0),AL3(12)                                    Y01043
DISPLAYC DC    AL1(L'COM1)         LENGTH                        Y02669
COM1     DC    C'DISPLAY C,K'      COMMAND TEST                  Y02669
DISPLAYP DC    AL1(L'COM2)         LENGTH                        Y02669
COM2     DC    C'DISPLAY PFK'      COMMAND TEST                  Y02669
DISPLAYU DC    AL1(L'COM3)         LENGTH                        Y02669
COM3     DC    C'DISPLAY U'        COMMAND TEST                  Y02669
FIXAREA  DC    CL50' ********** IEEPALTR PATCH AREA ********** ' Y02669
         EJECT
PARMLIST DSECT                                                   Y01043
CSCBPASS DS    F                   ADDR OF THE CSCB              Y01043
COMMAND  DS    F                   ADDR/LGTH OF COMMAND          Y01043
RTNADDR  DS    F                   RTN ADDR IN CASE OF FAILURE   Y01043
         EJECT
CHN      DSECT                                                    21002
         IEECHAIN                                                 21002
         END
