         MACRO
&NAME  TCONV   &MF=,&TYPE=EH,&FROM=,&TO=,&COUNT=,&MAP=NO,&LSTNAME=,    X
               &RET=
.*
         GBLB  &EXECUTV,&E536454
         GBLB  &RCC .        SET TO 1 IF INTLZE WILL SUPPORT RCC
         LCLA  &FLAG,&ERROR,&KOUNT
         LCLB  &RCC1
         LCLC  &F,&SIZE,&CHAR,&Z
.*
         AIF   ('&MAP' EQ 'YES').MAP1
         AIF   (T'&MF EQ 'O').STD
         AIF   ('&MF' EQ 'L' OR '&MF' EQ 'S').STD
         AIF   ('&MF'(1,3) NE '(E,').MFERROR
&CHAR    SETC  '&NAME'
         AIF   ('&MF'(4,3) EQ '(1)').RG1
         AIF   ('&MF'(4,1) EQ '(').RG2
&CHAR    LA    R1,&MF(2) .         ADDRESS OF PARAMETER LIST
&CHAR    SETC  ' '
         AGO   .RG1
.RG2     ANOP
&KOUNT   SETA  K'&MF(2)-2
&F       SETC  '&MF(2)'(2,&KOUNT)
         AIF   ('&F'(1,1) EQ 'R').C724
&CHAR    LR    R1,R&F .            ADDRESS OF PARAMETER LIST
&CHAR    SETC  ' '
         AGO   .RG1
.C724    ANOP
&CHAR    LR    R1,&F .             ADDRESS OF PARAMETER LIST
&CHAR    SETC  ' '
.RG1     ANOP
.RCC2    ANOP
         AIF   ('&RCC' EQ '0').RCCA
         AIF   (T'&RET EQ 'O').RCCD
         AIF   ('&RET'(1,1) EQ '#').RCCB
         AIF   ('&RET'(1,1) EQ '(').RCCC
         MVC   $RETMASK,&RET .     SET RETURN CODE MASK
         AGO   .RCCD
.RCCE    ANOP
         MVI   $RETMASK,#ALL-#RC0 . SET RETURN CODE MASK
         AGO   .RCCD
.RCCB    ANOP
         MVI   $RETMASK,#ALL-&RET . SET RETURN CODE MASK
         AGO   .RCCD
.RCCC    ANOP
         MVI   $RETMASK,#ALL-&RET(1) . SET RETURN CODE MASK
         AGO   .RCCD
.RCCA    ANOP
         AIF   (T'&RET EQ 'O').RCCD
         MNOTE 5,'RETURN CODE NOT SUPPORTED BY -INTLZE-'
.RCCD    ANOP
         AIF   (&RCC1).RCC3
&CHAR    BAL   R14,#EXIT .         BRANCH TO LINKAGE SUBROUTINE
         MEXIT
.MFERROR MNOTE 8,'MF VALUE (&MF) INCORRECT, NO STATEMENTS GENERATED.'
         MEXIT
.STD     ANOP
         AIF   (T'&FROM NE 'O').TEST3
&ERROR   SETA  1
         MNOTE 8,'FROM ADDRESS MISSING.'
.TEST3   AIF   (T'&TO NE 'O').TEST4
&ERROR   SETA  1
         MNOTE 8,'TO ADDRESS MISSING.'
.TEST4   AIF   (T'&COUNT NE 'O').BEGIN
&ERROR   SETA  1
         MNOTE 8,'COUNT MISSING.'
.BEGIN   ANOP
         AIF   (&ERROR EQ 1).EXIT
         AIF   ('&TYPE' NE 'HE').CNVRT2
&FLAG    SETA  X'8000'
.CNVRT2  ANOP
         AIF   ('&MF' EQ 'L').LIST
&NAME    LA    R14,&FROM(1) . GET ADDR OF DATA
         AIF   (N'&FROM EQ 1).CNVRT4
.TEST5   AIF   (&FROM(2) GE 2 OR &FROM(2) LE 14).CNVRT3
&ERROR   SETA  1
         MNOTE 8,'INVALID REG SPEC FOR FROM.'
.CNVRT3  ANOP
         AIF   (&ERROR EQ 1).EXIT
         AR    R14,&FROM(2) . INCREMENT ADDRESS
.CNVRT4  ANOP
         ST    R14,QWIE&SYSNDX . PLACE ADDR IN PARAMETER LIST
         LA    R14,&TO . GET OUTPUT DATA ADDR
         ST    R14,QWIU&SYSNDX . OUTPUT DATA STORE
         LA    R14,R&SYSNDX .     RETURN ADDRESS
&RCC1    SETB  1
         AGO   .RCC2
.RCC3    ANOP
         CNOP  0,4
         BAL   R1,#EXIT .          BRANCH TO LINKAGE SUBROUTINE
&CHAR    SETC  '&LSTNAME'
         AGO   .STD1
.LIST    ANOP
&CHAR    SETC  '&NAME'
         CNOP  0,4
.STD1    ANOP
         AIF   (&EXECUTV).OLTSEP
&CHAR    DC    B'00000000' .       CONTROL PROGRAM FLAGS
         AGO   .OLTSEP1
.OLTSEP  ANOP
&CHAR    DC    B'00000001' .       CONTROL PROGRAM FLAGS
.OLTSEP1 ANOP
         DC    AL1(2) .            MACRO LEVEL
         DC    CL2'41' .           MACRO ID
QWIE&SYSNDX  DC  XL4'00000000' . ADDRESS OF DATA
QWIU&SYSNDX  DC  XL4'00000000' .  OUTPUT DATA
C&SYSNDX DC    AL2(&COUNT+&FLAG) . COUNT AND TYPE INFORMATION
**                                BIT 0 SET TO 1 INDICATES 'HE' TYPE.
         AIF   ('&MF' EQ 'L').EXIT
R&SYSNDX DS    0H .               RETURN ON HALFWORD BOUNDARY
         MEXIT
.MAP1    ANOP
CONVMAP  DSECT
&Z       SETC  '$'
         AIF   (&E536454).ZIPPER
           DC  XL1'0' .            CONTROL PROGRAM FLAGS
           DC  XL1'0' .            MACRO LEVEL
           DC  XL2'0' .            MACRO ID
         AGO   .ZIPPED
.ZIPPER  ANOP
&Z.CNVTRMG DC  XL1'0' .            CONTROL PROGRAM FLAGS
&Z.CNVLEVL DC  XL1'0' .            MACRO LEVEL
&Z.CNVIDNT DC  XL2'0' .            MACRO ID
.ZIPPED  ANOP
&Z.CNVFROM DC  A(0) .              FROM ADDRESS
&Z.CNVTO   DC  A(0) .              TO   ADDRESS
&Z.CNVCT   DC  XL2'0' .            TYPE & COUNT
.EXIT    MEND
