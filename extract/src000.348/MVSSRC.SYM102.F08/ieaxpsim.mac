IEAXPSIM CSECT
         SPACE
***********************************************************************
*                                                                     *
* MODULE NAME - IEAXPSIM                                              *
*                                                                     *
* FUNCTION/OPERATION - IEAXPSIM DETERMINES IF THE EXTENDED PRECISION  *
*              FLOATING POINT FEATURE IS INSTALLED ON THE CPU.        *
*                                                                     *
* STATUS - CHANGE LEVEL 0                                             *
*                                                                     *
* ENTRY POINT - IEAXPSIM                                              *
*                                                                     *
* INPUT - REGISTER 1 CONTAINS THE POINTER TO A PARAMERTE LIST WHICH   *
*         CONTAINS A POINTER TO AN DOUBLE WORD AREA INTO WHICH        *
*         IEAXPSIM  WILL MOVE THE NAME OF THE SIMULATOR MODULE        *
*         SUITABLE TO THE CPU.                                        *
*                                                                     *
* EXITS - RETURN IS ALWAYS TO THE CALLER VIA REGISTER 14.             *
*                                                                     *
* ATTRIBUTES - THIS MODULE IS REENTRANT.                              *
*                                                                     *
***********************************************************************
         EJECT
* REGISTER DEFINITIONS
         SPACE
PARM     EQU   1         PARAMETER REGISTER
ADDR     EQU   3         ADDRESS OF AREA TO STORE NAME OF SIMULATOR
R4       EQU   4         WORK REGISTER
R5       EQU   5         PTR TO CVT
BASE     EQU   6         ADDRESSABILITY REGISTER
         SPACE
         USING SIM,15
         SPACE
SIM      B     PROLOG
         DC    X'08'
         DC    CL8'IEAXPSIM'
PROLOG   STM   14,12,12(13)        SAVE REGISTERS
         BALR  BASE,0
         USING *,BASE
         L     ADDR,0(PARM)        PICK UP ADDRESS OF NAME
         L     R5,CVTPTR            GET CVT ADDRESS
         LA    R4,XPDXR             ADDRESS OF NAME FOR MODELS WITH EXT*
                                    PRE HARDWARE
         USING CVT,R5
         TM    CVTOPTA,CVTXPFP     IS EXTENDED PRECISION FEATURE       *
                                   INSTALLED?
         BO    SETADDR
         LA    R4,8(R4)            MODELS WITHOUT EXT PRE FEATURE
SETADDR  MVC   0(8,ADDR),0(R4)     SET APPROPRIATE NAME
         LM    3,6,32(13)
         MVI   12(13),X'FF'
         BR    14                  RETURN TO CALLER
         SPACE
* CONSTANTS
XPDXR    DC    CL8'IEAXPDXR'
XPALL    DC    CL8'IEAXPALL'
         SPACE
CVT      DSECT
         CVT   SYS=AOS1
         END
