         TITLE 'IGG0197L-TRANSLATES OMR OR RCE FORMAT CD--3505/3525'
         START
IGG0197L CSECT
***********************************************************************
* AOS1 AND AOS2 ADDITIONS/CHANGES/DELETIONS                      Y01898
*D208000,C285000,C376500,C377000,C377500-377700                @ZA01942
*A137000                                                          M1865
*
*NAME--              IGG0197L
*
*TITLE--            'IGG0197L' - OMR OR RCE FORMAT CARD TRANSLATOR
*
*STATUS CHANGE LEVEL 000
*                    THIS MODULE WAS PUT INTO 'OS' RELEASE 21.0 AS A
*                    NEW MODULE
*
*MODULE TYPE--       OPEN - STAGE 1
*
*FUNCTION/OPERATION--THIS ROUTINE IS ENTERED FOR SAM USERS DURING
*                    OPEN WHEN:
*                             -OMR OR RCE IS SPECIFIED FOR 3505 CARD
*                             READER.
*                             RCE IS SPECIFIED FOR A 3525 CARD
*                             PUNCH'S READ ONLY FILE.
*                    IT TRANSLATES THE FORMAT CARD AND ISSUES AN
*                    OMR OR RCE COMMAND TO THE DEVICE TO PUT IT IN
*                    THE PROPER (OMR OR RCE) MODE.
*
*ENTRY POINT--       ENTERED FROM IGG0196B BY USE OF THE XCTL MACRO
*
*INPUT--             SEE DESCRIPTION OF REGISTERS
*
*OUTPUT--            SEE DESCRIPTION OF REGISTERS
*
*EXTERNAL ROUTINES-- NONE
*
*EXITS NORMAL--      XCTL TO IGG0197M TO CONTINUE PROCESSING
*
*EXITS ERROR--       XCTL TO IGG0197M IF FORMAT CARD IS INVALID OR
*                    IF OMR HAS BEEN SPECIFIED FOR 3525 CARD PUNCH
*
*TABLES/WORKAREAS--  WHERE-TO-GO TABLE
*                    BYTES  0- 7   NAME
*                    BYTES  8-10   RELATIVE DISK ADDRESS(TTR) OF
*                                  FIRST RECORD
*                    BYTE     11   CONCATENATION NUMBER
*                    BYTE     12   ZEROES
*                    BYTE     13   ALIAS INDICATOR AND USER DATA
*                                  FIELD DESCRIPTOR:
*                                      -ALIAS INDICATOR-- 1 BIT
*                                      -NUMBER OF TTRS IN USERS
*                                      DATA FIELD-- 2 BITS
*                    BYTES 14-16   TTR OF FIRST TEXT RECORD
*                    BYTE     17   ZEROES
*                    BYTES 18-19   TTR OF NOTE LIST OR SCATTER LIST
*                    BYTE     20   TRANSLATION TABLE
*                    BYTE     21   ZEROES OR NUMBER OF ENTRIES IN
*                                  THE NOTE LIST
*                    BYTES 22-23   ATTRIBUTES
*                    BYTES 24-26   TOTAL CONTIGUOUS MAIN STORAGE
*                                  REQUIRED FOR MODULE
*                    BYTES 27-28   LENGTH OF FIRST TEXT RECORD
*                    BYTE     29   LENGTH OF WTG TABLE(IN DOUBLE
*                                  WORDS)
*                    BYTES 30-31   PATH THROUGH LOADS OF ROUTINES
*                    BYTES 32-34   IDTTR OF EXECUTOR FOR FIRST DCB
*                    BYTE     35   ADDRESS OF STORAGE FOR DCB
*                    BYTES 36-39   TABLE OF IDTTR'S
*                    BYTE  32+N(8)--
*                            IDTTR OF EXECUTOR FOR NTH DCB (3 BYTES)
*                            ADDRESS OF STORAGE FOR DCB    (1 BYTE )
*                            IDTTR OF LAST ROUTINE LOAD    (3 BYTES)
*                            NOT USED                      (1 BYTE )
*                    --------------
*                    WORKAREA-     SEE 'FORCORE DSECT'(IECDSECT)
*                                  IN THIS LISTING.
*
*ATTRIBUTES--        REENTRANT AND REUSABLE.
*
***********************************************************************
***********************************************************************
*                        REGISTER  CONVENTIONS
***********************************************************************
*
RDCB     EQU   2                       DCB REGISTER
RBASE    EQU   3                       BASE REGISTER
RCORE    EQU   4                       DCB WORK AREA REGISTER
RPAR     EQU   5                       TOP OF PARAMETER LIST
RWTG     EQU   6                       TOP OF WHERE-TO-GO (WTG) TABLE
RPARC    EQU   7                       CURRENT PARAMETER
RWTGC    EQU   8                       CURRENT TRANS LOAD
RTIOT    EQU   9                       USED HERE AS WORK REGISTER
RUCB     EQU   10                      USED HERE AS WORK REGISTER
RDEB     EQU   11                      USED HERE AS FORMAT CARD POINTER
RB       EQU   12                      USED HERE AS WRK/SWITCH REGISTER
RC       EQU   13                      WORK REGISTER
RD       EQU   14                      WORK REGISTER
RJ       EQU   15                      WORK REGISTER
RE       EQU   0                       WORK REGISTER
RF       EQU   1                       WORK REGISTER
***********************************************************************
*                        DISPLACEMENTS AND MASKS
***********************************************************************
*
RCE      EQU   X'10'
OMR      EQU   X'20'
P3525    EQU   X'4C'
R3505    EQU   X'46'
FDSS     EQU   X'23'                    FEED SS COMMAND
OMRCEFSS EQU   X'2B'                    FEED SS CMD WITH OMR/RCE FLAG
RDONLY   EQU   X'C2'
OMRCMD   EQU   X'31'
RCECMD   EQU   X'11'
UNRELFLG EQU   X'02'
SLI      EQU   X'20'
BYTCNT80 EQU   X'50'
FIVE     EQU   5
FOUR     EQU   X'04'
THRE     EQU   X'03'
TWO      EQU   X'02'
ONE      EQU   X'01'
Z        EQU   X'E9'
SUCCFLCC EQU   X'7F'
NINE     EQU   X'09'
EIGHT    EQU   X'08'
TWELVE   EQU   X'0C'
SEVENTY1 EQU   X'47'
SIXTY8   EQU   X'44'
BLANK    EQU   X'40'
LFTPAREN EQU   X'4D'
RGTPAREN EQU   X'5D'
COMMA    EQU   X'6B'
ZERO     EQU   X'00'
SEVEN    EQU   X'07'
TEN      EQU   10
D72      EQU   72
F9       EQU   X'F9'
F0       EQU   X'F0'
D15      EQU   15
D80      EQU   80
D71      EQU   71
SIX      EQU   6
D14      EQU   14
*
**
***
**
*
         BALR  RBASE,RE                LOAD BASE ADDR IN BASE REG
         USING RCE1000,RBASE
         USING FORCORE,RCORE
         USING IHADCB,RDCB
RCE1000  EQU   *
         L     RDCB,0(RPARC)           GET DCB ADDRESS
         L     RCORE,FOUR(RWTGC)       GET WORK AREA ADDRESS
         MVC   DSCEXT1+NINE(EIGHT),DXDEBAPP SET UP FOR WORK AREA FIRST
         MVC   DSCEXT2+SEVEN(EIGHT),DXDEBSCC USAGE. SAVE CONTENTS
         ST    RPAR,DXDEBAPP           SAVE PARAMETER LIST
         XC    DXLBL(80),DXLBL          BLANK TRANSLATION AREA    M1865
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                                     *
*                                 BUILD IOB AND CCW                   *
*                                                                     *
         ST    RDCB,IOBWGHT            PUT DCB ADDR IN WORK IOB
         TM    DCBMODE,RCE             RCE SPECIFIED FOR THIS DEVICE
         BO    RCE1100                 YES, BRANCH
         TM    DCBMODE,OMR             OMR SPECIFIED FOR THIS DEVICE
         BNO   RCE4300                 NO, BRANCH TO TRANSFER CONTROL
         TM    DCBDEVT,P3525           3525 CARD PUNCH DEVICE
         BO    RCE4500                 YES, BRANCH
RCE1100  EQU   *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*        SET UP SWITCH (RB) TO DETERMINE OPERATION TO BE PERFORMED
*             SWITCH(RB)
*                  0    FIRST FORMAT CARD JUST READ
*                  1    READ ONLY CMD MUST BE ISSUED NOW
*                  2    TRANSLATION OF FORMAT CARD(S) COMPLETE
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         SR    RB,RB                   SET FIRST FORMAT CD SWITCH TO 0
RCE1200  EQU   *
         MVI   DXCCW1,RDONLY           SET READ ONLY CMD IN CCW
RCE1250  EQU   *
         LA    RF,DXCCW2               GET DATA ADDRESS FOR CCW
RCE1300  EQU   *
         ST    RF,DXDEBUCB             PREPARE DATA ADR FOR CCW STORAGE
         MVC   DXCCW1+ONE(THRE),DXDEBUCB+ONE  PUT DATA ADDR IN CCW
         MVI   DXCCW1+FOUR,SLI         TURN ON SLI BIT IN CCW
         MVI   DXCCW1+SEVEN,BYTCNT80   SET BYTE COUNT TO 80 IN CCW
*                                                                     *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         LA    RF,IOBFLAG1             GET IOB ADDRESS
         EXCP  (1)                     EXECUTE CHANNEL PROGRAM
         LA    RF,DXECB                GET ECB ADDRESS FOR WAIT
         WAIT  ECB=(1)
         TM    DXECB,SUCCFLCC          SUCCESSFUL COMPLETION (X'7F')
         BNO   RCE4500                 NO, BRANCH
         XC    DXECB(FOUR),DXECB       ZERO OUT ECB AREA
         LA    RF,TWO                  SET MASK FOR TEST
         CLR   RB,RF                   IS TRANSLATION COMPLETE
         BE    RCE4300                 YES, BRANCH TO TRANSFER CONTROL
         TM    DXCCW1,FDSS             FEED, SS CMD
         BO    RCE1200                 YES, GO BACK AN DO A RD ONLY CMD
         TM    DXCCW1,RDONLY           IS THIS A READ ONLY COMMAND
         BO    RCE1400                 YES,BRANCH
         LA    RB,TWO                  SET TRANSLATION COMPLETE SWITCH
         MVI   DXCCW1,OMRCEFSS          SET TRANSLATION COMPLETE FLAG
         B     RCE1250                 BRANCH
RCE1400  EQU   *
         LTR   RB,RB                   SWITCH EQUALS ZERO
         BC    7,RCE3100               NO, BRANCH
RCE1500  EQU   *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*        CHECK FIRST NINE COLUMNS OF FIRST FORMAT CARD FOR VALIDITY
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         CLC   DXCCW2(NINE),MYFRMT     ARE FIRST 9 COL. OF FORMAT VALID
         BNE   RCE4500                 NO, BRANCH
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*        'COLUMN NUMBER' COUNTER (RC) SPECIFIES THE COLUMN THAT IS
*        CURRENTLY BEING DIAGNOSED ON THE USER'S FORMAT CARD.
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         LA    RC,TEN                  COLUMN NUMBER COUNTER
         LA    RDEB,DXCCW2+NINE        POINT TO FIRST DIGIT OF OPERAND
         SR    RD,RD                   ZERO OUT WORK REGISTER
RCE1600  EQU   *
         L     RPAR,F000               SET UP FOR FIRST FORMAT COL. NO.
         LR    RJ,RPAR                 SET UP FOR NEXT FORMAT COL. NO.
RCE1650  EQU   *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*        IF THE 'COLUMN NUMBER' COUNTER IS POINTING AT COLUMN72 AND
*        A CONTINUATION MARK IS IN COLUMN 72, THEN SAVE LAST 3 CHARS
*        OF CARD FOR HELP IN DIAGNOSING THE NEXT CARD.
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         LA    RUCB,D72                LOAD NUMBER FOR COMPARE
         CLR   RC,RUCB                 IS COLUMN 72 BEING POINTED TO
         BL    RCE1700                 NO, BRANCH TO TRANSLATE COLUMN
         CLI   DXCCW2+SEVENTY1,BLANK   CONTINUATION MARK IN COL. 72
         BE    RCE1700                 NO, BRANCH TO TRANSLATE NEXT COL
         MVC   JFCBXPDT(THRE),DXCCW2+SIXTY8  SAVE LAST 3 BYTES OF CARD
RCE1680  EQU   *
         LA    RB,ONE                  SET SWITCH TO ONE
         MVI   DXCCW1,FDSS             SET FD,SS CMD IN CCW
         ST    RJ,DXDEBEHH             SAVE LAST NUMBER OF OPERAND
         B     RCE1250                 BRANCH
RCE1700  EQU   *
         SR    RUCB,RUCB               ZERO OUT WORK REGISTER
         IC    RUCB,0(RDEB)            GET CHAR POINTED TO ON FORMAT CD
RCE1800  EQU   *
         LA    RE,F9                   LOAD MASK TO CK FOR VALID CHAR
         CLR   RUCB,RE                 IS CHAR GREATER THAN A NUMBER
         BH    RCE4500                 YES, BRANCH
         LR    RTIOT,RDEB              GET FORMAT POINTER
         LA    RE,TWO                  GET A VALUE OF 2
         SR    RTIOT,RE                POINT BACK 2 COL. ON FORMAT CD
MOVE     MVC   DXDEBECC(FOUR),ZERO(RTIOT)  GET 4 CHAR OF FORMAT FOR TST
         LA    RF,F0                   GET MASK TO CK FOR SPECIAL CHAR
         CLR   RUCB,RF                 IS THIS CHAR A NUMBER
         BL    RCE2600                 NO, BR.--MUST BE SPECIAL CHAR
RCE1900  EQU   *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*        CONTINUE INTERROGATION OF OPERAND AND SAVE FIRST NUMBER
*        OF OPERAND IN REG. 5 (RPAR) AND LAST NUMBER IN REG.15 (RJ)
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         CLI   DXDEBECC+ONE,LFTPAREN   IS LEFT PAREN IN THIS FORMAT COL
         BNE   RCE2200                 NO, BRANCH
         CLI   DXDEBECC+THRE,COMMA     COMMA IN THIS FORMAT COLUMN
         BNE   RCE2100                 NO, BRANCH
RCE2000  EQU   *
         IC    RPAR,ZERO(RDEB)         SAVE CHAR BEING POINTED TO
         LA    RDEB,TWO(RDEB)          INCRE FORMAT SEARCH ADDR. BY 2
         LA    RC,TWO(RC)              INCRE 'COLUMN NO.' COUNTER BY 2
         B     RCE1650                 BRANCH TO CHECK NEXT COLUMN
RCE2100  EQU   *
         SR    RPAR,RPAR               ZERO OUT REGISTER
         IC    RPAR,ZERO(RDEB)         SAVE CHAR BEING POINTED TO
         SLA   RPAR,8                  PREPARE FOR SECOND DIGIT OF NO.
RCE2150  EQU   *
         LA    RDEB,ONE(RDEB)          INCRE FORMAT SEARCH ADDR. BY 1
         LA    RC,ONE(RC)              INCRE 'COLUMN NO.' COUNTER BY 1
         B     RCE1650                 BRANCH TO CHECK NEXT COLUMN
RCE2200  EQU   *
         CLI   DXDEBECC,LFTPAREN       LEFT PAREN IN THIS FORMAT COLUMN
         BE    RCE2000                 YES, BRANCH
         CLI   DXDEBECC+ONE,COMMA      COMMA IN THIS FORMAT COLUMN
         BNE   RCE4450                  NO BRANCH
         CLI   DXDEBECC+THRE,RGTPAREN  RGT. PAREN IN THIS FORMAT COLUMN
         BNE   RCE2400                 NO, BRANCH
RCE2300  EQU   *
         IC    RJ,ZERO(RDEB)           GET NUMBER BEING POINTED TO
         LA    RDEB,TWO(RDEB)          INCRE FORMAT CARD POINTER BY 2
         LA    RC,TWO(RC)              INCRE 'COLUMN NO.' COUNTER BY 2
         CLI   ZERO(RDEB),BLANK        IS FORMAT COMPLETED YET
         BE    RCE3350                  YES GO CK COLUMN 72
RCE2350  EQU   *
         CLI   ZERO(RDEB),COMMA        IS THERE STILL ANOTHER OPERAND
         BNE   RCE2375                 NO, BRANCH
         CLI   ONE(RDEB),BLANK         IS NEXT COLUMN BLANK
         BNE   RCE3400                 NO, BRANCH
RCE2375  EQU   *
         CLI   DXCCW2+SEVENTY1,BLANK   IS COLUMN 72 BLANK
         BE    RCE4500                 YES, BRANCH
         B     RCE3400                 BRANCH TO PROCESS THIS OPERAND
RCE2400  EQU   *
         SR    RJ,RJ                   ZERO OUT REG. 15
         IC    RJ,ZERO(RDEB)           GET NUMBER BEING POINTED TO
         SLA   RJ,8                    PREPARE FOR SECOND DIGIT OF NO.
         B     RCE2150                 BRANCH TO GET SECOND DIGIT
RCE2600  EQU   *
         CLI   DXDEBECC+TWO,COMMA      COMMA IN THIS COLUMN
         BE    RCE2800                  YES BRANCH
         CLI   DXDEBECC+TWO,RGTPAREN   RIGHT PAREN IN THIS COLUMN
         BNE   RCE2950                  NO BRANCH
         CLI   DXDEBECC+THRE,BLANK     END OF FORMAT CARD
         BE    SETEXCP                 YES GO SET UP OMR/RCE CCW
         B     RCE2150                 BRANCH
*
RCE2800  EQU   *
*
*
         CLI   DXDEBECC+ONE,RGTPAREN    RIGHT PAREN IN THIS COLUMN
         BE    RCE2150                  GO INCREMENT COUNTERS BY ONE
RCE2900  EQU   *
         CLI   DXDEBECC+ONE,LFTPAREN   LEFT PAREN IN THIS COLUMN
         BE    RCE4500                 YES, BRANCH
         B     RCE2150                 NO, BRANCH
RCE2950  EQU   *
*
*
         CLI   ZERO(RDEB),LFTPAREN      LEFT PAREN IN THIS COLUMN
         BE    RCE2150                  YES BRANCH
         CLI   ZERO(RDEB),BLANK         BLANK IN THIS COLUMN
         BNE   RCE4500                  NO GO SET UP TO ABEND
RCE3000  EQU   *
         CLI   DXCCW2+SEVENTY1,BLANK   COLUMN 72 BLANK
         BE    SETEXCP                 YES, GO TO SET UP OMR/RCE CCW
         LA    RUCB,THRE               LOAD VALUE OF 3 IN WORK REGISTER
         SR    RDEB,RUCB               PT. BACK 3 COL. ON FORMAT CARD
         MVC   JFCBXPDT(THRE),ZERO(RDEB)  SAVE LAST 3 CHAR OF THIS CD.
         B     RCE1680                 BRANCH
RCE3100  EQU   *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*        TEST FIRST 15 COLUMNS OF CONTINUATION CARDS.  IF OTHER THAN
*        BLANK(X'40'), THE FORMAT IS INVALID.  IF COLUMNS ARE BLANK
*        THEN MOVE INTO COLUMNS 13,14 AND 15 OF THIS FORMAT CARD
*        THE 3 CHARS. SAVED FROM PREVIOUS CARD.
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         LA    RC,ONE                  SET REGISTER TO ONE
         LA    RDEB,DXCCW2
         LA    RUCB,D15                SET UP TO TEST 1ST 15 COL. OF CD
RCE3200  EQU   *
         CLI   ZERO(RDEB),BLANK        IS THIS COL. OF FORMAT CD BLANK
         BNE   RCE4500                 NO, BRANCH
         CLR   RC,RUCB                 POINTING AT COL. 15 YET
         BL    RCE3300                 NO, BRANCH
         MVC   DXCCW2+TWELVE(THRE),JFCBXPDT  PUT SAVED CHARS. IN 13-15
         L     RJ,DXDEBEHH             RELOAD LAST NUMBER
         B     RCE2150                 BRANCH
RCE3300  EQU   *
         LA    RDEB,ONE(RDEB)          INCRE FORMAT POINTER BY 1
         LA    RC,ONE(RC)              INCREMENT COUNTER BY ONE
         B     RCE3200                 BR TO CHECK FOR BLANK COLUMN
RCE3350  EQU   *
*
*
         CLI   DXCCW2+SEVENTY1,BLANK    IS COLUMN 72 BLANK
         BE    RCE3400                  IF SO GO CONVERT IT TO BINARY
         LA    RUCB,D72                SET UP TO TEST FOR COLUMN 72
         CLR   RC,RUCB                  IS THIS COLUMN 72 OR LOWER
         BNH   RCE4500                  IN NOT GO SET UP TO ABEND
RCE3400  EQU   *                       IS NO. SAVED FROM LAST OPERAND
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*        TEST LAST NO. OF PREVIOUS OPERAND AGAINST FIRST NO. OF
*        CURRENT OPERAND.  IF LAST NO. IS LESS THAN FIRST NO., THE
*        FORMAT IS INVALID.
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         CLR   RD,RPAR                 IS PRIVIOUS LAST NUMBER
*                                      < CURRENT FIRST NUMBER
         BNL   RCE4500                 IF NO THEN BRANCH
         CLR   RPAR,RJ                 IS CURRENT FIRST NUMBER @ZA01942
*                                      <= CUREENT LAST NUMBER  @ZA01942
         BH    RCE4500                 IF NO THEN BRANCH TO    @ZA01942
*                                      ABEND                   @ZA01942
         STH   RPAR,DXDEBSHH           GET FIRST NUMBER OF OPERAND
EXPACK   PACK  DXCCW12(EIGHT),DXDEBSHH(TWO) PACK THE NUMBER
         CVB   RPAR,DXCCW12            PUT NO. IN ARITHMETIC FORM
         ST    RJ,DXDEBEHH             GET LAST NUMBER  OF OPERAND
         PACK  DXCCW12(EIGHT),DXDEBEHH(FOUR)     PACK THE NUMBER
         CVB   RJ,DXCCW12              PUT NO. IN ARITHMETIC FORM
         LTR   RD,RD                   IS THIS THE FIRST FORMAT OPERAND
         BZ    EEEE                    YES, BRANCH
         STH   RD,DXDEBSHH             GET NO. SAVED FROM LAST OPERAND
         EX    RE,EXPACK               GO TO PACK THE NUMBER
         CVB   RD,DXCCW12              PUT NO. IN ARITHMETIC FORM
EEEE     EQU   *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*        IF NUMBER IS GREATER THAN 80 OR EQUAL TO 0, THEN THE FORMAT
*        IS INVALID.
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         LA    RTIOT,D80               SET WORK REG TO 80
         CLR   RPAR,RTIOT              IS FIRST NO. MORE THAN 80
         BH    RCE4500                 YES, BRANCH
         CLR   RJ,RTIOT                IS LAST FIELD OF OPERAND > 80
         BH    RCE4500                 YES, BRANCH
         LTR   RPAR,RPAR               IS FIRST NO. = 0
         BZ    RCE4500                 YES, BRANCH
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*        FOR VALID USE OF OMR, THE DIFFERENCE BETWEEN THE LAST NO.
*        OF THE PREVIOUS OPERAND AND FIRST NO. OF CURRENT OPERAND
*        MUST EQUAL TO 2 OR MORE.
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         TM    DCBMODE,OMR             OMR SPECIFIED
         BNO   RCE3600                 NO, BRANCH
         LTR   RD,RD                   IS THIS THE 1ST COMPLETE OPERAND
         BC    8,RCE3500               YES, BRANCH
         LA    RD,TWO(RD)              INCREMENT SAVED NUMBER BY 2
         SR    RD,RPAR                 SUB. 1ST NO. OF CURR OPERAND TO
         LTR   RD,RD                   VALID SPACE PROVIDED FOR OMR USE
         BP    RCE4500                 NO, BRANCH
RCE3500  EQU   *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*        IF THE  TWO NUMBERS OF THIS OPERAND ARE NOT BOTH EVEN OR
*        BOTH ODD, THEN THE FORMAT IS INVALID FOR OMR.
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         STH   RC,DXDEBSCC
         SR    RB,RB
         LR    RC,RPAR                 LOAD FIRST NO. OF OPERAND IN R13
         LA    RUCB,TWO                SET DIVISOR TO TWO
         DR    RB,RUCB                 DIVIDE FIRST NO. TO GET REMAINDR
         LR    RTIOT,RB                SAVE REMAINDER FOR NEXT TEST
         SR    RB,RB
         LR    RC,RJ                   PUT LAST NO. OF OPERAND IN REG13
         DR    RB,RUCB                 DIVIDE LAST NO. TO GET REMAINDER
         CLR   RB,RTIOT                ARE BOTH NO. EITHER EVEN OR ODD
         BNE   RCE4500                 NO, BRANCH
         SR    RC,RC
         LH    RC,DXDEBSCC
RCE3600  EQU   *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*        BUILD FORMAT TRANSLATION FOR THE DEVICE AS FOLLOWS:  PLACE
*        CHARACTER IN EACH OMR OR RCE COLUMN AS SPECIFIED VIA THE
*        USER'S FORMAT CARD(S).
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         LA    RB,DXLBL                GET @ OF AREA USED FOR TRANSLATN
         LA    RUCB,ONE                SET WORK REG TO 1
         SR    RB,RUCB
         AR    RB,RPAR                 POINT TO COL. SPEC. ON FORMAT CD
         TM    DCBMODE,OMR             OMR SPECIFIED
         BNO   RCE3700                 NO, RCE ASSUMED, BRANCH
         LA    RUCB,TWO                SET WORK REG TO 2
RCE3700  EQU   *
         MVI   ZERO(RB),Z              PUT CHAR IN COL. TO BE RD IN OMR
         CLR   RPAR,RJ                 HAVE CHAR BEEN PLACED IN ALL COL
         BE    RCE3900                 YES, BRANCH
         AR    RPAR,RUCB               POINT TO NEXT FORMAT COLUMN
         AR    RB,RUCB                 INCREMENT FORMAT AREA ADDRESS
         B     RCE3700                 BR TO PLACE CHAR IN THIS COLUMN
RCE3900  EQU   *
         L     RD,DXDEBEHH             SAVE LAST NO. IN THIS OPERAND
         LA    RUCB,D80                SET UP TO TEST FOR FORMAT CD IND
         CLR   RJ,RUCB                 LAST NUMBER = 80
         BNE   RCE1600                 NO, BRANCH
         CLI   ZERO(RDEB),BLANK        IS NEXT COL OF FORMAT CARD BLANK
         BNE   RCE4500                 NO, BRANCH
         CLI   DXCCW2+SEVENTY1,BLANK   YES, THEN IS CONTINUATION SPEC
         BNE   RCE4500                  YES BRANCH TO ABEND
SETEXCP  EQU   *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*        LOAD PROPER OP CODE (X'11' FOR RCE OR X'31' FOR OMR) AND
*        DATA ADDRESS INTO CCW.
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
         MVI   DXCCW1,OMRCMD            SET OMR COMMAND IN CCW
         TM    DCBMODE,OMR             OMR SPECIFIED
         BO    RCE4200                  BRANCH TO PUT DATA ADDR. IN CCW
         MVI   DXCCW1,RCECMD           SET RCE CMD IN CCW
RCE4200  EQU   *
         LA    RF,DXLBL                GET DATA ADDRESS
         B     RCE1300                 BR TO CONTINUE CONSTRUCTION CCW
RCE4300  EQU   *
         SR    RB,RB                    SET ENTRY FOR BUFFER CHECKING
         B     RCE4600                  GO LOAD ID OF IGG0197M
RCE4450  EQU   *
*
*
         CLI   DXDEBECC,COMMA           COMMA IN THIS COLUMN
         BNE   RCE4500                  IF NOT GO SET UP TO ABEND
         CLI   DXDEBECC+THRE,RGTPAREN   RIGHT PAREN IN THIS COLUMN
         BE    RCE2300                  YES GO INCREMENT COUNTERS
         LA    RUCB,D71                SET UP TO CHECK FOR COLUMN 71
         CLR   RC,RUCB                  IS THIS COLUMN 71
         BE    RCE2300                  IF SO GO INCREMENT COUNTERS
RCE4500  EQU   *
         LA    RB,FIVE                 LOAD REG. WITH ERROR PARAMETER
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*        GET IDTTRL OF NEXT EXECUTOR FROM WTG TABLE.
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
*
RCE4600  EQU   *
*
*
         MVC   DXCCW6(1),DCBEROPT      SAVE FOR QSAM           @ZA15324
         ST    RB,DXCCW11              STORE CODE IN DCB WORK AREA
*                                                              @ZA10074
         LA    RF,SOPN7M               GET ID OF NEXT MODULE
XCTLGOGO EQU   *
         L     RPAR,DXDEBAPP           RESTORE PARAMETER LIST
         LA    RJ,DXDCB
         ST    RJ,IOBWGHT
         MVC   DXDEBAPP(EIGHT),DSCEXT1+NINE RESTORE WORK
         MVC   DXDEBSCC(EIGHT),DSCEXT2+SEVEN AREA AFTER USAGE
         MVC   ZERO(FIVE,RWTGC),ZERO(RF)
RELOOP   EQU   *
         LA    RWTGC,WGOFF(ZERO,RWTGC)
         LA    RPARC,PLOFF(ZERO,RPARC)
         CLC   ZERO(TWO,RWTGC),AMIDCNST
         BCR   8,RBASE                 BRANCH IF EQUAL
         CLC   ZERO(TWO,RWTGC),OPIDCNST
         BC    7,RELOOP                BRANCH IF NOT EQUAL
         LR    RPARC,RPAR
         LA    RWTGC,WAOFF(ZERO,RWTG)
ZCHEK    CLI   ZERO(RWTGC),ZERO
         BC    7,TCTLRTN               BRANCH IF NOT EQUAL
         LA    RWTGC,WGOFF(ZERO,RWTGC)
         LA    RPARC,PLOFF(ZERO,RPARC)
         BC    15,ZCHEK                BRANCH
TCTLRTN  EQU   *
         MVC   SIX(TWO,RWTG),ZERO(RWTGC)
         MVC   D14(THRE,RWTG),TWO(RWTGC)
         LA    RJ,DXCCW12
         XCTL  DE=(RWTG),SF=(E,(15))
***********************************************************************
*                      DECLARE WTG OFFSETS
WAOFF    EQU   32
WGOFF    EQU   8
PLOFF    EQU   4
         DS    0F
F000     DC    XL4'0000F000'           WORK DC TO BE LOADED IN REG
OPIDCNST DC    C'0S'                   ID
AMIDCNST DC    C'7L'                   THIS MODULE'S ID
MYFRMT   DC    C' FORMAT ('            FIRST NINE COL. OF FORMAT CD
***********************************************************************
*
         ORG   IGG0197L+1008           LEAVE MAINT SPACE AS REQUIRED
SOPN7M   DC    C'7M    '               IDTTRL
         DC    H'0'                    END OF TABLE MARKER
         DC    F'0'                    PAD TABLE TO DBL WORD BOUNDRY
         DC    C'019'                  OPEN SVC CODE
         DC    AL1(126)                DBL WD CNST TO TOP OF WTG TABLE
***********************************************************************
*
*                                   DSECTS
*
         DCBD  DSORG=PS
*
**
***
****
**
***
**
FORCORE  DSECT
         IECDSECT
         END
