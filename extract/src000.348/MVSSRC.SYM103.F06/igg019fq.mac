         TITLE 'IGG019FQ-EOB ROUTINE FOR ALL 3525 PRINT FILES'
         START
***********************************************************************
* AOS1 AND AOS2 ADDITIONS/CHANGES/DELETIONS                      Y01898
*A856000                                                          M1866
*D324000                                                        YA00439
*                                                              @ZA06630
*A033500,033700,036200,080500-080700,081100-081300,            @ZA32137
*A086500-086600,227600-227800                                  @ZA32137
*
*NAME--              IGG019FQ
*
*TITLE--             'IGG019FQ' - EOB ROUTINE FOR ALL 3525 PRINT FILES
*
*STATUS CHANGE LEVEL 000
*                    THIS MODULE WAS PUT INTO 'OS' RELEASE 21.0 AS A
*                    NEW MODULE
*
*MODULE TYPE--       END OF BLOCK
*
*FUNCTION/OPERATION--SAM END OF BLOCK ROUTINE FOR ALL 3525 PRINT
*                    FILES. IT TESTS FOR SEQUENCE FLAGS, IN THE CASE
*                    OF DEVICE ASSOCIATED DATA SETS, TO VERIFY PROPER
*                    ISSUANCE OF READ, PUNCH AND PRINT COMMANDS. IT
*                    INCREMENTS THE LINE POSITION COUNTER (DCBLNP) OR
*                    RESETS IT WHEN NECESSARY. MODIFIES COMMAND
*                    BYTE IN WRITE CCW TO INCICATE LINE ON WHICH
*                    PRINTING SHOULD BE DONE. IT SETS UP COMMAND
*                    BYTE IN THE CCW.  THE BUFFER ADDRESS AND RECORD
*                    LENGTH ARE PASSED FROM THE PUT OR WRITE ROUTINE.
*                    THE CHANNEL PROGRAM IS SCHEDULED WITH AN EXCP
*                    MACRO.
*
*ENTRY POINT--       IGG019FQ FROM WRITE OR PUT ROUTINES:
*                    LA    2,DCB
*                    LA    3,IOB
*                    L     15,76(DCB)       FOR BSAM
*                    L     15,92(DCB)       FOR QSAM
*                    BALR  14,15
*
*INPUT--             DCB ADDRESS IN REGISTER 1
*                             DCBIOBA---IOB ADDRESS
*                             DCBLNP----LINE POSITION COUNTER
*                             DCBDEBAD--DEB ADDRESS
*                             DCBRECFM--RECORD FORMAT
*
*OUTPUT--            IOB
*                    DCBLNP INCREMENTED OR RESET TO INCICATE LINE
*                             POSITION.
*
*EXTERNAL ROUTINES-- IOS SUPERVISOR (EXCP AND WAIT)
*
*EXITS-NORMAL--      RETURN TO CALLING ROUTINE ON REGISTER 14.
*
*EXITS-ERROR--       003 - ABEND--ASSOCIATED DATA SET I/O SEQUENCE
*                    ERROR. RETURN CODE OF 3 IN REGISTER 15.
*
*TABLES/WORKAREA--   NONE
*
*ATTRIBUTES--        REENTRANT, REUSABLE
*
***********************************************************************
WRKR0    EQU   0
WRKR1    EQU   1
DCBR     EQU   2                       DCB ADDRESS UPON ENTRY
IOBAR    EQU   3                       IOB ADDRESS UPON ENTRY
BUFR     EQU   4                       BUFFER ADDRESS
LENR     EQU   5                       LENGTH OF RECORD
R5       EQU   5                       USED IN OIL AND NIL     @ZA32137
OPR      EQU   6                       COMMAND BYTE
R6       EQU   6                       USED IN OIL AND NIL     @ZA32137
SAVR     EQU   13
RETR     EQU   14                      CALLING ROUTINE RETURN ADDRESS
DEBR     EQU   7                       DEB ADDRESS
WRKR2    EQU   5                        WORK REGISTER
WRKR3    EQU   9
R9       EQU   9                       USED IN OIL AND NIL     @ZA32137
RLINE    EQU   10
BASR     EQU   15                      BASE REGISTER
SAVE1    EQU   32
SAVE9    EQU   64
SAVE11   EQU   16
SEQFLAG  EQU   X'08'
SEQFLGOF EQU   X'F7'
SEGCODE  EQU   X'02'                   FIRST BYTE OF SEGMENT CODE
ZERO     EQU   X'00'
ONE      EQU   1
USED     EQU   X'7F'                   DCB USED INDICATOR
IOBCC    EQU   X'40'
IOBOV    EQU   X'80'                   IOB PRTOV INDICATOR
CLEAR    EQU   X'00'                   CLEAR BYTE MASK
CCOFF    EQU   X'BF'
SLI      EQU   X'20'
FOUR     EQU   4
CNTRL    EQU   X'02'
COMCH    EQU   X'40'                   CCW COMMAND CHAIN BIT
RP       EQU   X'60'
FORMV    EQU   X'40'                   VARIABLE RECORD FORMAT MASK
FORMU    EQU   X'C0'                   UNDEFINED RECORD FORMAT MASK
PRTOV    EQU   X'30'                   PRTOV MASK
MCHAR    EQU   X'02'                   MACHINE CONTROL CHARACTER MASK
ASCII    EQU   X'04'                   ASCII CONTROL CHARACTER MASK
THRE     EQU   X'03'
PW       EQU   X'30'                   PH/PRT    ASSOC. DATA SET MASK
RW       EQU   X'50'
CHN9OF   EQU   X'20'
CHN12OF  EQU   X'10'
LINE17   EQU   X'11'
LINE23   EQU   X'17'
SPACE1   EQU   X'40'
SPACE2   EQU   X'F0'
SPACE3   EQU   X'60'
SUPPRESS EQU   X'4E'
SKCHAN1  EQU   X'F1'
SKCHAN2  EQU   X'F2'
SKCHAN10 EQU   X'C1'
SKCHAN9  EQU   X'F9'
SKCHAN12 EQU   X'C3'
LNEDLT   EQU   X'07'                   MASK TO DELETE LINE CNT FROM CCW
TWENTY5  EQU   X'19'
FDSS     EQU   X'23'
T        EQU   X'02'
PRTOV9   EQU   X'20'                   CHANNEL 9 MASK
PRTOV12  EQU   X'10'                   CHANNEL 12 MASK
LINE1    EQU   X'0D'                   PRINT LINE ONE OP CODE
OFMASK   EQU   X'CF'                   OVERFLOW MASK
LINE18   EQU   18
LINE24   EQU   24
Z0       EQU   0
D2       EQU   2
A19      EQU   19
*
IGG019FQ CSECT
*
*
*
         USING IGG019FQ,BASR
         USING DEBBASIC,DEBR
         USING IHADCB,DCBR
         USING IOB,IOBAR
*
*        ENTRY POINT
*
         STM   WRKR3,RLINE,SAVE9(SAVR)  SAVE REGS. 9 AND 10
         L     DEBR,DCBDEBAD           GET DEB ADDRESS
PRNT1000 EQU   *
         TM    DCBFUNC,PW              (RD/)PCH/PRT ASSOC. DATA SET
         BO    PRNT1100                YES, BR TO CK FOR PRINT SEQ FLAG
         TM    DCBFUNC,RW              READ/PRINT ASSOC. DATA SET
         BO    PRNT1400                YES, BR TO CK FOR PRT/RD SEQFLGS
         B     PRNT1500                BR TO TEST FOR CONTROL CHARACTRS
LEVELID  DC    C'04'                   MODULE LEVEL CONTROL
PRNT1100 EQU   *
         TM    DCBQSWS,SEQFLAG         PRINT SEQUENCE FLAG ON
         BZ    PRNT1300                NO, BR TO CK FOR PUNCH SEQ. FLAG
PRNT1200 EQU   *
         LA    WRKR1,CCW2OP            GET ADDRESS OF PRINT CCW
         ST    WRKR1,IOBSTAD           STORT PRINT CCW @ IN IOB AS PTR.
         B     PRNT1500                BR TO CK FOR CONTROL CHARACTERS
PRNT1300 EQU   *
         CLC   DEBPDCBA(THRE),ZEROES   PUNCH DCB OPEN YET
         BE    PRNTWTP                 NO, BRANCH
         L     DCBR,DEBPDCB            GET PUNCH DCB ADDRESS
         TM    DCBQSWS,SEQFLAG         PUNCH SEQUENCE FLAG ON
         BZ    PRNTWTP                 NO, BR TO WRITE ERROR MESSAGE
*                                      TRUN SEQUENCE FLAG OFF  @ZA32137
         NIL   DCBQSWS,SEQFLGOF,REF=DCBQSWS,WREGS=(R5,R6,R9)   @ZA32137
PRNT1350 L     DCBR,DEBDCBAD           GET CURRENT DCB ADDRESS AGAIN
*                                      TURN ON PRINT SEQ FLAG  @ZA32137
         OIL   DCBQSWS,SEQFLAG,REF=DCBQSWS,WREGS=(R5,R6,R9)    @ZA32137
         B     PRNT1200                BR TO PUT CCW ADDRESS IN IOB
PRNT1400 EQU   *
         TM    DCBQSWS,SEQFLAG         PRINT SEQUENCE FLAG ON
         BO    PRNT1200                YES, BR TO SEE CHAN PROG START@
         CLC   DEBRDCBA(THRE),ZEROES   READ DCB OPEN YET
         BE    PRNTWTP                 NO, BRANCH
         L     DCBR,DEBRDCB            GET READ DCB ADDRESS
         TM    DCBQSWS,SEQFLAG         READ SEQUENCE FLAG ON
         BZ    PRNTWTP                 NO, BR TO WRITE ERROR MESSAGE
*                                      TURN READ SEQ FLAG OFF  @ZA32137
         NIL   DCBQSWS,SEQFLGOF,REF=DCBQSWS,WREGS=(R5,R6,R9)   @ZA32137
         B     PRNT1350                BR TO TURN ON PRINT SEQ. FLAG
PRNT1500 EQU   *
         LH    LENR,CCW1LN             LOAD BLOCK LENGTH
         L     BUFR,CCW1AD             LOAD BUFFER ADDRESS
         MVC   CCW2AD+ONE(THRE),CCW1AD+ONE MOVE BUF @ TO 2ND CCW
DDA2     EQU   *
         TM    DCBRECFM,ASCII          ASCII SPECIFIED
         BO    PRNT1550                YES, BRANCH
         TM    DCBRECFM,MCHAR          MACHINE CONTROL SPECIFIED
         BZ    PRNT4000                NO, BRANCH
         SR    OPR,OPR                 CLEAR WORK REG          @ZA06630
         SR    WRKR1,WRKR1             CLEAR WORK REG          @ZA06630
         IC    OPR,ZERO(BUFR)          OBTAIN MACHINE OP CODE  @ZA06630
         SRL   OPR,THRE                GET LINE NUMBER         @ZA06630
         IC    WRKR1,DCBLNP            LOAD LAST LINE PRINTED  @ZA06630
         CR    OPR,WRKR1               COMPARE CURRENT TO OLD  @ZA06630
         BH    DDA3                    IF CURRENT GREATER THAN @ZA06630
*                                      OLD, THEN BRANCH        @ZA06630
         MVI   DCBLNP,ZERO             ZERO TO FEED NEXT CARD  @ZA06630
DDA3     EQU   *                                               @ZA06630
         BCTR  LENR,Z0                 DECREMENT
         IC    OPR,ZERO(BUFR)          GET CONTROL CHAR FORM BUFFER
         LA    BUFR,ONE(BUFR)          INCREMENT BUFFER ADDR BY 1
         TM    DCBFUNC,RP              READ OR PUNCH BIT ON
         BNZ   DDA4                    BRANCH IF ONE OR MIXED
         CLI   DCBLNP,ZERO             IS LINE POSITION EQUAL TO ZERO
         BNE   DDA31                   NO, BRANCH
         LA    WRKR1,CCW1OP            GET ADDRESS OF FD,SS CCW
         ST    WRKR1,IOBSTAD           STORE PRINT CCW ADDR IN IOB
         B     DDA4                    BRANCH TO SET UP CCW
DDA31    EQU   *
         LA    WRKR1,CCW2OP            GET ADDRESS OF PRINT CCW
         ST    WRKR1,IOBSTAD           STORE PRINT CCW @ IN IOB AS PTR
DDA4     EQU *
         ST    BUFR,CCW2AD             STORE RECORD ADDRESS IN CCW
         NI    IOBIOS,CCOFF            TURN OFF CC FLAG IN IOB
         OI    CCW2FL,SLI              TURN ON SILI FLAG IN CCW
         STH   LENR,CCW2LN             RESTORE LENGTH
         STC   OPR,CCW2OP              STORE CMD BYTE IN WRITE CCW
         SRL   OPR,3                   SHIFT OUT PRINT OP CODE
         STC   OPR,DCBLNP              STORE LINE POSITION IN DCB
         B     PRNT4300                BRANCH
PRNT1550 EQU   *
         L     WRKR2,CCW1OP            GET BUFFER ADDRESS
         LR    WRKR1,WRKR2             GET BUFFER ADDRESS
         LA    WRKR1,ONE(WRKR1)        INCREMENT BUFFER ADDRESS
         IC    OPR,CCW2OP              SAVE PRINT COMMAND BYTE
         ST    WRKR1,CCW2OP            STORE BUFFER ADDRESS IN CCW
         STC   OPR,CCW2OP              RESTORE OP CODE IN CCW
         SR    WRKR1,WRKR1             ZERO OUT WORK REGISTER FOR USAGE
         IC    WRKR1,DCBLNP            GET LINE COUNTER FROM DCB
         CLI   ZERO(WRKR2),SPACE1      IS CONTROL CHAR FOR SPACE 1
         BNE   PRNT1700                NO, BRANCH
         LA    WRKR3,ONE               YES, GET COUNT OF ONE
PRNT1600 EQU   *
         AR    WRKR1,WRKR3             INCREMENT LINE COUNTER
         CLI   DCBLNP,ZERO             LINE POSITION EQUAL ZERO
         BNE   PRNT3000                NO, BRANCH
         STC   WRKR1,DCBLNP            UPDATE DCB LINE POSITION
PRNT1650 TM    DCBFUNC,RP              READ OR PUNCH BIT ON
         BNZ   PRNT3000                BRANCH ON ONE OR MIXED
         B    PRNT3200                 BRANCH TO SET UP CCW
PRNT1700 EQU   *
         LA    WRKR3,D2                SET UP FOR LINE COUNT INCREMENT
         CLI   Z0(WRKR2),SPACE2        CONTROL CHARACTER FOR SPACE 2
         BE    PRNT1600                IF SO, INCREMENT LINE COUNT BY 2
PRNT1800 EQU   *
         LA    WRKR3,THRE              SET UP FOR LINE COUNT INCREMENT
         CLI   Z0(WRKR2),SPACE3        CONTROL CHARACTER FOR SPACE 3
         BE    PRNT1600                IF SO, INCREMENT LINE COUNT BY 3
PRNT2000 EQU   *
         CLI   ZERO(WRKR2),SKCHAN1     CONTROL CHAR FOR SKIP TO CHAN 1
         BNE   PRNT2100                NO, BRANCH
PRNT2050 EQU   *
         LA    WRKR1,ONE               EQUATE LINE COUNTER TO 1
         STC   WRKR1,DCBLNP            STORE LINE COUNT IN DCB
         B     PRNT3100                BRANCH
PRNT2100 EQU   *
         LA    RLINE,THRE              SET UP LINE COUNTER MASK
         LA    WRKR3,SKCHAN2           LOAD SKIP TO CHAN 2 MASK
         IC    ONE,ZERO(WRKR2)
PRNT2200 EQU   *
         CR    WRKR3,ONE
         BH    PRNT2400                NO, MUST BE GREATER THAN CHAN 9
         BE    PRNT2300                YES, BR TO CK FOR VALIDITY
         LA    WRKR3,ONE(WRKR3)        INCRE SKIP MASK BY 1
         LA    RLINE,D2(RLINE)         INCRE LINE COUNTER MASK BY 2
         B     PRNT2200                BRANCH
PRNT2300 EQU   *
         CLI   ZERO(WRKR2),SKCHAN9     IS CHARACTER INVALID
         BNH   PRNT2350                NO, BRANCH
PRNT2325 EQU   *
         LA    WRKR3,ONE               YES, DEFAULT VALUE USED
         B     PRNT1600                BRANCH
PRNT2335 EQU   *
*
*
         CLI   ZERO(WRKR2),SKCHAN12     CONTROL CHARACTER VALID
         BH    PRNT2325                 NO INVALID CHAR. GO USE DEFAULT
PRNT2350 EQU   *
         LR    WRKR1,RLINE             REFLECT LINE POS. IN LINE CTR.
         IC    RLINE,DCBLNP            SAVE CURRENT LINE POSITION
         STC   WRKR1,DCBLNP            STORE LINE COUNTER IN DCB
         LTR   RLINE,RLINE             LINE COUNTER EQUALS ZERO
         BZ    PRNT1650                YES, BRANCH
         CR    WRKR1,RLINE             CURRENT LINE POS GT SKIP TO VALU
         BNH   PRNT3100                YES, BRANCH TO TEST FOR ASSOC DS
         B     PRNT2700                BRANCH
PRNT2400 EQU   *
         LA    RLINE,A19               LINE COUNTER MASK
         LA    WRKR3,SKCHAN10          LOAD SKIP TO CHAN 10 MASK
         LA    OPR,THRE                LOAD VALUE OF 3 INTO REG
         IC    ONE,ZERO(WRKR2)
PRNT2500 EQU   *
         CR    WRKR3,ONE
         BE    PRNT2335                 YES BRANCH TO CK FOR VALIDITY
         LA    WRKR3,ONE(WRKR3)        NO, INCREMENT CHAN MASK
         LA    RLINE,D2(RLINE)         INCREMENT LINE CTR MASK
         BCT   OPR,PRNT2500             LOOP THREE TIMES
         B     PRNT2325                 INVALID CHAR. GO USE DEFAULT
PRNT2700 EQU   *
         TM    DCBFUNC,RP              READ OR PUNCH BIT ON
         BZ    PRNT2750                NO, BRANCH
         IC    WRKR1,DCBLNP            GET THE LINE COUNT
         SLL   WRKR1,3                 SHIFT COUNT FOR CCW INSERTION
         NI    CCW2OP,LNEDLT           DELETE LINE CNT FROM OP CODE
         IC    WRKR2,CCW2OP            GET CCW OP CODE
         OR    WRKR2,WRKR1             OR LINE COUNT INTO OP CODE
         STC   WRKR2,CCW2OP            RESTORE OP CODE IN CCW
         B     PRNT2775                BRANCH TO FINISH CCW
PRNT2750 EQU   *
         LA    WRKR1,CCW2OP            GET CCW ADDRESS
         ST    WRKR1,IOBSTAD           LOAD CCW ADDR IN IOB
PRNT2760 EQU   *
         IC    WRKR1,DCBLNP            GET LINE COUNT FROM DCB
         SLL   WRKR1,3                 SHIFT COUNT FOR CCW INSERTION
         NI    CCW2OP,LNEDLT           DELETE LINE CNT FROM OP CODE
         IC    WRKR2,CCW2OP            GET CCW OP CODE
         OR    WRKR2,WRKR1             OR LINE COUNT INTO OP CODE
         STC   WRKR2,CCW2OP            RESTORE OP CODE IN CCW
PRNT2775 EQU   *
         MVC   CCW2OP+FOUR(FOUR),CCW1OP+FOUR  SET UP REST OF WRITE CCW
         TM    DCBRECFM,ASCII          ASCII SPECIFIED          YA00436
         BNO   PRNT2780                NO, BRANCH               YA00436
         LH    LENR,CCW1LN             LOAD BLOCK LENGTH        YA00436
         BCTR  LENR,Z0                 DECREMENT BYTE CNT BY 1  YA00436
         STH   LENR,CCW2LN             RESTORE LENGTH IN CCW    YA00436
PRNT2780 EQU   *                                                YA00436
         OI    CCW2FL,SLI              TURN ON SILI FLAG IN CCW
         NI    CCW2FL,CCOFF            TURN OFF CC FLAG IN CCW
         B     PRNT4300                BRANCH
PRNT2800 EQU   *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                   PRINT OVERFLOW ROUTINE
*           IF THE RIGHT OVERFOOW COND.EXIST A FEED SS WILL BE
*           PREFORMED BEFORE PRINTING ON LINE ONE OF NEXT CARD.
*
         TM    DCBDEVT+ONE,PRTOV9      OVERFLOW ON CH. 9
         BO    PRNT2810                YES, BRANCH
         TM    DCBDEVT+ONE,PRTOV12     OVERFLOW ON CH. 12
         BNO   PRNT4400                NO, BRANCH TO PRINT A LINE
         TM    DCBIFLGS,PRTOV12        LINE 24 BEEN PRINTED ON
         BO    PRNT2820                YES,  BRANCH
         B     PRNT4400                NO, BRANCH TO PRINT A LINE
PRNT2810 EQU   *
         TM    DCBIFLGS,PRTOV9         LINE 18 BEEN PRINTED ON
         BNO   PRNT4400                NO, BRANCH TO PRINT A LINE
PRNT2820 EQU   *
PRNT2900 EQU   *
         TM    DCBFUNC,RP              IS THIS AN ASSOC. DATA SET
         BM    PRNTWTP                 YES, BRANCH
         BO    PRNTWTP                 YES, BRANCH
         MVI   CCW1OP,FDSS             MOVE IN A FEED STACKER EL. CMD.
         MVI   CCW1FL,COMCH            SET CMD. CHAIN ON IN CCW
         MVI   DCBDEVT+ONE,ZERO        RESET DCB PRTOV INDICATOR
         LA    WRKR1,CCW1OP            GET FIRST CCW ADDRESS
         ST    WRKR1,IOBSTAD           LOAD CCW START ADDR. IN IOB
         MVI   DCBLNP,ONE              RESET LINE POSITION CTR. TO ONE
         MVI   CCW2OP,LINE1            SET LINE IN PRT CCW OP CODE TO 1
         B     PRNT4400                BRANCH
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
PRNT3000 EQU   *
         STC   WRKR1,DCBLNP            LOAD LINE CTR INTO DCB
         CLI   DCBLNP,TWENTY5          IS LINE CTR GREATER THAN 25
         BNH   PRNT2700                NO, BRANCH
         B     PRNT2050                YES, BR TO GET DEFAULT VALUE
PRNT3100 EQU   *
         TM    DCBFUNC,RP              READ OR PUNCH BIT ON
         BO    PRNTWTP                 BOTH ON, BRANCH FOR ERROR MSG.
         BM    PRNTWTP                 ONE  ON, BRANCH FOR ERROR MSG.
         OI    IOBIOS,IOBCC            TURN ON CC FLAG IN IOB
PRNT3200 EQU   *
         SLL   WRKR1,3                 SHIFT COUNT FOR CCW INSERTION
         NI    CCW2OP,LNEDLT           DELETE LINE CNT FROM OP CODE
         IC    WRKR2,CCW2OP            GET CCW OP CODE
         OR    WRKR2,WRKR1             OR LINE COUNT INTO OP CODE
         STC   WRKR2,CCW2OP            RESTORE OP CODE IN CCW
         LA    WRKR1,CCW1OP            GET CCW ADDRESS
         ST    WRKR1,IOBSTAD           STORE CCW ADDRESS IN IOB AS PTR
         B     PRNT2775                BRANCH
PRNT4000 EQU   *
         TM    DCBFUNC,T               TWO-LINE PRINT SPECIFIED
         BO    PRNT4100                YES, BRANCH
         CLI   DCBLNP,ZERO             LINE POSITION EQUALS ZERO
         BE    PRNT4200                YES, BRANCH
         TM    DCBMACRF+ONE,CNTRL      CNTRL SPECIFIED IN DCB
         BO    PRNT2700                YES, BRANCH
         CLI   DCBLNP,X'19'            LINE POSITION EQUALS 25
         BE    PRNT2900                YES, BRANCH
PRNT4020 IC    WRKR1,DCBLNP            LOAD DCBLNCT IN REGISTER
         LA    WRKR1,ONE(WRKR1)        INCREMENT AMOUNT BY ONE
PRNT4050 STC   WRKR1,DCBLNP            RESTORE AMOUNT IN DCB
         B     PRNT2700                BRANCH
PRNT4100 EQU   *
         CLI   DCBLNP,ZERO             LINE POSITION EQUALS ZERO
         BE    PRNT4200                YES, BRANCH
         CLI   DCBLNP,ONE              LINE POSITION EQUALS 1
         BNE   PRNT2900                NO, BRANCH
         IC    WRKR1,DCBLNP            YES, LOAD LINE CTR IN WORK REG
         LA    WRKR1,D2(Z0,WRKR1)      POINT TO LINE THREE
         B     PRNT4050                BRANCH
PRNT4200 EQU   *
         MVI   DCBLNP,ONE              SET LINE POSITION TO ONE
         IC    WRKR1,DCBLNP            GET LINE COUNT FROM DCB
         TM    DCBFUNC,RP              READ OR PUNCH BIT ON
         BO    PRNT2700                BOTH ON, BRANCH
         BM    PRNT2700                ONE  ON, BRANCH
         LA    WRKR1,CCW1OP            POINT TO 1ST CCW
         ST    WRKR1,IOBSTAD           STORE PRINT CCW @ IN IOB AS PTR
         B     PRNT2760                FINISH SETTING UP CCW
PRNT4300 EQU   *
         TM    DCBDEVT+ONE,PRTOV       PRINT OVERFLOW ISSUED
         BM    PRNT2800                YES, BRANCH TO OVERFLOW ROUTINE
PRNT4400 EQU   *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
*        THIS ROUTINE WILL SET UP FLAGS FOR AN OVERFOOW CONDITION.
*  NOTE- OVERFLOW IS DETECTED AFTER PRINTING THE LINE FOLLOWING THE
*        LINE CORRESPONDING TO CHANNEL 9 OR 12
*
         NI    DCBIFLGS,OFMASK         CLEAR OVERFOLW INDICATORS
         CLI   DCBLNP,LINE23           IS THIS CHANNEL 12 OR GREATER
         BL    PRNT4405                NO, BRANCH
         OI    DCBIFLGS,PRTOV12        SET CHAN. 12 OVERFLOW INDICATOR
         B     PRNT4410                BRANCH
PRNT4405 EQU   *
         CLI   DCBLNP,LINE17           IS THIS CHANNEL 9 OR GREATER
         BL    PRNT4410                NO, BRANCH
         OI    DCBIFLGS,PRTOV9         SET CHAN. 9 OVERFLOW INDICATOR
PRNT4410 EQU   *
         TM    DCBMACRF+ONE,CNTRL      CNTRL SPECIFIED IN DCB
         BZ    PRNT4415                NO, BRANCH
         IC    WRKR1,DCBLNP            GET LINE COUNT FROM DCB
         LA    WRKR1,ONE(WRKR1)        INCREMENT LINE COUNT BY ONE
         STC   WRKR1,DCBLNP            RESTORE LINE COUNT IN DCB
PRNT4415 EQU   *
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
         LA    WRKR1,IOBIOS
         EXCP  (1)
         NI    DCBCIND1,USED
*   RESTORE REGS. WHICH WILL NOT BE RESTORED IN THE PUT OR WRITE ROUTS.
         L     WRKR1,SAVE1(SAVR)        RESTORE REG. 1
         LM    WRKR3,RLINE,SAVE9(SAVR)  RESTORE REGS. 9 AND 10
         BR    RETR                    RETURN TO CALLING ROUTINE
*
         DS    0F                      ALIGN ON A FULLWORD BOUNDARY
ABEND3   DC    X'00000003'             AGEND CODE OF 3
ZEROES   DC    X'00000000'             WORK DC
PRNTWTP  EQU   *
         LR    RLINE,BASR              LOAD BASE ADDR INTO REG 10
         DROP  BASR                    DROP OLD BASE REG
         USING IGG019FQ,RLINE          SET UP NEW BASE
         WTO   'IEC950I 003-3 3525 ASSOCIATED DATA SET I/O SEQUENCE ERR*
               OR',ROUTCDE=(11),DESC=4
         LA    BASR,THRE               INDICATE ASSOCIATED DATA SET
*                                      PRINT SEQUENCE ERROR CODE TO BE
*                                      PASSED TO USER VIA REGISTER 15
         L     WRKR1,ABEND3            GET SAM ABEND ERROR CODE
         SLL   WRKR1,12                SET UP REGISTER 1 FOR ABEND
         ABEND (1),DUMP
MODID    DC    CL8'IGG019FQ'            MODULE ID              @ZA32137
FLAG     DC    CL8'@ZA32137'            FLAG                   @ZA32137
DATE     DC    CL8'&SYSDATE'            DATE OF LATEST FLAG    @ZA32137
         DC    12CL4'ZAPS'              MAINTENANCE AREA          M1866
*
*
*
*              THE FOLLOWING CNOP MUST REMAIN ON A HALF-WORD BOUNDARY
*
         CNOP  0,8                     PAD MODULE TO DBL. WD. BOUNDARY
*
*        DSECTS
*
*
IOB      DSECT
         DS    2F                      SPACE
IOBIOS   DS    1F                      SPACE
         DS    3F                      SPACE
IOBSTAD  DS    1F                      STAUS
         DS    3F                      SPACE
CCW1OP   DS    1F                      OP CODE
CCW1AD   EQU   CCW1OP                  CCW OP CODE
CCW1FL   DS    1H                      FLAGS
CCW1LN   DS    1H                      LENGTH
CCW2OP   DS    1F                      OP CODE
CCW2AD   EQU   CCW2OP                  OP CODE
CCW2FL   DS    1H                      FLAGS
CCW2LN   DS    1H                      LENGTH
         DCBD  DSORG=PS
         IEZDEB
         END
