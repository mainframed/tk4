         TITLE 'IGG0201P-CLOSE EXECUTOR FOR 3505 OMR AND RCE, AND 3525'
         START
IGG0201P CSECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*                                                       @ZA08168
* C 152000,159000                                              @ZA03830
* C 155000                                                     @ZA00861
* D 321200,344400,368300                                       @YM06152
* D 210500-220000,443400,204450,189500,413000,415000-416000    @YM06152
* D 507000-510000                                              @YM05576
* C 204450                                                     @YM05576
* C 1682000,1747000,1817000,1867000,1927000,19920000,20520000, @ZA29896
* C 2112000,2192000                                            @ZA29896
* A 235500-235850,252000-252350                                @ZA33956
*
*
* MODULE NAME= IGG0201P
*
* DESCRIPTIVE NAME= CLOSE EXECUTOR FOR 3525 ASSOCIATED DATA SETS OR
*                   3505 WITH OMR OR RCE USING BSAM AND QSAM
*
* COPYRIGHT= NONE
*
* STATUS = OS/VS2 REL. 3
*
* FUNCTION= THIS IS THE FIRST HALF OF THE CLOSE ROUTINE USED WHEN
*              OMR OR RCE IS SPECIFIED FOR THE 3505 AND WHEN
*              CLOSING 3525 ASSOCIATED DATA SETS.
*
*      OPERATION=    THE FOLLOWING OPERATIONS WILL BE PREFORMED BY
*                   THIS CLOSE EXECUTOR FOR BSAM/QSAM DATA SETS
*                   IF INPUT-1 DELETE ALL REQUEST FOR DATA (PURGE)
*                            2 RESET OMR OR RCE MODE VIA AN UNFLAGED
*                              FEED COMMAND IF THE DEVICE DID NOT
*                              PRESENT UNIT EXCEPTION WHICH IN ITSELF
*                              WOULD RESET THE OMR OR RCE MODE.
*                            3 ISSUE A FEED COMMAND CAUSING LAST CD TO
*                              BE FED AND STACKER SELECTED IF CNTRL WAS
*                              SPECIFIED.
*                            NOTE-THIS CARD FEED WILL BE IN ADDITION
*                                 TO THE FEED REFERENCED ABOVE IN
*                                 ITEM TWO IF OMR OR RCE WAS
*                                 SPECIFIED.
*
*                   IF OUTPUT- BSAM ASSUMES QUIESENCE
*                              QSAM WILL TRUNCATE AND FLUSH BUFFERS
*              FOR FORCE CLOSE, THIS ROUTINE WILL BE ENTERED   @YM06153
*              FOR QSAM OUTPUT DATA SETS ONLY.  IT WILL        @YM06153
*              PURGE ANY I/O INITIATED FOR PRIMING             @YM06153
*              INPUT BUFFERS.                                  @YM06153
*
*              THE 3525 HAS A PRINT STATION BETWEEN THE PUNCH-STATION
*              AND THE STACKER. THUS, CARD MOVEMENT AT CLOSE PRESENTS
*              AND UNUSUAL SITUATION. THE FOLLOWING RULES APPLY TO
*              CARD MOVEMENT WHEN THE 3525 FILE(S) ARE CLOSED.
*
*               FILE TYPE              FEED CAUSED BY CLOSE OF
*
*               READ                   READ
*               PUNCH                  PUNCH
*               PRINT                  PRINT
*               READ/PRINT             READ
*               READ/PUNCH/PRINT       READ
*               READ/PUNCH             READ
*               PUNCH/PRINT            PUNCH
*               PUNCH/INTERPRET        PUNCH
*
*                 NOTE- IF A DELIMITER CARD IS USED IT WILL STAY IN THE
*                       TRANSPORT UNTIL THE NEXT JOB CAUSES A CARD
*                       FEED OR AN NPRO IS PREFORMED.
*
* NOTES=
*   PATCH-LABEL= ZAPSZAPS
*
* MODULE TYPE=
*   PROCESSOR= ASSEM
*   ATTRIBUTES= REENTRANT
*
* ENTRY POINT= XCTL FROM IGG0201A
*   PURPOSE= TO CLOSE 3525 OR 3505 DATA SETS
*   INPUT= SEE DESCRIPTION OF REGISTERS
*
* EXIT NORMAL= XCTL TO IGG0201R
*
* EXIT ERROR=  XCTL TO IGG0201R
*
* EXTERNAL REFERENCES=
*   ROUTINES= DCBPUT(TO OUTPUT LAST BUFFER)
*   DATA AREAS= PSA
*               0/C/E WORK AREA (IECDSECT)
*   CONTROL BLOCKS=
*                  DCB-U,DEB-M,IOB-M,TCB-U,RB-U
*
* TABLES= WHERE TO GO TABLE (WTG)
*
* MACROS=
*       DEBCHK,MODESET,GETMAIN,FREEMAIN,XCTL,SYNCH
*       WAIT,PURGE,EXCP
*
* CHANGE ACTIVITY= VS/2 REL. 2 - Y02898,YM06152,YM05576
*
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
*        REGISTER USAGE
*
RE       EQU   0                       WORK/PARAMETER REGISTER
RF       EQU   1                       WORK/PARAMETER REGISTER
RDCB     EQU   2                       ADDRESS OF USER'S DCB
RBASE    EQU   3                       BASE REGISTER
RCORE    EQU   4                       ADDRESS OF CLOSE WORK AREA
RPAR     EQU   5                       PARAMETER LIST
RWTG     EQU   6                       START OF WTG
RPARC    EQU   7                       CURRENT ENTRY IN PARAMETER LIST
RWTGC    EQU   8                       CURRENT ENTRY IN WTG TABLE
RTCB     EQU   9                       ADDRESS OF CURRENT TCB    Y02898
RUCB     EQU   10                      UCB ADDRESS
RDEB     EQU   11                      DEB ADDRESS
RB       EQU   12                      WORK REGISTER
RC       EQU   13                      WORK REGISTER
RD       EQU   14                      WORK/PARAMETER REGISTER
RJ       EQU   15                      WORK/PARAMETER REGISTER
R2       EQU   2                                               @YM06152
R12      EQU   12                                              @YM06152
*
CLOSEB   EQU   X'20'
COMPLETE EQU   X'7F'
QSAMB    EQU   X'01'
BYTO     EQU   0                       DISPLACEMENT=0
ECBOS    EQU   4                       OFFSET TO ECB
ECBADOS  EQU   12                      OFFSET TO ECB ADDRESS
OUTPUT   EQU   X'07'                    MASK FOR OUTIN OR OUTPUT
SILIBIT  EQU   X'20'                   SUPPRESS INCORRECT LENGTH IND
TMFLAG   EQU   X'04'                   TM FLAG FOR CLOSE ROUTINE
UNUSED   EQU   X'80'                   DCB OPENED-NOT YET USED
ZERO     EQU   0                       DISPLACEMENT
ONE      EQU   1
TWO      EQU   2
THREE    EQU   3
FOUR     EQU   4
SIX      EQU   6
EIGHT    EQU   8
R3505    EQU   X'46'                   3505 DCB DEVICE TYPE MASK
P3525    EQU   X'4C'                   3525 DCB DEVICE TYPE MASK
RPMASK   EQU   X'60'
RWMASK   EQU   X'50'
PWMASK   EQU   X'30'
ERMASK   EQU   X'3F'
SLIFLAG  EQU   X'20'
WMASK    EQU   X'10'
WXMASK   EQU   X'14'
INMASK   EQU   X'0C'                    MASK FOR INOUT OR INPUT
CNTRL    EQU   X'02'
SEQMASK  EQU   X'08'                                           @ZA03830
SEQFDISP EQU   80                      ASSOCIATED DATA SET SQE. DISP.
DEBOFFST EQU   44
SEQOFF   EQU   X'C7'                MASK TO TURN OFF SEQ. FLAG @ZA00861
DEBRPTR  EQU   36
DEBPPTR  EQU   40
DEBWPTR  EQU   44
XMASK    EQU   X'04'                                           @ZA03830
LOCMASK  EQU   X'14'
WAOFF    EQU   32
WGOFF    EQU   8
PLOFF    EQU   4
UE       EQU   X'01'                   UNIT EXCEPTION MASK
STATUSN  EQU   20                      NORM  CH SCH DCB IOB OFFSET
ECBPNTR  EQU   12
CCW1     EQU   40
CPADDR   EQU   24
CCWFLAGS EQU   44
FDSS     EQU   X'23'                   FEED SS COMMAND
RW       EQU   X'50'
P        EQU   X'20'
W        EQU   X'10'
IRPD     EQU   X'E8'                   INT.,RD,PCH,DPI FLAGS   @ZA11813
WRTOP    EQU   X'40'
D14      EQU   14
PIODSID  EQU   X'B0'                   PURGE OPT               @YM06152
PALL     EQU   X'D0'                   PURGE OPT               @YM06152
SAVELG   EQU   76                      SAVE AREA LENGTH        @YM06152
KEYMASK  EQU   X'80'                   MASK TO DISTINGUISH     @YM06152
PPLLENTH EQU   12             LENGTH OF PURGE PARM LST         @YM06152
DCBTRUNC EQU   8              OFFSET INTO PUTRTF OF            @YM06152
*                             TRUNCATE ROUTINE                 @YM06152
*                                      SYSTEM AND USER KEY     @YM06152
*
         USING IHADCB,RDCB
         USING CSTART,RBASE
         USING DEBBASIC,RDEB
         USING FORCORE,RCORE
         USING PSA,0                                           @YM06152
         BALR  RBASE,RE                PUT BASE ADDR IN BASE REG
CSTART   EQU   *
         B     GETDCB                                          @ZA33956
         DC    CL8'IGG0201P'           MODULE NAME             @ZA33956
         DC    CL8'@ZA33956'           APAR ID                 @ZA33956
         DC    CL8'&SYSDATE'           DATE                    @ZA33956
*  SET CLOSE FLAG IN DCB AND INITALIZE REGISTERS AND WORKAREA , SAVE
*  ADDRESS OF DEB AND ISSUE PURGE MACRO
*
*
GETDCB   EQU   *                                               @ZA33956
         L     RCORE,FOUR(RWTGC)       GET DCB'S WORK AREA ADDR.
         MVI   DXCCW1,ZERO         CLEAR SWITCH                  Y02898
         MODESET KEYADDR=DXUKEY,WORKREG=15  USER KEY           @YM05576
         GETMAIN R,LV=SAVELG,SP=230     SAVE AREA              @YM06152
         MODESET EXTKEY=DATAMGT         DATA MANAGEMENT KEY    @YM06152
         ST    RF,SCWGETMA              SAVE AREA ADDR         @YM06152
         LR    RC,RF                    SAVE PTR TO SAVE AREA  @YM06152
         L     RDCB,DXPDCBAD            GET DATA MAN DCB ADDR  @YM06152
         L     RDEB,DCBDEBAD            GET DEB ADDRESS          Y02898
         L     RDCB,DXUDCBAD            USER DCB               @YM06152
*
         MODESET KEYADDR=DXUKEY,WORKREG=15  USER KEY           @YM06152
         L     RB,DCBIOBA              GET CURR IOB ADDR.      @YM05576
         LA    RB,0(RB)                CLEAR HI ORDER BYTE     @YM05576
         L     RUCB,DEBSUCBA            GET UCB ADDRESS          Y02898
         OI    DCBCIND2,CLOSEB          TURN ON CLOSE FLAG       Y02898
         TM    DEBOPATB,OUTPUT         IS DCB OPENED FOR OUTPUT
         BNO   AMCL1010                BRANCH IF NOT OUTPUT
*
         TM    DCBCIND2,QSAMB          DCB USING QSAM TO PROCESS
         BO    TRUNCATE                YES BRANCH
AMCL1010 EQU   *
         USING PPLDSID,RC              PURGE PARM LIST         @YM06152
         XC    PPLDSID(PPLLENTH),PPLDSID   CLEAR LIST          @YM06152
         ST    RDEB,PPLDSID            PUT DECB IN LIST        @YM06152
         MVI   PPLDSID,PALL            PURGE OPT TO QUISE      @YM06152
         TM    ZERO(RB),WRTOP          LAST OF A WRITE         @YM06152
         BZ    AMCL1015                NO                      @YM06152
         MVI   PPLDSID,PIODSID         OPT,HALT I/O DELETE,ETC @YM06152
AMCL1015 EQU   *
*
         PURGE (1)
*
*
         LA    RF,PPLCC       GET PARA LIST ECB ADDRESS        @YM06153
*
         WAIT  ECB=(RF)
         DROP  RC                                              @YM06153
*
         MODESET EXTKEY=DATAMGT     DATA MANAGEMENT KEY        @YM06152
         OI    FCACLOS2,FCACPURG    SET PURGE                  @YM06153
         TM    FCACLOS1,FCACFORC    IS THIS FORCE CLOSE        @YM06153
         BO    AMCL1040             YES, EXIT                  @YM06153
RP350090 EQU   *
*
*  DETERMINE DEVICE TYPE. IF 3505 AND UNIT EXCEPTION IS ON IN STATUS OF
*  IOB EXIT TO IGG0201R. IF NOT TURN OFF ERROR INDICATORS IN DCB AND
*  RESET DEVICE BY ISSUING A FEED STACKER-SELECT (23) COMMAND.
*
         MODESET KEYADDR=DXUKEY,WORKREG=15 USER KEY            @YM05576
         CLI   DCBDEVT,R3505           IS THIS A 3505 CARD READER
         BNE   RP350400                IF NO GO CK FOR ASSOCIATED DATA
*                                      ON 3525
*
*
*             CHECK TO SEE IF UNIT EXCEPTION HAS OCCURED
RP350300 EQU   *
         L     RB,DCBIOBA              LOAD ADDR OF NORMAL SHC IOB
         TM    STATUSN(RB),UE          TEST FOR UNIT EXCEPTION
         BO    AMCL1040                BRANCH IF YES           @YM06152
RP350310 EQU   *
         MODESET KEYADDR=DXUKEY,WORKREG=15 USER KEY            @YM05576
*                                      KEY                       Y02898
         NI    DCBIFLGS,ERMASK         TURN OFF ANY ERROR INDICATORS
*
*              DO A FEED STACKER SELECT WITHOUT OMR/RCE FLAG TO
*              RESET THE 3505 OR 3525
*
         L     RB,DCBIOBA              GET IOB ADDRESS
         LA    RF,CCW1(RB)             POINT TO FIRST CCW
         ST    RF,CPADDR(RB)           STORE CP. ADDR IN IOB
         MVI   CCWFLAGS(RB),SLIFLAG    SET SLI ON CCOFF IN CCW
         MVI   ZERO(RF),FDSS           STORE AFD SS CMD
         LA    RF,FOUR(RB)             GET SPECIAL ECB ADDR.
         ST    RF,ECBPNTR(RB)          STORE IN IOB ECB PNTR.
         LA    RF,EIGHT(RB)            POINT TO IOS'S IOB
*
*
         EXCP  (1)
*
         WAIT  ECB=4(RB)
         MODESET EXTKEY=DATAMGT        DATA MGT KEY              Y02898
*                              KEY
         CLI   DXCCW1,ONE              CK IF CONTROL FLAG SWITCH Y02898
         BNE   AMCL1040                LOAD ID OF NEXT EXEC    @YM06152
         MVI   DXCCW1,ZERO             CLEAR SWITCH              Y02898
         B     RP350310                BRANCH TO FEED ANOTHER CARD
*
*                   CHECKING FOR 3525 ASSOCIATED DATA SET
*                CHANGE THE NAME "DCBMODE+3" WHEN OUR DSECT IS AVAIL
*
*  CHECK TO SEE WHAT TYPE OF DATA SET IS BEIN CLOSED ON 3525. IF INPUT
*  DATA SET WITH CNTRL ISSUE TWO FEED STACKER-SELECT CCW'S,OTHEWISE
*  ISSUE ONE FEED STACKER-SELECT CCW.
*
RP350400 EQU   *
         TM    DCBMODE+THREE,RPMASK    IS THIS A READ/PUNCH DATA SET
*
         BO    RP350500                BRANCH IF YES
         TM    DCBMODE+THREE,RWMASK    IS THIS A READ/PRINT DATA SET
         BO    RP350500                BRANCH IF YES
         TM    DCBMODE+THREE,PWMASK    IS THIS A PUNCH/PRINT DATA SET
         BO    RP350500                BRANCH IF YES
RP350600 EQU   *
         CLI   DCBFUNC,WMASK           PRINT ONLY SPECIFIED BY FUNC=W
         BE    RP350310                BRANCH IF YES
         CLI   DCBFUNC,WXMASK          PRINT ONLY SPECIFIED BY FUNC=WX
         BE    RP350310                BRANCH IF YES
         TM    DEBOPATB,INMASK          IS THIS AN INPUT OR INOUT DCB
         BNZ   RP350310                 NO, BRANCH
*
*
*
RP350610 EQU   *
         TM    DCBMACRF,CNTRL          IS CNTRL SPECIFIED
         BNO   RP350310                IF NO GO FEED A CARD
RP350660 EQU   *
*                                      SET SYSTEM TO OPEN PROTECTY02898
         MODESET EXTKEY=DATAMGT        KEY                       Y02898
         MVI   DXCCW1,ONE              SET SWITCH TO ONE         Y02898
         B     RP350310                BRANCH TO FEED A CARD
*              TURN ON SEQUENCE FLAGS IN ASSOCIATED DCB'S AND
*              TURN OFF SEQUENCE FLAGS IN DCB BEING CLOSE
RP350500 EQU   *
         NI    DCBQSWS,SEQOFF          TURN OFF SEQUENCE FLAG
         SR    RB,RB                   SET FLAG TO ZERO
         L     RC,DEBRDCB              LOAD ASSOCIATED READ DCB ADDR.
         LA    RC,0(RC)                ZERO HI ORDER BYTE
         LR    RD,RC                    SAVE DCB ADDR          @YM06152
*                                  SET SYSTEM TO OPEN PROTECT  @ZA08168
         MODESET EXTKEY=DATAMGT    KEY                         @ZA08168
         CLR   RC,RB                   IS THE READ DCB ADDR ZERO
         BE    RP350520                IF SO GO CK ON PUNCH DCB
*
*  THIS ROUTINE CHECKS FOR READ ASSOCIATED DATA SET. IF READ IS PART
*  OF ASSOCIATED DATA SET,ZERO OUT READ DCB ADDRESS IN DEBRDCB FIELD OF
*  DEB FOR EACH DATA SET OPENED (READ/PUNCH/PRINT)
*
         CLR   RDCB,RC                 IS CUR DCB FOR READ
         BNE   RP350520                NO, BRANCH
         DEBCHK (RDCB)                 GO TO DEB VALIDITY CHK  @YM06152
         LR    RDEB,RF                 RESET DEB BASE          @ZA29896
         ST    RB,DEBRDCB              CLEAR DEB READ DCB POINTER
         L     RC,DEBPDCB              GET ADDRESS OF PUNCH DCB
         LA    RC,0(RC)                ZERO HI ORDER BYTE
         LR    RD,RC                   SAVE DCB ADDR           @YM06152
         CLR   RB,RC                   PUNCH DATA SET ALSO USED
         BE    RP350512                IF NO GO CK WRITE DCB
*                                      SET SYSTEM TO USER PROTECTY02898
         MODESET KEYADDR=DXUKEY,WORKREG=15 USER KEY            @YM05576
         OI    SEQFDISP(RC),SEQMASK    TURN ON SEQ FLAGS
         L     RC,DEBOFFST(RC)   GET PUNCH DATA SET DEB ADDR     Y02898
         MODESET EXTKEY=DATAMGT        SET SYSTEM TO CLOSE PROTECY02898
*                                      KEY                       Y02898
         DEBCHK (RD)                   GO TO DEB VALIDITY      @YM06152
         LR    RC,RF                   RESET DEB BASE          @ZA29896
         ST    RB,DEBRPTR(RC)          ZERO READ DCB @ IN PUNCH DEB
RP350512 EQU   *
         L     RC,DEBWDCB              GET PRINT DCB ADDRESS
         LA    RC,0(RC)                ZERO HI ORDER BYTE
         LR    RD,RC                   SAVE DCB ADDR           @YM06152
         CLR   RB,RC                   PRINT DATA SET USED
         BE    RP350520                NO GO CK PUNCH DCB
         MODESET KEYADDR=DXUKEY,WORKREG=15 USER KEY            @YM05576
*                                      KEY                       Y02898
         OI    SEQFDISP(RC),SEQMASK    TURN ON SEQUENCE MASK
         L     RC,DEBOFFST(RC)         GET PRINT DCB'S DEB
         MODESET EXTKEY=DATAMGT        SET SYSTEM TO CLOSE PROTECY02898
*                                      KEY                       Y02898
         DEBCHK (RD)                   GO TO DEB VALIDITY      @YM06152
         LR    RC,RF                   RESET DEB BASE          @ZA29896
         ST    RB,DEBRPTR(RC)          ZERO READ DCD @ IN PRINT DEB
RP350520 EQU   *
         L     RC,DEBPDCB              GET PUNCH DCB ADDRESS
         LA    RC,0(RC)                ZERO HI ORDER BYTE
         CLR   RC,RB                   PUNCH DCB USED IN ASSOCIATIVE DS
         BE    RP350530                NO GO CK PRINT DCB
         CLR   RDCB,RC                 PUNCH DCB BEING CLOSED
         BNE   RP350530                NO GO CK PRINT DCB
*                                      SET SYSTEM TO OPEN PROTECTY02898
         DEBCHK (RDCB)                 GO TO DEB VALIDITY      @YM06152
         LR    RDEB,RF                 RESET DEB BASE          @ZA29896
         ST    RB,DEBPDCB              ZERO PUNCH DCB @ IN DEBPDCB
         L     RC,DEBRDCB              GET @ OF READ DCB IN PUNCH DEB
         LA    RC,0(RC)                ZERO HI ORDER BYTE
         LR    RD,RC                   SAVE DCB ADDR           @YM06152
         CLR   RB,RC                   READ DCB @ IN PUNCH DEB
         BE    RP350522                IF NO GO CK IF PRINT DATA SET
         MODESET KEYADDR=DXUKEY,WORKREG=15 USER KEY            @YM05576
         OI    SEQFDISP(RC),SEQMASK    TURN ON SEQUENCE FLAG FOR PUNCH
         L     RC,DEBOFFST(RC)         GET PUNCH DCB DEB ADDRESS
         MODESET EXTKEY=DATAMGT        SET SYSTEM TO CLOSE PROTECY02898
*                                      KEY                       Y02898
         DEBCHK (RD)                   GO TO DEB VALIDITY      @YM06152
         LR    RC,RF                   RESET DEB BASE          @ZA29896
         ST    RB,DEBPPTR(RC)          ZERO PUNCH DCB @ PTR IN PUNC
RP350522 EQU   *
         L     RC,DEBWDCB              CHECK IF PRINT DATA SET EXIST
         LA    RC,0(RC)                ZERO HI ORDER BYTE
         LR    RD,RC                   SAVE DCB ADDR           @YM06152
         CLR   RB,RC                   PRINT DCB @ IN PRINT DEB
         BE    RP350530                IF NO BRANCH
         MODESET KEYADDR=DXUKEY,WORKREG=15 USER KEY            @YM05576
         OI    SEQFDISP(RC),SEQMASK    SET PRINT SEQUENCE FLAG
         L     RC,DEBOFFST(RC)         GET PRINT DCB DEB ADDRESS
         MODESET EXTKEY=DATAMGT        SET SYSTEM TO CLOSE PROTECY02898
*                                      KEY                       Y02898
         DEBCHK (RD)                   GO TO DEB VALIDITY      @YM06152
         LR    RC,RF                   RESET DEB BASE          @ZA29896
         ST    RB,DEBPPTR(RC)          ZERO PUNCH DCB @ PTR IN PUNC
*
RP350530 EQU   *
         L     RC,DEBWDCB
         LA    RC,0(RC)                ZERO HI ORDER BYTE
         CLR   RC,RB                   WRITE DCB OPENED
         BE    RP350650                IF NO GO TEST DATA SET TYPE
         CLR   RDCB,RC                 PRINT DCB NOW BEING CLOSED
         BNE   RP350650                IF NOT GO TEST DATA SET TYPE
*                                      SET SYSTEM TO USER PROTECTY02898
         MODESET EXTKEY=DATAMGT        KEY                       Y02898
         DEBCHK (RDCB)                 GO TO DEB VALIDITY      @YM06152
         LR    RDEB,RF                 RESET DEB BASE          @ZA29896
         ST    RB,DEBWDCB              ZERO PRINT DCB @ IN DEB
         L     RC,DEBRDCB              GET @ OF READ DCB
         LA    RC,0(RC)                ZERO HIGHER ORDER BYTE
         LR    RD,RC                   SAVE DCB ADDR           @YM06152
         CLR   RB,RC                   READ DCB OPENED
         BE    RP350532                IF NOT GO CHECK IF PUNCH OPENED
         MODESET KEYADDR=DXUKEY,WORKREG=15 USER KEY            @YM05576
         OI    SEQFDISP(RC),SEQMASK    SET SEQUENCE FLAG IN READ DCB
         L     RC,DEBOFFST(RC)         GET READ'S DCB DEB
         MODESET EXTKEY=DATAMGT        SET SYSTEM TO CLOSE PROTECY02898
*                                      KEY                       Y02898
         DEBCHK (RD)                   GO TO DEB VALIDITY      @YM06152
         LR    RC,RF                   RESET DEB BASE          @ZA29896
         ST    RB,DEBRPTR(RC)          ZERO OUT READ PTR @ IN DEB
*
RP350532 EQU   *
*
         L     RC,DEBPDCB              GET PUNCH DCB ADDRESS
         LA    RC,0(RC)                ZERO HIGHER ORDER BYTE
         LR    RD,RC                   SAVE DCB ADDR           @YM06152
         CLR   RB,RC                   PUNCH DCB OPENED
         BE    RP350650                IF NOT GO CK TYPE OF ASSOC.D.SET
         MODESET KEYADDR=DXUKEY,WORKREG=15 USER KEY            @YM05576
*                                      KEY                       Y02898
         OI    SEQFDISP(RC),SEQMASK    SET SEQUENCE FLAG IN PUNCH DCB
         L     RC,DEBOFFST(RC)         GET PUNCH DCB'S DEB ADDRESS
         MODESET EXTKEY=DATAMGT        SET SYSTEM TO CLOSE PROTECY02898
*                                      KEY                       Y02898
         DEBCHK (RD)                   GO TO DEB VALIDITY      @YM06152
         LR    RC,RF                   RESET DEB BASE          @ZA29896
         ST    RB,DEBPPTR(RC)          ZERO OUT PUNCH PTR @ IN DEB
RP350650 EQU   *
*                                      SET SYSTEM TO USER PROTECTY02898
         MODESET KEYADDR=DXUKEY,WORKREG=15 USER KEY            @YM05576
         TM    DCBFUNC,RWMASK          IS THIS A READ/PRINT DATA SET
         BO    RP350653                BRANCH IF YES
RP350651 EQU   *
         TM    DCBFUNC,RPMASK          IS THIS A READ/PUNCH DATA SET
         BO    RP350654                BRANCH IF YES
RP350652 EQU   *
         TM    DCBFUNC,PWMASK          IS THIS A PUNCH/PRINT DATA SET
         BO    RP350655                BRANCH IF YES
         B     RP350300                BRANCH
RP350653 EQU   *
         TM    DEBOPATB,INMASK         IS THIS AN INPUT FILE
         BZ    RP350610                BRANCH IF YES
         B     AMCL1040                BRANCH                  @YM06152
RP350654 EQU   *
         TM    DEBOPATB,INMASK
         BZ    RP350310                BRANCH, IF NO
         B     AMCL1040                BRANCH                  @YM06152
RP350655 EQU   *
         TM    DCBFUNC,XMASK           IS THIS A PRINT DATA SET
         BO    AMCL1040                BRANCH IF YES           @YM06152
         B     RP350310                BRANCH
TRUNCATE EQU   *
         TM    DCBCIND2,DCBCNIOE       HAS EOV BEEN ENTERED    @YM06152
         BO    RP350090                YES, BRANCH
         L     RF,DXUDCBAD             GET USER DCB ADDRESS      Y02898
         LA    RJ,LOADPUT              ADDR OF INST TO GO TO   @YM06152
*                                      TRUNCATE ROUTINE        @YM06152
         BAL   RB,SYNCHRTN             GO TO SYNCH RTN         @YM06152
***                                                            @ZA33956
         MODESET EXTKEY=DATAMGT                                @ZA33956
         DEBCHK (RDCB)                                         @ZA33956
         LR    RDEB,RF                 RELOAD DEB BASE         @ZA33956
         MODESET KEYADDR=DXUKEY,WORKREG=15 USER KEY            @ZA33956
***                                                            @ZA33956
         TM    DCBMACRF+ONE,LOCMASK    DCB USING LOCATE MODE
         BM    AMCLAD02                NO BRANCH
***
**
*        THIS ROUTINE WILL PREVENT ISSUING A PUT WHEN THE 3525 @ZA11813
*        PUNCH OR PRINT IS A PART OF AN ASSOCIATED DATA SET    @ZA11813
*        USING QSAM IN LOCATE MODE.                            @ZA11813
* NOTE:  THE USER MUST ISSUE A PUT FOR HIS LAST PUNCH OR PRINT @ZA11813
*        BUFFER WHEN USING QSAM IN LOCATE MODE FOR AN          @ZA11813
*        ASSOCIATED DATA SET.                                  @ZA11813
**                                                             @ZA11813
***                                                            @ZA11813
         TM    DCBFUNC,IRPD            FUNC OTHER THAN PRT ONLY@ZA11813
         BM    RP350656                YES, BRANCH             @ZA11813
         TM    DCBFUNC,W               FUNC EQUALS PRT ONLY    @ZA11813
         BO    AMCLAD01                YES, BR. TO ISSUE PUT   @ZA11813
RP350656 EQU   *                                               @ZA11813
         TM    DCBFUNC,RW              CK FOR READ OR PRINT DATA SET
         BZ    AMCLAD01                NO, BRANCH TO PUT ROUTINE
         TM    DCBFUNC,P               CK FOR PUNCH FLAG ONLY
         BO    AMCLAD02                YES, BYPASS PUT ROUTINE
         TM    DCBFUNC,W               PRINT FLAG ON
         BO    AMCLAD02                YES, BYPASS PUT ROUTINE
*
*
AMCLAD01 EQU   *
*
*  MUST ISSUE A PUT ROUTINE IF IN LOCATE MODE TO FLUSH BUFF AFTER TRUNC
*
         LR    RF,RDCB                 SET UP PARAMETER REGISTER Y02898
         L     RJ,DCBPUT               FOR PUT LOCATE ROUTINE  @YM06152
         BAL   RB,SYNCHRTN             TO PUT LOCATE ROUTINE   @YM06152
***                                                            @ZA33956
         MODESET EXTKEY=DATAMGT                                @ZA33956
         DEBCHK (RDCB)                                         @ZA33956
         LR    RDEB,RF                 RELOAD DEB BASE         @ZA33956
         MODESET KEYADDR=DXUKEY,WORKREG=15  USER KEY           @ZA33956
***                                                            @ZA33956
AMCLAD02 EQU   *
         L     RB,DCBIOBA              LOAD ADDR OF IOB
         LA    RB,0(RB)                CLEAR HI ORDER BYTE
         LR    RUCB,RB                 SAVE IOB ADDR TO LOOP ON
AMCLAD03 EQU   *
         LR    RD,RB                   SAVE IOB IN CASE OF ERROR
         L     RB,BYTO(RB)             GET NEXT IOB
         LA    RB,BYTO(RB)             CLEAR HI ORDER BYTE
         B     AMCLAD05                GO TEST COMPLETION
*
AMCLAD04 EQU   *                       ISSUE WAIT
         L     RF,ECBADOS(RB)          GET ECB ADDRESS
         WAIT  ECB=(1)                 WAIT ON IOB COMPLETION
AMCLAD05 TM    ECBOS(RB),COMPLETE      TEST IOB COMPLETE/OUT ERROR
         BM    AMCL1035                BRANCH COMPLETE BUT IN ERROR
         BZ    AMCLAD04                GO WAIT NOT YET COMPLETE
         CR    RUCB,RB                 IS THE NEXT IOB = IOBA
         BE    RP350090                YES
         B     AMCLAD03                GET NEXT IOB
AMCL1035 EQU   *
         ST    RD,DCBIOBA              STORE LAST IOB SCHEDULED
*
AMCL1040 EQU     *                                             @YM06153
         MODESET KEYADDR=DXUKEY,WORKREG=15                     @YM06153
         L       RF,SCWGETMA        GET SAVE AREA ADDR         @YM06153
         FREEMAIN R,LV=SAVELG,A=(1),SP=230  REG SAVE AREA      @YM06153
         MODESET EXTKEY=DATAMGT                                @YM06153
         MVC   ZERO(TWO,RWTGC),GOOD   LOAD ID  NEXT EXECUTOR   @YM05576
         TM      FCACLOS1,FCACFORC  IS THIS FORCE CLOSE        @YM06153
         BO      AMCL5019           YES                        @YM06153
*
*
RELOOP   EQU   *
         LA    RWTGC,WGOFF(ZERO,RWTGC) STEP TO NEXT ENTRY
         LA    RPARC,PLOFF(ZERO,RPARC) STEP TO NEXT ENTRY
         CLC   0(TWO,RWTGC),AMIDCNST
         BCR   EIGHT,RBASE             RECRUSE THRU MODULE
         CLC   0(TWO,RWTGC),CLLDB       END OF TABLE?          @YM05576
         BL    RELOOP                  NO BRANCH               @YM05576
         CLC   0(TWO,RWTGC),CLLDG      END OF TABLE?           @YM05576
         BH    RELOOP                  NO BRANCH               @YM05576
*
         LR    RPARC,RPAR              INITILIZATION
         LA    RWTGC,WAOFF(ZERO,RWTG)  REINITIALIZE WTG LIST POINTER
*
AMCL5011 EQU   *
         CLI   0(RWTGC),ZERO           IS THIS ENTRY ZERO
         BNE   AMCL5019                NO BRANCH
         LA    RWTGC,WGOFF(ZERO,RWTGC) STEP TO NEXT ENTRY
         LA    RPARC,PLOFF(ZERO,RPARC) STEP TO NEXT ENTRY
         B     AMCL5011                BRANCH
*
AMCL5019 EQU   *
         MVC   SIX(TWO,RWTG),ZERO(RWTGC) SET UP ROUTINE ID FOR XCTL
         MVC   D14(THREE,RWTG),TWO(RWTGC) MOVE TTR INTO TABLE
***
         XCTL  DE=(RWTG),SF=(E,DXCCW12)
*
*THIS ROUTINE TESTS THE CALLERS KEY TO DETERMINE WHETHER TO    @YM06152
*BALR (IF KEY IS LT 8) OR SYNCH (IF KEY IS GT 7) TO THE ACCESS @YM06152
*METHOD ROUTINES. CLOSES REGS ARE SAVED IN THE SVRB SAVEAREA   @YM06152
*AND THE USERS REGISTERS ARE LOADED AND PASSED TO THE ACCESS   @YM06152
*METHOD ROUTINES (FOR THE CASE AN EOV CONDITION ARISES AND A   @YM06152
*USER EOV EXIT IS TAKEN).                                      @YM06152
*
SYNCHRTN EQU     *                                             @YM06152
         L       RTCB,PSATOLD       GET CURRENT TCB@           @YM06152
         USING   TCB,RTCB                                      @YM06152
         L       RD,TCBRBP          GET RB ADDR                @YM06152
         USING   RBSECT,RD          BASE FOR RB                @YM06152
         MODESET EXTKEY=SUPR                                   @YM06152
         STM     RF,RB,RBEXSAVE     SAVE CLOSE REG IN SVRB     @YM06152
         L       RC,SCWGETMA        GET REG SAVE AREA ADDR     @YM06152
         MODESET KEYADDR=DXUKEY,WORKREG=12                     @YM06152
         TM      DXUKEY,KEYMASK     CALLER KEY 8 OR HIGHER?    @YM06152
         BO      PRB                SYNCH IF GT 0R EQ 8        @YM06152
         LM      R2,R12,RBGRS2      GET USER REGS              @YM06152
         DROP    RD                                            @YM06152
         BALR    RD,RJ              PUT ROUTINE                @YM06152
         USING   *,RD                                          @YM06152
         B       RELOAD                                        @YM06152
         DROP    RD                                            @YM06152
*
PRB      EQU     *                                             @YM06152
         USING   RBSECT,RD                                     @YM06152
         LM      R2,R12,RBGRS2      GET USERS REGS             @YM06152
         DROP    RD                                            @YM06152
         SYNCH   (15)               GO TO USER                 @YM06152
*
RELOAD   EQU     *                                             @YM06152
         L       RTCB,PSATOLD       CURRENT TCB ADDR           @YM06152
         USING   TCB,RTCB                                      @YM06152
         L       RD,TCBRBP          LOAD PTR TO SVRB           @YM06152
         DROP    RTCB                                          @YM06152
         USING   RBSECT,RD                                     @YM06152
         LM      RF,RB,RBEXSAVE     RESTORE CLOSES REG         @YM06152
         DROP    RD                                            @YM06152
         BR      RB                 RETURN TO MAIN ROUTINE     @YM06152
*
*INSTRUCTIONS USED TO GO TO THE ACCESS METHOD ROUTINE          @YM06152
         DROP    RDCB                                          @YM06152
         USING   IHADCB,RF                                     @YM06152
LOADPUT  L       RJ,DCBPUT          LOAD PUT RTN ADDR          @YM06152
         B       DCBTRUNC(RJ)       BR TO TRUNCATE ROUTINE     @YM06152
AMIDCNST DC    C'1P'                   ID OF THIS MODULE
*                   MUST BE INSERTED
*
ZAPSZAPS DC    8C'ZAPSZAPS'            WORK AREA USED BY CLOSE   Y02898
GOOD     DC    C'1R'             ID OF SECOND HALF OF 3505/25  @YM05576
CLLDB    DC   C'0B'                         ID OF IGG0200B     @YM05576
CLLDG    DC   C'0G'                     ID OF IGG0200G         @YM05576
*
         DCBD  DSORG=PS
         IEZDEB
         IECDSECS MAIN,EXPAND=YES                                Y02898
         IKJTCB                                                  Y02898
         IHARB                                                   Y02898
         IHAPSA                                                @YM06152
         IECDPPL DSECT=YES                                     @YM06152
FORCORE  DSECT                                                 @YM06152
         IHAFCAUD ORG=YES                                      @YM06152
FORCORE  DSECT                                                 @YM06152
         IGGSCW                                                @YM06152
         END
