         TITLE 'IGG019FU-EOB ROUTINE FOR 3525 INTERPRET FILE'
         START
***********************************************************************
*
*NAME--              IGG019FU
*
*TITLE--             'IGG019FU' - EOB ROUTINE FOR 3525 INTERPRET FILE
*
*STATUS CHANGE LEVEL 020                                       @YA04230
*                    THIS MODULE WAS PUT INTO 'OS' RELEASE 21.0 AS A
*                    NEW MODULE
*
*MODULE TYPE--       END OF BLOCK
*
*
*FUNCTION/OPERATION--SAM END OF BLOCK ROUTINE FOR 3525 INTERPRET
*                    FILE WITH MACHINE C CHARACTERS, ASCII C CHAR.
*                    OR NO C CHARACTERS. SETS UP COMMAND BYTE IN
*                    THE FIRST CCW. FIRST CCW IS A WR,FD,SS. BUFFER
*                    ADDRESS AND RECORD LENGTH ARE PASSED FROM WRITE
*                    OR PUT ROUTINE IN THIS CCW. SETS UP COMMAND
*                    BYTE, BUFFER ADDRESS AND LENGTH IN THE LAST
*                    TWO CCW'S. THESE TWO ARE PRINT CCW'S. THE CHAN.
*                    PROG. IS SCHEDULED WITH EXCP MACRO.  IT TESTS
*                    SEQUENCE FLAGS FOR DEVICE ASSOCIATED DATA SETS
*                    (DADS) TO VERIFY PROPER ISSUANCE OF READ,
*                    PUNCH AND PRINT COMMANDS.
*
*ENTRY POINTS--      IGG019FU FROM PUT OR WRITE ROUTINES.
*                    LA    2,DCB
*                    LA    3,IOB
*                    L     15,76(DCB)       FOR BSAM
*                    L     15,92(DCB)       FOR QSAM
*                    BALR  14,15
*
*INPUT--             IOB+40            BUFFER ADDRESS
*                    IOB+46            BLOCK SIZE
*                    DCBRECFM          TEST FOR MACHINE C CHAR.,
*                                      TEST FOR ASCII C CHAR.
*
*OUTPUT--            IOB+40            WRITE CCW SET WITH C CHAR.,
*                                      BUFFER ADDR. AND LENGTH
*                    IOB+48            PRINT CCW SET WITH BUFFER
*                                      ADDR. AND LENGTH
*                    IOB+56            PRINT CCW SET WITH BUFFER
*                                      ADDR. AND LENGTH.
*                    IOB+24            LOADED WITH START ADDR OF CHAN
*                                      PROG.
*
*EXTERNAL ROUTINES-- NONE
*
*EXITS-NORMAL--      RETURN TO PUT OR WRITE ROUTINE ON REG. 14
*
*EXITS-ERROR--       NONE
*
*TABLES/WORKAREA--   NONE
*
*ATTRIBUTES--        REENTRANT, REUSABLE
***********************************************************************
WRKR0    EQU   0                       WORK REGISTER
WRKR1    EQU   1                       TABLE INCREMENT AMOUNT (2)
DCBR     EQU   2                       DCB ADDRESS UPON ENTRY
IOBR     EQU   3                       IOB ADDRESS UPON ENTRY
LRECLR   EQU   4                       LENTGH OF RECORD
WRKR3    EQU   5                       TABLE START ADDRESS
BUFR     EQU   6                       BUFFER ADDRESS
DEBR     EQU   6                       DEB ADDRESS
OPR      EQU   7                       OP CODE REGISTER
RETR     EQU   14                      CALLING ROUTINE RETURN ADDRESS
BASR     EQU   15                      BASE REGISTER
*
*              MASKS
*
FORTY8   EQU   48
ZERO     EQU   X'00'
SSCLEAR  EQU   X'BF'
ONE      EQU   X'01'
HUNDRD12 EQU   X'70'
SIXTY3   EQU   63
SIXTY4   EQU   X'40'
SIXTY5   EQU   65
DEC80    EQU   80                                              @ZA11830
DEC81    EQU   81                                              @ZA11830
BLANK    EQU   X'40'
SIXTEEN  EQU   X'10'
STACK2   EQU   X'02'                   STACKER 2 MASK
PRTLN1   EQU   X'0D'                   PRINT LINE 1 OP-CODE
PRTLN3   EQU   X'1D'                   PRINT LINE 3 OP-CODE
MCHAR    EQU   X'02'                   MACHINE CONTROL CHAR MASK
ASCII    EQU   X'04'                   ASCII CONTROL CHAR MASK
MCRFBSAM EQU   X'20'                    MACRO FORM=BSAM
USED     EQU   X'7F'                   DCB USED INDICATOR
STACKER1 EQU   X'01'                    PUNCH USING STACKER # 1
STACKER2 EQU   C'W'                     ASCII PUNCH STACKER SELECT 2
WRFDSS2  EQU   X'41'                    OP CODE STACKER SELECT 2
CCUSED   EQU   X'06'                   MASK FOR MACH AND ASCII
WRKR4    EQU   4
*
IGG019FU CSECT
* C 036000,480000,754000,772000                                @YA04230
* A 468800-471200,480600-488400,653000-655000,712000-716000,   @YA04230
* A 749000-751000,754000,800800-803200,408500                  @YA04230
* D 472000,484000-492000,528000                                @YA04230
* A 324100,324200,452000-464000 (APPROX)                       @ZA11830
* C45390000                                                    @ZA15325
         USING IGG019FU,BASR
         USING IHADCB,DCBR
         USING IOB,IOBR
         MODID BR=YES                                          @YA04230
*
*              ENTRY POINT
*
*  THIS ROUTINE SAVE CCW INFORMATION AND BLANK OUT PRINT BUFFER. IT
*  THEN MOVE LAST SIXTEEN BYTES FROM OUT AREA TO BYTES 49-64 IN
*  PRINT BUFFER.
*
*
         L     BUFR,CCW1AD             GET BUFFER ADDRESS FROM CCW
         MVI   CCW2OP,ONE              SET WRITE CMD TO WR,FD,SS
         IC    OPR,CCW2OP              SAVE WTIRE CMD BYTE
         LA    LRECLR,DEC81            LOAD CC MAXIMUM VALUE   @ZA11830
         CH    LRECLR,CCW1LN           IS REAL VALUE LESS THAN @ZA11830
*                                      OR EQUAL                @ZA11830
         BL    DEFLT81                 NO. BRANCH AND USE DEFAULT
*                                                              @ZA11830
         LH    LRECLR,CCW1LN           USE REAL VALUE          @ZA11830
DEFLT81  EQU   *                                               @ZA11830
         TM    DCBRECFM,CCUSED         ARE CONTROL CHARS USED  @ZA11830
         BNZ   DEFLTOK                 YES. BRANCH. VALUE OK   @ZA11830
         LA    LRECLR,DEC80            LOAD NO CC MAX VALUE    @ZA15325
         CH    LRECLR,CCW1LN           IS REAL VALUE LESS THAN @ZA11830
*                                      OR EQUAL                @ZA11830
         BL    DEFLT80                 NO. BRANCH AND USE DEFAULT
*                                                              @ZA11830
         LH    LRECLR,CCW1LN           USE REAL VALUE          @ZA11830
DEFLT80  EQU   *                                               @ZA11830
DEFLTOK  EQU   *                                               @ZA11830
         MVI   IOBBUFR,BLANK           MOVE IN HEX 40
         MVC   IOBBUFR+ONE(SIXTY3),IOBBUFR  BLANK OUT PRT BUFFER
         LA    WRKR3,SIXTY5            LOAD POSSIBLE BUFF LENG @YA04230
         CR    LRECLR,WRKR3            IS BUFF LENG LT 65      @YA04230
         BL    DDB1                    YES. DONT COPY BUFFER   @YA04230
         SR    LRECLR,WRKR3            NO. GET BUFF LENG - 1   @YA04230
         TM    DCBRECFM,CCUSED         CONTROL CHARACTERS USED
         BZ    DDAA3                   NO. COPY BUFFER THEN    @YA04230
*                                      TEST FOR STACK PARAM    @YA04230
         LTR   LRECLR,LRECLR           IS BUFF LENG NOW ZERO   @YA04230
         BZ    DDB1                    YES. DONT COPY BUFFER   @YA04230
         BCTR  LRECLR,0                SUBTRACT 1 FOR CONTROL  @YA04230
*                                      CHARACTER.              @YA04230
         EX    LRECLR,MOVE2            COPY BUFFER DATA        @YA04230
         LH    LRECLR,CCW1LN           RESTORE BUFFER LENGTH   @YA04230
DDB1     EQU   *
         TM    DCBRECFM,ASCII          ASCII CONTROL CHARACTERS USED
         BZ    DDA2                    NO, BR TO CK FOR MACHINE C CHAR
*
*  WHEN ASCII USED THIS ROUTINE CHECKS FOR VALID CHARACTER. IF INVALID
*  CHARACTER IS USED, A DEFAULT OF X'01' OP CODE (STACKER SELECT=1) IS
*  USED. THE CCW IS CONSTRUCTED AND THEN EXECUTED
*
         CLI   ZERO(BUFR),STACKER2      STACKER SELECT 2 SPECIFIED
         BNE   DDD1                    NO, GO SET STACKER 1 OP CODE
         LA    WRKR3,WRFDSS2           SET STACKER 2 OP IN CCW
         B     DDB2                    BRANCH AROUND NEXT STMT
DDD1     EQU   *
         LA    WRKR3,STACKER1           LOAD STACKER SELICK 1 OP CODE
*
DDB2     EQU   *
*
*
         LA    BUFR,ONE(BUFR)          INCREMENT BUFFER ADDR BY ONE
         ST    BUFR,CCW2AD             STORE DATA ADDR IN WRITE CCW
         STC   WRKR3,CCW2OP            RESTORE WRITE COMMAND
         BCTR  LRECLR,WRKR0            DECREMENT VALUE BY ONE
         STH   LRECLR,CCW2LN           STORE LENGTH IN WRITE CCW
         LA    WRKR3,CCW2OP            START ADDRESS
         ST    WRKR3,IOBSTAD           PUNCH START ADDRESS
         B     LABEL                   BRANCH FOR EXCP
LEVELID  DC    C'04'                   MODULE LEVEL CONTROL
DDA2     EQU   *
         TM    DCBRECFM,MCHAR          MACHINE CONTROL CHARACTERS USED
*
*  THI ROUTINE DETERMINES IF MACHINE CONTROL CHARACTERS ARE USED.IF SO
*  THE CONTROL CHARACTER IS INSERTED INTO CCW AND EXECUTED
*
         BNO   DDA3                    NO, BRANCH
         LH    LRECLR,CCW1LN           LOAD RECORD LENGTH
         BCTR  LRECLR,WRKR0            DECREMENT
         IC    OPR,0(BUFR)             INSERT CONTROL CHARACTER
         LA    BUFR,ONE(BUFR)          ADD ONE TO BUFFER ADDRESS
         B     DDA4                    BRANCH TO SET UP CCW1
DDAA3    EQU   *
         EX    LRECLR,MOVE1            COPY BUFFER DATA        @YA04230
         LH    LRECLR,CCW1LN           RESTORE BUFFER LENGTH   @YA04230
DDA3     EQU   *
*
*  IF ASCII OR MACHINE CONTROL CHARACTERS ARE NOT SPECIFIED THIS
*  ROUTINE CHECKS TO SEE IF STACKER 2 SPECIFIED IN DCB IF YES THE OP.
*  CODE IS STORE IN CCW , IF NO STACKER SELECT TO POCKET 1 IS USED.
*
         TM    DCBSTACK,STACK2         IS STACKER 2 SPECIFIED
         BNO   DDA4                    NO, BRANCH
         LA    OPR,WRFDSS2              LOAD OP-CODE FOR STACKER # 2
*
DDA4     EQU   *
         ST    BUFR,CCW2AD             STORE RECORD ADDR IN WRITE CCW
         STH   LRECLR,CCW2LN           STORE LENGTH IN WRITE CCW
         STC   OPR,CCW2OP              STORE CMD BYTE IN WRITE CCW
         LA    WRKR3,CCW2OP            GET ADDR OF WRITE CCW   @YA04230
         ST    WRKR3,IOBSTAD           SAVE START ADDR FOR IOS @YA04230
LABEL    EQU   *
*
*  THIS ROUTINE BUILD THE TWO PRINT CCW THAT ARE CHAINED TO THE PUNCH
*  WRITE FEED STACKER (1 OR 2)CCW. IT THEN ISSUE EXCP AND EXIT TO
*  PUT OR WRITE ROUTINE
*
         ST    BUFR,CCW3AD             LOAD ADDR OF DATA IN PRT CCW
         MVI   CCW3OP,PRTLN1           MOVE IN PRT LINE 1 CCW OP CODE
         LA    WRKR3,SIXTY4            LOAD 64 FOR COMPARE     @YA04230
         CR    LRECLR,WRKR3            IS BUFFFER LENG LT 64   @YA04230
         BL    DDA5                    YES. USE EXACT LENGTH   @YA04230
         LR    LRECLR,WRKR3            LOAD BYTE COUNT OF 64   @YA04230
DDA5     EQU   *                                               @YA04230
         STH   LRECLR,CCW3LN           STORE BYTE COUNT IN PRT CCW
         LA    BUFR,IOBBUFR            GET PRINT BUFFER ADDRESS
         ST    BUFR,CCW4AD             PUT PRINT BUFFER ADDR IN CCW
         MVI   CCW4OP,PRTLN3           MOVE IN PRINT LINE 3 CCW OP
         STH   WRKR3,CCW4LN            STORE BYTE COUNT OF 64  @YA04230
         LA    WRKR1,IOBIOS            LOAD IOS IOB ADDR FOR EXCP
         EXCP  (1)                     SCHEDULE I/O
         NI    DCBCIND1,USED           INDICATE DCB HAS BEEN USED
         L     DEBR,DCBDEBAD           GET DEB ADDRESS
         BCR   15,RETR                 RETURN TO WRITE/PUT ROUTINE
*
*
MOVE1    MVC   IOBBUFR+FORTY8(ZERO),SIXTY4(BUFR) LOAD PRT BUFF @YA04230
MOVE2    MVC   IOBBUFR+FORTY8(ZERO),SIXTY5(BUFR) GET PROPER    @YA04230
*                                      OFFSET FOR PRT LINE 2   @YA04230
*                                      IF CONTROL CHARS USED   @YA04230
*
         DC    12CL4'ZAPS'             THIS AREA IS FOR MAINTENANCE
*
*              THE FOLLOWING CNOP MUST REMAIN ON A HALFWORD BOUNDARY
*
         CNOP  0,8                     PAD MODULE TO DOUBLE-WD MULTIPLE
*
*              DSECTS
*
IOB      DSECT
IOBLINK  EQU   IOB
         DS    2F                      ECB AREA & ADDR OF NEXT IOB
IOBIOS   DS    1F                      IOBFLAGS AND SENSE INFO
         DS    3F                      ECB ADDR & CSW INFO
IOBSTAD  DS    1F                      CHANNEL PROG ADDRESS
         DS    3F                      DCB ADDR & MISC INFO
CCW1OP   DS    1F                      OP CODE
CCW1AD   EQU   CCW1OP                  CCW OP CODE
CCW1FL   DS    1H                      FLAGS
CCW1LN   DS    1H                      LENGTH
CCW2OP   DS    1F                      OP CODE
CCW2AD   EQU   CCW2OP                  CCW OP CODE
CCW2FL   DS    1H                      FLAGS
CCW2LN   DS    1H                      LENGTH
CCW3OP   DS    1F                      OP CODE
CCW3AD   EQU   CCW3OP                  CCW OP CODE
CCW3FL   DS    1H                      FLAGS
CCW3LN   DS    1H                      LENGTH
CCW4OP   DS    1F                      OP CODE
CCW4AD   EQU   CCW4OP                  CCW OP CODE
CCW4FL   DS    1H                      FLAGS
CCW4LN   DS    1H                      LENGTH
IOBBUFR  DS    CL64                    BUFFER FOR INTERPRET
*
         DCBD  DSORG=PS
*
*
         END
