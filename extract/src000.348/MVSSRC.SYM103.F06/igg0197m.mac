         TITLE 'IGG0197M  WTO MESSAGE ROUTINE AND ABEND '
         START
IGG0197M CSECT
***********************************************************************
*
*NAME--              IGG0197M
*
*TITLE--             'IGG0197M' - WTO ROUTINE FOR OMR/RCE ERRORS
*
*STATUS CHANGE LEVEL 000
*                    THIS MODULE WAS PUT INTO 'OS' RELEASE 21.0 AS A
*                    NEW MODULE
*
*MODULE TYPE--       OPEN - STAGE 1
*
*FUNCTION/OPERATION--THIS ROUTINE IS ENTERED FOR SAM USERS DURING
*                    OPEN WHEN:
*                             1) FORMAT CARD FOR OMR OR RCE IS
*                               INVALID.
*                             2) INVALID DEVICE HAS BEEN SPECIFIED
*                                FOR OMR.
*                             3) NORMAL XCTL FROM IGG0197L IS TO THIS
*                                MODULE
*
*ENTRY POINT--       IGG0197M IS ENTERED FROM IGG0197L VIA XCTL.
*
*INPUT--             DCB ADDRESS
*                    WORK AREA ADDRESS
*                    WHERE-TO-GO TABLE
*                    PARAMETER LIST
*                    CURRENT ENTRY IN WHERE-TO-GO TABLE
*                    CURRENT PARAMETER(DCB)
*
*OUTPUT--            SEE DESCRIPTION OF REGISTERS.
*
*EXTERNAL ROUTINES-- NONE
*
*EXIT NORMAL--       XCTL TO IGG0191I  OF BUFNO HAS BEEN SPECIFIED
*                    XCTL TO IGG0197N IF BUFNO HAS NOT BEEN SPECIFIED
*
*EXIT ERROR--        004 ABEND- INVALID OMR OR RCE FORMAT CARD OR
*                    INVALID DEVICE SPECIFIED FOR OMR. REG 15 WILL
*                    CONTAIN A RETURN CODE OF 5 TO INDICATE THIS
*                    CONDITION.
*
*TABLES/WORKAREAS--  WHERE-TO-GO TABLE
*                    BYTES  0- 7   NAME
*                    BYTES  8-10   RELATIVE DISK ADDRESS(TTR) OF
*                                  FIRST RECORD
*                    BYTE     11   CONCATENATION NUMBER
*                    BYTE     12   ZEROES
*                    BYTE     13   ALIAS INDICATOR AND USER DATA
*                                  FIELD DESCRIPTOR:
*                                      -ALIAS INDICATOR-- 1 BIT
*                                      -NUMBER OF TTRS IN USERS
*                                      DATA FIELD-- 2 BITS
*                    BYTES 14-16   TTR OF FIRST TEXT RECORD
*                    BYTE     17   ZEROES
*                    BYTES 18-19   TTR OF NOTE LIST OR SCATTER LIST
*                    BYTE     20   TRANSLATION TABLE
*                    BYTE     21   ZEROES OR NUMBER OF ENTRIES IN
*                                  THE NOTE LIST
*                    BYTES 22-23   ATTRIBUTES
*                    BYTES 24-26   TOTAL CONTIGUOUS MAIN STORAGE
*                                  REQUIRED FOR MODULE
*                    BYTES 27-28   LENGTH OF FIRST TEXT RECORD
*                    BYTE     29   LENGTH OF WTG TABLE(IN DOUBLE
*                                  WORDS)
*                    BYTES 30-31   PATH THROUGH LOADS OF ROUTINES
*                    BYTES 32-34   IDTTR OF EXECUTOR FOR FIRST DCB
*                    BYTE     35   ADDRESS OF STORAGE FOR DCB
*                    BYTES 36-39   TABLE OF IDTTR'S
*                    BYTE  32+N(8)--
*                            IDTTR OF EXECUTOR FOR NTH DCB (3 BYTES)
*                            ADDRESS OF STORAGE FOR DCB    (1 BYTE )
*                            IDTTR OF LAST ROUTINE LOAD    (3 BYTES)
*                            NOT USED                      (1 BYTE )
*                    --------------
*                    WORKAREA-     SEE 'FORCORE DSECT'(IECDSECT)
*                                  IN THIS LISTING.
*
*ATTRIBUTES--        REENTRANT AND REUSABLE.
*
***********************************************************************
***********************************************************************
*                            REGISTER CONVENTIONS
***********************************************************************
RDCB     EQU   2                       DCB REGISTER
RBASE    EQU   3                       BASE REGISTER
RCORE    EQU   4                       DCB WORK AREA REGISTER
RPAR     EQU   5                       TOP OF PARAMETER LIST
RWTG     EQU   6                       TOP OF WHERE-TO-GO (WTG) TABLE
RPARC    EQU   7                       CURRENT PARAMETER
RWTGC    EQU   8                       CURRENT TRANS LOAD
RTIOT    EQU   9                       USED HERE AS WORK REGISTER
RUCB     EQU   10                      USED HERE AS WORK REGISTER
RDEB     EQU   11                      DEB REGISTER
RB       EQU   12                      WORK REGISTER
RC       EQU   13                      WORK REGISTER
RD       EQU   14                      WORK REGISTER
RJ       EQU   15                      WORK REGISTER
RE       EQU   0                       WORK REGISTER
RF       EQU   1                       WORK REGISTER
***********************************************************************
*                            DISPLACEMENTS AND MASKS
***********************************************************************
ZERO     EQU   X'00'
FOUR     EQU   X'04'
TWO      EQU   X'02'
SIX      EQU   X'06'
C14      EQU   X'0E'
THRE     EQU   X'03'
PW       EQU   X'30'
ONE      EQU   1
THREE    EQU   3
D70      EQU   70
SVC35    EQU   35
FIVE     EQU   5
INPUTMSK EQU   X'0F'
QSAMFLG  EQU   X'01'
*
*
**
*
         BALR  RBASE,RE                LOAD BASE ADDR IN BASE REG
         USING M1000,RBASE
         USING FORCORE,RCORE
         USING DEBBASIC,RDEB
         USING IHADCB,RDCB
*
M1000    EQU   *
         L     RDCB,ZERO(RPARC)        GET DCB ADDRESS
         LA    RDCB,ZERO(RDCB)         CLEAR HIGH ORDER BYTE
         L     RCORE,FOUR(RWTGC)       GET WORK AREA ADDRESS
         L     RDEB,DCBDEBAD           GET DEB ADDRESS
*
         CLC   DXCCW11(FOUR),ERROR     HAS AN ERROR CODE BEEN PASSED
*                                                              @ZA10074
         BNE   BYPASMSG                NO, BY PASS ERROR MESSAGE
**
*
         MVC   MESSAGEA(D70),WTO0045   MOVE IN WTO MESSAGE
         LA    RF,MESSAGEA              GET ADDRESS OF WTO MESSAGE
         SVC   SVC35                   ISSUE WTO MESSAGE
         LA    RJ,FIVE                 LOAD FORMAT ERROR CODE IN REG 15
         B     ABEN                    BRANCH TO ISSUE ABEND
LEVELID  DC    C'02'                   MODULE LEVEL CONTROL
*
BYPASMSG EQU   *
         SR    RB,RB
         IC    RB,DCBBUFNO             PICK UP BUFFER NUMBER
         TM    DCBCIND2,QSAMFLG        USING QSAM
         BNO   TESTBSAM                NO, BRANCH TO CHECK BSAM
         LTR   RB,RB                   HAS A BUFFER NUMBER BEEN SPECIFD
         BNZ   CONTINUE                YES, BRANCH AND ASSUME CORRECT
*                                      BUFFER NUMBER.
         TM    DEBOPATB,INPUTMSK       CHECK FOR INPUT DATA SET
         BO    CKOUT                   NO, BRANCH
         BM    CKOUT                   NO, BRANCH
         TM    DCBFUNC,PW              3525 PUNCH OR PRINT DATA SET
         BZ    READONLY                YES, BRANCH
CKOUT    EQU   *
         OI    DCBBUFNO,ONE            SET BUFFER NUMBER TO ONE
         B     CONTINUE                BRANCH
READONLY EQU   *
         OI    DCBBUFNO,TWO            DEFAULT TO TWO BUFFERS
CONTINUE EQU   *
         TM    DCBBUFNO+THREE,ONE      BUFFER CONTROL BLOCK SPECIFIED
         BNO   LIDOF7N                 NO BRANCH
TESTBSAM EQU   *
         TM    DCBBUFNO+THREE,ONE      BUFFER CONTROL BLOCK SPECIFIED
         BNO   LIDOF7N                 NO, BRANCH
         LTR   RB,RB                   HAS A BUFFER NO. BEEN SPECIFIED
         BZ    LIDOF7N                 NO, BRANCH TO LOAD ID OF 7N
LIDOF1I  EQU   *
         LA    RF,SOP7                 LOAD ID OF IGG0191I
         B     XCTLGOGO                BRANCH TO XCTL ROUTINE
LIDOF7N  EQU   *
         LA    RF,SOPN7N               LOAD ID OF IGG0197N
XCTLGOGO EQU   *
         MVC   ZERO(FIVE,RWTGC),ZERO(RF) SAVE ID OF NEXT MODULE
RELOOP   EQU   *
*
         LA    RWTGC,WGOFF(ZERO,RWTGC) INCREMENT CURRENT WTG ENTRY
         LA    RPARC,PLOFF(ZERO,RPARC) INCRE CURRENT DCB ENTRY POINTER
         CLC   ZERO(TWO,RWTGC),AMIDCNST THIS ROUTINE NEEDED AGAIN
         BCR   RWTGC,RBASE             BACK TO OPEN CONTROL ROUTINE
*
         CLC   ZERO(TWO,RWTGC),OPIDCNST END OF TABLE
         BNE   RELOOP                  NO, CHECK NEXT ENTRY
*
         LR    RPARC,RPAR
         LA    RWTGC,WAOFF(ZERO,RWTG)  REINITIALIZE WTG LIST POINTER
ZCHEK    CLI   ZERO(RWTGC),ZERO        ENTRY COMPLETE
         BNE   TCTLRTN                 IF NOT, TRANSFER CONTROL
*
         LA    RWTGC,WGOFF(ZERO,RWTGC) GET NEXT ENTRY
         LA    RPARC,PLOFF(ZERO,RPARC) GET NEXT ENTRY
         B     ZCHEK                   BRANCH TO ZERO CHECK
*
TCTLRTN  EQU   *
*
         MVC   SIX(TWO,RWTG),0(RWTGC)  STORE ID IN WTG TABLE
         MVC   C14(THRE,RWTG),TWO(RWTGC) MOVE TTR INTO TABLE
*
         LA    RJ,DXCCW12              SET UP FOR XCTL
*
         XCTL  DE=(RWTG),SF=(E,(15))   ISSUE XCTL
*
**
***
**
*
ABEN     EQU   *
*
         L     RF,ABEND4               LOAD ABEND CODE OF 4
         SLL   RF,12
         ABEND (1),DUMP
*
***********************************************************************
*
WAOFF    EQU   32
WGOFF    EQU   8                       OFFSET OF WTG ENTRIES
PLOFF    EQU   4                       OFFSET OF DCB ENTRIES
OPIDCNST DC    C'0S'                   ID
AMIDCNST DC    C'7M'                   THIS MODULE'S ID
         DS    0F                      ALIGN ON A FULLWORD BOUNDARY
ERROR    DC    X'00000005'             CONSTANT USED FOR COMPARES
ABEND4   DC    X'00000004'             ABEND CODE OF 4
*
WTO0045  WTO   'IEC951I 004-5 INVALID FORMAT CARD OR INVALID DEVICE FOR*
                OMR',MF=L,ROUTCDE=(11),DESC=4
*
***********************************************************************
*
         ORG   IGG0197M+1000           LEAVE MAINT SPACE AS REQ.
SOP7     DC    C'1I    '               ID OF IGG0191I
SOPN7N   DC    C'7N    '               ID OF IGG0197N
         DC    H'0'                    END OF TABLE MARKER
         DC    3H'0'                   FILLER
         DC    C'019'                  OPEN SVC CODE
EBL      DC    AL1(125)                DBL WD CNST TO TOP OF WTG
***********************************************************************
*************************       DSECTS       **************************
***********************************************************************
*
*
         DCBD  DSORG=PS
         IEZDEB
FORCORE  DSECT
         IECDSECT
         END
