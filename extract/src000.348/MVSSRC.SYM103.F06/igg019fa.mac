         TITLE 'IGG019FA-CNTRL MOD. FOR SKIP AND SPACE ON 3525 DEVICE'
         START
***********************************************************************
*
*         RELEASE       DELETIONS/CHANGES
*D668000                                                        YA00439
*C 432000,808000                                               @ZA01573
*
*NAME--              IGG019FA
*
*TITLE               'IGG019FA' - CNTRL MODULE FOR 3525 PRINTER
*
*STATUS CHANGE LEVEL 000
*                    THIS MODULE WAS PUT INTO 'OS' RELEASE 21.0 AS A
*                    NEW MODULE
*
*MODULE TYPE--       CONTROL
*
*
*FUNCTION/OPERATION--BSAM/QSAM CONTROL FOR SKIPPING AND SPACING ON
*                    THE 3525 CARD PUNCH. MODIFIES COMMAND BYTE IN
*                    WRITE CCW TO CAUSE REQUESTED SKIP OR SPACE.
*                    INCREMENTS LINE POSITION COUNTER (DCBLNP) TO
*                    INDICATE LINE BEING SKIPPED TO OR SPACED TO.
*
*ENTRY POINTS--      IGG019FA FROM CNTRL MACRO EXPANSION.
*                             SPACE
*                    LA    1,DCB
*                    LA    0,1         (SPACE 1)
*                    L     15,84(1,0)
*                    BALR  14,15
*
*                             SKIP
*                    LA    1,DCB
*                    LA    0,1         (SKIP 1)
*                    LCR   0,0
*                    L     15,84(1,0)
*                    BALR  14,15
*
*INPUT--             PARAMETERS
*                    DCB ADDRESS IN REGISTER 1
*                    SPACE OR SKIP MASK IN REGISTER 0
*                    DCB'S FOLLOWING FIELDS:
*                             DCBIOBA--IOB ADDRESS
*                             DCBLNP---LINE POSITION COUNTER
*                             DCBCIND2-X'01' INDICATES QSAM
*
*OUTPUT--            IOB
*                    DCBLNP INCREMENTED TO INDICATE LINE POSITION.
*                    CONTROL MODIFIER IN OP CODE OF CCW.
*
*EXTERNAL ROUTINES-- IOS SUPERVISOR (EXCP, WAIT)
*
*EXITS-NORMAL--      RETURN TO CALLING ROUTINE ON REG. 14
*
*EXITS-ERROR--       NONE
*
*TABLES/WORKAREA--   CALLING ROUTINE'S SAVE AREA--REGS. 14-9   @ZA01573
*                    BEGINNING IN SAVE+20.                     @ZA01573
*
*ATTRIBUTES--        REENTRANT AND REUSABLE.
***********************************************************************
PUTLOC   EQU   X'48'
QSAM     EQU   X'01'
ONE      EQU   X'01'
TWELVE   EQU   X'0C'
COMP     EQU   X'40'
RATE     EQU   8                       LAST REG USED BY THIS ROUTINE
WRKR     EQU   0
WRKR4    EQU   4
DCBR     EQU   7                       DCB ADDRESS
IOBAR    EQU   2
COUNTR   EQU   6
BASR     EQU   5                       BASE REG FOR THIS ROUTINE
SAVR     EQU   13
RETR     EQU   14
ENTR     EQU   15                      CONTAINS BASE OF THIS ROUTINE
LINECTR  EQU   3                       AT ENTRY
PARAM    EQU   1
CCWR     EQU   8
ONEBUFF  EQU   X'FE'
HUNDRD31 EQU   X'83'
FOUR     EQU   X'04'
CCOFF    EQU   X'B0'
CNTRL   EQU   X'08'
ZERO     EQU   X'00'
TWENTY4  EQU   X'18'
FDSSCMD  EQU   X'23'                   FEED, STACKER SELECT COMAND
RP       EQU   X'60'                   MASK FOR READ/PUNCH DADS
RW       EQU   X'50'                   MASK FOR READ/WRITE DADS
PW       EQU   X'30'                   MASK FOR PUNCH/PRINT DADS
D20      EQU   20
D08      EQU   8
D01      EQU   1
D03      EQU   3
D40      EQU   40
D07      EQU   7
DEBR     EQU   9                                                YA00439
PCHBIT   EQU   X'20'                                            YA00439
RDBIT    EQU   X'40'                                            YA00439
SEQFLAG  EQU   X'08'                                            YA00439
*
*
IGG019FA CSECT
*
         USING IHADCB,DCBR
         USING DEBBASIC,DEBR                                    YA00439
         USING IGG019FA,BASR
*
*
*
*  THIS ROUTINE SAVE REGISTERS AND CHECKS TO SEE IF THE IOB'S ECB IS
*  POSTED AS BEING COMPLETE. IF NOT A WAIT IS ISSUED TO IOB'S ECB. WHEN
*  ECB IS POSTED COMPLETE AND CNTRL ISSUED FOR SPACE ADD THIS VALUE TO
*  LINE COUNTER BUCKET IN 'DCBLNP'.
*
         STM   RETR,DEBR,D20(SAVR)     SAVE USER REGS. 14 - 9  @ZA01573
         LR    BASR,ENTR               GET BASE ADDRESS
         LR    DCBR,PARAM              GET DCB ADDRESS
         L     DEBR,DCBDEBAD           GET DEB ADDRESS FOR BASE YA00439
         L     IOBAR,DCBIOBA           LOAD IOB START ADDRESS
         OI    ZERO(IOBAR),CNTRL       SET CONTROL FLAG IN IOB PREFIX
         LR    COUNTR,WRKR             COPY REG 0 TO GET SP AND SK PARM
         L     IOBAR,ZERO(IOBAR)       LINK TO NEXT IOB
         TM    DCBCIND2,QSAM           IS QSAM BEING USED
         BZ    BSAM                    NO,BRANCH
         L     PARAM,TWELVE(IOBAR)     LOAD ECB ADDRESS IN A REGISTER
         TM    ZERO(PARAM),COMP        IS EVENT COMPLETE
         BO    COMPLETE                YES, BR TO CK FOR SP AND SK PARM
         LA    WRKR,D01                LOAD WAIT COUNT
         WAIT  (0),ECB=(1)
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
*
COMPLETE LTR   COUNTR,COUNTR           IS THIS A SKIP OR SPACE OPRATION
         BM    MINUS                   MINUS IS SKIP. PLUS IS SPACE.
         TM    DCBFUNC,RP              READ OR PUNCH BIT ON
         BM    SPACE                   YES, BRANCH AROUND
         BO    SPACE                   YES, BRANCH AROUND
         CLI   DCBLNP,ZERO             LINE POSITION IS ZERO
         BNE   SPACE                   NO, BRANCH
         B     CONTINUE                YES, DO FD, SS COMMAND
SPACE    EQU   *
         SR    LINECTR,LINECTR         ZERO OUT REGISTER
         IC    LINECTR,DCBLNP          GET LINE COUNT BUCKET FROM DCB
         AR    LINECTR,COUNTR          INCREMENT LINE COUNTER
         STC   LINECTR,DCBLNP          RESTORE LINE COUNTER IN DCB
         B     RETURN                  RETURN TO CALLING PROGRAM
LEVELID  DC    C'05'                   MODULE LEVEL CONTROL
*
*  THIS ROUTINE SKIP TO WHATEVER CHANNEL SPECIFIED ON CNTRL MACRO BY
*        1.ISSUING A FD,SS COMMAND IS SKIP TO CHANNEL 1 IS SPECIFIED OR
*          IF THE CHANNEL SPECIFIED IS LESS THAN THE CHANNEL'S PRESENT
*          LOCATION. THE 'DCBLNP' IS RESET TO CORRESPONDING LINE
*          POSITION AND EXCP ISSUED TO FEED ANOTHER CARD
*
*        2.UPDATING 'DCBLNP' IF SPECIFIED CHANNEL IS GREATER THAN THE
*          PRESENT CHANNEL'S LOCATION.
*
MINUS    EQU   *
         LCR   COUNTR,COUNTR           PUT SKIP NUMBER IN PLUS FORM
         TM    DCBFUNC,RP              READ OR PUNCH BIT ON
         BM    SKIP                    YES, BRANCH AROUND
         BO    SKIP                    YES, BRANCH AROUND
         CLI   DCBLNP,ZERO             LINE POSITION IS ZERO
         BNE   SKIP                    NO, BRANCH
         B     CONTINUE                YES, DO FD, SS COMMAND
SKIP     EQU   *
         LA    LINECTR,D01             SET UP TO COMPARE FOR CH-1
         CR    COUNTR,LINECTR          SKIP TO CHANNEL 1
         BE    CONTINUE                YES, BRANCH TO ISSUE FD,SS CMD
         SR    LINECTR,LINECTR         ZERO OUT REGISTER
         IC    LINECTR,DCBLNP          GET LINE COUNT BUCKET FROM DCB
         LR    WRKR4,COUNTR            SET UP WORK REG FOR COMPARE
         SLA   WRKR4,ONE               MULTIPLY BY TWO
         BCTR  WRKR4,ZERO              SUBTRACT ONE
         CLR   LINECTR,WRKR4           SKIP AMOUNT GE TO MASK
         BL    VALIDSKP                NO, BR TO CONTINUE       YA00439
         TM    DCBFUNC,PCHBIT          IS PUNCH BIT ON          YA00439
         BNO   CKRDBIT                 NO, CHECK FOR READ BIT   YA00439
         L     DCBR,DEBPDCB            GET PUNCH DCB ADDRESS    YA00439
SEQCK    EQU   *                                                YA00439
         TM    DCBQSWS,SEQFLAG         IS SEQUENCE FLAG ON      YA00439
         BZ    PRNTWTP                 NO, BR TO ERROR MSG.     YA00439
         L     DCBR,DEBDCBAD           GET CURRENT DCB ADDRESS  YA00439
VALIDSKP EQU   *                                                YA00439
         STC   WRKR4,DCBLNP            RESTORE LINE COUNTER IN DCB
         B     RETURN                  BRANCH, TO RETURN TO CALLER
CKRDBIT  EQU   *                                                YA00439
         TM    DCBFUNC,RDBIT           IS READ BIT ON           YA00439
         BNO   CONTINUE                NO, BR TO ISSUE FDSS CMD YA00439
         L     DCBR,DEBRDCB            IF SO,GET READ DCB ADDR  YA00439
         B     SEQCK                   GO CHECK FOR SEQ FLAG    YA00439
CONTINUE TM    DCBFUNC,RP              READ PUNCH ASSOCIATED DATA SET
         BO    PRNTWTP                 YES, BRANCH FOR ERROR MESSAGE
         TM    DCBFUNC,RW              READ PRINT ASSOCIATED DATA SET
         BO    PRNTWTP                 YES, BRANCH FOR ERROR MESSAGE
         TM    DCBFUNC,PW              PUNCH PRINT ASSOCIATED DATA SET
         BO    PRNTWTP                 YES, BRANCH FOR ERROR MESSAGE
         LA    CCWR,D40(IOBAR)         GET CCW ADDRESS
         NI    FOUR(CCWR),CCOFF        TURN OFF CMND CHAIN FLAG
         ST    CCWR,TWENTY4(IOBAR)     PUT CCW START ADDR IN IOB
         MVI   ZERO(CCWR),FDSSCMD      MOVE IN A FD,SS CMD
         MVI   D07(CCWR),ONE           LOAD BYTE COUNT OF ONE
*
*  ON SKIP TO CHANNEL Z 'DCBLNP'=2(Z)-1. E.G IF Z=5 THEN 'DCBLNP'=9
*  SINCE CHANNEL TO SKIP (Z) IS LESS THAN PRESENT CHANNEL POSITION
*  ISSUE FEED,STACKER,SELECT CCW AND UPDATE 'DCBLNP' WITH LINE VALUE
*
         SLA   COUNTR,ONE               DCBLNP = 2 TIMES CHANNEL VALUE
         BCTR  COUNTR,ZERO                       MINUS 1
*
         STC   COUNTR,DCBLNP            UPDATE DCBLNP WITH LINE COUNT
         LA    PARAM,D08(IOBAR)        PUT IOS IOB ADDRESS IN REG 1
         EXCP  (1)                     EXECUTE CHANNEL PROGRAM
         TM    DCBMACRF+ONE,PUTLOC     IS THIS IN PUT LOCATE MODE
         BZ    WAIT                    NO, BRANCH BECAUSE THIS IS BSAM
         BM    RETURN                  THIS IS QSAM WITHOUT LOCATE MODE
         CLI   DCBBUFNO,ONEBUFF        QSAM-LOCATE MODE WITH ONE BUFFER
         BM    RETURN                  NO, MORE THAN ONE BUFFER
WAIT     EQU   *
         LA    PARAM,FOUR(IOBAR)       LOAD ECB ADDR IN REG 1
         LA    WRKR,D01                LOAD WAIT COUNT
         WAIT  (0),ECB=(1)             WAIT
RETURN   EQU   *
         LM    RETR,DEBR,D20(SAVR)     RESTORE USER'S REGS     @ZA01573
         BR    RETR                    RETURN TO CALLING ROUTINE
*
*
BSAM     EQU   *
*
*
         LA    PARAM,FOUR(IOBAR)       GET ECB ADDRESS
         ST    PARAM,TWELVE(IOBAR)      STORE ECB IN IOB
         B     COMPLETE                 GO DETERMINE TYPE OF CNTRL
*
*
PRNTWTP  EQU   *
         WTO   'IEC950I 003-3 3525 ASSOCIATED DATA SET I/O SEQUENCE ERR*
               OR',ROUTCDE=(11),DESC=4
         LA    ENTR,D03                INDICATE ASSOCIATED DATA SET
*                                      PRINT SEQUENCE ERROR CODE TO BE
*                                      PASSED TO USER VIA REGISTER 15
         L     PARAM,ABEND3            GET SAM ABEND ERROR CODE
         SLL   PARAM,12                SET UP REGISTER 1 FOR ABEND
         ABEND (1),DUMP
*
         DS    0F                      ALIGN ON A FULLWORD BOUNDARY
ABEND3   DC    X'00000003'             ABEND CODE OF 3
         DC    12CL4'ZAPS'             MAINTENANCE AREA
*              THE FOLLOWING CNOP MUST REMAIN ON A HALF-WORD BOUNDARY
         CNOP  0,8
         DCBD  DSORG=PS
         IEZDEB                                                 XA00349
         END
