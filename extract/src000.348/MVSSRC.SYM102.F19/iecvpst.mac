PST      TITLE '   IECVPST - PROLOGUE   '
****************************************************************
*                                                              *
* MODULE NAME = IECVPST                                        *
*                                                              *
* DESCRIPTIVE NAME = POST STATUS                               *
*                                                              *
* COPYRIGHT = NONE                                             *
*                                                              *
* STATUS = CHANGE LEVEL 00                                     *
*                                                              *
* FUNCTION = POST STATUS IS AN EXTENSION OF IOS RUNNING        *
*            ENABLED. IT IS SCHEDULED BY IOS TO RUN UNDER      *
*            AN SRB FOR TERMINATING AN I/O REQUEST, AND        *
*            OPTIONALLY RUNS UNDER THE LOCAL LOCK. POST        *
*            STATUS INTERFACES WITH USER SPECIFIED EXITS       *
*            (NORMAL, ABNORMAL AND PCI), ATTENTION ROUTINES    *
*            AND DAVV FOR HANDLING UNSOLICITED DEVICE END      *
*            INTERRUPTS. POST STATUS ALSO INTERFACES WITH      *
*            DEVICE DEPENDENT ERROR ROUTINES AND HANDLES       *
*            THEIR RETURNS VIA SVC F.                          *
*                                                              *
* NOTES = NONE                                                 *
*                                                              *
*    DEPENDENCIES = NONE                                       *
*                                                              *
*    RESTRICTIONS = SVC F PROTECTS ITSELF FROM NON-AUTHORIZED  @ZA05814
*    USERS BY THE 'TESTAUTH' FUNCTION. IT ALLOWS ONLY SUPER-   @ZA05814
*    VISOR STATE CALLERS AND ABENDS ANY NON-SUPERVISOR STATE   @ZA05814
*    CALLER.                                                   @ZA05814
*                                                              *
*    REGISTER CONVENTIONS = SEE EQUATES WITHIN CSECT           *
*                                                              *
*    PATCH LABEL = IOSPATCH - LOCATED IN IECIOSCN              *
*                                                              *
* MODULE TYPE = CONTROL                                        *
*                                                              *
*    PROCESSOR = ASSEMBLER XF                                  *
*                                                              *
*    MODULE SIZE = 2500 DECIMAL BYTES                          *
*                                                              *
*    ATTRIBUTES = RESIDENT, REENTRANT, ENABLED, KEY 0,         *
*                 SUPERVISOR STATE                             *
*                                                              *
* ENTRY POINT = IECVPST                                        *
*               IGC015                                         *
*                                                              *
*    PURPOSE = SEE FUNCTION                                    *
*                                                              *
*    LINKAGE = R0 LOADED WITH SRB ADDRESS                      *
*              R1 LOADED WITH IOSB ADDRESS                     *
*              R15 LOADED WITH RETURN ADDRESS                  *
*                                                              *
*    INPUT = ADDRESS OF UCB IN IOSB                            *
*            ADDRESS OF ERP WORK AREA IN IOSB                  *
*            IOSFLA SET BY ERP AND DAVV                        *
*                                                              *
*    OUTPUT = COMPLETION CODE IN IOSB (IOSCOD)                 *
*             TRANSLATED CSW IN IOSB                           *
*                                                              *
*    EXIT-NORMAL = RETURN TO DISPATCHER                        *
*                                                              *
*    EXIT-ERROR = ABEND NON-SUPERVISOR STATE CALLERS OF SVC F  @ZA05814
*                                                              *
* EXTERNAL REFERENCES = NONE                                   *
*                                                              *
*    ROUTINES = NORMAL END APPENDAGE                           *
*               ABNORMAL END APPENDAGE                         *
*               PCI APPENDAGE                                  *
*               ATTENTION ROUTINE                              *
*               IECVSMGR - STORAGE MANAGER                     *
*               IECVDERP - DA ERROR PROCESSOR                  *
*               ADDRESS TRANSLATOR REAL TO VIRTUAL             *
*               STAGE II EXIT EFFECTOR                         *
*                                                              *
*    DATA SETS = NONE                                          *
*                                                              *
*    DATA AREAS = NONE                                         *
*                                                              *
* TABLES = NONE                                                *
*                                                              *
* MACROS = SCHEDULE, SETFRR, SETRP, STARTIO                    *
*           TESTAUTH,ABEND                                     @ZA05814
*                                                              *
* CHANGE ACTIVITY = NONE                                       *
*                                                              *
****************************************************************
         EJECT
****************************************************************
*                                                              *
* ENTRY POINT = IGC015                                         *
*                                                              *
*        PURPOSE = SEE FUNCTION                                *
*                                                              *
*        LINKAGE = R1 LOADED WITH IOSB ADDRESS                 *
*                  R 14 LOADED WITH RETURN ADDRESS             *
*                                                              *
* INPUT = ADDRESS OF UCB IN IOSB                               *
*         ADDRESS OF ERP WORK AREA IN IOSB                     *
*         IOSERR (IOSFLA) SET BY ERP AND DAVV                  *
*                                                              *
* OUTPUT = COMPLETION CODE IN IOSB (IOSCOD)                    *
*          TRANSLATED CCW ADDRESS IN IOSB (IOSCSW)             *
*                                                              *
* EXIT-NORMAL = RETURN TO DISPATCHER                           *
*                                                              *
* EXIT-ERROR = NONE                                            *
*                                                              *
* EXTERNAL REFERENCES = NORMAL END APPENDAGE                   *
*                       ABNORMAL END APPENDAGE                 *
*                       PCI APPENDAGE                          *
*                       ATTENTION ROUTINE                      *
*                       IOS STORAGE MANAGER ROUTINE            *
*                       RESIDENT DIRECT ACCESS ERROR PROCEDURE *
*                       REAL TO VIRTUAL ADDRESS TRANSLATOR     *
*                       STAGE II EXIT EFFECTOR                 *
*                                                              *
*        CONTROL BLOCKS = ASCB     ASVT     ASXB     CVT       *
*                         ERPWA    IOMB     IOQ      IOSB      *
*                         IRT      LCCA     LCH      PSA       *
*                         SDWA     SRB      UCB                *
*                                                              *
* TABLES = NONE                                                *
*                                                              *
* MACROS = SCHEDULE, SETFRR, SETRP, STARTIO                    *
*                                                              *
* CHANGE ACTIVITY = AS FOLLOWS                                 *
*                                                              *
****************************************************************
         TITLE '   IECVPST - DSECTS     '
         CVT   DSECT=YES,LIST=NO
         EJECT
         IHAASCB
         EJECT
         IHAASVT
         EJECT
         IHAASXB
         EJECT
         DSECT
         IEFUCBOB  LIST=YES,PREFIX=YES
         EJECT
         IHASRB
         EJECT
         IECDIOSB
         EJECT
         IHAPSA
         EJECT
         IHALCCA
         EJECT
         IECDIRT
         EJECT
         IHAWSAVT  DSECT=YES,CLASS=CPU
         EJECT
         IECDERWA
         EJECT
         IECDIOQ
         EJECT
         IECDIOCM
         EJECT
         IECDLCH
         EJECT
         IHAFRRS
         EJECT
         IHASDWA
         EJECT
         ILRIORB                                               @Z40WPLL
         EJECT
FRRAREA  DSECT
****************************************************************
*                                                              *
*  THIS FRR SAVE AREA IS FORMATTED AND USED BY POST STATUS     *
*  AND SVC F. IT IS SIX WORDS IN LENGTH.                       *
*                                                              *
****************************************************************
         SPACE 1
         DS    0F
FRRFLGA  DS    XL1                 POST STATUS INDICATORS
FRRLOCAL EQU   X'C0'               LOCAL LOCK HELD
FRRLLHLD EQU   X'80'               POST STATUS GOT LOCAL LOCK
FRRSVCNT EQU   X'40'               SVC ENTRANCE
FRRERPNT EQU   X'20'               DA ERP ENTERED
FRRSYNCH EQU   X'10'               IOSYNCH LOCK HELD
FRRCATLK EQU   X'08'               CAT LOCK HELD
FRRSAF   EQU   X'04'               SAVE AREA ADDRESS EXITS     @ZA00904
FRRWDAV  EQU   X'02'               UCBWDAV ON ENTRY TO PSTWAIT @ZA00904
*        EQU   X'01'               RESERVED
FRRFLGB  DS    XL1                 TRACKING INDICATORS
FRRSECNT EQU   X'80'               FRR SECOND ENTRY FLAG
FRRECRSN EQU   X'40'               FRR RECURSION FLAG
FRRIIOSB EQU   X'20'               IOS OWNS IOSB
FRRUCBLK EQU   X'10'               UCB LOCK HELD               @ZA06077
*        EQU   X'1F'               RESERVED
         DS    H                   RESERVED
FRRSRB   DS    A                   SRB ADDRESS
FRRSA    DS    A                   SAVE AREA ADDRESS           @ZA00904
FRRREGD  DS    A                   SAVE AREA FOR REG 13 IF SVC
*                                  F IS BRANCH ENTERED
FRRDISP  DS    A                   DISP RETURN ADDR FOR P.S.
FRRTMRET DS    A                   RTM RETURN ADDRESS
         SPACE 2
*  THIS FRR SAVE AREA IS USED BY PSTWAIT FOR PROTECTING THE
*    RESTART RESOURCE
FRRW     DSECT
FRRWBASE DS    F                   BASE REG                    @ZA00904
FRRWCVT  DS    F                   CVT POINTER                 @ZA00904
FRRWIOSB DS    F                   IOSB POINTER                @ZA00904
FRRWSRSW DS    X                   RESOURCE HELD SWITCH        @ZA00904
         DS    XL11
         EJECT
         IHARB                                                 @ZA05822
         EJECT
BUFFER   DSECT
****************************************************************
*                                                              *
*  THIS BUFFER AREA IS FORMATTED AND USED BY POST STATUS FRR   *
*  FOR INITIALIZING THE 4K BUFFER USED BY SDUMP.               *
*                                                              *
****************************************************************
         SPACE 1
         DS    0F
BUFDATAD DS    A                   ADDRESS OF DATA IN BUFFER
BUFDATLG DS    H                   LENGTH OF DATA
BUFDATA  DS    XL(IOSEND-IOSB)     DATA - IOSB FOR FRR
BUFAD2   DS    A                   ADDRESS OF NEXT DATA        @ZA06077
BUFLEN2  DS    H                   LENGTH OF DATA              @ZA06077
BUFDATA2 DS    XL4                 DATA-HI LOCK HELD IND.      @ZA06077
BUFCNST  DS    XL6                 6 BYTE ZERO DELIMITER
         SPACE 3
*  WORKAREA WITHIN SAVE AREA FOR RESTARTABLE WAIT SUBROUTINE
XPSA     DSECT
XPSACD   DS    0H                  WAIT STATE REASON CODE      @ZA00904
XPSAWAIT DS    X                   DEFINED NEXT                @ZA00904
XPSAVID  EQU   X'80'                 WRONG VOL SER             @ZA00904
XPSAIR   EQU   X'40'                 INTERVENTION REQUIRED     @ZA00904
XPSACC3  EQU   X'20'                 CONDITION CODE 3          @ZA00904
XPSAERR  EQU   X'10'                 PERM READ ERROR           @ZA00904
*        DS    X                     PADDING                   @ZA00904
XPSADEV  DS    H                     DEVICE ADDRESS            @ZA00904
XRNPSW1  DS    2F                  RESTART NEW PSW SAVE AREA   @ZA00904
         TITLE '   IECVPST - EQUATES    '
****************************************************************
*                                                              *
*                   REGISTERS  DEFINITIONS                     *
*                                                              *
****************************************************************
         SPACE 2
SRBREG   EQU   0                   SRB ADDRESS
REG0     EQU   0                   GENERAL REGISTER 0          @ZA00904
PARMRG   EQU   1                   PARM REGISTER CONTAINING IOSB
IOSBRG   EQU   2                   ADDRESS OF IOSB
*        EQU   3                   NOT USED
FRRSAV   EQU   4                   FRR WORKAREA SAVE
BASERG   EQU   5                   BASE REGISTER FOR POST STATUS
*        EQU   6                   NOT USED
UCBREG   EQU   7                   ADDRESS OF UCB;  ALSO USED
*                                  TO SAVE REG ACROSS SETLOCK
WRKRG1   EQU   8                   WORK REGISTER 1
WRKRG2   EQU   9                   WORK REGISTER 2
TRAREG   EQU   10                  XLTAE SUBROUTINE LINKAGE
CORERG   EQU   11                  CORE MGMNT INTERFACE REGISTER
SAVERG   EQU   11                  SAVE REGISTER FOR @SDWA     @ZA02135
SRBASE   EQU   12                  SRB BASE REG FOR SCHEDULE
LLSAVE   EQU   13                  ADDR OF LOCAL LOCK SAVE AREA
RTRNRG   EQU   14                  RETURN ADDRESS REGISTER
LINKRG   EQU   15                  CALLING ADDRESS REGISTER
         SPACE 1
*        MISCELLANEOUS EQUATES
         SPACE 1
D0       EQU   0                   DISPLACEMENT OF 0
D1       EQU   1                   DISPLACEMENT OF 1
D2       EQU   2                   DISPLACEMENT OF 2
D3       EQU   3                   DISPLACEMENT OF 3
D4       EQU   4                   DISPLACEMENT OF 4
D5       EQU   5                   DISPLACEMENT OF 5
D7       EQU   7                   DISPLACEMENT OF 7
D8       EQU   8                   DISPLACEMENT OF 8
D16      EQU   16                  DISPLACEMENT OF 16
D24      EQU   24                  DISPLACEMENT OF 24
D28      EQU   28                  DISPLACEMENT OF 24
D31      EQU   31                  DISPLACEMENT OF 31
D159     EQU   159                 DISPLACEMENT OF 159
D512     EQU   512                 DISPLACEMENT OF 512
CSWCUS   EQU   X'50'               BUSY - STATUS MODIFIER
CSWWLR   EQU   X'40'               INCORRECT LENGTH
CSWUEX   EQU   X'01'               UNIT EXCEPTION
CSWPCI   EQU   X'80'               PCI
MASK00   EQU   X'00'               CONSTANT OF ZERO
MASK0F   EQU   X'0F'               MASK FOR TESTING CSW STATUS
MASKFF   EQU   X'FF'               MASK FOR RESETTING BITS
POSTCOD  EQU   X'60'               MASK TO TESTING IOSCOD
HEX3     EQU   X'03'               SYMBOLIC 3                  @ZA05814
NVALUSER EQU   X'20F'              SYSTEM ABEND CODE FOR NON-  @ZA05814
*                                   SUPERVISOR STATE CALLER OF @ZA05814
*                                   SVC F                      @ZA05814
CON4     EQU   4                   CONSTANT OF 4               @ZA05814
DISABLE  EQU   X'FC'               DISABLE I/O & EXTERNAL      @ZA00904
         TITLE '   IECVPST - INITIALIZATION  '
IECVPST  CSECT
         ENTRY IGC015
         BALR  BASERG,D0           SET ADDRESSABILITY
         USING *,BASERG
         SPACE 1
PSTBASE  LR    IOSBRG,PARMRG       PUT IOSB ADDRESS IN REG 2
         USING IOSB,IOSBRG         SET UP ADDRESSABILITY TO IOSB
         SPACE 1
         USING FLC,SRBREG
         LA    TRAREG,PSTFRRTN     GET  FRR ROUTINE ADDRESS
         SETFRR  A,FRRAD=(TRAREG),PARMAD=(FRRSAV),                     *
               WRKREGS=(WRKRG1,WRKRG2)
         USING FRRAREA,FRRSAV
         ST    SRBREG,FRRSRB       SAVE SRB ADDRESS
         ST    RTRNRG,FRRDISP      SAVE DISP RETURN ADDR
         SPACE 1
* TRANSLATE CCW ADDRESS IN CSW TO VIRTUAL ADDRESS
         SPACE 1
         BAL   TRAREG,PSTTRA       BRANCH TO XLATE CCW IN CSW
         EJECT
* THE FOLLOWING TESTS DETERMINE WHETHER TO GET THE LOCAL LOCK  *
         SPACE 1
         TM    IOSOPT,IOSPSLL      IF LOCK NOT WANTED (BIT ON)
         BO    PSTIOST             BRANCH AROUND GET LOCK
         SPACE 1
*        GET LOCAL LOCK VIA SETLOCK MACRO
         SPACE 1
         BAL   RTRNRG,PSTLOCK      BRANCH TO GET LOCAL LOCK
         SPACE 1
* TEST IF IOSB WAS GENERATED BY IOS FOR SPECIAL HANDLING
         SPACE 1
PSTIOST  TM    IOSFLA,IOSIOSB      IF IOS GENERATED IOSB,
         BO    PSTIOSB             GO HANDLE SPECIALLY
         SPACE 1
PSTNRM   CLI   IOSCOD,IOSNRMC      IF COMP. CODE IS 7F, BRANCH
         BE    PSTXLT              TO CONTINUE NORMAL PROCESSING
         OI    IOSFLA,IOSEX        OTHERWISE, SET EXCEPTION FLAG
         CLI   IOSCOD,POSTCOD      ON INITIAL ENTRY, ALL COMP.
*                                  CODES OF X'4X' AND X'5F'
*                                  (X=0-F) ARE ROUTED TO THE
*                                  ABNORMAL EXIT AS NO ERP
*                                  PROCESSING IS REQUIRED.
         BL    PSTCODE             GO TO X'45' ENTRANCE TEST
         L     LINKRG,IOSABN       GET ADDRESS OF ABNORMAL EXIT
         B     PSTAPP              AND BRANCH TO GO TO ABE EXIT
         TITLE '    IECVPST - APPENDAGE INTERFACE  '
PSTXLT   TM    IOSTSB,CSWPCI       PCI CONDITION IN CSW STATUS?
         BZ    PSTERR              NO, SKIP APPENDAGE
         SPACE 1
* INTERFACE WITH PCI APPENDAGE
         SPACE 1
PSTPCI   LR    PARMRG,IOSBRG       PUT ADDRESS OF IOSB IN REG 1
         L     LINKRG,IOSPCI       GET ADDRESS OF PCI EXIT
         LTR   LINKRG,LINKRG       IF NO PCI APPENDAGE,
         BZ    PSTERR              BRANCH AROUND
         BALR  RTRNRG,LINKRG       GO TO APPENDAGE
         SPACE 2
PSTERR   TM    IOSFLA,IOSERR       IF ERP IN CONTROL, BRANCH TO
         BO    PSTEFF              INTERFACE WITH ERP
         LH    WRKRG1,IOSTATUS     GET CSW STATUS
         N     WRKRG1,CDUWPA       IF OTHER THAN CHE,CUE,DVE,  @ZA03292
         BNZ   PSTABE              WLR, PCI, ATTN, BRANCH
         SPACE 1
* INTERFACE WITH CHANNEL END APPENDAGE
         SPACE 1
PSTCHE   L     LINKRG,IOSNRM       GET NORMAL EXIT ADDR IN R15
         TM    IOSTSA,CSWUEX       IF UNIT EXCEPTION OR
         BO    PSTSEX              GO TO SET IOSEX
         TM    IOSTSB,CSWWLR       INCORRECT LENGTH,
         BO    PSTSEX              GO TO SET IOSEX
         B     PSTAPP              GO TO APPENDAGE
         SPACE 1
* INTERFACE WITH ABNORMAL END APPENDAGE
         SPACE 1
PSTABE   L     LINKRG,IOSABN       GET ABNORMAL EXIT ADDRESS
PSTSEX   OI    IOSFLA,IOSEX        SET EXCEPTION INDICATOR
         SPACE 1
         TM    IOSTSA,CSWCUS       IF BUSY - STATUS MODIFIER,
         BNO   PSTAPP              NO, GO TO APPENDAGE         @ZA20593
         CLI   IOSDVRID,IOSXCPID   YES, THEN IS DRIVER EXCP?   @ZA20593
         BNE   PSTZRTN             NO, BYPASS ABNORMAL EXIT    @ZA20593
         SPACE 1
* INTERFACE WITH CORRECT APPENDAGE
         SPACE 1
PSTAPP   LR    PARMRG,IOSBRG       PUT IOSB ADDRESS IN REG 1
         BALR  RTRNRG,LINKRG       GO TO CHE OR ABN EXIT
         SPACE 1
****************************************************************
*                                                              *
*        CHANNEL END AND ABNORMAL END RETURN VECTOR TABLE      *
*                                                              *
         B     PSTZRTN             NORMAL                      *
         B     PSTFRTN             DO NOTHING                  *
         B     PSTERTN             RETRY                       *
         B     PSTTRTN             DDR PROCESSING REQUIRED     *
*                                  * RETURN +16 VECTOR IS HERE @Y30IPLB
         TM    FRRFLGA,FRRSVCNT    IF ENTRY BY SVC 15 THEN     @YM30155
         BNO   PSTRLS              RELEASE LOCAL LOCK AND EXIT @YM30155
         B     PSTRTN              IF NOT,BYPASS TERMINATION   @YM30155
*                                  CALL, RELEASE THE FRR, AND
*                                  THEN RETURN TO DISPATCHER.  @Y30IPLC
         SPACE
*                                                              *
****************************************************************
         SPACE 1
*        NORMAL RETURN BRANCHES HERE
         SPACE 1
PSTZRTN  TM    IOSFLA,IOSEX        EXCEPTION LEFT ON BY EXIT,
         BO    PSTEFF              GO TO INTERFACE WITH ERPS
PSTAPR   BAL   RTRNRG,PSTSUB       GO TO SUBR TO FREE RESOURCES YM01041
         SPACE 1
         NI    IOSOPT,MASKFF-IOSAPR  RESET APR ACTIVE INDICATOR
         SPACE 1
*        SCHEDULE CALLER VIA SCHEDULE MACRO
         SPACE 1
PSTSCHC  EQU   *                   SCHEDULE CALLER
         L     SRBASE,IOSSRB       GET SRB ADDRESS
         TM    FRRFLGA,FRRSVCNT    IF NOT SVC ENTRANCE,
         BZ    PSTBRCH             SKIP SCHEDULES
         USING SRBSECT,SRBASE      ESTABLISH ADDRESSABILITY
*        SET UP SRB FOR SCHEDULE
         SPACE 1
         MVZ   SRBPKF,IOSPKEY      MOVE PROTECT KEY TO SRB
         L     WRKRG1,IOSPGAD      MOVE USER TERM ADDRESS
         ST    WRKRG1,SRBEP        TO THE SRB
         LR    PARMRG,SRBASE       PUT SRB ADDR IN REG 1
         TM    IOSPKEY,IOSLCL      IF USER NOT IN LOCAL MEMORY,
         BZ    PSTGSCHD            GO SCHED IN GLOBAL MEMORY
         SCHEDULE  SRB=(1)         SCHED IN LOCAL MEMORY
         B     PSTRTN              RELEASE LOCK
PSTGSCHD SCHEDULE  SRB=(1),SCOPE=GLOBAL
         DROP  SRBASE
         B     PSTRTN              BR AROUND REG 14 SETUP
PSTBRCH  LR    PARMRG,IOSBRG       PUT IOSB ADDR IN REG 1      @ZA10093
*                                                              @YM02469
         LNR   SRBREG,SRBASE       LOAD NEG INCASE REL LK NOT NEEDED
         SR    IOSBRG,IOSBRG       ZERO TO USE AS BR
         TM    IOSOPT-IOSB(PARMRG),IOSPSLL+IOSTSLL CALLER WANT REL LK
         BZ    PSTRTN              NO BRANCH AROUND SETLOCK
*                                                              @YM02469
         LR    SRBREG,SRBASE       RESET                       @YM05458
PSTLLH   TM    FRRFLGA,FRRLLHLD    LOCK HELD                   @YM03287
         BZ    PSTRTN              NO                          @YM03287
         SPACE 1
         SPACE 1
*        RELEASE LOCAL LOCK
         SPACE 1
PSTRLS   EQU   *                   RELEASE LOCAL LOCK
         SETLOCK RELEASE,TYPE=LOCAL,                                   *
               RELATED=(PSLCL,IECVPST(PSTLOCK))
         SPACE 1
PSTRTN   EQU   *                   PREPARE FOR RETURN TO CALLER
         TM    FRRFLGA,FRRSAF      IS THERE A SAVE AREA TO FREE@ZA00904
         BZ    PSTRTN1             NO--EXIT                    @ZA00904
*   GET CAT LOCK TO PROVIDE SAVE AREA FOR STORAGE MANAGER      @ZA00904
         L     TRAREG,D16          SET UP ADDRESSABILITY TO    @ZA00904
         USING CVT,TRAREG          THE IOCOM VIA THE CVT       @ZA00904
         L     TRAREG,CVTIXAVL                                 @ZA00904
         USING IOCOM,TRAREG                                    @ZA00904
         SPACE 1
PSTCATO4 SETLOCK  OBTAIN,TYPE=IOSCAT,ADDR=IOCCATLK,MODE=UNCOND,        *
               RELATED=(PSCAT,IECVPST(PSTCATR4))               @ZA00904
         OI    FRRFLGA,FRRCATLK    SET CAT LOCK HELD IND.      @ZA00904
         SPACE 1
         USING PSA,0               GET LCCA SAVE AREA IN REG 13@ZA00904
         L     LLSAVE,PSALCCAV                                 @ZA00904
         USING LCCA,LLSAVE                                     @ZA00904
         L     LLSAVE,LCCACPUS                                 @ZA00904
         USING WSAC,LLSAVE                                     @AA00904
         L     LLSAVE,WSACIOS      ADDRESS OF IOS SAVE AREA    @ZA00904
         LR    WRKRG1,PARMRG       SAVE REG                    @ZA00904
         L     PARMRG,FRRSA        GET SAVEAREA TO FREE        @ZA00904
         XC    0(4,PARMRG),0(PARMRG)   CLEAR CHAIN PTR         @ZA00904
         L     CORERG,CORCNST2     INDICATE FREE 160 BYTES     @ZA00904
         L     LINKRG,COREMGR      GET STORAGE MANAGER ADDRESS @ZA00904
         BAL   RTRNRG,D4(LINKRG)   CALL FREE ENTRY             @ZA00904
         LR    PARMRG,WRKRG1       RESTORE REG                 @ZA00904
PSTCATR4 SETLOCK RELEASE,TYPE=IOSCAT,ADDR=IOCCATLK,                    *
               RELATED=(PSTCAT,IECVPST(PSTCATO4))              @ZA00904
PSTRTN1  EQU   *                                               @ZA00904
         L     LLSAVE,FRRREGD      RESTORE REG 13 (VALID FOR
*                                  SVC F BRANCH ENTRANCE)
         L     RTRNRG,FRRDISP      RESTORE RETURN REGISTER
         SETFRR D,WRKREGS=(WRKRG1,WRKRG2)
         LTR   IOSBRG,IOSBRG       IF REG IS ZERO, BRANCH RETURN
         BCR   D7,RTRNRG           NO, RETURN TO DISPATCHER
         DROP  IOSBRG                                           YM00152
         USING IOSB,PARMRG
         SPACE 1
*        SWAP TO USERS KEY BEFORE BRANCHING TO HIM
         MODESET  SWAPKEY,KEYADDR=IOSPKEY,WORKREG=8
         SPACE 1
         L     LINKRG,IOSPGAD      SET UP ADDR FOR BR RET      @ZA10093
         BR    LINKRG              TO CALLER VIA BRANCH
         DROP  PARMRG                                           YM00152
         USING IOSB,IOSBRG                                      YM00152
         EJECT
***********************************************************************
*   IECVDAVV IS TO RECEIVE CONTROL.  IF THE UNSOLICITED DEVICE END    *
*       IS FROM A PAGE PACK, CALL DAVV DIRECTLY WHILE STILL IN SRB    *
*       MODE--THIS PATH IS TO PREVENT INTERLOCKS WHICH MIGHT OTHERWISE*
*       OCCUR IF THE LOCAL LOCK WERE ALREADY HELD AND A PAGE FAULT    *
*       OCCURRED.                                                     *
***********************************************************************
         SPACE
PSTDAVV1 L     UCBREG,IOSUCB       POINT TO UCB                @ZA00904
         USING UCBOB,UCBREG                                    @ZA00904
         TM    UCBSTAB,UCBPGFL     IS CALL FOR PAGE PACK       @ZA00904
         BZ    PSTEFF              NO--CALL THRU EXIT EFFECTOR @ZA00904
         BAL   RTRNRG,PSTLOCK      YES--GET SPECIAL SAVE AREA  @ZA00904
         LR    PARMRG,IOSBRG       SET IOSB POINTER            @ZA00904
         L     LINKRG,DAVALT       GET ALTERNATE DAVV ENTRY    @ZA00904
         BALR  RTRNRG,LINKRG       CALL IECVDAVV               @ZA00904
***********************************************************************
*        SPECIAL DAVV RETURN VECTOR
*
         B     PSTGDPT             RETRY I/O                   @ZA00904
         B     PSTEWA              FREE IOSB AND EXIT          @ZA00904
         B     PSTRTN              EXIT DIRECTLY               @ZA00904
         B     PSTWAIT             CALL RESTARTABLE WAIT       @ZA00904
         TITLE '   IECVPST - ERP INTERFACE   '
******************************************************************
*                                                                *
*  ERROR ROUTINE INTERFACE                                       *
*                                                                *
******************************************************************
         SPACE 1
PSTEFF   TM    IOSFLA,IOSIOSB      IS THIS IOS IOSB?
         BO    PSTEFFA             YES-BYPASS CHECK FOR IDR
         SPACE 1
         TM    IOSPKEY,IOSIDR      PAGING I/O?
         BZ    PSTEFFA             NO-USE LOCAL LOCK SAVE AREA
         SPACE 1
*  PAGING I/O REQUEST - DO NOT USE LOCAL LOCK SAVE AREA AS
*  INTERLOCK COULD OCCUR
         SPACE 1
         L     LLSAVE,IOSUSE       POINT AT IOMB
         USING IORB,LLSAVE                                     @Z40WPLL
         SPACE 1
         L     LLSAVE,IORSAVE      POINT AT USER'S SAVE AREA   @Z40WPLL
         B     PSTSKEP             CONTINUE
         SPACE 3
PSTEFFA  TM    FRRFLGA,FRRLOCAL    IF LOCAL LOCK  HELD,
         BM    PSTDAVV             BRANCH AROUND
         BAL   RTRNRG,PSTLOCK      GO TO GET LOCAL LOCK
PSTDAVV  TM    IOSFLA,IOSIOSB      IF NOT AN IOS IOSB,
         BZ    PSTSKEP             BRANCH AROUND TYPE TEST
         SR    WRKRG1,WRKRG1       ZERO WORK REGISTER
         IC    WRKRG1,IOSPROC      GET PROCESS TYPE INDICATOR
         LTR   WRKRG1,WRKRG1       SPECIAL PROCESSING?
         BP    PSTWTO              YES, SKIP ERP TESTS         @ZA00904
PSTSKEP  TM    IOSOPT,IOSNERP      ARE IBM ERPS TO BE USED?
         BO    PSTCODP             NO, SKIP ERP INTERFACE & POST
         L     WRKRG1,IOSERP       GET ADDRESS OF WORKAREA
         LTR   WRKRG1,WRKRG1       WORKAREA EXIST?
         BNZ   PSTDAT              YES, SKIP CORE MGMNT INTRFACE
         SPACE 1
* INTERFACE WITH CORE MANAGEMENT TO GET ERP WORKAREA
         SPACE 1
         L     LINKRG,COREMGR      GET ADDR OF CORE MGMNT RTN
         LH    CORERG,IOSASID      PUT ASID IN REG 11
         SLL   CORERG,D16          SHIFT TO MIDDLE OF REGISTER
         SRL   CORERG,D8
         O     CORERG,CORCNST      INDICATE 1 BLOCK IN HIGH BYTE
*                                  AND SIZE (160) IN LOW BYTE
         BALR  RTRNRG,LINKRG       GO TO CORE MANAGEMENT
         SPACE 2
         ST    CORERG,IOSERP       PUT ADDR OF ERP WA IN IOSB
         MVI   D0(CORERG),D0       ZERO FIRST BYTE OF WORKAREA
         MVC   D1(D159,CORERG),D0(CORERG)                      @YM03259
*                                  PROPOGATE TO ZERO A TOTAL OF
*                                  160 BYTES IN ERP WORKAREA
PSTDAT   L     UCBREG,IOSUCB       GET UCB ADDRESS
         USING UCBOB,UCBREG
         TM    UCBTBYT3,UCB3DACC   IF NOT DIRECT ACCESS
         BZ    PSTSCHE             GO SCHEDULE ERP
         SPACE 1
*        INTERFACE WITH DIRECT ACCESS ERP
         SPACE 1
         LR    PARMRG,IOSBRG       PUT IOSB ADDRESS IN REG 1
         L     LINKRG,DAERP        GET ADDRESS OF DA ERP
         BALR  RTRNRG,LINKRG       GO TO RESIDENT DA ERP
         EJECT
****************************************************************
*                                                              *
*        DIRECT ACCESS ERP RETURN VECTOR TABLE                 *
*                                                              *
         B     PSTGDPT             RETRY RETURN                *
         B     PSTFTST             FETCH/PERM/FIXED ERROR     *@YM08292
         B     PSTWTO              ASYNC ERROR PROCESSING     *@ZA00904
         B     PSTPOST             POST RETURN                 *
*                                                              *
****************************************************************
         SPACE 2
PSTPOST  BAL   RTRNRG,PSTSUB       GO TO SUBR TO FREE RESOURCES
         B     PSTEABN             GO TO INTRFC WITH ABN EXIT
         SPACE 2
*  TEST FOR CALL TO IGE0025C (WTO ROUTINE) WHEN IOSB IS FOR THE PAGE  *
*        PACK--IF IT IS THEN ENTER A RESTARTABLE WAIT STATE.  THIS IS *
*        TO PREVENT A POSSIBLE INTERLOCK BETWEEN WAITING FOR LOCAL    *
*        LOCK AND A PAGE FAULT                                        *
         SPACE
PSTWTO   CLI   IOSPROC,IOSAWTO     IS THIS A CALL TO 25C       @ZA00904
         BNE   PSTSCHE             NO--CONTINUE NORMAL         @ZA00904
         L     UCBREG,IOSUCB       GET UCB POINTER             @ZA00904
         TM    UCBTBYT3,UCB3DACC   IS THE DEVICE DIRECT ACCESS @ZA00904
         BZ    PSTSCHE             NO--CONTINUE NORMAL         @ZA00904
         TM    UCBSTAB,UCBPGFL     IS IT A PAGE PACK           @ZA00904
         BZ    PSTSCHE             NO--CONTINUE NORMAL         @ZA00904
         TM    UCBFLC,UCBIVRR      IS INTERVENTION REQUIRED    @ZA00904
         BZ    PSTWTO10            NO--CHECK CC 3              @ZA00904
         NI    IOSFLC,MASKFF-IOSRWAIT YES--CLEAR WAIT FLAG     @ZA00904
         OI    IOSFLC,IOSRWIR      INDICATE INTV. REQ          @ZA00904
         OI    UCBFLA,UCBNRY       SET NOT READY--RESET WHEN   @ZA33089
*                                    OPERATOR READY'S DEVICE   @ZA33089
         B     PSTWAIT             CALL RESTARTABLE WAIT       @ZA33089
PSTWTO10 TM    IOSCC,IOSCC3        CONDITION CODE 3?  @ZA00904,@ZA18100
         BNO   PSTSCHE             NO--PERM I/O ERROR IS ONLY CONDITION
*                                  LEFT-HANDLE NORMLY @ZA00904,@ZA18100
         SR    WRKRG1,WRKRG1       CLEAR REGISTER              @ZA26432
         SR    WRKRG2,WRKRG2       CLEAR REGISTER              @ZA26432
         IC    WRKRG1,UCBCHM       GET PATHS AVAILABLE FROM    @ZA26432
*                                  UCB CHANNEL MASK            @ZA26432
         IC    WRKRG2,IOSWTOPT     GET PATHS THAT HAVE CC3     @ZA26432
*                                  FROM IOSB                   @ZA26432
         OR    WRKRG1,WRKRG2       PATHS UNAVAILABLE×ATTEMPTED @ZA26432
         L     TRAREG,UCBEXTPT    GET ADDR OF COMMON EXTENSION @ZA30808
         USING UCBCMEXT,TRAREG                                 @ZA30808
         IC    WRKRG2,UCBPMSK      GET CC=3 PATHS              @ZA30808
         DROP  TRAREG                                          @ZA30808
         OR    WRKRG1,WRKRG2       UNAVAILABLE×CC=3            @ZA30808
         CH    WRKRG1,KF0          X'F0' = NO PATHS AVAILABLE  @ZA30808
         BNE   PSTSCHE             IF THERE ARE STILL PATHS    @ZA26432
*                                  AVAILABLE, ISSUE MESSAGE    @ZA26432
*                                  IF NOT, WAIT 02F            @ZA26432
         NI    IOSFLC,MASKFF-IOSRWAIT  CLEAR WAIT FLAG         @ZA00904
         OI    IOSFLC,IOSRWCC3     INDICATE CONDITION CODE 3   @ZA00904
         B     PSTWAIT             CALL RESTARTABLE WAIT       @ZA00904
         SPACE 2
* INTERFACE WITH EXIT EFFECTOR TO SCHEDULE ERROR ROUTINE
         SPACE 1
PSTSCHE  L     LINKRG,D16          SET UP ADDRESSABILITY TO CVT
         USING CVT,LINKRG
         TM    CVTOPTA,CVTNIP      BYPASS E.E. FOR NIP PROCSSNG?
         BO    PSTERC              YES, CONSIDER PERM ERROR
         DROP  LINKRG
         L     SRBREG,IOSSRB       PUT SRB ADDRESS IN REG 0
         L     LINKRG,XITEFF       GET ADDRESS OF EXIT EFFECTOR
         SR    PARMRG,PARMRG       ZERO REGISTER 1
         BALR  RTRNRG,LINKRG       GO TO EXIT EFFECTOR
         B     PSTLLH              GO RETURN TO CALLER
         SPACE
* INTERFACE FOR PCI FETCH, IF IOSCOD=X71 GO TO PSTCHE,ELSE PSTEXT
         SPACE
PSTFTST  CLI   IOSCOD,IOSFTCHC     IS IT A 71 FOR FETCH        @YM08292
         BNE   PSTEXT              NO, BRANCH PERM/FIXED ERROR @YM08292
         B     PSTCHE              YES, GO TO CHE APNDG WITH 71@YM08292
         TITLE '   IECVPST - APPENDAGE RETURNS 4,8,12  '
* DO NOTHING RETURN (+4) BRANCHES HERE
         SPACE 1
PSTFRTN  LR    UCBREG,LINKRG       SAVE RETURN TO APPENDAGE
         BAL   RTRNRG,PSTSUB       GO TO SUBR TO FREE RESOURCES
         TM    FRRFLGA,FRRLLHLD    LOCAL LOCK HELD?
         BZ    PSTAPRTN            NO, BRANCH AROUND RELEASE
         SPACE 1
*        RELEASE LOCAL LOCK
         SPACE 1
PSTRLS1  SETLOCK  RELEASE,TYPE=LOCAL,RELATED=(PSLCL,                   *
               IECVPST(PSTLOCK))
         SPACE 1
         XI    FRRFLGA,FRRLLHLD    RESET LOCAL LOCK HELD IND.
PSTAPRTN LR    LINKRG,UCBREG       RESTORE APPENDAGE RETURN
         LR    PARMRG,IOSBRG       RESTORE IOSB                @YM6180
         BALR  RTRNRG,LINKRG       RETURN TO APPENDAGE
         SPACE 1
****************************************************************
*                                                              *
*  ALL RETURNS FROM APPENDAGES AT THIS POINT ARE HANDLED THE   *
*  SAME IN THAT THEY ARE IGNORED.                              *
*                                                              *
         B     PSTRTN              RETURN TO DISPATCHER        *
         B     PSTRTN              FOR ALL RETUNS FROM THE     *
         B     PSTRTN              APPENDAGES                  *
         B     PSTRTN                                          *
*                                                              *
****************************************************************
         SPACE 1
* RETRY RETURN BRANCHES HERE
         SPACE 1
PSTERTN  BAL   RTRNRG,PSTSUB       GO TO SUBR TO FREE RESOURCES
         SPACE 1
PSTSIO   TM    FRRFLGA,FRRLOCAL    LOCAL LOCK HELD?
         BM    PSTSIOM             YES, SKIP GETTING LOCK
         TM    IOSPKEY,IOSIDR      PAGING I/O ?                @YM06764
         BZ    PSTSIOT             NO - GET LOCAL LOCK         @YM06764
         ST    BASERG,FRRREGD      SAVE BASE, FRRREGD NOT NEED @YM08289
         LR    LLSAVE,FRRSAV       SAVE R4 IN R13 FOR SIO      @YM08289
         B     PSTSIOM2            DO SIO THIS PATH (IDR) RTRY @YM08289
         SPACE
PSTSIOT  BAL   RTRNRG,PSTLOCK      GO GET LOCAL LOCK
PSTSIOM  L     PARMRG,IOSSRB       GET ADDRESS OF SRB FROM IOSB
         SPACE 1
         STM   IOSBRG,BASERG,D0(LLSAVE) SAVE REGS
         STARTIO SRB=(PARMRG),TCB=SRB
         LM    IOSBRG,BASERG,D0(LLSAVE) RESTORE REGS
         SPACE 1
         B     PSTLLH              BRANCH TO RELEASE LOCK
         SPACE 2
* RETURN 12 SPECIFIES DDR PROCESSING REQUIRED AND IS USED ONLY
* BY THE PAGING SUPERVISOR.
         SPACE 1
PSTTRTN  TM    FRRFLGA,FRRLOCAL    IF LOCAL LOCK HELD,
         BM    PSTSKP              BRANCH AROUND GETTING IT
         BAL   RTRNRG,PSTLOCK      SUBROUTINE TO GET IT
PSTSKP   MVI   IOSPROC,IOSADDR     SET IND. FOR DDR PROCESSING
         B     PSTSCHE             GO TO EXIT EFFECTOR INTERFCE
         SPACE
PSTSIOM2 L     PARMRG,IOSSRB       GET ADDR OF SRB             @YM08289
         STARTIO SRB=(PARMRG),TCB=SRB                          @YM08289
         SPACE
         LR    FRRSAV,LLSAVE       GET R4 TO ADDRESS FRR       @YM08289
         L     BASERG,FRRREGD      RESTORE BASE REG            @YM08289
         LA    IOSBRG,1            SET IOSBREG NOT ZERO TO DISP@YM08289
         B     PSTRTN              RETURN TO DISPATCHER        @YM08289
         TITLE '   IECVPST - IOS IOSB HANDLING    '
****************************************************************
*                                                              *
*   THIS CODE IS BRANCHED INTO AS A VECTOR TABLE DEPENDING ON  *
*   THE CONTENTS OF AN INDEX IN THE IOSB (IOSPROC). THE  FOL-  *
*   LOWING CODE SETS UP TO DO THE BRANCH BASED ON  THE  INDEX  *
*   VALUES AS DESCRIBED BELOW:                                 *
*                                                              *
*   +0  - RESERVED - HANDLE AS NON-IOS GENERATED IOSB          *
*   +4  - PCI PROCESSING REQUIRED                              *
*   +8  - ATTENTION PROCESSING REQUIRED                        *
*   +12 - PURGE PROCESSING REQUIRED                            *
*   +16 - DAVV PROCESSING REQUIRED                             *
*   +20 - WTO PROCESSING REQUIRED                              *
*   ALL OTHER VALUES ARE CURRENTLY IGNORED                     *
*                                                              *
****************************************************************
         SPACE 1
PSTIOSB  CLI   IOSCOD,IOSABNC      IF POST CODE IS X'45',
         BE    PSTERC              BYPASS PROCESSING
         SPACE 1
         SR    WRKRG1,WRKRG1       ZERO WORK REGISTER
         IC    WRKRG1,IOSPROC      PICK UP INDEX
         B     PSTIDX(WRKRG1)      BRANCH BASED ON INDEX
         SPACE 1
****************************************************************
*                                                              *
PSTIDX   B     PSTNRM                                          *
         B     PSTHPCI                                         *
         B     PSTHATTN                                        *
         B     PSTPURG                                         *
         B     PSTDAVV1                                        *
         B     PSTEFF                                          *
         B     PSTHPCI                                         *
         B     PSTEFF                                          *
*                                                              *
****************************************************************
         SPACE 1
* INTERFACE WITH PCI APPENDAGE
         SPACE 1
PSTHPCI  LR    PARMRG,IOSBRG       PUT IOSB ADDRESS IN REG 1
         L     LINKRG,IOSPCI       GET PCI ENTRY ADDRESS
         BALR  RTRNRG,LINKRG       GO TO PCI APPENDAGE
         SPACE 2
PSTPCICH LR    TRAREG,LLSAVE       SAVE 13 ACROSS SETLOCK
* GET IOSYNCH LOCK WHILE INTERROGATING AND ADJUSTING PCI CHAIN
         SPACE 1
         L     CORERG,D16          SET UP ADDRESSABILITY TO
         USING CVT,CORERG          THE IOCOM VIA THE CVT
         L     CORERG,CVTIXAVL
         USING IOCOM,CORERG
         LR    UCBREG,CORERG       SAVE IOCM ADDR ACROSS SETLOCK
         SPACE 1
PSTSYNO  SETLOCK OBTAIN,TYPE=IOSYNCH,ADDR=IOCSYNCH,MODE=UNCOND,        *
               RELATED=(PSSYN,IECVPST(PSTSYN1,PSTSYN2))
         OI    FRRFLGA,FRRSYNCH    SET SYNCH LOCK HELD IND.
         LR    LLSAVE,TRAREG       RESTORE SAVE AREA ADDRESS
         LR    CORERG,UCBREG       RESTORE IOCM ADDRESS
         SPACE 1
PSTPCIL  L     WRKRG1,IOSIPIB      GET PCI CHAIN ADDRESS
         LTR   WRKRG1,WRKRG1       ANOTHER SRB EXIST?
         BZ    PSTFREE             NO, GO TO RETURN SRB'S
         L     WRKRG2,IOSPCHN      GET ADDR OF ENDING STATUS SRB
         CR    WRKRG1,WRKRG2       IS NEXT SRB FOR FINAL STATUS
         BE    PSTADJ              YES, BRANCH TO CLEAN-UP
         SPACE 1
         L     WRKRG2,IOSSRB       GET ADDRESS OF THIS SRB
         LR    IOSBRG,WRKRG1       PUT NEXT IOSB ADDR IN REG 2
         L     WRKRG1,IOSSRB-IOSB(WRKRG1)
*                                  GET ADDRESS OF NEXT SRB
         ST    WRKRG1,D0(WRKRG2)   CHAIN SRB'S VIA FIRST WORD
         SR    WRKRG2,WRKRG2       ZERO WORK REGISTER
         ST    WRKRG2,D0(WRKRG1)   ZERO FIRST WORD OF NEXT SRB
         TM    FRRFLGB,FRRECRSN    IF FRR IN CONTROL,
         BO    PSTPCIL             CONTINUE CHAINING
         SPACE 1
* RELEASE IOSYNCH LOCK
         SPACE 1
         LR    TRAREG,LLSAVE       SAVE 13 ACROSS SETLOCK
PSTSYN1  SETLOCK  RELEASE,TYPE=IOSYNCH,ADDR=IOCSYNCH,                  *
               RELATED=(PSSYN,IECVPST(PSTSYNO))
         XI    FRRFLGA,FRRSYNCH    RESET SYNCH LOCK HELD IND.
         LR    LLSAVE,TRAREG       RESTORE SAVE AREA ADDRESS
         BAL   TRAREG,PSTTRA       TRANSLATE CSW               @YM07021
         B     PSTHPCI             BRANCH TO GO TO PCI EXIT
         SPACE 1
PSTFREE  L     WRKRG2,IOSPCHN      GET ENDING STATUS IOSB ADDR
         USING IOSB,WRKRG2
         SR    WRKRG1,WRKRG1       ZERO WORK REGISTER
         ST    WRKRG1,IOSPCHN      ZERO POINTER TO FIRST SRB
         DROP  WRKRG2
         USING IOSB,IOSBRG         REESTABLISH ADDRESSABILITY
         SPACE 1
* RELEASE IOSYNCH LOCK
         SPACE 1
         LR    TRAREG,LLSAVE       SAVE 13 ACROSS SETLOCK
PSTSYN2  SETLOCK  RELEASE,TYPE=IOSYNCH,ADDR=IOCSYNCH,                  *
               RELATED=(PSSYN,IECVPST(PSTSYNO))
         XI    FRRFLGA,FRRSYNCH    RESET SYNCH LOCK HELD IND.
         LR    LLSAVE,TRAREG       RESTORE SAVE AREA ADDRESS
         DROP  CORERG
         SPACE 1
* INTERFACE WITH STORAGE MANAGEMENT
         SPACE 1
*  CHECK FOR FRR PROCESSING BEFORE GOING TO STORAGE MNGR       @ZA06077
         SPACE 1
         TM    FRRFLGB,FRRECRSN    IF FRR IN CONTROL,          @ZA06077
         BO    PSTFRRSD            BRANCH BACK TO FRR CODE     @ZA06077
         TM    FRRFLGA,FRRLOCAL    LOCAL LOCK HELD?
         BM    PSTCRM              YES, BYPASS GETTING IT
         SPACE 1
         BAL RTRNRG,PSTLOCK        GO TO GET LOCAL LOCK
         SPACE 1
PSTCRM   L     PARMRG,FRRSRB       PUT FIRST SRB ADDR IN REG 1 @ZA06077
*        DELETED TWO INSTRUCTIONS FOR APAR                     @OZ08484
PSTCRMA  LA    RTRNRG,PSTLLH       SET UP RETURN TO RELEASE LOCK
         B     PSTSUBR             GO TO SUBR TO INTERFACE WITH
*                                  STORAGE MANAGEMENT
         SPACE 1
* SCHEDULE POST STATUS TO RUN UNDER THE ENDING STATUS SRB
         SPACE 1
*                                  SET ENDING STATUS SRB ADDR
PSTADJ   L     SRBASE,IOSSRB-IOSB(WRKRG2) SRB ADDRESS
         USING SRBSECT,SRBASE
         L     WRKRG1,FRRSRB       GET SCHEDULED SRB ADDRESS
*                                  MOVE ASCB ADDR TO E.S. SRB
         L     WRKRG2,SRBASCB-SRBSECT(WRKRG1)
         ST    WRKRG2,SRBASCB
         L     WRKRG2,SRBCPAFF-SRBSECT(WRKRG1)
         ST    WRKRG2,SRBCPAFF     MOVE CPU AFF AND ASID TO SRB
         LM    WRKRG2,TRAREG,SRBEP-SRBSECT(WRKRG1)
         STM   WRKRG2,TRAREG,SRBEP MOVE EP AND RMTR TO SRB
*                                  MOVE PROTECT KEY TO SRB
         MVZ   SRBPKF,SRBPKF-SRBSECT(WRKRG1)
         IC    WRKRG2,SRBPRIOR-SRBSECT(WRKRG1)
         STC   WRKRG2,SRBPRIOR     MOVE PRIORITY TO SRB         YM01390
         LR    WRKRG2,PARMRG       SAVE SDWA PTR
         LR    PARMRG,SRBASE       PUT SRB ADDR IN REG 1
         SCHEDULE SRB=(1),SCOPE=GLOBAL SCHEDULE POST STATUS
         SPACE 2
         LR    PARMRG,WRKRG2       RESTORE SDWA PTR
         B     PSTFREE             GO TO FREE IOSB CHAIN
         SPACE 2
* ATTENTION HANDLING FOR IOS - IOSB
         SPACE 1
PSTHATTN LR    PARMRG,IOSBRG       PUT IOSB ADDRESS IN REG 1
         L     LINKRG,IOSPGAD      GET ADDR OF ATTENTION RTN
         BALR  RTRNRG,LINKRG       GO TO ATTENTION ROUTINE
         SPACE 1
****************************************************************
*                                                              *
*        BRANCH TABLE FOR ATTENTION ROUTINE RETURN             *
*                                                              *
         B     PSTNATN             NORMAL, PROCESSING COMPLETE *
         B     PSTSATN             RESCHEDULE TO PROPER MEMORY *
*                                                              *
****************************************************************
         SPACE 2
*  NORMAL RETURN BRANCHES HERE AS DOES PURGE PROCESSING CLEANUP
         SPACE 1
PSTNATN  TM    FRRFLGA,FRRLOCAL    IF LOCAL LOCK IS HELD,
         BM    PSTCRM              GO FREE IOS IOSB NORMALLY
         SPACE 1
*  GET CAT LOCK TO PROVIDE SAVE AREA FOR CORE MANAGER. THIS IS
*  DONE BECAUSE ATTENTION AND PURGE PROCESSING IS DONE WHILE
*  RUNNING IN THE MASTER MEMORY
         SPACE 1
         L     TRAREG,D16          SET UP ADDRESSABILITY TO
         USING CVT,TRAREG          THE IOCOM VIA THE CVT
         L     TRAREG,CVTIXAVL
         USING IOCOM,TRAREG
         SPACE 1
PSTCATO  SETLOCK  OBTAIN,TYPE=IOSCAT,ADDR=IOCCATLK,MODE=UNCOND,        *
               RELATED=(PSCAT,IECVPST(PSTCATR))
         OI    FRRFLGA,FRRCATLK    SET CAT LOCK HELD IND.
         SPACE 1
         USING PSA,0               GET LCCA SAVE AREA IN REG 13
         L     LLSAVE,PSALCCAV
         USING LCCA,LLSAVE
         L     LLSAVE,LCCACPUS
         USING WSAC,LLSAVE
         L     LLSAVE,WSACIOS      ADDRESS OF IOS SAVE AREA
         SPACE 1
         L     PARMRG,FRRSRB       PUT SRB ADDR IN REG 1
         BAL   RTRNRG,PSTSUBR      GO TO CORE MANAGER
         SPACE 1
*  RELEASE CATLOCK
         SPACE 1
PSTCATR  SETLOCK  RELEASE,TYPE=IOSCAT,ADDR=IOCCATLK,                   *
               RELATED=(PSTCAT,IECVPST(PSTCATO))
         SPACE 1
         DROP  TRAREG
         DROP  LLSAVE
         SPACE 1
         B     PSTRTN              BRANCH TO RETURN
         SPACE 1
*  RE-SCHEDULE RETURN BRANCHES HERE - ASID RETURNED IN IOSASID
         SPACE 1
PSTSATN  EQU   *                   RE-SCHEDULE ATTENTION ROUTINE
         L     SRBASE,IOSSRB       GET SRB ADDRESS
         USING SRBSECT,SRBASE
         LH    WRKRG1,IOSASID      GET NEW ASID
         STH   WRKRG1,SRBPASID     AND MOVE TO SRB
         L     WRKRG2,D16          GET CVT ADDRESS
         USING CVT,WRKRG2                                       VS02561
         L     WRKRG2,CVTASVT      GET ASVT ADDRESS
         USING ASVT,WRKRG2         ADDRESSABILITY TO ASVT
         SLL   WRKRG1,D2           MULTIPLY ASID BY FOUR
         L     WRKRG2,ASVTFRST(WRKRG1) GET ASCB ADDRESS         VS02560
         ST    WRKRG2,SRBASCB      AND MOVE TO SRB
         LR    PARMRG,SRBASE
         SPACE 1
*   SCHEDULE POST STATUS INTO REQUESTED MEMORY
         SCHEDULE  SRB=(1),SCOPE=GLOBAL
         SPACE 1
         B     PSTLLH              GO TO RELEASE LOCK AND RETURN
         SPACE 1
         EJECT
PSTPURG  L     SRBASE,FRRSRB       GET SRB ADDRESS
         USING SRBSECT,SRBASE
         MVC   SRBID(D4),IOSERP    CHAIN ERP W.A. FROM SRB
         B     PSTNATN             USE ATTENTION ROUTINE PROCESSING
*                                  TO GET LOCK AND FREE SRB/IOSB AND W.
         DROP  SRBASE                                          @ZA06650
         TITLE '   IECVPST - SVC F ENTRANCE  '
*****************************************************************
*                                                               *
*    THIS IS THE SVC F ENTRANCE USED BY THE TRANSIENT ERPS TO   *
*    RETRY AND TO POST THE CALLER WHEN THE ERROR IS PERMANENT   *
*    OR FIXED. IF IOSERR IS ON UPON ENTRANCE,  A RETRY IS IN-   *
*    DICATED. IF IOSERR IS OFF AND IOSEX IS ON,  THE ERROR IS   *
*    CONSIDERED PERMANENT.  THE IOSB IS POSTED AND THE  USERS   *
*    ABNORMAL END APPENDAGE IS ENTERED.  IF IOSERR IS OFF AND   *
*    IOSEX IS OFF, THE ERROR HAS BEEN CORRECTED.  THE IOSB IS   *
*    POSTED WITH A 7F AND THE USER'S CHANNEL END APPENDAGE IS   *
*    ENTERED.  SVC F IS A TYPE I SVC AND IS ENTERED WITH  THE   *
*    LOCAL LOCK.                                                *
*    THIS SVC IS INTENDED ONLY FOR SUPERVISOR STATE CALLERS.   @ZA05814
*    ANY NON-SUPERVISOR STATE CALLER IS ABENDED.               @ZA05814
*                                                               *
*****************************************************************
         SPACE 1
IGC015   BALR  BASERG,D0           ADDRESSABILITY TO SVC F
         USING *,BASERG
PST015   S     BASERG,IGC015A      ADJUST BASE TO
         USING PSTBASE,BASERG      POST STATUS ADDRESSABILTY
         LR    IOSBRG,PARMRG       PUT IOSB ADDRESS IF REG 2
         SPACE 1
         LA    TRAREG,PSTFRRTN     GET FRR ROUTINE ADDRESS
         SETFRR A,FRRAD=(TRAREG),PARMAD=(FRRSAV),                      *
               WRKREGS=(WRKRG1,WRKRG2)
         USING FRRAREA,FRRSAV      ADDRESSABILITY TO FRR AREA
         ST    LLSAVE,FRRREGD      SAVE REG D FOR BR ENTRY
         L     SRBREG,IOSSRB       GET SRB ADDR FROM IOSB
         ST    SRBREG,FRRSRB       SAVE SRB ADDRESS
         ST    RTRNRG,FRRDISP      SAVE DISP RETURN ADDR
         OI    FRRFLGA,FRRSVCNT    INDICATE SVC ENTRANCE
         L     WRKRG1,PSATOLD      GET TCB ADDRESS             @ZA09252
         LTR   WRKRG1,WRKRG1       ARE WE RUNNING UNDER TCB ?  @ZA09252
         BZ    PSTUSROK            NO, BRANCH AROUND TESTAUTH  @ZA09252
*   TEST IF USER IS SUPERVISOR STATE-IF NOT ABEND HIM WITH 20F @ZA05814
         TESTAUTH  STATE=YES,RBLEVEL=1,BRANCH=YES CHECK STATE  @ZA05814
         LTR   LINKRG,LINKRG       IS CALLER OK                @ZA05814
         BZ    PSTUSROK            YES - GO RESTORE REGS       @ZA05814
         SETFRR D,WRKREGS=(WRKRG1,WRKRG2)  DELETE FRR          @ZA06077
         LA    PARMRG,NVALUSER     NO - ABEND HIM WITH 20F     @ZA05814
         ABEND (PARMRG),DUMP,,SYSTEM                           @ZA05814
         SPACE 2
PSTUSROK EQU   *
         L     IOSBRG,FRRSRB      GET SRB ADDRESS              @ZA05814
         LR    SRBREG,IOSBRG      RESTORE SRB POINTER          @ZA05814
         L     IOSBRG,SRBPARM-SRB(IOSBRG) RESTORE IOSB PTR     @ZA05814
         L     RTRNRG,FRRDISP      RESTORE DISP RETURN REG     @ZA05814
         SPACE 1
* PUT LOCAL LOCK SAVE AREA IN REGISTER 13
         SPACE 1
         USING PSA,0
         L     WRKRG1,PSAAOLD
         USING ASCB,WRKRG1
         L     WRKRG1,ASCBASXB
         USING ASXB,WRKRG1
         LA    LLSAVE,ASXBFLSA
         SPACE
         L     WRKRG2,ASXBSIRB     GET SIRB PTR FROM ASXB      @ZA05822
         TM    RBSTAB2-RBBASIC(WRKRG2),RBFACTV   IS SIRB ACTIVE@ZA05822
         BZ    PSTCKERR
         XC    RBIQEA+1-RBBASIC(D3,WRKRG2),RBIQEA+1-RBBASIC(WRKRG2)
*                                  ZERO SRB PTR IN SIRB        @ZA05822
PSTCKERR EQU   *                                               @ZA05822
         SPACE
         DROP  WRKRG1
         SPACE 1
         TM    IOSFLA,IOSERR       IF ERROR ROUTINE IN CONTROL,
         BO    PSTGDPT             GO CHECK FOR GDP AND APR
         SPACE 1
* PERMANENT OR FIXED ERROR HANDLED HERE
         SPACE 1
PSTERC   EQU   *                                               @YM03811
         TM    IOSFLA,IOSIOSB      IOS GENERATED IOSB?
         BO    PSTEWA              YES SEE IF EWA APPENDED     @YM06139
PSTEXT   TM    IOSFLA,IOSEX        IS ERROR PERMANENT?
         BO    PSTCODP             YES, GO TEST FOR IOSCOD SET
         MVI   IOSCOD,IOSNRMC      FIXED ERROR, SET CODE TO 7F
         B     PSTCHE              AND GO TO CHE APPENDAGE
PSTEWA   L     PARMRG,IOSERP       LOAD EWA                    @YM06139
         LTR   PARMRG,PARMRG       IS THERE APPENDED EWA       @YM06139
         BZ    PSTCRM              NO                          @YM06139
         LA    RTRNRG,PSTCRM       YES LOAD RETURN ADDRESS     @YM06139
         B     PSTSUBR             GO TO SUBR TO INTERFACE WITH@YM06139
*                                  STORAGE MANAGER             @YM06139
         SPACE 1
PSTCODP  CLI   IOSCOD,IOSNRMC      IF CODE IS NOT 7F, GO CHECK
         BNE   PSTCODI             FURTHER, OTHERWISE,
         MVI   IOSCOD,IOSERRC      SET TO 41 AND GO TO ABE
         B     PSTEABN             APPENDAGE
PSTCODI  CLI   IOSCOD,IOSFINTC     IF CODE IS NOT 7E, GO CHECK
         BNE   PSTCODF             FURTHER, OTHERWISE
         MVI   IOSCOD,IOSINTC      SET TO 44 AND GO TO ABE
         B     PSTEABN             APPENDAGE
PSTCODF  CLI   IOSCOD,IOSMIHC      IF CODE IS NOT 74, LEAVE AS
         BNE   PSTCODE             IS AND GO TO RETURN TO CALLER
         MVI   IOSCOD,IOSMIHCA     OTHERWISE SET CODE TO X'51'
         B     PSTEABN             GO TO ABE APPENDAGE
PSTCODE  CLI   IOSCOD,IOSABNC      IF CODE IS 45 ON ENTRANCE,
         BE    PSTAPR              BYPASS APPENDAGE PROCESSING
         SPACE 1
* INTERFACE WITH ABNORMAL END APPENDAGE
         SPACE 1
PSTEABN  LR    PARMRG,IOSBRG       PUT IOSB ADDRESS IN REG 1
         L     LINKRG,IOSABN       GET ENTRY TO ABN EXIT
         BALR  RTRNRG,LINKRG       GO TO ABNORMAL END APPENDAGE
         SPACE 2
****************************************************************
*                                                              *
*        ABNORMAL END APPENDAGE RETURN VECTOR TABLE            *
*                                                              *
         B     PSTAPR              NORMAL RETURN               *
         B     PSTFRTN             DO NOTHING RETURN           *
         B     PSTERTN             RETRY RETURN                *
         B     PSTTRTN             DDR PROCESSING REQUIRED     *
*                                  * RETURN +16 VECTOR IS HERE @Y30IPLB
         TM    FRRFLGA,FRRSVCNT    IF ENTRY BY SVC 15 THEN     @YM30155
         BNO   PSTRLS              RELEASE LOCAL LOCK AND EXIT @YM30155
         B     PSTRTN              IF NOT,BYPASS TERMINATION   @YM30155
*                                  CALL, RELEASE THE FRR, AND
*                                  THEN RETURN TO DISPATCHER.  @Y30IPLC
         SPACE
*                                                              *
****************************************************************
         SPACE 1
* RETRY REQUEST HANDLED HERE
         SPACE 1
PSTGDPT  L     WRKRG2,IOSERP       GET ERP W.A. ADDR.
         USING EWA,WRKRG2
         LTR   WRKRG2,WRKRG2       DOES EWA PTR = ZERO ?      @ZA19254
         BZ    PSTPATHT            YES, DONT SET EWA FLAGS    @ZA19254
         NI    EWAFLG1,MASKFF-EWABDSNS-EWASLIS
*                                  RESET BAD SENSE INDICATOR
*                                  AND SILI SENSE INDICATOR
         MVI   EWASNSCT,MASK00     ZERO SENSE LOOP COUNT
         DROP  WRKRG2
PSTPATHT TM    IOSPATH,IOSGDP      GUARANTEED DEVICE PATH     @ZA19254
         BO    PSTSIO              YES, GO SIO FOR RETRY
         BAL   RTRNRG,PSTRAPR      INTERFACE WITH APR
         B     PSTSIO              GO SIO FOR RETRY
         SPACE 1
         TITLE '   IECVPST - GET LOCK SUBROUTINE  '
****************************************************************
*     THIS SUBROUTINE GETS THE LOCAL  LOCK AND  PUTS THE       *
*     ADDRESS OF THE LOCAL LOCK SAVE AREA IN REGISTER 13.      *
****************************************************************
         SPACE 1
PSTLOCK  EQU   *                   GET LOCAL LOCK
         USING IORB,LLSAVE                                     @Z40WPLL
         TM    IOSOPT,IOSPSLL          LOCK NOT WANTED          @YM6764
         BNO   PSTLOCK1                YES IT'S WANTED          @YM6764
         TM    IOSPKEY,IOSIDR          PAGING IO                @YM6764
         BZ    PSTLOCK2                NO-GET LOCAL LOCK        @YM6764
         L     LLSAVE,IOSUSE           POINT AT IOMB            @YM6764
         L     LLSAVE,IORSAVE          POINT AT USER'S SAVE    @Z40WPLL
         BR    RTRNRG                  RETURN TO CALLER         @YM6764
PSTLOCK1 EQU   *
         LR    LINKRG,RTRNRG       SAVE CALLERS RETURN
         SETLOCK  OBTAIN,TYPE=LOCAL,MODE=UNCOND,                       *
               RELATED=(PSLCL,IECVPST(PSTRLS,PSTRLS1))
         SPACE 1
         OI    FRRFLGA,FRRLLHLD    SET LOCAL LOCK HELD INDICATOR
         USING PSA,0               PUT ADDRESS OF LOCAL LOCK
         L     WRKRG1,PSAAOLD      SAVE AREA IN REGISTER 13
         USING ASCB,WRKRG1
         L     WRKRG1,ASCBASXB
         USING ASXB,WRKRG1
         LA    LLSAVE,ASXBFLSA
         DROP  WRKRG1
         LR    RTRNRG,LINKRG       RESTORE CALLERS RETURN
         BR    RTRNRG              RETURN TO CALLER OF SUBR
         SPACE 2
*   THE FOLLOWING TESTS DO NOT ALLOW THE LOCAL LOCK TO BE OBTAINED    *
*       IF THE IOSB IS FOR I/O TO THE PAGE PACK (BUT NOT THE PAGE     *
*       DATA SET--THEN IOSIDR WOULD BE ON).  THE REAL PURPOSE OF THE  *
*       CALL IS TO OBTAIN A SAVE AREA.                                *
PSTLOCK2 L     WRKRG1,IOSUCB       POINT TO UCB                @ZA10446
         TM    UCBTBYT3-UCBOB(WRKRG1),UCB3DACC                 @ZA10446
*                                  IS IT A DA UCB?             @ZA10446
         BZ    PSTLOCK1            NO--GET LOCK                @ZA00904
         TM    UCBSTAB-UCBOB(WRKRG1),UCBPGFL                   @ZA10446
*                                  IS IT A PAGE PACK?          @ZA10446
         BZ    PSTLOCK1            NO--GET LOCK                @ZA00904
         TM    FRRFLGA,FRRSAF      SAVE AREA ALREADY EXIST     @ZA00904
         BO    PSTLK10             YES--USE IT                 @ZA00904
         LR    WRKRG1,RTRNRG       SAVE RETURN ADDR            @ZA00904
*    GET CAT LOCK TO PROVIDE SAVE AREA FOR STORAGE MANAGER     @ZA00904
         L     SAVERG,D16          SET UP ADDRESSABILITY TO    @ZA10446
         USING CVT,SAVERG          THE IOCOM VIA THE CVT       @ZA10446
         L     SAVERG,CVTIXAVL                                 @ZA10446
         USING IOCOM,SAVERG                                    @ZA10446
         SPACE 1
PSTCATO2 SETLOCK  OBTAIN,TYPE=IOSCAT,ADDR=IOCCATLK,MODE=UNCOND,        *
               RELATED=(PSCAT,IECVPST(PSTCATR2))               @ZA00904
         OI    FRRFLGA,FRRCATLK    SET CAT LOCK HELD IND.      @ZA00904
         SPACE 1
         USING PSA,0               GET LCCA SAVE AREA IN REG 13@ZA00904
         L     LLSAVE,PSALCCAV                                 @ZA00904
         USING LCCA,LLSAVE                                     @ZA00904
         L     LLSAVE,LCCACPUS                                 @ZA00904
         USING WSAC,LLSAVE                                     @ZA00904
         L     LLSAVE,WSACIOS      ADDRESS OF IOS SAVE AREA    @ZA00904
         LH    CORERG,IOSASID      CREATE INPUT PARM           @ZA00904
         SLL   CORERG,D8             FOR STORAGE MANAGER       @ZA00904
         O     CORERG,SMPARM         -- ONE 160 BYTE BLOCK     @ZA00904
         L     LINKRG,COREMGR      GET STORAGE MANAGER ADDRESS @ZA00904
         BALR  RTRNRG,LINKRG       GET STORAGE (A SAVE AREA)   @ZA00904
         ST    CORERG,FRRSA        SAVE STORAGE ADDRESS        @ZA00904
         L     SAVERG,D16          SET UP ADDRESSABILITY TO    @ZA10446
         USING CVT,SAVERG          THE IOCOM VIA THE CVT       @ZA10446
         L     SAVERG,CVTIXAVL                                 @ZA10446
         USING IOCOM,SAVERG                                    @ZA10446
PSTCATR2 SETLOCK RELEASE,TYPE=IOSCAT,ADDR=IOCCATLK,                    *
               RELATED=(PSTCAT,IECVPST(PSTCATO2))              @ZA00904
         DROP  SAVERG                                          @ZA10446
         DROP  LLSAVE                                          @ZA00904
         NI    FRRFLGA,MASKFF-FRRCATLK  RESET LOCK FLAG        @ZA00904
         OI    FRRFLGA,FRRSAF      INDICATE SAVE AREA OBTAINED @ZA00904
         LR    RTRNRG,WRKRG1       RESTORE RETURN ADDR         @ZA00904
PSTLK10  L     LLSAVE,FRRSA        SET SAVE AREA POINTER       @ZA00904
         BR    RTRNRG              RETURN                      @ZA00904
         TITLE '   IECVPST - SUBR TO FREE RESOURCES  '
****************************************************************
*   THIS SUBROUTINE INTERFACES WITH STORAGE MANAGER TO FREE    *
*   THE ERP WORKAREA (IOSERP) AND THEN THE SRB/IOSB.           *
****************************************************************
         SPACE 1
PSTSUB   L     WRKRG1,IOSERP       GET ADDRESS OF ERP WORKAREA
         LTR   PARMRG,WRKRG1       WORKAREA EXIST?
         BCR   D8,RTRNRG           NO, RETURN TO CALLER
         SR    WRKRG1,WRKRG1       CLEAR WORK REGISTER
         ST    WRKRG1,IOSERP       ZERO ERP WA ADDRESS
         SPACE 1
* THIS CHECK FOR THE LOCAL LOCK IS INSERTED HERE TO COVER THE
* CASE IN WHICH POST STATUS IS ENTERED WITH AN ERP WORK AREA
* APPENDED TO THE IOSB AND THE LOCAL LOCK NOT REQUESTED BUT
* THE ABNORMAL END APPENDAGE DECIDES THAT ERP PROCESSING OF
* THE ERROR IS NOT REQUIRED.
         SPACE 1
         TM    FRRFLGA,FRRLOCAL    LOCAL LOCK HELD?
         BM    PSTSUBR             YES, SKIP GETTING IT
         LR    TRAREG,RTRNRG       SAVE CALLERS RETURN REG.
         BAL   RTRNRG,PSTLOCK      GO TO GET LOCAL LOCK
         LR    RTRNRG,TRAREG       RESTORE CALLERS RETURN REG
         SPACE 1
* INTERFACE WITH CORE MANAGEMENT TO FREE WORK AREA OR SRB/IOSB
* REGISTER 14 IS LEFT AS IS SO CORE  MANAGEMENT WILL RETURN TO
* CALLER OF THIS SUBROUTINE.
         SPACE 1
PSTSUBR  L     LINKRG,COREMGR      GET ADDR OF CORE MGMNT RTN
         L     CORERG,CORCNST2     INDICATE BLOCK SIZE
         B     D4(LINKRG)          GO TO CORE MANAGEMENT
         TITLE '   IECVPST - INTERFACE FOR TRANSLATION  '
PSTTRA   L     PARMRG,IOSCC        GET CCW ADDRESS FROM CSW
         LA    PARMRG,D0(PARMRG)   CLEAR HIGH ORDER BYTE
         LTR   PARMRG,PARMRG       ADDRESS ZERO ?
         BCR   D8,TRAREG           YES, RETURN
         BCTR  PARMRG,D0           DECREMENT BY ONE TO INSURE
*                                  PAGE BOUNDARY
         L     LINKRG,D16          GET ADDRESS OF CVT
         USING CVT,LINKRG
         L     LINKRG,CVTPTRV      GET ADDRESS OF CONVERT RTN
         DROP  LINKRG
         BALR  RTRNRG,LINKRG       GO TO CONVERT ROUTINE
         SPACE 1
         LTR   LINKRG,LINKRG       ZERO RETURN CODE?
         BNZR  TRAREG              TRANSLATION UNSUCCESSFUL
*                                  LEAVE ADDRESS AS IS
         SPACE 1
         LA    PARMRG,D1(PARMRG)   INCR. VIRTUAL ADDR BY ONE
         IC    WRKRG1,IOSCC        SAVE CC ACROSS STORE
         ST    PARMRG,IOSCC        PUT CCW ADDRESS BACK IN CSW
         STC   WRKRG1,IOSCC        PUT COND. CODE BACK
         BR    TRAREG              RETURN TO CALLER
         TITLE '   IECVPST - APR ROUTINE   '
****************************************************************
*                                                              *
*  ALTERNATE PATH RETRY IS INITIATED ON ERP RETRY OF CHANNEL   *
*  ERRORS FOR ALL MULTI-PATH  DEVICES.  SHARED AND  RESERVED   *
*  CHECKS ARE MADE BY IOS AND THEREFORE, NOT DONE HERE. THIS   *
*  SUBROUTINE DETERMINES THE FAILING PATH AND SETS THE  COR-   *
*  RESPONDING INDICATOR IN THE  IOSAPMSK FIELD OF THE  IOSB.   *
*                                                              *
****************************************************************
         SPACE 2
PSTRAPR  EQU   *                   APR
         TM    IOSTSB,MASK0F       ANY CHANNEL ERRORS?
         BCR   D8,RTRNRG           NO,APR NOT NEEDED - RETURN
         SPACE 1
         TM    IOSOPT,IOSAPR       APR ALREADY ACTIVE FOR THIS
         BO    PSTAPRA             REQUEST? YES NO INIT NEEDED
         OI    IOSOPT,IOSAPR       SET APR IN PROGRESS
         XC    IOSAPMSK(D2),IOSAPMSK  INIT APR MASK TO ZERO
         SPACE 1
*  ADDRESS ALTERNATE PATH TABLE TO DETERMINE PRIMARY OR SEC-   *
*  ONDARY PATH VIA CVT, IOCOM, AND LCHTABLE USING UCBLCI.      *
         SPACE 1
PSTAPRA  L     WRKRG1,D16          POINT AT CVT
         USING CVT,WRKRG1
         L     WRKRG1,CVTIXAVL
         USING IOCOM,WRKRG1
         L     WRKRG1,IOCLCHTB
         USING LCH,WRKRG1          LCH ADDRESSABILITY
         L     UCBREG,IOSUCB       GET UCB ADDRESS              YM2588P
         SR    TRAREG,TRAREG
         IC    TRAREG,UCBLCI       GET LCH TABLE INDEX BYTE     YM2588P
         SLL   TRAREG,LCHELP2      MULTIPLY BY ENTRY LENGTH
         L     WRKRG1,LCHTCH(TRAREG) POINT TO ALT PATH TAB ENTRY
         SR    WRKRG2,WRKRG2       CLEAR WORK REG
         L     TRAREG,IOSERP       SET ADDRESSABILITY TO EWA
         USING EWA,TRAREG
PSTAPRB  CLI   D0(WRKRG1),MASKFF   AT END OF TABLE?
         BNE   PSTAPRC             NO, CONTINUE SEARCHING FOR
*                                  CORRECT ENTRY IN LCH TABLE
         NI    IOSOPT,MASKFF-IOSAPR RESET APR ACTIVE IND.
         BR    RTRNRG              RETURN
         SPACE 1
PSTAPRC  EQU   *                   SET FAILING PATH IN IOSB
         CLC   D0(D1,WRKRG1),EWACHA IS THIS THE FAILING PATH?
         BE    PSTAPRD             YES, CONTINUE
         LA    WRKRG1,D2(WRKRG1)   NO,INCR TO NEXT TABLE ENTRY
         B     PSTAPRB             AND CONTINUE SEARCH
PSTAPRD  IC    WRKRG2,D1(WRKRG1)   GET PATH INDICATOR
         SLL   WRKRG2,D28          SHIFT OUT EXTRA BITS
         SR    WRKRG1,WRKRG1
         IC    WRKRG1,EWACPU       GET FAILING CPU
         SLL   WRKRG1,D1           MULTIPLY BY 2
         SRL   WRKRG2,D0(WRKRG1)   SET TO CORRECT POSITION
         O     WRKRG2,IOSAPMSK     TURN FAILING PATH ON IN MASK
         SRL   WRKRG2,D16          MOVE TO LOW ORDER TWO BYTES
         STH   WRKRG2,IOSAPMSK     AND PUT IN IOSB - IOSAPMSK
         BR    RTRNRG              RETURN
         TITLE '   IECVPST - FRR RETRY ROUTINE'
****************************************************************
*                                                              *
*  THE POST STATUS FRR RETRY ROUTINE RECEIVES CONTROL AFTER    *
*  THE FRR ROUTINE HAS RETURNED TO RTM WITH RETRY SPECIFIED.   *
*                                                              *
****************************************************************
         SPACE 1
PSTFRRTY EQU   *                   FRR RETRY
         TM    FRRFLGB,FRRIIOSB    WAS IOSB IOS'S?
         BO    PSTLLH              YES - NO SCHEDULE
         SPACE 1
         B     PSTSCHC             NO - GO SCHEDULE CALLER
         TITLE '   IECVPST - FRR ROUTINE   '
****************************************************************
*                                                              *
*  THE POST STATUS FRR RECEIVES CONTROL DURING EXECUTION  OF   *
*  POST STATUS OR SVC F OR IS PERCOLATED TO FROM A LOWER FRR   *
*  IF A MACHINE CHECK OR UNEXPECTED PROGRAM CHECK OCCURS. IN   *
*  ALL CASES EXCEPT IF PERCOLATED TO THE FRR REQUESTS RECOR-   *
*  DING WITH THE IOSB IN THE  SDWA  VARIABLE  DATA AREA  AND   *
*  ALSO USES SDUMP TO PRINT THE IOSB.  IF THE IOSB IS AN IOS   *
*  IOSB, IT IS FREED. IN ALL OTHER CASES, THE IOSB IS POSTED   *
*  WITH A X'45' IN IOSCOD.                                     *
*                                                              *
****************************************************************
         SPACE 1
PSTFRRTN EQU   *                   FRR ROUTINE
         BALR  BASERG,D0           ESTABLISH ADDRESSABILITY
         USING *,BASERG
PSTFRR   S     BASERG,FRRCNST      SET UP FOR POST STATUS
         SPACE 1
         USING PSTBASE,BASERG      ADDRESSABILITY
         USING SDWA,PARMRG         ESTABLISH ADDRESSABILITY TO
         L     FRRSAV,SDWAPARM     SDWA AND FRR
         USING FRRAREA,FRRSAV
         ST    RTRNRG,FRRTMRET     SAVE RETURN REG TO SRM
         USING UCB,UCBREG          ADDRESSABILITY TO UCB
         LR    LLSAVE,REG0         USE 200-BYTE WORKAREA AS    @ZA06077
*                                     A SAVEAREA               @ZA06077
         L     IOSBRG,FRRSRB       GET SRB IN ORDER TO         @ZA00904
         L     IOSBRG,SRBPARM-SRB(IOSBRG)  GET IOSB ADDRESS
         TM    FRRFLGB,FRRSECNT    IF SECOND ENTRY FLAG ON     @ZA10508
         BO    PSTFRRDP            GO TO REQUEST ABEND         @ZA10508
         TM    FRRFLGB,FRRECRSN    IF RECURSION FLAG ON,
         BO    PSTFRRDP            GO TO REQUEST ABEND
         OI    FRRFLGB,FRRECRSN    ELSE, SET RECURSION FLAG
         LA    TRAREG,PSTFRRTN     GET THIS FRR ADDRESS
         SPACE 1
         SETFRR  A,FRRAD=(TRAREG),PARMAD=(WRKRG1),                     *
               WRKREGS=(WRKRG1,WRKRG2)
         SPACE 1
*  COPY ALL INFORMATION FROM PRIMARY TO SECONDARY FRR
         SPACE 1
         MVC   FRRFLGA-FRRAREA(D24,WRKRG1),FRRFLGA    @ZA10508,@ZA06077
         NI    FRRFLGB-FRRAREA(WRKRG1),MASKFF-FRRECRSN         @ZA06077
*               TURN OFF RECURSION FLAG IN SECONDARY FRR       @ZA06077
         SPACE 1
*   STORE PRIMARY FRR ADDR IN SECONDARY FRR AND SET SECOND     @ZA06077
*           ENTRY FLAG; STORE SECONDARY ADDR IN PRIMARY FRR    @ZA06077
         ST    FRRSAV,FRRREGD-FRRAREA(WRKRG1)
         OI    FRRFLGB-FRRAREA(WRKRG1),FRRSECNT
         ST    WRKRG1,FRRREGD                                  @ZA06077
         SPACE 1
PSTFRRSK TM    IOSFLA,IOSIOSB      IF THIS IS AN IOS BUILT IOSB,
         BO    PSTFRRIO            BRANCH FOR SPECIAL HANDLING
         MVI   IOSCOD,IOSABNC      ELSE, SET COMP CODE TO X'45'
         SPACE 1
PSTFRRSB LR    WRKRG2,PARMRG       SAVE SDWA ADDRESS
         L     WRKRG1,IOSERP       GET ADDRESS OF ERP WORKAREA @ZA06077
         LTR   PARMRG,WRKRG1       WORKAREA EXIST ?            @ZA06077
         BZ    PSTFRRSH            NO,BRNCH AROUND CALLING SMGR@ZA06077
         L     LINKRG,COREMGR      GET ADDR OF CORE MGMT RTN   @ZA06077
         L     CORERG,CORCNST2     INDICATE BLOCK LENGTH       @ZA06077
         BAL   RTRNRG,D4(LINKRG)   GO TO STORAGE MANAGER       @ZA06077
PSTFRRSH LR    PARMRG,WRKRG2       RESTORE SDWA ADDRESS        @ZA06077
         SPACE 1
PSTFRRSD TM    SDWAERRC,SDWAPERC   IF FRR HAS BEEN PERCOLATED TO,
         BO    PSTFRRVB            BR ARND DMP & SETRP@YM04948,@ZA06077
         SPACE 1
         L     WRKRG1,D16          GET ADDRESS OF WORK BUFFER
         USING CVT,WRKRG1
         L     WRKRG2,CVTSDBF
         LA    WRKRG2,D0(WRKRG2)   CLEAR HIGH ORDER BYTE OF ADDR
         L     TRAREG,FRRBUFCT     GET SWAPPING CONSTANT
         OR    TRAREG,WRKRG2       SET HIGH ORDER BIT
         SPACE 1
*        IF HIGH-ORDER BIT IS NOT ON, SET IT, OTHERWISE
*        DUMP CAN NOT BE TAKEN.
         SPACE 1
         CS    WRKRG2,TRAREG,CVTSDBF
         BNE   PSTFRRVB            BR TO CONTINUE PROCESSING
         DROP  WRKRG1
         SPACE 1
         USING BUFFER,WRKRG2       ADDRESSABILITY TO 4K BUFFER
PSTFRRSC LA    WRKRG1,BUFDATA      ADDRESS DATA PORTION
         ST    WRKRG1,BUFDATAD     AND STORE IN DATA ADDRESS
         LA    WRKRG1,IOSEND-IOSB  LENGTH OF DATA
         STH   WRKRG1,BUFDATLG     SET LENGTH IN BUFFER HEADER
         MVC   BUFDATA,IOSB        MOVE IOSB TO BUFFER DATA AREA
         LA    WRKRG1,BUFDATA2     ADDRESS SECOND DATA AREA    @ZA06077
         ST    WRKRG1,BUFAD2       STORE IN SECOND DATA ADDR   @ZA06077
         LA    WRKRG1,D4           LENGTH OF DATA              @ZA06077
         STH   WRKRG1,BUFLEN2      SET LENGTH IN HEADER        @ZA06077
         MVC   BUFDATA2(D4),PSAHLHI MOVE DATA INTO BUFFER      @ZA06077
         XC    BUFCNST,BUFCNST     ZERO NEXT SIX BYTES
         LH    WRKRG1,IOSASID      USER MEMORY                 @ZA30555
         SPACE 1
*        ISSUE SDUMP TO PRINT IOSB ETAL.
         SPACE 1
         LR    TRAREG,PARMRG       SAVE SDWA ACROSS SDUMP
*                                                              @YM06751
         SDUMP HDR='IOS - POST STATUS ERROR',BUFFER=YES,ASID=(WRKRG1), *
               BRANCH=YES,SDATA=(PSA,NUC,SQA,TRT),QUIESCE=NO   @ZA06077
         SPACE 1
         LR    PARMRG,TRAREG       RESTORE SDWA ADDRESS
         SPACE 1
*        MOVE IOSB TO VARIABLE RECORDING AREA OF SDWA AND LENGTH
         SPACE 1
PSTFRRVB MVI   SDWAURAL,IOSEND-IOSB SET LENGTH
         MVC   SDWAVRA(IOSEND-IOSB),IOSB
         OI    SDWADPVA,SDWAHEX    SET PRINT IN HEX IND.
         MVC   SDWAMODN,PSTNAME    PUT MODULE NAME IN SDWA VRA
         MVC   SDWACSCT,PTFNUM     PUT PTF NUMBER IN SDWA VRA
         MVC   SDWAREXN,FRRNAME    PUT FRR ROUTINE NAME IN SDWA VRA
         SPACE 1
         SETRP RETADDR=PSTFRRTY,RETREGS=YES,RECORD=YES,RC=4
         USING SDWA,PARMRG         REESTABLISH ADDRESSABILITY
         SPACE 1
PSTFRRIB TM    FRRFLGB,FRRIIOSB    IF THIS IS NOT AN IOS IOSB,
         BZ    PSTFRRLK            BRANCH AROUNG IOSB CHECKS
         CLI   IOSPROC,IOSADAVV    IF THIS IS NOT DAVV, BRANCH
         BNE   PSTFRRSM            AROUND FURTHER CHECKS       @ZA06077
PSTFRRUB L     UCBREG,IOSUCB       GET UCB ADDRESS
         LA    WRKRG1,D512         ADDRESSABILITY TO PREFIX
         SR    UCBREG,WRKRG1
         TM    UCBFL4,UCBDAVV      IS DAVV IN CONTROL?
         BZ    PSTFRRSM            NO,SKIP RESETTING FLAGS     @ZA06077
         SPACE 1
****************************************************************
*                                                              *
*  THIS CODE GETS THE UCB LOCK SO THE FOLLOWING FLAGS IN THE   *
*  UCB CAN BE RESET: UCBDAVV; UCBWDAV; UCBQISCE; AND UCBUDE.   *
*  THE CHANNEL MASK OF THE LAST PATH STARTED IS SWAPPED WITH   *
*  THE IRT MASK TO INDICATE THE PATH IS NOW AVAILABLE.         *
*                                                              *
****************************************************************
         SPACE 1
         LR    LINKRG,LLSAVE       SAVE REG 13 ACROSS SETLOCK
PSTFRR1  SETLOCK  OBTAIN,TYPE=IOSUCB,ADDR=UCBLOCK,MODE=UNCOND,         *
               RELATED=(FRRUCB,IECVPST(PSTFRR2))
         SPACE 1
         OI    FRRFLGB,FRRUCBLK    SET UCB LOCK INDICATOR      @ZA06077
         L     WRKRG1,FRRREGD      GET SECONDARY FRR ADDR      @ZA06077
         OI    FRRFLGB-FRRAREA(WRKRG1),FRRUCBLK ALSO SET FLAG  @ZA06077
         NI    UCBFLA,MASKFF-UCBQISCE
         NI    UCBFL4,MASKFF-UCBDAVV-UCBWDAV
         NI    UCBFLC,MASKFF-UCBUDE
         SPACE 1
         SR    WRKRG1,WRKRG1
         IC    WRKRG1,UCBCHA       GET LAST STARTED CHAN ADDR
         USING PSA,0
         L     CORERG,PSALCCAV
         USING LCCA,CORERG
         LA    CORERG,LCCAIRT      GET IRT ADDR AND ESTABLISH
         USING IRT,CORERG          ADDRESSABILITY
         LA    WRKRG2,D1           INIT WRKREG WITH 1
         SLL   WRKRG2,D31          SHIFT TO HIGH ORDER BIT
         SRL   WRKRG2,D0(WRKRG1)   SHIFT TO PROPER CHAN LOCATION
         L     WRKRG1,IRTCHMSK     GET IRT CHANNEL MASK
PSTFRRST LR    SRBREG,WRKRG2       MOVE TO REG0 FOR C/S
         OR    SRBREG,WRKRG1       SET THIS CHANNEL INDICATOR
         CS    WRKRG1,SRBREG,IRTCHMSK
         BNE   PSTFRRST            IF MASK IS NOT EQUAL, BRANCH
*                                  TO SWAP AGAIN
PSTFRR2  SETLOCK  RELEASE,TYPE=IOSUCB,ADDR=UCBLOCK,                    *
               RELATED=(FRRUCB,IECVPST(PSTFRR1))
         SPACE 1
         DROP  CORERG
         SPACE 1
         NI    FRRFLGB,MASKFF-FRRUCBLK TURN OFF UCB HOLD IND.  @ZA06077
         L     WRKRG1,FRRREGD     GET SECONDARY FRR ADDR       @ZA06077
         NI    FRRFLGB-FRRAREA(WRKRG1),MASKFF-FRRUCBLK         @ZA06077
*                         ALSO, TURN OFF IN SECONDARY FRR      @ZA06077
         LR    LLSAVE,LINKRG       RESTORE REG 13
         SPACE 1
PSTFRRSM LR    TRAREG,PARMRG       SAVE SDWA ADDRESS
         L     PARMRG,FRRSRB       PUT SRB ADDR IN REG 1
         BAL   RTRNRG,PSTSUBR      GO FREE SRB/IOSB
         LR    PARMRG,TRAREG       RESTORE SDWA ADDRESS
         EJECT
****************************************************************
*                                                              *
*   CLEANUP IS DONE HERE TO RETURN TO RTM. THE LOCKS HELD BY   *
*   POST STATUS  WILL BE INDICATED  IN THE SDWA SO  RTM WILL   *
*   RELEASE THEM.  ONE EXCEPTION TO  THIS IS IF THE  FRR WAS   *
*   ENTERED WITH THE LOCAL LOCK HAVING BEEN  OBTAINED BY THE   *
*   SVC HANDLER (SVC F ).                                      *
*                                                              *
****************************************************************
         SPACE 1
PSTFRRLK EQU   *                   FRR EXIT
         TM    FRRFLGA,FRRCATLK    IF CAT LOCK NOT HELD, DONT
         BZ    PSTFRRLD            SET INDICATOR
         OI    SDWAACF3,SDWAICAT   SET CAT LOCK INDICATOR
         L     WRKRG1,D16          GET ADDRESSABILITY TO CAT LOCK
         USING CVT,WRKRG1
         L     WRKRG1,CVTIXAVL
         USING IOCOM,WRKRG1
         LA    WRKRG1,IOCCATLK
         ST    WRKRG1,SDWAICLW     PUT CAT LOCKWORD IN SDWA
         DROP  WRKRG1
         SPACE 1
*   SET UP REGISTERS FOR RETRY EXIT
         SPACE 1
PSTFRRLD ST    IOSBRG,SDWASR02     PUT IOSB ADDR IN REG 2 AREA
         STM   FRRSAV,BASERG,SDWASR04
         TM    FRRFLGA,FRRSAF      IS THERE A SAVE AREA TO FREE@ZA00904
         BZ    PSTRTN2             NO--EXIT                    @ZA00904
         LR    WRKRG1,PARMRG       SAVE REG                    @ZA00904
         L     PARMRG,FRRSA        GET SAVEAREA TO FREE        @ZA00904
         XC    0(4,PARMRG),0(PARMRG)    CLEAR CHAIN PTR        @ZA00904
         L     CORERG,CORCNST2     INDICATE FREE 160 BYTES     @ZA00904
         L     LINKRG,COREMGR      GET STORAGE MANAGER ADDRESS @ZA00904
         BAL   RTRNRG,D4(LINKRG)   CALL FREE ENTRY             @ZA00904
         NI    FRRFLGA,MASKFF-FRRSAF  RESET SAVE AREA FLAG     @ZA00904
         LR    PARMRG,WRKRG1       RESTORE REG                 @ZA00904
PSTRTN2  EQU   *
         SPACE 1
         SETFRR  D,WRKREGS=(WRKRG1,WRKRG2)
         SPACE 1
         TM    SDWAERRA,SDWARKEY   IF PSW RESTART ERROR,
         BO    PSTFRRLA            GO SETRP TO PERCOLATE
         LH    WRKRG1,SDWAFMID     IF ERROR IS WRONG MEMORY,
         LTR   WRKRG1,WRKRG1       FALL THROUGH TO PERCOLATE
         BZ    PSTFRRLH            OTHERWISE BRANCH            @ZA06077
PSTFRRLA SETRP                     SETRP TO PERCOLATE
         USING SDWA,PARMRG         REESTABLISH SDWA ADDRESSABILITY
         SPACE 1
PSTFRRLB L     RTRNRG,FRRTMRET     RESTORE RETURN TO RTM
         BR    RTRNRG              RETURN TO RTM
         SPACE 2
PSTFRRLH TM    SDWAERRB,SDWACLUP   RETRY ALLOWED ?             @ZA06077
         BO    PSTFRRLA            NO, BRANCH TO PERCOLATE     @ZA06077
         B     PSTFRRLB            YES, BRANCH TO RETRY        @ZA06077
         EJECT
****************************************************************
*                                                              *
*   PCI HANDLING IS DONE HERE. A X'45' COMPLETION CODE IS PUT  *
*   IN THE ENDING STATUS IOSB AND ALL IOS SRB/IOSB'S FREED.    *
*                                                              *
****************************************************************
         SPACE 1
PSTFRRIO EQU   *                   IOS IOSB
         OI    FRRFLGB,FRRIIOSB    SHOW IOS IOSB
         CLI   IOSPROC,IOSAPCI     IF THIS IS NOT FOR PCI, RETURN
         BNE   PSTFRRSB            TO MAINLINE PROCESSING
         L     WRKRG2,IOSPCHN      GET ADDR OF ENDING STATUS IOSB
         MVI   IOSCOD-IOSB(WRKRG2),IOSABNC
*                                  SET COMP CODE=X'45' IN ENDING
*                                  STATUS IOSB
*                                  GO TO FREE PCI SRB/IOSB'S
         B     PSTPCICH            * RETURN WILL BE TO PSTFRRSD
*                                  * SINCE FRR IN CONTROL
         EJECT
****************************************************************
*                                                              *
*   IF RECURSION IS ON, THE SECOND ENTRY FLAG IS CHECKED TO    *
*   DETERMINE WHICH RETRY EXIT TO TAKE.                        *
*                                                              *
****************************************************************
         SPACE 1
PSTFRRDP EQU   *                   RECURSION TEST
PSTFRRDM TM    FRRFLGA,FRRCATLK    IF CAT LOCK NOT HELD,
         BZ    PSTFRRDS            BRANCH AROUND SET
         SPACE 1
         L     WRKRG1,D16          GET ADDRESSABILITY TO IOCOM
         USING CVT,WRKRG1
         L     WRKRG1,CVTIXAVL
         USING IOCOM,WRKRG1
         SETRP FRELOCK=IOSCAT(IOCCATLK)                        @ZA06077
PSTFRRDS TM    FRRFLGA,FRRSYNCH    IF SYNCH LOCK NOT HELD,
         BZ    PSTFRRUA            BRANCH AROUND SET           @ZA06077
         SETRP FRELOCK=IOSYNCH(IOCSYNCH)                       @ZA06077
PSTFRRUA TM    FRRFLGB,FRRUCBLK    UCB LOCK HELD ?             @ZA06077
         BZ    PSTFRRDA            NO, BRANCH AROUND SET       @ZA06077
         LA    WRKRG1,UCBLOCK      GET ADDR OF UCB LOCK        @ZA06077
         ST    WRKRG1,SDWAIULW     AND STORE IN SDWA           @ZA06077
         OI    SDWAACF3,SDWAIUCB   SET UCB LOCK IND. IN SDWA   @ZA06077
         DROP  WRKRG1
         SPACE 1
         TM    FRRFLGB,FRRSECNT    SECOND ENTRY FLAG ON ?      @ZA06077
         BZ    PSTFRRSE            NO, BYPASS ABEND            @ZA06077
         SPACE 1
*   REQUEST RTM TO ABEND CURRENT TASK IF ONE EXISTS
         SPACE 1
PSTFRRDA L     CORERG,FRRSRB       GET SRB ADDRESS
         USING SRBSECT,CORERG
         L     WRKRG1,SRBPTCB      GET TCB ADDRESS FROM SRB
         LTR   WRKRG1,WRKRG1       IF NO TCB ADDR IN SRB, BRANCH
         BZ    PSTFRRSE            AROUND ABEND                @ZA06077
         LH    WRKRG2,IOSASID
         DROP  CORERG
         LR    SAVERG,PARMRG       SAVE SDWA ADDRESSABILITY    @ZA02135
         SPACE 1
*   CALLRTM TO ABEND CURRENT TASK
         SPACE 1
         CALLRTM  TYPE=ABTERM,COMPCOD=X'0F2',ASID=(WRKRG2),TCB=(WRKRG1)
         SPACE 1
         LR    PARMRG,SAVERG       RESTORE SDWA ADDRESSABILITY @ZA02135
         SPACE 1
*          MAKE SURE THAT SYNCH LOCK HELD FLAG WAS NOT TURNED  @ZA06077
*           ON IN PRIMARY FRR                                  @ZA06077
         L     WRKRG1,FRRREGD      GET PRIMARY FRR ADDR        @ZA06077
         TM    FRRFLGA-FRRAREA(WRKRG1),FRRSYNCH                @ZA06077
         BZ    PSTFRRSE            BRANCH IF NOT ON            @ZA06077
         L     WRKRG1,D16          GET ADDRESSABILITY TO IOCOM @ZA06077
         USING CVT,WRKRG1                                      @ZA06077
         L     WRKRG1,CVTIXAVL                                 @ZA06077
         USING IOCOM,WRKRG1                                    @ZA06077
         SETRP FRELOCK=IOSYNCH(IOCSYNCH)                       @ZA06077
PSTFRRSE SETRP                     SETRP TO PERCOLATE
         SPACE 1
PSTFRRSF L     RTRNRG,FRRTMRET     RESTORE RETURN ADDR TO RTM
         BR    RTRNRG              RETURN TO RTM
         TITLE 'IECVPST - RESTARTABLE WAIT SUBROUTINE'
***********************************************************************
*                                                                     *
*   THE PSTWAIT SUBROUTINE CALLS IEESTPRS IN ORDER TO PLACE THE SYSTEM*
*       IN A RESTARTABLE WAIT STATE.  THIS ROUTINE IS CALLED WHEN THE *
*       PAGE PACK IS NOT AVAILABLE AND A WTO WOULD NORMALLY HAVE BEEN *
*       ISSUED--INSTEAD THE WAIT STATE IS USED TO NOTIFY THE OPERATOR.*
*       PSAWTCOD CONTAINS THE EXACT REASON FOR THE WAIT.              *
*       IF THE RESTART RESOURCE WORD CVTRSTWD IS NOT AVAILABLE, THE   *
*       CPU ON WHICH THE CODE IS CURRENTLY RUNNING IS PLACED          *
*       DIRECTLY IN THE WAIT STATE.                                   *
*                                                                     *
***********************************************************************
         SPACE 2
PSTWAIT  BAL   RTRNRG,PSTLOCK      GET A SAVE AREA             @ZA00904
         L     TRAREG,CVTPTR                                   @ZA00904
         USING CVT,TRAREG                                      @ZA00904
         NI    FRRFLGA,MASKFF-FRRWDAV  CLEAR DAVVWAIT FLAG     @ZA00904
         L     UCBREG,IOSUCB           POINT TO UCB            @ZA00904
         USING UCBOB,UCBREG                                    @ZA00904
         TM    UCBFL4,UCBWDAV          TEST UCB DAVV FLAG      @ZA00904
         BNO   PSTWAITR                OFF - SKIP              @ZA00904
         OI    FRRFLGA,FRRWDAV         ON - SET INTERNAL FLAG  @ZA00904
PSTWAITR STNSM PSASYMSK,DISABLE    SYSTEM MUST BE DISABLED BEFORE
*                                   INTERFACING TO STOP/RESTART@ZA00904
*  SET AN FRR TO COVER DISABLEMENT AND THE RESTART RESOURCE
         SETFRR A,FRRAD=PSTWFRRA,PARMAD=(WRKRG1),                      *
               WRKREGS=(WRKRG1,WRKRG2)                         @ZA00904
         USING FRRW,WRKRG1
         ST    BASERG,FRRWBASE     SAVE BASE REG               @ZA00904
         ST    IOSBRG,FRRWIOSB     IOSB REG AND                @ZA00904
         ST    TRAREG,FRRWCVT        CVT PTR FOR FRR           @ZA00904
*  SET SPECIFIC RESTARTABLE WAIT REASON FOR PSAWTCOD           @ZA00904
         USING XPSA,LLSAVE         BLD PSAWTCOD CDE IN SAV AREA@ZA00904
         XC    XPSACD(4),XPSACD    CLEAR FIELD                 @ZA00904
         MVC   XPSADEV,UCBCHAN     MOVE DEVICE ADDR            @ZA00904
         MVI   XPSAWAIT,XPSAVID    ASSUME WRONG VOL ID         @ZA00904
         TM    IOSFLC,IOSRWAIT     TEST REASON FOR WAIT        @ZA00904
         BZ    PSTWAIT2            WRONG VOL ID WAS RIGHT      @ZA00904
         MVI   XPSAWAIT,XPSAERR    ASSUME PERM ERROR           @ZA00904
         BO    PSTWAIT2            RIGHT AGAIN                 @ZA00904
         MVI   XPSAWAIT,XPSAIR     ASSUME INTV REQ             @ZA00904
         TM    IOSFLC,IOSRWIR      TEST FOR INTV REQ           @ZA00904
         BO    PSTWAIT2            RIGHT FINALLY               @ZA00904
         MVI   XPSAWAIT,XPSACC3    ONLY CASE LEFT IS CC3       @ZA00904
*  ACQUIRE RESTART RESOURCE
PSTWAIT2 LH    WRKRG2,PSACPULA     GET LOGICAL CPU ID AND      @ZA00904
         SLL   WRKRG2,D16            SHIFT TO HI BYTE          @ZA00904
         O     WRKRG2,PSTSRSCD     OR IN UNIQUE OWNER CODE     @ZA00904
         SR    PARMRG,PARMRG       COMPARE FOR ZERO CVTRSTWD   @ZA00904
         CS    PARMRG,WRKRG2,CVTRSTWD  SWAP IN CPUID & CODE    @ZA00904
         BNE   PSTRW05             GO STOP THIS CPU ONLY       @ZA00904
         MVI   FRRWSRSW,MASKFF     INDICATE RESOURCE HELD      @ZA00904
         MVC   PSAWTCOD(4),XPSACD  SET RESTART REASON          @ZA00904
         MVC   PSTDATA(4),XPSACD   REASON CODE AND UNIT ADDR   @ZA33089
*                                    TO LOGREC RECORD          @ZA33089
         STCK  PSTTIME             TIME STAMP RECORD           @ZA00904
         RECORD TYPE=LOGREC,RCVRY=SETFRR,HEADER=YES,LENGTH=20,         *
               PARMADR=PSTLOGBF                                @ZA33089
         L     REG0,PSTWAITC       GET WAIT STATE CODE         @ZA00904
         SR    PARMRG,PARMRG       INDICATE NO STATUS AREA     @ZA00904
         L     LINKRG,CVTSTPRS     GET STOP ROUTINE ADDR       @ZA00904
         BALR  RTRNRG,LINKRG       CALL STOP/RESTART           @ZA00904
         LTR   LINKRG,LINKRG       DID IT WORK                 @ZA00904
         BZ    PSTRW10             YES--EXIT                   @ZA00904
PSTRW05  MVC   PSAWTCOD(4),XPSACD  SET RESTART REASON          @ZA00904
         MVC   XRNPSW1(8),FLCRNPSW NO--SAVE RESTART NEW PSW    @ZA00904
         MVC   FLCRNPSW(8),RSPSW2  SET PSW FOR 'PSTRESTR'      @ZA00904
         LPSW  RSPSW3              LOAD WAIT PSW               @ZA00904
PSTRESTR MVC   FLCRNPSW(8),XRNPSW1 OPERATOR HAS HIT RESTART KEY@ZA00904
*                                    RESTORE SAVED PSW         @ZA00904
         CLI   FRRWSRSW,MASKFF     WAS RESTART RESOURCE HELD   @ZA00904
         BNE   PSTRW20             NO--SKIP FREEING IT         @ZA00904
PSTRW10  SR    PARMRG,PARMRG       ZERO REG IN ORDER           @ZA00904
         ST    PARMRG,CVTRSTWD       TO ZERO RESOURCE          @ZA00904
         DROP  LLSAVE
         DROP  TRAREG
PSTRW20  SETFRR D,WRKREGS=(WRKRG1,WRKRG2)  DELETE FRR          @ZA00904
         IC    WRKRG1,PSASYMSK     GET ENTRY SYSTEM MASK       @ZA00904
         EX    WRKRG1,PSTENA         AND RESTORE IT            @ZA00904
         TM    FRRFLGA,FRRWDAV     IS DAVV WAIT FLAG ON        @ZA00904
         BO    PSTRTN              YES--EXIT TO WAIT DEVICE END@ZA00904
         TM    IOSCC,IOSCC3        CONDITION CODE 3?           @ZA30808
         BO    PSTCRM              YES, GO FREE IOSB & EXIT    @ZA30808
         B     PSTGDPT             NO--RETRY I/O               @ZA00904
         SPACE 2
***********************************************************************
*  FRR TO PROTECT RESTART RESOURCE WORD AND DISABLEMENT               *
***********************************************************************
         SPACE
         USING SDWA,PARMRG
PSTWFRR  L     WRKRG1,SDWAPARM     GET PARM LIST               @ZA00904
         USING FRRW,WRKRG1                                     @ZA00904
         L     BASERG,FRRWBASE     RESTORE KEY REGS AND        @ZA00904
         ST    BASERG,SDWAGRSV+4*BASERG  SAVE FOR RETRY        @ZA00904
         L     IOSBRG,FRRWIOSB                                 @ZA00904
         ST    IOSBRG,SDWAGRSV+4*IOSBRG                        @ZA00904
         L     TRAREG,FRRWCVT                                  @ZA00904
         ST    TRAREG,SDWAGRSV+4*TRAREG                        @ZA00904
         CLI   FRRWSRSW,MASKFF     WAS RESTART RESOURCE HELD   @ZA00904
         LA    REG0,PSTRW20        NO--SET RETRY ADDR          @ZA00904
         BNE   PSTWFRR2            EXIT                        @ZA00904
         LA    REG0,PSTRW10        YES--SET RETRY ADDR         @ZA00904
PSTWFRR2 SETRP RETADDR=(REG0),RETREGS=YES,RC=4                 @ZA00904
         BR    RTRNRG              EXIT TO RTM                 @ZA00904
         TITLE '   IECVPST - CONSTANTS     '
         DS    0F
IGC015A  DC    A(PST015-PSTBASE)   ADDRESSABILITY CONSTANT
FRRCNST  DC    A(PSTFRR-PSTBASE)   ADDRESSABILITY CONSTANT FOR FRR
DAERP    DC    V(IECVDERP)         DA ERP ADDRESS
XITEFF   DC    V(IEA0EF00)         EXIT EFFECTOR ADDRESS
COREMGR  DC    V(IECVSMGR)         CORE MANAGER ADDRESS
CDUWPA   DC    X'0000523F'         CHE,DVE,CUE,WLR,PCI,ATTN    @ZA03292
CORCNST  DC    X'10000020'         CORE MGMNT CONSTANT FOR R11
CORCNST2 DC    X'00000020'         CORE MGMNT CONSTANT FOR FREEING
FRRBUFCT DC    X'80000000'         CONSTANT FOR CS ON BUFFER ADDR
C02      DC    X'02'               CONSTANT FOR 02
C04      DC    X'04'               CONSTANT FOR 04
C10      DC    X'10'               CONSTANT FOR 10
FRRNAME  DC    CL8'PSTFRRTN'       FRR NAME FOR FRR PROCESSING @ZA26432
PSTWFRRA DC    A(PSTWFRR)          FRR ROUTINE ADDR            @ZA00904
DAVALT   DC    V(IECVDAV2)         BRANCH ENTRY POINT FOR DAVV @ZA00904
SMPARM   DC    X'10000020'         ONE 160 BYTE BLOCK          @ZA00904
PSTSRSCD DC    X'0000',C'PS'       RESTART CODE                @ZA00904
PSTLOGBF DC    X'84830000'         LOGREC RECORD FOR ENTERING  @ZA09704
         DC    A(PSTLOGDA)          THE SYSTEM STOP/RESTART    @ZA00904
PSTLOGDA DC    F'12'                ROUTINE. RECORD COMPLETE   @ZA00904
PSTWAITC DC    X'0000002F'          EXCEPT FOR TIME STAMP,     @ZA00904
PSTTIME  DC    XL8'00'              REASON CODE, AND UNIT ADDR @ZA33089
PSTDATA  DC    F'0'                 WHICH ARE SERIALIZED BY    @ZA33089
*                                   THE RESTART RESOURCE WORD. @ZA33089
PSTENA   STOSM PSASYMSK,D0          EXECUTED TO RE-ENABLE      @ZA00904
RSPSW2   DC    X'040C0000',A(PSTRESTR) RESTART NEW PSW         @ZA00904
         DS    0D                  PSW MUST BE ON DWORD        @ZA00904
RSPSW3   DC    X'000E00000000002F'  WAIT STATE PSW             @ZA00904
KF0      DC    H'240'               CONSTANT X'FO'             @ZA26432
PSTPATCH DC    C'POST STATUS PATCH AREA'                       @ZA10093
         DC    10F'0'                                          @ZA10093
PSTNAME  DC    CL8'IECVPST'        CSECT NAME FOR FRR PROCESSING
         DC    CL8'&SYSDATE'
PTFNUM   DC    CL8'&SYSPARM '
         END
