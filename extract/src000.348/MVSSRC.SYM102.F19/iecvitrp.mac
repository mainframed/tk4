ITRP     TITLE '    IECVITRP - PROLOGUE'
***********************************************************************
*                                                                     *
*  MODULE NAME = IECVITRP                                             *
*                                                                     *
*  DESCRIPTIVE NAME = ERROR INTERPRETER                               *
*                                                                     *
*  COPYRIGHT = NONE                                                   *
*                                                                     *
*  STATUS = RELEASE 2 LEVEL 0                                         *
*  FUNCTION = COMMON ROUTINE USED BY THE ERROR ROUTINES TO            *
*             ANALYZE THE STATUS AND SENSE INFORMATION.               *
*  NOTES = IT IS THE CALLING ERROR ROUTINE'S RESPONSIBILITY TO SET UP *
*          A LIST FOR THE INTERPRETER ROUTINE. EACH ENTRY IN THIS     *
*          LIST IS 2 BYTES LONG. THE FIRST BYTE IS A CODE REPRESENTING*
*          THE SENSE OR STATUS BIT TO BE TESTED AND THE SECOND BYTE   *
*          CONTAINS A DISPLACEMENT USED TO DEERMINE THE ADDRESS TO    *
*          BRANCH TO IF THE STATUS AND SENSE BIT WAS ON. THE LIST MUST*
*          BE SET UP IN THE ORDER IN WHICH THE SENSE AND STATUS BITS  *
*          ARE TO BE TESTED. THE CODE INDICATING THE END OF THE LIST  *
*          IS X'2F'.AND THE BYTE FOLLOWING THIS CODE WILL BE USED TO  *
*          COMPUTE THE ADDRESS TO RETURN TO THE ERP.                  *
*                                                                     *
*          THE CODE FOR THE 16 BITS IN THE 2 SENSE BYTES HAS BIXED    *
*          VALUES FROM HEXADECIMAL 00 TO 0F STARTING WITH BIT 0 IN    *
*          BYTE 0 TO BIT 7 IN BYTE 1. TH DISTINGUISH BETWEEN THE LIST *
*          OF SENSE DATA AND THE LIST OF CSW DATA, THE CODE FOR THE   *
*          CSW DATA HAS BEEN GIVEN VALUES FROM HES 10 TO 1F.          *
*                                                                     *
*     DEPENDENCIES = SEE ABOVE                                        *
*                                                                     *
*     RESTRICTIONS = NONE                                             *
*                                                                     *
*     REGISTER CONVENTIONS = SEE BELOW                                *
*                                                                     *
*     PATCH LABEL = NONE                                              *
*                                                                     *
*  MODULE TYPE = TABLE                                                *
*                                                                     *
*     PROCESSOR = ASSEMBLER                                           *
*                                                                     *
*     MODULE SIZE = 80 BYTES                                          *
*                                                                     *
*     ATTRIBUTES = REENTRANT                                          *
*                                                                     *
*  ENTRY POINT = IECXITP OR IECVITRP                                  *
*                                                                     *
*     PURPOSE = SEE ABOVE                                             *
*                                                                     *
*     LINKAGE = R1 LOADED WITH IOSB ADDRESS                           *
*               R14 LOADED WITH INTERPRETER TABLE ADDRESS             *
*               R15 LOADED WITH ENTRY POINT ADDRESS                   *
*               R0,11,AND 12 USED AS WORK REGS AND NOT RESTORED       *
*               R15 WILL BE RESTORED FROM R9                          *
*                                                                     *
*  INPUT = IOSB                                                       *
*                                                                     *
*  OUTPUT = NONE                                                      *
*                                                                     *
*  EXIT-NORMAL = RETURN TO CALLING ERP AT ADDRESS COMPUTED FROM TABLE *
*                                                                     *
*  EXIT-ERROR = NONE                                                  *
*                                                                     *
*  EXTERNAL REFERENCES = SEE BELOW                                    *
*                                                                     *
*     ROUTINES = NONE                                                 *
*                                                                     *
*     DATA AREAS = NONE                                               *
*                                                                     *
*     CONTROL BLOCKS = IOSB                                           *
*                                                                     *
*  TABLES = SEE ABOVE DESCRIPTION                                     *
*                                                                     *
*  MACROS = NONE                                                      *
*                                                                     *
*  CHANGE ACTIVITY = NONE YET                                         *
*                                                                     *
***********************************************************************
         TITLE '     IECVITRP - DSECTS'
         IECDIOSB
         TITLE '     IECVITRP - INTERPRET ERROR STATUS'
IECVITRP CSECT
         ENTRY IECXITP
         SPACE 1
***************************************************************
*                                                             *
*               REGISTER DEFINITIONS AND EQUATES              *
*                                                             *
***************************************************************
         SPACE 2
ERREG3   EQU   0                        WORK REG
IOSBR    EQU   1                        IOSB ADDRESS
SAVREG   EQU   9                        MAY CONTAIN CALLER'S BASE
ERREG4   EQU   11                       WORK REG.
ERREG1   EQU   12                       WORK REG
ERRLNK   EQU   14                       LINK REG
BASREG   EQU   15                       BASE REGISTER
         SPACE 1
TSTOPT   EQU   X'10'                    STATUS BIT INDICATING TEST
*                                       TEST CSW AS OPPOSED TO SENSE
ZERO     EQU   0                        VALUE OF 0
ONE      EQU   1                        VALUE OF 1
TWO      EQU   2                        VALUE OF 2
F        EQU   15                       VALUE OF 15
         SPACE 2
*     THIS ROUTINE WILL BE COMMON FOR ALL IOS ERROR ROUTINES
*
*          TO INTERPRET  STATUS AND SENSE CODE BITS
*
         USING IOSB,IOSBR
         USING *,BASREG
IECXITP  BC    15,ERRTSTA               THIS IS FIRST TIME IN
ERRTST   LA    ERRLNK,TWO(ERRLNK)       ADDRESS OF NEXT SENSE BIT
ERRTSTA  SLR   ERREG1,ERREG1            ZERO REGISTER
         IC    ERREG1,ZERO(ERRLNK)      GET SENSE OR STATUS CODE BIT
         TM    ZERO(ERRLNK),TSTOPT      IS CSW TO BE TESTED
         BC    1,ERRSTS                 YES
         LH    ERREG4,IOSSNS            GET SENSE INFO. FROM IOSB
ERRSHT   LNR   ERREG1,ERREG1            COMPLEMENT CODE BIT
         LA    ERREG3,ONE               INITIATE REG TO 1
         SLA   ERREG3,F(ERREG1)         SHIFT TO FIND POSITION OF BIT
         BC    1,ERR810                 END OF TEST
         NR    ERREG3,ERREG4            TEST IF CODE BIT IS ON
         BC    8,ERRTST                 NO, GET NEXT BIT
ERR810   SLR   ERREG1,ERREG1            ZERO REGISTER
         IC    ERREG1,ONE(ERRLNK)       GET DISPLACENT
         LR    BASREG,SAVREG            RESTORE BASE ADDRESS
         BC    15,ZERO(ERREG1,ERRLNK)   BRANCH TO PROPER ROUTINE
         SPACE 1
ERRSTS   LH    ERREG4,IOSTATUS          GET STATUS INFO. FROM IOB
         N     ERREG1,ERRCN2            ZERO STATUS CODE BIT
         BC    15,ERRSHT                GO TEST CSW STATUS
         SPACE 1
         DS    0F
ERRCN2   DC    X'0000000F'              CONSTANT TO ZERO STATUS CODE
PATCH    DC    2F'0'                    PATCH AREA
         END
