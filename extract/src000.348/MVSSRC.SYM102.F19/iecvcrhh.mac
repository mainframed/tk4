    TITLE 'IECVCRHH - CHANNEL RECONFIGURATION HARDWARE HOOK ROUTINES'
***********************************************************************
*
*  MODULE NAME - IECVCRHH
*
*  DESCRIPTIVE NAME - CHANNEL RECONFIGURATION HARDWARE (CRH) HOOKS
*
*  COPYRIGHT - NONE
*
*  STATUS - CHANGE LEVEL 0.0  OCTOBER 8, 1974
*
*  FUNCTION - THIS MODULE PERFORMS THE FUNCTIONS NECESSARY TO CAUSE
*             IOS MAINLINE MODULE IECIOSCN TO USE THE CHANNEL
*             RECONFIGURATION HARDWARE (CRH) ON A 168MP SYSTEM.
*
*  NOTES - DEPENDENCIES
*             THIS MODULE IS EXTREMELY DEPENDENT UPON THE OPERATION
*             OF IOS MAINLINE IECIOSCN. ANY CHANGES MADE TO THE
*             SUBROUTINES HOOKED BY THE CRH FUNCTION OR BRANCHED
*             TO FROM IECVCRH1 MUST BE EVALUATED FOR INCLUSION IN THIS
*             MODULE.
*
*          PATCH AREA
*             THE IOS PATCH AREA WITH LABEL IOSPATCH IS USED.
*
*          RESTRICTIONS
*             SEE DEPENDENCIES
*
*          REGISTER CONVENTIONS
*             THE SAME EQUATES FOR REGISTERS AS IOS MAINLINE
*             SEE PROLOGUE FOR EACH ENTRY POINT
*
* MODULE TYPE - I/O CONTROL
*
*          PROCESSOR - PLS2
*
*          MODULE SIZE - 1050 DECIMAL BYTES
*
*          ATTRIBUTES - KEY 0,SUPERVISOR STATE,NUCLEUS RESIDENT,
*                       REUSEABLE
*
*  ENTRY POINTS -
*             IECVCRCA - CRH COMMUNICATIONS AREA.
*
*             IECVCRH1 - HOOK ROUTINE FOR IOS MAINLINE TCH SUBROUTINE.
*
*             IECVCRH2 - HOOK ROUTINE FOR IOS MAINLINE SIO SUBROUTINE.
*
*             IECVCRH3 - HOOK ROUTINE FOR IOS SENSE SUBROUTINE.
*
*  OPERATION - SEE PROLOGUE FOR EACH ENTRY POINT.
*
*  INPUT REGISTERS - SEE PROLOGUE FOR EACH ENTRY POINT.
*
*  OUTPUT REGISTERS - SEE PROLOGUE FOR EACH ENTRY POINT.
*
* EXTERNAL REFERENCES
*
*     ROUTINES - IECVCRH1 USES SOME OF THE INTERNAL SUBROUTINES
*             IN IOS MAINLINE,SEE PROLOGUE FOR IECVCRH1.
*
*     DATA SETS - NONE
*
*     DATA AREAS - NONE
*
* TABLES - THE CRH COMMUNICATION AREA IECVCRCA
*
* MACROS - NO EXECUTEABLE MACROS
*
* CHANGE ACTIVITY - NONE
*
***********************************************************************
           IECDCAT
           EJECT
           CVT   DSECT=YES,LIST=YES
           EJECT
           EWAMAP ,                                            @ZA16177
           EJECT
           IECDIOCM
           EJECT
           IECDIOQ
           EJECT
           IECDIOSB
           EJECT
           IHALCCA
           EJECT
           IECDIRT
           EJECT
           IECDLCH
           EJECT
           IHAPCCA
           EJECT
           IHAPSA
           EJECT
           IEFUCBOB PREFIX=YES,LIST=YES
         EJECT
IECVCRHH CSECT
         ENTRY IECVCRCA
         ENTRY IECVCRH1
         ENTRY IECVCRH2
         ENTRY IECVCRH3
         IECDCRCA DSECT=NO
         EJECT
         MODID BR=NO
    TITLE 'IECVCRH1 - CRH TEST CHANNEL HOOK ROUTINE'
***********************************************************************
* ENTRY NAME - IECVCRH1
*
* DESCRIPTIVE NAME - CRH TEST CHANNEL HOOK ROUTINE
*
* FUNCTION -  FIND A PATH TO A GIVEN DEVICE THRU A CHANNEL
*          RECONFIGURATION HARDWARE CONNECTION.  THIS PROGRAM
*          PERFORMS THE SAME FUNCTION AS THE TCH SUBROUTINE
*          IN IOS MAINLINE MODULE IECIOSCN.
*
* OPERATION - IF THE IOS MAINLINE SUBROUTINE FOR TEST CHANNEL
*             HAS BEEN UNABLE TO FIND A PATH TO A DEVICE ON
*             THE ALIVE CPU AND ATTEMPTS TO SHOULDER TAP THE
*             DEAD CPU TO FIND A PATH TO THE DEVICE, IECVCRH1
*             RECEIVES CONTROL THROUGH A HOOK IN IOS MAINLINE
*             IECIOSCN.  BECAUSE THIS PROGRAM ACTS AS AN EXTENSION
*             TO IOS MAINLINE, IT FUNCTIONS AS CLOSELY AS POSSIBLE
*             TO THE CORRESPONDING TEST CHANNEL SUBROUTINE
*             IN IECIOSCN.
*
* NOTES - DEPENDENCIES
*             THIS PROGRAM RECEIVES CONTROL THRU A HOOK IN IOS
*             MAINLINE AND PERFORMS THE SAME FUNCTION AS THE
*             TCH SUBROUTINE IN IECIOSCN, THEREFORE IT IS ENTIRELY
*             DEPENDENT ON IOS MAINLINE MODULE IECIOSCN.
*
*         PATCH AREA
*             THE IOS PATCH AREA WITH LABEL IOSPATCH IS USED.
*
* INPUT - REGISTERS
*             IECVCRH1 HAS THE SAME EQUATES FOR REGISTERS AS IOS
*             MAINLINE MODULE IECIOSCN. WHEN IECVCRH1 RECEIVES
*             CONTROL IT EXPECTS THE FOLLOWING REGISTER CONTENTS.
*               WRK1    (REGISTER  1) - ADDRESS OF CRCA
*               IOSBR   (REGISTER  2) - ADDRESS OF IOSB
*               LCCAR   (REGISTER  3) - ADDRESS OF LCCA
*               UCBR    (REGISTER  7) - ADDRESS OF UCB
*               WRKEVEN (REGISTER 14) - RETURN ADDRESS
*               WRKODD  (REGISTER 15) - ADDRESS OF IECVCRH1
*
* OUTPUT - REGISTERS
*            SAME AS ON ENTRY
*
*   EXIT - NORMAL - RETURN TO HOOK IN IOS MAINLINE
*
*   EXIT - ERROR - NONE
*
* EXTERNAL REFERENCES
*
*        ROUTINES - IECVCRH1 BRANCHES TO TWO INTERNAL SUBROUTINES
*               IN IOS MAINLINE WHICH DO NOT EXPECT TO BE CALLED
*               EXTERNALLY. THEREFORE IECVCRH1 SETS UP THEIR BASE
*               REGISTERS BEFORE CALLING THEM.
*                   IECVCRH1 BRANCHES TO ENTRY POINT IECVSRB TO
*               SCHEDULE AN SRB TO TERMINATE A GDP REQUEST AND
*               BRANCHES TO ENTRY POINT IECVDVT PLUS AN OFFSET
*               TO GET I/O STARTED TO A DEVICE.
*
***********************************************************************
         EJECT
IECVCRH1 EQU   *
***********************************************************************
*                                                                     *
*        ESTABLISH ADDRESSABILITY AND ENVIRONMENT                     *
*                                                                     *
***********************************************************************
         USING IECVCRH1,WRKODD
         STM   WRK0,WRKODD,CRH1SAVE    SAVE CALLERS REGISTERS IN
*                                      INTERNAL SAVE AREA
         LR    BASEA,WRKODD            SET UP ACTUAL BASE REGISTER
         DROP  WRKODD
         USING IECVCRH1,BASEA
*
         USING FLC,ZERO
         USING IOSB,IOSBR              INDICATE
         USING UCB,UCBR                     CONTROL
         USING LCCA,LCCAR                       BLOCK
         USING IECVCRCA,WRK1                        ADDRESSABILITY
*
*
         L     WRKC,IRTLCHAD           PICK UP SAVED POINTER TO LCH
         USING LCH,WRKC                INDICATE ADDRESSABILITY
         L     IOQR,IRTIOQ             RESTORE POINTER TO IOQ
         USING IOQ,IOQR                INDICATE ADDRESSABILITY
         MVI   IRTENVR,IRTSHTP         INDICATE SHOULDER TAP ENVIRON.
         OI    IRTFLB,IRTCHBSY         SET ALL CHANNELS BUSY
         NI    UCBFLA,FF-UCBCUB    TURN OFF CNTRL UNIT BUSY    @OZ05977
         L     WRKA,LCHTCH             GET ADDRESS OF CHANNEL LIST
         NI    IOSOPT2,FF-IOSHTP       NO SHOULDER TAP         @ZM30442
         EJECT
***********************************************************************
*                                                                     *
*        GUARANTEED DEVICE PATH PROCESSING                            *
*                                                                     *
***********************************************************************
         TM    IOSPATH,IOSGDP          GUARANTEED DEVICE PATH REQUEST
         BNO   CRH1100                 NO,GO CHECK IF CHANNEL AVAILABLE
***                                                                 ***
*        FIND REQUESTED CHANNEL                                       *
***                                                                 ***
CRH1010  LH    WRK6,ZERO(WRKA)         GET NEXT CHANNEL FROM LIST
         LTR   WRK6,WRK6               END OF LIST
         BM    CRH1065                 YES - TERMINATE REQUEST
*
         LR    WRKEVEN,WRK6            COPY CHANNEL TO WORK REGISTER
         N     WRKEVEN,DEVMASK         AND OFF DEVICE ADDRESS
         LH    WRK0,IOSPATH            GET REQUESTED PATH
         TM    ONE(WRKA),FF-F          SELECTOR SUBCHANNEL
         BZ    CRH1030                 NO - AND OFF CONTROL UNIT ALSO
         N     WRK0,DEVMASK            AND OFF DEVICE ADDRESS
         B     CRH1040                 GO COMPARE
CRH1030  N     WRK0,CUMASK             AND OFF DEVICE AND CU ADDRESSES
*
CRH1040  CR    WRKEVEN,WRK0            REQUESTED CHANNEL
         BE    CRH1050                 YES - GO CHECK IF RESERVED
         NI    IRTFLB,FF-IRTCHBSY      NO - TURN OFF ALL CHANNELS BUSY
         LA    WRKA,NXTCHN(WRKA)       POINT AT NEXT CHANNEL IN LIST
         B     CRH1010                 TRY NEXT PATH
*
***                                                                 ***
*  GDP REQUESTED CHANNEL FOUND - CHECK IF CORRECT PATH RESERVED       *
***                                                                 ***
CRH1050  TM    UCBFLB,UCBCRHRV     DEVICE RESERVED THROUGH CRH
         BNO   CRH1055                 NO - GET CAT ENTRY ADDR @ZA10497
         LR    WRKODD,WRK6             COPY CHANNEL TO WORK REGISTER
         N     WRKODD,CUMASK           AND OFF CONTROL UNIT ADDRESS
         LH    WRK0,UCBCHAN            GET LAST PATH STARTED
         N     WRK0,CUMASK             AND OFF CONTROL UNIT ADDRESS
         CR    WRKODD,WRK0             CORRECT CHANNEL
         BE    CRH1060                 YES - GET CAT ENTRY ADDRESS
         MVI   IOSCOD,IOSGDPRD         GUARANTEED DEVICE PATH RESERVED
*                                      WRONG PATH
         B     CRH1070                 GO TERMINATE I/O REQUEST
*
CRH1055  TM    UCBFLB,UCBRESVH     IS DEVICE RESERVED BY OTHER @ZA10497
         BO    CRH1065             CPU, YES TERMINATE REQUEST  @ZA10497
***                                                                 ***
* PUT CAT ENTRY ADDRESS IN REGISTER 0 FOR INTERFACE WITH IOS MAINLINE *
***                                                                 ***
CRH1060  SRL   WRKEVEN,EIGHT-CATELP2   GET CAT ENTRY DISPLACEMENT
         A     WRKEVEN,CRCACAT         ADD TO CAT TABLE ADDRESS
         LR    WRK0,WRKEVEN            PUT ADDRESS IN REGISTER 0
         B     CRH1400                 GO MAKE CRH CONNECTION
         EJECT
***********************************************************************
*                                                                     *
*   GUARANTEED DEVICE PATH PROCESSING FAILED - TERMINATE I/O REQUEST *
*                                                                     *
***********************************************************************
***                                                                 ***
*  INTERFACE TO SUBROUTINES IN IOS MAINLINE - THE SIO SUBROUTINES     *
*  ARE INTERNAL SUBROUTINES WHICH DO NOT EXPECT TO BE CALLED FROM     *
*  OUTSIDE OF IOS MAINLINE, THEREFORE THIS END OF THE INTERFACE MUST  *
*  SETUP ALL OF THE REGISTERS THAT THE DEVICE DEPENDENT SIO ROUTINES  *
*  EXPECT, INCLUDING THE THREE BASE REGISTERS.                        *
***                                                                 ***
CRH1065  MVI   IOSCOD,IOSGDPCC         REQUESTED PATH NOT FOUND
CRH1070  L     WRKODD,TERM
         LM    BASEB,BASEC,HISBASES    SET UP BASE REGISTERS FOR IOS
         L     BASEA,HISBASEA          MAINLINE
         BALR  LNKR,WRKODD             GO TO SUBROUTINE IN IOS TO
*                                      TERMINATE THE I/O REQUEST
***                                                                 ***
*        RESTORE MY BASE AND GO EXIT                                  *
***                                                                 ***
CRH1090  L     WRKEVEN,FLCCVT          GET ADDRESS OF CVT
         USING CVTMAP,WRKEVEN          INDICATE ADDRESSABILITY
         L     WRK1,CVTCRCA            RESTORE ADDRESS OF CRCA
         DROP  WRKEVEN                 FREE CVT BASE REGISTER
         L     BASEA,CRCACRH1          RESTORE MY BASE
         NI    IRTFLB,FF-IRTCHBSY      ENSURE MAINLINE TCH ROUTINE
*                                      WILL EXIT
         B     CRH1900                 GO EXIT
         EJECT
***********************************************************************
*                                                                     *
*        CHECK IF CHANNEL AVAILABLE                                   *
*                                                                     *
***********************************************************************
CRH1100  LH    WRK6,ZERO(WRKA)         GET NEXT CHANNEL FROM LIST
         LTR   WRK6,WRK6               END OF LIST
         BM    CRH1800                 YES - GO RETURN
         L     WRKEVEN,CRCACAT         GET SAVED ADDRESS OF CAT
         LR    WRKODD,WRK6             COPY CHANNEL ADDRESS TO USE
*                                      AS AN INDEX INTO CAT
         SRL   WRKODD,EIGHT            ISOLATE CHANNEL NUMBER
         SLL   WRKODD,CATELP2          MULTIPLY BY LENGTH OF CAT TABLE
         ALR   WRKEVEN,WRKODD          POINT AT CAT ENTRY
         USING CAT,WRKEVEN             INDICATE ENTRY ADDRESSABILITY
*
         TM    CATFLG,CATNOP+CATNGEN+CATNCPU  TEST AVAILABILITY BITS
         BZ    CRH1200                 YES - GO CHECK IF RESERVED
*
         LA    WRKA,NXTCHN(WRKA)       BUMP TO NEXT CHANNEL
         B     CRH1100                 GO CHECK OUT NEXT CHANNEL
*
***********************************************************************
*                                                                     *
*        RESERVED DEVICE PROCESSING                                   *
*                                                                     *
***********************************************************************
CRH1200  TM    UCBFLB,UCBCRHRV         DEVICE RESERVED BY CRH
         BNO   CRH1250                 NO - GO TEST IF ONLINE  @ZA10497
***                                                                 ***
*        TEST FOR CORRECT CHANNEL                                     *
***                                                                 ***
         LR    WRKODD,WRK6             COPY CHANNEL TO WORK REGISTER
         N     WRKODD,CUMASK           AND OFF CONTROL UNIT ADDRESS
         LH    WRK0,UCBCHAN            GET LAST PATH STARTED
         N     WRK0,CUMASK             AND OFF CONTROL UNIT AND DEVICE
*
         CR    WRKODD,WRK0             CORRECT CHANNEL
         BE    CRH1300                 YES-GO CHECK OF PATH OFFLINE
         NI    IRTFLB,FF-IRTCHBSY      TURN OFF ALL CHANNELS BUSY FLAG
         LA    WRKA,NXTCHN(WRKA)       BUMP TO NEXT CHANNEL
         B     CRH1100                 GO CHECK OUT NEXT CHANNEL
CRH1250  TM    UCBFLB,UCBRESVH     IS DEVICE RESERVED BY OTHER @ZA10497
         BO    CRH1800             CPU, YES EXIT               @ZA10497
         EJECT
***********************************************************************
*                                                                     *
*        MAKE SURE PATH IS ONLINE                                     *
*                                                                     *
***********************************************************************
CRH1300  LR    WRK0,WRKEVEN            SAVE CAT ADDRESS
         SLR   WRKODD,WRKODD           ZERO REGISTER
         IC    WRKODD,UCBCHM           GET UCB OFFLINE MASK
         LR    WRKEVEN,WRK6            COPY CHAN. AND MASK TO WORK REG.
***                                                                 ***
* NOTE: THIS MODULE SUBSTITUTES FOR A SHOULDER TAP TO THE OTHER CPU   *
*       BECAUSE THE OTHER CPU IS DEAD. THEREFORE THE TEST FOR ONLINE  *
*       PATHS CHECKS TO MAKE SURE THE PATH SELECTED IS ONLINE TO THE  *
*       DEAD CPU - THAT IS THE CPU WE ARE NOT RUNNING ON.             *
***                                                                 ***
         CLI   PSACPUSA+ONE,ZERO       RUNNING ON CPU 0
         BE    CRH1320                 YES - SHIFT FOR CPU 1
         SLL   WRKEVEN,TWO             SHIFT FOR CPU 0
CRH1320  SLL   WRKEVEN,TWO             SHIFT FOR CPU 1
         STC   WRKEVEN,IOQPTH          SAVE PATH MASK
         TM    IOSOPT,IOSAPR           IS ALTERNATE PATH RETRY ACTIVE
         BNO   CRH1340                 NO - GO CHECK IF PATH IS OFFLINE
***                                                                 ***
* ALTERNATE PATH RETRY ACTIVE - CREATE NEW ANDING MASK USING IOSAPMSK *
***                                                                 ***
         TM    UCBFLB,UCBCRHRV         IS DEVICE RESERVED
         BO    CRH1330                 YES - ZERO APR MASK
         SLR   WRKEVEN,WRKEVEN         ZERO REGISTER
         IC    WRKEVEN,IOSAPMSK        GET APR MASK
         OR    WRKODD,WRKEVEN          SETUP NEW MASK FOR ANDING
         IC    WRKEVEN,IOQPTH          RESET PATH TO TEST
         C     WRKODD,PATHMASK         ARE ALL PATHS NO GO
         BNE   CRH1340                 NO - GO CHECK IF PATH IS OFFLINE
         IC    WRKODD,UCBCHM           RESET UCB MASK
CRH1330  MVI   IOSAPMSK,ZERO           ZERO APR MASK
*
CRH1340  NR    WRKODD,WRKEVEN          CHECK IF THIS PATH IS OFFLINE
         BZ    CRH1400                 NO - GO ISSUE TCH
*
         NI    IRTFLB,FF-IRTCHBSY      TURN OFF ALL CHANNELS BUSY
         LA    WRKA,NXTCHN(WRKA)       BUMP TO NEXT CHANNEL IN LIST
         B     CRH1100                 GO CHECK OUT NEXT CHANNEL
         EJECT
***********************************************************************
*                                                                     *
*        CONNECT CHANNEL RECONFIGURATION HARDWARE                     *
*                                                                     *
***********************************************************************
CRH1400  STH   WRK6,CRCACHAN           SAVE THE CHOSEN PATH
***                                                                 ***
*        SET UP MCW TO CONNECT CRH TO CHOSEN PATH                     *
***                                                                 ***
         STCM  WRK6,TWO,CRCAMCWF       STORE CHANNEL NUMBER IN SECOND
*                                      NIBBLE OF MCW CRH FUNCTION BYTE
         OI    CRCAMCWF,CRCAMCWI+CRCAMCWM  TURN ON BIT 26 TO INDICATE
*                                      CRH MCW AND BIT 27 TO MAKE CRH
*                                      CONNECTION
***                                                                 ***
*   SET UP WORK REGISTER TO USE CHANNEL 6 WHEN ISSUING TCH AND SIO    *
***                                                                 ***
         L     WRK6,CHAN6              LOAD CHANNEL 6 INTO PATH REG
***                                                                 ***
*  MAKE CHANNEL RECONFIGURATION HARDWARE CONNECTION TO CHOSEN CHANNEL *
***                                                                 ***
         OI    CRCAFLG1,CRCADIAG       INDICATE CRH CONNECTION
         DC    X'8300'                 ISSUE DIAGNOSE TO MAKE CRH
         DC    S(CRCAMCW)              CONNECTION
         EJECT
***********************************************************************
*                                                                     *
*        ISSUE TEST CHANNEL ACROSS CRH CONNECTION                     *
*                                                                     *
***********************************************************************
IECVCHK1 EQU   *                       HOOK
         ENTRY IECVCHK1
CRH1500  TCH   ZERO(WRK6)              ISSUE TEST CHANNEL
         BC    EIGHT+FOUR+ONE,CRH1510  IF NOT BUSY GO CALL DEVICE
*                                      DEPENDENT SIO SUBROUTINE
***                                                                 ***
*  CHANNEL BUSY - TRY NEXT PATH UNLESS GDP REQUEST                    *
***                                                                 ***
         LR    WRKEVEN,WRK0            RESTORE CAT BASE ADDR   @ZM34511
         OI    CATFLA,CATBSY           INDICATE CHANNEL BUSY
         TM    IOSPATH,IOSGDP          GUARANTEED DEVICE PATH
         BO    CRH1800                 YES - GO DISCONNECT CRH AND
*                                      RETURN
         LA    WRKA,NXTCHN(WRKA)       BUMP TO NEXT CHANNEL IN LIST
         B     CRH1100                 GO CHECK OUT NEXT CHANNEL
***                                                                 ***
*  CHANNEL NOT BUSY - GET CONTROL UNIT AND DEVICE ADDRESS             *
***                                                                 ***
CRH1510  NI    IRTFLB,FF-IRTCHBSY      TURN OFF ALL CHANNELS BUSY FLAG
         ST    WRKA,IRTCHNL            SAVE CHANNEL LIST POINTER
         TM    ONE(WRKA),FF-F          SELECT SUBCHANNEL SPECIFIED
         BZ    CRH1520                 NO - SKIP
         MVZ   UCBUA,ONE(WRKA)         MOVE IN CONTROL UNIT IF IN LIST
CRH1520  IC    WRK6,UCBUA              GET CONTROL UNIT AND DEVICE
         EJECT
***********************************************************************
*                                                                     *
*        CALL DEVICE DEPENDENT SIO SUBROUTINE IN IOS MAINLINE         *
*                                                                     *
***********************************************************************
         L     WRKODD,UCBEXTPT         LOAD POINTER TO UCB EXTENSION
         USING UCBCMEXT,WRKODD         INDICATE ADDRESSABILITY
         LH    WRKC,UCBCCWOF
         SLL   WRKC,THREE                                      @ZA16150
         SLR   WRKEVEN,WRKEVEN
         IC    WRKEVEN,UCBDTI          GET INDEX INTO DEVICE TABLE
         L     WRKODD,FLCCVT           GET ADDRESS OF CVT
         USING CVTMAP,WRKODD           INDICATE ADDRESSABILITY
         L     WRKODD,CVTIXAVL         GET ADDRESS OF IOCOM
         USING IOCOM,WRKODD            INDICATE ADDRESSABILITY
         A     WRKC,IOCIOSCP           POINT AT CHANNEL PROGRAM AREA
         DROP  WRKODD
***                                                                 ***
*  INDEX INTO DEVICE TABLE TO GET ADDRESS OF DEVICE DEPENDENT ROUTINE *
***                                                                 ***
         L     WRKODD,DVT              GET ADDRESS OF DEVICE TABLE
         L     WRKODD,ZERO(WRKODD,WRKEVEN) GET ADDRESS OF SIO ROUTINE
***                                                                 ***
*  INTERFACE TO SUBROUTINES IN IOS MAINLINE - THE SIO SUBROUTINES     *
*  ARE INTERNAL SUBROUTINES WHICH DO NOT EXPECT TO BE CALLED FROM     *
*  OUTSIDE OF IOS MAINLINE, THEREFORE THIS END OF THE INTERFACE MUST  *
*  SETUP ALL OF THE REGISTERS THAT THE DEVICE DEPENDENT SIO ROUTINES  *
*  EXPECT, INCLUDING THE THREE BASE REGISTERS.                        *
***                                                                 ***
         LM    BASEB,BASEC,HISBASES    SETUP BASE REGISTERS FOR IOS
         L     BASEA,HISBASEA          MAINLINE
         BALR  LNKR,WRKODD             GO TO DEVICE DEPENDENT SIO
*                                      SUBROUTINE
         EJECT
***********************************************************************
*                                                                     *
*     RETURN FROM DEVICE DEPENDENT SIO SUBROUTINE IN IOS MAINLINE     *
*                                                                     *
***********************************************************************
***                                                                 ***
*                                                                     *
*     +0 - REQUEST STARTED,ENQUEUE NOT NEEDED                         *
*     +4 - REQUEST STARTED,ENQUEUE NEEDED                             *
*     +8 - REQUEST NOT STARTED,TRY NEXT PATH                          *
*        REQUEST IS ALREADY ENQUEUED THEREFORE +4 RETURN IS NOT       *
*        POSSIBLE.                                                    *
***                                                                 ***
CRH1700  LA    LNKR,TWO(LNKR)          PLUS ZERO RETURN
         LA    LNKR,TWO(LNKR)          PLUS FOUR RETURN
         LA    LNKR,TWO(LNKR)          PLUS EIGHT RETURN
         L     WRKEVEN,FLCCVT          GET CVT ADDRESS
         USING CVTMAP,WRKEVEN          INDICATE ADDRESSABILITY
         L     WRK1,CVTCRCA            RESTORE ADDRESS OF CRCA
         DROP  WRKEVEN                 FREE CVT BASE REGISTER
         L     BASEA,CRCACRH1          RESTORE MY BASE
         LA    WRKODD,CRH1700          GET +0 RETURN ADDRESS
         SR    LNKR,WRKODD             SUBTRACT FROM RETURN VECTOR
         CLM   LNKR,THREE,TOO          CHECK IF +8(NOT STARTED)RETURN
         BNE   CRH1750                 NO - GO DISCONNECT CRH
***                                                                 ***
*    I/O REQUEST NOT STARTED, TRY TO FIND ANOTHER PATH UNLESS GDP     *
***                                                                 ***
         TM    IOSPATH,IOSGDP          CHECK IF GUARANTEED DEVICE PATH
         BO    CRH1800                 YES - GO DISCONNECT CRH
         L     WRKA,IRTCHNL            RESTORE CHANNEL LIST ADDRESS
         LA    WRKA,NXTCHN(WRKA)       BUMP TO NEXT CHANNEL IN LIST
         B     CRH1100                 GO CHECK OUT NEXT CHANNEL
***                                                                 ***
*    SET UP TO RETURN +0 OR +4 DEPENDING ON RETURN                    *
***                                                                 ***
CRH1750  CLM   LNKR,THREE,FORE         CHECK PLUS FOUR RETURN  @ZM30442
         BNE   CRH1800                 NOT PLUS FOUR,ZERO      @ZM30442
         A     LNKR,RETURN             ADD RETURN VALUE FROM   @ZM30442
         ST    LNKR,RETURN             DEVICE DEPEND. ROUTINE  @ZM30442
*                                      TO RETURN ADDRESS       @ZM30442
         EJECT
***********************************************************************
*                                                                     *
*        DISCONNECT CHANNEL RECONFIGURATION HARDWARE AND RETURN       *
*                                                                     *
***********************************************************************
CRH1800  NI    IOSOPT2,FF-IOSHTP       INDICATE NOT ELIGIBLE FOR
*                                      SHOULDER TAP
         NI    CRCAMCWF,CRCAMCWB       TURN OFF BIT 27 IN MCW TO
*                                      BREAK CRH CONNECTION
         DC    X'8300'                 ISSUE DIAGNOSE TO BREAK CRH
         DC    S(CRCAMCW)              CONNECTION
         NI    CRCAFLG1,FF-CRCADIAG    INDICATE NO CRH CONNECTION
***                                                                 ***
*     EXIT LINKAGE                                                    *
***                                                                 ***
CRH1900  LM    WRK0,WRKODD,CRH1SAVE    RESTORE CALLERS REGISTERS FROM
*                                      INTERNAL SAVE AREA
         BR    WRKEVEN                 RETURN
         EJECT
    TITLE 'IECVCRH2 - CRH SIO HOOK ROUTINE'
***********************************************************************
* ENTRY NAME - IECVCRH2
*
* DESCRIPTIVE NAME - CRH SIO HOOK ROUTINE
*
* FUNCTION - THIS ENTRY PROCESSES ALL SIOS DONE ACROSS A CRH
*         CONNECTION.
*
* OPERATION -  IF THE SIO IN IOS MAINLINE WAS THRU A CHANNEL
*           RECONFIGURATION HARDWARE CONNECTION,THIS PROGRAM
*           ENSURES THAT THE PATH STORED IN THE UCB IS THE
*           ACTUAL ADDRESS OF THE DEVICE. IF THE CONDITION CODE
*           WAS ZERO, THE UCBIORST FLAG IS TURNED ON TO INDICATE
*           THE LAST PATH STARTED FOR THAT DEVICE WAS THROUGH CRH.
*           IF THE CONDITION CODE WAS ONE,IECVCRH2 CHECKS WHETHER
*           A SENSE THROUGH CRH IS REQUIRED.
*
* NOTES - DEPENDENCIES
*             THIS PROGRAM RECEIVES CONTROL THRU A HOOK IN THE IOS
*             MAINLINE SIO SUBROUTINE, THEREFORE IT IS ENTIRELY
*             DEPENDENT ON IOS MAINLINE MODULE IECIOSCN.
*
*         PATCH AREA
*             THE IOS PATCH AREA WITH LABEL IOSPATCH IS USED.
*
* INPUT - REGISTERS
*             IECVCRH2 HAS THE SAME EQUATES FOR REGISTERS AS IOS
*             MAINLINE MODULE IECIOSCN. WHEN IECVCRH2 RECEIVES
*             CONTROL IT EXPECTS THE FOLLOWING REGISTER CONTENTS.
*               WRK1    (REGISTER  1) - ADDRESS OF CRCA
*               IOSBR   (REGISTER  2) - ADDRESS OF IOSB
*               UCBR    (REGISTER  7) - ADDRESS OF UCB
*               IOQR    (REGISTER 11) - ADDRESS OF IOQE
*               WRKEVEN (REGISTER 14) - RETURN ADDRESS
*               WRKODD  (REGISTER 15) - ADDRESS OF IECVCRH2
*
* OUTPUT - REGISTERS
*            SAME AS ON ENTRY
*
*   EXIT - NORMAL - RETURN TO HOOK IN IOS MAINLINE
*
*   EXIT - ERROR - NONE
*
* EXTERNAL REFERENCES - NONE
*
***********************************************************************
         EJECT
IECVCRH2 EQU   *
***********************************************************************
*                                                                     *
*        ESTABLISH ADDRESSABILITY AND ENVIRONMENT                     *
*                                                                     *
***********************************************************************
         USING IECVCRH2,WRKODD         INDICATE ADDRESSABILITY
         USING IOSB,IOSBR              INDICATE
         USING LCCA,LCCAR                                      @ZA16177
         USING UCB,UCBR                    CONTROL
         USING IOQ,IOQR                        BLOCK
         USING IECVCRCA,WRK1                     ADDRESSABILITY
***                                                                 ***
*     CHECK IF THIS SIO WAS ACROSS CRH CONNECTION                     *
***                                                                 ***
         NI    IRTENVR,FF-IRTCRHIN     IN CASE NON-CRH CC-1    @ZA16177
         TM    CRCAFLG1,CRCADIAG       IS A CRH CONNECTION OUTSTANDING
         BNO   CRH2600                 NO - GO RETURN - I/O NOT ACROSS
*                                      CRH CONNECTION
         ST    WRK6,CRH2SAV6           SAVE SIO ADDRESS        @ZM30449
         EJECT
***********************************************************************
*                                                                     *
*        PROCESS I/O ACROSS CRH CONNECTION                            *
*                                                                     *
***********************************************************************
         SPM   WRKC                    SET UP CONDITION CODE FROM SIO
         BC    EIGHT,CRH2300           SKIP IF CONDITION CODE ZERO
         BC    TWO+ONE,CRH2500         SKIP IF CC IS 2 OR 3
***                                                                 ***
*     CONDITION CODE ONE - CHECK WHETHER SENSE REQUIRED               *
***                                                                 ***
         TM    CSWUNST,CSWCE       CHANNEL END ?               @ZA16179
         BO    CRH2000             YES, TURN ON IORST          @ZA16179
         TM    CSWUNST,CSWBSY          BUSY CONDITION ?
         BZ    CRH2100                 NO, BRANCH AROUND       @ZA16179
CRH2000  OI    UCBFLB,UCBIORST         SET FOR LATER INTERRUPT @ZA16177
CRH2100  TM    CSWUNST,CSWUCK          UNIT CHECK ?            @ZA16177
         BNO   CRH2200                 NO,CHECK FOR CHANNEL ERRORS
         OI    UCBFLB,UCBCRHSN         YES, INDICATE CRH SENSE REQUIRED
         OI    IRTENVR,IRTCRHIN        STATUS FROM CRH PATH    @ZA16177
CRH2200  TM    CSWCHST,CSWCHER         CHANNEL ERRORS ?
         BZ    CRH2500                 NO, SENSE NOT REQUIRED
         OI    UCBFLB,UCBCRHSN         YES, INDICATE CRH SENSE REQUIRED
         OI    IRTENVR,IRTCRHIN        STATUS FROM CRH PATH    @ZA16177
         B     CRH2500                 GO PUT REMOTE CHANNEL IN UCBCHAN
***                                                                 ***
*     CONDITION CODE ZERO - I/O STARTED                               *
***                                                                 ***
CRH2300  OI    UCBFLB,UCBIORST         INDICATE CRH I/O STARTED
         SPACE 1
         TM    IOQFLB,IOQRESV          WAS CCW A RESERVE
         BZ    CRH2400                 NO - GO CHECK IF RELEASE
         OI    UCBFLB,UCBRESVH+UCBCRHRV  YES - INDICATE DEVICE RESERVED
*                                              ACROSS CRH CONNECTION
CRH2400  TM    IOQFLB,IOQRLSE          WAS CCW A RELEASE
         BZ    CRH2500                 NO - GO PUT CHANNEL IN UCB
         NI    UCBFLB,FF-UCBRESVH-UCBCRHRV  YES - CLEAR RESERVE FLAGS
***                                                                 ***
*     PUT ACTUAL REMOTE CHANNEL ADDRESS IN UCB FOR TRACE FACILITY     *
***                                                                 ***
CRH2500  ICM   WRK6,TWO,CRCACHAN       GET SAVED CHANNEL ADDRESS
         STH   WRK6,UCBCHAN            EXECUTE OVERLAID HOOK INSTRUTION
*                                      USING ACTUAL PATH TO DEVICE
         L     WRK6,CRH2SAV6           RESTORE SIO ADDRESS     @ZM30449
*
***********************************************************************
*                                                                     *
*        RETURN                                                       *
*                                                                     *
***********************************************************************
CRH2600  BR    WRKEVEN                 RETURN
         EJECT
    TITLE 'IECVCRH3 - CRH SENSE HOOK ROUTINE'
***********************************************************************
* ENTRY NAME - IECVCRH3
*
* DESCRIPTIVE NAME - CRH SENSE HOOK ROUTINE
*
* FUNCTION -  ISSUE A SENSE SIO TO A GIVEN DEVICE THRU A CHANNEL
*             RECONFIGURATION HARDWARE CONNECTION, IF I/O WAS
*             STARTED FOR THAT DEVICE THRU CRH.
*
* OPERATION - IECVCRH3 CHECKS WHETHER THE SENSE MUST BE ISSUED
*          THROUGH A CRH CONNECTION AND IF SO MAKES THE CRH
*          CONNECTION TO THE REQUIRED CHANNEL, ISSUES THE SENSE SIO,
*          SAVES THE CONDITION CODE AND BREAKS THE CRH CONNECTION.
*          IF THE SENSE SIO WAS SUCESSFUL, IECVCRH3 INDICATES
*          THE LAST PATH STARTED TO THE DEVICE WAS THROUGH CRH.
*
* NOTES - DEPENDENCIES
*             THIS PROGRAM RECEIVES CONTROL THRU A HOOK IN IOS
*             MAINLINE, THEREFORE IT IS ENTIRELY DEPENDENT ON
*             IOS MAINLINE MODULE IECIOSCN.
*
*         PATCH AREA
*             THE IOS PATCH AREA WITH LABEL IOSPATCH IS USED.
*
* INPUT - REGISTERS
*             IECVCRH3 HAS THE SAME EQUATES FOR REGISTERS AS IOS
*             MAINLINE MODULE IECIOSCN. WHEN IECVCRH3 RECEIVES
*             CONTROL IT EXPECTS THE FOLLOWING REGISTER CONTENTS.
*               WRK1    (REGISTER  1) - ADDRESS OF CRCA
*               WRK6    (REGISTER  6) - DEVICE ADDRESS FOR SENSE
*               UCBR    (REGISTER  7) - ADDRESS OF UCB
*               WRKEVEN (REGISTER 14) - RETURN ADDRESS
*               WRKODD  (REGISTER 15) - ADDRESS OF IECVCRH3
*
* OUTPUT - REGISTERS
*            SAME AS ON ENTRY
*
*   EXIT - NORMAL - RETURN TO HOOK IN IOS MAINLINE
*
*   EXIT - ERROR - NONE
*
* EXTERNAL REFERENCES - NONE
*
***********************************************************************
         EJECT
IECVCRH3 EQU   *
***********************************************************************
*                                                                     *
*        ESTABLISH ADDRESSABILITY AND ENVIRONMENT                     *
*                                                                     *
***********************************************************************
         USING IECVCRH3,WRKODD         INDICATE ADDRESSABILITY
         STM   WRK0,WRKODD,CRH3SAVE    SAVE CALLERS REGISTERS IN
*                                      INTERNAL SAVE AREA
         DROP  WRKODD
         LR    BASEA,WRKODD            SET UP ACTUAL BASE REGISTER
         USING IECVCRH3,BASEA
*
         USING UCB,UCBR                INDICATE CONTROL
         USING IECVCRCA,WRK1                BLOCK ADDRESSABILITY
         USING EWA,WRKB                                        @ZA16177
         USING FLC,0                                           @ZA16177
***********************************************************************
*        CHECK IF CRH SENSE REQUIRED                                  *
***********************************************************************
         NI    UCBFLB,FF-UCBCRHSN      RESET FLAG              @ZA16177
         CLC   EWACPU,PSACPUSA+ONE     CRH SENSE NEEDED ?      @ZA16177
         BNE   CRH3100                 YES, SET IORST          @ZA16177
         SPACE 1
         NI    UCBFLB,FF-UCBIORST      INSURE IORST IS OFF     @ZA16177
         B     CRH3300                 GO START SENSE          @ZA16177
         SPACE 1
CRH3100  OI    UCBFLB,UCBIORST         SET IORST ON FOR CRH IO @ZA16177
         TM    CRCAFLG1,CRCADIAG       IS A CRH CONNECTION OUTSTANDING?
*                                      ( SIO GET A CONDITION CODE 1 ? )
         BO    CRH3200                 YES-SKIP MAKING CRH CONNECTION
*                                      BUT USE CHANNEL 6 TO ISSUE SENSE
         EJECT
***********************************************************************
*                                                                     *
*        CONNECT CHANNEL RECONFIGURATION HARDWARE                     *
*                                                                     *
***********************************************************************
         STH   WRK6,CRCACHAN           SAVE THE CHOSEN PATH
***                                                                 ***
*        SETUP MCW TO CONNECT CRH TO CHOSEN PATH                      *
***                                                                 ***
         STCM  WRK6,TWO,CRCAMCWF       STORE CHANNEL NUMBER IN SECOND
*                                      NIBBLE OF MCW CRH FUNCTION BYTE
         OI    CRCAMCWF,CRCAMCWI+CRCAMCWM  TURN ON BIT 26 TO INDICATE
*                                      CRH MCW AND BIT 27 TO MAKE CRH
*                                      CONNECTION
***                                                                 ***
*  MAKE CHANNEL RECONFIGURATION HARDWARE CONNECTION TO CHOSEN CHANNEL *
***                                                                 ***
         OI    CRCAFLG1,CRCADIAG       INDICATE CRH CONNECTION
         DC    X'8300'                 ISSUE DIAGNOSE TO MAKE CRH
         DC    S(CRCAMCW)              CONNECTION
***                                                                 ***
*   SETUP WORK REGISTER TO USE CHANNEL 6 WHEN ISSUING SENSE SIO       *
***                                                                 ***
CRH3200  N     WRK6,CHANMASK           AND OFF CHANNEL NUMBER
         O     WRK6,CHAN6              OR CHANNEL 6 INTO CHOSEN PATH
         EJECT
***********************************************************************
*                                                                     *
*        ISSUE SENSE SIO                                              *
*                                                                     *
***********************************************************************
CRH3300  SIO   ZERO(WRK6)             ISSUE SENSE SIO
         BALR  WRKA,ZERO              SAVE CONDITION CODE FROM SIO
*    ONE LINE DELETED FOR OZ16177                              @ZA16177
         TM    CRCAFLG1,CRCADIAG      CRH SENSE ?
         BNO   CRH3500                NO, LEAVE CRH SENSE FLAG ALONE
*   FOUR LINES DELETED FOR OZ16177                             @ZA16177
***********************************************************************
*                                                                     *
*        DISCONNECT CHANNEL RECONFIGURATION HARDWARE AND RETURN       *
*                                                                     *
***********************************************************************
         NI    CRCAMCWF,CRCAMCWB       TURN OFF BIT 27 IN MCW TO
*                                      BREAK CRH CONNECTION
         DC    X'8300'                 ISSUE DIAGNOSE TO BREAK CRH
         DC    S(CRCAMCW)              CONNECTION
         NI    CRCAFLG1,FF-CRCADIAG    INDICATE NO CRH CONNECTION
***                                                                 ***
*             EXIT LINKAGE                                            *
***                                                                 ***
CRH3500  SPM   WRKA                    RESTORE CONDITION CODE
         LM    WRK0,WRKODD,CRH3SAVE    RESTORE CALLERS REGISTERS FROM
*                                      INTERNAL SAVE AREA
         BR    WRKEVEN                 RETURN
         EJECT
***********************************************************************
*                                                                     *
*             REGISTER EQUATES (SAME AS IOS MAINLINE - IECIOSCN)      *
*                                                                     *
***********************************************************************
WRK0     EQU   0                       WORK REGISTER 0
WRK1     EQU   1                       WORK REGISTER 1
IOSBR    EQU   2                       IOSB REGISTER
LCCAR    EQU   3                       LCCA REGISTER (CONTAINS IRT)
LNKR     EQU   4                       LINK REGISTER
BASEA    EQU   5                       BASE REGISTER
WRK6     EQU   6                       WORK REGISTER 6
UCBR     EQU   7                       UCB REGISTER
BASEB    EQU   8                       BASE REGISTER B
BASEC    EQU   9                       BASE REGISTER C
WRKA     EQU   10                      WORK REGISTER 10
WRKB     EQU   11                      WORK REGISTER 11
IOQR     EQU   11                      IOQE REGISTERS
WRKC     EQU   12                      WORK REGISTER 12
SAVR     EQU   13                      LCCA SAVE AREA REGISTER
WRKEVEN  EQU   14                      EVEN OF PAIR WORK REGISTER
WRKODD   EQU   15                      ODD OF PAIR WORK REGISTER
***********************************************************************
*                                                                     *
*                   EQUATES                                           *
*                                                                     *
***********************************************************************
ZERO     EQU   0                       ZERO
ONE      EQU   1                       ONE
NXTCHN   EQU   2                       LENGTH OF ENTRY IN TCH TABLE
TWO      EQU   2                       TWO
THREE    EQU   3                       THREE
FOUR     EQU   4                       FOUR
FIVE     EQU   5                       FIVE
EIGHT    EQU   8                       EIGHT
F        EQU   X'F'
FF       EQU   X'FF'
CSWUNST  EQU   FLCCSW+FOUR
CSWCHST  EQU   FLCCSW+FIVE
CSWUCK   EQU   X'02'
CSWCHER  EQU   X'0E'
CSWBSY   EQU   X'10'
CSWCE    EQU   X'08'                                           @OZ05693
         EJECT
***********************************************************************
*                                                                     *
*       INTERNAL  SAVE  AREAS                                         *
*                                                                     *
***********************************************************************
***                                                                 ***
*              IECVCRH1 SAVE AREA FOR CALLERS REGISTERS
***                                                                 ***
CRH1SAVE DS    0F
         DS    5F
HISBASEA DS    F                       IOS MAINLINE BASEA
         DS    2F
HISBASES DS    2F                      IOS MAINLINE BASEB AND BASEC
         DS    4F
RETURN   DS    2F
***                                                                 ***
*              IECVCRH2 SAVE AREA FOR CALLERS REGISTERS
***                                                                 ***
CRH2SAV6 DS    F                                               @YM30449
***                                                                 ***
*              IECVCRH3 SAVE AREA FOR CALLERS REGISTERS
***                                                                 ***
CRH3SAVE DS    16F
*
***********************************************************************
*                                                                     *
*        CONSTANTS                                                    *
*                                                                     *
***********************************************************************
         DS    0F                      GET WORD BOUNDARY
CHAN6    DC    X'00000600'             CHANNEL 6
DEVMASK  DC    X'00000FF0'             MASK TO AND OFF DEVICE ADDRESS
CUMASK   DC    X'00000F00'             MASK TO AND OFF DEVICE AND
*                                      CONTROL UNIT ADDRESS
CHANMASK DC    X'000000FF'             MASK TO AND OFF CHANNEL NUMBER
PATHMASK DC    X'000000F0'             PATH MASK TESTER
TERM     DC    V(IECVSRB)              ADDRESS OF I/O TERMINATION
*                                      SUBROUTINE IN IOS MAINLINE
DVT      DC    V(IECVDVT)              ADDRESS OF DEVICE TABLE
TOO      DC    H'2'                    TWO
FORE     DC    H'4'                    FOUR
       END
