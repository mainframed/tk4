IGE0002E TITLE 'CHANNEL-TO-CHANNEL DEVICE ERROR ROUTINE'
******************************************************************
*                                                                *
* MODULE NAME = IGE0002E                                         *
*                                                                *
* DESCRIPTIVE NAME = CHANNEL-TO-CHANNEL ERROR ROUTINE            *
*                                                                *
* COPYRIGHT = NONE                                               *
*                                                                *
* STATUS =                                                       *
*                                                                *
* FUNCTION =   THIS MODULE WILL BE ENTERED FOR ERRORS ON THE     *
*              CHANNEL-TO-CHANNEL DEVICE.  THE ERRORS CAUSING    *
*              ENTRY INCLUDE;                                    *
*                  CC=3 FROM SIO                                 *
*                  CHANNEL ERRORS                                *
*                  DEVICE ERRORS                                 *
*                  INTERCEPT CONDITION                           *
*                                                                *
* NOTES = SEE BELOW                                              *
*                                                                *
*    DEPENDENCIES = NONE                                         *
*                                                                *
*    REGISTER CONVENTIONS = SEE REGISTER EQUATES                 *
*                                                                *
*    PATCH LABEL = PATCH                                         *
*                                                                *
* MODULE TYPE = DEVICE ERROR RECOVERY ( CONTROL )                *
*                                                                *
*    PROCESSOR = ASSEMBLER                                       *
*                                                                *
*    MODULE SIZE = 300 BYTES DECIMAL                             *
*                                                                *
*    ATTRIBUTES = SUPRVR STATE,KEY 0,ENABLED,PAGEABLE,UNLOCKED   *
*                                                                *
* ENTRY POINT = IGE0002E - SCHEDULED ON THE USER MEMORY'S SIRB   *
*                                                                *
* INPUT = IOSB                                                   *
*                                                                *
* OUTPUT = NONE                                                  *
*                                                                *
* EXIT-NORMAL = 1. SVC 15,SVC 3 SEQUENCE                         *
*               2. TO IGE0025D VIA IECVERPL FOR MESSAGE          *
*                                                                *
* EXIT-ERROR = NONE                                              *
*                                                                *
* EXTERNAL REFERENCES = CVTXTLER FOR IECVERPL ERP XCTL ENTRY     *
*                                                                *
* TABLES = NONE                                                  *
*                                                                *
* MACROS =                                                       *
*      MAPPING -                                                 *
*                                                                *
*             IECDIOCM       IEFUCBOB                            *
*             IECDIOSB       IECDERWA                            *
*             CVT                                                *
*                                                                *
* CHANGE ACTIVITY = NONE                                         *
*                                                                *
******************************************************************
         EJECT
         IECDIOCM
         EJECT
         IEFUCBOB LIST=NO,PREFIX=YES
         EJECT
         IECDIOSB
         EJECT
         IECDERWA
******************************************************************
*              DSECT OF USER AREA IN EWA                         *
******************************************************************
         ORG   EWAIERP
EWC      EQU   *                   *
EWCSAVE  DS    16F                 SAVE AREA ADDRESSED BY R13
EWCREPLY DS    CL3                 REPLY AREA FOR WTOR
EWCRECB  DS    F                   REPLY ECB
EWCERCNT DS    H                   COUNT OF ATTEMPTED RETRIES
EWCEND   EQU   *                   *
         EJECT
         CVT   DSECT=YES
         EJECT
         IHAPSA
******************************************************************
*      DSECT FOR LOGREC RECORD BUFFER                          @ZA11845
******************************************************************
CTCLOGBF DSECT                                                 @ZA11845
CTCHDR   DS    XL4                 RECORD HEADER               @ZA11845
CTCPARMA DS    AL4                 ADDRESS OF DATA             @ZA11845
CTCDATA  DS    0X                                              @ZA11845
CTCCCW   DS    XL8                 CCW AT TIME OF FAILURE      @ZA11845
CTCCSW   DS    XL8                 CURRENT CSW                 @ZA11845
CTCCNT   DS    X                   CNT OF DEV DEPENDENT DATA   @ZA11845
CTCSECUA DS    XL3                 ADDR OF FAILING DEVICE      @ZA11845
CTCTYPE  DS    XL4                 DEV TYPE OF FAILING DEVICE  @ZA11845
CTCSDR   DS    X                   COUNT OF STATISTICAL DATA   @ZA11845
CTCPCUA  DS    XL3                 ADDR OF FAILING DEVICE      @ZA11845
CTCRETRY DS    XL2                 NUMBER OF RETRY ATTEMPTS    @ZA11845
CTCSCNT  DS    XL2                 COUNT OF SENSE DATA         @ZA11845
CTCSNS   DS    XL2                 ACTUAL SENSE DATA           @ZA11845
CTCSAVE  DS    18F                 SAVE AREA FOR RECORD        @ZA11845
******************************************************************
*        GENERAL EQUATES                                         *
******************************************************************
         SPACE 2
R0       EQU   0                   *
R1       EQU   1                   *
R2       EQU   2                   *
R3       EQU   3                   *
R4       EQU   4                   *G E N E R A L
R5       EQU   5                   *
R6       EQU   6                   *  R E G I S T E R
R7       EQU   7                   *
R8       EQU   8                   *     E Q U A T E S
R9       EQU   9                   *
R10      EQU   10                  *
R11      EQU   11                  *
R12      EQU   12                  *
R13      EQU   13                  *
R14      EQU   14                  *
R15      EQU   15                  *
IOSBREG  EQU   8                   BASE REG FOR IOSB           @ZA11845
UCBREG   EQU   9                   BASE REG FOR UCB
ERPWORK  EQU   11                  BASE REG FOR ERP WORKAREA
BASEREG  EQU   12                  BASE REG FOR IGE0002E
SAVREG   EQU   13                  SAVE AREA POINTER
RETREG   EQU   14                  RETURN ADDRESS
BSY      EQU   X'10'               DEVICE BUSY
UC       EQU   X'02'               UNIT CHECK
UE       EQU   X'01'               UNIT EXCEPTION
PCI      EQU   X'80'               PROGRAM CONTROLLED INTERRUPT
ILI      EQU   X'40'               INCORRECT LENGTH INDICATION
CPGMCK   EQU   X'20'               CHANNEL PROGRAM CHECK
CPRTCK   EQU   X'10'               CHANNEL PROTECTION CHECK
CDATCK   EQU   X'08'               CHANNEL DATA CHECK
CCTLCK   EQU   X'04'               CHANNEL CONTROL CHECK
INCTLCK  EQU   X'02'               INTERFACE CONTROL CHECK
CHNCK    EQU   X'01'               CHAINING CHECK              @OZ06059
INTREQ   EQU   X'40'               INTERVENTION REQUIRED       @OZ06059
BUFDACK  EQU   X'20'               BUFFER DATA CHECK           @OZ06059
EQPMTCK  EQU   X'10'               EQUIPMENT CHECK             @OZ06059
FF       EQU   X'FF'               MASK OF ALL ONES
OFF      EQU   X'0F'               MASK FOR RESETTING BITS     @ZM32806
EXIT     EQU   3                   SVC 3 (EXIT) EQUATE
RETRNIOS EQU   15                  SVC 15 RETURN TO IOS EQUATE
MAXRETRY EQU   3                   MAXIMUM NUMBER OF RETRYS
WTOLOAD  EQU   253                 IGE0025C BINARY NAME VALUE
         EJECT
IGE0002E CSECT
******************************************************************
*                                                                *
*        INITIALIZATION OF IGE0002E                              *
*                                                                *
******************************************************************
         LR    IOSBREG,R1          BASE FOR IOSB               @ZA11845
         USING IOSB,IOSBREG
         USING EWA,ERPWORK
         L     ERPWORK,IOSERP      EWA ADDRESS
         USING UCB,UCBREG
         L     UCBREG,IOSUCB       UCB ADDRESS
         LA    R2,UCBPRFX          GET PREFIX LENGTH           @ZM32806
         SR    UCBREG,R2           POINT TO UCB PREFIX         @ZM32806
         USING IGE0002E,BASEREG
         LR    BASEREG,R15
         MVC   EWACHA,UCBCHAN      PRIME DEVICE ADDR           @ZA11845
         EJECT
******************************************************************
*              ERROR TYPE DETERMINATION                          *
******************************************************************
         SPACE 1
*        TEST FOR CHANNEL CONTROL CHECK
         TM    IOSTSB,CCTLCK
         BO    CTCECCC             BRANCH YES TO RETRY
         SPACE 1
*        TEST FOR INTERFACE CONTROL CHECK
         TM    IOSTSB,INCTLCK
         BO    CTCEICC             BRANCH YES TO RETRY
         SPACE 1
*        TEST FOR INTERCEPT
         TM    UCBFLC,UCBITF
         BO    CTCEINTC            BRANCH YES
         SPACE 1
*        TEST FOR CONDITION CODE 3
         TM    IOSCC,IOSCC3
         BO    CTCECC3             BRANCH YES TO PERM ERROR
         SPACE 1
*      TEST FOR CHANNEL DATA CHECK
         TM    IOSTSB,CDATCK
         BO    CTCECDC             BRANCH YES TO RETRY
         SPACE 1
*      TEST FOR UNIT CHECK                                     @OZ06059
         TM    IOSTSA,UC                                       @OZ06059
         BO    CTCEUNTC            BRANCH YES TO TEST SENSE    @OZ06059
         SPACE 1
*      TEST FOR CHAINING CHECK                                 @OZ06059
         TM    IOSTSB,CHNCK                                    @OZ06059
         BO    CTCECHC             BRANCH YES TO RETRY         @OZ06059
         SPACE 1
*      TEST FOR CHANNEL PROGRAM CHECK
         TM    IOSTSB,CPGMCK
         BO    CTCECPGC            BRANCH YES TO PERM ERROR
         SPACE 1
*      TEST FOR CHANNEL PROTECTION CHECK
         TM    IOSTSB,CPRTCK
         BO    CTCECPTC            BRANCH YES TO PERM ERROR
         SPACE 1
*      TEST FOR UNIT EXCEPTION
         TM    IOSTSA,UE
         BO    CTCEUE              BRANCH YES TO ERROR EXCP
         SPACE 1
*      TEST FOR INCORRECT LENGTH INDICATION
         TM    IOSTSB,ILI
         BO    CTCEILI             BRANCH YES TO PERM ERROR    @ZA11845
         EJECT
******************************************************************
*        TEST FOR DEVICE BUSY
******************************************************************
         TM    IOSTSA,BSY
         BO    CTCEBSY             BRANCH YES
         SPACE 1
         B     CTCENORM            BR-RECOVERY SUCCESSFUL      @ZA11845
         SPACE 3
***************************************************************@OZ06059
*        UNIT CHECK - TEST SENSE BITS                         *@OZ06059
***************************************************************@OZ06059
CTCEUNTC DS    0H                                              @OZ06059
         SPACE 1
*      TEST FOR EQUIPMENT CHECK                                @OZ06059
         TM    IOSSNS,EQPMTCK                                  @OZ06059
         BO    CTCEEQPC            BRANCH YES TO RETRY         @OZ06059
         SPACE 1
*      TEST FOR INTERVENTION REQUIRED                          @OZ06059
         TM    IOSSNS,INTREQ                                   @OZ06059
         BO    CTCEIREQ            BRANCH YES TO INT REQ       @ZA11845
         SPACE 1
*      TEST FOR BUFFER DATA CHECK                              @OZ06059
         TM    IOSSNS,BUFDACK                                  @OZ06059
         BO    CTCEBDAC            BRANCH YES TO PERM ERROR    @OZ06059
         SPACE 1
         B     CTCENORM            EXIT ON ANY OTHER STATUS    @OZ06059
         EJECT
******************************************************************
*        CHAINING CHECK                                        @OZ06059
*        EQUIPMENT CHECK                                       @OZ06059
*        CHANNEL CONTROL CHECK                                   *
*        INTERFACE CONTROL CHECK                                 *
*        CHANNEL DATA CHECK                                      *
*        UNIT CHECK - INTERVENTION REQUIRED                    @ZA11845
******************************************************************
CTCECHC  DS    0H                  *                           @OZ06059
CTCEEQPC DS    0H                  *                           @OZ06059
CTCECCC  DS    0H                  *
CTCEICC  DS    0H                  *
CTCECDC  DS    0H                  *
CTCEUCIR DS    0H                  *                           @ZA11845
         LR    R1,IOSBREG          RESTORE REG 1 FOR EXITS     @ZA11845
         LH    R3,EWCERCNT         GET RETRY COUNT
         LA    R2,MAXRETRY         MAX NUMBER OF RETRIES ALLOWD@ZM32806
         CR    R3,R2               Q/MORE RETRIES ALLOWED      @ZM32806
         BNL   CTCEPERR            NO/BR-TRY TO SWITCH CTC DEVS@ZM32806
         TM    EWARGFG1,EWANORTY   Q/IS RETRY POSSIBLE
         BNO   CTCERTRY            YES/BR-RETRY
         SPACE 2
******************************************************************
*        BUFFER DATA CHECK ON UNIT CHECK                       @OZ06059
*        INTERCEPT ERROR                                         *
******************************************************************
CTCEBDAC DS    0H                  *                           @OZ06059
CTCEINTC B     CTCEPERR            BR-PERMANENT ERROR
         SPACE 2
******************************************************************
*        INCORRECT LENGTH INDICATION                             *
*        CHANNEL PROGRAM CHECK                                   *
*        CHANNEL PROTECTION CHECK                                *
*        DEVICE BUSY AT SIO                                      *
*              THESE ERRORS ARE USER CAUSED                      *
******************************************************************
CTCEILI  DS    0H                  *                           @ZA11845
CTCECPGC DS    0H                  *
CTCECPTC DS    0H                  *
CTCEBSY  DS    0H                  *
         B     CTCEPERR            BR-PERMANENT ERROR
         SPACE 2
******************************************************************
*        CONDITION CODE THREE - GO TRY TO SWITCH                 *
******************************************************************
CTCECC3  DS    0H                  *
         B     CTCEPERR            BR-TRY TO SWITCH CTC DEVICES
         EJECT
******************************************************************
*        INTERVENTION REQUIRED                                   *
******************************************************************
CTCEIREQ DS    0H                                              @ZA11845
CTCEGET  GETMAIN RC,LV=128,RELATED=(CTCEFREE)                  @ZA11845
         LTR   R15,R15             GETMAIN SUCCESSFUL ?        @ZA11845
         BNZ   CTCEUCIR            NO, BRANCH AROUND RECORD    @ZA11845
         LR    R5,R1               GET GOTTEN CORE PTR         @ZA11845
         USING CTCLOGBF,R5         ADDRESS BUFFER              @ZA11845
         MVC   CTCHDR,CTCHEAD      MOVE HEADER INTO BUFFER     @ZA11845
         LA    R4,CTCDATA          PUT DATA ADDRESS INTO       @ZA11845
         ST    R4,CTCPARMA         BUFFER                      @ZA11845
         L     R4,IOSCC            GET CCW ADDRESS             @ZA11845
         LA    R4,0(R4)            CLEAR HI-ORDER BYTE         @ZA11845
         LTR   R4,R4               TEST FOR ZERO CCW ADDR      @ZA11845
         BZ    CTCE0000            IF ZERO, DONT RECORD IT     @ZA11845
         S     R4,EIGHT            POINT TO CURRENT CCW        @ZA11845
         MVC   CTCCCW(8),0(R4)     SAVE CCW IN BUFFER          @ZA11845
CTCE0000 MVC   CTCCSW(8),IOSCSW-1  MOVE IN CSW                 @ZA11845
         SR    R4,R4               ZERO REGISTER               @ZA11845
         STC   R4,CTCCNT           NO DEVICE DEPENDENT DATA    @ZA11845
         MVC   CTCSECUA(3),UCBNAME DEVICE ADDRESS IN EBCDIC    @ZA11845
         MVC   CTCTYPE(4),UCBTYP   DEVICE TYPE                 @ZA11845
         STC   R4,CTCSDR           INDICATE NO SDR DATA        @ZA11845
         MVC   CTCPCUA(3),UCBNAME  DEVICE ADDRESS IN EBCDIC    @ZA11845
         MVC   CTCRETRY(2),EWCERCNT   RETRY COUNT              @ZA11845
         LA    R4,2                COUNT OF SENSE INFORMATION  @ZA11845
         STH   R4,CTCSCNT          SAVE IN BUFFER              @ZA11845
         MVC   CTCSNS(2),IOSSNS    SAVE SENSE                  @ZA11845
         LA    R13,CTCSAVE         ADDR OF SAVE AREA           @ZA11845
*                                                              @ZA11845
         LA    R4,200(UCBREG)      POINT TO COMMON SEG         @ZA11845
         IOSGEN UCBFLG,UCB=(R4),VAR=ON,TABLE=UCBNRY,REG=UNCOND
         RECORD TYPE=LOGREC,RCVRY=ESTAE,HEADER=YES,LENGTH=34,  @ZA11845X
               PARMADR=(1)                                     @ZA11845
CTCEFREE FREEMAIN RC,LV=128,A=(5),RELATED=(CTCEGET)            @ZA11845
         B     CTCEUCIR            GO TO TEST RETRY COUNT      @ZA11845
*                                                              @ZA11845
         SPACE 2
******************************************************************
*        RECOVERY SUCCESSFUL EXIT                                *
******************************************************************
CTCENORM DS    0H                  RECOVERY SUCCESSFUL         @ZA11845
CTCEUE   DS    0H                  UNIT EXCEPTION
CTCERCVR DS    0H                  *
         SLR   R0,R0               CLEAR R0
         STH   R0,IOSTSA           CLEAR IOSTSA & IOSTSB
         NI    IOSFLA,FF-IOSERR-IOSEX RESET TO INDICATE NO ERROR
         SVC   RETRNIOS            ERROR EXCP
         SVC   EXIT                EXIT
         SPACE 2
******************************************************************
*        SUBROUTINE TO SET UP AND EXIT FOR RETRY                 *
******************************************************************
CTCERTRY DS    0H
         TM    IOSFLC,IOSCTCNR     SHOULD WE ATTEMPT RETRY ?   @ZA11845
         BO    CTCEPERR            NO, BRANCH TO PERM ERROR    @ZA11845
         NI    IOSFLA,FF-IOSEX     RESET TO ALLOW RETRY
         OI    IOSFLA,IOSERR       SET TO CAUSE RETRY
         LH    R3,EWCERCNT         GET CURRENT RETRY COUNT
         LA    R3,1(0,R3)          INCREMENT COUNT OF RETRIES
         STH   R3,EWCERCNT         SAVE NEW RETRY COUNT        @ZM32806
         NI    IOSTSB,FF-OFF       INSURE CHANNEL ERRORS RESET @ZM32806
         SVC   RETRNIOS            ERROR EXCP
         SVC   EXIT                EXIT
         SPACE 2
******************************************************************
******************************************************************
*        A PERMANENT  ERROR HAS BEEN DETERMINED             *
******************************************************************
CTCEPERR DS    0H
         NI    IOSFLA,FF-IOSERR    FLAG ERROR PERMANENT
         OI    IOSFLA,IOSEX        FLAG ERROR PERMANENT
         OI    IOSFLB,IOSLOG       WRITE OBR LOGREC            @ZA06472
         OI    IOSFLB,IOSMSG       INDICATE I/O ERROR          @ZA11845
         LA    R13,WTOLOAD         PLACE IGE0025C ID IN R13
         L     RETREG,CVTPTR       CVT ADDRESS
         L     RETREG,CVTXTLER-CVT(,RETREG) IECVERPL ERP XCTL ENTRY
         BR    RETREG              GO TO IGE0025C VIA IECVERPL
******************************************************************
         EJECT
******************************************************************
*        CONSTANTS FOR IGE0002E                                  *
******************************************************************
EIGHT    DC    F'8'                CONSTANT - EIGHT            @ZA11845
CTCHEAD  DC    X'30830000'         LOGREC RECORD HEADER        @ZA11845
CTCPATCH DC    C'CTCPATCH'         EXPANSION AREA-RESERVED     @ZM32806
         DC    8F'0'
         END
