CATN     TITLE 'IECTCATN - CTC ATTENTION PROCESSOR'
*****************************************************************
*                                                               *
* MODULE NAME = IECTCATN ( OS/VS2 )                             *
*                                                               *
* DESCRIPTIVE NAME = CHANNEL-TO-CHANNEL DEVICE ATTENTION ROUTINE*
*                                                               *
* COPYRIGHT = NONE                                              *
*                                                               *
* STATUS = CHANGE LEVEL 0                                       *
*                                                               *
* FUNCTION = THIS MODULE WILL BE SCHEDULED BY IOS WHEN AN       *
*            ATTENTION STATUS OCCURS ON A CHANNEL-TO-CHANNEL    *
*            DEVICE.  IF THE UCB HAS AN ADDRESS IN THE UCBCTCAD *
*            FIELD OF THE UCB THEN AN I/O OPERATION WILL BE     *
*            INITIATED TO SENSE THE COMMAND BYTE FROM THE       *
*            CHANNEL-TO-CHANNEL ADAPTER                         *
*                                                               *
* NOTES =                                                       *
*    DEPENDENCIES = NONE                                        *
*    RESTRICTIONS = NONE                                        *
*    REGISTER CONVENTIONS = SEE EQUATES                         *
*    PATCH LABEL =                                              *
*                                                               *
* MODULE TYPE = PROCEDURE                                       *
*    PROCESSOR = ASSEMBLER                                      *
*    MODULE SIZE = HEX 82 BYTES                                 *
*    ATTRIBUTES = RE-ENTRANT                                    *
*              REFRESHABLE                                      *
*              SUPERVISOR STATE                                 *
*              PROTECT KEY ZERO                                 *
*              MASTER SCHEDULER MEMORY                          *
*                                                               *
* LOCKS = NONE                                                  *
*                                                               *
* ENTRY POINT = IECTCATN - BRANCH ENTERED BY POST STATUS        *
*    REGISTERS AT ENTRY:                                        *
*              R1 = ADDRESS OF THE IOSB FROM IOS                *
*              R13 = ADDRESS OF SAVE AREA                       *
*              R14= RETURN ADDRESS TO POST STATUS               *
*              R15= ENTRY POINT ADDRESS OF IECTCATN             *
*              R6-R12 ARE WORK REGS THAT NEED NOT BE RETURNED   *
*              R2-R5,R13 MUST HAVE CONTENTS PRESERVED           *
*                                                               *
*    INPUT =   - AN IOSB CONTAINING THE UCB ADDRESS.            *
*              - THE UCB WILL CONTAIN A SRB/IOSB AT UCBCTCAD IF *
*                THE DEVICE HAS BEEN INITIALIZED FOR USE BY AN  *
*                IOS DRIVER.  THE SRB/IOSB WILL BE INITIALIZED  *
*                FOR STARTIO.                                   *
*                                                               *
*    OUTPUT =  - STARTIO WILL HAVE BEEN ISSUED FOR A 'SENSE     *
*                COMMAND BYTE' COMMAND TO THE CHANNEL-TO-CHANNEL*
*                ADAPTER.                                       *
*                                                               *
*    EXIT-NORMAL = RETURN TO ADDRESS PASSED IN R14              *
*                                                               *
*    EXIT-ERROR = NONE                                          *
*                                                               *
* ENTRY POINT = IECTCQSC - RESET UCBQISCE SERVICE ENTRY         *
*    REGISTERS AT ENTRY:                                        *
*              R7 = UCB BASIC SEGMENT ADDRESS                   *
*              R13 = 18 WORD SAVEAREA ADDRESS (ONLY 2ND WORD    *
*                    OF AREA WILL REMAIN UNCHANGED)             *
*              R14 = RETURN ADDRESS                             *
*                                                               *
*    INPUT =   - THE UCB FOR WHICH UCBQISCE WILL BE SET TO 0    *
*                AND PENDING I/O REQUESTS RESTARTED             *
*                                                               *
*    OUTPUT =   - UCBQISCE SET TO 0 & I/O RESTARTED             *
*                                                               *
*    EXIT-NORMAL = RETURN TO ADDRESS PASSED IN R14              *
*                                                               *
*    EXIT-ERROR = NONE                                          *
*                                                               *
* EXTERNAL REFERENCES = UCB                                     *
*                                                               *
* MACROS = STARTIO                                              *
*                                                               *
* CHANGE ACTIVITY = NONE                                        *
*                                                               *
*****************************************************************
         EJECT
         CVT   DSECT=YES
         EJECT
         IHAPSA
         EJECT
         IECDIOCM
         EJECT
         IECDIOSB
         ORG    IOSEEK+4
IOSCTCCW EQU   *
IOSCSKM  DS     XL1
IOSCSKBB DS     XL2
IOSCSKCC DS     XL2
IOSCSKHH DS     0XL2
IOSCSKH1 DS     XL1
IOSCSKH2 DS     XL1
IOSCSKR  DS     XL1
         EJECT
         IHASRB
         EJECT
         IEFUCBOB LIST=YES,PREFIX=NO
         EJECT
UCBCTC80 EQU   X'80'               ALTERNATE UCB NEEDED FOR CTC
****************************************************************
*                                                               *
*        EQUATES                                                *
*                                                               *
****************************************************************
R0       EQU   0                   REG 0
R1       EQU   1                   REG 1
R2       EQU   2                   REG 2
R3       EQU   3                   REG 3
R4       EQU   4                   REG 4
R5       EQU   5                   REG 5
R6       EQU   6                   REG 6
R7       EQU   7                   REG 7
R8       EQU   8                   REG 8
R9       EQU   9                   REG 9
R10      EQU   10                  REG 10
R11      EQU   11                  REG 11
R12      EQU   12                  REG 12
R13      EQU   13                  REG 13
R14      EQU   14                  REG 14
R15      EQU   15                  REG 15
ZERO     EQU   X'00'
FF       EQU   X'FF'               CONSTANT 256-MASK OF ALL ONES
CTCSCB   EQU   X'14'               CTC CMD CODE-SENSE CMD BYTE
C1       EQU   1                   CONSTANT OF ONE
ATTN     EQU   X'80'               ATTENTION CSW BIT           @ZA05993
         EJECT
IECTCATN CSECT
         LA    R12,0(R15)          INTERNAL ADDRESSABILITY
*                                  AND HI BIT ENTRY SWITCH     @ZA05993
         USING IECTCATN,R12
         B     CTC1                MAIN ENTRY
         SPACE 1
         ENTRY IECTCQSC
IECTCQSC BALR R15,0
         USING *,R15
         ST    R12,0(R13)            SAVE FOR BASE REG         @ZA05993
         LA    R13,8(R13)            IOSGEN MACRO WILL STM 0,F,0(13)--
*                                    ADVANCE SAVEAREA PTR SO BACK SAVE
*                                    PTR WILL NOT BE OVERLAID  @ZA05993
         L    R12,CMAD             ESTABLISH ADDRESSABILITY & ENTRY SW.
         DROP  R15
         USING IECTCATN,R12
         B     ENLAB               BRANCH IOSGEN(UCB@ IN R7)
         SPACE
*****************************************************************
*        INITIALIZATION                                         *
*****************************************************************
CTC1     L     R7,IOSUCB-IOSB(R1)  LOAD UCB ADDRESS
         USING UCBOB,R7
         TM    IOSTSA-IOSB(R1),ATTN     IS ATTENTION ON        @ZA05993
         BNO   ENLAB                 NO--RESET QUISCE & EXIT   @ZA05993
         TM    UCBCTCF1,UCBCTC80   ALTERNATE CTC REQUEST
         BZ    CTC010              NO CONTINUE
         LR    R6,R14              SAVE RETURN REG
         L     R15,UCBCTCAD        GET
         LTR   R15,R15             IS THERE A ROUTINE TO GO TO
         BZ    EXIT                NO-EXIT
         BALR  R14,R15             YES
         LR    R14,R6              ZERO RETURN
         BR    R14                 VECTOR EXIT-4 VECTOR IS LABEL CTC010
         LR    R14,R6              RESTORE RTN ADDR-NORMAL CASE@OZ06060
CTC010   L     R10,UCBCTCAD        LOAD CTC SRB/IOSB-
         USING SRBSECT,R10
         SPACE
*****************************************************************
*        VALIDITY CHECKING                                      *
*****************************************************************
         LTR   R10,R10             Q/UCB INITIALIZED FOR CTC ATTN
         BNZ   CTC0100             YES - INITIALIZE SRB/IOSB
ENLAB    IOSGEN UCBFLG,UCB=(R7),VAR=OFF,TABLE=UCBQISCE
         IOSGEN SIMINT,UCB=(R7),REG=(R9)
         LTR   R12,R12             ENTRY TWO                   @ZA05993
         BP    EXIT                NO BRANCH                   @ZA05993
         LM    R0,R15,0(R13)  RESTORE FROM ENTRY TWO AS SAVED BY IOSGEN
         S     R13,EIGHT           RESTORE TO ORIG. ADDRESS    @ZA05993
         L     R12,0(R13)          RESTORE 12
EXIT     BR    R14                 RETURN
CTC0100  L     R9,SRBPARM          LOAD IOSB ADDRESS
         USING IOSB,R9
         L     R6,IOSUSE           GET LOCK FIELD
         LR    R7,R6               GET COMPARE FIELD
         LA    R7,0(R7)            CLEAR HI-ORDER BIT
         O     R6,INUSE            SET SWAP FIELD
         CS    R7,R6,IOSUSE        LOCK IOSB
         BCR   7,R14               NO - ALREADY IN USE         @YM04963
         OI    IOSOPT,IOSQISCE     TURN ON QUIESCED OPTION
         SPACE 5
*****************************************************************
*        SRB/IOSB INITIALIZATION                                *
*****************************************************************
         NI    IOSFLA,FF-IOSERR-IOSSMDA-IOSSMDB-IOSEX-IOSIOSB
*                                  RESET IOSB
         MVI   IOSFLB,0            RESET IOSB
         MVI   IOSFLC,0            RESET IOSB
         MVI   IOSPROC,0           RESET IOSB
         NI    IOSOPT,FF-IOSAPR    RESET IOSB
         SLR   R0,R0               CLEAR GPR-0
         STH   R0,IOSAPMSK         RESET IOSB
         STH   R0,IOSSNS           RESET IOSB
         ST    R0,IOSIPIB          RESET IOSB
         ST    R0,IOSPCHN          RESET IOSB
         ST    R0,IOSERP           RESET IOSB
*
         ST    R0,SRBSAVE          RESET SRB
*
         LA    R0,C1               SET COUNT
         ST    R0,IOSCTCCW+4       SET CCW COUNT
         LRA   R0,IOSCSKH1
         ST    R0,IOSCTCCW         SET DATA ADDRESS
         MVI   IOSCTCCW,CTCSCB     CCW CMD CODE-CTC SNS CMD BYTE
         LA    R0,IOSCTCCW
         ST    R0,IOSVST           SET VIRTUAL CCW ADDRESS
         LRA   R0,IOSCTCCW
         ST    R0,IOSRST           SET REAL CCW ADDRESS
         EJECT
*****************************************************************
*        LINK TO IOS VIA STARTIO                                *
*****************************************************************
         LR    R6,R14              SAVE RETURN POINTER
         STM   R2,R6,0(R13)        SAVE REGS ACROSS STARTIO
*
         STARTIO SRB=(R10),TCB=SRB
*
         LM    R2,R6,0(R13)        RESTORE REGISTERS
*****************************************************************
*        RETURN TO POST STATUS                                  *
*****************************************************************
         SR    R15,R15
         BR    R6                  RETURN TO POST STATUS
         DS    0F
CMAD     DC    X'80',AL3(IECTCATN) BASE @ AND ENTRY TWO SW     @ZA05993
INUSE    DC    X'80000000'         LOCK BYTE
EIGHT    DC    F'8'                                            @ZA05993
         END
