O25D     TITLE 'IGE0025D - STAT UPDATE ROUTINE'                  S20201
***********************************************************************
*                                                                     *
* MODULE NAME = IGE0025D                                              *
*                                                                     *
* DESCRIPTIVE NAME = STATISTICS UPDATE ROUTINE                        *
*                                                                     *
* COPYRIGHT = NONE                                                    *
*                                                                     *
*0121000120,028500-028600,028800                                 S21021
*        RELEASE 20 DELETIONS                                         *
*016820-16830,027800,035800-036000,036520                        S20201
*        RELEASE 19 DELETIONS                                         *
* 006800,015300,016000,016400                                    19023
* 017200                                                         T314
* STATUS CHANGE LEVEL 010                                             *
*                                                                     *
* FUNCTION =  COMMON ROUTINE USED BY THE ERROR ROUTINES TO UPDATE     *
*              THE DEVICE DEPENDENT STATISTICS TABLE                  *
*                                                                     *
* NOTES = SEE BELOW                                                   *
*                                                                     *
*    DEPENDENCIES = NONE                                              *
*                                                                     *
*    RESTRICTIONS = NONE                                              *
*                                                                     *
*    REGISTER CONVENTIONS = SEE BELOW FOR REGISTER EQUATES            *
*                                                                     *
*    PATCH LABEL = PATCH                                              *
*                                                                     *
* MODULE TYPE = STATISTICS                                            *
*                                                                     *
*    PROCESSOR = ASSEMBLER                                            *
*                                                                     *
*    MODULE SIZE = HEX 16C BYTES                                      *
*                                                                     *
*    ATTRIBUTES = SUPERVISOR STATE, KEY 0, ENABLED, UNLOCKED          *
*                 LINK PACK,PAGEABLE                                  *
*                                                                     *
*                                                                     *
* ENTRY POINT = IECSTATA                                              *
*                                                                     *
*    PURPOSE =                                                        *
*              THE ROUTINE WILL UPDATE THE 4 BITS COUNTERS IN THE     *
*              STATAB. THE BITS IN THE ERP WORK AREA FIELD EWASTUP    *
*              INDICATES WHICH COUNTER TO BE UPDATED                  *
*              WHEN AN OVERFLOW OCCURS THE ROUTINE WILL XCTL TO       *
*              THE SDR ROUTINE                                        *
*                                                                     *
*    LINKAGE = IOSB IN REG 1, ENTRY POINT IN REG 15                   *
*                                                                     *
*    INPUT = IOSB                                                     *
*                                                                     *
*    OUTPUT = NONE                                                    *
*    EXIT-NORMAL =  1. EXCPER, EXIT TO I/O SUPERVISOR TO RETURN IOSB  *
*                 2. SVC 3 EXIT TO RETURN CONTROL TO THE SUPERVISOR   *
*                 AND RELINQUISH CONTROL OVER THE SIRB                *
*              3. EXIT TO OUTBOARD RECORDING (OBR-IGEOO25F)           *
*                 RETURN TO THE I/O SUPERVISOR AND                    *
*                 THE SUPERVISOR WILL BE MADE FROM THIS OR CALLED     *
*                 ROUTINES VIA SVC 15 AND SVC 3 RESPECTIVELY          *
*                 EXIT IS EFFECTED VIA THE ERP XCTL FUNCTION OF       *
*                 IECVERPL (ACCESSED VIA CVTXTLER).                   *
*                                                                     *
*    EXIT-ERROR = NONE                                                *
*                                                                     *
*                                                                     *
*  EXTERNAL REFERENCES = NONE                                         *
*                                                                     *
*  TABLES  :   STATAB(STATISTICS TABLE) OBTAINED VIA CVT              *
*              INDEX TO STATAB OBTAINED FROM UCB'S STATAB FACTOR      *
*                                                                     *
* MACROS = NONE                                                       *
*                                                                     *
***********************************************************************
         EJECT
         CVT   LIST=NO,DSECT=YES
         EJECT
         IECDIOSB
         EJECT
         IECDERWA (NONE)
         EJECT
UCB      DSECT
         IEFUCBOB
         EJECT
IECSTATA CSECT
         SPACE 3
EXCPER   EQU   15                       SVC ERROR EXCP
RETURN   EQU   3                        SVC EXIT
*    REGISTER DEFINITION
IOSBR    EQU   1                        IOSB BASE REGISTER
CVTREG   EQU   3                        CVT REGISTER
WKREG1   EQU   5                        WORK REGISTER
ERREG4   EQU   4                        WORK REG.
ERREG6   EQU   6                        WORK REG.
UCBREG   EQU   7                        UCB ADDR.REG.
ERREGA   EQU   8                        WORK REG. *** USED AS     19023
ERREG9   EQU   9                        WORK REG. *** EVEN/ODD PAIR
EWAR     EQU   10                       EWA ADDR.REG.
ERREGB   EQU   11                       WORK REG.
STAREG   EQU   12                       STAT.TABLE ADDR REG
ERREGC   EQU   13                       WORK REG.
ERRETR   EQU   14                       RETURN REG.
BASREG   EQU   15                       BASE REGISTER             19023
         SPACE 1
*
*    MISCELLANEOUS EQUATES
*
LOC016   EQU   16                       VECTOR TABLE
SERLOG   EQU   256                      LOAD NAME TO LOG OUT RTN.
ZERO     EQU   0                        VALUE OF 0
ONE      EQU   1                        VALUE 0F 1
TWO      EQU   2                        VALUE OF 2
THREE    EQU   3                        VALUE OF 3
FOUR     EQU   4                        VALUE OF 4
SEVEN    EQU   7                        VALUE OF 7
LTINCR   EQU   X'10'                    LEFT HAND COUNTER INCREMENT
RTINCR   EQU   X'01'                    RIGHT HAND COUNTER INCR.
LCNTOFLO EQU   X'F0'                    LEFT HAND COUNTER OVER-
*                                       FLOW MASK
RCNTOFLO EQU   X'0F'                    RIGHT HAND COUNTER OVER-
*                                       FLOW MASK
DATACHK  EQU   X'08'                    EWASTUP DATA CHECK INDICATOR
CMDREJ   EQU   X'80'                    EWASTUP COMMAND REJECT
*                                       INDICATOR
FF       EQU   X'FF'                    NI BYTE MASK
NXT      EQU   2                        NEXT CONTROL ENTRY         256D
TFTYSX   EQU   256                      INCREMENT STAT INDEX       256D
         EJECT
         SPACE 1
*    THIS ROUTINE WILL UPDATE THE 4 BITS COUNTERS IN THE DEVICE
*    DEPENDENT STAT.TABLE.
*    IF AN OVERFLOW OCCURS OR A LOG OUT IS REQUIRED THE ROUTINE WILL
*    XCTL TO SERR.
         SPACE 1
*    PART ONE OF THIS ROUTINE WILL SHIFT THE BITS TO CORRESPOND TO ITS
*    COUNTER LOCATION IN THE STAT. TABLE
*    THE STATISTICS TABLE ENTRY CONSISTS OF 16 4 BIT COUNTERS.
*    THE FIRST TWO COUNTERS ARE THE TEMPORARY READ AND WRITE
*    ERROR COUNTS WHICH ARE UPDATED AS A RESULT OF DATA CHECKS.
*    ASSUMING BITS IN EWASTUP ARE NUMBERED FROM 0 TO 15,
*    THE NEXT 14 COUNTERS ARE ASSIGNED TO EWASTUP BITS
*    1,2,3,5,6,7,8,9,10,11,12,13,14,AND 15 RESPECTIVELY.
         SPACE 1
         USING EWA,EWAR
         USING IOSB,IOSBR
         USING UCBOB,UCBREG
         USING CVT,CVTREG
         USING *,BASREG
         SPACE 2
         L     EWAR,IOSERP              ERP WORK AREA ADDRESS
         L     CVTREG,LOC016            GET CVT ADDRESS           19023
         NI    EWAFLG2,FF-EWACOVF       OVER FLOW BIT TO ZERO
         L     UCBREG,IOSUCB            LOAD UCB ADDRESS
         CLI   UCBTBYT3,UCB3TAPE        TEST FOR TAPE
         BE    STA050                   YES-EXIT PROCESS          19023
         CLI   UCBTBYT3,UCB3DACC        TEST FOR DIRECT ACCESS
         BE    STA050                   YES-EXIT PROCESS
         L     STAREG,CVTSTB            GET ADDR TO STAT TABLE    19023
         SLR   ERREG4,ERREG4
         L     WKREG1,UCBEXTPT          LOAD EXTENSION ADDRRESS     UCB
         USING UCBCMEXT,WKREG1
         IC    ERREG4,UCBSTI            GET INDEX TO STAT. TABLE
         DROP  WKREG1
         LR    WKREG1,STAREG            GET STAT. TAB START ADR    256D
ERRLOOP1 CLM   UCBREG,THREE,ZERO(WKREG1) DEV SUP BY THIS SEC   @Z30ANLG
         BL    ERRLOOP2                 YES,INCR ADJUSTED
         LA    WKREG1,NXT(WKREG1)       INCR TO NXT CTL SECTION    256D
         LA    ERREG4,TFTYSX(ERREG4)    INCR STAT INDEX TO NXT SECT256D
         B     ERRLOOP1                 LOOP TO CHECK THIS SECT    256D
         SPACE 1
ERRLOOP2 MH    ERREG4,ERRC10            MULTIPLY BY 10             256D
         AR    STAREG,ERREG4            GET PROPER ENTRY
         SPACE 1
*    UPDATE DATA CHECK ERROR COUNT
         TM    EWASTUP,DATACHK          TEST IF DATA CHECK BIT IS ON
         BZ    STA005                   BR-ON NO- BYPASS UPDATE DC CNT.
         SPACE 1
         IC    ERREG4,ZERO(STAREG)      GET DATA CHECK COUNTERS
         TM    EWAFLG2,EWAWTEMP         IS THIS WRIT ERROR
         BO    STA002                   BR-ON YES
         SPACE 1
*    UPDATE TEMPORARY READ ERROR COUNT.
         LA    ERREG4,LTINCR(ERREG4)    INCREMENT COUNT
         STC   ERREG4,ZERO(STAREG)      STORE UPDATED COUNT.
         TM    ZERO(STAREG),LCNTOFLO    TEST FOR OVER FLOW
         BO    STA003                   BR-ON YES
         B     STA005                   GO INSPECT COUNTERS
         SPACE 1
STA002   LA    ERREG4,RTINCR(ERREG4)    INCREMENT WRITE CNT.
         STC   ERREG4,ZERO(STAREG)      STORE UPDATED CNT.
         TM    ZERO(STAREG),RCNTOFLO    TEST FOR OVER FLOW
         BNO   STA005                   BR-ON NO
STA003   OI    EWAFLG2,EWACOVF          INDICATE OVER FLOW
         SPACE 1
*    SHIFT BITS IN BYTE 1 TO CORRECT POSITION
STA005   NI    EWAFLG2,FF-EWAWTEMP      CLEAR TEMP R/W INDICATOR
         NI    EWASTUP,FF-CMDREJ-DATACHK CLEAR THESE BITS
         SLR   ERREGA,ERREGA            ZERO REGISTER
         IC    ERREGA,EWASTUP           1ST BYTE OF INCR INDICATORS
         SRDL  ERREGA,FOUR              REALIGN TO FORMAT OF -
         SLL   ERREG9,ONE               *  00123567
         SLDL  ERREGA,THREE             *
         STC   ERREGA,EWASTUP           STORE UPDATED COUNT
         SPACE 1
         LA    WKREG1,EWASTUP+ONE       TEST BITS IN THIS BYTE FIRST
         LA    ERREG9,TWO               INITIALIZE REG.
         LA    ERREG4,SEVEN             INITIALIZE POINTER TO STAT CNT
STA008   LA    ERREGB,ONE               INITIALIZE BIT TESTING
         LA    ERREGC,FOUR
         SPACE 1
STA010   EQU   *
         EX    ERREGB,STA030            TEST BITS
         BO    STA015                   BR-IF ON TO UPDATE CNT.
STA011   SLA   ERREGB,ONE               GET NEXT BIT TO BE TESTED
         EX    ERREGB,STA030            EXECUTE TEST FOR BIT ON
         BO    STA020                   BR-IF ON TO UPDATE CNT.
STA00A   SLA   ERREGB,ONE               GET NEXT BIT TO BE TESTED
         BCTR  ERREG4,0                 DECREMENT POINTER TO STAT. CNT.
STA00B   BCT   ERREGC,STA010            TEST IF BYTE TEST COMPLETE
         LA    WKREG1,EWASTUP           TEST BITS IN THIS BYTE NOW
         BCT   ERREG9,STA008            TEST IF UPDATE COMPLETE
         SPACE 1
         XC    EWASTUP,EWASTUP          CLER STAT ENTRY WORK     S20201
STA050   LA    ERREGC,SERLOG            PREPARE FOR XCTL TO OBR  S20201
         TM    IOSFLB,IOSLOG            IS LOG OUT REQUIRED
         BO    STA012                   YES - GO TO SERR           CCHA
STA00B1  TM    EWAFLG2,EWACOVF          TEST IF OVERFLOW BIT IS  S21021
*                                       ON                       S21021
         BZ    STA040                   BRANCH NO, RETURN TO IOS
*   TRANSFER CONTROL TO SERR                                       CCHA
         SPACE 1
STA012   TM    IOSFLB,IOSSDR       CHECK IF ERP WANTS NO OBR   @Y40MPLG
         BNO   STA014              NO, THEN GO TO OBR          @Y40MPLG
         NI    IOSFLB,FF-IOSLOG    TURN OFF IOSLOG & SKIP OBR  @Y40MPLG
         B     STA040              EXIT VIA SVC15, SVC3        @Y40MPLG
STA014   L     ERRETR,CVTXTLER     GET ADDR TO XCTL ROUTINE    @Y40MPLG
         BR    ERRETR                   BR- TO XCTL ROUTINE
         SPACE 1
         SPACE 1
*    THIS CODE WILL UPDATE THE RIGHT HAND COUNTERS IN STAT.TABLE
STA015   EQU   *
         AR    ERREG4,STAREG
         IC    ERREG6,ZERO(ERREG4)      GET CURRENT COUNT
         TM    ZERO(ERREG4),RCNTOFLO    COUNTERS WAITING FOR OBR
         BO    STA016                   YES,SKIP UPDATE
         LA    ERREG6,RTINCR(ERREG6)    ADD ONE TO COUNT
         STC   ERREG6,ZERO(ERREG4)      STORE UPDATED COUNT
         TM    ZERO(ERREG4),RCNTOFLO    TEST FOR OVER FLOW
         BNO   STA016                   BR-ON NO
         OI    EWAFLG2,EWACOVF          INDICATE OVERFLOW
STA016   SLR   ERREG4,STAREG            UPDATE TO TEST NEXT BIT
         B     STA011                   BR-TO TEST NEXT BIT
         SPACE 1
*    THIS CODE WILL UPDATE THE LEFT HAND COUNTERS IN STAT.TABLE
STA020   EQU   *
         AR    ERREG4,STAREG
         IC    ERREG6,ZERO(ERREG4)      GET CURRENT COUNT
         TM    ZERO(ERREG4),LCNTOFLO    COUNTER WAITING FOR OBR
         BO    STA021                   YES - SKIP THIS UPDATE
         LA    ERREG6,LTINCR(ERREG6)    ADD ONE TO COUNT
         STC   ERREG6,ZERO(ERREG4)      STORE UPDATED COUNT
         TM    ZERO(ERREG4),LCNTOFLO    TEST FOR OVER FLOW
         BNO   STA021                   BR-ON NO
         OI    EWAFLG2,EWACOVF          INDICATE OVERFLOW
         SPACE 1
STA021   SLR   ERREG4,STAREG            UPDATE TO TEST NEXT BIT
         B     STA00A                   BR-TO TEST NEXT BIT
         SPACE 1
STA030   TM    ZERO(WKREG1),ZERO        TEST FOR BIT SETTING
         SPACE 1
STA040   SVC   EXCPER                   ISSUE SVC 15 TO IOS
         SVC   RETURN                   ISSUE EXIT SVC
         SPACE 2
ERRC10   DC    AL2(10)                  DECIMAL 10
PATCH    DC    5F'0'                    PATCH AREA
         END
