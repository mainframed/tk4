GCIV     TITLE 'IKJEGCIV - TSO TEST - COMMAND INVOKER'
         COPY  IKJEGSIO
IKJEGCIV CSECT
*C300000                                                        YA01840
*A300500                                                        YA01840
         EJECT
***********************************************************************
*                                                                     *
* STATUS --                                                           *
*    CHANGE LEVEL 00, VERSION 02, VS2   02/28/73                      *
*                                                                     *
* FUNCTION --                                                         *
*                                                                     *
*    THIS SERVICE ROUTINE OF TEST PERFORMS A BLDL ON THE COMMAND      *
*    SPECIFIED TO DETERMINE IF THE COMMAND EXISTS.  IF THE COMMAND    *
*    EXISTS, IT IS ATTACHED AS A SUBTASK OF TEST.  CIV WAITS FOR      *
*    THE COMMAND TO COMPLETE.  IT THEN UNALLOCATES ALL DATA SETS      *
*    GOTTEN BY THE COMMAND AND DETACHS THE SUBTASK.                   *
*                                                                     *
* ENTRY POINTS --                                                     *
*         IKJEGCIV    ENTERED FROM IKJEGMNL                           *
*                                                                     *
* INPUT -- A POINTER TO TCOMTAB, THE COMMUNICATION TABLE, IS EXPECTED *
*    IN REGISTER 9  ALL INFORMATION NEEDED CAN BE ACCESSED IN TCOMTAB.*
*    THE ECT CONTAINS THE NAME OF THE SUBCOMMAND TO BE ATTACHED.      *
*                                                                     *
* OUTPUT -- DIAGNOSTIC MESSAGES ON CERTAIN ERRORS.                    *
*                                                                     *
* EXTERNAL REFERENCES --                                              *
*         IKJDAIR IS INVOKED VIA A LINK MACRO INSTRUCTION TO          *
*         UNALLOCATE ALL DATA SETS ALLOCATED BY THE SUBTASK.          *
*                                                                     *
*         IKJEGIO IS INVOKED VIA IKJEGSIO MACRO TO PUT ERROR          *
*         MESSAGES TO THE TERMINAL USER.                              *
*                                                                     *
*         ESTAE IS CALLED TO ESTABLISH AN ABNORMAL TERMINATION EXIT.  *
*                                                                     *
*         FREEMAIN IS CALLED TO FREE CORE USED FOR A TERMINAL         *
*         INPUT BUFFER.                                               *
*                                                                     *
*         BLDL IS CALLED TO DETERMINE IF THE COMMAND SPECIFIED        *
*         IS VALID.                                                   *
*                                                                     *
*         ATTACH IS CALLED TO CREATE THE SUBTASK TO PROCESS THE       *
*         COMMAND.                                                    *
*                                                                     *
*         WAIT IS CALLED TO WAIT ON NOTIFICATION THAT ONE OR MORE     *
*         EVENTS HAS COMPLETED.                                       *
*                                                                     *
*         DETACH IS CALLED TO TERMINATE THE SUBTASK CREATED TO        *
*         PROCESS THE COMMAND.                                        *
*                                                                     *
* EXITS, NORMAL -- NORMAL EXIT IS BACK TO IKJEGMNL.                   *
*         RETURN IS ACCOMPLISHED VIA AN SVC 3 INSTRUCTION             *
*         WHICH IS POINTED TO BY R14.                                 *
*          RETURN CODES ARE:                                          *
*           0-IKJEGCIV COMPLETED NORMALLY                             *
*          16-ATTENTION HAS BEEN SCHEDULED                            *
*                                                                     *
* EXITS, ERROR -- ERROR EXIT IS BACK TO IKJEGMNL                      *
*          RETURN IS ACCOMPLISHED VIA AN SVC 3 INSTRUCTION            *
*          WHICH IS POINTED TO BY R14.                                *
*           RETURN CODES ARE:                                         *
*           20-ESTAE RETRY ROUTINE WAS ENTERED                        *
*           24-ESTAE MACRO ERROR                                      *
*                                                                     *
* TABLES/WORK AREAS --                                                *
*    THE WORKAREA POINTED TO IN TCOMTAB IS USED FOR ALL TEMPORARY     *
*    STORAGE                                                          *
*                                                                     *
* ATTRIBUTES --                                                       *
*    REFRESHABLE AND REENTRANT                                        *
*                                                                     *
* CHARACTER CODE DEPENDENCY --                                        *
*    REASSEMBLY MAY BE NEEDED FOR CHANGE OF CODE                      *
* NOTES --                                                            *
*    R12 IS BASE REG                                                  *
***********************************************************************
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
X0       EQU   0              VALUE = 0.
X1       EQU   1              VALUE = 1.
X2       EQU   2              VALUE = 2.
X3       EQU   3              VALUE = 3.
X4       EQU   4              VALUE = 4.
X5       EQU   5              VALUE = 5.
X7       EQU   7              VALUE = 7.
X8       EQU   8              VALUE = 8.
X12      EQU   12             VALUE = 12.
X16      EQU   16             VALUE = 16.
XFF      EQU   X'FF'          MASK X'FF'
CVTPTR   EQU   16             ADDRESS OF CVT POINTER
CIVSP078 EQU   78             SUBPOOL 78 ID
CIVENDL  EQU   X'80'          END OF LIST INDICATOR FOR WAIT.
CIVTSTAT EQU   X'48'          POST CODE FROM TEST ATTENTION EXIT.
CIVWAIT  EQU   X'80'          ECB FLAG - AWAITING COMPLETION
CIVPOST  EQU   X'40'          POST CODE FROM STOP/MODIFY.
CIVRC016 EQU   16             RETURN CODE 16.
CIVRC020 EQU   20             RETURN CODE 20.
CIVRC028 EQU   28             RETURN CODE 28.
N28      EQU   28             DISPLACEMENT IN SAVE AREA TO R2
CIVBLANK EQU   C' '           CHARACTER BLANK.
CIVCZERO EQU   C'0'           CHARACTER ZERO.
CIVZONE  EQU   X'0F'          PRINTABLE ZONE IN PACKED DECIMAL
M0269    EQU   269           MESSAGE NUMBER M0269
M0270    EQU   270           MESSAGE NUMBER M0270
I0041    EQU   41            INSERT NUMBER 41
I0042    EQU   42            INSERT NUMBER 42
RC24     EQU   24            RETURN CODE 24
         EJECT
         SAVE  (14,12),,*     SAVE ENTRY REGISTERS.
         LR    R12,R15        ESTABLISH A BASE REGISTER FOR THIS
         USING IKJEGCIV,R12   MODULE.
         USING TCOMTAB,R9     ESTABLISH ADDRESSABILITY TO TCOMTAB
         L     R15,REGSAVE2   ADDRESS OF AVAILABLE SAVE AREA.
         ST    R13,X4(,R15)   CROSS-CHAIN CURRENT AND PREVIOUS
         ST    R15,X8(,R13)   SAVE AREAS.
         LR    R13,R15        PUT SAVE AREA ADDRESS IN STANDARD
*                             REGISTER.
         SPACE 5
CIV01010 EQU   *
         L     R10,WORKAREA   ADDRESS OF GENERAL TEST WORKAREA.
         USING TSTCWORK,R10   ESTABLISH ADDRESSABILITY TO
*                             WORKAREA.
         LA    R10,CWORKCMD   ADDRESS OF SUBCOMMAND PORTION OF
*                             WORKAREA.
         DROP  R10
         USING CIVWORKA,R10   ESTABLISH ADDRESSABILITY TO THE
*                             COMMAND INVOKER'S WORKAREA.
         SPACE 5
         MVI   CIVFLGS,X0     CLEAR FLAG BYTE
         XC    CIVSTAE,CIVSTAE CLEAR ESTAE PARM LIST
         L     R15,TSTSTAE   R15=ADDR OF ESTAE EXIT ROUTINE
         ESTAE (R15),CT,PARAM=CIVSPL,XCTL=NO,RECORD=YES,               *
               MF=(E,CIVSTAE) ESTABLISH ESTAE EXIT
         LTR   R15,R15        TEST FOR SUCCESSFUL COMPLETION.
         BZ    CIV02010       RETURN CODE = 0, BRANCH TO FREE
*                             AND EXCHANGE BUFFERS.
         STC   R15,TSTESTRC  SAVE ESTAE RETURN CODE
         LA    R15,RC24      SET RETURN CODE TO 24
         B     CIV07030      RETURN TO MAINLINE
         SPACE 3
IORC1    EQU   *
         CH    R15,ATTNCODE  IS RC=16,20,24
         BNL   CIV07030      YES,RETURN WITH RC
         B     CIV07010      ZERO RC AND RETURN
         EJECT
* FREE COMMAND BUFFER
CIV02010 EQU   *
         L     R11,TPLPTR     ADDRESS OF TPL.
         USING TPL,R11        ESTABLISH ADDRESSABILITY TO TPL.
         L     R1,TPLCBUF     ADDRESS OF COMMAND BUFFER.
         LH    R0,X0(,R1)     LENGTH OF COMMAND BUFFER.
         LTR   R0,R0          DETERMINE IF LENGTH IS ZERO.
         BZ    CIV03010       IF ZERO, NOTHING TO BE FREED.
         FREEMAIN RC,LV=(0),A=(1),SP=1 FREE COMMAND BUFFER.
         LTR   R15,R15       WAS FREEMAIN SUCCESSFUL?
         BZ    CIV03010      YES,CONTINUE
         XC    TSTIOPRM(L'TSTIOPRM),TSTIOPRM CLEAR IO PARM LIST
* ISSUE 'FREEMAIN SVC ERROR' MESSAGE
         IKJEGSIO SVCERR,SVC=120,RC=(R15),ID=CIV07,MF=(E,TSTIOPRM)
* IGNORE IO RETURN CODES AND CONTINUE
         SPACE 5
* SUBSTITUTE @ INBUF IN TPL
CIV03010 EQU   *
         MVC   TPLCBUF(L'TPLCBUF),INBUF PLACE BUFFER ADDRESS IN
*                             TPL.
         XC    INBUF(L'INBUF),INBUF SHOW NO SUBCOMMAND BUFFER TO
*                             BE FREED.
         L     R2,TPLECT      ADDRESS OF ECT.
         USING ECT,R2         ESTABLISH ADDRESSABILITY TO ECT.
         MVC   CIVBLDLC(L'ECTSCMD),ECTSCMD MOVE COMMAND INTO
*                             BLDL LIST.
         MVC   CIVBLDLH(L'CIVBLDLH),CIVBLDLW INITIALIZE LENGTH OF
*                             AND NUMBER OF ENTRIES IN BLDL LIST.
         BLDL  0,CIVBLDLH     DETERMINE IF COMMAND IS VALID.
         LTR   R15,R15        FOR RETURN CODE = 0, BRANCH TO
         BZ    CIV04010       ATTACH COMMAND.
*
         XC    TSTIOPRM(L'TSTIOPRM),TSTIOPRM CLEAR IO APRM LIST
* ISSUE 'COMMAND NOT FOUND' MESSAGE
         IKJEGSIO MSG,FIRST=M0067,ID=CIV07,MF=(E,TSTIOPRM)
         B     IORC1         TEST IO RC'S
         EJECT
* ATTACH NEW SUBTASK
CIV04010 EQU   *
         MVC   CIVECTCM(L'CIVECTCM),ECTPCMD SAVE PROBLEM PROGRAM
         MVC   ECTPCMD(L'ECTPCMD),CIVCTEST NAME AND SUBSTITUTE
*                             WITH TEST.
         L     R3,ECBTERM     OBTAIN ADDRESS OF TERMINATION ECB
         LA    R5,ECTSCMD     ADDRESS OF COMMAND PROCESSOR NAME.
         XC    X0(X4,R3),X0(R3) AND ZERO IT.
         MVC   CIVATCHL(CIVATTLN),CIVATTCH INITIALIZE ATTACH
*                             PARAMETER LIST.
         LA    R15,CIVATCHL   ADDRESS OF SUPERVISOR PARAMETERS.
         MVC   CIVCPPL(CIVECT+L'CIVECT-CIVBUF),TPLCBUF BUILD
         LA    R1,CIVCPPL     TMP-LIKE PARAMETER LIST.
         LA    R4,CIVSTAIX    ADDRESS OF STAI EXIT FOR NEW
*                             SUBTASK.
         LA    R15,CIVATCHL   ADDRESS OF COMMAND TO BE ATTACHED.
         ATTACH EPLOC=(R5),ECB=(R3),STAI=((R4)),MF=(E,(1)),     YA01840*
               SF=(E,(15))                                      YA01840
         LTR   R15,R15        TEST FOR SUCCESSFUL COMPLETION.
         BZ    CIV04030       RETURN CODE = 0, BRANCH TO SAVE
*                             TCB ADDRESS.
         XC    TSTIOPRM(L'TSTIOPRM),TSTIOPRM CLEAR IO PARM LIST
* ISSUE 'INSUFFICIENT CORE' MESSAGE
         IKJEGSIO MSG,FIRST=M0103,ID=CIV07,MF=(E,TSTIOPRM)
         B     IORC1         TEST IO RETURN CODES AND EXIT
         SPACE 5
CIV04030 EQU   *
         ST    R1,TSTHTCB     SAVE TCB ADDRESS FOR DETACH.
         OI    TSTFLGS3,TSTHELP INDICATE THAT A SUBTASK EXISTS.
         MVI   ECBLOG,CIVENDL INDICATE END OF ECB LIST.
         WAIT  ECBLIST=ECBTERM WAIT ON COMMAND PROCESSOR TO            &
                              COMPLETE.
         SPACE 5
         MVC   CIVDAP2C(CIV2CLEN),CIV2CBLK INITIALIZE DAIR
         MVC   CIVDAP2C+DA2CTCB-DAPB2C(L'DA2CTCB),TSTHTCB PARAMETER
*                             BLOCK.
         MVC   CIVDAPL(DAPLPSCB-DAPL),PARMLIST INITIALIZE DAIR
         L     R0,TPLPSCB     PARAMETER LIST.
         LA    R1,CIVDAP2C
         STM   R0,R1,CIVDPSCB
         LA    R1,CIVDAPL     ADDRESS OF DAIR PARAMETER LIST.
         LINK  EP=IKJDAIR,MF=(E,(1)),ERRET=DAIRFAIL INVOKE DAIR TO     *
                              UNALLOCATE TCB'S
         LTR   R0,R15         LOAD R0 WITH RETURN CODE AND TEST
         BZ    CIV05010       IF DAIR WAS SUCCESSFUL, DETACH SUBTASK.
         LA    R1,CIVDAPL    GET ADDRESS OF DAPL
         ST    R1,DFDAPLP    STORE IT IN DAPL POINTER
         LA    R1,DFRC       GET ADDRESS OF DAIR RC AREA
         ST    R1,DFRCP      STORE IT IN DAIR RC POINTER
         ST    R15,DFRC      STORE DAIR RETURN CODE
         LA    R1,DFMWEA     GET ADDRESS OF MESSAGE WRITER AREA
         ST    R1,DFMWEAP    STORE IT IN MESSAGE WRITER POINTER
         XC    DFMWEA,DFMWEA ZEROS MEANS NO MESSAGE WRITER
         LA    R1,DFCID      GET ADDRESS OF CALLER ID NO.
         ST    R1,DFCIDP     STORE IT IN CALLER ID NO. PTR
         LA    R1,X1         R1=F'1' CALLER ID NUMBER
         STH   R1,DFCID      STORE CALLER ID NUMBER
         LA    R1,DFDAPLP    R1=ADDRESS OF DAIR FAIL PARM LIST
* INVOKE DAIR FAIL ERROR HANDLER
         LINK  EP=IKJEFF18,MF=(E,(1)),ERRET=JEFFAIL
* IGNORE RETURN CODES AND CONTINUE
         EJECT
CIV05010 EQU   *
         LA    R1,TSTHTCB     ADDRESS OF TCB POINTER TO BE DETACHED.
         OI    CIVFLGS,CIVDTACH  INDICATE DETACH IN PROGRESS.
*                             IF THE SUBTASK TERMINATED BEFORE IT
*                             WAS COMPLETE, THE DETACH WILL ABEND.
*                             IF THIS HAPPENS, THE STAE RETRY
*                             WILL REINSTATE THE STAE AND
*                             RESTART FOLLOWING THE DETACH.
         DETACH (1),STAE=YES  DETACH SUBTASK.
BLOW1    EQU   *              POSSIBLE ABEND HERE
CIV05020 NI    CIVFLGS,XFF-CIVDTACH    TURN OFF DETACH FLG
         SPACE 5
*
*        DETERMINE TYPE OF SUB-TASK TERMINATION.
*
         NI    TSTFLGS3,XFF-TSTHELP INDICATE THAT SUBTASK DETACHED.
         L     R1,ECBTMPA     ADDRESS OF ATTENTION ECB.
         TM    X0(R1),CIVTSTAT DETERMINE IF TEST OR TMP POSTED
*                             ATTENTION ECB.
         BNZ   CIV06010       IF ECB POSTED, BRANCH TO RETURN TO
*                             IKJEGMNL WITH ATTENTION RETURN CODE.
         SPACE 5
         L     R1,ECBTERM    R1=ADDR OF ECB
         OC    X1(X2,R1),X1(R1) DID MODULE ABEND
         BZ    CIV05030      NO,CHECK FOR STOP/MODIFY
         L     R4,OUTBUF     R4=ADDR OF OUTPUT BUFFER
         MVC   X0(X4,R4),CMDLCON MOVE INSERT LENGTH TO OUTBUF
         MVC   X4(L'ECTPCMD,R4),ECTPCMD MOVE IN COMMAND NAME
         LR    R3,R4         R3=ADDR OF OUTBUF
         AH    R3,CMDLCON    R3=ADDR OF OUTBUF+12
         MVC   X0(X4,R3),INSTLCON MOVE INSERT LENGTH TO OUTBUF
         L     R5,X0(R1)     PUT ECB INTO R5
         LA    R5,X0(R5)     CLEAR HIGH ORDER BYTE
         LR    R6,R5         SAVE R5 FOR DESTRUCTIVE TEST
         N     R5,CIVSYSCD   WAS IT USER COMPLETION CODE
         BZ    CIV05170      YES,GO PROCESS IT
         SRL   R6,X16        SHIFT SYS COMPLETION CODE
         STH   R6,X4(R3)     STORE IT IN INSERT AREA
         LA    R6,M0269      R6=MESSAGE NUMBER
         B     CIV05180      GO OUTPUT MESSAGE
CIV05170 EQU   *
         STH   R5,X4(R3)     STORE COMPLETION CODE IN INSERT
         LA    R6,M0270      R6=MESSAGE NUMBER
CIV05180 EQU   *
         XC    TSTIOPRM(L'TSTIOPRM),TSTIOPRM CLEAR IO PARM LIST
* ISSUE 'MODULE ENDED DUE TO ERROR' MESSAGE
* ISSUE 'ABEND CODE IS' MESSAGE
         IKJEGSIO MSG,FIRST=M0068,INST11=(ADDR,(R4)),SECOND=(R6),      *
               INST21=(ADDRCONX,(R3)),ID=CIV07,MF=(E,TSTIOPRM)
         B     IORC1         TEST IO RC'S
         SPACE 5
CIV05030 EQU   *
         L     R1,ECBLOG      ADDRESS OF STOP/MODIFY ECB.
         TM    X0(R1),CIVPOST IS ECB POSTED.
         BZ    CIV07010       IF NOT POSTED, BRANCH TO EXIT.
         LA    R15,CIVRC028   LOAD LOGOFF RETURN CODE.
         B     CIV07030       BRANCH TO EXIT.
         SPACE 5
CIV06010 EQU   *
         LA    R15,CIVRC016   LOAD ATTENTION RETURN CODE.
         B     CIV07030       BRANCH TO EXIT.
         SPACE 5
CIV07010 EQU   *
         SR    R15,R15        LOAD COMPLETION RETURN CODE.
CIV07030 EQU   *
         L     R1,ECBTERM     ZERO TERMINATION
         XC    X0(X4,R1),X0(R1)        ECB
         MVC   ECTPCMD(L'ECTPCMD),CIVECTCM RESTORE PROBLEM
*                             PROGRAM NAME TO ECT.
         LR    R2,R15        SAVE RETURN CODE
         ESTAE 0             REMOVE ESTAE
         LR    R15,R2        RESET RETURN CODE
         NI    TSTFLGS4,XFF-TSTRERTN TURN OFF RETRY BIT
         L     R13,X4(,R13)   RESTORE HIGHER SAVE AREA ADDRESS.
         RETURN (14,12),,RC=(15) RETURN.
         EJECT
*
* ERRET'S FOR LINKS
*
DAIRFAIL EQU   *             LINK TO IKJDAIR ERRET
         LA    R3,I0041      INSERT MODULE NAME IKJDAIR
         B     JEFFAIL1      GO ISSUE MESSAGE
JEFFAIL  EQU   *             LINK TO IKJEFF18 ERRET
         LA    R3,I0042      INSERT MODULE NAME IKJEFF18
JEFFAIL1 EQU   *
         XC    TSTIOPRM(L'TSTIOPRM),TSTIOPRM CLEAR IO PARM LIST
         LR    R0,R1         LOAD ABEND CODE INTO R0
* ISSUE 'LINK TO MODULE NAME SVC ERROR' MESSAGE
         IKJEGSIO SVCERR,SVC=6,ABENDRG=(R0),RC=(R15),INST11=(NUM,(R3)),*
               ID=CIV07,MF=(E,TSTIOPRM)
         C     R15,ATTNCODE  IS RETURN CODE 16,20 OR 24?
         BL    CIV05010      NO,CONTINUE AFTER DAIR LINKS
         B     CIV07030      YES, SEND RETURN CODE TO MAINLINE
         EJECT
* STAI EXIT FOR SUBTASK
CIVSTAIX EQU   *
         LA    R15,CIVRC016   SET STAI RETURN CODE TO INDICATE
*                             CONTINUATION OF ABEND PROCESSING
*                             FOR SUBTASK.
         BR    R14            RETURN TO ABEND.
         EJECT
* ESTAE RETRY ROUTINE
CIVSTAEX EQU   *
         USING *,R15          ESTABLISH ADDRESSABILITY
         L     R12,BASEAD     *
         DROP  R15            *
         L     R9,CVTPTR      CVTPTR
         L     R9,X0(R9)      NEW/CURRENT WORDS.
         L     R9,X4(,R9)     CURRENT TCB.
         L     R9,TCBTRN-TCB(R9)  TCOMTAB ADDRESS.
         L     R13,REGSAVE2   RESET R13
         LM    R2,R11,N28(R13) RESTORE REGISTERS
*
         TM    CIVFLGS,CIVDTACH DID DETACH FAIL?
         BZ    CIVSTAE2       NO. SET R.C. AND EXIT
         SPACE 1
CIVSTAE1 EQU   *
         XC    CIVSTAE,CIVSTAE CLEAR ESTAE PARM LIST
         L     R15,TSTSTAE   R15=ADDR OF ESTAE EXIT ROUTINE
         ESTAE (R15),OV,PARAM=CIVSPL,XCTL=NO,RECORD=YES,               *
               MF=(E,CIVSTAE) REISSUE ESTAE
         LTR   R15,R15       WAS ESTAE SUCCESSFUL?
         BNZ   CIVSTAE2      NO, SET RETURN CODE AND EXIT
         NI    TSTFLGS4,XFF-TSTRERTN TURN OFF RETRY BIT
         B     CIV05020      CONTINUE AFTER DETACH
         SPACE 1
CIVSTAE2 LA    R15,CIVRC020   SET ESTAE RETURN CODE.
         B     CIV07030       BRANCH TO EXIT.
         EJECT
*
*        CONSTANTS AND AREAS.
*
CIVBLDLW DS    0F             ALIGN ON WORD BOUNDARY.
         DC    AL2(1)         NUMBER OF ENTRIES IN LIST.
         DC    AL2(12)        LENGTH OF EACH ENTRY IN LIST.
CIVCTEST DC    CL8'TEST'      TEST COMMAND NAME.
ATTNCODE DC    H'16'         ATTENTION RETURN CODE
CMDLCON  DS    0F            LENGTH OF MODULE NAME INSERT
         DC    H'12'
         DC    H'00'
INSTLCON DS    0F            LENGTH OF COMPLETION CODE INSERT
         DC    H'6'
         DC    H'00'
CIVSPL   IKJEGSPL RTRY=CIVSTAEX,ABNTB=ICTAB,MODNM=IKJEGCIV,TNM=CIV
         SPACE 1
CIVATTCH ATTACH DCB=0,SHSPV=CIVSP078,SZERO=NO,SF=L ATTACH              &
                              PARAMETER LIST.
CIVATTLN EQU   *-CIVATTCH     PARAMETER LIST LENGTH.
         SPACE 1                                                  MSGYM
         SPACE 1
CIVSYSCD DC    XL4'00FFF000'  MASK USED TO DETERMINE IF A USER
*                             ABEND OCCURRED.
         SPACE 1
CIVTRTAB DC    C'0123456789ABCDEF' TRANSLATE TABLE.
         SPACE 1
BASEAD   DC    A(IKJEGCIV)    ENTRY POINT ADDR FOR ADDR'BILITY
         SPACE 1
ICTAB    DC    A(BLOW1)       IC TABLE FOR IKJEGSTA
         DC    X'FF'          * END OF TABLE
         EJECT
         IKJDAP2C
CIV2CLEN EQU   *-DAPB2C       PARAMETER BLOCK LENGTH.
IKJEGCIV CSECT
CIV2CBLK DC    XL16'0'        INITIALIZE BLOCK TO ZERO.
         ORG   DA2CCD-DAPB2C+CIV2CBLK ORIGIN TO ENTRY CODE.
         DC    XL2'002C'      ENTRY CODE.
         ORG   ,              ORIGIN TO END OF CONSTANTS.
         EJECT
         TCOMTAB
         EJECT
         TSTCWORK
         SPACE 2
***********************************************************************
*                                                                     *
*        COMMAND INVOKER WORK AREA DUMMY SECTION                      *
*                                                                     *
***********************************************************************
         SPACE 2
CIVWORKA DSECT
CIVSTAE  DS    CL16           ESTAE PARAMETER LIST.
CIVBLDLH DS    F              BLDL LIST CONTROL WORD.
CIVBLDLC DS    CL12           MEMBER NAME TO BE SEARCHED FOR.
CIVECTCM DS    CL8            SAVE AREA FOR COMMAND NAME FROM ECT
CIVFLGS  DS    X              FLAG BYTE
CIVDTACH EQU   X'80'          FLAG TO INDICATE DETACH TO STAE RETRY
CIVCONT  EQU   X'40'          FLAG TO INDICATE CONTINUED PROCESSING
CIVATCHL DS    15F            SUPERVISOR PARAMETER LIST - ATTACH.
CIVCPPL  DS    0F             DUMMY COMMAND PARAMETER LIST.
CIVBUF   DS    F              ADDRESS OF INPUT BUFFER.
CIVUPT   DS    F              ADDRESS OF UPT.
CIVPSCB  DS    F              ADDRESS OF PSCB.
CIVECT   DS    F              ADDRESS OF ECT.
CIVDAP2C DS    4F             PARAMETER BLOCK FOR IKJDAIR.
         ORG   CIVATCHL       RE-ORIGIN TO OVERLAY WORK AREA.
CIVDAPL  DS    0F             PARAMETER LIST FOR IKJDAIR.
CIVDUPT  DS    F              ADDRESS OF UPT.
CIVDECT  DS    F              ADDRESS OF ECT.
CIVDECB  DS    F              ADDRESS OF ECB.
CIVDPSCB DS    F              ADDRESS OF PSCB.
CIVDAPB  DS    F              ADDRESS OF DAIR PARAMETER BLOCK.
CIVCCODE DS    D              BINARY TO DECIMAL CONVERSION AREA.
CIVMTEXT DS    F              COMPLETION CODE FOR MESSAGE.
DFDAPLP  DS    F             DAIR PARM LIST POINTER
DFRCP    DS    F             DAIR RETURN CODE POINTER
DFMWEAP  DS    F             MESSAGE WRITER POINTER
DFCIDP   DS    F             CALLER ID POINTER
DFRSVRD  DS    F             RESERVED
DFRC     DS    F             DAIR RETURN CODE
DFMWEA   DS    F             MESSAGE WRITER ADDRESS=ZEROS
DFCID    DS    H             CALLID NUMBER=1
         EJECT
         IKJTPL
         EJECT
         IKJECT
         EJECT
         IKJDAPL
         EJECT
         IKJTCB
         EJECT
IKJEGCIV CSECT
         DS    0F            FORCE FULLWORD ALIGNMENT
CIVPATCH DC    50C'Z'        PATCH AREA
         END   IKJEGCIV
