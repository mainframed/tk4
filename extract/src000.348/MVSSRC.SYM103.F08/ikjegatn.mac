ATN      TITLE 'IKJEGATN - ATTENTION EXIT ROUTINE FOR TSO/TEST'
IKJEGATN CSECT
         EJECT
***********************************************************************
*                                                                     *
* STATUS --                                                           *
*                                                                     *
*    CHANGE LEVEL 00, VERSION NO.02, OS/VS2, 03/14/73                 *
*                                                                     *
* FUNCTION --                                                         *
*                                                                     *
*    THIS ROUTINE PROCESSES AN ATTENTION INTERRUPT FOR THE TEST       *
*    COMMAND PROCESSOR.  WHEN THE OPERATING SYSTEM DETERMINES THAT    *
*    THE TERMINAL USER HAS HIT THE TERMINAL ATTENTION KEY TO CAUSE    *
*    AN INTERRUPT IN TEST, IT SCHEDULES THIS ROUTINE ASYNCHRONOUSLY.  *
*                                                                     *
*    AFTER RECEIVING CONTROL FROM THE OPERATING SYSTEM, IKJEGATN      *
*    DETERMINES IF THE TERMINAL USER HAS ENTERED ANY INPUT IN         *
*    RESPONSE TO THE TEST MODE MESSAGE.  IF THE INPUT BUFFER          *
*    LENGTH IS ZERO, CONTROL IS RETURNED TO THE OPERATING SYSTEM      *
*    WITH A RETURN CODE OF ZERO.                                      *
*                                                                     *
*    WHEN THE TERMINAL USER HAS ENTERED SOMETHING IN RESPONSE TO      *
*    THE TEST MODE MESSAGE, THE PUTGET SERVICE ROUTINE IS INVOKED     *
*    TO PUT ALL ENQUEUED SECOND LEVEL MESSAGES TO THE TERMINAL        *
*    USER, AND TO ACQUIRE THE INPUT HE PREVIOUSLY ENTERED.  ALSO,     *
*    THE SUBCOMMAND CHAIN POINTER (SUBCHAIN) IS ZEROED AND THE        *
*    CURRENT INPUT BUFFER, IF ANY, IS FREED.  THE NEW INPUT           *
*    BUFFER ACQUIRED BY PUTGET IS PLACED IN TCOMTAB (INBUF).          *
*                                                                     *
*    IF TEST HAS BEEN RUNNING FROM AN IN-CORE PROCEDURE, THE LIST     *
*    DESCRIPTOR FOR THAT PROCEDURE IS DELETED FROM THE INPUT          *
*    STACK AND INPUT IS REDIRECTED TO THE TERMINAL.  THE INPUT        *
*    QUEUES ARE ALSO CLEARED TO ENSURE THAT THE NEXT INPUT RECEIVED   *
*    FROM THE TERMINAL USER WILL BE SYNCHRONIZED WITH TEST            *
*    PROCESSING.                                                      *
*                                                                     *
*    ADDITIONAL PROCESSING IS PERFORMED DEPENDING ON WHICH OF THE     *
*    FOLLOWING SITUATIONS EXISTS -- (1) IS THE USER PROGRAM           *
*    PROCESSING, (2) IS HELP PROCESSING, OR (3) IS TEST PROCESSING.   *
*        (1)   WHEN THE USER PROGRAM IS PROCESSING, IT IS SET         *
*              SET NON-DISPATCHABLE AND A BREAKPOINT IS INSERTED      *
*              IN THE PROGRAM.                                        *
*        (2)   IF HELP WAS PROCESSING, IT IS SET NON-DISPATCHABLE.    *
*        (3)   IF TEST WAS PROCESSING, AND IN (1) AND (2) ABOVE,      *
*              THE ATTENTION ECB IS POSTED COMPLETE WITH A X'48'      *
*              COMPLETION CODE.                                       *
*    IN ALL CASES, CONTROL IS RETURNED TO THE OPERATING SYSTEM        *
*    WITH A RETURN CODE OF ZERO.                                      *
*                                                                     *
* ENTRY POINTS --                                                     *
*                                                                     *
*         IKJEGATN - ADDRESS OF THE ATTENTION EXIT ROUTINE.           *
*                                                                     *
* INPUT --                                                            *
*                                                                     *
*    REGISTER 1 POINTS TO A THREE WORD PARAMETER LIST OF THE          *
*    FOLLOWING FORMAT --                                              *
*                                                                     *
*              ..........................................             *
*              .                                        .             *
*        +0    .  ADDRESS OF THE TAIE                   .             *
*              ..........................................             *
*              .                                        .             *
*        +4    .  ADDRESS OF THE INPUT BUFFER           .             *
*              ..........................................             *
*              .                                        .             *
*        +8    .  ADDRESS OF TCOMTAB                    .             *
*              ..........................................             *
*                                                                     *
* OUTPUT --                                                           *
*                                                                     *
*    REGISTER 15 ALWAYS CONTAINS A RETURN CODE OF ZERO (0).           *
*                                                                     *
* EXTERNAL REFERENCES --                                              *
*                                                                     *
*         PUTGET   - OBTAINS INPUT FROM TERMINAL.                     *
*         STACK    - RESETS INPUT STACK.                              *
*                                                                     *
*    MACROS USED --                                                   *
*                                                                     *
*         ESTAE    - INTERCEPT ABENDS.                                *
*         FREEMAIN - FREE INPUT BUFFERS.                              *
*         IKJECT   - ECT MAPPING MACRO.                               *
*         IKJEGS9G - INTERFACE TO SVC 097.                            *
*         IKJPGPB  - PUTGET PARAMETER BLOCK MAPPING MACRO.            *
*         IKJRB    - RB MAPPING MACRO.                                *
*         IKJSTPL  - STACK PARAMETER LIST MAPPING MACRO.              *
*         IKJTAIE  - TAIE MAPPING MACRO.                              *
*         IKJTCB   - TCB MAPPING MACRO.                               *
*         IKJTPL   - TPL MAPPING MACRO.                               *
*         POST     - INDICATE ATTENTION INTERRUPT PROCESSED.          *
*         PUTGET   - INVOKE PUTGET SERVICE ROUTINE.                   *
*         RETURN   - RESTORE REGISTERS UPON EXIT.                     *
*         SAVE     - SAVE REGISTERS UPON ENTRY.                       *
*         STACK    - INVOKE STACK SERVICE ROUTINE.                    *
*         STATUS   - SET SUBTASKS NONDISPATCHABLE.                    *
*   ***** TCLEARQ  - FLUSH TERMINAL INPUT QUEUES.**NOT ISSUED @ZA15714*
*         TCOMTAB  - TEST COMMUNICATION AREA MAPPING MACRO.           *
*                                                                     *
* EXITS, NORMAL --                                                    *
*                                                                     *
*    RETURN VIA A BRANCH TO THE ADDRESS SPECIFIED IN REGISTER 14.     *
*                                                                     *
* EXITS, ERROR --                                                     *
*                                                                     *
*    NONE.                                                            *
*                                                                     *
* TABLES/WORK AREAS --                                                *
*                                                                     *
*    THE FOLLOWING FIELDS IN TCOMTAB ARE UPDATED --                   *
*         INBUF                                                       *
*         SUBCHAIN                                                    *
*         TSTGOWCF                                                    *
*                                                                     *
* ATTRIBUTES --                                                       *
*                                                                     *
*    REFRESHABLE, ENABLED, NON-PRIVILEGED.                            *
*                                                                     *
* CHARACTER CODE DEPENDENCY --                                        *
*                                                                     *
*    THIS CSECT HAS NO CHARACTER CODE DEPENDENCIES.                   *
*                                                                     *
* NOTES --                                                            *
*                                                                     *
*    NONE.                                                            *
* CHANGE ACTIVITY --                                                  *
*                                                                     *
*    APAR 0Z15714 - DO NOT ISSUE TCLEARQ AFTER ATTN HIT. MUST @ZA15714*
*                   SUPPORT STACK OR COMD. CHAINING.          @ZA15714*
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*                                                                     *
*        GENERAL REGISTER EQUATES                                     *
*                                                                     *
***********************************************************************
ZERO     EQU   0              PARAMETER REGISTER
PARMREG1 EQU   1              PARAMETER REGISTER
WORKE    EQU   2              EVEN WORK REGISTER
WORKO    EQU   3              ODD WORK REGISTER
REG4     EQU   4              WORK REGISTERS
REG5     EQU   5              *
REG6     EQU   6              *
REG7     EQU   7              *
REG8     EQU   8              ADDRESSABILITY TO WORK SPACE
TCOMADDR EQU   9              ADDRESS OF TCOMTAB
REG10    EQU   10             WORK REGISTER
REG11    EQU   11             WORK REGISTER
BASEREG  EQU   12             BASE REGISTER
SAVEREG  EQU   13             SAVE AREA ADDRESS REGISTER
RETREG   EQU   14             ADDRESS OF RETURN POINT
ADDREG   EQU   15             ADDRESS REGISTER
RETCODE  EQU   15             RETURN CODE REGISTER
ENTRYREG EQU   15             ENTRY POINT ADDRESS.
         EJECT
         SAVE  (14,12),,*     SAVE REGISTERS
         LR    BASEREG,ENTRYREG GET ENTRY POINT ADDRESS.
         USING IKJEGATN,BASEREG ESTABLISH ADDRESSABILITY
         USING ATNPARMS,PARMREG1 ESTABLISH ADDRESSABILITY TO
*                             INPUT PARAMETERS
         L     TCOMADDR,ATNPTUSR GET POINTER TO TCOMTAB
         USING TCOMTAB,TCOMADDR ESTABLISH ADDRESSABILITY
         L     REG8,WORKAREA  GET POINTER TO WORK AREA
         USING TSTCWORK,REG8  ESTABLISH ADDRESSABILITY TO IT
         LA    REG8,CWORKATN  R8=ADDRESS OF ATTENTION'S WORK
*                             AREA
         DROP  REG8
         USING ATNWKARA,REG8  ESTABLISH ADDRESSABILITY TO
*                             WORK AREA
         XC    ATNWKARA(ATNLEN),ATNWKARA ZERO WORK AREA
         ST    SAVEREG,FOUR(,REG8)  SAVE ADDR OF CALLER'S SAVE AREA
         ST    REG8,EIGHT(,SAVEREG) STORE ATTN'S SAVE AREA POINTER
*                             IN CALLER'S SAVE AREA
         LR    SAVEREG,REG8   R13=ATTN'S SAVE AREA POINTER
*                             AND WORK AREA POINTER
         DROP  REG8
         SPACE 2
         LR    REG6,PARMREG1  SAVE REG 1 OVER ESTAE CALL
         USING ATNWKARA,SAVEREG  ADDRESSABILITY TO ATN WORK AREA
         L     WORKO,TSTSTAE  ADDRESS OF ESTAE EXIT ROUTINE
         ESTAE (WORKO),PARAM=LISTESTA,RECORD=YES,MF=(E,ATNESTAE)
         SR    REG4,REG4      ZERO POST CODE.
         LR    PARMREG1,REG6  RESTORE REG 1
         L     REG5,ATNPTAIE  R5=POINTER TO TAIE
         DROP  PARMREG1
         USING TAIE,REG5      ESTABLISH ADDRESSABILITY TO THE
*                             TAIE
         CLI   TAIETGET,ZERO  CHECK FOR NO TERMINAL INPUT
         BZ    ATN03010       YES, RETURN
         DROP  REG5
         SPACE 2
         XC    SUBCHAIN(L'SUBCHAIN),SUBCHAIN CLEAR SUBCHAIN FIELD
         L     REG5,TSTECT    R5=ADDRESS OF ECT
         USING ECT,REG5       ESTABLISH ADDRESSABILITY TO IT
         OI    ECTMSGF,ONBIT  INDICATE SECOND LEVEL MESSAGES
*                             TO BE CLEARED
         DROP  REG5
         MVC   ATNPGPB(ATNPTGTL),ATNPTGT INITIALIZE PARAM BLOCK.
         MVC   ATNIOPL(TSTCPECB-TSTUPT+L'TSTCPECB),TSTUPT SET UP
*                             UPT, ECT, AND ECB POINTERS.
         PUTGET PARM=ATNPGPB, ADDRESS OF PUTGET PARAMETER BLOCK        *
               MF=(E,ATNIOPL) EXECUTE FORM OF MACRO.
         SPACE 2
         LTR   RETCODE,RETCODE TEST RETURN CODE.
         BZ    ATN01010       IF ZERO, BRANCH TO CONTINUE.
         LA    RETREG,EIGHT   CREATE CONSTANT 8.
         CR    RETCODE,RETREG CHECK IF AN ATTENTION OCCURRED.
         BE    ATN01030       IF ATTENTION, BRANCH TO CLEAN-UP.
         LA    REG4,ATNPG(,RETCODE) PUTGET ERROR CODE.
         B     ATN01090       BRANCH TO POST AND CLEAN-UP.
         SPACE 2
ATN01010 EQU   *
         L     REG5,ATNPGPB+(PGPBIBUF-PGPB) INPUT BUFFER ADDRESS
         OC    INBUF(L'INBUF),INBUF MUST INBUF BE FREED
         BZ    ATN01030       NO, INSERT NEW BUFFER ADDR
         L     PARMREG1,INBUF R1=ADDR OF CORE TO BE FREED
         LH    ZERO,ZERO(,PARMREG1) R0=LENGTH OF CORE TO BE FREED
         O     ZERO,SUBPOOL1  INDICATE SUBPOOL NUMBER.
         FREEMAIN R,LV=(0),A=(1) FREE LAST INPUT BUFFER.
         SPACE 2
ATN01030 EQU   *
         ST    REG5,INBUF     SAVE BUFFER ADDR IN TCOMTAB
         LH    REG5,ZERO(,REG5) GET LENGTH OF INPUT LINE.
         LA    WORKO,FOUR     CREATE CONSTANT FOUR.
         CR    REG5,WORKO     CHECK FOR NULL LINE INPUT.
         BE    ATN03010       IF NULL LINE, RETURN.
         TM    TSTFLGS4,TSTA  Q.RUNNING FROM TERMINAL
         BO    ATN01050       YES.DONT FLUSH THE STACK.
         MVC   ATNSTCKL(ATNSTLEN),ATNSTCK MOVE L FORM OF STACK MACRO
*                             TO WORK AREA
         STACK PARM=ATNSTCKL, ADDRESS OF STACK PARAMETER BLOCK         *
               MF=(E,ATNIOPL) EXECUTE FORM OF MACRO.
         SPACE 2
         LTR   RETCODE,RETCODE TEST RETURN CODE.
         BZ    ATN01050       IF ZERO, BRANCH TO CONTINUE.
         LA    REG4,ATNST(,RETCODE) STACK ERROR CODE.
         B     ATN01090       BRANCH TO POST AND CLEAN-UP.
         SPACE 2
ATN01050 EQU   *
*        TCLEARQ IS DELETED.....                               @ZA15714
         SPACE 2
ATN01070 EQU   *
         TM    TSTFLGS3,TSTHELP WAS HELP RUNNING
         BO    ATN02010       YES. STOP IT
         L     REG5,ECBTST    R5=ADDRESS OF TEST'S ECB
         TM    ZERO(REG5),WAITBIT IS TEST WAITING
         BO    ATN02030       YES, PP MODE - SET BREAKPOINT
         SPACE 2
ATN01090 EQU   *
         L     PARMREG1,ECBTMPA GET ADDR OF ATTENTION ECB
         TM    ZERO(PARMREG1),ATNSET HAS ATTENTION BEEN SET
         BO    ATN03010       YES, RETURN TO SYSTEM
         O     REG4,POSTCODE  R0=POST CODE
         POST  (1),(REG4)     POST THE ATTENTION ECB
         B     ATN03010       RETURN TO SYSTEM
         SPACE 2
ATN02010 EQU   *
         STATUS STOP,TCB=TSTHTCB STOP HELP SUBTASK.
****
****     RETURN CODE CHECK IGNORED -- EITHER TCB WAS STOPPED
****     SUCCESSFULLY OR TCB WAS NOT A SUBTASK OF TSO/TEST.
****
         B     ATN01090       CONTINUE.
         SPACE 2
ATN02030 EQU   *
         OC    TSTGO(L'TSTGO),TSTGO HAS A BREAKPOINT BEEN SET
         BNZ   ATN03010       YES, RETURN
         STATUS STOP          STOP ALL SUBTASKS
****
****     RETURN CODE CHECK IGNORED -- EITHER TCB WAS STOPPED
****     SUCCESSFULLY OR TCB WAS NOT A SUBTASK OF TSO/TEST.
****
         L     WORKE,TPLPTR   OBTAIN ADDRESS OF TPL.
         USING TPL,WORKE      ADDRESSABILITY TO TPL.
         L     WORKE,TPLCTCB  OBTAIN ADDRESS OF TEST'S DAUGHTER TCB
         DROP  WORKE
         USING TCB,WORKE      ADDRESSABILITY TO TCB.
         L     WORKO,TCBRBP   GET POINTER TO RB QUEUE
         USING RBSECT,WORKO   ADDRESSABILITY TO RB.
         SPACE 2
ATN02050 EQU   *
         TM    RBSTAB1,RBFTSIRB IS RB A PRB OR AN IRB
         BZ    ATN02090       YES, CHECK FOR PROBLEM
*                             PROGRAM STATE
         SPACE 2
ATN02070 EQU   *
         L     WORKO,RBLINK   GET ADDR OF NEXT RB
         B     ATN02050       CHECK FOR PRB OR IRB
         SPACE 2
ATN02090 EQU   *
         TM    RBOPSW+1,PSWPBIT IS PROGRAM STATE BIT ON
         BZ    ATN02070       NO, TRY NEXT RB
         TM    RBOPSW+1,PSWKEY IS THE KEY ZERO
         BZ    ATN02070       NO, TRY NEXT RB
         MVC   TSTGOPSW(L'TSTGOPSW),RBOPSW+4 MOVE IC TO TSTGO
         MVC   TSTGOWCF(L'TSTGOWCF),RBWCF MOVE WAIT COUNT TO TSTGO
         SPACE 2
         IKJEGS9G WCF,TCBADDR=(WORKO),MF=(E,ATNSVC) TURN OFF WAIT      *
                              ON IN ACTIVE IRB OR PRB.
         SPACE 2
         IKJEGS9G OPSW,TCBADDR=(WORKO),VALUE=TSTSVC,MF=(E,ATNSVC)      *
                              CHANGE RESUME ADDRESS.
         SPACE 2
         STATUS START,TCB=(WORKE) START TEST'S DAUGHTER TASK
****
****     RETURN CODE CHECK IGNORED -- EITHER TCB WAS STOPPED
****     SUCCESSFULLY OR TCB WAS NOT A SUBTASK OF TSO/TEST.
****
         SPACE 2
ATN03010 EQU   *
         ESTAE 0              REMOVE ESTAE REQUEST
ATN03020 EQU   *
         NI    TSTFLGS4,HEXFF-TSTRERTN  SHUT OFF RETRY FLAG
         XR    RETCODE,RETCODE SET RETURN CODE TO ZERO
         L     SAVEREG,FOUR(,SAVEREG) RESTORE ADDR OF CALLER'S SAVE
*                             AREA
         RETURN (14,12),,RC=(15) RESTORE REGISTERS AND RETURN
         EJECT
***********************************************************************
*                                                                     *
*        CONSTANTS, EQUATES, AND OTHER AREAS.                         *
*                                                                     *
***********************************************************************
ATNSTCK  STACK DELETE=ALL,    RESET THE INPUT STACK                    *
               MF=L           LIST FORM OF MACRO
ATNSTLEN EQU   *-ATNSTCK      LENGTH OF MACRO
ATNPTGT  PUTGET OUTPUT=(IKJEGMMD,SINGLE,ATTN), MODE MESSAGE ADDRESS    *
               TERMPUT=(EDIT,WAIT,NOHOLD,NOBREAK), OUTPUT OPTIONS      *
               TERMGET=(EDIT,WAIT), INPUT OPTIONS                      *
               MF=L           LIST FORM OF MACRO
ATNPTGTL EQU   *-ATNPTGT      PARAMETER BLOCK LENGTH.
         DS    0F             ALIGN ON WORD BOUNDARY.
SUBPOOL1 DC    X'01000000'    SPECIFIES SUBPOOL 1
POSTCODE DC    X'08000000'    POST CODE FOR ATTENTION ECB
CONSTANT DC    50C'Z'         ZAP AREA
ONE      EQU   1              NUMBER 1
FOUR     EQU   4              NUMBER 4
EIGHT    EQU   8              NUMBER 8
TWELVE   EQU   12             NUMBER 12
RETRY    EQU   0              SIGNIFIES NO RETRY ROUTINE
L1       EQU   1              LENGTH 1
L3       EQU   3              LENGTH 3
ONBIT    EQU   X'80'          HIGH ORDER BIT ON
PSWPBIT  EQU   X'01'          SIGNIFIES P BIT ON IN PSW
PSWKEY   EQU   X'F0'          BIT SETTING TO TEST FOR KEY
*                             ZERO
WAITBIT  EQU   X'80'          ECB WAITING
ATNSET   EQU   X'40'          ATTENTION BIT
ATNPG    EQU   X'000100'      PUTGET ERROR CODE.
ATNST    EQU   X'000200'      STACK ERROR CODE.
ATNTQ    EQU   X'000400'      TCLEARQ ERROR CODE.
HEXFF    EQU   X'FF'          HEX FF
LISTESTA IKJEGSPL MODNM=IKJEGATN,ABNTB=ABENDTB,RTRY=RETRY,             *
               TNM=ATTENTION  ESTAE PARAMETER LIST
         SPACE 1
ABENDTB  DS    0F             ABEND TABLE FOR IKJEGSTA
         DC    X'FF'          END OF TABLE FLAG
         TITLE 'IKJEGATM - MESSAGE CONTROL SECTION FOR IKJEGATN'
IKJEGATM CSECT
         ENTRY IKJEGMMD       OUTPUT LINE DESCRIPTOR FOR MESSAGE.
         ENTRY IKJEGMMG
         ENTRY IKJEGMMT
IKJEGMMD DC    A(1)           INDICATE ONE SEGMENT.
         DC    A(IKJEGMMG)    ADDRESS OF MODE MESSAGE.
IKJEGMMG DC    AL2(ATMMODEX-IKJEGMMG) LENGTH OF MODE MESSAGE.
         DC    AL2(0)         OFFSET TO MODE MESSAGE.
         DC    C' '           LEADING BLANK FOR PUTGET.
IKJEGMMT DC    CL4'TEST'      TEST MODE MESSAGE.
ATMMODEX EQU   *              END OF MESSAGE.
         EJECT
         IKJPGPB
         EJECT
         TCOMTAB
         EJECT
         IKJTPL
         EJECT
         IKJECT
         EJECT
         IKJSTPL
         EJECT
***********************************************************************
*                                                                     *
*        THIS DSECT DESCRIBES THE WORKAREA USED BY IKJEGATN.          *
*                                                                     *
***********************************************************************
ATNWKARA DSECT                ATTENTION'S WORK AREA
ATNSVARA DS    18F            SAVE AREA
ATNIOPL  DS    4F             IO SERVICE ROUTINE PARAMETER LIST
ATNSTCKL DS    2F             STACK PARAMETER LIST
         ORG   ATNSTCKL       REUSE WORKAREA
ATNESTAE DS    4F             PARAMETER LIST FOR ESTAE
         ORG   ATNSTCKL       REUSE WORKAREA
ATNPGPB  DS    4F             PUTGET PARAMETER BLOCK
         ORG   ATNSTCKL       REUSE WORKAREA
ATNSVC   DS    3F             PARAMETER LIST FOR BREAKPOINT SVC
ATNLEN   EQU   *-ATNWKARA     WORK AREA LENGTH
         EJECT
         IKJTAIE
         EJECT
         IKJRB
         EJECT
         IKJTCB
         EJECT
**********************************************************************
*                                                                     *
*        THIS DSECT DESCRIBES THE INPUT PARAMETER LIST TO IKJEGATN    *
*                                                                     *
**********************************************************************
ATNPARMS DSECT                DSECT FOR INPUT PARAMETERS
ATNPTAIE DS    A              ADDR OF TAIE
ATNPTIPT DS    A              ADDR OF INPUT BUFFER
ATNPTUSR DS    A              USER INFORMATION - ADDRESS
*                             OF TCOMTAB
         TITLE 'IKJEGBLD - ADDRESS STRING CONSTRUCTION SERVICE ROUTINE'
* REGISTER EQUATES
R0       EQU   0              REGISTER 0
R1       EQU   1              REGISTER 1
R2       EQU   2              REGISTER 2
R3       EQU   3              REGISTER 3
R4       EQU   4              REGISTER 4
R5       EQU   5              REGISTER 5
R6       EQU   6              REGISTER 6
R7       EQU   7              REGISTER 7
R8       EQU   8              REGISTER 8
R9       EQU   9              REGISTER 9
R10      EQU   10             REGISTER 10
R11      EQU   11             REGISTER 11
R12      EQU   12             REGISTER 12
R13      EQU   13             REGISTER 13
R14      EQU   14             REGISTER 14
R15      EQU   15             REGISTER 15
         SPACE
* OTHER EQUATES
REGSYM   EQU   C'R'           GENERAL REGISTER SYMBOL
DECSYM   EQU   C'N'           SYMBOL FOR DECIMAL VALUE
REGD     EQU   C'D'           SYMBOL FOR DOUBLE PRECISION FLOATING
*                                POINT REGISTER
REGE     EQU   C'E'           SYMBOL FOR SINGLE PRECISION FLOATING
*                                POINT REGISTER
DOUBLE   EQU   X'10'          FLAG FOR DOUBLE PRECISION
SINGLE   EQU   X'08'          FLAG FOR SINGLE PRECISION
DECVAL   EQU   X'04'          DECIMAL EXPRESSION VALUE
PERCENT  EQU   C'%'           PERCENT SIGN
EXPFLGS5 EQU   8              OFFSET INTO EXPRESSION PDE FOR FLAGS
NINE     EQU   9              NUMBER 9
TEN      EQU   10             NUMBER 10
THIRTEEN EQU   13             NUMBER 13
TWENTY8  EQU   28             NUMBER 28
MAXLEN   EQU   100            MAXIMUM LENGTH OF STRING
ADDRLEN  EQU   26             MAXIMUM LENGTH OF STRING FOR MSG
         SPACE
IKJEGBLD CSECT                THIS CSECT BUILDS AN ADDRESS STRING TO A
* MAXIMUM LENGTH OF 100 BYTES.  AS INPUT IT REQUIRES A POINTER TO THE
* ADDRESS PDE IN REGISTER ZERO AND A POINTER IN REGISTER ONE TO THE
* AREA IN WHICH THE STRING IS TO BE BUILT.  AT EXIT REGISTER ZERO WILL
* CONTAIN THE NUMBER OF BYTES CONSTRUCTED AND REGISTER ONE WILL CONTAIN
* A POINTER TO THE BYTE FOLLOWING THE LAST ONE IN THE ADDRESS STRING.
* IF ON INPUT REGISTER 1 IS NEGATIVE IT WILL BE KNOWN THAT THE MAXIMUM
* LENGTH ALLOWED FOR THE STRING IS 26 BYTES
         SPACE
         SAVE  (14,12),,IKJEGBLD SAVE CALLER'S REGISTERS
         LR    R12,R15        ESTABLISH ADDRESSABILITY FOR THIS
         USING IKJEGBLD,R12      CSECT
         LR    R10,R0         R10=POINTER TO ADDRESS PDE
         USING IKJPARMA,R10   ESTABLISH PDE ADDRESSABILITY
         LTR   R1,R1          IS THE MAXIMUM STRING LENGTH 26 BYTES
         BNM   RESET          NO, SET MAXIMUM TO 100
         LCR   R1,R1          LOAD COMPLEMENT OF REGISTER ONE
         LA    R9,ADDRLEN     SET MAXIMUM TO 26
CLEAR    EQU   *              CLEAR ACCUMULATOR
         SR    R4,R4          CLEAR BYTE ACCUMULATOR
         TM    PDEFLG1,LDNAMFLG Q.IS LOAD NAME SPECIFIED.
         BO    LOADYES        YES, PICK UP EVERYTHING.
         TM    PDEFLG2,CTNAMFLG Q.IS ENTRYNAME PRESENT.
         BO    CSECTYES       YES, PICK UP ENTRY-ADDRESS.
         B     ADDRONLY       NO, PICK UP ADDR ONLY.
         SPACE
RESET    EQU   *              SET MAXIMUM LENGTH TO 100
         LA    R9,MAXLEN      SET MAXIMUM TO 100
         B     CLEAR          BEGIN BUILDING ADDRESS STRING
LOADYES  EQU   *              LOADNAME GIVEN
         L     R7,PDELDNAM    R7=ADDR OF LOAD NAME.
         LH    R6,PDELEN1     R6=LOAD NAME LENGTH.
         BAL   R11,ADMOVE     MOVE LOAD NAME AND BUMP ACCUMULATORS.
         SPACE
CSECTYES EQU   *              CSECTNAME GIVEN
         L     R7,PDECTNAM    R7=ADDR OF CSECT NAME.
         LH    R6,PDELEN2     R6=CSECT NAME LENGTH.
         BCTR  R7,ZERO        PICK UP PRECEDING PERIOD.
         LA    R6,ONE(R6)     *
         TM    PDEFLG3,AFLG   Q.ANY ADDRESS.
         BZ    MOVE           NO, MOVE CSECT NAME.
         LA    R6,ONE(R6)     YES, PICK UP TRAILING PERIOD.
         SPACE
MOVE     EQU   *              SAVE CSECT NAME
         BAL   R11,ADMOVE     MOVE CSECT NAME & ETC.
         SPACE
ADDRONLY EQU   *              CHECK FOR ADDRESS STRING
         TM    PDEFLG3,AFLG   Q.ANY ADDR.
         BZ    GOBACK         NO, RETURN TO CALLER
         L     R7,PDEADRPT    R7=ADDR OF ADDRESS.
         LA    R5,PDEADRPT    R5=ADDR OF POINTER TO ADDRESS
         LH    R6,PDELEN3     R6=ADDRESS STRING LENGTH.
         TM    PDEFLG4,ABSCOMPL Q.IS IT ABSOLUTE
         BM    RELCHK         NO, CHECK FOR RELATIVE.
         B     BUMP           YES, PICK UP PERIOD.
         SPACE
RELCHK   EQU   *              TEST FOR RELATIVE ADDRESS
         TM    PDEFLG4,RELADDR Q.IS IT A RELATIVE.
         BZ    NEITHER        NO, MOVE STRING.
         BCTR  R7,ZERO        YES, PICK UP PLUS SIGN.
         SPACE
BUMP     EQU   *              INCREMENT LENGTH COUNTER
         LA    R6,ONE(R6)     BUMP FOR PUNCTUATION.
         SPACE
NEITHER  EQU   *              ADDR IS NEITHER ABSOLUTE NOR RELATIVE
         BAL   R11,ADMOVE     MOVE ADDRESS STRING & ETC.
         TM    PDEFLG4,GENR   IS ADDRESS GENERAL REGISTER TYPE
         BZ    TRYD           NO, TRY FLOATING POINT REGISTER
         MVI   ZERO(R1),REGSYM MOVE IN REGISTER SYMBOL
         SPACE
BUMPIT   EQU   *              BUMP POINTER AND ACCUMULATOR
         LA    R1,ONE(R1)     BUMP THE SAVE AREA POINTER
         LA    R4,ONE(R4)     BUMP THE ACCUMULATOR
         SPACE
GOIND    EQU   *              HANDLE PERCENT SIGNS
         BAL   R11,INDIRECT   WHISTLE THROUGH PERCENT SIGNS.
         SPACE
ADDRLOOP EQU   *              HANDLE ADDRESS EXPRESSION
         OC    THIRTEEN(L3,R5),THIRTEEN(R5) IS THERE AN
*                                EXPRESSION PDE
         BZ    GOBACK         NO, RETURN TO CALLER
         MVC   ZERO(L1,R1),NINE(R5) MOVE SIGN
         L     R5,TWELVE(R5)  R5=ADDR OF NEXT EXPRESSION PDE
         L     R7,ZERO(R5)    R7=ADDR OF EXPRESSION STRING.
         LH    R6,FOUR(R5)    R6=EXPRESSION LENGTH.
         LA    R1,ONE(R1)     BUMP SAVE POINTER.
         LA    R4,ONE(R4)     BUMP THE ACCUMULATOR.
         BAL   R11,ADMOVE     MOVE EXPRESSION.
         TM    EXPFLGS5(R5),DECVAL IS VALUE DECIMAL
         BZ    TRYIND         NO, CHECK FOR PERCENT SIGNS
         MVI   ZERO(R1),DECSYM MOVE IN SYMBOL FOR DECIMAL
         LA    R1,ONE(R1)     BUMP SAVE POINTER
         LA    R4,ONE(R4)     BUMP THE ACCUMULATOR
         SPACE
TRYIND   EQU   *              HANDLE PERCENT SIGNS
         BAL   R11,INDIRECT   ZING THROUGH PERCENT SIGNS.
         B     ADDRLOOP       CHECK FOR MORE EXPRESSIONS.
         SPACE
ADMOVE   EQU   *              SAVE PORTION OF ADDRESS
         LR    R8,R4          R8=TOTAL LENGTH SO FAR
         AR    R8,R6          R8=POSSIBLE LENGTH
         CR    R8,R9          WILL LENGTH BE TOO GREAT
         BH    DECR           YES, DECREMENT COUNT FOR MOVE
         AR    R4,R6          BUMP THE ACCUMULATOR.
         LTR   R6,R6          DONT DECREMENT IF SIX ZERO
         BZ    GOBACK         IF ZERO, LET'S GET OUT OF HERE
         BCTR  R6,ZERO        DECREMENT R6 FOR EXECUTE.
         EX    R6,STRMVC      MOVE THE STRING.
         LA    R1,ONE(R6,R1)  BUMP THE SAVE AREA POINTER.
         CR    R8,R9          IS LENGTH EQUAL TO MAXIMUM
         BE    GOBACK         YES, RETURN TO CALLER
         BR    R11            NO, KEEP GOING
         SPACE
STRMVC   MVC   ZERO(ONE,R1),ZERO(R7) MOVE STRING EXECUTED MVC.
         SPACE
INDIRECT EQU   *              CHECK FOR INDIRECT ADDRESS
         LH    R14,TEN(R5)    R14=INDIRECT COUNT.
         LTR   R14,R14        Q.ANY COUNT SPECIFIED.
         BCR   EIGHT,R11      NO, RETURN TO CALLER.
         SPACE
PCLOOP   EQU   *              SAVE PERCENT SIGN
         LR    R8,R4          R8=ACCUMULATED LENGTH COUNT
         LA    R8,ONE(R8)     BUMP TEST ACCUMULATOR
         CR    R8,R9          IS COUNT GREATER THAN MAXIMUM
         BH    GOBACK         YES, RETURN TO CALLER
         MVI   ZERO(R1),PERCENT MOVE IN A PERCENT SIGN.
         SPACE
         LA    R1,ONE(R1)     BUMP SAVE AREA POINTER.
         LA    R4,ONE(R4)     BUMP ACCUMULATOR.
         BCT   R14,PCLOOP     MOVE 'EM ALL.
         CR    R8,R9          IS COUNT EQUAL TO MAXIMUM
         BE    GOBACK         YES, ALL THROUGH
         BR    R11            NO, KEEP GOING
         SPACE
DECR     EQU   *              DECREMENT COUNT FOR MOVE
         BCTR  R6,ZERO        SUBTRACT ONE
         B     ADMOVE         TRY AGAIN
         SPACE
GOBACK   EQU   *              STRING IS BUILT
         LR    R0,R4          R0=TOTAL LENGTH OF STRING BUILT
         LM    R14,R15,TWELVE(R13) RESTORE R14-R15
         LM    R2,R12,TWENTY8(R13) RESTORE R2-R12
         BR    R14            RETURN TO CALLER
         SPACE
TRYD     EQU   *              IS ADDRESS DOUBLE PRECISION FLOATING
*                                POINT REGISTER
         TM    PDEFLG4,DOUBLE IS FLAG ON
         BZ    TRYE           NO, TRY SINGLE PRECISION
         MVI   ZERO(R1),REGD  MOVE IN REGISTER SYMBOL
         B     BUMPIT         BRANCH TO BUMP COUNTER
         SPACE
TRYE     EQU   *              IS ADDRESS SINGLE PRECISION FLOATING
*                                POINT REGISTER
         TM    PDEFLG4,SINGLE IS FLAG ON
         BZ    GOIND          NO, CHECK FOR PERCENT SIGNS
         MVI   ZERO(R1),REGE  MOVE IN REGISTER SYMBOL
         B     BUMPIT         BRANCH TO BUMP COUNTER
         SPACE
         SPACE
         TSTCWORK             DSECT FOR WORK AREA
         SPACE
         IKJPARMA             DSECT FOR ADDRESS PDE
         END
