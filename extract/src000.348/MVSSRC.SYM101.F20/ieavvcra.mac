         TITLE 'IEAVVCRA -- CONSOLE ATTENTION INTERRUPT HANDLER' Y02752
***********************************************************************
*MODULE NAME= IEAVVCRA
*
*DESCRIPTIVE NAME= CONSOLE ATTENTION PROCESSOR
*
*COPYRIGHT= N/A
*
*STATUS= OS/VS2 MVS 3033 PROCESSOR SUPPORT(SCP)                @G51APSS
*
*FUNCTION= RECEIVES CONTROL WHEN A CONSOLE ATTENTION IS DETECTED BY IOS
*          TO FLAG THE APPROPIATE CONSOLE AND POST COMM TASK.
*
*  OPERATION= THIS MODULE RECEIVES CONTROL FROM IOS POST STATUS WHEN
*             IOS  HAS RECEIVED AN ATTENTION FROM ONE OF THE SYSTEM
*             CONSOLES. CONTROL IS UNDER AN SRB IN SUPERVISOR STATE
*             AND KEY WITH THE LOCAL LOCK HELD. THE ROUTINE FIRST
*             SAVES THE REGISTERS IN THE LOCAL LOCK SAVE AREA, THEN
*             OBTAINS THE CMS LOCK. INFORMATION ABOUT THE INTERRUPT
*             IS PASSED IN AN IOSB POINTED TO BY REGISTER 1 AT ENTRY.
*             THE UCB ADDRESS IS RETRIEVED FROM THE IOSB AND A
*             SEARCH IS MADE THRU THE UCME'S TO FIND ONE WITH THE SAME
*             UCB ADDRESS.
*
*             IF ONE IS FOUND, A CHECK IS MADE TO SEE IF AN ERP
*             IS IN CONTROL (IOSERR IN IOSFLA OF THE IOSB) AND
*             IF THE CORRESPONDING CONSOLE IS A CARD READER. IF
*             BOTH THESE CONDITIONS HOLD, THE ATTENTION IS IGNORED.
*             OTHERWISE, THE CONSOLE IS TESTED TO SEE THAT IT
*             SUPPORTS ATTENTIONS. IF NOT THEN THE ATTENTION   @G51APSS
*             IS IGNORED. OTHERWISE A CHECK IS MADE TO SEE IF  @G51APSS
*             A NO-CONSOLES CONDITION EXISTS. IF NOT THEN THE  @G51APSS
*             DEVICE IS CHECKED TO SEE IF IT IS ACTIVE AND     @G51APSS
*             DOESN'T HAVE CLOSE PENDING ON. IF SO THEN THE    @G51APSS
*             UCME IS MARKED WITH ATTENTION PENDING(UCMAF).    @G51APSS
*             IF A UNIT CHECK OCCURED WITH AN ATTENTION THEN   @G51APSS
*             THE SENSE BYTES IN THE IOSB(IOSSNS) ARE COPIED   @G51APSS
*             INTO THE UCB(UCBSNS). COMMTASK IS THEN POSTED.   @G51APSS
*                                                              @G51APSS
*             IF THE DEVICE DID NOT SUPPORT ATTENTIONS OR IT   @G51APSS
*             WAS NOT ACTIVE OR CLOSE PENDING WAS ON THEN THE  @G51APSS
*             SEARCH FOR A MATCHING UCB ADDRESS CONTINUES.     @G51APSS
*                                                              @G51APSS
*             IF A NO-CONSOLES CONDITON DID EXIST (UCMSYSE=1)  @G51APSS
*             AND A MASTER CONSOLE CANDIDATE WAS NOT CHOSEN    @G51APSS
*             (UCMFATCN=0) THEN THIS DEVICE IS MADE TO APPEAR  @G51APSS
*             AS IF A VARY CONSOLE WAS ISSUED FOR IT.          @G51APSS
*                1 ATTENTION PENDING IS SET OFF    (UCMAF)     @G51APSS
*                2 OPEN PENDING IS SET ON          (UCMTA)     @G51APSS
*                3 DEVICE ACTIVE IS SET ON         (UCMUF)     @G51APSS
*                4 CLOSE PENDING IS SET OFF        (UCMCF)     @G51APSS
*                5 CHANGING STATUS OUT OF CONSOLE              @G51APSS
*                  STATE IS SET OFF                (UCBDADI)   @G51APSS
*                6 STATUS CHANGING TO OFFLINE                  @G51APSS
*                  IS SET OFF                      (UCBCHGS)   @G51APSS
*                7 OFFLINE BY VARY IS SET OFF      (UCBVORSN)  @G51APSS
*                8 DEVICE ONLINE IS SET ON         (UCBONLI)   @G51APSS
*                9 ACTIVE CONSOLE IS SET ON        (UCBSYSR)   @G51APSS
*             IF THE DEVICE IS A COMPOSITE THEN BOTH THE       @G51APSS
*             INPUT AND OUTPUT HALFS ARE MARKED. THE DEVICES   @G51APSS
*             UCME ADDRESS OR THE INPUT HALFS UCME ADDRESS     @G51APSS
*             IS SAVED IN THE UCM(UCMFATCN).                   @G51APSS
*                                                              @G51APSS
*             IF A NO-CONSOLES CONDITION DOES EXIST AND A      @G51APSS
*             CANDIDATE HAS ALREADY BEEN CHOSEN THEN THE       @G51APSS
*             ATTENTION IS IGNORED.                            @G51APSS
*                                                              @G51APSS
*             COMMTASKS ATTENTION ECB IS NOT POSTED IF A       @G51APSS
*             NO-CONSOLES CONDITION EXISTS.                    @G51APSS
*
*NOTES
*  DEPENDENCIES= LOCAL LOCK MUST BE HELD AT ENTRY
*
*    CHARACTER-CODE DEPENDENCIES= NONE
*
*  RESTRICTIONS= MUST BE GIVEN CONTROL IN THE MASTER SCHEDULERS
*                ADDRESS SPACE (SAME ADDRESS SPACE AS COMM TASK).
*
*  REGISTER-CONVENTIONS= REG EQUATES
*
*  PATCH-LABEL= N/A
*
*MODULE TYPE= PROCEDURE
*
*  PROCESSOR= ASSEM
*
*  MODULE SIZE= X'206' BYTES                                   @G51APSS
*
*  ATTRIBUTES= REENTRANT, NUCLEUS, SUPERVISOR STATE, KEY ZERO
*
*ENTRY POINT= IEEBA1
*
*  CONDITIONS= LOCAL LOCK HELD, KEY ZERO, SUPERVISOR STATE, MASTER
*              SCHEDULER ADDRESS SPACE, UNDER THE IOS POST STATUS SRB
*
*  PURPOSE= ONLY ENTRY POINT, ENTERED TO NOTIFY COMM TASK OF AN
*           INTERRUPT FROM A CONSOLE DEVICE.
*
*  LINKAGE= BRANCHED TO (BALR 14,15) FROM IOS POST STATUS
*
*  INPUT= REG1     ADDRESS OF IOSB
*         REG13    ADDRESS OF LOCAL LOCK SAVE AREA
*         REG14    RETURN ADDRESS
*         REG15    ENTRY POINT ADDRESS
*
*  REGISTERS SAVED= 0 THRU 15
*
*  REGISTER USAGE= SEE 'REG EQUATES' BELOW
*
*  REGISTERS RESTORED= 0 THRU 15
*
*EXIT - NORMAL= BR 14    RETRUN TO CALLER
*
*  CONDITIONS= SAME AS AT ENTRY
*
*  OUTPUT= IF A NO-CONSOLES CONDITION EXISTS THEN              @G51APSS
*            1) UCMFATCN CONTAINS THE ADDRESS OF THE UCME      @G51APSS
*               OF THE DEVICE WHICH WAS CHOSEN TO BECOME THE   @G51APSS
*               NEW MASTER CONSOLE                             @G51APSS
*            2) THE UCM ENTRY FOR THE CONSOLE THAT RECEIVED    @G51APSS
*               THE INTERRUPT HAS THE FOLLOWING FIELDS SET:    @G51APSS
*                 UCMAF = 0       UCMCF = 0                    @G51APSS
*                 UCMTA = 1       UCMFATCN = ADDRESS OF UCME   @G51APSS
*                 UCMUF = 1                                    @G51APSS
*            3) THE CORESPONDING UCB HAS THE FOLLOWING FIELDS  @G51APSS
*               SET:                                           @G51APSS
*                 UCBONLI = 1     UCBDADI = 0                  @G51APSS
*                 UCBSYSR = 1     UCBCHGS = 0                  @G51APSS
*                                 UCBVORSN= 0                  @G51APSS
*                                                              @G51APSS
*          OTHERWISE                                           @G51APSS
*            1) THE UCM ENTRY FOR THE CONSOLE THAT RECEIVED    @G51APSS
*               THE INTERRUPT HAS FIELD UCMAF SET TO 1.        @G51APSS
*            2) UCMAECB IS POSTED TO WAKE UP COMM TASK.        @G51APSS
*
*  RETURN CODES=NONE
*
*EXIT - ERROR= NONE
*
*EXTERNAL REFERENCES= AS FOLLOWS:
*
*  ROUTINES= POST, BRANCH ENTRY, TO DO LOCAL POST OF UCMAECB
*            SETLOCK, TO OBTAIN AND FREE THE CMS LOCK
*
*  DATA AREAS= UCM
*              IOSB
*              UCB                                             @G51APSS
*              FRR                                             @ZA16899
*
*  CONTROL BLOCKS= UCM   U,M
*                  IOSB  U
*                  UCB   U,M                                   @G51APSS
*
*TABLES= NONE
*
*MACROS= SETLOCK
*
*  SERIALIZATION= LOCAL LOCK, HELD AT ENTRY, NEVER RELEASED
*.                CMS LOCK, OBTAINED AT ENTRY, FREED AT EXIT
*
*CHANGE ACTIVITY= ZA16889,G51APSS                              @G51APSS
*
*MESSAGES= NONE
*
*ABEND CODES= NONE
*
***********************************************************************
         EJECT
IEAVVCRA CSECT
* A (GETFRR)   APPROX. 033680 ADDITIONAL                       @ZA16889
* A (FREEFRR)  APPROX. 048350    RECOVERY                      @ZA16889
* A (VCRACLUP) APPROX. 049640       CODE                       @ZA16889
IEEBA1   EQU   *                   SYSTEM ENTRY POINT NAME
         ENTRY IEEBA1
         SPACE 1                                                 Y02893
*        REGISTER  EQUATES
R0       EQU   0                   *
R1       EQU   1                   ADDRESS OF THE IOSB           Y02146
R2       EQU   2                   CVT BASE REG                  Y02893
R4       EQU   4                   WORK REGISTER               @ZA16889
R5       EQU   5                   FOR SAVING RC FROM SETLOCK    Y02751
R6       EQU   6                   PROGRAM BASE REG              Y02893
R7       EQU   7                   UCB ADDRESS FROM IOSB         Y02146
R8       EQU   8                   BASE REG FOR UCM BASE         Y02893
R9       EQU   9                   SAVES ENTRY CONTENTS REG 13   Y02146
R10      EQU   10                  BASE REG FOR UCM PREFIX     @G51APSS
R11      EQU   11                  WORK REG AND SETLOCK, POST    Y02893
R12      EQU   12                  WORK REG AND SETLOCK, POST    Y02893
R13      EQU   13                  WORK REG AND SETLOCK, POST    Y02893
R14      EQU   14                  LINKAGE REGISTER, RETURN ADDR Y02893
R15      EQU   15                  LINKAGE REGISTER, ENTRY ADDR  Y02893
*              GENERAL EQUATES
ZERO     EQU   0                   CONSTANT OF ZERO
C2       EQU   2                   LENGTH OF 2
C3       EQU   3                   CONSTANT OF 3
C4       EQU   4                   CONSTANT OF 4                YM05671
C12      EQU   12                  CONSTANT OF 12              @ZA16889
CFF      EQU   X'FF'               'AND' MASK                  @G51APSS
UNCHK    EQU   X'02'               UNIT CHECK STATUS            YM05671
         EJECT
***********************************************************************
*  REG 1 CONTAINS ADDRESS OF IOSB AT ENTRY, USE IT FOR IOSB BASE Y02146
***********************************************************************
         SPACE 1                                                 Y02146
         USING IOSB,R1                                           Y02146
         USING UCB,R7              ADDRESSABILITY FOR UCB       YM05671
         SPACE 1                                                 102146
         STM   R0,R15,ZERO(R13)    SAVE REGS IN LOCAL LOCK SAVE  Y02146
         LR    R6,R15              SETUP R6 AS PROGRAM BASE REG  Y02893
         USING IEEBA1,R6           BASE IEAVVCRA                 Y02893
         SPACE 1                                                 Y02893
         MODID ,                                                 Y02893
         SPACE 1                                                 Y02893
         L     R2,CVTPTR           GET BASE FOR CVT              Y02893
         USING CVT,R2              BASE THE CVT DSECT            Y02893
         SPACE 1                                                 Y02893
         L     R8,CVTCUCB          GET PTR TO UCM BASE           Y02893
         USING UCM,R8              BASE THE UCM BASE SECTION     Y02893
         SPACE 1                                                 Y02893
         LR    R9,R13              SAVE LOCAL LOCKS' SAVE ADDR   Y02146
         SPACE 1                                                 Y02751
***********************************************************************
*  GET THE CMS LOCK TO SERIALIZE ON THE UCM RESOURCE.            Y02751
***********************************************************************
         SPACE 1                                                 Y02751
GETCMS   SETLOCK OBTAIN,TYPE=CMS,MODE=UNCOND,                    Y02751*
               RELATED=(UCM,IEAVVCRA(FREECMS))                   Y02751
         SPACE 1                                                 Y02751
         LR    R5,R13              SAVE RETURN CODE OF SETLOCK   Y02751
         EJECT                                                 @ZA16889
***********************************************************************
*        SET UP FRR FOR PROTECTION WHILE HOLDING THE CMS LOCK  @ZA16889
***********************************************************************
         SPACE 1                                               @ZA16889
         L     R11,UCMFRRAD-UCM(R8) GET PTR TO RECOVERY RTN    @ZA16889
         USING PSA,R0                                          @ZA16889
GETFRR   SETFRR A,FRRAD=(R11),PARMAD=(R13),WRKREGS=(R12,R13)   @ZA16889
         LR    R4,R13              SAVE ADDR OF PARMLIST       @ZA16889
         USING PARMLIST,R4         ADDRESSABILITY FOR PARMLIST @ZA16889
         MVC   PARMID,VCRAID       MOVE ID TO PARMLIST         @ZA16889
         LA    R13,VCRACLUP        GET ADDR OF CLEANUP RTN     @ZA16889
         ST    R13,PARMCLAD        MOVE CLEANUP ADDR TO LIST   @ZA16889
         L     R7,IOSUCB           GET UCB ADDR OF DEVICE        Y02146
         EJECT
***********************************************************************
* INITIALIZATION IS COMPLETED. DO REQUIRED PROCESSING.
***********************************************************************
         LM    R11,R13,UCMVEA           LOAD PTR TO UCM LIST, SIZE OF
*                                       AN ENTRY, AND ADDR OF LAST
*                                       ENTRY IN UCM LIST
         USING UCMLIST,R11         BASE THE UCME                 Y02893
UCMLOOP  CL    R7,UCMUCB                COMPARE UCB ADDRESSES (IOS-UCM)
         BE    TESTERP                  IF EQUAL, BRANCH
NXTENTRY BXLE  R11,R12,UCMLOOP          WILL NEVER FALL THRU     A30203
         B     SAFE                     BUT IF IT DOES, HANDLE IT
         SPACE 1                                                 Y02146
***********************************************************************
**  SEE IF ERP HAS CONTROL
***********************************************************************
TESTERP  TM    IOSFLA,IOSERR       IS ERP IN CONTROL ?           Y02146
         BZ    SETFLAG             NO, GO SET THE FLAG
*  IF ERP IS IN CONTROL, CHECK FOR THE READER
         CLC   UCMNAME+C3(C2),RDRNAME  F0F2 IN UCMNAME SIGNIFIES THE
*                                      2540 PROCESSOR HAD CONTROL
         BE    SAFE                IF A RDR, DO NOT TURN ON ATN FLG
         SPACE 1                                                 Y02146
SETFLAG  EQU   *
         TM    UCMATR,UCMIF        DOES DEVICE SUPPORT ATTENTIONS?     X
                                                               @G51APSS
         BNO   NXTENTRY            NO, CONTINUE SCAN.            A30203
***********************************************************************
*        SEE IF A NO-CONSOLES CONDITION EXISTS                 @G51APSS
***********************************************************************
         SPACE 1                                               @G51APSS
         LA    R15,C4              OFFSET OF UCM PREFIX PTR    @G51APSS
         LR    R10,R8              ADDR OF UCM BASE            @G51APSS
         SR    R10,R15             ADDR OF UCM PREFIX PTR      @G51APSS
         L     R10,0(R10)          ADDR OF UCM PREFIX          @G51APSS
         USING UCMPRFX,R10         BASE THE UCM PREFIX         @G51APSS
         TM    UCMSFLG1,UCMSYSE    DOES A NO-CONSOLES CONDITION EXIST? X
                                                               @G51APSS
         BO    NOCON               YES, BRANCH                 @G51APSS
         DROP  R10                 FREE BASE OF UCM PREFIX     @G51APSS
         TM    UCMATR,UCMUF        IS THE DEVICE ACTIVE?       @G51APSS
         BNO   NXTENTRY            NO, CONTINUE SCAN           @G51APSS
         TM    UCMSTS,UCMCF        IS CLOSE PENDING ON CONSOLE   Y02752
         BO    NXTENTRY            IF  SO, CONTINUE SCAN         Y02752
         OI    UCMSTS,UCMAF        YES, SET ATTENTION PENDING FLAG.
         TM    UCMDISP1,UCMDISPC   IS THIS A GRAPHIC DEVICE?    YM05671
         BNO   POSTCT              IF NO, CONTINUE PROCESSING   YM05671
         TM    IOSTSA,UNCHK        UNIT CHECK WITH ATTENTION    YM05671
         BZ    POSTCT              NO, DON'T MOVE SENSE BYTE    YM05671
* THE FOLLOWING MOVE WILL GET THE TWO BYTES OF SENSE DATA       YM05671
* FROM FIELD 'IOSSNS' AND THE FIRST TWO BYTES FROM 'IOSATTSN'.  YM05671
*                                                               YM05671
         MVC   UCBSNS(C4),IOSSNS   MOVE SENSE DATA TO UCB       YM05671
         EJECT                                                 @G51APSS
***********************************************************************
*        POST THE ATTENTION ECB AFTER SETTING UP THE           @G51APSS
*        REQUIRED REGS                                         @G51APSS
***********************************************************************
         SPACE 1
POSTCT   EQU   *
         SR    R10,R10             COMPLETION CODE OF ZERO     @G51APSS
         LA    R11,UCMAECB         ECB ADDRESS
         L     R15,CVT0PT01        LOAD ENTRY POINT OF POST    @G51APSS
         SR    R12,R12             NO ERRET ADDR FOR LOCAL POST  Y02752
         SR    R13,R13             NO ASCB ADDR FOR LOCAL POST   Y02752
         SPACE 1                                                 Y02752
         BALR  R14,R15             BRANCH TO POST SERVICE ROUTINE
         SPACE 1                                                 Y02752
SAFE     DS    0H                                                Y02752
FREEFRR  SETFRR D,WRKREGS=(R12,R13) RELEASE FRR                @ZA16889
         LTR   R5,R5               TEST SETLOCK RETURN CODE      Y02751
         BNZ   DONTFREE            IF RC NONZERO, LOCK WAS HELD  Y02751
*                                  AT ENTRY TO MODULE, DON'T     Y02751
*                                  FREE THE CMS LOCK.            Y02751
         SPACE 1                                                 Y02751
FREECMS  SETLOCK RELEASE,TYPE=CMS,                               Y02751*
               RELATED=(UCM,IEAVVCRA(GETCMS))                    Y02751
         SPACE 1                                                 Y02752
DONTFREE DS    0H                  FOR SKIPPING FREEING CMS LOCK Y02751
         LM    R0,R15,ZERO(R9)     RESTORE CALLERS REGS          Y02146
         BR    R14                 RETURN TO IOS                   0773
         EJECT                                                 @G51APSS
***********************************************************************
*        A NO-CONSOLES CONDITION EXISTS                        @G51APSS
*        CHECK TO SEE IF A CANDIDATE HAS ALREADY BEEN CHOSEN   @G51APSS
***********************************************************************
         SPACE 1                                               @G51APSS
NOCON    EQU   *                                               @G51APSS
         L     R13,UCMBFEXT        ADDR OF UCM FIXED EXTENSION BASE    X
                                                               @G51APSS
         USING UCMFEXTA,R13        BASE THE UCM FIXED EXTENSION BASE   X
                                                               @G51APSS
         L     R12,UCMFATCN        ADDR OF CANDIDATES UCME     @G51APSS
         LTR   R12,R12             CANDIDATE CHOSEN YET?       @G51APSS
         BNZ   SAFE                YES, IGNORE ATTN            @G51APSS
         ST    R11,UCMFATCN        SAVE CANDIDATES UCME ADDR   @G51APSS
         DROP  R13                 FREE BASE OF UCM FIXED EXTENSION    X
                                   BASE                        @G51APSS
         SPACE 1                                               @G51APSS
***********************************************************************
*        THIS DEVICE IS THE CANDIDATE. MARK IT AS IF A         @G51APSS
*        VARY CONSOLE WAS ISSUED FOR IT                        @G51APSS
*          1) ATTENTION PENDING(UCMAF) SET OFF                 @G51APSS
*          2) OPEN PENDING(UCMTA) SET ON                       @G51APSS
*          3) DEVICE IS ACTIVE(UCMUF) SET ON                   @G51APSS
*          4) CLOSE PENDING(UCMCF) SET OFF                     @G51APSS
*          5) DEVICE IS ONLINE(UCBONLI) SET ON                 @G51APSS
*          6) DEVICE IS AN ACTIVE CONSOLE(UCBSYSR) IS SET ON   @G51APSS
*          7) CHANGING STATUS OUT OF CONSOLE STATE(UCBDADI)    @G51APSS
*             IS SET OFF                                       @G51APSS
*          8) STATUS CHANGING TO OFFLINE(UCBCHGS) IS SET OFF   @G51APSS
*          9) OFFLINE BY VARY(UCBVORSN) SET OFF                @G51APSS
***********************************************************************
         SPACE 1                                               @G51APSS
         SR    R12,R12             USED IF CANDIDATE IS A COMPOSITE    X
                                                               @G51APSS
MARKDEV  EQU   *                                               @G51APSS
         OI    UCMSTS,UCMTA        MARK OPEN PENDING           @G51APSS
         OI    UCMATR,UCMUF        MARK DEVICE ACTIVE          @G51APSS
         NI    UCMSTS,CFF-UCMCF-UCMAF TURN OFF CLOSE+ATTN PENDING      X
                                                               @G51APSS
         SPACE 2                                               @G51APSS
***********************************************************************
*        THE FOLLOWING INSTRUCTION WILL BE PROTECTED BY THE    @G51APSS
*        COMPARE-AND-SWAP INSTRUCTION.                         @G51APSS
*                                                              @G51APSS
*        OI    UCBSTAT,UCBONLI+UCBSYSR                         @G51APSS
*                                                              @G51APSS
***********************************************************************
         SPACE 1                                               @G51APSS
         OIL   UCBSTAT,UCBONLI+UCBSYSR,REF=UCBOB,WREGS=(R15,R14,R13)   X
                                   TURN ON DEVICE IS ONLINE AND A      X
                                   CONSOLE                     @G51APSS
         SPACE 2                                               @G51APSS
***********************************************************************
*        THE FOLLOWING INSTRUCTION WILL BE PROTECTED BY THE    @G51APSS
*        COMPARE-AND-SWAP INSTRUCTION.                         @G51APSS
*                                                              @G51APSS
*        NI    UCBSTAT,CFF-UCBCHGS-UCBDADI                     @G51APSS
*                                                              @G51APSS
***********************************************************************
         SPACE 1                                               @G51APSS
         NIL   UCBSTAT,CFF-UCBCHGS-UCBDADI,REF=UCBOB,                  X
               WREGS=(R15,R14,R13) TURN OFF CHANGING TO OFFLINE AND    X
                                   GOING OUT OF CONSOLE STATUS @G51APSS
         SPACE 2                                               @G51APSS
***********************************************************************
*        THE FOLLOWING INSTRUCTION WILL BE PROTECTED BY THE    @G51APSS
*        COMPARE-AND-SWAP INSTRUCTION.                         @G51APSS
*                                                              @G51APSS
*        NI    UCBWGT,CFF-UCBVORSN                             @G51APSS
*                                                              @G51APSS
***********************************************************************
         SPACE 1                                               @G51APSS
         NIL   UCBWGT,CFF-UCBVORSN,REF=UCBOB,WREGS=(R15,R14,R13)       X
                                   TURN OFF OFFLINE BY VARY    @G51APSS
         SPACE 2                                               @G51APSS
***********************************************************************
*        CHECK TO SEE IF CANDIDATE IS A COMPOSITE              @G51APSS
***********************************************************************
         SPACE 1                                               @G51APSS
         L     R14,UCMCOMPC        ADDR OF COMPOSITE CONSOLE   @G51APSS
         LTR   R14,R14             IS CANDIDATE A COMPOSITE?   @G51APSS
         BZ    SAFE                NO, CONTINUE                @G51APSS
         LTR   R12,R12             YES, WAS OUTPUT HALF MARKED?        X
                                                               @G51APSS
         BNZ   RESTUCB             YES, RESTORE REGS           @G51APSS
         LR    R12,R7              SAVE INPUT HALF'S UCB ADDR  @G51APSS
         LR    R11,R14             BASE UCME ON OUTPUT HALF    @G51APSS
         L     R7,UCMUCB           OUTPUT HALF'S UCB ADDR      @G51APSS
         B     MARKDEV             GO MARK OUTPUT HALF         @G51APSS
RESTUCB  EQU   *                                               @G51APSS
         LR    R11,R14             RESTORE PTR TO INPUT HALF'S UCME    X
                                                               @G51APSS
         LR    R7,R12              RESTORE PTR TO INPUT HALF'S UCB     X
                                                               @G51APSS
         B     SAFE                CONTINUE                    @G51APSS
         EJECT                                                 @ZA16889
***********************************************************************
*        THE FOLLOWING CODE WILL BE BRANCHED TO FROM THE       @ZA16889
*        RECOVERY ROUTINE - IEAVMFRR. THIS CODE WILL FREE THE  @ZA16889
*        CMS LOCK AND RETURN TO THE RECOVERY ROUTINE.          @ZA16889
***********************************************************************
         SPACE 1                                               @ZA16889
VCRACLUP EQU   *                                               @ZA16889
         USING *,R15               ADDRESSABILITY FRR ENTRY    @ZA16889
         STM   R14,R12,C12(R13)    SAVE CALLERS REGS           @ZA16889
         LR    R9,R13              SAVE SAVEAREA PTR           @ZA16889
         SPACE 1                                               @ZA16889
RFREFRR  SETFRR D,WRKREGS=(R12,R13) RELEASE FRR                @ZA16889
         SPACE 1                                               @ZA16889
         SETLOCK RELEASE,TYPE=CMS,                             @ZA16889X
               RELATED=(UCM,IEAVVCRA(GETCMS))                  @ZA16889
         SPACE 1                                               @ZA16889
         LR    R13,R9              RESTORE SAVEAREA PTR        @ZA16889
         LM    R14,R12,C12(R13)    RESTORE CALLERS REGS        @ZA16889
         BR    R14                 RETURN TO RECOVERY ROUTINE  @ZA16889
         EJECT                                                 @G51APSS
***********************************************************************
*        CONSTANTS                                             @G51APSS
***********************************************************************
         SPACE 1                                               @G51APSS
         DS    0F                                              @G51APSS
VCRAID   DC    CL4'VCRA'           IDENTIFIER FOR ATTN RTN     @ZA16889
RDRNAME  DC    X'F0F2'             TO CHECK FOR THE 2540 PROCESSOR
         TITLE 'IEAVVCRA..AOS/2..FRR DSECT'                    @ZA16889
         IHAFRRS                                               @ZA16889
         TITLE 'IEAVVCRA..AOS/2..FRR USER''S PARM LIST'        @ZA16889
         IHACTM FTPT                                           @ZA16889
         TITLE 'IEAVVCRA..AOS/2..UCB DSECT'                    @G51APSS
UCB      DSECT
         IEFUCBOB LIST=YES
         TITLE 'IEAVVCRA..AOS/2..CVT DSECT'
         CVT   DSECT=YES
         TITLE 'IEAVVCRA..AOS/2..IOSB DSECT'
         IECDIOSB
         TITLE 'IEAVVCRA..AOS/2..UCM DSECT'
         IEECUCM  FORMAT=NEW
         TITLE 'IEAVVCRA..AOS/2..PSA DSECT'                      Y02751
         IHAPSA
         END
