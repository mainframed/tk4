         TITLE 'IEAVMWTL -- NIP BUFFER WRITER '                  Y02752
*
* STATUS
*
*    VS/2 RELEASE 2                                              Y02752
*
* FUNCTION
*
*    THE PURPOSE OF THIS ROUTINE IS TO WRITE THE MESSAGES
*    TO HARD COPY WHICH WERE PRODUCED DURING NIP AND TO FREE THE
*    STORAGE OBTAINED FOR THE BUFFER.
*
* ENTRY POINTS
*
*    IEAVMWTL
*
*    MAIN ENTRY POINT FROM IEAVMQWR
*
* INPUT
*
*    SUPERVISOR STATE, KEY ZERO
*
*    REGISTERS
*
*    GR 0 - GR 12 IRRELEVANT
*    GR 13      POINTER TO SAVE AREA ADDRESS
*    GR 14      RETURN ADDRESS
*    GR 15      IRRELEVANT
*
* OUTPUT
*
*    PSW UNCHANGED
*    GR 0 - GR 13   UNCHANGED
*    GR 14          RETURN ADDRESS
*    GR 15          UNCHANGED
*
* EXTERNAL REFERENCES
*
*    FREEMAIN
*    WTO
*
* EXITS,NORMAL
*
*    TO SVC EXIT VIA BR 14
*
* EXITS,ERROR
*
*    NONE
*
* TABLES/WORK AREAS
*
*    CVT
*    EIL
*    UCM
*    UCMPRFX
*    WPL
*
* ATTRIBUTES
*
*    SERIALLY REUSABLE
*
* CHARACTER CODE DEPENDENCY   NA
*
* NOTES
*
*    THIS ROUTINE IS USED ONCE PER IPL.
*
          EJECT
IEAVMWTL CSECT
* MODULE CREATED FOR MCS - REL 18
**********************************************************************
*
*
*                    REGISTER  EQUATES
*
*
**********************************************************************
X0       EQU   0                   REGISTER 0, UNUSED            Y02893
X1       EQU   1                   WPL (PARAMETER LIST) BASE     Y02893
X2       EQU   2                   REGISTER 2, UNUSED            Y02893
X3       EQU   3                   WORK REG.
X4       EQU   4                   MCS PREFIX REG.
X5       EQU   5                   BASEA REGREGISTER 5, UNUSED   Y02893
X6       EQU   6                   UCM BASE REG                  Y02893
X7       EQU   7                   UCMEIL BASE REG               Y02893
X8       EQU   8                   WORK REG.
X9       EQU   9                   REGISTER 9, UNUSED            Y02893
XA       EQU   10                  REGISTER 10, UNUSED           Y02893
XB       EQU   11                  REGISTER 11, UNUSED           Y02893
XC       EQU   12                  BASE REGISTER
XD       EQU   13                  PTR TO SAVE AREA              Y02893
XE       EQU   14                  RETURN REGISTER.
XF       EQU   15                  RETURN CODE REGISTER.
*
ZERO     EQU   0                   ZERO
FOUR     EQU   4                   USED TO ADDRESS THE PREFIX PTR
NIPBUFLN EQU   4096                SYMBOLIC LENGTH OF BUFFER     Y02752
NIPBUFSP EQU   255                 SYMBOLIC SUBPOOL NO. FOR BUF  Y02752
NOMORE   EQU   X'7B'               TEST FOR END OF THE PARAMETER LIST.
         EJECT
         STM   X0,XF,ZERO(XD)      SAVE REGS FOR QWR           SRB299.1
         BALR  XC,ZERO             SET UP BASE REG               Y02893
         USING *,XC                MAKE 12 THE BASE
         L     X6,CVTPTR           GET PTR TO THE CVT
         USING CVT,X6              MAKE 6 THE TEMP CVT BASE
         L     X6,CVTCUCB          LOAD PTR TO UCM.
         DROP  X6                  DROP TEMP CVT BASE
         USING UCM,X6              MAKE 6 THE UCM BASE
         L     X7,UCMLSTP          GET PTR TO EIL
         USING UCMEIL,X7           MAKE 7 THE EIL BASE
         L     X1,UCMNIPTR         GET PTR TO 2 K BUFFER
         LA    X3,FOUR             PREPARE TO GET PREFIX POINTER
         LR    X4,X6               DUPLICATE THE UCM BASE PTR
         SR    X4,X3               PREFIX PTR IS 4 BYTES BEFORE UCM
         L     X4,ZERO(X4)         GET PTR TO MCS PREFIX
         USING UCMPRFX,X4          SET 4 AS PREFIX BASE
         TM    UCMSFLGS,UCMSYSB    IS HARDCOPY NEEDED
         BZ    FINI                NO GO TO FREEMAIN
* AT THIS POINT, IT IS NECESSARY TO LOOP THROUGH THE BUFFER AND WRITE
* ALL THE MESSAGES TO HARD COPY.
         USING WPL,X1              SET 1 AS THE PARAM LIST BASE
WTOHALF  CLI   WPLLGH,NOMORE       IS THIS THE END OF THE LIST
         BE    FINI                YES, GO DO CLEANUP WORK.
         OI    WPLMCSF1,WPLMCSFG   QUEUE TO HARD COPY
         OI    WPLMCSF2,WPLMCSFI   NO TIME STAMP REQUESTED
         LR    X8,X1               SAVE ADR. OF PARM. LIST
         SPACE 1                                                 Y02893
**************************************************************** Y02893
*                                                                Y02893
*  THE MESSAGES IN THE NIP BUFFER ARE IN THE FORM OF WPL'S.      Y02893
*  ISSUE AN EXECUTE FORM WTO WITH REGISTER 1 POINTING TO THE     Y02893
*                                                                Y02893
**************************************************************** Y02893
         SPACE 1                                                 Y02893
         WTO   MF=(E,(1))          ISSUE WTO AGAINST NEXT MSG    Y02893
         SPACE 1                                                 Y02893
         LR    X1,X8               RESTORE ADR. SAVED ABOVE.
         AH    X1,WPLLGH           UPDATE THE PTR.
         B     WTOHALF             CONTINUE THROUGH PARM. LIST.
         EJECT
**************************************************************** Y02893
*                                                                Y02893
*    BEFORE EXIT, FREE THE NIP BUFFER. (A BUFFER IS ALWAYS       Y02893
*    OBTAINED.)                                                  Y02893
*                                                                Y02893
**************************************************************** Y02893
         SPACE 1                                                 Y02893
FINI     L     X1,UCMNIPTR         GET PTR FOR FREEING STORAGE AREA.
         SPACE 1                                                 Y02752
         FREEMAIN R,SP=NIPBUFSP,LV=NIPBUFLN,A=(1)  FREE BUFFER   Y02752
         SPACE 1                                                 Y02752
         XC    UCMNIPTR,UCMNIPTR   SET PTR TO ZERO.
*
*    EXIT
*
         LM    X0,XF,ZERO(XD)      RESTORE REGS FOR QWR        SRB299.1
         BR    XE                  RETURN.
         SPACE 2
PATCH    DC    XL50'00'            MODULE PATCH AREA             Y02893
         TITLE 'IEAVMWTL..AOS/2..CVT'
         CVT   DSECT=YES
         TITLE 'IEAVMWTL..AOS/2..UCM'
         IEECUCM   FORMAT=NEW
         TITLE 'IEAVMWTL..AOS/2..WPL'
         IEZWPL
         END
