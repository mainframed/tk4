         TITLE 'IEAVMWSV - COMM TASK QUEUEING ROUTINE'
IEAVMWSV CSECT
* A 260502-260640,311200                                       @ZA01916
* C 001760,002730                                              @ZA01916
* C (ENQTSTCN) APPROX. 156500, C (ENQTSTCD) APPROX. 165000,    @ZA11370
* C (ENQQR01) APPROX. 211170, C (ENQMASTR) APPROX. 186000      @ZA11370
* C (ENQMASTR) APPROX. 185700                                  @ZA16551
***** START OF SPECIFICATIONS *******************************
*
*01*     MODULE-NAME=IEAVMWSV
*
*01*     DESCRIPTIVE-NAME= COMM TASK CONSOLE QUEUEING ROUTINE
*
*01*     COPYRIGHT=NONE
*
*01*     STATUS=RELEASE 3                                      @ZA11370
*
*01*     FUNCTION= TO SELECTIVELY QUEUE MESSAGES TO CONSOLES' OUTPUT
*             QUEUES. THE CRITERIA FOR QUEUEING IS A FUNCTION OF
*             MESSAGE ROUTING CODES, THE MSGTYP PARAMETER, MCS FLAGS
*             AND TYPE OF CONSOLE. THIS MODULE ALSO DETERMINES IF THE
*             MESSAGE SHOULD BE HARDCOPIED.
*
*02*          OPERATION= A WQE IS SELECTED FROM THE SYSTEM OUTPUT
*                  QUEUE. SUBROUTINE IEECMENQ IS INVOKED TO QUEUE THE
*                  WQE TO ALL APPROPRIATE CONSOLES. IT SEARCHES THE
*                  UCMES TO DETERMINE WHICH CONSOLES SHOULD RECEIVE
*                  THE MESSAGE. IF THE MESSAGE IS TO BE QUEUED,
*                  SUBROUTINE ENQCNSLE IS INVOKED TO CREATE A CQE
*                  ENTRY ON THE CONSOLE OUTPUT QUEUE. IF A WQE IS
*                  QUEUED TO AN INACTIVE CONSOLE, THE QREG0 PROCESSOR
*                  IS ATTACHED. IF A WQE WITH MSGTYPE IS FOUND AND
*                  THERE ARE MONITORING TSO TERMINALS, THE TPUT
*                  ROUTINE IS ATTACHED IF NECESSARY AND POSTED. WHEN
*                  ALL THE UCMES HAVE BEEN SEARCHED FOR ONE WQE,
*                  ANOTHER WQE IS SELECTED. WHEN THE QUEUE OF WQES IS
*                  EXHAUSTED, IEAVMDSV IS CALLED TO BEGIN OUTPUTTING
*                  THE MESSSAGES.
*
*01*     NOTES=NONE
*
*02*          DEPENDENCIES= THERE IS A SYSGEN DEPENDENCY THAT THE TWO
*                  UCMES FOR A COMPOSITE CONSOLE ARE CONTIGUOUS WITH
*                  THE UCME FOR THE INPUT PORTION OF THE CONSOLE
*                  SPECIFIED FIRST.
*
*                  THE CALLER OF THIS ROUTINE MUST HOLD THE CMS AND
*                  LOCAL LOCKS.
*
*03*               CHARACTER-CODE-DEPENDENCIES= CHARACTER CODE
*                       INDEPENDENT
*
*02*          RESTRICTIONS= THE FOLLOWING REGISTERS AND FIELDS
*                  MUST BE PRESERVED ACROSS THE INTERFACE
*                  TO IEAVMDSV:
*                  R2= ADDR OF UCM
*                  R3= ADDR OF UCME
*                  R4= ADDR OF UCM PREFIX
*                  R8= ADDR OF THE EIL
*                  R9= BASE REGISTER OF THE WAIT ROUTINE
*                  UCMSAVE0 IN UCM= RETURN ADDR TO THE WAIT
*                                   ROUTINE.
*
*02*          REGISTER-CONVENTIONS= SEE LABEL WSVREGS
*
*02*          PATCH-LABEL=PATCH                                @ZA01916
*
*01*     MODULE-TYPE=PROCEDURE
*
*02*          PROCESSOR=ASSEMBLER-370R
*
*02*          MODULE-SIZE=2500 BYTES                           @ZA16551
*
*02*          ATTRIBUTES= KEY 0, SUPERVISOR STATE, SERIALLY REUSABLE,
*                  RESIDES IN MASTER SCHEDULER MEMORY, PART OF  LOAD
*                  MODULE IEAVCTSK OF THE SYS1.LPALIB DATA SET.
*
*01*     ENTRY-POINT=IEAVMWSV
*
*02*          PURPOSE= TO QUEUE WQES ON THE SYSTEM OUTPUT QUEUE TO
*                  THE CONSOLES OUPUT QUEUES.
*
*02*          LINKAGE= BRANCH AND LINK FROM IEAVMQWR WHEN THE WTO ECB
*                  IS POSTED
*
*02*          INPUT=    RO,R1,R3,R5-R9,R10-R13= IRRELEVANT
*                       R2=ADDR OF UCM
*                       R4=ADDR OF UCM PREFIX
*                       R14=RETURN ADDR
*                       R15=ENTRY POINT ADDR
*
*02*          OUTPUT=NONE
*
*02*          REGISTERS-SAVED=NONE
*
*02*          REGISTER-USAGE=R6,R7,R12,R10,R11,R13,R0,R1,R15
*
*02*          REGISTERS-RESTORED=NONE
*
*01*     ENTRY-POINT= IEECMENQ
*
*02*            PURPOSE= TO QUEUE A WQE TO ALL APPROPRIATE CONSOLES.
*
*02*          LINKAGE= BRANCH AND LINK FROM IEAVSWCH.  CONSOLE SWITCH
*                  CREATES ITS OWN WQE IN ORDER TO ISSUE A MESSAGE
*                  TO A CONSOLE AND CALLS THIS ROUTINE TO QUEUE IT.
*
*02*          INPUT=    R1=WQE ADDR
*                       R2=UCM ADDR
*                       R4=UCM PREFIX ADDR
*                       R14=RETURN ADDR
*                       R15=ENTRY POINT ADDR
*                       R0,R3,R5-R13= IRRELEVANT
*
*02*          OUTPUT=NONE
*
*02*          REGISTERS-SAVED=R3-R14
*
*02*          REGISTER-USAGE=R0,R1,R3,R5-7,R9-13,R15
*
*02*          REGISTERS-RESTORED=R3-R14
*
*01*     ENTRY-POINT=IEECMQCN
*
*02*          PURPOSE= TO CREATE A CQE ENTRY ON A GIVEN CONSOLES'S
*                  OUTPUT QUEUE.
*
*02*          LINKAGE= BRANCH AND LINK FROM IEAVMDSV TO QUEUE A
*                  HARDCOPY MESSAGE TO THE HARDCOPY CONSOLE.
*
*02*          INPUT=    R2=UCM ADDR
*                       R4=UCM PREFIX ADDR
*                       R6=WQE ADDR
*                       R9=RETURN ADDR
*                       R11=UCME ADDR
*                       R1=ENTRY POINT ADDR
*
*02*          OUTPUT=NONE
*
*02*          REGISTERS-SAVED=R3,R4,R15
*
*02*          REGISTER-USAGE=R0,R1,R5,R8,R10,R14,R15
*
*02*          REGISTERS-RESTORED=R3,R4,R15
*
*01*     EXIT-NORMAL= TO CALLER VIA BRANCH TO RETURN ADDR
*
*02*          CONDITIONS= FUNCTION COMPLETE
*
*02*          OUTPUT=NONE
*
*02*          RETURN-CODES= NONE
*
*01*     EXIT-ERROR=NONE
*
*01*     EXTERNAL-REFERENCES=SEE BELOW
*
*02*          ROUTINES= WHEN ENTERED AT IEAVMWSV, IEAVMDSV IS CALLED
*                  TO BEGIN PROCESSING OUTPUT AFTER ALL THE WQES HAVE
*                  BEEN QUEUED.
*
*02*          DATA-AREAS=NONE
*
*02*          CONTROL-BLOCKS=IEAMMB-C,W,  IHAWQE-W,  IEECUCM-W,
*                  IHACTM(CQE)-C,W,  IHASCVT-R,  CVT-R
*
*01*     TABLES=NONE
*
*01*     MACROS=ATTACH, SETLOCK, GETMAIN
*
*02*          SERIALIZATION= THE CMS AND LOCAL LOCKS ARE HELD
*                  TO SERIALIZE USE OF THE UCM, WQE AND CQE QUEUES.
*
*01*     CHANGE-ACTIVITY= ZA01916, OZ11370, OZ16551            @ZA16551
*
*01*     MESSAGES=NONE
*
*01*     ABEND-CODES=NONE
*
**** END OF SPECIFICATIONS **************************************
         ENTRY IEECMENQ
         ENTRY IEECMQCN
          EJECT
WSVREGS  DS    0H                  REGISTER-USAGE                Y02893
         SPACE ,                                                 Y02893
R0       EQU   0              WTOSERV- UNUSED
*                             ENQ- PARAMETER REG, WORKREG ONLY
R1       EQU   1              PARAMETER REG, WORKREG ONLY
R2       EQU   2              UCM ADDRESS
R3       EQU   3              WTOSERV- RESERVED
*                             ENQ- MESSAGE ROUTED COUNT
R4       EQU   4                   MCS UCM ADDRESS
R5       EQU   5              WTOSERV- RESERVED
*                             ENQ- WORKREG ONLY
R6       EQU   6              WQE ADDRESS
R7       EQU   7                  WTOSERV-WORKREG  ENQ- NON-GRPH COUNT
R8       EQU   8              WTOSERV- WORKREG
*                             ENQ- UNUSED,WORKREG ONLY
R9       EQU   9              WTOSERV- RESERVED
*                             ENQ- WORKREG
R10      EQU   10                 WTOSERV-BASE    ENQ-BASE
R11      EQU   11             WTOSERV- UNUSED   ENQ-  UCM ENTRY
R12      EQU   12             WTOSERV- UNUSED   ENQ-    BXLE
R13      EQU   13             WTOSERV- UNUSED   ENQ-    REGS
R14      EQU   14             RETURN REG
R15      EQU   15             BRANCH REG
ZERO     EQU   0
ONE      EQU   1
TWO      EQU   2
THREE    EQU   3
FOUR     EQU   4
SEVEN    EQU   7
EIGHT    EQU   8                   USED AS DIVISOR, OFFSET       Y02893
FOURTEEN EQU   14
SIXTEEN  EQU   16
SWMASK   EQU   255
BLKLEN   EQU   24
TWENTY5  EQU   25                  SHIFT VALUE TO MAKE MASK      S21002
POST     EQU   X'40'               POST FLAG IN AN ECB           Y02756
         EJECT
         LR    R10,R15             SET UP BASE REGISTER          Y02893
         USING IEAVMWSV,R10        ESTABLISH ADDRESSABILITY      Y02893
         USING UCM,R2              PTR TO UCM - SET BY QWR       Y02893
         USING UCMPRFX,R4          PTR TO UCM PREFIX - SET UP BY Y02893
*                                  QWR                           Y02893
         USING UCMLIST,R11         PTR TO 1ST UCME               Y02893
         USING WQE,R6              PTR TO WQE                    Y02893
         SPACE  5
         MODID
         SPACE
         L     R6,UCMWTOQ          GET 1ST WQE IN SYSTEM QUEUE
         SPACE
WTOWQE   LA    R6,ZERO(R6)         ZERO HI BYTE
         LTR   R6,R6               IS THIS LAST WQE
         BZ    WTOMNEP             YES, DO MONITOR EPILOGUE      Y02756
         TM    WQEXA,WQESUSP       PROCESSING TEMPORILY          Y02757
*                                  SUSPENDED                     Y02757
         BO    WTONEXT             YES, DO NOT PROCESS WQE       Y02757
         TM    WQEAVAIL,WQEBUFE    NO,BUT HAS THIS WQE BEEN      Y02893
*                                  SERVICED                      Y02893
         BZ    WTOSERV             NO,GO TO SERVICE IT
         CLI   WMJMMLW,ZERO        MAJOR WQE                     S21002
         BE    WTONEXT             NO                            S21002
         TM    WMJMMLW,WMJMMLWD    CHAIN ALTERED BY SVC 35       S21002
         BO    ALTMLWTO            YES, SERVICE CHAIN            Y02893
         SPACE
WTONEXT  L     R6,WQELKP           GET NEXT WQE
         B     WTOWQE              LOOP TO TEST FOR LAST
         SPACE 2
WTOSERV  EQU   *
         NI    WMJMMLW,SWMASK-WMJMMLWF   TURN OFF RE-USE FLAG    S21002
         SPACE
         ST    R14,UCMSAVE0        SAVE RETURN ADDRESS TO ROUTER
         LR    R1,R6
         L     R15,UCMQRTN
         BALR  R14,R15             GO TO ENQ RTN
         L     R14,UCMSAVE0        RESTORE RETURN ADDRESS
         B     WTONEXT             GET NEXT WQE
         EJECT ,                                                 Y02756
WTOMNEP  EQU   *                   MONITOR EPILOGUE
***********************************************************************
*                                                                     *
*        BEFORE EXIT TO THE DEVICE SERVICE ROUTINE TO BEGIN           *
*        PROCESSING CONSOLE OUTPUT QUEUES, A CHECK IS MADE TO         *
*        DETERMINE IF THE TPUT ROUTINE SHOULD BE ATTACHED.  IF        *
*        A MESSAGE WAS QUEUED FOR A MONITORING TERMINAL AND THE       *
*        TPUT ROUTINE IS NOT ATTACHED, IT IS ATTACHED HERE TO AVOID   *
*        RELEASING THE CMS LOCK WHILE PROCESSING THE SYSTEM OUTPUT    *
*        QUEUE.                                                       *
*                                                                     *
***********************************************************************
         ST    R14,UCMSAVE0        SAVE RETURN ADDRESS TO ROUTER Y02756
         TM    UCMMNECB,POST       IS THE MONITOR ECB POSTED     Y02756
         BZ    WTODVSV             NO, NO NEED TO ATTACH         Y02756
         TM    UCMMODE,UCMTPUTA    IS THE TPUT ROUTINE ALREADY   Y02756
*                                  ATTACHED                      Y02756
         BO    WTODVSV             YES, NO NEED TO ATTACH        Y02756
         SPACE
*                                                                Y02756
* NOTE:  SETLOCK WILL DESTROY REGISTERS 11,12,13,14              Y02756
*                                                                Y02756
         SPACE
         SETFRR D,WRKREGS=(R11,R12)                              Y02755
         SPACE
         SETLOCK RELEASE,TYPE=CMS,                                     *
               RELATED=(UCM,IEAVMQWR(WRLOCK),IEAVMWSV(WTOMNEP))  Y02752
         SETLOCK RELEASE,TYPE=LOCAL,                                   *
               RELATED=(UCM,IEAVMQWR(WRLOCK),IEAVMWSV(WTOMNEP))  Y02752
         ATTACH SF=(E,ATTPARM) ATTACH TPUT ROUTINE               Y02756
         SETLOCK OBTAIN,TYPE=LOCAL,MODE=UNCOND,                        *
               RELATED=(UCM,IEAVMWSV(WTOMNEP))                   Y02752
         SETLOCK OBTAIN,TYPE=CMS,MODE=UNCOND,                          *
               RELATED=(UCM,IEAVMWSV(WTOMNEP))                   Y02752
         SPACE
         SETFRR A,FRRAD=UCMFRRAD,PARMAD=(R12),WRKREGS=(R11,R12)  Y02755
         USING PARMLIST,R12        ADDRESSABILITY TO PARM AREA   Y02755
         MVC   PARMID,CTSK         PUT LOAD MODULE ID            Y02755
         DROP  R12
         SPACE
         OI    UCMMODE,UCMTPUTA    INDICATE TPUT ROUTINE IS      Y02756
*                                  ATTACHED                      Y02756
         SPACE
WTODVSV  SR    R1,R1               CODE=0 FOR OUTPUT
         L     R15,ADDVSV          GET ADDR OF DEVSERV
         BALR  R14,R15             GO TO DEVSERV
         L     R14,UCMSAVE0        RESTORE RETURN ADDR
         BR    R14                 RETURN TO WAIT ROUTINE
         EJECT ,                                                 Y02893
ALTMLWTO EQU   *                   SERVICE ALTERED MLWTO CHAIN
***********************************************************************
*                                                                     *
*          MORE LINES MAY BE ADDED TO A MLWTO CHAIN AFTER IT HAS      *
*        BEEN QUEUED TO THE CONSOLES OUTPUT QUEUES.  A SEARCH IS      *
*        DONE OF THE CQE BLOCKS OF EACH UCME LOOKING FOR A MATCHING   *
*        WQE PTR IN THE CQE ENTRIES.  WHEN A MATCH IS FOUND,          *
*        OUTPUT PENDING IS INDICATED IN THE UCME.  IF THE MLWTO       *
*        SHOULD GO TO A MONITORING TSO TERMINAL, A MMB IS BUILT FOR   *
*        EACH TEXT LINE NOT ALREADY QUEUED FOR TPUT TO MONITORING     *
*        TERMINALS.                                                   *
*                                                                     *
***********************************************************************
         SPACE
         STM   R3,R14,UCMSVB0      SAVE REGS IN UCM SAVEAREA     Y02756
         SR    R8,R8               INIT NUMBER OF CQE ENTRIES    Y02756
         LA    R7,ONE              INIT START-AT-TOP INDICATOR   Y02893
         NI    WMJMMLW,SWMASK-WMJMMLWD TURN OFF ALTERED FLAG
         TM    WMJMMLW,WMJMMLWF    START AT TOP OF CHAIN         S21002
         BNO   ALTER5            NO
         SR    R7,R7               SET REG TO REMEMBER           S21002
         NI    WMJMMLW,SWMASK-WMJMMLWF   TURN OFF TOP FLAG       S21002
ALTER5   EQU   *                                                 S21002
         L     R11,UCMVEA          GET FIRST ENTRY POINTER       S21002
REQ      EQU   *                                                 S21002
         L     R12,UCMOUTQ         GET ITS QUEUE POINTER         S21002
         LTR   R12,R12             QUEUE EMPTY                   S21002
         BZ    REQ30               YES, CHECK NEXT ENTRY         S21002
REQ0     EQU   *                                                 S21002
         USING CQE,R12
         TM    CQEFLAG,CQEENTR     VALID WQE ENTRY HERE ?
         BO    REQ2                YES                           S21002
REQ1     EQU   *                                                 S21002
         TM    CQEFLAG,CQEEOB     NEXT BLOCK ADDRESS             S21002
         BO    REQ4                YES                           S21002
         TM    CQEFLAG,CQEEOQ     END OF QUEUE                   S21002
         BO    REQ30               YES                           S21002
         LA    R12,FOUR(R12)       POINT TO NEXT ENTRY           S21002
         B     REQ0                TEST ENTRY                    S21002
REQ2     EQU   *                                                 S21002
         L     R13,CQEWQE          GET WQE POINTER
         LA    R13,ZERO(R13)       CLEAR FLAGS                   S21002
         CLR   R13,R6              IS THE MLWTO THIS ONE         S21002
         BNE   REQ1                NO, CONTINUE LOOKUNG          S21002
         LA    R8,ONE(R8)          INCREMENT NUMBER OF CQES      Y02756
         TM    WMJMDEC1,WMJMDECH   DESCRIPTOR CODE 8
         BNO   REQ2A               NO, INLINE WQE                S21002
         TM    WMJMDEC2,WMJMDECI   DESCRIPTOR CODE 9             S21002
         BNO   REQ2A               NO, INLINE WQE                S21002
         CLI   WMJMAREA,Z          AREA EQUAL Z                  S21002
         BE    REQ2A               YES, INLINE WQE               S21002
         LR    R0,R12              SAVE ENTRY POINTER            S21002
         LR    R1,R14              SAVE RETURN ADDRESS           S21002
         SR    R13,R13             CLEAR REGISTER                S21002
         IC    R13,UCMID           GET CONSOLE ID                S21002
         BCTR  R13,R0              REDUCE FOR ALGORITHM          S21002
         SR    R12,R12             CLEAR REGISTER                S21002
         LA    R15,EIGHT           GET DIVISOR                   S21002
         DR    R12,R15             DIVIDE ID BY 8                S21002
*        REG 13 HAS BYTE IN FIELD, REG 12 HAS BIT IN BYTE        S21002
         LA    R14,ONE             GET ONE BIT                   S21002
         SRDL  R14,TWENTY5(R12)    CREATE MASK                   S21002
         LA    R14,WMJMCONS        POINT TO FIELD IN MAJOR       S21002
         AR    R13,R14             POINT TO CORRECT BYTE         S21002
         LR    R12,R0              RESTORE                       S21002
         LR    R14,R1              REGISTERS                     S21002
         EX    R15,TESTWMJM        CAN I GO TO THIS CONSOLE FOR  S21002
*                                  THIS DISPLAY (FRAME NOT FULL) S21002
         BO    REQ3                NO, CONTINUE                  S21002
         OI    UCMSTS,UCMPF        OUTPUT PENDING AND            S21002
         OI    UCMSDS5,UCMSDS5C    OUT-OF-LINE OUTPUT PENDING    S21002
         B     REQ3                CONTINUE                      S21002
REQ2A    EQU   *                                                 S21002
         OI    UCMSTS,UCMPF        OUTPUT PENDING AND            S21002
         OI    UCMSDS5,UCMSDS5B    INLINE OUTPUT PENDING         S21002
REQ3     EQU   *                                                 S21002
         LTR   R7,R7               START AT TOP OF CHAIN         S21002
         BNZ   REQ30               NO                            S21002
         OI    CQEFLAG,CQEATTOP     TELL PROCESSORS              S21002
REQ30    EQU   *                                                 S21002
         A     R11,UCMVEZ          POINT TO NEXT ENTRY           S21002
         CL    R11,UCMVEL          WAS LAST ENTRY HANDLED        S21002
         BNH   REQ                 NO, LOOK AT THIS QUEUE        S21002
         LTR   R8,R8               ANY CQE ENTRIES FOUND         Y02756
         BNZ   REQ5                YES, CONTINUE                 Y02756
         OI    UCMSFLG2,UCMSYSI    NO, SET CLEANUP NEEDED FLAG   Y02756
         SPACE ,                                                 Y02756
REQ5     EQU   *                   TEST MSGTYPE                  Y02756
         TM    WQEMCSF1,WQEMCSD    WQE QUEUED BY MSGTYPE         Y02756
         BZ    ALTOUT              NO, EXIT                      Y02756
         L     R9,UCMMQPTR         GET PTR TO MONITOR QUEUE      Y02756
         LTR   R9,R9               IS THERE A MONITOR QUEUE      Y02756
         BZ    ALTMN               NO, CLEANUP AND EXIT          Y02756
         TM    WMJMBUF,WMJMBUFF    MAJOR QUEUED FOR TPUT         Y02756
         BZ    ALTOUT              NO, EXIT                      Y02756
         LA    R8,MNTRPROC         SET UP ADDR OF MNTRPROC       Y02756
*                                  SUBROUTINE                    Y02756
         L     R10,UCMQRTN         SET UP BASE REGISTER FOR      Y02756
*                                  CALL TO MNTRPROC SUBROUTINE   Y02756
         BALR  R9,R8               QUEUE ADDED MINORS FOR TPUT   Y02756
         SPACE
ALTOUT   EQU   *                   EXIT FROM ALTMLWTO            Y02756
         LM    R3,R14,UCMSVB0      RESTORE REGS SAVED AT ENTRY   Y02756
*                                  TO ALTMLWTO                   Y02756
*                                  INCLUDES RESTORE OF BASE REG  Y02756
*                                  AFTER CALL TO MNTRPROC        Y02756
         B     WTONEXT             CONTINUE WITH NEXT WQE        Y02756
REQ4     EQU   *                                                 S21002
         L     R12,CQEWQE          POINT TO NEXT BLOCK
         B     REQ0                CHECK ENTRIES                 S21002
         SPACE ,                                                 Y02756
ALTMN    EQU   *                   CLEANUP TPUT FLAGS            Y02756
         NI    WMJMBUF,SWMASK-WMJMBUFF TURN OFF POSSIBLE         Y02756
*                                  MAJOR QUEUED FOR TPUT FLAG    Y02756
*                                  SO THAT CHAIN CAN BE CLEANED  Y02756
*                                  UP EVEN THOUGH ALL MINORS     Y02756
*                                  HAVE NOT BEEN QUEUED FOR      Y02756
*                                  TPUT                          Y02756
         B     ALTOUT              EXIT                          Y02756
         SPACE 1
TESTWMJM EQU   *                                                 S21002
         TM    ZERO(R13),ZERO      FRAME FULL BIT ON             S21002
         DROP  R12                 CQE BASE NO LONGER NEEDED     Y02893
         EJECT
***********************************************************************
*                                                                     *
*                       IEECMENQ                                      *
*                                                                     *
*        THIS ROUTINE DETERMINES THE APPROPRIATE CONSOLES TO          *
*        WHICH A SPECIFIED WQE IS TO BE QUEUED. WHEN EACH             *
*        APPROPRIATE CONSOLE IS FOUND, IEECMQCN IS CALLED             *
*        TO DO THE ACTUAL QUEUEING. AFTER ALL CONSOLES ARE            *
*        EXHAUSTED, IEECMENQ DETERMINES WHETHER THE WQE IS            *
*        TO GO TO HARDCOPY.                                           *
*                                                                     *
*        INPUT TO THIS ROUTINE CONSISTS OF THE FOLLOWING-             *
*              REGISTER 1- WQE ADDRESS                                *
*              REGISTER 2- UCM ADDRESS                                *
*              REGISTER 4- MCS UCM PREFIX ADDRESS                     *
*              REGISTER 14- RETURN ADDRESS                            *
*              REGISTER 15- ENTRY POINT ADDRESS                       *
*                                                                     *
*                                                                     *
***********************************************************************
IEECMENQ EQU   *                   INTERNAL/EXTERNAL ENTRY POINT Y02893
         STM   R3,R14,UCMSVB0      SAVE REGS IN UCM SAVE AREA    Y02893
         LM    R11,R13,UCMVEA      INITIALIZE UCME BXLE REGS     Y02893
         LR    R10,R15             SET BASE REG - NEEDED FOR     Y02893
*                                  EXTERNAL CALL                 Y02893
         USING IEECMENQ,R10        ESTABLISH ADDRESSABILITY -    Y02893
*                                  NEEDED FOR EXTERNAL CALL      Y02893
         LR    R6,R1               SET UP WQE BASE               Y02893
         SR    R3,R3               INIT TOTAL COUNT              Y02893
         SR    R7,R7               INIT NON-GRAPHIC COUNT        Y02893
         SR    R0,R0               INIT OUTPUT-ONLY COUNT        Y02893
         TM    WQEMCSF,WQEMCSG     WQE FOR HARDCOPY ONLY         Y02893
         BZ    ENQWQE              NO,BRANCH
         TM    WQEXA,WQEWTOR       YES, IS IT ALSO A WTOR        Y02893
         BZ    ENQSET              NO, SKIP CONSOLE QUEUEING     Y02893
         L     R11,UCMMCENT        YES, GET UCME ADDR OF MASTER  Y02893
         CL    R11,UCMHCUCM        IS MASTER ALSO HARDCOPY       Y02756
         BNE   ENQHCBYP            NO, BYPASS FLAG SETTING       Y02756
         OI    WQEXA,WQEQDFHC      YES, INDICATE QUEUED FOR HC   Y02756
ENQHCBYP EQU   *                   QUEUE WTOR TO MASTER          Y02756
         BAL   R9,ENQCNSLE         USE COMMON QUEUEING ROUTINE   Y02893
         B     ENQHCCHK            GO TO HARDCOPY ROUTINE        Y02893
         SPACE
ENQSET   EQU   *                   HC ONLY - NOT A WTOR          Y02893
         OI    UCMSFLG2,UCMSYSI    YES, INDICATE CLEANUP NEEDED  Y02893
         OI    WQEAVAIL,WQEBUFC    INDICATE READY FOR HARDCOPY   Y02756
         B     ENQHCCHK            GO TO HARDCOPY ROUTINE        Y02893
ENQWQE   EQU   *                   START OF QUEUEING TESTS       Y02756
         TM    WQEMCSF1,WQEMCSH    Q BY ID UNCONDITIONALLY       Y02756
         BO    ENQQR01             YES, DO QREG0 TESTS           Y02756
ENQTSTCN TM    UCMSTS,UCMCF        IF CLOSE PENDING,
         BO    ENQNEXT             OR DEVICE
         TM    UCMATR,UCMUF       NOT ACTIVE
         BC    FOURTEEN,ENQNEXT       DO NOT Q TO THIS CONSOLE
         TM    WQEMCSF1,WQEMCSFF     IF BROADCAST MSG,
         BNO   ENQCONT             NO, CONTINUE WQE CHECK      @ZA11370
         L     R9,UCMCOMPC         GET COMP CONS ADR OR 0      @ZA11370
         LTR   R9,R9               IS THIS A COMPOSITE CONS    @ZA11370
         BZ    ENQGOOD             NO, CHECK HARDCOPY          @ZA11370
         TM    UCMATR,UCMOF        YES, OUTPUT UCME?           @ZA11370
         BNO   ENQNEXT             NO, GET NEXT UCME           @ZA11370
         B     ENQGOOD             YES, CHECK HARCOPY THEN     @ZA11370
ENQCONT  EQU   *                   QUEUE WQE TO THIS CONSOLE   @ZA11370
         SPACE ,                                                 Y02893
         TM    WQEMCSF,WQEMCSD     QUEUED BY MSGTYPE             Y02893
         BZ    ENQTSTCD            NO, CONTINUE WITH OTHER TESTS Y02893
         LH    R5,UCMMSG           GET MSGTYPE FLAGS FROM UCME
         SLL   R5,SIXTEEN         CLEAR HI TWO BYTES FOR N        19772
         SRL   R5,SIXTEEN         INSTRUCTION TO TEST MSGTYPE     19772
         N     R5,WQEMSGTP-TWO     Q TO THIS CONSOLE
         BZ    ENQNEXT             GET NEXT ENTRY
         B     ENQGOOD             GO TO Q MSG
ENQTSTCD L     R5,UCMRTCD
         N     R5,WQEROUT          IF THE WQE ROUTING CODES
         BC    SEVEN,ENQGOOD        MATCH ANY OF THE CONSOLE'S CODES,
         TM    WQEMCSF,WQEMCSB    QUEUE TO THAT CONSOLE
         BZ    ENQNEXT             IF A PARTICULAR CONSOLE
         CLC   WQEUCMID(ONE),UCMID  IS INDICATED, AND THIS IS
         BNE   ENQNEXT             THAT CONSOLE, Q TO THIS CONSOLE
         L     R9,UCMCOMPC         YES, IS THIS A COMPOSITE    @ZA11370
         LTR   R9,R9                CONSOLE?                   @ZA11370
         BZ    ENQGOOD             NO, QUEUE NORMALLY          @ZA11370
         TM    UCMATR,UCMOF        YES, OUTPUT UCME?           @ZA11370
         BO    ENQGOOD             YES, QUEUE NORMALLY         @ZA11370
*                                  OUTPUT UCME ALREADY IN R9.  @ZA11370
         CL    R9,UCMHCUCM         IS OUTPUT HALF HC?          @ZA11370
         BNE   ENQGOOD1            NO, SKIP HC FLAG SETTING    @ZA11370
         OI    WQEXA,WQEQDFHC      YES, SET HC FLAG IN WQE     @ZA11370
         B     ENQGOOD1            NO, QUEUE TO CNSLE        @ZA11370
ENQGOOD  EQU   *                   QUEUE IT
         CL    R11,UCMHCUCM        IS THIS CONSOLE ALSO THE HC   Y02756
*                                  CONSOLE                       Y02756
         BNE   ENQGOOD1            NO, SKIP HC FLAGS SETTINGS    Y02756
         OI    WQEXA,WQEQDFHC      INDICATE QUEUED FOR HC        Y02756
ENQGOOD1 EQU   *                   QUEUE IT                      Y02756
         BAL   R9,ENQCNSLE         QUEUE TO CONSOLE
ENQNEXT  BXLE  R11,R12,ENQWQE      GET NEXT CONSOLE ENTRY
         TM    WQEMCSF1,WQEMCSD    WQE QUEUED BY MSGTYPE         Y02756
         BZ    ENQCHK              NO, SKIP MONITOR PROCESSING   Y02756
         L     R11,UCMMQPTR        GET PTR TO MONITOR QUEUE      Y02756
         LTR   R11,R11             IS THERE A MONITOR QUEUE      Y02756
         BZ    ENQCHK              NO, SKIP MONITOR PROCESSING   Y02756
         BAL   R9,MNTRPROC         DO MONITOR PROCESSING         Y02756
         EJECT
ENQCHK   EQU   *                   CHECK WHERE IT WAS QUEUED     Y02893
***********************************************************************
*                                                                     *
*        THE FOLLOWING CODE DETERMINES WHERE THE WQE WAS QUEUED.      *
*        A WQE IS ALWAYS SENT TO SOME CONSOLE UNLESS IT IS A          *
*        MSGTYPE WQE.  IF IT HAS NOT BEEN QUEUED ANYWHERE, IT IS      *
*        SENT TO THE MASTER CONSOLE.                                  *
*                                                                     *
***********************************************************************
         TM    WQEMCSF,WQEMCSB+WQEMCSH QUEUED BY ID              Y02893
         BZ    ENQWTOR             NO, CHECK FOR WTOR            Y02893
         CLI   WQEUCMID,ZERO       IS ID 0 - 0 ID IS USED TO     Y02893
*                                  DIRECT COMMAND RESPONSES TO   Y02893
*                                  THE MASTER CONSOLE WHEN THE   Y02893
*                                  COMMAND CAME FROM THE INPUT   Y02893
*                                  STREAM                        Y02893
         BNE   ENQWTOR             NO, CHECK FOR WTOR            Y02893
         L     R5,UCMMCENT         GET UCME FOR MASTER           Y02893
         L     R5,UCMRTCD-UCMLIST(R5) GET MASTERS ROUTE CODES    Y02893
         N     R5,WQEROUT          DO ANY ROUT CODES MATCH       Y02893
         BZ    ENQMASTR            NO, QUEUE WQE TO MASTER       Y02893
         SPACE
ENQWTOR  EQU   *                   WTOR CHECK                    Y02893
         TM    WQEXA,WQEORE        IS THIS A WTOR                Y02893
         BZ    ENQNONE             NO, CHECK FOR NONE            Y02893
         LTR   R3,R3               IS TOTAL COUNT ZERO           Y02893
         BZ    ENQMASTR            YES, QUEUE TO MASTER          Y02893
         CR    R0,R3               QUEUED ONLY TO OUTPUT-ONLY    Y02893
*                                  CONSOLES                      Y02893
         BE    ENQMASTR            YES, QUEUE TO MASTER          Y02893
         SPACE
ENQNONE  EQU   *                   NOT QUEUED CHECK              Y02893
         LTR   R3,R3               QUEUED TO ANY CONSOLE         Y02893
         BP    ENQHCCHK            YES, DO HC TESTS              Y02893
         TM    WQEMCSF1,WQEMCSD    QUEUED BY MSGTYPE             Y02893
         BZ    ENQMASTR            NO, QUEUE TO MASTER           Y02756
         TM    WQEMCSF1,WQEMCSE    WTO IN RESPONSE TO A WTOR     Y02756
         BO    ENQMASTR            YES,QUEUE TO MASTER           Y02756
         SPACE
         OI    UCMSFLG2,UCMSYSI    SET WQE Q CLEANUP NEEDED FLAG Y02756
         B     ENQEND              GO TO SET SERVICED BIT        Y02756
         SPACE
ENQMASTR EQU   *                   QUEUE TO MASTER CONSOLE       Y02893
         TM    WQEMCSF2,WQEMCSN    SHOULD HARDCOPY BY BYPASSED?@ZA16551
         BO    ENQMSCON            YES,CONTINUE MASTER QUEUEING@ZA16551
         L     R5,UCMHRDRT         GET HARDCOPY ROUT CODES.    @ZA16551
         N     R5,WQEROUT          DO ROUT CODES MATCH HARDCOPY@ZA16551
         BZ    ENQMSCON            NO, CONTINUE MASTER Q-ING   @ZA16551
         TM    UCMSFLG1,UCMSYSG    IS HARDCOPY SYSLOG?         @ZA16551
         BZ    ENQHCUCM            GET HARDCOPY UCME           @ZA16551
***************************************************************@ZA16551
* NEXT 3 STMTS WILL ALLOW A WQE TO BE AUEUED TO SYSLOG ONLY ON @ZA16551
* ENTRY TO IEAVMDSV WHEN SYSLOG IS HARDCOPY AND WQE ROUT CODES @ZA16551
* MATCH ONLY HARDCOPY ROUT CODES.                              @ZA16551
***************************************************************@ZA16551
         OI    WQEAVAIL,WQEBUFC    TURN ON READY FOR HARDCOPY  @ZA16551
         OI    WQEXA,WQEQFHC       TURN ON QUEUE FOR HARDCOPY  @ZA16551
         OI    UCMSFLG2,UCMSYSI    TURN ON CLEAN-UP NEEDED.    @ZA16551
         B     ENQCWTOR            CHECK WQE FOR WTOR TYPE     @ZA16551
ENQHCUCM EQU   *                   HERE TO CHECK HARDCOPY DEV  @ZA16551
         L     R11,UCMHCUCM        GET HARDCOPY DEV UCME OR 0  @ZA16551
         LTR   R11,R11             IS THERE A HARDCOPY DEVICE  @ZA16551
         BZ    ENQMSCON            NO, HARDCOPY NOT ACTIVE     @ZA16551
         OI    WQEXA,WQEQDFHC      INDICATE QUEUED FOR HARDCOPY@ZA16551
         BAL   R9,ENQCNSLE         QUEUE WQE TO HARDCOPY DEVICE@ZA16551
         L     R5,UCMMCENT         GET MASTER CONSOLE UCME     @ZA16551
         CL    R5,UCMHCUCM         IS MASTER CONS ALSO HARDCOPY@ZA16551
         BE    ENQHCCHK            YES, FINISH HARDCOPY PROCESS@ZA16551
         L     R5,UCMCOMPC-UCMECB(R5) GET OTHER HALF OF COMP.  @ZA16551
         LTR   R5,R5               IS MASTER CONSOLE A COMP    @ZA16551
         BZ    ENQCWTOR            NO, CHECK WQE FOR WTOR      @ZA16551
         CL    R5,UCMHCUCM         IS OTHER HALF HARCOPY DEV. @ZA16551
         BE    ENQHCCHK            YES, FINISH HARDCOPY PROCESS@ZA16551
ENQCWTOR EQU   *                   HERE TO CHECK FOR WTOR TYPE @ZA16551
         TM    WQEXA,WQEORE        IS WQE FOR A WTOR ?         @ZA16551
         BZ    ENQHCCHK            NO, DO NOT QUEUE WQE TO MAST@ZA16551
ENQMSCON EQU   *                   HERE TO QUEUE WQE TO MASTER @ZA16551
         L     R11,UCMMCENT        GET UCME OF MASTER            Y02893
         L     R9,UCMCOMPC         IS MASTER A COMPOSITE       @ZA11370
         LTR   R9,R9                  CONSOLE                  @ZA11370
         BZ    ENQMHCK             NO, CHECK MASTER FOR HC     @ZA11370
         TM    UCMATR,UCMOF        YES, IS THIS OUTPUT UCME    @ZA11370
         BO    ENQMHCK             YES, CHECK MASTER FOR HC    @ZA11370
         CL    R9,UCMHCUCM         NO, DOES OUTPUT HALF = HC   @ZA11370
         BNE   ENQCN               NO, QUEUE TO MASTER         @ZA11370
         B     ENQMSTHC            YES, QUEUE TO MASTER FOR HC @ZA11370
ENQMHCK  EQU   *                   CHECK MASTER FOR HC         @ZA11370
         CL    R11,UCMHCUCM        IS MASTER ALSO HC CONSOLE     Y02893
         BNE   ENQCN               NO, BYPASS FLAG SETTING       Y02893
ENQMSTHC EQU   *                   MASTER CONSOLE HARDCOPY     @ZA11370
         OI    WQEXA,WQEQDFHC      INDICATE QUEUED FOR HC        Y02893
         SPACE
ENQCN    EQU   *                   QUEUE IT                      Y02893
         BAL   R9,ENQCNSLE         CALL ENQCNSLE TO QUEUE IT     Y02893
         EJECT
ENQHCCHK EQU   *                   DETERMINE IF WQE SHOULD BE    Y02893
*                                  HARDCOPIED
***********************************************************************
*                                                                     *
*        THE FOLLOWING CODE DETERMINES IF THE WQE SHOULD BE           *
*        HARDCOPIED AND FLAGS THE WQE IF NECESSARY.                   *
*                                                                     *
*        A WQE THAT IS NOT A COMMAND RESPONSE OR A STATUS             *
*        DISPLAY IS HARDCOPIED IF                                     *
*           1) IT WAS QUEUED TO GRAPHIC CONSOLES ONLY OR              *
*           2) ITS ROUTE CODES MATCH ONE OF THE HARDCOPY ROUTE        *
*              CODES.                                                 *
*                                                                     *
*        A WQE THAT IS A COMMAND RESPONSE BUT NOT A STATUS            *
*        DISPLAY IS HARDCOPIED IF THE CMDS, STCMDS OR INCMDS          *
*        HARDCOPY OPTION IS SPECIFIED.                                *
*                                                                     *
*        A WQE THAT IS A STATUS DISPLAY IS HARDCOPIED IF              *
*          1) IT IS A STATIC DISPLAY AND STCMDS OR CMDS IS            *
*             SPECIFIED OR                                            *
*          2) IT IS A DYNAMIC DISPLAY AND CMDS IS SPECIFIED.          *
*                                                                     *
***********************************************************************
         SPACE ,
         TM    WQEMCSF2,WQEMCSN    SHOULD HARDCOPY BE BYPASSED   Y02893
         BO    ENQEND              YES, SKIP HARDCOPY TESTS      Y02893
         TM    UCMSFLG1,UCMSYSG    IS HARDCOPY SYSLOG            Y02893
         BO    ENQHCTST            YES, DO HARDCOPY TESTS        Y02893
         L     R11,UCMHCUCM        GET UCME ADDR OF HC CONSOLE   Y02893
         LTR   R11,R11             IS THERE A HC CONSOLE         Y02893
         BZ    ENQEND              NO, HARDCOPY NOT WANTED       Y02893
         SPACE ,                                                 Y02893
ENQHCTST EQU   *                   HARDCOPY TESTS                Y02893
         CLI   WMJMMLW,ZERO        IS WQE FOR A MLWTO            Y02893
         BE    ENQHCCR             NO, CANNOT BE A STATUS DISP   YM5570
         TM    WMJMDEC1,WMJMDECH   DESC CODE 8                   Y02893
         BZ    ENQHCCR             NO, CANNOT BE A STATUS DISP.  Y02893
         TM    WMJMDEC2,WMJMDECI   DESC CODE 9                   Y02893
         BZ    ENQHCCR             NO, CANNOT BE A STATUS DISP   Y02893
         TM    WMJMDEC2,WMJMDECJ   DESC CODE 10                  Y02893
         BZ    ENQHCSSD            NO, MUST BE A STATIC STATUS   Y02893
*                                  DISPLAY                       Y02893
*                                  YES, IT IS A DYNAMIC STATUS   Y02893
*                                  DISPLAY                       Y02893
         TM    UCMSFLG1,UCMSYSC    CMDS HARDCOPIED               Y02893
         BZ    ENQEND              NO, DO NOT HARDCOPY           Y02893
         SPACE ,                                                 Y02893
ENQHCQF  EQU   *                   HARDCOPY TEST SUCCESSFUL      Y02893
         OI    WQEXA,WQEQFHC       INDICATE QUEUE FOR HARDCOPY   Y02893
         SPACE ,                                                 Y02893
ENQEND   OI    WQEAVAIL,WQEBUFE   MARK WQE SERVICED
         MVC   WQERTCT(ONE),WQEUSE SAVE ROUTED USE COUNT
         LR    R1,R6
         LM    R3,R14,UCMSAVE0+FOUR RESTORE REGS
         BR    R14                 RETURN
         SPACE ,                                                 Y02893
ENQHCSSD EQU   *                   HC TESTS FOR STATIC STATUS    Y02893
*                                  DISPLAY                       Y02893
         TM    UCMSFLG1,UCMSYSC    CMDS HARDCOPIED               Y02893
         BO    ENQHCQF             YES, GOOD FOR HARDCOPY        Y02893
         TM    UCMSDS1,UCMSDS1A    STCMDS HARDCOPIED             Y02893
         BO    ENQHCQF             YES, GOOD FOR HARDCOPY        Y02893
         B     ENQEND              NO, DO NOT HARDCOPY           Y02893
         SPACE ,                                                 Y02893
ENQHCCR  EQU   *                   HC TESTS FOR NON-STATUS-      Y02893
*                                  DISPLAY                       Y02893
         TM    WQEMCSF1,WQEMCSC    COMMAND RESPONSE              Y02893
         BZ    ENQHCOLY            NO, DO OTHER HC TESTS         Y02893
         TM    UCMSFLG1,UCMSYSC    CMDS HARDCOPIED               Y02893
         BO    ENQHCQF             YES, GOOD FOR HC              Y02893
         TM    UCMSDS1,UCMSDS1A+UCMSDS1B STCMDS OR INCMDS        Y02893
*                                  HARDCOPIED                    Y02893
         BZ    ENQEND              NO, DO NOT HC                 Y02893
         B     ENQHCQF             YES, GOOD FOR HC              Y02893
         SPACE ,                                                 Y02893
ENQHCOLY EQU   *                   NORMAL HC TESTS               Y02893
         TM    WQEMCSF,WQEMCSG     WQE FOR HC ONLY               Y02893
         BO    ENQHCQF             YES, GOOD FOR HC              Y02893
         LTR   R7,R7               IS NON-GRAPHIC COUNT 0        Y02893
         BZ    ENQHCQF             YES, Q'D TO GRAPHICS ONLY -   Y02893
*                                  GOOD FOR HC                   Y02893
         TM    WQEMCSF1,WQEMCSD    MSGTYPE WQE                   Y02893
         BO    ENQEND              YES, IGNORE ROUTE CODES - DO  Y02893
*                                  NOT HARDCOPY                  Y02893
         L     R5,UCMHRDRT         GET HC ROUTE CODES            Y02893
         N     R5,WQEROUT          DO ANY ROUTE CODES IN WQE     Y02893
*                                  MATCH HC ROUTE CODES          Y02893
         BZ    ENQEND              NO, DO NOT HC                 Y02893
         B     ENQHCQF             YES, GOOD FOR HC              Y02893
         EJECT
MNTRPROC EQU   *                   MONITOR PROCESSING            Y02756
***********************************************************************
*                                                                     *
*        THE FOLLOWING SUBROUTINE PROCESSES A WQE WHICH IS TO BE      *
*        QUEUED FOR TPUT TO MONITORING TERMINALS.  IT IS CALLED       *
*        DURING NORMAL QUEUEING FROM IEECMENQ AND FOR AN ALTERED      *
*        MLWTO CHAIN FROM ALTMLWTO.  A MONITOR MESSAGE BLOCK IS       *
*        CREATED AND QUEUED FOR EACH TEXT LINE.  FOR A                *
*        MLWTO THE MAJOR AND MINORS ARE ALSO MARKED QUEUED FOR        *
*        TPUT TO AVOID REQUEUEING IF THE CHAIN IS ALTERED.            *
*                                                                     *
*        INPUT - R6=WQE ADDR                                          *
*                R9=RETURN ADDR                                       *
*                                                                     *
*        USAGE - R8=MINOR ADDR, R10,R11,R14,R15=USED FOR POST         *
*                                                                     *
***********************************************************************
         CLI   WMJMMLW,ZERO        MAJOR WQE                     Y02756
         BE    MNTRNORM            NO, BUILD MMB FOR NORMAL      Y02756
         TM    WMJMBUF,WMJMBUFF    MAJOR ALREADY QUEUED FOR TPUT Y02756
         BO    MNTRMAJ1            YES, SKIP MMB BUILD           Y02756
         LR    R8,R6               SET UP INPUT TO BUILDMMB      Y02756
         BAL   R12,BUILDMMB        GET, BUILD, AND CHAIN A MMB   Y02756
         OI    WMJMBUF,WMJMBUFF    MARK MAJOR QUEUED FOR TPUT    Y02756
         SPACE
MNTRMAJ1 EQU   *                                                 Y02756
         TM    WMJMLTY1,WMJMLTYD   IS MAJOR ALSO THE END LINE    Y02756
         BO    MNTREND             YES, ALL DONE                 Y02756
         TM    WMJMMLW,WMJMMLWH    IS FIRST MINOR A DUMMY        Y02756
         BO    MNTREND             YES, ALL DONE                 Y02756
         L     R8,WMJMMIN          GET PTR TO FIRST MINOR        Y02756
         USING WQE,R8              ADDRESSABILITY TO MINORS      Y02756
         SPACE
MNTRMINL EQU   *                   MINOR LOOP                    Y02756
         LTR   R8,R8               DOES MINOR EXIST              Y02756
         BZ    MNTREND             NO, ALL DONE                  Y02756
         CLI   WMNMTL1,ZERO        IS MINOR TEXT LENGTH 0        Y02756
         BE    MNTRENDL            YES, MUST BE END LINE, ALL    Y02756
*                                  DONE                          Y02756
         TM    WMNMST1,WMNMTPD1    MINOR ALREADY QUEUED FOR TPUT Y02756
         BO    MNTRMINE            YES, SKIP MMB BUILD           Y02756
         BAL   R12,BUILDMMB        GET, BUILD, AND CHAIN A MMB   Y02756
*                                  FOR THE MINOR                 Y02756
         OI    WMNMST1,WMNMTPD1    MARK MINOR QUEUED FOR TPUT    Y02756
         SPACE
MNTRMINE EQU   *                   CHECK FOR END IN MINOR        Y02756
         TM    WMNMLT1,WMNMLT1D    END LINE IN MINOR             Y02756
         BO    MNTREND             YES, ALL DONE                 Y02756
         L     R8,WMNMEXT          GET PTR TO NEXT MINOR         Y02756
         LA    R8,ZERO(R8)         CLEAR THE USE COUNT           Y02756
         B     MNTRMINL            PROCESS NEXT MINOR            Y02756
         SPACE ,                                                 Y02756
MNTRENDL EQU   *                   END LINE ONLY MINOR           Y02756
         TM    WMNMML1,WMNMML1F    ABEND'S MINOR                 Y02756
         BO    MNTREND             YES, SKIP FLAG SETTING        Y02756
         OI    WMNMST1,WMNMTPD1    INDICATRE QUEUED FOR TPUT     Y02756
         DROP  R8                  GIVE UP ADDRESSABILITY TO     Y02756
*                                  MINORS                        Y02756
         SPACE
MNTREND  EQU   *                   NOTIFY TPUT ROUTINE VIA POST  Y02756
         LA    R11,UCMMNECB        ADDR OF MONITOR ECB           Y02756
         L     R15,CVTPTR          CVT POINTER                   Y02756
         USING CVT,R15             ADDRESSABILITY TO CVT         Y02756
         L     R15,CVT0PT01        ENTRY POINT OF POST           Y02756
         DROP  R15                 GIVE UP CVT BASE              Y02756
         ST    R10,UCMSAVE4        SAVE BASE REG                 Y02756
         LA    R10,ZERO            COMPLETION CODE               Y02756
         BALR  R14,R15             BRANCH TO POST                Y02756
         L     R10,UCMSAVE4        RESTORE BASE REG              Y02756
         BR    R9                  RETURN TO CALLER              Y02756
         SPACE
MNTRNORM EQU   *                   NORMAL WQE                    Y02756
         LR    R8,R6               SET UP INPUT FOR BUILDMMB     Y02756
         BAL   R12,BUILDMMB        GET, BUILD AND CHAIN A MMB    Y02756
         B     MNTREND             GO DO POST                    Y02756
         EJECT
ENQQR01  EQU   *                   QREG0 TESTS                   Y02756
         SPACE
***********************************************************************
*                                                                     *
*        IF A WQE INDICATES IT SHOULD BE QUEUED UNCONDITIONALLY       *
*        BY CONSOLE ID AND THE CONSOLE IS INACTIVE, THE WQE IS        *
*        QUEUED TO THE INACTIVE CONSOLE AND THE QREG0 PROCESSOR       *
*        IS ATTACHED.  THE QREG0 PROCESSOR WILL ISSUE A WTOR SO       *
*        THAT THE SYSTEM OPERATOR MAY DELETE OR REROUTE THE MSG.      *
*                                                                     *
***********************************************************************
         SPACE
         CLC   WQEUCMID(ONE),UCMID IS THIS THE SPECIFIED CONSOLE Y02756
         BNE   ENQTSTCN            NO, CONTINUE NORMAL TESTS     Y02756
         TM    UCMSTS,UCMCF        IS CLOSE PENDING              Y02756
         BO    ENQNEXT             YES, IGNORE QREG0 REQUEST     Y02756
         TM    UCMATR,UCMUF        IS THE CONSOLE ACTIVE         Y02756
         BNO   QREG0               NO, QUEUE UNCONDITIONALLY   @ZA11370
         L     R9,UCMCOMPC         YES, IS THIS A COMPOSITE    @ZA11370
         LTR   R9,R9                CONSOLE?                   @ZA11370
         BZ    ENQGOOD             NO, QUEUE NORMALLY          @ZA11370
         TM    UCMATR,UCMOF             YES, OUTPUT UCME?      @ZA11370
         BO    ENQGOOD             YES, QUEUE NORMALLY         @ZA11370
*                                  OUTPUT UCME ADDR IN REG 9   @ZA11370
         CL    R9,UCMHCUCM         IS OUTPUT HALF HC?          @ZA11370
         BNE   ENQGOOD1            NO, SKIP HC FLAG SETTING    @ZA11370
         OI    WQEXA,WQEQDFHC      YES, SET HC FLAG IN WQE     @ZA11370
         B     ENQGOOD1            NO, QEUE TO ACTIVE CONSOLE @ZA11370
QREG0    EQU   *                   UNCONDITIONALLY             @ZA11370
         SPACE
         BAL   R9,ENQCNSLE         QUEUE TO CONSOLE              Y02756
         SPACE
*
*        USE THE BRANCH ENTRY TO GETMAIN TO GET SPACE FOR A      Y02756
*        PARMATER LIST FOR IEAVMQR0 FROM SUBPOOL 250             Y02756
*        REGS 0,1,3,4,7,14,15 ARE USED FOR THE BRANCH TO GETMAIN Y02756
*        REGS 11,12,13,14 WILL BE DESTOYED BY SETLOCK BEFORE     Y02756
*        ATTACH.                                                 Y02756
*
         STM   R0,R4,UCMSAVE4      SAVE REGS NEEDED FOR GETMAIN  Y02756
         ST    R7,UCMSAVE4+20      SAVE REGS NEEDED FOR GETMAIN  Y02756
         STM   R11,15,UCMSAVE4+24  SAVE REGS NEEDED FOR GETMAIN  Y02756
*                                  AND SETLOCK                   Y02756
         L     R4,UCMPXA           GET COMMTASKS TCB ADDR        Y02756
         L     R7,UCMASCB          GET COMMTASKS ASCB ADDR       Y02752
         GETMAIN R,LV=QR0PSIZE,SP=QR0PSP,BRANCH=YES GET QR0PLIST Y02756
*
*        BUILD QREG0 PARAMETER LIST
*
         LA    R3,FOUR(R1)         SET PARAMETER LIST BASE       Y02756
         ST    R3,ZERO(R1)         STORE PTR TO PARM LIST        Y02756
         USING QR0,R3              ADDRESSABILITY TO PLIST MAP   Y02756
         ST    R6,QR0WQE           WQE ADDRESS                   Y02756
         ST    R11,QR0UCME         UCME ADDR OF INACTIVE CONSOLE Y02756
         ST    R2,QR0UCM           UCM ADDR                      Y02756
         LA    R0,IEECMQCN         GET ADDR OF CQE CREATE RTN    Y02756
         ST    R0,QR0CMQCN         STORE IN QREG0 PARM LIST      Y02756
         SPACE
         SETFRR D,WRKREGS=(R11,R12)                              Y02755
         SPACE
*        SETLOCK WILL DESTROY REGS 11, 12, 13, 14                Y02751
         SPACE
         SETLOCK RELEASE,TYPE=CMS,                                     C
               RELATED=(UCM,IEAVMQWR(WRLOCK))                    Y02751
         SETLOCK RELEASE,TYPE=LOCAL,                                   C
               RELATED=(UCM,IEAVMQWR(WRLOCK))                    Y02751
         ATTACH EP=IEAVMQR0,SF=(E,ATTPARM) ATTACH QREG0 ROUTINE  Y02756
         SETLOCK OBTAIN,TYPE=LOCAL,MODE=UNCOND,                        C
               RELATED=(UCM,IEAVMQWR(WRLOCK))                    Y02751
         SETLOCK OBTAIN,TYPE=CMS,MODE=UNCOND,                          C
               RELATED=(UCM,IEAVMQWR(WRLOCK))                    Y02751
         SPACE
         SETFRR A,FRRAD=UCMFRRAD,WRKREGS=(R11,R12),PARMAD=(R12)  Y02755
         USING PARMLIST,R12        ADDRESSABILITY TO PARM AREA   Y02755
         MVC   PARMID,CTSK         PUT LOAD MODULE ID IN PARMS   Y02755
         DROP  R12
         SPACE
*
*        RESTORE REGISTERS
*
         LM    R0,R4,UCMSAVE4      RESTORE REGISTERS             Y02756
         L     R7,UCMSAVE4+20      RESTORE REGISTERS             Y02756
         LM    R11,R15,UCMSAVE4+24 RESTORE REGISTERS             Y02756
         B     ENQNEXT             TEST NEXT UCME                Y02756
         EJECT
BUILDMMB EQU   *                   GET, BUILD, AND QUEUE A MMB   Y02756
         USING WQE,R8                                            Y02756
         STM   R0,R4,UCMSAVE4      SAVE REGS AROUND GETMAIN      Y02756
         ST    R7,UCMSAVE4+20      SAVE REGS AROUND GETMAIN      Y02756
         STM   R14,R15,UCMSAVE4+24 SAVE REGS AROUND GETMAIN      Y02756
         SPACE ,                                                 Y02756
*   GET SPACE FOR A NEW MMB                                      Y02756
         SPACE ,                                                 Y02756
         L     R4,UCMPXA           GET COMMTASK TCB ADDRESS      Y02756
         L     R7,UCMASCB          GET COMMTASK ASCB ADDRESS     Y02756
         GETMAIN R,LV=MMBSIZE,SP=MMBSUBPL,BRANCH=YES GET MMB     Y02756
         USING MMB,R1              ADDRESSABILITY TO NEW MMB     Y02756
         SPACE ,                                                 Y02756
*   BUILD THE NEW MMB WITH DATA FROM THE WQE                     Y02756
         SPACE ,                                                 Y02756
         XC    MMB(MMBSIZE),MMB    CLEAR THE NEW MMB             Y02756
         MVC   MMBNAME,=CL4'MMB '  PUT IN CONTROL BLOCK ID       Y02756
         TM    WMJMMLW,WMJMMLWC    IS THIS A MINOR WQE           Y02756
         BO    BLDMMBMI            YES, BUILD MMB FOR A MINOR    Y02756
         MVC   MMBTXLN,WQENBR+2    COPY TEXT LENGTH FROM WQE     Y02756
         MVC   MMBTYPE,WQEMSGTP    COPY MSG TYPE FROM WQE        Y02756
         MVC   MMBTEXT,WQETXT      COPY TEXT FROM WQE            Y02756
         SPACE ,                                                 Y02756
*   CHAIN THE NEW MMB TO THE END OF THE MMB QUEUE                Y02756
         SPACE ,                                                 Y02756
BLDMMBCH DS    0H                  CHAIN THE NEW MMB             Y02756
         NC    UCMMBPTR,UCMMBPTR   IS THE MMB QUEUE EMPTY        Y02756
         BZ    BLDMMBH             YES, GO SET QUEUE HEAD PTR    Y02756
         L     R4,UCMMBEND         GET PTR TO LAST MMB           Y02756
         ST    R1,MMBLINK-MMB(R4)  SET FORWARD CHAIN PTR IN OLD  Y02756
*                                     LAST MMB                   Y02756
         MVC   MMBBKPTR,UCMMBEND   SET BACK PTR IN NEW MMB       Y02756
         B     BLDMMBT             SKIP SETTING QUEUE HEAD PTR   Y02756
         SPACE ,                                                 Y02756
BLDMMBH  DS    0H                  SET QUEUE HEAD PTR            Y02756
         ST    R1,UCMMBPTR                                       Y02756
BLDMMBT  DS    0H                  SET QUEUE TAIL PTR            Y02756
         ST    R1,UCMMBEND                                       Y02756
         LM    R0,R4,UCMSAVE4      RESTORE REGS                  Y02756
         L     R7,UCMSAVE4+20      RESTORE REGS                  Y02756
         LM    R14,R15,UCMSAVE4+24 RESTORE REGS                  Y02756
         BR    R12                 RETURN TO CALLER              Y02756
         SPACE ,                                                 Y02756
*   MMB BUILD FOR A MINOR                                        Y02756
         SPACE ,                                                 Y02756
BLDMMBMI DS    0H                  MMB BUILD FOR A MINOR         Y02756
         MVC   MMBTXLN+ONE,WMNMTL1 TEXT LENGTH                   Y02756
         MVC   MMBTEXT(WMNMST1-WMNMTXT1),WMNMTXT1 TEXT           Y02756
         MVC   MMBTYPE,WMJMMT-WMJM(R6) MSGTYPE FROM MAJOR WQE    Y02756
         B     BLDMMBCH            ADD THE NEW MMB TO THE CHAIN  Y02756
         DROP  R8                                                Y02756
         EJECT
*        DEFINED CONSTANTS AND SOME EQUATES
ADDVSV   DC    V(IEAVMDSV)         CMD SERVICE
CTSK     DC    CL4'CTSK'           LOAD MODULE ID FOR SETFRR     Y02755
         SPACE ,                                                 Y02756
*   ATTACH PARAMETER LIST                                        Y02756
         SPACE ,                                                 Y02756
ATTPARM  ATTACH EP=IEAVTPUT,SF=L,SM=SUPV                         Y02756
         SPACE ,                                                 Y02756
MMBSUBPL EQU   250                 SUBPOOL NO. FOR MMB SPACE     Y02756
QR0PSIZE EQU   20                  LENTH OF QR0 PARMLIST + PTR   Y02756
*                                  TO PARM LIST                  Y02756
QR0PSP   EQU   250                 QR0 PARM LIST SUBPOOL         Y02756
Z        EQU   C'Z'                AREA ID Z (MESSAGE STREAM)    S21002
         EJECT
***********************************************************************
*                                                                     *
*                             IEECMQCN                                *
*                                                                     *
*        THIS ROUTINE QUEUES A PARTICULAR WQE ONTO A SPECIFIED        *
*        CONSOLE'S OUTPUT QUEUE                                       *
*                                                                     *
*        INPUT TO THIS ROUTINE CONSISTS OF THE FOLLOWING-             *
*            REGISTER 2- UCM ADDRESS                                  *
*            REGISTER 4- MCS UCM PREFIX ADDRESS                       *
*            REGISTER 6- WQE ADDRESS                                  *
*            REGISTER 9- RETURN ADDRESS                               *
*            REGISTER 11- CONSOLE ENTRY ADDRESS                       *
*                                                                     *
*        THIS ROUTINE  DESTROYS THE PREVIOUS CONTENTS OF-             *
*            REGISTERS 1, 5, 8, 10 (10 IS PRESERVED ONLY              *
*              IF THE CALLER IS IEECMENQ)                             *
*                                                                     *
***********************************************************************
         SPACE 3
ENQCNSLE EQU   *
         LR    R15,R11                  SAVE UCM ENTRY PTR
         TM    UCMATR,UCMOF               ENTRY SUPPORT OUTPUT
         BNZ   CONSOK                   YES - CONTINUE NORMALLY
         L     R11,UCMCOMPC             GET OUTPUT PORTION OF
*                                        COMPOSITE CONSOLE
CONSOK   EQU   *                                                 S21003
         TM    UCMDISP,UCMDISPG    OUT-OF-LINE ONLY CONSOLE      S21003
         BNO   CONSOK1             NO                            S21003
         CLI   WMJMMLW,ZERO       MAJOR WQE?
         BE    ENQADDQ1          NO,CONTINUE
         TM    WMJMDEC1,WMJMDECH      DESCRIPTOR CODE 8          S21003
         BNO   ENQADDQ1            NO, NOT OUT-OF-LINE           S21003
         TM    WMJMDEC2,WMJMDECI   DESCRIPTOR CODE 9             S21003
         BNO   ENQADDQ1            NO, NOT OUT-OF-LINE           S21003
         CLI   WMJMAREA,Z          AREA EQUAL Z                  S21003
         BE    ENQADDQ1            YES, NOT OUT-OF-LINE          S21003
CONSOK1  EQU   *                                                 S21003
         LA    R3,ONE(R3)              INCREMENT ROUTED COUNT
         TM    UCMDISP,UCMDISPD         THIS CONSOLE OUTPUT ONLY
         BNO   ENQNTOUT            NO
         A     R0,FULL1          INCR OUTP ONLY COUNT
ENQNTOUT EQU   *
         TM    UCMDISP,UCMDISPC    IF NOT A GRAPHICS DEVICE
         BO    IEECMQCN            INCREMENT NON-GRAPHICS COUNT
         LA    R7,ONE(R7)
IEECMQCN EQU   *
         BALR  R10,ZERO            SET BASE REG
         USING *,R10
         SR    R5,R5
         IC    R5,WQEUSE           INCREMENT WQE USE COUNT
         LA    R5,ONE(R5)
         STC   R5,WQEUSE
         CLI   WMJMMLW,ZERO        MAJOR WQE                     S21002
         BE    ENQUSE2             NO, CONTINUE                  S21002
         TM    WMJMMLW,WMJMMLWH    DUMMY MINOR                   S21002
         BO    ENQUSE2             YES, CONTINUE                 S21002
         L     R8,WMJMMIN          POINT TO FIRST MINOR          S21002
         USING WQE,R8
ENQUSE1  EQU   *                                                 S21002
         LTR   R8,R8               ANY ADDRESS                   S21002
         BZ    ENQUSE2             NO, CONTINUE                  S21002
***************************************************************@ZA01916
*                                                              @ZA01916
*         THE FOLLOWING TEST IS MADE TO DETERMINE IF THIS IS A @ZA01916
*         MLWTO THAT HAS NOT BEEN SERVICED BUT HAS BEEN MARKED @ZA01916
*         FOR ABEND. THIS CHECK MUST BE MADE BECAUSE AN MLWTO  @ZA01916
*         CAN BE MARKED FOR ABEND BEFORE IT HAS BE PLACED ON   @ZA01916
*         ANY CONSOLE QUEUE TO BE DISPLAYED.                   @ZA01916
*                                                              @ZA01916
***************************************************************@ZA01916
         TM    WMNMML1,WMNMML1F    MINOR MARKED FOR ABEND?     @ZA01916
         BO    ENQUSE2             YES, TERMINATE MLWTO SEARCH @ZA01916
         SR    R5,R5               CLEAR WORK REG                Y02756
         IC    R5,WMNMUC1          INCREMENT MINOR USE CT        Y02756
         LA    R5,ONE(R5)          INCREMENT MINOR USE CT        Y02756
         STC   R5,WMNMUC1          STORE COUNT OF FIRST MESSAGE  S21002
         TM    WMNMML2,WMNMML2H    IS LINE 2 AVAILABLE?
         BO    ENQUSE1A            YES
         SR    R5,R5               CLEAR WORK REG                Y02756
         IC    R5,WMNMUC2          INCREMENT MINOR USE CT        Y02756
         LA    R5,ONE(R5)          INCREMENT MINOR USE CT        Y02756
         STC   R5,WMNMUC2          STORE COUNT OF SECOND MESSAGE S21002
ENQUSE1A EQU   *                                                 S21002
         L     R8,WMNMNX2-ONE          GET NEXT MINOR PTR        S21002
         LA    R8,ZERO(R8)         CLEAR USE COUNT               S21002
         B     ENQUSE1             CHECK IT                      S21002
ENQUSE2  EQU   *                                                 S21002
         DROP  R8
         LA    R8,UCMOUTQ          INITIALIZE LINKAGE PTR ADDRESS
         L     R5,UCMOUTQ          GET FIRST PTR IN OUTPUT Q
         LTR   R5,R5
         BP    ENQLOOPQ             IF THERE IS NO OUTPUT Q,
         B     ENQGETQ              AND BUILD FIRST BLOCK
         USING CQE,R5
ENQLOOPQ TM    CQEFLAG,CQEEOB      TEST FOR END CONDITIONS IN QUEUE
         BM    ENQENDQ             BRANCH IF END-OF-QUEUE
         BO    ENQPTRQ             BRANCH IF PTR TO NEXT BLOCK
         LA    R5,FOUR(R5)
         B     ENQLOOPQ            GET NEXT PTR
ENQPTRQ  LR    R8,R5               UPDATE LINK PTR ADDRESS
         L     R5,CQEWQEA-ONE         GET ADDRESS OF NEXT BLOCK
         B     ENQLOOPQ            CONTINUE SEARCH FOR END-OF-QUEUE
ENQENDQ  NI    CQEFLAG,SWMASK-CQEEOQ    TURN OFF END OF QUEUE INDICATOR
         LA    R5,FOUR(R5)
         TM    CQEFLAG,CQEEOB      IF NEXT PTR IS A PTR
         BZ    ENQADDQ              TO THE NEXT BLOCK,
         LR    R8,R5          A NEW BLOCK MUST BE GOTTEN
ENQGETQ  EQU   *                   GETMAIN BLOCK FOR CQE         Y02752
*
*        USE THE BRANCH ENTRY TO GETMAIN TO GET A 24 BYTE        Y02752
*        BLOCK FROM SUBPOOL 241 FOR A CQE BLOCK                  Y02752
*
         STM   R3,R4,UCM2WD        SAVE REGS AROUND GETMAIN      Y02752
         ST    R7,UCM4WD           SAVE REGS                     Y02752
         ST    R0,UCM5WD           SAVE REGS                     Y02752
         ST    R15,UCM6WD          SAVE REGS                     Y02752
         SPACE ,                                                 Y02752
* NOTE: REGS 3,4,7,0,15 ARE SAVED IN THE UCM PREFIX              Y02752
*       REGS 3,4,7,0,1,14,15 ARE DESTROYED TO USE GETMAIN        Y02752
         SPACE ,                                                 Y02752
         L     R4,UCMPXA           GET COMMTASKS TCB ADDR        Y02752
         L     R7,UCMASCB          GET COMMTASKS ASCB ADDR       Y02752
         GETMAIN RU,LV=CQESIZE,SP=CQESP,BRANCH=YES GET CQE SPACE Y02752
         SPACE 1                                                 Y02752
*   RESTORE THE UCM PREFIX BASE REGISTER.                        Y02752
         SPACE 1
         LA    R3,FOUR             NEG OFFSET TO GET PRFX PTR    Y02752
         LR    R4,R2               UCMBASE ADDR                  Y02752
         SR    R4,R3               BACK UP ADDR TO PREFIX PTR    Y02752
         L     R4,ZERO(R4)         GET UCM PREFIX ADDRESS        Y02752
         LM    R3,R4,UCM2WD        RESTORE REGS AFTER GETMAIN    Y02752
         L     R7,UCM4WD           RESTORE REG                   Y02752
         L     R0,UCM5WD           RESTORE REG                   Y02752
         L     R15,UCM6WD          RESTORE REG                   Y02752
         SPACE ,                                                 Y02752
         ST    R1,UCMR9SV          SET UP FOR CHAINING           Y02893
         MVC   ONE(THREE,R8),UCMR9SV+ONE  LINK NEW BLOCK TO OLD QUEUE
         XC    ZERO(BLKLEN,R1),ZERO(R1)   CLEAR NEW BLOCK
         LR    R5,R1                   NEW CQE ADDRESS
         OI    CQEEND,CQEEOB    INDICATE SPOT FOR NEXT BLOCK PTR
ENQADDQ  ST    R6,CQEWQEA-ONE     WQE ADDR AFTER PREVIOUS LAST ENTRY
         OI    CQEFLAG,CQEEOQ+CQEENTR      INDICATE NEW LAST ENTRY
         TM    WQEXA,WQEQDFHC      QUEUED FOR HARDCOPY           Y02756
         BZ    ENQMLT              NO, CHECK FOR MLWTO           Y02756
         CL    R11,UCMHCUCM        IS THIS THE HARDCOPY CONSOLE  Y02756
         BNE   ENQMLT              NO, CHECK FOR MLWTO           Y02756
         OI    CQEFLAG,CQEMLQHC    YES, INDICATE QUEUED FOR HC   Y02756
         SPACE
ENQMLT   EQU   *                   MLWTO TESTS                   Y02756
         TM    WMJMMLW,WMJMMLWB        MLWTO CHAIN               S21002
         BNO   ENQADDQA            NO                            S21002
         OI    CQEFLAG,CQEMAJOR      SHOW THIS IS AN MLWTO       S21002
         TM    WMJMDEC1,WMJMDECH         DESCRIPTOR CODE 8       S21002
         BNO   ENQADDQA            NO OUT-OF-LINE                S21002
         TM    WMJMDEC2,WMJMDECI      DESCRIPTOR CODE 9          S21002
         BNO   ENQADDQA            NO OUT-OF-LINE                S21002
         CLI   WMJMAREA,Z          AREA ID EQUAL Z               S21002
         BE    ENQADDQA            YES, NOT OUT-OF-LINE          S21002
         OI    UCMSTS,UCMPF        INDICATE OUTPUT PENDING       S21002
         OI    UCMSDS5,UCMSDS5C    INDICATE OUT-OF-LINE PENDING  S21002
         B     ENQADDQ1            CONTINUE                      S21002
ENQADDQA EQU   *                                                 S21002
         TM    UCMSTS,UCMTC        IS CONSOLE WORKING ON INLINE  S21002
         BO    ENQADDQ1            MLWTO, YES, DON'T GO TO CONS  S21002
         OI    UCMSTS,UCMPF        INDICATE OUTPUT PENDING       S21002
         OI    UCMSDS5,UCMSDS5B    INDICATE INLINE OUTPUT PENDNG S21002
         USING UCMPRFX,R4
ENQADDQ1 EQU   *
         L     R10,UCMQRTN         RESTORE BASE REG FOR ENQ
         BR    R9                  RETURN
         DS    0F
FULL1    DC    X'00000001'
PATCH    DC    CL150'***IEAVMWSV***'     PATCH AREA            @ZA01916
         EJECT
         IEAMMB                                                  Y02756
         EJECT ,                                                 Y02756
         IHAWQE
         EJECT
         IEECUCM FORMAT=NEW
         EJECT
         IHASCVT
         EJECT
         IHACTM CQE
         EJECT
         IHACTM QR0                                              Y02756
         EJECT
         IHACTM FTPT
         EJECT
         CVT       SYS=AOS2,OPTIONS=(TSLICE),TSO=YES,DSECT=YES
         EJECT
         IHAASVT
         EJECT
         IHAPSA                    REQUIRED FOR SETLOCK
         EJECT
         IHAFRRS
         END   ,                                                 Y02893
