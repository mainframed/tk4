         TITLE 'IEAVVCTR - COMM TASK MINI ROUTER AND CONTROL MODULE'
IEAVVCTR CSECT                       MINI ROUTER
* A 025487-025790,035926-040291                                @ZA00873
********************************************************************
* STATUS
*    CHANGE LEVEL 000
* FUNCTION
*    THIS MODULE RECEIVES CONTROL AS A RESULT OF AN SVC 72 AND
*    THE AUTHORITY OF THE ISSUER (MUST BE KEY 0-7 AND SUPERVISOR Y02893
*    STATE), DETERMINES WHETHER THE CALL IS FOR A COMM TASK      Y02893
*    SERVICE (WHICH IS BRANCHED TO), OR AS A ROUTER (IN WHICH    Y02893
*    CASE IT XCTL'S TO THE REQUESTED MODULE).                    Y02893
*                                                                Y02893
* ENTRY POINTS
*         IEAVVCTR - ENTERED AS A RESULT OF SVC 72 ISSUED
*                    BY SUPERVISOR ROUTINES TO PASS CONTROL
*                    TO A SUB-PROCESSOR
* INPUT
*    REG 0 - PASSED FROM ORIGINAL CALLER                         Y02893
*    REG 1 - PARAMETER LIST ADDRESS, INCLUDES MODULE NAME OF
*            PROCESSOR TO GET CONTROL
*    REG 2 - IRRELEVANT                                          Y02893
*    REG 3 - ADDRESS OF THE CVT                                  Y02893
*    REG 4 - ADDRESS OF TCB                                      Y02893
*    REG 5 - ADDRESS OF THE SVRB                                 Y02893
*    REG 6 - ADDRESS OF THIS MODULES ENTRY POINT.                Y02893
*    REG 7 - ADDRESS OF THE ASCB                                 Y02893
*    REG 8 THRU 12 - IRRELEVANT                                  Y02893
*    REG 13 - PASSED FROM ORIGINAL CALLER                        Y02893
*    REG 14 - EXIT ADDRESS (SAME AS CONTENTS OF 'CVTEXPRO')      Y02893
*    REG 15 - PASSED FROM ORIGINAL CALLER                        Y02893
*                                                                Y02893
*    A 32 BYTE PARAMETER LIST WHICH CONTAINS THE MODULE NAME     Y02893
*    TO BE GIVEN CONTROL IN THE FIRST 8 BYTES, AND FOR COMM      Y02893
*    TASK TYPE CALLS, THE ADDRESS OF THE UCME TO BE SERVICED     Y02893
*    AND THE ADDRESS OF THE UCM BASE.                            Y02893
*                                                                Y02893
* OUTPUT
*    REGS 0, 2-14 UNCHANGED
*    REG 1 - ADDRESS OF EXTENDED SAVE AREA (XSA)
*    REG 15 - ADDRESS OF XSA IF XCTL; ADDRESS OF IEAV1052 OTHERWISE
*    SVRB EXTENDED SAVE AREA WHICH CONTAINS THE 32 BYTES OF THE  Y02893
*    PARAMETER LIST AT OFFSET +8 AND IS SET UP AS THE LIST FORM  Y02893
*    OF AN XCTL MACRO AT +0.                                     Y02893
*                                                                Y02893
* EXTERNAL REFERENCES
*    SVRB EXTENDED SAVE AREA
*    TESTAUTH, BRANCH ENTRY                                      Y02893
*                                                                Y02893
* EXITS,NORMAL
*    BRANCH TO IEAV1052 IF MODULE NAME PASSED IS IGC0007B
*    BRANCH TO  THE ADDRESS CONTAINED IN UCMSBR IF NAME PASSED   Y02893
*    IS; IGC0107B                                                Y02893
*        IGC0207B                                                Y02893
*        IGC5107B                                                Y02893
*        IGC5W07B                                                Y02893
*        IGCXX07B                                                Y02893
*    BRANCH TO THE ADDRESS CONTAINED IN UCMSWCH IF NAME PASSED   Y02893
*    IS; IGC0407B                                                Y02893
*    OTHERWISE XCTL TO A PROCESSOR MODULE
*                                                                Y02893
* EXITS,ERROR
*    NONE
* TABLES/WORK AREAS
*    CVT, TCB, SVRB EXTENDED SAVE AREA, UCM                      Y02893
*                                                                Y02893
* ATTRIBUTES
*    ENABLED, SUPERVISOR KEY AND STATE, REENTRANT, REFRESHABLE   Y02893
*    MUST BE LINK-EDITED  TOGETHER WITH ALL CONSOLE DEVICE       Y02893
*    SUPPORT PROCESSORS TO FORM LOAD MODULE IGC0007B (ALIAS      Y02893
*    IGC0107B, IGC0207B, IGC0407B, IGC5107B, IGC5W07B, IGCXX07B, Y02893
*    IGC0I07B), WHICH RESIDES IN THE PAGEABLE LINK PACK AREA.    Y02893
*                                                                Y02893
* CHARACTER CODE DEPENDENCE
*    NOT APPLICABLE
* NOTES
*    THIS IS A REPACKAGE OF THE MINI-ROUTER AND 1052 DSP         Y02893
*    WITH THE OTHER CONSOLE DEVICE PROCESSORS OF SVC 72          Y02893
*    TO FORM ONE LOAD MODULE. THE MINI-ROUTER NOW BRANCHES TO
*    ALL COMM TASK FUNCTIONS OF SVC 72. AN AUTHORITY CHECK ON    Y02893
*    THE ISSUER IS ALSO MADE. THE ISSUER OF SVC 72 MUST BE       Y02893
*    CONTROL PROGRAM KEY, AND SUPERVISOR STATE.                  Y02893
*                                                                Y02893
********************************************************************
         EJECT
         USING IEAVVCTR,R6         ESTABLISH MODULE BASE         Y02893
         MODID                                                   Y02893
         SPACE 1                                                 Y02893
         USING RBSECT,R5           BASE THE SVRB                 Y02893
         XC    RBEXSAVE(L48),RBEXSAVE  CLEAR EXTENDED SAVE AREA  Y02752
         LA    R2,ZERO(R1)         SAVE CONTENTS OF REG 1        Y02893
         STM   R14,R3,RBEXSAVE+(CSAXA-CXSA)  SAVE INITIAL REGS   Y02752
**************************************************************** Y02753
*                                                                Y02753
*  OBTAIN LOCAL LOCK TO USE BRANCH ENTRIES TO GETMAIN AND ESTAE  Y02753
*                                                                Y02753
**************************************************************** Y02753
         SPACE 1                                                 Y02753
GETLOCK SETLOCK OBTAIN,TYPE=LOCAL,MODE=UNCOND,                   Y02753*
               RELATED=(ESTAE,IEAVVCTR(FREELOCK))                Y02753
         EJECT
**************************************************************** Y02753
*                                                                Y02753
*  GETMAIN A RECOVERY AREA TO SAVE REGS AND CREATE A PARMLIST    Y02753
*                                                                Y02753
**************************************************************** Y02753
         SPACE 1                                                 Y02753
         SR    R8,R8               ZERO PTR TO AREA BEFORE GETM  Y02753
         GETMAIN RC,LV=S72RAL,BRANCH=YES,SP=S72SP                Y02753
         SPACE 1                                                 Y02753
         L     R3,CVTPTR           RESTORE CVT PTR AFTER GETMAIN Y02753
         LTR   R15,R15             WAS GETMAIN SUCCESSFUL (RC=0) Y02753
         BNZ   SKIPSTAE            IF NOT, DONT ISSUE ESTAE      Y02753
         LR    R8,R1               PTR TO RECOVERY WORK AREA     Y02753
         USING S72RCVR,R8          BASE RECOVERY WORK AREA       Y02753
         XC    S72RCVR(S72RAL),S72RCVR  CLEAR AREA TO ZERO       Y02753
         SPACE 1                                                 Y02753
*  STORE PTR TO GETMAINED AREA IN SVRB EXTENDED SAVE AREA        Y02753
         SPACE 1                                                 Y02753
         ST    R8,RBEXSAVE+(CSANPTR-CXSA)                        Y02753
         EJECT
**************************************************************** Y02753
*                                                                Y02753
*  ISSUE ESTAE TO PROTECT SVC 72                                 Y02753
*                                                                Y02753
**************************************************************** Y02753
         SPACE 1                                                 Y02753
         MVC   S72RSAVE(LENLF),STAELF  LIST FORM OF ESTAE        Y02753
         LA    R1,S72RSAVE         SET UP PTR TO ESTAE LIST FORM Y02753
         L     R9,CVTCUCB-CVT(R3)  PTR TO UCM BASE               Y02753
         L     R9,UCMFRRAD-UCM(R9) GET PTR TO ESTAE EXIT ROUTINE Y02753
         L     R13,ASCBASXB-ASCB(R7)   PTR TO ASXB               Y02753
         L     R13,ASXBSPSA-ASXB(R13)  PTR TO THE WSAVT          Y02753
         L     R13,WSALSTAE-WSAL(R13)     PTR TO STAE REGSAVE    Y02753
         SPACE 1                                                 Y02753
         ESTAE (R9),CT,PARAM=(R8),XCTL=NO,BRANCH=YES,            Y02753*
               SVEAREA=(R13),MF=(E,(1))                          Y02753
         EJECT
**************************************************************** Y02752
*                                                                Y02752
*  TEST WHETHER ESTAE WAS SET AND, IF SO, SET UP FOR RETRY       Y02753
*                                                                Y02752
**************************************************************** Y02752
         SPACE 1                                                 Y02752
         LA    R13,EIGHT           IF RC WAS 8 OR LESS, ESTAE OK Y02753
         CR    R15,R13             IS ESTAE RETURN CODE GT 8     Y02753
         BH    SKIPSTAE            IF GT 8, SKIP RETRY SETUP     Y02753
         SPACE 1                                                 Y02753
*  SET UP PARMLIST FOR ESTAE EXIT ROUTINE (IEAVMFRR)             Y02753
         SPACE 1                                                 Y02753
         LA    R9,S72PARAM         MAKE BASE FOR PARMLIST        Y02753
         USING PARMLIST,R9         BASE THE PARMLIST             Y02753
         MVC   PARMID,SVC72ID      MOVE IN MODULE IDENTIFIER     Y02753
         LA    R15,S72RFLGS        PTR TO REG SAVE AND FLAGS     Y02753
         ST    R15,PARMRGAD        PLACE IT IN PARMLIST          Y02753
         LA    R15,CTRRTRY1        PTR TO 1ST RETRY ADDRESS      Y02753
         ST    R15,PARMRTAD        PLACE IT IN PARMLIST          Y02753
         SR    R15,R15             SET REGISTER TO ZERO          Y02753
         BCTR  R15,0               MAKE REGISTER ALL F'S         Y02753
         STH   R15,S72RFLGS        INDICATE ALL REGS BE RESTORED Y02753
         STM   R0,R15,S72RSAVE     SAVE REGS FOR RETRY ROUTINE   Y02753
         EJECT
**************************************************************** Y02752
*                                                                Y02752
*  TEST AUTHORIZATION OF CALLER.                                 Y02752
*                                                                Y02752
**************************************************************** Y02752
SKIPSTAE DS    0H                  START HERE IF ESTAE NOT SET   Y02753
         SPACE 1                                                 Y02752
         TESTAUTH STATE=YES,KEY=YES,BRANCH=YES TEST CALLERS AUTH Y02752
         SPACE 1                                                 Y02752
**************************************************************** Y02753
*                                                                Y02753
*  RELEASE THE LOCAL LOCK                                        Y02753
*                                                                Y02753
**************************************************************** Y02753
         SPACE 1                                                 Y02753
FREELOCK SETLOCK RELEASE,TYPE=LOCAL,                             Y02753*
               RELATED=(ESTAE,IEAVVCTR(GETLOCK))                 Y02753
         SPACE 1                                                 Y02753
CTRCHKRC DS    0H                                                Y02753
         LTR   R15,R15             TEST RETURN CODE OF TESTAUTH  Y02752
         LM    R14,R3,RBEXSAVE+(CSAXA-CXSA) RESTORE ORIGINAL REG Y02752
         BNZ   NOTAUTH             IF CALLER WAS NOT SUPERVISOR  Y02752
*                                  STATE AND KEY (RC NOT=ZERO),  Y02752
*                                  RETURN WITHOUT ACTION.        Y02752
         LTR   R8,R8               WAS RECOVERY AREA GETMAINED   Y02753
         BZ    NORECVR             IF NOT, SKIP 2ND RETRY SETUP  Y02753
         ST    R14,S72SAV14        SAVE ORIGINAL RETURN ADDRESS  Y02753
         LA    R14,CTRSWEXT        GET EXIT ADDR FOR RETRY     @ZA00873
         ST    R14,PARMRTAD        STORE EXIT ADDR IN PARMLIST @ZA00873
         LA    R14,CTREXIT         MAKE DEVICE PROCESSORS EXIT   Y02753
*                                  THROUGH THIS MODULE.          Y02753
         STM   R0,R15,S72RSAVE     SAVE CURRENT REGS             Y02753
         DROP  R8,R9               S72RCVR AND PARMLIST BASES    Y02753
         SPACE 3                                                 Y02752
NORECVR  DS    0H                                                Y02753
         LA    R1,RBEXSAVE         ESTABLISH BASE ADDR FOR CXSA  Y02893
         USING CXSA,R1                  ESTABLISH CXSA ADDRESSABILITY
         MVC   CSANAME(K32),ZERO(R2)    MOVE PARMS TO XSA        Y02893
         EJECT
**************************************************************** Y02752
*                                                                Y02752
*    DETERMINE WHICH ROUTINE TO GO TO.                           Y02752
*        COMPARE THE NAME PASSED IN THE PARAMETER LIST AGAINST   Y02752
*        THOSE IN THE DEVICE PROCESSOR NAME TABLE. IF ONE        Y02752
*        MATCHES, PICK UP UCME ADDRESS FROM PARAMETER LIST, AND  Y02752
*        GET ADDRESS OF PROCESSOR FROM FIELD UCMSBR THEN BRANCH  Y02752
*        TO IT. IF NO NAME MATCH IS FOUND IN THE NAME TABLE,     Y02752
*        COMPARE THE PARAMETER LIST NAME TO 'IGC0407B', THE      Y02752
*        CONSOLE SWITCH ROUTINE NAME. IF EQUAL, GET ADDRESS OF   Y02752
*        SWITCH ROUTINE FROM THE UCMBASE AND BRANCH TO IT.       Y02752
*                                                                Y02752
*        IF NO NAME MATCH IS FOUND IN THE ABOVE SEARCH, XCTL TO  Y02752
*        THE NAME PASSED IN THE PARAMETER LIST.                  Y02752
*                                                                Y02752
**************************************************************** Y02752
         SPACE 1                                                 Y02752
*  DEVICE PROCESSOR NAME SEARCH.                                 Y02752
         LM    R9,R11,BXLEINFO     ADDR 1ST, LEN, ADDR LAST      Y02752
         USING MODTBL,R9           BASE THE TABLE NAME ENTRY     Y02752
NAMESRCH DS    0H                                                Y02752
         CLC   CSANAME,MODNAME     DOES INPUT NAME MATCH TABLE   Y02752
         BE    DEVPROC             IF YES, GO TO DEVICE CALLER   Y02752
         BXLE  R9,R10,NAMESRCH     IF NO MATCH IS FOUND, UPDATE  Y02752
*                                  TABLE ENTRY POINTER, CHECK    Y02752
*                                  FOR TABLE END, LOOF IF MORE   Y02752
*                                  OR FALL THRU IF END.          Y02752
         SPACE 1                                                 Y02752
         CLC   CSANAME,SWCHNAME    COMPARE INPUT TO 'IGC0007B'   Y02752
         BNE   GOBYXCTL            IF NOT CONSOLE SWCH, BRANCH   Y02752
         USING CVT,R15             BASE FOR CVT                @YM08346
         L     R15,CVTPTR          GET ADDR OF CVT             @YM08346
         L     R15,CVTCUCB         GET UCM BASE ADDR           @YM08346
         DROP  R15                                             @YM08346
         USING UCM,R15             BASE THE UCM                  Y02752
         L     R15,UCMSWCH         GET ADDR OF CONSOLE SWITCH    Y02752
         DROP  R15                                               Y02752
         BR    R15                 GO TO CONSOLE SWITCH ROUTINE  Y02752
         SPACE 1                                                 Y02752
DEVPROC  DS    0H                                                Y02752
         L     R15,CSAUCM          GET ADDR OF UCME              Y02752
         USING UCMLIST,R15         BASE THE UCME                 Y02752
         L     R15,UCMSBR          GET ADDRESS OF PROCESSOR      Y02752
         DROP  R15                                               Y02752
         SPACE 1                                                 Y02752
*    IF THE ADDR IN UCMSBR IS THE SAME AS THE E.P. ADDR OF       Y02752
*    IEAVVCTR, THE CALL IS FOR IEAV1052.                         Y02752
         SPACE 1                                                 Y02752
         CR    R6,R15              IS CALL FOR IEAV1052          Y02752
         BNER  R15                 IF NOT, GOT TO PROCESSOR      Y02752
         L     R15,AD1052          IF CALLED FOR IEAV1052, GET   Y02752
*                                  THE ADDRESS FROM A VCON.      Y02752
         BR    R15                 BRANCH TO IEAV1052            Y02752
         SPACE 1                                                 Y02752
GOBYXCTL DS    0H                                                Y02752
         L     R8,CSANPTR          PTR TO RECOVERY AREA IF OBTND Y02753
         LTR   R8,R8               WAS RECOVERY AREA GOTTEN      Y02753
         BZ    CTRXCTL1            IF NOT, PROCEDE WITH XCTL     Y02753
         USING S72RCVR,R8          BASE RECOVERY DSECT           Y02753
         LR    R1,R8               ADDRESS OF STORAGE TO FREE    Y02753
         L     R14,S72SAV14        GET ORIGINAL RETURN ADDRESS   Y02753
         SR    R15,R15             MAKE A ZERO CONSTANT          Y02753
         SPACE 1                                                 Y02753
*  ZERO RETRY ADDRESS BEFORE ISSUING FREEMAIN                    Y02753
         SPACE 1                                                 Y02753
         ST    R15,S72PARAM+(PARMRTAD-PARMLIST)                  Y02753
         DROP  R8                  BASE FOR S72RCVR              Y02753
         SPACE 1                                                 Y02753
         FREEMAIN R,LV=S72RAL,SP=S72SP,A=(1) FREE RECOVERY AREA  Y02753
         LA    R1,RBEXSAVE         RESTORE PTR TO CXSA           Y02753
         SPACE 1                                                 Y02753
CTRXCTL1 DS    0H                                                Y02753
         LA    R15,CSANAME              POINT TO MODULE NAME
         ST    R15,CSANPTR              STORE ADDR OF MODULE NAME
         LR    R15,R1                   NO, RE-ESTABLISH ADDR OF XSA
         XCTL  SF=(E,(15))              PASS CONTROL TO NEXT MODULE
         SPACE 3                                                 Y02753
NOTAUTH  DS    0H                                                Y02753
         LTR   R8,R8               WAS RECOVERY AREA GOTTEN      Y02753
         BZR   R14                 IF NOT, RETURN TO CALLER      Y02753
         B     CTREXIT1            OTHERWISE, GO FREE AREA       Y02753
************************************************************** @ZA00873
*                  RETRY ROUTINE                               @ZA00873
*                                                              @ZA00873
* FUNCTION: THIS EXIT WILL GET CONTROL WHEN THERE IS A PROBLEM @ZA00873
*           IN THE DSP (DEVICE SERVICE PROCESSOR) THAT IT CAN  @ZA00873
*           NOT TOTALLY RECOVER FROM. TO PREVENT THE           @ZA00873
*           PROBLEM FROM RECURRING OR CAUSING OTHER DSP        @ZA00873
*           PROBLEMS, THIS EXIT WILL CREATE THE ENVIRONMENT    @ZA00873
*           NECESSARY TO INVOKE CONSOLE SWITCH FOR THE         @ZA00873
*           ASSOCIATED CONSOLE.                                @ZA00873
*                                                              @ZA00873
* INPUT: REGISTER 14 = ENTRY ADDR OF THIS SEGMENT              @ZA00873
*                                                              @ZA00873
* OUTPUT: PARMLIST FOR CONSOLE SWITCH ROUTINE                  @ZA00873
*                                                              @ZA00873
************************************************************** @ZA00873
CTRSWEXT DS    0H                                              @ZA00873
         BALR R14,ZERO             ADDRESSABILITY              @ZA00873
         USING *,R14               TEMPORARY ADDRESSABILITY    @ZA00873
         L     R6,ADVCTR           RESTORE NORMAL BASE         @ZA00873
         DROP  R14                 DROP TEMPORARY BASE         @ZA00873
         USING PSA,R0              BASE FOR PSA                @ZA00873
         L     R5,PSATOLD          ADDR OF CURRENT TCB         @ZA00873
         DROP  R0                  DROP PSA BASE               @ZA00873
         L     R5,TCBRBP-TCB(R5)   ADDR OF SVRB                @ZA00873
         L     R8,RBEXSAVE+(CSANPTR-CXSA) ADDR OF RECOVERY AREA@ZA00873
         USING S72RCVR,R8          BASE FOR RECOVERY AREA      @ZA00873
         LA    R14,CTREXIT         MAKE RETURN EXIT THIS MOD   @ZA00873
         ST    R14,S72PARAM+(PARMRTAD-PARMLIST) RETRY EXIT     @ZA00873
         LA    R1,RBEXSAVE         GET POINTER TO CXSA         @ZA00873
         USING CXSA,R1             ADDRESSABILITY FOR CXSA     @ZA00873
         MVI   CSACODE,CSASWTCH    INDICATE CONSOLE SWITCH     @ZA00873
         MVC   CSANAME,SWCHNAME    NAME OF SWITCH ROUTINE      @ZA00873
* ALL OTHER FIELDS NEEDED IN THE PARAMETER LIST TO THE CONSOLE @ZA00873
* SWITCH ROUTINE ARE ALREADY FILLED IN.                        @ZA00873
         USING CVT,R15             ADDRESSABILITY FOR CVT      @ZA00873
         L     R15,CVTPTR          ADDRESS OF CVT              @ZA00873
         L     R15,CVTCUCB         GET UCM BASE ADDRESS        @ZA00873
         DROP  R15                 DROP OUT ADDRESSABILITY     @ZA00873
         USING UCM,R15             ADDRESSABILITY FOR UCM BASE @ZA00873
         L     R15,UCMSWCH         ADDR OF CONSOLE SWITHC      @ZA00873
         DROP  R15                 DROP UCM ADDRESSABILITY     @ZA00873
         BR    R15                 GIVE CONTROL TO  SWITCH RTN @ZA00873
**************************************************************** Y02753
         EJECT
**************************************************************** Y02753
*                                                                Y02753
*  COMMON EXIT POINT FOR SVC 72 WHEN AN ESTAE IS SET             Y02753
*                                                                Y02753
**************************************************************** Y02753
         SPACE 1                                                 Y02753
CTREXIT  DS    0H                                                Y02753
         USING *,R14               TEMPROARY ADDRESSABILITY      Y02753
         L     R6,ADVCTR           RESTORE NORMAL PROGRAM BASE   Y02753
         DROP  R14                 DROP TEMPORARY PROGRAM BASE   Y02753
         USING PSA,R0              BASE PSA ON LOCATION ZERO     Y02753
         L     R5,PSATOLD          GET PTR TO MY TCB             Y02753
         DROP  R0                                                Y02753
         L     R5,TCBRBP-TCB(R5)   GET PTR TO MY SVRB            Y02753
         SPACE 1                                                 Y02753
*  THE RB IS ALREADY BASED ON REG 5                              Y02753
         SPACE 1                                                 Y02753
         L     R8,RBEXSAVE+(CSANPTR-CXSA)  PTR TO RECOVERY AREA  Y02753
         USING S72RCVR,R8          BASE THE RECOVERY DSECT       Y02753
         L     R14,S72SAV14        GET ORIGINAL RETURN ADDRESS   Y02753
CTREXIT1 DS    0H                                                Y02753
         SR    R1,R1               MAKE A ZERO CONSTANT          Y02753
         ST    R1,S72PARAM+(PARMRTAD-PARMLIST)  ZERO RETRY ADDR  Y02753
         LR    R1,R8               AREA TO BE FREEMAINED         Y02753
         DROP  R8                  DROP BASE FOR S72RCVR         Y02753
         LR    R8,R14              SAVE R14 OVER FREEMAIN        M03457
         LR    R9,R15              SAVE R15 OVER FREEMAIN        M03457
         SPACE 1                                                 Y02753
         FREEMAIN  R,LV=S72RAL,A=(1),SP=S72SP FREE RECOVERY AREA Y02753
         LR    R14,R8              RESTORE R14 AFTER SVC         M03457
         LR    R15,R9              RESTORE R15 AFTER SVC         M03457
         SPACE 1                                                 Y02753
         BR    R14                 RETURN TO SVC 72 ISSUER       Y02753
         EJECT
CTRRTRY1 DS    0H                  1ST RETRY ENTRY POINT         Y02753
         USING S72RCVR,R8          REBASE THE RECOVERY AREA      Y02753
         SR    R15,R15             MAKE RC FROM TESTAUTH ZERO    Y02753
         B     CTRCHKRC            GO ON WITH PROCESSING         Y02753
         TITLE    'IEAVVCTR, EQUATES, CONSTANTS, AND DSECTS'     Y02893
TEL      EQU   8                   TABLE ENTRY LENGTH, MODNMTBL  Y02752
         SPACE 1                                                 Y02752
BXLEINFO DS    0F                  THREE ADCONS FOR A LM INSTR   Y02752
         DC    A(MODNMTBL)         ADDR OF START OF TABLE        Y02752
         DC    A(TEL)              LENGTH OF A TABLE ENTRY       Y02752
         DC    A(MODNMEND)         ADDR OF END OF TABLE          Y02752
         SPACE 1                                                 Y02752
ADVCTR   DC    A(IEAVVCTR)         USED TO RESTORE PROGRAM BASE  Y02753
AD1052   DC    V(IEAV1052)         ADDRESS OF 1052 DEVICE SUPPORT
         SPACE 1                                                 Y02752
MODNMTBL DS    0D                  DEVICE PROCESSOR NAME TABLE   Y02752
         DC    CL(TEL)'IGC0007B'   1052 DEVICE PROCESSOR         Y02752
         DC    CL(TEL)'IGC5107B'   GRAPHICS DEVICE PROCESSOR     Y02752
         DC    CL(TEL)'IGCXX07B'   2740 DEVICE PROCESSOR         Y02752
         DC    CL(TEL)'IGC5W07B'   3277 PRINTER PROCESSOR        Y02752
         DC    CL(TEL)'IGC0107B'   PRINTER DEVICE PROCESSOR      Y02752
         DC    CL(TEL)'IGC0207B'   CARD READER DEVICE PROCESSOR  Y02752
         ORG   *-TEL               BACK UP TO LAST TABLE ENTRY   Y02752
MODNMEND EQU   *                   END OF TABLE SYMBOL           Y02752
         ORG
         SPACE 1                                                 Y02752
PATCH    DC    XL50'0'             PATCH AREA FOR IEAVVCTR       Y02752
         SPACE 2                                                 Y02752
SWCHNAME DC    CL8'IGC0407B'       NAME OF CONSOLE SWITCH ROUTIN Y02752
         SPACE 2                                                 Y02752
SVC72ID  DC    CL4'VCTR'           IDENTIFIER FOR SVC 72         Y02753
         SPACE 2                                                 Y02753
*  LIST FORM OF ESTAE TO MAKE MODULE REENTRANT                   Y02753
         SPACE 1                                                 Y02753
STAELF   ESTAE ,CT,BRANCH=YES,XCTL=NO,MF=L                       Y02753
LENLF    EQU   *-STAELF            LENGTH OF LIST FORM EXPANSION Y02753
         EJECT
R0       EQU   0                   REG 0                         Y02893
R1       EQU   1                   HOLDS PTR TO CXSA             Y02893
R2       EQU   2                   REG 2                         Y02893
R3       EQU   3                   CVT ADDR                      Y02893
R4       EQU   4                   TCB ADDR                      Y02893
R5       EQU   5                   SVRB ADDR                     Y02893
R6       EQU   6                   EP ADDR, MODULES BASE         Y02893
R7       EQU   7                   ASCB ADDR                     Y02893
R8       EQU   8                   REG 8                         Y02893
R9       EQU   9                   REG 9                         Y02893
R10      EQU   10                  REG 10                        Y02893
R11      EQU   11                  REG 11                        Y02893
R12      EQU   12                  REG 12                        Y02893
R13      EQU   13                  REG 13                        Y02893
R14      EQU   14                  RETURN ADDR REGS              Y02893
R15      EQU   15                  REG 15                        Y02893
         SPACE 1                                                 Y02893
ZERO     EQU   0                        DISPLACEMENT
FOUR     EQU   4                        DISPLACEMENT
EIGHT    EQU   8                   RETURN CODE COMPAREND         Y02753
K32      EQU   32                       MOVE 32 BYTES
L48      EQU   48                  LENGTH OF SVRB EXTENDED SAVE  Y02752
         SPACE 2                                                 Y02752
MODTBL   DSECT
         DS    0D                                                Y02752
MODNAME  DS    CL(TEL)             MAPPING OF A MODNMTBL ENTRY   Y02752
         SPACE 1                                                 Y02753
S72RCVR  DSECT
S72PARAM DS    6F                  PARMLIST AREA FOR ESTAE EXIT  Y02753
S72SAV14 DS    A                   SAVE AREA FOR RETURN REG      Y02753
S72RSVD1 DS    H                   RESERVED                      Y02753
S72RFLGS DS    XL2                 FLAGS FOR REG RESTORING       Y02753
S72RSAVE DS    16F                 REG SAVE AREA FOR 16 REGS     Y02753
S72RAL   EQU   *-S72RCVR           LENGTH OF DSECT FOR GETMAIN   Y02753
         SPACE 1                                                 Y02753
S72SP    EQU   231                 SUBPOOL NUMBER FOR GETMAIN    Y02753
         EJECT
         IHAASCB                   ADDRESS SPACE CONTROL BLOCK   Y02753
         EJECT
         IHAASXB                   ADDRESS SPACE EXTENSION BLOCK Y02753
         EJECT
         IKJTCB                    TASK CONTROL BLOCK            Y02753
         EJECT
         CVT   DSECT=YES                                         Y02752
         EJECT
         IHACTM FTPT               CTM, FOOTPRINT (PARMLIST)     Y02753
         EJECT
         IEECUCM  FORMAT=NEW                                     Y02752
         EJECT
         IHAPSA                    PREFIXED SAVE AREA            Y02753
         EJECT
         IHARB DSECT=YES                                         Y02893
         EJECT
         IHACTM CXSA
         EJECT
         IHAWSAVT                  WSA VECTOR TABLE              Y02753
         END
