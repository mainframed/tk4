         TITLE 'IEAVMQWR - COMM TASK WAIT AND ROUTER'
IEAVMQWR CSECT
* WRLOCK(C) APPROX 062603           OBLOCK(A)  APPROX 134057   @ZA11392
* WRWTO(A)  APPROX 109860           FRLOCKS(C) APPROX 134125   @ZA11392
* WRSWCH(A) APPROX 074400           TWENTY(A)  APPROX 144100   @ZA19273
**** START OF SPECIFICATIONS ******************************************
*
*01*     MODULE-NAME = IEAVMQWR
*
*01*     DESCRIPTIVE-NAME = COMM TASK WAIT AND ROUTER
*
*01*     COPYRIGHT = NONE
*
*01*     STATUS = MVS 3033 PROCESSOR SUPPORT(SCP)              @G51APSS
*
*01*     FUNCTION = THE WAIT ROUTINE WAITS ON ALL COMMUNICATIONS
*                   EVENT CONTROL BLOCKS.  IT ROUTES CONTROL TO THE
*                   COMM TASK SERVICE ROUTINES WHEN ANY OF THE ECBS
*                   IS POSTED.
*
*02*          OPERATION = WHEN THE WAIT ROUTINE IS AWAKENED IT CHECKS
*                         THE ECBS AND SERVICES REQUESTS ACCORDING TO
*                         THE FOLLOWING PRIORITY: AUTOMATIC CPU
*                         RECOVERY, EXTERNAL INTERRUPT,
*                         ATTENTION, I/O COMPLETION, CONSOLE OUTPUT,
*                         WTO, DOM AND LOG INITIALIZATION.
*
*                         IF A NO-CONSOLES CONDITION EXISTS    @G51APSS
*                         AND THE MASTER CONSOLE CANDIDATE HAS @G51APSS
*                         BEEN CHOSEN THEN ONLY THE AUTOMATIC  @G51APSS
*                         CPU RECOVERY AND EXTERNAL INTERRUPT  @G51APSS
*                         ECBS ARE WAITED ON.                  @G51APSS
*
*                         BEFORE CHECKING TO SEE IF OECB IS    @ZA11392
*                         POSTED SEE IF MESSAGE IEA406I MSG    @ZA11392
*                         SHOULD BE ISSUED AND IF A DOM SHOULD @ZA11392
*                         BE ISSUED FOR MESSAGE IEA404A.       @ZA11392
*                                                              @ZA11392
*                         IF OECB IS POSTED THEN CHECK TO SEE  @ZA11392
*                         IF THE WQE THRESHOLD MESSAGES SHOULD @ZA11392
*                         BE ISSUED.(IEA405E IEA404A).         @ZA11392
*                                                              @ZA11392
*                         AFTER A POSTED ECB HAS BEEN SERVICED, IT
*                         RETURNS TO ITS STARTING POINT AND BEGINS
*                         CHECKING THE ECB LIST AGAIN IN THE SAME
*                         ORDER.  WHEN ALL THE ECBS HAVE BEEN CHECKED
*                         AND NOTHING IS POSTED, IT RETURNS TO A WAIT.
*
*01*     NOTES = NONE
*
*02*          DEPENDENCIES = NO DEVICE DEPENDENCIES.
*
*03*               CHARACTER-CODE-DEPENDENCIES = NONE.
*
*02*          RESTRICTIONS = NONE.
*
*02*          REGISTER CONVENTIONS = SEE LABEL QWRREGS
*
*02*          PATCH-LABEL = SPACE                              @G51APSS
*
*01*     MODULE TYPE = PROCEDURE
*
*02*          PROCESSOR = ASSEMBLER-370R
*
*02*          MODULE-SIZE = X'43C' BYTES                       @G51APSS
*
*02*          ATTRIBUTES = KEY 0, SERIALLY REUSEABLE,
*                          SUPERVISOR STATE, PART OF LOAD
*                          MODULE IEAVCTSK OF THE SYS1.LPALIB DATA SET
*
*01*     ENTRY-POINT = IEAVMQWR
*
*02*          PURPOSE = TO SET UP THE ENVIRONMENTAL REGISTERS, TO
*                       INITIALIZE THE ECBS IN THE EIL AND ISSUE A
*                       WAIT ON THE EIL.
*
*02*          LINKAGE = BRANCHED TO FROM THE DISPATCHER AFTER SYSTEM
*                       INITIALIZATION.
*
*02*          INPUT = NONE.
*
*02*          OUTPUT = N/A (NO TRUE EXIT)
*
*02*          REGISTER-SAVED = NONE
*
*02*          REGISTER-USAGE = SEE LABEL QWRREGS
*
*02*          REGISTERS-RESTORED = N/A (NEVER ENDING ROUTINE)
*
*01*     ENTRY-POINT = WRABXLE
*
*02*          PURPOSE = TO CONTINUE THE SEARCH OF THE UCMES FOR AN
*                       ATTENTION PENDING.  THE CURRENT UCME WITH AN
*                       ATTENTION PENDING CANNOT BE PROCESSED.
*
*02*          LINKAGE = BRANCHED TO FROM MODULE IEAVMDSV.
*
*02*          INPUT = R2 - ADDR OF UCM
*                     R3 - ADDR OF NEXT UCME
*                     R4 - ADDR OF UCM PREFIX
*                     R8 - ADDR OF EIL
*                     R9 - BASE REGISTER
*                     R11-13 - UCME BXLE REGISTERS
*                     THE OTHER REGISTERS ARE IRRELEVANT.
*
*02*          OUTPUT = N/A (NO TRUE EXIT)
*
*02*          REGISTERS-SAVED = NONE
*
*02*          REGISTER-USAGE = SEE LABEL QWRREGS
*
*02*          REGISTERS-RESTORED = N/A (NEVER ENDING ROUTINE)
*
*01*     EXIT-NORMAL = NONE. (NEVER ENDING ROUTINE.)
*
*01*     EXIT-ERROR = NONE.
*
*01*     EXTERNAL REFERENCES = THIS MODULE SERVES AS A ROUTER TO THE
*                              FOLLOWING ROUTINES:
*
*02*          ROUTINES = IEAVSWCH, IEAVMDSV, IEAVMWSV, IEAVMDOM,
*                        IEAVMWTL
*
*02*          DATA AREAS = NONE.
*
*02*          CONTROL BLOCKS = IEECUCM-W,  CVT-R,  IHAFRRS-R,
*                  IHAPSA-R
*
*01*     TABLES = NONE.
*
*01*     MACROS = WAIT, SETLOCK, LINK, GETMAIN, FREEMAIN, SETFRR,
*                 WTO, DOM                                     @ZA11392
*
*02*          SERIALIZATION = THE CMS AND LOCAL LOCKS ARE USED TO
*                             SERIALIZE USE OF THE UCM.
*
*01*     CHANGE-ACTIVITY = ZM34522,ZA11392,ZA19273,G51APSS     @G51APSS
*
*01*     MESSAGES =
*
*                   IEA404A WTO BUFFER SHORTAGE CRITICAL       @ZA11392
*                   IEA405E WTO BUFFER SHORTAGE                @ZA11392
*                   IEA406I WTO BUFFER SHORTAGE RELIEVED       @ZA11392
*
*01*     ABEND CODES = NONE
*
**** END OF SPECIFICATIONS ********************************************
         EJECT
         ENTRY WRABXLE
         EXTRN IEAVMDSV
         EXTRN IEAVMWSV
         EXTRN IEAVMDOM
         USING UCM,R2
         USING UCMPRFX,R4
         USING UCMEIL,R8
         USING CVT,R10
         USING UCMLIST,R11
         SPACE 1
***********************************************************************
*                                                                     *
*        IEECMWRT GETS CONTROL AFTER SYSTEM INITIALIZATION.           *
*        IT SETS UP ENVIRONMENTAL REGISTERS FOR ALL COMMUNICATIONS    *
*        TASK RESIDENT ROUTINES. IT THEN GIVES CONTROL TO CTWSTART    *
*                                                                     *
***********************************************************************
IEECMWRT BALR  R9,ZERO             SET UP R9 BASE REGISTER
         USING *,R9                ESTABLISH ADDRESSABILITY
         MODID
AA       L     R10,CVTPTR          LOAD CVT POINTER
         L     R2,CVTCUCB          GET ADDRESS OF UCM
         DROP  R10                 DROP BASE FOR CVT             Y02752
         L     R3,UCMVEA           SAVE UCM ENTRY PTR
         LR    R5,R2               POINT TO BEGINNING OF UCM
         LA    R4,FOUR             DECREMENT BY FOUR
         SR    R5,R4               POINT TO ADDED PREFIX POINTER
         L     R4,ZERO(R5)         R4 NOW POINTS TO THE PREFIX
         SPACE 1
***********************************************************************
*                                                                     *
*        CTWSTART ISSUES A WAIT ON ALL COMMUNICATIONS ECB'S.          *
*        THIS BECOMES THE ENTRY POINT OF THE WAIT MODULE AFTER        *
*        IEECMWRT EXITS.                                              *
*                                                                     *
***********************************************************************
         L     R8,UCMLSTP          INITIALIZE EIL PTR
CTWSTART LA    R1,UCMXECBA         POINT R1 TO LIST OF ECB ADDRESSES
WRNCCWAT EQU   *                                               @G51APSS
         WAIT  ECBLIST=(1)         WAIT FOR NEXT EVENT
         EJECT
**********************************************************************
*                                                                    *
*        OBTAIN THE LOCKS AND SET UP FRR RECOVERY             @ZA11392
*                                                                    *
**********************************************************************
         SPACE
WRLOCK   EQU *            OBTAIN THE LOCKS TO SERIALIZE       @ZA11392
         BAL R10,OBLOCKS  THE USE OF THE UCM                  @ZA11392
         SPACE
WREXT    EQU   *                   BEGIN CHECKING FOR POSTED ECBS
         SPACE
***********************************************************************
*                                                                     *
*        CHECK AUTOMATIC CPU RECOVERY ECB.  GO TO CONSOLE SWITCH FOR  *
*        EACH BIT ON IN THE CPU MASK CONTAINED IN THE COMPLETION CODE *
*        FIELD OF THE ECB.  ALL BITS IN UCMARECB MUST BE TURNED OFF   *
*        VIA A CS INSTRUCTION SINCE ACR DOES NOT USE THE CMS LOCK     *
*        TO SERIALIZE MODIFICATION OF UCMARECB.                       *
*                                                                     *
***********************************************************************
         SPACE
         TM    UCMARECB,POST       IS ACR ECB POSTED
         BZ    WREXT1              NO, CHECK NEXT ECB
WRACR    EQU   *                   SET UP TEST OF CPU ID MASK
         ICM   R11,CCMASK,UCMARECB+TWO GET CPU ID INTO FIRST 2
*                                  BYTES OF WORKREG
         LA    R12,ONE             INITIALIZE CPU ID PARM REG
         SLL   R12,FIFTEEN         GET HIGH ORDER BIT OF 2ND
*                                  HALF WORD ON
         LA    R13,SIXTEEN         NUMBER OF BITS IN CPU ID MASK
         SPACE
WRACRLP  EQU   *                   LOOP THROUGH CPU ID MASK
*                                  LOOKING FOR A BIT ON AND AT
*                                  THE SAME TIME SETTING UP
*                                  CONSOLE SWITCH PARAMETER
         LTR   R11,R11             IS HIGH ORDER BIT OF CPU MASK
*                                  ON
         BM    WRACRGO             YES, GO TO CONSOLE SWITCH
         SLL   R11,ONE             NO, SET UP TO TEST NEXT BIT
         SRL   R12,ONE             SET UP CONSOLE SWITCH PARM
         BCT   R13,WRACRLP         GO TEST NEXT BIT
* NO BITS IN CPU ID MASK ARE ON, TURN OFF POST BIT IN ECB
         SPACE
         SR    R11,R11             ZERO SWAP REG
         LA    R12,ONE             GET A BIT ON
         SLL   R12,THIRTY          SET UP COMPARE REG WITH ONLY
*                                  POST BIT ON
         CS    R12,R11,UCMARECB    ATTEMPT TO CLEAR POST BIT
         BNE   WRACR               SWAP FAILED - ANOTHER CPU BIT
*                                  WAS SET, GO SERVICE IT
         EJECT
**********************************************************************
*                                                                     *
*        SERVICE EXTERNAL INTERRUPTS BY GOING TO IGC0407B FOR         *
*        EVERY INTERRUPT                                              *
*                                                                     *
**********************************************************************
         SPACE 1
WREXT1   CLI   UCMXCT,ZERO         HAS EXTERNAL ECB BEEN POSTED
         BE    WRNCC               NO, CHECK FOR NO-CONSOLES           X
                                   CONDITION                   @G51APSS
         SR    R6,R6               CLEAR REG BEFORE USE          Y02752
         L     R5,UCMXCT           GET CURRENT COPY OF           Y02752
*                                     INTERRUPT COUNT            Y02752
         SPACE
WREXT2   DS    0H                  CS LOOP                       Y02752
         LR    R7,R5               MAKE WORKING COPY FOR CS INST Y02752
         SLDL  R6,ONEBYTE          ISOLATE INTERRUPT COUNT       Y02752
         BCTR  R6,ZERO             DECREMENT COUNT               Y02752
         SRDL  R6,ONEBYTE          MOVE COUNT BACK TO REG 7      Y02752
         CS    R5,R7,UCMXCT        STORE NEW COUNT IF OLD        Y02752
*                                     COUNT IS STILL VALID       Y02752
         BNE   WREXT2              IF COMPARE FAILED, GO BACK    Y02752
*                                     AND TRY AGAIN. NEW VALUE   Y02752
*                                     OF UCMXCT HAS BEEN PLACED  Y02752
*                                     IN REG 5 IF COMPARE FAILED Y02752
         MVI   UCMXSA+SIXTEEN,X04  INDICATE EXTERNAL INTERRUPT
         SPACE
WRSWCH   EQU   *                   GO TO CONSOLE SWITCH
         MVC   UCMXSA+EIGHT(EIGHT),WREXTNAM NAME OF CONSOLE
*                                  SWITCH ROUTINE
         ST    R2,UCMXSA+TWENTY    PUT UCMBASE INTO PARMLIST   @ZA19273
         LA    R1,UCMXSA+EIGHT     POINT TO NAME FIELD
         SPACE
* RELEASE LOCKS IN ORDER TO ISSUE AN SVC
         SPACE
         BAL   R10,FRLOCKS         RELEASE LOCKS                 Y02752
         SVC   SVC72               SVC 72 - GO TO SWITCH RTN
         B     WRLOCK              REOBTAIN LOCKS AND CHECK ECBS
*                                  AGAIN
         EJECT
***********************************************************************
*        IF A NO-CONSOLES CONDITION EXISTS AND A CANDIDATE     @G51APSS
*        FOR THE MASTER CONSOLE HAS BEEN CHOSEN THEN WAIT ONLY @G51APSS
*        ON THE ACR AND THE EXTERNAL INTERRUPT ECBS.           @G51APSS
***********************************************************************
WRNCC    EQU   *                                               @G51APSS
         XC    UCMXECB(FOUR),UCMXECB CLEAR EXTERNAL ECB        @G51APSS
         TM    UCMSFLG1,UCMSYSE    DOES A NO-CONSOLES CONDITION EXIST? X
                                                               @G51APSS
         BNO   WRATTN              NO, CHECK ATTN ECB          @G51APSS
         L     R7,UCMBFEXT         ADDR OF UCM FIXED EXTENSION BASE    X
                                                               @G51APSS
         USING UCMFEXTA,R7         BASE TO UCM FIXED EXTENSION BASE    X
                                                               @G51APSS
         L     R10,UCMFATCN        ADDR OF CANDIDATES UCME     @G51APSS
         LTR   R10,R10             CANDIDATE CHOSEN YET?       @G51APSS
         BNP   WRATTN              NO, CHECK ATTN ECB          @G51APSS
         BAL   R10,FRLOCKS         RELEASE THE LOCKS           @G51APSS
         LA    R1,UCMFECBL         ECB LIST TO WAIT ON         @G51APSS
         DROP  R7                  FREE BASE TO UCM FIXED EXTENSION    X
                                   BASE                        @G51APSS
         B     WRNCCWAT            WAIT FOR ACR OR EXTERNAL INTERUPT   X
                                                               @G51APSS
***********************************************************************
*                                                                     *
*        GO THROUGH CONSOLE ENTRIES, AND WHENEVER AN ATTENTION IS     *
*        PENDING, GO TO ROUTINE WRATSERV.                             *
*                                                                     *
***********************************************************************
WRATTN   EQU   *                                               @G51APSS
         TM    UCMAECB,POST        HAS ATTENTION ECB BEEN POSTED
         BZ    WRIOCOMP            NO, CHECK I/O ECB'S
         TM    UCMSFLG2,UCMSYSO    WAS ATTN POSTED BY WTL
         BNO   WRALOOP1            NO - REGULAR ATTENTION
         NI    UCMSFLG2,MASK255-UCMSYSO TURN OFF FLAG
         CLI   UCMAECB+THREE,HCSWCC HC SWITCH REQUESTED BY WTO   Y02756
         BE    WRHCSW              YES, SET UP TO ISSUE SVC 72   Y02756
         LA    R1,EIGHT            SIGNAL CLEANUP WITH CODE = 8
         B     WRAWTLSV            GO TO DEVICE SERVICE ROUTINE
WRALOOP1 LM    R11,R13,UCMVEA      INITIALIZE BXLE UCM ENTRY REGS
WRALOOP  TM    UCMSTS,UCMAF        IS ATTN PENDING FOR THIS ENTRY
         BNO   WRABXLE             NO, BRANCH TO GET NEXT ENTRY
         TM    UCMATR,UCMUF        IS THIS DEVICE ACTIVE?
         BO    WRATSERV            YES, BRANCH
         NI    UCMSTS,MASK255-UCMAF NO, TURN OFF ATTENTION FLAG
WRABXLE  BXLE  R11,R12,WRALOOP     GET NEXT ENTRY
         XC    UCMAECB(FOUR),UCMAECB  CLEAR ATTENTION ECB
         EJECT                                                 @G51APSS
**********************************************************************
*
*        GO THROUGH LIST OF I/O ECB'S, AND WHENEVER ONE IS POSTED
*        COMPLETE, GO TO ROUTINE WRIOSERV.
*
**********************************************************************
WRIOCOMP LM    R11,R13,UCMVEA      GET BXLE UCM ENTRY REGS
         SR    R6,R6               INITIALIZE INDEX
WRIOLOOP L     R1,UCMRECBA+FOUR(R6) GET ADDRESS OF THIS ENTRY'S ECB
         TM    ZERO(R1),POST       IS IT POSTED COMPLETED?
         BO    WRIOSERV            YES, BRANCH
         LA    R6,FOUR(R6)         INCREMENT ECB PTR ADDRESS
         BXLE  R11,R12,WRIOLOOP    AND ENTRY ADDRESS TO DO NEXT ENTRY
         SPACE 3                                               @G51APSS
**********************************************************************
*
*        IF WE ARE STILL PROCESSING CONSOLE OUTPUT QUEUES,
*        CONTINUE PROCESSING BY GOING TO IEAVMDSV WITH OUTPUT CODE.
*        (NOTE: REG 3 IS UPDATED BY THE DEVICE SERVICE ROUTINE TO
*        POINT TO THE NEXT CONSOLE ENTRY TO BE PROCESSED.)
*
**********************************************************************
WRDVSERV C     R3,UCMVEA           ARE WE IN THE MIDDLE OF 'DEVSERV'?
         BNE   WROUTPUT            YES
         TM    UCMSFLG2,UCMSYSJ    NO, BUT IS THERE H.C. OUTPUT TO DO?
         BZ    WRWTO               NO, CHECK WTO/R ECB
         NI    UCMSFLG2,MASK255-UCMSYSJ  TURN OFF H.C. OUTPUT FLAG
WROUTPUT SR    R1,R1               CODE = 0 FOR OUTPUT
WRAWTLSV L     R15,ADDVSV          GET ADDRESS OF DEVICE SERVICE RTN
WRBALR   BALR  R14,R15             GO TO PROCESSOR
         B     WREXT               RETURN TO CHECK EXTERNAL ECB
         EJECT
***********************************************************************
*                                                                     *
*        THE FOLLOWING CODE DETERMINES IF A WQE SHORTAGE       @ZA11392
*        CONDITION IS RELIEVED. IF SO THEN MESSAGE IEA406I     @ZA11392
*        IS ISSUED.                                            @ZA11392
*          1) IEA406I WTO BUFFER SHORTAGE RELIEVED             @ZA11392
*             ONE OR BOTH OF ABOVE MSGS HAVE BEEN ISSUED AND   @ZA11392
*             THE WQE COUNT HAS DROPPED BELOW 60%. IF MSG      @ZA11392
*             IEA404A WAS ISSUED THEN A DOM IS ISSUED FOR THE  @ZA11392
*             MSG.                                             @ZA11392
*                                                                     *
***********************************************************************
WRWTO    EQU   *                                               @ZA11392
         L     R7,UCMBFEXT         ADDRESS OF FIXED EXTENSION  @G51APSS
         USING UCMFEXTA,R7         BASE TO FIXED EXTENSION     @ZA11392
         LH    R10,UCMWQLM         GET WQE BUFFER LIMIT        @ZA11392
         CH    R10,UCMWQLM1        IS LIMIT = IPL SPECIFIED LIMIT?     X
                                                               @ZA11392
         BNE   TESTOECB            NO, DON'T TEST WQE THRESHOLD        X
                                                               @ZA11392
         TM    UCMFFLG1,UCMFMSGN   SHOULD ANY MSGS BE ISSUED?  @ZA11392
         BNZ   TESTOECB            NO,DON'T TEST WQE THRESHOLD @ZA11392
         TM    UCMFFLG1,UCMFMSGE   MSG IEA405E ISSUED ?        @ZA11392
         BZ    TESTOECB            NO,GO SEE IF OECB POSTED    @ZA11392
         EJECT
***********************************************************************
*                                                                     *
*        SEE IF THE CURRENT WQE COUNT IS LESS THAN 60%         @ZA11392
*        OF THE LIMIT SPECIFIED AT IPL. IF SO ISSUE            @ZA11392
*        MESSAGE IEA406I                                       @ZA11392
*                                                                     *
***********************************************************************
         SPACE
         LH    R6,UCMWQNR          GET CURRENT WQE COUNT       @ZA11392
         CH    R6,UCMF60WQ         WQE COUNT <= 60% OF LIMIT?  @ZA11392
         BH    TESTOECB            NO,GO SEE IF OECB IS POSTED @ZA11392
         BAL   R10,FRLOCKS         RELEASE THE LOCKS           @ZA11392
         WTO   'IEA406I WTO BUFFER SHORTAGE RELIEVED',ROUTCDE=(2),     X
               DESC=(4)                                        @ZA11392
         TM    UCMFFLG1,UCMFMSGA   MSG IEA404A ISSUED?         @ZA11392
         BZ    WRESET              NO, GO TURN OFF INDICATORS  @ZA11392
         L     R10,UCMFPPTR        GET ADDRESS OF PAGEABLE EXTENSION   X
                                                               @ZA11392
         USING UCMPEXTA,R10        BASE TO PAGEABLE EXTENSION  @ZA11392
         L     R1,UCMPWQE          GET MSG IEA404A DOM ID      @ZA11392
         DOM   MSG=(1)             DOM THE MESSAGE             @ZA11392
         NI    UCMFMGFS,MASK255-UCMFMSGA TURN OFF MESSAGE IEA404A      X
                                   INDICATOR                   @ZA11392
         XC    UCMPWQE,UCMPWQE     ZERO DOM ID FIELD           @ZA11392
         DROP  R10                                             @ZA11392
WRESET   EQU   *
         NI    UCMFMGFS,MASK255-UCMFMSGE TURN OFF MESSAGE IEA405E      X
                                   INDICATOR                   @ZA11392
         BAL   R10,OBLOCKS         GO OBTAIN THE LOCKS         @ZA11392
         EJECT
**********************************************************************
*
*        IF CONSOLE OUTPUT QUEUES ARE STILL NOT BEING          @ZA11392
*        PROCESSED, SERVICE A POSTED WTO/R ECB BY GOING TO     @ZA11392
*        IEAVMWSV. NOTE - WHEN IEAVMWSV FINISHES QUEUEING, IT  @ZA11392
*        GOES TO IEAVMDSV TO START PROCESSING CONSOLE OUTPUT   @ZA11392
*        QUEUES. THIS GETS US INTO THE PREVIOUS LOOP, AND THE  @ZA11392
*        WTO/R ECB IS NOT CHECKED AGAIN UNTIL ALL CONSOLE      @ZA11392
*        QUEUES HAVE BEEN PROCESSED.                           @ZA11392
*
**********************************************************************
TESTOECB TM    UCMOECB,POST        IS WTO/R ECB POSTED?        @ZA11392
         BZ    WRCLNUP             NO, DO NEXT SERVICE         @ZA11392
         SPACE 2
***********************************************************************
*                                                                     *
*        SEE IF THE CURRENT WQE COUNT IS GREATER THAN 80% OF   @ZA11392
*        THE LIMIT SPECIFIED AT IPL. IF SO ISSUE MSG IEA405E.  @ZA11392
*          1) IEA405E WTO BUFFER SHORTAGE                      @ZA11392
*             80% OF WQE'S ARE IN USE                          @ZA11392
*                                                                     *
***********************************************************************
         SPACE
         LH    R10,UCMWQLM          GET WQE BUFFER LIMIT       @ZA11392
         CH    R10,UCMWQLM1         IS LIMIT = IPL SPECIFIED LIMIT?    X
                                                               @ZA11392
         BNE   WRCONT               NO, DON'T TEST WQE THRESHOLD       X
                                                               @ZA11392
         TM    UCMFFLG1,UCMFMSGN   SHOULD ANY MSGS BE ISSUED?  @ZA11392
         BNZ   WRCONT              NO,DON'T TEST WQE THRESHOLD @ZA11392
         LH    R6,UCMWQNR          GET CURRENT WQE COUNT       @ZA11392
         CH    R6,UCMF80WQ         WQE COUNT >= 80% OF LIMIT?  @ZA11392
         BL    WRCONT              NO,MSGS NOT NEEDED CONTINUE @ZA11392
         TM    UCMFFLG1,UCMFMSGE   WAS MSG IEA405E ISSUED ?    @ZA11392
         BO    WRCMP100            YES,GO COMPARE COUNT TO 100%@ZA11392
         BAL   R10,FRLOCKS         RELEASE THE LOCKS OVER WTO  @ZA11392
         WTO   'IEA405E WTO BUFFER SHORTAGE',ROUTCDE=(2),DESC=(3)
         OI    UCMFFLG1,UCMFMSGE   SET INDICATOR THAT MSG IEA405E WAS  X
                                   ISSUED                      @ZA11392
         BAL   R10,OBLOCKS         OBTAIN THE LOCKS            @ZA11392
         EJECT
***********************************************************************
*                                                                     *
*        SEE IF CURRENT WQE COUNT IS EQUAL TO OR GREATER THAN  @ZA11392
*        THE LIMIT SPECIFIED AT IPL. IF SO ISSUE MSG IEA404A.  @ZA11392
*          1) IEA404A WTO BUFFER SHORTAGE CRITICAL             @ZA11392
*             ALL OF THE WQE'S ARE IN USE                      @ZA11392
*                                                                     *
***********************************************************************
         SPACE
WRCMP100 EQU   *                                               @ZA11392
         LH    R6,UCMWQNR          GET CURRENT WQE COUNT       @ZA11392
         CH    R6,UCMWQLM          WQE COUNT >= LIMIT OF WQE'S @ZA11392
         BL    WRCONT              NO,CONTINUE WITH PROCESSING @ZA11392
         TM    UCMFFLG1,UCMFMSGA   WAS MSG IEA404A ISSUED?     @ZA11392
         BO    WRCONT              YES,DON'T ISSUE AGAIN       @ZA11392
         BAL   R10,FRLOCKS         RELEASE THE LOCKS OVER WTO  @ZA11392
         WTO   'IEA404A WTO BUFFER SHORTAGE CRITICAL',ROUTCDE=(2),     X
               DESC=(2)                                        @ZA11392
         L     R10,UCMFPPTR        GET ADDRESS OF PAGEABLE EXTENSION   X
                                                               @ZA11392
         USING UCMPEXTA,R10        BASE TO PAGEABLE EXTENSION  @ZA11392
         ST    R1,UCMPWQE          SAVE THE MSG DOM ID         @ZA11392
         DROP  R10                                             @ZA11392
         OI    UCMFFLG1,UCMFMSGA   SET INDICATOR THAT MSG IEA404A WAS  X
                                   ISSUED                      @ZA11392
         BAL   R10,OBLOCKS         OBTAIN THE LOCKS            @ZA11392
         DROP  R7                  DROP ADDRESSIBILITY TO UCM FIXED    X
                                   EXTENSION                   @ZA11392
         EJECT
***********************************************************************
*                                                                     *
*        SET UP AND GO TO WTO/R SERVICE PROCESSOR              @ZA11392
*                                                                     *
***********************************************************************
         SPACE
WRCONT   EQU   *                                               @ZA11392
         XC    UCMOECB(FOUR),UCMOECB  YES, CLEAR ECB
         L     R15,ADWTOSV         GET ADDRESS OF WTO/R SERVICE
         B     WRBALR              GO TO WTO/R SERVICE
         SPACE 2
**********************************************************************
*
*        IF THE SYSTEM WQE QUEUE NEEDS TO BE CONSOLIDATED,
*        GO TO IEAVMDSV WITH CLEANUP CODE.
*
**********************************************************************
WRCLNUP  TM    UCMSFLG2,UCMSYSI    IS CLEANUP SERVICE NEEDED?
         BZ    WRDOM               NO, DO NEXT SERVICE
         LA    R1,EIGHT            YES, CODE = 8 FOR CLEANUP
         NI    UCMSFLG2,MASK255-UCMSYSI  TURN OFF CLEANUP INDICATOR
         B     WRAWTLSV            GO TO DEVICE SERVICE ROUTINE
         EJECT
**********************************************************************
*
*        IF THE DOM ECB HAS BEEN POSTED, GO TO THE DOM PROCESSOR
*        (IEAVMDOM).
*
**********************************************************************
WRDOM    TM    UCMDECB,POST        IS DOM ECB POSTED?
         BZ    PXFOXL              NO, GO TO SYSLOG CHECKER ROUTINE
         L     R15,ADDOM           YES, GET ADDRESS OF DOM PROCESSOR
         B     WRBALR              GO TO DOM PROCESSOR
         SPACE 4
**********************************************************************
*
*        IF THE LOG INITIALIZATION ECB HAS BEEN POSTED, GO TO
*        SERVICE ROUTINE (IEAVMWTL) TO CHECK IF HARDCOPY IS SYSLOG
*
**********************************************************************
PXFOXL   EQU   *                   CHECK LOG INITIALIZATION ECB
         SPACE
* RELEASE LOCKS IN PREPARATION FOR LINK TO WTL ROUTINE OR WAIT
* ON ECBLIST.  UCMNPECB NEED NOT BE SERIALIZED.
         SPACE
         BAL   R10,FRLOCKS         RELEASE LOCKS                 Y02752
         SPACE
         TM    UCMNPECB,POST       HAS LOG JUST BEEN INITIALIZED
         BZ    CTWSTART            NO, GO TO WAIT ROUTINE
         XC    UCMNPECB(FOUR),UCMNPECB CLEAR LOG ECB IN UCM
         GETMAIN R,LV=64,SP=255    SAVE AREA 16 WORDS IN LSQS
         LR    R13,R1              STANDARD LINKAGE CONVENTION
         LINK  EP=IEAVMWTL
         L     R0,SP255L64         SET UP TO FREE SAVEAREA
         LR    R1,R13              POINT R1 TO SAVE AREA
         FREEMAIN R,LV=(0),A=(1)
         B     WRLOCK              REOBTAIN LOCKS AND CHECK ECBS
*                                  AGAIN
         SPACE 4
**********************************************************************
*
*        IF AN ATTENTION OR I/O COMPLETE IS PENDING, SERVICE BY
*        GOING TO IEAVMDSV.
*
**********************************************************************
WRIOSERV OI    UCMDEVC,UCMDEVE     SET I/O COMPLETION FLAG
WRATSERV LA    R1,FOUR             CODE = 4 FOR ATTN OR I/O
         B     WRAWTLSV            GO TO DEVICE SERVICE ROUTINE
         SPACE
* SET UP ACR PARAMETERS TO CONSOLE SWITCH
* TURN OFF CORRESPONDING BIT IN CPU ID MASK IN ACR ECB
         SPACE
WRACRGO  EQU   *                   SET UP FOR CONSOLE SWITCH FOR
*                                  ACR
         MVI   UCM5WD,ACRCODE      INPUT CODE FOR ACR
         STCM  R12,CSMASK,UCM5WD+ONE INSERT SINGLE CPU ID
         L     R11,UCMARECB        GET CONTENTS OF ACR ECB
         SPACE
WRACRCS  EQU   *                   COMPARE AND SWAP LOOP
         LR    R13,R11             MOVE CONTENTS OF ACR ECB TO
*                                  SWAP REG
         XR    R13,R12             TURN OFF BIT IN SWAPREG
         CS    R11,R13,UCMARECB    IF CONTENTS HAS NOT CHANGED,
*                                  TURN OFF BIT IN ACR ECB
         BNE   WRACRCS             CONTENTS HAS CHANGED, TRY
*                                  AGAIN
         B     WRSWCH              SUCCESSFUL, GO TO CONSOLE
*                                  SWITCH
         EJECT
**************************************************************** Y02756
*                                                                Y02756
*   SET UP HC SWITCH PARAMETERS TO CONSOLE SWITCH ROUTINE TO     Y02756
*   SWITCH HC FROM THE SYSTEM LOG TO A CONSOLE.  WTO HAS         Y02756
*   REQUESTED A SWITCH BECAUSE THE LOG HAS NOT YET BEEN          Y02756
*   INITIALIZED, MESSAGES FOR HC ARE BEING KEPT IN WQES IN CSA   Y02756
*   SPACE WAITING TO BE HARDCOPIED AND THERE IS NO MORE CSA      Y02756
*   SPACE FOR WQES FOR ADDITIONAL MESSAGES OR ANY TYPE.          Y02756
*                                                                Y02756
**************************************************************** Y02756
         SPACE
WRHCSW   EQU   *                   SWITCH HARDCOPY               Y02756
         MVI   UCMAECB+THREE,ZERO  CLEAR COMPLETION CODE         Y02756
         MVI   UCM5WD,HCSWCODE     INPUT CODE FOR HC SWITCH      Y02756
*                                  FROM SYSLOG                   Y02756
         B     WRSWCH              GO TO CONSOLE SWITCH          Y02756
         EJECT
***********************************************************************
*                  OBTAIN LOCKS SUBROUTINE                            *
* THE CMS LOCK IS USED TO SERIALIZE USE OF THE UCM.  THE LOCAL LOCK   *
* MUST BE HELD IN ORDER TO OBTAIN THE CMS LOCK.                       *
* DESTROYS REGS 11,12,13,14                                           *
* RETURN ON REG10                                             @ZA11392*
***********************************************************************
         SPACE
OBLOCKS  EQU   *                   OBTAIN LOCKS TO SERIALIZE USE
*                                  OF THE UCM
         SETLOCK OBTAIN,TYPE=LOCAL,MODE=UNCOND,                        X
               RELATED=(UCM,IEAVMQWR,(FRLOCKS))                @ZA11392
         SETLOCK OBTAIN,TYPE=CMS,MODE=UNCOND,                          X
               RELATED=(UCM,IEAVMQWR,(FRLOCKS))                @ZA11392
         SPACE
         SETFRR A,FRRAD=UCMFRRAD,PARMAD=(R12),WRKREGS=(R11,R12)  Y02755
         USING PARMLIST,R12        ADDRESSABILITY TO PARM AREA   Y02755
         MVC   PARMID,=CL4'CTSK'   LOAD MODULE ID                Y02755
         DROP  R12
         BR    R10                 RETURN TO CALLER            @ZA11392
         EJECT
FRLOCKS  DS    0H                  RELEASE LOCKS SUBROUTINE      Y02752
*                                                                Y02752
*   DESTROYS REGS 11,12,13,14                                    Y02752
*   RETURN ON REG 10                                             Y02752
*                                                                Y02752
         SPACE
         SETFRR D,WRKREGS=(R11,R12)                              Y02755
         SPACE
         SETLOCK RELEASE,TYPE=CMS,RELATED=(UCM,IEAVMQWR(OBLOCKS))      X
                                                               @ZA11392
         SETLOCK RELEASE,TYPE=LOCAL,RELATED=(UCM,IEAVMQWR(OBLOCKS))    X
                                                               @ZA11392
         BR    R10                 RETURN TO CALLER              Y02752
         EJECT
QWRREGS  EQU   *                   REGISTER EQUATES
         SPACE
R0       EQU   0                   GETMAIN REG
R1       EQU   1                   ADDR OF PARM LIST OR INPUT CODE
R2       EQU   2                   PTR TO THE UCM
R3       EQU   3                   PTR TO 1ST UCME
R4       EQU   4                   PTR TO UCM PREFIX
R5       EQU   5                   WORK REG
R6       EQU   6                   WORK REG
R7       EQU   7                   WORK REG
R8       EQU   8                   PTR TO EIL
R9       EQU   9                   BASE REGISTER
R10      EQU   10                  PTR TO CVT, LINK TO FRLOCKS   Y02752
R11      EQU   11                  UCME LOOP REG, ACR WORK REG
R12      EQU   12                  UCME LOOP REG, ACR WORK REG
R13      EQU   13                  UCME LOOP REG, ACR WORK REG
R14      EQU   14                  BRANCH AND LINK REG
R15      EQU   15                  BRANCH AND LINK REG
ZERO     EQU   0                   USED FOR INDEXING
ONE      EQU   1                   USED FOR SHIFT
TWO      EQU   2                   OFFSET OF CPU ID INTO UCMARECB
THREE    EQU   3                   USED AS AN OFFSET             Y02756
FOUR     EQU   4                   LENGTH USED IN CLEARING ECBS
EIGHT    EQU   8                   USED AS AN OFFSET
FIFTEEN  EQU   15                  USED FOR SHIFT
SIXTEEN  EQU   16                  NO. OF BITS IN CPU ID MASK
TWENTY   EQU   20                  USED AS AN OFFSET           @ZA19273
THIRTY   EQU   30                  USED FOR SHIFT
SVC72    EQU   72                  SVC 72
MASK255  EQU   255                 USED TO TURN OFF FLAGS
X00      EQU   X'00'               USED TO CLEAR THE WAIT IN ECBS
X04      EQU   X'04'               EXTERNAL INTERRUPT CODE
X80      EQU   X'80'               INDICATES LAST ECB IN LIST
POST     EQU   X'40'               CHECK FOR POSTED ECB
CCMASK   EQU   B'1100'             MASK FOR MOVING CPU ID FROM
*                                  UCMARECB
ACRCODE  EQU   X'10'               INPUT CODE TO CONSOLE SWITCH
*                                  FOR  ACR                    @ZM34522
HCSWCODE EQU   X'02'               INPUT CODE TO CONSOLE SWITCH  Y02756
*                                    FOR HC SWITCH FROM SYSLOG   Y02756
HCSWCC   EQU   X'23'               UCMAECB COMPLETION CODE FOR   Y02756
*                                    HC SWITCH REQUEST FROM WTO  Y02756
CSMASK   EQU   B'0011'             MASK FOR STORING CPU ID INTO
*                                  CONSOLE SWITCH PARMS
ONEBYTE  EQU   8                   USED TO ISOLATE EXTERNAL      Y02752
*                                     INTERRUPT COUNT            Y02752
ADDVSV   DC    A(IEAVMDSV)         ADDRESS OF DEVICE SERVICE ROUTINE
ADWTOSV  DC    A(IEAVMWSV)         ADDRESS OF WRITE SERVICE
ADDOM    DC    A(IEAVMDOM)         DOM PROCESSOR ROUTINE
SP255L64 DC    X'FF000040'         LSQA SUBPOOL 255, LENGTH 16 WORDS
WREXTNAM DC    CL8'IGC0407B'       NAME OF CONSOLE SWITCH ROUTINE
SPACE    DC    2CL25'** IEAVMQWR PATCH AREA **'                @G51APSS
         EJECT
         CVT DSECT=YES
         EJECT
         IEECUCM FORMAT=NEW
         EJECT
         IHACTM FTPT
         EJECT
         IHAFRRS                   REQUIRED FOR SETFRR
         EJECT
         IHAPSA                    REQUIRED FOR SETLOCK
         END
