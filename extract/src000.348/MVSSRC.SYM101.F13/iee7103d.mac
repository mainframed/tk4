         TITLE 'IEE7103D - MONITOR PROCESSOR'
***********************************************************************
*                                                                     *
* MODULE NAME      =   IEE7103D                                 Y02669*
*                                                                     *
* DESCRIPTIVE NAME =   MONITOR COMMAND PROCESSOR                Y02669*
*                                                                     *
* COPYRIGHT        =   N/A                                      Y02669*
*                                                                     *
* STATUS           = OS/VS2 RELEASE 037                       @ZA30320*
*                                                                     *
* FUNCTION         =   THIS MODULE DETERMINES AND PROCESSES     Y02669*
*                      THE MONITOR COMMAND OPERANDS: SESS(,T);  Y02669*
*                      JOBNAMES(,T); STATUS; DSNAME; SPACE.     Y02669*
*                                                                     *
* OPERATION        =   THE MONITOR COMMAND OPERANDS             Y02669*
*                      RECOGNIZED BY IEE7103D AND THEIR         Y02669*
*                      VALIDITY ARE AS FOLLOWS:                 Y02669*
*                                                                     *
*                                  INPUT   TIME     SUBSYSTEM  @Y02668P
*                                  STREAM  SHARING  CONSOLE    @Y02668P
*                                  OR      (OPERATOR  (JES3)   @Y02668P
*                                  CONSOLE MODE)               @Y02668P
*                                                              @Y02668P
*                      JOBNAMES(,T)  VALID   VALID   INVALID   @Y02668P
*                      STATUS        VALID   VALID   INVALID   @Y02668P
*                      SESS(,T)      VALID   VALID   INVALID   @Y02668P
*                      SPACE         VALID  INVALID   VALID    @Y02668P
*                      DSNAME        VALID  INVALID   VALID    @Y02668P
*                                                              @Y02668P
*                      IEE7103D, DEPENDING ON THE COMMAND       Y02669*
*                      SOURCE, REGISTERS MONITOR REQUESTS BY    Y02669*
*                      SETTING THE BITS IN THE APPROPIATE       Y02669*
*                      MONITOR CONTROL BLOCKS AND COMMON        Y02669*
*                      DATA AREAS:                              Y02669*
*                                                                     *
*                      SOURCE       CONTROL BLOCK  COMMON AREA  Y02669*
*                      -------      -------------  -----------  Y02669*
*                      ISSUED INTERNALLY UCME          BASEA    Y02669*
*                      INPUT STREAM      UCME          BASEA    Y02669*
*                      OPERATOR CONSOLE  UCME          BASEA    Y02669*
*                      TIME SHARING                             Y02669*
*                       (OPERATOR MODE)  MQE           BASEA    Y02669*
*                                                                     *
*                      IEE7103D INTERFACES WITH COMMUNICATIONS  Y02669*
*                      TASK, IEAVMNTR (MONIOTR QUEUE MANAGER),  Y02669*
*                      VIA IEZMPL (MONITOR PARAMETER LIST)      Y02669*
*                      TO SET THE BITS AND FIELDS FOR MONITOR   Y02669*
*                      JOBNAMES; SESS; AND STATUS IN BASEA, THE Y02669*
*                      UCME, AND MQE (MONITOR QUEUE ELEMENT).   Y02669*
*                      THE VECTOR FOR IEAVMNTR IS FOUND IN THE  Y02669*
*                      UCM (UCMMNTR).                           Y02669*
*                                                                     *
*                      THE BITS IN BASEA FOR THE T (TIME);      Y02669*
*                      DSNAME; AND SPACE OPERANDS ARE SET       Y02669*
*                      WITHIN THIS MODULE.                      Y02669*
*                                                                     *
* NOTES                IF THE MONITOR COMMAND IS ENTERED THRU   Y02669*
*                      THE INPUT STREAM OR ISSUED INTERNALLY,   Y02669*
*                      THE CONSOLE ID (XAU) WILL BE X'00'.      Y02669*
*                                                                     *
*                      IF THE ISSUING CONSOLE IS PART OF A      Y02669*
*                      COMPOSITE CONSOLE XAU WILL CONTAIN       Y02669*
*                      THE CONSOLE ID OF THE INPUT DEVICE.      Y02669*
*                                                                     *
*    CHARACTER-CODE                                             Y02669*
*     DEPENDENCIES =   ANY CHARACTER CODE OTHER THAN EBCDIC     Y02669*
*                      WILL REQUIRE REASSEMBLY OF THIS MODULE.  Y02669*
*                                                                     *
*    DEPENDENCIES  =   SVC 100 WILL NOT PERMIT ANY TIME SHARING Y02669*
*                      USERS TO ISSUE MONITOR DSNAME; OR SPACE. Y02669*
*                                                                     *
*                      CHECK THE UCMDISPK BIT. IF THE BIT      @Y02668P
*                      IS ON, A SUBSYSTEM CONSOLE ISSUED THE   @Y02668P
*                      COMMAND ONLY MN DSNAME AND MN SPACE ARE @Y02668P
*                      VALID                                   @Y02668P
*                                                                     *
*                      IEE0403D TO RECOGNIZE THE MONITOR        Y02669*
*                      COMMAND AND ROUTE THE OPERAND            Y02669*
*                      INFORMATION IN THE XSA.                  Y02669*
*                                                                     *
*                      MONITOR QUEUE MANAGER (IEAVMNTR) TO      Y02669*
*                      UPDATE THE PROPER FIELDS IN THE UCME,    Y02669*
*                      BASEA, AND MQE (MONITOR REQUEST ELEMENT) Y02669*
*                      BASED ON INFORMATION PASSED TO IT IN     Y02669*
*                      IEZMPL.                                  Y02669*
*                                                                     *
*                      THE STAE EXIT FOR SVC 34 SHOULD EXPECT   Y02669*
*                      AN ABEND 077 FROM COMMUNICATIONS TASK    Y02669*
*                      (IEAVMNTR) IF THE IEZMPL PARAMETER LIST  Y02669*
*                      CONTAINS INVALID INFORMATION.            Y02669*
*                                                                     *
*                      IEEBASEA, IEECUCM, IEEXSA WILL BE        Y02669*
*                      UPDATED TO REFLECT THE NEW FIELDS.       Y02669*
*                                                                     *
*                      A MAPPING MACRO MUST BE PROVIDED FOR     Y02669*
*                      IEZMPL.                                  Y02669*
*                                                                     *
*    RESTRICTIONS  =   TIME SHARING USERS ARE NOT PERMITTED     Y02669*
*                      TO ISSUE MONITOR DSNAME OR SPACE.        Y02669*
*                      (SEE DEPENDENCIES)                       Y02669*
*                                                                     *
*    REGISTER/                                                  Y02669*
*     CONVENTIONS  =   IEE7103D WILL SUPPLY AN 18 WORD          Y02669*
*                      SAVE AREA POINTED TO BY REGISTER 13      Y02669*
*                      WHEN CALLING IEAVMNTR.                   Y02669*
*                      THE SAVE AREA IS PART OF THE XSA.        Y02669*
*                                                                     *
*    PATCH-LABEL   =   PATCH                                    Y02669*
*                                                                     *
* MODULE TYPE      =   CSECT                                    Y02669*
*                                                                     *
*    PROCESSOR     =   N/A                                      Y02669*
*                                                                     *
*    MODULE SIZE   =   X'26B'                                 @ZA30320*
*                                                                     *
*    ATTRIBUTES    =   REENTRANT, KEY 0, SUPERVISOR MODE,       Y02669*
*                      PAGED LPA.                               Y02669*
*                                                                     *
* ENTRY POINT      =   IEE7103D FROM IEE0403D                   Y02669*
*    PURPOSE       =   TO PROCESS THE OPERANDS ON THE MONITOR   Y02669*
*                      COMMAND                                  Y02669*
*    LINKAGE       =   BRANCH                                   Y02669*
*    INPUT DATA    =   REGISTER TWO ADDRESSES THE XSA           Y02669*
*                       XAL POINTS TO 1ST OPERAND IN THE BUFFER Y02669*
*                       XAU CONTAINS A CONSOLE UCMI (X'00' -    Y02669*
*                       ISSUED INTERNALLY OR FROM INPUT STREAM) Y02669*
*                       XAA CONTAINS AN ASID (X'0000'=NON-TS)   Y02669*
*                       XAR POINTS TO LENGTH FIELD OF BUFFER    Y02669*
*                       XAN CONTAINS VERB CODE X'64' - MONITOR  Y02669*
*                       XAV CONTAINS THE COMMAND VERB           Y02669*
*                       XAP CONTAINS ADDRESS OF END OF          Y02669*
*                           BUFFER +1                           Y02669*
*                                                                     *
* REGISTERS USAGE                                               Y02669*
*    SAVED         =   REGISTER 14 IS SAVED, AT COMINTER, IN    Y02669*
*                      THE XSA (XAS) ONLY WHEN CALLING IEAVMNTR Y02669*
*                                                                     *
*    CONTAINED     =   R0    0     USED FOR THE OIL MACRO       Y02651*
*                      R1    1     PTR TO MONITOR PARM LIST     Y02669*
*                                  USED FOR THE OIL MACRO       Y02651*
*                      R2    2     INPUT - POINTER TO XSA       Y02669*
*                      R3    3     MODULE BASE REGISTER         Y02669*
*                      R4    4     BASE REG FOR BASEA           Y02669*
*                      R5    5     PTR TO OPERAND BUFFER        Y02669*
*                      R6    6     ADDRESS COMPARE TABLE ENTRY  Y02669*
*                      R7    7     BASE REG FOR CVT             Y02669*
*                      R8    8     BASE REG FOR UCM             Y02669*
*                      R9    9     CONTAINS ASID OR UCMI        Y02669*
*                      R10   10    USED FOR THE OIL MACRO       Y02651*
*                      R11   11    ADDRESS COMPARE TABLE END    Y02669*
*                      R12   12    UCM ENTRY BASE REGISTER     @Y02668P
*                                  DROP IT THEN USE R12 TO     @Y02668P
*                                  SAVE MPL POINTER            @Y02668P
*                      R13   13    POINTER TO SAVE AREA         Y02669*
*                      R14   14    BALR AND RETURN ADDRESS      Y02669*
*                      R15   15    USED FOR BALR AND BRANCH     Y02669*
*                                                                     *
*    RESTORED      =   REGISTER 14 RESTORED AT LABEL COMINTER   Y02669*
*                      ONLY WHEN RETURNING FROM IEAVMNTR.       Y02669*
*                                                                     *
*  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *
*                                                                     *
**EXIT - NORMAL    =   RETURN VIA BR R14 FROM DDSNAME           Y02669*
*    CONDITIONS    =   MN DSNAME REQUEST REGISTERED IN BASEA    Y02669*
*    OUTPUT DATA   =   BIT SET IN BASEA                         Y02669*
*                        BAMONITR - BADSN X'80'                 Y02669*
*    RETURN CODES  =   NONE                                     Y02669*
*                                                                     *
*  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *
*                                                                     *
**EXIT - NORMAL    =   RETURN VIA BR R14 FROM DDSPACE           Y02669*
*    CONDITIONS    =   MN SPACE REQUEST REGISTERED IN BASEA     Y02669*
*    OUTPUT DATA   =   BIT SET IN BASEA                         Y02669*
*                        BAMONITR - BASPACE X'40'               Y02669*
*    RETURN CODES  =   NONE                                     Y02669*
*                                                                     *
*  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *
*                                                                     *
**EXIT - NORMAL    =   BALR 14,15 TO IEAVMNTR FROM COMINTER     Y02669*
*    CONDITIONS    =   TO HAVE MN JOBNAMES; SESS; OR STATUS     Y02669*
*                      REQUEST REGISTERED BY COMM TASK          Y02669*
*    OUTPUT DATA   =   FIELDS SET IN MONITOR PARM LIST          Y02669*
*                      (IEZMPL) WHOSE ADDRESS IS STORED IN XAR  Y02669*
*                      WHICH IN TURN IS ADDRESSED BY REG 1.     Y02669*
*                      (SEE DATA AREAS)                         Y02669*
*                      REG 13 ADDRESSES 18 WORD SAVE AREA       Y02669*
*                      IN THE XSA
*    RETURN CODES  =   NONE (HOWEVER, IEAVMNTR WILL ABEND ITS   Y02669*
*                      CALLER WITH AN '077' IF THE PARM LIST    Y02669*
*                      CONTAINS INVALID INFORMATION)            Y02669*
*                      (SEE DEPENDENCIES)                       Y02669*
*                                                                     *
*  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *
*                                                                     *
**EXIT - NORMAL    =   RETURN VIA BR R14 FROM COMINTER          Y02669*
*    CONDITIONS    =   MN JOBNAMES; SESS; OR STATUS REQUEST     Y02669*
*                      REGISTERED BY COMMUNICATIONS TASK        Y02669*
*    OUTPUT DATA   =   FIELDS SET IN BASEA AND UCME AND MQE     Y02669*
*                      VIA COMM TASK (IEAVMNTR)                 Y02669*
*                      AND IF ,T WAS SPECIFIED THIS MODULE      Y02669*
*                      WILL HAVE SET MSBTN = X'02' IN BASEA.    Y02669*
*    RETURN CODES  =   REGISTER 15 WILL CONTAIN A 4 IF A        Y02669*
*                      GETMAIN ERROR OCCURRED IN IEAVMNTR.      Y02669*
*                      (MN JOBNAMES; STATUS; SESS FOR TS ONLY)  Y02669*
*                      SEE ERROR EXIT - IEE0503D                Y02669*
*                                                                     *
         EJECT
*  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *
*                                                                     *
**EXIT - ERROR     =   BRANCH TO IEE0503D FROM D0503D           Y02669*
*    CONDITIONS    =   TO ISSUE MESSAGE 'IEE305I COMMAND        Y02669*
*                      INVALID', IF AN INVALID MONITOR OPERAND  Y02669*
*                      IS FOUND.                                Y02669*
*                      TO ISSUE MESSAGE 'IEE311I PARAMETER      Y02669*
*                      MISSING', IF NO OPERAND IS FOUND IN      Y02669*
*                      THE COMMAND BUFFER.                      Y02669*
*                      TO ISSUE MESSAGE 'IEE328I COMMAND        Y02669*
*                      ABORTED', IF IEAVMNTR ENCOUNTERED        Y02669*
*                      A GETMAIN ERROR FOR A MONITOR REQUEST    Y02669*
*                      FROM A TIME SHARING USER.                Y02669*
*                      TO ISSUE MESSAGE 'IEE777I INVALID FROM  @Y02668P
*                      SUBSYSTEM CONSOLE'. IF MONITOR WAS      @Y02668P
*                      ISSUED FROM A SUBSYSTEM CONSOLE         @Y02668P
*    OUTPUT DATA   =   REG 2 POINTS TO XSA WHICH CONTAINS       Y02669*
*                      ERROR MESSAGE INDEXES FOR IEE0503D:      Y02669*
*                        XAE WILL CONTAIN  X'05' FOR IEE305I    Y02669*
*                                          X'0B' FOR IEE311I    Y02669*
*                                          X'3A' FOR IEE328I    Y02669*
*                                          X'15' FOR IEE777I   @Y02668P
*                        XAV STILL CONTAINS THE COMMAND VERB    Y02669*
*                            FOR THE MESSAGE.                   Y02669*
*                        XAU STILL CONTAINS THE CONSOLE ID      Y02669*
*                        XAA STILL CONTAINS ASID IF REQUESTER   Y02669*
*                            IS TIME SHARING USER ELSE X'0000'  Y02669*
*    RETURN CODES  =   NONE                                     Y02669*
*                                                                     *
*  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *
*                                                                     *
* EXTERNAL                                                      Y02669*
*    REFERENCES                                                 Y02669*
*                                                                     *
*    ROUTINES      =   BALR TO COMMUNICATION TASK (IEAVMNTR)    Y02669*
*                      TO ADJUST FIELDS IN BASEA, UCME, AND MQE Y02669*
*                      WITH REG 1 POINTING TO THE ADDRESS       Y02669*
*                      OF THE IEZMPL (MONITOR PARM LIST)        Y02669*
*                      AND REG 13 POINTING TO REG SAVE AREA     Y02669*
*                                                                     *
*                      BRANCH TO IEE0503D                       Y02669*
*                                                                     *
*    DATA AREAS    =   IEZMPL - MONITOR PARAMETER LIST          Y02669*
*                      ( ORG'ED ON XAX; MAPPED BY IEZMPL;       Y02669*
*                        ADDRESSED BY XAR WHICH IN TURN         Y02669*
*                        IS POINTED TO BY REGISTER 1)           Y02669*
*                         MPLPROC  DS  H     PROCESSING FLAGS   Y02669*
*                         MPLCONS  EQU X'80'  MPLID=CONSOLE ID  Y02669*
*                         MPLTERM  EQU X'40'  MPLID=ASID        Y02669*
*                         MPLSTRT  EQU X'20'  START MONITOR     Y02669*
*                         MPLSTOP  EQU X'10'  STOP MONITOR      Y02669*
*                         MPLID    DS  H     CONSOLE ID OR ASID Y02669*
*                         MPLTYPE  DS  H     MONITOR TYPE FLAGS Y02669*
*                         MPLJBNM  EQU X'80'  MN JOBNAMES       Y02669*
*                         MPLSTAT  EQU X'40'  MN STATUS         Y02669*
*                         MPLSESS  EQU X'04'  MN SESSIONS       Y02669*
*                         MPLRSV10 DS  H     RESERVED           Y02669*
*                                                                     *
*    CONTROL BLOCKS=   CVT        R  -  ADDRESS BASEA & UCM     Y02669*
*                      IEEBASEA   W  -  TIME/DSNAME/SPACE BITS  Y02669*
*                      IEEXSA     R  -  INPUT INFORMATION       Y02669*
*                                 W  -  MONITOR PARM LIST       Y02669*
*                      IEECUCM    R  -  VECTOR FOR IEAVMNTR     Y02669*
*                                                                     *
*    TABLES        =   A COMPARE TABLE IS USED TO VALIDITY      Y02669*
*                      CHECK THE OPERANDS ON THE MONITOR        Y02669*
*                      COMMAND.                                 Y02669*
*                      THE FORMAT OF EACH ENTRY IS THE SAME:    Y02669*
*                                                                     *
*                      DLEN   DC   AL1(L'XXX-1) - LENGTH OF     Y02669*
*                                              SPECIFIC OPERAND Y02669*
*                      XXX    DC   C'YYYYY' - OPERAND CHARACTER Y02669*
*                                             REPRESENTATION    Y02669*
*                                                                     *
*                      DLEN AND 'XXX' ARE USED IN AN EX OF A    Y02669*
*                      CLC. IF THE OPERAND MATCHES THE          Y02669*
*                      ENTERED OPERAND, A BRANCH IS TAKEN TO    Y02669*
*                      EITHER HANDLE THE COMMAND IN THIS MODULE Y02669*
*                      OR ELSE BUILT A PARM LIST AND BALR TO    Y02669*
*                      IEAVMNTR.                                Y02669*
*                      OTHERWISE, THE REGISTER USED TO ADDRESS  Y02669*
*                      THE TABLE IS INCREMENTED BY TWO + THE    Y02669*
*                      VALUE OF DLEN TO REACH THE NEXT TABLE    Y02669*
*                      ENTRY.                                   Y02669*
*                                                                     *
*                      A BRANCH TABLE IS USED IN CONNECTION     Y02669*
*                      WITH THE COMPARE TABLE. IT CONSISTS OF   Y02669*
*                      BRANCHES TO THE PROPER OPERAND           Y02669*
*                      PROCESSORS. AN INDEX REGISTER IS         Y02669*
*                      MAINTAINED THROUGHOUT THE SCAN OF THE    Y02669*
*                      COMPARE TABLE. THIS IS USED AT DBRANCH   Y02669*
*                      TO GIVE CONTROL TO THE INDICATED         Y02669*
*                      PROCESSOR INDEXED INTO THE BRANCH TABLE. Y02669*
*                                                                     *
*    MACROS        =   MODID                                    Y02669*
*                      OIL FOR SERIALIZATION ON BASEA BITS      Y02651*
*                                                                     *
*    SERIALIZATION =   BASEA - OIL FOR TIME/DSNAME/SPACE FIELDS Y02651*
*                                                                     *
*    ENQUEUE                                                    Y02669*
*     RESOURCES    =   NONE                                     Y02669*
*                                                                     *
*    CHANGE LEVEL  =   Y02669                                   Y02669*
*                      Y02651                                   Y02651*
*                      YM1911 (PROLOGUE & COMMENT CHANGES ONLY) YM1911*
*                      Y02668                                 @Y02668P*
*                      ZA30198                                        *
*                      ZA30320                                        *
* MESSAGES         =   THE FOLLOWING MESSAGES ARE CONTAINED     Y02669*
*                      IN AND ISSUED BY IEE0503D:               Y02669*
*                                                                     *
*                       MSGID   CODE   MESSAGE TEXT             Y02669*
*                      IEE305I  X'05'  INVALID COMMAND          Y02669*
*                      IEE311I  X'11'  PARAMETER MISSING        Y02669*
*                      IEE328I  X'3A'  COMMAND ABORTED          Y02669*
*                      IEE777I  X'15'  INVALID FROM SUBSYSTEM  @Y02668P
*                                      CONSOLE                 @Y02668P
*                                                                     *
* SYSGEN           =   INCLUDED WITH SVC 34 IN LOAD MODULE      Y02669*
*                      IGC0003D, DLIB AOSB3, MACRO SGIEF441,    Y02669*
*                      TARGLIB LPALIB.                          Y02669*
*                                                                     *
* SYSTEM LIBRARIES =   NONE                                     Y02669*
*                                                                     *
***********************************************************************
* C - DBRANCH, TEST FOR ZERO CONSOLE ID                       @ZA30198*
* C - COMINTER, CHANGE INVALID BRANCH                         @ZA30320*
         EJECT
IEE7103D CSECT
***********************************************************************
*                                                                     *
*                         REGISTER    EQUATES                         *
*                                                                     *
***********************************************************************
R0       EQU   0                  USED FOR THE OIL MACRO         Y02651
R1       EQU   1                  PTR TO MONITOR PARM LIST       Y02669
*                                 USED FOR THE OIL MACRO         Y02651
R2       EQU   2                  INPUT - POINTER TO XSA
R3       EQU   3                  MODULE BASE REGISTER
R4       EQU   4                  BASE REG FOR BASEA
R5       EQU   5                  PTR TO OPERAND BUFFER          Y02669
R6       EQU   6                  USED TO ADDRESS TABLE ENTRIES
R7       EQU   7                  BASE REG FOR CVT
R8       EQU   8                  BASE REG FOR UCM               Y02669
R9       EQU   9                  CONTAINS ASID OR UCMI          Y02669
R10      EQU   10                 USED FOR THE OIL MACRO         Y02651
R11      EQU   11                 ADDRESSES END OF OPERAND TABLE
R12      EQU   12                 BASE FOR UCM ENTRY DROP IT   @Y02668P
*                                 THEN USE IT TO SAVE MPL PTR  @Y02668P
R13      EQU   13                 ADDR OF SAVE AREA FOR IEAVMNTR Y02669
R14      EQU   14                 USED FOR BALR AND RETURN       Y02669
R15      EQU   15                 USED FOR BALR AND BRANCH       Y02669
         SPACE  3
***********************************************************************
*                                                                     *
*                 DISPLACEMENTS AND LENGTHS AND MASK                  *
*                                                                     *
***********************************************************************
D1       EQU   1                  DISPLACEMENT OF 1
D2       EQU   2                  DISPLACEMENT OF 2
D4       EQU   4                  DISPLACEMENT OF 4
L0       EQU   0                  LENGTH OF 0                    Y02669
L8       EQU   8                  LENGTH OF 8                    Y02669
M7       EQU   7                  MASK FOR ADDRESS FOR ICM       Y02669
         SPACE 2
***********************************************************************
*                                                                     *
*                         ERROR CODES                            Y02669
*                                                                     *
***********************************************************************
ERR305   EQU   X'05'              ERR CODE-IEE305I CMD INVALID   Y02669
ERR311   EQU   X'0B'              ERR CODE-IEE311I MISSING PARM  Y02669
ERR328   EQU   X'3A'              ERR CODE-IEE328I CMD ABORTED   Y02669
ERR777   EQU   X'15'              ERR CODE-IEE777I SUBSYSTEM   @Y02668P
         EJECT
         BALR  R3,0               ESTABLISH MODULE ADDRESSABILITY
         USING *,R3
         MODID BRANCH=YES                                        Y01886
         SPACE 3
         USING IEEXSA,R2          ADDRESS EXTENDED SAVE AREA
         SR    R5,R5              CLEAR REGISTER                 Y02669
         ICM   R5,M7,XAL+1        GET 3 BYTE ADDR OF OPERANDS    Y02669
         BZ    DERR311            NONE, ERR-IEE311I MISSING PARM Y02669
         EJECT
***********************************************************************
*                                                                     *
* LOOP THROUGH COMPARE TABLE LOOKING FOR MATCHING VALID OPERAND  Y02669
*                                                                     *
***********************************************************************
         SPACE 2
         SR    R9,R9              CLEAR REGISTER FOR OPERAND LENGTH
         SR    R15,R15            USED TO INDEX BRANCH TABLE
         LA    R11,DENTAB         ADDRESS END OF COMPARE TABLE    21002
         LA    R6,DTAB            ADDRESS BEGINNING OF COMPARE TABLE
         USING DTAB,R6            EST. ADDRESSABILITY TO COMPARE TABLE
DLOOP    EQU   *                                                 Y02669
         IC    R9,DLEN            GET LENGTH OF COMPARE TABLE ENTRY
         EX    R9,COMPARE         CHECK FOR MATCH                Y02669
         BE    DBRANCH            IF MATCH, PROCESS ACCORDINGLY
         LA    R6,D2(R9,R6)       NO MATCH, INCRE TO NEXT TABLE ENTRY
         LA    R15,D4(R15)        INCRE INDEX FOR BRANCH TABLE
         CR    R6,R11             END OF COMPARE TABLE REACHED?   M0467
         BL    DLOOP              NO, GO BACK AND CHECK NEXT      20030
*                                 COMPARE TABLE ENTRY             20030
         DROP  R6                 NECESSARY TO RE-EST BASE REG   Y02669
         B     DERR305            YES, ERR-IEE305I CMD INVALID   Y02669
         SPACE  2
***********************************************************************
*                                                                     *
* WHEN A VALID MATCH IS FOUND ADDRESS CONTROL BLOCKS AND BRANCH  Y02669
* THEN CHECK, IF THE ISSUING CONSOLE IS JES3 THEN THE          @Y02668P
* COMMAND IS INVALID                                           @Y02668P
*                                                                     *
***********************************************************************
         SPACE 2
DBRANCH  EQU   *
         L     R7,CVTPTR          GET ADDRESS OF CVT             Y02669
         USING CVT,R7             ESTABLISH CVT ADDRESSABILITY   Y02669
         L     R4,CVTMSER         ADDR OF MASTER RESIDENT CORE   Y02669
         USING BASE,R4            EST. MSTR RES. CORE BASE
         L     R8,CVTCUCB         GET ADDRESS OF UCM             Y02669
         USING UCM,R8             ESTABLISH UCM ADDRESSABILITY   Y02669
         DROP  R7                                                Y02669
         USING UCMLIST,R12        EST UCME ADDRESSABILITY      @Y02668P
         L     R12,UCMVEA         ADDRESS OF THE FIRST UCME    @Y02668P
         SR    R1,R1              ZERO OUT REG1                @Y02668P
         IC    R1,XAU             CONSOLE ID                   @Y02668P
         LTR   R1,R1              INTERNAL,INPUT CMND SOURCE?  @ZA30198
         BZ    CONTINUE           YES,DEFAULT TO MSTR CONS.    @ZA30198
         BCTR  R1,L0              SUBTRACT ONE FROM CONSOLE ID @Y02668P
         M     R0,UCMVEZ          DISPLACEMENT OF UCME         @Y02668P
         AR    R12,R1             ADD TO FIRST UCME            @Y02668P
         SR    R0,R0              ZERO OUT REG0 FOR OIL        @Y02668P
         SR    R1,R1              ZERO OUT REG1 FOR OIL        @Y02668P
         TM    UCMDISP2,UCMDISPK  IS THIS A SUBSYSTEM CONSOLE  @Y02668P
         BZ    CONTINUE           NOT JES3                     @Y02668P
         MVI   XAE,ERR777         ERROR CODE 15 JES3           @Y02668P
         DROP  R12                FINISHED WITH THE UCME       @Y02668P
CONTINUE EQU   *                                               @Y02668P
         SR    R9,R9              CLEAR REG FOR CONSOLE TESTING  Y02669
         LH    R9,XAA             GET ASID                       Y02669
         XC    XAX(L8),XAX        CLEAR AREA FOR MNTRPL          Y02669
         LA    R1,XAX             GET ADDRESS OF AREA AND        Y02669
         USING MPL,R1             ORG IEZMPL ON XAX              Y02669
         LR    R12,R1             SAVE MPL BASE FROM OIL       @YM4795P
         B     BRTABLE(R15)       BRANCH TO PROPER PROCESSOR
         EJECT
***********************************************************************
*                                                                     *
*                        MONITOR JOBNAMES,T                       20030
*             SET TIME INDICATOR IN BASEA AND BUILD PARM LIST    Y02669
*                                                                     *
***********************************************************************
         SPACE
DDJOBT   EQU   *
         CLI   XAE,ERR777         ?JES3 CONSOLE-YES            @Y02668P
         BE    DERR777            BRANCH ERROR                 @Y02668P
         OIL   MSBTN,MSTN,WREGS=(0,1,10),REF=BASE                      *
                              SET THE TIME BIT IN BASEA          Y02651
         SPACE  5
***********************************************************************
*                                                                     *
*                         MONITOR JOBNAMES                        20030
*                BUILD PARM LIST FOR COMMUNICATIONS TASK         Y02669
*                                                                     *
***********************************************************************
         SPACE
DDJOB    EQU   *
         CLI   XAE,ERR777         ?JES3 CONSOLE-YES            @Y02668P
         BE    DERR777            BRANCH ERROR                 @Y02668P
         LR    R1,R12             RESTORE MPL BASE FROM OIL    @YM4795P
         LTR   R9,R9              TEST FOR COMMAND SOURCE         20030
         BZ    DDJOB2             BRANCH IF NOT TIME SHARING USER 20030
         OI    MPLPROC1,MPLTERM+MPLSTRT    START MN WITH ASID    Y02669
         OI    MPLTYPE1,MPLJBNM   MONITOR JOBNAMES               Y02669
         B     COMINTER           GO TO COMMUNICATIONS TASK      Y02669
         SPACE
DDJOB2   EQU   *                                                  20030
         OI    MPLPROC1,MPLCONS+MPLSTRT    START MN WITH CONS ID Y02669
         OI    MPLTYPE1,MPLJBNM   MONITOR JOBNAMES               Y02669
         IC    R9,XAU             GET CONSOLE ID                 Y02669
         B     COMINTER           GO TO COMMUNICATIONS TASK      Y02669
         EJECT
******************************************************************20030
*                                                                 20030
*                         MONITOR SESS,T                          20030
*             SET TIME INDICATOR IN BASEA AND BUILD PARM LIST    Y02669
*                                                                 20030
******************************************************************20030
         SPACE
DDSEST   EQU   *                                                  20030
         CLI   XAE,ERR777         ?JES3 CONSOLE-YES            @Y02668P
         BE    DERR777            BRANCH ERROR                 @Y02668P
         OIL   MSBTN,MSTN,WREGS=(0,1,10),REF=BASE                      *
                              SET THE TIME BIT IN BASEA          Y02651
         SPACE  5
******************************************************************20030
*                                                                 20030
*                          MONITOR SESS                           20030
*                BUILD PARM LIST FOR COMMUNICATIONS TASK         Y02669
*                                                                 20030
******************************************************************20030
         SPACE
DDSES    EQU   *                                                  20030
         LR    R1,R12             RESTORE MPL BASE FROM OIL    @YM4795P
         LTR   R9,R9              TEST FOR COMMAND SOURCE         20030
         BZ    DDSES2             BRANCH IF NOT TIME SHARING USER 20030
         OI    MPLPROC1,MPLTERM+MPLSTRT    START MN WITH ASID    Y02669
         OI    MPLTYPE1,MPLSESS   MONITOR SESSIONS               Y02669
         B     COMINTER           GO TO COMMUNICATIONS TASK      Y02669
         SPACE
DDSES2   EQU   *                                                  20030
         CLI   XAE,ERR777         ?JES3 CONSOLE-YES            @Y02668P
         BE    DERR777            BRANCH ERROR                 @Y02668P
         OI    MPLPROC1,MPLCONS+MPLSTRT    START MN WITH CONS ID Y02669
         OI    MPLTYPE1,MPLSESS   MONITOR SESSIONS               Y02669
         IC    R9,XAU             GET CONSOLE ID                 Y02669
         B     COMINTER           GO TO COMMUNICATIONS TASK      Y02669
         EJECT
***********************************************************************
*                                                                     *
*                           MONITOR STATUS                        20030
*                BUILD PARM LIST FOR COMMUNICATIONS TASK         Y02669
*                                                                     *
***********************************************************************
         SPACE
DDSTAT   EQU   *
         LTR   R9,R9              TEST FOR COMMAND SOURCE        Y02669
         BZ    DDSTAT2            BRANCH IF NOT TIME SHARING USER 20030
         OI    MPLPROC1,MPLTERM+MPLSTRT    START MN WITH ASID    Y02669
         OI    MPLTYPE1,MPLSTAT   MONITOR STATUS                 Y02669
         B     COMINTER           GO TO COMMUNICATIONS TASK      Y02669
         SPACE
DDSTAT2  EQU   *                                                  20030
         CLI   XAE,ERR777         ?JES3 CONSOLE-YES            @Y02668P
         BE    DERR777            BRANCH ERROR                 @Y02668P
         OI    MPLPROC1,MPLCONS+MPLSTRT    START MN WITH CONS ID Y02669
         OI    MPLTYPE1,MPLSTAT   MONITOR STATUS                 Y02669
         IC    R9,XAU             GET CONSOLE ID                 Y02669
         B     COMINTER           GO TO COMMUNICATIONS TASK      Y02669
         EJECT
***********************************************************************
*                                                                     *
*                          MONITOR DSNAME                         20030
*              SET MONITOR DSNAME BIT IN BASEA AND RETURN        Y02669
*                                                                     *
***********************************************************************
         SPACE
DDSNAME  EQU   *                                                 Y02669
         OIL   BAMONITR,BADSN,WREGS=(0,1,10),REF=BASE                  *
                              SET THE MN DSNAME BIT IN BASEA     Y02651
         BR    R14                RETURN TO SYSTEM
         EJECT
***********************************************************************
*                                                                     *
*                           MONITOR SPACE                         20030
*              SET MONITOR SPACE BIT IN BASEA AND RETURN         Y02669
*                                                                     *
***********************************************************************
         SPACE
DDSPACE  EQU   *                                                 Y02669
         OIL   BAMONITR,BASPACE,WREGS=(0,1,10),REF=BASE                *
                              SET THE MN SPACE BIT IN BASEA      Y02651
         BR    R14                RETURN TO SYSTEM
         EJECT
***********************************************************************
*                                                                     *
*          GO TO COMM TASK VIA BALR 14,15 TO SET THE APPROPIATE  Y02669
*          MONITOR FIELDS IN THE APPROPIATE CONTROL BLOCKS.      Y02669
*             R1 CONTAINS ADDRESS OF XAR WHICH IN TURN           Y02669
*                POINTS TO IEZMPL (MONITOR PARM LIST)            Y02669
*             R13 CONTAINS ADDRESS OF THE REGISTER SAVE AREA     Y02669
*                 FOUND IN THE XSA                               Y02669
*             R15 CONTAINS ADDRESS OF IEAVMNTR FROM THE UCM      Y02669
*          UPON RETURN FROM IEAVMNTR:                            Y02669
*             IF THERE WAS A GETMAIN ERROR, (REG 15 = 4          Y02669
*          INDICATING A GETMAIN FAILURE FOR A MONITOR REQUEST    Y02669
*          FROM A TIME SHARING USER) CONTROL IS GIVEN TO         Y02669
*          IEE0503D TO ISSUE 'IEE328I MONITOR COMMAND ABORTED'   Y02669
*             IF THERE WERE NO ERROR, (REG 15 = ZERO)            Y02669
*          AFTER RESTORING REGISTER 14 FROM XAS, CONTROL IS      Y02669
*          RETURNED VIA REGISTER 14.                             Y02669
*                                                                     *
***********************************************************************
         SPACE   3
COMINTER EQU   *                                                 Y02669
         STH   R9,MPLID           GET ASID OR CONSOLE ID         Y02669
         LA    R13,XASAVSTD       GET ADDRESS OF SAVE AREA       Y02669
         ST    R14,XAS            SAVE REGISTER 14               Y02669
         ST    R1,XAR             PUT ADDR OF MNTRPL IN XAR AND  Y02669
         LA    R1,XAR             PASS ADDRESS OF XAR IN REG 1   Y02669
         L     R15,UCMMNTR        GET ADDRESS OF COMM TASK MOD   Y02669
         BALR  R14,R15            HAVE FIELDS SET BY COMM TASK   Y02669
         SPACE 2
         L     R14,XAS            RESTORE RETURN ADDRESS         Y02669
         LTR   R15,R15            WAS THERE A GETMAIN ERROR?     Y02669
         BNZ   DERR328            YES, ISSUE AN ERROR MSG      @ZA30320
         BR    R14                RETURN TO CALLER               Y02669
         EJECT
***********************************************************************
*                                                                     *
*                       ERROR EXIT                               Y02669
*                                                                     *
***********************************************************************
DERR328  EQU   *                                                 Y02669
         MVI   XAE,ERR328         ERROR CODE=28 FOR CMD ABORTED  Y02669
         B     D0503D             BRANCH TO MESSAGE MODULE       Y02669
DERR311  EQU   *                                                 Y02669
         MVI   XAE,ERR311         ERROR CODE=11 FOR PARM MISSING Y02669
         B     D0503D             BRANCH TO MESSAGE MODULE       Y02669
DERR777  EQU   *                                              @Y02668P*
         MVI   XAE,ERR777         ERROR CODE=15 FOR INVALID   @Y02668P*
         B     D0503D             FROM SUBSYSTEM CONSOLE      @Y02668P*
DERR305  EQU   *                                                 Y02669
         MVI   XAE,ERR305         ERROR CODE=5 FOR COMD INVALID  Y02669
D0503D   EQU   *                                                 Y02669
         L     R15,VCON0503       GET ADDR OF IEE0503D           Y02669
         BR    R15                BRANCH TO MESSAGE MODULE       Y02669
         EJECT
***********************************************************************
*                                                                     *
*                              CONSTANTS                              *
*                                                                     *
***********************************************************************
         SPACE
VCON0503 DC    V(IEE0503D)            ERR MESSAGE MODULE ADDRESS Y02669
         SPACE
COMPARE  CLC   D1(L0,R6),L0(R5)       COMPARE TABLE ENTRY WITH   Y02669
*                                     COMMAND OPERAND            Y02669
         SPACE   7
***********************************************************************
*                                                                     *
*                        PATCH AREA                              Y02669
*                                                                     *
***********************************************************************
         SPACE
PATCH    DC    2CL25'** IEE7103D PATCH AREA **'                @ZA30320
         EJECT
***********************************************************************
*                                                                     *
*                     BRANCH  TABLE                              Y02669
*                                                                     *
*        THIS TABLE MUST BE IN A ONE-TO-ONE CORRESPONDENCE WITH       *
*        THE 'COMPARE TABLE'                                          *
*                                                                     *
***********************************************************************
         SPACE
BRTABLE  DS    0H                                                Y02669
         B     DDJOBT             BRANCH TO PROCESS MN JOBNAMES,T 21002
         B     DDJOB              BRANCH TO PROCESS MN JOBNAMES   21002
         B     DDSEST             BRANCH TO PROCESS MN SESS,T     20030
         B     DDSES              BRANCH TO PROCESS MN SESS       20030
         B     DDSTAT             BRANCH TO PROCESS MN STATUS    Y02669
         B     DDSNAME            BRANCH TO PROCESS MN DSNAME    Y02669
         B     DDSPACE            BRANCH TO PROCESS MN SPACE     Y02669
         SPACE 2
***********************************************************************
*   NOTE: THE FOLLOWING TABLE MUST BE THE LAST ITEM IN THIS     Y02669*
*         MODULE SINCE A USING FOR THE TABLE IS ISSUED ON       Y02669*
*         REGISTER 6 AND THUS CAN REESTABLISH THE MODULE'S BASE Y02669*
*         REGISTER FOR ANY CODE WHICH FOLLOWS THIS TABLE.       Y02669*
*                                                                     *
*                          COMPARE TABLE                              *
*                                                                     *
*         THIS TABLE MUST BE IN A ONE-TO-ONE CORRESPONDENCE           *
*         WITH THE BRANCH TABLE.                                      *
*                                                                     *
***********************************************************************
         SPACE
DTAB     EQU   *
DLEN     EQU   *
         DC    AL1(L'DJOBNT-1)    LENGTH OF MN OPERAND            20030
DJOBNT   DC    C'JOBNAMES,T '     MONITOR OPERAND                 20030
         DC    AL1(L'DJOBN-1)           *
DJOBN    DC    C'JOBNAMES '             *
         DC    AL1(L'DSESST-1)          *                         20030
DSESST   DC    C'SESS,T '               *                         20030
         DC    AL1(L'DSESS-1)           *                         20030
DSESS    DC    C'SESS '                 *                         20030
         DC    AL1(L'DSTATUS-1)         *
DSTATUS  DC    C'STATUS '               *
         DC    AL1(L'DSNAME-1)          *                        Y02669
DSNAME   DC    C'DSNAME '               *                        Y02669
         DC    AL1(L'DSPACE-1)          *
DSPACE   DC    C'SPACE '                *
DENTAB   EQU   *
         EJECT
         IEEBASEA
         EJECT
IEEXSA   DSECT
         IEEXSA
         EJECT
         IEZMPL
         EJECT
CVT      DSECT                                                   Y02669
         CVT
         EJECT
         IEECUCM  FORMAT=NEW
         END
