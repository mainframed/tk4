      TITLE 'IEE3103D - VARY ONLINE/OFFLINE'
***********************************************************************
*                                                                     *
* TITLE    IEE3103D:                                                  *
*                           VARY ONLINE/OFFLINE                       *
*                                                                     *
***********************************************************************
*
* MODULE NAME      = IEE3103D
*    CSECT NAME    = IEE3103D
*
* LOAD MODULE NAME = IEE3603D
*
* DESCRIPTIVE NAME = VARY COMMAND FOR ONLINE/OFFLINE KEYWORDS
*                    FOR NON CONSOLE UNITS
*
* COPYRIGHT        = MISSING
*
* STATUS           = OS/VS/2 RELEASE 3                         @OZ11847
*
* FUNCTION         = THIS MODULE VALIDATES UNIT AND VARIES ONLINE OR
*                    OFFLINE ANY UNITS IN THE COMMAND WHICH ARE
*                    FOUND TO BE NON-CONSOLE UNITS
*
* OPERATION        = FOR VARY, A UCB SEARCH IS ACCOMPLISHED TO ENSURE A
*                    VALID UNITNAME.
*                    ANY UNITS VARIED IMMEDIATELY BY THIS
*                    ROUTINE, HAVE AN ONLINE OR OFFLINE MSG
*                    ISSUED TO THE OPERATOR.  IF VARY RANGE    @Z30LPTJ
*                    IS BEING PROCESSED, SUCH MESSAGES ARE     @Z30LPTJ
*                    SUPPRESSED.                               @Z30LPTJ
*
*                    IF DEVICE IS ONLINE AND CMD IS V ONLINE
*                    STATUS IS NOT CHANGED.
*                    THE FOLLOWING BITS ARE SET:
*                       ONLINE BIT          UCB  N/A
*                       ON/OFF CHANGE STS   UCB  OFF
*                       MP-OPERATOR BIT     UCB  N/A
*                    IF DEVICE IS OFFLINE AND CMD IS V ONLINE
*                    THE STATUS IS CHANGED IMMEDIATELY.  THE
*                    FOLLOWING BITS ARE SET:
*                       ONLINE              UCB  ON
*                       ON/OFF CHANGE STS   UCB  OFF
*                       MP-OPERATOR BIT     UCB  OFF
*                    IF DEVICE IS OFFLINE AND CMD IS V OFFLINE
*                    THE STATUS IS NOT CHANGED.
*                    THE FOLLOWING BITS ARE SET:
*                       ONLINE              UCB  N/A
*                       ON/OFF CHANGE STS   UCB  OFF
*                       MP-OPERATOR BIT     UCB  N/A
*                    IF DEVICE IS ONLINE,(ALLOCATED OR NOT) AND THE
*                    CMD IS V OFFLINE, STATUS WILL BE CHANGED
*                    BY TERMINATION WHENEVER THE DEVICE BECOMES
*                    UNALLOCATED IF IT WAS ALLOC., OR WHENEVER
*                    TERMINATION GETS CONTROL IN THE SYSTEM.
*                    THE FOLLOWING BITS ARE SET:
*                       ON/OFF CHANGE STS   UCB  OFF
*                       MSSUM-UCB SEARCH BIT MRC ON
*                       MP-OPERATOR BIT     UCB  ON
*
* NOTES
*    DEPENDENCIES    = NONE
*    CHARACTER/CODE DEPENDECIES
*                  = THIS MODULE CAN BE ASSEMBLED WITH ANY NEW
*                    CHARACTER CODE SET.
*   RESTRICTIONS   = ON ENTRY REG2 PTR TO THE PARM LIST (DUMMY XSA)
* REGISTER CONVENTIONS
*                  = REGISTERS ARE DEFINED AT: REGDEF
*    PATCH LABEL   = PATCH
*
* MODULE TYPE      = CSECT
*    PROCESSOR     = ASSEMBLER
*    MODULE SIZE   = VS/2 = X'59B'                             @OZ11847
*    ATTRIBUTES    = REENTRANT,REUSABLE,REFRESHABLE,PAGED LPA,
*                    ZERO PSW PROTECT KEY
*
* ENTRY POINTS     = IEE3103D FROM IEE4203D OR/AND IEE4603D
*    PURPOSE       = TO PROCESS VARY COMMAND
*    LINKAGE       = BRANCH
*    INPUT         = REGISTER 2 POINTS TO THE DUMMY EXTENDED SAVE AREA
*                    XAR PTS TO THE LOGICAL END OF BUFFER
*                      (PTS TO COMMA BEFORE 'KEYWORD')
*                    XAL POINTS TO THE COMMAND PARAM LIST
*                    XAV CONATINS THE VERB
*                    XAS+0 (XOPCODE) AND XAS+1 (XSWITCH) CONTAINS
*                    BIT DESIGNATING THE KEYWORD:
*                       ONLINE
*                       OFFLINE
*                    REG13 PTR TO THE SAVE AREA GOTTEN IN IEE3603D
*                    WORD 17 OR THE SAVE AREA HAS THE PTR TO THE CSCB
*    REGISTERS SAVED=ALL
*    REGISTER USAGE= R1  LINKAGE REG
*                    R2  DUMMY XSA PTR
*                    R6  EOBUFFER PTR
*                    R7  UCB TABLE STOPPER
*                    R9  UNIT PTR
*                    R11 RESIDENT CORE BASE
*                    R13 USED AS PTR TO 1ST UCME, SAVE AREA PTR STORED
*                        IN XAA+2 OF XSA
*    REGISTERS RESTORED
*                  = ALL
*
* OUTPUT DATA      = .IF THE UNIT IS A DATA STAGING MANAGER    @Z30LPTJ
*                     DEVICE AND A SS/1 PARAMETER IS NOT       @Z30LPTJ
*                     SPECIFIED, MESSAGE IEE084I IS ISSUED.    @Z30LPTJ
*                    .IF ONLINE IS SPECIFIED, BUT THE UNIT IS  @Z30LPTJ
*                     IN USE BY A SYSTEM COMPONENT, THE REQUEST@YM07231
*                     IS DENIED AND MESSAGE IEE329I IS ISSUED. @YM07231
*                    .OTHERWISE THE FOLLOWING IS DONE
*                       THE ONLINE MESSAGE (IEE302I) IS WRITTEN
*                    .IF OFFLINE IS SPECIFIED AND THE UNIT IS
*                     ALREADY OFFLINE, THE OFFLINE MESSAGE
*                     (IEE303I) IS WRITTEN.
*                    .IF UNIT IS ONLINE AND ALLOCATED,
*                       SRTECHGS IS TURNED ON
*                    .IF THE UNIT IS ONLINE BUT NOT ALLOCATED
*                       THE UNIT IS MADE OFFLINE IMMEDIATELY AND
*                       AN OFFLINE MESSAGE IS SENT TO THE OPERATOR
*                    .ALL THE ABOVE CASES CAUSE THE MP-OPERATOR BIT
*                     TO BE TURNED ON
*                    .IEE302I AND IEE303I MESSAGES ARE         @Z30LPTJ
*                     SUPPRESSED IF VARY RANGE IS BEING        @Z30LPTJ
*                     PROCESSED.                               @Z30LPTJ
*
* EXITS - NORMAL-1-= RETURN TO CALLER                          @Z30LPTJ
*   CONDITIONS     = IF ALL IS OK AND SATISFIED.
*   OUTPUT         = ON RETURN TO THE CALLER
*                    THE CSCB IS FREED VIA AN MGCR MACRO
*                    THE SAVE AREA AND DUMMY XSA ARE ALSO FREED.
*   RETURN CODES   = NONE
*
* EXITS - NORMAL-2-= TO IEECB904 VIA BRANCH                    @Z30LPTJ
*   CONDITIONS     = IF ALL IS OK AND SATISFIED AND VARY RANGE @Z30LPTJ
*                    IS BEING PROCESSED                        @Z30LPTJ
*   OUTPUT         = REGISTER 2 POINTING TO THE XSA WHICH HAS  @Z30LPTJ
*                    THE XAL POINTING TO THE VARY RANGE WORK   @Z30LPTJ
*                    AREA.                                     @Z30LPTJ
*   RETURN CODES   = NONE                                      @Z30LPTJ
*
* EXITS - ERROR    = NONE, ALL ERRORS ARE HANDLED INTERNALLY
*
* EXTERNAL REFERENCES
*    ROUTINES      = RETURN TO CALLER
*                    IEECB904     VARY RANGE PROCESSOR         @Z30LPTJ
*    DATA AREAS    = SEE EXITS - OUTPUT DATA -
*
*    CONTROL BLOCKS= 1. DUMMY EXTENDED SAVE AREA (XSA)
*                    2. UNIT CONTROL BLOCK (UCB)
*                       FIELDS TESTED-  UCBFL2- BIT= X'40'
*                                       UCBNAME
*                                       UCBTBYT3- BIT= UCB3DACC
*                                                      UCB3UREC@Z30LPTJ
*                                       UCBTBYT4- BIT= UCBDSM  @Z30LPTJ
*                                       SRTESTAT- BIT= SRTESYSR
*                                                      SRTEONLI
*                       FIELDS UPDATED- UCBFL2- BIT= X'40'
*                                       SRTESTAT- BIT= SRTECHGS
*                                                      SRTEONLI
*                                       UCBWGT BIT= UCBVORSN
*                    3. COMMAND SCHEDULING CONTROL BLOCK (CSCB)
*                       FIELDS UPDATED- CHSTS - BIT= CHFC
*                       FIELDS TESTED-  NONE
*                    4. CVT
*                       FIELDS UPDATED- NONE
*                       FIELDS TESTED-  CVTILK2
*                                       CVTMSER
*                    5. UNIT CONTROL MODULE (UCM)
*                       FIELDS UPDATED- NONE
*                       FIELDS TESTED-  UCMUCB
*                                       UCMVEA
*                    6. MASTER SCHEDULER RESIDENT DATA AREA (MRC)
*                       FIELDS TESTED  - NONE
*                       FIELDS UPDATED - MSECBFL - BIT= MSSUM
*
* TABLES           = NONE
*
* MACROS           = 1. IOSGEN  -  FOR SIMULATED INTERRUPT
*                    2. WTO     -  FOR MSGS
*                    3. DEQ     -  FOR TESTING
*                                  CHANGEABLE UCB BITS
*                    4. FREEMAIN - TO FREE SAVE AREA AND DUMMY XSA
*                    5. MGCR     - TO FREE CSCB ON EXIT
*                    6. IOSGEN   - FOR TURNING OFF QUIESCE     @OZ11847
*
* MAPPING MACRO    = IEEXSA
*                    IEECHAIN
*                    IEFUCBOB
*                    CVT
*                    IEECUCM
*                    IEEBASEA
*                    IECDIOCM                                  @0Z11847
*    SERIALIZATION = NO LOCKS ARE REQUIRED IN THIS MODULE SINCE
*                    THERE IS NO NEED FOR SERIALIZATION WITH
*                    COMM. TASK
*                    PROTECTION FROM VARY/UNLOAD,ALLOCATION
*                    AND SYSTEM COMPONENT IS ACCOMPLISHED VIA  @YM07231
*                    AN ENQ ISSUED IN MODULE IEE3603D.         @YM07231
*                    THIS ENQ IS RELEASED VIA A DEQ WHEN
*                    THIS MODULE COMPLETES PROCESSING AND IS
*                    READY TO EXIT, AT: VDEQUE
*                    AT ALL TIMES, WHEN BITS ARE SET IN THE UCB AND
*                    MRC, THEY ARE DONE VIA THE COMPARE-AND-SWAP
*                    INSTRUCTION, FOR PROTECTION
*
* CHANGE LEVEL     = Y02669, Y02651, Z30LPTJ, ZM30481, OZ05159 @OZ05159
*                    OZ07161, @OZ11847                         @OZ11847
*
* MESSAGES         = NORMAL - WHEN SPECIFIED VARY CONTIONS IS
*                             ALREADY SATISFIED
*                             'IEE302I XXX ONLINE'
*                             'IEE303I XXX OFFLINE'
*                             (XXX IS THE UNITNAME)
*                             THE ABOVE MESSAGES ARE SUPPRESSED@Z30LPTJ
*                             WHEN VARY RANGE IS BEING         @Z30LPTJ
*                             PROCESSED.                       @Z30LPTJ
*
*                    ERROR -
*                             'IEE313I XXX UNIT REF. INVALID'
*                             WHEN UNITNAME IS NOT IDENTIFIED
*                             THIS MESSAGE IS SUPPRESSED WHEN  @ZM30481
*                             VARY RANGE IS BEING PROCESSED,   @ZM30481
*                             EXCEPT WHEN A SYSRES DEVICE IS   @ZM30481
*                             BEING ATTEMPTED TO BE VARIED     @ZM30481
*                             OFFLINE.                         @ZM30481
*
*                             'IEE329I XXX IN USE BY A SYSTEM  @YM07231
*                                                  COMPONENT'  @YM07231
*                             WHEN UNIT SPECIFIED CANNOT BE
*                             BROUGHT ONLINE
*
*                             'IEE084I XXX 3851 PARAMETER      @Z30LPTJ
*                                                MISSING'      @Z30LPTJ
*                             WHEN A DATA STAGING MANAGER UNIT @Z30LPTJ
*                             IS SPECIFIED WITHOUT A SS/1      @Z30LPTJ
*                             PARAMETER                        @Z30LPTJ
*
* ABEND CODES      = NONE
*
* PACKAGING        = IEE3103D IS PART OF LOAD MODULE
*                       = IEE3603D
*                    THIS LOAD MODULE ALIASES ARE
*                       = NONE
*                    THIS LOAD MODULE ENTRY POINTS ARE
*                       = IEE3603D
*                    MODULE WILL RESIDE IN:  LPALIB
*
* SYSGEN           = SUPPLIED BY SECTIONS:
*                       .LOAD MODULE NAME
*                       .PACKAGING
*                       .THIS CSECT WILL BE INCLUDED AT SYSGEN FROM
*                        DLIB -AOSB3- BY MACRO -SGIEF441-
*
* SYSTEM LIBRARIES = NONE
*
***********************************************************************
    EJECT
*****************************************************************Y02669
*   ***       ***                                                Y02669
*   *           *                                                Y02669
* V * XXX       *,ONLINE /OR/OFFLINE                             Y02669
*   *           *                                                Y02669
*   ***       ***                                                Y02669
*****************************************************************Y02669
    EJECT
IEE3103D CSECT
* C 104000-105000,178000-179000,200000-200500,275000-276000,   @YM07231
* C 331000-332000,592000,594000,597000,599000-602000,          @YM07231
* C 631000-632000                                              @YM07231
* A 102200-102600,137300-137600,202100-202500,274500,          @Z30LPTJ
* A 278500,330080-330880,500500,626300-626600                  @Z30LPTJ
* C 019000,071000,103000,187000,386000                         @Z30LPTJ
* D 072000-073000,108000                                       @Z30LPTJ
* A 573100-573700                                              @ZM30481
* C 071000,584000                                              @ZM30481
* A 476100-476500                                              @OZ05159
* FIX A                                                        @ZA07161
* VONLIN1 (D)                                                  @ZA08705
* VONLIN1 (A)                                                  @OZ11847
*       REGISTER EQUATES
REGDEF   EQU   *                                                 Y02669
R0       EQU   0
R1       EQU   1
R2       EQU   2                             XSA BASE
R3       EQU   3                             CVT BASE
R4       EQU   4                             DATA CELL BIN NUM CHECK
R5       EQU   5                             LINK REG
R6       EQU   6                             END OF PARM PTR
R7       EQU   7                             UCB TABLE STOPPER
R8       EQU   8                             SAVE AREA PTR STORE Y02669
R9       EQU   9                             UNIT NAME POINTER
R10      EQU   10
R11      EQU   11
*
*
*
VBASE    EQU   12
*
*
*
R13      EQU   13                       USED AS PTR TO 1ST UCM   Y02669
R14      EQU   14
R15      EQU   15
*
* CODE EQUATES
*
*
HEX80    EQU   X'80'
ZERO     EQU   0
ONE      EQU   1
TWO      EQU   2
TRI      EQU   3
FOR      EQU   4
SVN      EQU   7
EIT      EQU   8
FIFTN    EQU   15                                                 19084
SXTN     EQU   16
SVTN     EQU   17
TWENTY3  EQU   23                                              @Z30LPTJ
TWENTY9  EQU   29                                              @YM07231
FORTY9   EQU   49                                              @YM07231
TWTY8    EQU   28
THRTY7   EQU   37
FORTY3   EQU   43                                              @Z30LPTJ
D64      EQU   64                                                Y02669
D68      EQU   68                                                Y02669
D76      EQU   76                                                Y02669
D132     EQU   132                                               Y02669
SIXTY    EQU   60                                                Y02669
EIGHTY   EQU   80                                                Y02669
*
*
ZEROCD   EQU   X'00'
LPAREN   EQU   C'('
BLANK    EQU   C' '
         EJECT
* INITIALIZATION - SETS UP MODULE ADDRESSABILITY, AND ESTABLISHES     *
*   THE END OF BUFFER AND PARAMETER POINTERS.                         *
*
         BALR   VBASE,ZERO            ESTABLISH PROGRAM
         USING  *,VBASE            ADDRESSABILITY
         MODID BR=YES                                            Y01886
         USING  XSA,R2             BASE FOR EXTENDED SAVE A
         USING  SETCVT,R3          CVT ADDRESSABILITY
*
         STM   R0,R15,ZERO(R13)                                  Y02669
         ST    R13,XAK             STORE SAVE AREA PTR         @Z30LPTJ
         L     R3,CVTPTR           CVT PTR TO REG3
         L      R9,XAL
*                                  USE REG 9 AS POINTER
         LA     R9,ZERO(R9)           TO PARAMETER KEYWORD
         EJECT
*
*
         L     R11,CVTCUCB        OPTAIN PTR TO UCM (HEADER)
         USING UCM,R11           ..
         DROP  R3                                                 19084
*
*
***********************************************************************
*
*        AFTER LOCATING THE UCB, CHECK TO SEE
*        IF UNIT IS A CONSOLE BY LOOKING FOR A UCME WHICH
*        CORRESPONDS TO THE UCB.  IF FOUND, UNIT IS A CONSOLE
*        AND HAS BEEN PROCESSED BEFORE BY CONSOLE ROUTINES
***********************************************************************
         MVI   XAE,ZEROCD          CLEAR HIGH ORDER BYTE          19084
         CLI   ZERO(R9),LPAREN     ONE UNIT ONLY
         BE    VMLTUNT            NO, MORE THAN ONE
         BAL   R5,VULUCB          GO LOOK UP UCBS
VDCHK    EQU   *                  ..
         USING UCBOB,R3           SET UP BASE                     19084
         CLI   UCBTBYT3,UCB3UREC  TEST 1ST BYTE FOR DSM        @Z30LPTJ
         BNE   NOTDSM             DATA STAGING MANAGER NOT UP  @Z30LPTJ
         CLI   UCBTBYT4,UCBDSM    TEST 2ND BYTE FOR DSM        @Z30LPTJ
         BNE   NOTDSM             DATA STAGING MANAGER NOT UP  @Z30LPTJ
         BAL   R6,VMOVE           GET UNIT NAME                @Z30LPTJ
         SR    R0,R0              CLEAR REG                    @Z30LPTJ
         TM    XAU,HEX80          WAS ISSUER THE R/I           @Z30LPTJ
         BO    OKAY               YES, OK                      @Z30LPTJ
         IC    R0,XAU             NO, GET CONSOLE ID           @Z30LPTJ
OKAY     EQU   *                                               @Z30LPTJ
         MVC   XAI(EIT),SS1ID     GET MESSAGE ID               @Z30LPTJ
         MVC   XAT(TWENTY3),SS1TEXT GET MESSAGE TEXT           @Z30LPTJ
         MVC   XAT+TWENTY3(FOR),ROUTDESC GET ROUT & DESC CODES @Z30LPTJ
         LA    R6,FORTY3          GET MESSAGE LENGTH           @Z30LPTJ
         B     VMSG1              PREPARE FOR WTO              @Z30LPTJ
NOTDSM   EQU   *                                               @Z30LPTJ
         TM    UCBFL5,UCBNALOC    IS DEVICE IN USE BY A SYSTEM @YM07231*
                                   COMPONENT?                  @YM07231
         BO    COMPUNT            YES, ISSUE MESSAGE           @YM07231
         TM    XASOPCOD,XAON      ONLINE                       @Z30LPTJ
         BO    VONLIN             YES                             19084
         EJECT                                                    19084
*                                                                 19084
* VARY OFFLINE                                                    19084
*                                                                 19084
VOFFLN   EQU    *                  ..                             19084
         TM    SRTESTAT,SRTESYSR   SYSRES
         BO    VUBDUN              YES, INVALID UNIT REFERENCE    19726
*        NI    SRTESTAT,F-SRTECHGS TURN OFF THE ON/OFF CHGE STATSY02651
*                                                                Y02651
********** THE ABOVE INSTRUCTION WILL BE PROTECTED VIA THE       Y02651
*          COMPARE-AND-SWAP INSTRUCTION                          Y02651
*                                                                Y02651
         L     R6,XAK                                          @Z30LPTJ
         STM   R4,R5,D68(R6)  SAVE R4,R5 AND R15 FOR MACRO USE.  Y02651
         ST    R15,D76(R6)    R3 POINTS TO UCB FOR MACRO ADDR    Y02651
NICHGS   EQU   *                                                 Y02651
FSRTECGS EQU   X'BF'  X'FF'-SRTECHGS-                            Y02651
         NIL   SRTESTAT,FSRTECGS,WREGS=(15,4,5),REF=UCBOB        Y02651
*        OI    UCBWGT,UCBVORSN    INDICATE V OFFL FOR V DEVICE   Y02651
OILOPER  EQU   *                                                 Y02651
         OIL   UCBWGT,UCBVORSN,WREGS=(15,4,5),REF=UCBOB          Y02651
         LM    R4,R5,D68(R6)                                     Y02651
         L     R15,D76(R6)    RESTORE R4,R5,AND R15              Y02651
*                                                                Y02651
         TM     SRTESTAT,SRTEONLI  NO. IS UNIT ONLINE NOW        Y02651
         BO     VSBAVU             UES. GO SET BIT TO SEARCH     Y02651
VWOFF    EQU    *                  ..                             19084
         BAL    R6,VMOVE           INSET UNIT FOR MESSAGE         19084
         MVC    XAT(EIT),VARYOFF     SET WTO TEXT                 19084
         MVC    XAI(EIT),VOFFID      OFFLINE IDENT                19084
         B      VUNIT              GO TO MSG WRITER.              19084
VMLTUNT  EQU   *                                                  19084
         LA    R3,FIFTN              ASSUME (I-XXX,O-XXX) SPECIFI 19084
         CLC   ONE(TRI,R9),LPARENI     IS IT '(I-'                19084
         BE    VMLTUNT2           YES,OMIT. GO CHECK IF E O BUFFER19084
         LA    R3,SVN               ASSUME O-XXX SPECIFIED        19084
         CLC   ONE(TWO,R9),OUNITDSH    IS IS 'O-'                 19084
         BE    VMLTUNT2           YES,OMIT.  CHECK E O BUFFER     19084
         LA    R9,ONE(R9)           NO, MAY BE REGULAR UNIT       19084
         BAL   R5,VULUCB          GO LOOK UCBS                    19084
         LM    R13,R15,UCMVEA     R13 PTS TO THE FIRST UCME       19084
*                                 R14 INDICATES UCME SIXE         19084
*                                 R15 PTS TO THE LST UCME         19084
         USING UCMLIST,R13        ..                              19084
ULOOP    EQU   *                  ..                              19084
         C     R3,UCMUCB          IS THERE A UCME FOR THIS UCB    19084
         BE    VMAINN             YES,OMIT. GO CHECK E / BUFFER  Y02651
         BXLE  R13,R14,ULOOP      UCME NOT FOUND                  19084
         B     VDCHK              RETURN ADDRESS                  19084
         SPACE 2                                                  19084
VMOVE    EQU    *                  ..                             19084
         LM    R4,R5,XAR         STORE XAR AND XAL IN REGS     @Z30LPTJ
         L     R3,XAS              SAVE XAS SWITCHES              19084
         L     R7,XAK             STORE SAVE AREA POINTER      @Z30LPTJ
         L     R8,XAU             STORE UCM ID
         MVC    XAV(EIT),XAX         ALTER POSITION FOR UNIT      19084
         MVC    XAF(EIT),XAV         TO KEEP FORM OVERLAYING UNIT 19084
         BR    R6                  RETURN TO CALLING SUBROUTINE   19084
VSBAVU   EQU   *                                                  19084
*        OI    SRTESTAT,SRTECHGS  SET THE ON/OFF CHAG STATS BIT  Y02651
*                                                                Y02651
********** THE ABOVE INSTRUCTION WILL BE PROTECTED VIA THE       Y02651
*          COMPARE-AND-SWAP INSTRUCTION                          Y02651
*                                                                Y02651
         L     R6,XAK             SAVE AREA POINTER            @Z30LPTJ
         STM   R4,R5,D68(R6)                                     Y02651
         ST    R15,D76(R6)     R3 PTS TO UCB FOR MACRO ADDR.     Y02651
OICHGS   EQU   *                                                 Y02651
         OIL   SRTESTAT,SRTECHGS,WREGS=(15,4,5),REF=UCBOB        Y02651
         LM    R4,R5,D68(R6)                                     Y02651
         L     R15,D76(R6)     RESTORE R4,R5,AND R15             Y02651
*                                                                Y02651
         L     R1,CVTPTR                                         Y02651
         USING SETCVT,R1                                         Y02651
         L     R1,CVTMSER         MSTER RESIDENT CORE POINTER    Y02651
         DROP  R1                                                Y02651
         USING BASE,R1                                           Y02651
*        OI    MSECBFL,MSSUM      NO, SET UCB SEARCH FLAG IN     Y02651
*                                                                Y02651
********** THE ABOVE INSTRUCTION WILL BE PROTECTED VIA THE       Y02651
*          COMPARE AND SWAP INSTRUCTION                          Y02651
*                                                                Y02651
OIMSSUM  EQU   *                                                 Y02651
         OIL   MSECBFL,MSSUM,WREGS=(15,4,5),REF=BASE             Y02651
         LM    R4,R5,D68(R6)                                     Y02651
         L     R15,D76(R6)    RESTORE REGS 4,5,AND 15            Y02651
*                                                                Y02651
         DROP R1                                                 Y02651
*                                  MASTER RESIDENT CORE          Y02651
         B     VMAINN              GO CHECK ANYMORE              Y02651
         EJECT                                                    19084
*                                                                 19084
* VARY ONLINE                                                     19084
*                                                                 19084
VONLIN   EQU    *                  ..                             19084
*        OI     SRTESTAT,SRTEONLI  SET UCB BIT                   Y02651
*        NI     SRTESTAT,F-SRTECHGS                              Y02651
*                                                                Y02651
********** THE ABOVE TWO INSTRUCTIONS WILL BE PROTECTED VIA THE  Y02651
*          COMPARE-AND-SWAP INSTRUCTION                          Y02651
*                                                                Y02651
         L     R6,XAK             SAVE AREA POINTER            @Z30LPTJ
         STM   R4,R5,D68(R6)                                     Y02651
         ST    R15,D76(R6)       R3 PTS TO UCB FOR MACRO ADDR.   Y02651
OIONLI   EQU   *                                                 Y02651
         OIL   SRTESTAT,SRTEONLI,WREGS=(15,4,5),REF=UCBOB        Y02651
CHGSNI   EQU   *                                                 Y02651
FSRTECHS EQU   X'BF'  X'FF'-SRTECHGS-                            Y02651
         NIL   SRTESTAT,FSRTECHS,WREGS=(15,4,5),REF=UCBOB        Y02651
*        NI    UCBWGT,F-UCBVORSN                                 Y02651
NILOPER  EQU   *                                                 Y02651
FUCBVORS EQU   X'FB'  X'FF'-UCBVORSN-                            Y02651
         NIL   UCBWGT,FUCBVORS,WREGS=(15,4,5),REF=UCBOB          Y02651
         LM    R4,R5,D68(R6)                                     Y02651
         L     R15,D76(R6)         RESTORE R4,R5,AND R15         Y02651
*                                                                Y02651
READYBIT EQU    X'40'              FOR 'ANDING OFF NOT READY BIT Y02651
         EJECT
         L     R13,XAK            POINTER TO SAVE AREA         @Z30LPTJ
         STM   R0,R13,ZERO(R13)   STORE ALL MY REGS EXCEPT R14   Y02669
         ST    R15,SIXTY(R13)                                    Y02669
         LA    R4,IOSGENST        START OR FIXED AREA            Y02669
         LA    R5,IOSGEND         END+1 ADDRESS OR FIXED AREA    Y02669
         LA    R6,EIGHTY(R13)     ECB ADDR                       Y02669
         STM   R4,R6,D68(R13)      SAVE THIS INFO                Y02669
         XC    EIGHTY(FOR,R13),EIGHTY(R13)  CLEAR ECB            Y02669
FIX      EQU   *                                                 Y02669
         PGFIX R,A=(4),EA=(5),ECB=(6),LONG=N
         LTR   R15,R15             TEST THE RETURN CODE        @ZA07161
         BZ    IOSGENST            DON'T WAIT ON POSTED ECB    @ZA07161
         WAIT  1,ECB=(6)
*                                                                Y02669
IOSGENST EQU   *                                                 Y02669
*    THE IOSGEN MACRO WAS ADDED FOR APAR 20947 AND PTM 2947
         IOSGEN SIMINT,UCB=(3),REG=(15)
IOSGEND  EQU   *                                                 Y02669
         LM    R4,R5,D68(R13)     START AND END ADDR             Y02669
FREE     EQU   *                                                 Y02669
         PGFREE  R,A=(4),EA=(5)
*                                                                Y02669
         LM    R0,R15,ZERO(R13)   RESTORE 3103D"S REGS           Y02669
         EJECT
VONLIN1  EQU    *                  ..                             19084
         TM     UCBTBYT3,UCB3DACC  IS CEVICE DIRECT ACCESS        19084
         BZ     VWON               NO-DO NOT SET NOT READY BIT OFF19084
         TM     UCBTBYT2,UCBRVDEV  IS THIS A VIRTUAL DEVICE?   @OZ11847
         BNO    NONVIRT            NO, SKIP RESETTING UCBQISCE @OZ11847
*   SET THE QUIESCE BIT OFF FOR VIRTUAL UNIT COMING ONLINE     @OZ11847
*                                                              @OZ11847
         IOSGEN UCBFLG,UCB=(3),TABLE=UCBQISCE,VAR=OFF          @OZ11847
         EJECT
NONVIRT  EQU   *                                               @OZ11847
*        NI     UCBFL2,READYBIT    TURN OFF NOT READY BIT        Y02651
*                                                                Y02651
********** THE ABOVE INSTRUCTION WILL BE PROTECTED VIA THE       Y02651
*          COMPARE-AND-SWAP INSTRUCTION                          Y02651
*                                                                Y02651
         L     R6,XAK              SAVE AREA POINTER           @Z30LPTJ
         STM   R4,R5,D68(R6)                                     Y02651
         ST    R15,D76(R6)                                       Y02651
NIREADY  EQU   *                                                 Y02651
NOTRDY   EQU   X'BF'  X'FF'-READYBIT-                            Y02651
         NIL   UCBFL2,NOTRDY,WREGS=(15,4,5),REF=UCBOB            Y02651
         LM    R4,R5,D68(R6)                                     Y02651
         L     R15,D76(R6)                                       Y02651
VWON     EQU    *
         BAL    R6,VMOVE           INSET UNIT FOR MESSAGE
         MVC    XAT(EIT),VARYON    SET WTO TEXT
         MVC    XAI(EIT),VONID       ID FOR VARY ON WTO
         EJECT
*
* MESSAGE WRITER
*
VUNIT    EQU   *
         LA    R6,TWTY8              LENGTH OF WTO LINE
         MVC   XAT+EIT(FOR),ROUTDESC MOVE IN ROUTE AND DESC CODES FLAGS
         SR    R0,R0               CLEAR REG                     Y02669
         TM    XAU,HEX80           WAS ISSUER THE R/I            Y02669
         BO    VMSG                YES, OK
         IC    R0,XAU              NO, GET CONSOLE ID
VMSG     EQU   *
         TM     XAAVRANG,XAARNG1  VARY RANGE BEING PROCESSED?  @Z30LPTJ
         BO     PROCEED           YES, SUPPRESS THE MESSAGE    @Z30LPTJ
VMSG1    EQU   *                                               @Z30LPTJ
         SLL    R6,SXTN              SHIFT TO FIRST HALF OR GEG
         ST     R6,XAH             PUT LEN IN FIRST HALF AND ZERO
         MVC   XAH+TWO(TWO),MCSFLAGS     MOVE IN MCSFLAGS
*                                  NEXT HALF WORD OF WTO BUFF LEN
         WTO    MF=(E,(R2))        WRITE LINE
PROCEED  EQU   *                                               @Z30LPTJ
         STM   R4,R5,XAR           RESTORE XAR AND XAL
         ST    R3,XAS              RESTORE XAS                    19084
         ST    R7,XAK             RESTORE SAVE AREA POINTER    @Z30LPTJ
         ST    R8,XAU             RESTORE UCMID                  Y02669
         B     VMAINN             GO,CHECK IF ANYMORE             19084
VMLTUNT2 EQU   *                                                  19084
         AR    R9,R3               R9 PTR TO DELIMITER            19084
*                                                                 19084
         SPACE 2                                                  19084
VMAINN   EQU   *                                                  19084
         C     R9,XAR             END OF BUFFER REACHED           19084
         BCTR  R9,ZERO            MINUS 1
         BL    VMLTUNT            NO, GO CHECK NEXT UNIT          19084
         TM    XAAVRANG,XAARNG1   VARY RANGE BEING PROCESSED?  @Z30LPTJ
         BNO   NORMEXIT           NO, AVOID EXIT TO IEECB904   @Z30LPTJ
         L     R15,VCON904        GET IEECB904 ADDRESS         @Z30LPTJ
         BR    R15                GO TO VARY RANGE PROCESSOR   @Z30LPTJ
NORMEXIT EQU   *                                               @Z30LPTJ
         EJECT
**************************************************               Y02651
*                                                                Y02651
*  THIS IS A TERMINAL EXIT AND THEREFORE THE ENQ THAT WAS        Y02651
*  ISSUED IN IEE3603D MUST BE RELEASED VIA A DEQ.                Y02651
*                                                                Y02651
         DEQ   MF=(E,IEFDEQUE)                                   Y02651
**************************************************               Y02651
         SPACE 10
         L     R13,XAK             RESTORE SAVE AREA PTR       @Z30LPTJ
         L     R1,D64(R13)         GET CSCB PTR INTO REG1        Y02669
         USING CHAIN1,R1                                         Y02669
         OI    CHSTS,CHFC          SET TO FREE CSCB              Y02669
         MGCR  (1),CHAIN                                         Y02669
         LM    R0,R15,ZERO(R13)    RESTORE ALL REGS              Y02669
         LR    R4,R14              SAVE RETURN ADDRESS           Y02669
*                                  FREE SAVE AREA GOTTEN IN 3303DY02669
         LR    R1,R2               DUMMY XSA PTR INTO REG1       Y02669
         LA    R0,D132             DUMMY XSA SIZE AND SVAREA SIZ Y02669
         FREEMAIN R,LV=(0),A=(1)                                 Y02669
*                                  FREE XSA CORE GOTTEN IN 3603D Y02669
         LR    R14,R4              RESTORE RETURN ADDRESS        Y02669
         BR    R14                 RETURN                        Y02669
*                                                                Y02669
*
         EJECT
* UCB LOOK-UP SUBROUTINE - THE TWO BYTE UCB POINTERS ARE EXAMINED TO  *
*  VERIFY THE UNITNAME.  SATISFACTORY IDENTIF. LEADS TO A RETURN 19084*
*  TO CALLER (EITHER VARY OR UNLOAD ROUTINE).  IF THE UNITNAME IS19084*
*  NOT FOUND, AN INVALID REFERENCE MESSAGE IS CALLED FOR.        19084*
*                                                                 19084
VULUCB   EQU    *                  ..                             19084
         MVI   XAX,BLANK           BLANK DEV HOLD AREA            19084
         MVC   XAX+ONE(SVN),XAX                                   19084
         LR    R3,R9                    SAVE UNITNAME POINTER     19084
         LA    R6,ONE                     SET BUFFER UPDATE COUNT 19084
         LA    R9,TRI(ZERO,R9)              SET FOR DELIMITER CHEK19084
         MVC   XAX(TRI),ZERO(R3)             SET UNITNAME         19084
VULUCB1  EQU   *                                                  19084
         AR    R9,R6                   POINT TO NEXT DEVICE       19084
         L     R6,CVTPTR                                          19084
         USING SETCVT,R6                                          19084
         L      R6,CVTILK2         POINT TO 2 BYTE UCB PTRS       19084
         DROP  R6                                                 19084
VULOOK   LH     R3,ZERO(R6)           LOAD A UCB POINTER          19084
         LTR    R3,R3              IS ADDRESS ZERO (A VOID)       19084
         BZ     VUBUMP             YES. LOOK AT NEXT              19084
         LH     R7,VFFFF           TABLE STOPPER - FFFF           19084
         CLR    R7,R3              IS IT TABLE END                19084
         BNE    VUUCB              NO. CHECK UCB NAME             19084
VUBDUN   EQU    *                  ..                             19084
         TM    XAAVRANG,XAARNG1    VARY RANGE BEING PROCESSED? @ZM30481
         BNO   INSERT              NO, PROCESS NORMALLY        @ZM30481
         TM    XASOPCOD,XAON       AN 'ONLINE' COMMAND?        @ZM30481
         BO    VMAINN              YES, AVOID MESSAGE OUTPUT   @ZM30481
         TM    SRTESTAT,SRTESYSR   A SYSRES DEVICE?            @ZM30481
         BNO   VMAINN              NO, AVOID MESSAGE OUTPUT    @ZM30481
INSERT   EQU   *                                               @ZM30481
         BAL   R6,VMOVE            INSERT UNIT FOR MSG            19084
         MVC   XAI(EIT),UNITERR          SET UP MSG ID            19084
         SR    R0,R0                                              19084
         TM    XAU,HEX80           R/I COMMAND                    19084
         BO    VMSGCONT            YES, GO CONTINUE WITH MSG BUILD19084
         IC    R0,XAU              NO, GET CONID IN REG0 FOR WTO  19084
VMSGCONT EQU   *                                                  19084
         MVC    XAT(SVTN),UNITTXT    SET UP MSG TEXT              19084
         MVC   XAT+SVTN(FOR),ROUTDESC     MOVE IN ROUT AND DESC   19084
         LA    R6,THRTY7               SET WTO LENGTH             19084
         B     VMSG1               PUT OUT ERROR MESSAGE       @ZM30481
VUUCB    EQU    *                  ..                             19084
         N      R3,VFFFF-TWO         CLEAR ANY HIGH ORDER BITS    19084
         CLC   UCBNAME(TRI),XAX          IS THIS THE SOUGHT UCB   19084
         BCR     EIT,R5            RETURN                         19084
VUBUMP   LA    R6,TWO(R6)            NO. BUMP UCB ADDR PTR        19084
         B     VULOOK              GO LOOK AT NEXT UCB            19084
         SPACE 5
COMPUNT  EQU   *                                               @YM07231
         BAL   R6,VMOVE           INSET UNIT IN MSG              Y02669
         MVC   XAI(EIT),COMPID    SET UP MESSAGE ID            @YM07231
         SR    R0,R0                                             Y02669
         TM    XAU,HEX80          IS IT A R/I COMMAND            Y02669
         BO    COMP2              YES, R0 IS PRESET            @YM07231
         IC    R0,XAU             GET CONSOLE ID IN R0           Y02669
COMP2    EQU   *                                               @YM07231
         MVC   XAT(TWENTY9),COMPMSG MOVE MESSAGE INTO BUFFER   @YM07231
         MVC   XAT+TWENTY9(FOR),ROUTDESC MOVE IN ROUTING AND   @YM07231*
                                          DESCRIPTOR CODES     @YM07231
         LA    R6,FORTY9          OBTAIN SIZE OF MESSAGE ID    @YM07231
         B     VMSG1              GO ISSUE MSG                 @Z30LPTJ
         EJECT
**************************************************               Y02651
*  THE LIST FORM OF THE DEQ MACRO FOLLOWS                        Y02651
*                                                                Y02651
IEFDEQUE DEQ   (IEFSQCBS,IEFQ4QCB,TWO,SYSTEM,IEFSQCBS,IEFVQCBS,SVN,    *
               SYSTEM),RET=HAVE,MF=L                           @YM5443P
IEFVQCBS DC    C'VARYDEV'
IEFSQCBS DC    C'SYSIEFSD'                                       Y02651
IEFQ4QCB DC    C'Q4'                                             Y02651
**************************************************               Y02651
         EJECT
*
*
VARYON   DC    C' ONLINE '         POSITIONAL OPERAND
VARYOFF  DC    C' OFFLINE'         POSITIONAL OPERAND
*
MCSFLAGS DC    B'1110000000000000'  MCS FLAGS=RESP AND REG0
*
ROUTDESC DC B'0000100000000000'        DESC CODE = 5
         DC    B'0000000000000000'     ROUTCODE IS NONE
*
VONID    DC    C'IEE302I '         MESSAGE ID FOR ONLINE
VOFFID   DC    C'IEE303I '         MESSAGE ID FOR OFFLINE
UNITERR  DC    C'IEE313I '         MSG ID FOR UNIT REF INVALID
UNITTXT  DC    C' UNIT REF INVALID'    MSG TEXT
SS1ID    DC    C'IEE084I '                                     @Z30LPTJ
SS1TEXT  DC    C' 3851 PARAMETER MISSING'                      @Z30LPTJ
*
         DS    0F
VCON904  DC    V(IEECB904)                                     @Z30LPTJ
         DC    X'0000'             MASK OF ZEROES
VFFFF    DC    X'FFFF'             MASK FOR UCB TABLE END
COMPID   DC    C'IEE329I '         SYSTEM COMPONENT MESSAGE ID @YM07231
COMPMSG  DC    C' IN USE BY A SYSTEM COMPONENT' MESSAGE TEXT   @YM07231
LPARENI  DC    C'(I-'              BEGINNING OF A COMPOSITE
OUNITDSH DC    C'O-'               O-XXX SPECIFICATION
*
*
*
PATCH    DC    CL65'***IEE3103D***'                            @Z30LPTJ
         EJECT
UUCM     DSECT
         IEECUCM  FORMAT=NEW
         EJECT
SETXSA   DSECT
         IEEXSA
         EJECT
SETCVT   DSECT
         CVT
         EJECT
UCB      DSECT
         IEFUCBOB
         IECDIOCM
         EJECT
CHAIN1   DSECT
         IEECHAIN
         EJECT
         IEEBASEA
         EJECT
         IHAPSA
         END
