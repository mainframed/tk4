         TITLE 'IEE7803D SVC 34 K C,D,ID COMMAND '
***********************************************************************
*                                                                     *
* MODULE NAME         =  IEE7803D                               Y02669*
*                                                                     *
* DESCRIPTIVE NAME    =  CONTROL (K) C,D,ID COMMAND             Y02669*
*                                                                     *
* COPYRIGHT           =  N/A                                    Y02669*
*                                                                     *
* STATUS              =  01 - UPDATE FOR VS/2 RELEASE 2         Y02669*
*                                                                     *
* FUNCTION            =  PROCESSES K C,D,MLWTOID CONTROL        Y02669*
*                        COMMAND WHICH CANCELS AN INLINE        Y02669*
*                        MLWTO BY PURGING THE UNDISPLAYED       Y02669*
*                        MINORS FROM THE OUTPUT QUEUE.          Y02669*
*                                                                     *
*   OPERATION         =  THE COMMAND BUFFER IS CHECKED FOR      Y02669*
*                        THE PRESENCE OF A VALID MLWTO ID.      Y02669*
*                        IF NOT FOUND, 'IEE927I REQUEST         Y02669*
*                        AMBIGUOUS' IS ISSUED. IF THE VALUE     Y02669*
*                        FOUND IS INVALID ( NOT A DIGIT ),      Y02669*
*                        'IEE156I INVALID OPERAND' IS ISSUED.   Y02669*
*                                                                     *
*                        THE TARGET CONSOLE ID FOUND IN XASCID  Y02669*
*                        IS THEN PACKED AND CONVERTED TO BINARY Y02669*
*                        AND THE RESULTING VALUE IS USED TO     Y02669*
*                        INDEX THE CORRESPONDING UCME.          Y02669*
*                                                                     *
*                        IF THIS CONSOLE'S WQE QUEUE ADDRESS,   Y02669*
*                        FOUND IN THE UCME, IS ZERO, 'IEE156I   Y02669*
*                        INVALID OPERAND' IS ISSUED.            Y02669*
*                                                                     *
*                        OTHERWISE, THE WQE QUEUE IS SEARCHED   Y02669*
*                        FOR A MATCHING MLWTO ID. IF NOT FOUND, Y02669*
*                        'IEE156I INVALID OPERAND' IS ISSUED.   Y02669*
*                                                                     *
*                        IF FOUND ISSUE A DOM MACRO TO         @Y02668P
*                        BUILD AN SSIB AND SSOB TO INFORM      @Y02668P
*                        THE SUBSYSTEM THAT A MLWTO HAS        @Y02668P
*                        BEEN DELETED                          @Y02668P
*                            WMJMSEQ - IS THE DOM SEQUENCE     @Y02668P
*                                      NUMBER IN THE MAJOR WQE @Y02668P
*                                                                     *
*                        THE USE COUNT FOR ALL THE MINOR WQES   Y02669*
*                        ON EACH MINOR WQE QUEUE IS DECREMENTED Y02669*
*                        UNTIL THEY ARE ZERO.                   Y02669*
*                        THE UCME IS RESET TO MAKE IT APPEAR    Y02669*
*                        AS IF THE MINORS HAD ALL BEEN WRITTEN: Y02669*
*                           OUTPUT PENDING BIT IS TURNED ON     Y02669*
*                               UCMSTS - UCMPF X'40'            Y02669*
*                           WORKING ON INLINE MLWTO TURNED OFF  Y02669*
*                               UCMSTS - UCMTC X'01'            Y02669*
*                           WAITING ON INLINE MLWTO TURNED OFF  Y02669*
*                               UCMSDS5 - UCMSDS5A X'80'        Y02669*
*                           DEQUEUE OUTPUT QUEUE ENTRIES        Y02669*
*                               UCMSTS - UCMTB X'04'            Y02669*
*                           PURGE CQE TURNED ON                 Y02669*
*                               CQEFLAG - CQEAVAIL X'02'        Y02669*
*                           CQU ACTIVE FLAG TURNED OFF          Y02669*
*                               CQEFLAG - CQEENTR               Y02669*
*                        AND CONTROL IS RETURNED.               Y02669*
*                                                                     *
* NOTES                                                         Y02669*
*                                                                     *
*   CHARACTER CODE                                              Y02669*
*    DEPENDENCIES     =  ANY CHARACTER CODE OTHER THAN EBCDIC   Y02669*
*                        WILL REQUIRE REASSEMBLY OF THIS MODULE.Y02669*
*                        NOTE THAT THIS MODULE IS DEPENDENT     Y02669*
*                        UPON THE CHARACTER REPRESENTATION OF   Y02669*
*                        DECIMAL DIGITS, IE. X'F1' = 1.         Y02669*
*                                                                     *
*   DEPENDENCIES      =  THE CMS LOCK AND LOCAL LOCK WILL       Y02651*
*                        REPLACE SSM (DISABLE AND ENABLE) FOR   Y02651*
*                        PURGING THE MINOR WQE'S.               Y02651*
*                                                                     *
*   RESTRICTIONS      =  NONE                                   Y02669*
*                                                                     *
*   REGISTER                                                    Y02669*
*    CONVENTIONS      =  NONE                                   Y02669*
*                                                                     *
*   PATCH-LABEL       =  PATCH                                  Y02669*
*                                                                     *
* MODULE TYPE         =  CSECT                                  Y02669*
*                                                                     *
*   PROCESSOR         =  N/A                                    Y02669*
*                                                                     *
*   MODULE SIZE       =  X'1A8' + X'48' PATCH AREA              Y02669*
*                                                                     *
*   ATTRIBUTES        =  REFRESHABLE, REENTRANT, PAGED LPA,     Y02669*
*                        KEY 0, SUPERVISOR MODE                 Y02669*
*                                                                     *
* ENTRY POINT         =  IEE7803D FROM IEE7503D                 Y02669*
*   PURPOSE           =  TO PROCESS THE K C,D,ID COMMAND FOR    Y02669*
*                        ALL NON-CRT DEVICES                    Y02669*
*   LINKAGE           =  BRANCH                                 Y02669*
*   INPUT DATA        =  REG 2 POINTS TO THE XSA                Y02669*
*                           XAS CONTAINS ROUTING INFORMATION    Y02669*
*                              XASCID - TARGET CONSOLE ID       Y02669*
*                              XASSDS5 ON -  LOCKS HELD         Y02651*
*                           XAL POINTS TO 1ST OPERAND IN BUFFER Y02669*
*                     =  REG 14 CONTAINS THE RETURN ADDRESS     Y02669*
*                                                                     *
*   REGISTERS SAVED   =  REGISTERS 11, 12, 13, 14 ARE SAVED     Y02651*
*                        BEFORE ISSUING SETLOCK RELEASE AT      Y02651*
*                        ENDSRCH.                               Y02651*
*                                                                     *
*   REGISTER CONTENTS                                           Y02669*
*    DURING PROCESSING=  R0  =  WORK REG                        Y02669*
*                        R1  =  WORK REG                        Y02669*
*                        R2  =  BASE REG FOR XSA                Y02669*
*                        R3  =  USUSED                          Y02669*
*                        R4  =  BASE REG FOR WQE                Y02669*
*                        R5  =  BASE REG FOR CQE                Y02669*
*                        R6  =  WORK REG                        Y02669*
*                        R7  =  WORK REG                        Y02669*
*                        R8  =  BASE REG FOR UCM ENTRY          Y02669*
*                        R9  =  UNUSED                          Y02669*
*                        R10 =  OPERAND POINTER                 Y02669*
*                        R11 =  BASE REG FOR THIS MODULE        Y02669*
*                        R12 =  BASE REG FOR UCM                Y02669*
*                        R13 =  UNUSED                          Y02669*
*                        R14 =  RETURN ADDRESS                  Y02669*
*                        R15 =  WORK REG & BRANCH REG           Y02669*
*                                                                     *
*   REGISTERS RESTORED=  REGISTERS 11, 12, 13, 14 ARE RESTORED  Y02651*
*                        AFTER ISSUING SETLOCK AT ENDSRCH.      Y02651*
*                                                                     *
*  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *
*                                                                     *
* EXIT - NORMAL       =  RETURN VIA BR 14 FROM ENDSRCH          Y02669*
*   CONDITIONS        =  PROCESSING OF K C,D COMPLETE           Y02669*
*   OUTPUT DATA       =  ALL UNDISPLAYED MINORS OF MLWTO        Y02669*
*                        ARE PURGED AND THE LOCKS RELEASED      Y02651*
*                        REG 2 STILL POINTS TO XSA              Y02669*
*                           XASSDS5 IS TURNED OFF -  LOCKS      Y02651*
*                           RELEASED                            Y02651*
*   RETURN CODES      =  NONE                                   Y02669*
*                                                                     *
*  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *
*                                                                     *
* EXIT - ERROR        =  BRANCH TO IEE5603D FROM D5603D         Y02669*
*   CONDITIONS        =  TO ISSUE ERROR MESSAGES 'IEE156I       Y02669*
*                        INVALID OPERAND' AND 'IEE927I REQUEST  Y02669*
*                        AMBIGUOUS'                             Y02669*
*   OUTPUT DATA       =  REG 2 POINTS TO XSA                    Y02669*
*                           XAE CONTAINS MAJOR MESSAGE INDEX    Y02669*
*                           FOR IEE5603D   X'04' - IEE156I      Y02669*
*                                          X'0D' - IEE927I      Y02669*
*                           XASTSAVE POINTS TO INVALID OPERAND  Y02669*
*                            & CONTAINS ERROR CODE FOR IEE5903D Y02669*
*                              X'16' - SPECIFY DISPLAY ID       Y02669*
*                           XASSDS5 IS STILL ON - LOCKS HELD    Y02651*
*   RETURN CODES      =  NONE                                   Y02669*
*                                                                     *
*  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *
*                                                                     *
* EXTERNAL REFERENCES                                           Y02669*
*                                                                     *
*   ROUTINES          =  IEE5603D                               Y02669*
*                                                                     *
*   DATA AREAS        =  NONE                                   Y02669*
*                                                                     *
         EJECT
*   CONTROL BLOCKS    =  IEECUCM  R - ADDR CONSOLE'S WQE QUEUE  Y02669*
*                                 W - RESET WQE QUEUE BITS      Y02669*
*                        IEEXSA   R - INPUT                     Y02669*
*                                 W - ERROR MSG INDEX           Y02669*
*                        CVT      R - ADDRESS UCM               Y02669*
*                        IHAWQE   R - COMPARE ID'S              Y02669*
*                                 W - DEQUEUE MINORS            Y02669*
*                        IHAPSA   NECESSARY FOR SETLOCK         Y02651*
*                        IHACTM   (CQE) R - RIGHT WQE           Y02669*
*                                       W - RESET BITS          Y02669*
*                                                                     *
*   TABLES            =  NONE                                   Y02669*
*                                                                     *
*   MACROS            =  SETLOCK - CMS AND LOCK LOCKS RELEASED  Y02651*
*                                                                     *
*   ENQUEUE RESOURCES =  NONE                                   Y02669*
*                                                                     *
*   LOCKS HELD        =  CMS AND LOCAL LOCKS ARE OBTAINED IN    Y02651*
*                        IEE7503D AND HELD THROUGHOUT IEE7803D. Y02651*
*                        UNDER NORMAL CONDITIONS THEY ARE       Y02651*
*                        RELEASED AND XASSDS5 IS RESET BEFORE   Y02651*
*                        RETURNING AT ENDSRCH.                  Y02651*
*                        FOR ERROR CONDITIONS THEY ARE RELEASED Y02651*
*                        IN IEE5603D OR IEE5903D.               Y02651*
*                                                                     *
*   CHANGE LEVEL      =  Y02669                                 Y02669*
*                        Y02651                                 Y02651*
*                        YM1911  (PROLOGUE & COMMENT CHANGES)   YM1911*
*                                                                     *
* MESSAGES            =  THE FOLLOWING MESSAGES ARE CONTAINED   Y02669*
*                        IN AND ISSUED FROM IEE5603D & IEE5903D:Y02669*
*                                                                     *
*                         MSGID   CODE  IN  MESSAGE TEXT        Y02669*
*                        IEE156I  X'04' 56  INVALID OPERAND     Y02669*
*                        IEE927I  X'0D' 56  REQUEST AMBIGUOUS   Y02669*
*                                 X'16' 59  SPECIFY DISPLAY ID  Y02669*
*                                                                     *
* SYSGEN              =  INCLUDED WITH SVC 34 IN LOAD MODULE    Y02669*
*                        IGC0003D,DLIB AOSB3, MACRO SGIEF441,   Y02669*
*                        TARGLIB LPALIB.                        Y02669*
*                                                                     *
* SYSTEM LIBRARIES    =  NONE                                   Y02669*
*                                                                     *
***********************************************************************
         EJECT
IEE7803D CSECT
***********************************************************************
*                                                                     *
*                            REGISTERS                                *
*                                                                     *
***********************************************************************
R0       EQU   0                   WORK REGISTER
R1       EQU   1                   WORK REGISTER
PARMREG  EQU   2                   BASE REGISTER FOR XSA
R3       EQU   3      (UNUSED)     UNUSED                        Y02669
WQEREG   EQU   4                   BASE REGISTER FOR MINOR WQE   Y02669
CQEREG   EQU   5                   BASE REGISTER FOR CQE         Y02669
R5       EQU   5                   WORK REGISTER                 Y02669
R6       EQU   6                   WORK REGISTER
R7       EQU   7                   WORK REGISTER
UCMEBASE EQU   8                   BASE REGISTER FOR UCM ENTRY
R9       EQU   9                   DOM SEQUENCE NUMBER         @Y02668P
KOPS     EQU   10                  OPERAND POINTER
MYBASE   EQU   11                  THIS ROUTINES BASE REGISTER
UCMBASE  EQU   12                  UCM BASE REGISTER
R13      EQU   13                  UNUSED                        Y02669
R14      EQU   14                  RETURN REGISTER               Y02669
R15      EQU   15                  WORK REGISTER                 Y02669
         EJECT
***********************************************************************
*                                                                     *
*                          CHARACTER CONSTANTS                        *
*                                                                     *
***********************************************************************
COMMA    EQU   C','                COMMA CHAR FOR SYNTAX CHECK   Y02669
BLANK    EQU   C' '                BLANK CHAR FOR SYNTAX CHECK
ZERO     EQU   X'00'               ZERO CONSTANT
***********************************************************************
*                                                                     *
*                             MASKS                                   *
*                                                                     *
***********************************************************************
DIGIT    EQU   X'F0'               MASK TO TEST FOR VALID DIGIT  Y02669
XFF      EQU   255                 USED IN NI INSTRUCTIONS TO TURN
*                                  OFF BITS FOR TM INSTRUCTIONS  Y02669
***********************************************************************
*                                                                     *
*                       ERROR MESSAGE CODES                           *
*                                                                     *
***********************************************************************
MSG156   EQU   4                   IEE156I INVALID OPERAND       Y02669
MSG927   EQU   13                  IEE927I K REQUEST AMBIGUOUS   Y02669
MSG927I  EQU   22                          SPECIFY DISPLAY ID    Y02669
***********************************************************************
*                                                                     *
*                   DISPLACEMENTS AND INCREMENTS                      *
*                                                                     *
***********************************************************************
N0       EQU   0                   FIELD INCREMENT
N1       EQU   1                   FIELD INCREMENT
N2       EQU   2                   FIELD INCREMENT
N3       EQU   3                   FIELD INCREMENT
N4       EQU   4                   FIELD INCREMENT
N7       EQU   7                   MASK FOR ICM INSTRUCTION      Y02669
N8       EQU   8                   FIELD INCREMENT
         EJECT
***********************************************************************
*                                                                     *
*      ESTABLISH ADDRESSABILITY                                       *
*                                                                     *
*      ON ENTRY, REGISTER 2 CONTAINS ADDRESS OF XSA                   *
*                                                                     *
***********************************************************************
         BALR  MYBASE,N0           ESTABLISH ROUTINE 'S
         USING *,MYBASE            BASE REGISTER
         MODID BRANCH=YES                                        Y01886
         USING FAUXSA,PARMREG      ESTABLISH XSA ADDRESSABILITY
         L     UCMBASE,CVTPTR      GET CVT POINTER
         USING CVTSECT,UCMBASE     BASE CVT REG
         L     UCMBASE,CVTCUCB     GET UCM POINTER FROM CVT
         DROP  UCMBASE                                           Y02669
         USING UCM,UCMBASE         ESTABLISH UCM ADDRESSABILITY  Y02669
         USING UCMLIST,UCMEBASE    BASE UCM ENTRY LIST
         USING WQE,WQEREG          ADDRESS WQE                   YM2304
         USING CQE,CQEREG          ESTABLISH BASE FOR CQE        Y02669
         SPACE 2
***********************************************************************
*                                                                     *
*              ESTABLISH POINTER TO K OPERANDS                   Y02669
*           'C,D' IS VALIDITY CHECKED IN IEE7503D                Y02669
*  IEE7503D ENSURES THAT TARGET CONSOLES ARE ALWAYS GRAPHICS DEVICES  *
*                                                                     *
***********************************************************************
         SPACE 1
         L     KOPS,XAL            LOAD POINTER TO OPERANDS
         LA    KOPS,N0(KOPS)       CLEAR HIGH ORDER BYTE
         EJECT
***********************************************************************
*                                                                     *
*                          K C,D                                      *
*                                                                     *
*   THE MLWTO ID IS OBTAINED FROM THE COMMAND BUFFER, VALIDITY        *
*   CHECKED, AND PACKED INTO XAP, FINDUCME IS THEN INVOKED TO         *
*   CONVERT THE VALUE TO BINARY AND DETERMINE WHETHER IT SPECIFIES    *
*   A VALID CONSOLE.                                                  *
*                                                                     *
*   ERR - IEE927I REQUEST AMBIGIOUS - IF MLWTO ID NOT SPECIFIED       *
*         IEE156I INVALID OPERAND - IF MLWTO ID NOT DIGIT             *
***********************************************************************
         SPACE 2
KCDRTN   EQU   *
         CLI   N3(KOPS),BLANK      IS AN MLWTO ID SPECIFIED?     Y02669
         BE    DERR927             NO, IEE927I REQUEST AMBIGIOUS Y02669
         LA    KOPS,N3(KOPS)       INCREMENT POINTER TO COMMA
         CLI   N0(KOPS),COMMA      IS NEXT CHARACTER COMMA?      Y02669
         BNE   BADKOP              NO, IEE156I INVALID OPERAND   Y02669
         SPACE
         LA    KOPS,N1(KOPS)       INCREMENT OPERAND POINTER TO ID
         TM    N0(KOPS),DIGIT      IS 1ST CHAR OF ID A DIGIT?    Y02669
         BNO   BADKOP              NO, IEE156I INVALID OPERAND
         SPACE
         TM    N1(KOPS),DIGIT      IS 2ND CHAR OF ID A DIGIT?    Y02669
         BO    TWODIGIT            YES, BRANCH
         CLI   N1(KOPS),BLANK      IS 2ND CHARACTER A BLANK?     Y02669
         BNE   BADKOP              NO, IEE156I INVALID OPERAND   Y02669
         SPACE
         PACK  XAP(N8),N0(N1,KOPS) PACK EBCDIC MLWTO ID
         B     FINDUCME            BRANCH TO CONVERT NUMBER
         SPACE  2
TWODIGIT EQU   *
         TM    N2(KOPS),DIGIT      IS 3RD CHAR OF ID A DIGIT?    Y02669
         BO    THRDIGIT            YES, BRANCH
         CLI   N2(KOPS),BLANK      IS 3RD CHARACTER A BLANK?     Y02669
         BNE   BADKOP              NO, IEE156I INVALID OPERAND   Y02669
         SPACE
         PACK  XAP(N8),N0(N2,KOPS) PACK EBCDIC MLWTO ID
         B     FINDUCME            BRANCH TO CONVERT NUMBER
         SPACE  2
THRDIGIT EQU   *
         CLI   N3(KOPS),BLANK      IS 4TH CHARACTER A BLANK?     Y02669
         BNE   BADKOP              NO, IEE156I INVALID OPERAND   Y02669
         SPACE
         PACK  XAP(N8),N0(N3,KOPS) PACK EBCDIC MLWTO ID
         EJECT
***********************************************************************
*                                                                     *
*             THE MLWTO ID ENTERED IS IN EBCDIC AND MUST         Y02669
*             BE CONVERTED TO BINARY FOR COMPARE                 Y02669
*                                                                     *
*    FIND AND BASE UCM ENTRY                                          *
*    INPUT- REG 1-CONSOLE ID RIGHT JUSTIFIED, HIGH ORDER BYTES ZERO   *
*    OUTPUT-POINTER TO UCMENTRY SPECIFIED BY CONSOLE ID IN UCMEBASE REG
*    REG USAGE-DESTROYS CONTENTS OF: REG 0,1,UCMEBASE                 *
*                                                                     *
*  ERR - IEE156I INVALID OPERAND - IF TARGET CONSOLE'S WQE ADDRESS    *
*                                  IS ZERO                            *
***********************************************************************
         SPACE 2
FINDUCME EQU   *
         CVB   R7,XAP              CONVERT TO BINARY
         SR    R1,R1               CLEAR REG
         IC    R1,XASCID           LOAD TARGET CONSOLE ID
         L     UCMEBASE,UCMVEA     POINT TO FIRST UCM ENTRY
         BCTR  R1,ZERO             SUBTRACT ONE FROM CONSOLE ID
         M     R0,UCMVEZ           MULTIPLY ENTRY SIZE BY REDUCED ID
         AR    UCMEBASE,R1         POINT TO DESIRED UCM ENTRY
         L     CQEREG,UCMOUTQ      GET CQE POINTER FROM UCME     Y02669
         LTR   CQEREG,CQEREG       IS THERE A CQE CHAIN?         Y02669
         BZ    BADKOP              NO, IEE156I INVALID OPERAND
         EJECT
***********************************************************************
*                                                                     *
*        SEARCH CONSOLE OUTPUT QUEUE FOR PROPER MLWTO WQE        Y02669
*                                                                     *
***********************************************************************
         SPACE 2
RETEST   EQU   *
         TM    CQEFLAG,CQEENTR     IS THIS QUE ENTRY ACTIVE?     Y02669
         BNO   NXTENTRY            BRANCH NO FOR NEXT ELEMENT    Y02669
         SPACE 2
         TM    CQEFLAG,CQEMAJOR    IS THIS A MLWTO?              Y02669
         BNO   NXTENTRY            BRANCH NO FOR NEXT ELEMENT    Y02669
         SPACE 2
         TM    CQEFLAG,CQEMLQHC    IS THIS A HARD COPY MLWTO?    Y02669
         BO    NXTENTRY            BRANCH YES FOR NEXT ELEMENT   Y02669
         SPACE 2
         SR    WQEREG,WQEREG       CLEAR REG FOR ADDRESS         Y02669
         ICM   WQEREG,N7,CQEWQEA   GET MAJOR WQE POINTER FROM CQEY02669
         L     R6,WMJMMSGN         LOAD WQE MLWTO ID
         SR    R9,R9               ZERO OUT REGISTER           @Y02668P
         ICM   R9,N7,WMJMSEQ       SAVE DOM SEQUENCE #         @Y02668P
LOOP     EQU   *                                                 Y02669
         C     R6,B1000            IS ID GREATER THAN 1000?       M2722
         BL    COMPARE             NO, GO COMPARE                 M2722
         S     R6,B1000            YES, REDUCE BY 1000            M2722
         B     LOOP                TRY AGAIN                      M2722
         SPACE 2
COMPARE EQU   *                                                   M2722
         CR    R6,R7               COMPARE THE 2 MLWTO ID'S
         BE    SETSUBQ             COMMAND ID EQUALS WQE ID
         EJECT
***********************************************************************
*                                                                     *
*    BRANCH HERE IF:  WQE NOT ACTIVE                             Y02669
*                     WQE NOT MLWTO                              Y02669
*                     WQE REPRESENTS HARD COPY MLWTO             Y02669
*                     WQE ID DOES NOT MATCH COMMAND'S ID         Y02669
*   ERR - IEE156I INVALID OPERAND - IF END OF QUEUE AND NO MATCH Y02669
*                                                                     *
***********************************************************************
         SPACE 2
NXTENTRY EQU   *                                                 Y02669
         TM    CQEFLAG,CQEEOB      IS IT A POINTER TO NEW BLOCK? Y02669
         BO    BUMPSUBQ            NO, JUST BUMP BLOCK POINTER
         SPACE 2
         TM    CQEFLAG,CQEEOQ      END OF LIST?                  Y02669
         BO    BADKOP              YES, IEE156I INVALID OPERAND
         SPACE 2
         LA    CQEREG,N4(CQEREG)   INCREMENT Q PTR TO NEXT ENTRY Y02669
         B     RETEST              CONTINUE SEARCH
         SPACE 2
BUMPSUBQ EQU   *
         L     CQEREG,N0(CQEREG)   LOAD POINTER TO NEXT SECTION  Y02669
         B     RETEST              CONTINUE SEARCH
         EJECT
***********************************************************************
*                                                                     *
*           DELETION OF THE MLWTO                              @Y02669P
*             1. OBTAIN DOM SEQUENCE NUMBER - WMJMSEQ          @Y02669P
*             2. THEN MAKE IT LOOK AS IF THE MLWTO HAS         @Y02669P
*                   BEEN PROCESSED                             @Y02669P
*                                                                     *
***********************************************************************
         SPACE 3
SETSUBQ  EQU   *
         OI    CQEFLAG,CQEAVAIL    SET PURGE BIT ON IN CQE       Y02669
         NI    CQEFLAG,XFF-CQEENTR TURN OFF ACTIVE BIT IN CQE    Y02669
         OI    UCMSTS,UCMTB        SET DEQUEUE BIT ON IN UCME
         SPACE 2
***********************************************************************
*                                                                     *
*              GET MINOR WQE POINTER FROM UCME                        *
*                1. IF ZERO, RETURN TO SYSTEM                         *
*                OR UCMMLAST MAY PT TO THE MINOR                      *
*                   THAT WAS JUST WRITTEN AND MAY                     *
*                   HAVE USE COUNT DECREMENTED                        *
*                1. IF NOT FULL CAPABILITY, GET POINTER TO NEXT       *
*                2. IF GRAPHICS CONSOLE, GET POINTER TO NEXT          *
*                                                                     *
***********************************************************************
         SPACE 2
         L     WQEREG,UCMMLAST     MINOR WQE PTR FROM UCME       YM2304
         LTR   WQEREG,WQEREG       IS MINOR POINTER ZERO?        YM2304
         BZ    ENDSRCH             YES, END SEARCH
         TM    UCMDISP,UCMDISPE    IS TARGET CONSOLE FULL CAPABILITY?
         BZ    NXTMINOR            NO, GET NEXT MINOR            Y02669
         SPACE
         TM    UCMDISP,UCMDISPC    IS THIS CONSOLE A GRAPHIC DEVICE?
         BO    NXTMINOR            YES, GET NEXT MINOR           Y02669
         EJECT
***********************************************************************
*                                                                     *
*                 DECREMENT USE COUNT IN MINOR WQE'S                  *
*                                                                     *
***********************************************************************
         SPACE 2
MINLOOP  EQU   *
         LA    WQEREG,N0(WQEREG)   ZERO HI ORDER BYTE OF MIN WQE YM2304
         LTR   WQEREG,WQEREG       IS IT A VALID POINTER?        YM2304
         BZ    ENDSRCH             NO, END SCAN
         DROP  CQEREG
         SR    R5,R5               CLEAR REG                     Y02669
         IC    R5,WMNMUC1          GET USE COUNT FROM MINOR      Y02669
         BCTR  R5,N0               DECREMENT USE COUNT BY ONE    Y02669
         STC   R5,WMNMUC1          PUT NEW COUNT BACK IN MINOR   Y02669
         LTR   R5,R5               IS THE USE COUNT ZERO?        Y02669
         BNZ   NXTMINOR            NO                            Y02669
         OI    WMJMMLW,WMJMMLWG    SERVICE THIS CHAIN = ON     @YM8168P
NXTMINOR EQU   *
         L     WQEREG,N0(WQEREG)   LOAD PTR TO NEXT MINOR WQE    YM2304
         B     MINLOOP
         EJECT
***********************************************************************
*                                                                     *
*                       NORMAL EXIT                                   *
*                                                                     *
* NOTE: LOCKS ARE ALWAYS HELD ON ENTRY TO IEE7803D                    *
*             1. RELEASE LOCKS                                        *
*             2. ISSUE A DOM MACRO TO BUILD AN SSIB AND SSOB   @Y02669P
*                   TO INFORM THE SUBSYSTEM THE MLWTO HAS BEEN @Y02669P
*                   DELETED                                    @Y02669P
***********************************************************************
         SPACE 2
ENDSRCH  EQU   *
         OI    UCMSTS,UCMPF        TURN ON OUTPUT PENDING BIT
         NI    UCMSTS,XFF-UCMTC    WORKING ON INLINE MLWTO=OFF   Y02669
         NI    UCMSDS5,XFF-UCMSDS5A WAITING ON INLINE MLWTO=OFF  Y02669
         STM   MYBASE,R14,XASAVLOC SAVE REGS AROUND SETLOCK      Y02651
         SETLOCK RELEASE,TYPE=CMS,RELATED=(K,IEE7503D(LOCKSET))
         SETLOCK RELEASE,TYPE=LOCAL,RELATED=(K,IEE7503D(LOCKSET))
         LM    MYBASE,R14,XASAVLOC RESTORE REGS AFTER SETLOCK    Y02651
         NI    XASSDS,XFF-XASSDS5  INDICATE LOCKS NOW OFF        Y02651
         DOM   MSG=(9)                                         @Y02668P
         BR    R14                 RETURN TO SYSTEM              Y02669
         EJECT
***********************************************************************
*                                                                     *
*                       ERROR EXITS                                   *
*                                                                     *
***********************************************************************
         SPACE 2
BADKOP   EQU   *                                                 Y02669
         MVI   XAE,MSG156          IEE156I INVALID OPERAND       Y02669
         ST    KOPS,XASTSAVE       ADDR OF INVALID OPERAND       Y02669
         B     D5603D              GO TO WRITE ERROR MSG         Y02669
         SPACE 2
DERR927  EQU   *                                                 Y02669
         ST    KOPS,XASTSAVE       CURSOR ADDRESS FOR MSG RTN
         MVI   XAE,MSG927          IEE927I REQUEST AMBIGIOUS     Y02669
         MVI   XASTSAVE,MSG927I    2ND CODE - SPECIFY DISPLAY ID Y02669
         SPACE 2
D5603D   EQU   *                                                 Y02669
         L     R15,VCON5603        GET ADDRESS OF MESSAGE MODULE Y02669
         BR    R15                 GO ISSUE ERROR MESSAGES       Y02669
        EJECT
***********************************************************************
*                                                                     *
*                           CONSTANTS                                 *
*                                                                     *
***********************************************************************
         SPACE 2
         DS    0F                  ALIGN ON WORD BOUNDARY
B1000    DC    F'1000'             COMPARE FOR MLWTOID OVER 1000  M2772
VCON5603 DC    V(IEE5603D)         MSG MOD                       Y02669
         SPACE 5
***********************************************************************
*                                                                     *
*                             PATCH AREA                              *
*                                                                     *
***********************************************************************
         SPACE 2
PATCH    DC    C'****PATCH AREA****'
PATCH2   DC    C'IEE7803D-TEST ONLY'
PATCH3   DC    36C' '
         EJECT
         IEECUCM  FORMAT=NEW
         EJECT
         IHACTM CQE                                              Y01939
         EJECT
         IHAWQE DSECT=YES                                        YM2304
         EJECT
FAUXSA   DSECT
         IEEXSA
         EJECT
CVTSECT  DSECT
         CVT
         EJECT
         IHAPSA
         END
