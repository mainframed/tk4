 TITLE 'IEE7303D - CONSOLE INFORMATION MESSAGE ROUTINE #2'
IEE7303D CSECT
***********************************************************************
* MODULE NAME      = IEE7303D
*   CSECT NAME     = IEE7303D
*
* LOAD MODULE NAME = IEE3603D
*
* DESCRIPTIVE NAME = CONSOLE INFORMATIONAL MESSAGE ROUTINE
*
* COPYRIGHT        = MISSING
*
* STATUS           = OS/VS/2 RELEASE 3                         @Z30LPTJ
*
* FUNCTION         = CONSTRUCTS A MESSAGE DISPLAYING THE CURRENT
*                    STATUS OF THE CONSOLE
*
* OPERATION        = AFTER A DEVICE HAS BEEN CHANGED INTO THE
*                    CONSOLE STATE, THIS ROUTINE IN CONJUNCTION
*                    WITH IEE4803D BUILDS AND SENDS A MESSAGE
*                    TO THE OPERATOR DISPLAYING THE CURRENT STATUS
*                    OF THE NEW CONSOLE DEVICE
*                    THE MESSAGE CONTAINS THE PHYSICAL DEVICE
*                    ADDRESS OF THE CONSOLE AND ITS ALTERNATE CONSOLE
*                    AND WHETHER THE CONSOLE IS ACTIVE AND/OR
*                    THE MASTER CONSOLE.  IT ALSO HAS THE ROUT CODES
*                    AND COMMAND AUTHORITY OF THE NEW CONSOLE.
*                    THIS RTN IS CALLED REPEATEDLY BY 4803D, AS
*                    MANY TIMES AS THERE ARE UNITS ON THE CMD.
*
* NOTES
*   DEPENDENCIES   = NONE
*   CHARACTER/CODE DEPENDENCIES
*                  = THIS MODULE IS DEPENDENT ON THE EBCDIC
*                    REPRESENTATION OF DECIMAL DIGITS
*
* RESTRICTIONS     = SEE ENTRY POINTS - INPUT DATA
*   REGISTER CONVENTIONS
*                  = REGISTERS ARE DEFINED AT LABEL: DEFREG
*   PATCH LABEL    = PATCH
*
* MODULE TYPE      = CSECT
*   PROCESSOR      = ASSEMBLER
*   MODULE SIZE    = VS/2 = '356'                              @ZM30445
*   ATTRIBUTES     = REENTRANT, REFRESHABLE, REUSABLE,
*                    ZERO PSW PROTECT KEY
*
* ENTRY POINTS     = IEE7303D
*                    FROM IEE4803D
*   PURPOSE        = TO ISSUE THE INFORMATIONAL CONSOLE MESSAGE
*                    AND ALSO THE HARDCPY MESSAGE IF THE CONSOLE
*                    IS ACTING AS THE HARDCPY DEVICE AS WELL
*   LINKAGE        = BRANCH
*   INPUT          = REG2 PTS TO THE DUMMY XSA
*                    REG0 HAS COMPOSITE UCME PTR OR 0 IF NONE
*                    REG4 HAS THE PROCESS BITS
*                    REG5 PTS TO UCME OF UNIT OR I-UNIT IF COMP.
*                    REG 13 PTS TO THE SAVE AREA, WORD 17 OF THE
*                       SAVE AREA CONTAINS THE PTR TO THE CSCB
*                    REG1 PTS TO THE MESSAGE CORE GOTTEN BY
*                    IEE4903D ( WHO ISSUE THE FIRST INFORMATIONAL
*                    HEADER MSG).
*                    XAR OF THE DUMMY XSA HAS THE PROCESS SWITCHES
*                    XAL OF THE DUMMY XSA HAS THE POINTER TO THE
*                       PARMS OF THE COMMAND
*                    XRESV MAY HAVE BIT1 SET TO INDICATE IMMEDIATE
*                    BRANCH TO LABEL: CRT2
*                    REG9 POINTS TO THE UNIT
*                    REG11 WILL BE ZERO UNLESS HARDCPY MSG IS NEEDED.
*                    THEN, IT WILL CONTAIN THE POINTER TO THE UCME
*                    OF THE HARDCPY UNIT
*                    REG12 CONTAINS THE ADDRESS OF THE MSG AREA
*                    XAP CONTAINS THE MLWTO ID AND  THE GRAPHICS
*                    INDICATOR.
*   REGISTERS SAVED  = ALL
*   REGISTER USAGE = REG1    PTR TO WTO CORE ON INPUT
*                    REG2    PTR TO  THE DUMMY XSA
*                    REG3    ADDRESSABILITY
*                    REG4    PROCESS BITS
*                    REG5    UCME PTR
*                    REG10   UCM PTR
*                    REG11   HCPY OF NOHCPY MSG SWITCH
*                    REG12   MSG ADDRESSABILITY
*                    REG13   SAVE AREA PTR
*                    REG14   COMPOSITE SWITCH
*   REGISTERS RESTORED
*                  = ALL
*
* EXITS - NORMAL-1-= TO IEE4803D VIA BRANCH
*   CONDITIONS     = IF A HARDCPY MESSAGE IS STILL REQUIRED
*   OUTPUT         = REG2 PTS TO THE DUMMY XSA
*                    REG13 PTS TO THE SAVE AREA
*                    REG1 PTS TO THE MESSAGE CORE GOTTEN BY 4903D
*                    REG11 HAS THE INDICATOR FOR THE HARDCPY MSG
*                    XRESV OF THE XSA WILL HAVE THE HIGH ORDER BIT
*                       SET TO IMMEDIATELY BRANCH TO LABEL: CENDCHK
*                    REG4 CONTAINS THE CURRENT PROCESS SWITCHES
*   RETURN CODES   = NONE
*
* EXITS - NORMAL-2-  = RETURN TO CALLER VIA A BR 14 AFTER THE CSCB
*                    HAS BEEN FREED VIA AN MGCR MACRO AND THE
*                    DUMMY XSA GOTTEN BY 3603D, AND THE SAVE AREA
*                    GOTTEN BY 3303D ARE FREED.
*   CONDITIONS     = ALL MESSAGES ARE ISSUED, THE COMMAND IS
*                    COMPLETED
*   OUTPUT         =  MESSAGES HAVE BEEN SENT AND ALL CORE HAS BEEN
*                    FREED.
*   RETURN CODES   = NONE
*
* EXITS - ERROR    = NONE
*
* EXTERNAL REFERENCES
*   ROUTINES       = IEE4803D
*   DATA AREAS     = SEE EXITS - OUTPUT DATA
*
* CONTROL BLOCKS   = 1. DUMMY EXTENDED AREA (XSA)
*                    2. DISPLAY CONTROL MODULE (DCM)
*                       FIELDS TESTED -  DCMTOPAR
*                       FIEDLS UPDATED - NONE
*                    3. CVT
*                       FIELDS TESTED  - CVTCUCB
*                       FIELDS UPDATED - NONE
*                    4. UNIT CONTROL MODULE (UCM)
*                       FIELDS TESTED  - UCMDISP- BIT=UCMDISPC
*                                                     UCMDISPG
*                                        UCMID
*                                        UCMRTCD
*                                        UCMXB
*                       FIELDS UPDATED - NONE
*                    5. COMMAND SCHEDULING CONTROL BLOCK (CSCB)
*                       FIELDS TESTED  - NONE
*                       FIELDS UPDATED - CHSTS - BIT= CHFC
*
* TABLES           = MESSAGE BUFFER GOTTEN BY IEE4903D
*
* MACROS           = MGCR,FREEMAIN,WTO,DEQ,SETLOCK
*
* MAPPING MACROS   = IEEXSA
*                    CVT
*                    IEECUCM
*                    IEERDCM
*                    IEECHAIN
*                    IHAPSA
*   SERIALIZATION  = PROTECTION AGAINST COMM. TASK IS ACCOMPLISHED
*                    VIA THE CMS AND LOCAL LOCKS OBTAINED AT:
*                        CSETLOCK IN MODULE IEE4803D
*                    AND RELEASED IN THIS MODULE AT:
*                        UNSTLOCK
*                    PROTECTION AGAINST OLTEP, VARY/UNLOAD AND
*                    ALLOCATION IS ACCOMPLISHED VIA THE ENQ
*                    ISSUE IN MODULE IEE3603D.
*                    A DEQ IS ISSUED IN THIS MODULE AT: CDEQUE
*                    IF THE COMMAND HAS A TERMINAL EXIT FROM HERE.
*                    OTHERWISE, THE DEQ WILL BE ISSUED IN 4803D
*                    WHERE THAT MODULE WILL EXIT IF NOT FROM HERE.
*
* CHANGE LEVEL     = Y02669, Y02651, Z30LPTJ, ZM30445          @ZM30445
*
* CHANGE ACTIVITY  = N/A
*
* MESSAGES         = REFER TO PROLOGUE OF IEE4903D FOR FULL INFORMATION
*
* ABEND CODES      = NONE
*
* PACKAGING        = IEE7303D WILL BE PART OF LOAD MODULE
*                       IEE3603D
*                    THIS LOAD MODULE ALIASES ARE
*                       NONE
*                    THIS LOAD MODULE ENTRY POINTS ARE
*                       IEE3603D
*                    MODULE WILL RESIDE IN: LPALIB
*
* SYSGEN           = SUPPLIED BY THE FOLLOWING SECTIONS:
*                       LOAD MODULE NAME
*                       PACKAGING
*                    THIS CSECT WILL BE INCLUDED AT SYSGEN FROM
*                    DLIB -AOSB3- BY MACRO -SGIEF441-
*
* SYSTEM LIBRARIES = NONE
*
***********************************************************************
         EJECT
*                                                                 19084
***********************************************************************
*                                                                 19084
*        REGISTER EQUATES                                         19084
*                                                                 19084
***********************************************************************
DEFREG   EQU   *                                                 Y02669
R0       EQU   0                   INPUT = COMPOSITE SWITCH      Y02669
R1       EQU   1                   IN-PTR TO WTO CORE            Y02669
R2       EQU   2                   XSA                            19084
R3       EQU   3                   ADDRESSABILITY                 19084
R4       EQU   4                   PROCESS BITS
R5       EQU   5                   UCME PTR
R6       EQU   6                                                  19084
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10                  UCM                            19084
R11      EQU   11                  HCPY OR NOHCPY MSG SWITCH      19084
R12      EQU   12                  MSG ADDRESSABILITY             19084
R13      EQU   13                  SAVE AREA PTR                 Y02669
R14      EQU   14                  USED AS COMPOSITE SWITCH      Y02669
R15      EQU   15                                                 19084
*                                                                 19084
         SPACE 10
***********************************************************************
*                                                                 19084
*        EQUATES                                                  19084
*                                                                 19084
***********************************************************************
ZERO     EQU   0                                                  19084
ONE      EQU   1                                                  19084
TWO      EQU   2                                                  19084
FOR      EQU   4
TRE      EQU   3                                                  19084
FIV      EQU   5                                                  19084
FIFTN    EQU   15                                                 19084
D64      EQU   64                                                Y02669
D132     EQU   132                                               Y02669
D68      EQU   68                                                Y02669
SVN      EQU   7
*
DASH     EQU   C'-'                                               19084
COMMA    EQU   C','                                               19084
FOXES    EQU   X'FF'                                              19084
FOXZERO  EQU   X'F0'                                              19084
NONE     EQU   X'00'                   NO Z FIELD                 21002
JUSTA    EQU   C'A'                    NO END OF AREA FIELD       21002
ZONEBIT  EQU   X'0F'                   UNPACK CONSOLE ID          21002
LOWORDR  EQU   8                  SHIFT MLWTO TO HI ORDER BITS    21002
EIT      EQU   8                       LENGTH OF ELINE            21002
BLNK   EQU     X'40'                   BLANK                      21002
ELEVEN   EQU   11                                                 21002
TEN      EQU   10                                                 21002
DISPLAY1 EQU   C'Z'                    CHARACTER Z FOR MESSAGE    21002
DISPLAY2 EQU   C','                    COMMA FOR AREA FIELD       21002
DISPLAY3 EQU   C'A'                    CHARACTER A FOR MSG        21002
DISPLAY4 EQU   C'-'                    DASH SEPARATOR FOR MSG     21002
*                                                                 19084
*        LENGTH AND SHIFT EQUATES                                 19084
SHIFT1   EQU   1                                                  19084
GRPX     EQU   X'FF'                   GRAPHICS INDICATOR         21002
*                                                                 19084
RI       EQU   X'80'                                           @ZM30445
         EJECT                                                    19084
***********************************************************************
*                                                                Y02669
*        CONSOLE MESSAGE PROCESSOR                               Y02669
*                                                                Y02669
***********************************************************************
*                                                                Y02669
         SPACE 3
         BALR  R3,ZERO                                            19084
         USING *,R3                                               19084
         MODID BR=YES                                            Y01886
         USING XSA,R2                                             19084
         USING CMESSAGE,R12        ADDRESS MSG AREA               19084
*                                                                Y02669
         STM   R0,R15,ZERO(R13)                                  Y02669
*  AS OF NOW THE FOLLOWING REGISTERS HAVE THE FOLLOWING CONTENT  Y02669
*  R0  = COMPOSITE (O-UNIT) UCME PTR OR IS 0                     Y02669
*  R1  = MSG AREA POINTER                                        Y02669
*  R2  = DUMMY XSA POINTER                                       Y02669
*  R4  = CURRENT PROCESS BITS                                    Y02669
*  R5  = UNIT OR I-UNIT (IF COMP) UCME POINTER                   Y02669
*  R9  = CURRENT UNITNAME POINTER                                Y02669
*  R11 = HCPY INDICATOR OR 0 IF NONE REQUIRED                    Y02669
*  R12 = MSG AREA POINTER = R1                                   Y02669
*  R13 = SAVE AREA POINTER                                       Y02669
*  R14 = RETURN ADDRESS                                          Y02669
*                                                                Y02669
         LR    R14,R0              SAVE COMPOSITE SWITCH         Y02669
         L     R10,CVTPTR                                         19084
         USING XCVT,R10                                           19084
         L     R10,CVTCUCB         GET PTR TO UCM                 19084
         DROP  R10                                                19084
         USING UCM,R10                                           Y02669
         USING UCMLIST,R5                                         19084
***********************************************************************
*          CONSOLE ID AND DISPLAY AREA ID                        21002*
***********************************************************************
         LTR   R14,R14                 IS THIS A COMPOSITE CONSL Y02669
         BZ    NOCOMP                  NO                         21002
         LR    R5,R14                  USE UCME FOR COMPOSITE    Y02669
NOCOMP   EQU   *                                                  21002
*
*  REG5  HAS UCME POINTER TO UNIT AS PASSED BY 4803D OR
*        UCME POINTER OF THE O-UNIT OF A COMP IF THIS IS A COMP
*
         SR    R8,R8              CLEAR REGISTER FOR CONSOLE ID   21002
         IC    R8,UCMID                CONSOLE ID                 21002
         CVD   R8,XMSGWK+FOR           CONVERT TO DECIMAL         21002
         OI    XMSGWK+ELEVEN,ZONEBIT                              21002
         UNPK  XMSGID(TRE),XMSGWK+TEN(TWO)                        21002
         MVI   XMSGID,BLNK         BLANK OUT FIRST BYTE OF AREA   21002
         SPACE
***********************************************************************
* IF XAX + 3 CONTAINS A X'FF' THERE IS AN AREA FIELD.  AT LEAST   21002
* ONE CONSOLE BEING VARIED IS A GRAPHICS CONSOLE.  IF THIS UNIT  21002*
* IS NOT A GRAPHICS CONSOLE, THE FIELD REMAINS BLANK.            21002*
***********************************************************************
         SPACE
         CLI   XAP+TRE,GRPX            IS THIS CONSOLE GRAPHICS  Y02669
         BNE   NOGRPX                  NO, NO AREA FIELD          21002
         TM    UCMDISP,UCMDISPC        IS THIS CONSOLE GRAPHICS   21002
         BNO   RTCDE                   BLANK AREA FIELD           21002
         TM    UCMDISP,UCMDISPG        GRPX STATUS DISPLAY ONLY   21002
         BO    CKAREA                  YES, NO Z IN AREA FIELD    21002
         MVI   XMSGDID,DISPLAY1        MOVE IN Z FOR MSG AREA     21002
CKAREA   EQU   *                                                  21002
         L     R8,UCMXB                ADDRESS OF DCM             21002
         USING DCMRDST,R8                                         21002
         CLI   DCMTOPAR,NONE           IF THERE IS A ZERO         21002
         BE    RTCDE                   NO A FIELD                 21002
         TM    UCMDISP,UCMDISPG        SHOULD COMMA BE INSERTED   21002
         BO    NOZ                     NO, NO Z FIELD             21002
         MVI   XMSGDID+ONE,DISPLAY2    MOVE IN COMMA              21002
NOZ      EQU   *                                                  21002
         MVI   XMSGDID+TWO,DISPLAY3    MOVE IN BEGINNING OF DISPLAY
* THE PREVIOUS STATEMENT WAS ADDED FOR SDS                        21002
         CLI   DCMTOPAR,JUSTA          IF THERE IS AN A           21002
         BE    RTCDE                   NO END OF AREA FIELD       21002
         MVI   XMSGDID+TRE,DISPLAY4    MOVE IN SEPARATOR          21002
         MVC   XMSGDID+FOR(ONE),DCMTOPAR       INSERT END FIELD   21002
         DROP  R8                                                 21002
         B     RTCDE                   DSECT POINTER IS RIGHT     21002
NOGRPX   EQU   *                                                  21002
         SH    R12,AREAOFS             CORRECT OFFSET INTO MSG DSECT
*    THE PREVIOUS STATEMENT WAS ADDED FOR SDS                     21002
RTCDE    EQU   *                                                  21002
         TM    XARESV,XACRT2    IS THIS A RETURN FROM IEE4803D @Z30LPTJ
*     THE PREVIOUS STATEMENT WAS ADDED FOR SDS                    21002
         BO    CRT2                    YES, BRANCH TO CRT2       Y02669
***********************************************************************
*                                                                 19084
*        ROUTING CODE  DETERMINATION                              19084
*                                                                 19084
***********************************************************************
*                                                                 19084
CRTCHK   EQU   *                                                  19084
         LH    R6,UCMRTCD          ASSUME 10 UNIT, PICK UP RT CODE19084
*  ROUTS ARE PICKED UP BY USING REG5 AS A POINTER TO THE UCME.   Y02669
*  R5 IS THE UCME OF THE UNIT (XXX OR O-XXX)                     Y02669
*  OR THE UCME OF THE O-UNIT IF THIS IS A COMPOSITE.             Y02669
*                                                                 19084
CRT2     EQU   *                                                  19084
          SPACE 10
**************************************************               Y02651
*                                                                Y02651
*  LOCKS ARE NO LONGER NEEDED AND THEY MUST BE RELEASED. VERY    Y02651
*  SHORTLY A WTO MACRO WILL BE ISSUED.                           Y02651
*  THESE LOCKS WERE OBTAINED AT CSETLOCK IN MODULE IEE4803D      Y02651
*                                                                Y02651
UNSTLOCK EQU   *                                                 Y02651
         LR    R7,R13             SAVE AREA POINTER              Y02651
         STM   R11,R14,D68(R7)    SAVE SETLOCK REGS              Y02651
         SETLOCK  RELEASE,TYPE=CMS,RELATED=(IEE4803D(CSETLOCK))  Y02651
         SETLOCK  RELEASE,TYPE=LOCAL,RELATED=(IEE4803D(CSETLOCK))
         LM    R11,R14,D68(R7)                                   Y02651
**************************************************               Y02651
         SPACE 10
         NI    XARESV,FOXES-XACRT2     TURN OFF BRANCH BIT     @Z30LPTJ
         LTR   R6,R6               ANY                            19084
         BZ    CNONERT             NO, ROUT IS -NONE-             19084
         CH    R6,CRTALL           ALL                            19084
         BE    CROUTALL            YES, ROUT IS -ALL-             19084
*                                                                 19084
*        PROCESS INDIVIDUAL ROUTCODES                             19084
*                                                                 19084
*        R5 HAS PTR TO UNTS UCME OR O-UNT'S UCME IF THIS IS COMP Y02669
*        R6 HAS ROUTCODES                                         19084
*        R4 HAS XAR PROCESS BITS                                  19084
*        R14 HAS THE COMPOSITE UCME PTR OR IS 0                  Y02669
*        R10 PTS TO UCM                                           19084
*        R11 HAS INDICATION FOR HCPY MSG                          19084
*        R12 HAS ADDRESSABILITY FOR MSG AREA                      19084
*                                                                 19084
*                                                                 19084
CRTIND   EQU   *                                                  19084
         SLL   R6,FIFTN            SHIFT ROUTS OVER               19084
         LA    R7,CTABLE           R7 PTS TO COMPARE TAB          19084
         LA    R1,XMSGWK           R1 PTS TO WORK AREA            19084
         CLI   XAP+TRE,GRPX           IS THERE A GRAPHICS CONSOLEY02699
         BE    GRPX11                  YES, PTR IS TO WORD BOUND  21002
         LA    R1,ONE(R1)              PTR TO WORD BOUNDARY       21002
GRPX11   EQU   *                                                  21002
NEXT     EQU   *                                                  19084
         SLL   R6,SHIFT1           SHIFT TO HIGH ORDER            19084
         LTR   R6,R6               IS IT ON                       19084
         BZ    NOMORE              NO, NOMORE                     19084
         BNM   NEXT1               NOT THIS ONE                   19084
         MVC   ZERO(TWO,R1),ZERO(R7)    YES, MOVE ROUT TO WORK ARE19084
         LA    R1,TWO(R1)          INDEX TO NEXT FREE SPACE       19084
NEXT1    EQU   *                                                  19084
         LA    R7,TWO(R7)          INDEX TO NEXT ROUT IN TAB      19084
         B     NEXT                GO THECK NEXT ROUT CODE        19084
*                                                                 19084
NOMORE   EQU   *                                                  19084
         MVI   ZERO(R1),FOXES      SET END DELIMITER              19084
         LA    R1,XMSGRTCD         ROUT AREA IN MSG               19084
         LA    R7,XMSGWK           WORK AREA                      19084
         CLI   XAP+TRE,GRPX           IS THERE A GRAPHICS CONSOLEY02669
         BE    GRPX22                  YES, PTR IS TO WORD BOUND  21002
         LA    R7,ONE(R7)              PTR TO WORD BOUNDARY       21002
GRPX22   EQU   *                                                  21002
         MVC   ZERO(TWO,R1),ZERO(R7)    MOVE OVER TO MSG          19084
         CLI   ZERO(R7),FOXZERO    ONE DIGIT                      19084
         BNE   MOVED                    NO, OK                    19084
         MVC   ZERO(ONE,R1),ONE(R7)     YES, ELIMINATE LEADING ZER19084
         BCTR  R1,ZERO                                            19084
MOVED    EQU   *                                                  19084
         LA    R1,TWO(R1)          INDEX PAST ROUT                19084
ENDCHK   EQU   *                                                  19084
         CLI   TWO(R7),FOXES       END OF WORK                    19084
         BE    CISSUE              YES GO ISSUE MSG               19084
         MVI   ZERO(R1),COMMA      NO, DELIMIT ROUT               19084
         LH    R8,ZERO(R7)                                        21002
         MVC   ONE(TWO,R1),TWO(R7) MOVE IN NEXT ROUT INTO MSG     19084
         CH    R8,TAB09                WAS ROUT A 09              21002
         BNE   ENDCHK3             NO, GO ADD ONE                 19084
         AH    R8,FOX7                 YES, ADD SPECIAL ALGORITHM 21002
         B     ENDCHK5             GO CHECK IF                    19084
*                                                                 19084
ENDCHK3  EQU   *                                                  19084
         AH    R8,HONE                 PLUS ONE                   21002
ENDCHK5  EQU   *                                                  19084
         LA    R7,TWO(R7)                                         19084
         CH    R8,ZERO(R7)             IS LINE ROUTS              21002
         BE    SLASH1               YES, DELIMIT WITH A DASH      19084
ENDSLASH EQU   *                                                  19084
         CLI   ONE(R1),FOXZERO     NO, IS IT ONE DIGIT           Y02669
         BE    MOVEOVER            YES, GET RID OF LEADING F0     19084
         LA    R1,ONE(R1)                                         19084
CONT     EQU   *                                                  19084
         LA    R1,TWO(R1)          INDEX TO NEXT FREE SPACE       19084
         B     ENDCHK              GO CHECK ENDINGS               19084
MOVEOVER EQU   *                                                  19084
         MVC   ONE(ONE,R1),TWO(R1) MOKE IT A SINGLE DIGIT         19084
         B     CONT                CONTINUE                       19084
SLASHRTN EQU   *                                                  19084
         LA    R7,TWO(R7)          INDEX TO NEXT DIGIT IN WORK    19084
         CLI   TWO(R7),FOXES       END                            19084
         BE    SLASH6              YES, GO MAKE FINAL PROCEDURES  19084
SLASH1   EQU   *                                                  19084
         MVI   ZERO(R1),DASH       MOVE IN A DASH                 19084
         CH    R8,TAB09                ROUT IS 09                 21002
         BNE   SLASH2              NO                             19084
         AH    R8,FOX7                 YES, ADD SPECIAL ALGORITHM 21002
         B     SLASH4              GO CHECK                       19084
SLASH2   EQU   *                                                  19084
         AH    R8,HONE                 PLUS ONE                   21002
SLASH4   EQU   *                                                  19084
         CH    R8,TWO(R7)              EQUAL                      21002
         BE    SLASHRTN            YES, GO CHECK END              19084
SLASH6   EQU   *                                                  19084
         MVC   ONE(TWO,R1),ZERO(R7)     NO,MOVE IN LAST ROUT CODE 19084
         B     ENDSLASH            GO CHECK END                   19084
*                                                                 19084
*                                                                 19084
CNONERT  EQU   *                                                  19084
         MVC   XMSGRTCD(FOR),CNONE      MOVE IN NONE              19084
         LA    R1,XMSGRTCD+FOR          INDEX TO END              19084
         B     CISSUE                   GO ISSUE                  19084
*                                                                 19084
CROUTALL EQU   *                                                  19084
         MVC   XMSGRTCD(TRE),CALL       MOVE IN ALL               19084
         LA    R1,XMSGRTCD+TRE          INDEX TO END              19084
*                                                                 19084
         EJECT                                                    19084
CISSUE   EQU   *                                                  19084
         L     R0,XAP                  GET MLWTO ID              Y02669
         IC    R0,XAU                  CONSOLE ID                 21002
         TM    XAU,RI               IS THIS FROM INPUT STREAM? @ZM30445
         BNO   XAUIN1               NO, AVOID NEXT INSTRUCTION @ZM30445
         IC    R0,ZEROZERO        YES, INDICATE MASTER CONSOLE @ZM30445
XAUIN1   EQU   *                                               @ZM30445
*        REG0 CONTAINS THE MLWTO ID IN THE THREE HIGH ORDER BYTES 21002
*        AND THE CONSOLE ID IN THE LOW ORDER BYTE                 21002
         CLI   XAP+TRE,GRPX            GRAPHICS INDICATOR SET    Y02669
         BE    GRPXH                   YES                        21002
         AH    R12,AREAOFS             RESET PTR TO MSG DSECT     21002
GRPXH    EQU   *                                                  21002
***********************************************************************
* A DATA LINE IS ISSUED.  THEN A CHECK MADE TO SEE IF ANY MORE   21002*
* UNITS EXIST (R4 IS NOT ZERO).  IF THERE ARE MORE UNITS, RETURN 21002*
* IS MADE TO IEE4803D.  IF THERE ARE NO MORE UNITS, A TEST IS MAD21002*
* TO SEE IF ONE OF THE UNITS WAS THE HARDCOPY DEVICE (R11 WILL CONTAIN*
* THE ADDRESS OF A UCME).  IF THERE WAS A HARDCOPY UNIT, RETURN Y02669*
* TO IEE4803D.  OTHERWISE AN END LINE IS ISSUED AND CONTROL RETURNED  *
* TO THE COMMUNICATION TASK.                                     21002*
***********************************************************************
         SPACE
         MVC   ZERO(FOR,R12),DLINE       MCSFLAGS                 21002
         MVC   ZERO(FOR,R1),DLINE+FIV     LINE TYPE AND LINE NO   21002
         SR    R1,R12                  CALCULATE LENGTH OF        21002
         STC   R1,ONE(R12)             PUT LENGTH IN MSG          21002
         LR    R1,R12                  ADDRESS OF MESSAGE         21002
         WTO   MF=(E,(1))          ISSUE MSG                      19084
*                                                                 19084
         SLL   R1,LOWORDR              MLWTO IN THREE HI ORDER BYT21002
         IC    R1,XAP+TRE              GET GRAPHICS INDICATOR    Y02669
         ST    R1,XAP                  SAVE  NEWMLWTO ID         Y02669
         SLL   R4,SHIFT1               THIS UNIT WAS PROCESSED    21002
         LTR   R4,R4                   ANY MORE UNITS TO PROCESS  21002
         BNZ   CEXIT                   MULTIPLE UNITS             21002
         LTR   R11,R11                 HARDCOPY LINE NEEDED       21002
         BNZ   CEXIT                   YES, RETURN TO 4803D       21002
         SPACE 10                                                 19084
RETURN   EQU   *                                                  21002
         L     R0,XAP                  GET MLWTO ID              Y02669
         IC    R0,XAU                  GET CONSOLE ID             21002
         TM    XAU,RI               IS THIS FROM INPUT STREAM? @ZM30445
         BNO   XAUIN2               NO, AVOID NEXT INSTRUCTION @ZM30445
         IC    R0,ZEROZERO        YES, INDICATE MASTER CONSOLE @ZM30445
XAUIN2   EQU   *                                               @ZM30445
         MVC   ZERO(EIT,R12),ELINE     END LINE                   21002
         LR    R1,R12                  ADDRESS OF BUFFER          21002
         WTO   MF=(E,(1))                                         21002
         SPACE 10
***************************************************              Y02651
*  THIS IS A TERMINAL EXIT AND THEREFORE THE ENQ ISSUED IN       Y02651
*  MODULE IEE3603D WILL BE RELEASED VIA A DEQ ISSUED HERE        Y02651
*                                                                Y02651
CDEQUE   EQU   *                                                 Y02651
         DEQ   MF=(E,IEFDEQUE)                                   Y02651
***************************************************              Y02651
         SPACE 10
         LR    R1,R12                   PTR TO MSG AREA IN 1      19084
         FREEMAIN R,LV=96,A=(1),SP=229                           Y02669
         L     R1,D64(R13)         GET CSCB PTR                  Y02669
         USING CHAIN1,R1                                         Y02669
         OI    CHSTS,CHFC          SET TO FREE SCB               Y02669
         MGCR  (1),CHAIN                                         Y02669
         LM    R0,R15,ZERO(R13)    RESTORE REGS                  Y02669
         LR    R4,R14              SAVE RETURN ADDRESS           Y02669
*                                  FREE SAVE AREA GOTTEN IN 3303DY02669
         LR    R1,R2               PRT TO DUMMY XSA              Y02669
         LA    R0,D132             SIZE OF DUMMY XSA             Y02669
         FREEMAIN R,LV=(0),A=(1)                                 Y02669
*                                  FREEMAIN DUMMY XSA            Y02669
         LR    R14,R4              RESTORE RETURN REG            Y02669
         BR    R14                      RETURN                    19084
         EJECT                                                    19084
CEXIT    EQU   *                                                  21002
         OI    XARESV,XACENDCK       INDICTE BRANCH TO CENDCHK @Z30LPTJ
         MVC   XAX+TRE(TWO),EXITNAME     NAME OF 4803D           Y02669
         LM    R0,R15,ZERO(R13)                                  Y02669
         L     R15,VCON48          GET ADDRESS OF 4803D          Y02669
         BR    R15                                               Y02669
         SPACE
*                                                                Y02669
*  AS OUTPUT THE REGISTER ARE IN THE FOLLOWING STATUS:           Y02669
*  R0  = POINTER TO THE JUST PROCESSED O-UCME OF 0 IF NON-COMP   Y02669
*  R1  = MSG CORE POINTER                                        Y02669
*  R2  = DUMMY XSA POINTER                                       Y02669
*  R4  = CURRENT PROCESS BITS                                    Y02669
*  R5  = POINTER TO JUST PROCESSED UNIT OR I-UNIT UMCE IF COMP   Y02669
*  R9  = POINTER TO CURRENT UNIT NAME                            Y02669
*  R11 = HCPY INDICATOR OR 0                                     Y02669
*  R12 = MSG AREA POINTER                                        Y02669
*  R13 = SAVE AREA POINTER                                       Y02669
*  R14 = RETURN ADDRESS                                          Y02669
*                                                                Y02699
         EJECT
**************************************************               Y02651
*  THE LIST FORM OF THE DEQ MACRO FOLLOWS:                       Y02651
*                                                                Y02651
IEFDEQUE DEQ   (IEFSQCBS,IEFQ4QCB,TWO,SYSTEM,IEFSQCBS,IEFVQCBS,SVN,    *
               SYSTEM),RET=HAVE,MF=L                           @YM5443P
IEFVQCBS DC    C'VARYDEV'
IEFSQCBS DC    C'SYSIEFSD'                                       Y02651
IEFQ4QCB DC    C'Q4'                                             Y02651
**************************************************               Y02651
         EJECT
***********************************************************************
*                                                                 19084
*        DECLARE AREA FOR MODULE                                  19084
*                                                                 19084
***********************************************************************
*                                                                 19084
         DS    0F                                                Y02669
VCON48   DC    V(IEE4803D)                                       Y02669
EXITNAME DC    C'48'                                             Y02669
CNONE    DC    C'NONE'             NO CMD AUTH OR NO ROUTS        19084
SYSLOG   DC    C'SYSLOG'           SEE IF CONSOLE ID IS NEEDED.   21002
CRTALL   DC    X'FFFF'             ALL ROUTS                      19084
FOX7     DC    X'00F7'             ..                             19084
HONE     DC    X'0001'             ..                             19084
CALL     DC    C'ALL'              ALL ROUT AND CMD AUTH          19084
ZEROZERO DC    X'00'                                           @ZM30445
ELINE    WTO   (' ',E),MF=L                                       21002
DLINE    WTO   (' ',D),MF=L                                       21002
AREAOFS  DC    H'7'                    CORRECTION FOR MSG DSECT   21002
         EJECT
***********************************************************************
         DS    0H                                                 19084
CTABLE   DC    C'01'               ROUTCODE COMPARE TABLE         19084
         DC    C'02'                      ---**---                19084
         DC    C'03'                      ---**---                19084
         DC    C'04'                      ---**---                19084
         DC    C'05'                      ---**---                19084
         DC    C'06'                      ---**---                19084
         DC    C'07'                      ---**---                19084
         DC    C'08'                      ---**---                19084
***********************************************************************
TAB09    DC    C'09'                      ---**---                19084
         DC    C'10'                      ---**---                19084
         DC    C'11'                      ---**---                19084
         DC    C'12'                      ---**---                19084
         DC    C'13'                      ---**---                19084
         DC    C'14'                      ---**---                19084
         DC    C'15'                      ---**---                19084
         DC    C'16'                      ---**---                19084
*                                                                 19084
         SPACE 5
PATCH    DC    CL50'***IEE7303D***'
*
         EJECT                                                    19084
CMESSAGE DSECT                                                    19084
XTEXT    DS    1F                      HEADER FIELD               21002
*                                                                 19084
*                                                                 19084
XUNIT1   DS    CL3                 1ST PHUSICAL UNIT ADDRES       19084
XBLANK   DS    CL1                 COMMA                          19084
XUNIT2   DS    CL3                                                19084
XCOMMA   DS    CL1                                                19084
XAUNIT1  DS    CL3                                                19084
XBLK     DS    CL1                                                19084
XAUNIT2  DS    CL3                                                19084
XMSGPAD1 DS    CL1                                                19084
XMSGSTAT DS    CL4                                                19084
XMSGPAD2 DS    CL1                                                19084
XMSGAUTH DS    CL1                                                19084
XMSGPAD3 DS    CL7                     BLANKS                     21002
XMSGID   DS    CL3                     CONSOLE ID                 21002
XMSGPAD4 DS    CL1                     BLANK                      21002
XMSGDID  DS    CL5                     DSIPLAY AREA ID            21002
XMSGPAD5 DS    CL2                     BLANKS                     21002
XMSGRTCD DS    CL8                                                19084
XMSGWK   DS    CL30                                               19084
*                                                                 19084
         EJECT
CXSA     DSECT
         IEEXSA
         EJECT
DCMRDST  DSECT
         IEECRDCM
         EJECT
XCVT     DSECT
         CVT
         EJECT
XUCM     DSECT
         IEECUCM    FORMAT=NEW
         EJECT
CHAIN1   DSECT
         IEECHAIN
         EJECT
         IHAPSA
         END
