 TITLE 'IEE4803D - CONSOLE INFORMATION MESSAGE ROUTINE #1'
IEE4803D CSECT
***********************************************************************
* MODULE NAME      = IEE4803D
*   CSECT NAME     = IEE4803D
*
* LOAD MODULE NAME = IEE3603D
*
* DESCRIPTIVE NAME = CONSOLE INFORMATIONAL MESSAGE ROUTINE
*
* COPYRIGHT        = MISSING
*
* STATUS           = OS/VS/2 RELEASE 3                         @Z30LPTJ
*
* FUNCTION         = THIS MODULE USES THE UCM AND THE UCB TO
*                    CONSTRUCT A MESSAGE DISPLAYING THE CURRENT
*                    STATUS OF THE CONSOLE
*                    THE MESSAGE CONTAINS THE PHYSICAL DEVICE ADDRESS
*                    OF THE CONSOLE AND ITS ALTERNATE, AND WHETHER
*                    THE CONSOLE IS ACTIVE AND/OR THE MASTER CONSOLE.
*
* OPERATION        = ON ENTRY, THIS ROUTINE GETS THE UNIT'S UCM AND
*                    UCB POINTERS AND FILLS THE MESSAGE BUFFER WITH
*                    ALL THE PERTINENT INFORMATION ABOUT THE NEW
*                    CONSOLE SUCH AS ITS COMPOSITE (IF ANY), ITS
*                    ALTERNATE CONSOLE, WHETHER IT IS A HARDCPY
*                    DEVICE OR NOT, ITS ROUT CODES AND ITS COMMAND
*                    COMMAND AUTHORITY.
*                    ALSO INFO SUCH AS WHETHER THE NEW CONSOLE IS
*                    ALREADY ACTIVE AS A CONSOLE OR WHETHER IT
*                    IS PENDING TO CHANGE TO THE CONSOLE STATE AS
*                    SOON AS ANY PREVIOUS WORK THAT IT DID AS A
*                    SYSTEM DEVICE IS COMPLETE.
*                    IT ALSO SHOWS WHETHER THIS IS THE MASTER CONSOLE
*                    WHEN ALL THE INFO HAS BEEN GATHERED AND PLACED
*                    IN THE MESSAGE BUFFER, THIS ROUTINE EXITS TO
*                    IEE7303D TO ACTUALLY ISSUE THE WTO TO THE
*                    OPERATOR.
*                    IF THE ROUTINE WAS ENTERED FROM IEE7303D TO SET
*                    UP THE HARDCPY MESSAGE BECAUSE THE CONSOLE IS
*                    THE SYSTEM HARDCPY DEVICE AS WELL, THEN IT
*                    PRESETS THE MESSAGE TO TELL THE OPERATOR THAT
*                    FACT.
*
* NOTES
*   DEPENDENCIES   = NONE
*   CHARACTER/CODE DEPENDENCIES
*                  = THIS MODULE CAN BE ASSEMBLED WITH ANY NEW
*                    CHARACTER/CODE SET.
*
* RESTRICTIONS     = SEE ENTRY POINTS - INPUT DATA
*   REGISTER CONVENTIONS
*                  = REGISTERS ARE DEFINED AT LABEL: DEFREG
*   PATCH LABEL    = PATCH
*
* MODULE TYPE      = CSECT
*   PROCESSOR      = ASSEMBLER
*   MODULE SIZE    = VS2 = '38E'                               @ZM30445
*   ATTIBUTES      = REENTRANT, REFRESHABLE, REUSABLE,
*                    ZERO PSW PROTECT KEY
*
* ENTRY POINTS     = IEE4803D FROM IEE4903D OR IEE7303D
*   PURPOSE        = TO SET UP THE CONSOLE INFORMATIONAL MESSAGE
*                    WHEN A VARY CONSOLE COMMAND WAS ISSUED
*                    AND TO SET THE HARDCPY MESSAGE IF THAT
*                    NEW CONSOLE IS THE HARDCPY DEVICE AS WELL.
*   LINKAGE        = BRANCH
*   INPUT          = REG13 CONTAINS THE SAVE AREA POINTER
*                    WORD 17 OR THE SAVE AREA HAS THE PTR TO THE CSCB
*                    REG2 CONTAINS THE PTR TO THE DUMMY XSA
*                    XAP HAS THE MLWTO ID IN THE THREE HIGH
*                    ORDER BYTES AND THE GRAPHICS INDICATOR
*                    IN THE LOW BYTE
*                    REG1 POINTS TO THE MESSAGE CORE GOTTEN BY
*                    IEE4903D
*                    THE XAD SWITCHES ARE STORED IN THE DUMMY XSA
*                    XAR OF THE DUMMY XSA HAS THE PROCESS SWITCHES
*                    XAL HAS THE PTR TO THE PARMS OF THE COMMAND
*
*                    IF ENTRY IS FROM IEE7303D THEN THE FOLLOWING
*                    IS ALSO TRUE:
*                    REG11 CONTAINS AN INDICATOR FOR THE HARDCPY MSG
*                    THE XARESV FIELD OF THE DUMMY XSA HAS THE HIGH
*                    ORDER BIT SET TO MEAN THAT A BRANCH HAS TO BE
*                    IMMEDIATELY MADE TO 'CENDCHK'
*                    REG9 POINTS TO THE CURRENT PROCESSED UNITNAME
*   REGISTERS SAVED = ALL
*   REGISTER USAGE  =REG1 ON INPUT IS THE POINTER TO THE MESSAGE CORE
*                    REG2 PTS TO THE DUMMY XSA
*                    REG3 ADDRESSABILITY
*                    REG4 PROCESS BITS
*                    REG5 UCME PTR
*                    REG9 UNIT PTR
*                    REG10 UCM PTR
*                    REG11 HARDCPY OF NOHCPY MSG SWITCH
*                    REG12 MESSAGE ADDRESSABILITY
*                    REG13 SAVE AREA PTR
*                    REG14 COMPOSITE SWITCH
*   REGISTERS RESTORED
*                  = ALL
*
* EXITS - NORMAL-1-= TO IEE7303D VIA BRANCH
*   CONDITIONS     = TO FINISH CONSTRUCTION MESSAGE AND TO ISSUE
*                    IT TO THE OPERATOR
*   OUTPUT         = REG2 PTS TO THE DUMMY XSA
*                    R0 HAS THE COMPOSITE UCME PTR OR IS 0
*                    REG13 PTS TO THE SAVE AREA, WORD17 OF THE
*                    SAVE AREA HAS THE CSCB PTR
*                    REG1 PTS TO THE MESSAGE CORE GOTTEN BY 4903D
*                    THE XAD SWITCHES ARE STORED IN THE DUMMY XSA
*                    XAR OF THE DUMMY XSA HAS THE PROCESS SWITCHES
*                    XAL OF THE DUMMY XSA HAS THE POINTER TO THE
*                    PARMS OF THE COMMAND
*                    XARESV FIELD OF THE DUMMY XSA  HAS BIT1 SET TO
*                    INDICATE TO TAKE AN IMMEDIATE BRANCH TO
*                    LABEL: 'CRT2'
*                    XAP HAS THE MLWTO ID
*                    REG4 HAS THE PROCESS BITS
*                    REG 9 PTS TO THE UNIT
*                    REG11 WILL BE 0 UNLESS THE HARDCPY MSG IS NEEDED
*                    THEN IT WILL CONTAIN THE PTR TO THE UCME OF
*                    THE HARDCPY UNIT
*                    REG12 HAS THE ADDRESS OF THE MESSAGE AREA
*   RETURN CODES   = NONE
*
* EXITS - NORMAL-2- = RETURN TO CALLER
*   CONDITIONS     = ALL NECESSARY MESSAGES HAVE BEEN ISSUED
*   OUTPUT         = THE CSCB CORE IS FREED VIA AN MGCR MACRO
*                    THE DUMMY XSA AND THE SAVE AREA CORE ARE
*                    FREEMAINED
*                    THE MESSAGE BUFFER IS ALSO FREEMAINED
*   RETURN CODES   = NONE
*
* EXITS - ERROR    = NONE
*
* EXTERNAL REFERENCES
*   ROUTINES       = IEE7303D
*   DATA AREAS     = SEE EXITS - OUTPUT DATA
*
* CONTROL BLOCKS   = 1. DUMMY EXTENDED SAVE AREA (XSA)
*                    2. CVT
*                       FIELDS TESTED  - CVTCUCB
*                       FIELDS UPDATED - NONE
*                    3. UNIT CONTROL BLOCK (UCB)
*                       FIELDS TESTED  - SRTESTAT -BIT= SRTEDADI
*                                        UCBNAME
*                       FIELDS UPDATED - NONE
*                    4. UNIT CONTROL MODULE (UCM)
*                       FIELDS TESTED  - UCMALTEN
*                                        UCMAUTH
*                                        UCMCOMPC
*                                        UCMDISP -BIT= UCMDISPB
*                                                      UCMDISPA
*                                                      UCMDISPD
*                                        UCMHRDRT
*                                        UCMSDS1 - BIT=UCMSDS1A
*                                                      UCMSDS1B
*                                        UCMSFLGS - BIT=UCMSYSC
*                                        UCMUCB
*                                        UCMVEA
*                       FIELDS UPDATED - NONE
*                    5. COMMAND SCHEDULING CONTROL BLOCK (CSCB)
*                       FIELDS TESTED  - NONE
*                       FIELDS UPDATED - CHSTS - BIT=CHFC
*
* TABLES           = NONE
*
* MACROS           = MGCR,FREEMAIN,SETLOCK,DEQ
*
* MAPPING MACROS   = IEEXSA
*                    CVT
*                    IEECUCM
*                    IEFCUBOB
*                    IEECHAIN
*                    IHAPSA
*   SERIALIZATION  = PROTECTION FROM COMM. TASK IS ACCOMPLISHED
*                    VIA THE CMS AND LOCAL LOCKS OBTAINED AT:
*                       CSETLOCK IN THIS MODULE
*                    THESE LOCKS ARE RELEASED AT:
*                       UNSTLOCK IN MODULE IEE7303D
*                    PROTECTION AGAINST OLTEP, VARY/UNLOAD AND
*                    ALLOCATION IS ACCOMPLISHED VIA THE ENQ ISSUED IN
*                    MODULE IEE3603D.
*                    A DEQ WILL BE ISSUED AT CDEQUE IN THIS MODULE
*                    IF THE COMAND HAS A TERMINAL EXIT FROM HERE.
*                    OTHERWISE, THE DEQ WILL BE ISSUED IN MODULE
*                    IEE7303D  WHERE THE MODULE WILL EXIT IF NOT
*                    EXITING FROM HERE.
*
* CHANGE LEVEL     = Y02669, Y02651, Z30LPTJ, ZM30445          @ZM30445
*
* CHANGE ACTIVITY  = N/A
*
* MESSAGES         = REFER TO PROLOGUE OF IEE4903D FOR FULL INFO.
*
* ABEND CODES      = NONE
*
* PACKAGING        = IEE4803D WILL BE PART OF LOAD MODULE
*                       IEE3603D
*                    THIS LOAD MODULE ALIASES ARE
*                       NONE
*                    THIS LOAD MODULE ENTRY POINTS ARE
*                       IEE3603D
*                    MODULE WILL RESIDE IN:  LPALIB
*
* SYSGEN           = SUPPLIED BY THE FOLLOWING SECTIONS:
*                       LOAD MODULE NAME
*                       PACKAGING
*                    THIS CSECT WILL BE INCLUDED AT SYSGEN FROM
*                    DLIB -AOSB3- BY MACRO -SGIEF441-
*
* SYSTEM LIBRARIES = NONE
*
***********************************************************************
         EJECT
*                                                                 19084
***********************************************************************
*                                                                 19084
*        REGISTER EQUATES                                         19084
*                                                                 19084
***********************************************************************
DEFREG   EQU   *                                                 Y02669
R0       EQU   0          OUTPUT TO 7303D = COMPOSITE PTR OR 0   Y02669
R1       EQU   1                   IN-PTR TO WTO CORE             19084
R2       EQU   2                   XSA                            19084
R3       EQU   3                   ADDRESSABILITY                Y02669
R4       EQU   4                   PROCESS BITS                  Y02669
R5       EQU   5                   UCME PTR                      Y02669
R6       EQU   6                                                  19084
R7       EQU   7
R8       EQU   8
R9       EQU   9                   UNIT PTR                       19084
R10      EQU   10                  UCM                            19084
R11      EQU   11                  HCPY OR NOHCPY MSG SWITCH      19084
R12      EQU   12                  MSG ADDRESSABILITY             19084
R13      EQU   13                  SAVE AREA PTR
R14      EQU   14                  COMPOSITE SWITCH IN THIS RTN. Y02669
R15      EQU   15                                                 19084
*                                                                 19084
         SPACE 10
***********************************************************************
*                                                                 19084
*        CONSOLE MESSAGE PROCESSOR                                19084
*                                                                 19084
***********************************************************************
*                                                                 19084
         EJECT
         SPACE 10
***********************************************************************
*                                                                 19084
*        EQUATES                                                  19084
*                                                                 19084
***********************************************************************
ZERO     EQU   0                                                  19084
ONE      EQU   1                                                  19084
TWO      EQU   2                                                  19084
TRE      EQU   3                                                  19084
FOR      EQU   4                                                  19084
FIV      EQU   5                                                  19084
SIX       EQU   6                                                 19084
SVN      EQU   7                                                  19084
EIT      EQU   8                                                  19084
TWLV     EQU   12                                                 19084
THIRTY6  EQU   36                                                Y02669
FORTY4   EQU   44                                                Y02669
BLANK    EQU   C' '                                               19084
COMMA    EQU   C','                                               19084
SLASH    EQU   C'/'                                               19084
CHCPY    EQU   C'H'                                               19084
CACTIVE  EQU   C'A'                                               19084
CMSTCONS EQU   C'M'                                               19084
FOXES    EQU   X'FF'                                              19084
L3       EQU   3                                                  19084
L67      EQU   67                                                 19084
SHIFT16  EQU   16                                                 19084
SHIFT1   EQU   1                                                  19084
D64      EQU   64                                                Y02669
D68      EQU   68                                                Y02669
D132     EQU   132                                               Y02669
RI       EQU   X'80'                                           @ZM30445
         EJECT                                                    19084
         BALR  R3,ZERO                                            19084
         USING *,R3                                               19084
         MODID BR=YES                                            Y01886
         USING XSA,R2                                             19084
*
         STM   R0,R15,ZERO(R13)                                  Y02669
         L     R4,XAR              GET PROCESS SWITCHE IN REG 7   19084
         LR    R12,R1              GOTTEN CORE FOR MSG            19084
         USING CMESSAGE,R12        ADDRESS MSG AREA               19084
         L     R10,CVTPTR                                         19084
         USING XCVT,R10                                           19084
         L     R10,CVTCUCB         GET PTR TO UCM                 19084
         DROP  R10                                                19084
         USING UCM,R10
         USING UCMLIST,R5                                         19084
         TM    XARESV,XACENDCK    SHOULD 48 BRANCH TO CENDCHK  @Z30LPTJ
*                                 BECAUSE THIS IS A 2ND ENTRY
         BO    CENDCHK            YES, HARDCOPY OR MULTIPLE UNITS 21002
         L     R9,XAL              GET PTR TO UNITS               19084
         LA    R9,ZERO(R9)         CLEAR HIGH ORDER BYTE          19084
         SR    R11,R11                 HARDCOPY INDICATOR         21002
*                                                                 19084
         TM    XADUSWIT,XAXPAREN   MULTIPLE UNITS              @Z30LPTJ
         BZ    CSINGLE             NO,PTR IS OK                   19084
CPAREN   LA    R9,ONE(R9)          YES, INDEX BY ONE             Y02669
         NI    XADUSWIT,XAXPAREN   OFF,ALL BUT UDVC AND XPAREN @Z30LPTJ
CSINGLE  EQU   *                                                  19084
         CLC   ZERO(TWO,R9),COUNIT UNIT IS O-XXX                  19084
         BNE   CCOMP               NO,MAYBE COMPOSITE             19084
         LA    R9,TWO(R9)          INDEX TO UNIT NAME             19084
         B     CPROCESS            GO PROCESS                     19084
*                                                                 19084
CCOMP    EQU   *                                                  19084
         CLC   ZERO(TRE,R9),CIUNIT IS IT A COMPOSITE              19084
         BNE   CPROCESS            NO, REG UNIT                  Y02669
         OI    XADUSWIT,XAIOCOMP   YES, SET COMP SWITCH        @Z30LPTJ
         LA    R9,TRE(R9)          INDEX TO I-UNIT NAME           19084
*                                                                 19084
         EJECT
         SPACE 10                                                 19084
CPROCESS EQU   *                                                  19084
         LTR   R4,R4               IS PROCESS FLAG ON             19084
         BZ    CHCPYCK                 NO MORE UNITS TO PROCESS   21002
         BNM   CENDCHK                 DON'T PROCESS THIS UNIT    21002
CUCME    EQU   *                                                  21002
         LM    R5,R7,UCMVEA    GET PTR TO FIRST AND LAST UCME     21002
CLOOP    L     R1,UCMUCB               AND UCB                    21002
         USING XUCB,R1                                            21002
         CLC   ZERO(TRE,R9),UCBNAME    IT IS SOUGHT UCME          21002
         BE    CPROC                   YES                        21002
         BXLE  R5,R6,CLOOP             NO, TRY ANOTHER            21002
*                                                                 19084
         EJECT
CPROC    MVI   XTEXT,BLANK         BLANK OUT                      19084
         MVC   XTEXT+ONE(L67),XTEXT AREA                          19084
         USING XUCB,R1                                            19084
         SPACE 10
*************************************************                Y02651
*                                                                Y02651
*  LOCKS MUST BE ACQUIRED NOW TO PROTECT AGAINST ANY BIT CHANGES Y02651
*  BY COMM. TASK                                                 Y02651
*                                                                Y02651
CSETLOCK EQU   *                                                 Y02651
         LR    R8,R13             SAVE AREA POINTER              Y02651
         STM   R11,R14,D68(R8)    SAVE SETLOCK REGS              Y02651
         SETLOCK  OBTAIN,TYPE=LOCAL,MODE=UNCOND,                 Y02651*
               RELATED=(IEE7303D(UNSTLOCK))                      Y02651
         SETLOCK  OBTAIN,TYPE=CMS,MODE=UNCOND,                   Y02651*
               RELATED=(IEE7303D(UNSTLOCK))                      Y02651
         LM    R11,R14,D68(R8)                                   Y02651
*************************************************                Y02651
         SPACE 10
         TM    UCMDISP,UCMDISPB    IS UNIT ALSO HCPY             Y02669
         BZ    CNOHCPY             NO, BRANCH AROUND              19084
         LR    R11,R5              YES, KEEP PTR TO UCME FOR LATER19084
*                                                                 19084
CNOHCPY  EQU   *                                                  19084
         L     R14,UCMCOMPC        PTR TO COMP OR ZEROES IF NONE Y02669
*                                                                 19084
         LTR   R14,R14             ANY COMPOSITE                 Y02669
         BZ    CONEU               NO, SINGLE UNIT                19084
         DROP  R5                                                 19084
         USING UCMLIST,R14                                       Y02669
         TM    UCMDISP,UCMDISPB    MAYBE O-UNIT IS HCPY           19084
         DROP  R14                                               Y02669
         USING UCMLIST,R5                                         19084
         BZ    CMOVE               NO, OK                         19084
         LR    R11,R5              YES,PTR TO UCME OF I-UNIT FOR  19084
*                                      HARDCPY MESSAGE            19084
CMOVE    EQU   *                                                  19084
         MVC   XUNIT1(L3),UCBNAME  MOVE IN 1-UNIT                 19084
         MVI   XBLANK,COMMA        MOVE COMMA                     19084
         L     R1,TWLV(R14)        GET UCB OF O-UNIT             Y02669
CONEU    EQU   *                                                  19084
         MVC   XUNIT2(L3),UCBNAME  MOVE IT IN                     19084
         MVI   XCOMMA,SLASH        MOVE IN SLASH TO DELIMIT ALTERN19084
         L     R6,UCMALTEN         GET ITS PTR                    19084
         DROP  R5                                                 19084
         USING UCMLIST,R6                                         19084
         LTR   R6,R6               HAVE AN ALTERNATE?            Y02669
         BZ    CATTRB              NOPE, A ONE CONS SYSTEM       Y02669
         L     R1,UCMUCB           GET UCB PTR                    19084
         MVC   XAUNIT1(L3),UCBNAME MOVE IN 1-OF ALTCONS           19084
         L     R6,UCMCOMPC         GET COMP OF ALTERNATE          19084
         LTR   R6,R6               ANY                            19084
         BZ    CATTRB              NO,GO CHECK CONS ATTRIBUTES    19084
         MVI   XBLK,COMMA          YES, MOVE DELIMITING COMMA     19084
         L     R1,UCMUCB                                          19084
         MVC   XAUNIT2(L3),UCBNAME MOVE IN O-ALT UNIT             19084
         DROP  R6,R1                                              19084
         USING UCMLIST,R5                                         19084
         SPACE 10                                                 19084
CATTRB   EQU   *                                                  19084
         MVI   XMSGSTAT+ONE,CHCPY  MOVE IN H                      19084
         LTR   R4,R4                    IS THIS THE HARDCOPY MSG  21002
         BZ    HARDAUTH                YES, DETERMINE AUTHORITY   21002
         L     R1,UCMUCB           UCB PTR TO CONSOLE IN CMD      19084
         USING XUCB,R1                                            19084
         MVI   XMSGSTAT+ONE,CACTIVE    IT WILL BE EITHER ACTIVE OR19084
*                                      CHANGING TO ACTIVE         19084
         TM    SRTESTAT,SRTEDADI   IS IT CHANGING TO ACTIVE       19084
         BO    PENDING                 YES,BRANCH                 M5933
         TM    XADUSWIT,XAIOCOMP       COMPOSITE CONSOLE       @Z30LPTJ
         BZ    CMASTER                 NO, THIS IS XXX CONSOLE   Y02669
         L     R1,TWLV(R14)            GET UCB OF O-UNIT         Y02669
         TM    SRTESTAT,SRTEDADI       O-UNIT CHANGING TO ACTIVE  M5933
         BZ    CMASTER                 NO                         M5933
         DROP  R1                                                 M5933
PENDING  MVC   XMSGSTAT+TWO(TWO),CPENG NEW CONS,CHANGING TO ACTIVEM5933
         B     CAUTHCK             GO CHECK CMD AUTH              19084
*                                                                 19084
*                                                                 19084
COONLY   EQU   *                                                  19084
         MVC   XMSGAUTH(FOR),CNONE      MOVE IN NO AUTH           19084
         BR    R15                  GO PROCESS ROUTS              19084
*                                                                 19084
*                                                                 19084
CMASTER  EQU   *                                                  19084
         TM    UCMDISP,UCMDISPA    MASTER                         19084
         BZ    CAUTHCK             NO,GO CHECK AUTH               19084
         MVI   XMSGSTAT+ONE,CMSTCONS    YES, MOVE IN M            19084
*                                                                 19084
         EJECT                                                    19084
CAUTHCK  EQU   *                                                  19084
         LA    R15,CEXIT               EXIT TO IEE7303D          Y02669
         LH    R6,UCMAUTH          GET AUTH IN REG5               19084
         TM    UCMDISP,UCMDISPD        O- ONLY CONSOLE            19084
         BO    COONLY              YES OUTPUT ONLY, AUTH=NONE    Y02669
         MVC   XMSGAUTH(TRE),CALL  ASSUME ALL                     19084
         CH    R6,CCMDALL         IS IT ALL AUTH                  19084
         BCR   EIT,R15             YES,GO TO EXIT TO 73          Y02669
*                                                                 19084
         MVC   XMSGAUTH(FOR),CINFO ASSUME INFO                    19084
         LTR   R6,R6               IS IT INFO                     19084
         BCR   EIT,R15             YES, GO CHECK RTCDS IN 73     Y02669
*                                                                 19084
         SR    R7,R7               NO, INDIVIDUAL AUTH            19084
         LA    R1,XMSGAUTH         PTR TO MSG AREA FOR AUTH       19084
         SLL   R6,SHIFT16          SHIFT AUTH                     19084
         LTR   R6,R6               IS IT ON                       19084
         BNM   CIND2               NO, BUT SOMETHING ELSE ON      19084
         MVC   ZERO(FOR,R1),CSYS   YES, IT IS SYS-MOVE IT IN      19084
         LA    R1,TRE(R1)          INDEX PAST IT                  19084
         BCTR  R7,ZERO             SET SECOND TIME SWITCH         19084
CIND2    EQU   *                                                  19084
         SLL   R6,SHIFT1          SHIFT ONE MORE                  19084
         LTR   R6,R6               IS IT ON                       19084
         BCR   EIT,R15             NO NOMORE, GO CHECK ROUT CODES 19084
         BNM   CIND4               NOT THIS ONE, GO ASSUME CONS   19084
         MVC   ZERO(FOR,R1),CIO+ONE     YES, ITS 10, MOVE IN -10- 19084
         LTR   R7,R7               SECOND TIME                    19084
         BZ    CIND3               NO,OK CHECK NEXT AUTH          19084
         MVC   ZERO(FOR,R1),CIO    YES, MOVE IN -,10-             19084
         BR    R15                 GO CHECK ROUTS IN 7303D       Y02669
*                                                                 19084
CIND3    EQU   *                                                  19084
         BCTR  R7,ZERO             SET SECOND TIME SWITCH         19084
         LA    R1,TWO(R1)          INDEX PAST -10-                19084
         SLL   R6,SHIFT1           SHIFT TO NEXT AUTH             19084
         LTR   R6,R6               ON                             19084
         BCR   EIT,R15             NO, NOMORE                     19084
*                                                                 19084
CIND4    EQU   *                                                  19084
         MVC   ZERO(FOR,R1),CONS+ONE    YES, MOVE IN -CONS-       19084
         LTR   R7,R7               SECOND TIME AROUND             19084
         BCR   EIT,R15             NO, GO CHECK ROUTS             19084
         MVC   ZERO(FIV,R1),CONS   YES, MOVE IN -,CONS-           19084
         BR    R15                     GO CHECK ROUT CODES        21002
         EJECT                                                    19084
*                                                                 19084
CENDCHK  EQU   *                                                  19084
         NI    XARESV,FOXES-XACENDCK   TURN OFF ENTRY BIT      @Z30LPTJ
         SLL   R4,SHIFT1           PROCESS BITS TO NEXT UNIT      19084
         ST    R4,XAR                  SAVE NEW PATTERN FOR       21002
*                                      MULTIPLE UNITS             21002
         LA    R9,TRE(R9)          INDEX TO NEXT UNIT             19084
         TM    XADUSWIT,XAIOCOMP   COMPOSITE SPECIFIED         @Z30LPTJ
         BZ    CMULT               GO CHECK IF MORE UNITS         19084
         LA    R9,SVN(R9)          INDEX PAST COMP UNIT          Y02669
CMULT    EQU   *                                                  19084
         TM    XADUSWIT,XAXPAREN   YES, MULTIPLE UNITS EXISTS? @Z30LPTJ
         BO    CPAREN              YES, CONTINUE                  19084
*                                                                 19084
CHCPYCK  EQU   *                                                  19084
         LTR   R11,R11             ANY HCPY MSG                   19084
         BZ    CFREE               NO, GO FINISH UP               19084
         LR    R5,R11              GET UCME OF HCPY INTO R5       19084
         L     R1,UCMUCB                                          19084
         B     CPROC               GO TO ISSUE AND BUILD MSG      19084
HARDAUTH EQU   *                                                  19084
         SR    R11,R11                 HARDCOPY MSG PROCESSED     21002
         LR    R6,R10              UCM PTR                        19084
         SH    R6,FOUR             MINUS 4 IS PTR TO PFX          19084
         L     R6,ZERO(R6)         GET PFX ADDR                   19084
         USING UCMPRFX,R6                                         19084
         MVC   XMSGAUTH(SIX),CNO       ASSUME NOCMDS              21002
         TM    UCMSDS1,UCMSDS1A        IS THIS STCMDS             21002
         BZ    HARDATH1                NO, TEST FOR INCMDS        21002
         MVC   XMSGAUTH(TWO),STCMDS    INDICATE STCMDS            21002
         B     HARDROUT                INDICATE CMDS ARE CHANGING 21002
HARDATH1 EQU   *                                                  21002
         TM    UCMSDS1,UCMSDS1B        IS THIS INCMDS             21002
         BZ    HARDATH2                CHECK FOR CMDS             21002
         MVC   XMSGAUTH(TWO),INCMDS    INDICATE INCMDS            21002
         B     HARDROUT                CONTINUE                   21002
HARDATH2 EQU   *                                                  21002
         TM    UCMSFLGS,UCMSYSC        IS THIS CMDS               21002
         BZ    HARDROUT                                           21002
         MVC   XMSGAUTH(SIX),CMDS      MOVE IN CMDS               21002
HARDROUT EQU   *                                                  19084
         LH    R6,UCMHRDRT             GET ROUTS OF HARCPY        21002
         DROP  R6                                                 19084
         OI    XARESV,XACRT2     INDICATE BRANCH TO CRT2 IN 73 @Z30LPTJ
CEXIT    EQU   *                                                  21002
         ST    R14,ZERO(R13)     REG0 SPOT - COMPOSITE SWITCH OR0Y02669
         STM   R4,R6,SHIFT16(R13) REG 4 HAS CURRENT PROCESS BITS Y02669
*                                 REG5 HAS UCME POINTER OF UNIT O
*                                 OR I-UNIT IF THIS IS COMPOSITE
*                                 REG6 HAS HARDCPY DEV ROUTS IF  Y02669
*                                 THIS IS THE HARDCPY MSG OR IS  Y02669
*                                  N/A IF THIS IS THE CONSOLE MSGY02669
         ST    R9,THIRTY6(R13)    REG9 HAS THE CURRENT UNIT PTR  Y02669
         STM   R11,R13,FORTY4(R13)   REG11 HAS HARDCPY INDICATOR Y02669
*                                 REG12 HAS MESSAGE AREA POINTER Y02669
*                                 REG13 HAS SAVE AREA POINTER    Y02669
         MVC   XAX+TRE(TWO),EXITNAME         IEE7303D            Y02669
*                                                                Y02669
         LM    R0,R15,ZERO(R13)   PRESET ALL REGS FOR 7303D      Y02669
         L     R15,VCON73                                        Y02669
         BR    R15            BRANCH TO 7303D                    Y02669
*                                                                Y02669
*         R0  = PRESET FOR COMPOSITE SWITCH                      Y02669
*         R4  = PROCESS BITS                                     Y02669
*         R1  = MSG POINTER                                      Y02669
*         R2  = DUMMY XSA POINTER                                Y02669
*         R5  = UCME PTR OF UNIT OR I-UNIT IF THIS IS COMP       Y02669
*         R9  = UNIT POINTER                                     Y02669
*         R11 = HARDCPY MSG INDICATOR OR 0 IF NOT NEEDED         Y02669
*         R12 = MESSAGE AREA POINTER                             Y02669
*         R13 = SAVE AREA POINTER                                Y02669
*         R14 = RETURN ADDRESS                                   Y02669
*         R15 = ADDRESS OF 7303D                                 Y02669
*                                                                Y02669
         EJECT
CFREE    EQU   *                                                 Y02669
         L     R0,XAP                  MLWTO ID                   21002
         IC    R0,XAU             CONSOLE ID IN LOW ORDER BYTE    21002
         TM    XAU,RI             IS THIS FROM INPUT STREAM?   @ZM30445
         BNO   XAUIN              NO, AVOID NEXT INSTRUCTION   @ZM30445
         IC    R0,ZEROZERO        YES, INDICATE MASTER CONSOLE @ZM30445
XAUIN    EQU   *                                               @ZM30445
         MVC   ZERO(8,R12),ELINE       LAST LINE OF MLWTO         21002
         LR    R1,R12                  ADDRESS OF MESSAGE         21002
         WTO   MF=(E,(1))
         SPACE 10
**************************************************               Y02651
*                                                                Y02651
*  THIS IS A TERMINAL EXIT AND THEREFORE THE ENQ ORIGINALLY      Y02651
*  ISSUE IN MODULE IEE3603D MUST BE DEQUEUED OF.                 Y02651
*                                                                Y02651
CDEQUE   EQU   *                                                 Y02651
         DEQ   MF=(E,IEFDEQUE)                                   Y02651
*                                                                Y02651
**************************************************               Y02651
         SPACE 10
         LR    R1,R12                   PTR TO MSG AREA IN 1      19084
         FREEMAIN R,LV=96,A=(1),SP=229                           Y02669
         L     R1,D64(R13)         CSCB PTR                      Y02669
         USING CHAIN1,R1                                         Y02669
         OI    CHSTS,CHFC          SET TO FREE CSCB              Y02669
         MGCR  (1),CHAIN                                         Y02669
         LM    R0,R15,ZERO(R13)    RESTORE REGS                  Y02669
         LR    R4,R14              SAVE RETURN ADDRESS           Y02669
*                                  FREE SAVE AREA GOTTEN IN 3303DY02669
         LR    R1,R2               DUMMY XSA POINTER             Y02669
         LA    R0,D132             DUMMYXSA SIZE AND SVAREA SIZE Y02669
         FREEMAIN R,LV=(0),A=(1)                                 Y02669
*                                  FREE DUMMY XSA GOTTEN IN 3603DY02669
         LR    R14,R4              RESTORE RETURN REG            Y02669
         BR    R14                      RETURN                    19084
         EJECT
**************************************************               Y02651
*  DEQ LIST FORM FOLLOWS:                                        Y02651
*                                                                Y02651
IEFDEQUE DEQ   (IEFSQCBS,IEFQ4QCB,TWO,SYSTEM,IEFSQCBS,IEFVQCBS,SVN,    *
               SYSTEM),RET=HAVE,MF=L                           @YM5443P
IEFVQCBS DC    C'VARYDEV'
IEFSQCBS DC    C'SYSIEFSD'                                       Y02651
IEFQ4QCB DC    C'Q4'                                             Y02651
**************************************************               Y02651
         EJECT
***********************************************************************
*                                                                 19084
*        DECLARE AREA FOR MODULE                                  19084
*                                                                 19084
***********************************************************************
*                                                                 19084
         DS    0F                                                Y02669
VCON73   DC    V(IEE7303D)                                       Y02669
EXITNAME DC    C'73'                                             Y02669
CNO      DC    C'NO'               HARDCPY NOCMDS OR CMDS- DO NOT 19084
CMDS     DC    C'CMDS  '                                          21002
INCMDS   DC    C'IN'                   INCMDS                     21002
STCMDS   DC    C'ST'                   STCMDS                     21002
CNONE    DC    C'NONE'             NO CMD AUTH OR NO ROUTS        19084
FOUR     DC    H'4'                CONSTANT TO GET PFX PTR        19084
FOX7     DC    X'00F7'             ..                             19084
CCMDALL  DC    X'E000'             ALL CMD AUTH                   19084
CINFO    DC    C'INFO'             INFO COMD AUTH                 19084
CSYS      DC    C'SYS '              CMD AUTH OF 1                19084
CIO      DC    C',IO  '            CMD AUTH OF 2                  19084
CONS     DC    C',CONS'            CMD AUTH OF 3                  19084
CALL     DC    C'ALL'              ALL ROUT AND CMD AUTH          19084
COUNIT   DC    C'O-'               OUTPUT ONLY UNIT               19084
CIUNIT   DC    C'(I-'              BEGINNING OF COMPOSITE         19084
CPENG    DC    C',P'               PENDING TO GO TO CONSOLE       19084
OLTTEST  DC    C'N,T'                  DEVICE IS NOT ACTIVE AND   21051
*                                      UNDER TEST BY OLTEP        21051
ZEROZERO DC    X'00'                                           @ZM30445
ELINE    WTO   (,E),MF=L                                          21002
         SPACE 5
PATCH    DC    CL50'***IEE4803D***'                            @Z30LPTJ
         EJECT                                                    19084
CMESSAGE DSECT                                                    19084
XMSGHDR  DS    1F                      HEADER FIELD FOR MLWTO     21002
XTEXT    EQU   *                                                  19084
*                                                                 19084
*                                                                 19084
XUNIT1   DS    CL3                 1ST PHUSICAL UNIT ADDRES       19084
XBLANK   DS    CL1                 COMMA                          19084
XUNIT2   DS    CL3                                                19084
XCOMMA   DS    CL1                                                19084
XAUNIT1  DS    CL3                                                19084
XBLK     DS    CL1                                                19084
XAUNIT2  DS    CL3                                                19084
XMSGPAD1 DS    CL1                                                19084
XMSGSTAT DS    CL4                                                19084
XMSGPAD2 DS    CL1                                                19084
XMSGAUTH DS    CL1                                                19084
XMSGPAD3 DS    CL7                     BLANKS                     21002
XMSGID   DS    CL3                     CONSOLE ID                 21002
XMSGPAD4 DS    CL1                     BLANK                      21002
XMSGDID  DS    CL5                     DISPLAY AREA ID            21002
XMSGPAD5 DS    CL2                     BLANKS                     21002
XMSGRTCD DS    CL8                                                19084
XMSGWK   DS    CL30                                               19084
*                                                                 19084
         EJECT
CXSA     DSECT
         IEEXSA
         EJECT
XCVT     DSECT
         CVT
         EJECT
XUCM     DSECT
         IEECUCM   FORMAT=NEW
         EJECT
XUCB     DSECT
         IEFUCBOB
         EJECT
CHAIN1   DSECT
         IEECHAIN
         EJECT
         IHAPSA
         END
