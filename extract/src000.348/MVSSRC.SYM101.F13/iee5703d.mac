 TITLE 'IEE5703D - VARY HARDCPY,OFF AND HARDCPY MESSAGE ROUTINE'
IEE5703D CSECT
***********************************************************************
*
* MODULE NAME      = IEE5703D
*    CSECT NAME    = IEE5703D
*
* LOAD MODULE NAME = IGC0003D
*
* DESCRIPTIVE NAME = VARY HARDCPY,OFF  PROCESSOR
*                    AND
*                    HARDCPY MESSAGE ROUTER
*
* COPYRIGHT        = MISSING
*
* STATUS           = OS/VS/2 RELEASE 3                         @Z30LPTJ
*
* FUNCTION         = 1. THIS MODULE TAKES THE HARDCPY FUNCTION OUT
*                    OF A SYSTEM WHEN THE OPERATOR REQUESTS IT.
*                    IT IS THE RESPONDER TO THE HARDCPY,OFF COMMAND.
*                    2. IF AN ERROR CONDITION HAS BEEN DETECTED IN
*                    THE HARDCPY PROCESSOR OR IN THIS MODULE,
*                    THIS ROUTINE GIVES CONTROL TO THE APPROPRIATE
*                    ERROR MESSAGE MODULE AND PASSES IT THE CORRECT
*                    ERROR CODE.
*
* OPERATION        = THIS ROUTINE CHECKS THE CODE PASSED TO IT IN
*                    THE XAE AND EITHER PROCESSES THE COMMAND OR
*                    SETS UP TO GO TO AN ERROR MESSAGE MODULE.
*                    IN PROCESSING THE COMMAND, IT CHECKS THAT
*                    THE SYSTEM DOES NOT REQUIRE HARDCPY, VALIDATES
*                    THAT THE UNIT SPECIFIED IN THE COMMAND IS THE
*                    CURRENT HARDCPY LOG.  IF NO UNIT WAS SPECIFIED
*                    THAN IT VALIDATES THE PRESENCE OF A HARDCPY
*                    DEVICE.
*                    IF ALL IS CORRECT, THIS ROUTINE SETS THE
*                    APPROPRIATE INFORMATION INDICATING THAT NO
*                    HARDCPY EXISTS AND GOES TO IEE2103D TO ISSUE
*                    A MESSAGE TO THE REQUESTING CONSOLE INFORMING
*                    THE OPERATOR THAT HIS COMMAND WAS ACCEPTED
*                    AND PROCESSED
*
* NOTES
*    DEPENDENCIES  = NONE
*   CHARACTER/CODE DEPENDENCIES
*                  = THIS MODULE CAN BE ASSEMBLED WITH ANY NEW
*                    CHARACTER/CODE SET.
*
* RESTRICTIONS     = REG2 PTS TO THE XSA ON INPUT AND ON OUTPUT
*   REGISTER CONVENTIONS
*                  = REGISTERS ARE DEFINES AT LABEL: DEFREG
*   PATCH LABEL    = PATCH
*
* MODULE TYPE      = CSECT
*   PROCESSOR      = ASSEMBLER
*   MODULE SIZE    = VS/2 = '2BE'                              @Z30LPTJ
*   ATTRIBUTES     = REENTRANT, REUSABLE, REFRESHABLE, PAGED LPA,
*                    ZERO PSW PROTECT KEY, SUPERVISOR MODE
*
* ENTRY POINT      = IEE5703D FROM IEE4703D
*   PURPOSE        = TO PROCESS THE HARDCPY,OFF COMMAND (FROM 4703D)
*                    OR TO ROUTE MSG TO IEE0503D OR IEE2103D FOR
*                    ERROR IN THE V HARDCPY COMMAND (FROM 47 AND 72)
*   LINKAGE        = BRANCH
*   INPUT          = IF CONTROL IS FROM IEE4703D FOR A
*                    V HARDCPY,OFF COMMAND:
*                       R2 PTS TO THE XSA
*                       XAE = X'00'
*                    IF CONTROL IS FROM 4703D FOR ERROR IN THE
*                    V HARDCPY COMMAND:
*                       R2 PTR TO THE XSA
*                       XAE = X'20' = GOTO 0503D -
*                            MSG = UNIDENTIFIABLE KEYWORD
*                       XAE = X'24' - GOTO 2103D
*                            MSG = AUTHORITY INVALID
*                       XAE = X'28' - GOTO 0503D
*                            MSG = INVALID NUMERICS
*                    IF CONTROL IS FROM 7203D FOR ERROR CONDT.
*                       R2 PTS TO THE XSA
*                       XAE = X'12' - GOTO 0503D
*                            MSG = UNIT REF INVALID
*                       XAE = X'16' - GOTO 0503D
*                            MSG = SYSLOG NOT SUPPORTED
*                       XAE = X'20' - GOTO 0503D
*                            MSG = UNIDENTIFIABLE KEYWORD
*                       XAE = X'28' - GOTO 0503D
*                            MSG = INVALID NUMERICS
*   REGISTERS SAVED= NONE
*   REGISTER USAGE = R2 - PTR TO XSA
*                    R3 - ADDRESSABILITY
*                    R4 - UCME PTR
*                    R7 - EOB PTR
*                    R9 - UNIT PTR
*                    R10 - UCM REG
*                    R11 - UCMPRFX PTR
*                    R14 - RETURN ADDRESS
*   REGISTERS RESTORED
*                  = NONE
*
* EXIT - NORMAL    = TO IEE2103D VIA BRANCH
*   CONDITIONS     = HARDCPY,OFF COMMAND WAS SUCCESSFUL
*                    INFORMATIONAL MESSAGE HAS TO BE SENT TO
*                    THE OPERATOR
*   OUTPUT         = REG2 PTR TO THE XSA
*                    XAE = X'2A' FOR MESSAGE:
*                       INACTIVE AS HARDCOPY
*                    XAV HAS 'UNIT' NAME OR SYSLOG (WHATEVER
*                    WAS THE HARDCPY DEVICE)
*   RETURN CODES   = NONE
*
* EXIT - ERROR     = TO IEE2103D OR IEE0503D VIA BRANCH
*   CONDITIONS     = ERROR HAS BEEN DETECTED EITHER IN THE
*                    V HARDCPY,OFF OR THE V HARDCPY COMMANDS
*   OUTPUT         = REG2 PTR TO THE XSA
*                    XAE = X'2D' - MSG: 'REQ'D FOR HARDCPY' MOD=2103D
*                    XAE = X'2C' - MSG: 'CHANGING STATUS  ' MOD=2103D
*                    XAE = X'1A' - MSG: 'SYSLOG NOT SUPPORTED'
*                                                           MOD=2103D
*                    XAE = X'29' - MSG: 'AUTHORITY INVALID' MOD=2103D
*                    XAE = X'09' - MSG: 'UNIDENTIFIABLE KEYWORD'
*                                                           MOD=0503D
*                    XAE = X'06' - MSG: 'INVALID NUMERICS'  MOD=0503D
*                    XAE = X'0D' - MSG: 'UNIT REF INVALID'  MOD=0503D
*   RETURN CODES   = NONE
*
* EXTERNAL REFERENCES
*   ROUTINES       = IEE2103D
*                    IEE0503D
*   DATA AREAS     = SEE EXITS - OUTPUT
*
* CONTROL BLOCKS   = 1. EXTENDED SAVE AREA (XSA)
*                    2. UNIT CONTROL MODULE (UCM)
*                       FIELDS TESTED  - UCMATR- BIT=UCMUF
*                                                    UCMOF
*                                        UCMDISP-BIT=UCMDISPB
*                                                    UCMDISPC
*                                        UCMHCUCM
*                                        UCMSFLGS-BIT=UCMSYSG
*                                        UCMUCB
*                                        UCMVEA
*                       FIELDS UPDATED - UCMDISP - BIT=UCMDISPB
*                                        UCMHCUCM
*                                        UCMSFLGS- BIT=UCMSYSB
*                                                      UCMSYSG
*                    3. UNIT CONTROL BLOCK (UCB)
*                       FIELDS TESTED  - SRTESTAT - BIT=SRTEDADI
*                                        UCBNAME
*                       FIELDS UPDATED - NONE
*                    4. CVT
*                       FIELDS TESTED  - CVTCUCB
*                       FIELDS UPDATED - NONE
*
* TABLES           = NONE
*
* MACROS           = SETLOCK
*
* MAPPING MACROS   = IEEXSA
*                    IEECUCM
*                    IEFUCBOB
*                    CVT
*                    IHAPSA
*   SERIALIZATION  = THE CMS AND LOCAL LOCKS WILL PROTECT THE
*                    UCB AND UCM IN THIS MODULE FOR THE VARY HARDCPY
*                    AND THE VARY HARDCPY,OFF COMANDS
*                    THE LOCKS ARE OBTAINED AT:
*                       HSETLCK
*                    AND THEY ARE RELEASED AT:
*                       HUNSTLK
*                    THE FOLLOWING AREAS MUST BE PROTECTED:
*                       UCMDISP   BIT= UCMSYSB         UCM
*                       UCMSFLGS  BIT= UCMSYSB         UCM
*                                      UCMSYSG
*                       UCMHCUCM
*                       UCMATR    BIT= UCMUF           UCM
*
*                       SRTESTAT  BIT= SRTEDADI        UCB
*                    NO LOCKS ARE REQUIRED FOR THIS MODULE'S
*                    MESSAGE ROUTINE FUNCTION.
*
* CHANGE LEVEL     = Y02669, Y02651, Z30LPTJ                   @Z30LPTJ
*
* CHANGE ACTIVITY  = N/A
*
* MESSAGES         = IEE345I VARY AUTHORITY INVALID
*                       THE MASTER CONSOLE DID NOT ISSUE THIS COMMAND
*                    IEE026I SYSLOG NOT SUPPORTED
*                       SYSLOG WAS SPECIFIED IN THE COMMAND BUT
*                       SYSLOG IS NOT SUPPORTED
*                    IEE313I UNIT REF INVALID
*                       THIS MESSAGE IS WRITTEN WHENEVER AN INCORRECT
*                       UNIT IS GIVEN OR NO UNIT IS GIVEN BUT NO
*                       HARDCPY EXISTS.
*                    IEE309I UNIDENTIFIABLE KEYWORD
*                       A KEYWORD WAS SPECIFIED IN THE COMMAND, BUT
*                       IT DOES NOT MATCH SYNTACTICALLY TO ANY
*                       ALLOWED KEYWORD
*                    IEE338I INACTIVE AS HARDCPY
*                       THIS MESSAGE IS ISSUED UPON PROCESSING OF A
*                       VALID HARDCPY,OFF COMMAND.
*                    IEE339I CHANGING STATUS
*                       A VARY HARDCPY COMMAND WAS ISSUED FOR A
*                       DEVICE WHICH IS IN THE PROCESS OF CHANGING
*                       TO A CONSOLE OR FROM THE CONSOLE STATE.
*                    IEE299I REQ'D FOR HARDCPY
*                       A VARY HARDCPY,OFF WAS ISSUED HOWEVER, THE
*                       HARDCPY DEVICE IS NEEDED BECAUSE EITHER
*                       MORE THAN ONE ACTIVE CONSOLE IS PRESENT ON
*                       THE SYSTEM, OF AT LEAST ONE (OR MORE) ACTIVE
*                       GRAPHIC CONSOLES ARE PRESENT.
*                    IEE306I VARY INVALID NUMERICS
*                       MSG COMES OUT WHEN AN INVALID ROUTCODE WAS
*                       TO BE ASSIGNED TO THE HARDCPY DEVICE.
*
* ABEND CODES      = NONE
*
* PACKAGING        = IEE5703D WILL BE PART OF LOAD MODULE
*                       = IGC0003D
*                    THIS LOAD MODULE ALIASES ARE
*                       = NONE
*                    THIS LOAD MODULE ENTRY POINTS ARE
*                       = IGC0003D
*                    MODULE WILL RESIDE IN : LPALIB
*
* SYSGEN           = SUPPLIED BY SECTIONS:
*                    PACKAGING
*                    LOAD MODULE NAME
*                    THIS CSECT WILL BE INCLUDED AT SYSGEN FROM
*                    DLIB -AOSB3- BY MACRO -SGIEF441-
*
* SYSTEM LIBRARIES = NONE
*
***********************************************************************
         EJECT
*
*   REGISTER EQUATES
*
DEFREG   EQU   *                                                 Y02669
R0       EQU   0
R1       EQU   1
R2       EQU   2                   XSA
R3       EQU   3                   ADDRESS
R4       EQU   4                   UCME                           19784
R5       EQU   5                                                  19784
R6       EQU   6                                                  19784
R7       EQU   7                   EOB REG                        19784
R8       EQU   8                                                  19784
R9       EQU   9                   UNIT PTR                       19784
R10      EQU   10                  UCM REG                        19784
R11      EQU   11                  UCMPFX REG                     19784
R12      EQU   12                                                 19784
R13      EQU   13                                                 19784
R14      EQU   14                  RET ADDR                       19784
R15      EQU   15                                                 19784
         SPACE 10
***********************************************************************
*                                                                 19784
*        EQUATES                                                  19784
*                                                                 19784
***********************************************************************
*                                                                 19784
ZERO     EQU   0                                                  19784
ONE      EQU   1                                                  19784
TWO      EQU   2                                                  19784
TRE      EQU   3                                                  19784
FOR      EQU   4                                                  19784
SIX      EQU   6                                                  19784
EIT      EQU   8                                                  19784
TWLV     EQU   12                                                 19784
FIFTN    EQU   15                                                 19784
*                                                                 19784
*                                                                 19784
REQCD    EQU   8                                                  19784
UREFCD   EQU   12                                                 19784
KEYCD    EQU   20                                                 19784
NUM6     EQU   6                                                  19784
*                                                                 19784
CD9      EQU   9                                                  19784
CD13     EQU   13                                                 19784
CD26     EQU   26                                                 19784
CD41     EQU   41                                                 19784
CD44     EQU   44                                                 19784
CD45     EQU   45                                                 19784
*                                                                 19784
*                                                                 19784
*                                                                 19784
BLANK    EQU   C' '                                               19784
*                                                                 19784
MSG42    EQU   42                                                 19784
*                                                                 19784
FOX      EQU   255                                                19784
*                                                                 19784
         EJECT
***********************************************************************
*                                                                 19784
*        HARDCPY,OFF PROCESSOR                                    19784
*                                                                 19784
***********************************************************************
*                                                                 19784
*                                                                 19784
         BALR  R3,ZERO                                            19784
         USING *,R3                                               19784
         MODID BR=YES                                            Y01886
         USING XSA,R2                                             19784
         L     R5,XAL              GET PTR TO UNIT BEGIN          19784
         LA    R5,ZERO(R5)                                        19784
         L     R10,CVTPTR                                         19784
         USING OCVT,R10                                           19784
         L     R10,CVTCUCB         GET PTR TO UCM                 19784
         DROP  R10                                                19784
        USING UCM,R10                                            Y02669
*                                                                 19784
         LR    R11,R10             PTR TO UCM                     19784
         SH    R11,FOUR             MINUS 4                       19784
         L     R11,ZERO(R11)       UCMPFX PTR                     19784
         USING UCMPRFX,R11                                        19784
         SPACE 10                                                 19784
*                                                                 19784
OTABLE   EQU   *                                                  19784
         SR    R4,R4                                              19784
         IC    R4,XAE              INSERT CODE FOR WORK BRANCH    19784
         BC    FIFTN,BRANCH(R4)    GO TO APPROPRIATE WORK         19784
*                                                                 19784
*                                                                 19784
BRANCH   EQU   *                                                  19784
         B     OSCAN               GO TO PROCESS HARDCPY,OFF      19784
         B     MSGCHG              GO PUT CHANGING STATUS MSG     19784
         B     REQHCPY             GO PUT REQ'D FOR HARDCPY MSG   19784
         B     UTREFINV            XAE 12-UNIT REF INVALID        19784
         B     NOTSUPP             XAE 16-SYSLOG NOT SUPPORTED    19784
         B     KEYWORD             XAE 20-KEYWORD MISSPELLED      19784
         B     AUTHINV             XAE 24- AUTHORITY INVALID      19784
         B     INVNUM              XAE IS 28 INVALID NUMERICS    Y02669
*                                                                 19784
         EJECT                                                    19784
*                                                                 19784
OSCAN    EQU   *                                                  19784
         LR    R12,R5              SAVE UNIT PTR FOR MSG          19784
OLOOP    EQU   *                                                  19784
         CLC   ZERO(TWO,R5),HUNIT      ,H FOLLOWS                 19784
         BE    OFOUND              YES, FOUND IT                  19784
         LA    R5,ONE(R5)          INDEX BY ONE MORE              19784
         B     OLOOP                GO CHECK NEXT CHARACTER       19784
OFOUND   EQU   *                                                  19784
         LA    R5,TWLV(R5)         INDEX PAST COMD                19784
         L     R4,XAR              PTR TO PARAMS                  19784
         LA    R4,ZERO(R4)         CLEAR HIGH ORDER BYTE          19784
         AH    R4,ZERO(R4)         GET END OF BUFFER ADDRESS      19784
         CR    R5,R4               IS THIS END OF BUFFER          19784
         BH    OINVALID            NO,HIGHER,KEYWORD MISSPELLED   19784
         BE    OCONT               OK, GO PROCESS CMD             19784
         CLI   ZERO(R5),BLANK      DOES A BLANK FOLLOE COMD       19784
         BNE   OINVALID            NO,  KEYWORD MISSPELLED        19784
OCONT    EQU   *                                                  19784
         LR    R5,R12              RESTORE PTR TO UNIT FIELD      19784
          EJECT
***************************************************              Y02651
*                                                                Y02651
*  LOCKS ARE REQUIRED AT THIS POINT AND THEY ARE OBTAINED AT:    Y02651
*              HSETLCK                                           Y02651
*  THESE LOCKS WILL BE RELEASED AT:                              Y02651
*              HUNSTLK                                           Y02651
*                                                                Y02651
HSETLCK  EQU   *                                                 Y02651
         STM   R11,R14,XASAVLOC                                  Y02651
         SETLOCK OBTAIN,TYPE=LOCAL,MODE=UNCOND,                  Y02651*
               RELATED=(IEE5703D(HUNSTLK))                       Y02651
         SETLOCK OBTAIN,TYPE=CMS,MODE=UNCOND,                    Y02651*
               RELATED=(IEE5703D(HUNSTLK))                       Y02651
         LM    R11,R14,XASAVLOC                                  Y02651
*                                                                Y02651
***************************************************              Y02651
          SPACE 10
         CLC   ZERO(TWO,R5),HUNIT     UNIT WAS SPECIFIED          19784
         BE    HOFFNUNT            NO, GO VARY OFF PRESENTHARDCPY 19784
*                                                                 19784
         SPACE 10
***********************************************************************
*                                                                 19784
*    UNIT WAS SPECIFIED                                           19784
*                                                                 19784
***********************************************************************
HOFFUNIT EQU   *                                                  19784
         CLC   ZERO(EIT,R12),HSYSLOG  WAS SYSLOG SPECIFIED        19784
         BE    HOFFSYLG            YES,GO CHECK IF IT IS HCPY     19784
*                                                                 19784
HUNITS   EQU   *                                                  19784
         LM    R5,R7,UCMVEA       PTR TO 1ST AND LAST UCMES       19784
         USING UCMLIST,R5                                         19784
HLOOPOFF EQU   *                                                  19784
         L     R4,UCMUCB           PTR TO UCB                     19784
         USING OUCB,R4                                            19784
         CLC   ZERO(TRE,R12),UCBNAME      IS THE SOUGHT UCME      19784
         BE    HFNDOFF             YES,BINGO                      19784
         BXLE  R5,R6,HLOOPOFF      NO, KEEP LOOKING               19784
         SPACE 5
***************************************************              Y02651
*                                                                Y02651
*  SINCE THE ABOVE DEFAULTS INTO AN ERROR EXIT, WE MUST RELEASE  Y02651
*  ALL LOCKS FIRST                                               Y02651
         B     HOFFLCK             SORRY,NO MATCH                Y02651
*                                                                Y02651
***************************************************              Y02651
         SPACE 5
HFNDOFF  EQU   *                                                  19784
       TM    UCMDISP,UCMDISPB    IS IT REALLY HCPY                19784
         SPACE 5
***************************************************              Y02651
*                                                                Y02651
*  SINCE THE ABOVE DEFAULTS INTO AN ERROR EXIT, WE MUST RELEASE  Y02651
*  ALL LOCKS FIRST                                               Y02651
         BZ    HOFFLCK                                           Y02651
***************************************************              Y02651
         SPACE 5
         DROP  R5                                                 19784
         LR    R9,R5                                              19784
         B     HOFF                YES,GO PROCESS                 19784
*                                                                 19784
HOFFSYLG EQU   *                                                  19784
         SR    R12,R12                                            19784
         TM    UCMSFLGS,UCMSYSG    IS SYSLOG REALLY HCPY          19784
         BO    HOFFNUNT            YES,GO PROCESS                 19784
         EJECT
*****************************************************************Y02651
*                                                                Y02651
*  SINCE THIS IS AN ERROR CONDITION, MUST RELEASE LOCKS FIRST    Y02651
*                                                                Y02651
HOFFLCK  EQU   *                                                 Y02651
         BAL   R4,HUNSTLK         GO TO RELEASE LOCKS SUBROUTINE Y02651
*                                                                Y02651
*****************************************************************Y02651
         SPACE 5
         B    HERR2               NO, SORRY                       19784
         EJECT
***********************************************************************
*                                                                 19784
*        THIS ROUTINE PROCESSED THE HARDCPY OFF COMMAND           19784
*                                                                 19784
*                  R5 PTR TO ,H OR UNIT,H                         19784
*                  R11 HAS PTR TO THE UCMPFX                      19784
*                  R10 HAS PTR TO THE UCM                         19784
*                                                                 19784
***********************************************************************
HOFFNUNT EQU   *                   UNIT WAS NOT SPECIFIED IN CMD  19784
         SR    R12,R12             ZERO UNIT PTR IN CASE OF SYSLOG19784
         SR    R9,R9               ZERO UCME REG IN CASE OF SYSLOG19784
         TM    UCMSFLGS,UCMSYSG    IS SYSLOG HARDCPY              19784
         BO    HOFF                YES,GO PROC%SS                 19784
         LR    R12,R5                                             19784
         L     R9,UCMHCUCM         GET PTR TO UCME OF CURRENT HCPY19784
         LA    R9,ZERO(R9)         CLEAR HIGH ORDER BYTE          19784
         LTR   R9,R9               ANY PTR                        19784
         SPACE 5
*****************************************************************Y02651
*                                                                Y02651
*  MUST GO RELEASE ALL LOCKS FIRST                               Y02651
*                                                                Y02651
         BZ    HOFFLCK                                           Y02651
*                                                                Y02651
*****************************************************************Y02651
         SPACE 5
         USING UCMLIST,R9          REG9 PTR TO UCME OF HCPY DEV   19784
         L     R4,UCMUCB           PTR TO UCM                     19784
         USING OUCB,R4                                            19784
         LA    R12,UCBNAME         PTR TO NAME OF DEV IN UCB FOR MG
         DROP  R4                  MESSAGE                        19784
         DROP  R9                                                 19784
HOFF     EQU   *                                                  19784
*                                                                 19784
*   CHECK WHETHER CMD CAN BE ACCEPTED AND PROCESS ACCORDINGLY     19784
*                                                                 19784
*                   THIS RTN EXPECTS                              19784
*                                                                 19784
*                           R11 IS PTR TO UCMPFX                  19784
*                           R10 IS PTR TO UCM                     19784
*                             R9 IS PTR TO UCME OF HCPY DEV OR ZERO FOR
*                             SYSLOG                              19784
*                           R12 PTS TO UNITNAME OF IS ZERO FOR SYSLOG
*                                                                 19784
*                                                                 19784
         LM    R5,R7,UCMVEA        PTRS TO FIRST AND LAST UCMES   19784
         USING UCMLIST,R5                                         19784
         SR    R4,R4               SERO OUT ACTV CONSOLE SWITCH   19784
         SR    R13,R13             ZERO OUT GFX CONSOLE SWITCH    19784
HACTVCK  EQU   *                   CHECK FOR NO OF ACTV CONSOLES  19784
         L     R15,UCMUCB                                         19784
         USING OUCB,R15                                           19784
         TM    UCMATR,UCMUF        IS THE ACTIVE BIT ON IN UCME   19784
         BZ    HNOACTV             NO, CHECK IF CHANGING TO ACTIVE19784
         TM    SRTESTAT,SRTEDADI   ACTIVE,IS IT PENDING TO CHANGE 19784
         BO    HBXLE               YES, CHANGING TO NONACTIVE,OMIT19784
         B     HPLUS1              NOT CHANGING, COUNT IT IN      19784
*                                                                 19784
HNOACTV  EQU   *                                                  19784
         TM    SRTESTAT,SRTEDADI   CHANGING STATUS                19784
         BZ    HBXLE               NO OMIT                        19784
HPLUS1   EQU   *                                                  19784
         TM    UCMATR,UCMOF        WTO SUPPORT                    19784
         BZ    HBXLE               NO, I-UNIT, OMIT               19784
         LA    R4,ONE(R4)          PLUS 1 ACTV CONSOLES           19784
         TM    UCMDISP,UCMDISPC    IS CONSOLE GFX                 19784
         BO    HPLUS2              YES,TWO STRIKES                19784
HBXLE    BXLE  R5,R6,HACTVCK       CONTINUE                       19784
*                                  SORRY,RAN OUT OF UCMES         19784
         DROP  R15                                                19784
         BCTR  R4,ZERO             DECREASE ACTIVE CONSOLE SWITCH 19784
         LTR   R4,R4               IF ZERO MEANS ONLY ONE ACTIVE  19784
         BNZ   HERR1               NORE THAN 1,BYE TO CMD         19784
         LTR   R13,R13             IS THERE ONE GFX CONSOLE ACTIVE19784
         BNZ   HERR1               YES,BYE TO CMD                 19784
         DROP  R5                                                 19784
         USING UCMLIST,R9          ALL OK,CAN ACCEPT CMD          19784
         XC    UCMHCUCM(FOR),UCMHCUCM   ZERO OUT HCPY PTR IN PRFX 19784
         NI    UCMSFLGS,FOX-UCMSYSB-UCMSYSG    TURN OFF HARDCPY EXISTS
*                                  ON SYSTEM FLAG - X'40' AND     19784
*                                  POSSIBLE HARDCPY IS SYSLOG - X'02'
         LTR   R9,R9               WAS IT FOR SYSLOG              19784
         BZ    HOFFMSG             YES,GO TO ISSUE  MSG           19784
         NI    UCMDISP,FOX-UCMDISPB    TURN OFF HCPY FLAG IN UCM  19784
*                                                                 19784
         EJECT
HOFFMSG  EQU   *                                                  19784
         SPACE 10
***************************************************              Y02651
*                                                                Y02651
*  COMMAND HAS ENDED AND NOW BEFORE EXITING WE MUST RELEASE      Y02651
*  ALL LOCKS AT HUNSTLK                                          Y02651
*  LOCKS WERE GOTTEN AT:                                         Y02651
*    HSETLCK                                                     Y02651
*                                                                Y02651
         BAL   R4,HUNSTLK                                        Y02651
*                                                                Y02651
***************************************************              Y02651
         SPACE 10
*          R12 IS ZERO IF FOR SYSLOG OR PTR TO UNIT NAME          19784
         MVI   XAE,MSG42            MSG CODE IS 42,MSG IS ---     19784
*                                  INACTIVE AS HARDCPY            19784
*                                  MSG MODULE IS 2103D            19784
HSYLOG   EQU   *                                                  19784
         MVC   XAV(TRE),ZERO(R12)  ASSUME REG 7 PTS TO UNIT       19784
         MVI   XAV+TRE,BLANK       NOVE IN BLANK PAST UNIT        19784
         MVC   XAV+FOR(FOR),XAV+TRE  BLANK REST OF AREA           19784
         LTR   R12,R12             WAS SYSLOG SPECIFIED           19784
         BNZ   HERREXIT            NO, UNIT ALREADY IS IN         19784
         MVC   XAV(SIX),HSYSLOG        YES, MOVE IN SYSLOG        19784
HERREXIT EQU   *                                                  19784
         MVC   XAX+TRE(TWO),MSG2103        MSG MODULE IS 2103D   Y02669
         L     R15,VCON21                                        Y02669
         BR    R15                                               Y02669
*                                                                 19784
HPLUS2   EQU   *                                                  19784
         LA    R13,ONE(R13)        PLUS 1 GFX CONSOLES            19784
         B     HBXLE               GO CONTINUE UCME SCAN          19784
         EJECT
***********************************************************************
*                                                                 19784
* ERROR MESSAGE ROUTINES                                          19784
*                                                                 19784
***********************************************************************
*                                                                 19784
*                                                                 19784
HERR1    EQU   *                                                  19784
         SPACE 10
***************************************************              Y02651
*                                                                Y02651
*  THIS IS AN ERROR EXIT AND WE ARE STILL HOLDING SOME LOCKS     Y02651
*  THESE LOCKS MUST BE RELEASED BEFORE EXITING AT SUBROUTINE:    Y02651
*              HUNSTLK                                           Y02651
*  LOCKS WERE OBTAINED AT:                                       Y02651
*              HSETLCK                                           Y02651
*                                                                Y02651
         BAL   R4,HUNSTLK                                        Y02651
*                                                                Y02651
***************************************************              Y02651
         SPACE 10
         MVI   XAE,REQCD           REQ'D FOR HARDCPY MSG CODE     19784
         B     OTABLE              GO TO PROCESS IT               19784
*                                                                 19784
HERR2    EQU   *                                                  19784
         MVI   XAE,UREFCD          UNIT REF INVALID               19784
         B     OTABLE              GO PROCESS                     19784
*                                                                 19784
*                                                                 19784
MSGCHG   EQU   *                                                  19784
         MVI   XAE,CD44            CHANGING STATUS,2103D          19784
         B     HSYLOG              GO PROCESS                     19784
*                                                                 19784
*                                                                 19784
REQHCPY  EQU   *                                                  19784
         MVI   XAE,CD45            REQ'D FOR HCPY,2103D           19784
         B     HSYLOG              GO PROCESS                     19784
*                                                                 19784
*                                                                 19784
NOTSUPP  EQU   *                                                  19784
         MVI   XAE,CD26            SYSLOG NOT SUPPORTED,2103D     19784
         B     HERMOD0             GO XCTL                        19784
*                                                                 19784
*                                                                 19784
AUTHINV  EQU   *                                                  19784
         MVI   XAE,CD41            AUTHORITY INVALID,2103D        19784
*                                  NO,UNIT, MSG WILL HAVE 'VARY'  19784
         B     HERREXIT            GO PROCESS                     19784
*                                                                 19784
*                                                                 19784
KEYWORD  EQU   *                                                  19784
         MVI   XAE,CD9             KEYWORD MISSPELLED-0503D       19784
*                                  NO UNIT, MSG WILL HAVE 'VARY'  19784
         B     HERMOD              GO PROCESS                     19784
*                                                                 19784
INVNUM   EQU   *                                                  19784
         MVI   XAE,NUM6            MOVE IN ERROR CODE             19784
         MVC   XAV(EIT),HVARY      MOVE IN VARY COMMAND VERB      19784
         B     HERMOD              XCTL TO 0503D                  19784
*                                                                 19784
UTREFINV EQU   *                                                  19784
         MVI   XAE,CD13            UNIT REF INVALID-0503D         19784
         MVC   XAV(TRE),ZERO(R12)      MOVE UNIT                  19784
         MVI   XAV+TRE,BLANK       BLANK OUT REST                 19784
         CLC   ZERO(TWO,R12),HUNIT     WAS DEVICE SPECIFIED       M0048
         BNE   UNIT                    YES                        M0048
         MVC   XAV(EIT),HARDCOPY       MOVE IN HARDCOPY           M0048
UNIT     LTR   R12,R12                 WAS SYSLOG SPECIFIED       M0048
         BNZ   HERMOD              NO, OK                         19784
*                                                                 19784
HERMOD0  EQU   *                                                  19784
         MVC   XAV(SIX),HSYSLOG        NO, MOVE IN SYSLOG         19784
HERMOD   EQU   *                                                  19784
         MVC   XAX+TRE(TWO),MSG0503        MSG MODULE IS 0503D   Y02669
         L     R15,VCON05                                        Y02669
       BR    R15                                                 Y02669
*                                                                 19784
OINVALID EQU   *                                                  19784
         MVI   XAE,KEYCD                                          19784
         B     OTABLE              GO PROCESS                     19784
*                                                                 19784
*                                                                 19784
         EJECT                                                    19784
*****************************************************************Y02651
*                                                                Y02651
*  THIS IS THE RELEASE-LOCKS SUBROUTINE                          Y02651
*  ALL LOCKS ARE RELEASED HERE WHEN THIS ROUTINE DOES NO LONGER  Y02651
*  NEED THEM                                                     Y02651
*  ALL THESE LOCKS WERE OBTAINED AT LABEL:                       Y02651
*              HSETLCK                                           Y02651
*                                                                Y02651
HUNSTLK  EQU   *                                                 Y02651
         STM   R11,R14,XASAVLOC                                  Y02651
         SETLOCK RELEASE,TYPE=CMS,RELATED=(IEE5703D(HSETLCK))    Y02651
         SETLOCK RELEASE,TYPE=LOCAL,RELATED=(IEE5703D(HSETLCK))  Y02651
         LM    R11,R14,XASAVLOC                                  Y02651
         BR    R4                                                Y02651
*                                                                Y02651
*****************************************************************Y01651
         EJECT                                                    19784
*                                                                 19784
*                                                                 19784
***********************************************************************
*                                                                 19784
*       DECLARES                                                  19784
*                                                                 19784
***********************************************************************
*                                                                 19784
FOUR     DC    H'4'                USED TO ACCESS UCMPRFX         19784
***********************************************************************
HSYSLOG  DC    C'SYSLOG'           DO NOT SEPARATE THIS AND THE   19784
HUNIT    DC    C',H'               FOLLOWING CONSTANT             19784
HVARY    DC    C'VARY    '         VARY COMMAND VERB              19784
*
***********************************************************************
*                                                                 19784
*                                                                 19784
MSG2103  DC    C'21'                                             Y02669
MSG0503  DC    C'05'                                             Y02669
         DS    0F                                                Y02669
VCON21   DC    V(IEE2103D)                                       Y02669
VCON05   DC    V(IEE0503D)                                       Y02669
*                                                                 19784
HARDCOPY DC    C'HARDCOPY'                                        M0048
         SPACE 5
PATCH    DC    CL50'***IEE5703D***'                            @Z30LPTJ
         EJECT
OXSA     DSECT
         IEEXSA
         EJECT
OCVT     DSECT
         CVT
         EJECT
OUCM     DSECT
         IEECUCM    FORMAT=NEW
         EJECT
OUCB     DSECT
         IEFUCBOB
         EJECT
         IHAPSA
         END
