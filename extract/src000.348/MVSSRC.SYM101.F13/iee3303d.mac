 TITLE 'IEE3303D - VARY COMMAND SYNTAX SCANNER'
IEE3303D CSECT
**********************************************************************
* MODULE NAME      = IEE3303D
*   CSECT NAME     = IEE3303D
*
* LOAD MODULE NAME = IEE3603D
*
* DESCRIPTIVE NAME = VARY COMMAND SYNTAX SCANNER FOR KEYWORDS:
*                            ONLINE
*                            OFFLINE
*                            CONSOLE
*
* COPYRIGHT        = MISSING
*
* STATUS           = OS/VS/2 RELEASE 3                         @Z30LPTJ
*
* FUNCTION         = THIS ROUTINE SYNTAX SCANS THE UNIT FIELDS IN THE
*                    COMMAND FOR:
*                    . VALID UNIT SPECIFICATION
*                    . CORRECT NUMBER AND LOCATION OF COMMAS, PARENS
*                      BLANKS, END OF BUFFER DELIMITING END OF
*                      COMMAND
*                    . CHECKS COMMAND AUTHORITY,MUST BE EITHER 2 OR 3
*                    . IF SMF IS IN THE SYSTEM, CONTROL IS GIVEN TO
*                      MODULE IEE2303D WITH CODES SPECIFYING WHICH
*                      MODULE SHOULD IT RETURN TO.
*                    . CHECKS FOR COMMA FOLLOWING 'CONSOLE'
*                      KEYWORD. IN THIS CASE CONTROL IS PASSED
*                      TO 4403D FIRST FROM HERE (IF SMF IS NOT
*                      ACTIVE, IF IT IS ACTIVE THEN A FLAG IS SET
*                      FOR 2303D TO GO TO 4403D FIRST WHEN IT IS
*                      READY TO RELINQUISH CONTROL)
*
* OPERATION        = THIS ROUTINE VALIDATES THE SYNTAX OF UNITS:
*                       XXX            (SINGLE UNIT)
*                       O-XXX          (OUTPUT ONLY UNIT)
*                       (I-XXX,O-XXX)  (COMPOSITE SPECIFICATION)
*                    IT VALIDATES THAT PARENS ENCLOSE MULTIPLE UNITS
*                    AND THAT EACH UNIT IS SEPARATED BY COMMAS
*                    IF THE KEYWORD IS 'CONSOLE', IT VALIDATES THAT
*                    THE ISSUER HAS COMMAND AUTHORITY OF 3
*                    IF THE KEYWORD IS 'ONLINE/OFFLINE' THAN IT
*                    VALIDATES THAT THE ISSUER HAS COMMAND
*                    AUTHORITY OF 2 OR 3
*                    IT CHECKS WHETHER ADDITIONAL PARAMETERS ARE
*                    PRESENT ON THE 'CONSOLE' KEYWORD. IF SO, IT SETS
*                    A BIT INFORMING THAT 4403D MUST RECEIVE CONTROL
*                    FIRST, AND IT STORES THE POINTER TO THESE
*                    ADDITIONAL KEYWORDS IN XAS+4 FOR 4403D
*                    IF SMF IS ACTIVE, IT GIVES CONTROL TO
*                    2303D FIRST, FOR THE CONSOLE AND ONLINE
*                    KEYWORDS.
*                    TO FIND WHETHER SMF IS ACTIVE, THIS MODULE
*                    CHECKS THE CVTSMCA BYTE IN THE CVT TO SEE IF
*                    IT HAS BEEN INITIALIZED WITH THE ADDRESS OF THE
*                    SMCA.  IF NOT, THEN WE KNOW THAT SMF HAS NOT
*                    BEEN INITIALIZED AS YET AND NO RECORD CAN BE
*                    WRITTEN.  THEREFORE, IEE2303D'S FUNCTION IS
*                    BYPASSED.  IF CVTSMCA CONTAINS AN ADDRESS
*                    (WHICH IS THE PTR TO THE SMCA) WE TEST THE
*                    SMCAUSER AND THE SMCAMAN BITS IN THE SMCA
*                    (BYTE = SMCAMISC) TO SEE IF SMF IS OPERATING.
*                    IF NONE OR ONLY ONE IS ON, WE CANNOT WRITE
*                    A RECORD AND, THEREFORE, IEE2303D'S FUNCTION
*                    IS AGAIN BYPASSED.  IF BOTH BITS ARE ON, THEN
*                    WE DETERMINE IF JOB ACCOUNTING WAS REQUESTED
*                    BY THE USER BY TESTING THE SMCAOPT BYTE OF THE
*                    SMCA, BIT = SMCAOPT1.  IF IT IS OFF, THEN JOB
*                    ACCOUNTING WAS NOT REQUESTED AND IEE2303D'S
*                    FUNCTION IS BYPASSED.
*                    IF THIS BIT IS ON, THEN WE KNOW THAT SMF IS UP
*                    AND RUNNING AND THAT JOB ACCOUNTING WAS
*                    REQUESTED AND THEREFORE, WE GIVE CONTROL TO
*                    IEE2303D TO WRITE THE SMF RECORD FOR ALL VALID
*                    DEVICES CHANGING TO THE 'ONLINE' AND
*                    'CONSOLE' STATES.
*                    IT ALSO SETS BITS IN THE XOPCODE BYTE OF THE
*                    DUMMY XSA TO SHOW WHICH KEYWORD WAS SPECIFIED:
*                    X'80' = CONSOLE
*                    X'20' = ONLINE
*                    X'10' = OFFLINE
*
* NOTES
*   DEPENDENCIES   = NONE
*   CHARACTER/CODE DEPENDENCIES
*                  = THIS MODULE CAN BE ASSEMBLED WITH ANY NEW
*                    CHARACTER/CODE SET.
*
* RESTRICTIONS     = R1 OUTPUT = XAD SWITCHES FOR 4203D
*                       INCLUDING CCAHOLD
*                    R2 ADDRESSABILITY FOR DUMMY XSA
*   REGISTER CONVENTIONS
*                  = REGISTERS ARE DEFINED AT LABEL: DEFREG
*   PATCH LABEL    = PATCH
*
* MODULE TYPE      = CSECT
*   PROCESSOR      = ASSEMBLER
*   MODULE SIZE    = VS/2 = '354'                              @Z30LPTJ
*   ATTRIBUTES     = REENTRANT, REUSABLE, REFRESHABLE,
*                    ZERO PSW PROTECT KEY, PAGED LPA
*
* ENTRY POINTS     = IEE3303D FROM IEE3603D
*   PURPOSE        = VARY ONLINE/OFFLINE/CONSOLE COMMANDS TO BE
*                    PROCESSED
*   LINKAGE        = BRANCH
*   INPUT          = DUMMY XSA POINTED BY REG 2
*                    IN THE DUMMY XSA THE FOLLOWING FIELDS:
*                       XAS AND XAS+1 CONTAIN READER/INTERPRETER
*                       COMMAND AUTHORITY
*                       XAR CONTAINS THE SIZE OF THE COMMAND BUFFER
*                       XAL CONTAINS THE PTR TO THE LIST POSITION
*                       XAV CONTAINS THE VERB OF THE COMMAND 'VARY'
*                       XAU CONTAINS THE CONSOLE ID OF THE ISSUER
*                       OF THE COMMAND
*                    R13 POINTS TO THE SAVE AREA, WORD17 = CSCB PTR
*   REGISTERS SAVED  = ALL
*   REGISTER USAGE = R1 R/I COMMAND AUTHORITY
*                    R2 PTR TO THE DUMMY XSA
*                    R3 ADDRESSABILITY
*                    R4 UCME PTR
*                    R6 CVT PTR
*                    R7 EOB REG
*                    R9 PARAM LIST REG
*                    R10 UCM REG
*                    R11 UCMPRFX REG
*                    R12 UCME REG
*                    R13 SAVE AREA REG
*   REGISTERS RESTORED
*                  = ALL
*
* EXITS - NORMAL   = IF SMF IS IN THE SYSTEM = IEE2303D VIA BRANCH
*   CONDITIONS     = WITH INDICATOR WHETHER IT SHOULD GO TO
*                    4203D OR 4403D WHEN RETURNING
* EXITS - NORMAL-2-  = IF SMF IS NOT ACTIVE = TO:
*   CONDITIONS     = IEE4203D FOR ONLINE/OFFLINE
*                    IEE4403D OR IEE4203D FOR CONSOLE
*                    FOR VARY OFFLINE, AN EXIT IS TAKEN ALWAYS
*                    DIRECTLY TO 4203D SINCE NO RECORD HAS TO BE
*                    WRITTEN FOR AN OFFLINE COMMAND.
*   OUTPUT         = DUMMY XSA POINTED BY REG2
*                    REG1 HAS THE XAD SWITCHES FOR 4203D
*                            BYTE 1 = USWITCH SWITCHES
*                            BYTE 2 = USWTWO - UNUSED SWITCHES
*                            BYTE 3 AND 4 = CCAHOLD - HAS COMMAND
*                                           AUTHORITY OF ISSUER
*                    XAS
*                            BYTE 1 = XOPCODE - HAS INDICATION OF
*                                      KEYWORD
*                            BYTE 2 = XSWITCH - UNUSED IN THIS MODULE
*                            BYTE3&4= UNUSED IN THIS MODULE
*                            BYTE5-8= TEMPORARITY HAS PTR TO END OF
*                                      KEYWORD FOR 4403D
*                    SAVE AREA PTR IN REG 13
*   RETURN CODES   = NONE
*
* EXITS - ERROR-1- = TO IEE0503D AND IEE2103D VIA BALR
*                    ON RETURN, THE CSCB IS FREED VIA AN MGCR MACRO
*                    THE SAVE AREA AND THE DUMMY XSA
*                    GOTTEN IN 3603D IS ALSO FREED VIA FREEMAIN
*                    AND A RETURN IS TAKEN TO THE CALLER OF THE SVC
*   CONDITIONS     = AN ERROR SITUATION WAS DETECTED
*   OUTPUT         = DUMMY XSA PTR IN REG 2 AND THE FOLLOWING:
*                       XAE = ERROR MESSAGE CODE
*                       XAV = VERB 'VARY'
*                       XAU = UCM ID
*   RETURN CODES = NONE
*
* EXITS - ERROR-2- = TO IEE0503D AND IEE2103D VIA BALR.        @Z30LPTJ
*                    ON RETURN, AN EXIT IS MADE TO IEECB904    @Z30LPTJ
*                    FOR FURTHER VARY RANGE PROCESSING.        @Z30LPTJ
*   CONDITIONS     = AN ERROR SITUATION WAS DETECTED AND VARY  @Z30LPTJ
*                    RANGE IS BEING PROCESSED.                 @Z30LPTJ
*   OUTPUT         = DUMMY XSA PTR IN REG 2 AND THE FOLLOWING: @Z30LPTJ
*                        XAE = ERROR MESSAGE CODE              @Z30LPTJ
*                        XAV = VERB 'VARY'                     @Z30LPTJ
*                        XAU = UCM ID                          @Z30LPTJ
*   RETURN CODES   = NONE                                      @Z30LPTJ
* EXTERNAL REFERENCES
*   ROUTINES       = IEE2303D     SMF ROUTINE
*                    IEE4203D     ONLINE/OFFLINE/CONSOLE PROCESSOR
*                    IEE4403D     CONSOLE SUBKEYWORD PROCESSOR
*                    IEECB904     VARY RANGE PROCESSOR         @Z30LPTJ
*                    IEE0503D     MESSAGE MODULE
*                    IEE2103D     MESSAGE MODULE
*   DATA AREAS     = SEE EXITS - OUTPUT DATA
*
* CONTROL BLOCKS   = . DUMMY EXTENDED SAVE AREA (XSA)
*                    . CVT
*                       FIELDS TESTED  - CVTCUCB
*                                        CVTSMCA
*                       FIELDS UPDATED - NONE
*                    . UNIT CONTROL MODULE (UCM)
*                       FIELDS TESTED  - UCMVEZ
*                                        UCMVEA
*                                        UCMAUTH
*                       FIELDS UPDATED - NONE
*                    . SYSTEM MANAGMENT FACILITIES CONTROL TAB (SMCA)
*                       FIELDS TESTED  - SMCAMISC - BIT=SMCAUSER
*                                                       SMCAMAN
*                                        SMCAOPT  - BIT=SMCAOPT1
*                       FIELDS UPDATED - NONE
*                    . COMMAND SCHEDULING CONTROL BLOCK (CSCB)
*                       FIELDS TESTED  - NONE
*                       FIELDS UPDATED - CHSTS - BIT= CHFC
*
* TABLES           = SAVEAREA AND SAVE WORK AREA =
*                    84 BYTES
*
* MACROS           = MGCR, FREEMAIN, LOAD, DELETE ,DEQ
*
* MAPPING MACROS   = IEEXSA
*                    IEECUCM
*                    CVT
*                    IEESMCA
*                    IEECHAIN
*   SERIALIZATION  = PROTECTION AGAINST ALLOC, OLTEP AND ANOTHER VARY
*                    IS DONE VIA AN ENQ ISSUE EARLIER IN MODULE
*                    IEE3603D. A DEQ WILL BE ISSUED HERE IF A TERMINAL
*                    ERROR CONDITION IS FOUND. THIS IS DONE AT:  SDEQUE
*                    PROTECTION AGAINST COMM TASK IS STILL NOT REQUIRED
*                    AND THEREFORE THIS MODULE WILL NOT ISSUE ANY
*                    SETLOCKS
*
* CHANGE LEVEL     = Y02669, Y02651, Z30LPTJ                   @Z30LPTJ
*
* CHANGE ACTIVITY  = N/A
*
* MESSAGES         = IEE345I AUTHORITY INVALID - FROM IEE2103D
*                    IEE307I DELIMITER ERROR - FROM IEE0503D
*                    IEE308I TERM LENGTH ERROR - FROM IEE0503D
*
* ABEND CODES      = NONE
*
* PACKAGING        = IEE3303D WILL BE PART OF LOAD MODULE
*                       IEE3603D
*                    THIS LOAD MODULE ALIASES ARE
*                       NONE
*                    THIS LOAD MODULE ENTRY POINTS ARE
*                       IEE3603D
*                    MODULE WILL RESIDE IN:  LPALIB
*
* SYSGEN           = SUPPLIED BY THE FOLLOWING SECTIONS:
*                       LOAD MODULE NAME
*                       PACKAGING
*                       THIS CSECT WILL BE INCLUDED AT SYSGEN FROM
*                       DLIB -AOSB3- BY MACRO -SGIEF441-
*
* SYSTEM LIBRARIES = NONE
*
**********************************************************************
         EJECT
*
*  REGISTER ASSIGNMENTS
*
DEFREG   EQU   *                                                 Y02669
R0       EQU   0
R1       EQU   1                  R/I COMMAND AUTHORITY
R2       EQU   2                  DUMMY XSA PTR
R3       EQU   3                  ADDRESSABILITY
R4       EQU   4                  UCME PTR
R5       EQU   5                  WORK REG - SAVE FOR UCME SIZE
R6       EQU   6                  WORK REG - SAVE FOR LAST UCME PTR
R7       EQU   7                  END OF BUFFER REG
R8       EQU   8
R9       EQU   9                  PARAM LIST REG
RA       EQU   10                 UCM ADDR REG
RB       EQU   11                 UCMPRFX REG
RC       EQU   12                  UCMPFX REG                     19084
RD       EQU   13                                                 19084
RE       EQU   14                  RET ADD REG                    19084
RF       EQU   15                  WORK AND XCTL REG              19084
*                                                                 19084
         EJECT                                                    19084
*                                                                 19084
*        CONSTANTS AND CODES                                      19084
*                                                                 19084
CA3      EQU   X'20'               COMD AUTH OF 3 - CONS          19084
CA2OR3   EQU   X'60'                                              19084
URI      EQU   X'80'                                              19084
USYST    EQU   X'00'                                              19084
SYSCODE  EQU   X'E0'                                              19084
XFF      EQU   X'FF'                                           @Z30LPTJ
*                                                                 19084
         SPACE 10                                                 19084
*                                                                 19084
*        NUMBER ASSIGNMENTS                                       19084
*                                                                 19084
ZERO     EQU   0                                                  19084
ONE      EQU   1                                                  19084
TWO      EQU   2                                                  19084
TRE      EQU   3                                                  19084
FOR      EQU   4                                                  19084
SIX      EQU   6                                                  19084
SVN      EQU   7                                                  19084
EIT      EQU   8                                                  19084
TEN      EQU   10                                                 19084
TWLV     EQU   12                                                 19084
FIFTN    EQU   15                                                 19084
D14      EQU   14                                                Y02669
D64      EQU   64                                                Y02669
D132     EQU   132                                               Y02669
*                                                                 19084
         EJECT                                                    19084
*                                                                 19084
*        CHARACTER CONSTANTS                                      19084
*                                                                 19084
BLANK    EQU   C' '                                               19084
CONSOLE  EQU   C'C'                                               19084
RPAREN   EQU   C')'                                               19084
LPAREN   EQU   C'('                                               19084
COMMA    EQU   C','                                               19084
*                                                                 19084
*        ERROR CONSTANTS                                          19084
*                                                                 19084
ERR41    EQU   41                       AUTH INVALID              19084
ERR7     EQU   7                        DELIMITER ERROR           19084
ERR8     EQU   8                                                  19084
         EJECT                                                    19784
***********************************************************************
*                                                                 19084
*        ADDRESSABILITY AND HOUSE KEEPING                         19084
*                                                                 19084
***********************************************************************
         BALR  R3,ZERO
         USING *,R3                                               19084
         USING XSA,R2                                             19084
         MODID BR=YES                                            Y01886
*
         LH    R8,XAA              SAVE VARY RANGE BITS        @Z30LPTJ
         STM   R0,RF,ZERO(RD)                                    Y02669
         L     R9,XAL              PTR TO LIST POSITION           19084
         LA    R9,ZERO(R9)         CLEAR HIGH ORDER BYTE          19084
         L     R7,XAR                                             19084
         AR    R7,R9               COMPUTE END OF BUFFER INTO R7 Y02669
         XC    XAD(FOR),XAD        CLEAR SWITCH AREA              19084
         MVC   XACAHOLD(TWO),XAS  MOVE OVER R/I CMD AUTH TO XAS@Z30LPTJ
         XC    XAS(EIT),XAS        ZERO INDICATOR AREA            19084
         L     R6,CVTPTR                                          19084
         USING CVT,R6                                            Y01012
*                                                                 19084
***********************************************************************
*        START  OF SCAN                                           19084
***********************************************************************
*                                                                 19084
         CLI   ZERO(R9),LPAREN    ONE PAREN
         BNE   CONTINUE           NOPE, SINGLE UNIT
         CLI   ONE(R9),LPAREN     OUTPAREN IS HERE, SECON PAREN?
         BNE   CONTINUE           NOPE, JUST SINGLE PAREN
         CLI   TWO(R9),LPAREN     DOUBLE PAREN, IS THERE MORE?
         BE    SERR4              YES, NO GOOD TO HAVE 3PAREN LEVELS
CONTINUE EQU   *
*
         CLI   ZERO(R9),LPAREN     PAREN FOR COMP OR MULT UNITS   19084
         BNE   S1UNIT              NO,SINGLE UNIT                 19084
         CLC   ONE(TWO,R9),HIUNIT+ONE  IS IT A SINGLE COMPOSITE   19084
         BE    S1COMP              YES,SINGLE COMP SPECIFIED      19084
         OI    XADUSWIT,XAXPAREN   NO,MULTIPLE UNITS,SET PAREN @Z30LPTJ
SINDEX   EQU   *                                                  19084
         LA    R9,ONE(R9)           INDEX TO UNIT                 19084
S1UNIT   EQU   *                                                  19084
         CLC   ZERO(TRE,R9),HIUNIT  DO WE HAVE A COMPOSITE        19084
         BE    S1COMP                                             19084
         CLC   ZERO(TWO,R9),HOUNIT+ONE   DO WE HAVE AN O-XXX      19084
         BNE   SCOMACK             NO, THIS IS A REGULAR XXX
SOUNIT   EQU   *                   INDEX PLUS 2 INTO UNIT         19084
         LA    R9,TWO(R9)                                         19084
SCOMACK  EQU   *                                                  19084
         LA    R9,TRE(R9)          INDEX TO WHERE COMMA SHOULD BE 19084
         TM    XADUSWIT,XAXPAREN   IS THE XTR PAREN ON         @Z30LPTJ
         BO    SPARNCK             YES,CHECK FOR RPAREN           19084
         CLI   ZERO(R9),COMMA      COMMA ENDS UNIT FIELD          19084
         BNE   SERR4    ANY OTHER VALID UNIT SHOULD BE 3 CHAR    Y01012
SKEYCK   EQU   *                                                 Y02669
         CLI   ZERO(R9),COMMA  DOES A COMMA FOLLOW THE LAST UNT? Y02669
         BE    KEYTST             YES,OK                         Y02669
         LA    R9,ONE(R9)         UP BY ONE TO NEXT UNIT         Y02669
         B     SKEYCK             CHECK IF COMMA EXISTS NOW      Y02669
KEYTST   EQU   *                                                 Y02669
         CLI   ONE(R9),CONSOLE     IS THIS A CONSOLE KEYWORD     Y02669
         BNE   SONOFF              NO,MUST BE ON/OFFLINE          19084
         OI    XASOPCOD,XACON    TURN ON CONSOLE KEY INDICATOR @Z30LPTJ
         BAL   R5,SUCMID           GO GET REQUESTORS COMD AUTH    19084
         TM    XACAHOLD,CA3      CA PLACED IN CCAHOLD OF XAD   @Z30LPTJ
         BZ    SERR1               AUTHORITY IS INVALID           19084
         LA    R9,EIT(R9)          INDEX PAST CONSOLE KEYWORD     19084
SMFRTN   EQU   *                                                  19084
         ST    R9,XAS+FOR          PTR TO KEYWORD FOR 4403D       19084
         SR    R5,R5                                              19084
         TM    XASOPCOD,XAOFF       IS IT OFFLINE              @Z30LPTJ
         BO    FOR2EXT                                            19084
*                                                                 19084
*        PRESET FOR SMF TO RETURN TO 4203D                        19084
*                                                                 19084
         TM    XASOPCOD,XACON      IS IT CONSOLE KEY           @Z30LPTJ
         BZ    SMFEXT              NO, GOTO  SMF                  19084
         CR    R9,R7               EOB                            19084
         BE    SMFEXT              YES, COMMAND FINISHED          19084
         CLI   ZERO(R9),BLANK      BALNK DELIMITS END             19084
         BE    SMFEXT              YES,GO CHECK FOR SMF           19084
*                                                                 19084
*   PRESET FOR SMF TO GO TO 4403D- XAS+4 WILL PT TO,KEYWORDS      19084
*                                                                 19084
         OI    XASWITCH,XASMF44   MODULE TO RETURN TO IS 4403D @Z30LPTJ
         B     SMFEXT2             GO TO SMF ROUTINE
SMFEXT   EQU   *                                                  19084
         OI    XASWITCH,XASMF42    RETURN TO 4203D SWITCH      @Z30LPTJ
SMFEXT2  EQU   *                                                  19084
         L     R6,CVTPTR                                         Y02669
         USING CVT,R6                                            Y02669
         L     R7,CVTSMCA          ADDRESS IF SNCA               Y02669
         LTR   R7,R7               ANY THERE                     Y02669
         BZ    NOSMF               NO, EXIT NOT TAKEN TO 2303D   Y02669
         USING SMCABASE,R7                                       Y02669
         TM    SMCAMISC,SMCAUSER+SMCAMAN  TEST TO SEE IF RECD IS Y02669
*                                  TO BE WRITTEN OUT             Y02669
         BC    D14,NOSMF           NO, EXIT NOT TAKEN TO 2303D   Y02669
         TM    SMCAOPT,SMCAOPT1    IS HOB ACCOUNTING REQUESTED   Y02669
         BO    YESSMF              YES, GO TO 2303D              Y02669
NOSMF    EQU   *                                                 Y02669
*                                                                Y02669
*    NO 2303D, CHECK IF MUST GO TO 4203D OF 4403D                Y02669
*                                                                Y02669
         TM    XASWITCH,XASMF42    EXIT TO 4203D?              @Z30LPTJ
         BO    FOR2EXT             4203D                         Y02669
         B     FOR4EXT             NO, 4403D                     Y02669
YESSMF   EQU   *                                                 Y02669
         MVC   XAX+TRE(TWO),MOD23                                Y02669
         L     RF,SMFMOD           GET ADDRESS OF IEE2303D       Y02669
         B     COMONEXT            GO TO EXIT ROUTINE            Y02669
S1COMP   EQU   *                                                  19084
         CLC   SIX(TRE,R9),HOUNIT   IS ,O- SPECIFIED              19084
         BNE   SERR3               NO, DELIMITER ERROR            19084
         CLI   TWLV(R9),RPAREN     RPAREN ENDS COMPOSITE          19084
         BNE   SERR3               NO, ERROR                      19084
         LA    R9,TEN(R9)          INDEX PAST UNIT                19084
         B     SCOMACK             GO TO CHECK ENDING             19084
*                                                                 19084
SPARNCK  EQU   *                                                  19084
         CLI   ZERO(R9),RPAREN     ARE WE AT END OF UNIT FLD      19084
         BNE   SMOREUNT            NO, APPARENTLY MORE UNITS      19084
         LA    R9,ONE(R9)          INDEX PAST 'PARENS'            19084
         B     SKEYCK              GO TO CONTINUE                 19084
SMOREUNT EQU   *                                                  19084
         CLI   ZERO(R9),COMMA      COMMA DELIMITS UNITS           19084
         BE    SINDEX              YES,CONTINEU UNIT SCAN         19084
         B     SERR4               TERM LENGTH ERROR              19084
SONOFF   EQU   *                                                  19084
         CLC   ONE(TWO,R9),ONLINE  IS IT ONLINE KEY               19084
         BNE   SOFFLINE            NO,MUST BE OFFLINE             19084
         OI    XASOPCOD,XAON       TURN ON ONLINE BIT          @Z30LPTJ
         LA    R9,SVN(R9)          INDEX PAST KEYWORD             19084
SCOMDATH EQU   *                                                  19084
         BAL   R5,SUCMID           GO GET COMD AUTH OF ISSUER     19084
         TM    XACAHOLD,CA2OR3   IS C A  TWO OR THREE          @Z30LPTJ
         BZ    SERR1               NO, AUTH IS INVALID            19084
         B     SMFRTN              YES, GO CHECK SMF              19084
*                                                                 19084
SOFFLINE EQU   *                                                  19084
         OI    XASOPCOD,XAOFF     TURN OFFLINE BIT             @Z30LPTJ
         LA    R9,EIT(R9)          INDEX PAST KEYWORD             19084
         B     SCOMDATH            GO TO CHECK EOB               Y01012
*                                                                 19084
SUCMID   EQU   *                                                  19084
         CLI   XAU,URI             WAS R/I ISSER                  19084
         BCR   EIT,R5              YES,RETURN                     19084
*                                                                 19084
         CLI   XAU,USYST           WAS A SYSTEM COMMAND           19084
         BNE   UCONS               NO,FROM CONSOLE                19084
         XC    XACAHOLD(TWO),XACAHOLD  YES, ZERO CAHOLD        @Z30LPTJ
         OI    XACAHOLD,SYSCODE    AND INSERT ALL C.A.            19084
         BCR   FIFTN,R5            RETURN                         19084
*                                                                 19084
UCONS    EQU   *                                                  19084
         LR    RE,RD              SAVE SAVE AREA REG IN R14      Y02669
         SR    RD,RD               ZERO FOR IC                   Y02669
         IC    RD,XAU              CONSOLE ID                    Y02669
         BCTR  RD,ZERO             MINUS ONE                     Y02669
         L     R4,CVTCUCB                                         19084
         USING UCM,R4
         L     RC,UCMVEZ           PTR TO FIRST UCME              19084
         L     R4,UCMVEA           AND SIZE                       19084
         DROP  R4                                                 19084
         MR    RC,RC               ID TIMES SIZE                  19084
         AR    R4,RD               PLUS ADDR OF FIRST UCME       Y02669
         USING UCMLIST,R4          GIVES ISSUERS UCME             19084
         MVC   XACAHOLD(TWO),UCMAUTH    MOVE OVER HIS C.A.     @Z30LPTJ
         LR    RD,RE              RESTORE REG13 SAVE AREA PTR    Y02669
         BCR   FIFTN,R5            RETURN                         19084
         EJECT
***********************************************************************
*                                                                 19084
*        EXIT ROUTINES                                            19084
*                                                                 19084
***********************************************************************
FOR2EXT  EQU   *                                                 Y01029
         MVC   XAX+TRE(TWO),MOD42                                Y02669
         L     RF,UCMMOD     ADDRESS OF IEE4203D                 Y01029
         B     COMONEXT      GO TO EXIT RTN                      Y02669
*                                                                Y02669
FOR4EXT  EQU   *                                                 Y02669
         MVC   XAX+TRE(TWO),MOD44                                Y02669
         L     RF,VCON44           ADDRESS OF 4403D              Y02669
COMONEXT EQU   *                                                 Y02669
         LM    R0,RE,ZERO(RD)    RESTORE ALL REGS BUT BRANCH REG Y02669
         BR    RF                  BRANCH TO NEXT RTN            Y02669
*
SERR3    MVI   XAE,ERR7                 DELIMITER ERROR          Y01029
         B     SERR05                   MSG MOD IS 0503D         Y01029
*                                                                 19084
SERR4    MVI   XAE,ERR8                 TERM LENGTH ERROR        Y01029
*                                                                 19084
SERR05   EQU   *
         SPACE 5
**************************************************               Y02651
*                                                                Y02651
*  THIS IS A TERMINAL ERROR CONDITION. A DEQ WILL BE             Y02651
*  ISSUED BEFORE EXITING TO THE ERROR MESAGE MODULE.             Y02651
*                                                                Y02651
         BAL   R4,SDEQUE                                         Y02651
*                                                                Y02651
**************************************************               Y02651
         SPACE 5
         LOAD  EP=IEE0503D                                       Y02669
         LR    RF,R0               GET ADDRESS OF RTN IN REG15   Y02669
         MVC   XAX+TRE(TWO),MOD05                                Y02669
         XC    XAA(TWO),XAA        CLEAR OUT VARY RANGE BITS   @Z30LPTJ
         BALR  RE,RF               GO TO 0503D                   Y02669
* ON RETURN                                                      Y02669
         LM    R0,RF,ZERO(RD)      RESTORE ALL REGS              Y02669
         STH   R8,XAA              RESTORE VARY RANGE BITS     @Z30LPTJ
         DELETE EP=IEE0503D                                      Y02669
EXIT     EQU   *                                                 Y02669
         TM    XAAVRANG,XAARNG1    VARY RANGE BEING PROCESSED? @Z30LPTJ
         BO    CB904EXT            YES, EXIT VIA IEECB904      @Z30LPTJ
         L     R1,D64(RD)          GET CSCB PTR                  Y02669
         USING CHAIN1,R1                                         Y02669
         OI    CHSTS,CHFC          SET FREE CSCB                 Y02669
         MGCR  (1),CHAIN                                         Y02669
         LM    R0,RF,ZERO(RD)      RESTORE ALL REGS              Y02669
         LR    R4,RE               SAVE RETURN ADDRESS           Y02669
         LA    R0,D132             SAVE AREA SIZE                Y02669
         LR    R1,R2               SAVE AREA POINTER             Y02669
         FREEMAIN R,LV=(0),A=(1)                                 Y02669
*                                  FREE SAVE AREA                Y02669
*                                  FREE DUMMY XSA GOTTEN IN 3603DY02669
         LR    RE,R4               RESTORE RETURN REG            Y02669
         BR    RE                  RETURN                        Y02669
*                                                                Y02669
*                                                                 19084
SERR1    MVI   XAE,ERR41                 AUTHORITY INVALID MSG   Y01029
         SPACE 5
**************************************************               Y02651
*                                                                Y02651
*  THIS IS A TERMINAL ERROR CONDITION. A DEQ WILL BE             Y02651
*  ISSUED BEFORE EXITING TO THE ERROR MESSAGE MODULE.            Y02651
*                                                                Y02651
         BAL   R4,SDEQUE                                         Y02651
*                                                                Y02651
**************************************************               Y02651
         SPACE 5
         LOAD  EP=IEE2103D                                       Y02669
         LR    RF,R0               GET RTN'S ADDRESS INTO REG15  Y02669
         MVC   XAX+TRE(TWO),MOD21                                Y02669
         XC    XAA(TWO),XAA        CLEAR OUT VARY RANGE BITS   @Z30LPTJ
         BALR  RE,RF               GO TO 2103D                   Y02669
*  ON RETURN                                                     Y02669
         LM    R0,RF,ZERO(RD)      RESTORE ALL REGS              Y02669
         STH   R8,XAA              RESTORE VARY RANGE BITS     @Z30LPTJ
         DELETE EP=IEE2103D                                      Y02669
         B     EXIT                                              Y02669
*                                                                 19084
CB904EXT EQU   *                                               @Z30LPTJ
         OI    XAAVRANG,XAARNG3    TURN TERMINATION EXIT BIT ON@Z30LPTJ
         L     RF,VCON904          GET IEECB904 ADDRESS        @Z30LPTJ
         BR    RF                  GO TO VARY RANGE PROCESSOR  @Z30LPTJ
         EJECT
*****************************************************************Y02651
*                                                                Y02651
*  THIS IS THE DEQ SUBROUTINE                                    Y02651
*  THE ENQ WAS ISSUED IN MODULE IEE3603D                         Y02651
SDEQUE   EQU   *                                                 Y02651
         TM    XAAVRANG,XAARNG1    VARY RANGE BEING PROCESSED? @Z30LPTJ
         BO    PROCEED             YES, AVOID DEQUEING         @Z30LPTJ
         DEQ   MF=(E,IEFDEQUE)                                   Y02651
PROCEED  EQU   *                                               @Z30LPTJ
         BR     R4                                               Y02651
*****************************************************************Y02651
         EJECT
*****************************************************************Y02651
*                                                                Y02651
*  THE LIST FORM OF THE DEQ MACRO INTRUCTION FOLLOWS             Y02651
*                                                                Y02651
IEFDEQUE DEQ   (IEFSQCBS,IEFQ4QCB,TWO,SYSTEM,IEFSQCBS,IEFVQCBS,SVN,    *
               SYSTEM),RET=HAVE,MF=L                           @YM5443P
IEFVQCBS DC    C'VARYDEV'
IEFSQCBS DC    C'SYSIEFSD'                                       Y02651
IEFQ4QCB DC    C'Q4'                                             Y02651
*                                                                Y02651
*****************************************************************Y02651
         EJECT
***********************************************************************
*                                                                 19084
*        CONSTANTS USED BY 3303D                                  19084
*                                                                 19084
***********************************************************************
HIUNIT    DC    C'(I-'                   BEGINNING OF COMPOSITE   19084
HOUNIT   DC    C',O-'                   MIDDLE OF COMPOSITE       19084
*                                       OR OUTPUT UNIT            19084
ONLINE   DC    C'ON'                    ONLINE KEYWORD            19084
*                                                                 19084
         DS    0F                                                Y02669
SMFMOD   DC    V(IEE2303D)              SMF MODULE               Y02669
UCMMOD   DC    V(IEE4203D)              MCS UCM SCANNER          Y02669
VCON44   DC    V(IEE4403D)                                       Y02669
VCON904  DC    V(IEECB904)                                     @Z30LPTJ
*                                                                Y02669
MOD23    DC    C'23'                                             Y02669
MOD42    DC    C'42'                                             Y02669
MOD44    DC    C'44'                                             Y02669
MOD05    DC    C'05'                                             Y02669
MOD21    DC    C'21'                                             Y02669
*                                                                Y02669
         SPACE 5
PATCH    DC    CL50'***IEE3303D***'                            @Z30LPTJ
         EJECT
SXSA     DSECT
         IEEXSA
         EJECT
CVT      DSECT                                                   Y01029
         CVT
         EJECT
UUCM     DSECT                                                   Y01029
         IEECUCM   FORMAT=NEW
         EJECT
CHAIN1   DSECT
         IEECHAIN
         EJECT
         IEESMCA
         END
