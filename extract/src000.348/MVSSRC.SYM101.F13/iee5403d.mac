IEE5403D TITLE 'PROLOG'                                       @ZA16354
**********************************************************************
* MODULE NAME     = IEE5403D                                   Y02669*
*                                                              Y02669*
* DESCRIPTIVE NAME= COMMAND TRANSLATOR ROUTINE                 Y02669*
*                                                              Y02669*
* COPY RIGHT      = N/A                                       @ZA14128*
*                                                              Y02669*
* STATUS          = 0S/VS2 RELEASE 3 PTF UZ08069              @ZA16354*
*                                                              Y02669*
* FUNCTION        = THIS MODULE TRANSLATES LOWER CASE LETTERS  Y02669*
*                   IN COMMANDS TO UPPER CASE, INITIALIZES THE Y02669*
*                   XSA, SCANS THE INPUT BUFFER FOR THE        Y02669*
*                   BEGINNING OF THE VERB, AND DIRECTS         Y02669*
*                   COMMANDS TO HARDCOPY.                      Y02669*
*                                                              Y02669*
* OPERATIONS      = UPON ENTRY REGISTER 1 POINTS TO A COMMAND  Y02669*
*                   INPUT BUFFER AND REGISTER 0 CONTAINS THE   Y02669*
*                   SOURCE OF THE COMMAND.                     Y02669*
*                                                              Y02669*
*                   THE COMMAND BUFFER IS SCANNED FROM LEFT    Y02669*
*                   TO RIGHT FOR THE FIRST NON-BLANK CHARACTER.Y02669*
*                   ONCE FOUND, THE FOLLOWING CHARACTERS       Y02669*
*                   ARE TRANSLATED TO UPPER                  @ZA14128*
*                   CASE BY MEANS OF AN INTERNAL TRANSLATE     Y02669*
*                   TABLE.  IF ANY INVALID CHARACTERS ARE      Y02669*
*                   LOCATED A CODE (X'2B') IS PLACED IN THE    Y02669*
*                   XAE FIELD OF THE XSA.  AN INVALID          Y02669*
*                   CHARACTER MESSAGE WILL BE ISSUED BY        Y02669*
*                   IEE0403D UPON READING THIS CODE.           Y02669*
*                                                              Y02669*
*                   IF THE SCAN FINDS APOSTROPHES NO           Y02669*
*                   TRANSLATION OF THE CHARACTER STRING WITHIN Y02669*
*                   THE QUOTES IS MADE.  IF NO QUOTES ARE      Y02669*
*                   FOUND, NORMAL TRANSLATION TO UPPER CASE    Y02669*
*                   OF THE CHARACTER STRING IS DONE.           Y02669*
*                                                              Y02669*
*                   A CHECK IS MADE FOR THE COMMAND SOURCE     Y02669*
*                   WHETHER INPUT STREAM OR TSO TERMINAL.      Y02669*
*                   FOR TSO THE ASID FIELD IS SAVED IN THE XAA Y02669*
*                   FIELD OF THE XSA.  IF FROM THE INPUT       Y02669*
*                   STREAM THE XAA FIELD IS ZEROED OUT.        Y02669*
*                                                              Y02669*
*                   IN SYSTEMS WITH THE MULTIPLE CONSOLE       Y02669*
*                   SUPPORT (MCS) OPTION, THIS ROUTINE         Y02669*
*                   DETERMINES WHETHER OR NOT THE COMMAND      Y02669*
*                   IS TO BE ADDED TO THE HARD COPY LOG.       Y02669*
*                   IF SO, A CHECK IS MADE TO DETERMINE        Y02669*
*                   IF THE COMMAND IS 'REPLY' OR 'CONTROL'.    Y02669*
*                   FOR SECURITY REASONS THESE COMMANDS        Y02669*
*                   WILL NOT BE WRITTEN TO THE LOG.            Y02669*
*                   A CHECK IS ALSO MADE TO DETERMINE IF THE @ZA03236*
*                   COMMAND BEGINS WITH A NUMBER. IF IT DOES,@ZA03236*
*                   IT WILL NOT BE HARDCOPIED BY THIS MODULE @ZA03236*
*                   IN CASE IT IS A REPLY COMMAND. BIT       @ZA03236*
*                   XASSDS4 IN THE XSA IS SET AND IEE0403D   @ZA03236*
*                   WILL HARDCOPY THE COMMAND IF IT DOES NOT @ZA03236*
*                   GET CHANGED TO A REPLY COMMAND BY THE    @ZA03236*
*                   SUBSYSTEM                                 @ZA03236*
*                                                              Y02669*
*                   THE SCAN POINTER IS RELOCATED AT THE       Y02669*
*                   BEGINNING OF THE VERB AND A BRANCH TAKEN   Y02669*
*                   TO IEE0403D.                               Y02669*
*                                                              Y02669*
* NOTES:                                                       Y02669*
*   CHARACTER-CODE= THIS MODULE IS CHARACTER CODE DEPENDENT.   Y02669*
*                   IF A CHARACTER SET OTHER THAN EBCDIC       Y02669*
*                   IS USED, THIS MODULE WILL NOT ASSEMBLE     Y02669*
*                   CORRECTLY.  THE TRANSLATE TABLE LOCATED    Y02669*
*                   AT THE END OF THIS MODULE IS WHERE THE     Y02669*
*                   CHARACTER DEPENDENT CODE IS LOCATED.       Y02669*
*   DEPENDENCIES  = NONE                                       Y02669*
*   RESTRICTIONS  = NONE                                       Y02669*
*   REGISTER-                                                  Y02669*
*    CONVENTIONS  = NONE                                       Y02669*
*   PATCH-LABEL   = PATCH                                      Y02669*
*                                                              Y02669*
* MODULE TYPE:                                                 Y02669*
*   PROCESSOR     = ASSEMBLER                                  Y02669*
*   MODULE SIZE   = X'344'                                    @ZA16354*
*   ATTRIBUTES    = ZERO PSW PROTECT KEY,REFRESHABLE,          Y02669*
*                   SUPERVISOR KEY,PAGED LPA                   Y02669*
*                                                              Y02669*
* ENTRY POINT     = IEE5403D                                   Y02669*
*   PURPOSE       = TRANSLATIONS OF COMMANDS TO UPPER CASE.    Y02669*
*   LINKAGE       = BRANCH FROM IEE0303D                       Y02669*
*   INPUT DATA    = REG 1 CONTAINS A POINTER TO COMMAND BUFFER Y02669*
*                   REGISTER 2 CONTAINS XSA POINTER            Y02669*
*                   REGISTER 0 CONTAINS UCM POINTER,           Y02669*
*                   READER AUTHORITY AND ASID                  Y02669*
*                                                              Y02669*
* REGISTER SAVED  = NONE                                       Y02669*
* REGISTER CONTENTS DURING                                     Y02669*
* PROCESSING      = REG 0  - COMMAND SOURCE                    Y02669*
*                   REG 1  - COMMAND INPUT BUFFER              Y02669*
*                   REG 2  - XSA                               Y02669*
*                   REG 3  - BASE REGISTER                     Y02669*
*                   REG 4  - WORK REGISTER                     Y02669*
*                   REG 5  - SCAN REGISTER                     Y02669*
*                   REG 6  - WORK REGISTER                     Y02669*
*                   REG 7  - UCM POINTER                       Y02669*
*                   REG 8  - WORK REGISTER                     Y02669*
*                   REG 9  - (OPEN)                            Y02669*
*                   REG 10 - SCAN POINTER                      Y02669*
*                   REG 11 - (OPEN)                            Y02669*
*                   REG 12 - WORK REGISTER                     Y02669*
*                   REG 13 - WORK REGISTER                     Y02669*
*                   REG 14 - RETURN REGISTER                   Y02669*
*                   REG 15 - LINK REGISTER                     Y02669*
* REGISTERS                                                    Y02669*
*   RESTORED      = NONE                                       Y02669*
*                                                              Y02669*
* EXIT - NORMAL   = IEE0403D                                   Y02669*
*   CONDITIONS    = NORMAL SVC 34 PROCESSING                   Y02669*
*   OUTPUT DATA   = LOWER CASE LETTERS CONVERTED TO UPPER CASE Y02669*
*                   IF REQUIRED COMMANDS WRITTEN TO HARDCOPY   Y02669*
*                   XAL POINTS TO BEGINNING OF VERB            Y02669*
*                   REGISTER 1 POINTS TO COMMAND BUFFER        Y02669*
*                   IF TSO XAA FIELD OF XSA CONTAINS ASID      Y02669*
*                   IF COMMAND STARTS WITH NUMBER, XASSDS4    @ZA03236*
*                   SET ON                                    @ZA03236*
*   RETURN CODE   = NONE                                       Y02669*
*                                                              Y02669*
* EXIT - ERROR    = NONE                                       Y02669*
*   CONDITIONS    = NONE                                       Y02669*
*   OUTPUT DATA   = NONE                                       Y02669*
*   RETURN CODE   = NONE                                       Y02669*
*                                                              Y02669*
* EXTERNAL                                                     Y02669*
*   REFERENCES    =                                            Y02669*
*   ROUTINES      = IEE0403D                                   Y02669*
*   DATA AREAS    = COMMAND BUFFER                             Y02669*
*   CONTROL BLOCKS= EXTENDED SAVE AREA                         Y02669*
*                   UNIT CONTROL BLOCK                         Y02669*
*   TABLES        = TRANSLATE TABLE                            Y02669*
*   MACRO         = NONE                                       Y02669*
*   ENQUEUED                                                   Y02669*
*    RESOURCES    = NONE                                       Y02669*
*   LOCKS         =                                            Y02669*
*   CHANGE LEVEL  = Y02669,ZA14128,ZA16354                    @ZA16354*
*                                                              Y02669*
* SYSGENS         = THIS MODULE IS INCLUDED IN LOAD MODULE     Y02669*
*                   IGC0003D AND LOADED INTO LPALIB. IT IS     Y02669*
*                   INCLUDED AT SYSGEN TIME FROM DLIB AOSB3    Y02669*
*                   USING SYSGEN MACRO SGIEF441.               Y02669*
*                                                              Y02669*
* SYSTEM LIBRARIES= NONE                                       Y02669*
*                                                              Y02669*
* MESSAGES        = NONE                                       Y02669*
**********************************************************************
         TITLE ' COMMUNICATIONS VECTOR TABLE'
TRCVT    DSECT
         CVT
         TITLE ' EXTENDED SAVE AREA'
TRXSA    DSECT
         IEEXSA
         TITLE ' UNIT CONTROL MODULE'
TRUCM    DSECT
         IEECUCM
         TITLE ' TRANSLATE'
IEE5403D CSECT
* A 188500-189000,411500,411820                                  YM1788
* C 249800,249920,314201,314401,314500,315500,411620,411900      YM1788
* D 194500-194600                                                YM1788
* A 249100-249400                                                YM2309
* A 249784,249920                                              @Y04979P
* C 249960                                                     @Y04979P
* D PROLOG APPROX 024100                                       @ZA14128
* C TRANS0 APPROX 249000-272000                                @ZA14128
* C TRTAB APPROX 418000                                        @ZA16354
         SPACE
* R E G I S T E R   A S S I G N M E N T S
         SPACE
R0       EQU   0     UCM INDICATOR                                 IMCS
R1       EQU   1    INPUT REG, PTR TO BUFFER
R2       EQU   2    XSA BASE REG
R3       EQU   3    MODULE BASE REG
R4       EQU   4    XSA WORKREG
R5       EQU   5    TRANSLATE WORK REG
R6       EQU   6    TRANSLATE WORK REG
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15   XCTL PARA-REG
         SPACE
         SPACE
* B A S E   M O D U L E
BLANK    EQU   X'40'
D3       EQU   3                                                  20030
D5       EQU   5                                                 A49195
D7       EQU   7                                                  20030
IEE298I  EQU   X'2B'                    CODE FOR MESSAGE IEE298I Y01120
         SPACE
D0       EQU   0                        USED FOR OFFSETS,LENGTHS,ETC.
D1       EQU   1                        *
D2       EQU   2                        *
D4       EQU   4                        *
D6       EQU   6                                                 YM1788
D8       EQU   8                                                 YM1788
         SPACE
X80      EQU   X'80'
UCMEQU   EQU   UCMSDS1A+UCMSDS1B                                  M0130
         EJECT
         BALR  R3,0
         USING *,R3
         MODID BRANCH=YES                                        Y01886
         USING TRXSA,R2
         MVI   XAE,INVALID        ZERO OUT XAE FIELD             Y02669
         EJECT
* T R A N S L A T E   T H E   C O M M A N D
         SPACE
TRANS0   LA    R10,D3(R1)         POINT TO BYTE BEFORE TEXT    @ZA14128
         LA    R8,D1(R10)         POINT TO TEXT                @ZA14128
         LH    R7,0(R1)            LENGTH OF BUFFER
         AR    R7,R1
         ST    R7,XAP              SAVE FOR LATER USE BY         Y02669
*                                  MODULES ISSUING IEEBUFSC      Y02669
         BCTR  R7,0                END OF BUFFER
         LA    R6,1                INCREMENT
         SPACE
***********************************************************************
*   SKIP OVER CHARACTERS UNTIL THE 1ST NON-BLANK IS FOUND.     @ZA14128
*   IF NONE ARE FOUND THEN CONTROL IS GIVEN TO LABEL CHKMCS.   @ZA14128
*   IF A NON-BLANK IS FOUND THEN SEE IF IT IS A //BLANK. IF    @ZA14128
*   IT IS NOT THEN CONTROL IS GIVEN TO LABEL NOSLASH. IF IT    @ZA14128
*   IS THEN SKIP OVER IT AND CONTINUE LOOKING FOR A NON-BLANK  @ZA14128
*   CHARACTER.                                                 @ZA14128
***********************************************************************
         SPACE
SKIPBLKS EQU   *
         BXH   R10,R6,CHKMCS      UPDATE PTR. TEST BUFFER END  @ZA14128
         CLI   D0(R10),BLANK      IS CHAR A BLANK?             @ZA14128
         BE    SKIPBLKS           YES THEN BRANCH              @ZA14128
         CLC   D0(D3,R10),XSLASH  NOT BLANK. IS IT A //BLANK?  @ZA14128
         BNE   NOSLASH            NO THEN BRANCH               @ZA14128
         LA    R10,D2(R10)        YES IT'S //BLANK. SKIP OVER  @ZA14128
         B     SKIPBLKS           FIND NEXT NON-BLANK CHAR     @ZA14128
         EJECT
***********************************************************************
*   THE COMMAND HAS BEEN FOUND. R10 POINTS TO THE BEGINNING    @ZA14128
*   OF THE COMMAND.                                            @ZA14128
*
*   TRANSLATE ALL CHARACTERS TO UPPER CASE AS LONG AS THEY     @ZA14128
*   ARE NOT IN SINGLE QUOTES. THESE WILL REMAIN AS THEY WERE   @ZA14128
*   ENTERED.                                                   @ZA14128
*
*   IF AN INVALID CHARACTER IS FOUND DURING TRANSLATION THEN   @ZA14128
*   FIELD XAE IN THE XSA IS SET TO INDICATE THIS TO MODULE     @ZA14128
*   IEE0403D.                                                  @ZA14128
*
*   WHEN THE END OF THE BUFFER IS REACHED THEN CONTROL IS THEN @ZA14128
*   GIVEN TO LABEL CHKMCS.                                     @ZA14128
***********************************************************************
         SPACE
NOSLASH  EQU   *                                               @ZA14128
         LR    R8,R10             SAVE PTR TO COMMAND          @ZA14128
FNDAPOS  EQU   *                                               @ZA14128
         CLI   D0(R10),C''''      IS CHAR OPENING APOSTROPHE?  @ZA14128
         BE    INQUOTE            YES THEN BRANCH              @ZA14128
         TR    D0(1,R10),TRTAB    TRANSLATE THE CHARACTER      @ZA14128
         CLI   D0(R10),INVALID    WAS IT AN INVALID CHARACTER? @ZA14128
         BNE   NEXTCHAR           NO THEN BRANCH               @ZA14128
         MVI   XAE,IEE298I        INDICATE INVALID CHAR FOUND  @ZA14128
         B     NEXTCHAR           GET NEXT CHARACTER           @ZA14128
INQUOTE  EQU   *                                               @ZA14128
         BXH   R10,R6,CHKMCS      UPDATE PTR. TEST BUFFER END  @ZA14128
         CLI   D0(R10),C''''      CLOSING APOSTROPHE FOUND?    @ZA14128
         BNE   INQUOTE            NO THEN BRANCH               @ZA14128
NEXTCHAR EQU   *                                               @ZA14128
         BXH   R10,R6,CHKMCS      UPDATE PTR. TEST BUFFER END  @ZA14128
         B     FNDAPOS            CONTINUE TRANSLATION         @ZA14128
         EJECT
* C H E C K  F O R  M C S
CHKMCS   EQU   *
         USING TRCVT,R7                 ADDRESS CVT                 MCS
         L     R7,CVTPTR                GET CVT ADDR
         L     R7,CVTCUCB               PTR TO UCM BASE             MCS
         DROP  R7                       NO LONGER NEED CVT          MCS
         USING TRUCM,R7                 ADDRESS UCM                 MCS
         MVI   XAU,ZERO            ZERO OUT XAU FIELD             M5521
         LTR   R0,R0               IS COMMAND FROM INPUT STREAM   M5521
         BM   INPUTST              YES, ZEROS IN BOTH XAU AND XAJ M5521
         STH   R0,XAA              SAVE TJID/CONSOLE ID           M5521
         TM    XAA,X'80'           TEST HIGH ORDER BIT FOR TJID   M5521
         BZ    NONTSO              NO. NOT TSO TJID               M5521
         XI    XAA,X'80'           YES. TURN OFF BIT              M5521
         B     INMCS               CHECK FOR MCS                 Y01939
NONTSO   STC   R0,XAU              SAVE CONSOLE ID IN XSA         M5521
INPUTST  MVC   XAA(2),ZEROS        ZERO TJID FLD IN XSA WHEN NON-TSO
*                                                                 M5521
         EJECT
* C H E C K  F O R  Q U E U E  T O  H A R D C O P Y
INMCS    EQU   *                                                    MCS
         SH    R7,FOUR                  PT TO WORD BEFORE UCM BASE  MCS
         L     R7,D0(R7)                PTR TO MCS UCM PREFIX       MCS
         DROP  R7                       NO LONGER NEED UCM BASE     MCS
         USING UCMPRFX,R7               ADDRESS UCM MCS PREFIX      MCS
         TM    UCMSFLGS,UCMSYSB    IS HARDCOPY REQUIRED?          M0130
         BZ    NOHCPY              BRANCH IF NO                   M0130
         TM    UCMSDS1,UCMEQU      STCMDS OR INCMDS TO HARD COPY? M0130
         BM    HARDEXST            BRANCH IF YES                  M0130
         TM    UCMSFLGS,UCMSYSC         QUEUE TO HARDCOPY           MCS
         BZ    NOHCPY                   NO                          MCS
HARDEXST EQU   *                                                  M0130
         CLI   D0(R8),NUMBER      IS THIS HASP FORM REPLY?     @ZA03236
         BL    CHKRPLY            LOOK FOR REPLY               @ZA03236
         OI    XASSDS,XASSDS4     SHOW COMMAND NOT             @ZA03236
*                                 HARDCOPIED FOR 0403D         @ZA03236
         B     NOHCPY             DONT HARDCOPY                @ZA03236
CHKRPLY  CLC   D0(D2,R8),R        IS THIS THE 'R' COMMAND?       YM1788
         BE    NOHCPY             YES, BYPASS WTO                Y01012
         CLC   D0(D6,R8),REPLY    IS THIS THE 'REPLY' COMMAND?   YM1788
         BE    NOHCPY             YES, BYPASS WTO                Y01012
         CLC   D0(D2,R8),K        IS THIS THE 'K' COMMAND?       YM1788
         BE    NOHCPY             YES, DO NOT SEND CMD TO HARDCPY 21002
         CLC   D0(D8,R8),CONTROL  IS THIS THE 'CONTROL' COMMAND? YM1788
         BE    NOHCPY             YES, DO NOT SEND CMD TO HARDCPY 21002
         EJECT
* H A R D C O P Y  W T O  R O U T I N E                             MCS
         LH    R5,D0(R1)                GET LENGTH OF CIB           MCS
         L     R12,D0(R1)               SAVE CONTENTS OF REG 1   A41171
         AR    R5,R1                    PT TO ONE PAST BUFFER END   MCS
         LA    R13,D4(R1)               PT TO START OF TEXT AREA    MCS
TRSCAN   EQU   *                                                    MCS
         BCTR  R5,D0                    BACK UP ONE BYTE            MCS
*                                                                   MCS
* SCAN BACKWARDS IN COMMAND INPUT BUFFER TO FIND A NON-BLANK CHAR-  MCS
* ACTER. THIS IS TO PREVENT WTO-ING UNNECESSARY TRAILING BLANKS.    MCS
*                                                                   MCS
         CLI   D0(R5),BLANK             IS THIS BYTE BLANK          MCS
         BNE   TRWTO                    NO. GO WRITE COMMAND        MCS
         CR    R13,R5                   AT THE BEGINNING OF BUFFER  MCS
         BCR   8,R14                    YES. BLANK CIB. NO-OP       MCS
         B     TRSCAN                   NO. CONTINUE BACK SCAN      MCS
TRWTO    EQU   *                                                    MCS
         SR    R5,R1                    COMMAND LENGTH LESS ONE     MCS
         LA    R5,D1(R5)                COMMAND LENGTH              MCS
         STH   R5,D0(R1)                WTO LENGTH FIELD            MCS
         SPACE
HCFLG    EQU   X'0200'                  Q-TO-HARDCOPY ONLY FLAG     MCS
         SPACE
         LA    R5,HCFLG                 SET Q-TO-HARDCOPY BIT ON    MCS
         STH   R5,D2(R1)                IN MCSFLAG FLD OF BUFFER    MCS
         LR    R6,R0                    SAVE PARAMETER REGISTER     MCS
         LR    R5,R1                    SAVE CIB PTR                MCS
         WTO   MF=(E,(1))               WRITE COMMAND TO HARDCOPY   MCS
         LR    R0,R6                    RESTORE  ...                MCS
         LR    R1,R5                    ...REGISTERS                MCS
         ST    R12,D0(R1)               RESTORE ORIGINAL REG 1   A41171
NOHCPY   EQU   *                                                    MCS
         LTR   R0,R0                    COMMAND FROM INPUT STREAM   MCS
         BNL   SCANVERB                 NO. FROM A CONSOLE          MCS
         MVI   XAU,X80                  INPUT STREAM INDICATOR      MCS
         STH   R0,XAS                   R/I COMMAND AUTHORITY       MCS
         EJECT
SCANVERB EQU   *                                                    MCS
         SPACE
         CLI   XAE,IEE298I        INVALID CHARACTER              Y01120
         BE    SETIND             YES SAVE INDICATOR              20030
         ST    R1,XAR             POINTER TO COMMAND IMAGE        20030
XBACK    MVI   XAV,BLANK          INIT VERB SLOT TO BLANKS        20030
         MVC   XAV+D1(D7),XAV                                     20030
         L     R7,XAP             GET ADDR OF BUFFER END +1 Y02669
         BCTR  R7,D0              GET END OF BUFFER               20030
         LA    R4,D4(R1)          BYPASS HEADER                   20030
         LA    R6,D1         INSERT INCREMENT                     20030
         CLC   D0(D3,R4),XSLASH   BUFFER PRECEECED BY //BLANK     20030
         BNE   SCAN               IF NOT, BEGIN VERB SCAN         20030
         LA    R4,D3(R4)          YES, BYPASS HEADER              20030
SCAN     EQU   *                                                  20030
         CLI   D0(R4),BLANK       IS THIS CHARACTER A BLANK       20030
         BNE   ADJBUFFR           IF NOT, ADJUST BUFFER          A49195
         BXLE   R4,R6,SCAN        IF SO UPDATE PTR AND TEST NEXT  20030
*        IF NON BLANK CHAR WAS NOT FOUND ENTIRE BUFFER WAS BLANK  20030
         BR   R14                                                 20030
SETIND   EQU   *                                                  20030
         ST    R1,XAR             INDICATOR MUST BE REPLACED      20030
         MVI   XAE,IEE298I        ABOVE INSTRCTION OVERLAYS CODE Y01120
         B     XBACK              CONTINUE PROCESSING             20030
ADJBUFFR EQU   *                                                 A49195
         LA    R6,D4(R1)          POINT TO BEGINNING OF BUFFER   A49195
         CR    R6,R4              VERB IN FIRST BUFFER POSITION? A49195
         BE    SAVEPTR            YES, NO ADJUSTMENT NEEDED      A49195
         LR    R6,R7              GET END OF BUFFER ADDRESS      A49195
         SR    R6,R4              COMPUTE VALID BUFFER LENGTH    A49195
         EX    R6,SHIFTMVE        MOVE VERB INTO FIRST POSITION  A49195
         LA    R4,D5(R6,R1)       POINT TO 1ST INVALID POSITION  A49195
         MVI   D0(R4),BLANK       INSERT BLANK                   A49195
         LR    R6,R7              GET END OF BUFFER ADDRESS      A49195
         CR    R6,R4              IS THIS LAST CHAR IN BUFFER    SM4227
         BE    SETPTR             YES. DONT BLANK OUT ANY MORE   SM4227
         SR    R6,R4              OBTAIN LENGTH OF BLANKS        A49195
         BCTR  R6,D0              DECREMENT THE LENGTH BY ONE    A49195
         EX    R6,PADMOVE         BLANK OUT THE INVALID PART OF  A49195*
                                       BUFFER
SETPTR   EQU   *
         LA    R4,D4(R1)          SET POINTER TO THE VERB TEXT   A49195
SAVEPTR  EQU   *                                                  20030
         ST    R4,XAL             PUT PTR TO VERB IN XAL          20030
         L     R15,V0403D         GET ADDRESS OF ROUTER ROUTINE  Y02669
         BR    R15                GO TO IEE0403D (ROUTER)        Y02669
         EJECT
SHIFTMVE MVC   D4(,R1),D0(R4)     SHIFT BUFFER INSTRUCTION       A49195
PADMOVE  MVC   D1(,R4),D0(R4)     ADD BLANKS INSTRUCTION         A49195
*                                 CONSTANT MUST BE CONTIGUOUS    Y01120
FOUR     DC    H'0004'            WORK CONSTANT                     MCS
ZEROS    DC    X'0000'
V0403D   DC    V(IEE0403D)             POINTER TO ROUTER ROUTINE Y02669
ZERO     EQU   0                                                 A21432
INVALID  EQU   X'00'                                             A21432
K        DC    CL2'K '            COMPRAND FOR 'K' COMMAND       YM1788
CONTROL  DC    CL8'CONTROL '      COMPRAND FOR 'CONTROL' COMMAND YM1788
XSLASH   DC   CL3'// '                                            20030
R        DC    CL2'R '            COMPRAND FOR 'R' COMMAND       YM1788
REPLY    DC    CL6'REPLY '        COMPRAND FOR 'REPLY' COMMAND   YM1788
PATCH    DS    CL50'*** IEE5403D PATCH AREA ***'               @ZA14128
NUMBER   EQU   X'F0'              CHECK FOR REPLY CMD          @ZA03236
         TITLE ' TRANSLATE TABLE'
TRTAB    DS    0F
         DC    256FL1'00'     ZERO FILLER FOR INVALID CHARACTER CHECK  *
                                                               @ZA16354
         ORG   TRTAB+C' '
         DC    FL1'64'        BLANK
         ORG   TRTAB+C''''
         DC    FL1'125'       APOSTROPHE
         ORG   TRTAB+C'#'
         DC    FL1'123'       POUND SIGN
         ORG   TRTAB+C'$'
         DC    FL1'91'        DOLLAR SIGN
         ORG   TRTAB+X'50'     EBCDIC
         DC    FL1'80'        AND SIGN
         ORG   TRTAB+C'('
         DC    FL1'77'        LEFT PARENTHESES
         ORG   TRTAB+C')'
         DC    FL1'93'        RIGHT PARENTHESES
         ORG   TRTAB+C'*'
         DC    FL1'92'        ASTERISK
         ORG   TRTAB+C'+'
         DC    FL1'78'        PLUS SIGN
         ORG   TRTAB+C','
         DC    FL1'107'       COMMA
         ORG   TRTAB+C'-'
         DC    FL1'96'        MINUS SIGN
         ORG   TRTAB+C'.'
         DC    FL1'75'        PERIOD
         ORG   TRTAB+C'/'
         DC    FL1'97'        SLASH
         ORG   TRTAB+C'Ö'
         DC    FL1'74'             CENT SIGN
         ORG   TRTAB+C'<'
         DC    FL1'76'             LESS THAN SIGN
         ORG   TRTAB+C'×'
         DC    FL1'79'
         ORG   TRTAB+C'!'
         DC    FL1'90'             EXCLAMATION POINT
         ORG   TRTAB+C';'
         DC    FL1'94'             SEMICOLON
         ORG   TRTAB+C'^'
         DC    FL1'95'
         ORG   TRTAB+C'%'
         DC    FL1'108'            PERCENT SIGN
         ORG   TRTAB+C'_'
         DC    FL1'109'            DASH SIGN
         ORG   TRTAB+C'>'
         DC    FL1'110'            GREATER THAN SIGN
         ORG   TRTAB+C'?'
         DC    FL1'111'            QUESTION MARK
         ORG   TRTAB+C':'
         DC    FL1'122'            COLON
         ORG   TRTAB+C'@'
         DC    FL1'124'            AT SIGN
         ORG   TRTAB+C'"'
         DC    FL1'127'            QUOTES
         ORG   TRTAB+C'0'
         DC    AL1(240)       ZERO
         DC    AL1(241)       ONE
         DC    AL1(242)       TWO
         DC    AL1(243)       THREE
         DC    AL1(244)       FOUR
         DC    AL1(245)       FIVE
         DC    AL1(246)       SIX
         DC    AL1(247)       SEVEN
         DC    AL1(248)       EIGHT
         DC    AL1(249)       NINE
         ORG   TRTAB+C'='
         DC    FL1'126'       EQUAL SIGN
         ORG   TRTAB+C'@'
         DC    FL1'124'       AT SIGN
         ORG   TRTAB+C'A'-X'40'    EBCDIC
         DC    AL1(193)       A TRANSLATED FROM LOWER CASE
         DC    AL1(194)       B TRANSLATED FROM LOWER CASE
         DC    AL1(195)       C TRANSLATED FROM LOWER CASE
         DC    AL1(196)       D TRANSLATED FROM LOWER CASE
         DC    AL1(197)       E TRANSLATED FROM LOWER CASE
         DC    AL1(198)       F TRANSLATED FROM LOWER CASE
         DC    AL1(199)       G TRANSLATED FROM LOWER CASE
         DC    AL1(200)       H TRANSLATED FROM LOWER CASE
         DC    AL1(201)       I TRANSLATED FROM LOWER CASE
         ORG   TRTAB+C'J'-X'40'    EBCDIC
         DC    AL1(209)       J TRANSLATED FROM LOWER CASE
         DC    AL1(210)       K TRANSLATED FROM LOWER CASE
         DC    AL1(211)       L TRANSLATED FROM LOWER CASE
         DC    AL1(212)       M TRANSLATED FROM LOWER CASE
         DC    AL1(213)       N TRANSLATED FROM LOWER CASE
         DC    AL1(214)       O TRANSLATED FROM LOWER CASE
         DC    AL1(215)       P TRANSLATED FROM LOWER CASE
         DC    AL1(216)       Q TRANSLATED FROM LOWER CASE
         DC    AL1(217)       R TRANSLATED FROM LOWER CASE
         ORG   TRTAB+C'S'-X'40'    EBCDIC
         DC    AL1(226)       S TRANSLATED FROM LOWER CASE
         DC    AL1(227)       T TRANSLATED FROM LOWER CASE
         DC    AL1(228)       U TRANSLATED FROM LOWER CASE
         DC    AL1(229)       V TRANSLATED FROM LOWER CASE
         DC    AL1(230)       W TRANSLATED FROM LOWER CASE
         DC    AL1(231)       X TRANSLATED FROM LOWER CASE
         DC    AL1(232)       Y TRANSLATED FROM LOWER CASE
         DC    AL1(233)       Z TRANSLATED FROM LOWER CASE
         ORG   TRTAB+C'A'
         DC    AL1(193)       A
         DC    AL1(194)       B
         DC    AL1(195)       C
         DC    AL1(196)       D
         DC    AL1(197)       E
         DC    AL1(198)       F
         DC    AL1(199)       G
         DC    AL1(200)       H
         DC    AL1(201)       I
         ORG   TRTAB+C'J'
         DC    AL1(209)       J
         DC    AL1(210)       K
         DC    AL1(211)       L
         DC    AL1(212)       M
         DC    AL1(213)       N
         DC    AL1(214)       O
         DC    AL1(215)       P
         DC    AL1(216)       Q
         DC    AL1(217)       R
         ORG   TRTAB+C'S'
         DC    AL1(226)       S
         DC    AL1(227)       T
         DC    AL1(228)       U
         DC    AL1(229)       V
         DC    AL1(230)       W
         DC    AL1(231)       X
         DC    AL1(232)       Y
         DC    AL1(233)       Z
         END
