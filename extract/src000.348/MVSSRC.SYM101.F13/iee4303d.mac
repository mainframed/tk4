 TITLE 'IEE4303D - VARY MASTER CONSOLE COMMAND PROCESSOR'
***********************************************************************
*
* MODULE NAME      = IEE4303D
*   CSECT NAME     = IEE4303D
*   LOAD MODULE NAME
*                  = IGC0003D
*
* DESCRIPTIVE NAME = VARY MASTER CONSOLE COMMAND PROCESSOR
*
* COPYRIGHT        = MISSING
*
* STATUS           = OS/VS/2 RELEASE 3                         @Z30LPTJ
*
* FUNCTION         = THIS MODULE PROCESSES THE COMMANDS :
*                    VARY UNIT,MSTCONS AND
*                    VARY (I-XXX,O-XXX),MSTCONS
*                    FOR THE CONSOLE SWITCH ROUTINE IGCXLO7B
*
* OPERATION        = THIS MODULE RECEIVES CONTROL FROM THE INITIAL
*                    KEYWORD SCAN MODULE - IEE3203D.
*                    THE UNIT CONTROL MODULE ENTRY (UCME) OF THE
*                    NAMED UNIT IS CHECKED TO ASSURE THAT THE UNIT
*                    IS A CONSOLE CAPABLE OF INPUT AND OUTPUT.
*                    IF THE COMMAND NAMES A COMPOSITE CONSOLE,
*                    INDICATED BY (I-XXX,O-XXX), THE UNITS ARE
*                    CHECKED TO ASSURE THEY ARE ACTIVE AND THAT
*                    THEY WERE SYSGENED AS A COMPOSITE CONSOLE.
*                    IF THE UNIT NAMED DOES NOT MEET THESE
*                    REQUIREMENTS THE COMMAND IS REJECTED.
*                    IF THE CONSOLE IS ACTIVE PENDING TO BE CHANGED
*                    TO AN ACTIVE STATE, THE COMMAND IS ALSO REJECTED.
*                    HOWEVER, IF THE CONSOLE IS NON-ACTIVE BUT THE
*                    UCME SHOWS A STATUS WHERE IT IS PENDING TO BE
*                    CHANGED TO THE CONSOLE STATE, THE OPERATOR IS
*                    INFORMED VIA A 'DEVICE CHANGING STATUS' MSG.
*                    PUT OUT BY IEE2103D.  THE COMMAND, THEN IS
*                    REJECTED AT THIS TIME.
*                    THE AUTHORITY OF THE ISSUING CONSOLE IS
*                    CHECKED.  THE COMMAND IS PROCESSED IF IT IS
*                    ISSUED BY THE EXISTING MASTER CONSOLE OF THE
*                    SYSTEM.  IF THE MASTER CONSOLE ALTERNATE CHAIN
*                    IS EXHAUSTED, THE COMMAND IS ACCEPTED FROM
*                    ANY CONSOLE.
*                    THE MODULE THEN PASSES TO THE CONSOLE SWITCH
*                    ROUTINE A FLAG INDICATING THE CAUSE OF THE
*                    SWITCH, THE ADDRESS OF THE NEW MASTER CONSOLE
*                    UCME AND THE ADDRESS OF THE OLD MASTER
*                    CONSOLE UCME OR ZEROES IF THE COMMAND WAS
*                    ISSUED BY ANY OTHER MEANS THAT THE OLD MASTER.
*
* NOTES
*   DEPENDENCIES   = NONE
*   CHARACTER/CODE DEPENDENCIES
*                  = THIS MODULE CAN BE ASSEMBLED WITH ANY NEW
*                    CHARACTER/CODE SET.
*
* RESTRICTIONS     = REG2 PTS TO THE XSA ON INPUT
*   REGISTER CONVENTIONS
*                  = REGS ARE DEFINED AT LABEL: DEFREG
*   PATCH LABEL    = PATCH
*
* MODULE TYPE      = CSECT
*   PROCESSOR      = ASSEMBLER
*   MODULE SIZE    = VS/2 = '296'                              @Z30LPTJ
*   ATTRIBUTES     = REENTRANT, REUSABLE, REFRESHABLE, PAGED LPA,
*                    ZERO PSW PROTECT KEY, SUPERVISOR MODE
*
* ENTRY POINT      = IEE4303D FROM IEE3203D
*   PURPOSE        = TO PROCESS THE V MSTCONS COMMAND
*   LINKAGE        = BRANCH
*   INPUT          = THE VARY SCAN AND ROUTER MODULE PASSES A PTR
*                    TO THE XSA IN REG2.
*                    THE XSA CONTAINS A POINTER TO THE COMMAND
*                    OPERAND FIELD AND ALSO CONTAINS THE COMMAND
*                    VERB (V OR VARY).
*                    THE CVT POINTER IS LOCATED AT ADDRESS 16,
*                    THE CVT IS USED TO LOCATE THE UCM AND THE UCME'S
*                    EACH UCME CONTAINS A POINTER TO ITS UCB.
*   REGISTERS SAVED  = NONE
*    REGISTER USAGE= R2 PTR TO XSA
*                    R3 UCM PTR
*                    R5 CUME PTR
*                    R9 UNIT PTR
*                    R10 UCB PTR
*                    R13 ADDRESSABILITY
*                    R14 RETURN ADDRESS
*   REGISTERS RESTORED
*                  = NONE
*
* EXIT - NORMAL    = RETURN
*   CONDITIONS     = WHEN THE COMMAND HAS BEEN EXECUTED
*   OUTPUT         = A NEW CONSOLE IS THE MASTER CONSOLE
*   RETURN CODE    = NONE
*
* EXIT - ERROR     = IEE2103D OR IEE0503D VIA BRANCH
*   CONDITIONS     = ERROR HAS BEEN DETECTED  IN THE V COMMAND
*   OUTPUT         = REG2 PTS TO THE XSA
*                    XAE HAS APPROPRIATE MSG CODE
*   RETURN CODES   = NONE
*
* EXTERNAL REFERENCES
*   ROUTINES       = IEE2103D
*                    IEE0503D
*                    COMM. TASK
*   DATA AREAS     = FOR EXIT TO 0503D OR 2103D - SEE OUTPUT-DATA
*                    FOR COMM. TASK =
*                    IF THE COMMAND IS PROCESSED, THE CONSOLE SWITCH
*                    ROUTINE IS PASSED THE ADDRESS OF THE XSA
*                    IN REG1.  THE XSA CONTAINS:
*                       PTR TO MODULE NAME
*                       UNUSED
*                       NAME OF ROUTINE (IGCXLO7B)
*                       X'00' CONSOLE SWITCH TO BE PERFORMED
*                       X'04' RESULT OF EXTERNAL INTERRUPT
*                       X'08' RESULT OF VARY MSTCONS COMMAND
*                       PTR TO NEW MASTER UCME
*                       PTR TO COMMAND ISSUER'S UCME
*
* CONTROL BLOCKS   = 1. EXTENDED SAVE AREA
*                    2. UNIT CONTROL MODULE (UCM)
*                       FIELDS TESTED  - UCMDISP - BIT=UCMDISPA
*                                        UCMCOMPC
*                                        UCMATR - BIT=UCMOF
*                                                     UCMIF
*                                                     UCMUF
*                                        UCMMODE - BIT=UCMAMFA
*                                        UCMUCB
*                                        UCMVEA
*                       FIELDS UPDATED - NONE
*                    3. UNIT CONTROL BLOCK (UCB)
*                       FIELDS TESTED  - UCBNAME
*                                        SRTESTAT- BIT=SRTEDADI
*                       FIELDS UPDATED - NONE
*                    4. CVT
*                       FIELDS TESTED  - CVTCUCB
*                       FIELDS UPDATED - NONE
*
* TABLES           = NONE
*
* MACROS           = SVC 72,SETLOCK
*
* MAPPING MACROS   = IEEXSA
*                    IEECUCM
*                    IEFUCBOB
*                    CVT
*                    IHAPSA
*   SERIALIZATION  = TESTED FIELDS IN THE UCB AND THE UCM WILL BE
*                    PROTECTED BY THE CMS AND LOCAL LOCKS
*                    AGAINST COMM TASK AND ANOTHER VARY COMMAND
*                    LOCKS ARE GOTTEN AT:
*                         MSETLCK
*                    LOCKS ARE RELEASED AT:
*                         MUNSTLK
*                    FIELDS PROTECTED ARE:
*                         UCMMODE - BIT = UCMAMFA  -UCM
*                         UCMATR  - BIT = UCMUF    -UCM
*                         UCMDISP - BIT = UCMDISPA -UCM
*
*                         SRTESTAT- BIT = SRTEDADI -UCB
*
* CHANGE LEVEL     = Y02669, Y02651, Z30LPTJ                   @Z30LPTJ
*
* CHANGE ACTIVITY  = N/A
*
* MESSAGES         = TO IEE0503D FOR ERROR MESSAGES:
*                       IEE307I VARY DELIMITER ERROR
*                       (ISSUED WHEN THE SYNTAX OF THE UNIT FIELD
*                       IS NOT CORRECT)
*                       IEE313I XXX UNIT REF INVALID
*                       (ISSUED WHEN UNIT CANNOT BECOME THE
*                       MASTER CONSOLE)
*                    TO IEE2103D FOR ERROR MESSAGES:
*                       IEE345I VARY AUTHORITY INVALID
*                       (ISSUED WHEN THE COMMANDS SOURCE CONSOLE
*                       DOES NOT HAVE THE COMMAND AUTHORITY TO
*                       VARY A MASTER CONSOLE.)
*
* ABEND CODES      = NONE
*
* PACKAGING        = IEE4303D WILL BE PART OF LOAD MODULE
*                    = IGC0003D
*                    THIS LOAD MODULE ALIASES ARE
*                    = NONE
*                    THIS LOAD MODULE ENTRY POINTS ARE
*                    = IGC0003D
*                      MODULE WILL RESIDE IN : LPALIB
*
* SYSGEN           = SUPPLIED BY SECTIONS:
*                       LOAD MODULE NAME
*                       PACKAGING
*                       THIS CSECT WILL BE INCLUDED AT SYSGEN FROM
*                       DLIB -AOSB3- BY MACRO -SGIEF441-
*
* SYSTEM LIBRARIES = NONE
*
***********************************************************************
         EJECT
***********************************************************************
*  COMMAND TO BE PROCESSED IS:
*
*    ***           ***
*    *               *
*    * XXX           *
*  V *               *,MSTCONS
*    * (I-XXX,O-XXX) *
*    *               *
*    ***           ***
***********************************************************************
         EJECT
IEE4303D CSECT
DEFREG   EQU   *                                                 Y02669
*
R0       EQU   0
R1       EQU   1
MXSABASE EQU   2         XSA PTR
R2       EQU   2
UCMBASE  EQU   3         UCM PTR                                 Y02669
R4       EQU   4                                                 Y02669
R5       EQU   5       UCME PTR                                  Y02669
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9       UNIT PTR
R10      EQU   10      UCB PTR
R11      EQU   11
R12      EQU   12
MBASE    EQU   13      ADDRESSABILITY                            Y02669
MRETURN  EQU   14      RETURN ADDRESS                            Y02669
R14      EQU   14                                                Y02651
R15      EQU   15
*
*
*  TESTING AND BIT SETTING MASKS
*
MRI      EQU   X'80'                   READER/INTERPRETER ID
MSYS     EQU   0                       SYSTEM ID
VARMST   EQU   X'08'
CD44     EQU   44                                                 19084
*
* ERROR MESSAGE CODES
*
ER1      EQU   41                      AUTHORITY INVALID
ER2      EQU   7                       DELIMITER ERROR
ER3      EQU   13                      UNIT REF INVALID
*
ZERO     EQU   0
ONE      EQU   1
TWO      EQU   2
TRE      EQU   3
FOR      EQU   4
SIX      EQU   6
EIT      EQU   8
ELEV     EQU   11
TWLV     EQU   12
EITN     EQU   18                                                 M0676
LPAREN   EQU   C'('
COMMA    EQU   C','
BLANK    EQU   C' '
SVCSVN2  EQU   72
         EJECT
***********************************************************************
*
*        START OF THE ROUTINE
*
***********************************************************************
         BALR  MBASE,ZERO                 ESTABLISH PROGRAM ADDRESS
         USING *,MBASE
         MODID BR=YES                                            Y01886
         USING XSA,MXSABASE            SAVE AREA ADDRESSIBILITY
         L     R5,CVTPTR               POINTER TO CVT USED
         USING USETCVT,R5              USE CVT TO PT.TO UCB INFO
         L     UCMBASE,CVTCUCB         ..
         LR    R8,UCMBASE              SAVE UCM BASE ADDRESS   @YM02762
         DROP  R5                      CVT NO LONGER NEEDED
         USING UCM,UCMBASE             UCM ADDRESSIBILITY        Y02669
***********************************************************************
*                                                                     *
* COMMAND AUTHORITY VERIFICATION                                      *
*                                                                     *
***********************************************************************
           SPACE 10
*****************************************************************Y02651
*                                                                Y02651
*  LOCKS ARE SET HERE USING SUBROUTINE:                          Y02651
*              MSETLCK                                           Y02651
*  LOCKS WILL BE RELEASED WHEN NO NEEDED AT:                     Y02651
*              MUNSTLK                                           Y02651
*                                                                Y02651
         BAL   R4,MSETLCK         GO GET LOCKS FOR UCB AND UCM   Y02651
*                                                                Y02651
*****************************************************************Y02651
           SPACE 10
         L     R9,XAL              UNIT PTR                       19084
         LA    R9,ZERO(R9)                                        19084
         XC    XAL(FOR),XAL              ISSUING CONSOLE PTR IN PARM
         CLI   XAU,MSYS                IF ISSUING SYSTEM-OK
         BE    MPROCESS                GO PROCESS UNIT
         CLI   XAU,MRI                 READER/INTERPRETER ONLY ALLOWED
         BE    MCHAIN                  IF CHAIN EXHAUSTED-GO CHECK
*
         SR    R11,R11                  IS COMMAND FROM MASTER CONSOLE
         IC    R11,XAU                 USE ALGORITHUM TO ACCESS UCME
         LM    R6,R7,UCMVEA            R6 PTS TO FIRST UCM ENTRY
*                                      R7 CONTAINS SIZE OF ENTRY
         BCTR  R11,ZERO                   REF. INTO UCME'S
         MR    R10,R7                  LENGTH 1 ENTRY
         AR    R11,R6                  ADD ADDR OF 1ST ENTRY
         ST    R11,XAL                 SAVE ADR IN PARM LIST
         USING UCMLIST,R11             REFERENCE TO ISSUER'S UCME
         TM    UCMDISP,UCMDISPA        WAS ISSUER MASTER CONSOLE Y02669
         BO    MPROCESS                YES,PROCESS UNIT SPECIFIED
MCHAIN   EQU   *
         TM    UCMMODE,UCMAMFA         IF CHAIN NOT EXHAUSTED    Y02669
         DROP  R11
         BO    MPROCESS                PROCESS UNIT              Y02669
         SPACE 10
*****************************************************************Y02651
*                                                                Y02651
*  RELEASE LOCKS HERE.                                           Y02651
*  EXIT BECAUSE OF ERRORS                                        Y02651
*                                                                Y02651
         BAL   R4,MUNSTLK         GO RELEASE THE LOCKS           Y02651
*                                                                Y02651
*****************************************************************Y02651
         SPACE 10
         B     ERR1                                              Y02651
         EJECT
***********************************************************************
*                                                                     *
* UNIT SYNTAX AND VALIDITY CHECK                                      *
*                                                                     *
***********************************************************************
MPROCESS EQU   *
         XC    XAD(FOR),XAD        ASSUME NON-COMPOSITE UNIT
         CLI   ZERO(R9),LPAREN          IS COMPOSITE SPGCIFIED    19084
         BE    MRTPAR                  YES,GO PROCESS             19084
         CLI   TRE(R9),COMMA            IF NOT COMPOSITE, TEST FOR19084
         BNE   ERR2                    DELIMITER ERROR,IF NOT     19084
         BAL   R1,MCHECK               CHECK FOR VALID UNIT       19084
         USING UCMLIST,R5               VALID,HAS CORRECT SUPPORT 19084
         TM    UCMATR,UCMOF+UCMIF       HAS I/O SUPPORT          Y02669
         BO    MKEYCHK                 UNIT OK,CHECK KEYWORD      M4449
         SPACE 10
*****************************************************************Y02651
*                                                                Y02651
*  ERROR CONDITION, RELEASE LOCKS FIRST AND THEN ISSUE ERROR MSG Y02651
*  LOCKS WILL BE RELEASED AT: MUNSTLK                            Y02651
*  LOCKS WERE SET AT:                                            Y02651
*              MSETLCK                                           Y02651
*                                                                Y02651
         LA    R4,ERR3            ERROR MSG ROUTINE AFTER RELEASEY02651
         B     MUNSTLK            GO RELEASE ALL LOCKS           Y02651
*                                                                Y02651
*****************************************************************Y02651
         EJECT
*                                                                 19084
* COMPOSITE UNIT VERIFICATION CHECK                               19084
*                                                                 19084
MRTPAR   EQU   *                                                  19084
         OI    XADUSWIT,XAIOCOMP        UNIT IS COMPOSITE      @Z30LPTJ
         CLC   ONE(TWO,R9),MIUNT        I-XXX SPECIFIED PROPERLY  19084
         BNE   ERR2                    NO,ERROR                   19084
         CLC   SIX(TRE,R9),MOUNIT       O-XXX SPECIFIED PROPERLY  19084
         BNE   ERR2                    NO,ERROR
         CLC   TWLV(TWO,R9),CMARTPN    IS ), PRESENT              19084
         BNE   ERR2                    NO,ERROR
         LA    R9,TRE(R9)                PTR TO FIRST CHAR OF I-UN19084
         BAL   R1,MCHECK               CHECK FOR VALID I-UNIT     19084
         TM    UCMATR,UCMIF             HAS INPUT SUPPORT        Y02669
         BZ    ERR31                   NO,ERROR                   M0128
         L     R11,UCMCOMPC             GET PTR TO COMP UCME      M0128
         LA     R11,ZERO(R11)           CLEAR HIGH ORDER BYTE     M0128
         LTR   R11,R11                  WAS I-UNIT A VALID COMP   M0128
         BZ    ERR31                    NO,UNIT REF WAS NOT VALID M0128
         DROP  R5                                                 M0128
         USING UCMLIST,R11              ADDRESSABILTY O-UNT UCME  M0128
         L     R10,UCMUCB               GET PTR TO O-UNT UCB      M0128
         USING UCB,R10                  ADDRESSABILITY O-UNT UCB  M0128
         CLC   SIX(TRE,R9),UCBNAME      IS SPEC O-UNT THE LOG COM M0128
         BNE   ERR31                    NO,UNIT REF WAS NOT VALID M0128
         CLI   EITN(R9),BLANK           BLANK FOLLOWS KEYWORD     M0128
         BNE   ERR2                     NO,DELIMETER ERROR        M0128
         TM    UCMATR,UCMUF             IS O-UNIT ACTIVE          M0676
         BZ    MNACTIVE                 NO,SEE IF PENDING CHANGE  M0676
         TM    SRTESTAT,SRTEDADI        IS IT CHNGING TO NONACTIVEM0676
         BO    ERR31        ERROR, CHANGING STATUS AND THEREFORE Y02669
*                           CANNOT BECOME THE MASTER CONSOLE     Y02669
         B     MPARMSET                 GO CHECK I-UNIT           M0676
         EJECT
MNACTIVE EQU   *                                                  M0676
         TM    SRTESTAT,SRTEDADI        IS IT CHNGING TO ACTIVE   M0676
         BO    ERR4                     CHANGING STATUS MSG       M0676
         B     ERR31                    NO,INVALID UNIT REF       M0676
         DROP  R11,R10                                            M0676
         EJECT
MKEYCHK  EQU   *                                                  M4449
         USING UCMLIST,R5               ADDRESSABLTY FOR UNIT CK  M0676
         CLI   ELEV(R9),BLANK          BLANK FLOOWS KEYWORD       M4449
         BNE   ERR2                    NO, DELIMITER ERROR        M4449
         EJECT
*
* PARAMETER LIST FOR SVC 72
*
MPARMSET ST    R5,XAR                  PTR TO NEW MSTCONS UCME
         TM    UCMATR,UCMUF        ACTIVE BIT ON                  19084
         L     R5,UCMUCB                                          19084
         USING UCB,R5                                             19084
         BO    MACTIVE             YES,GO CHECK IF PENDING TO CHNG19084
         TM    SRTESTAT,SRTEDADI   IS NONACTIVE,IS IT PENDING     19084
         BZ    ERR31               NO, PURELY NON ACTIVE, NO GOOD 19084
         B     ERR4                YES, PENDING,CHANGING STATUSMSG19084
*                                                                 19084
MACTIVE  EQU   *                                                  19084
         TM    SRTESTAT,SRTEDADI   PENDING TO CHANGE TO NONACTIVEY02669
         BO    ERR31               ERR,CHNGING STATS ,THEREFORE  Y02669
*                                  CANNOT BECOME THE MASTER CONSOLE
         SPACE 10
*****************************************************************Y02651
*                                                                Y02651
*  FINISHED WITH OUR PROCESSING, AND SVC 72 WILL SOON BE ISSUED  Y02651
*  MUST RELEASE ALL LOCKS GOTTEN ON ENTRY TO THIS MODULE.        Y02651
*  LOCKS WERE OBTAINED AT:                                       Y02651
*     MSETLCK                                                    Y02651
*                                                                Y02651
         BAL   R4,MUNSTLK                                        Y02651
*                                                                Y02651
*****************************************************************Y02651
         SPACE 10
         ST    R8,XAN                 INSERT UCM BASE ADDRESS  @YM02762
*                                     INTO PARAMETER LIST
         MVI   XAE,VARMST             SWITCH RESULT VARY MSTCONS
         MVC   XAX(EIT),MSWTRTN       NAME OF SWITCH RTN
         LA    R5,XAX
         ST    R5,XAP
         LR    R1,R5                  LOAD PTR TO NAME
         SVC   SVCSVN2
         BR    MRETURN                 RETURN
         EJECT
*
***********************************************************************
*
* PHYSICAL UNIT ADDRESS VALIDITY CHECK
*
***********************************************************************
* ON ENTRY REGISTER 8 POINTS TO FIRST CHARACTER OF UNIT NAME
MCHECK   LM    R5,R7,UCMVEA            R5 CONTAINS PTR 1ST UCME
*                                      R6 CONTAINS SIZE UCMES
*                                      R7 CONTAINS ADDR. OF LAST
         USING UCMLIST,R5              REFERENCE TO UCME
MLOOP    L     R10,UCMUCB              PTR TO ASSOCIATED UCB
         USING UCB,R10                 ..
         CLC   ZERO(TRE,R9),UCBNAME         COMPARE PHYSICAL UNIT ADDR
         BE    MFND                    FOUND,UNIT HAS UCME        19874
         BXLE  R5,R6,MLOOP             NO,CONTINUE SEARCH         19874
         B     ERR31                   NOT FOUND,ERROR            M0128
         DROP  R5,R10                                             19874
MFND     BR    R1                      RETURN                     19874
* ON RETURN, REGISTER 5 CONTAINS ADDRESS OF UNITS UCME            19874
*                                                                 19874
         EJECT
***********************************************************************
*                                                                 19084
*  THE FOLLOWING SECTION PASSES A ERROR MESSAGE CODE TO EITHER OF THE
*  MESSAGE WRITING MODULES IGC0503D OR IGG2103D. THE CODE IS PLACED IN
*  THE XSA AT XAE.  THE SOURCE TEXT IS PLACED IN THE XSA AT XAV.  19084
***********************************************************************
*                                                                 19084
ERR1     EQU   *
         BAL   R5,ERLINK1              MSG MOD IS 2103D           19084
         DC    AL1(ER1)                ERROR CODE IS 41           19084
*                                                                 19084
         DS    0H                                                Y02651
ERR2     EQU   *                                                 Y02651
         SPACE 5
***************************************************              Y02651
*                                                                Y02651
*  THIS IS AN ERROR EXIT, LOCKS OBTAINED AT:                     Y02651
*       MSETLCK                                                  Y02651
*  MUST BE RELEASED HERE                                         Y02651
*                                                                Y02651
         BAL   R4,MUNSTLK                                        Y02651
*                                                                Y02651
***************************************************              Y02651
         SPACE 5
         BAL   R5,ERLINK               MSG MOD IS 0503D           19084
         DC    AL1(ER2)                ERROR CODE IS 7            19084
*                                                                 19084
ERR3     DS    0F                                                 19084
         MVI   XAV+TRE,BLANK       PAD WITH BLANKS                19084
ERR32    EQU   *                                                  19084
         MVC   XAV(TRE),ZERO(R9)                                  19084
         BAL   R5,ERLINK               MSG MOD IS 0503D
         DC    AL1(ER3)                ERROR CODE IS 13
*
         DS    0H                                                Y02651
ERR31    EQU   *                                                 Y02651
         SPACE 5
***************************************************              Y02651
*                                                                Y02651
*  THIS IS AN ERROR EXIT, LOCKS OBTAINED AT:                     Y02651
*       MSETLCK                                                  Y02651
*  MUST BE RELEASED HERE                                         Y02651
*                                                                Y02651
         BAL   R4,MUNSTLK                                        Y02651
*                                                                Y02651
***************************************************              Y02651
         SPACE 5
         TM    XADUSWIT,XAIOCOMP   COMPOSITE CONSOLE?          @Z30LPTJ
         BZ    ERR3                NO                             19084
         MVI   XAV+TRE,COMMA       MOVE IN DELIMITER              19084
         MVC   XAV+FOR(TRE),SIX(R9)    MOVE IN O-UNIT             19084
         B     ERR32               GO MOVE IN I-UNIT              19084
*                                                                 19084
ERR4     DS    0F                                                Y02651
         SPACE 5
***************************************************              Y02651
*                                                                Y02651
*  THIS IS AN ERROR EXIT, LOCKS OBTAINED AT:                     Y02651
*       MSETLCK                                                  Y02651
*  MUST BE RELEASED HERE                                         Y02651
*                                                                Y02651
         BAL   R4,MUNSTLK                                        Y02651
*                                                                Y02651
***************************************************              Y02651
         SPACE 5
         MVI   XAV+TRE,BLANK       BLANK AREA                     19084
         TM    XADUSWIT,XAIOCOMP   WAS IT A COMPOSITE?         @Z30LPTJ
         BZ    M1UNT               NO, SINGLE UNIT
         MVI   XAV+TRE,COMMA       MOVE IN DELIMITER              19084
         MVC   XAV+FOR(TRE),SIX(R9)    MOVE IN O-UNIT             19084
M1UNT    EQU   *
         MVC   XAV(TRE),ZERO(R9)   MOVE IN UNITNAME INTO MSG      19084
*                                                                 19084
MMSG     EQU   *                                                  19084
         BAL   R5,ERLINK1          GO TO ERROR ROUTINE            19084
         DC    AL1(CD44)           CHAANGING STATUS MSG           19084
ERLINK   MVC   XAX(EIT),ERMOD1           ERROR MSGE MODULE NAM   Y02669
         L     R15,VCON05         GET ADDRESS OF 0503D           Y02669
         B     MERGO                   EXIT RTN                  Y02669
ERLINK1  EQU   *
         MVC   XAX(EIT),ERMOD2                                   Y02669
         L     R15,VCON21         GET ADDRESS OF 2103D           Y02669
MERGO    MVC   XAE(ONE),ZERO(R5)            INSERT ERROR CODE IN XSA
         BR    R15                EXIT                           Y02669
*
         EJECT
*****************************************************************Y02651
*                                                                Y02651
*  SUBROUTINE WHICH OBTAINS AND RELEASES REQUIRED LOCKS          Y02651
*                                                                Y02651
MSETLCK  EQU   *                                                 Y02651
         STM   R11,R14,XASAVLOC                                  Y02651
         SETLOCK OBTAIN,TYPE=LOCAL,MODE=UNCOND,                  Y02651*
               RELATED=(IEE4303D(MUNSTLK))                       Y02651
         SETLOCK OBTAIN,TYPE=CMS,MODE=UNCOND,                    Y02651*
               RELATED=(IEE4303D(MUNSTLK))                       Y02651
         LM    R11,R14,XASAVLOC                                  Y02651
         BR    R4                                                Y02651
*                                                                Y02651
         SPACE 5                                                 Y02651
*                                                                Y02651
MUNSTLK  EQU   *                                                 Y02651
         STM   R11,R14,XASAVLOC                                  Y02651
         SETLOCK RELEASE,TYPE=CMS,RELATED=(IEE4303D(MSETLCK))    Y02651
         SETLOCK RELEASE,TYPE=LOCAL,RELATED=(IEE4303D(MSETLCK))  Y02651
         LM    R11,R14,XASAVLOC                                  Y02651
         BR    R4                                                Y02651
*                                                                Y02651
*****************************************************************Y02651
         EJECT
*****************************************************************
*
MIUNT    DC    C'I-'                   I-XXX SPECIFICATION
MOUNIT   DC    C',O-'                  0-XXX SPECIFICATION
CMARTPN  DC    C'),'               PAREN, AFTER COMPOSITE         19084
MSWTRTN  DC    C'IGC0407B'             CONSOLE SWITCH RTN      @YM02762
ERMOD1   DC    C'IEE0503D'             ERROR MSG MODULE          Y02669
ERMOD2   DC    C'IEE2103D'             ERROR MSG MODULE          Y02669
         DS    0F                                                Y02669
VCON21   DC    V(IEE2103D)                                       Y02669
VCON05   DC    V(IEE0503D)                                       Y02669
         SPACE 5
PATCH    DC    CL50'***IEE4303D***'                            @Z30LPTJ
        EJECT
UCB      DSECT
         IEFUCBOB
         EJECT
USETCVT  DSECT
         CVT      SYS=VMS
         EJECT
XSADSECT DSECT
         IEEXSA
         EJECT
UUCM     DSECT
         IEECUCM    FORMAT=NEW
         EJECT
         IHAPSA
         END
