         TITLE 'HALT, SWITCH SYNTAX CHECK; TRACE PROCESSING'
*****************************************************************Y02669
*                                                                Y02669
*  MODULE NAME =  IEE1403D                                       Y02669
*                                                                Y02669
*  DESCRIPTIVE NAME =  HALT/ SWITCH SYNTAX CHECKER; TRACE      @Y02669P
*                      PROCESSOR                               @Y02669P
*                                                                Y02669
*  COPYRIGHT =                                                   Y02669
*                                                                Y02669
*  STATUS =  OS/VS2 - RELEASE 3                                @Y30LPSV
*                                                                Y02669
*  FUNCTION =                                                    Y02669
*              THIS MODULE SYNTAX CHECKS THE HALT (Z) AND        Y02669
*              SWITCH (I) COMMANDS, AND EXECUTES THE TRACE     @Y02669P
*              COMMAND.                                        @Y02669P
*                                                                Y02669
*     OPERATION =                                                Y02669
*              A. PERFORMS VALIDITY CHECKING OF THE EOD OPERAND TO    *
*                 ASCERTAIN WHETHER THE OPERAND OF HALT IS NULL OR    *
*                 MISPELLED.                                          *
*                                                                     *
*              B. IF THE OPERAND IS NULL, CONTROL PASSES TO THE  Y02669
*                 APPROPRIATE ERROR ERROR EXIT TO SET UP THE     Y02669
*                 INTERFACE TO THE MESSAGE MODULE (IEE0503D)     Y02669
*                 TO ISSUE MESSAGE IEE311I.                      Y02669
*                                                                     *
*              C. IF THE OPERAND WAS MISSPELLED, CONTROL PASSES  Y02669
*                 TO THE APPROPRIATE ERROR ERROR EXIT TO SET UP  Y02669
*                 THE INTERFACE TO THE MESSAGE MODULE (IEE0503D) Y02669
*                 TO ISSUE MESSAGE IEE309I.                      Y02669
*                                                                     *
*                 (IF THE OPERAND BEGINS WITH 'TP,' OR 'TP',      20002
*                 THIS MODULE BRANCHES TO IED1303D FOR TCAM      Y02669
*                 PROCESSING)                                     20002
*                                                                 20002
*                 (IF THE OPERAND BEGINS WITH 'NET,'  OR 'NET ', Y02674
*                 THIS MODULE BRANCHES TO ISTCFF3D FOR VTAM      Y02674
*                 PROCESSING)                                    Y02674
*                                                                Y02669
*              D. IF THE OPERAND IS 'SMF' OR 'EOD', CONTROL      Y02669
*                 PASSES TO IEE0803D TO BUILD A CSCB.            Y02669
*                 THIS IS TO AVOID PROCESSING HALT/SWITCH CMDS     R271
*                 UNDER THE COMMUNICATIONS TASK.                   R271
*                                                                     *
*              E. IF THE OPERAND ON TRACE IS 'ON', BIT BATRACE @Y02669P
*                 IS SET 'ON', AND THE INTERFACE TO THE        @Y02669P
*                 MESSAGE MODULE (IEE0503D) IS SET UP TO ISSUE @Y02669P
*                 MESSAGE IEE712I.                             @Y02669P
*                                                              @Y02669P
*                 IF THE OPERAND ON TRACE IS 'OFF', BIT        @Y02669P
*                 BATRACE IS SET 'OFF', AND THE MESSAGE        @Y02669P
*                 INTERFACE IS AGAIN SET FOR MESSAGE IEE712I.  @Y02669P
*                                                              @Y02669P
*                 IF THE OPERAND ON TRACE IS 'STATUS' OR       @Y02669P
*                 NULL, CVTTRACE IS TESTED.                    @Y02669P
*                 IF THE FIELD CONTAINS X'07FB', INDICATING    @Y02669P
*                 THAT THE SYSTEM TRACE IS INACTIVE, THE       @Y02669P
*                 MESSAGE INTERFACE IS AGAIN SET FOR MESSAGE   @Y02669P
*                 IEE341I.  OTHERWISE, THE SYSTEM TRACE IS     @Y02669P
*                 ACTIVE, AND THE MESSAGE INTERFACE IS         @Y02669P
*                 SET FOR MESSAGE IEE382I.                     @Y02669P
*                                                              @Y02669P
*                 IF NONE OF THE ABOVE OPERANDS IS PRESENT,    @Y02669P
*                 THE MESSAGE INTERFACE IS SET FOR MESSAGE     @Y02669P
*                 IEE535I.                                     @Y02669P
*                                                              @Y30LPSV
*              F. IF THE OPERAND ON HALT IS 'DINIT','LONG',    @Y30LPSV
*                 OR 'SNAP' THE INTERFACE TO THE MESSAGE       @Y30LPSV
*                 MODULE IS SET UP TO ISSUE MESSAGE IEE084I.   @Y30LPSV
*                                                              @Y30LPSV
*                 IF THE OPERAND BEGINS WITH 'S,', THIS MODULE @Y30LPSV
*                 BRANCHES TO IEE9403D FOR SS/1 PROCESSING.    @Y30LPSV
*                                                                     *
*  NOTES =                                                       Y02669
*                                                              @Y02669P
*           TO BE EFFECTIVE, 'TRACE ON' AND 'TRACE OFF'        @Y02669P
*           MUST BE ISSUED BEFORE THE SYSTEM TRACE IS          @Y02669P
*           TERMINATED BY IEEVWAIT, WHEN INITIALIZATION        @Y02669P
*           OF THE PRIMARY JOB ENTRY SUBSYSTEM IS COMPLETE.    @Y02669P
*                                                              @Y02669P
*           TRACE IS INVALID IN THE INPUT STREAM.              @Y30LPSV
*                                                              @Y02669P
*     CHARACTER CODE DEPENDENCIES =  THIS MODULE HAS BEEN WRITTENY02669
*                                    IN SUCH A WAY THAT, IF THE  Y02669
*                                    CHARACTER CODE IS CHANGED   Y02669
*                                    FROM EBCDIC, REASSEMBLY WILLY02669
*                                    YIELD A VALID MODULE.       Y02669
*                                                                     *
*     DEPENDENCIES =                                             Y02669
*                                                                     *
*     RESTRICTIONS =  THIS MODULE EXPECTS REGISTER TWO TO POINT  Y02669
*                     AN EXTENDED SAVE AREA (XSA), AND         @Y02669P
*                     REGISTER TEN TO POINT TO MASTER          @Y02669P
*                     RESIDENT DATA AREA(BASEA).               @Y02669P
*                                                                     *
*     REGISTER CONVENTIONS =                                     Y02669
*                   REGISTER 2     ADDRESS OF XSA                Y02669
*                   REGISTER 5     ADDRESS OF CVT              @Y02669P
*                   REGISTER 7     ADDRESS OF TRACE TABLE      @Y02669P
*                   REGISTER 8     MESSAGE CODE                  Y02669
*                   REGISTER 9     EXIT BRANCH ADDRESS           Y02669
*                   REGISTER 10    ADDRESS OF BASEA            @Y02669P
*                   REGISTER 11    ADDRESS OF COMMAND OPERAND    Y02669
*                   REGISTER 12    MODULE BASE                   Y02669
*                   REGISTER 15    RETURN CODE                   Y02669
*                                                                     *
*     PATCH-LABEL =  PATCH                                       Y02669
*                                                                     *
*  MODULE-TYPE =  CSECT                                          Y02669
*
*     PROCESSOR =  ASSEMBLER                                     Y02669
*
*     MODULE-SIZE =  APPROXIMATELY X'200'                      @Y30LPSV
*
*     ATTRIBUTES =                                               Y02669
*                  PAGED LPA                REENTRANT            Y02669
*                  ZERO PSW PROTECT KEY     REUSABLE             Y02669
*                  SUPERVISOR MODE          REFRESHABLE          Y02669
*
*  ENTRY POINT =  IEE1403D, FROM IEE0403D                        Y02669
*
*     PURPOSE =  TO SYNTAX CHECK THE HALT AND SWITCH COMMANDS    Y02669
*                AND PROCESS THE TRACE COMMAND                 @Y02669P
*
*     LINKAGE =  VIA BRANCH FROM IEE0403D                        Y02669
*
*     INPUT DATA =  REGISTER TWO CONTAINS THE ADDRESS OF AN XSA  Y02669
*                   INITIALIZED AS FOLLOWS:                      Y02669
*
*                   XAN       VERB CODE (HALT, SWITCH OR TRACE)@Y02669P
*                   XAL       ADDRESS OF COMMAND OPERAND         Y02669
*                   REGISTER TEN CONTAINS THE ADDRESS OF BASEA @Y02669P
*
*     REGISTERS SAVED =  NONE                                    Y02669
*
*     REGISTER CONTENTS DURING PROCESSING =  SEE REGISTER CON-   Y02669
*                                            VENTIONS            Y02669
*
*     REGISTERS RESTORED =  NONE                                 Y02669
*
*  EXIT-NORMAL =  MSGBRCH                                      @Y02669P
*
*     CONDITIONS =  OPERAND ON TRACE WAS 'ON' OR 'OFF'         @Y02669P
*
*     OUTPUT DATA =  REGISTER TWO POINTS TO AN XSA CONTAINING: @Y02669P
*                    XAE      MESSAGE CODE X'03'               @Y02669P
*                    XAV      COMMAND VERB                     @Y02669P
*                    ***      OTHER FIELDS ARE UNCHANGED       @Y02669P
*
*     RETURN CODES =  ZERO                                     @Y02669P
*
*  EXIT-NORMAL = CSCBBRCH                                        Y02669
*
*     CONDITIONS =  COMMAND IS 'HALT EOD' OR 'SWITCH SMF'        Y02669
*                   EXIT TO CSCB CREATION MODULE (IEE0803D) VIA  Y02669
*                   BRANCH ON VCON.                              Y02669
*
*  OUTPUT DATA =  REGISTER TWO POINTS TO THE XSA CONTAINING:     Y02669
*                 XAV   COMMAND VERB                             Y02669
*                 OTHER FIELDS ARE UNCHANGED                     Y02669
*
*     RETURN CODES =  NONE                                       Y02669
*
*  EXIT-NORMAL =  TPBRCH                                         Y02669
*
*     CONDITIONS =  OPERAND ON HALT WAS 'TP ' OR 'TP,'           Y02669
*                   EXIT TO TCAM PROCESSOR (IED1303D) VIA BRANCH Y02669
*                   ON VCON.                                     Y02669
*
*     OUTPUT DATA =  SAME AS EXIT CSCBBRCH                       Y02669
*
*     RETURN CODES =  NONE                                       Y02669
*
*  EXIT-NORMAL =  NETBRCH                                        Y02674
*
*     CONDITIONS =  OPERAND ON HALT WAS 'NET ' OR 'NET,'         Y02674
*                   EXIT TO VTAM PROCESSOR (ISTCFF3D) VIA        Y02674
*                   BRANCH ON VCON.                              Y02674
*
*     OUTPUT DATA =  SAME AS EXIT CSCBBRCH                       Y02669
*
*     RETURN CODES =  NONE                                       Y02669
*
*  EXIT-NORMAL = SS1BRCH                                       @Y30LPSV
*
*     CONDITIONS = OPERAND ON HALT WAS 'S,LONG' OR             @Y30LPSV
*                  'S,SNAP'. EXIT TO SS/1 PROCESSOR (IEE9403D) @Y30LPSV
*                  VIA BRANCH ON VCON.                         @Y30LPSV
*
*     OUTPUT DATA = SAME AS EXIT CSCBBRCH                      @Y30LPSV
*
*  EXIT-NORMAL =  MSGBRCH                                      @Y02669P
*
*     CONDITIONS = OPERAND ON TRACE WAS 'STATUS' OR NULL, AND  @Y02669P
*                  SYSTEM TRACING WAS IN EFFECT                @Y02669P
*
*     OUTPUT DATA =  REGISTER TWO POINTS TO AN XSA CONTAINING  @Y02669P
*                    XAE      MESSAGE CODE X'34'               @Y02669P
*                    XAV      COMMAND VERB                     @Y02669P
*                    ***      OTHER FIELDS ARE UNCHANGED       @Y02669P
*
*     RETURN CODES =  ZERO                                     @Y02669P
*
*  EXIT-NORMAL =  MSGBRCH                                      @Y02669P
*
*     CONDITIONS =  OPERAND ON TRACE WAS 'STATUS' OR NULL,     @Y02669P
*                   AND SYSTEM TRACING WAS NOT ACTIVE.         @Y02669P
*
*     OUTPUT DATA =  REGISTER TWO POINTS TO AN XSA CONTAINING  @Y02669P
*                    XAE      MESSAGE CODE X'24'               @Y02669P
*                    XAV      COMMAND VERB                     @Y02669P
*                    ***      OTHER FIELDS ARE UNCHANGED       @Y02669P
*
*     RETURN CODES =  ZERO                                     @Y02669P
*
*  EXIT-ERROR =  MSGBRCH                                         Y02669
*
*     CONDITIONS = MISSING OPERAND ON HALT OR SWITCH.          @Y02669P
*                  EXIT TO MESSAGE MODULE (IEE0503D) VIA         Y02669
*                  BRANCH ON VCON TO ISSUE MESSAGE IEE311I.      Y02669
*
*     OUTPUT DATA =  REGISTER TWO POINTS TO AN XSA CONTAINING:   Y02669
*                    XAE      MESSAGE CODE X'0B'                 Y02669
*                    XAV      COMMAND VERB                       Y02669
*                    ***      OTHER FIELDS ARE UNCHANGED         Y02669
*
*     RETURN CODES =  FOUR.                                      Y02669
*
*  EXIT-ERROR =  MSGBRCH                                         Y02669
*
*     CONDITIONS = OPERAND NEITHER 'SMF' FOR SWITCH NOR 'EOD',   Y02669
*                  'TP ', 'TP,', 'NET ', OR 'NET,' FOR HALT.     Y02669
*                  EXIT TO MESSAGE MODULE (IEE0503D) VIA         Y02669
*                  BRANCH ON VCON TO ISSUE MESSAGE IEE309I.      Y02669
*
*     OUTPUT DATA =  REGISTER TWO POINTS TO AN XSA CONTAINING:   Y02669
*                    XAE      MESSAGE CODE X'09'                 Y02669
*                    XAV      COMMAND VERB                       Y02669
*                    ***      OTHER FIELDS ARE UNCHANGED         Y02669
*
*     RETURN CODES =  FOUR.                                      Y02669
*
*  EXIT-ERROR =  MSGBRCH                                       @Y02669P
*
*     CONDITIONS =  OPERAND ON TRACE WAS NOT AMONG 'ON',       @Y02669P
*                   'OFF', 'STATUS' AND NULL.                  @Y02669P
*
*     OUTPUT DATA =  REGISTER TWO POINTS TO AN XSA CONTAINING  @Y02669P
*                    XAE      MESSAGE CODE X'3E'               @Y02669P
*                    XAV      COMMAND VERB                     @Y02669P
*                    ***      OTHER FIELDS ARE UNCHANGED       @Y02669P
*
*     RETURN CODES =  FOUR                                     @Y02669P
*
*  EXIT-ERROR = MSGBRCH                                        @Y30LPSV
*
*     CONDITIONS = OPERAND ON HALT WAS EITHER 'LONG'           @Y30LPSV
*                  OR 'SNAP'. EXIT TO MESSAGE MODULE           @Y30LPSV
*                  (IEE0503D) TO ISSUE MESSAGE IEE084I.        @Y30LPSV
*
*     OUTPUT DATA = REGISTER TWO POINTS TO AN XSA CONTAINING   @Y30LPSV
*                   XAE      MESSAGE CODE X'19'                @Y30LPSV
*                   XAV      COMMAND VERBS                     @Y30LPSV
*                   ***      OTHER FIELDS ARE UNCHANGED        @Y30LPSV
*
*     RETURN CODES = FOUR                                      @Y30LPSV
*
*  EXTERNAL REFERENCES =                                         Y02669
*
*     ROUTINES = IEE0503D - TO ISSUE MESSAGES, VIA BRANCH        Y02669
*                IEE0803D - TO BUILD CSCB, VIA BRANCH            Y02669
*                IED1303D - TO PROCESS TCAM COMMANDS, VIA BRANCH Y02669
*                ISTCFF3D - TO PROCESS VTAM COMMANDS, VIA BRANCH Y02674
*                IEE9403D - TO PROCESS SS/1 COMMANDS, VIA BRCH @Y30LPSV
*
*     DATA AREAS =  NONE                                         Y02669
*
*     CONTROL BLOCKS =  XSA, CVT, BASEA                        @Y02669P
*
*     TABLES =  NONE                                             Y02669
*
*     MACROS =  MODID, IEEXSA, CVT, IEEBASEA                   @Y02669P
*
*     CHANGE LEVEL =  Y02669, Y02674, @Y30LPSV                 @Y30LPSV
*
*  MESSAGES =                                                    Y02669
*            CODE                         TEXT                   Y02669
*            X'09'       IEE309I  (VERB)  UNIDENTIFIABLE KEYWORD Y02669
*            X'0B'       IEE311I  (VERB)  PARAMETER MISSING      Y02669
*            X'24'       IEE341I  (VERB)  NOT ACTIVE           @Y02669P
*            X'34'       IEE382I  (VERB)  CURRENTLY ACTIVE     @Y02669P
*            X'3E'       IEE535I  (VERB)  INVALID PARAMETER    @Y02669P
*            X'03'       IEE712I  (VERB)  PROCESSING COMPLETE  @Y02669P
*            X'19'       IEE084I  (VERB)  SS/1 PARAMETER       @Y30LPSV
*                                         MISSING              @Y30LPSV
*
*            ALL MESSAGES CONTAINED IN AND ISSUED BY IEE0503D.   Y02669
*
*  SYSGEN =  THIS MODULE IS IN LOAD MODULE IGC0003D IN           Y02669
*            SYS1.LPALIB.  IT IS INCLUDED AT SYSGEN FROM DLIB    Y02669
*            AOSB3 BY SYSGEN MACRO SGIEF441.                     Y02669
*
*  SYSTEM LIBRARIES =  THIS MODULE MAKES REFERENCE TO NO SYSTEM  Y02669
*                      LIBRARIES.                                Y02669
*
***********************************************************************
         EJECT
***********************************************************************
*   SYNTAX =
*             ************
*             *  ON      *
*      TRACE  *  OFF     *     TRACE CANNOT BE ABBREVIATED; STATUS
*             *  STATUS  *     IS THE DEFAULT
*             ************
*
***************************************************************@Y02669P
         EJECT
* REGISTERS USED
PARREG    EQU  2                       USED FOR ADDRESSABILITY TO XSA
R5       EQU   5                       REG POINTS CVT          @Y02669P
WKREG    EQU   8                       CODE REGISTER
BRCHREG  EQU   9                       EXIT BRANCH REGISTER      Y02669
LISTREG  EQU   11                      REG POINTS TO EOD OPERAND
R10      EQU   10                      BASEA ADDRESS REGISTER  @Y02669P
BASEREG  EQU   12                      BASE   REGISTER
RETCDREG EQU   15                      RETURN CODE REGISTER      Y02669
E0       EQU   0                                                  20002
E1       EQU   1                       *                       @Y02669P
E2       EQU   2                       *                       @Y30LPSV
E3       EQU   3                                                  20002
E4       EQU   4                                                 Y02669
E5       EQU   5                       *                       @Y30LPSV
E6       EQU   6                       *                       @Y30LPSV
E7       EQU   7                       *                       @Y02669P
E8       EQU   8                                                  20002
SWITCHCD EQU   52                      SWITCH VERB CODE        @Y02669P
TRACECD  EQU   20                      TRACE VERB CODE         @Y02669P
HALTCD   EQU   60                      HALT VERB CODE          @Y02669P
FOUR     EQU   4                       COMMAND NOT EXECUTED      Y02669
*                                      RETURN CODE               Y02669
XFF      EQU   255                     MASK FOR TURNING OFF    @Y02669P
*                                      BITS                    @Y02669P
IEE084I  EQU   X'19'                ERROR CODE- MESSAGE MODULE @Y30LPSV
*                                   C'SS/1 PARAMETER MISSING'  @Y30LPSV
*
IEE309I  EQU   X'09'                   ERROR CODE- MESSAGE MODULE
*                                      C'UNIDENTIFIABLE KEYWORD.'Y02669
*                                                              @Y30LPSV
IEE311I  EQU   X'0B'                   ERROR CODE- MESSAGE MODULEY02669
*                                      C' PARAMETER MISSING.'    Y02669
*
IEE341I  EQU   X'24'                   ERROR CODE- MSG MODULE  @Y02669P
*                                      C' NOT ACTIVE '         @Y02669P
*
IEE382I  EQU   X'34'                   ERROR CODE- MSG MODULE  @Y02669P
*                                      C' CURRENTLY ACTIVE '   @Y02669P
*
IEE535I  EQU   X'3E'                   ERROR CODE- MSG MODULE  @Y02669P
*                                      C' INVALID PARAMETER '  @Y02669P
*
IEE712I  EQU   X'03'                   ERROR CODE- MSG MODULE  @Y02669P
*                                      C' PROCESSING COMPLETE '@Y02669P
*
         EJECT
IEE1403D CSECT                                                   Y02669
         BALR  BASEREG,0               ESTABLISH ADDRESSIBILITY FOR
         USING *,BASEREG               MODULE                    Y02669
         MODID BRANCH=YES                                        Y02669
         USING EXSAVSEC,PARREG         SET ADDRESSIBILITY FOR ESA
*
         L     LISTREG,XAL             LOAD POINTER TO OPERAND
*
         LA    LISTREG,0(LISTREG)      GET ADDR OF OPERAND
*
         LA    WKREG,IEE311I           SET FOR MSG IEE311I       Y02669
         CLI   XAN,SWITCHCD            ENTRY FOR SWITCH?       @Y02669P
         BE    SWITCH                  YES, BRANCH TO SWITCH   @Y02669P
*                                      CODE                    @Y02669P
         CLI   XAN,TRACECD             ENTRY FOR TRACE?        @Y02669P
         BE    TRACE                   YES, BRANCH TO TRACE    @Y02669P
*                                      CODE                    @Y02669P
*
*        OTHERWISE, ENTRY IS FOR HALT                          @Y02669P
*
         EJECT
***************************************************************@Y02669P
*                                                              @Y02669P
* ENTRY WAS MADE FOR A HALT COMMAND                            @Y02669P
*                                                              @Y02669P
***************************************************************@Y02669P
         SPACE 2
         MVC   XAV(E8),AHALT           MOVE HALT VERB TO XSA   @Y02669P
*                                      FOR MSG                 @Y02669P
         LTR   LISTREG,LISTREG         OPERAND NULL?           @Y02669P
         BZ    MSGBRCH                 YES, BRANCH FOR MSG     @Y02669P
*                                      IEE311I                 @Y02669P
         SPACE 2
*
*        TEST FOR TCAM OPERANDS                                  Y02669
*
         SPACE
         CLC   E0(E3,LISTREG),TPCOMMA  Q. 'TP,'                  Y02669
         BE    TPBRCH                  YES, BRANCH TO 1303D      Y02669
         CLC   E0(E3,LISTREG),TPBLANK  Q. 'TP '                  Y02669
         BE    TPBRCH                  YES, BRANCH TO 1303D      Y02669
           SPACE
*
*        TEST FOR VTAM OPERANDS                                  Y02674
*
         SPACE
         CLC   E0(E4,LISTREG),NETCOMMA Q. 'NET,'                 Y02674
         BE    NETBRCH                 YES, BRANCH TO ISTCFF3D   Y02674
         CLC   E0(E4,LISTREG),NETBLANK Q. 'NET '                 Y02674
         BE    NETBRCH                 YES, BRANCH TO ISTCFF3D   Y02674
         SPACE 1                                               @Y30LPSV
*                                                              @Y30LPSV
*        TEST FOR SS/1 OPERANDS                                @Y30LPSV
*                                                              @Y30LPSV
         SPACE 1                                               @Y30LPSV
         CLC   E0(E2,LISTREG),S        Q. 'S,'                 @Y30LPSV
         BE    SS1BRCH                 YES, BRANCH TO IEE9403D @Y30LPSV
         LA    WKREG,IEE084I           SET UP FOR ERROR MSG    @Y30LPSV
         CLC   E0(E5,LISTREG),LONG     Q. 'LONG'               @Y30LPSV
         BE    MSGBRCH                 YES,TAKE ERROR MSG EXIT @Y30LPSV
         CLC   E0(E5,LISTREG),SNAP     Q. 'SNAP'               @Y30LPSV
         BE    MSGBRCH                 YES,TAKE ERROR MSG EXIT @Y30LPSV
         EJECT                                                 @Y30LPSV
*                                                              @Y30LPSV
*        TEST FOR END OF DAY                                   @Y30LPSV
*                                                              @Y30LPSV
         SPACE 1                                               @Y30LPSV
         CLC   E0(E4,LISTREG),AEOD     OPERAND = EOD?            Y02669
         BE    CSCBBRCH                YES- GO TO IEE0803D       Y02669
ERROR    LA    WKREG,IEE309I           SET UP FOR ERROR          A40296
         B     MSGBRCH                 TAKE ERROR EXIT           A40296
          EJECT
***************************************************************@Y02669P
*                                                                A40296
* ENTRY WAS MADE FOR A SWITCH COMMAND                            A40296
*                                                                A40296
***************************************************************@Y02669P
          SPACE 2
SWITCH   MVC   XAV(E8),ASWITCH         MOVE SWITCH VERB TO     @Y02669P
*                                      XSA FOR MSG             @Y02669P
         LTR   LISTREG,LISTREG         OPERAND NULL?           @Y02669P
         BZ    MSGBRCH                 YES, BRANCH FOR MSG     @Y02669P
*                                      IEE311I                 @Y02669P
         CLC   E0(E4,LISTREG),ASMF     OPERAND = SMF?            Y02669
         BNE   ERROR                   NO - BAD OPERAND          A40296
         B     CSCBBRCH                YES - GOTO IEE0803D     @Y02669P
       EJECT
***************************************************************@Y02669P
*                                                              @Y02669P
* ENTRY WAS MADE FOR A TRACE COMMAND                           @Y02669P
*                                                              @Y02669P
***************************************************************@Y02669P
        SPACE 2
TRACE    MVC   XAV(E8),ATRACE          MOVE TRACE VERB TO XSA  @Y02669P
*                                      FOR MSG                 @Y02669P
         LTR   LISTREG,LISTREG         OPERAND NULL?           @Y02669P
         BZ    TRACESTS                YES, DO STATUS          @Y02669P
*                                      PROCESSING              @Y02669P
         USING BASE,R10                BASEA ADDRESSABILITY    @Y02669P
         LA    WKREG,IEE712I           SET UP FOR MSG IEE712I; @Y02669P
*                                      USED FOR BOTH ON AND OFF@Y02669P
         CLC   E0(E3,LISTREG),ON       TRACE ON?               @Y02669P
         BE    TRACEON                 YES, DO ON PROCESSING   @Y02669P
         CLC   E0(E4,LISTREG),OFF      TRACE OFF?              @Y02669P
         BE    TRACEOFF                YES, DO OFF PROCESSING  @Y02669P
         CLC   E0(E7,LISTREG),STATUS   TRACE STATUS            @Y02669P
         BE    TRACESTS                YES, DO STATUS          @Y02669P
*                                      PROCESSING              @Y02669P
         LA    WKREG,IEE535I           SET UP FOR IEE535I;     @Y02669P
         B     MSGBRCH                 TAKE MSG EXIT           @Y02669P
         SPACE 2
* TRACE ON ROUTINE                                             @Y02669P
         SPACE
TRACEON  OI    BASPBYTE,BATRACE        SET TRACE BIT ON        @Y02669P
         B     MSGBRCH1                TAKE MSG EXIT FOR       @Y02669P
*                                      IEE712I                 @Y02669P
         SPACE 2
* TRACE OFF ROUTINE                                            @Y02669P
         SPACE
TRACEOFF NI    BASPBYTE,XFF-BATRACE    SET TRACE BIT OFF       @Y02669P
         B     MSGBRCH1                TAKE MSG EXIT FOR       @Y02669P
*                                      IEE712I                 @Y02669P
         SPACE 2
* TRACE STATUS ROUTINE                                         @Y02669P
         SPACE
TRACESTS L     R5,CVTPTR               CVT...                  @Y02669P
         USING CVT,R5                  ...ADDRESSABILITY       @Y02669P
         CLC   CVTTRACE,TRACNACT       CVT TRACE BRCH INACTIVE @Y02669P
         BE    MIEE341I                YES - NO TRACE ACTIVE   @Y02669P
         LA    WKREG,IEE382I           SET UP FOR MSG IEE382I  @Y02669P
         B     MSGBRCH1                TAKE MSG EXIT           @Y02669P
MIEE341I LA    WKREG,IEE341I           SET UP FOR MSG IEE341I  @Y02669P
         B     MSGBRCH1                TAKE MSG EXIT           @Y02669P
         EJECT
**************************************************************** Y02669
*                                                                Y02669
*        BRANCH EXIT ROUTINES                                    Y02669
*                                                                Y02669
**************************************************************** Y02669
       SPACE 2
*                                                                Y02669
*        EXIT TO CSCB CREATION                                   Y02669
*                                                                Y02669
          SPACE
CSCBBRCH EQU   *                                                 Y02669
         L     BRCHREG,CSCBMOD         GET ADDRESS OF CSCB       Y02669
*                                      CREATION MODULE           Y02669
         BR    BRCHREG                 BRANCH TO IT              Y02669
       SPACE 2
*                                                                Y02669
*        EXIT TO TCAM PROCESSOR                                  Y02669
*                                                                Y02669
          SPACE
TPBRCH EQU   *                                                   Y02669
         L     BRCHREG,TPMOD           GET ADDRESS OF TCAM       Y02669
*                                      PROCESSOR MODULE          Y02669
         BR    BRCHREG                 BRANCH TO IT              Y02669
       SPACE 2
*                                                                Y02674
*        EXIT TO VTAM PROCESSOR                                  Y02674
*                                                                Y02674
          SPACE
NETBRCH EQU   *                                                  Y02674
         L     BRCHREG,NETMOD          GET ADDRESS OF VTAM       Y02674
*                                      PROCESSOR MODULE          Y02674
         BR    BRCHREG                 BRANCH TO IT              Y02674
         SPACE 2
*                                                              @Y30LPSV
*              EXIT TO SS/1 PROCESSOR                          @Y30LPSV
*                                                              @Y30LPSV
SS1BRCH  EQU   *                                               @Y30LPSV
         L     BRCHREG,SS1MOD          GET ADDRESS OF SS/1     @Y30LPSV
*                                      PROCESSOR MODULE        @Y30LPSV
         BR    BRCHREG                 BRANCH TO IT            @Y30LPSV
         EJECT                                                 @Y30LPSV
*                                                                Y02669
*        EXIT TO MESSAGE MODULE                                  Y02669
*                                                                Y02669
          SPACE
MSGBRCH EQU   *                        ERROR MESSAGE EXIT      @Y02669P
         STC   WKREG,XAE               STORE CODE IN EXT SAVE AREA.
         L     BRCHREG,MSGMOD          GET ADDRESS OF MESSAGE    Y02669
*                                      MODULE                    Y02669
         LA    RETCDREG,FOUR           SET FAILING RET CODE      Y02669
         BR    BRCHREG                 BRANCH TO MESSAGE MODULE  Y02669
         SPACE
MSGBRCH1 EQU  *                        SUCCESS MESSAGE EXIT    @Y02669P
         STC   WKREG,XAE               STORE CODE IN EXT SAVE AREA.
         L     BRCHREG,MSGMOD          GET ADDRESS OF MESSAGE    Y02669
*                                      MODULE                    Y02669
         SR    RETCDREG,RETCDREG       SET SUCCESS RET CODE      Y02669
         BR    BRCHREG                 BRANCH TO MESSAGE MODULE  Y02669
        EJECT
**************************************************************** Y02669
*                                                                Y02669
*        MODULE CONSTANTS                                        Y02669
*                                                                Y02669
**************************************************************** Y02669
        SPACE 3
AHALT    DC    CL8'HALT'               VERB
TPMOD    DC    V(IED1303D)             TCAM MODULE               Y02669
NETMOD   DC    V(ISTCFF3D)             VTAM MODULE               Y02674
MSGMOD   DC    V(IEE0503D)             MESSAGE MODULE            Y02669
CSCBMOD  DC    V(IEE0803D)             CSCB CREATE MODULE        Y02669
SS1MOD   DC    V(IEE9403D)             SS1 MODULE              @Y30LPSV
*
AEOD     DC    C'EOD '                 EOD OPERAND
ATRACE   DC    CL8'TRACE'              TRACE VERB FOR MESSAGES @Y02669P
ASWITCH  DC    C'SWITCH  '                                       A40296
TPCOMMA  DC    C'TP,'                  TCAM                       20002
TPBLANK  DC    C'TP '                  PARAMETERS                 20002
NETCOMMA DC    C'NET,'                 VTAM                      Y02674
NETBLANK DC    C'NET '                 PARAMETERS                Y02674
ASMF     DC    C'SMF '                                           A35849
TRACNACT DC    X'07FB'                 TRACE INACTIVE INDICATOR@Y02669P
*                                      IN CVT                  @Y02669P
ON       DC    C'ON '                  TRACE                   @Y02669P
OFF      DC    C'OFF '                 COMMAND                 @Y02669P
STATUS   DC    C'STATUS '              OPERANDS                @Y02669P
*
S        DC   C'S,'                    SS/1 TRIGGER            @Y30LPSV
LONG     DC   C'LONG '                 SS/1-ONLY               @Y30LPSV
SNAP     DC   C'SNAP '                 SECONDARY OPERANDS      @Y30LPSV
*                                                              @Y30LPSV
PATCH    DC    CL50'***  IEE1403D  PATCH AREA ***'             @Y02669P
*
         TITLE 'CVT'                                           @Y02669P
         CVT   DSECT=YES                                       @Y02669P
         TITLE 'MASTER RESIDENT DATA AREA'                     @Y02669P
         IEEBASEA                                              @Y02669P
         TITLE 'EXTENDED SAVE AREA'
EXSAVSEC DSECT                          DSECT FOR EXTENDED SAVE AREA.
         IEEXSA
         END
