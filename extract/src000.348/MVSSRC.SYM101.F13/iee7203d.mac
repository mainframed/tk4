 TITLE 'IEE7203D - VARY HARDCPY PROCESSOR-PART TWO'
HUCMSET  DSECT
         IEECUCM  FORMAT=NEW
         EJECT
         IEEBASEA
         EJECT
HXSASET  DSECT
         IEEXSA
         EJECT
HCVTSET  DSECT
         CVT   SYS=VMS
         EJECT
HUCBSET  DSECT
         IEFUCBOB
         EJECT
    IHAPSA
    EJECT
IEE7203D CSECT
*                                                                 21002
* HADDREQ(A)                                                   @ZA04559
***********************************************************************
*
* MODULE NAME      = IEE7203D
*   CSECT NAME     = IEE7203D
*
* LOAD MODULE NAME = IGC0003D
*
* DESCRIPTIVE NAME = VARY HARDCPY OPERAND PROCESSOR - PART TWO
*
* COPYRIGHT        = MISSING
*
* STATUS           = OS/VS/2 RELEASE 3 PTF                     @ZA04559
*
* FUNCTION         = THIS MODULE USING INFORMATION IN THE XSA(SEE
*                    INPUT BELOW) CHANGES THE HARDCPY CONFIGURATION
*                    AS FOLLOWS:
*                    1. CAN CHANGE THE ROUTING CODES AND/OR ITS
*                    ABILITY TO RECEIVE COMANDS AND RESPONSES FOR
*                    EXISTING HARDCPY.
*                    2. CAN ADD HARDCPY CAPABILITIES TO AN ACTIVE
*                    CONSOLE
*                    3. WILL CHANGE HARDCPY FUNCTIONS FROM AN ACTIVE
*                    CONSOLE (OR SYSLOG) TO SYSLOG, OR ANOTHER
*                    ACTIVE CONSOLE.
*
* OPERATION        = IF ROUT=, CMDS, NOCMDS, INCMDS, STCMDS,
*                    ARE PRESENT AND ARE CORRECT, THIS MODULE
*                    DETERMINES WHETHER A NEW CONSOLE (OR SYSLOG)
*                    IS TO HAVE THE ADDED FUNCTION OF HARDCPY LOG
*                    OR WHETHER THE CAPABILITIES OF THE CURRENT
*                    HARDCPY LOG ARE TO BE CHANGED.
*                    IN ALL CASES, A CHECK FOR MINIMUM MAINTENANCE
*                    REQUIREMENTS IS MADE AND MAINTENANCE REQUIREMENTS
*                    ARE ADDED TO THE CHANGES REQUESTED BY THE VARY
*                    COMMAND IF NECESSARY. (THE CHANGES ARE INDICATED
*                    IN THE XSA)
*
* NOTES
*   DEPENDENCIES   = NONE
*   CHARACTER/CODE DEPENDENCIES
*                  = THIS MODULE CAN BE ASSEMBLED WITH ANY NEW
*                    CHARACTER/CODE SET
*
* RESTRICTIONS     = REG2 PTS TO THE XSA ON INPUT
*                    REG 12 PTS TO THE UNIT OR SYSLOG
*   REGISTER CONVENTIONS
*                  = REGISTERS ARE DEFINED AT LOCATION: DEFREG
*   PATCH LABEL    = PATCH
*
* MODULE TYPE      = CSECT
*   PROCESSOR      = ASSEMBLER
*   MODULE SIZE    = VS/2 = '30C'                              @Z30LPTJ
*   ATTRIBUTES     = REENTRANT, REUSABLE, REFRESHABLE, PAGED LPA,
*                    ZERO PSW PROTECT KEY, SUPERVISOR MODE
*
* ENTRY POINT      = IEE7203D FROM IEE4703D
*   PURPOSE        = TO COMPLETE PROCESSING THE VARY HARDCPY COMMAND
*   LINKAGE        = BRANCH
*   INPUT          = REG2 PTS TO THE XSA
*                       XAR PTS TO THE COMMAND BUFFER
*                       XAL PTS TO THE COMMAND PARAMETERS IN THE
*                       COMMAND BUFFER
*                       XAV CONTAINS THE VERB
*   REGISTERS SAVED= NONE
*   REGISTER USAGE = R2  XSA PTR
*                    R4  UCME PTR
*                    R9  UNIT PTR
*                    R10 UCM PTR
*                    R11 CUMPRFX PTR
*                    R12 PASSED UNIT PTR OR 0 IF SYSLOG
*   REGISTERS RESTORED
*                  = NONE
*
* EXIT - NORMAL    = TO IEE4103D VIA BRANCH
*   CONDITIONS     = TO ISSUE THE HARDCPY INFORMATIONAL MESSAGE
*   OUTPUT         = REG1 PTS TO THE MESSAGE BUFFER GOTTEN IN 7203D
*                    REG2 PTS TO THE XSA
*                    REG 12 PTS TO THE UNIT FIELD OR IS 0 IS SYSLOG
*                    REG 14 HAS THE CALLER'S RETURN ADDRESS
*   RETURN CODES   = NONE
*
* EXIT - ERROR     = 1.      TO IEE5703D VIA BRANCH
*   CONDITIONS     =         ERROR WAS DETECTED IN THE COMMAND
*   OUTPUT         =         REG2 PTS TO THE XSA
*                            THE FOLLOWING CODES ARE SET IN THE XAE:
*                            XAE=X'12'=5703D MUST GO TO MODULE 0503D
*                                 FOR ERROR MSG
*                                 'IEE313I UNIT REF INVALID'
*                            XAE=X'16'=5703D MUST GO TO MODULE 0503D
*                                 FOR ERROR MSG
*                                 'IEE315I SYSLOG NOT SUPPORTED'
*                            XAE=X'20'=5703D MUST GO TO MODULE 0503D
*                                 FOR ERROR MSG
*                                 'IEE309I UNIDENTIFIABLE KEYWORD'
*                            XAE=X'28'=5703D MUST GO TO MODULE 0503D
*                                 FOR ERROR MSG
*                                 'IEE306I INVALID NUMERICS'
*   RETURN CODES   = NONE
* EXIT - ERROR     = 2.      TO IEE2103D VIA BRANCH
*   CONDITIONS     =         ERROR WAS DETECTED IN THE COMMAND
*   OUTPUT         =         REG2 PTS TO THE XSA
*                            XAE IS SET TO X'39' FOR NO STORAGE
*                            MESSAGE = 'COMMAND ABORTED' MSG.
*   RETURN CODES   = NONE
*
* EXTERNAL REFERENCES
*                  = NONE
*   ROUTINE        = IEE5703D
*                    IEE2103D
*                    IEE4103D
*   DATA AREAS     = SEE EXITS - OUTPUT
*   CONTROL BLOCKS = 1. EXTENDED SAVE AREA (XSA)
*                    2. UNIT CONTROL BLOCK (UCB)
*                       FIELDS TESTED  - SRTESTAT- BIT= SRTEDADI
*                                        UCBNAME
*                       FIELDS UPDATED - NONE
*                    3. UNIT CONTROL MODULE (UCM)
*                       FIELDS TESTED  - UCMATR- BIT= UCMUF
*                                                     UCMOF
*                                        UCMDISP-BIT= UCMDISPC
*                                                     UCMDISPB
*                                        UCMHCUCM
*                                        UMSFLGS-BIT= UCMSYSG
*                                        UCMUCB
*                                        UCMVEA
*                       FIELDS UPDATED - UCMDISP- BIT=UCMDISPB
*                                        UCMHCUCM
*                                        UCMHRDRT
*                                        UCMSDS1-BIT= UCMSDS1A
*                                                     UCMSDS1B
*                                        UCMSFLGS-BIT= UCMSYSG
*                                                     UCMSYSC
*                                                     UCMSYSB
*                    4. CVT
*                       FIELDS TESTED  - CVTCUCB
*                                        CVTMSER
*                       FIELDS UPDATED - NONE
*                    5. MASTER SCHEDULER RESIDENT DATA AREA (BASEA)
*                       FIELDS TESTED  - BALOG
*                       FIELDS UPDATED - NONE
*
* TABLES           = MESSAGE BUFFER = 160 BYTES GOTTEN IN
*                    SUBPOOL 255 OF LSQA
*
* MACROS           = GETMAIN, FREEMAIN, SETLOCK
*
* MAPPING MACROS   = IEEXSA
*                    IEECUCM
*                    IEFUCBOB
*                    IEEBASEA
*                    CVT
*                    IHAPSA
*   SERIALIZATION  = LOCAL AND CMS LOCKS ARE USED IN THIS MODULE
*                    TO PROTECT THE UCB AND UCM - UCMPRFX AND UCME
*                    PROTECTION ON THE UCB IS FROM SVC34 ONLY SINCE
*                    THE BIT TESTED IS SRTEDADI WHICH CAN ONLY BE
*                    CHANGED BY SVC34 (VARY) IN THIS MOD'S PROCESSING
*                    PROTECTION FOR THE UCM'S IS ALSO THE LOCAL AND
*                    CMS LOCKS AGAINST ANY UPDATES FROM SVC 34
*                    AND COMM TASK
*                    FIEDLS IN BASEA (BALOG) WILL BE PROTECTED WITH
*                    THE ABOVE LOCKS ALTHOUGH IT IS NOT NEEDED.
*                    LOCKS WILL BE SET AT:
*                         HSETLCK IN THIS MODULE
*                    THEY WILL BE RELEASED AT:
*                         HUNSTLK IN IEE4103D FOR VALID COMMANDS
*                         OR AT LABEL: HUNSTLK IN THIS MODULE FOR ERROR
*                         CONDITIONS WHERE EXIT IS TAKEN TO ERR MSG MOD
*                    FIELDS THAT MUST BE PROTECTED ARE:
*                        UCMHCUCM - PTR TO CURRENT HARDCPY  -  UCM
*                        UCMSFLGS - BITS: UCMSYSB - HCPY IS PRESENT
*                                         UCMSYSG - SYSLOG IS HCPY
*                                         UCMSYSC - CMDS SET IN HCPY
*                                   ALL THESE ARE IN THE    -  UCM
*                        UCMDISP  - BIT: UCMDISPB - THIS DEV IS THE
*                               HARDCPY DEV- UCME
*                        UCMATR   - BIT: UCMUF - ACTV CONSL  - UCM
*                        UCMSDS1  - BIT: UCMSDS1A
*                                        UCMSDS1B
*                                   STCMDS AND INCMDS STATUS FOR HCPY
*                        UCMHRDRT - HDCPY ROUT CODES        -  UCM
*
*                        SRTESTAT - BIT: SRTEDADI-CHANGE STATUS
*                                                 FOR CONSL. - UCB
*
* CHANGE LEVEL     = Y02669, Y02651, Z30LPTJ, ZA04559          @ZA04559
*
* CHANGE ACTIVITY  = N/A
*
* MESSAGES         = REFER TO EXITS - ERROR.
*
* ABEND CODES      = NONE
*
* PACKAGING        = IEE7203D WILL BE PART OF LOAD MODULE
*                    = IGC0003D
*                    THIS LOAD MODULE ALIASES ARE
*                    = NONE
*                  THIS LOAD MODULE ENTRY POINTS ARE
*                    = IGC0003D
*                      THE MODULE WILL RESIDE IN: LPALIB
*
* SYSGEN           = SUPPLIED BY SECTIONS:
*                       LOAD MODULE NAME
*                       PACKAGING
*                       THIS CSECT WILL BE INCLUDED FROM
*                       DLIB -AOSB3- BY SYSGEN MACRO -SGIEF441-
*
* SYSTEM LIBRARIES = NONE
***********************************************************************
          EJECT
* HARDCOPY PROCESSOR REGISTER ASSIGNMENTS-                            *
         SPACE 10
DEFREG   EQU   *                                                 Y02669
R0      EQU   0
R1       EQU   1                                                  19084
R2       EQU   2                   XSA                            19084
R3       EQU   3                   ADDRESS                        19084
R4       EQU   4                   UCME                           19084
R5       EQU   5                                                  19084
R6       EQU   6
R7       EQU   7
R8       EQU   8                                                  19084
R9       EQU   9                   UNIT PTR                       19084
R10      EQU   10                  UCM                            19084
R11      EQU   11                  UCMPFX                         19084
R12      EQU   12                  UNIT PTR IF 0,ITS SYSLOG       19084
R13      EQU   13
R14      EQU   14                      RETURN REGISTER
R15      EQU   15                                                 19084
         SPACE 10
*
* ISSUING CONSOLE IDENT MASKS
*
* ERROR MESSAGE CODES
*
ERR13    EQU   12                  UNIT REF. INVALID - 0503D      19084
ERR26    EQU   16                  SYSLOG NOT SUPPORTED - 0503D   19084
ERR9     EQU   20                  KEYWORD MISPELLED - 0503D      19084
GETERCD  EQU   X'3A'                   INSUFFICIENT STORAGE-2103DY02669
*
         SPACE 5
*                                                                 19084
* GENERAL EQUATES                                                 19084
*                                                                 19084
SYSBALOG EQU   X'00'
HNUM     EQU   2          MINIMUM NUMBER CONSOLES FOR HARDCOPY REQUIRED
CHGCD    EQU   X'04'               MSG - CHANGING STATUS  5703D   19084
BLANK    EQU   C' '
MSGLEN   EQU   160                     MESSAGE AREA LENGTH.       21002
*
* NUMERAL EQUATES                                                 19084
*                                                                 19084
ZERO     EQU   0
TWO      EQU   2
TRE      EQU   3
FOR      EQU   4
EIT      EQU   8
FOX      EQU   255
         EJECT
***********************************************************************
*                                                                     *
* HARDCOPY PROCESSING MODULE                                          *
*                                                                     *
***********************************************************************
         BALR  R3,ZERO             ADDRESSABILITY                 19084
         USING *,R3                                               19084
         MODID BR=YES                                            Y01886
         USING XSA,R2                                             19084
         L     R12,XAL                                           Y02669
         LA    R12,ZERO(R12)       GET POINTER TO PARAMS         Y02669
         L     R15,CVTPTR          CVT POINTER                    19084
         USING HCVTSET,R15                                        19084
         L     R10,CVTCUCB         PTR TO UCM                     19084
         USING UCM,R10                                           Y02669
*                                                                 19084
         LR    R11,R10             PTR TO UCM                     19084
         LA    R1,FOR                                             19084
         SR    R11,R1              MINUS FOUR IS PTR TO PFX       19084
         L     R11,ZERO(R11)                                      19084
         USING UCMPRFX,R11                                        19084
         MVC   XAD(GETLNTH),GETLIST    MOVE IN LIST FORM.         21002
         LA    R1,XAX                  GET LIST POINTER.          21002
         ST    R1,XAX                  STORE LIST POINTER.        21002
         LA    R1,XAD                  GET LIST ADDRESS.          21002
* DO A CONDITIONAL GETMAIN. THE GOTTEN ADDRESS WILL BE IN XAX.    21002
         GETMAIN MF=(E,(1))            DO CONDITIONAL GETMAIN.    21002
         LTR   R15,R15                 GETMAIN SUCCESSFUL ?       21002
         BNZ   GETERR                  NO,SO TAKE ERROR EXIT.     21002
         EJECT
***********************************************************************
*
* UNIT SCAN
*
***********************************************************************
HSCAN    EQU   *
          SPACE 10
***************************************************              Y02651
*                                                                Y02651
*  LOCKS MUST BE SET AT THIS POINT AT LABEL: HSETLCK             Y02651
*  THEY WILL BE RELEASED AT: HUNSTLK                             Y02651
*                                                                Y02651
HSETLCK  EQU   *                                                 Y02651
         STM   R11,R14,XASAVLOC                                  Y02651
         SETLOCK OBTAIN,TYPE=LOCAL,MODE=UNCOND,                  Y02651*
               RELATED=(IEE4103D(HUNSTLK),IEE7203D(HUNSTLK))     Y02651
         SETLOCK OBTAIN,TYPE=CMS,MODE=UNCOND,                    Y02651*
               RELATED=(IEE4103D(HUNSTLK),IEE7203D(HUNSTLK))     Y02651
         LM    R11,R14,XASAVLOC                                  Y02651
***************************************************              Y02651
          SPACE 10
         L     R9,UCMHCUCM         PTR TO CURRENT HARDCPY, IF ANY 19084
         LA    R9,ZERO(R9)                                        19084
         CLC   ZERO(TWO,R12),HUNIT     IS UNIT SPECIFIED          19084
         BE    HPRESNT             NO, PRESENT HCPY IS TO BE MODIF19084
         CLC   TRE(TWO,R12),HUNIT      IS UNIT GIVE               19084
         BE    HUCHK               YES, GO CHECK VALIDITY         19084
         CLC   ZERO(EIT,R12),HSYSLOG   IS UNIT SYSLOG             19084
         BNE   HERR2                   NO,INVALID UNIT REFERENCE
         SPACE 10
***********************************************************************
*
* PROCESSING OF SYSLOG AS UNIT SPECIFIED IN COMMAND
*
***********************************************************************
         SR    R12,R12               SYSLOG,ZERO R12 FOR MOD 4803D19084
         TM    UCMSFLGS,UCMSYSG     IS SYSLOG EXISTING HARDCPY    19084
         BO    HMAINCK                 YES, BRANCH TO MAINTENANCE CHECK
         L     R15,CVTPTR              POINTER TO CVT             21002
         USING HCVTSET,R15             ADDRESSIBILITY FOR CVT     21002
         L     R15,CVTMSER              PTR TO MASTER RESIDENT COR19084
         USING BASE,R15                 USE TO DETERMINE IF SYSLOG19084
         CLI    BALOG,SYSBALOG      IS SYSLOG SUPPORTED           19084
         BE    HERR3                   ERROR MESSAGE IS WRITTEN   19084
         DROP  R15                  OTHERWISE                     19084
         LTR   R9,R9                   IF IS ZERO,NON-EXISTENT    19084
         BZ    HSYS1                   HARDCOPY SO DON'T DELETE
         USING UCMLIST,R9              OTHERWISE,                 19084
         NI    UCMDISP,FOX-UCMDISPB           TURN OFF HARDCOPY BIT9084
HSYS1    EQU   *
         XC    UCMHCUCM(FOR),UCMHCUCM  ZERO OUT SYSLOG PTR
         OI    UCMSFLGS,UCMSYSG         SET SYSLOG BIT IN UCM ON  19084
***********************************************************************
*                                                                     *
* CHECK FOR BASIC MAINTENANCE REQUIRED                                *
*                                                                     *
***********************************************************************
HMAINCK  LA    R13,HNUM                NUMBER OF NON-GRAPHICS     19084
         LM    R5,R7,UCMVEA        TPR TO FIRST,LAST UCME AND SIZE19084
         DROP  R9                                                 19084
         USING UCMLIST,R5                                         19084
         SPACE
HGRAPH   EQU   *
         L     R1,UCMUCB                                          19084
         USING HUCBSET,R1                                         19084
         TM    UCMATR,UCMUF        IS AN ACTIVE CONSOLE          Y02669
         BZ    HNONACT             NO, GO CHECK IF CHANGING TO ACT19084
         TM    SRTESTAT,SRTEDADI   ACTIVE,CHECK IF CHANGING TO NONACTV
         BO    HGRAPH1             YES,CHANGING,OMIT              19084
HGRAPH3  EQU   *                                                  19084
         TM    UCMDISP,UCMDISPC    IS IT AN ACTIVE GFX CONSOLE    19084
         BO    HADDREQ             YES,MAKE IT REQ FOR HCPY
         TM    UCMATR,UCMOF         OUTPUT UNIT                   19084
         BZ    HGRAPH1             NO, OMIT WILL BE PICKED UP BY O-
         BCT   R13,HGRAPH1         DECREMENT AND CHECK NEXT UCME  19084
         SPACE 10
HADDREQ  EQU   *                                                  19084
***********************************************************************
* IF THERE IS AN ACTIVE GRAPHICS CONSOLE OR IF THERE ARE MORE    21002*
* THAN ONE ACTIVE CONSOLE, HARDCOPY IS REQUIRED. IF NO AUTH      21002*
* AND NO HARDCOPY DEVICE ARE SPECIFIED (THE HARDCOPY DEVICE IS   21002*
* NOT CHANGING) THE AUTH WILL REMAIN UNCHANGED. IF NO DEVICE IS  21002*
* BEING CHANGED AND AUTH IS NOT SPECIFIED, CMDS IS ASSUMED.      21002*
***********************************************************************
         OC    XARTCODE(TWO),HBASIC1   ADD MANDATORY ROUT CODES@Z30LPTJ
         OI    XASWITCH,XAHRDREQ    INDICATE HARDCOPY REQUIRED @Z30LPTJ
         TM    XASOPCOD,XACMD         IS AUTH TO CHANGE.       @Z30LPTJ
         BO    HARDCPY                 YES, CK AUTH CHANGE.       21002
         TM    UCMSDS1,UCMSDS1A+UCMSDS1B WAS STCMDS OR INCMDS  @ZA04559X
                                         SPECIFIED BEFORE      @ZA04559
         BNZ   HROUTCD             YES THEN BRANCH. DON'T USE  @ZA04559X
                                   DEFAULT AUTH (CMDS)         @ZA04559
         B     DEFAULT             SET MANDAT RT CODES & CMDS    Y02669
HNONACT  EQU   *                                                  19084
         TM    SRTESTAT,SRTEDADI   NONACTV CHANGING TO ACTIVE     19084
         BO    HGRAPH3             YES,COUNT IT IN                19084
HGRAPH1  BXLE  R5,R6,HGRAPH        CONTINUE CHCK TILL ALL TESTED  19084
         DROP  R5,R1                                              19084
         TM    XASOPCOD,XACMD      IS CMDS TO BE CHANGED       @Z30LPTJ
         BZ    HROUTCD                   NO,CHECK FOR ROUT CHANGES19084
         SPACE 5
HARDCPY  EQU   *                                                  21002
***********************************************************************
*   AUTHORITY CODE DETERMINATION.                                     *
***********************************************************************
         NI    UCMSDS1,FOX-UCMSDS1A-UCMSDS1B TURN OFF STCMDS INCMDS
*                            THE PREVIOUS STMT WAS ADDED FOR      21002
         NI    UCMSFLGS,FOX-UCMSYSC    TURN OFF CMDS              21002
         TM    XASWITCH,XASTHDCP       IS THIS STCMDS          @Z30LPTJ
         BO    HGRAPH2                 YES, SET BITS              21002
         TM    XASWITCH,XAINHDCP       IS THIS INCMDS          @Z30LPTJ
         BO    HGRAPH4                 YES, SET BITS              21002
         TM    XASWITCH,XAHRDCMD       IS THIS CMDS            @Z30LPTJ
         BO    DEFAULT                 YES SET BITS               21002
***********************************************************************
*  IF HARDCOPY IS REQUIRED AND NOCMDS IS SPECIFIED, CMDS IS USED 21002*
***********************************************************************
         TM    XASWITCH,XAHRDREQ       IS HARDCOPY REQUIRED    @Z30LPTJ
         BNO   HROUTCD1                NO, NOCMDS IS VALID        21002
DEFAULT  EQU   *                                                  21002
         OI    UCMSFLGS,UCMSYSC          SET COMMANDS WANTED BITON19084
         B     HROUTCD                   GO CHECK FOR ROUT CHANGES19084
HGRAPH2  EQU   *                                                  21002
         OI    UCMSDS1,UCMSDS1A        INDICATE STCMDS            21002
         B     HROUTCD                 CHECK ROUTING CODES        21002
HGRAPH4  EQU   *                                                  21002
         OI    UCMSDS1,UCMSDS1B        INDICATE INCMDS            21002
         SPACE 2
HROUTCD  EQU   *                                                  21002
***********************************************************************
* IF HARDCOPY IS REQUIRED, EVEN IF NO NEW ROUT CODES WERE SPECIF-21002*
* IED, THE ROUT CODES WILL CHANGE TO MAKE SURE THAT THE MANDA-   21002*
* TORY ROUT CODES ARE INCLUDED.                                  21002*
***********************************************************************
         SPACE
         TM    XASWITCH,XAHRDREQ       IS HARDCOPY REQUIRED    @Z30LPTJ
         BO    HROUTMVC                INCLUDE MANDAOTRY RT CODES 21002
HROUTCD1 EQU   *                                                  21002
         TM    XASWITCH,XAROUT      ARE RT.CODES TO BE CHANGED @Z30LPTJ
         BZ    HSTOP                   NO,COMMAND FINISHED
HROUTMVC EQU   *                                                  19084
         TM    XASWITCH,XAROUT    IF ROUTS WERE SPECIFIED      @Z30LPTJ
*                                 ON THE CMD THEY ARE ALREADY IN Y02669
         BO    MOVE               THEN JUST MOVE THEM IN UCME    Y02669
         OC    XARTCODE(TWO),UCMHRDRT  OR IN OLD ROUTS ALSO    @Z30LPTJ
MOVE     EQU   *                                                 Y02669
         MVC   UCMHRDRT(TWO),XARTCODE   CHANGE ROUTING CODES   @Z30LPTJ
HSTOP    EQU   *
         L     R1,XAX                  PASS MSG AREA ADDRESS     Y02669
         MVC   XAX(EIT),HMSG3           HARDCPY MSG MOD 4103D
         OI    UCMSFLGS,UCMSYSB    TURN BIT MEANING HCPY IS ON SYS19084
         L     R15,VCON41          GO TO XCTL TO MES MODULE      Y02669
         BR    R15                 GO TO IT                      Y02669
*
         EJECT
***********************************************************************
*
* UNIT IS UNSPECIFIED-CHANGES ARE MADE TO EXISTING HARDCOPY
*
***********************************************************************
HPRESNT  EQU   *
         SR    R12,R12                                            19084
         TM    UCMSFLGS,UCMSYSG     DOES HARDCPY EXIST (SYSLOG)   19084
         BO    HMAINCK                 YES,GOTO MAINT. REQUIREMENTS CHK
         L     R12,XAL             PTR TO BEGINNING               19084
         LA    R12,ZERO(R12)                                      19084
         LTR   R9,R9               SETO FOR SYSLOG OR PTR TO HCPY 19084
         BZ    HERR5                   HARDCOPY EXISTS,OTHERWISE ERROR
         USING UCMLIST,R9                                         19084
         L     R9,UCMUCB           PTR TO THE UCB OF THE HARDCPY  19084
         DROP  R9                                                 19084
         USING HUCBSET,R9          ADDRESSABILITY FOR THE UCB     19084
         LA    R12,UCBNAME          PTR TO THE DEVICE NAME FOR 48 19084
         DROP  R9                                                 19084
         B     HMAINCK                 ..
*                                                                 19084
         EJECT
***********************************************************************
*
* UNIT SPECIFIED
*
***********************************************************************
HUCHK    EQU   *                                                  19084
         LM    R5,R7,UCMVEA        R4 HAS PTR TO FIRST UCME       19084
*                                  R5 HAS SIZE OF EACH UCME       19084
*                                  R6 HAS PTR TO LAST UCME        19084
         USING UCMLIST,R5             REFERENCE TO UCM ENTRY      19084
HLOOP    EQU   *                                                  19084
         L     R4,UCMUCB           PTR TO UCB ASSOCIATED WITH UCME19084
         USING HUCBSET,R4                                         19084
         CLC   ZERO(TRE,R12),UCBNAME  COMPARE PHYSICAL UNIT ADRS. 19084
         BE    HFND                    EQUAL, UNIT HAS UCME
         BXLE  R5,R6,HLOOP         NO, CONTINUE SEARCH TILL EXHAUS19084
HERR5    EQU   *                                                  19084
         MVI   XAE,ERR13           UNIT REF INVALID TO 0503D      19084
         MVC   XAV(TRE),ZERO(R12)  MOVE IN INCORRECT UNIT         19084
         MVI   XAV+TRE,BLANK       MOVE IN BLANK TO CLEAR AREA    19084
         B     HOFFMVC             GO TO XCTL RTN                 19084
*                                                                 19084
*
HFND     EQU   *
         TM    UCMDISP,UCMDISPB       IS UNIT CURRENT HARDCPY     19084
         BO    HMAINCK                 YES,CHECK FOR MAINTENANCE
         TM    UCMDISP,UCMDISPC      IS DEVICE A GRAPHICS         19084
         BO    HERR5               ERR,HCPY SHOULD NOT BE GFX     19084
         TM    UCMATR,UCMOF         DOES IT HAVE WTO SUPPORT     Y02669
         BZ    HERR5               ERROR                          19084
         TM    UCMATR,UCMUF        ACTIVE BIT ON IN UCME         Y02669
         BO    HPENG               YES, CHECK IF CHANGING STATUS  19084
         TM    SRTESTAT,SRTEDADI   NONACTV, IS IT CHANGING TO ACTV19084
        BO     HERR4               YES, CHANGING STATUS MESSAGE   19084
         B     HERR5               NO, PURELY NONACTV,UNIT REF INV19084
HPENG    EQU   *                                                  19084
         TM    SRTESTAT,SRTEDADI   ACTIVE CHANGING TO NONACTIVE   19084
         BO    HERR5               YES, UNIT REF INVALID          19084
*
         TM    UCMSFLGS,UCMSYSG         WAS HARDCOPY SYSLOG       19084
         BO    HSOFF                   YES, GO TURN SYSLOG BIT OFF
         LTR   R9,R9               ZERO  IS NONEXISTANT HCPY      19084
         BZ    HON                     ..
         DROP  R5                                                 19084
         USING UCMLIST,R9              GO TO EXISTING HARDCPY UCME19084
         NI    UCMDISP,FOX-UCMDISPB           TURN OFF HARDCPY BIT19084
         DROP  R9                                                 19084
         USING UCMLIST,R5                                         19084
HON      OI    UCMDISP,UCMDISPB           TURN BIT ON IN NEW HARDC19084
         ST    R5,UCMHCUCM            STORE POINTER TO UCME       19084
         B     HMAINCK                 GO CHECK MAINTENANCE REQUIREMET
*
HSOFF    NI    UCMSFLGS,FOX-UCMSYSG        TURN SYSLOG BIT OFF    19084
         B     HON                     GO CHANGE POINTER
         EJECT
***********************************************************************
*
* ERROR MESSAGE ROUTINES
* AND EXIT ROUTINE                                                19084
*
***********************************************************************
*
*
HERR2    EQU   *                                                  21002
         MVI   XAE,ERR9                MISSPELLED KEYWORD         21002
         B     HOFFMVC                 EXIT TO IGC5703D           21002
HERR3    EQU   *                                                  19084
         MVI   XAE,ERR26           ERR IS 26,SYSLOG NOT SUPPORTED 19084
*                                                                 19084
         B     HOFFMVC                                            19084
*                                                                 19084
HERR4    EQU   *                                                  19084
         MVI   XAE,CHGCD           MOVE CODE TO PUT CHANGING STATU19084
*                                  MESSAGE FROM 5703D             19084
HOFFMVC  EQU   *                                                  19084
         SPACE 10
***************************************************              Y02651
*                                                                Y02651
*  LOCKS ARE NO LONGER NEEDED AND SINCE EXIT WILL BE TAKEN       Y02651
*  THEY MUST BE RELEASED FIRST AT LABEL:  HUNSTLK                Y02651
*                                                                Y02651
         BAL   R11,HUNSTLK        RELEASE ALL LOCKS              Y02651
*                                                                Y02651
***************************************************              Y02651
         SPACE 10
         L     R1,XAX                  GET MESSAGE AREA ADDRESS.  21002
* IF WE TAKE THE ERROR PATH THE MESSAGE AREA MUST BE FREED.       21002
         FREEMAIN R,LV=MSGLEN,A=(1),SP=229                       Y02669
         MVC   XAX(EIT),OFFMOD     MOD NAME IS IGC5703D           19084
         L     R15,VCON57          ADDRESS OF 5703D              Y02669
         BR    R15                 GO TO IT                      Y02669
*                                                                 19084
GETERR   EQU   *                                                  21002
         MVI   XAE,GETERCD             PUT IN MESSAGE INDICATOR.  21002
         MVC   XAX(EIT),GETERMOD       SET UP ERROR XCTL.         21002
         L     R15,VCON21              ADDRESS OF 2103D          Y02669
         BR    R15                     GOTO IT                   Y02669
         EJECT
*****************************************************************Y02651
*                                                                Y02651
*  THIS IS THE RELEASE-LOCKS SUBROUTINE                          Y02651
*  BOTH THE LOCAL AND CMS LOCKS WILL BE RELEASED HERE            Y02651
*                                                                Y02651
HUNSTLK  EQU   *                                                 Y02651
         STM   R11,R14,XASAVLOC                                  Y02651
         SETLOCK RELEASE,TYPE=CMS,RELATED=(IEE7203D(HSETLCK))    Y02651
         SETLOCK RELEASE,TYPE=LOCAL,RELATED=(IEE7203D(HSETLCK))  Y02651
         LM    R11,R14,XASAVLOC                                  Y02651
         BR    R11                                               Y02651
*                                                                Y02651
*****************************************************************Y02651
         EJECT
***********************************************************************
*
* CONSTANTS USED BY HARDCPY PROCESSOR
*
***********************************************************************
HSYSLOG  DC    C'SYSLOG'           DO NOT SEPARATE THESE TWO DCLS 19084
HUNIT    DC    C',H'               THEY ARE USED TOGHETHER        19084
*                                                                 19084
*
*
HMSG3    DC    C'IGC4103D'         HARDCOPY MSG MOD = 4103D
OFFMOD   DC    C'IGC5703D'         HARDCPY,OFF PROCESSOR          19084
GETERMOD DC    C'IGG2103D'             ERROR MODULE.              21002
HBASIC1  DC    X'F340'                 MASK FOR BASIC ROUTINE CODES
         DS    0F                                                Y02669
VCON41   DC    V(IEE4103D)                                       Y02669
VCON57   DC    V(IEE5703D)                                       Y02669
VCON21   DC    V(IEE2103D)                                       Y02669
GETLIST  GETMAIN EC,LV=MSGLEN,A=0,SP=229,MF=L                     21002
GETLNTH  EQU   *-GETLIST               LENGTH OF LIST FORM.       21002
         SPACE 5
PATCH    DC    CL50'***IEE7203D***'                            @Z30LPTJ
         END
