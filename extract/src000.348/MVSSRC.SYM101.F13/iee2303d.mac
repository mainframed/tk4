         TITLE 'IEE2303D - SMF VARY RECORD HANDLER'
* TITLE        SMF VARY RECORD HANDLER                                *
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
* MODULE NAME      = IEE2303D
*    CSECT NAME    = IEE2303D
*
* LOAD MODULE NAME = IEE3603D
*
* DESCRIPTIVE NAME = SMF-VARY ONLINE AND CONSOLE RECORD HANDLER
*
* COPYRIGHT        = MISSING
*
* STATUS           = OS/VS/2 RELEASE 3                         @Z30LPTJ
*
* FUNCTION         = WRITE AN SMF RECORD FOR DEVICES VARIED TO THE
*                    ONLINE OR CONSOLE STATUS.
*
* OPERATION        = ALL DEVICES SPECIFIED IN THE VARY COMMAND
*                    ARE CHECKED FOR VALIDITY.  THEN, IF AT LEAST
*                    ONE DEVICE IS VALID, A VARY RECORD, TYPE 9
*                    IS WRITTEN TO SYS1.MAN DATA SET.
*                    AN SMF VARY IS FORMATTED THUSLY,
*                       LL0009TTTTDDDDCCCCXXKYUU......KYHU
*                                           *            *
*                                           ** VARIABLE  *
*                       LL   -TWO BYTE LENGTH OF RECORD
*                       9    -ONE BYTE RECORD TYPE
*                       TTTT -FOUR BYTE TIME OF DAY (BINARY)
*                       DDDD -FOUR BYTE DAY OF YEAR (BINARY)
*                       CCC  -FOUR BYTE CPU ID
*                       XX   -TWO BYTE LENGTH OF VARIABLE DATA FIELD
*                       KYHU -VARIABLE DATA FIELDS. THERE ARE AS
*                             MANY OF THESE FIELDS AS THERE ARE
*                             VALID DEVICES IN THE VARY COMMAND.
*                             K   -ONE BYTE DEVICE CHARACTERISTICS
*                             Y   -ONE BYTE UNIT TYPE
*                             H   -ONE BYTE
*                                      BITS 0-3, UNUSED
*                                      BITS 4-7, CHANNEL ADDRESS
*                             U   -ONE BYTE UNIT ADDRESS
*                    THE LOGIC FLOW IS AS FOLLOWS:
*                    ON ENTRY FROM IEE3303D, A 168 BYTE AREA   @ZA03202
*                    IS GOTTEN FOR THE SMF RECORD.  (THIS      @ZA03202
*                    NUMBER WAS CHOSEN ARBITRARILY.  THE LOGIC @ZA03202
*                    RECOGNIZES THIS AS SOON AS THIS AREA IS   @ZA03202
*                    FULL AND FIRST WRITES OUT THE SMF RECORD  @ZA03202
*                    AND REINITIALIZES THE AREA).              @ZA03202
*
*                    FIRST 20 BYTES ARE INITIALIZED AS FOLLOWS:
*                       'LL' FIELD IS INITIALIZED TO '20'
*                       'XX' FIELD IS INITIALIZED TO '00'
*
*                    THE PTR TO THE COMMAND IMAGE IS OBTAINED
*                    IN REG9
*
*                    IN CASE OF AN I-UNIT SPECIFICATION, FOLLOWING
*                    CHECKS ARE MADE:
*                       . CHARACTERS ',O-' SHOULD BE THERE WITH  THE
*                            I-UNIT SPECIFICATION.
*                       . RIGHT PARENS IMMEDIATLY AFTER 'O-UNIT'
*                            SPECIFICATION SHOULD BE FOUND.
*                       . UCME SHOULD BE AVAILABLE FOR THE I-UNIT.
*                       . THIS UCME SHOULD HAVE INPUT CAPABILITY
*                            (UCMIF SHOULD BE 'ON')
*                       . DEVICE AS POINTED TO BY THIS UCME SHOULD
*                            NOT BE ONLINE
*                       . DEVICE AS POINTED TO BY THIS UCME SHOULE
*                            NOT BE UNDER TEST BY OLTEP.
*                       . THIS UCME SHOULD POINT TO A COMPOSITE
*                            CONSOLE UCME.
*                       . THE 'UCBNAME' IN THE UCB AS POINTED TO BY
*                            THIS COMPOSITE CONSOLE UCME SHOULD BE
*                            SAME AS IN THE 'O-UNIT' SPECIFICATION.
*                    NOTE: IF ANY OF THE ABOVE CONDITIONS IS NOT MET,
*                            THIS UNIT SPECIFICATION IS IGNORED
*                       . A CHECK IS MADE IF SPACE TO PUT TWO ENTRIES
*                            IN THE SMF RECORD EXIST.  IT NOT, THE
*                            CURRENT SMF RECORD IS WRITTEN BY ISSUING
*                            AN SVC 83 .
*                       . ENTRIES ARE MADE IN THE RECORD
*                       . REG9 IS UPDATED TO POINT TO THE NEXT UNIT
*                            SPECIFICATION.
*                       . BRANCH IS TAKEN TO PROCESS
*                            EITHER THE NEXT UNIT SPECIFICATION OR TO
*                            DETERMINE IF IT IS THE END OF THE PROCESS.
*
*                    IN CASE OF O-UNIT SPECIFICATION, THE FOLLOWING
*                    CHECKS ARE MADE:
*                       . THERE SHOULD BE A UCME PERTAINING TO THE
*                            O-UNIT
*                       . THIS UCME SHOULD HAVE 'OUTPUT ONLY'
*                            CAPABILITIES  (SPECIFIED  BY
*                            UCMDISPD SHOULD BE 'ON')
*                       . THE UNIT SHOULD NOT BE 'ONLINE'
*                       . THE UNIT SHOULD NO BE UNDER TEST
*                            BY OLTEP
*                       . SEE IF SPACE FOR ONE ENTRY EXISTS IN THE
*                            168 BYTE BUFFER, IF NOT WRITE THE @ZA03202
*                            RECORD FIRST (ISSUE SVC 83).      @ZA03202
*                       . PUT AN ENTRY IN THE RECORD
*                       . UPDATE REG9 TO POINT TO THE NEXT UNIT
*                       . BRANCH IS TAKEN TO PROCESS
*                            EITHER NEXT  UNIT SPECIFICATION OR
*                            TO DETERMINE IF IT IS THE END OF THE
*                            PROCESSING.
*
*                    IF IT IS NEITHER (I-UNIT,O-UNIT) COMBINATION NOR
*                    THE O-UNIT SPECIFICATION, THEN CONTROL PASSES TO
*                    THIS POINT, WHERE WE DETERMINE IF THE UNIT
*                    SPECIFICATION IF CAPABLE OF BOTH INPUT AND OUTPUT
*                    THE FOLLOWING CHECKS ARE MADE:
*                       . THERE SHOULD BE A UCME FOR THE UNIT SPECIFIED
*                    NOTE: IF THE ABOVE IS NOT TRUE, THE UNIT IS
*                       PROCESSED AS IF IT WAS AN I/O UNIT AND NOT AS
*                       A SYSGENED CONSOLE UNIT.
*                       . THE UCME SHOULD INDICATE BOTH INPUT AND
*                            OUTPUT CAPABILITY OF THE UNIT (UCMIF AND
*                            UCMOF SHOULD BE 'ON')
*                       . THE UNIT SPECIFIED SHOULD NOT BE 'ONLINE'
*                       . THE UNIT SPECIFIED SHOULD NOT BE UNDER
*                            TEST BY OLTEP
*                    NOTE: IF EITHER OF THE ABOVE IS NOT TRUE, THE
*                       UNIT SPECIFIED IS IGNORED.
*                       . SPACE IS CHECKED IN THE SMF RECORD BUFFER
*                            FOR PUTTING ONE ENTRY. IF NOT AVAILABLE
*                            THE RECORD IS WRITTEN FIRST
*                       . BRANCH TO PROCESS EITHER NEXT
*                            UNIT OR TO DETERMINE IF IT IS THE END OF
*                            THE PROCESSING.
*
*                    CONTROL IS RECEIVED AT THIS POINT WHEN THE UNIT
*                    SPECIFIED IS TO BE VARIED ONLINE ONLY AND ANY
*                    UNIT WITH THE 'VARY CONSOLE' KEYWORD AT THIS
*                    POINT OF CONTROL IS TO BE IGNORED.
*                    FOLLOWING CHECKS ARE MADE:
*                       . IF THE COMMAND IS 'VARY CONSOLE' THE UNIT
*                            IS IGNORED.
*                       . IF IT IS A 'VARY ONLINE' THE UNIT WILL BE
*                            PROCESSED
*                       . THE PTR TO THE UCBLIST IS OBTAINED FROM THE
*                            CVT.
*                       . THE PTR TO THE MAIN UCB PERTAINING TO THE
*                            UNIT SPECIFICATION IS OBTAINED.
*                       . THE UCB IS CHECKED TO SEE IF THE UNIT IS
*                            ALREADY ONLINE
*                       . IN CASE IT IS MARKED ALREADY ONLINE, THE
*                            UNIT IS IGNORED.
*                       . IN CASE IT IS UNDER TEST BY OLTEP, THE
*                            UNIT IS IGNORED.
*                       . OTHERWISE SPACE IS CHECKED IN THE SMF
*                            RECORD TO PUT AN ENTRY .
*                       . IF SPACE IS NOT AVAILABLE, FIRST THE RECORD
*                            IS WRITTEN AND THEN ANY ENTRY IS MADE.
*                       . WHEN THE COMMAND IS COMPLETE, AN EXIT IS
*                            MADE TO  IEE4203D
*                            OR       IEE4403D
*                            DEPENDING ON THE SWITCHES SET BY
*                            IEE3303D IN THE XSWITCH BYTE OF THE XSA.
*
* NOTES
*    DEPENDENCIES  = NONE
*    CHARACTER/CODE DEPENDENCIES
*                  = THIS MODULE CAN BE ASSEMBLED WITH ANY NEW
*                    CHARACTER/CODE SET.
*
* RESTRICTIONS     = REG2 PTR TO THE DUMMY XSA ON INPUT AND OUTPUT
*     REGISTER CONVENTIONS
*                  = REGISTERS ARE DEFINED AT LABEL: DEFREG
*    PATCH LABEL   = PATCH
*
* MODULE TYPE      = CSECT
*    PROCESSOR     = ASSEMBLER
*    MODULE SIZE   = VS/2 = '43A'                              @ZA03202
*    ATTRIBUTES    = REENTRANT, REUSABLE, REFRESHABLE,PAGED LPA,
*                    ZERO PSW PROTECT KEY
*
* ENTRY POINTS     = IEE2303D FROM IEE3303D
*    PURPOSE       = TO WRITE AN SMF RECORD FOR VALID DEVICES VARIED
*                    TO THE ONLINE OR CONSOLE STATES.
*    LINKAGE       = BRANCH
*    INPUT         = REG2 PTS TO THE DUMMY XSA
*                       XSWITCH BYTE OF XSA CONTAINS SWITCHES TELLING
*                       WHICH MODULE TO GO NEXT (4403D OR 4203D)
*                     REG13 PTS TO THE SAVE AREA GOTTEN IN 3603D
*                     WORD 17 OF THE SAVE AREA HAVE THE PTR TO THE CSCB
*    REGISTERS SAVED= ALL
*    REGISTER USAGE = R1  RECORD PTR
*                     R2 = DUMMY XSA PTR
*                     R3  SMCA REG / OR CVT PTR, LATER = UCME PTR OF
*                        COMPOSITE OF 0 IF NONE THERE
*                     R4  EOB PTR
*                     R5  UCME REG
*                     R8  UNIT REG
*                     R9  UNIT PTR
*                     R10 UCB PTR
*                     R11 UCB PTR OF COMPOSITE OR 0 IF NOT COMP.
*                     R13 SAVE AREA PTR STORED IN THIS MODULE IN THE
*                        IN THE XAA+2 OF THE DUMMY XSA. REG13 IS THEN
*                        USED AS THE PAREN SWITCH.
*    REGISTERS RESTORED
*                  = ALL
*
* EXITS - NORMAL   = TO IEE4203D OR IEE4403D DEPENDING ON THE
*                    SWITCHES SET BY IEE3303D IN XSWITCH BYTE
*                    OF THE XSA. (VIA BRANCH)
*                    EXIT TO 4203D FOR VARY ONLINE/CONSOLE.
*                    EXIT TO 4403D FOR VARY CONSOLE WHEN THE CMD IS
*                    FOLLOWED BY THE CONSOLE CMD SUBPARAMETERS
*    CONDITIONS    = EXIT IS TAKEN WHEN THE FULL COMMAND HAS
*                    BEEN CHECKED AND SMF RECORDS HAVE BEEN
*                    WRITTEN FOR ALL ELIGIBLE DEVICES.
*    OUTPUT        = REG2 PTS TO THE DUMMY XSA
*                  = REG1 HAS XAD SWITCHES AS PRESET BY 3303D FOR
*                    4203D (SEE PROLOGUE OF IEE3303D AND 4203D)
*                    REG13 PTS TO THE SAVE AREA GOTTEN IN 3303D
*    RETURN CODES  = NONE
*
* EXITS - ERROR    = NONE
*
* EXTERNAL REFERENCES
*    ROUTINES      = IEE4203D
*                    IEE4403D
*    DATA AREAS    = SEE EXITS - OUTPUT DATA
*
* CONTROL BLOCKS   = . DUMMY EXTENDED SAVE AREA (XSA)
*                    . SYSTEM MANAGMENT FACILITIES CONTROL BLOCK (SMCA)
*                       FIELDS TESTED  - SMCAID
*                       FIELDS UPDATED - NONE
*                    . CVT
*                       FIELDS TESTED  - CVTCUCB
*                                        CVTILK2
*                                        CVTSMCA
*                       FIELDS UPDATED - NONE
*                    . UNIT CONTROL MODULE (UCM)
*                       FIELDS TESTED  -
*                       FIELDS UPDATED = NONE
*                    . UNIT CONTROL BLOCK (UCB)
*                       FIELDS TESTED  = UCBCHA
*                                        UCBFL5-BIT= UCBNALOC
*                                        UCBNAME
*                                        SRTESTAT-BIT= SRTEONLI
*                                        UCBTBYT3
*                       FIELDS UPDATED = NONE
*
* TABLES           = 168 BYTES GOTTEN FOR SMF RECORD           @ZA03202
*
* MACROS           = GETMAIN, FREEMAIN, SVC 83(SMFRECORD WTR),SVC 11
*
* MAPPING MACROS   = IEEXSA
*                    CVT
*                    IEECUCM
*                    IEFUCBOB
*                    IEESMCA
*    SERIALIZATION = THE MODULE EXECUTES UNDER ENQ FOR PROTECTION
*                    AGAINST ANOTHER VARY/UNLOAD,ALLOCATION AND
*                    OLTEP.
*                    THIS ENQ WAS ISSUED EARLIER IN IEE3603D AND A
*                    DEQ WILL NOT BE ISSUED HERE SINCE THERE ARE
*                    NO TERMINAL CONDITION EXITS FROM THIS MODULE.
*                    NO PROTECTION IS REQUIRED AGAINST COMM TASK
*                    AND THEREFORE NO LOCKS WILL BE GOTTEN IN THIS
*                    ROUTINE
*
* CHANGE LEVEL     = Y02669, Z30LPTJ, ZA03202                  @ZA03202
*
* CHANGE ACTIVITY  = N/A
*
* MESSAGES         = NONE
*
* ABEND CODES      = NONE
*
* PACKAGING        = IEE2303D WILL BE PART OF LOAD MODULE
*                       = IEE3603D
*                    THIS LOAD MODULE ALIASES ARE
*                       = NONE
*                    THIS LOAD MODULE ENTRY POINTS ARE
*                       = IEE3603D
*                    MODULE WILL RESIDE IN: LPALIB
*
* SYSGEN           = SUPPLIED BY SECTIONS
*                       LOAD MODULE NAME
*                       PACKAGING
*                    THIS CSECT WILL BE INCLUDED AT SYSGEN FROM
*                    DLIB -AOSB3- BY MACRO -SGIEF441-
*
* SYSTEM LIBRARIES = NONE
*
***********************************************************************
         EJECT
IEE2303D CSECT
* C 343000,768000                                              @YM07237
* ENTRY (A)                                                    @ZA03202
 SPACE 3
TWENTY   EQU   20
ONESIXTY EQU   160
ONESXTY5 EQU   165                     CONSTANT OF 165         @ZA03202
ONESXTY8 EQU   168                     CONSTANT OF 168         @ZA03202
ONEFORTY EQU   140                                               Y02669
RECTYP   EQU   5                       OFFSET FOR REC. TYPE
SMFON    EQU   9                       SMF RECORD TYPE
VARLNTH  EQU   18                      OFFSET FOR VARIABLE LNTH SLOT
*
* R E G I S T E R   U S A G E
*
DEFREG   EQU   *                                                 Y02669
R0       EQU   0
R1       EQU   1                       PTR TO START OF SMF RECORD
R2       EQU   2                       XSA PTR
R3       EQU   3                       UCME OF COMPOSITE OF 0 IF NONE
R4       EQU   4                       EOB PTR
R5       EQU   5             WORKING REG(INITIALISING HEADER RECORD),
*                            UCME PTR(DURING UCME SEARCH)
R6       EQU   6
R7       EQU   7
R8       EQU   8                       TEMP UNIT PTR
R9       EQU   9                       UNIT PTR
R10      EQU   10                      UCB PTR
R11      EQU   11                      UCB PTR OF COMP. OR 0 IF NONE
R12      EQU   12                      MODULE BASE REG
R13      EQU   13                      SAVE AREA PTR ON INPUT
*                        SAVE AREA PTR IS STORED IN XAD AND REG  Y02669
*                        USED AS NUMBER OF PARENS SWITCH         Y02669
R14      EQU   14                      VARIABLE DATA FIELD REG
R15      EQU   15
DEC0     EQU   0
DEC1     EQU   1
DEC2     EQU   2
DEC3     EQU   3
DEC4     EQU   4
DEC5     EQU   5
DEC6     EQU   6
DEC8     EQU   8
DEC9     EQU   9
DEC12    EQU   12                                                 M2935
DEC13    EQU   13
DEC20    EQU   20
*
LPAREN   EQU   C'('
COMMA    EQU   C','
RPAREN   EQU   C')'
COMP     EQU   X'80'
OUNITBT  EQU   X'40'
XZERO    EQU   X'00'
REGUNT   EQU   X'20'
NRECI    EQU   X'08'
NRECO    EQU   X'04'
HEX0F    EQU   X'0F'                                           @YM07237
VUNIT    EQU   X'80'                   HIGH ORDER BIT MASK     @Z30LPTJ
XC1      EQU   X'C1'                   COMPRAND FOR 'A'        @ZA03202
XC6      EQU   X'C6'                   COMPRAND FOR 'F'        @ZA03202
*
         EJECT
         BALR  R12,0                   ESTABLISH ADDRESSABILITY
         USING *,R12
         MODID BR=YES                                            Y01886
         USING XSA,R2
         STM   R0,R15,DEC0(R13)                                  Y02669
         ST    R13,XAK                 STORE SAVE AREA ADDRESS @Z30LPTJ
         L     R3,CVTPTR               OBTAIN CVTPTR TO SEE IF
         USING SETCVT,R3
         L     R3,CVTSMCA              CVTSMCA FIELD WILL BE ZEROS IF
         USING SMCABASE,R3
*
*
*
         LA    R0,ONESXTY8             OBTAIN 168 BYTE CORE    @ZA03202
*                                 FROM SUBPOOL 0 OF VARY'S REGIONY02669
         GETMAIN R,LV=(0)
         XC    DEC0(ONESXTY8,R1),DEC0(R1) CLEAR HEADER AREA    @ZA03202
         MVI   RECTYP(R1),SMFON        MOVE IN THE RECORD TYPE
         MVI   DEC1(R1),TWENTY         MOVE IN TOTAL LENTH '20'
*                                      THIS LENGTH WILL BE
*                                      UPDATED AS AND WHEN VAR.
*                                      DATA RECORDS ARE ADDED   )
         MVC   VARLNTH(DEC2,R1),TWOCON    MOVE IN '2' AS VAR LNTH
         USING UCBOB,R10
         L     R9,XAL                  OBTAIN PTR TO PARAMETER LIST
         DROP  R3
         LA    R9,DEC0(R9)             OBTAIN PTR TO COMMAND
         L     R4,XAR
         AR    R4,R9              COMPUTE END OF BUFFER INTO REG4Y02669
         LA    R14,DEC20(R1)           REG 14 POINTS TO THE BEGINNING
*                                      OF THE VARIABLE DATA FIELD)
         SR    R13,R13                 IT WILL CONTAIN THE NUMBR
*                                      OF LEFT PARENS
         XC    XAP(DEC4),XAP       ZERO OUT SWITCH AREA
*
         EJECT
START    EQU   *                                                 Y02669
         CLC   DEC0(DEC3,R9),CON  IS IT THE CONSOLE KEYWORD      Y02669
         BE    SEXIT              YES, FINISHED                  Y02669
         CLC   DEC0(DEC3,R9),ONL  IS IT THE ONLINE KEYWORD       Y02669
         BE    SEXIT              YES, FINISHED                  Y02669
SPRNCK   EQU   *                                                 Y02669
         CLI   DEC0(R9),LPAREN    IS THERE AN EXTERIOR PAREN     Y02669
         BNE   SINGLE             NO SINGLE UNIT SPECIFIED       Y02669
         CLC   DEC0(DEC3,R9),PARENI    IS IT A COP  SPECIFICATIONY02669
         BE    SCOMP              YES, GO PROCESS A COMPOSITE UNIY02669
         AH    R13,HONE           SET EXTERIOR PAREN SWITCH      Y02669
         LA    R9,DEC1(R9)        UPDATE PAST THE PAREN          Y02669
         CR    R9,R4              IS IT END OF BUFFER            Y02669
         BE    SEXIT              YES, FINISHED                  Y02669
         B     SPRNCK             NO, CHECK MORE FOR A UNIT      Y02669
*                                                                Y02669
SINGLE   EQU   *                                                 Y02669
         CLC   DEC0(DEC2,R9),OUNIT     IS A SINGLE OUTPUT-ONLY UNY02669
         BE    SOUNIT             YES, GO PROCESS OUTPUT-ONLY UNIY02669
         CLI   DEC3(R9),COMMA     NO, IS IT A XXX, REGULAR UNIT  Y02669
         BE    REGUNIT            YES, GO PROCESS REGULAR UNIT   Y02669
         CLI   DEC3(R9),RPAREN    MAYBE IT IS A XXX)             Y02669
         BE    REGUNIT            YES, GO PROCESS REGULAR UNIT   Y02669
*                                                                Y02669
* NOTHING RECOGNIZABLE, SCAN TO MEAREST COMMA OF EOB             Y02669
*                                                                Y02669
ENDSCAN  EQU   *                                                 Y02669
         CLI   DEC0(R9),COMMA                                    Y02669
         BE    SOMEMORE           YES,MAYBE MORE UNITS PRESENT   Y02669
ENDSCAN2 EQU   *                                                 Y02669
         CR    R9,R4              IS IT END OF BUFFER            Y02669
         BE    SEXIT              YES, FINISHED                  Y02669
         LA    R9,DEC1(R9)        NO, UPDATE PTR BY ONE          Y02669
         CLI   DEC0(R9),COMMA     SCAN TO NEARES COMMA           Y02669
         BNE   ENDSCAN2           NO COMMA YET, KEEP CHECKING    Y02669
SOMEMORE EQU   *                                                 Y02669
         LA    R9,DEC1(R9)        UP BY ONE TO PT TO NEXT UNT    Y02669
         LTR   R13,R13            FOUND ',' MULTIPL UNTS?        Y02669
*                                 IF NO MULT UNITS WERE PRESENT  Y02669
*                                 THEN REACHING A COMMA WITHOUT  Y02669
*                                 UNIT IS AN ERROR               Y02669
         BZ    SEXIT              AND WERE ARE FINISHED          Y02669
*                                 WE DID HAVE MULT UNITS, THERE- Y02669
*                                 FORE THERE ARE MORE THAN ONE UNY02669
*                                 AND JUST THIS ONE IS IN ERROR. Y02669
         B     START              GO FIND A NEW UNIT TO PROCESS  Y02669
*                                                                Y02669
         EJECT
SCOMP    EQU   *                                                 Y02669
*                                                                Y02669
*  PROCESSING A COMPOSITE UNIT    (I-XXX,O-XXX)                  Y02669
*                                                                Y02669
         CLC   DEC6(DEC3,R9),CMAO      IS IT (I-XXX,O-           Y02669
         BNE   ENDSCAN            NO ERROR IN COMPOSITE SPECIF   Y02669
         CLI   DEC12(R9),RPAREN   IS IT (I-XXX,O-XXX)            Y02669
         BNE   ENDSCAN            NO,ERROR IN COMPOSITE SPECIFI  Y02669
         OI    XAP+DEC0,COMP      SET 'COMPOSITE SWITCH'         Y02669
         LA    R8,DEC3(R9)        INDEX TO POINT TO INPUT UNTNME Y02669
         BAL   R15,SEARCH         GO GET UCME                    Y02669
         LTR   R5,R5              IF R5 (UCME PTR)IS 0 = NO UCME Y02669
         BNZ   GOTUCME            OK, WE HAVE AN INPUT UNIT UCME Y02669
BADCOMP  EQU   *                                                 Y02669
         LA    R9,DEC13(R9)       INDEX PAST UNIT                Y02669
         MVI   XAP+DEC0,XZERO     CLEAR 'COMPOSITE SWITHC'       Y02669
         B     ENDSCAN            GO FIND NEXT UNIT IS ANY       Y02669
         USING UCMLIST,R5                                        Y02669
GOTUCME  EQU   *                                                 Y02669
         TM    UCMATR,UCMIF       IS IT REALLY AN INPUT DEV.     Y02669
         BZ    BADCOMP            NO ERROR IN COMP. SPECIFIED    Y02669
         L     R3,UCMCOMPC        GET PTR TO UCME OF COMPOSITE   Y02669
         LTR   R3,R3              ANY COMPOSITE, SHOULD BE       Y02669
         BZ    BADCOMP            NO, ERROR IN COMP. SPECIFIED   Y02669
         DROP  R10                                               Y02669
         USING UCBOB,R11                                         Y02669
         DROP  R5                                                Y02669
         USING UCMLIST,R3                                        Y02669
         L     R11,UCMUCB         GET UCB PTR TO THE COMPOSITE   Y02669
         CLC   DEC9(DEC3,R9),UCBNAME   DO THE UNITNAMES MATCH    Y02669
         BNE   BADCOMP            NO, ERROR IN COMP SPECIFIED    Y02669
         DROP  R11                                               Y02669
         USING UCBOB,R10                                         Y02669
         DROP  R3                                                Y02669
         USING UCMLIST,R5                                        Y02669
*                                                                Y02669
*        R9 PTS TO THE BEGINNING OF UNIT OPERAND (I-XXX,O-XXX)   Y02669
*        R8 PTS TO THE INPUT UNITNAME                            Y02669
*        R10 HAS PTR TO INPUT UNIT UCB                           Y02669
*        R11 HAS PTR TO OUTPUT UNIT UCB                          Y02669
*        R5 HAS PTR TO INPUT UNIT UCME                           Y02669
*        R3 HAS PTR TO OUTPUT UNIT UCME                          Y02669
*                                                                Y02669
*  MUST CHECK VIA BOTH UCBS IF ANY OR BOTH DEVICES ARE ONLINE OR Y02669
*  UNDER TEST BY OLTEP.                                          Y02669
*  IF ANY OR BOTH ARE SO, ITS AN ERR AND NO RECD WILL BE PUT OUT Y02669
*                                                                Y02669
         BAL   R6,CHECKUCB        GO CHECK UCB'S                 Y02669
         LTR   R15,R15          IS INPT OFFL AND NOT UNDER OLTP  Y02669
         BNZ   CHECKERI           SOMETHING IS WRONG             Y02669
CHECKONT EQU   *                                                 Y02669
         BAL   R6,COMPUCB         I- IS OK, GO CHECK OUTPUT UNIT Y02669
         LTR   R15,R15          IS OUTPT OFFL AND NOT UNDER OLTP Y02669
         BNZ   CHECKERO           SOMETHING IS WRONG             Y02669
RECCOMP  EQU   *                                                 Y02669
         LA    R9,DEC13(R9)       UPDATE PAST COMP.              Y02669
         B     COMPSPAC          GO PUT RECORDS SINCE ALL IS OK  Y02669
*                                                                Y02669
CHECKERI EQU   *                                                 Y02669
         C     R15,EIGHT          IS I-UNIT ONLINE               Y02669
         BNE   BADCOMP            NO, IT IS UNDER TEST BY OLTEP  Y02669
*                                 MUST OMIT PUTTING RECORD FOR   Y02669
*                                 THE WHOLE COMPOSITE UNIT       Y02669
         OI    XAP+DEC0,NRECI     INPUT IS ONLINE, SET FLAG      Y02669
*                             NOT TO PRINT RECORD FOR INPUT BUT  Y02669
*                              CHECK OUTPUT SINCE IT MAY BE OFFL Y02669
         B     CHECKONT           GO CHECK OUTPUT                Y02669
*                                                                Y02669
CHECKERO EQU   *                                                 Y02669
         C     R15,EIGHT          IS O-UNIT ONLINE               Y02669
         BNE   BADCOMP            NO, IT IS UNDER TEST BY OLTEP  Y02669
*                                 MUST OMIT PUTTING RECORD FOR   Y02669
*                                 WHOLE COMPOSITE UNIT           Y02669
         OI    XAP+DEC0,NRECO     SET 'NOREC FOR OUPUT UNT SWTCH'Y02669
         B     RECCOMP            GO ISSUE RECORD FOR INPUT MAYBEY02669
*                                                                Y02669
         EJECT
SOUNIT   EQU   *                                                 Y02669
*                                                                Y02669
*  PROCESSING OUTPUT ONLY UNIT  O-XXX                            Y02669
*                                                                Y02669
         OI    XAP+DEC0,OUNITBT   SET OUTPUT-ONLY SWITCH         Y02669
         LA    R8,DEC2(R9)        UPDATE TO UNIT NAME            Y02669
         BAL   R15,SEARCH         GO GET UCME                    Y02669
         LTR   R5,R5              IS THERE A UCME                Y02669
         BNZ   GOTUCME2           YEP                            Y02669
BADOUNT  EQU   *                                                 Y02669
         LA    R9,DEC5(R9)        NO UCME, ERROR UPDATE PAST UNITY02669
         MVI   XAP+DEC0,XZERO     CLEAR OUTPUT-ONLY SWITCH       Y02669
         B     ENDSCAN            GO GET NEW UNIT, IF ANY        Y02669
*                                                                Y02669
GOTUCME2 EQU   *                                                 Y02669
         TM    UCMDISP,UCMDISPD   IS IT REALLY AN OUTPUT-ONLY UNTY02669
         BZ    BADOUNT            NO, BAD OUTPUT-ONLY SPECIFIED  Y02669
         SR    R11,R11            NO UCB FOR COMPORITE           Y02669
         SR    R3,R3              NO UCME FOR COMPOSITE          Y02669
*                                                                Y02669
*        R9 PTS TO THE UNIT SPECIFICATION   O-XXX                Y02669
*        R8 PTS TO THE UNIT NAME                                 Y02669
*        R10 HAS UCB PTR FOR UNIT                                Y02669
*        R11 = 0 NO COMPOSITE UCB PTR                            Y02669
*        R5 HAS PTR TO UNIT'S UCME                               Y02669
*        R3 = 0 NO COMPOSITE UCME PTR                            Y02669
*                                                                Y02669
*  MUST CHECK VIA UCB IF DEVICE IS ONLINE OR UNDER TEST BY OLTEP Y02669
*  EITHER OF BOTH ARE ERRORS, AND NO RECORD CAN BE WRITTEN       Y02669
*                                                                Y02669
         BAL   R6,CHECKUCB        GO CHECK UCB                   Y02669
         LTR   R15,R15         WAS UNT OFFL AND NOT UNDER OLTEP  Y02669
         BNZ   BADOUNT            NO, NO RECORD ISSUED           Y02669
         LA    R9,DEC5(R9)        INDEX PAST UNIT                Y02669
         B     SEESPACE           GO PUT RECORD                  Y02669
*                                                                Y02669
         EJECT
REGUNIT  EQU   *                                                 Y02669
*                                                                Y02669
*  PROCESSING REGULAR 3 CHARACTER UNIT  XXX                      Y02669
*                                                                Y02669
         OI    XAP+DEC0,REGUNT    SET 'REG-UNIT SWITCH'          Y02669
         LR    R8,R9              SET REG8 TO THE UNITNAME       Y02669
         BAL   R15,SEARCH         GET UCME, IF ANY               Y02669
         LTR   R5,R5              ANY UCME                       Y02669
         BZ    IOUNIT             NO, THIS IS A REGULAR I/O UNT  Y02669
         TM    UCMATR,UCMOF+UCMIF HAS UCME, DOES IT HAVE INPUT   Y02669
*                                 OUTPUT CAPABILITIES            Y02669
         BC    DEC12,BADREG       NO, ERROR IN REG. UNIT         Y02669
         SR    R11,R11            NO COMPOSITE UCB PTR           Y02669
         SR    R3,R3              NO COMPOSITE UCME PTR          Y02669
*                                                                Y02669
*        R8 = R9 PTR TO UNIT NAME                                Y02669
*        R10 HAS PTR TO UNIT'S UCB                               Y02669
*        R11 = 0 NO COMPOSITE PTR                                Y02669
*        R5 HAS PTR TO UNIT'S UCME                               Y02669
*        R3 = 0 NO COMPOSITE UCME                                Y02669
*                                                                Y02669
*  MUST CHECK VIA UCB IF DEVICE IS ONLINE OF UNDER TEST BY OLTEP Y02669
*  EITHER OR BOTH ARE ERRORS, AND NO RECORD CAN BE PRINTED       Y02669
*                                                                Y02669
         BAL   R6,CHECKUCB        GO CHECK UCB                   Y02669
CODECHK  EQU   *                                                 Y02669
         LTR   R15,R15            UNIT OFFL AND NOT UNDER OLTEP  Y02669
         BNZ   BADREG             NO, NO RECORD PRINTED          Y02669
         LA    R9,DEC3(R9)        INDEX PAST UNIT                Y02669
         B     SEESPACE           GO PUT RECORD FOR UNIT         Y02669
*                                                                Y02669
BADREG   EQU   *                                                 Y02669
         LA    R9,DEC3(R9)        INDEX PAST UNIT                Y02669
         MVI   XAP+DEC0,XZERO     CLEAR 'REG-UNIT SWITCH'        Y02669
         B     ENDSCAN            GO FIND ANOTHER UNIT           Y02669
*                                                                Y02669
         EJECT
IOUNIT   EQU   *                                                 Y02669
*                                                                Y02669
* PROCESSING REG UNIT WHICH IS I/O ONLY (NO CONSOLE CAPABILITIES)Y02669
*                                                                Y02669
         TM    XASOPCOD,XAON      IS IT AN ONLINE KEYWORD?     @Z30LPTJ
         BZ    BADREG             NO, NO CONSL KEY ALLOWED HERE  Y02669
         DROP  R5                                                Y02669
         USING SETCVT,R5                                         Y02669
         L     R5,CVTPTR          CVT PTR                        Y02669
         L     R5,CVTILK2         UCB LOOK-UP TABLE              Y02669
         DROP  R5                                                Y02669
VULOOK   EQU   *                                                 Y02669
         LH    R10,DEC0(R5)       OBTAIN UCB PTR                 Y02669
         LTR   R10,R10            ANY                            Y02669
         BZ    VBUMP              TAKE BRANCH TO TEST THE NEXT   Y02669
         LH    R6,VFFFF           CHECK END OF UCBLIST           Y02669
         CLR   R6,R10             COMPARING THE FFFF'S           Y02669
         BE    BADREG             NO UCB FOUND, ERROR            Y02669
         N     R10,VFFFF-DEC2     ZERO OUT HIGH ORDER BITS       Y02669
         CLC   UCBNAME(DEC3),DEC0(R9)  DOES THE UNITNAMES MATCH  Y02669
         BE    GOTUCB            YES, FOUND THE UCB              Y02669
VBUMP    EQU   *                                                 Y02669
         LA    R5,DEC2(R5)        UPDATE UCBLIST PTR             Y02669
         B     VULOOK             CHECK NEXT UCB                 Y02669
*                                                                Y02669
*                                                                Y02669
GOTUCB   EQU   *                                                 Y02669
         SR    R11,R11            NO COMPOSITE UCB PTR           Y02669
*                                                                Y02669
*        R8 = R9 PTR TO THE UNITNAME                             Y02669
*        R11 = 0 NO COMPOSITE UCB PTR                            Y02669
*        R10 UCB PTR OF UNIT                                     Y02669
*        R5 = R3 = 0 NO UNITS OR NO COMPOSITES UCME              Y02669
*                                                                Y02669
         SR    R5,R5                                             Y02669
         SR    R3,R3                                             Y02669
*                                                                Y02669
*  MUST CHECK VIA UCB IF DEVICE IS ONLINE OF UNDER TEST BY OLTEP Y02669
*  EITHER OR BOTH ARE ERRORS AND NO RECORD CAN BE WRITTEN        Y02669
*                                                                Y02669
         BAL   R6,CHECKUCB        GO CHECK UCB                   Y02669
         B     CODECHK            AND CHECK RESULTS              Y02669
*                                                                Y02669
         EJECT
CHECKUCB EQU   *                                                 Y02669
*                                                                Y02669
*  THIS SUBROUTINE WILL CHECK VIA THE UCB POINTED BY REG10,      Y02669
*  IF THE DEVICE IS ONLINE/OFFLINE AND IF THE DEVICE IS          Y02669
*  UNDER TEST UNDER OLTEP.                                       Y02669
*  IF THE DEVICE IS ONLINE, NO RECORD SHOULD BE WRITTEN AND THIS Y02669
*  SUBROUTINE RETURNS A RETURN CODE OF : 8                       Y02669
*  IF THE DEVICE IS UNDER TEST BY OLTEP, THEN AGAIN NO RECORD    Y02669
*  SHOULD BE WRITTEN AND THIS SUBROUTINE RETURNS A RETCODE OF :4 Y02669
*                                                                Y02669
*  ON ENTRY THE FOLLOWING INPUTS ARE EXPECTED                    Y02669
*        R8 PTS TO THE UNITNAME                                  Y02669
*        R10 PTS TO THE UCB OF THE UNIT                          Y02669
*                                                                Y02669
         LA    R15,DEC4           PRESET FOR AN OLTEP ERROR      Y02669
         TM    UCBFL5,UCBNALOC    DEVICE IS UNDER OLTEP          Y02669
         BO    RET                YES, ERROR, NO RECORD          Y02669
         LA    R15,DEC8           PRESET FOR ONLINE ERROR        Y02669
         TM    SRTESTAT,SRTEONLI  IS DEVICE ONLINE               Y02669
         BO    RET                YES, ERROR, NO RECORD          Y02669
         SR    R15,R15            ALL OK, CODE IS 0 ON RETURN    Y02669
RET      EQU   *                                                 Y02669
         BR    R6                 RETURN TO CALLER               Y02669
*                                                                Y02669
COMPUCB  EQU   *                                                 Y02669
*                                                                Y02669
*  THIS SUBROUTINE WILL CHECK SAME AS THE 'CHECKUCB' BUT IT WILL Y02669
*  PERFORM THIS FUNCTION FOR THE OUTPUT UNIT OF A COMPOSITE UNIT Y02669
*                                                                Y02669
*        R8 PTS TO THE UNITNAME                                  Y02669
*        R11 PTS TO THE UCB OF THE OUTPUT UNIT OF THE COMPOSITE  Y02669
*                                                                Y02669
         DROP  R10                                               Y02669
         USING UCBOB,R11                                         Y02669
         LA    R15,DEC8           PRESET FOR THE ONLINE ERROR    Y02669
         TM    SRTESTAT,SRTEONLI  IS UNIT ONLINE                 Y02669
         BO    RET                RETURN, ERROR                  Y02669
         LA    R15,DEC4           PRESET FOR OLTEP ERROR         Y02669
         TM    UCBFL5,UCBNALOC    IS COMP UNDER TEST BY OLTEP    Y02669
         BO    RET                YES, ERROR NO RECORD           Y02669
         SR    R15,R15            ALL IS OK, RETURN CODE = 0     Y02669
         BR    R6                 RETURN TO CALLER               Y02669
         DROP  R11                                               Y02669
         USING UCBOB,R10                                         Y02669
*                                                                Y02669
         EJECT
SEESPACE EQU   *                                                 Y02669
*                                                                Y02669
*  THIS SUBROUTINE WILL CHECK IF ENOUGH SPACE IS AVAILABLE IN THEY02669
*  SMF RECORD BUFFER TO INSERT ONE MORE RECD (FOR A SINGLE DEV)  Y02669
*  (HANDLED UNDER SEESPACE), OR FOR TWO DEVICES IF THE UNIT IS A Y02669
*  COMPOSITE DEVICE (HANDLED UNDER COMPSPACE).                   Y02669
*  IF THERE IS ENOUGH SPACE, THEN THE RECORD IS INSERTED, PTRS   Y02669
*  ARE UPDATED AND WE RETURN TO SEE IF MORE UNITS ARE TO BE      Y02669
*  PROCESSED.                                                    Y02669
*  IF NO SPACE IS AVAILABLE, THEN WE WRITE THIS RECORD FIRST, ANDY02669
*  THEN WE UPDATED A BRANK-NEW RECORD WITH THIS DEVICE'S INFO    Y02669
*                                                                Y02669
         LH    R3,DEC0(R1)    OBTAIN TOTAL LENGTH TO DETERMINE   Y02669
*                                 IF SPACE FOR PUTTING AN ENTRY  Y02669
*                                 EXISTS OR NOT                  Y02669
         CH    R3,CONS156         TEST FOR ONE ENTRY ONLY        Y02669
         BL    ITISOK             YES, ENOUGH ROOM               Y02669
         BAL   R6,WRITEIT         NO, MUST WRITE THIS REC FIRST  Y02669
*                                                                Y02669
ITISOK   EQU   *                                                 Y02669
         BAL   R6,ENTRY           UPDATE REC WITH THIS DEV'S INFOY02669
SMORE    EQU   *                                                 Y02669
         MVI   XAP+DEC0,XZERO     CLEAR UNTYPE SWITCHS SINCE REC Y02669
*                            HAS BEEN PUT DOWN IN THE BUFFER AND Y02669
*                                 FINISHED WITH THIS DEVICE      Y02669
PARENCHK EQU   *                                                 Y02669
         LTR   R13,R13            WAS THIS A MULTIPLE UNIT COMD  Y02669
         BZ    SEXIT              NO, NOMORE UNITS, WERE FINISHEDY02669
         CLI   DEC0(R9),RPAREN    IS THERE AN EXT PAREN HERE     Y02669
         BNE   NOPAREN            NO, NO CLOSING PARENS HERE     Y02669
         BCTR  R13,DEC0           YES, DECREMNT PARNS SWITH BY 1 Y02669
         LA    R9,DEC1(R9)        UPDATE PTR PAST THE PARENS     Y02669
         B     PARENCHK           SEE IF WE ARE FINISHED         Y02669
NOPAREN  EQU   *                                                 Y02669
         CLI   DEC0(R9),COMMA     IS THERE A COMMA HERE          Y02669
         BNE   ENDSCAN            NO, SOMETHING IS WRONG         Y02669
         LA    R9,DEC1(R9)        YES, UPDATE PAST THE COMMA     Y02669
         B     START              GO CHECK FOR A NEW UNIT        Y02669
*                                                                Y02669
*                                                                Y02669
COMPSPAC EQU   *                                                 Y02669
         LH    R3,DEC0(R1)        OBTAIN TOTAL LNG TO DETERMINE  Y02669
*                                 IF SPACE FOR PUTTING ENTRIES   Y02669
*                                 EXISTS OR NOT                  Y02669
         CH    R3,CONS152         TST FOR 2 ENTRIES (I AND O)    Y02669
         BL    SIZEOK             YES, ENOUGH ROOM               Y02669
         BAL   R6,WRITEIT         NO, WRITE CURRENT RECORD FIRST Y02669
*                                                                Y02669
SIZEOK   EQU   *                                                 Y02669
         TM    XAP+DEC0,NRECI     IS INPUT UNT TO HAVE A REC     Y02669
         BO    RECORDO            NO CHECK IF THE OUTPUT IS      Y02669
         BAL   R6,ENTRY           GO FILL ENTRY FOR INPUT UNIT   Y02669
*                                                                Y02669
RECORDO  EQU   *                                                 Y02669
         TM    XAP+DEC0,NRECO     IS THE O UNIT TO HAVE A RECORD Y02669
         BO    SMORE              NO, FINISHED                   Y02669
         LR    R10,R11        GET R10 TO PT TO OUTPUT UNIT'S UCB Y02669
         BAL   R6,ENTRY           FILL ENTRY FOR OUTPUT UNIT     Y02669
         B     SMORE              FINISHED                       Y02669
*                                                                Y02669
         EJECT
WRITEIT  EQU   *                                                 Y02669
*                                                                Y02669
*  THIS ROUTINE ISSUES AN SVC83 TO WRITE AN SMF RECORD           Y02669
*                                                                Y02669
         LR    R7,R1              SAVE POINTER ACROSS SVC CALL @YM3500P
         SVC   83                                                Y02669
         LR    R1,R7            RESTORE POINTER AFTER SVC CALL @YM3500P
         MVI   DEC1(R1),TWENTY    REINIT TOTAL LENGTH TO 20      Y02669
         MVC   VARLNTH(DEC2,R1),TWOCON REINIT VARY LENGTH TO 2   Y02669
         LA    R14,DEC20(R1)      REINIT R14 TO PT TO NEXT AREA  Y02669
         XC    DEC0(ONEFORTY,R14),DEC0(R14)  ZERO OUT PREV. DATA Y02669
         BR    R6                 RETURN TO CALLER               Y02669
*                                                                Y02669
         EJECT
ENTRY    EQU   *                                                 Y02669
*                                                                Y02669
*  THIS SUBROUTINE WILL ENTER THE APPROPRIATE INFO INTO THE SMF  Y02669
*  RECORD BUFFER FOR THE DEVICE WHOSE UCB PTR IS IN REG10        Y02669
*                                                                Y02669
         LH    R3,DEC0(R1)        OBTAIN TOTAL LENGTH AND        Y02669
         LA    R3,DEC4(R3)        ADD FOUR TO IT                 Y02669
         STH   R3,DEC0(R1)        PUT IN THE UPDATED FIGURE      Y02669
         LH    R3,VARLNTH(R1)     UPDATE THE VARIABLE LENGTH ALSOY02669
         LA    R3,DEC4(R3)        BY ADDING FOUR IN IT           Y02669
         STH   R3,VARLNTH(R1)     AND PUT IT BACK                Y02669
         MVC   DEC0(DEC2,R14),UCBTBYT3 COPY DEVICE'S INFO        Y02669
         LR    R7,R1              WORKING ADDRESS REGISTER     @ZA03202
         LA    R3,DEC3            LIMIT OF 3 LOOPS             @ZA03202
         MVC   ONESXTY5(DEC3,R7),UCBNAME OBTAIN UNIT NAME      @ZA03202
LOOP3X   EQU   *                                               @ZA03202
         CLI   ONESXTY5(R7),XC1   IS CHAR. BELOW X'C1' (A)?    @ZA03202
         BL    TESTLOOP           YES, CHECK NEXT BYTE IN LOOP @ZA03202
         CLI   ONESXTY5(R7),XC6   IS CHAR. ABOVE X'C6' (F)?    @ZA03202
         BH    TESTLOOP           YES, CHECK NEXT BYTE IN LOOP @ZA03202
         TR    ONESXTY5(DEC1,R7),TABLE ALTER BYTE FOR PACK     @ZA03202
TESTLOOP EQU   *                                               @ZA03202
         LA    R7,DEC1(R7)        BUMP TO NEXT BYTE            @ZA03202
         BCT   R3,LOOP3X          LOOP UNTIL DONE              @ZA03202
         PACK  ONESIXTY(DEC4,R1),ONESXTY5(DEC3,R1) DELETE ZONES@ZA03202
         L     R3,ONESIXTY(R1)    SAVE PACKED ADDRESS          @ZA03202
         SRL   R3,DEC4            SHIFT OUT THE SIGN BIT       @ZA03202
         STH   R3,DEC2(R14)       INSERT IN THE SMF FILE       @ZA03202
         NI    DEC2(R14),HEX0F    CLEAR UNUSED BITS            @YM07237
         TM    UCBTBYT2,UCBRVDEV  IS IT A VIRTUAL DEVICE?      @Z30LPTJ
         BNO   NOHIGHBT           NO, DO NOT SET HIGH ORDER BIT@Z30LPTJ
         CLC   UCBTBYT3(DEC2),THRTY330 IS IT A 3330 DEVICE?    @Z30LPTJ
         BNE   NOHIGHBT           NO, DO NOT SET HIGH ORDER BIT@Z30LPTJ
         OI    DEC2(R14),VUNIT    SET HIGH ORDER BIT           @Z30LPTJ
NOHIGHBT EQU   *                                               @Z30LPTJ
         LA    R14,DEC4(R14)      UPDATE THE VAR. DATA PTR       Y02669
         BR    R6                 RETURN TO CALLER               Y02669
*                                                                Y02669
         EJECT
SEARCH   EQU   *                                                 Y02669
*                                                                Y02669
*  THIS SUBROUTINE WILL SEARCH AND FIND IF THERE IS ANY THE      Y02669
*  UCME FOR THE DEVICE WHOSE UNITNAME IS POINTED BY REG8         Y02669
*  ON RETURN THE UCB PTR WILL BE IN REG 10 AND THE UCME WILL     Y02669
*  BE POINTED BY REG5                                            Y02669
*  OTHERWISE THESE REGS WILL BE 0                                Y02669
*                                                                Y02669
         L     R5,CVTPTR                                         Y02669
         USING SETCVT,R5                                         Y02669
         L     R5,CVTCUCB                                        Y02669
         DROP  R5                                                Y02669
         USING UCM,R5                                            Y02669
         LM    R5,R7,UCMVEA                                      Y02669
         USING UCMLIST,R5                                        Y02669
UCMESRCH EQU   *                                                 Y02669
         L     R10,UCMUCB                                        Y02669
         CLC   DEC0(DEC3,R8),UCBNAME   UNITNAMES MATCH           Y02669
         BCR   DEC8,R15           NAMES MATCH, FINISHED          Y02669
         CLR   R5,R7              IS IT END OF UCME LIST         Y02669
         BE    NOUCME             YES, ERROR, NO UCME FOUND      Y02669
         AR    R5,R6              NO, GO TO CHECK NEXT UCME      Y02669
         B     UCMESRCH           LOOP TO SEE IF THIS THE UCME   Y02669
*                                                                Y02669
NOUCME   EQU   *                                                 Y02669
         SR    R5,R5              NO UCME FOUND                  Y02669
         SR    R10,R10            AND, THEREFORE NO UCB          Y02669
         BR    R15                RETURN TO CALLER               Y02669
         DROP  R5                                                Y02669
*                                                                Y02669
         EJECT
SEXIT    EQU   *                                                 Y02669
*                                                                Y02669
*  THIS SUBROUTINE CLEANS UP AND EXITS TO 4403D OR TO 4303D      Y02669
*  DEPENDING ON WHAT WAS PRESET BY 3303D                         Y02669
*                                                                Y02669
OVER     EQU   *                                                 Y02669
         CLC   VARLNTH(DEC2,R1),TWOCON HAS ANY ENTRY BEEN MADEIN Y02669
*                                 THE SMF RECORD                 Y02669
         BE    NORECORD           DO NOT WRITE THE RECORD        Y02669
*                                 IN THAT CASE                   Y02669
         BAL   R6,WRITEIT         GO AND WRITE THE RECORD, FIRST Y02669
NORECORD EQU   *                                                 Y02669
         LA    R0,ONESXTY8        SIZE OF SMF RECORD BUFFER    @ZA03202
         FREEMAIN  R,LV=(0),A=(1)                                Y02669
VEXIT    EQU   *                                                 Y02669
         L     R13,XAK            RESTORE SAVE AREA PTR        @Z30LPTJ
         XC    XAP(DEC4),XAP                                     Y02669
         MVC   XAX+DEC3(DEC2),X4203    ASSUME GOING TO 4203D     Y02669
         L     R15,VCON42         GET ADDRESS OF 4203D           Y02669
         TM    XASWITCH,XASMF42   IS IT A 4203D EXIT           @Z30LPTJ
         BO    VEXIT2             YES, GO TO BRANCH              Y02669
         MVC   XAX+DEC3(DEC2),X4403      NO, EXIT IS TO 4403D    Y02669
         L     R15,VCON44                                        Y02669
VEXIT2   EQU   *                                                 Y02669
         LM    R0,R14,DEC0(R13)                                  Y02669
         BR    R15                EXIT                           Y02669
*                                                                Y02669
         EJECT
X4203    DC    C'42'               4203D VARY MODULE             Y02669
X4403    DC    C'44'               4403D VARY MODULE             Y02669
         DS    0F
VCON42   DC    V(IEE4203D)                                       Y02669
VCON44   DC    V(IEE4403D)                                       Y02669
         DS    0F                                                Y02669
TWOCON   DC    H'2'
CONS152  DC    H'152'
CONS156  DC    H'156'
CON      DC    C'CON'                                            Y02669
ONL      DC    C'ONL'                                            Y02669
PARENI   DC    C'(I-'                                            Y02669
HONE     DC    H'1'                                              Y02669
CMAO     DC    C',O-'                                            Y02669
OUNIT    DC    C'O-'                                             Y02669
         DS    0F                                                Y02669
EIGHT    DC    X'00000008'                                       Y02669
         DS    0F
         DC    X'0000'        THESE TWO CONSTANTS MUST BE LEFT   Y02669
VFFFF    DC    X'FFFF'        TOGETHER                           Y02669
THRTY330 DC    X'2009'        3330 UCB DEFINITION              @Z30LPTJ
TABLE    EQU   *-C'A'                                          @ZA03202
         DC    X'FAFBFCFDFEFF'                                 @ZA03202
         SPACE 5
PATCH    DC    CL50'***IEE2303D***'
         EJECT
EXSAVEC  DSECT
         IEEXSA
         EJECT
         IEESMCA
         EJECT
SETCVT   DSECT
         CVT
         EJECT
UUCM     DSECT
         IEECUCM  FORMAT=NEW
         EJECT
UCB      DSECT
         IEFUCBOB
         EJECT
         END
