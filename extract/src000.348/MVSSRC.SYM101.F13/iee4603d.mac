 TITLE 'IEE4603D - VARY ONLINE AND OFFLINE FOR CONSOLE DEVICES'
IEE4603D CSECT
* C 072000-072900,234000-234900
* D 246600
***********************************************************************
* MODULE NAME      = IEE4603D
*   CSECT NAME     = IEE4603D
*
* LOAD MODULE NAME = IEE3603D
*
* DESCRIPTIVE NAME = VARY ONLINE, OFFLINE FOR CONSOLE DEVICES
*
* COPYRIGHT        = MISSING
*
* STATUS           = OS/VS/2 RELEASE 3                         @Z30LPTJ
*
* FUNCTION         = IEE4603D WILL PROCESS THE ONLINE/OFFLINE
*                    KEYWORDS OF THE VARY COMMAND BY SETTING BITS
*                    IN THE UNIT'S UCB AND UCME
*
* OPERATION        = IEE4603D IS ENTERED ONLY FROM IEE4203D
*                    WHICH HAS SCANNED THE UNIT FIELD OF THE VARY
*                    COMMAND FOR ALL ERRORS.
*                    THIS MODULE WILL PROCESS THE ONLINE/OFFLINE
*                    KEYWORDS OF THE VARY COMMAND
*                    IT WILL SET BITS FOR ON/OFFLINE PROCESSING
*                    IN THE UNIT'S UCB AND UCME
*                    WHEN THE UNIT HAS BEEN VARIED, 4603D WILL BALR
*                    TO IEAVMNTR TO PROCESS ALL BIT CHANGES REQUIRED
*                    FOR THE FOLLOWING COMANDS:
*                            MONITOR JOBNAMES
*                            MONITOR STATUS
*                            MONITOR SESSIONS
*                    FOR THIS DEVICE WHICH IS CHANGING STATUS FROM
*                    FORM A CONSOLE TO A NON-CONSOLE STATE.
*                    THIS IS DONE BECAUSE:
*                    1. SINCE THIS CONSOLE IS NOT FUNCTIONING AS
*                    SUCH ANYMORE, WE DO NOT WANT ANY MORE OF THESE
*                    COMMAND'S MESSAGES BEING QUEUED TO IT (IF IN
*                    FACT HAD EVER ISSUED ANY OF THE ABOVE THREE
*                    COMMANDS).
*                    2. IF THIS CONSOLE, WHEN IT WAS ONE, DID IN
*                    FACT ISSUE ANY OF THE ABOVE THREE COMMANDS,
*                    AND IT WAS THE ONLY CONSOLE ON THE SYSTEM
*                    WHICH HAS ISSUED THEM, THEN WE ALSO WANT
*                    THE BITS INDICATING THESE COMMANDS TURNED
*                    OFF IN THE MASTER RESIDENT CORE AREA.
*                    THIS SUBROUTINE (IEAVMNTR) WILL DO ALL THE
*                    CHECKING AND ALL THE BIT'S SETTING.
*                    3. DEVICES WHICH ARE CURRENTLY INACTIVE AS
*                    CONSOLES AND ARE:
*                    OFFLINE                CMD ISSUED: V OFFLINE
*                    ONLINE,ALLOC           CMD ISSUED: V ONLINE
*                    ONLINE,UNALLOC         CMD ISSUED: V ONLINE
*                    OFFLINE                CMD ISSUED: V ONLINE
*                    ARE ALL HANDLED AS IMMEDIATE COMMANDS AND ALL
*                    STATUS BITS ARE SET IN THIS MODULE.
*                    HOWEVER, WHEN THE DEVICE IS:
*                    ONLINE                 CMD ISSUED: V OFFLINE
*                    THEN IT CANNOT BE MADE OFFLINE IMMEDIATELY
*                    AND TERMINATION WILL HANDLE THIS SITUATION.
*                    ANY DEVICES FUNCTIONING CURRENTLY AS
*                    CONSOLES CANNOT BE VARIED TO THE ONLINE/OFFLINE
*                    STATES IMMEDIATELY AND COMM. TASK AND
*                    TERMINATION WILL HANDLE THIS CASES.
*
*                    IEE4603D SETS THE FOLLOWING BITS:
*                     ..FOR ACTIVE CONSOLE TO ONLINE:
*                            CLOSE PENDING       ON   UCME
*                            ON/OFF CHANGE STATS OFF  UCB
*                            CONSL.CHANGE STATUS ON   UCB
*                            MP-OPERATOR BIT     N/A  UCB
*
*                     ..FOR ACTIVE CONSOLE TO OFFLINE:
*                            CLOSE PENDING       ON   UCME
*                            CONSL.CHANGE STATUS ON   UCB
*                            ON/OFF CHANG STATUS ON   UCB
*                            OPERATOR-MP BIT     ON   UCB
*
*                       FOR INACTIVE CONSOLE TO ONLINE, THE
*                       REQUEST IS NOT HONORED IF THE DEVICE
*                       IS CURRENTLY IN USE BY A SYSTEM        @YM07231
*                       COMPONENT.                             @YM07231
*
*                     ..FOR INACTIVE CONSOLE TO ONLINE:
*                           .IF INACTIVE CONSL = OFFLINE:
*                            ONLINE BIT            UCB  ON
*                            ONL/OFF CHANGE STATUS UCB  OFF
*                            CONSL.CHANGE STATUS   UCB  OFF
*                            OPERATOR-MP BIT       UCB  OFF
*
*                           .IF INACTIVE CONSOLE = ONLINE,ALLOCATED
*                            ONLINE BIT            UCB  ON
*                            ON/OFF CHANGE STATUS  UCB  OFF
*                            CONSL.CHANGE STATUS   UCB  OFF
*                            MP-OPERATOR BIT       UCB  N/A
*
*                           .IF INACTIVE CONSOLE = ONLINE,UNALLOCATED
*                            ONLINE BIT           UCB  ON
*                            ON/OFF CHANGE STATUS UCB  OFF
*                            CONSL.CHANGE STATUS  UCB  OFF
*                            MP-OPERATOR BIT      UCB  N/A
*
*                     ..FOR INACTIVE CONSOLE TO OFFLINE:
*                           .IF INACTIVE CONSOLE = OFFLINE:
*                            ON/OFF CHANGE STATS.  UCB  OFF
*                            CONSL..CHANGE STATUS  UCB  OFF
*                            MP-OPERATOR BIT       UCB  N/A
*
*                           .IF INACTIVE CONSOLE = ONLINE,ALLOCATED:
*                            ON/OFF CHANGE STATUS  UCB  ON
*                            CONSL.CHANGE STATUS   UCB  OFF
*                            MP-OPERATOR BIT       UCB  ON
*
*                           .IF INACTIVE CONSOLE = ONLINE,UNALLOCATED:
*                            ON/OFF CHANGE STATUS  UCB  ON
*                            CONSL.CHANGE STATUS   UCB  OFF
*                            MP-OPERATOR BIT       UCB  ON
*                            MRC SEARCH BIT(MSSUM) MRC  ON
*
*                    IEE4603D WILL DISPATCH PROPER MESSAGES TO THE
*                    VARIED CONSOLES, RESTEP THE UNIT FIELD POINTER,
*                    AND DETERMINE IF ALL UNITS HAVE BEEN PROCESSED.
*                    IF ALL UNITS HAVE BEEN PROCESSED AND IF ONLY I/O
*                    UNITS ARE TO BE VARIED ON/OFF, THEN IT BRANCHES
*                    TO IEE3103D. IF NO UNITS ARE (INCLUDING
*                    ANY I/O UNITS IT EXITS TO THE CALLER OF THE SVC
*                    IF VARY RANGE IS BEING PROCESSED, THEN    @Z30LPTJ
*                    MESSAGES ARE SUPPRESSED AND WHEN EXIT IS  @Z30LPTJ
*                    MADE, IT IS A BRANCH TO IEECB904 FOR      @Z30LPTJ
*                    FURTHER VARY RANGE PROCESSING             @Z30LPTJ
*
* NOTES
*    DEPENDENCIES = NONE
*    CHARACTER/CODE DEPENDENCIES
*                 = THIS MODULE CAN BE ASSEMBLER WITH ANY NEW
*                   CHARACTER/CODE SET.
*
* RESTRICTIONS    = R2 CONTAINS THE PTR TO THE XSA ON INPUT AND OUTPUT
*   REGISTER CONVENTIONS
*                 = REGISTER DEFINITION WILL BE FOUND AT:  REGDEF
*   PATCH LABEL   = PATCH
*
* MODULE TYPE     = CSECT
*   PROCESSOR     = ASSEMBLER
*   MODULE SIZE   = VS/2 = '692'
*   ATTRIBUTES    = REENTRANT, REUSABLE,REFRESHABLE,ZERO PSW
*                    PROTECT KEY, PAGED LPA
*
* ENTRY POINTS     = IEE4603D FROM IEE4203D
*    PURPOSE       = TO VARY ONLINE AND OFFLINE CONSOLE DEVICES
*    LINKAGE       = BRANCH
*    INPUT         = REG2 PTR TO THE DUMMY XSA
*                    XAD HAS THE SWITCHES COMPUTED BY IEE4203D
*                    XAR CONTAINS PROCESS SWITCHES FOR THE UNITS
*                    XAL PTR TO THE PARAMETER OF THE COMMAND
*                    REG13 PTS TO THE SAVE AREA GOTTEN BY 3603D
*                    WORD 17 OF THE SAVE AREA HAS THE PTR TO THE CSCB
*    REGISTERS SAVED= ALL
*    REGISTER USAGE= R0  CONSOLE ID FOR WTO
*                    R1  MRC PTR AND WTO
*                    R2  DUMMY XSA PTR
*                    R3  ADDRESSABILITY
*                    R4  UCME PTR
*                    R7  PROCESS BIT REG
*                    R9  UNIT PTR
*                    R10 UCM PTR
*                    R11 UCMPRFX PTR
*                    R12 UCB PTR
*                    R13 SAVE AREA PTR
*    REGISTERS RESTORED
*                  = ALL
*
* EXITS - NORMAL 1 = TO IEE3103D VIA BRANCH
*    CONDITIONS    = IF SOME UNITS (I/O) ARE LEFT TO BE
*                       PROCESSED BY IEE3103D (AS INDICATED BY
*                       XAUDVC BIT IN XAD), EXIT TO IEE3103D VIA BR.
*    OUTPUT        = 1. WTO'S FOR VARIED DEVICES (SEE MESSAGES)
*                    2. UCME AND UCB BIT SETTINGS FOR VARIED
*                       DEVICES (SEE OPERATION)
*                    3. IF EXIT TO IEE3103D, THE OUTPUT IS
*                       REG2 PTS TO THE DUMMY XSA
*                            XAR PTR TO THE LOGICAL END OF BUFFER
*                            (PTS TO COMMA BEFORE KEYWORD)
*                            XAL HAS COMMAND PARAM LIST
*                            XAV HAS THE VERB
*                            XAS+0(XASOPCOD) AND XAS+1(XSWITCH)
*                            HAVE BIT DESIGNATING THE KEYWORD:
*                                 ONLINE
*                                 OFFLINE
*                    REG13 HAS PTR TO THE SAVE AREA, WORD17=CSCB PTR
*    RETURN CODES  = NONE
*
* EXIT - NORMAL 2  = IEAVMNTR VIA BALR
*                  = ADDR. OF THIS RTN IS PRESET IN AREA:UCMMNTR
*                     OF THE UCM
*    CONDITIONS    = MONITOR COMMAND BIT HANDLING RTN. FOR THIS
*                    CONSOLE GOING OUT OF THIS STATE, AND THEREFORE,
*                    DOES NOT NEED MONITOR MSGS SENT TO IT ANYLONGER
*    OUTPUT        = REG1 POINTS TO A POINTER WHICH IN TURN
*                    POINTS TO A TWO WORD PARAMETER LIST CONTAINING
*                    THE DEVICE'S ID AND FUNCTION CODES.(SEE DSECT)
*                    R13 PTS TO A SAVEAREA
*                    R14 HAS RETURN ADDRESS
*    RETURN CODES  = NONE
*
* EXITS - NORMAL -3-= RETURN TO CALLER
*    CONDITIONS    = WHEN NO MORE UNITS AND NO I/O ARE TO BE PROCESSED
*    OUTPUT        = ALL UNITS IN COMMAND ARE COMPLETED
*                    SAVE AREA AND DUMMY XSA ARE FREED
*                    CSCB IS FREED VIA THE MGCR MACRO
*    RETURN CODES  = NONE
*
* EXITS - NORMAL 4 = TO IEECB904 VIA BRANCH                    @Z30LPTJ
*    CONDITIONS    = WHEN NO MORE UNITS AND NO I/O UNITS ARE   @Z30LPTJ
*                    TO BE PROCESSED AND VARY RANGE IS BEING   @Z30LPTJ
*                    PROCESSED                                 @Z30LPTJ
*    OUTPUT        = REGISTER 2 POINTING TO THE XSA WHICH HAS  @Z30LPTJ
*                    THE XAL POINTING TO THE VARY RANGE WORK   @Z30LPTJ
*                    AREA                                      @Z30LPTJ
*    RETURN CODES  = NONE                                      @Z30LPTJ
*
* EXITS - ERROR    = NONE
*
* EXTERNAL REFERENCES
*    ROUTINES      = IEE3103D
*                    IEECB904                                  @Z30LPTJ
*
*    DATA AREAS    = SEE EXITS - OUTPUT DATA
*
* CONTROL BLOCKS   = DUMMY EXTENDED SAVE AREA (XSA)
*                    CVT
*                       FIELDS UPDATED = NONE
*                       FIELDS TESTED  = CVTCUCB
*                                        CVTMSER
*                    UNIT CONTROL MODULE (UCM)
*                       FIELDS UPDATED = UCMSTS - UCMCF
*                       FIELDS TESTED  = UCMCOMPC
*                                        UCMMSG - BIT= UCMMSGA
*                                        UCMUCB
*                                        UCMVEA
*                                        UCMMNTR
*                    UNIT CONTROL BLOCK (UCB)
*                       FIELDS UPDATED = SRTESTAT - BIT= SRTECHGS
*                                                        SRTEDADI
*                                                        SRTEONLI
*                                        UCBWGT - BIT= UCBVORSN
*                       FIELDS TESTED  = SRTESTAT - BIT= SRTEALOC
*                                                        SRTEONLI
*                                                        SRTESYSR
*                                        UCBNAME
*                     COMMAND SCHEDULING CONTROL BLOCK (CSCB)
*                       FIELDS TESTED  - NONE
*                       FIELDS UPDATED - CHSTS -BIT= CHFC
*                     MASTER SCHEDULER RESIDENT DATA AREA (MRC)
*                       FIELDS TESTED  - NONE
*                       FIELDS UPDATED - MSECBFL - BIT= MSSUM
*
* TABLES           = NONE
*
* MACROS           = WTO,GETMAIN,FREEMAIN,MGCR,GETMAIN,SETLOCK,DEQ
*
* MAPPING MACROS   = IEEXSA
*                    CVT
*                    IEECUCM
*                    IEFUCBOB
*                    IEEBASEA
*                    IEECHAIN
*                    IHAMPL
*                    IHAPSA
*    SERIALIZATION = PROTECTION AGAINST COMM TASK WHEN SETTING ON/OFF
*                    THE UCME BITS IS ACCOMPLISHED VIA THE CMS AND
*                    LOCAL LOCKS OBTAINED AT: OSETLOCK
*                    AND RELEASED AT: RELEASE
*                    BOTH IN THIS MODULE.
*                    PROTECTION AGAINST ALLOCATION, VARY/UNLOAD AND
*                    SYSTEM COMPONENT IS ACCOMPLISHED BY       @YM07231
*                    EXECUTION OF THIS ROUTINE UNDER AN ENQ    @YM07231
*                    GOTTEN EARLIER, IN MODULE IEE3603D.       @YM07231
*                    A DEQ WILL BE ISSUED IN THIS MODULE IF THE
*                    TERMINAL EXIT IS TAKEN AT LOC: ODEQUE
*                    ALL UCB AND MRC BITS THAT ARE TURNED ON/OFF IN
*                    THIS ROUTINE ARE SET BY THE COMPARE-AND-SWAP
*                    INSTRUCTION.
*
* CHANGE LEVEL     = Y02669, Y02651, Z30LPTJ                   @Z30LPTJ
*
* CHANGE ACTIVITY = N/A
*
* MESSAGES         = 'IEE302I XXX ONLINE'
*                    'IEE303I XXX OFFLINE'
*                       (WHERE XXX IS THE UNIT NAME IN QUESTION)
*                       THE ABOVE MESSAGES ARE SUPPRESSED      @Z30LPTJ
*                       WHEN VARY RANGE IS BEING PROCESSED.    @Z30LPTJ
*
* ABEND CODES      = NONE
*
* PACKAGING        = IEE4603D WILL BE PART OF LOAD MODULE
*                       = IEE3603D
*                    THIS LOAD MODULE ALIASES ARE
*                       = NONE
*                    THIS LOAD MODULE ENTRY PTS ARE
*                       = IEE3603D
*                    MODULE WILL RESIDE IN:  LPALIB
*
* SYSGEN           = SUPPLIED BY SECTIONS:
*                       LOAD MODULE NAME
*                       PACKAGING
*                       THIS CSECT WILL BE INCLUDED AT SYSGEN FROM
*                       DLIB -AOSB3- BY MACRO -SGIEF441-
*
* SYSTEM LIBRARIES = NONE
***********************************************************************
         EJECT
***********************************************************************
*
*   ***           ***
*   *               *
*   * XXX           *
*   *               * ONLINE
* V * O-XXX         *,
*   *               * OFFLINE
*   * (I-XXX,O-XXX) *
*   *               *
*   ***           ***
***********************************************************************
         EJECT
*                                                                 19084
*        REGISTER EQUATES                                         19084
*                                                                 19084
DEFREG   EQU   *                                                 Y02669
R0       EQU   0                   CONSOLE ID FOR WTO             19084
R1       EQU   1                   IN-XAD,USE-MRC PTR,AND WTO     19084
R2       EQU   2                   XSA                            19084
R3       EQU   3                   ADDRESS                        19084
R4       EQU   4                   UCME                           19084
R5       EQU   5                                                  19084
R6       EQU   6                                                  19084
R7       EQU   7                   PROCESS BIT REG                19084
R8       EQU   8                                                  19084
R9       EQU   9                   UNIT PTR                       19084
R10      EQU   10                  UCM REG                        19084
R11      EQU   11                  UCMPFX REG                     19084
R12      EQU   12                  UCB REG                        19084
R13      EQU   13                                                 19084
R14      EQU   14                  RET ADDR                       19084
R15      EQU   15                                                 19084
***********************************************************************
*                                                                 19084
*        EQUATES                                                  19084
*                                                                 19084
***********************************************************************
ZERO     EQU   0                                                  19084
ONE      EQU   1                                                  19084
TWO      EQU   2                                                  19084
TRE      EQU   3                                                  19084
FOR      EQU   4                                                  19084
SIX      EQU   6                                                  19084
SVN      EQU   7                                                  19084
EIT      EQU   8                                                  19084
TWELV    EQU   12                                                Y02669
SIXTN    EQU   16                                                Y02669
NINETEEN EQU   19                      *                          21051
D64      EQU   64                                                Y02669
D132     EQU   132                                               Y02669
D68      EQU   68                                                Y02669
*                                                                 19084
FOX      EQU   255                                                19084
*                                                                 19084
RIID     EQU   X'80'                                              19084
         SPACE 10                                                 19084
ONOFKEY  EQU   C'O'                                               19084
NLINE    EQU   C'N'                                               19084
FFLINE   EQU   C'F'                                               19084
EIGHTY8  EQU   88                                                Y02669
         EJECT
***********************************************************************
*                                                                 19084
*        ONLINE AND OFFLINE OF CONSOLE DEVICES PROCESSOR          19084
*                                                                 19084
***********************************************************************
         BALR  R3,ZERO                                            19084
         USING *,R3                                               19084
         MODID BR=YES                                            Y01886
         USING XSA,R2                                             19084
*
         STM   R0,R15,ZERO(R13)                                  Y02669
         L     R4,XAR              PROCESS SWITCHES               19084
         L     R9,XAL              GET PTR TO UNIT BEGIN          19084
         LA    R9,ZERO(R9)                                        19084
         L     R10,CVTPTR                                         19084
         USING OCVT,R10                                           19084
         L     R10,CVTCUCB         GET PTR TO UCM                 19084
         DROP  R10                                                19084
         USING UCM,R10                                           Y02669
*                                                                 19084
         SPACE 10                                                 19084
         TM    XADUSWIT,XAXPAREN   MULTIPLE UNITS              @Z30LPTJ
         BZ    OSINGLE                                            19084
OSTART   LA    R9,ONE(R9)          YES,INDEX PAST BEGINNING PARENS19084
OSINGLE  EQU   *                                                  19084
         CLC   ZERO(TWO,R9),OOUNIT IS IT AN O-XXX                 19084
         BNE   OCOMP               NO, MAYBE A COMPOSITE          19084
         LA    R9,TWO(R9)               YES INDEX TO UNITNAME     19084
         B     OTEST               GO CHECK IF MUST PROCESS IF    19084
*                                                                 19084
OCOMP    EQU   *                                                  19084
         CLC   ZERO(TRE,R9),OIUNIT     DO WE HAVE A COMPOSITE     19084
         BNE   OTEST               NO, ITS A REGULAR UNIT        Y02669
         LA    R9,TRE(R9)               YES,INDEX TO INPUT UNIT   19084
         OI    XADUSWIT,XAIOCOMP        TURN COMPOSITE SWITCH  @Z30LPTJ
*                                                                 19084
OTEST    EQU   *                                                  19084
         LTR   R4,R4                    DO WE PROCESS THIS UNIT   19084
         BZ    ONOMORE             NO, AND NOMORE EITHER,FINISHED 19084
         BM    OPROCES             YES,PROCESS THIS ONE           19084
*                                       NO, NOT PROCESS THIS ONE  19084
OOMIT    EQU   *                                                  19084
         TM    XADUSWIT,XAIOCOMP   IS IT A COMP WE HAVE TO OMIT@Z30LPTJ
         BZ    OENDCK              NO,                            19084
         LA    R9,SVN(R9)               YES INDEX OVER            19084
*                                                                 19084
OENDCK   EQU   *                                                  19084
         LA    R9,TRE(R9)          PT AFTER DLIM ADTER UNIT       19084
         NI    XADUSWIT,XAXPAREN+XAUDVC MULT. UNITS, CLEAN UP  @Z30LPTJ
         SLL   R4,ONE              ALL SWITCHES EXCEPT XAXPAREN@Z30LPTJ
         B     OSTART              AND IODEV SWITCH. GO START OVER19084
*                                                                 19084
OPROCES  EQU   *                                                  19084
         LM    R5,R7,UCMVEA        GET UCMES PTRS                 19084
         USING UCMLIST,R5                                         19084
OLOOP    L     R12,UCMUCB          AND UCB ADDRESSABILITY         19084
         USING OUCB,R12                                           19084
         CLC   ZERO(TRE,R9),UCBNAME     IS THIS THE UCME FOR UNIT 19084
         BE    OONOFF              YES,                          Y02669
         BXLE  R5,R6,OLOOP         MUST BE NEXT ONE GO CHECK      19084
*                                  WILL ALWAYS FIND ONE,CHECKED BY 4203
         EJECT                                                    19084
OONOFF   EQU   *                                                 Y02669
         SPACE 5
*****************************************************************Y02651
*                                                                Y02651
*  LOCKS MUST BE GOTTEN NOW FOR PROTECTION AGAINST COMM TASK     Y02651
*  THESE LOCKS WILL BE RELEASED IN THIS MODULE AT:               Y02651
*      RELEASE                                                   Y02651
*                                                                Y02651
OSETLOCK EQU   *                                                 Y02651
         LR    R8,R13             SAVE SAVEAREA POINTER          Y02651
         STM   R11,R14,D68(R8)                                   Y02651
         SETLOCK OBTAIN,TYPE=LOCAL,MODE=UNCOND,                  Y02651*
               RELATED=(IEE4603D(RELEASE))                       Y02651
         SETLOCK OBTAIN,TYPE=CMS,MODE=UNCOND,                    Y02651*
               RELATED=(IEE4603D(RELEASE))                       Y02651
         LM    R11,R14,D68(R8)                                   Y02651
*****************************************************************Y02651
        SPACE 10                                                19084
***********************************************************************
*                                                                 19084
*        PROCESS KEYWORDS                                         19084
*                                                                 19084
***********************************************************************
         TM    XASOPCOD,XAON       ONLINE                      @Z30LPTJ
         BZ    OOFFLN              NO, OFFLINE                    19084
         EJECT
***********************************************************************
*                                                                 19084
*        ONLINE KEYWORD                                           19084
*                                                                 19084
***********************************************************************
OONLINE  EQU   *                                                  19084
*                                                                 19084
****************PROCESS TO VARY CONSOLE TO ONLINE STATUS          19084
*                                                                 19084
*                                                                 19084
*                                                                 19084
         OI    XADUSWIT,XAUBLANK   MSG MUST BE PUT OUT         @Z30LPTJ
         TM    SRTESTAT,SRTESYSR   IS IT A CONSOLE NOW            19084
         BZ    ONTEST              NO, FUNCTIONING AS IO DEV      19084
SETBITS2 EQU   *                                                 Y02669
         OI    UCMSTS,UCMCF        TURN CLOSE PENDING FOR CTASK  Y02651
*        OI    SRTESTAT,SRTEDADI   SET CHAGING STATUS IN UCB     Y02651
*        NI    SRTESTAT,FOX-SRTECHGS  SET ON/OFF CHANGE STTS OFF Y02651
*                                                                Y02651
*********** THE ABOVE TWO INSTRUCTION WILL BE PROTECTED VIA THE  Y02651
*           COMPARE AND SWAP INSTRUCTION                         Y02651
*                                                                Y02651
         STM   R4,R6,D68(R13)     SAVE USED REGS                 Y02651
OIDADI   EQU   *                                                 Y02651
         OIL   SRTESTAT,SRTEDADI,WREGS=(4,5,6),REF=OUCB          Y02651
NICHGS   EQU   *                                                 Y02651
NILCHANG EQU   X'BF'    -X'FF'-SRTECHGS-
         NIL   SRTESTAT,NILCHANG,WREGS=(4,5,6),REF=OUCB          Y02651
         LM    R4,R6,D68(R13)     RESTORE REGS                   Y02651
*                                                                Y02651
         TM    XADUSWIT,XAIOCOMP  I-UNIT OF A COMP IN PROCESS? @Z30LPTJ
         BZ    OUNTTST            NO CHECK FOR OUNIT OF COMP     Y02669
SETOUNT  EQU   *                  YES, PRCES O-UNIT OF COMP FRST Y02669
         LR    R8,R9              SAVE I-UNIT'S UNITNAME PTR     Y02669
         LR    R6,R5              SAVE I-UNIT'S UCME PTR         Y02669
         LR    R11,R12            SAVE I-UNIT'S UCB PTR          Y02669
         L     R5,UCMCOMPC        GET O-UNIT'S UCME PTR          Y02669
         L     R12,UCMUCB         GET O-UNIT'S UCB PTR           Y02669
         LA    R9,SIX(R9)         GET O-INUT'S UNITNAME PTR      Y02669
         NI    XADUSWIT,FOX-XAIOCOMP TURN OFF I-UNIT'S FLAG    @Z30LPTJ
         OI    XADUSWIT,XAIPAREN  SET O-UNIT IN PROCESSED SW   @Z30LPTJ
         B     SETBITS2           GO TO SET FLAGS FOR O-UNIT     Y02669
*                                                                Y02669
OUNTTST  EQU   *                                                 Y02669
         TM    XADUSWIT,XAIPAREN  IF NOT I-UNIT, IS IT O-UNIT  @Z30LPTJ
         BZ    OMSGPTR            NO REGUNIT, GO ISSUE MSG       Y02669
         LR    R9,R8              YES, JUST ENDED PROCESING O-UNTY02669
         LR    R5,R6              RESTORE REGS TO CONTAIN        Y02669
         LR    R12,R11            THE I-UNIT'S INFOR             Y02669
         NI    XADUSWIT,FOX-XAIPAREN TURN OFF O-UNIT FLAG      @Z30LPTJ
         OI    XADUSWIT,XAIOCOMP  AND TURN ON THE I-UNIT FLAG  @Z30LPTJ
         B     OMSGPTR            GO ISSUE MSGS FOR BOTH UNITS   Y02669
*                                                                 19084
*                                                                 19084
ONTEST   EQU   *                                                  19084
*        NI    UCBWGT,FOX-UCBVORSN                               Y02651
*                                                                Y02669
*        OI    SRTESTAT,SRTEONLI       TURN ONLINE BIT           Y02651
*        NI    SRTESTAT,FOX-SRTECHGS+SRTEDADI  TURN OFF THE      Y02651
*                      ON/OFF AND CONSOLE CHANGE STATUS BITS     Y02651
*                                                                Y02651
*********   THE ABOVE TWO INSTRUCTIONS WILL BE PROTECTED VIA THE Y02651
*            COMPARE-AND-SWAP INSTRUCTION                        Y02651
*                                                                Y02651
         STM   R4,R6,D68(R13)     SAVE USED REGS                 Y02651
NILOPER  EQU   *                                                 Y02651
NILVORSN EQU    X'FB'     -X'FF'-UCBVORSN-
         NIL   UCBWGT,NILVORSN,WREGS=(4,5,6),REF=OUCB            Y02651
OIONLI   EQU   *                                                 Y02651
         OIL   SRTESTAT,SRTEONLI,WREGS=(4,5,6),REF=OUCB          Y02651
NILCHGS  EQU   *                                                 Y02651
         NIL   SRTESTAT,NILCHANG,WREGS=(4,5,6),REF=OUCB          Y02651
NIDADI   EQU   *                                                 Y02651
NILDADI  EQU    X'FE'     -X'FF'-SRTEDADI-
         NIL   SRTESTAT,NILDADI,WREGS=(4,5,6),REF=OUCB           Y02651
         LM    R4,R6,D68(R13)     RESTORE REGS                   Y02651
         TM    XADUSWIT,XAIOCOMP  I-UNIT OF COMP PROCESSED     @Z30LPTJ
         BZ    NOTCOMP            NO,CHECK IF O-UNIT OF COMP     Y02669
         LR    R8,R9              YES,MUST SET UP FOR O-UNIT     Y02669
         LR    R6,R5              R8=I-UNT'S UTNM PTR AND R15=   Y02669
         LR    R11,R12            I-UNT'S UCME, R11=I-UNT'S UCB  Y02669
         L     R5,UCMCOMPC        GET O-UNIT'S UCME PTR          Y02669
         L     R12,UCMUCB         O-UNIT'S UCB PTR               Y02669
         LA    R9,SIX(R9)         O-UNIT'S UNITNAME PTR          Y02669
         NI    XADUSWIT,FOX-XAIOCOMP TURN OFF I-UNIT FLAG      @Z30LPTJ
         OI    XADUSWIT,XAIPAREN  TURN ON O-UNIT'S FLAG        @Z30LPTJ
         B     ONTEST             GO SET BITS IN THE O-UNIT      Y02669
*                                                                Y02669
NOTCOMP  EQU   *                                                 Y02669
         TM    XADUSWIT,XAIPAREN  WAS IT THE O-UNIT PROCESSED  @Z30LPTJ
         BZ    OMSGPTR            NO, REG UNIT, GO ISSUE MSG     Y02669
         LR    R9,R8              YES, THEREFORE RESTORE I-UNT'S Y02669
         LR    R12,R11            POINTER REGISTERS              Y02669
         LR    R5,R6                                             Y02669
         NI    XADUSWIT,FOX-XAIPAREN TURN OFF O-UNIT FLAG      @Z30LPTJ
         OI    XADUSWIT,XAIOCOMP  TURN ON I-UNIT FLAG          @Z30LPTJ
*                                 ISSUE MSG FOR BOTH UNITS       Y02669
OMSGPTR  EQU   *
         SPACE 10
***************************************************              Y02651
*                                                                Y02651
*  SINCE A WTO MACRO WILL NOW BE ISSUED FOR THE DEVICE(S) JUST   Y02651
*  PROCESSED, ALL LOCKS MUST BE TEMPORARITY RELEASED AND RE-     Y02651
*  OBTAINED IF MORE DEVICES ARE TO BE PROCESSED                  Y02651
*                                                                Y02651
         BAL   R8,RELEASE                                        Y02651
*                                                                Y02651
***************************************************              Y02651
         SPACE 10
*                                                                 19084
         LA    R15,ONLMSG           PT TO ONLINE MSG              19084
         B     OMSGS               GO ISSUE MSG FOR UNIT          19084
*                                                                 19084
         EJECT
***********************************************************************
*                                                                 19084
*       OFFLINE KEYWORD                                           19084
*                                                                 19084
***********************************************************************
OOFFLN   EQU   *                                                  19084
*                                                                 19084
*                                                                 19084
*                                                                 19084
*************   PROCESS TO VARY CONSOLE TO OFFLINE STATUS         19084
*                                                                 19084
         TM    SRTESTAT,SRTESYSR   IS IT CURRENTLY A CONSOLE      19084
         BZ    UOFFTEST            NO, NOW IS USED AS IO DEV      19084
SETBITS  EQU   *                                                 Y02669
         OI    UCMSTS,UCMCF        SET CLOSE PENG BIT FOR C TASK Y02651
*                                                                Y02651
*        OI    UCBWGT,UCBVORSN                                   Y02651
*                                                                Y02669
*        OI    SRTESTAT,SRTECHGS+SRTEDADI  TURN ON/OFF CHANGEBIT Y02651
*                                  AND CONSOLE CHANGE STATUS BIT Y02651
*                                                                Y02651
*********** THE ABOVE INSTRUCTION WILL BE PROTECTED VIA THE      Y02651
*           COMPARE-AND-SWAP INSTRCTION                          Y02651
*                                                                Y02651
         STM   R4,R6,D68(R13)     SAVE USED REGS                 Y02651
OILOPER  EQU   *                                                 Y02651
         OIL   UCBWGT,UCBVORSN,WREGS=(4,5,6),REF=OUCB            Y02651
OICHGS   EQU   *                                                 Y02651
         OIL   SRTESTAT,SRTECHGS,WREGS=(4,5,6),REF=OUCB          Y02651
OILDADI  EQU   *                                                 Y02651
         OIL   SRTESTAT,SRTEDADI,WREGS=(4,5,6),REF=OUCB          Y02651
         LM    R4,R6,D68(R13)     RESTORE REGS                   Y02651
         OI    XADUSWIT,XAUBLANK  INDICATE MSG SENT            @Z30LPTJ
         TM    XADUSWIT,XAIOCOMP  PROCESSING I-UNIT OF COMP    @Z30LPTJ
         BZ    OFFPTR1            NO SEE IF IF IS O-UNIT OF COMP Y02669
         LR    R8,R9         YES,SAVE I-UNT'S REGS R8=UNTNM PTR  Y02669
         LR    R6,R5              STORE I-UNIT'S UCME PTR        Y02669
         LR    R11,R12            STORE I-UNIT'S UCB PTR         Y02669
         L     R5,UCMCOMPC        GET O-UNIT'S UCME PTR          Y02669
         L     R12,UCMUCB         GET O-UNIT'S UCB PTR           Y02669
         LA    R9,SIX(R9)         GET O-UNIT'S UNITNAME PTR      Y02669
         NI    XADUSWIT,FOX-XAIOCOMP TURN OFF I-UNIT FLAG      @Z30LPTJ
         OI    XADUSWIT,XAIPAREN  SET O-UNIT'S FLAG            @Z30LPTJ
         B     SETBITS            GO SET BITS IN THE O-UNIT      Y02669
*                                                                Y02669
OFFPTR1  EQU   *                                                 Y02669
         TM    XADUSWIT,XAIPAREN  WAS IF O-UNIT OF COMP        @Z30LPTJ
         BZ    OFFPTR             NO, REG UNIT, GO ISSUE MSG     Y02669
         LR    R9,R8              RESTORE I-UNIT'S REGISTER PTRS Y02669
         LR    R5,R6                                             Y02669
         LR    R12,R11                                           Y02669
         NI    XADUSWIT,FOX-XAIPAREN TURN OFF O-UNIT'S FLAG    @Z30LPTJ
         OI    XADUSWIT,XAIOCOMP  TURN ON I-UNIT FLAG          @Z30LPTJ
OFFPTR   EQU   *                                                  19084
         SPACE 10
***************************************************              Y02651
*                                                                Y02651
*  SINCE A WTO MACRO WILL NOW BE ISSUED FOR THE DEVICE(S) JUST   Y02651
*  PROCESSED, ALL LOCKS MUST BE TEMPORARILY RELEASE AND          Y02651
*  REOBTAINED IF MORE DEVICES ARE TO BE PROCESSED                Y02651
*                                                                Y02651
         BAL   R8,RELEASE                                        Y02651
*                                                                Y02651
***************************************************              Y02651
         SPACE 10
         LA    R15,OFFLMSG         OFFLINE MSG                    19084
         B     OMSGS               GO ISSUE MESSAGE               19084
*                                                                 19084
*                                                                 19084
UOFFTEST EQU   *                                                  19084
*        NI    SRTESTAT,FOX-SRTECHGS+SRTEDADI TURN OFF THE ON/OFFY02651
*                                  AND CONSOLE CHANGE STATUS BITSY02651
*                                                                Y02651
*********** THE ABOVE INSTRUCTION WILL BE PROTECTED VIA THE      Y02651
*           COMPARE-AND-SWAP INSTRUCTION                         Y02651
*                                                                Y02651
         STM   R4,R6,D68(R13)     SAVE REGS                      Y02651
OFFCHGS  EQU   *                                                 Y02651
         NIL   SRTESTAT,NILCHANG,WREGS=(4,5,6),REF=OUCB          Y02651
OFFDADI  EQU   *                                                 Y02651
         NIL   SRTESTAT,NILDADI,WREGS=(4,5,6),REF=OUCB           Y02651
*        OI    UCBWGT,UCBVORSN                                   Y02669
*                                                                Y02651
*********** THE ABOVE INSTRUCTION WILL BE PROTECTED VIA THE      Y02651
*           COMPARE-AND-SWAP INSTRUCTION                         Y02651
*                                                                Y02651
OILOPER2 EQU   *                                                 Y02651
         OIL   UCBWGT,UCBVORSN,WREGS=(4,5,6),REF=OUCB            Y02651
         LM    R4,R6,D68(R13)     RESTORE REGS
         TM    SRTESTAT,SRTEONLI  IS DEV NOW IN ONLINE STATE
         BZ    TESTCOMP           OFFLINE, TO GO OFFLINE
*        OI    SRTESTAT,SRTECHGS  SET ON/OFF CHNG STTS IN UCB
ONCHGS   EQU   *                                                 Y02651
         OIL   SRTESTAT,SRTECHGS,WREGS=(4,5,6),REF=OUCB          Y02651
         LM    R4,R6,D68(R13)                                    Y02651
*                                                                Y02651
         L     R1,CVTPTR                                         Y02669
         USING OCVT,R1                                           Y02669
         L     R1,CVTMSER                                        Y02669
         DROP  R1                                                Y02669
         USING BASE,R1                                           Y02669
*        OI    MSECBFL,MSSUM   SET THE UCB-SEARCH FLAG IN MRC    Y02651
OIMSSUM  EQU  *                                                  Y02651
         OIL   MSECBFL,MSSUM,WREGS=(4,5,6),REF=BASE              Y02651
         LM    R4,R6,D68(R13)                                    Y02651
*                                                                Y02651
         DROP  R1                                                Y02669
         NI    XADUSWIT,FOX-XAUBLANK SET TO ISSUE NO-MESSAGES  @Z30LPTJ
         TM    XADUSWIT,XAIOCOMP  I-UNIT OF COMP PROCESSED     @Z30LPTJ
         BO    GETOUNT            YES, GET O-UNIT AND PROCESS    Y02669
         TM    XADUSWIT,XAIPAREN  NO, WAS IT THE O-UNIT        @Z30LPTJ
         BZ    OCONT2        NO,REGUNT,RELEASE AND GOTO NXT UNT  Y02669
         LR    R9,R8              YES, RESTORE I-UNITS REG PTRS  Y02669
*                                 R9 HAS I-UNIT'S UNITNAME PTR   Y02669
         LR    R5,R6              R5 HAS I-UNIT'S UCME PTR       Y02669
         LR    R12,R11            R12 HAS I-UNIT'S UCB PTR       Y02669
         NI    XADUSWIT,FOX-XAIPAREN TURN OFF O-UNIT FLAG      @Z30LPTJ
         OI    XADUSWIT,XAIOCOMP  TURN ON I-UNIT FLAG          @Z30LPTJ
OCONT2   EQU   *                                                 Y02669
         SPACE 10
***************************************************              Y02651
*                                                                Y02651
*  THIS ONE DEVICE OF COMP DEVICES HAS BEEN PROCESSED            Y02651
*  RELEASE LOCKS BEFORE GOING TO THE NEXT DEVICE BECAUSE         Y02651
*  THERE MAY NOT BE A DEVICE TO PROCESS ANYWAY                   Y02651
*                                                                Y02651
         BAL   R8,RELEASE                                        Y02651
*                                                                Y02651
***************************************************              Y02651
         SPACE 10
         B     OCONT              CONTINUE DO NO SEND MSG        Y02669
*                                                                Y02669
TESTCOMP EQU   *                                                 Y02669
         OI    XADUSWIT,XAUBLANK  SET TO SEND MSG              @Z30LPTJ
         TM    XADUSWIT,XAIOCOMP  I-UNIT OF COMP PROCESSED     @Z30LPTJ
         BZ    TESTO              NO CHECK IF O-UNIT OF COMP     Y02669
GETOUNT  EQU   *                                                 Y02669
         LR    R8,R9              SAVE I-UNIT'S UNITNAME PTR     Y02669
         LR    R6,R5              SAVE I-UNIT'S UCME PTR         Y02669
         LR    R11,R12            SAVE I-UNIT UCB PTR            Y02669
         L     R5,UCMCOMPC        GET O-UNIT'S UCME              Y02669
         L     R12,UCMUCB         GET O-UNIT'S UCB               Y02669
         LA    R9,SIX(R9)         GET O-UNIT'S UNITNAME PTR      Y02669
         NI    XADUSWIT,FOX-XAIOCOMP TURN OFF I-UNIT FLAG      @Z30LPTJ
         OI    XADUSWIT,XAIPAREN  SET O-UNIT FLAG              @Z30LPTJ
         B     UOFFTEST           GO SET BITS IN THE O-UNIT      Y02669
TESTO    EQU   *                                                 Y02669
         TM    XADUSWIT,XAIPAREN  WAS IT THE O-UNIT OF COMP    @Z30LPTJ
         BZ    OFFPTR             NO REG UNIT                    Y02669
         LR    R9,R8              YES, RESTORE I-UNIT'S REGS     Y02669
         LR    R5,R6                                             Y02669
         LR    R12,R11                                           Y02669
         NI    XADUSWIT,FOX-XAIPAREN TURN OFF O-UNIT FLAG      @Z30LPTJ
         OI    XADUSWIT,XAIOCOMP  TURN ON I-UNIT'S FLAG        @Z30LPTJ
         B     OFFPTR                                            Y02669
*                                                                Y02669
         EJECT
***********************************************************************
*                                                                 19084
*        MESSAGE ROUTINE                                          19084
*                                                                 19084
***********************************************************************
*                                                                 19084
OMSGS    EQU   *                                                  19084
         TM    XAAVRANG,XAARNG1    VARY RANGE BEING PROCESSED? @Z30LPTJ
         BO    OCONT               YES, SUPPRESS THE MESSAGE   @Z30LPTJ
         ST    R13,XAK             SAVE SAVEAREA POINTER       @Z30LPTJ
         L     R11,XAD             STORE SWITCHES                 19084
         LM    R12,R13,XAS         STORE BITS AND SWITCHES        19084
         LM    R6,R7,XAR           STORE VERB CODE AND LIST POS  Y02669
         SR    R0,R0                                              19084
         L     R8,XAU              CONSOLE ID MUST BE SAVED    @Z30LPTJ
*                                  ACROSS MULTIPLE WTO'S
         TM    XAU,RIID            READER INTERPRETER COMMAND     19084
         BO    ORDR                SEND TO MASTER CONSOLE         19084
         IC    R0,XAU              NO, PICK UP CONSOLE ID         19084
ORDR     EQU   *                                                  19084
         MVC   XAH(FOR),OINTRO2    DECLARED MCSFLGS AND LENGTH    19084
         MVC   XAI(EIT),OBASIC     MESSAGE ID                     19084
         MVC   XAF(EIT),XAI+SVN    BLANK OUT REST                 19084
         MVC   XAI+TRE(TRE),ZERO(R15)   MOVE IN ACCURATE MSG ID   19084
         LA    R15,TRE(R15)          INDEX TO TEXT                19084
         MVC   XAT(NINETEEN),ZERO(R15) MOVE IN MESSAGE            21051
         MVC   XAT+NINETEEN(FOR),OEND  ROUT AND DESC CODES        21051
         MVC   XAF(TRE),ZERO(R9)   MOVE IN UNITS                  19084
*                                                                 19084
         LR    R1,R2                                              19084
*                                                                 19084
         WTO   MF=(E,(1))          ISSUE MESSGE                   19084
*                                                                 19084
*        RESTORE ENVIRONMENT                                      19084
*                                                                 19084
         ST    R11,XAD             RESTORE ALL SWITCHES AND INFO  19084
         STM   R12,R13,XAS         **                             19084
         L     R13,XAK             RESTORE SAVE AREA POINTER   @Z30LPTJ
         STM   R6,R7,XAR             **                          Y02669
         ST    R8,XAU              RESTORE CONSOLE ID IN XSA   @Z30LPTJ
         MVC   XAV(EIT),OVARY                                    Y02669
         MVC   XAX(EIT),MODID                                    Y02669
*                                                                 19084
OCONT    EQU   *                                                  19084
         TM    XADUSWIT,XAIPAREN  O-XXX OF A COMPOSITE         @Z30LPTJ
         BO    NOMONTR            YES, MNTOR CMD CHECKED ALREADY Y02669
         GETMAIN R,LV=EIGHTY8     GETMAIN CORE FOR PARAM LIST    Y02669
         ST    R13,TWELV(R1)      STORE VARY'S SAVE AREA PTR     Y02669
         LA    R14,FOR(R1)        ADDRESS OF PARAM LIST          Y02669
         ST    R14,ZERO(R1)                                      Y02669
         USING MPL,R14                                           Y02669
         XC    FOR(EIT,R1),FOR(R1)  ZERO PARAM AREA              Y02669
         OI    MPLPROC1,MPLCONS+MPLSTOP                          Y02669
*                                 SET THE VARY CONSOLE AND STOP  Y02669
*                                 ALL MONITOR CMDS - OPTION BITS Y02669
         OI    MPLTYPE1,MPLALL                                   Y02669
*                                 SET THE MONITOR JOBNAMES,MNTOR Y02669
*                                 STATUS AND MNITOR SESSIONS BIT Y02669
*                                 ON SO THEY WILL STOP ALL THREE Y02669
*                                 FROM GOING TO THIS CONSOLE     Y02669
*                                                                Y02669
*  REG 5 STILL PTS TO THE VARIED DEVICE'S UCME                   Y02669
*                                                                Y02669
         SR    R13,R13                                           Y02669
         IC    R13,UCMID          GET THE CONSOLE ID OF THE DEV  Y02669
         STC   R13,MPLID+ONE      PLACE IT IN THE PARAMLIST      Y02669
*                                 RIGHT JUSTIFIED                Y02669
         LA    R13,SIXTN(R1)       SAVE AREA FOR VMNTR PTR       Y02669
*                                                                Y02669
* REG 10 STILL HAS THE ADDRESS OF THE UCM                        Y02669
*                                                                Y02669
         L     R15,UCMMNTR        GET ADDRESS OF THE ROUTINE     Y02669
*                                 IEAVMNTR- FROM THE UCM         Y02669
         BALR  R14,R15            GO TO IEAVMNTR                 Y02669
*                                                                Y02669
         L     R13,TWELV(R1)      RE-ADDRESS R14 WITH CALLER'S   Y02669
         LA    R0,EIGHTY8         SIZE OF THE PARAM LIST AND SA  Y02669
         FREEMAIN R,LV=(0),A=(1)  FREE THE CORE                  Y02669
NOMONTR  EQU   *                                                 Y02669
         TM    XADUSWIT,XAIOCOMP   PROCESSING COMP I-XXX?      @Z30LPTJ
         BZ    ONOCOMP             NO                             19084
         LA    R9,SIX(R9)          INDEX TO O-XXX                 19084
         NI    XADUSWIT,FOX-XAIOCOMP TURN OFF COMP SWITCH      @Z30LPTJ
         OI    XADUSWIT,XAIPAREN   BUT TURN ON INTERNPAREN SW  @Z30LPTJ
         L     R5,UCMCOMPC         GET PTR TO O-UNITS UCME        19084
         L     R12,UCMUCB          GET PTR TO OUTPUT UCB          19084
         TM    XADUSWIT,XAUBLANK   MUST ISSUE THE MSG          @Z30LPTJ
         BZ    ONOCOMP            NO RETURN
         LA    R15,OFFLMSG        YES, ASSUME OFFLINE MSG
         TM    XASOPCOD,XAON        WAS IT THE ONLINE MSG
         BZ    OMSGS              NO, OFFLINE MSG
         LA    R15,ONLMSG         ONLINE MSG
         B     OMSGS              GO ISSUE MSG FOR THE O-UNIT
*                                                                 19084
ONOCOMP  EQU   *                                                  19084
         TM    XADUSWIT,XAIPAREN   PROCESSING COMP O-UNIT?     @Z30LPTJ
         BZ    OENDCK              NO, OF                         19084
         LA    R9,ONE(R9)          YES,ADD ONE FOR INTERN PAREN   19084
         B     OENDCK              GO CHECK IF END                19084
*                                                                 19084
         SPACE 10                                                 19084
ONOMORE  EQU   *                                                  19084
         TM    XADUSWIT,XAUDVC     DO WE HAVE IOUNITS YET      @Z30LPTJ
         BZ    RETEXT              NO, RETURN                    Y02669
IOLOOP   EQU   *                                                  19084
         CLI   ZERO(R9),ONOFKEY    IS AN O-XXX FOR ONLINE/OFFLINE 19084
         BE    O3103D              YES, GO CHECK FURTHER          19084
OINDEX   LA    R9,ONE(R9)          INDEX TO NEXT CHARACTER        19084
         B     IOLOOP              GO CHECK FOR -O-               19084
*                                                                 19084
O3103D   EQU   *                                                  19084
         CLI   ONE(R9),NLINE       MAKE SURE IF KEYWORD, IS IT AN -N-
         BE    OEXT                YES, GO TO 3103D               19084
         CLI   ONE(R9),FFLINE      NO, IS IT AN -F- FOR FFLINE    19084
         BNE OINDEX                NO, KEYWORD NO FOUND YET       19084
*                                                                 19084
OEXT     EQU   *                                                  19084
         BCTR  R9,ZERO             PT TO COMMA BEFORE KEYWORD     19084
         ST    R9,XAR              3103D NEEDS THIS POINTER       19084
         MVC   XAX+TRE(TWO),IOMOD      3103D NAME                Y02669
         LM    R0,R15,ZERO(R13)    RESTORE REGS                  Y02669
         L     R15,VCON31         ADDRESS OF IEE3103D            Y02669
         BR    R15                GO TO 3103D                    Y02669
*                                                                 19084
*                                                                 19084
*                                                                 19084
         SPACE 5
RETEXT   EQU   *                                                 Y02669
         TM    XAAVRANG,XAARNG1    VARY RANGE BEING PROCESSED? @Z30LPTJ
         BNO   PROCEED             NO, PROCEED NORMALLY        @Z30LPTJ
         L     R15,VCON904         GET IEECB904 ADDRESS        @Z30LPTJ
         BR    R15                 GO TO VARY RANGE PROCESSOR  @Z30LPTJ
PROCEED  EQU   *                                               @Z30LPTJ
         SPACE 5
***************************************************              Y02651
*                                                                Y02651
*  THIS IS A TERMINAL EXIT.  THE ENQ ISSUED IN MODULE            Y02651
*  IEE3603D                                                      Y02651
*  WE MUST BE DEQUEUED HERE                                      Y02651
*                                                                Y02651
         DEQ   MF=(E,IEFDEQUE)                                   Y02651
*                                                                Y02651
***************************************************              Y02651
         SPACE 5
         L     R1,D64(R13)         GET CSCB PTR                  Y02669
         USING CHAIN1,R1                                         Y02669
         OI    CHSTS,CHFC          SET TO FREE CSCB              Y02669
         MGCR  (1),CHAIN                                         Y02669
         LM    R0,R15,ZERO(R13)    RESTORE REGS                  Y02669
         LR    R4,R14              SAVE RETURN ADDRESS           Y02669
*                                  FREE SAVE AREA GOTTEN IN 3303DY02669
         LR    R1,R2               DUMMY XSA POINTER             Y02669
         LA    R0,D132             DUMMY XSA SIZE                Y02669
         FREEMAIN R,LV=(0),A=(1)                                 Y02669
*                                  FREE DUMMY XSA GOTTEN IN 3603DY02669
         LR    R14,R4              RESTORE RETURN REG            Y02669
         BR    R14                 RETURN                        Y02669
         EJECT
***********************************************************************
*                                                                 19084
*       DECLARES                                                  19084
*                                                                 19084
***********************************************************************
         DS    0F                                                Y01022
OOUNIT   DC    C'O-'               O-XXX                          19084
OIUNIT   DC    C'(I-'              COMPOSITE                      19084
*                                                                 19084
OINTRO2  DC    X'0027A100'             LENGTH AND MCSFLAGS        21051
OBASIC   DC    C'IEE   I '         MSG ID                         19084
OEND     DC    X'08000000'         DESC AND ROUT CODES            19084
*                                                                 19084
         SPACE 10                                                 19084
*                                                                 19084
IOMOD    DC    C'31'         IO VARY MODULE                      Y02669
OVARY    DC    C'VARY    '         VARY COMMAND                   19084
         DS    0F                                                Y02669
VCON31   DC    V(IEE3103D)        VCON OF IEE3103D               Y02669
VCON904  DC    V(IEECB904)        VCON OF IEECB904             @Z30LPTJ
MODID    DC    C'IEE4603D'                                       Y02669
*                                                                 19084
         EJECT
*****************************************************************Y02651
*                                                                Y02651
*  THE LIST FORM OF THE DEQ MACRO INSTRUCTION FOLLOWS            Y02651
*                                                                Y02651
IEFDEQUE DEQ   (IEFSQCBS,IEFQ4QCB,TWO,SYSTEM,IEFSQCBS,IEFVQCBS,SVN,    *
               SYSTEM),RET=HAVE,MF=L                           @YM5443P
IEFVQCBS DC    C'VARYDEV'
IEFSQCBS DC    C'SYSIEFSD'                                       Y02651
IEFQ4QCB DC    C'Q4'                                             Y02651
*                                                                Y02651
*****************************************************************Y02651
         EJECT
*****************************************************************Y02651
*                                                                Y02651
*  THE RELEASE-LOCKS SUBROUTINE FOLLOWS                          Y02651
*  THESE LOCKS WERE OBTAINED IN THIS MODULE AT:                  Y02651
*        OSETLOCK                                                Y02651
*                                                                Y02651
         DS    0H
RELEASE  EQU   *                                                 Y02651
         LR    R0,R8                                             Y02651
*                                 SAVE RETURN ADDRESS            Y02651
         LR    R8,R13             SAVE AREA POINTER              Y02651
         STM   R11,R14,D68(R8)                                   Y02651
         SETLOCK RELEASE,TYPE=CMS,RELATED=(IEE4603D(OSETLOCK))   Y02651
         SETLOCK RELEASE,TYPE=LOCAL,RELATED=(IEE4603D(OSETLOCK)) Y02651
         LM    R11,R14,D68(R8)                                   Y02651
         LR    R8,R0              RESTORE RETURN ADRS            Y02651
         BR    R8                                                Y02651
*                                                                Y02651
*****************************************************************Y02651
         EJECT
***********************************************************************
*                                                                 19084
*        MESSAGES                                                 19084
*                                                                 19084
***********************************************************************
*                                                                 19084
ONLMSG   DC    C'302'              ID OF ONLINE MSG               19084
         DC    C'ONLINE             '  TEXT                       21051
*                                                                 19084
*                                                                 19084
*                                                                 19084
OFFLMSG  DC    C'303'              ID OF OFFLINE MSG              19084
         DC    C'OFFLINE            '  TEXT                       21051
*                                                                 19084
         SPACE 5
PATCH    DC    CL80'***IEE4603D***'
         EJECT
OXSA     DSECT
         IEEXSA
         EJECT
         IEZMPL
         EJECT
OCVT     DSECT
         CVT
         EJECT
OUCM     DSECT
         IEECUCM  FORMAT=NEW
         EJECT
OUCB     DSECT
         IEFUCBOB
         EJECT
CHAIN1   DSECT
         IEECHAIN
         EJECT
         IEEBASEA
         EJECT
         IHAPSA
         END
