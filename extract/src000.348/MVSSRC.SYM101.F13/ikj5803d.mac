IKJ5803D CSECT
*/*IKJ5803D:  CHART */
*/*           E  IKJ5803D */
***********************************************************************
*                                                                     *
* MODULE-NAME = IKJ5803D                                              *
*                                                                     *
* DESCRIPTIVE-NAME = OPERAND LENGTH CHECK ROUTINE                     *
*                                                                     *
* COPYRIGHT = N/A                                                     *
*                                                                     *
* STATUS = REL. 02.0                                                  *
*                                                                     *
* FUNCTION = THIS MODULE PERFORMS LENGTH CHECKING ON THE              *
*            SEND OPERAND.                                            *
*                                                                     *
*   OPERATION = DETERMINES IF THE COMMAND IMAGE LENGTH IS             *
*               GREATER THAN 124 CHARACTERS.  IF IT IS LESS           *
*               THAN OR EQUAL TO 124 CHARACTERS THE MODULE            *
*               BRANCHES TO IEE0803D (CSCB CREATION MODULE).          *
*                                                                     *
*               IF THE COMMAND IMAGE IS  GREATER THAN 124             *
*               CHARACTERS, THE OPERAND FIELD IS SCANNED FOR          *
*               A BLANK INDICATING THE END OF THE PARAMETER           *
*               FIELD.  IF THE BLANK IS IN A POSITION LESS            *
*               THAN OR EQUAL TO 124 CHARACTERS, THEN THE             *
*               CHARACTERS TO THE RIGHT OF THE BLANK ARE              *
*               CONSIDERED AS COMMENTS.  IF IN FACT THE               *
*               PARAMETER FIELD OF THE COMMAND IS FOUND TO            *
*               BE GREATER THAN 124 CHARACTERS; THAT IS, A            *
*               BLANK (DELIMITER) WAS NOT FOUND, THE MODULE           *
*               BRANCHES TO IEE0503D TO ISSUE AN ERROR                *
*               MESSAGE INDICATING THE COMMAND LENGTH                 *
*               EXCEEDED THE MAXIMUM ALLOWABLE.                       *
*                                                                     *
* NOTES = N/A                                                         *
*                                                                     *
*   DEPENDENCIES = N/A                                                *
*                                                                     *
*     CHARACTER-CODE-DEPENDENCIES = NONE                              *
*                                                                     *
*   RESTRICTIONS = NONE                                               *
*                                                                     *
*   REGISTER-CONVENTIONS = SEE "REGISTER EQUATES"                     *
*                                                                     *
*   PATCH-LABEL = PTCHAREA (50 BYTES)                                 *
*                                                                     *
* MODULE-TYPE = PROCEDURE                                             *
*                                                                     *
*   PROCESSOR = ASSEMBLER                                             *
*                                                                     *
*   MODULE-SIZE - APPROXIMATELY 300 DECIMAL BYTES                     *
*                                                                     *
*   ATTRIBUTES = REUSABLE                                             *
*                                                                     *
* ENTRY-POINT = IKJ5803D                                              *
*                                                                     *
*   PURPOSE = THIS MODULE IS ENTERED TO LENGTH CHECK THE              *
*             OPERAND ON A SEND COMMAND.                              *
*                                                                     *
*   LINKAGE = BRANCH FROM THE ROUTING ROUTINE IN SVC 34               *
*                                                                     *
*   INPUT = NONE                                                      *
*                                                                     *
*   REGISTERS-SAVED = NONE                                            *
*                                                                     *
*   REGISTER-USAGE = SEE "REGISTER EQUATES" BELOW                     *
*                                                                     *
*   REGISTERS-RESTORED = NONE                                         *
*                                                                     *
* EXIT-NORMAL = IEE0803D (CSCB CREATION MODULE)                       *
*                                                                     *
*   CONDITION = MODULE IKJ5803D BRANCHES TO IEE0803D AFTER            *
*               DETERMINING THAT THE OPERAND LENGTH IS WITHIN         *
*               THE VALID RANGE.                                      *
*                                                                     *
*   OUTPUT = REGISTER 2 POINTS TO THE EXTENDED SAVE AREA              *
*            WHICH HAS NOT BEEN ALTERED FORM INPUT TIME.              *
*                                                                     *
*   RETURN-CODE = N/A                                                 *
*                                                                     *
* EXIT-ERROR = IEE0503D (MESSAGE MODULE)                              *
*                                                                     *
*   CONDITION = MODULE IKJ5803D BRANCHES TO IEE0503D AFTER            *
*               DETERMINING THAT THE OPERAND LENGTH IS NOT            *
*               WITHIN THE VALID RANGE.                               *
*                                                                     *
*   OUTPUT = REGISTER 2 POINTS TO THE EXTENDED SAVE AREA              *
*            CONTAINING:                                              *
*             .  THE XAE FIELD OF THE XSA CONTAINS THE MSG            *
*                INDEX, AND THE OTHER XSA FIELDS WILL REMAIN          *
*                AS THEY WERE AT INPUT.                               *
*                                                                     *
*   RETURN-CODE = N/A                                                 *
*                                                                     *
* EXIT-ERROR = CALLER                                                 *
*                                                                     *
*   CONDITION = FOR ERRORS OTHER THAN FOR LENGTH CHECKING,            *
*               THIS MODULE WILL RETURN TO ITS CALLER.                *
*                                                                     *
*   OUTPUT = NONE                                                     *
*                                                                     *
*   RETURN-CODE = N/A                                                 *
*                                                                     *
* EXTERNAL-REFERENCES = NONE                                          *
*                                                                     *
*   ROUTINES = NONE                                                   *
*                                                                     *
*   DATA-AREAS = XSA                                                  *
*                                                                     *
*   CONTROL-BLOCKS = NONE                                             *
*                                                                     *
* TABLES = MESSAGE AREA TABLE                                         *
*                                                                     *
* MACROS = NONE                                                       *
*                                                                     *
*   SERIALIZATION = NONE                                              *
*                                                                     *
* CHANGE-ACTIVITY = Y02676                                            *
*                                                                     *
* MESSAGES = MESSAGE ''IEE908I 'COMMAND NAME' CMD LENGTH              *
*            EXCEED MAX'' WILL BE ISSUED BY IEE0503D WHEN             *
*            THE OPERAND EXCEEDS THE SPECIFIED LENGTH.                *
*                                                                     *
* ABEND-CODES = NONE                                                  *
*                                                                     *
***********************************************************************
        TITLE 'IKJ5803D - DETERMINE LENGTH OF SEND OPERAND BUFFER'
*
***********************************************************************
*              REGISTER EQUATES                                       *
***********************************************************************
REG0     EQU   0                       UNUSED
REG1     EQU   1                       PTR TO PARM LIST
REG2     EQU   2                       BASE REGISTER FOR IEEXSA
REG3     EQU   3                       CONTAINS MAX LENGTH OF OPERAND
*                                      FIELD
RND      EQU   4                       LENGTH OF COMMAND OPERAND FIELD
QCTR     EQU   5                       QUOTE COUNTER
REG6     EQU   6                       PTR TO OPERAND FIELD
REG7     EQU   7                       UNUSED
REG8     EQU   8                       INCREMENT REGISTER
REG9     EQU   9                       COMPARAND FOR BXH
REGA     EQU   10                      BASE REGISTER FOR CSECT
REGB     EQU   11                      UNUSED
REGC     EQU   12                      UNUSED
REGD     EQU   13                      POINTER TO BRANCH NAME    Y02676
REGF     EQU   15                      POINTER TO BRANCH ADDRESS Y02676
N0       EQU   0
ONE      EQU   1                       INCREMENT FOR BXH
N1       EQU   1                       DISPLACEMENT TO LENGTH OF BUFFER
N4       EQU   4
N8       EQU   8
N16      EQU   16                      DISPLACEMENT TO CVT
N57      EQU   57                      MSG CODE INDEX FOR 0503D
N96      EQU   96                      DISPLACEMENT TO XSA FROM THE RB
MAX      EQU   124                     MAX LENGTH OF OPERAND BUFFER
BLANK    EQU   X'40'
*/*           P  SET ADDRESSIBILITY FOR CSECT */
         BALR  REGA,N0                 SET UP ADDRESSIBILITY FOR CSECT
         USING *,REGA
         MODID  BRANCH=YES                                      Y01886
*/*         P     SET ADDRESSIBILITY FOR EXTENDED SAVE AREA */
         USING MXSA,REG2               SET ADDRESSIBILITY FOR IEEXSA
         LA    REG3,MAX                GET MAX LENGTH OF OP FIELD M1586
         L     REG1,XAR                GET PTR TO PARM LIST       M1586
         LH    RND,N0(REG1)            GET LENGTH OF BUFFER       M1586
         AR    RND,REG1                COMPUTE END OF BUFFER ADDR M1586
         L     REG6,XAL                GET PTR TO OPERAND FIELD   M1586
         LA    REG6,N0(REG6)           CLEAR HI ORDER BYTE        M1586
         LTR   REG6,REG6               TEST FOR NO OPERAND       YM4710
         BZ    B803D                   NO OPERAND PRESENT        YM4710
BKSCAN   EQU   *                       START OF BACK SCAN LOOP   YM4710
         BCTR  RND,N0                  DECREMENT THE BUFFER END  YM4710
         CLI   N0(RND),BLANK           COMPARE LAST CHAR TO BLANKYM4710
         BNE   BUFEND                  NONBLANK--END OF OPERAND  YM4710
         CR    REG6,RND                END = START OF OPERAND    YM4710
         BC    8,B803D                 BLANK OPERAND--VALID OPER YM4710
         B     BKSCAN                  END CHAR IS BLANK--BACKUP YM4710
BUFEND   EQU   *                       END OF OPERAND FOUND      YM4710
         LA    RND,ONE(RND)            FIRST BLANK AFTER OPERAND YM4710
         SR    RND,REG6                COMPUTE LENGTH OF OPERAND FIELD
         CR    RND,REG3                IS LENGTH < OR = TO 124?   M1586
         BNH   B803D                   GO TO BRANCH TO IEE803D   Y02676
         SR    QCTR,QCTR               INITIALIZE QUOTE COUNTER TO ZERO
*                                                                 M1586
*/*           P  SCAN BUFFER FOR A BLANK (DELIMITER) */
         LA    REG8,ONE                SET INCREMENT              M1586
         LR    REG9,REG6               LOAD PTR TO BEG OF OPERAND FIELD
*                                                                 M1586
         LA    REG9,MAX-ONE(REG9)      CALCULATE MAX BUFFER ADDR  M1586
TEST1    EQU   *                                                  M1586
*/*TEST1:     D  (YES,,NO,CKBLANK) IS THIS A QUOTE? */
         CLI   N0(REG6),C''''          IS THIS CHAR A QUOTE?      M1586
         BNE   CKBLANK                 NO,CHECK FOR A BLANK       M1586
*/*           P  (,INCRE) FLIP SWITCH */
         X     QCTR,SW                 YES, FLIP SWITCH           M1586
         B     INCREMNT                BRANCH TO CHECK NEXT CHAR  M1586
*/*CKBLANK:   D  (YES,,NO,INCRE) IS THIS A BLANK? */
CKBLANK  EQU   *                                                  M1586
         CLI   N0(REG6),BLANK          IS THIS CHAR A BLANK?      M1586
         BNE   INCREMNT                NO, CHECK NEXT CHAR        M1586
*/*           D  (YES,B803D,NO,) EVEN NO. OF QUOTES? */          Y02676
         LTR   QCTR,QCTR               IS QUOTE COUNT EVEN?       M1586
         BZ    B803D                   GO TO BRANCH TO 803D      Y02676
*/*INCRE:     P  CHECK NEXT CHARACTER */
INCREMNT EQU   *                                                  M1586
*/*           D  (NO,TEST1,YES,) END OF BUFFER? */
         BXLE  REG6,REG8,TEST1         CHECK FOR END OF BUFFER    M1586
         CLI   N0(REG6),BLANK          IS CHAR 125 A BLANK?       M1586
         BNE   B0503D                  BRANCH TO SET UP ERR MSG  Y02676
*/*           D  (YES,B803D,NO,) EVEN NO. QUOTES? */             Y02676
         LTR   QCTR,QCTR               IS QUOTE COUNTER EVEN?     M1586
         BZ    B803D                   YES, GO TO B803D          Y02676
*/*B0503D:    P  (,BRCONT) SET UP FOR ERROR MSG */               Y02676
B0503D   EQU   *                                                 Y02676
         MVI   XAE,N57                 SET MSG INDEX FOR 0503D    M1586
         L     REGF,BCH0503D           LOAD REG 15 FOR BRANCH    Y02676
         B     BRCONT                  BRANCH TO SET UP XSA       M1586
*/*B803D:     P  SET UP FOR BRANCH TO IEE0803D */                Y02676
B803D    EQU   *                                                 Y02676
         L     REGF,BRCH803D           LOAD REG 15 FOR BRANCH    Y02676
*/*BRCONT: R GO TO IEE0803D OR IEE0503D TO CONTINUE */           Y02676
BRCONT   EQU   *                                                 Y02676
         BCR   15,REGF                 BR TO CONTINUE PROCESSING Y02676
BRCH803D DC    V(IEE0803D)             BRANCH ADDRESS            Y02676
BCH0503D DC    V(IEE0503D)             BR ADDRESS FOR ERROR PATH Y02676
ZERO     DC    F'0'                                               M1586
SW       DC    F'1'                                               M1586
MXSA     DSECT
         IEEXSA
PTCHAREA DC  CL50'***************PATCH AREA FOR IKJ5803D************'
*                                                                Y02676
*/*IKJ5803D:  END END OF IKJ5803D */
         END
