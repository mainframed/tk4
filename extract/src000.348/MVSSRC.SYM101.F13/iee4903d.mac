 TITLE 'IEE4903D - CONSOLE OPERAND PROCESSOR'
IEE4903D CSECT
* A 481600-482200                                              @ZA02592
* C 080100,220500                                              @ZA02592
* A 057700-058000,066800-067200,220900,322800,332100,361300,   @ZM34520
* A 407800-408100,421500-421800,469850-477240,738090-738810,   @ZM34520
* A 780400-780700,845150-846500,850000,855400                  @ZM34520
* C 066600,080100                                              @ZM34520
* D 449100,658800,659700,781200-791100,792900-800100           @ZM34520
* D (IEFDEQUE) 811800 - 818100                                 @ZA12404
* C (CDEQUE  ) APPROX. 845400                                  @ZA12404
* A (IEE36DEQ) APPROX. 890100                                  @ZA12404
* C (RETURN  )                                                 @ZA13369
***********************************************************************
* MODULE NAME       = IEE4903D
*    CSECT NAME     = IEE4903D
*
* LOAD MODULE NAME  = IEE3603D
*
* DESCRIPTIVE NAME  = CONSOLE OPERAND PROCESSOR
*
* COPYRIGHT         = NONE
*
* STATUS            = OS/VS/2 MVS 3033 PROCESSOR SUPPORT(SCP)  @G51APSS
*
* FUNCTION          = PROCESSES VARY XXX,CONSOLE,ETC.
*                        SETS ALL REQUIRED INTERFACE AND STATUS BITS IN
*                        THE UCM, UCME, AND UCB
*                     PROCESSES PARAMETERS AND CHECKS FOR THEIR
*                     VALIDITY AND ALLOWABILITY
*                     PARAMETERS:
*                        AUTH=
*                        ROUT=
*                        ALTCONS=
*
* OPERATION         = THIS MODULE FIRST CHECKS TO SEE IF A     @G51APSS
*                     NO-CONSOLES CONDITION EXISTS. IF SO THEN @G51APSS
*                     MESSAGE IEE744I IS ISSUED, A DEQ IS      @G51APSS
*                     ISSUED FOR Q4 AND VARYDEV, THE GETMAINED @G51APSS
*                     WORK AREA IS FREED, THE CSCB IS FREED,   @G51APSS
*                     THE DUMMY XSA IS FREED AND CONTROL IS    @G51APSS
*                     RETURNED TO THE CALLER. IF A NO-CONSOLES @G51APSS
*                     CONDITION DOES NOT EXIST THEN A CHECK IS @G51APSS
*                     MADE TO SEE IF THE DEVICE TO BE          @G51APSS
*                     VARIED TO THE CONSOLE STATE IS ALREADY A CONSOLE.
*                     IF IT IS NOT, THEN HARDCPY REQUIREMENTS ARE
*                     CHECKED SINCE THIS WOULD BE A SECOND OR MORE
*                     CONSOLE ON THE SYSTEM NOW AND THEREFORE HARDCPY
*                     IS NOW DEFINITELY REQUIRED.
*                     IF HARDCPY ALREADY EXISTS THEN THERE IS NO
*                     PROBLEM.  THE ONLY THING DONE NOW IS TO CHANGE
*                     IF REQUIRED, THE AUTHORITY AND ROUTCODES OF THE
*                     HARDCPY DEVICE TO THE MANDATORY ONES.
*                     HOWEVER IF HARDCPY DOES NOT EXIST, THEN
*                     WE CHECK WHETHER SYSLOG IS ACTIVE AND IF SO
*                     WE MAKE IT HARDCPY AND ISSUE A MSG LETTING THE
*                     OPERATOR KNOW THIS.
*                     IF SYSLOG IS NOT ACTIVE, THEN WE TRY TO MAKE
*                        1. THE MASTER CONSOLE    OR
*                        2. THE NEW CONSOLE COMING ON
*                     THE HARDCPY DEVICE. IF ONE OR THE OTHER ARE NOT
*                     GRAPHIC DEVICES.  IF IT TURNS OUT THAT BOTH ARE
*                     GRAPHICS, THEN WE ALLOW THIS UNIT TO BECOME A
*                     CONSOLE BUT WE NOTIFY THE OPERATOR THAT HE WILL
*                     BE WORKING WITHOUT A HARDCPY DEVICE WHEN IT IS
*                     REQUIRED TO HAVE ONE.
*                     THEN THIS MODULE SETS THE OPEN PENDING FLAG IN
*                     THE UCME OF THE NEW DEVICE AND CHECKS IF IT IS
*                     CURRENTLY ONLINE OR OFFLINE.
*                     IF OFFLINE, THEN THE ONLINE AND CONSOLE BITS
*                     ARE SET IN THE UCB OF THE DEVICE AND WE GO AND
*                     SERVICE ANY FURTHER REQUEST THAT THE OPERATOR
*                     MIGHT HAVE HAD SUCH AS NEW AUTHORITY, ROUTCODES
*                     ETC., FOR THIS NEW CONSOLE.
*                     IF THE DEVICE IS CURRENTLY ONLINE, WE CHECK IF
*                     THE DEVICE IS CURRENTLY ALLOCATED.
*                     IF YES, WE TURN ON THE CHANGE STATUS BIT IN THE
*                     UCB AND GO PROCESS FURTHER REQUEST FOR CHANGES
*                     FOR THE UNIT.
*                     IF IT IS NOT ALLOCATED,THEN THE CONSL BIT IS SET
*                     ON AND WE CONTINUE PROCESSING FURTHER CHANGES AS
*                     REQUESTED BY THE OPERATOR.
*                     IF IT IS NOT ACTIVE AND IS NOT READY, AN @ZM34520
*                     ERROR MESSAGE (IEE313I) WILL BE ISSUED   @ZM34520
*                     AND ITS CORRESPONDING PROCESS BIT TURNED @ZM34520
*                     OFF.                                     @ZM34520
*                     IN ALL CASES, WHEN THE DEVICE IS VARIED TO THE
*                     CONSOLE STATE,FROM AN OFFLINE STATE, THIS RTN
*                     WILL TURN THE MP-OPERATOR BIT IN THE UCB OFF.
*                     THIS IS DONE SO THAT THE SYSTEM WILL KNOW
*                     THAT THE UNIT IS IN THE ONLINE/CONSOLE STATE
*                     DUE TO A 'VARY DEVICE' COMMAND.
*                     WHEN ALL UNITS ON THE COMMAND HAVE BEEN PRO-
*                     CESSED AS DESCRIBED ABOVE, THEN WE ISSUE A
*                     HEADER MESSAGE AND EXIT TO IEE4803D TO COMPLETE
*                     THE MESSAGE FOR THIS COMMAND.  IF THERE  @ZM34520
*                     ARE NO PROCESS BITS REMAINING, A RETURN  @ZM34520
*                     TO CALLER WILL BE MADE, INSTEAD OF GOING @ZM34520
*                     TO IEE4803D.                             @ZM34520
*
* NOTES
*    DEPENDENCIES   = NONE
*    CHARACTER/CODE DEPENDENCIES
*                  = THIS MODULE CAN BE ASSEMBLED WITH ANY NEW
*                    CHARACTER/CODE SET.
*
* RESTRICTIONS     = SEE ENTRY POINTS - INPUT DATA
*                    XSA MUST NOT BE FREED BEFORE Q4 AND       @ZA12404
*                    VARYDEV ARE DEQUEUED AS FIELD XAWORKB     @ZA12404
*                    CONTAINS POINTER TO THE DYNAMIC DEQUE     @ZA12404
*                    LIST.                                     @ZA12404
*                                                              @ZA12404
*    REGISTER CONVENTIONS
*                  = REGISTERS ARE DEFINED AT LABEL: DEFREG
*    PATCH LABEL   = PATCH
*
* MODULE TYPE      = CSECT
*    PROCESSOR     = ASSEMBLER
*    MODULE SIZE   = X'83C' BYTES                              @G51APSS
*    ATTRIBUTES    = REENTRANT, REUSABLE, REFRESHABLE, ZERO PSW
*                    PROTECT KEY,PAGED LPA
*
* ENTRY POINTS      = IEE4903D FROM IEE4203D
*   PURPOSE         = TO PROCESS CONSOLE OPERAND ON THE VARY COMMAND
*   LINKAGE         = BRANCH
*   INPUT           = REGISTER 2 POINTS TO THE DUMMY XSA
*                     XAR HAS THE PROCESS SWITCHES FOR THE UNITS
*                     XAL HAS THE PTR TO THE PARMS OF THE COMMSND
*                    ADDITIONAL XSA FIELDS ARE DEFINED AT      @ZA13369
*                    SECTION 'XSA POINTER SAVE AREA'.          @ZA13369
*                     REGISTER 4 CONTAINS A GRAPHICS INDICATOR
*                        SET BY IEE4203D
*                    REG13 PTS TO THE SAVE AREA GOTTEN IN 3603D
*                    WORD 17 OF THE SAVE AREA HAS THE CSCB PTR
*   REGISTERS SAVED = ALL
*   REGISTER USAGE  = R1  OUTPUT=WTO CORE PTR
*                     R2  XSA PTR
*                     R3  ADDRESSABILITY
*                     R4  UCME PTR
*                     R6  MLWTO AND GFX BYTE INDICATOR
*                     R7  PROCESS BITS
*                     R9  UNIT PTR
*                     R10 UCM REGISTER
*                     R11 UCMPRFX REGISTER
*                     R12 UCB PTR
*                     R13 SAVE AREA PTR
*   REGISTERS RESTORED
*                   = ALL
*
* EXITS - NORMAL    = TO IEE4803D VIA BRANCH
*                     IF ISSUER OF THE COMMAND HAS THE PROPER
*   CONDITIONS      = AUTHORITY, IF THE UNIT SPECIFIED HAS THE
*                     ABILITY TO BE VARIED AS A 'CONSOLE' AND
*                     IF THE PARAMETERS SPECIFIED MAY BE SET,
*                     IEE4903D WILL EXIT TO IEE4803D SO THAT
*                     MESSAGES CAN BE SENT INFORMING THE OPERATOR OF
*                     AUTHORITIES, AND ALTERNATE CONSOLES.
*   OUTPUT          = XAP CONTAINS THE MLWTO ID AND GRAPHICS ID
*                     R13 HAS THE SAVE AREA POINTER
*                     R2 HAS THE DUMMY XSA POINTER
*                     R1 CONTAINS PTR TO BUFFER FOR MSG
*                     A MLWTO IS ISSUED FROM THIS MODULE:
*                     IEE349I CONSOLES
*                     CONSOLE/ALT  COND  AUTH  ID  AREA  ROUTCD
*                        (IF THIS CONSOLE IS GRAPHICS)
*                        OR
*                     IEE349I CONSOLES
*                     CONSOLE/ALT  COND  AUTH  ID  ROUTCD
*                        ( IF CONSOLE IS NON-GRAPHICS)
*                     CONDITIONS SET IN THIS MODULE:
*                        OFFLINE TO CONSOLE...
*                             SETS UCB BITS:  ON/OFFLINE = ON
*                                             PRIM CONSL = ON
*                                             MP-OPERATOR = OFF
*                             SETS UCME BITS: OPEN PENDG = ON
*                        IF DEVICE IS UNDER TEST BY OLTEP THEN
*                        THE 'VARY CONSOLE' REQUEST CANNOT BE
*                        HONORED
*                        ONLINE ALLOCATED TO CONSOLE...
*                             SETS UCB BITS:  CONSOLE CHNG STATUS = ON
*                                             MP-OPERATOR = N/A
*                             SETS UCME BITS: OPEN PENDING        = ON
*                        ONLINE UNALLOCATED TO CONSOLE...
*                             WHEN ALLOC IS USING THE UCBS:
*                               SETS UCB BIT:  CONS CHANG STATUS = ON
*                                              MP-OPERATOR = N/A
*                               SETS UCME BIT: OPEN PENDING      = ON
*                             WHEN ALLOC IS NOT USING UCB:
*                               SETS UCB BIT:  PRIMARY CONSOLE = ON
*                                              MP-OPERATOR = N/A
*                               SETS UCME BIT: OPEN PENDING    = ON
*                     SETS OWN SWITCHES WITHIN XAD OF XSA
*                     ALSO PUTS OUT THE CONSOLE MESSAGE HEADER WHEN
*                     ALL THE UNITS IN THE COMMAND HAVE BEEN PROCESSED.
*                     IN ALL CASES WHERE THE COMMAND WAS HONORED:
*                             SETS UCB BIT: VARY-OPERATOR-MP  = OFF
*   RETURN CODES    = NONE
*
* EXITS - ERROR     = RETURN TO CALLER
*                     THIS MODULE WILL CLEAN UP ALL CORE BY FREEING
*                     THE SAVE AREA AND THE DUMMY XSA, FREEING THE
*                     CSCB CORE VIA AN MGCR MACRO
*   CONDITIONS      = A GETMAIN FAILED OR INVALID UNITS HAVE BEEN
*                     DETECTED AND DELETED, AND HAS RESULTED IN NO
*                     UNITS LEFT IN THE BUFFER OR THE VARY     @G51APSS
*                     CONSOLE COMMAND WAS ISSUED WHEN A        @G51APSS
*                     NO-CONSOLES CONDITION EXISTS.            @G51APSS
*
*   OUTPUT          = 'IEE328I VARY COMMAND ABORTED'
*
*                              -- OR --
*
*                     'IEE313I XXX      UNIT REF INVALID'
*
*                              -- OR --                        @G51APSS
*
*                     'IEE744I VARY CONSOLE REJECTED -         @G51APSS
*                              NO FULL CAPABILITY CONSOLES'    @G51APSS
*                     '     TO RESTORE MASTER CONSOLE:'        @G51APSS
*                     '  1) PRESS ENTER, REQUEST, OR END ON    @G51APSS
*                           ANY AVAILABLE CONSOLE'             @G51APSS
*                     '  2) PRESS THE EXTERNAL INTERRUPT KEY'  @G51APSS
*
*   RETURN CODES    = NONE
*
* EXTERNAL REFERENCES
*   ROUTINES        = IEE4803D
*                     IEE2103D
*   DATA AREAS      = SEE EXITS - OUTPUT DATA
*
* CONTROL BLOCKS    = 1. EXTENDED SAVE AREA (XSA)
*                     2. CVT
*                        FIELDS TESTED  = CVTCUCB
*                                         CVTMSER
*                        FIELDS UPDATED = NONE
*                     3. MASTER SCHEDULER RESIDENT DATA AREA (BASEA)
*                        FIELDS TESTED  = BALOG
*                        FIELDS UPDATED = NONE
*                     4. UNIT CONTROL MODULE (UCM)
*                        FIELDS TESTED = UCMVEA,UCMUCB,UCMATR-BIT UCMUF
*                                         UCMCOMPC,UCMVEZ,UCMHCUCM
*                                         UCMSFLG1 BIT-UCMSYSB @G51APSS
*                                                      UCMSYSE @G51APSS
*                                                      UCMSYSG
*                                         UCMDISP BIT -UCMDISPA
*                                                      UCMDISPC
*                                         UCMSDS1 BIT -UCMSDS1A
*                                                      UCMSDS1B
*                        FIELDS UPDATED = UCMSTS BIT-  UCMTA
*                                         UCMSFLGS BIT - UCMSYSB
*                                                        UCMSYSC
*                                         UCMDISP BIT- UCMDISPB
*                                         UCMHCUCM,UCMHRDRT,UCMAUTH,
*                                         UCMRTCD,UCMOAOEN,UCMALTEN
*                     5. UNIT CONTROL BLOCK (UCB)
*                        FIELDS TESTED  = UCBNAME
*                                         UCBFL5 BIT- UCBNALOC
*                                         SRTESTAT BIT- SRTEONLI
*                                                       SRTEALOC
*                        FIELDS UPDATED = SRTESTAT BIT- SRTEONLI
*                                                       SRTESYSR
*                                                       SRTEDADI
*                                         UCBWGT BIT - UCBVORSN
*                     6. COMMAND SCHEDULING CONTROL BLOCK (CSCB)
*                        FIELDS TESTED  - NONE
*                        FIELDS UPDATED - CHSTS - BIT= CHFC
*
* TABLES            = GETMAIN IS ISSUED FOR THE WTO BUFFER.
*                     96 BYTES FOR SUBPOOL 229.
*                                                              @ZA12404
* XSA POINTER SAVE                                             @ZA12404
*    AREA          = XAWORKA - LV PARAM FOR DYNAMIC STORAGE    @ZA13369
*                    XAWORKB - IEE36DEQ POINTER                @ZA12404
*                    XAWORKC - IEE36WRK POINTER                @ZA13369
*                    XAWORKD - UNUSED                          @ZA12404
*                    XAWORKE - UNUSED                          @ZA12404
*                    XAWORKF - UNUSED                          @ZA12404
*                    XAWORKG - UNUSED                          @ZA12404
*                    XAWORKH - UNUSED                          @ZA12404
*
* MACROS            = DEQ,GETMAIN,FREEMAIN,WTO,LOAD,DELETE,MGCR,SETLOCK
*
* MAPPING MACROS    = IEEXSA
*                     CVT
*                     IEEBASEA
*                     IEECUCM
*                     IEFUCBOB
*                     IEECHAIN
*                     IHAPSA
*    SERIALIZATION =  PROTECTION AGAINST COMM. TASK IS ACCOMPLISHED
*                    IN THIS MODULE BY OBTAINING THE LOCAL AND CMS
*                    LOCKS AT:
*                       CSETLOCK
*                    AND RELEASING THEM AT:
*                       UNSETLOK
*                    PROTECTION AGAINST OLTEP, ALLOCATION AND
*                    VARY/UNLOAD IS ACCOMPLISHED VIA AN ENQ IN MODULE
*                    IEE3603D
*                    THE DEQ IS ISSUED AT CDEQUE IN THIS MODULE ONLY IF
*                    A TERMINAL EXIT IS TAKEN IN THIS MODULE
*                    ALL BIT SETTINGS IN THE UCB ARE TURNED ON/OFF
*                    VIA THE COMPARE-AND-SWAP INSTRUCTION.
*
* CHANGE LEVEL      = Y02669, Y02651, Z30LPTJ, ZA02592,        @ZA02592
*                     ZM34520, OZ12404, OZ13369, G51APSS       @G51APSS
*
* MESSAGES          = IEE349I CONSOLES
*                     CONSOLE/ALT  COND  AUTH  ID  AREA  ROUTCD
*                        (IF THIS CONSOLE IS GRAPHICS)
*                        OR
*                     IEE349I CONSOLES
*                     CONSOLE/ALT  COND  AUTH  ID  ROUTCD
*                        (IF THIS CONSOLE IS NON-GRAPHICS)
*
*                     'IEE709I HARDCOPY REQUIRED-NO HARDCOPY DEVICE
*                     AVAILABLE.'
*                     (IF HARCPY IS REQUIRED ON THE SYSTEM, AND YET
*                     THE MASTER CONSOLE OR THE NEW CONSOLE ARE BOTH
*                     GRAPHICS DEVICES AND SYSLOG IS INACTIVE.)
*
*                     'IEE710I         NOW RECEIVING HARDCOPY.'
*                     (WHEN A HARDCPY DEVICE, SUCH AS SYSLOG, OR THE
*                     MASTER CONSOLE OR THE NEW CONSOLE UNIT IS
*                     FORCED AS THE HARDCPY, BECAUSE HARDCPY IS NOW
*                     REQUIRED)
*
*                     MESSAGE 'IEE328I VARY COMMAND ABORTED'
*                     IS ISSUED FROM IEE2103D WHEN THE GETMAIN
*                     FOR THE WTO BUFFER IS UNSUCCESSFUL.
*
*                     'IEE313I XXX      UNIT REF INVALID'
*                     (WHEN A NOT ACTIVE & NOT READY CONSOLE DEVICE IS
*                     DETECTED)
*
*                     'IEE744I VARY CONSOLE REJECTED -         @G51APSS
*                              NO FULL CAPABILITY CONSOLES'    @G51APSS
*                     '     TO RESTORE MASTER CONSOLE:'        @G51APSS
*                     '  1) PRESS ENTER, REQUEST, OR END ON    @G51APSS
*                           ANY AVAILABLE CONSOLE'             @G51APSS
*                     '  2) PRESS THE EXTERNAL INTERRUPT KEY'  @G51APSS
*
* ABEND CODES       = NONE
*
* PACKAGING         = IEE4903D WILL BE PART OF LOAD MODULE
*                        = IEE3603D
*                     THIS LOAD MODULE ALIASES ARE
*                        = NONE
*                     THIS LOAD MODULE ENTRY POINTS ARE
*                        = IEE3603D
*                     THIS MODULE WILL RESIDE IN:  LPALIB
*
* SYSGEN            = SUPPLIED BY THE FOLLOWING SECTIONS:
*                        LOAD MODULE NAME
*                        PACKAGING
*                     THIS CSECT WILL BE INCLUDED AT SYSGEN FROM
*                     DLIB -AOSB3- BY MACRO -SGIEF441-
*
* SYSTEM LIBRARIES  = NONE
*
***********************************************************************
         EJECT
*****************************************************************Y02669
*                             ***                                ***
*                             *        ***                     *** *
*                             *        *                         * *
*                             *        * ALL                     * *
*                             *        * INFO                    * *
*                             * ,AUTH= * **   ** **  ** **    ** * *
*                             *        * * SYS *,* IO *,* CONS * * *
*                             *        * **   ** **  ** **    ** * *
*                             *        *                         * *
*                             *        ***                     *** *
*   ***           ***         *                                    *
*   *               *         *        ***                     *** *
*   * XXX           *         *        *                         * *
*   *               *         *        * ALL                     * *
* V * O-XXX         *,CONSOLE * ,ROUT= * NONE                    * *
*   *               *         *        * ROUTCODE(,ROUTCODE,...) * *
*   * (I-XXX,O-XXX) *         *        *                         * *
*   *               *         *        ***                     *** *
*   ***           ***         *                                    *
*                             *           ***           ***        *
*                             *           *               *        *
*                             *           * XXX           *        *
*                             * ,ALTCONS= * O-XXX         *        *
*                             *           * (I-XXX,O-XXX) *        *
*                             *           *               *        *
*                             *           ***           ***        *
*                             ***                                ***
*****************************************************************Y02669
         EJECT
*                                                                 19084
*        REGISTER EQUATES                                         19084
*                                                                 19084
DEFREG   EQU   *                                                 Y02669
R0       EQU   0                                                  19084
R1       EQU   1                   IN-XAD,OUT-WTO CORE PTR        19084
R2       EQU   2                   XSA                            19084
R3       EQU   3                   ADDRESS                        19084
R4       EQU   4
R5       EQU   5                   UCME                          Y02669
R6       EQU   6                                                  19084
R7       EQU   7                   PROCESS BITS                   19084
R8       EQU   8                   STAE REG                       19084
R9       EQU   9                   UNIT PTR                       19084
R10      EQU   10                  UCM REG                        19084
R11      EQU   11                  UCMPFX REG                     19084
R12      EQU   12                  UCB PTR                        19084
R13      EQU   13                                                 19084
R14      EQU   14                  RET ADDR                       19084
R15      EQU   15                                                 19084
         SPACE 3                                               @G51APSS
***********************************************************************
*        EQUATES                                                  19084
***********************************************************************
ZERO     EQU   0                                                  19084
ONE      EQU   1                                                  19084
TWO      EQU   2                                                  19084
TRE      EQU   3                                                  19084
FOR      EQU   4                                                  19084
SIX      EQU   6                                                  19084
SVN      EQU   7                                                  19084
EIT      EQU   8                                                  19084
FOX      EQU   255                                                19084
MSGLEN   EQU   96                      LENGTH FOR GETMAIN.        21002
GETERRCD EQU   X'3A'                   ERROR MESSAGE INDICATOR.  Y02669
UNITERR  EQU   X'0D'                   ERROR MESSAGE INDICATOR @ZM34520
D12      EQU   12                                                Y02669
D8       EQU   8                                                 Y02669
D3       EQU   3                                                 Y02669
D15      EQU   15                                                Y02669
D5       EQU   5                                                 Y02669
D64      EQU   64                                                Y02669
D16      EQU   16                                                Y02669
D68      EQU   68                                                Y02669
D46      EQU   46                                                Y02669
D76      EQU   76                                              @ZM34520
D132     EQU   132                                               Y02669
GRPX    EQU   X'FF'                   GRAPHICS INDICATOR          21002
SHIFT8  EQU   8                       REMOVE ONE BYTE             21002
MASK00   EQU   X'00'                                             Y02669
RIID     EQU   X'80'                                              19084
MC       EQU   X'80'                                              19084
USYSLG   EQU   X'00'                                              19084
         EJECT
***********************************************************************
*        CONSOLE PROCESSOR MODULE                                 19084
         BALR  R3,ZERO                                            19084
         USING *,R3                                               19084
         MODID BR=YES                                            Y01886
         USING XSA,R2                                             19084
         STM   R0,R15,ZERO(R13)                                  Y02669
         ST    R13,XAK           STORE SAVE AREA PTR, NEED R13 @Z30LPTJ
         L     R10,XAD                 SAVE FLAGS.               Y02669
         MVC   XAD(GETLNTH),GETLIST    MOVE IN GETMAIN LIST.      21002
         LA    R1,XAX                  GET ADDRESS POINTER.       21002
         ST    R1,XAX                  GETMAIN ADDRESS.           21002
         LA    R1,XAD                  GETMAIN LIST POINTER.      21002
         GETMAIN MF=(E,(1))            DO CONDITIONAL GETMAIN.    21002
         LTR   R15,R15                 WAS GETMAIN SUCCESSFUL ?   21002
         BNZ   GETFAIL                 NO,SO TAKE ERROR EXIT.     21002
* XAX CONTAINS THE ADDRESS OF GOTTEN CORE
         STC   R4,XAX+FOR              SAVE GRAPHICS INDICATOR    21002
         ST    R10,XAD                 SET UP PASSED SWITCHES.    21002
         L     R4,XAR              PROCESS SWITCHES               19084
         XC    XAP(ONE),XAP        ZERO OUT THE SHIFT COUNTER  @ZM34520
         L     R9,XAL              GET PTR TO UNIT BEGIN          19084
         LA    R9,ZERO(R9)                                        19084
         L     R10,CVTPTR                                         19084
         USING CCVT,R10                                           19084
         L     R10,CVTCUCB         GET PTR TO UCM                 19084
         DROP  R10                                                19084
         USING UCM,R10
*                                                                 19084
         LR    R11,R10             UCM PTR                        19084
         SH    R11,FOUR            MINUS FOUR IS UCMPFX ADD PTR   19084
         L     R11,ZERO(R11)       GET PFX PTR                    19084
         USING UCMPRFX,R11                                        19084
         EJECT
**************************************************               Y02651
*                                                                Y02651
* LOCKS ARE NEEDED AT THIS POINT AS PROTECTION AGAINST COMM TASK Y02651
*  LOCKS WILL BE GOTTEN AT : CSETLOCK                            Y02651
*  AND THEY WILL BE RELEASED AT: UNSETLOK                        Y02651
*                                                                Y02651
         BAL   R8,CSETLOCK                                       Y02651
**************************************************               Y02651
         SPACE 2                                               @G51APSS
***********************************************************************
*        IF A NO-CONSOLES CONDITION EXISTS THEN ISSUE MSG      @G51APSS
*        IEE744I AND IGNORE THE VARY CONSOLE                   @G51APSS
***********************************************************************
         SPACE 1                                               @G51APSS
         TM    UCMSFLG1,UCMSYSE    DOES A NO-CONSOLES CONDITION EXIST? X
                                                               @G51APSS
         BNO   OKPROCES            NO, PROCESS THE CMD         @G51APSS
         SR    R0,R0               READY FOR CONSOLE ID        @G51APSS
         TM    XAU,RIID            WAS IT READER COMMAND?      @G51APSS
         BO    ISS744I             YES, R0 IS NOW SET          @G51APSS
         IC    R0,XAU              GET CONSOLE ID              @G51APSS
ISS744I  EQU   *                                               @G51APSS
         BAL   R8,UNSETLOK         RELEASE THE LOCKS           @G51APSS
         LA    R1,NCCMSG           MSG IEE744I                 @G51APSS
         WTO   MF=(E,(1))          ISSUE MSG IEE744I           @G51APSS
         B     CLEANUP             GO FREE RESOURCES           @G51APSS
         SPACE 3                                               @G51APSS
OKPROCES EQU   *                                               @G51APSS
         TM    XADUSWIT,XAXPAREN   MULTIPLE UNITS              @Z30LPTJ
         BZ    CSINGLE             NO                             19084
CSTART   LA    R9,ONE(R9)          YES,INDEX PAST BEGINING PARE   19084
CSINGLE  EQU   *                                                  19084
         CLC   ZERO(TWO,R9),COUNIT      IS IT AN O-XXX            19084
         BNE   CCOMP                    NO, MAYBE A COMPOSITE     19084
         LA    R9,TWO(R9)               YES INDEX TO UNITNAME     19084
         OI    XADUSWIT,XAOUNIT       SET XAOUNIT SWITCH ON    @Z30LPTJ
         B     CTEST                    GO CHECK IF MUST PROCESS  19084
*                                                                 19084
CCOMP    EQU   *                                                  19084
         CLC   ZERO(TRE,R9),CIUNIT      DO WE HAVE A COMPOSITE    19084
         BNE   CTEST                  NO,ITS A REGULAR UNIT     19084
         LA    R9,TRE(R9)               YES,INDEX TO INPUT UNIT NM19084
         OI    XADUSWIT,XAIOCOMP        TURN COMPOSITE SWITCH  @Z30LPTJ
*                                                                 19084
CTEST    EQU   *                                                  19084
         LTR   R4,R4                    DO WE PROCESS THIS UNIT   19084
         BZ    CMSGRDY                  NO, AND NOMOR EITHER, FINI19084
         BM    CPROCES                  YES, PROCESS THIS ONE     19084
*                                       NO, NOT PROCESS THIS ONE  19084
COMIT    EQU   *                        THERE ARE OTHERS          19084
         TM    XADUSWIT,XAIOCOMP   WAS IT A COMP TO OMIT?      @Z30LPTJ
         BZ    CENDCK                   NO,                       19084
         LA    R9,SVN(R9)               YES INDEX OVER            19084
*                                                                 19084
CENDCK   EQU   *                                                  19084
         LA    R9,TRE(R9)               TO THE DELIMITER AFTER UNI19084
         NI    XADUSWIT,XAXPAREN TURN ALL OFF EXPT EXTR PARENS @Z30LPTJ
         SLL   R4,ONE              UP ALL SWITCHES               Y02669
         SR    R8,R8               ZERO OUT FOR NEXT INSTR.    @ZM34520
         IC    R8,XAP              OBTAIN CURRENT COUNTER      @ZM34520
         LA    R8,ONE(R8)          INCREMENT COUNTER BY ONE    @ZM34520
         STC   R8,XAP              STORE UPDATED SHIFT COUNTER @ZM34520
         B     CSTART              AND START AGAIN               Y02669
*                                                                 19084
CPROCES  EQU   *                                                  19084
         LM    R5,R7,UCMVEA        GET UCMES PTRS                 19084
         USING UCMLIST,R5                                         19084
CLOOP    L     R12,UCMUCB          AND UCB ADDRESSABILITY         19084
         USING CUCB,R12                                           19084
         CLC   ZERO(TRE,R9),UCBNAME     IS THIS THE UCME FOR UNIT 19084
         BE    CFIND               YES,                           19084
         BXLE  R5,R6,CLOOP         MUST BE NEXT ONE GO CHECK      19084
*                                  WILL ALWAYS FIND ONE,CHECKED BY 4203
CFIND    EQU   *                                                  19084
         TM    UCMATR,UCMUF        ACTIVE CONSOLE CURRENTLY       19084
         BO    CATTRIB             YES, GO CHANGE ATTRIBUTES ONLY 19084
*                                  CONSOLE IS NON ACTIVE          19084
         TM    UCBFLA,UCBNRY       IS DEVICE NOT READY?        @ZM34520
         BO    CONLINE             YES, ISSUE ERROR MESSAGE    @ZM34520
         TM    XADUSWIT,XAIOCOMP   PROCESSING I-UNIT           @Z30LPTJ
         BZ    CHCPREQ             NO,GO CHECK HCPY REQ          Y02669
*                                                                 19084
CSTATUS  EQU   *                                                  19084
*                                                                 19084
*        SET APPRORIATE BITS IN UCME AND UCB TO SHOW THAT THIS DEV19084
*        IS TO BE A CONSOLE                                       19084
*                                                                 19084
         OI    UCMSTS,UCMTA        OPEN FOR COMM TASK            Y02669
*        NI    SRTESTAT,FOX-SRTEDADI+SRTECHGS  TURN ALL PNDG BITSY02651
*                                 OFF JUST IN CASE THEY ARE ON   Y02651
*                                                                Y02651
********** THE ABOVE INSTRUCTION WILL BE PROTECTED VIA THE       Y02651
*          COMPARE-AND-SWAP INSTRUCTION                          Y02651
*                                                                Y02651
         L     R8,XAK             SAVE AREA POINTER            @Z30LPTJ
         STM   R4,R6,D68(R8)      STORE USED FOR OIL/NIL         Y02651
DADINI   EQU   *                                                 Y02651
NILDADI  EQU   X'FE'     -X'FF'-SRTEDADI-                        Y02651
         NIL   SRTESTAT,NILDADI,WREGS=(4,5,6),REF=CUCB           Y02651
CHGSNI   EQU   *                                                 Y02651
NILCHGS  EQU   X'BF'     -X'FF'-SRTECHGS-                        Y02651
         NIL   SRTESTAT,NILCHGS,WREGS=(4,5,6),REF=CUCB           Y02651
         LM    R4,R6,D68(R8)                                     Y02651
*                                                                Y02651
         TM    SRTESTAT,SRTEONLI   IS DEV CURRENTLY ONLINE        19084
         BO    CONLINE             YES,GO TO ONLINE PROCESSOR     19084
         EJECT
*       NI    UCBWGT,FOX-UCBVORSN                                Y02669
*       OI    SRTESTAT,SRTEONLI+SRTESYSR  TURN ONL AND CONS BITS Y02651
*                                                                Y02651
********** THE ABOVE INSTRUCTION WILL BE PROTECTED VIA THE       Y02651
*          COMPARE-AND-SWAP INSTRUCTION                          Y02651
*                                                                Y02651
NILOPER  EQU   *                                                 Y02651
NILVORSN EQU   X'FB'    -X'FF'-UCBVORSN-                         Y02651
         NIL   UCBWGT,NILVORSN,WREGS=(4,5,6),REF=CUCB            Y02651
ONLIOI   EQU   *                                                 Y02651
         OIL   SRTESTAT,SRTEONLI,WREGS=(4,5,6),REF=CUCB          Y02651
OISYSR   EQU   *                                                 Y02651
         OIL   SRTESTAT,SRTESYSR,WREGS=(4,5,6),REF=CUCB          Y02651
         LM    R4,R6,D68(R8)                                     Y02651
*                                                                Y02651
*                                                                 19084
         B     CATTRIB             GO CHECK ATTRIBUTES            19084
*                                                                 19084
         EJECT                                                    19084
CONLINE  EQU   *                                                  19084
         L     R8,XAK             SAVE AREA POINTER            @Z30LPTJ
         STM   R4,R6,D68(R8)                                     Y02669
         TM    UCBFLA,UCBNRY       IS DEVICE NOT READY?        @ZM34520
         BNO   ITSREADY            NO, IT IS READY, BRANCH     @ZM34520
         TM    XADUSWIT,XAIOCOMP   IS IT COMPOSITE 'I-UNIT'?   @ZM34520
         BO    PARTTWO             YES, PROCESS 'O-UNIT'       @ZM34520
         L     R8,XAX              OBTAIN POINTER TO CORE      @ZM34520
         STM   R4,R6,D64(R8)       SAVE DATA IN GOTTEN CORE    @ZM34520
         L     R4,MASKBIT          GET MASK FOR BIT TURN OFF   @ZM34520
         L     R5,XAR              OBTAIN PROCESS BIT SWITCHES @ZM34520
         SR    R6,R6               ZERO OUT FOR NEXT INSTR.    @ZM34520
         IC    R6,XAP              OBTAIN THE SHIFT COUNTER    @ZM34520
         LTR   R6,R6               IS THERE ANY SHIFT COUNTER? @ZM34520
         BZ    AROUND              NO, BRANCH AROUND THE LOOP  @ZM34520
LOOP     EQU   *                                               @ZM34520
         SRL   R4,ONE              SHIFT RIGHT ONE BIT         @ZM34520
         BCT   R6,LOOP             LOOP UNTIL CORRECT POSITION @ZM34520
AROUND   EQU   *                                               @ZM34520
         XR    R5,R4               TURN OFF THE BAD UNIT BIT   @ZM34520
         SR    R6,R6               ZERO OUT FOR NEXT INSTR.    @ZM34520
         IC    R6,XAP              SAVE SHIFT COUNTER          @ZM34520
         MVI   XAE,UNITERR         SET ERROR MESSAGE CODE      @ZM34520
         MVC   XAV(D3),ZERO(R9)    MOVE IN INVALID DEVICE NAME @ZM34520
         MVC   XAV+TRE(D5),BLANKS  BLANK OUT REST OF AREA      @ZM34520
         MVC   D76(D16,R8),XAP     SAVE IMPORTANT XSA DATA     @ZM34520
         LR    R4,R8               SAVE THE SAVE AREA POINTER  @ZM34520
         BAL   R8,UNSETLOK         RELEASE THE LOCKS FOR WTO   @ZM34520
         L     R13,XAX             OBTAIN POINTER TO CORE      @ZM34520
         BAL   R8,MSGMODA          ISSUE THE ERROR MESSAGE     @ZM34520
         BAL   R8,CSETLOCK         OBTAIN THE LOCKS            @ZM34520
         LR    R8,R4               RESTORE THE SAVE AREA PTR.  @ZM34520
         MVC   XAP(D16),D76(R8)    RESTORE THE XSA DATA        @ZM34520
         MVC   XAV(EIT),VARYID     RESTORE 'VARY' CMD. NAME    @ZM34520
         ST    R5,XAR              STORE UPDATED BIT SWITCHES  @ZM34520
         STC   R6,XAP              RESTORE THE SHIFT COUNTER   @ZM34520
         LM    R4,R6,D64(R8)       RESTORE SAVED REGISTERS     @ZM34520
         B     CRETURN             CHECK FOR MORE UNITS        @ZM34520
ITSREADY EQU   *                                               @ZM34520
         TM    SRTESTAT,SRTEALOC   IS DEVICE ALLOCATED            19084
         BO    ONALLOC             YES, GO PROCESS ACCORDINGLY    19084
*        OI    SRTESTAT,SRTESYSR   NO, SET CONSOLE BIT IN UCB    Y02651
         B     OISYSR              GO EXECUTE THE ABOVE INSTR    Y02651
*                                                                 19084
ONALLOC  EQU   *                                                  19084
*        OI    SRTESTAT,SRTEDADI   TURN ON CHANGE STATUS BIT     Y02651
*                                                                Y02651
********** THE ABOVE INSTRUCTION WILL BE PROTECTED VIA THE       Y02651
*          COMPARE-AND-SWAP INSTRUCTION                          Y02651
*                                                                Y02651
OIDADI   EQU   *                                                 Y02651
         OIL   SRTESTAT,SRTEDADI,WREGS=(4,5,6),REF=CUCB          Y02651
         L     R15,CVTPTR          OBTAIN CVT POINTER          @ZA02592
         USING CCVT,R15                                        @ZA02592
         L     R15,CVTMSER         GET MASTER RESIDENT CORE    @ZA02592
         DROP  R15                                             @ZA02592
         USING BASE,R15                                        @ZA02592
         OIL   MSECBFL,MSSUM,WREGS=(4,5,6),REF=BASE            @ZA02592
         DROP  R15                                             @ZA02592
         LM    R4,R6,D68(R8)                                     Y02651
         B     CATTRIB             GO CHECK CONSOLES ATTRIBUTES   19084
         EJECT
CHCPREQ  EQU   *                                                  19084
*                                                                 19084
*        PROCESING AN OUTPUT ONLY, AN IO CONSOLE, OR THE O-UNIT OF19084
*        COMPOSITE. THEREFORE HARDCPY REQUIREMENTS MUST BE CHECKED19084
*        R5    UCME PTR TO NEW CONSOLE                           Y02669
*        R9    PTS TO UNIT IN COMMAND                             19084
*        R10   UCM PTR                                            19084
*        R11   UCMPFX PTR                                         19084
*        R12   UCB PTR                 *                          21051
*                                                                 21051
         OI    UCMSFLGS,UCMSYSB    TURN HCPY EXISTS BIT        @YM08624
         L     R7,UCMHCUCM         HARDCOPY ADDRESS, IF PRESENT@YM07658
         LTR   R7,R7               HARDCOPY PRESENT?           @YM07658
         BNZ   MANDRT              YES, CHECK AUTHORITY        @YM07658
*        NO HCPY EXISTS, THEREFORE PAST- ONE ACTV CONS - MSTR     19084
*                                  NOW - NEW CONSOLE WILL MAKE HCP19084
*                                        MANDATORY                19084
         TM    UCMSFLGS,UCMSYSG        IS SYSLOG FUNCTIONING     Y02669
*                                 AS THE HARDCPY LOG NOW         Y02669
         BO    MANDRT             YES, CK CMD AUTHORITY          Y02669
         L     R15,CVTPTR                                        Y02669
         USING CCVT,R15                                          Y02669
         L     R15,CVTMSER         PICK UP MSTER RES CORE ADDR   Y02669
         DROP  R15                                               Y02669
         USING BASE,R15                                          Y02669
         CLI   BALOG,USYSLG        IS SYSLOG SUPPORTED           Y02669
         BNE   SYSHCPY             YES, MAKE IT THE HARDCPY      Y02669
         DROP  R15                                               Y02669
*                                                                Y02669
*     CONTINUE TO FIND MASTER'S UCME SINCE SYSLOG IS NOT ACTIVE  Y02669
*                                                                Y02669
         LM    R13,R15,UCMVEA                                     19084
         DROP  R5                                                 19084
         USING UCMLIST,R13                                        19084
CTST     EQU   *                                                  19084
         TM    UCMDISP,UCMDISPA    IS THIS THE MSTR CONSOLE       19084
         BO    CMSTR               YES,FOUND IF                   19084
         BXLE  R13,R14,CTST        NO,MUST BE NEXT                19084
CMSTR    EQU   *                   NO,ERROR CONDITION             19084
         TM    UCMDISP,UCMDISPC    IS THE MASTER A GRAPHICS      Y02669
         BO    CXSTS               YES, CHECK THE NEW CONSOLE    Y02669
         OI    UCMDISP,UCMDISPB    TURN MSTR UCME TO INDICATE HCPY19084
         ST    R13,UCMHCUCM        AND STORE PTR TO IT IN PFX     19084
         DROP  R13                                                19084
         USING UCMLIST,R5                                         19084
         B     MANDRT01            SET MANDATORY ROUTS AND CMDS  Y02669
*                                                                 19084
*        HARDCPY DOES EXIST- CHECK IF IT IS GFX                   19084
*                                                                 19084
CXSTS    EQU   *                                                  19084
         TM    UCMDISP,UCMDISPC    IS NEW CONS. A GFX
         BO    CBOTHGFX            OOPS-BOTH ARE GFX CONSOLES
*
*    NEW CONSOLE IS PAPER PROD, THEREFORE MAKE NEW CONSOLE INTO
*    HARDCPY
*
         OI    UCMDISP,UCMDISPB    TURN HCPY BIT IN NEW CONSOLE
         ST    R5,UCMHCUCM         STORE ITS UCME PTR IN HCPY PTR
         B     MANDRT02            GO SET MANDTRY ROUTCDS AND CM Y02669
*                                                                 19084
*        BOTH CONSOLES ARE GFX                                    19084
*                   IF SYSLOG IS PRESENT MAKE IT HCPY             19084
*                   ELSE LEAVE ALL AS IS                          19084
*                                                                 19084
*                                                                 19084
CBOTHGFX EQU   *                                                 Y02669
         NI    UCMSFLGS,FOX-UCMSYSB   TURN OFF BIT,CANT GET HCPY Y02669
*****************************************************************Y02669
*   MESSAGE - SINCE CANNOT GET A HARDCPY DEVICE                  Y02669
*****************************************************************Y02669
         SR    R0,R0                                             Y02669
         TM    XAU,RIID            WAS IT AN INPUT STREAM COMD.  Y02669
         BO    RDRCMD              YES, R0 FOR WTO IS PRESET     Y02669
         IC    R0,XAU              GET CONSL ID INTO R0 FOR WTO  Y02669
RDRCMD   EQU   *                                                 Y02669
         SPACE 3                                               @G51APSS
**************************************************               Y02651
*                                                                Y02651
*  LOCKS MUST BE RELEASED AT THIS POINT.  A WTO MACRO            Y02651
*  MUST BE ISSUED                                                Y02651
*  LOCKS WERE OBTAINED EARLIER AT:                               Y02651
*    CSETLOCK                                                    Y02651
*                                                                Y02651
         BAL   R8,UNSETLOK                                       Y02651
*                                                                Y02651
**************************************************               Y02651
         SPACE 2                                               @G51APSS
         LA    R1,NOCPYMSG         GET ADDRESS OF THE MESSAGE    Y02669
         WTO   MF=(E,(1))          ISSUE THE MESSAGE             Y02669
         SPACE 2                                               @G51APSS
**************************************************               Y02651
*                                                                Y02651
*  WTO MACRO HAS BEEN ISSUED. NEW LOCKS MUST BE OBTAINED         Y02651
*  TO PROTECT AGAINST COMM. TASK AND ANOTHER VARY CMD            Y02651
*  THEY WILL BE RELEASED AT:                                     Y02651
*      UNSETLOK                                                  Y02651
*                                                                Y02651
         BAL   R8,CSETLOCK                                       Y02651
*                                                                Y02651
**************************************************               Y02651
         SPACE 2                                               @G51APSS
         B     CSTATUS             GO CHANGE CONSOLE'S STATUS
         SPACE 5                                               @G51APSS
SYSHCPY  EQU   *                                                 Y02669
         XC    UCMHCUCM(FOR),UCMHCUCM   ZERO OUT HCPY PTR SHOWING 19084
         OI    UCMSFLGS,UCMSYSG    TURN BIT IN PRX MEANING SYSLOG 19084
*                                  IS HARDCPY                     19084
MANDRTS  EQU   *                                                 Y02669
         L     R1,XAX             POINTER TO GOTTEN CORE         Y02669
         MVC   ZERO(D46,R1),HCPYMSG   MOVE MSG INTO DYNAMIC CORE Y02669
         MVC   D12(D8,R1),SYSLOGUT ISSUE MSG ,SYSLOG IS HCPY NOW Y02669
         B     READY               CONTINUE MSG PROCESSING       Y02669
*                                                                 19084
*                                                                Y02669
*                                                                Y02669
MANDRT01 EQU   *                                                 Y02669
         DROP  R5                                                Y02669
         DROP  R12                                               Y02669
         USING UCMLIST,R13         ADDRESS OF MASTER'S UCME      Y02669
         L     R8,UCMUCB           AND ITS UCB                   Y02669
         USING CUCB,R8                                           Y02669
PUTMSG   EQU   *                                                 Y02669
         L     R1,XAX              POINTER TO GOTTEN CORE        Y02669
         MVC   ZERO(D46,R1),HCPYMSG     MOVE MSG INTO DYNAM CORE Y02669
         MVC   D12(D3,R1),UCBNAME  MOVE IN MASTER'S UNITNAME     Y02669
         MVC   D15(D5,R1),BLANKS   PAD WITH BLANKS               Y02669
         DROP  R8                                                Y02669
         USING CUCB,R12                                          Y02669
READY    EQU   *                                                 Y02669
         SR    R0,R0               READY CUMID REG FOR WTO       Y02669
         TM    XAU,RIID            WAS IT A READER COMMAND       Y02669
         BO    WRITEMSG            YES, R0 IS ALL PRESET         Y02669
         IC    R0,XAU              PRESET CONSOLE ID             Y02669
WRITEMSG EQU   *                                                 Y02669
         SPACE 2                                               @G51APSS
**************************************************               Y02651
*                                                                Y02651
*  LOCKS MUST BE RELEASED AT THIS POINT. A WTO MACRO             Y02651
*  MUST BE ISSUED.                                               Y02651
*  LOCKS WERE OBTAINED EARLIER AT:                               Y02651
*        CSETLOCK                                                Y02651
*                                                                Y02651
         BAL   R8,UNSETLOK                                       Y02651
*                                                                Y02651
**************************************************               Y02651
         SPACE 2                                               @G51APSS
         WTO   MF=(E,(1))          ISSUE MESSAGE                 Y02669
         SPACE 2                                               @G51APSS
**************************************************               Y02651
*                                                                Y02651
*  WTO MACRO HAS BEEN ISSUED. NEW LOCKS MUST BE OBTAINED         Y02651
*  TO PROTECT AGAINST COMM. TASK                                 Y02651
*  THEY WILL BE RELEASED AT:                                     Y02651
*      UNSETLOK                                                  Y02651
*                                                                Y02651
         BAL   R8,CSETLOCK                                       Y02651
*                                                                Y02651
**************************************************               Y02651
         SPACE 2                                               @G51APSS
         B     MANDRT              CONTINUE CONSOLE PROCESSING   Y02669
         DROP  R13                                               Y02669
         USING UCMLIST,R5                                        Y02669
         SPACE 5                                               @G51APSS
MANDRT02 EQU   *                                                 Y02669
         L    R8,UCMUCB            GET UCB ADDRESS OF CONSOLE    Y02669
         B     PUTMSG                                            Y02669
*                                                                Y02669
*                                                                Y02669
*                                                                Y02669
MANDRT   EQU   *                                                  21002
         TM    UCMSDS1,UCMSDS1A+UCMSDS1B STCMDS OR INCMDS         21002
         BNZ   MANDRT1             YES, DON'T CHANGE AUTHORITY    21002
CRTCODE  EQU   *                                                  19084
         OI    UCMSFLGS,UCMSYSC         CMDS TO HCPY              21002
MANDRT1  EQU   *                                                  21002
         OC    UCMHRDRT(TWO),CHARTRT    MANDATORY ROUT CDS        19084
         B     CSTATUS             GO TO CHANGE STATUS TO CONSOLE 19084
*                                                                 19084
         EJECT
CATTRIB  EQU   *                                                  19084
         TM    UCMDISP,UCMDISPA    MSTR CONSOLE                   19084
         BNO   NO                      NO                         M0739
         TM    XADUSWIT,XAIOCOMP       IS MASTER A COMPOSITE?  @Z30LPTJ
         BNO   CROUT                   NO                         M0739
         OI    XASWITCH,XACOMPMS     YES,TURN ON MAST COMP BIT @Z30LPTJ
         B    CROUT                    DO NOT CHANGE CMD AUTH     M0739
NO       TM    XADUSWIT,XAOUNIT           OUTPUT ONLY          @Z30LPTJ
         BO    CROUT               YES, DO NOT CHANGE CMD AUTH    19084
         TM    XADUSWIT,XAIPAREN        O-UNIT OF COMP?        @Z30LPTJ
         BO    CROUT                    YES,OMIT CMD AUTH CHANGES 19084
         TM    XASOPCOD,XACMD      ARE CMD AUTHS TO BE CHANGED @Z30LPTJ
         BZ    CROUT               NO, GO CHECK IF ROUT TO BE CHAN19084
         MVC   UCMAUTH(TWO),XACMDATH PUT NEW CMD AUTH INTO UCME@Z30LPTJ
*                                                                 19084
CROUT    EQU   *                                                  19084
         TM    XASWITCH,XAROUT     IS ROUTS TO BE CHANGED?     @Z30LPTJ
         BZ    CALT                NO, SEE IF ALT MUST BE CHANGED 19084
         TM    XADUSWIT,XAIOCOMP   I-UNIT OF COMP?             @Z30LPTJ
         BO    CALT                YWA,OMIT ROUT CHANGES          19084
CMOVERT  MVC   UCMRTCD(TWO),XARTCODE   YES, MOVE RTS INTO UCME @Z30LPTJ
         TM    UCMDISP,UCMDISPA        MSTR CONSOLE               M0739
         BO    TURNONE                 YES, BRANCH                M0739
         TM    XASWITCH,XACOMPMS       COMPOSITE MASTER IF SO, @Z30LPTJ
         BNO   CALT                    UCME IS OF OUTPUT DEVICE   M0739
*                                      AND MASTER BIT IS ON ONLY  M0739
*                                      FOR INPUT DEVICE           M0739
TURNONE  OI    UCMRTCD,MC              TURN ON ROUT CODE 1        M0739
         NI    XASWITCH,FOX-XACOMPMS   ENSURE BIT IS TURNED OFF@Z30LPTJ
CALT     EQU   *                                                  19084
         TM    XASWITCH,XALTCHG    IS ALTERNATE TO BE CHANGED  @Z30LPTJ
         BZ    CRETURN             NO, GO MAKE FINAL CHECKS       19084
         TM    XASOPCOD,XALTCOM    IS ALT AN O-UNIT            @Z30LPTJ
         BZ    CALTCOMP            NO,MAYBE A COMP                19084
         BAL   R6,CID              GET UCME OF ALTERNATE          19084
CCOMPALT XC    UCMOAOEN(FOR),UCMOAOEN   ZERO OUT ALTS-COMPOS PTR  19084
         B     CRETURN             GO, MAKE FINAL CHECKS          19084
*                                                                 19084
CALTCOMP EQU   *                                                  19084
         TM    XASOPCOD,XALTCON    IS ALTERNATE A COMPOSITE    @Z30LPTJ
         BZ    CALTREG             NO, IO CONSOLE                 19084
         BAL   R6,CID              GO GET ALTERNATES UCME         19084
         DROP  R5                                                 19084
         USING UCMLIST,R15                                        19084
         L     R15,UCMCOMPC        GET PTR TO COMPOS OF ALTERNATE 19084
         DROP  R15                                                19084
         USING UCMLIST,R5                                         19084
         ST    R15,UCMOAOEN        STORE  IT IN NEW CONSL UCME    19084
*                                                                 19084
CRETURN  EQU   *                                                  19084
         TM    XADUSWIT,XAIOCOMP   PROCESSIN I-XXX?            @Z30LPTJ
         BZ    CNOIUNT             NO                             19084
PARTTWO  EQU   *                       *                          21051
         LA    R9,SIX(R9)          INDEX TO OUTPUT UNITNAME       19084
         NI    XADUSWIT,FOX-XAIOCOMP TURN OFF COMPOSITE SWITCH @Z30LPTJ
         OI    XADUSWIT,XAIPAREN   TURN ON OUT-OF-COMP- SWITCH @Z30LPTJ
         L     R5,UCMCOMPC         GET OUTPUT UCME PTR            19084
         B     CLOOP               GO PROCESS IT                  19084
*                                                                 19084
CALTREG  EQU   *                                                  19084
         BAL   R6,CID              GO GET ALTERNATES UCME         19084
         B     CCOMPALT            GO FIX PTRS                    19084
*                                                                 19084
*                                                                 19084
CNOIUNT  EQU   *                                                  19084
         TM    XADUSWIT,XAIPAREN   PROCESSIN OUT-OF-COMPOS?    @Z30LPTJ
         BZ    CENDCK              NO, GO CHECK EOB               19084
         LA    R9,ONE(R9)          YES, ADD 1 FOR INTERNAL PARENS 19084
         B     CENDCK              GO CHECK EOB                   19084
*        GET ALTERNATE UCME ROUTINE                               19084
*                                                                 19084
CID      EQU   *                                                  19084
         SR    R15,R15                                            19084
         LR    R7,R14              SAVE RET ADDRESS               19084
         IC    R15,XALTPTR         GETCONS ID OF ALTERNATE        19084
         BCTR  R15,ZERO            MINUS 1                        19084
         L     R14,UCMVEZ          GET SIZE OF UCMES              19084
         MR    R14,R14             TIMES ID                       19084
         A     R15,UCMVEA          PLUS ADDR OF FIRST UCME        19084
         ST    R15,UCMALTEN        EQUALS ADDR OF ALT UCME        19084
         LR    R14,R7              RESTORE RET ADDRESS            19084
         BR    R6                  RETURN                         19084
         EJECT                                                    19084
*        ISSUE HEADER FOR CONSOLES MSG                            19084
CMSGRDY  EQU   *                                                  19084
         SPACE 5
**************************************************               Y02651
*                                                                Y02651
*  WE ARE FINISHED PROCESSING THE COMMAND AND WTO MACROS         Y02651
*  WILL NOW BE ISSUED.  THE LOCKS OBTAINED AT:                   Y02651
*        CSETLOCK                                                Y02651
*  WILL NOW BE RELEASED AT:                                      Y02651
*        UNSETLOK                                                Y02651
*                                                                Y02651
         BAL   R8,UNSETLOK                                       Y02651
*                                                                Y02651
**************************************************               Y02651
         SPACE 2                                               @G51APSS
         L     R4,XAR              OBTAIN THE PROCESS SIWTCHES @ZM34520
         LTR   R4,R4               ARE THERE ANY BITS LEFT?    @ZM34520
         BNZ   DISPLAY             YES, PROCEED TO DISPLAY     @ZM34520
CLEANUP  EQU   *                                               @G51APSS
         BAL   R8,CDEQUE           NO, PREPARE FOR EXIT        @ZM34520
         L     R1,XAX              OBTAIN GOTTEN CORE ADDRESS  @ZM34520
         FREEMAIN R,LV=96,A=(1),SP=229                         @ZM34520
         L     R13,XAK             GET SAVE AREA ADDRESS       @ZM34520
         B     RETURN              BRANCH TO SET UP RETURN     @ZM34520
DISPLAY  EQU   *                                               @ZM34520
         L     R4,XAX                  ADDRESS OF GOTTEN CORE     21002
         LR    R1,R4                   SAVE ADDRESS               21002
         LA    R6,CHEADR                                          19084
         MVC   ZERO(LCHEADR,R1),ZERO(R6)  MOVE MSG TO BUFFER      21002
         CLI   XAX+FOR,GRPX            ARE ANY CONSOLES GRAPHICS  21002
         BE    CONSOLE                 YES, MSG CONTAINS AREA LABE21002
         USING MSGDSECT,R1                                        21002
         MVC   AREA(SIX),ROUTCD        REMOVE AREA FIELD          21002
         MVI   LABEL+ONE,NEWLEN        CHANGE LEN OF LABEL LINE   21002
         DROP  R1                                                 21002
CONSOLE  EQU   *                                                  21002
         SR    R0,R0                                              19084
         TM    XAU,RIID                 GET CONSOLE ID FOR WTO    19084
         BO    CHEADER                                            19084
         IC    R0,XAU                                             19084
*                                                                 19084
CHEADER  EQU   *                                                  19084
         STC   R0,XAU                                             19084
         WTO   MF=(E,(1))          ISSUE MSG                      19084
*                                                                 19084
         SPACE 10                                                 19084
EXIT     EQU   *                                                  19084
         L     R13,XAK           RESTORE REG13 TO PT TO SVAREA @Z30LPTJ
         ST    R4,D16(R13)        STORE MSG ADDR                 Y02669
         LM    R2,R15,EIT(R13)                                   Y02669
         LR    R6,R1                GET MLWTO ID FOR IEE7303D     21002
         SLL   R6,SHIFT8               PUT MLWTO INTO HI ORDER BYT21002
         IC    R6,XAX+FOR              INSERT GRAPHICS INDICATOR  21002
         ST    R6,XAP              STORE IT INTO THE XAP OF XSA  Y02669
         LR    R1,R4                   POINTER TO MSG AREA        21002
         NI    XADUSWIT,ZERO+XAXPAREN+XAUDVC TURN OFF ALL BUT IO & XPAR*
                                                               @Z30LPTJ
         MVI   XARESV,MASK00      SET UP 48/7303D SWITCHES     @Z30LPTJ
*                                 TO 0 SINCE THIS WILL BE THE    Y02669
*                                 FIRST ENTRY TO 4803D           Y02669
*                                                                 19084
*        XAU HAS CONSOLE ID OF CONSOLE,RDR OF SYSTEM              19084
*        XAR HAS PROCESS SWITCHES                                 19084
*        XAL HAS PTR TO UNIT FIELD                                19084
*        XAP HAS MLWTO ID AND GFX INDICATOR                      Y02669
*                                                                 19084
         MVC   XAX(EIT),CONSMSG         4803D NAME                19084
         L     R15,VCON48          GET ADDRESS OF IEE4803D       Y02669
         BR    R15                 GO TO IT                      Y02669
         EJECT
*                                                                Y02669
*                                                                Y02669
GETFAIL  EQU   *                                                  21002
         BAL   R8,CDEQUE           DEQUEUE THE HELD RESOURCES  @ZM34520
         MVI   XAE,GETERRCD        GETMAIN FAIL MESSAGE CODE   @ZM34520
         BAL   R8,MSGMOD           ISSUE THE ERROR MESSAGE     @ZM34520
RETURN   EQU   *                                               @ZM34520
         L     R13,XAK          RESTORE SAVE AREA PTR INTO R13 @Z30LPTJ
         L     R1,D64(R13)         GET CSCB ADDRESS              Y02669
         USING CHAIN1,R1                                         Y02669
         OI    CHSTS,CHFC          SET FREE CSCB FLAG            Y02669
         MGCR  (1),CHAIN                                         Y02669
         LM    R0,R15,ZERO(R13)                                  Y02669
         LR    R4,R14              SAVE RETURN ADDRESS           Y02669
         L     R0,XAWORKA          LOAD LV PARAM               @ZA13369
         LR    R1,R2                                             Y02669
         FREEMAIN  R,LV=(0),A=(1)  FREE DUMMY XSA                Y02669
         LR    R14,R4              RESTORE RETURN ADDRESS        Y02669
         BR    R14                 RETURN                        Y02669
         EJECT
**************************************************               Y02651
*                                                                Y02651
*  THE OBTAIN LOCKS SUBROUTINE FOLLOWS:                          Y02651
*  THESE LOCKS ARE RELEASED AT: UNSETLOK                         Y02651
         DS    0H
CSETLOCK EQU   *                                                 Y02651
         L     R15,XAK            SAVE AREA POINTER            @Z30LPTJ
         STM   R11,R14,D68(R15)                                  Y02651
         SETLOCK OBTAIN,TYPE=LOCAL,MODE=UNCOND,                  Y02651*
               RELATED=(IEE4903D(UNSETLOK))                      Y02651
         SETLOCK OBTAIN,TYPE=CMS,MODE=UNCOND,                    Y02651*
               RELATED=(IEE4903D(UNSETLOK))                      Y02651
         LM    R11,R14,D68(R15)                                  Y02651
         BR    R8                                                Y02651
**************************************************               Y02651
         EJECT
**************************************************               Y02651
*                                                                Y02651
*  THE RELEASE LOCKS SUBROUTINE FOLLOWS.                         Y02651
*  THESE LOCKS ARE OBTAINED AT: CSETLOCK                         Y02651
*                                                                Y02651
UNSETLOK EQU   *                                                 Y02651
         L     R15,XAK            SAVE AREA POINTER            @Z30LPTJ
         STM   R11,R14,D68(R15)                                  Y02651
         SETLOCK  RELEASE,TYPE=CMS,RELATED=(IEE4903D(CSETLOCK))  Y02651
         SETLOCK  RELEASE,TYPE=LOCAL,RELATED=(IEE4903D(CSETLOCK)) 02651
*                                                                Y02651
         LM    R11,R14,D68(R15)                                  Y02651
         BR    R8                                                Y02651
**************************************************               Y02651
         EJECT
**************************************************             @ZM34520
*                                                              @ZM34520
*  THIS A TERMINAL EXIT FROM THIS MODULE.                      @ZM34520
*  THE ENQ ISSUED IN MODULE IEE3603D MUST BE DEQUEUED OFF.     @ZM34520
*                                                              @ZM34520
CDEQUE   EQU   *                                               @ZM34520
         USING IEE36DEQ,R1                                     @ZA12404
         L     R1,XAWORKB          REG1, PARM LIST POINTER     @ZA12404
         DEQ   MF=(E,(R1))         FREE RESOURCES              @ZA12404
         DROP  R1                                              @ZA12404
         BR    R8                 RETURN TO CALLER             @ZM34520
**************************************************             @ZM34520
         EJECT
**************************************************             @ZM34520
MSGMOD   EQU   *                                               @ZM34520
         L     R13,XAK            RESTORE SAVE AREA POINTER    @ZM34520
MSGMODA  EQU   *                                               @ZM34520
         STM   R0,R15,ZERO(R13)                                @ZM34520
         MVC   XAX(EIT),ERRORMOD  SET IGC2103D NAME            @ZM34520
         LOAD  EP=IEE2103D                                     @ZM34520
         LR    R15,R0             STORE ROUTINE'S ADDRESS      @ZM34520
         BALR  R14,R15            GO TO 2103D TO PUT MESSAGE   @ZM34520
         LM    R0,R15,ZERO(R13)                                @ZM34520
         DELETE  EP=IEE2103D                                   @ZM34520
         BR    R8                 RETURN TO CALLER             @ZM34520
**************************************************             @ZM34520
         EJECT
***********************************************************************
*                                                                 19084
*        DECLARES                                                 19084
*                                                                 19084
***********************************************************************
MASKBIT  DC    X'80000000'         MASK TO TURN OFF BAD BIT    @ZM34520
FOUR     DC    H'4'                                               19084
COUNIT   DC    C'O-'               O-XXX                          19084
CIUNIT   DC    C'(I-'              COMPOSITE                      19084
CHARTRT  DC    X'F340'             HARDCPY MANDATORY ROUT CODES   19084
SYSLOGUT DC    C'SYSLOG  '                                       Y02669
BLANKS   DC    CL5'     '                                        Y02669
VARYID   DC    C'VARY    '                                     @ZM34520
         SPACE 2                                               @G51APSS
         DS    0F                                                Y02669
VCON48   DC    V(IEE4803D)                                       Y02669
CONSMSG  DC    C'IEE4803D'                                       Y02669
ERRORMOD DC    C'IEE2103D'             ERROR MODULE.             Y02669
         SPACE 2                                               @G51APSS
GETLIST  GETMAIN EC,LV=MSGLEN,A=0,SP=229,MF=L                    Y02669
GETLNTH  EQU   *-GETLIST               LENGTH OF LIST FORM.       21002
         EJECT
***********************************************************************
*                                                                 19084
*        MESSAGE LIST FORMS                                    @G51APSS
*                                                                 19084
***********************************************************************
         SPACE 1                                               @G51APSS
CHEADR   WTO   ('IEE349I CONSOLES',C),('CONSOLE/ALT     COND   AUTH   I*
               D  AREA    ROUTCD',L),MF=L,DESC=(5),MCSFLAG=(REG0,RESP)
CEND     EQU   *                       END OF WTO                 21002
LCHEADR  EQU   CEND-CHEADR             LENGTH OF WTO              21002
         SPACE 2                                               @G51APSS
NOCPYMSG WTO   'IEE709I HARDCOPY REQUIRED-NO HARDCOPY DEVICE AVAILABLE'*
               ,MF=L,DESC=(5),MCSFLAG=(REG0,RESP)                Y02669
         SPACE 2                                               @G51APSS
HCPYMSG  WTO   'IEE710I         NOW RECEIVING HARDCOPY',MF=L,DESC=(5), *
               MCSFLAG=(REG0,RESP)                               Y02669
         SPACE 2                                               @G51APSS
NCCMSG   WTO   ('IEE744I VARY CONSOLE REJECTED - NO FULL CAPABILITY CONX
               SOLES',D),                                              X
               ('     TO RESTORE MASTER CONSOLE:',D),                  X
               ('     1) PRESS ENTER, REQUEST, OR END ON ANY AVAILABLE X
               CONSOLE',D),                                            X
               ('     2) PRESS THE EXTERNAL INTERRUPT KEY',DE),        X
               MF=L,DESC=(5),MCSFLAG=(REG0,RESP)               @G51APSS
         SPACE 5
         DS    0F                                              @G51APSS
PATCH    DC    4CL25'**IEE4903D PATCH AREA **'                 @G51APSS
         SPACE 5
MSGDSECT DSECT                                                    21002
CONTROL  DS    7F                                                 21002
LABEL    DS    38C                     UP TO AREA FIELD           21002
AREA     DS    7C                      WHERE ROUTCD SHOULD GO     21002
END      DS    1C                      END IF NO AREA FIELD       21002
ROUTCD   DS    6C                      ROUTCD FIELD               21002
NEWLEN   EQU   END-LABEL          LEN OF LINE IF NO AREA FIELD    21002
*                                                                 19084
         EJECT
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *  @ZA12404
*                                                           *  @ZA12404
* DSECT TO REFERENCE GETMAIN'D WORK AREA FOR DEQ.  Q4 AND   *  @ZA12404
* VARYDEV ARE ENQ'D IN IEE3603D AND DEQ'D IN THIS AND       *  @ZA12404
* OTHER MODULES.                                            *  @ZA12404
* SEE 'SERIALIZATION' IN MODULE DESCRIPTION FOR IEE3603D.   *  @ZA12404
*                                                           *  @ZA12404
* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *  @ZA12404
         SPACE 1                                               @ZA12404
IEE36DEQ DSECT                                                 @ZA12404
DEQPARM  DEQ   (,,TWO,SYSTEM,,,SVN,SYSTEM),RET=HAVE,MF=L       @ZA12404
DEQLNTH  EQU   *-DEQPARM           LENGTH OF PARM LIST         @ZA12404
         DS    0D                  ROUND TO DOUBLE WORD        @ZA12404
*GETLNTH EQU   *-IEE36DEQ          LENGTH OF GETMAIN'D AREA    @ZA12404
         EJECT
CXSA     DSECT
         IEEXSA
         EJECT
CCVT     DSECT
         CVT
         EJECT
         IEEBASEA
         EJECT
CUCM     DSECT
         IEECUCM  FORMAT=NEW
         EJECT
CUCB     DSECT
         IEFUCBOB
         EJECT
CHAIN1   DSECT
         IEECHAIN
         EJECT
         IHAPSA
         END
