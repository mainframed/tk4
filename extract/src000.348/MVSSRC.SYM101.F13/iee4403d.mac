 TITLE 'IEE4403D - KEYWORD SCANNER FOR VARY'
IEE4403D CSECT
* A (K2      ) APPROX 438000  (KK2     ) APPROX 445000         @ZA12703
* A (KPAR3   ) APPROX 485000                                   @ZA12703
* D (K2      ) APPROX 433000  (KPAR9   ) APPROX 512000         @ZA12703
***********************************************************************
* MODULE NAME      = IEE4403D
*   CSECT NAME     = IEE4403D
*
* LOAD MODULE NAME = IEE3603D
*
* DESCRIPTIVE NAME = KEYWORD SCANNER FOR VARY
*
* COPYRIGHT        = MISSING
*
* STATUS           = OS/VS/2 RELEASE 3 PTF UZ05997             @ZA12703
*
* FUNCTION         =THIS MODULE CONTINUES TO SCAN OF THE COMMAND
*                    INPUT BUFFER. IT DETERMINES THE VALIDITY OF
*                    THE KEYWORD VALUES OF THE VARY COMMAND AND
*                    SETS APPROPRIATE BITS IN THE XSA (DUMMY XSA)
*                    BEFORE PASSING CONTROL TO THE SECOND OPERAND
*                    PROCESSOR ROUTINE = IEE4203D
*
* OPERATION        = THIS MODULE SCANS FOR THE CORRECT KEYWORDS AND
*                    TESTS THE VALIDITY OF THEIR VALUES.  IF ANY OF
*                    THE KEYWORDS OR THEIR VALUES ARE WRONG, THE
*                    COMMAND IS IGNORED AND AN ERROR MESSAGE IS
*                    RETURNED.
*                    IF A KEYWORD IS NOT PRESENT, ITS FIELD IN THE
*                    XAS CONTAINS ZEROES.
*                    KEYWORDS CHECKED FOR ARE:
*                       AUTH=
*                       ROUT=
*                       ALTCONS=
*
* NOTES
*   DEPENDENCIES   = NONE
*   CHARACTER/CODE DEPENDENCIES
*                  = THIS MODULE IS DEPENDENT ON THE EBCDIC
*                   REPRESENTATION OF DECIMAL CHARACTERS
*
* RESTRICTIONS     = SEE ENTRY POINTS - INPUT DATA
*   REGISTER CONVENTIONS
*                  = REGISTERS ARE DEFINED AT LABEL: DEFREG
*   PATCH LABEL    = PATCH
*
* MODULE TYPE      = CSECT
*   PROCESSOR      = ASSEMBLER
*   MODULE SIZE    = VS/2 = '528'                              @Z30LPTJ
*   ATTRIBUTES      = REENTRANT, REUSABLE, REFRESHABLE,
*                    ZERO PSW PROTECT KEY
*
* ENTRY POINTS     = IEE4404D FROM IEE2303D OR IEE3303D
*   PURPOSE        = TO SCAN THE SECONDARY KEYWORDS THAT MAY BE
*                    PRESENT IN THE VARY CONSOLE COMMAND
*   LINKAGE        = BRANCH
*   INPUT          = REG13 PTS TO THE SAVE AREA GOTTEN BY IEE3603D
*                    REG 2 PTS TO THE DUMMY XSA
*                       XAR CONTAINS THE BUFFER LENGTH
*                       XAL POINTS TO THE COMMAND PARAMTERS
*                       XAV CONTAINS THE VERB (VARY)
*                    REG1 CONTAINS THE COMMAND AUTHORITY OF THE
*                    READER/INTERPRETER IF IT  ISSUED THE COMMAND
*                    WORD 17 OF THE SAVE AREA POINTED BY REG13
*                    CONTAINS THE CSCB POINTER.
*   REGISTERS SAVED  = ALL
*   REGISTER USAGE =    R1   XAD SWITCHES ON INPUT AND OUTPUT
*                            OTHERWISE, CONTAINS THE R/I COMD AUTHORITY
*                       R2   POINTER TO THE DUMMY XSA
*                       R6   INCREMENTER FOR LOOPS
*                       R7   END OF BUFFER PTR
*                       R9   UNIT PTR
*                       R12  ADDRESSABILITY
*                       R13  SAVE AREA PTR STORE HERE IN XAA+2 OF XSA
*   REGISTERS RESTORED
*                  = ALL
*
* EXITS - NORMAL   = TO IEE4203D VIA BRANCH
*   CONDITIONS     = WHEN THE SECONDARY KEYWORDS HAVE BEEN CHECKED
*                       AND NO ERRORS HAVE BEEN FOUND
*   OUTPUT         = REG13 PTS TO THE SAVE AREA
*                    WORD 17 HAS THE CSCB PTR
*                    REG2 CONTAINS THE DUMMY XSA PTR
*                    REG1 HAS XAD FLAGS DETERMINED BY 3303D SUCH AS:
*                       WHETHER MULTIPLE UNITS OR A SINGLE UNIT
*                       ARE PRESENT IN THE COMMAND
*                    XAR  HAS THE SIZE OF THE BUFFER
*                    XAL POINTS TO THE COMMAND PARAMETERS
*                    XAV HAS THE VERB (VARY)
*   RETURN CODES   = NONE
*
* EXITS - ERROR    = TO IEE0503D OR IEE2103D VIA BALR
*   CONDITIONS     = ERROR HAS BEEN DETECTED IN THE COMMAND
*                    ON RETURN FROM THE MESSAGE MODULE THE CSCB IS
*                    FREED VIA THE MGCR MACRO, THE SAVE AREA AND THE
*                    DUMMY XSA IS FREED ALSO AND EXIT IS TAKEN TO THE
*                    CALLER OF THE PROCESSOR.
*   OUTPUT         = REG13 CONTAINS THE SAVE AREA PTR
*                    REG2 CONTAINS A POINTER TO THE DUMMY XSA
*                       XAE CONTAINS THE ERROR MESSAGE CODE
*   RETURN CODES   = NONE
*
* EXTERNAL REFERENCES
*   ROUTINES       = IEE4203D
*                    IEE0503D
*                    IEE2103D
*   DATA AREAS     = SEE OUTPUT - DATA
*
* CONTROL BLOCKS   = 1. DUMMY EXTENDED SAVE AREA (XSA)
*                    2. UNIT CONTROL MODULE (UCM)
*                       FIELDS TESTED  - UCMATR -BIT= UCMOF
*                                                     UCMIF
*                                        UCMCOMPC
*                                        UCMDISP - BIT=UCMDISPA
*                                        UCMID
*                                        UCMUCB
*                                        UCMVEA
*                       FIELDS UPDATED - NONE
*                    3.UNIT CONTROL BLOCK (UCB)
*                       FIELDS TESTED  - UCBNAME
*                       FIELDS UPDATED - NONE
*                    4. CVT
*                       FIELDS TESTED  - CVTCUCB
*                       FIELDS UPDATED - NONE
*                    5. COMMAND SCHEDULING CONTROL BLOCK (CSCB)
*                       FIELDS TESTED  - NONE
*                       FIELDS UPDATED - CHSTS - BIT= CHFC
*
* TABLES           = NONE
*
* MACROS           = MGCR,FREEMAIN,LOAD,DELETE,DEQ
*
* MAPPING MACROS   = IEEXSA
*                    CVT
*                    IEECUCM
*                    IEFUCBOB
*                    IEECHAIN
*   SERIALIZATION  = THIS MODULE EXECUTES PROTECTED FROM VARY/UNLOAD
*                    ALLOCATION AND OLTEP VIA AN ENQ ISSUED IN
*                    MODULE IEE3603D
*                    A DEQ IS ISSUED WHEN THERE IS A TERMINAL
*                    EXIT CONDITION IS ENCOUNTERED
*                    THE DEQ IS ISSUED AT LOCATION:  KDEQUE
*                    PROTECTION FROM COMM TASK IS NOT REQUIRED
*                    HERE THEREFORE NO LOCKS WILL BE GOTTEN.
*
* CHANGE LEVEL     = Y02669, Y02651, ZA12703                   @ZA12703
*
* CHANGE ACTIVITY  = N/A
*
* MESSAGES         = 'IEE309I VARY UNIDENTIFIABLE KEYWORD'
*                       INDICATES A SYNTAX ERROR IN THE KEYWORD OR
*                       ITS VALUE.
*                    'IEE313I XXX UNIT REF. INVALID'
*                       UNIT SPECIFIED FOR ALTERNATE CONSOLE HAS NO
*                       UCME OR COMPOSITE WAS INVALID (XXX,XXX ARE
*                       COMPOSITE UNIT ADDRESSES SPECIFIED)
*                    ' IEE306I VARY INVALID NUMERICS'
*                       WHEN AN INVALID ROUTCODE WAS SPECIFIED.
*                    'IEE345I VARY AUTHORITY INVALID'
*                       IF THE ISSUING CONSOLE ISN'T THE MASTER
*                       CONSOLE, THIS MESSAGE IS PUT OUT.
*                       NOTE: ALL MSGS ARE ISSUED BY IEE0503D
*
* ABEND CODES      = NONE
*
* PACKAGING        = IEE4403D WILL BE PART OF LOAD MODULE
*                       IEE3603D
*                    THIS LOAD MODULE ALIASES ARE
*                       NONE
*                    THIS LOAD MODULE ENTRY POINTS ARE
*                       IEE3603D
*                    MODULE WILL RESIDE IN:  LPALIB
*
* SYSGEN           = SUPPLIED BY SECTIONS:
*                       LOAD MODULE NAME
*                       PACKAGING
*                    THIS CSECT WILL BE INCLUDED AT SYSGEN FROM
*                    DLIB -AOSB3- BY MACRO -SGIEF441-
*
* SYSTEM LIBRARIES = NONE
*
***********************************************************************
         EJECT
* SCAN REGISTER ASSIGNMENTS        FUNCTIONS:
DEFREG   EQU   *                                                 Y02669
R0       EQU   0
R1       EQU   1
R2       EQU   2                       DUMMY XSA BASE
R3       EQU   3
R4       EQU   4                       GENERAL USE
R5       EQU   5                       VARIOUS  DSECT BASES
R6       EQU   6                       INCREMENT FOR LOOPS
R7       EQU   7                       POINTER TO END OF BUFFER
R8       EQU   8
R9       EQU   9                       PTR TO CHARACTER BEING CHECKED
R10      EQU   10
R11      EQU   11
KBASE    EQU   12                      ADDRESSIBILITY BASE-THIS MODULE
R13      EQU   13                      SAVE AREA PTR ON INPUT    Y02669
R14      EQU   14
R15      EQU   15                      USED FOR BRANCHES
*
* ERROR MESSAGE CODES
*
ERR9     EQU   9                       KEYWORD MISSPELLED
ERR13    EQU   13                      UNIT REF INVALID
ERR6     EQU   6                                                  19084
ERR29    EQU   X'29'                   INVALID AUTHORITY MESSAGE  21002
*
* COMMAND AUTHORITY GROUP BIT SETTINGS
*
KG3CONS  EQU   X'20'                   CONS-GROUP3
KG2IO    EQU   X'40'                   I/O-GROUP2
KGISYS   EQU   X'80'                   SYS-GROUP1
KMASK3   EQU   X'E0'                   ALL GROUPS
*
*
ZERO     EQU   0
ONE      EQU   1
TWO      EQU   2
TRE      EQU   3
FOR      EQU   4
FIV      EQU   5
SIX      EQU   6
SVN      EQU   7
EIT      EQU   8
NIN      EQU   9
TEN      EQU   10
TWLV     EQU   12
FIFTN    EQU   15
SIXTN    EQU   16
FOX      EQU   255                                                19084
LEFPAREN EQU   C'('
RTPAREN  EQU   C')'
COMMA    EQU   C','
BLK      EQU   C' '
F0       EQU   X'F0'                                             A33219
F1       EQU   X'F1'                                             A33219
F6       EQU   X'F6'                                             A33219
F9       EQU   X'F9'                                             A33219
D64      EQU   64                                                Y02669
D132     EQU   132                                               Y02669
SYS      EQU   X'00'                                             Y02669
RI       EQU   X'80'                                             Y02669
         EJECT
***********************************************************************
*
* SCAN AND ROUTE ROUTINE CONTINUED                                    *
*
***********************************************************************
         BALR  KBASE,ZERO                 ESTABLISH ADDRESSIBILITY
         USING *,KBASE
         MODID BR=YES                                            Y01886
         STM   R0,R15,ZERO(R13)                                  Y02669
         L     R5,CVTPTR               PTR TO CVT
         USING SETCVT,R5               USE CVT TO
         L     R5,CVTCUCB              OBTAIN PTR TO UCM (HEADER)
         USING UCM,R5                  UCM ADDRESSIBILITY
         USING XSA,R2
         ST    R13,XAK             SAVEAREA POINTER STORED     @Z30LPTJ
         L     R9,XAS+FOR          RETRIEVE CHARACTER POINTER
         XC    XAS+FOR(FOR),XAS+FOR ZERO INDICATOR FIELD
         L     R7,XAR                  LOAD PTR TO COMMAND INPUT BUFFER
         A     R7,XAL             GET EOB PTR INTO REG7          Y02669
         MVC   XAX+TRE(TWO),KCONSLE        MOVE IGC4203D INTO XSA
*                                 IF AN ERROR IS FOUND, THE 42    21002
*                                 WILL BE OVERLAYED WITH A 21 OR  21002
*                                      AN 05                      21002
*
***********************************************************************
*
* DETERMINE WHICH KEYWORD SPECIFIED
*
***********************************************************************
KEYSCAN  EQU   *
         CLC   ONE(FIV,R9),KAUTH   AUTH= KEYWORD
         BE    K1                  YES
         CLC   ONE(FIV,R9),KROUT   ROUT= KEYWORD
         BE    K2                  YES
         CLC   ONE(EIT,R9),KALT    ALTCONS= KEYWORD
         BNE   KERR3               NO,ERROR
*
         EJECT
***********************************************************************
*
*   VALUES FOR ALTCONS=
*
***********************************************************************
K3       EQU   *
         OI    XASWITCH,XALTCHG    SET THE ALT TO BE CHANGED BIT IN XSA*
                                                               @Z30LPTJ
         LA    R9,NIN(R9)          GET PTR TO PARMS ON KEYWORD
         CLI   ZERO(R9),LEFPAREN   TEST FOR ALTERNATE CONSOLE
         BE    KPAREN              YES
         CLC   ZERO(TWO,R9),OUNIT+ONE   TST FOR O-XXX SPECIFICATION
         BNE   KUNTCK2                 CHECK FOR MORE KEYWORDS    21002
         LA    R9,TWO(R9)              UPDATE POINTER             21002
         OI    XASOPCOD,XALTCOM      SET BIT INDICATING O-UNIT @Z30LPTJ
KUNTCK2  EQU   *
         LR    R10,R9         SAVE PTR TO UNITS
         LA    R9,TRE(R9)       UPDATE PTR TO END OF UNIT
         CR    R9,R7               IS IT END OF BUFFER
         BE    KU1                 YES,END OF COMMAND
         BH    KERR5               ERROR                          19084
         CLI   ZERO(R9),COMMA      COMMA INDICATES MORE KEYWORDS
         BE    KU2
         CLI   ZERO(R9),BLK        BLANK INDICATES END OF COMMAND
         BNE   KERR5               OTHERWISE INVALID UNIT SPECIFID19084
*
KU1      EQU   *
         LA    R1,KDETRM
KU3      EQU   *
         BAL   R11,KUNTCK          CHECK FOR UNIT VALIDITY
*        REGISTER 13 CONTAINS ADDRESS OF UCM ENTRY FOR UNIT SPECIFIED
*        IF UNIT IS VALID
*
         USING UCMLIST,R13         ..
         TM    UCMATR,UCMOF+UCMIF  DOES UNT HAVE WTO AND ATENTIONY02669
         BO    KPUTID                  BOTH WTO AND ATTENTION     21002
         BZ    KERR5               HAD NEITHER, ERROR             19084
*
KOUNIT   EQU   *
         TM    XASOPCOD,XALTCOM    WAS O-UNIT SPECIFIED?       @Z30LPTJ
         BZ    KERR5               NO, ERR                        19084
         USING UCMLIST,R13                                        19084
         TM    UCMATR,UCMOF        YES,HAS IT WTO SUPPORT        Y02669
         BZ    KERR5               NO, ERR                        19084
         TM    UCMDISP,UCMDISPD   HAS O-SUPPORT, IS IT O-ONLY?   Y02669
         BZ    KERR5              NO ERROR, IS NOT O-ONLY        Y02669
KPUTID   EQU   *                                                  21002
         MVC   XALTPTR(ONE),UCMID      INSERT UCMI FOR ALTERNATE  21002
         BR    R1                 BRANCH TO APPROPRIATE PROCESSOR 21002
KU2      EQU   *
         LA    R1,KEYSCAN          PREST TO CHECK OTHER KEYWORDS
         B     KU3                 GO CHECK UNIT'S VALIDITY
*
*        PROCESSING OF COMPOSITE CONSOLE SPECIFIED AS ALTCON
*
KPAREN   EQU   *
         LR    R10,R9                                            Y02669
         CLC   ONE(TWO,R9),IUNIT   CHECK FOR I-XXX PRESENT
         BNE   KERR5               NOPE, UNIT REF INVALID        Y02669
         CLC   SIX(TRE,R9),OUNIT   ,O- SPECIFICATION CHECK
         BNE   KERR5               NO,ERROR                      Y02669
         CLI   TWLV(R9),RTPAREN    ')' ENDS IT
         BNE   KERR5               NO,ERROR                      Y02669
*
*        COMPOSITE CONSOLE SYNTAX IS OKAY
*
         OI    XASOPCOD,XALTCON    SET BIT INDICATING COMP ALT @Z30LPTJ
         LA    R9,TRE(R9)            UPDATE PTR TO UNIT
         LR    R10,R9         SAVE POINTER TO UNITS
         BAL   R11,KUNTCK          CHECK VALIDITY OF UNIT SPECIFIED
         USING UCMLIST,R13         VALID
         TM    UCMATR,UCMIF        DOES I-UNIT HAVE ATTENTION SUPY02669
         BZ    KERR4               NO,ERR
         L     R4,UCMCOMPC         COMPOSITE POINTER FROM THIS UCME
         LA    R4,ZERO(R4)            CLEAR HIGH ORDER BYTE
         DROP  R13
         LTR   R4,R4               IS UNIT PART OF COMPOSITE
         BZ    KERR4               NO,ERROR CONDITION
         USING UCMLIST,R4          GO INTO COMPOSITE'S UCME TO GET
         TM    UCMATR,UCMOF        DOES O-UNIT HAVE WTO SUPPORT  Y02669
         BZ    KERR4               NO,ERROR
         L     R10,UCMUCB          GET PTR TO UCB
         DROP  R4
         USING UCB,R10             USING THIS PTR,GET PHYSICAL
         CLC   SIX(TRE,R9),UCBNAME  UNIT ADDRS AND COMPARE TO COMMAND
         DROP  R10
         BNE   KERR4               IF NOT CORRECT,COMPOSITE, ERROR
         USING UCMLIST,R13
         MVC   XALTPTR(ONE),UCMID    INSERT UCMI FOR ALTERNATE
         DROP  R13
         LA    R9,TEN(R9)
*
         EJECT
*
*        DETERMINATION OF END OF SYNTAX SCAN
*
KSK1     EQU   *
         CR    R9,R7               END OF BUFFER
         BE    KDETRM              YES,GOTO DETERMINATION NEXT MOD
         CLI   ZERO(R9),COMMA      ',' INDICATES MORE KEYWORDS
         BE    KEYSCAN             CONTINUE SCAN
         CLI   ZERO(R9),BLK        BLANK INDICATES END OF COMMAND
         BNE   KERR3               IF NONE,ERROR
*
KDETRM   EQU   *
         L     R13,XAK            RESTORE SAVE AREA PTR        @Z30LPTJ
         MVC   XAV(EIT),VARY                                     Y02669
         L     R15,VCON42         GET ADDRESS OF 4203D           Y02669
         LM    R0,R14,ZERO(R13)   RESTORE REGS                   Y02669
         BR    R15                GO TO 4203D                    Y02669
*
         EJECT
***********************************************************************
*
*              VALUES FOR AUTH=
*
***********************************************************************
K1       EQU   *
***********************************************************************
* THIS PARM IS VALID ONLY WHEN THE COMMAND IS ISSUED FROM THE    21002*
* MASTER CONSOLE TO ENSURE THAT NO OTHER CONSOLE CHANGES ITS OWN 21002*
* OR ANOTHER CONSOLES AUTHORITY                                  21002*
***********************************************************************
         CLI   XAU,RI             READER/INTERPRETER COMMAND     Y02669
         BE    KERR7              YES,ER,R/I CANT ISSUE THIS CMD Y02669
         CLI   XAU,SYS             SYSTEM CMD                    Y02669
         BE    AUTHOK             YES, NO NEED TO CHECK AUTHORITYY02669
         LR    R1,R7                   SAVE END OF BUFFER PPR     21002
         SR    R11,R11                                            21002
         IC    R11,XAU                 USE ALGOR. TO ACCESS UCME  21002
         LM    R6,R7,UCMVEA            R6 FIRST UCM ENTRY         21002
         BCTR  R11,ZERO                SUBTRACT ONE               21002
         MR    R10,R7                  LENGTH OF FIRST ENTRY      21002
         AR    R11,R6                  ADDRESS OF ENTRY           21002
         USING UCMLIST,R11                                        21002
         TM    UCMDISP,UCMDISPA        WAS ISSUER MASTER CONSOLE  21002
         BZ    KERR7                   NO, INVALID AUTHORITY      21002
         LR    R7,R1                   RESTORE END OF BUFFER PTR  21002
AUTHOK   EQU   *                                                 Y02669
         OI    XASOPCOD,XACMD   SET BIT INDICATING CMD CHANGE  @Z30LPTJ
         LA    R9,SIX(R9)          GET PTR TO PARM FIELD
         CLI   ZERO(R9),LEFPAREN   ARE THERE SUBFIELDS OF PARM OF
*                             KEYWORD
         BE    KPAREN1        YES, GO PROCESS
*
         CLC   ZERO(TRE,R9),KALL   IS IT 'ALL'
         BNE   KOTHRS         NO, GO CHECK OTHER PARAMS
***********************************************************************
*              AUTH=ALL
***********************************************************************
         OI    XACMDATH,KMASK3  SET CMD AUTH TO ALL            @Z30LPTJ
         LA    R9,TRE(R9)       UPDATE PTR TO END OF KEYWORD
         B     KSK1           GO CHECK END OF BUFFER
*
*
KOTHRS   EQU   *
         CLC   ZERO(FOR,R9),KINFO  IS IT AUTH=INFO
         BE    KINFOPRM       YES, GO PROCESS
         SR    R6,R6          SET EXTERIROR PARENS SWITCH OFF
KOTHRS2  EQU   *
         CLC   ZERO(TRE,R9),KSYS   NO,IS IT AUTH=SYS
         BE    KSYSPRM        YES, GO PROCESS
         CLC   ZERO(TWO,R9),KIO    NO,IS IT AUTH=10
         BE    KIOPRM         YES, GO PROCESS
         CLC   ZERO(FOR,R9),KCONS  NO,IS IT AUTH=CONS
*
         BE    KCONSPRM       YES, GO PROCESS
         B     KERR3          NO, ERROR
*
KINFOPRM EQU   *
***********************************************************************
*              AUTH=INFO
***********************************************************************
         LA    R9,FOR(R9)       PT TO END OF KEYWORD, SINCE AREA IS
         B     KSK1           ALREADY ZEROES, GO CHECK END OF SCAN
*
*
*              PROCESSING OF AUTH=(VALUES WITHIN)
*
KPAREN1  EQU   *
         LA    R6,ONE           SET EXTERIOR PAREN SWITCH
         LA    R9,ONE(R9)       UPDATE PTR TO PARM
         B     KOTHRS2        GO CHECK VALIDTY
*
KSYSPRM  EQU   *
         OI    XACMDATH,KGISYS  SET INDICATOR BIT FOR SYSTEM   @Z30LPTJ
         LA    R9,TRE(R9)       UPDATE TO END OF SYS
KENDPARM LTR   R6,R6          DID ( START FIELD
         BZ    KSK1           NO, GO CHECK END OF SCAN
         CLI   ZERO(R9),COMMA      YES,DOES COMMA FOLLOW
         BE    KOTHRS3        YES, GO CHECK OTHERS
         CLI   ZERO(R9),RTPAREN    NO, IS PAREN END KEYWORD
         LA    R9,ONE(R9)          UPDATE PTR
         BE    KSK1           YES, GO CHECK END OF SCAN
         B     KERR3          NO - ERROR
*
KIOPRM   EQU   *
         OI    XACMDATH,KG2IO      SET INDICATOR BIT FOR IO    @Z30LPTJ
         LA    R9,TWO(R9)       UPDATE TO END OF PARM
         B     KENDPARM       GO CHECK IF END OF KEYWORD
*
KCONSPRM EQU   *
         OI    XACMDATH,KG3CONS    SET INDICATOR BIT FOR CONS  @Z30LPTJ
         LA    R9,FOR(R9)          SET PTR TO END OF PARM
         B     KENDPARM       GO CHECK IF END OF KEYWORD
*
KOTHRS3  EQU   *
         LA    R9,ONE(R9)          UPDATE PTR
         B     KOTHRS2        GO CHECK IF ANY MORE
*
         EJECT
***********************************************************************
*              VALUES FOR ROUT=
***********************************************************************
K2       EQU   *
         LA    R9,SIX(R9)       UPDATE TO PARM
         CLI   ZERO(R9),LEFPAREN   ARE THERE INDIVIDUAL ROUTCODES
         BE    KPAREN2        YES, GO PROCESS
         CLC   ZERO(TRE,R9),KALL   ARE ALL RTCDS TO BE ASSIGNED
         BNE   KK2            NO, GO CHECK FOR OTHERS
         OI    XASWITCH,XAROUT        ROUT CODES TO BE CHANGED @ZA12703
         MVC   XARTCODE(TWO),KFFFF   SET ALL ROUTING CODES     @Z30LPTJ
         LA    R9,TRE(R9)       UPDATE TO END OF KEYWORD PARM
         B     KSK1           GO CHECK END OF SCAN
*
KK2      EQU   *
         CLC   ZERO(FOR,R9),KNONE  ARE THERE NO RTCDS TO BE ASSIGNED
         BNE   KK3            NONE IS NOT THE PARM, GO CHECK AGAIN
         OI    XASWITCH,XAROUT    ROUT CODES TO BE CHANGED     @ZA12703
         SR    R0,R0              CLEAR REGISTER               @ZA12703
         STH   R0,XARTCODE        NO ROUTE CODES WANTED        @ZA12703
         LA    R9,FOR(R9)       UPDATE PTR TO END OF PARM
         B     KSK1           ..
KK3      EQU   *
         SR    R6,R6          SET EXTERIOR PAREN TO AFF
         B     KPAR1          GO PROCESS RT CODE
*
KPAREN2  EQU   *
         LA    R9,ONE(R9)       UPDATE TO PASS THE PAREN
         LA    R6,ONE           SET EXT PAREN SWITCH
*
KPAR1    EQU   *
         XC    XAV(EIT),XAV       CLEAR AREA FOR CVB OF RT CODES Y02669
         CLI   ONE(R9),COMMA       ONE DIGIT NUM FOLLOWED BY COMMA
         BE    KPAR6               YES,GO PROCESS
         CLI   ONE(R9),RTPAREN     END OF PARM
         BE    KPAR1B              YES, ONE DIGIT UNIT ONLY
         CLI   TWO(R9),COMMA       TWO DIGIT NUM FOLLOWED BY COMMA
         BE    KPAR2          YES
KPAR1B   EQU   *
         LTR   R6,R6          EXTERIOR PAREN SWITH SET
         BZ    KPAR8          NO, ERROR
         CLI   ONE(R9),RTPAREN     YES,ONE DIGIT NUM FPLLOWED BY ')'
         BE    KPAR6          YES, PROCESS
         CLI   TWO(R9),RTPAREN     TWO DIGIT NUM FOLLOWED BY ')'
KPAR1A   EQU   *                   ..
         BNE   KERR3          NO, ERROR
*
KPAR2    EQU   *
         CLI   ZERO(R9),F1        SYNTAX CHECK THE DIGIT USED    A33219
         BNE   KERR6              AS THE ROUTING CODE. THE 1ST   A33219
         CLI   ONE(R9),F0         DIGIT MUST BE 1. THE 2ND       A33219
         BL    KERR6              DIGIT CAN BE 0 THROUGH 6       A33219
         CLI   ONE(R9),F6         INCLUSIVE. IF NOT THEN AN      A33219
         BH    KERR6              ERROR CONDITION HAS BEEN       A33219
*                                 REACHED.                       A33219
         PACK  XAV+SIX(TWO),ZERO(TWO,R9)  RTCODE IS 2 DIGITS     Y02669
         LA    R9,TWO(R9)           INDEX PAST DIGITS
KPAR3    LA    R13,SIXTN           MAXIMUM VALUE ALLOWED         Y02669
         CVB   R4,XAV         CONVERT PACKED DECIMAL             Y02669
         LTR   R4,R4          IS IT ZERO
         BZ    KERR6               YES, ILLEGAL                   19084
         CR    R4,R13         IS IT 16
         BE    KPAR9               YES, NO ROUT CODE TO BE SET    19084
         BH    KERR6               HIGH, ILLEGAL                  19084
*
         BCTR  R4,ZERO             SET TO ZERO BASE
         OI    XASWITCH,XAROUT    ROUT CODES TO BE CHANGED     @ZA12703
         LA    R3,ONE              SET COUNTER TO ONE FOR SHIFT
         SLL   R3,FIFTN            SET FIRST BIT OF MASK ON
         SRL   R3,ZERO(R4)         MOVE TO APPROPRIATE RT CODE BIT
         O     R3,XACMDATH         SET BIT IN INDICATOR AREA   @Z30LPTJ
         ST    R3,XACMDATH          AND SAVE IN XSA            @Z30LPTJ
*
KPAR5    EQU   *
         LTR   R6,R6          EXTERIOR PARENS SET
         BZ    KSK1                NO, GO CHECK END OF SCAN       19084
         CLI   ZERO(R9),RTPAREN    YES,CHECK IF ')' PRESENT
         LA    R9,ONE(R9)          YES, UPDATE PAST PARENS
         BNE   KPAR1          NO, MORE ROUT CODES, PROCESS
         B     KSK1                 GO TO EOB ROUTINE
*
KPAR6    EQU   *
         CLI   ZERO(R9),F0        SYNTAX CHECK THE DIGIT USED AS A33219
         BL    KERR6              THE ROUTCODE. IT MUST BE       A33219
         CLI   ZERO(R9),F9        0 THROUGH 9 SINCE ROUTCODE IS  A33219
         BH    KERR6              JUST 1 CHARACTER.              A33219
         PACK  XAV+SVN(ONE),ZERO(ONE,R9)     ROUT CODE=ONE DIGIT Y02669
         LA    R9,ONE(R9)                    INDEX PAST DIGIT
         B     KPAR3          GO TO PROCESS
*
KPAR8    EQU   *
         CLI   ONE(R9),BLK         BLANK MAY FOLLOW SINGLE DIGIT
         BE    KPAR6               YES, GO PROCESS
         CLI   TWO(R9),BLK         BLK MAY FOLLOW TWO DIGITS
         B     KPAR1A              IS NOT - ERROR, IF YES - PROCESS
*
KPAR9    EQU   *                                                  19084
         LTR   R6,R6               EXTERIOR PARENS SET            19084
         BNZ   KPAR5               YES, GO CONTINUE ROUTS         19084
         B     KSK1                GO CHECK IF OTHER KEYWORDS     19084
         EJECT
*
*              DETERMINATION OF VALID UNIT SPECIFIED
*
KUNTCK   EQU   *
         LM    R13,R15,UCMVEA R13 PTR TO THE 1ST UCM ENTRY
*                             R14 HAS LENGTH OF UCM ENTRY
*                             R15 HAS PTR TO LAST UCM ENTRY
         USING UCMLIST,R13
KUNTCK1  EQU   *
         L     R4,UCMUCB      PT TO UCM ASSOCIATED WITH UCME
         USING UCB,R4         REFERENCE TO UCB
         CLC   ZERO(TRE,R10),UCBNAME    COMPARE PHYSICAL UNIT ADDRESS
         BE    KSU1           FOUND -
         BXLE  R13,R14,KUNTCK1     NOT FOUND, CONTINUE CHECK FOR UNIT
         DROP  R4,R13         UNTIL EXHAUST UCME'S
         B     KERR5               ERROR                          19084
*  UNIT GIVEN IN COMMAND IS VALID (HAS UCM ENTRY)
*
KSU1     EQU   *
         LA    R13,ZERO(R13)     CLEAR HIGH ORDER BYTE
         BR    R11            RETURN
*
         EJECT
***********************************************************************
*
* TRANSFER OF CONTROL TO VARIOUS MODULES DETERMINATION
*
***********************************************************************
*
* ERROR CODE HANDLING
*
KERR3    BAL   R5,KERREXIT    ERROR CODE =9
         DC    AL1(ERR9)           ERROR CODE IS 9
*
KERR5    LR    R9,R10                                             19084
KERR4    MVC   XAV(TRE),ZERO(R9)   INSERT CHARS OF UNIT
         TM    XASOPCOD,XALTCON    WAS COMP. CONS SPECIFIED?   @Z30LPTJ
         BO    KERR4C              YES,GO MOVE IN O-UNIT          M0128
         MVI   XAV+TRE,BLK         PAD WITH BLANKS
KERR4R   EQU   *                                                  M0128
         BAL   R5,KERREXT2         GO TO EXIT ROUTINE            Y02669
         DC    AL1(ERR13)          ERROR CODE IS 13
KERR4C   DS    0H                                                 M0128
         MVI   XAV+TRE,COMMA       MOVE IN COMMA TO DELIM I-UNIT  M0128
         MVC   XAV+FOR(TRE),SIX(R9) MOVE IN O-UNIT                M0128
         XI    XASOPCOD,XALTCON    TURN OFF COMP. SPEC. SWITCH @Z30LPTJ
         B     KERR4R              GO TO PROCESS ERROR MSG        M0128
*
* CONTROL TRANSFERING ROUTINE
KERR7    DS    0H                                                 21002
         SPACE 5
**************************************************               Y02651
*                                                                Y02651
*  THIS A TERMINAL ERROR EXIT AND THEREFORE A DEQ WILL           Y02651
*  BE ISSUED BEFORE THE EXIT TO THE ERROR MSG MOD IS TAKEN       Y02651
*  THE ENQ WAS ISSUED IN MODULE IEE3603D                         Y02651
*                                                                Y02651
         BAL   R4,KDEQUE                                         Y02651
*                                                                Y02651
**************************************************               Y02651
         SPACE 5
         L     R13,XAK            GET SAVEAREA POINTER         @Z30LPTJ
         STM   R0,R13,ZERO(R13)   STORE REGS                     Y02669
         MVI   XAE,ERR29               INVALID AUTHORITY          21002
         MVC   XAX+TRE(TWO),KERMSG2    IGG2103D                  Y02669
         MVC   XAV(EIT),VARY                                     Y02669
         LOAD  EP=IEE2103D                                       Y02669
         LR    R15,R0             SAVE RTN'S ADDRESS IN REG15    Y02669
         BALR  R14,R15            GO TO MESSAGE RTN              Y02669
* ON RETURN                                                      Y02669
         LM    R0,R15,ZERO(R13)   RESTORE REGS                   Y02669
         ST    R13,XAK            RESTORE SAVEAREA PTR TO XSA  @Z30LPTJ
         DELETE EP=IEE2103D                                      Y02669
KEXIT    EQU   *                                                 Y02669
         L     R1,D64(R13)        GET CSCB PTR                   Y02669
         USING CHAIN1,R1                                         Y02669
         OI    CHSTS,CHFC         SET TO FREE CSCB               Y02669
         MGCR  (1),CHAIN                                         Y02669
         LR    R4,R14             SAVE RETURN ADDRESS            Y02669
*                                 FREE SAVE AREA GOTTEN IN 3303D Y02669
         LA    R0,D132            SIZE OF DUMMY XSA AND SAVE AREAY02669
         LR    R1,R2              PTR TO DUMMY XSA               Y02669
         FREEMAIN R,LV=(0),A=(1)                                 Y02669
*                                 FREE DUMMY XSA GOTTEN IN 3603D Y02669
         LR    R14,R4             RESTORE RETURN REG             Y02669
         BR    R14                RETURN                         Y02669
*                                                                Y02669
KERREXIT EQU   *                                                 Y02669
         MVC   XAV(EIT),VARY                                     Y02669
KERREXT2 EQU   *                                                 Y02669
         SPACE 5                                                 Y02669
**************************************************               Y02651
*                                                                Y02651
*  THIS A TERMINAL ERROR EXIT AND THEREFORE A DEQ WILL           Y02651
*  BE ISSUED BEFORE THE EXIT TO THE ERROR MSG MOD IS TAKEN       Y02651
*  THE ENQ WAS ISSUED IN MODULE IEE3603D                         Y02651
*                                                                Y02651
         BAL   R4,KDEQUE                                         Y02651
*                                                                Y02651
**************************************************               Y02651
         SPACE 5
         L     R13,XAK            SAVE SAVEAREA PTR FROM 2103D @Z30LPTJ
         STM   R0,R13,ZERO(R13)   STORE REGS                     Y02669
*        0-13, DO NOT CLOBBER R14 AREA STORED BEFORE, SINCE      Y02669
*        VALUE OF R14 HAS CHANGED SINCE THEN                     Y02669
         MVC   XAE(ONE),ZERO(R5)   INSERT ERROR CODE
         MVC   XAX+TRE(TWO),KERMSG     IGC0503D                   21002
         LOAD  EP=IEE0503D                                       Y02669
         LR    R15,R0             SAVE RTN'S ADDRESS IN REG15    Y02669
         BALR  R14,R15            GO TO 0503D                    Y02669
*  ON RETURN                                                     Y02669
         LM    R0,R15,ZERO(R13)   RESTORE REGS                   Y02669
         DELETE EP=IEE0503D                                      Y02669
         B     KEXIT               GO TO EXIT RTN
*                                                                 19084
KERR6    EQU   *                                                  19084
         BAL   R5,KERREXIT         TO 0503D                       19084
         DC    AL1(ERR6)           INVALID NUMERICS               19084
         EJECT
*****************************************************************Y02651
*                                                                Y02651
*  THIS THE DEQ SUBROUTINE                                       Y02651
*  THE ENQ WAS ISSUED IN MODULE IEE3603D                         Y02651
         DS    0F                                                Y02651
KDEQUE   EQU   *                                                 Y02651
         DEQ   MF=(E,IEFDEQUE)                                   Y02651
         BR    R4                                                Y02651
*****************************************************************Y02651
         EJECT
*****************************************************************Y02651
*                                                                Y02651
*  THE LIST FORM OF THE DEQ MACRO INSTRUCTION FOLLOWS:           Y02651
*                                                                Y02651
IEFDEQUE DEQ   (IEFSQCBS,IEFQ4QCB,TWO,SYSTEM,IEFSQCBS,IEFVQCBS,SVN,    *
               SYSTEM),RET=HAVE,MF=L                           @YM5443P
IEFVQCBS DC    C'VARYDEV'
IEFSQCBS DC    C'SYSIEFSD'                                       Y02651
IEFQ4QCB DC    C'Q4'                                             Y02651
*****************************************************************Y02651
         EJECT
***********************************************************************
*
* CONSTANTS USED 34 SCAN AND ROUTING ROUTINE -
*
***********************************************************************
KFFFF    DC    X'FFFF'             MASK FOR ALL ROUT CODES
*
KERMSG   DC    C'05'                   IEE0503D                  Y02669
KERMSG2  DC    C'21'                  IEE2103D                   Y02669
KCONSLE  DC    C'42'             CONSOLE,ONLINE,OFFLINE          Y02669
         DS    0F                                                Y02669
VCON42   DC    V(IEE4203D)                                       Y02669
*
* CHARACTER STRINGS USED TO DETERMINE KEYWORDS
KAUTH    DC    C'AUTH='            KEYWORD TO CHANGE CMD AUTH
KROUT    DC    C'ROUT='            KEYWORD TO CHANGE ROUT CDS
KALT     DC    C'ALTCONS='         KEYWD TO CHNG ALTER.CONSL.
* CHARACTER STRINGS USED TO DETERMINE KEYWORD VALUES
KALL     DC    C'ALL'              CHECK FOR ALL AUTH,RTCDS
KINFO    DC    C'INFO'             CHECK FOR AUTH
KSYS      DC    C'SYS'             CHECK FOR AUTH=
KIO      DC    C'IO'               CHECK FOR AUTH=
KCONS    DC    C'CONS'             CHECK FOR AUTH=
KNONE    DC    C'NONE'             CHECK FOR ROUT=
IUNIT    DC    C'I-'                   (I-XXX,0-XXX)
OUNIT    DC    C',O-'              O-XXX
VARY     DC    C'VARY    '                                       Y02669
         SPACE 5
PATCH    DC    CL65'***IEE4403D***'                            @Z30LPTJ
         EJECT
SUCM     DSECT
         IEECUCM   FORMAT=NEW
         EJECT
SETXSA   DSECT
         IEEXSA
         EJECT
SETCVT   DSECT
         CVT   SYS=VMS
UCB      DSECT
         IEFUCBOB
         EJECT
CHAIN1   DSECT
         IEECHAIN
         END
