 TITLE 'IEE4203D - UCME SCANNER FOR VARY ONLINE/OFFLINE/CONSOLE'
IEE4203D CSECT
* A 121900,361400-362150,363500,477700                         @YM07231
* C 046984-046986,050993-051053,191400-191700,192084-192086,   @YM07231
* C 344700,344900,439200                                       @YM07231
* C 312700                                                     @ZM30481
* D 326980-327480                                              @ZM30481
***********************************************************************
*
* MODULE NAME      = IEE4203D
*   CSECT NAME     = IEE4203D
*
* LOAD MODULE NAME = IEE3603D
*
* DESCRIPTIVE NAME = UCME SCANNER FOR VARY ONLINE/OFFLINE/CONSOLE
*
* COPYRIGHT        = MISSING
*
* STATUS           = OS/VS/2 RELEASE 3                         @Z30LPTJ
*
* FUNCTION         = TO CONTINUE SYNTAX VALIDATION OF THE COMMAND
*
* OPERATION        = CONSOLE KEYWORD:
*                       . VALIDATE THAT UNIT SPECIFIED HAS A UCME
*                       . VALIDATE THAT AN O-XXX STANDS FOR AN
*                            OUTPUT ONLY UNIT
*                       . VALIDATE THAT A (I-XXX,O-XXX) IS A VALID
*                            COMPOSITE CONSOLE UNIT
*                       . VALIDATE THAT A XXX UNIT HAS I/O
*                            CAPABILITIES
*                       . CHECK TO SEE IF UNIT IS CURRENTLY BUSY
*                            CHANGING STATUS
*                       . VALIDATE THAT THE COMMAND AUTHORITY OF THE
*                            ISSUER IS 3
*                       .VALIDATE THAT IF THE ALTCONS PARAMETER
*                            WAS USED, THAN THE ALTERNATE UNIT IS NOT
*                            SAME AS THE CONSOLE ITSELF, OR THAT AN
*                            OUTPUT ONLY UNIT IS NOT AN ALTERNATE TO
*                            AN I/O CONSOLE.
*                       .INDICATE IF ANY CONSOLES ARE GRAPHICS
*                    ONLINE/OFFLINE KEYWORDS:
*                       . CHECK IF CONSOLE UNIT OR REGULAR UNIT
*                            IS BEING CHANGED. THIS IS DONE BY
*                            TRYING TO FIND A UCME FOR THE UNIT
*                       . VALIDATE THAT AN O-XXX STANDS FOR AN
*                            OUTPUT ONLY UNIT
*                       . VALIDATE THAT A (I-XXX,O-XXX) IS A VALID
*                            COMPOSITE CONSOLE UNIT
*                       . VALIDATE THAT A XXX UNIT HAS I/O
*                            CAPABILITIES
*                       . CHECK TO SEE IF UNIT IS CURRENTLY BUSY
*                            CHANGING STATUS
*                       . VALIDATE THE COMMAND AUTHORITY OF THE
*                            ISSUER:
*                                 IF UNIT HAS UCME - MUST BE 3 -
*                                 IF UNIT DOES NOT HAVE UCME -2-
*                       . MAKE SURE THE ONLINE/OFFLINE COMMAND IS
*                            NOT FOR THE MASTER CONSOLE OF THE
*                            HARDCPY DEVICE.
*
* NOTES
*   DEPENDENCIES   = NONE
*   CHARACTER/CODE DEPENDENCIES
*                  = THIS MODULE CAN BE ASSEMBLED WITH ANY NEW
*                    CHARACTER/CODE SET
*
* RESTRICTIONS     = REG2 PTS TO THE DUMMY XSA ON INPUT AND ON OUTPUT
*   REGISTER CONVENTIONS
*                  = REGS ARE DEFINED AT LABEL: DEFREG
*   PATCH LABEL    = PATCH
*
* MODULE TYPE      = CSECT
*   PROCESSOR      = ASSEMBLER
*   MODULE SIZE    = VS/2 = '4C6'                              @ZM30481
*   ATTRIBUTES     = REENTRANT, REUSABLE, REFRESHABLE, PAGED LPA,
*                    ZERO PSW PROTECT KEY
*
* ENTRY POINT      = IEE4203D FROM IEE3303D OR IEE2303D OR IEE4403D
*   PURPOSE        = TO CONTINUE SCANNING THE VARY CONSOLE/ONLINE/
*                    OFFLINE COMMAND FOR ERRORS
*   LINKAGE        = BRANCH
*   INPUT          = REG2 PTS TO THE DUMMY XSA
*                    XAD HAS THE FLAGS DETERMINED BY 3303D SUCH AS
*                       WHETHER MULTIPLE UNITS OR A SINGLE UNIT
*                       ARE PRESENT IN THE COMMAND
*                    XAD+2 CONTAINS THE ISSUER COMMAND AUTHORITY
*                    XAR CONTAINS THE SIZE OF THE COMMAND BUFFER
*                    XAL PTS TO THE COMMAND PARAMETERS
*                    XAV HAS THE VERB (VARY)
*                    XAU HAS THE UCMID OF THE ISSUER
*                    SAVE AREA WORD 17 CONTAINS THE CSCB PTR
*   REGISTERS SAVED= ALL
*   REGISTER USAGE =    R0   WTO - CONSOLE ID
*                       R2   DUMMY XSA PTR
*                       R3   ADDRESSABILITY
*                       R4   UCME PTR
*                       R5   UCME PTR
*                       R7   EOB REG
*                       R8   PROCESS BITS
*                       R9   UNIT PTR
*                       R10  UCM REG
*                       R11  UCMPRFX REG
*                       R12  PROCESS BITS REG
*                       R13  SAVE AREA PTR
*   REGISTERS RESTORED
*                  = ALL
*
* EXITS - NORMAL-1-  = TO IEE4903D VIA BRANCH
*   CONDITIONS     = VARY CONSOLE COMMAND
*   OUTPUT         = REG2 PTS TO THE DUMMY XSA
*                    XAD HAS THE SWITCHES SUCH AS:
*                       MULTIPLE OR SINGLE UNIT IN COMMAND
*                    AND COMMAND AUTHORITY OF ISSUER
*                    XAR HAS THE PROCESSING BITS SHOWING:
*                       ON - PROCESS THIS UNIT
*                       OFF- DO NOT PROCESS THIS UNIT
*                    REG4 HAS A GRAPHICS INDICATOR
*                    REG 13 PTR TO THE SAVE AREA
*   RETURN CODES   = NONE
*
* EXITS - NORMAL-2-  = TO IEE4603D VIA BRANCH
*   CONDITIONS     = V ONLINE/OFFLINE OF CONSOLE UNITS
*   OUTPUT         = REG2 PTS TO THE DUMMY XSA
*                    XAD HAS SWITCHES SUCH AS:
*                       MULTIPLE OF SINGLE UNIT IN COMMAND
*                       WHETHER A REGULAR (I/O) UNIT IS PRESENT
*                    XAR HAS THE PROCESSING BITS SHOWING:
*                       ON - PROCESS THIS UNIT
*                       OFF- DO NOT PROCESS THIS UNIT
*                    REG 13 PTR TO THE SAVE AREA
*   RETURN CODES   = NONE
*
* EXITS - NORMAL-3-  = TO IEE3103D VIA BRANCH
*   CONDITIONS     = ONLINE/OFFLINE FOR ONLY I/O UNITS
*   OUTPUT         = REG2 PTS TO THE DUMMY XSA
*                    XAD HAS THE SWITCHES SUCH AS:
*                       MULTIPLE OR SINGLE UNIT IN COMMAND
*                       I/O UNITS PRESENT TO PROCESS
*                    XAR HAS THE END OF BUFFER POINTER
*                    REG13 PTS TO THE SAVE AREA
*   RETURN CODES   = NONE
*
* EXIT - NORMAL-4- = RETURN TO CALLER VIA A BR 14
*    CONDITIONS    = WHEN NO VALID UNITS ARE LEFT IN THE CMD.
*    OUTPUT        = COMMAND WAS REJECTED AND AN ERROR MSG
*                    WAS SENT TO THE OPERATOR.
*                    THE CSCB IS FREED VIA AN MGCR MACRO AND
*                    DUMMY XSA GOTTEN BY 3603D AND THE SAVE
*                    AREA GOTTEN BY 3603D ARE FREED.
*    RETURN CODES  = NONE
*
* EXIT - NORMAL-5- = TO IEECB904 VIA BRANCH                    @Z30LPTJ
*   CONDITIONS     = WHEN NO VALID UNITS ARE LEFT IN THE       @Z30LPTJ
*                    COMMAND AND VARY RANGE IS BEING PROCESSED @Z30LPTJ
*   OUTPUT         = REGISTER 2 POINTING TO THE XSA WHICH HAS  @Z30LPTJ
*                    THE XAL POINTING TO THE VARY RANGE WORK   @Z30LPTJ
*                    AREA                                      @Z30LPTJ
*   RETURN CODES   = NONE                                      @Z30LPTJ
*
* EXITS - ERROR    = NONE, ALL ERROR CONDITIONS ARE HANDLED
*                    INTERNALLY
*
* EXTERNAL REFERENCES
*   ROUTINES       = IEE4903D
*                    IEE4603D
*                    IEE3103D
*                    IEECB904                                  @Z30LPTJ
*   DATA AREAS     = SEE OUTPUT - DATA
*
* CONTROL BLOCKS   = 1. DUMMY EXTENDED SAVE AREA (XSA)
*                    2. CVT
*                       FIELDS TESTED  - CVTCUCB
*                       FIELDS UPDATED - NONE
*                    2. UNIT CONTROL MODULE (UCM)
*                       FIELDS TESTED  - UCMATR - BIT= UCMIF
*                                                       UCMOF
*                                        UCMCOMPC
*                                        UCMDISP - BIT=UCMDISPA
*                                                       UCMDISPB
*                                                       UCMDISPC
*                                                       UCMDISPD
*                                        UCMID
*                                        UCMUCB
*                                        UCMVEA
*                                        UCMVEZ
*                       FIELDS UPDATED - NONE
*                    3. UNIT CONTROL BLOCK (UCB)
*                       FIELDS TESTED  - SRTESTAT - BIT = SRTEDADI
*                                        UCBNAME
*                                        UCBFL5    - BIT =UCBNALOC
*                       FIELDS UPDATED - NONE
*                    4. COMMAND SCHEDULING CONTROL BLOCK (CSCB)
*                       FIELDS TESTED  - NONE
*                       FIELDS UPDATED - CHSTS - BIT= CHFC
*
* TABLES           = NONE
*
* MACROS           = WTO,FREEMAIN,MGCR,DEQ
*
* MAPPING MACROS   = IEEXSA
*                    IEECUCM
*                    IEFUCBOB
*                    CVT
*                    IEECHAIN
*   SERIALIZATION  = NO PROTECTION FROM COMM TASK IS REQUIRED HERE
*                    THEREFORE NO LOCKS WILL BEE GOTTEN IN THIS RTN
*                    THIS MODULE HOWEVER EXECUTES PROTECTED FROM
*                    VARY/UNLOAD, ALLOCATION, AND SYSTEM       @YM07231
*                    COMPONENT VIA AN ENQ ISSUED IN IEE3603D.  @YM07231
*                    IF THIS MODULE FINDS THAT THE TOTAL COMMAND MUST
*                    BE ABORTED DUE TO AN ERROR CONDITION, BEFORE
*                    IT EXITS , IT ISSUED A DEQ AT LOC: UDEQUE
*
* CHANGE LEVEL     = Y02669, Y02651, Z30LPTJ, ZM30455, ZM30481 @ZM30481
*
* CHANGE ACTIVITY  = N/A
*
* MESSAGES         = 'IEE313I UNIT REF. INVALID'
*                       WHEN AN I-UNIT OF A COMPOSITE DOES NOT HAVE
*                       INPUT CAPABILITIES.
*                       WHEN THE UCME OF AN INPUT UNIT OF A COMPOSITE
*                       DOES NOT HAVE AN OUTPUT UCME PTR.
*                       WHEN THE OUTPUT UNIT OF A COMPOSITE AS SPECI-
*                       FIED IN THE COMMAND DOES NOT MATCH WITH THE
*                       OUTPUT UNIT SPECIFIED IN THE UCME OF THE COMP.
*                       WHEN AN OUTPUT ONLY UNIT IS NOT REALLY AN
*                       OUTPUT ONLY AS SPECIFIED BY THE UCME.
*                       WHEN AN I/O CONSOLE UNIT DOES NOT HAVE INPUT/
*                       OUTPUT CAPABILITIES AS SHOWN BY ITS UCME.
*                       WHEN THE UNIT SPECIFIED HAS NO UCME BUT THE
*                       VARY COMMAND IS A VARY CONSOLE, OR THE
*                       UNIT SPECIFICATION IS A COMPOSITE OR AN
*                       OUTPUT ONLY SPECIFICATION.
*                       WHEN A VARY ONLINE/OFFLINE COMES IN FOR
*                       THE MASTER CONSOLE UNIT.
*                    'IEE345I AUTHORITY INVALID'
*                       WHEN A VARY CONSOLE IS ISSUED FROM CONSOLE
*                       WHOSE AUTHORITY IS NOT 3
*                       WHEN A VARY ONLINE/OFFLINE IS ISSUED FROM
*                       CONSOLE WHOSE AUTHORITY IS NOT 3 OR 2
*                    'IEE299I REQ'D FOR HARDCPY'
*                       WHEN A VARY ONLINE/OFFLINE IS ISSUED FOR A
*                       DEVICE WHICH IS THE HARDCPY LOG.
*                    ' IEE300I INVALID ALTCON'
*                       WHEN THE ALTERNATE CONSOLE SPECIFIED
*                       DOES NOT HAVE I/O CAPABILITIES BUT THE
*                       PRIMARY CONSOLE DOES.
*                       WHEN THE ALTERNATE CONSOLE SPECIFIED
*                       IS THE SAME AS THE PRIMARY CONSOLE.
*                    'IEE339I CHANGING STATUS'
*                       WHEN A VARY COMMAND HAS BEEN ISSUED BUT THE
*                       DEVICE IS CHANGING FROM ITS STATE TO SOME
*                       OTHER STATE AND CANNOT BE USED NOW.
*                    'IEE329I XXX IN USE BY A SYSTEM COMPONENT'@YM07231
*                       WHEN THE DEVICE TO BE VARIED IS IN USE @YM07231
*                       BY A SYSTEM COMPONENT AND CAN NOT      @YM07231
*                       CHANGE STATUS.                         @YM07231
*
* ABEND CODES      = NONE
*
* PACKAGING        = IEE4203D WILL BE PART OF LOAD MODULE
*                       IEE3603D
*                    THIS LOAD MODULE ALIASES ARE
*                       NONE
*                    THIS LOAD MODULE ENTRY POINTS ARE
*                       IEE3603D
*                    MODULE WILL RESIDE IN:  LPALIB
*
* SYSGEN           = SUPPLIED BY SECTIONS:
*                       LOAD MODULE NAME
*                       PACKAGING
*                    THIS CSECT WILL BE INCLUDED AT SYSGEN FROM
*                    DLIB -AOSB3- BY MACRO -SGIEF441-
*
* SYSTEM LIBRARIES = NONE
*
***********************************************************************
         EJECT
DEFREG   EQU   *           REGISTER EQUATES                      Y02669
R0       EQU   0                   WTO-CONSOLE ID                 19084
R1       EQU   1                   INPUT AND OUTPUT-XAD           19084
R2       EQU   2                   XSA                            19084
R3       EQU   3                   ADDRESS                        19084
R4       EQU   4                   UCME                          Y02669
R5       EQU   5                   UCME PTR                      Y02669
R6       EQU   6                                                  19084
R7       EQU   7                   EOB REG                        19084
R8       EQU   8                   PROCESS BITS                  Y02669
R9       EQU   9                   UNIT PTR                       19084
R10      EQU   10                  UCM REG                        19084
R11      EQU   11                  UCMPFX REG                     19084
R12      EQU   12                  PROCESS BITS                   19084
R13      EQU   13                  SAVE AREA PTR                 Y02669
R14      EQU   14
R15      EQU   15                                                 19084
*                                                                 19084
         SPACE 5                                                  19084
ZERO     EQU   0                                                  19084
ONE      EQU   1                                                  19084
TWO      EQU   2                                                  19084
TRE      EQU   3                                                  19084
FOR      EQU   4                                                  19084
FIV      EQU   5                                                  19084
SIX      EQU   6                                                  19084
SVN      EQU   7                                                  19084
EIT      EQU   8                                                  19084
ELEV     EQU   11                                                 19084
SIXTN    EQU   16                                                Y02669
TWENTY   EQU   20                                                Y02669
TWENTY2  EQU   22                                                Y02669
TWENTY4  EQU   24                                                 19084
TWENTY8  EQU   28                                              @YM07231
THIRTY1  EQU   31                                                 19084
EASY     EQU   14                      BRANCH NOT ON              21002
D64      EQU   64                                                Y02669
D132     EQU   132                                               Y02669
*                                                                 19084
         SPACE 10                                                 19084
*        CHARACTER AND HEXADECIMAL CONSTANTS                      19084
URI      EQU   X'80'                                              19084
OUNCOMP  EQU   X'06'                                              19084
RTC2     EQU   X'40'                                              19084
CA2      EQU   X'40'                                              19084
CA3      EQU   X'20'                                              19084
*                                                                 19084
SLASH    EQU   C'/'                                               19084
COMMA    EQU   C','                                               19084
VARY     EQU   C'V'                                               19084
GRPX     EQU   X'FF'                   GRAPHICS INDICATOR         21002
         EJECT
***********************************************************************
****  UCME SCANNER FOR ONLINE - OFFLINE - CONSOLE                 19084
***********************************************************************
*                                                                 19084
*                                                                 19084
*        ADDRESSABILITY AND HOUSEKEEPING                          19084
         BALR  R3,ZERO                                            19084
         USING *,R3                                               19084
         MODID BR=YES                                            Y01886
         USING XSA,R2                                             19084
*
         STM   R0,R15,ZERO(R13)                                  Y02669
         L     R9,XAL              PTR TO PARM                    19084
         LA    R9,ZERO(R9)         CLEAR HIGH ORDER BYTE          19084
         SR    R12,R12             ZERO OUT UNIT- REG             19084
         LA    R8,ONE              TURN UNIT DETERMINING ---     Y02669
         SLL   R8,THIRTY1          REGISTER                      Y02669
*                                                                 19084
***********************************************************************
******** START SCAN                                               19084
***********************************************************************
*                                                                 19084
         TM    XADUSWIT,XAXPAREN   MULTIPLE UNITS              @Z30LPTJ
         BZ    UCOMP2              NO,NO XTERIOR PARENS           19084
UCOMP    LA    R9,ONE(R9)          YES, INDEX TO UNIT             19084
UCOMP2   EQU   *                                                  19084
         USING UCMLIST,R5                                         19084
         L     R11,CVTPTR          CVT PTR                        19084
         USING UCVT,R11                                           19084
         L     R10,CVTCUCB         UCM PTR                        19084
         DROP  R11                                                19084
         USING UUCB,R11                                           19084
         USING UCM,R10            XAS HAS KEY SWITCHES FROM 3303DY02669
         NI    XADUSWIT,XAXPAREN+XAUDVC  LEAVE XPAREN AND IO   @Z30LPTJ*
                                           SWITCHES            @Z30LPTJ
         SR    R4,R4               COMPSOTE/NOCOMP SWITCH         19084
         CLC   ZERO(TRE,R9),UIUNIT (I- FOR COMPOSITE              19084
         BNE   UOUSCAN             NO, CHECK IF O-UNIT SPECIFIED  19084
         LA    R9,TRE(R9)          YES, INDEX TO UNIT             19084
         OI    XADUSWIT,XAIOCOMP    TURN COMP SWITCH ON        @Z30LPTJ
         BAL   R15,UCMEFND         GO GET UCME                    19084
         TM    UCMATR,UCMIF        IS THIS AN I-UNIT              19084
         BZ    UREFINV             NO, UCME NOT FOR I-UNIT        19084
         L     R4,UCMCOMPC         GET PTR TO OUTPUT COMPOSITE    19084
         LTR   R4,R4               ANY                            19084
         BZ    UREFINV             NO, NO COMPOSITE               19084
         DROP  R5                                                 19084
         USING UCMLIST,R4                                         19084
         DROP  R11                                                19084
         L     R6,UCMUCB           GET PTR TO COMPOSITE'S UCB     19084
         USING UUCB,R6                                            19084
         CLC   SIX(TRE,R9),UCBNAME IS THIS THE LOGICAL COMPOSITE  19084
         BNE   UREFINV             NO, ERROR                      19084
         DROP  R4                                                 19084
         DROP  R6                                                 19084
         USING UCMLIST,R5          ADDR TO I-UNIT UCME            19084
         USING UUCB,R11                                           19084
*                                  R5 HAS PTR TO COMP UCME        19084
UACTVCK  EQU   *                                                  19084
*                                                                 19084
** UCB  *CHECK UCB BIT HERE TO SEE IF PENDING TO CHANGE STATUS-(UCHNG)
*        IF SO ISSUE -CHANGING STATUS MSG                         19084
*                    -LEAVE PROCESS-BIT OFF                       19084
         TM    SRTESTAT,SRTEDADI   IS DEVICE CHANGING STATUS      19084
         BO    UCHNG               YES,CHANGING STATUS MESSAGE    19084
         TM    UCBFL5,UCBNALOC    DEVICE IN USE BY A SYSTEM    @YM07231*
                                   SYSTEM COMPONENT?           @YM07231
         BO    COMPMSG            YES, REJECT AND ISSUE MESSAGE@YM07231
         TM    XADUSWIT,XAIOCOMP   COMP SPECIFIED              @Z30LPTJ
         BZ    CONTTST            NOPE                           Y02651
         DROP  R11                                               Y02651
         USING UUCB,R6                                           Y02651
         TM    SRTESTAT,SRTEDADI  IS THE O UNIT CHANGING STATUS  Y02651
         BO    UCHNG              YEP, NO GOOD, MESSAGE ISSUED   Y02651
         TM    UCBFL5,UCBNALOC    IS THE O-UNIT IN USE BY A    @YM07231*
                                   SYSTEM COMPONENT?           @YM07231
         BO    COMPMSG            YES, REJECT AND ISSUE MESSAGE@YM07231
         DROP  R6                                                Y02651
         USING UUCB,R11                                          Y02651
CONTTST  EQU   *                                                 Y02651
         SPACE 10                                                 19084
*        IF UCB DOES NOT SHOW IT IS CHANGING                      19084
*                                                                 19084
         TM    XACAHOLD,CA3       SINCE ITS CONSOLE,CA3 REQ    @Z30LPTJ
         BZ    UAUTHINV            AUTHORITY IS INVALID           19084
         TM    XASOPCOD,XACON      CONSOLE KEYWORD             @Z30LPTJ
         BZ    UOFON               NO,ONLINE OR OFFLINE           19084
*                                  YES, CONSOLE KEY               19084
***********************************************************************
*                                                                 19084
******** CONSOLE KEYWORD                                          19084
*                                                                 19084
***********************************************************************
         TM    XASWITCH,XALTCHG    ALTCONS TO BE CHANGED       @Z30LPTJ
         BZ    UBITON              NO,OMIT ALTCONS VALIDATION     19084
         TM    XASOPCOD,XALTCOM    IS ALT-IS-O-UNIT SWITCH ON  @Z30LPTJ
         BZ    UCKID               NO,GO CHECK IF ALT IS SAME AS CONSL
         TM    UCMDISP,UCMDISPD    IS THIS AN OUTPUT ONLY CONSOL  19084
         BZ    UINVALTC            INVALID ALTCONS MSG            19084
UCKID    EQU   *                   YES,OK,                        19084
         CLC   UCMID(ONE),XALTPTR  ARE THE CONS AND THE ALTC EQUAL19084
         BE    UINVALTC            NO GOOD,INVALID ALTCONS MSG    19084
*                                                                 19084
UBITON   EQU   *                                                  19084
         OR    R12,R8              TURN ON PROCESS FLAG          Y02669
MOVEONE  EQU   *                                               @Z30LPTJ
         SRL   R8,ONE              SET FOR NEXT UNIT             Y02669
*                                                                 19084
UENDCK   EQU   *                                                  19084
         TM    XADUSWIT,XAIOCOMP   COMPOSITE CONSOLE SPECIFIED @Z30LPTJ
         BZ    UPARENCK            NO, FOR DELIMITER              19084
         LA    R9,SVN(R9)                                         19084
UPARENCK EQU   *                                                  19084
         LA    R9,TRE(R9)          INDEX TO DELIMITER             19084
         TM    XADUSWIT,XAXPAREN   SINGLE UNIT                 @Z30LPTJ
         BZ    UKEYEXIT            YES,CHECK KEYWORD AND GO       19084
UCOMMA   EQU   *                                                  19084
         CLI   ZERO(R9),COMMA      ANY MORE UNITS                 19084
         BE    UCOMP               YES, START NEXT UNIT           19084
UKEYEXIT  EQU  *                                                  19084
         NI    XADUSWIT,XAXPAREN+XAUDVC   CLEAR ALL BUT        @Z30LPTJ*
                                     EXTERIOR  PARENTHESIS     @Z30LPTJ*
                                     AND I/O DEVICE SWITCHES   @Z30LPTJ
         LA    R9,ONE(R9)                                         19084
*                                                                 19084
         ST    R12,XAR             STORE PROCESS BITS IN XAR      19084
*        R9  PTS TO LOGICAL EOB, RIGHT AT END OF UNIT FLD         19084
*                                                                 19084
         TM    XASOPCOD,XACON      CONSOLE KEYWORD             @Z30LPTJ
         BO    U49EXT              YES,GOTO 4903D                 19084
         LTR   R12,R12             ANY PROCESS BITS ON            19084
         BNZ   U46EXT              YES,OK GOTO 4603D              19084
         TM    XADUSWIT,XAUDVC      ANY IO UNITS               @Z30LPTJ
         BZ    RETEXT              NO, RETURN                    Y02669
         B     U31EXT              YES,GO TO 310OD                19084
*                                                                 19084
U46EXT   MVC   XAX(FIV),ONOFFMOD                                 Y02669
         L     R15,VCON46          GET ADDRESS OF 4603D          Y02669
         LM    R0,R14,ZERO(R13)    RESTORE REGS                  Y02669
         BR    R15                                               Y02669
*                                                                 19084
******** OUNIT SCAN                                               19084
*                                                                 19084
UOUSCAN  EQU   *                                                  19084
         CLC   ZERO(TWO,R9),UOUNIT IS IT O-XXX                    19084
         BNE   UUSCAN              NO,MUST BE REGULAR UNIT OR DTCL19084
         LA    R9,TWO(R9)          INDEX TO UNIT                  19084
         OI    XADUSWIT,XAOUNIT                                @Z30LPTJ
         BAL   R15,UCMEFND         GO GET UCME                    19084
         TM    UCMDISP,UCMDISPD    OUTPUT ONLY FLAG               19084
         BO    UACTVCK             YES,GO CHECK VALIDITY          19084
         B     UREFINV             NO, OUNIT SPEC BUT DEV IS NOT O19084
*                                                                 19084
******** UNIT SCAN                                                19084
*                                                                 19084
UUSCAN   EQU   *                                                  19084
         BAL   R15,UCMEFND         NO,REGULAR UNIT,GO GET UCME    19084
         TM    UCMATR,UCMOF+UCMIF  IO SUPPORT PRESENT             19084
         BO    UACTVCK             GO CHECK ACTIVITY              19084
         B     UREFINV             UNIT REF INVALID               19084
***********************************************************************
*                                                                 19084
******** ROUTINE TO EXTRACT UCME FOR UNIT                         19084
*                                                                 19084
UCMEFND  EQU   *                                                  19084
         LM    R5,R7,UCMVEA        R5 PTS TO FIRST UCME          Y02669
*                                  R6 HAS SIZE OF EACH UCME      Y02669
*                                  R7 PTS TO LAST UCME           Y02669
ULOOP    L     R11,UCMUCB          GET PTR TO UCB                 19084
         CLC   ZERO(TRE,R9),UCBNAME     IS UCBNAME EQUAL TO UNITNA19084
         BE    GRAHCK              YES, CK FOR GRPX               21002
*                                                                 19084
         BXLE  R5,R6,ULOOP         NO, GO CHECK NEXT UCME         19084
*                                                                 19084
******** UCME  NOT FOUND                                          19084
         TM    XASOPCOD,XACON      CONSOLE KEYWORD             @Z30LPTJ
         BO    UREFINV                                            19084
         TM    XADUSWIT,OUNCOMP     IS O-XXX OR COMP           @Z30LPTJ
         BC    SVN,UREFINV         YES,UNIT REF INVALID           19084
         TM    XACAHOLD,CA2         COMMAND AUTH SHOULD BE 2   @Z30LPTJ
         BZ    UAUTHINV            NO, INVALID AUTH              Y02669
         OI    XADUSWIT,XAUDVC     TURN ON I/O SWITCH FOR 3103 @Z30LPTJ
         SRL   R8,ONE              OMIT PROCESS SWITCH           Y02669
         LA    R9,TRE(R9)          INDEX TO NEXT UNIT             19084
         TM    XADUSWIT,XAXPAREN  ANY MORE UNITS               @Z30LPTJ
         BZ    U31EXT              NO, GO TO 3103D                19084
         B     UCOMMA        GO TO FIND WHERE TO GO               19084
         SPACE
***********************************************************************
* IF THIS CONSOLE IS GRAPHICS SET GRAPHICS INDICATOR IN XAX.  THIS21002
* IS USED BY IEE4903D AND IEE7303D WHEN BUILDING THE CONSOLE     21002*
* MESSAGE                                                        21002*
***********************************************************************
         SPACE
GRAHCK   EQU   *                                                  21002
         TM    UCMDISP,UCMDISPC        IS THIS CONSOLE GRAPHICS   21002
         BCR   EASY,R15                NO, DON'T SET GRPX ID      21002
         MVI   XAX,GRPX                SET GRAPHICS INDICATOR     21002
         BR    R15                     RETURN TO CALLER           21002
         SPACE
U31EXT   EQU   *                                                  19084
         MVC   XAX(FIV),IODEVMOD                                 Y02669
         L     R15,VCON31          GET ADDRESS OF 3103D          Y02669
         ST    R9,XAR                                             19084
         LM    R0,R14,ZERO(R13)    RESTORE ALL BUT BRANCH REG    Y02669
*                                  3103 NEEDSPTR TO EOB,-PTRTO COM19084
         BR    R15                                               Y02669
*                                                                 19084
*                                                                 19084
******** EXIT ROUTINES                                            19084
*                                                                 19084
*                                                                 19084
U49EXT   EQU   *                                                  21002
         IC    R4,XAX                  INDICATOR OF GRAPHICS      21002
         MVC   XAX(FIV),CONSMOD                                  Y02669
         LTR   R12,R12             ANY PROCESS BITS ON            19084
         BE    RETEXT              NO, RETURN                    Y02669
         L     R15,VCON49          GET ADDRESS OF 4903D          Y02669
         ST    R4,SIXTN(R13)       STORE GFX INDIC               Y02669
         LM    R0,R14,ZERO(R13)    RESTORE ALL BUT BRANCH REG    Y02669
         BR    R15                                               Y02669
*                                                                 19084
*                                                                 19084
*                                                                 19084
UOFON    EQU   *                                                  19084
         TM    UCMDISP,UCMDISPA    IS THIS MASTE CONSOLE          19084
         BO    UREFINV             YES, UNIT REF INVALID       @ZM30481
         LTR   R4,R4               COMPOSITE THERE                19084
         BZ    UHCPYCK             NO, CHECK UCME                 19084
         LR    R5,R4               GET OUNITS UCME PTR FOR TST    19084
UHCPYCK  EQU   *                                                  19084
         TM    UCMDISP,UCMDISPB    IS THIS THE HCPY DEV           19084
         BO    UREQHCPY            YES,REQ'D FOR HCPY MSG         19084
         B     UBITON              GO SET PROCESS BIT             19084
         EJECT                                                    19084
RETEXT   EQU   *                                                 Y02651
         TM    XAAVRANG,XAARNG1    VARY RANGE BEING PROCESSED? @Z30LPTJ
         BNO   PROCEED             NO, PROCEED NORMALLY        @Z30LPTJ
         L     R15,VCON904         GET IEECB904 ADDRESS        @Z30LPTJ
         BR    R15                 GO TO VARY RANGE PROCESSOR  @Z30LPTJ
PROCEED  EQU   *                                               @Z30LPTJ
         SPACE 5
***************************************************              Y02651
*                                                                Y02651
*  THIS IS A TERMINAL EXIT AND THEREFORE THE ENQ ISSUED IN       Y02651
*  IN IEE3603D MUST BE DEQUEUED OFF OF                           Y02651
*                                                                Y02651
         DEQ   MF=(E,IEFDEQUE)                                   Y02651
*                                                                Y02651
***************************************************              Y02651
         SPACE 5
         L     R1,D64(R13)         GET CSCB PTR                  Y02669
         USING CHAIN1,R1                                         Y02669
         OI    CHSTS,CHFC          SET TO FREE CSCB              Y02669
         MGCR  (1),CHAIN                                         Y02669
         LM    R0,R15,ZERO(R13)    RESTORE REGS                  Y02669
         LR    R4,R14              SAVE RETURN ADDRESS           Y02669
*                                  FREE SVEAREA GOTTEN IN 3603D  Y02669
         LA    R0,D132             DUMMY XSA SIZE                Y02669
         LR    R1,R2               SUMMY XSA POINTER             Y02669
         FREEMAIN R,LV=(0),A=(1)                                 Y02669
*                                  FREE DUMMY XSA GOTTEN IN 3603DY02669
         LR    R14,R4              RESTORE BRANCH REG            Y02669
         BR    R14                 RETURN                        Y02669
         EJECT
*                                                                 19084
*                                                                 19084
*                                                                 19084
*        MSG  SUBROUTINE                                          19084
*                                                                 19084
*                                                                 19084
         SPACE 10                                                 19084
        DS    0F                                                 Y02651
UREFINV  EQU   *                                               @Z30LPTJ
         BAL   R15,UMSGS           GO TO MESSAGE PROCESSOR     @Z30LPTJ
UA       DC    C'313'                  MSG ID FOR UNIT REF INVALID19084
UONE     DC    C'UNIT REF INVALID    '  REQ CONSOLE,UNIT - XXX - Y02651
*                                                                 19084
UAUTHINV BAL   R15,UMSGS               **                         19084
UB       DC    C'345'              ID FOR AUTHORITY  INVALID      19084
UTWO     DC    C'AUTHORITY INVALID   ' REQ CONSOL, UNIT -V XXX-  Y02651
*                                                                 19084
UREQHCPY BAL   R15,UMSGS               **                         19084
UC       DC    C'299'                  ID FOR REQ'D FOR HARDCPY   19084
UTRE     DC    C'REQ''D FOR HARDCPY   ' REQ CONS AND MSTR,UN -XXXY02651
*                                                                 19084
UINVALTC BAL   R15,UMSGS               **                         19084
UD       DC    C'300'                  ID FOR INVALID ALTCONS     19084
UFOR     DC    C'INVALID ALTCON      ' REQ CONS, UNIT -XXX/AAA-  Y02651
*                                                                 19084
UCHNG    BAL   R15,UMSGS               **                         19084
UE       DC    C'339'                  ID FOR CHANGING STATUS     19084
UFIVE    DC    C'CHANGING STATUS     ' REQ CONS,UNT-XXX-OR-COMPO Y02651
*                                                                 19084
COMPMSG  BAL   R15,UMSGS          SYSTEM COMPONENT MESSAGE     @YM07231
UF       DC    C'329'             ID                             Y02651
USIX     DC    C'IN USE BY A SYSTEM COMPONENT'                 @YM07231
*                                                                 19084
         EJECT                                                    19084
         DS    0F                                                Y02651
UMSGS    EQU   *                                                  19084
         DROP  R11                                                19084
         SRL   R8,ONE              OMIT UNIT,SET NO PROCESS BIT  Y02669
*                                                                 19084
         MVC   XAH(FOR),UINTRO     MOVE IN MCSFLAGS AND LENGTH    19084
         L     R7,XAD              STORE XAD                      19084
         LM    R4,R6,XAS           STORE XAS IN 4,5 AND XAU IN 6  19084
         LM    R10,R11,XAE         STORE XAEXAR AND XAN-XAL       19084
         SR    R0,R0                                              19084
         TM    XAU,URI             R/I COMMAND                    19084
         BO    URDR                YES, ISSUE MSG TO MSTR CONSOLE 19084
         IC    R0,XAU              NO, GET COMS ID IN REG0 FOR WTO19084
URDR     EQU   *                                                  19084
         MVC   XAI(EIT),UBASIC     MOVE IN MSG ID                 19084
         MVC   XAF(EIT),XAI+SVN    BLANK REST OF AREA             19084
         MVC   XAI+TRE(TRE),ZERO(R15)  FILL IN MSG NUMBERS        19084
         LA    R15,TRE(R15)        INDEX TO MSG TEST              19084
         CLC   XAI+TRE(TRE),UF     IN USE BY SYSTEM COMPONENT? @YM07231
         BNE   NOSYSCOM            NO, ISSUE DIFFERENT MESSAGE @YM07231
         MVC   XAT(TWENTY8),ZERO(R15) MOVE IN MESSAGE TEXT     @YM07231
         MVC   XAT+TWENTY8(FOR),UEND MOVE IN DESCRIPTOR AND    @YM07231*
                                      ROUTING CODES            @YM07231
         MVC   XAH(TWO),UINTRO8    MOVE IN LENGTH              @YM07231
         B     AROUND                                          @YM07231
NOSYSCOM EQU   *                                               @YM07231
         MVC   XAT(TWENTY),ZERO(R15)    MOVE IT IN               Y02651
         MVC   XAT+TWENTY(FOR),UEND     MOVE IN DESC AND ROUTCDS Y02651
AROUND   EQU   *                                               @YM07231
         CLC   XAI+TRE(TRE),UB     AUTHORITY INVALID              19084
         BE    UAUTH               YES,GO PROCESS                 19084
*                                                                 19084
*        MOVE UNIT                                                19084
*                                                                 19084
         MVC  XAF(TRE),ZERO(R9)   MOVE IN UNIT                    19084
         CLC   XAI+TRE(TRE),UC     REQ'D FOR HCPY                 19084
         BNE   UCONT               NO CHECK FOR OTHER MSGS        19084
         MVI   XAT+TWENTY2,RTC2      YES,MOVE ROUTCODE OF TWO    Y02651
         LR    R1,R7               NEEDED SWITCHES INTO R1        19084
         SLL   R1,FIV              SHIFT THEM TO HIGH ORDER       19084
         LTR   R1,R1               IS IT ON                       19084
         BC    ELEV,UISSUE         NO IK PROCESS AS NORMAL        19084
         MVC   XAF(TRE),SIX(R9)    MOVE IN OUTPUT UNIT            19084
         B     UISSUE              GO TO MASTER CONSOLE ALSO      19084
*                                                                 19084
UAUTH    EQU   *                                                  19084
         MVI   XAF,VARY            AUTHORITY INVALID,MOVE IN      19084
         MVC   XAF+TWO(TRE),ZERO(R9)   VARY AND UNIT              19084
         B     UISSUE              FINISHED                       19084
*                                                                 19084
UCONT    EQU   *                                                  19084
        CLC   XAI+TRE(TRE),UA     UNIT REF INVALID                19084
         BE    UCOMINV             YES,SEE IF COMP                M0128
*                                                                 19084
        CLC   XAI+TRE(TRE),UD     INVALID ALTCON                  19084
         BNE   UCHGSTTS            NO, CHANGING STATUS            19084
*                                                                 19084
         MVI   XAF+TRE,SLASH       MOVE IN SLASH TO DELIMIT ALTC  19084
         SR    R15,R15                                            19084
         LR    R14,R4              GET XAS SWITCHES INTO R14      19084
        SRDL  R14,EIT             SHIFT ALTCON ID INTO REG15      19084
         SRL   R15,TWENTY4         ITS LOW ORDER BYTE             19084
         BCTR  R15,ZERO            MINUS 1                        19084
         L     R1,CVTPTR                                          19084
         USING UCVT,R1                                            19084
         L     R1,CVTCUCB                                         19084
         DROP  R1                                                 19084
         DROP  R10                                                19084
         USING UCM,R1
         L     R14,UCMVEZ          GET SIZE OF EACH UCME          19084
         MR    R14,R14             MULTIPLY SIZE BY ID            19084
         A     R15,UCMVEA          PLUS ADDR OF FIRST UCME        19084
         DROP  R5                                                 19084
         DROP  R1                                                 19084
         USING UCMLIST,R15         ADDR TO UCME OF ALTERNATE      19084
         L     R15,UCMUCB          AND ITS UCB                    19084
         DROP  R15                                                19084
         USING UCMLIST,R5                                         19084
         USING UUCB,R15                                           19084
         MVC   XAF+FOR(TRE),UCBNAME    MOVE ALTCONS UNIT NAME     19084
         DROP  R15                                                19084
         B     UISSUE              GO ISSUE MSG                   19084
*                                                                 19084
UCOMINV  EQU   *                   UNIT REF INVALID OR            M0128
UCHGSTTS EQU   *                   SYSTEM COMPONENT MESSAGE OR @YM07231
         LR    R15,R7              CHANGING STATUS MSG            19084
*                                  XAD SWITCHES INTO REG15        19084
         SLL   R15,FIV             SHIFT TO GET IOCOMP IN HIGH ORD19084
         LTR   R15,R15             IS IT ON                       19084
         BNM   UISSUE              NO, GO ISSUE MSG               19084
        MVI   XAF+TRE,COMMA       YES,MOVE IN COMMA               19084
         MVC   XAF+FOR(TRE),SIX(R9)    AND OUNITS UNIT NAME       19084
*                                                                 19084
UISSUE   EQU   *                                                  19084
*                                                                 19084
         LR    R1,R2               PTR TO WTO BUFFER              19084
MSG      EQU   *                                                 Y02651
         WTO   MF=(E,(1))              ISSUE MSG                  19084
*                                                                 19084
*                                                                 19084
*        RESTORE XSA                                              19084
*                                                                 19084
*                                                                 19084
         ST    R7,XAD                                             19084
         STM   R4,R6,XAS                                          19084
         STM   R10,R11,XAE                                        19084
         MVC   XAV(EIT),OVARY                                    Y02669
         MVC   XAX(EIT),MODNAME                                  Y02669
         B     UENDCK             GO CHECK END OF CMD             19084
*                                                                 19084
*                                                                 19084
*                                                                 19084
         EJECT                                                    19084
***************************************************              Y02651
*                                                                Y02651
*  THE LIST FORM OF THE DEQ MACRO INSTRUCTION FOLLOWS:           Y02651
*                                                                Y02651
IEFDEQUE DEQ   (IEFSQCBS,IEFQ4QCB,TWO,SYSTEM,IEFSQCBS,IEFVQCBS,SVN,    *
               SYSTEM),RET=HAVE,MF=L                           @YM5443P
IEFVQCBS DC    C'VARYDEV'
IEFSQCBS DC    C'SYSIEFSD'                                       Y02651
IEFQ4QCB DC    C'Q4'                                             Y02651
***************************************************              Y02651
         EJECT                                                    19084
*                                                                 19084
*        DECLARES                                                 19084
*                                                                 19084
UIUNIT   DC    C'(I-'                                             19084
UOUNIT   DC    C'O-'                                              19084
IODEVMOD DC    C'IEE31'                                          Y02669
ONOFFMOD DC    C'IEE46'                                          Y02669
CONSMOD  DC    C'IEE49'                                          Y02669
         DS    0F
VCON31   DC    V(IEE3103D)
VCON46   DC    V(IEE4603D)
VCON49   DC    V(IEE4903D)
VCON904  DC    V(IEECB904)                                     @Z30LPTJ
OVARY    DC    C'VARY    '                                       Y02669
MODNAME  DC    C'IEE4203D'                                       Y02669
         SPACE 10                                                 19084
*        DECLARES FOR MSG ROUTINE                                 19084
*                                                                 19084
*                                                                 19084
UINTRO   DC    X'0028E000'             LENGTH AND MCSFLAGS        19084
UINTRO8  DC    X'0030'             LENGTH FOR SYSTEM COMPONENT @YM07231
UBASIC   DC    C'IEE   I '             MSG ID                     19084
UEND    DC    X'08000000'             DESC AND ROUT CODES         19084
*                                                                 19084
PATCH    DC    CL60'***IEE4203D***'                              Y02669
         EJECT
UXSA     DSECT
         IEEXSA
         EJECT
UCVT     DSECT
         CVT
         EJECT
UUCM     DSECT
         IEECUCM  FORMAT=NEW
         EJECT
UUCB     DSECT
         IEFUCBOB
         EJECT
CHAIN1   DSECT
         IEECHAIN
         END
