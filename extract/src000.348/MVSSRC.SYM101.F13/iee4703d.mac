 TITLE ' IEE4703D - VARY HARDCPY PROCESSOR'
HUCMSET  DSECT
         IEECUCM  FORMAT=NEW
         EJECT
         IEEBASEA
         EJECT
HXSASET  DSECT
         IEEXSA
         EJECT
HCVTSET  DSECT
         CVT   SYS=VMS
         EJECT
IEE4703D CSECT
*
* A HROUTRTN  APPROX 358500   KK2 APPROX 369000                @ZA12703
* D HROUTRTN  APPROX 355500   KPAR7 APPROX 400000-403000       @ZA12703
* A KPAR3     APPROX 387500                                    @ZA12703
*
*****************************************************************Y02669
*                                                                Y02669
*  THE COMMAND PROCESSED IN THIS MODULE IS:                      Y02669
*                                                                Y02669
*                     ***     *** ***                               ***
*   ***    ***        *         * *        ***                    *** *
*   *        *        *  NOCMDS * *        *                        * *
*   * XXX    *        *  INCMDS * *        * ALL                    * *
* V *        *,HARDCPY* ,STCMDS * * ,ROUT= * NONE                   * *
*   * SYSLOG *        *  CMDS   * *        * ROUTCODE(,ROUTCODE...  * *
*   *        *        *  OFF    * *        *                        * *
*   ***    ***        *         * *        ***                    *** *
*                     ***     *** ***                               ***
*****************************************************************Y02669
*
         EJECT
**********************************************************************
* MODULE NAME      = IEE4703D
*    CSECT NAME    = IEE4703D
*
* LOAD MODULE NAME = IGC0003D
*
* DESCRIPTIVE NAME = VARY HARDCPY OPERAND PROCESSOR
*
* COPYRIGHT        = MISSING
*
* STATUS           = OS/VS/2 RELEASE 3  PTF UZ05997            @ZA12703
*
* FUNCTION         = THIS MODULE INDICATES WHAT CHANGE ARE  TO
*                    BE MADE BY SETTING BITS IN THE XSA.
*                    1. IT VALIDITY CHECKS ROUTING CODES IF THEY
*                       ARE SPECIFIED AND INITIALIZES A FIELD,
*                       (XRTCODE), WHICH CONTAINS NEW ROUTING
*                       CODES.  IT ALSO SETS A BIT TO INDICATE
*                       THAT ROUTING CODES ARE CHANGING.
*                    2. IT VALIDITY CHECKS THE NEW AUTHORITY IF IT
*                       IS SPECIFIED.  IT SETS A BIT INDICATING
*                       WHAT THE NEW AUTHORITY WILL BE AND A BIT
*                       INDICATING THAT THE AUTHORITY IS CHANGING.
*
* OPERATION        = UPON RECEIVING CONTROL, THE COMMAND AUTHORITY
*                    OF THE ISSUING CONSOLE IS CHECKED.  IF THE
*                    ISSUER WAS THE MASTER CONSOLE, THE SYNTAX
*                    OF THE UNIT FIELD, AND THE PRESENCE OF
*                    KEYWORDS SUCH AS ROUT=,CMDS,STCMDS,INCMDS,
*                    NOCMDS,OFF ARE CHECKED.
*                    IF HARDCPY,OFF  WAS ISSUED FROM THE MASTER
*                    CONSOLE, THAN CONTROL IS GIVEN TO ITS
*                    PROCESSOR - IEE5703D-.
*
* NOTES
*    DEPENDENCIES  = NONE
*    CHARACTER/CODE DEPENDENCIES
*                  = THIS MODULE CAN BE ASSEMBLED WITH ANY NEW
*                    CHARACTER/CODE SET.
*
* RESTRICTIONS     = REG2 PTS TO THE XSA ON INPUT
*    REGISTER CONVENTIONS
*                  = REGISTER ARE DEFINED AT LABEL: DEFREG
*    PATCH LABEL   = PATCH
*
* MODULE TYPE      = CSECT
*    PROCESSOR     = ASSEMBLER
*    MODULE SIZE   = VS/2 = '2BE'                              @Z30LPTJ
*    ATTRIBUTES    = REENTRANT, REUSABLE, REFRESHABLE, PAGED LPA,
*                    ZERO PSW PROTECT KEY, SUPERVISOR MODE
*
* ENTRY POINT      = IEE4703D FROM IEE3203D
*    PURPOSE       = TO BEGIN PROCESSING A VARY HARDCPY COMMAND
*    LINKAGE       = BRANCH
*    INPUT         = REG2 POINTS TO THE XSA
*                    XAR POINTS TO THE COMMAND BUFFER
*                    XAL POINTS TO THE COMMAND PARAMETERS
*                    IN THE COMMAND BUFFER
*                    XAV CONTAINS THE VERB
*    REGISTERS SAVED= NONE
*    REGISTER USAGE= R2  XSA PTR
*                    R5  UCME PTR
*                    R6  EXTERIOR PARENS SWITCH
*                    R7  END OF BUFFER PTR
*                    R9  UNIT PTR
*                    R10 UCM PTR
*                    R11 UMPRFX PTR
*    REGISTERS RESTORED
*                  = NONE
*
* EXITS - NORMAL 1 = TO IEE7203D
*                    VIA BRANCH
*    CONDITIONS    = WHEN INITIAL UNIT SCAN HAS COMPLETED
*                    WE EXIT TO 7203D TO PROCESS THE UNIT OR
*                    SYSLOG PARM AND TO MAKE ANY CHANGES TO THE
*                    ROUTING OR HARDCPY FUNCTIONS.
*   OUTPUT         = R2 PTS TO THE XSA
*   RETURN CODES   = NONE
*
* EXITS - NORMAL 2 = TO IEE5703D
*                    VIA BRANCH
*    CONDITIONS    = TO PROCESS A HARDCPY,OFF COMMAND
*    OUTPUT        = REG 2 PTS TO THE XSA
*                    XAE = X'00'
*    RETURN CODES  = NONE
*
* EXITS - ERROR    = TO IEE5703D VIA BRANCH
*    CONDITIONS    = ERROR HAS BEEN DETECTED IN THE COMMAND
*    OUTPUT        = REG2 PTR TO THE XSA
*                    XAE = X'20' - MEANS:  IEE5703D MUST GO
*                                 TO IEE0503D FOR MSG:
*                                 'IEE309I UNIDENTIFIABLE KEYWORD'
*                    XAE = X'24' - MEANS:  IEE5703D MUST GO
*                                 TO IEE2103D FOR MSG:
*                                 'IEE345I AUTHORITY INVALID'
*                    XAE = X'28' - MEANS: IEE5703D MUST GO
*                                 TO IEE0503D FOR MSG:
*                                 'IEE306I INVALID NUMERICS'
*    RETURN CODES  = NONE
*
* EXTERNAL REFERENCES
*    ROUTINES      = IEE7203D
*                  = IEE5703D
*    DATA AREAS    = SEE EXITS - OUTPUT - DATA
*
* CONTROL BLOCKS   = .EXTENDED SAVE AREA (XSA)
*                    .UNIT CONTROL MODULE (UCM)
*                       FIELDS TESTED  - UCMDISP- BIT= UCMDISPA
*                                        UCMVEA
*                       FIELDS UPDATED - NONE
*                    .CVT
*                       FIELDS TESTED  - CVTCUCB
*                       FIELDS UPDATED - NONE
*
* TABLES           = NONE
*
* MACROS           = NONE
*
* MAPPING MACROS   = IEEXSA
*                    IEECUCM
*                    CVT
*    SERIALIZATION = NO SERIALIZATION IS REQUIRED IN THIS MODULE.
*                    ONLY FIELD TESTED IS 'UCMDISP' AREA IN THE UCM TO
*                    CHECK BIT = UCMDISP = DID THE MASTER CONSOLE
*                    ISSUE THE COMMAND (SINCE IT IS ONLY VALID FROM
*                    THE MASTER CONSOLE.)
*                    A SITUATION COULD ARISE WHERE THE 'MASTER CONSOLE'
*                    IS CHANGING TO OR FROM THE MASTER CONSOLE STATE
*                    WHEN CONTROL IS RECEIVED IN THIS MODULE FOR A
*                    VARY HARDCPY OR VARY HARDCPY,OFF COMMAND.
*                    HOWEVER, IF IT IS CHANGING TO THE MASTER CONSOLE
*                    STATE AND THE UCMDISP BIT IS STILL NOT ON, THEN
*                    THE COMMAND IS REJECTED AS IT WOULD HAPPEN IF THE
*                    BIT WAS OFF AND TECHNICALLY, AS LONG AS THE
*                    BIT IS OFF THE DEVICE IS NOT THE MASTER.
*                    IF IT IS CHANGING TO THE MASTER CONSOLE STATE AND
*                    AND THIS BIT IS ON ALREADY, THEN IT IS A MASTER
*                    CONSOLE AND CAN ISSUE THIS COMMAND ANYWAY. (A LOCK
*                    WOULD NOT CHANGE THESE SITUATIONS)
*                    IF IT IS CHANGING OUT OF THE MASTER CONSOLE STATE
*                    WHETHER A LOCK IS HELD OF NOT IN THIS FIELD, AS
*                    LONG AS UCMDISPA IS STILL ON, THIS DEVICE IS
*                    TECHNICALLY STILL THE MASTER CONSOLE AND CAN
*                    ISSUE THIS COMMANDS.
*
* CHANGE LEVEL     = Y02669, Y02651, Z30LPTJ, ZA12703          @ZA12703
*
* CHANGE ACTIVITY  = N/A
*
* MESSAGES         = REFER TO EXITS - ERROR
*
* ABEND CODES      = NONE
*
* PACKAGING        = IEE4703D WILL BE PART OF LOAD MODULE
*                       = IGC0003D
*                    THIS LOAD MODULE ALIASES ARE
*                       = NONE
*                    THIS LOAD MODULE ENTRY POINTS ARE
*                       = IGC0003D
*                    THIS ROUTINE WILL RESIDE IN: LPALIB
*
* SYSGEN           = SUPPLIED BY THE FOLLOWING SECTIONS:
*                    LOAD MODULE NAME
*                    PACKAGING
*                    THIS CSECT WILL BE INCLUDED AT SYSGEN FROM
*                    DLIB -AOSB3- BY MACRO -SGIEF441-
*
* SYSTEM LIBRARIES = NONE
*
***********************************************************************
      EJECT
F0       EQU   X'F0'                                              M3945
F1       EQU   X'F1'                                              M3945
F6       EQU   X'F6'                                              M3945
F9       EQU   X'F9'                                              M3945
         EJECT
* HARDCOPY PROCESSOR REGISTER ASSIGNMENTS-                            *
         SPACE 10
DEFREG   EQU   *                                                 Y02669
R0      EQU   0
R1       EQU   1                                                  19084
R2       EQU   2                   XSA                            19084
R3       EQU   3                   ADDRESS                        19084
R4       EQU   4                   UCME                           19084
R5       EQU   5                                                  19084
R6       EQU   6
R7       EQU   7
R8       EQU   8                   STAE                           19084
R9       EQU   9                   UNIT PTR                       19084
R10      EQU   10                  UCM                            19084
R11      EQU   11                  UCMPFX                         19084
R12      EQU   12                  UNIT PTR IF 0,ITS SYSLOG       19084
R13      EQU   13
R14      EQU   14                      RETURN REGISTER
R15      EQU   15                                                 19084
         SPACE 10
*
* ISSUING CONSOLE IDENT MASKS
*
HSYSTEM  EQU   0                       SYSTEM ISSUING ID
HRI      EQU   X'80'                   READER/INTERPRETER ID
*
         SPACE 5
*
* ERROR MESSAGE CODES
*
ERR41    EQU   24                  AUTHORITY INVALID - 2103D      19084
ERR9     EQU   20                  KEYWORD MISPELLED - 0503D      19084
ERR6     EQU   28                  INVALID NUMERICS - 0503D       19084
*
         SPACE 5
*                                                                 19084
* GENERAL EQUATES                                                 19084
*                                                                 19084
HOFFCD   EQU   X'00'               VARY HARDCPY,OFF      5703D    19084
*                                                                 19084
*                                                                 19084
BLANK    EQU   C' '
COMMA    EQU   C','                                               19084
BLK      EQU   C' '                                               19084
LEFPAREN EQU   C'('                                               19084
RTPAREN  EQU   C')'                                               19084
*
* NUMERAL EQUATES                                                 19084
*                                                                 19084
ZERO     EQU   0
ONE      EQU   1
TWO      EQU   2
TRE      EQU   3
FOR      EQU   4
FIV      EQU   5
SIX      EQU   6
EIT      EQU   8
ELEV     EQU   11
SVN      EQU   7
TWLV     EQU   12                                                 19084
SIXTN    EQU   16                                                 19084
FOX      EQU   255
         EJECT
***********************************************************************
*                                                                     *
* HARDCOPY PROCESSING MODULE                                          *
*                                                                     *
***********************************************************************
         BALR  R3,ZERO             ADDRESSABILITY                 19084
         USING *,R3                                               19084
         MODID BR=YES                                            Y01886
         USING XSA,R2                                             19084
         MVC   XAX(EIT),HCPYMOD                                  Y02669
         L     R15,CVTPTR          CVT POINTER                    19084
         USING HCVTSET,R15                                        19084
         L     R10,CVTCUCB         PTR TO UCM                     19084
         USING UCM,R10                                           Y02669
*                                                                 19084
         LR    R11,R10             PTR TO UCM                     19084
         LA    R1,FOR                                             19084
         SR    R11,R1              MINUS FOUR IS PTR TO PFX       19084
         L     R11,ZERO(R11)                                      19084
         USING UCMPRFX,R11                                        19084
         L     R9,XAL              PTR TO UNITS                   19084
         LA    R9,ZERO(R9)         CLEAR HIGH ORDER BYTE          19084
         XC    XAS(EIT),XAS                                       19084
***********************************************************************
*
* CHECK OF COMMAND AUTHORITY FOR VARY HARDCPY
*
***********************************************************************
         CLI   XAU,HSYSTEM         DID SYSTEM ISSUE THE COMMAND
         BE    HSYNTAX                   YES,BEGIN UNIT SCAN      19084
*
         CLI   XAU,HRI             DID THE R/I ISSUE IT
         BNE   HCONSOLE                NO,CHECK CONSOLES CMD AUTHORITY
         MVI   XAU,HSYSTEM             R/I MESSAGES ALSO RETURN MSTCONS
HERR1    EQU   *                                                  19084
         MVI   XAE,ERR41           ERROR IS AUTHORITY INVALID     19084
         B     HOFFMVC             GO TO XCTL ROUTINE             19084
*                                                                 19084
HCONSOLE EQU   *                                                  19084
         SR    R5,R5                                              19084
         IC    R5,XAU              ISSUING CONSOLE ID             19084
         LM    R6,R7,UCMVEA        PTR TO 1ST UCME AND SIZE       19084
*                                                                 19084
         BCTR  R5,ZERO             MINUS1                         19084
         MR    R4,R7               TIMES ZIZE                     19084
         AR    R5,R6               PLUS ADDR OF 1ST UCME          19084
         USING UCMLIST,R5                                         19084
         TM    UCMDISP,UCMDISPA    MASTER CONSOLE WAS ISSUED      19084
         BZ    HERR1               NO, AUTHORITY INVALID          19084
         DROP  R5                                                 19084
         EJECT
***********************************************************************
*                                                                 19084
*        SYNTAX SCAN, TRY TO FIND SECONDARY KEYWORD               19084
*                                                                 19084
***********************************************************************
*        R9 PTS TO BEGINNING OF UNIT FIELD                        19084
*                                                                 19084
*        A.    SCAN TOWARDS -,HARDCPY-                            19084
*        *******************************                          19084
HSYNTAX  EQU   *                                                  19084
         LR    R12,R9              SAVE UNIT PTR                  19084
HLOOPFND EQU   *                                                  19084
         CLI   ZERO(R9),COMMA      IS THIS THE UNITS COMMA        19084
         LA    R9,ONE(R9)          NO, INDEX BY ONE               19084
         BNE   HLOOPFND            GO CHECK NEXT CHARACTER        19084
         SPACE 10                                                 19084
*        FOUND KEYWORD                                            19084
*        B.    INDEX PAST KEYWORD AND CHECK FOR OTHERS PRESENT    19084
*        ******************************************************** 19084
HKEYFND  EQU   *                                                  19084
         LA    R9,SVN(R9)          INDEX PAST KEYWORD             19084
         CLI   ZERO(R9),COMMA      COMMA FOLLOWS KEYWORD          19084
         BNE   HEOB                NO, GO CHECK END OF BUFFER     19084
         SPACE 10                                                 19084
*                                                                 19084
*        OTHER KEYWORDS ARE PRESENT                               19084
*        C.    CHECK FOR CMDS,NOCMDS,OFF AND ,                    19084
*        ****************************************                 19084
         CLC   ONE(FOR,R9),HCMDS       IS IS CMDS TO HCPY KEYWORD 19084
         BE    HSET1               YES, GO SET SWITCHES           19084
         CLC   ONE(SIX,R9),HNOCMDS     IS IT CMDS NOT TO HCPY     19084
         BNE   HSP8                NO MAYBE OTHER KEYWORD         19084
INCRMT   EQU   *                                                  21002
         LA    R9,SVN(R9)          INDEX PAST IT                  19084
HSP6     EQU   *                                                  19084
         OI    XASOPCOD,XACMD     TURN ON CMD TO BE CHANGED BIT@Z30LPTJ
         B     HROUTCK                 CHECK ROUT KEYWORD.        21002
*                                                                 19084
HSP8     EQU   *                                                  19084
         CLC   ONE(SIX,R9),HINCMDS     IS THIS INCMDS             21002
         BE    HSET3                   SET BITS TO INDICATE INCMDS21002
         CLC   ONE(SIX,R9),HSTCMDS     IS THIS STCMDS             21002
         BE    HSET4                   YES, SET BITS TO INDICATE  21002
         LA    R9,ONE(R9)          INDEX BY ONE                   19084
         CLC   ZERO(TRE,R9),HOFF       IS IT THE HARDCPY,OFF      19084
         BNE   HROUTCK             NO, CHECK IF ,ROUT= PRESENT    19084
*                                  FOR HARDCPY,, CMD              19084
         MVI   XAE,HOFFCD          SER ,OFF CODE FOR 5703D        19084
         B     HOFFMVC             GO TO 5703D EXIT               19084
HSET1    EQU   *                                                  19084
         OI    XASWITCH,XAHRDCMD   TURN TO MEAN THAT CMDS ARE  @Z30LPTJ
         LA    R9,FIV(R9)          INDEX PAST KEYWORD             19084
         B     HSP6                GO CHECK ENDINGS               19084
HSET3    EQU   *                                                  21002
         OI    XASWITCH,XAINHDCP       INDICATE INCMDS         @Z30LPTJ
         B     INCRMT                  INCREMENT PAST INCMDS.     21002
HSET4    EQU   *                                                  21002
         OI    XASWITCH,XASTHDCP       INDICATE STCMDS         @Z30LPTJ
         B     INCRMT                  INCREMENT PAST STCMDS.     21002
         SPACE 6                                                  21002
*        D.    CHECK FOR PRESENCE OF ROUT= KEYWORD                21002
*        **********************************************           21002
HROUTCK  EQU   *                                                  21002
         CLC   ZERO(SIX,R9),HROUT      IS ,ROUT=PRESENT           21002
         BE    HROUTRTN                YES,GO CHECK ROUT=         21002
         CLC   ZERO(FIV,R9),HROUT+ONE  IS IT 'ROUT='              21002
         BNE   HEOB                    NO, CHECK END OF BUFFER    21002
         BCTR  R9,ZERO                 MINUS ONE                  21002
         EJECT                                                    19084
*                                                                 19084
*        E.    ROUT= IS PRESENT                                   19084
*        **************************                               19084
*                                                                 19084
***********************************************************************
*              VALUES FOR ROUT=                                   19084
**********************************************************************
*                                                                 19084
HROUTRTN EQU   *                                                  19084
         SR    R6,R6               CLEAR EXTERIOR PARENS SWITCH   19084
         LA    R9,SIX(R9)       UPDATE TO PARM                    19084
         CLI   ZERO(R9),LEFPAREN   ARE THERE INDIVIDUAL ROUTCODES 19084
         BE    KPAREN2        YES, GO PROCESS                     19084
         CLC   ZERO(TRE,R9),KALL   ARE ALL RTCDS TO BE ASSIGNED   19084
         BNE   KK2            NO, GO CHECK FOR OTHERS             19084
         OI    XASWITCH,XAROUT       ROUT CODES TO BE CHANGED  @ZA12703
         MVC   XARTCODE(TWO),KFFFF   SET ALL ROUTING CODES     @Z30LPTJ
         BCTR  R9,ZERO                                            19084
HEOB1    EQU   *                                                  19084
         LA    R9,FOR(R9)          INDEX PAST KEYWORD JUST CHECKED19084
HEOB     EQU   *                                                  19084
         L     R7,XAR             PTR TO LIST POSITION            19084
         LA    R7,ZERO(R7)         CLEAR HIGH ORDER BYTE          19084
         AH    R7,ZERO(R7)         COMPUTE END OF BUFFER          19084
         CR    R9,R7               ARE WE AT THE END              19084
         BH    HERR2               NO, WE ARE OVER ERROR          19084
         BE    HSCAN               WE ARE OK, GO START PROCESS    19084
         CLI   ZERO(R9),BLANK      IS BLANK A DELIMITER FOR END   19084
         BE    HSCAN               GO START PROCESS               19084
HERR2    EQU   *                                                  19084
         MVI   XAE,ERR9            ERROR CODE IS 9KEYWORD MISPELD 19084
         B     HOFFMVC             GO TO EXIT RTN                 19084
*                                                                 19084
*                                                                 19084
KK2      EQU   *                                                  19084
         CLC   ZERO(FOR,R9),KNONE  ARE THERE NO RTCDS TO BE ASSGND19084
         BNE   KPAR1               NONE IS NOT THE PARM CHECK MORE19084
         OI    XASWITCH,XAROUT     ROUT CODES TO BE CHANGED    @ZA12703
         SR    R1,R1               CLEAR REGISTER              @ZA12703
         STH   R1,XARTCODE         NO ROUTE CODES WANTED       @ZA12703
         B     HEOB1               GO CHECK END OF BUFFER         19084
*                                                                 19084
KPAREN2  EQU   *                                                  19084
         LA    R9,ONE(R9)       UPDATE TO PASS THE PAREN          19084
         BCTR  R6,ZERO             SET EXTERNAL PARENS SWITCH     19084
*                                                                 19084
KPAR1    EQU   *                                                  19084
         XC    XAP(TWLV),XAP       CLEAR AREA FOR CVB OF RT CDS   19084
         CLI   ONE(R9),COMMA       ONE DIGIT NUM FOLLOWED BY COMMA19084
         BE    KPAR6               YES,GO PROCESS                 19084
         CLI   ONE(R9),RTPAREN     IS A RIGHT PARENS THAT FOLLOWS 19084
         BE    KPAR1B              FIND IF NEEDED                 19084
         CLI   TWO(R9),COMMA       TWO DIGIT NUM FOLLOWED BY COMMA19084
         BE    KPAR2          YES                                 19084
KPAR1B   EQU   *                                                  19084
         LTR   R6,R6          EXTERIOR PAREN SWITH SET            19084
         BZ    KPAR8          NO, ERROR                           19084
         CLI   ONE(R9),RTPAREN     YES,ONE DIGIT NUMB PLUS )      19084
         BE    KPAR6          YES, PROCESS                        19084
         CLI   TWO(R9),RTPAREN     TWO DIGIT NUM FOLLOWED BY ')'  19084
         BNE   HERR2               NO, KEYWORD IS MISPELLED       19084
*                                                                 19084
KPAR2    EQU   *                                                  19084
         CLI   ZERO(R9),F1             SYNTAX CHECK THE ROUTING   M3945
         BNE   HERR6                   CODE. THE 1ST DIGIT MUST   M3945
         CLI   ONE(R9),F0              BE 1, THE 2ND DIGIT MUST   M3945
         BL    HERR6                   BE 0-6. OTHERWISE, AN      M3945
         CLI   ONE(R9),F6              ERROR CONDITION HAS BEEN   M3945
         BH    HERR6                   DETECTED                   M3945
         PACK  XAD+TWO(TWO),ZERO(TWO,R9)  RTCODE IS 2 DIGITS      19084
KPAR3    EQU   *                                                  19084
         LA    R9,TWO(R9)           INDEX PAST DIGITS             19084
         LA    R13,SIXTN           MAXIMUM VALUE ALLOWED          19084
         CVB   R4,XAP         CONVERT PACKED DECIMAL              19084
         LTR   R4,R4          IS IT ZERO                          19084
         BZ    HERR6          YES, ILLEGAL                        19084
         CR    R4,R13         IS IT 16                            19084
         BE    KPAR7               YES, NO ROUT TO BE SET         19084
         BH    HERR6               HIGH, IT IS ILLEGAL KEYWORD    19084
*                                                                 19084
         BCTR  R4,ZERO             SET TO ZERO BASE               19084
         OI    XASWITCH,XAROUT     ROUT CODES TO BE CHANGED    @ZA12703
         SLL   R13,ELEV            SET FIRST BIT OF MASK ON       19084
         SRL   R13,ZERO(R4)        MOVE TO APPROPRIATE RT CODE BIT19084
         O     R13,XACMDATH     SET BIT IN INDICATOR AREA      @Z30LPTJ
         ST    R13,XACMDATH     AND SAVE IN XSA                @Z30LPTJ
*                                                                 19084
KPAR5    EQU   *                                                  19084
         LTR   R6,R6          EXTERIOR PARENS SET                 19084
         BZ    HEOB                GO CHECK IF END OF BUFFER      19084
         CLI   ZERO(R9),RTPAREN    YES,CHECK IF ')' PRESENT       19084
         LA    R9,ONE(R9)          YES, UPDATE PAST PARENS        19084
         BNE   KPAR1          NO, MORE ROUT CODES, PROCESS        19084
         B     HEOB                GO PUT IT IN ROUT FIELD        19084
*                                                                 19084
KPAR8    EQU   *                                                  19084
         CLI   ONE(R9),BLK         BLANK MAY FOLLOW SINGLE DIGIT  19084
         BNE   KPAR2               GO PROCESS IT                  19084
KPAR6    EQU   *                                                  19084
         CLI   ZERO(R9),F0             SYNTAX CHECK THE DIGIT     M3945
         BL    HERR6                   USED AS THE ROUTCODE. IT   M3945
         CLI   ZERO(R9),F9             MUST BE 0-9 SINCE ROUTCODE M3945
         BH    HERR6                   IS ONE CHARACTER           M3945
         PACK  XAD+TRE(ONE),ZERO(ONE,R9)     ROUT CODE IS ONE DGT 19084
         BCTR  R9,ZERO                                            19084
         B     KPAR3          GO TO PROCESS                       19084
*                                                                 19084
KPAR7    EQU   *                                                  19084
         LTR   R6,R6               ANY MORE ROUTS                 19084
         BNZ   KPAR5               MAYBE, GO CHECK                19084
         B     HEOB                GO CHECK ACCURACY OF EOB       19084
         EJECT
         EJECT
***********************************************************************
*
* ERROR MESSAGE ROUTINES
* AND EXIT ROUTINE                                                19084
*
***********************************************************************
*
*
HSCAN    EQU   *                                                  21002
         MVC   XAX+TRE(TWO),UNITSCAN       IGC7203D              Y02669
         L     R15,VCON72              ADDRESS OF 7203D          Y02669
         BR    R15                                               Y02669
HERR6    EQU   *                                                  19084
         MVI   XAE,ERR6            INVALID NUMERICS               19084
HOFFMVC  EQU   *                                                  21002
         MVC   XAX+TRE(TWO),OFFMOD         IGC5703D              Y02669
         L     R15,VCON57          ADDRESS OF IEE5703D           Y02669
         BR    R15                 XCTL TO HARDCOPY OFF PROCESSORY02669
         EJECT
***********************************************************************
*
* CONSTANTS USED BY HARDCPY PROCESSOR
*
***********************************************************************
OFFMOD   DC    C'57'         HARDCPY,OFF PROCESSOR               Y02669
HOFF     DC    C'OFF'              HARDCPY,OFF                    19084
*                                                                 19084
*                                                                 19084
*        DO NOT SEPARATE THE FOLLOWING TWO DECALRES -NO CMDS-     19084
HNOCMDS  DC    C'NO'               NO CMDS TO HCPY                19084
HCMDS    DC    C'CMDS'             CMDS TO HARDCPY                19084
HINCMDS  DC    C'INCMDS'               NO STATUS DISPLAY TO       21002*
                                       HARDCOPY.                  21002
HSTCMDS  DC    C'STCMDS'               NO DYNAMIC STATUS DISPLAY  21002*
                                       TO HARDCOPY.               21002
HROUT    DC    C',ROUT='           ROUT KEYWORD                   19084
KNONE    DC    C'NONE'             CHECK FOR ROUT=                19084
KFFFF    DC    X'FFFF'             MASK FOR ALL ROUT CODES        19084
KALL     DC    C'ALL'              CHECK FOR ALL AUTH,RTCDS       19084
UNITSCAN DC    C'72'           UNIT SCAN PROCESSOR               Y02669
HCPYMOD  DC    CL8'IEE4703D'                                     Y02669
         DS    0F                                                Y02669
VCON57   DC    V(IEE5703D)                                       Y02669
VCON72   DC    V(IEE7203D)                                       Y02669
                   SPACE 5
PATCH    DC   CL50'***IEE4703D***'                             @Z30LPTJ
*
         END
