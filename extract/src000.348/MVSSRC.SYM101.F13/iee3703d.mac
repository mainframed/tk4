         TITLE 'IEE3703D--VS2 CANCEL/FORCE COMMAND'
***********************************************************************
*                                                                     *
*  MODULE NAME =  IEE3703D                                       Y02669
*                                                                     *
*  DESCRIPTIVE NAME =  CANCEL/FORCE COMMAND HANDLER            @G17FPSS
*                                                                     *
*                                                                     *
*  STATUS =  OS/VS2 - SERVICE DATA IMPROVEMENTS                @G17FPSS
*                                                                     *
*  FUNCTION =  CANCEL/FORCE BOTH FOREGROUND AND BACKGROUND     @G17FPSS
*              JOBS IN EXECUTION                               @G17FPSS
*     OPERATION =                                                Y02669
*        0.  IF THE COMMAND WAS FORCE THEN CHECK TO SEE IF IT  @ZD00106
*            WAS ISSUED ON THE MASTER CONSOLE. IF NOT THEN A   @ZD00106
*            BRANCH IS MADE TO MODULE IEE0503D TO ISSUE        @ZD00106
*            MESSAGE IEE345I.                                  @ZD00106
*
*        1.  OPERAND SYNTAX IS CHECKED.  VALID SYNTAX IS:        Y02669
*                CANCEL                FORCE                   @G17FPSS
*             A) NAME               E) NAME                    @G17FPSS
*             B) NAME,DUMP          F) U=NAME                  @G17FPSS
*             C) U=NAME
*             D) U=NAME,DUMP
*            WHERE NAME IN A,B AND E MAY BE THE NAME OF A      @G17FPSS
*            BACKGROUND JOB OR A DEVICE ID (CANCEL/FORCE       @G17FPSS
*            IS INDIFFERENT TO THE EXTERNAL SIGNIFICANCE OF THE  Y02669
*            NAME). NAME IN C,D AND F IS THE USERID OF A TIME  @G17FPSS
*            SHARING USER.  IN ALL CASES, THE FINAL DELIMITER    Y02669
*            MUST BE A BLANK OR END OF BUFFER, THE INTERMEDIATE  Y02669
*            DELIMITER, A COMMA.                                 Y02669
*
*            IF THE SYNTAX IS INVALID, CONTROL PASSES TO THE     Y02669
*            MESSAGE MODULE TO ISSUE THE MESSAGE INDICATED AT    Y02669
*            THE ERROR EXIT.                                     Y02669
*
*        2.  ENQ SHARE ON THE CSCB CHAIN RESOURCE AND SEARCH THE Y02651
*            CHAIN FOR A CSCB WITH NAME IN THE CHKEY OR CHUNIT   Y02669
*            FIELDS MATCHING THE NAME IN THE OPERAND FIELD OF THEY02669
*            COMMAND.  IF NONE IS FOUND, CONTROL IS PASSED TO    Y02669
*            THE MESSAGE MODULE TO ISSUE THE MESSAGE INDICATED   Y02669
*            AT THE EXIT.                                        Y02669
*
*        3.  IF A CSCB IS FOUND, WITH THE ASSIGNMENT PENDING     Y02669
*            BIT (CHAP) OFF, THE TYPE IS CHECKED AGAINST THE     Y02669
*            TYPE SPECIFIED IN THE COMMAND:  FOREGROUND OR       Y02669
*            BACKGROUND.  IF CHAP IS ON, OR THE TYPES DO NOT     Y02669
*            MATCH, THE SCAN CONTINUES.                          Y02669
*
*        4.  IF THE TYPES MATCH AND FORCE WAS THE COMMAND THEN @G17FPSS
*            A TEST IS MADE TO SEE IF A CANCEL COMMAND WAS     @G17FPSS
*            ISSUED FOR THE JOB OR TSO USER(IF BIT CHAFORCE IS @G17FPSS
*            ON IN THE CSCB).  IF NOT THEN CONTROL IS PASSED   @G17FPSS
*            TO THE MESSAGE MODULE TO ISSUE MESSAGE IEE838I.   @G17FPSS
*            IF CANCEL WAS ISSUED BEFORE THE FORCE             @G17FPSS
*            THEN A CALLRTM TYPE=MEMTERM IS ISSUED WITH THE    @G17FPSS
*            JOBS ASID GOTTEN FROM THE CSCB. A COMPLETION CODE @G17FPSS
*            OF X'A22' IS PASSED TO CALLRTM. CONTROL IS THEN   @G17FPSS
*            GIVEN TO STEP 6.                                  @G17FPSS
*
*        5.  A CANCEL COMMAND WAS ISSUED.                      @G17FPSS
*            IF THE TYPES MATCH, AND FOREGROUND WAS SPECIFIED    Y02669
*            (BIT CHTSID IN BYTE CHTRKID IS ON AND U=NAME        Y02669
*            APPEARS ON THE COMMAND), THE BRANCH TO SIC IS TAKEN,Y02669
*            AND THE CANCEL ECB (CHCECB) IN THE CSCB IS POSTED,  Y02669
*            WITHIN SIC, WITH ONE OF THE FOLLOWING COMPLETION    Y02669
*            CODES:                                              Y02669
*                           00000122          DUMP               Y02669
*                           00000222          NO DUMP            Y02669
*            IF BACKGROUND CANCEL WAS SPECIFIED, AND BIT CHJOBID Y02669
*            IN BYTE CHTRKID IS ON, INDICATING A BACKGROUND JOB  Y02669
*            CSCB.  THE ABOVE BRANCH TO SIC IS TAKEN (SIC WILL @ZA05360
*            NOT POST THE CANCEL ECB FOR BACKGROUND JOBS)      @ZA05360
*            AND THE CANCEL ECB IS POSTED WITH ONE OF THE      @ZA05360
*            ABOVE COMPLETION CODES. BIT CHAFORCE IS TURNED ON @G17FPSS
*            TO SHOW THAT A CANCEL HAS BEEN ISSUED FOR THIS    @G17FPSS
*            JOB.                                              @G17FPSS
*
*        6.  DEQ OFF THE CSCB CHAIN RESOURCE AND PASS CONTROL TO Y02651
*            EXIT CMBRCH TO CAUSE THE MESSAGE MODULE TO INDICATE Y02669
*            ACCEPTANCE OF THE COMMAND, AFTER ISSUING SYSEVENT @YM6770P
*            BRINGIN TO SWAP IN THE CANCELLED MEMORY --        @YM6770P
*            OTHERWISE, THE MEMORY, IF SWAPPED-OUT AT THE      @YM6770P
*            TIME OF CANCEL, COULD REMAIN SWAPPED-OUT FOR      @YM6770P
*            A SUBSTANTIAL PERIOD OF TIME.                     @YM6770P
*
*        7.  IF A SATISFACTORY CSCB IS NOT FOUND, AND FOREGROUND Y02669
*            CANCEL/FORCE WAS SPECIFIED, OR IF A CSCB WAS NOT  @G17FPSS
*            FOUND FOR A BACKGROUND CANCEL/FORCE, CONTROL PASSES Y02669
*            TO EXITS NOTFORE, NOTBACK AND NONCAN TO SET UP    @G17FPSS
*            APPROPRIATE PARAMETER LISTS FOR THE MESSAGE       @G17FPSS
*            MODULE.                                           @G17FPSS
*                                                                     *
*  NOTES=  THE CSCB CHAIN ENQ ON Q10 MUST BE ISSUED BEFORE THE   Y02651
*          CHAIN SEARCH AND RETAINED UNTIL AFTER THE POST, TO    Y02651
*          ASSURE THAT A CSCB FOUND WILL REMAIN ON THE CHAIN     Y02651
*          UNTIL POSTED.                                         Y02651
*
*          SIC IS ENTERED FOR BACKGROUND JOBS TO CAUSE THE     @ZA05360
*          INITIATOR TO BE FORCED DISPATCHABLE IF THE          @ZA05360
*          RCT IS HUNG; THE XMPOST IS ISSUED IN THIS           @ZA05360
*          MODULE BECAUSE SIC HAS NO WAY TO PROPERLY           @ZA05360
*          PROCESS JOBS WITH MORE THAN ONE CSCB.  THE          @ZA05360
*          EXTERNAL WRITER (XWTR) IS AN EXAMPLE.               @ZA05360
*
*     CHARACTER CODE DEPENDENCIES =                              Y02669
*          THIS MODULE HAS BEEN CODED IN SUCH A WAY THAT IF THE  Y02669
*          EXTERNAL CHARACTER SET IS CHANGED FROM EBCDIC,        Y02669
*          REASSEMBLY WILL YIELD A VALID MODULE.                 Y02669
*
*     DEPENDENCIES =  ENQ, DEQ, SIC, CROSS MEMORY POST,          Y02669
*                     SYSEVENT, CALLRTM                        @G17FPSS
*
*     RESTRICTIONS =  THIS MODULE EXPECTS REGISTER TWO TO CON-   Y02669
*                     TAIN THE ADDRESS OF AN EXTENDED SAVE AREA. Y02669
*                     IT SAVES NO REGISTERS, BUT WILL NOT ALTER  Y02669
*                     REGISTERS 13 AND 14.                       Y02669
*
*     REGISTER CONVENTIONS =                                     Y02669
*              REGISTER 7         BUFFER SCAN POINTER            Y02669
*              REGISTER 8         CVT POINTER                    Y02657
*              REGISTER 11        CSCB POINTER                   Y02669
*              REGISTER 12        MODULE BASE                    Y02669
*              REGISTER 14        RETURN ADDRESS                 Y02669
*              REGISTER 15        ADDRESS OF SUCCESSOR MODULE    Y02669
*
*     PATCH-LABEL =  PATCH                                       Y02669
*
*  MODULE-TYPE =  CSECT                                          Y02669
*
*     PROCESSOR =  ASSEMBLER                                     Y02669
*
*     MODULE-SIZE =  N/A                                         Y02669
*
*     ATTRIBUTES =                                               Y02669
*                  PAGED LPA              REENTRANT              Y02669
*                  ZERO PSW PROTECT KEY   REUSABLE               Y02669
*                  SUPERVISOR MODE        REFRESHABLE            Y02669
*                                                                     *
*  ENTRY-POINT =  IEE3703D, FROM IEE0403D                        Y02669
*
*     PURPOSE =  CANCEL A JOB IN EXECUTION                     @G17FPSS
*                FORCE A JOB THAT WAS NOT REMOVED BY A         @G17FPSS
*                      CANCEL COMMAND.                         @G17FPSS
*
*     LINKAGE =  REGISTER TWO CONTAINS THE ADDRESS OF AN XSA,    Y02669
*                INITIALIZED AS FOLLOWS:                         Y02669
*                    XAR    POINTS TO COMMAND BUFFER
*                    XAV    CONTAINS THE VERB IN EBCDIC
*                    XAV    CONTAINS THE VERB CODE
*                    XAL    POINTS TO THE COMMAND PARAMETER LIST
*                    XAU    CONTAINS THE ID OF ISSUING CONSOLE   Y02669
*                    XAA    CONTAINS ASID OF TIME-SHARING USER   Y02669
*
*     INPUT DATA =  AS DESCRIBED ABOVE                           Y02669
*
*     REGISTERS SAVED =  NONE                                    Y02669
*
*     REGISTER CONTENTS DURING PROCESSING =                      Y02669
*                        SEE REGISTER CONVENTIONS                Y02669
*
*     REGISTERS RESTORED =  NONE                                 Y02669
*
*  EXIT-NORMAL =  CMBRCH                                         Y02669
*     CONDITIONS =  THE CANCEL ECB IN THE CSCB CORRESPONDING TO  Y02669
*                   THE JOB SPECIFIED ON THE COMMAND IS POSTED.  Y02669
*                   CONTROL IS PASSED VIA BRANCH TO IEE0503D TO  Y02669
*                   ISSUE MESSAGE IEE301I.                       Y02669
*
*     OUTPUT DATA =  REGISTER TWO POINTS TO AN XSA CONTAINING:   Y02669
*                    XAE     MESSAGE INDEX X'01'                 Y02669
*                    XAV     JOBNAME                             Y02669
*                    XAU     CONSOLE ID FOR WTO                  Y02669
*                    XAA     ASID FOR TPUT                       Y02669
*
*                    CSCB FOR THIS JOB WITH CANCEL ECB (CHCECB)  Y02669
*                    POSTED AND CHCLD BIT SET ON.                Y02669
*
*     RETURN CODES =  NONE                                       Y02669
*
*  EXIT-NORMAL =  CMBRCH                                         Y02669
*     CONDITIONS =  CALLRTM TYPE=MEMTERM IS DONE. CONTROL IS   @G17FPSS
*                   PASSED VIA BRANCH TO IEE0503D TO ISSUE     @G17FPSS
*                   MESSAGE IEE301I.                           @G17FPSS
*
*     OUTPUT DATA =  REGISTER TWO POINTS TO AN XSA CONTAINING: @G17FPSS
*                    XAE     MESSAGE INDEX X'02'               @G17FPSS
*                    XAV     JOBNAME                           @G17FPSS
*                    XAU     CONSOLE ID FOR WTO                @G17FPSS
*                    XAA     ASID FOR TPUT                     @G17FPSS
*
*     RETURN CODES =  NONE                                     @G17FPSS
*                                                                     *
*  EXIT-ERROR =  NOTFORE                                         Y02669
*     CONDITIONS =  FOREGROUND CANCEL/FORCE SPECIFIED FOR A    @G17FPSS
*                   USER NOT LOGGED ON.                          Y02669
*                   CONTROL IS PASSED VIA BRANCH ON VCON TO      Y02669
*                   MESSAGE MODULE (IEE0503D) TO ISSUE MESSAGE   Y02669
*                   IEE324I.                                     Y02669
*
*     OUTPUT DATA =  REGISTER TWO CONTAINS THE ADDRESS OF AN     Y02669
*                    XSA INITIALIZED AS FOLLOWS:                 Y02669
*                    XAE     MESSAGE INDEX  X'35'                Y02669
*                    XAV     USERID (NAME FROM THE COMMAND)      Y02669
*                    XAU     CONSOLE ID FOR WTO                  Y02669
*                    XAA     ASID FOR TPUT                       Y02669
*
*     RETURN CODES =  NONE                                       Y02669
*                                                                     *
*  EXIT-ERROR =  NOTBACK                                         Y02669
*     CONDITIONS =  BACKGROUND CANCEL/FORCE SPECIFIED FOR A    @G17FPSS
*                   NAME WITH NO CORRESPONDING CSCB ON THE CHAIN.Y02669
*                   CONTROL IS PASSED VIA BRANCH ON VCON TO      Y02669
*                   MESSAGE MODULE (IEE0503D) TO ISSUE MESSAGE   Y02669
*                   IEE341I.                                     Y02669
*
*     OUTPUT DATA =  REGISTER TWO CONTAINS THE ADDRESS OF AN     Y02669
*                    XSA INITIALIZED AS FOLLOWS:                 Y02669
*                    XAE     MESSAGE INDEX  X'24'                Y02669
*                    XAV     JOBNAME (NAME FROM THE COMMAND)     Y02669
*                    XAU     CONSOLE ID FOR WTO                  Y02669
*                    XAA     ASID FOR TPUT                       Y02669
*
*     RETURN CODES =  NONE                                       Y02669
*                                                                     *
*  EXIT-ERROR =  CMERRC                                          Y02669
*     CONDITIONS =  NO OPERAND ON THE COMMAND                    Y02669
*                   CONTROL IS PASSED VIA BRANCH ON VCON TO      Y02669
*                   MESSAGE MODULE (IEE0503D) TO ISSUE MESSAGE   Y02669
*                   IEE311I.                                     Y02669
*
*     OUTPUT DATA =  REGISTER TWO CONTAINS THE ADDRESS OF AN     Y02669
*                    XSA INITIALIZED AS FOLLOWS:                 Y02669
*                    XAE     MESSAGE INDEX  X'0B'                Y02669
*                    XAV     COMMAND VERB                        Y02669
*                    XAU     CONSOLE ID FOR WTO                  Y02669
*                    XAA     ASID FOR TPUT                       Y02669
*
*     RETURN CODES =  NONE                                       Y02669
*                                                                     *
*  EXIT-ERROR =  AUTHERR                                       @ZD00106
*    CONDITIONS =  THE FORCE COMMAND WAS NOT ISSUED ON THE     @ZD00106
*                  MASTER CONSOLE. CONTROL IS PASSED VIA       @ZD00106
*                  BRANCH TO IEE0503D TO ISSUE MESSAGE         @ZD00106
*                  IEE345I.                                    @ZD00106
*
*    OUTPUT DATA = REGISTER TWO CONTAINS THE ADDRESS OF AN     @ZD00106
*                  XSA INITIALIZED AS FOLLOWS:                 @ZD00106
*                  XAE     MESSAGE INDEX X'29'                 @ZD00106
*                  XAV     JOBNAME (NAME FROM THE COMMAND)     @ZD00106
*                  XAU     CONSOLE ID FOR WTO                  @ZD00106
*                  XAA     ASID FOR TPUT                       @ZD00106
*
*    RETURN CODES =  NONE                                      @ZD00106
*                                                                     *
*  EXIT-ERROR =  CMERR8A                                         Y02669
*     CONDITIONS =  LENGTH OF AN OPERAND EXCEEDS EIGHT           Y02669
*                   CHARACTERS.                                  Y02669
*                   CONTROL IS PASSED VIA BRANCH ON VCON TO      Y02669
*                   MESSAGE MODULE (IEE0503D) TO ISSUE MESSAGE   Y02669
*                   IEE308I.                                     Y02669
*
*     OUTPUT DATA =  REGISTER TWO CONTAINS THE ADDRESS OF AN     Y02669
*                    XSA INITIALIZED AS FOLLOWS:                 Y02669
*                    XAE     MESSAGE INDEX  X'08'                Y02669
*                    XAV     COMMAND VERB                        Y02669
*                    XAU     CONSOLE ID FOR WTO                  Y02669
*                    XAA     ASID FOR TPUT                       Y02669
*
*     RETURN CODES =  NONE                                       Y02669
*                                                                     *
*  EXIT-ERROR =  CMERR8                                          Y02669
*     CONDITIONS =  LENGTH OF AN OPERAND EXCEEDS EIGHT           Y02669
*                   CHARACTERS.                                  Y02669
*                   CONTROL IS PASSED VIA BRANCH ON VCON TO      Y02669
*                   MESSAGE MODULE (IEE0503D) TO ISSUE MESSAGE   Y02669
*                   IEE308I.                                     Y02669
*
*     OUTPUT DATA =  REGISTER TWO CONTAINS THE ADDRESS OF AN     Y02669
*                    XSA INITIALIZED AS FOLLOWS:                 Y02669
*                    XAE     MESSAGE INDEX  X'08'                Y02669
*                    XAV     COMMAND VERB                        Y02669
*                    XAU     CONSOLE ID FOR WTO                  Y02669
*                    XAA     ASID FOR TPUT                       Y02669
*
*     RETURN CODES =  NONE                                       Y02669
*                                                                     *
*  EXIT-ERROR =  INVAL                                           Y02669
*     CONDITIONS =  NAME NOT SPECIFIED OR AN INVALID PARAMETER   Y02669
*                   ON THE COMMAND.                              Y02669
*                   CONTROL IS PASSED VIA BRANCH ON VCON TO      Y02669
*                   MESSAGE MODULE (IEE0503D) TO ISSUE MESSAGE   Y02669
*                   IEE305I.                                     Y02669
*
*     OUTPUT DATA =  REGISTER TWO CONTAINS THE ADDRESS OF AN     Y02669
*                    XSA INITIALIZED AS FOLLOWS:                 Y02669
*                    XAE     MESSAGE INDEX  X'05'                Y02669
*                    XAV     COMMAND VERB                        Y02669
*                    XAU     CONSOLE ID FOR WTO                  Y02669
*                    XAA     ASID FOR TPUT                       Y02669
*
*     RETURN CODES =  NONE                                       Y02669
*
*  EXIT-ERROR =  NONCAN                                        @G17FPSS
*    CONDITIONS =  CANCEL/FORCE WAS ISSUED FOR A JOB WHICH IS  @G17FPSS
*                  MARKED NON-CANCELABLE. CONTROL IS PASSED    @G17FPSS
*                  VIA BRANCH ON VCON TO MESSAGE MODULE        @G17FPSS
*                  (IEE0503D) TO ISSUE MESSAGE IEE841I.        @G17FPSS
*
*    OUTPUT DATA = REGISTER TWO CONTAINS THE ADDRESS OF AN     @G17FPSS
*                  XSA INITIALIZED AS FOLLOWS:                 @G17FPSS
*                  XAE     MESSAGE INDEX X'1C' FOR CANCEL      @G17FPSS
*                                        X'16' FOR FORCE       @G17FPSS
*                  XAV     JOBNAME (NAME FROM THE COMMAND)     @G17FPSS
*                  XAU     CONSOLE ID FOR WTO                  @G17FPSS
*                  XAA     ASID FOR TPUT                       @G17FPSS
*
*    RETURN CODES =  NONE                                      @G17FPSS
*                                                                     *
*  EXTERNAL REFERENCES =                                         Y02669
*
*     REFERENCES =  IEE0503D - MESSAGE MODULE                    Y02669
*                   SIC INTERFACE                                Y02669
*
*     DATA AREAS =  NONE                                         Y02669
*
*     CONTROL BLOCKS =  XSA, CSCB, PSA, ASVT                     Y02669
*
*     TABLES =                                                   Y02669
*
*     MACROS =  MODID, ENQ, DEQ, POST, SYSEVENT, IEEXSA,         Y02669
*               IEECHAIN, SETLOCK, IHAPSA, IHAASVT, CALLRTM    @G17FPSS
*
*     SERIALIZATION =                                            Y02651
*        ENQUEUE RESOURCES =       QNAME             RNAME       Y02651
*                                  SYSIEFSD          Q10         Y02651
*        LOCKS HELD =  CMS, LOCAL                                Y02651
*
*     CHANGE LEVEL =  Y02651, Y02657, Y02669, @YM6770P         @YM6770P
*                     ZA05360, G17FPSS, ZD00106                @ZD00106
*                                                                     *
         EJECT
*  MESSAGES =                                                    Y02669
*        CODE                   TEXT                             Y02669
*        X'01'       IEE301I  (JOBNAME)  CANCEL COMMAND ACCEPTED Y02669
*        X'02'       IEE301I  (JOBNAME)  FORCE COMMAND ACCEPTED@G17FPSS
*        X'05'       IEE305I  (VERB)     COMMAND INVALID         Y02669
*        X'08'       IEE308I  (VERB)     TERM LENGTH ERROR       Y02669
*        X'0B'       IEE311I  (VERB)     PARAMETER MISSING       Y02669
*        X'16'       IEE841I  (JOBNAME)  NON-FORCIBLE          @G17FPSS
*        X'1B'       IEE838I  (JOBNAME)  CANCELABLE-ISSUE CANCEL
*                                           BEFORE FORCE       @G17FPSS
*        X'1C'       IEE841I  (JOBNAME)  NON-CANCELABLE        @G17FPSS
*        X'24'       IEE341I  (JOBNAME)  NOT ACTIVE
*        X'29'       IEE345I  (VERB)     AUTHORITY INVALID     @ZD00106
*        X'35'       IEE324I  (USERID)   NOT LOGGED ON           Y02669
*
*        ALL MESSAGES CONTAINED IN AND ISSUED BY IEE0503D.       Y02669
*
*  COMPLETION CODE =
*        A22 - PASSED TO CALLRTM WHEN FORCING AN ADDRESS SPACE @G17FPSS
*
*  SYSGEN =  THIS MODULE IS IN LOAD MODULE IGC0003D IN           Y02669
*            SYS1.LPALIB.  IT IS INCLUDED AT SYSGEN FROM DLIB    Y02669
*            AOSB3 BY SYSGEN MACRO SGIEF441.                     Y02669
*
*  SYSTEM LIBRARIES =  THIS MODULE MAKES REFERENCE TO NO SYSTEM  Y02669
*                      LIBRARIES.                                Y02669
*                                                                     *
***********************************************************************
         EJECT
**************************************************************** Y02669
*
*        REGISTER EQUATES                                        Y02669
*
**************************************************************** Y02669
         SPACE 2
IEE3703D  CSECT
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         EJECT
**************************************************************** Y02669
*
*        EQUATES FOR LENGTHS OFFSETS, AND CONSTANTS              Y02669
*
**************************************************************** Y02669
         SPACE 2
D0       EQU   0
D1       EQU   1
D2       EQU   2
D3       EQU   3                                                   I272
D4       EQU   4
D5       EQU   5                                                   O102
D6       EQU   6                                                  20035
D7       EQU   7                                                  20030
D8       EQU   8
D9       EQU   9
FIVE     EQU   5                                                   O102
CMCOMP   EQU   X'40'              COMPLETION CODE ECB
BLANK    EQU   C' '
UNCK     EQU   X'01'              UNCHECKED PARAMETER INDICATOR   M0232
EOS      EQU   X'02'              END OF SCAN SWITCH             Y02669
BOTHALF  EQU   X'03'              MASK FOR LAST TWO BYTES        Y02669
*                                 OF REGISTER                    Y02669
DUMPSW   EQU   X'04'              DUMP SWITCH                    Y02669
NONCANSW EQU   X'10'              NON-CANCELABLE SWITCH        @G17FPSS
COMMA    EQU   C','
FOX      EQU   255                                                20030
FORCE    EQU   132                FORCE CMD VERB CODE          @G17FPSS
A22      EQU   2594               COMPLETION CODE FOR FORCE    @G17FPSS
SYSISSUD EQU   X'00'              CMD ISSUED BY SYSTEM         @ZD00106
         SPACE
*
*        MESSAGE CODE EQUATES                                    Y02669
*
         SPACE
CMCD01   EQU   1
CMCD02   EQU   2                  FORCE CMD ACCEPTED           @G17FPSS
CMCD05   EQU   5                                                   O102
CMCD08   EQU   8
CMCD0B   EQU   11                                                  O102
CMCD12   EQU   12                                                  O102
CMCD17   EQU   17                                                  O102
CMCD22   EQU   22                 NON-FORCIBLE MSG CODE        @G17FPSS
CMCD27   EQU   27                 ISSUE CANCEL FOR JOB MSG     @G17FPSS
CMCD28   EQU   28                 NON-CANCELABLE MSG CODE      @G17FPSS
CMCD36   EQU   36                 NOT ACTIVE MSG CODE            Y02669
CMCD41   EQU   41                 AUTHORITY INVALID MSG CODE   @G17FPSS
CMCD53   EQU   53                 MSG CODE- NOT LOGGED ON        Y02669
         EJECT
***********************************************************************
*  ENTRY ROUTINE- THIS PORTION OF CODE ESTABLISHES ADDRESSABILITY FOR *
*        THE MODULE AND THE VARIOUS DSECTS, SET UP THE EXTENDED SAVE  *
*        AREA.                                                        *
***********************************************************************
         BALR  R12,0              ESTABLISH ADDRESSABILITY
         USING  *,R12
         MODID BRANCH=YES                                        Y01886
         USING XSA,R2             ADDR. FOR XSA
         USING CMCVT,R8           CVT...                         Y02657
         L     R8,CVTPTR          ...ADDRESSABILITY              Y02657
         L     R10,CVTMSER        PTR TO MASTER RESIDENT CORE
         USING BASE,R10           ADDR. FOR MRC
         L     R11,BACHN
         USING CMCHAIN,R11        ADDR. FOR CHAIN
         L     R9,XAL             PTR TO COMMAND PARM LIST
         LA    R9,D0(R9)          CLEAR HIGH ORDER BYTE           M2420
         L     R7,XAR             PTR TO CIB
         LA    R7,D0(R7)
         AH    R7,D0(R7)          ABSOLUTE END OF BUFFER
         ST    R7,XAP             SAVE IN 1ST WORD OF XSA
         MVI   XAX,BLANK
         MVC   XAX+D1(D7),XAX     BLANK OUT JOBNAME
         MVC   XAS(D8),XAX        XAS AS WELL
         EJECT
***********************************************************************
*                                                                     *
* IF THIS IS THE FORCE COMMAND THEN CHECK TO SEE IF IT WAS     @ZD00106
* ISSUED FROM THE MASTER CONSOLE. IF NOT THEN MODULE IEE0503D  @ZD00106
* IS CALLED TO ISSUE MESSAGE IEE345I.                          @ZD00106
*                                                                     *
* TO FIND ISSUING CONSOLE'S UCME THE FOLLOWING FORMULA IS USED @ZD00106
*    UCME ADDR = UCMVEA + UCMVEZ * (XAU - 1)                   @ZD00106
*       UCMVEA - ADDR OF FIRST UCME                            @ZD00106
*       UCMVEZ - LENGTH OF A UCME                              @ZD00106
*       XAU    - CONSOLE ID WHERE THE COMMAND WAS ISSUED       @ZD00106
*                                                                     *
***********************************************************************
         SPACE
         CLI   XAN,FORCE          WAS THIS A FORCE CMD ?       @ZD00106
         BNE   AUTHOK             NO, CONTINUE                 @ZD00106
         CLI   XAU,SYSISSUD       WAS FORCE ISSUED BY SYSTEM?  @ZD00106
         BE    AUTHERR            YES, ERROR, GO ISSUED MSG    @ZD00106
         L     R5,CVTCUCB         ADDR OF UCM BASE             @ZD00106
         USING UCM,R5             UCM ADDRESSABILITY           @ZD00106
         L     R3,UCMVEA          ADDR OF 1ST UCME             @ZD00106
         L     R6,UCMVEZ          LENGTH OF A UCME             @ZD00106
         DROP  R5                                              @ZD00106
         SR    R5,R5              CLEAR REGISTER               @ZD00106
         IC    R5,XAU             GET CONSOLE ID OF ISSUER     @ZD00106
         BCTR  R5,D0              SUBRTACT 1                   @ZD00106
         MR    R4,R6              MULT LENGTH * ID-1           @ZD00106
         AR    R5,R3              ADD TO ADDR OF 1ST UCME      @ZD00106
         USING UCMLIST,R5         UCME ADDRESSABILITY          @ZD00106
         TM    UCMDISP1,UCMDISPA  IS THIS THE MASTER CONSOLE ? @ZD00106
         BZ    AUTHERR            NO GO TO ISSUE MESSAGE       @ZD00106
         DROP  R5                                              @ZD00106
         SPACE
***********************************************************************
*                                                                     *
* WAS A PARAMETER SPECIFIED WITH THE COMMAND ?                 @ZD00106
*                                                                     *
***********************************************************************
         SPACE
AUTHOK   EQU   *                  AUTH HAS BEEN CHECKED AND OK @ZD00106
         LA    R4,D9              *INITIALIZE COUNTER          *   O102
         LTR   R7,R9              PARAMETER AREA PTR=0
         BZ    CMERRC             IF ZERO GO TO ERROR MESSAGE
         XC    XAE,XAE            *ZERO PARM BYTE              *   O102
         EJECT
*                                                                 20030
* SCAN BUFFER- CHECK IF FIRST TWO CHARACTERS ARE 'U=', CHECK      20030
*        LENGTH OF JOBNAME, CHECK FOR DUMP                        20030
*                                                                 20030
         CLC   D0(L'UEQUAL,R9),UEQUAL CANCEL TS USER             Y02669
         BNE   CMSTNEXT           NO, PROCESS BATCH              Y02669
         LA    R9,D2(R9)          POINT TO BEGINNING OF JOBNAME   20030
         LR    R7,R9                                              20030
CMSTNEXT EQU   *
         C     R7,XAP             HAS END OF BUFFER BEEN REACHED
         BE    CMTSKNME           END OF SCAN, CHECK NAME          O102
         CLI   D0(R7),BLANK       CHARACTER  A BLANK
         BE    CMTSKNME           END OF SCAN, CHECK NAME          O102
         CLI   D0(R7),COMMA
         BE    PERAMS             *CONTINUE SCAN               *   O102
         OI    XAE,UNCK           SET UNCHECKED PARM BIT          M0232
         LA    R7,1(R7)           INCREMENT  PTR
         BCT   R4,CMSTNEXT        *CONTINUE SCAN               *   O102
         B     CMERR8A                                             O102
CMTSKNME  EQU  *
         OI    XAE,EOS            END OF SCAN INDICATOR          Y02669
PERAMS   EQU   *                                                   O102
         CLI   XAX,BLANK         *JOBNAME GOTTEN YET?     *        O102
         BNE   JOBCK                                               O102
         LA    R3,D8              * MAX NAME LENGTH       *        O102
         SR    R3,R4                                               O102
         BM    CMERR8             TERM LENGTH ERROR                O102
         EX    R3,CMMVCX          MOVE TASKNAME TO XAX             O102
         MVC   XAV(D8),XAX         NAME FOR CANCEL AND MSG         O102
         NI    XAE,FOX-UNCK       TURN OFF UNCHECKED PARM BIT     M0232
JOBCK    EQU   *                                                   O102
         TM    XAE,EOS                 IS THIS END OF SCAN?      Y02669
         BNO   NXPARM                  NO, CHECK NEXT FIELD        O102
         TM    XAE,UNCK           YES; UNCHECKED PARM BIT ON?     M0232
         BO    INVAL                   YES, COMMAND INVALID        O102
         B     ENDCK                   NO, SEARCH CSCB'S           O102
NXPARM   EQU   *                                                   O102
         NI    XAE,FOX-UNCK       TURN OFF UNCHECKED PARM BIT     M0232
         LA    R4,FIVE                                             O102
         CLI   XAN,FORCE          WAS THIS A FORCE COMMAND?    @G17FPSS
         BE    INVAL              YES. NO ACCEPTABLE KEYWORDS  @G17FPSS
         CLC   D1(D4,R7),PDUMP                                     O102
         BE    DUMP               *IF NO HITS,GIVE ERROR MSG-*     O102
         B     INVAL              *NO OTHER ACCEPTABLE KEYWORDS*   O102
DUMP     EQU   *                                                   O102
         OI    XAE,DUMPSW         *  SET DUMP SWITCH           * Y02669
         LA    R7,D5(R7)          * UPDATE PARM POINTER            O102
         B     CMSTNEXT           *AND BRANCH TO CONTINUE SCAN *   O102
ENDCK    EQU   *                                                   O102
         EJECT
*                                                                  I272
* CSCB SCAN ROUTINE COMPARES FIRST ON CHKEY FOR TASKNAME EQUAL     I272
* TO TASKNAME IN XSA.  IF NO MATCH, A CHECK IS MADE ON 3-BYTE      I272
* UNIT NAME FIELD. IF NO MATCH IN CSCB CHAIN, BRANCH TO          Y02669
* IEE0503D, MESSAGE MODULE.                                      Y02669
        SPACE
* THE CSCB CHAIN RESOURCE IS SERIALIZED VIA ENQ ON Q10. THE      Y02651
* RESOURCE IS RELEASED AT CMBRCH.  THE HOLDER OF THE CSCB ENQ    Y02651
* MUST BE NON-SWAPPABLE TO PREVENT SWAP OUT WHILE HOLDING THE    Y02651
* RESOURCE, WITH THE PERFORMANCE DEGRADATION WHICH WOULD RESULT  Y02651
* WHEN ANOTHER ENQ IS ISSUED TO OBTAIN THE RESOURCE.             Y02651
         SPACE
         SYSEVENT DONTSWAP         DON'T SWAP CURRENT MEMORY     Y02651
         SPACE
         MVC   XASAVSTD(LNE),ENQCSCB MOVE ENQ PARM LIST TO XSA   Y02651
         ENQ   MF=(E,XASAVSTD)     ENQ ON CSCB CHAIN RESOURCE    Y02651
CMSCAN   EQU   *                                                   I272
         LTR   R11,R11             END OF CHAIN                    I272
         BZ    CMBRCH              YES. GO SET UP NOT FOUND MSG  Y02669
         TM    CHSTS,CHAP          ASSIGNMENT PENDING              I272
         BNZ   CMNOGO              GO GET NEXT CSCB                I272
         CLC   CHKEY,XAX           DO IDS MATCH                    I272
         BE    FOUND               YES                             I272
         CLC   CHUNIT(D3),XAX      DO UNIT NAMES MATCH             I272
         BNE   CMNOGO              NO. GO GET NEXT CSCB            I272
FOUND    EQU   *                                                   I272
         TM    CHACT,CHCL         IS JOB CANCELLABLE
         BZ    CMNOCAN            NO. SCAN REMAINIMG CSCB'S    @G17FPSS
*                                                                 20030
* THIS CHECK IS TO ASSURE THAT WHEN A FOREGROUND JOBNAME IS       20030
*        SPECIFIED TO BE CANCELLED, A BACKGROUND JOB OF THE SAME  20030
*        NAME WILL NOT ACCIDENTLY BE CANCELLED AND VICA VERSA.    20030
*                                                                 20030
         L     R5,XAL             PTR TO COMMAND PARM LIST        20030
         LA    R5,D0(R5)          CLEAR HIGH ORDER BYTE           20030
         CLR   R5,R9              WAS FORE OR BACKGROUND SPECIFIED20030
         BNE   CHPOST             FOREGROUND. BRANCH TO CHECK CSCBM6205
* BACKGROUND SPECIFIED, CHECK CSCB FOUND                          20030
         TM    CHTRKID,CHJOBID    IS THIS A BACKGROUND JOB CSCB  Y02669
         BNO   CMNOGO             NO. BRANCH TO SCAN THE NEXT    Y02669
*                                 CSCB                           Y02669
         B     CANJOB             BYPASS FOREGROUND TEST         Y02669
CMNOCAN  EQU   *                  JOB WAS NON-CANCELABLE       @G17FPSS
         OI    XAE,NONCANSW       SET NON-CANCELABLE SWITCH    @G17FPSS
         B     CMNOGO
CHPOST   EQU   *                                                  20030
         TM    CHTRKID,CHTSID     VERIFY FOREGROUND CSCB         Y02669
         BO    CANJOB             YES, CANCEL IT.                Y02669
CMNOGO   EQU   *                                                 Y02669
         L     R11,CHPTR          NEXT CSCB                      Y02669
         B     CMSCAN             GO TEST CSCB                   Y02669
CANJOB   EQU   *                                                   O102
         CLI   XAN,FORCE          THIS A FORCE CMD ?           @G17FPSS
         BE    FORCEMEM           YES, GO FORCE THE ADDR SPACE @G17FPSS
         MVC   CHUCMP,XAU          UPDATE UCMI IN CSCB             I272
         OI    CHACT,CHCLD        POSITION POST CODE
         TM    CHCECB,CMCOMP       COMPLETION CODE SET             I272
         BZ    POST                NO                              I272
         XC    CHCECB,CHCECB       YES. CLEAR ECB                  I272
*
* LOCATE THE ASCB FOR SIC AND XMPOST                             Y02657
*
* THE ADDRESS OF THE ASCB ASSOCIATED WITH THE ASID IN CHASID     Y02657
* IS CALCULATED FOR USING THIS FORMULA:                          Y02657
*   ASCBPTR = ((CHASID*4) + ADDR(ASVTENTY) - 4),                 Y02657
* WHERE ASVTENTY IS THE FIRST ASCB POINTER IN THE ASVT AND       Y02657
* ASCBPTR IS THE ASVT FIELD CONTAINING THE ADDRESS OF THE ASCB.  Y02657
*
POST     EQU   *                                                   I272
         L     R5,CVTASVT          ASVT...                       Y02657
         USING ASVT,R5             ...ADDRESSABILITY             Y02657
         LA    R5,ASVTENTY         ADDRESS FIRST ASVT ENTRY      Y02657
         DROP  R5                  DROP ASCB ADDRESSABILITY      Y02657
         S     R5,FOUR             DECREMENT TO FIRST SLOT       Y02657
         SR    R6,R6               ZERO ASID REG FOR INSERT      Y02657
         ICM   R6,BOTHALF,CHASID   ASID FROM CSCB                Y02657
         SLL   R6,D2               QUADRUPLE IT FOR OFFSET       Y02657
         AR    R5,R6               ADDRESS ASVT ENTRY FOR ASID   Y02657
         L     R6,D0(R5)           LOAD IT FOR SIC AND XMPOST    Y02657
*
* PLACE COMPLETION CODE IN REGISTER ZERO FOR CROSS MEMORY POST. Y02657
* THE POST IS DONE IN SIC FOR CANCELLATION OF FOREGROUND JOBS,  Y02657
* AND IN THIS MODULE FOR CANCELLATION OF BACKGROUND JOBS.       Y02657
*
         L     R0,CNODUMP          CANCEL JOB WITHOUT DUMP       Y02657
         TM    XAE,DUMPSW          DUMP SWITCH ON                Y02657
         BZ    CNCJBN              * NO,                       * Y02657
         L     R0,CDUMP            CANCEL JOB WITH DUMP          Y02657
         EJECT
*        SCHEDULE SRB TO CANCELLED ADDRESS SPACE               @ZA05360
*                                                                Y02669
*        TO DO SO, A BRANCH ENTRY TO SYSTEM INITIATED CANCEL     Y02669
*        (SIC) WILL BE TAKEN, WITH THE LOCAL AND CMS LOCKS HELD. Y02669
*        THE LOCKS ARE FREED IMMEDIATELY UPON RETURN FROM SIC.   Y02669
*        THE REGISTERS ON ENTRY TO SIC:                          Y02669
*              REGISTER 0          COMPLETION CODE FOR POST      Y02669
*              REGISTER 1          TARGET MEMORY ASCB ADDR       Y02669
*              REGISTER 13         ADDRESS OF FULL SAVE AREA     Y02669
*              REGISTER 14         RETURN POINT IN IEE3703D      Y02669
*              REGISTER 15         SIC ENTRY POINT ADDRESS       Y02669
CNCJBN   EQU   *                                               @ZA05360
         SPACE
         STM   R11,R14,XASAVLOC    SAVE REGISTERS ACROSS LOCKS   Y02651
SETLOCK1 SETLOCK OBTAIN,TYPE=LOCAL,MODE=UNCOND,                  Y02651X
               RELATED=(SIC,IEE3703D(CLRLOCK1))                  Y02651
SETLOCK2 SETLOCK OBTAIN,TYPE=CMS,MODE=UNCOND,                    Y02651X
               RELATED=(SIC,IEE3703D(CLRLOCK2))                  Y02651
         LR    R1,R6               GET ADDRESS OF ASCB           Y02669
         LA    R13,XASAVSTD        GET ADDRESS OF SAVE AREA      Y02669
         L     R15,CVTSIC          GET ADDR OF SIC ENTRY POINT   Y02669
         BALR  R14,R15             BRANCH TO SIC                 Y02669
CLRLOCK2 SETLOCK RELEASE,TYPE=CMS,                               Y02651X
               RELATED=(SIC,IEE3703D(GETLOCK2))                  Y02651
CLRLOCK1 SETLOCK RELEASE,TYPE=LOCAL,                             Y02651X
               RELATED=(SIC,IEE3703D(GETLOCK1))                  Y02651
         LM    R11,R14,XASAVLOC    RESTORE REGS SAVED ACR0SS     Y02651
*                                  LOCKS                         Y02651
         TM    CHTRKID,CHTSID    IS THIS A FOREGROUND JOB CSCB @ZA05360
         BO    CMCCA            YES, GO SET UP FOR MSG IEE301I @ZA05360
         EJECT
*
* POST THE CANCEL ECB IN THE CSCB                                Y02669
*
DOPOST   EQU   *                                                  M6205
*                                  MOVE POST PARM LIST TO XSA    Y02657
         MVC   XASAVLOC(POSTLEN),POSTER                          Y02657
*
* XMPOST CANCEL ECB                                              Y02657
*
         POST  CHCECB,(0),ASCB=(R6),ERRET=CVTBRET,               Y02657X
               MF=(E,XASAVLOC)     XMPOST CANCEL ECB             Y02657
CMCCA    EQU   *                                                 Y02669
         SYSEVENT  BRINGIN,ASIDL=CHASID   INFORM SRM THAT      @YM6770P
*                                  ADDRESS SPACE IS TO BE      @YM6770P
*                                  SWAPPED IN FOR CANCELLATION @YM6770P
         OI    CHACT,CHAFORCE  MARK JOB AS CANCEL ISSUED FOR IT@G17FPSS
         MVI   XAE,CMCD01          CANCEL COMMAND ACCEPTED         I272
         B     CMBRCH              GO FREE RESOURCES           @G17FPSS
         EJECT
***********************************************************************
*                                                                     *
*            FORCE COMMAND PROCESSING                                 *
*                                                                     *
***********************************************************************
***********************************************************************
*                                                                     *
*        CALL RTM TO FORCE THE ADDRESS SPACE TO TERMINATE             *
*                                                                     *
***********************************************************************
         SPACE 2
FORCEMEM EQU    *                                              @G17FPSS
         TM     CHACT,CHAFORCE   WAS A CANCEL ISSUED FOR THIS
*                                ASID?                         @G17FPSS
         BO     CALLRTM          YES, GO FORCE ASID            @G17FPSS
         MVI    XAE,CMCD27       CODE FOR ISSUE CANCEL FOR JOB @G17FPSS
         B      CMBRCH           GO RELEASE REASOURCES         @G17FPSS
         SPACE
CALLRTM  EQU    *
         SR     R6,R6            ZERO ASID REG FOR INSERT      @G17FPSS
         ICM    R6,BOTHALF,CHASID GET ASID FROM CSCB           @G17FPSS
         STM    R11,R14,XASAVLOC SAVE REGS ACROSS CALLRTM      @G17FPSS
         LA     R13,XASAVSTD     CALLRTM NEEDS A WORK AREA     @G17FPSS
         LR     R5,R2            SAVE ADDR OF XSA              @G17FPSS
         SPACE
         CALLRTM TYPE=MEMTERM,COMPCOD=A22,ASID=(R6)   CANCEL THE       X
                                 ADDRESS SPACE                 @G17FPSS
         SPACE
         LR     R2,R5            RESTORE XSA ADDR              @G17FPSS
         LM     R11,R14,XASAVLOC RESTORE THE REGS              @G17FPSS
         MVI    XAE,CMCD02       CODE FOR FORCE CMD ACCEPTED   @G17FPSS
         EJECT
*  THE CSCB CHAIN RESOURCE (Q10) IS RELEASED HERE. IT WAS        Y02651
*  OBTAINED AT ENDCK. SWAPPING IS THEN AGAIN MADE POSSIBLE.      Y02651
CMBRCH   EQU    *                                                Y02669
         MVC   XASAVSTD(LND),DEQCSCB MOVE DEQ PARM LIST TO XSA   Y02651
         DEQ   MF=(E,XASAVSTD)     DEQ OFF CSCB CHAIN RESOURCE   Y02651
         SPACE
         SYSEVENT OKSWAP          ALLOW SWAP FOR THIS MEMORY     Y02651
         SPACE
         LTR   R11,R11            END OF CSCB CHAIN                I272
         BC    7,CMBRCH5
         L     R5,XAL             PTR TO COMMAND PARM LIST        20030
         LA    R5,D0(R5)          CLEAR HIGH ORDER BYTE           20030
         CLR   R5,R9              WAS A BACKGROUND JOB SPECIFIED  20030
         BNE   NOTFORE            NO, GO ISSUE NOT LOGGED ON MSG Y02669
NOTBACK  EQU   *                                                 Y02669
         TM    XAE,NONCANSW       WAS JOB NONCANCELABLE?       @G17FPSS
         BO    NONCAN             YES, GO USE NON-CAN MSG      @G17FPSS
         MVI   XAE,CMCD36         MSG CODE - NOT ACTIVE        @G17FPSS
         B     CMBRCH5            GO TO IEE0503D               @G17FPSS
NONCAN   EQU   *                  JOB NON-CANCELABLE           @G17FPSS
         MVI   XAE,CMCD28         MSG CODE - NON-CANCELABLE    @G17FPSS
         CLI   XAN,FORCE          WAS THIS A FORCE CMD?        @G17FPSS
         BNE   CMBRCH5            NO, GO TO IEE0503D           @G17FPSS
         MVI   XAE,CMCD22         MSG CODE NON-FORCIBLE        @G17FPSS
         B     CMBRCH5            GO TO IEE0503D               @G17FPSS
NOTFORE  EQU   *                   USER NOT LOGGED ON            Y02669
         MVI   XAE,CMCD53         MSG CODE- NOT LOGGED ON        Y02669
         B     CMBRCH5            GO BRANCH TO IEE0503D          Y02669
CMERRC   EQU   *                                                   O102
         MVI   XAE,CMCD0B         PARAMETER MISSING CODE         Y02669
         B     CMBRCH5F           GO OVERLAY XAV WITH COMMAND  @G17FPSS
*                                 VERB AND BRANCH TO IEE0503D    Y02669
AUTHERR  EQU   *                  THE AUTH OF ISSUING CONSOLE WAS      *
                                  INVALID                      @ZD00106
         MVI   XAE,CMCD41         AUTH INVALID CODE            @ZD00106
         B     CMBRCH5F           GO OVERLAY XAV WITH COMMAND VERB     *
                                  AND BRANCH TO IEE0503D       @ZD00106
CMERR8A  EQU   *                                                   O102
CMERR8   EQU    *
         MVI   XAE,CMCD08         TERM LENGTH ERROR
         B     CMBRCH5F           GO OVERLAY JOBNAME W/ CMD    @G17FPSS
*                                 VERB AND BRANCH TO IEE0503D    Y02669
INVAL    MVI   XAE,CMCD05     * CODE FOR 'COMMAND INVALID'     * Y02669
         SPACE 2
**************************************************************** Y02669
*                                                              * Y02669
*       EXIT CODE FOR BRANCH TO:                               * Y02669
*                                                              * Y02669
*              MESSAGE MODULE         IEE0503D                 * Y02669
*                                                              * Y02669
**************************************************************** Y02669
CMBRCH5F EQU   *                                               @G17FPSS
         MVC   XAV(D8),CANC       MOVE CANCEL VERB TO MESSAGE  @G17FPSS
         CLI   XAN,FORCE          THIS A FORCE CMD ?           @G17FPSS
         BNE   CMBRCH5            NO                           @G17FPSS
         MVC   XAV(D8),FORCNAM    YES, MOVE FORCE INTO MSG     @G17FPSS
CMBRCH5  EQU   *                                                 Y02669
         L     R11,VCON05         GET ADDRESS OF IEE0503D        Y02669
         BR    R11                GO BRANCH TO MESSAGE MODULE    Y02669
         EJECT
***********************************************************************
*                                                                     *
*                       DEFINED CONSTANTS                             *
*                                                                     *
***********************************************************************
         DS    0F
VCON05   DC    V(IEE0503D)        ADDRESS OF MESSAGE MODULE      Y02669
CDUMP    DC    X'00000122'        CANCEL WITH A DUMP             Y02669
CNODUMP  DC    X'00000222'        CANCEL WITHOUT A DUMP          Y02669
FOUR     DC    F'4'               CONSTANT FOR ASID CONVERSION   Y02657
CANC     DC    C'CANCEL  '        CANCEL COMMAND                   O102
FORCNAM  DC    C'FORCE   '        FORCE COMMAND                @G17FPSS
PDUMP    DC    C'DUMP'            DUMP COMPARAND                   O102
UEQUAL   DC    C'U='              TS USER INDICATOR              Y02669
CMMVCX   MVC   XAX(1),0(R9)
         SPACE
POSTER   POST  MF=L                LIST FORM OF MACRO            Y02657
         SPACE
POSTLEN  EQU   *-POSTER            LENGTH OF PARMLIST FOR MOVE   Y02657
         SPACE
* ENQ PARAMETER LIST FOR CSCB RESOURCE                           Y02651
ENQCSCB  ENQ   (IEECSCB,IEESQ10,S,3,SYSTEM),MF=L                 Y02651
LNE      EQU   *-ENQCSCB           LENGTH OF ENQ PARM LIST       Y02651
         SPACE
* DEQ PARAMETER LIST FOR CSCB RESOURCE                           Y02651
DEQCSCB  DEQ   (IEECSCB,IEESQ10,3,SYSTEM),MF=L                   Y02651
LND      EQU   *-DEQCSCB           LENGTH OF DEQ PARM LIST       Y02651
         SPACE
IEECSCB  DC    C'SYSIEFSD'         QNAME                         Y02651
IEESQ10  DC    C'Q10'              RNAME                         Y02651
         SPACE
PATCH    DC    CL50'***   IEE3703D  PATCH AREA  ***'             Y02669
         TITLE 'IEE3703D--XSA MAP'
CMXSA    DSECT
        IEEXSA
         TITLE 'IEE3703D--CVT MAP'
CMCVT    DSECT
         CVT   DSECT=NO                                          Y01012
         TITLE 'IEE3703D--MASTER RESIDENT CORE MAP'
         IEEBASEA
         TITLE 'IEE3703D--CSCB MAP'
CMCHAIN  DSECT
         IEECHAIN
         TITLE  'IEE3703D--PREFIX STORAGE AREA MAP'
         IHAPSA
         TITLE 'IEE3703D--ADDRESS SPACE VECTOR TABLE MAP'
         IHAASVT                                                 Y02657
         TITLE 'IEE3703D--UNIT CONTROL MODULE(UCM)'            @G17FPSS
         IEECUCM  FORMAT=NEW                                   @G17FPSS
         END
