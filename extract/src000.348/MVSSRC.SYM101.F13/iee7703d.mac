         TITLE 'IEE7703D SVC 34 K V COMMAND '
***********************************************************************
*                                                                     *
*   MODULE NAME         =      IEE7703D                         Y02669*
*                                                                     *
*   DESCRIPTIVE NAME    =      CONTROL VARY (K V) COMMAND       Y02669*
*                              PROCESSOR                        Y02669*
*                                                                     *
*   COPYRIGHT           =      N/A                              Y02669*
*                                                                     *
*   STATUS              =      01 - UPDATE FOR VS/2 RELEASE 2   Y02669*
*                                                                     *
*   FUNCTION            =      TO VALIDITY CHECK AND PROCESS    Y02669*
*                              THE K V (CONTROL VARY) COMMAND.  Y02669*
*                                                                     *
*      OPERATION        =      THE COMMAND IS FIRST VALIDITY    Y02669*
*                              CHECKED:  K  V,USE=              Y02669*
*                              IF INVALID 'IEE156I INVALID      Y02669*
*                              OPERAND' IS ISSUED.              Y02669*
*                              THEN THE PARAMETERS ARE          Y02669*
*                              PROCESSED:                       Y02669*
*                              THE TARGET CONSOLE IS VARIED TO: Y02669*
*                               SD - MONITOR ONLY STATUS        Y02669*
*                                    DISPLAYS (RESULT OF TRACK  Y02669*
*                                    AND DISPLAY COMMANDS)      Y02669*
*                               FC - FULL CAPABILITY INPUT/     Y02669*
*                                    OUTPUT MODE                Y02669*
*                               MS - MONITOR THE DISPLAY OF     Y02669*
*                                    NORMAL OPERATOR MASSAGES   Y02669*
*                              AND THE PROPER BITS ARE SET IN   Y02669*
*                              THE UCME AND RDCM ACCORDINGLY.   Y02669*
*                                                                     *
*                              IF NECESSARY, DIDOCS IS POSTED   Y02669*
*                              TO COMPLETE THE PROCESSING.      Y02669*
*                                                                     *
*   NOTES                                                       Y02669*
*                                                                     *
*      CHARACTER CODE                                           Y02669*
*       DEPENDENCIES    =      ANY CHARACTER CODE OTHER THAN    Y02669*
*                              EBCDIC WILL REQUIRE REASSEMBLY   Y02669*
*                              OF THIS MODULE.                  Y02669*
*                              NOTE: THIS MODULE USES CHARACTER Y02669*
*                              REPRESENTATION FOR COMMA.        Y02669*
*                                                                     *
*      DEPENDENCIES     =      IEE7503D WILL VERIFY THAT ALL    Y02669*
*                              TARGET CONSOLES ARE GRAPHICS.    Y02669*
*                                                                     *
*      RESTRICTIONS     =      THE COMMAND IS INVALID IF:       Y02669*
*                              THE TARGET CONSOLE IS THE MASTER Y02669*
*                              THE TARGET CONSOLE IS A 2250     Y02669*
*                              FC ISSUED FOR OUTPUT-ONLY DEVICE Y02669*
*                              MS ISSUED WITHOUT PAPER CONSOLE  Y02669*
*                              ON-LINE                          Y02669*
*                                                                     *
*                              THE COMMAND IS APPLICABLE ONLY   Y02669*
*                              FOR A 2260 0R 3270.              Y02669*
*                                                                     *
*                              ONLY A FC CONSOLE CAN K V ITSELF Y02669*
*                              AND ONLY IF THERE WILL STILL BE  Y02669*
*                              AN INPUT CONSOLE AVAILABLE.      Y02669*
*                                                                     *
*      REGISTER                                                 Y02669*
*       CONVENTIONS     =      NONE                             Y02669*
*                                                                     *
*      PATCH-LABEL      =      PATCH                            Y02669*
*                                                                     *
*   MODULE TYPE         =      CSECT                            Y02669*
*                                                                     *
*      PROCESSOR        =      N/A                              Y02669*
*                                                                     *
*      MODULE SIZE      =      X'19D' + X'48' PATCH             Y02669*
*                                                                     *
*      ATTRIBUTES       =      REENTRANT, REFRESHABLE, KEY 0,   Y02669*
*                              SUPERVISOR MODE, PAGED LPA       Y02669*
*                                                                     *
*   ENTRY POINT         =      IEE7703D FROM IEE6703D           Y02669*
*      PURPOSE          =      TO PROCESS THE K V COMMAND       Y02669*
*                              FOR CRT DEVICES ONLY             Y02669*
*      LINKAGE          =      BRANCH                           Y02669*
*      INPUT DATA       =      REG 2 POINTS TO XSA              Y02669*
*                                XAS HAS LOCATION ROUTING INFO  Y02669*
*                                    XASCID - CONSOLE ID OR XAU Y02669*
*                                    XASDID - AREA ID           Y02669*
*                                    XASSDS5 - ON LOCKS HELD    Y02651*
*                                XAU CONTAINS ISSUING CONSOLE IDY02669*
*                                XAL POINTS TO THE OPERANDS     Y02669*
*                              REG 14 CONTAINS RETURN ADDRESS   Y02669*
*                                                                     *
*      REGISTERS SAVED  =      REG 14 IS SAVED IN XSA. (XAP)    Y02669*
*                              REGISTERS 11 - 14 ARE SAVED      Y02651*
*                              BEFORE ISSUING SETLOCK AT        Y02651*
*                              LOCKSOFF.                        Y02651*
*                                                                     *
*      REGISTER CONTENTS                                        Y02669*
*      DURING PROCESSING=      R0    WORK REG                   Y02669*
*                              R1    WORK REG                   Y02669*
*                              R2    XSA BASE REG               Y02669*
*                              R3    UNUSED                     Y02669*
*                              R4    OPERAND POINTER            Y02669*
*                              R5    UNUSED                     Y02669*
*                              R6    UNUSED                     Y02669*
*                              R7    UNUSED                     Y02669*
*                              R8    UCME BASE REG              Y02669*
*                              R9    UNUSED                     Y02669*
*                              R10   RESIDENT DCM BASE REG      Y02669*
*                              R11   UNUSED                     Y02669*
*                              R12   UCM BASE REG               Y02669*
*                              R13   MODULE BASE REG            Y02669*
*                              R14   RETURN REG                 Y02669*
*                              R15   BRANCH REG                 Y02669*
*      REGISTERS                                                Y02669*
*       RESTORED        =      R14 RESTORED AT LABEL RETURN1    Y02669*
*                              REGISTERS 11 - 14 ARE RESTORED   Y02651*
*                              AFTER ISSUING SETLOCK AT         Y02651*
*                              LOCKSOFF.                        Y02651*
*                                                                     *
*  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *
*                                                                     *
**  EXIT - NORMAL       =      RETURN FROM RETURN1 -> RETURN    Y02669*
*      CONDITIONS       =      IF NO PROCESSING IS NECESSARY    Y02669*
*                              FOR MS OR SD, I.E. THE CONSOLE   Y02669*
*                              IS ALREADY IN THE DESIRED STATE. Y02669*
*      OUTPUT DATA      =      CMS AND LOCAL LOCKS RELEASED     Y02651*
*                              REG 2 POINTS TO XSA              Y02669*
*                                 XASSDS5 - OFF LOCKS RELEASED  Y02651*
*      RETURN CODES     =      NONE                             Y02669*
*                                                                     *
*  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *
*                                                                     *
**  EXIT - NORMAL       =      POST DIDOCS & RETURN VIA BR R14  Y02669*
*                              FROM SETPOST -> RETURN           Y02669*
*      CONDITIONS       =      TO HAVE THE REQUEST FULFILLED    Y02669*
*                              BY DIDOCS                        Y02669*
*                              AFTER RELEASING THE LOCKS        Y02651*
*                              POST UCMECB (POSTCODE = X'FD')   Y02669*
*                              FOR DIDOCS PROCESSING AND        Y02669*
*                              RETURN VIA BR 14.                Y02669*
*      OUTPUT DATA      =      PROPER BITS SET IN UCM ENTRY TO  Y02669*
*                              INDICATE REQUESTED CONSOLE STATE Y02669*
*                                SD - UCMDISP = UCMDISPG  X'02' Y02669*
*                                               UCMDISPD  X'10' Y02669*
*                                     UCMATR = UCMIF X'40'(OFF) Y02669*
*                                     UCMSDS5 = UCMSDS5G  X'02' Y02669*
*                                MS - UCMDISP = UCMDISPF  X'04' Y02669*
*                                               UCMDISPD  X'10' Y02669*
*                                     UCMSDS5 = UCMSDS5G  X'02' Y02669*
*                                     UCMATR = UCMIF X'40'(OFF) Y02669*
*                                FC - UCMDISP = UCMDISPE  X'08' Y02669*
*                                     UCMATR = UCMIF      X'40' Y02669*
*                                     UCMDISP = UCMDISPD  (OFF) Y02669*
*                                             = UCMDISPF  (OFF) Y02669*
*                                             = UCMDISPG  (OFF) Y02669*
*                                     UCMSDS5 = UCMSDS5G  (OFF) Y02669*
*                              AND BITS SET IN DCM INDICATING   Y02669*
*                                K V COMMAND                    Y02669*
*                                     DCMR3FLG = DCMKVIP        Y02669*
*                                AND CHANGING STATUS            Y02669*
*                                     DCMR3FLG = DCMSTSWT.      Y02669*
*                                REINIT WTO HEADERS TO SYSGEN   Y02669*
*                                VALUE (HARDCOPY & JOBNAME)     Y02669*
*                                     UCMDISP2 = UCMDISPI (OFF) Y02669*
*                                                UCMDISPJ (OFF) Y02669*
*                              REG 2 POINTS TO XSA              Y02669*
*                                XASSDS5 - OFF LOCKS RELEASED   Y02651*
*      RETURN CODES     =      NONE                             Y02669*
*                                                                     *
*  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *
*                                                                     *
**  EXIT - ERROR        =      BRANCH TO IEE5603D FROM D5603D   Y02669*
*      CONDITIONS       =      TO ISSUE MESSAGES FOR:           Y02669*
*                                INVALID OPERAND(S)             Y02669*
*                                TARGET CONSOLE MASTER OR 2250  Y02669*
*                                FC ISSUED FOR OUTPUT ONLY      Y02669*
*                                MS ISSUED WHEN NO PAPER CONSOLEY02669*
*      OUTPUT DATA      =      REG 2 POINTS TO XSA CONTAINING   Y02669*
*                              ERROR CODES FOR IEE5603D:        Y02669*
*                              XAE CONTAINS THE MESSAGE INDEX   Y02669*
*                                X'04' - IEE156I INVALID        Y02669*
*                                        OPERAND                Y02669*
*                                X'08' - IEE345I AUTHORITY      Y02669*
*                                        INVALID                Y02669*
*                                X'09' - IEE921I CM OP          Y02669*
*                                        REJECTED               Y02669*
*                                X'0C' - IEE926I L=CCA OPERAND  Y02669*
*                                        INVALID                Y02669*
*                              XASTSAVE  CONTAINS THE SECONDARY Y02669*
*                                        THE MESSAGE INDEX      Y02669*
*                                X'1C' - DEVICE NOT SUPPORTED   Y02669*
*                                        (IEE921I)              Y02669*
*                                X'1D' - OUTPUT ONLY            Y02669*
*                                        (IEE926I)              Y02669*
*                              XAS CONTAINS TARGET INFO (INPUT) Y02669*
*                                XASSDS5 - STILL ON LOCKS HELD  Y02651*
*      RETURN CODES     =      NONE                             Y02669*
*                                                                     *
*  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *  *
*                                                                     *
*   EXTERNAL                                                    Y02669*
*    REFERENCES                                                 Y02669*
*                                                                     *
*      ROUTINES         =      POSTS DIDOCS                     Y02669*
*                              BRANCHES TO IEE5603D             Y02669*
*                                                                     *
*      DATA AREAS       =      NONE                             Y02669*
*                                                                     *
*      CONTROL BLOCKS   =      CVT      R - ADDRESS UCM         Y02669*
*                              IEEXSA   R - PARM LIST & L=CCA   Y02669*
*                                       W - ERR MESSAGE CODES   Y02669*
*                              IEECUCM  R - CONSOLE STATUS      Y02669*
*                                       W - CHANGED STATUS BITS Y02669*
*                              IEERDCM  R - CONSOLE DEVICE TYPE Y02669*
*                                       W - SHOW K V & CHANGE   Y02669*
*                              IHAPSA   NECESSARY FOR SETLOCK   Y02651*
*                                                                     *
*      TABLES           =      NONE                             Y02669*
*                                                                     *
*      MACROS           =      MODID                            Y02669*
*                              POST UCMECB POSTCODE X'FD'       Y02669*
*                              SETLOCK - CMS AND LOCAL RELEASED Y02651*
*                                                                     *
*      ENQUEUE RESOURCES=      NONE                             Y02669*
*                                                                     *
*      LOCKS HELD       =      LOCKS OBTAINED IN IEE7503D ARE   Y02651*
*                              RELEASED BEFORE POSTING DIDOCS   Y02651*
*                              AT SETPOST OR BEFORE RETURNING   Y02651*
*                              AT RETURN1.                      Y02651*
*                                                                     *
*      CHANGE LEVEL     =      Y02669                           Y02669*
*                              Y02651                           Y02651*
*                              YM1915                           YM1915*
*                                                                     *
*   MESSAGES            =      THE FOLLOWING MESSAGES ARE       Y02669*
*                              CONTAINED IN AND ISSUED FROM     Y02669*
*                              IEE5603D AND IEE5903D:           Y02669*
*                                                                     *
*                               MSGID  CODE  IN MESSAGE TEXT    Y02669*
*                               _____  ____  __ _______ ____    Y02669*
*                              IEE156I X'04' 56 INVALID OPERAND Y02669*
*                              IEE345I X'08' 56 AUTHORITY       Y02669*
*                                                 INVALID       Y02669*
*                              IEE921I X'09' 56 REJECTED        Y02669*
*                                      X'1C' 59 DEVICE NOT      Y02669*
*                                                 SUPPORTED     Y02669*
*                              IEE926I X'0C' 56 L OPERAND       Y02669*
*                                                 INVALID       Y02669*
*                                      X'1D' 59 'CC' OUTPUT     Y02669*
*                                                 ONLY          Y02669*
*                                                                     *
*   SYSGEN              =      INCLUDED WITH SVC 34 IN LOAD     Y02669*
*                              MODULE IGC0003D, DLIB AOSB3,     Y02669*
*                              MACRO SGIEF441, TARGLIB LPALIB   Y02669*
*                                                                     *
*   SYSTEM LIBRARIES    =      NONE                             Y02669*
*                                                                     *
***********************************************************************
         EJECT
IEE7703D CSECT
***********************************************************************
*                                                                     *
*                        REGISTERS                                    *
*                                                                     *
***********************************************************************
R0       EQU   0                   WORK REG
R1       EQU   1                   WORK REG
PARMREG  EQU   2                   XSA BASE REG
R3       EQU   3      (UNUSED)     UNUSED                        Y02669
KOPS     EQU   4                   OPERAND POINTER
R5       EQU   5      (UNUSED)     UNUSED                        Y02669
R6       EQU   6      (UNUSED)     UNUSED                        Y02669
R7       EQU   7      (UNUSED)     UNUSED                        Y02669
UCMEBASE EQU   8                   UCM EMTRY BASE REG
R9       EQU   9      (UNUSED)     UNUSED                        Y02669
RDCMBASE EQU   10                  RESIDENT DCM BASE REG
R11      EQU   11     (UNUSED)     UNUSED                        Y02669
UCMBASE  EQU   12                  UCM BASE REG
MYBASE   EQU   13                  MODULE BASE REG
RE       EQU   14                  RETURN REG
R15      EQU   15                  BRANCH REG
         EJECT
***********************************************************************
*                                                                     *
*                 ERROR MESSAGE FLAG BIT SETTINGS                     *
*                                                                     *
***********************************************************************
IEE156I  EQU   X'04'               IEE156I INVALID OPERAND
IEE345I  EQU   X'08'               IEE345I INVALID AUTHORITY
IEE921I  EQU   X'09'               IEE921I REJECTED
ERR28    EQU   X'1C'                       DEVICE NOT SUPPORTED
IEE926I  EQU   X'0C'               IEE926I L OPERAND INVALID
ERR29    EQU   X'1D'                       CC OUTPUT ONLY
***********************************************************************
*                                                                     *
*                 DISPLACEMENTS AND INCREMENTS                        *
*                                                                     *
***********************************************************************
N0       EQU   0                   FIELD INCREMENT
N1       EQU   1                   FIELD INCREMENT
N2       EQU   2                   FIELD INCREMENT
N3       EQU   3                   FIELD INCREMENT               Y02669
N4       EQU   4                   FIELD INCREMENT
N5       EQU   5                   FIELD INCREMENT
***********************************************************************
*
*                           CONSTANTS
*
***********************************************************************
M7       EQU   7                   MASK FOR ICM INSTRUCTION      Y02669
PTRCVT   EQU   16                  ADDR OF CVT
ZERO     EQU   X'00'               ZERO CONSTANT
COMMA    EQU   C','                COMMA CHARACTER CONSTANT
XFF      EQU   255                 MASK FOR TURNING OFF BITS     Y02669
POSTCOD1 EQU   X'FD'               CODE USED IN POSTING UCM ECB
         EJECT
***********************************************************************
*                                                                     *
*                     ESTABLISH ADDRESSABILITY                        *
*                                                                     *
*           ON ENTRY, REGISTER 2 CONTAINS ADDRESS OF XSA              *
*            TARGET CONSOLE IS ALWAYS A GRAPHICS DEVICE          Y02669
*            CHECK MADE IN IEE7503D                              Y02669
*                                                                     *
***********************************************************************
         SPACE 2
         BALR  MYBASE,N0           ESTABLISH ROUTINE 'S
         USING *,MYBASE            BASE REGISTER
         MODID BRANCH=YES                                        Y01886
         USING FAUXSA,PARMREG      ESTABLISH XSA ADDRESSABILITY
         L     UCMBASE,PTRCVT      LOAD CVT POINTER              Y02669
         USING CVTSECT,UCMBASE     BASE CVT REG
         L     UCMBASE,CVTCUCB     GET UCM POINTER FROM CVT
         DROP  UCMBASE                                           Y20669
         USING UCM,UCMBASE         ESTABLISH UCM ADDRESSABILITY  Y02669
         USING UCMLIST,UCMEBASE    BASE UCM ENTRY LIST
         USING DCMTSRT,RDCMBASE    ESTABLISH RDCM BASE REG       Y02669
         ST    RE,XAP              SAVE RETURN POINTER
         EJECT
***********************************************************************
*                                                                     *
*    FIND AND BASE TARGET UCM ENTRY AND RESIDENT DCM (ALWAYS CRT)     *
*    REG 1-TARGET CONSOLE ID RIGHT JUSTIFIED, HIGH ORDER BYTES ZERO   *
*    OUTPUT-POINTER TO TARGET UCMENTRY IN UCMEBASE REG                *
*    REG USAGE-DESTROYS CONTENTS OF: REG 0,1,UCMEBASE                 *
*                                                                     *
***********************************************************************
         SPACE 2
         SR    R1,R1               CLEAR REG
         IC    R1,XASCID           LOAD TARGET CONSOLE ID
         BCTR  R1,ZERO             SUBTRACT ONE FROM CONSOLE ID
         L     UCMEBASE,UCMVEA     POINT TO FIRST UCM ENTRY
         M     R0,UCMVEZ           MULTIPLY ENTRY SIZE BY REDUCED ID
         AR    UCMEBASE,R1         POINT TO DESIRED UCM ENTRY
         L     RDCMBASE,UCMXB      LOAD ADDRESS OF RESIDENT DCM
         EJECT
***********************************************************************
*                                                                     *
*                      VALIDITY CHECK THE COMMAND                     *
*                                                                     *
*  ERR - IEE156I INVALID OPERAND - IF SYNTAX CHECK FAILS         Y02669
***********************************************************************
         SPACE 2
VALID    EQU   *
         L     KOPS,XAL            LOAD ADDRESS OF OPERANDS
         LA    KOPS,N0(KOPS)       CLEAR HIGH ORDER BYTE
         ST    KOPS,XAD            SAVE CURSOR POSITION          Y02669
         LA    KOPS,N1(KOPS)       INCREMENT OPERAND POINTER
         CLI   N0(KOPS),COMMA      IS SECOND CHARACTER A COMMA?
         BNE   BADKOP              NO, IEE156I INVALID OPERAND
         SPACE 2
         LA    KOPS,N1(KOPS)       INCREMENT OPERAND POINTER     Y02669
         CLC   N0(N4,KOPS),USE     IS NEXT 4 CHARS 'USE='?       Y02669
         BNE   BADKOP              NO, IEE156I INVALID OPERAND
         SPACE 2
***********************************************************************
*                                                                     *
*          TARGET CONSOLE CAN NOT BE MASTER CONSOLE OR A 2250         *
*                                                                     *
*  ERR - IEE345I AUTHORITY INVALID - IF TARGET IS MASTER CONSOLE Y02669
*        IEE921I REJECTED COMMAND-DEVICE NOT SUPPORTED - IF 2250 Y02669
*                                                                     *
***********************************************************************
         SPACE
         TM    UCMDISP,UCMDISPA    IS TARGET THE MASTER CONSOLE?
         BO    NOTMAST             YES, ERROR - CAN'T V MASTER
*                                  IEE345I AUTHORITY INVALID     Y02669
         SPACE 2
         TM    DCMDEVTY,DCMTY50    IS TARGET A 2250?
         BO    ERRHARD             YES, ERROR - CAN'T V 2250
*                                  IEE921I DEVICE NOT SUPPORTED
         EJECT
***********************************************************************
*        VALIDITY CHECK AND VARY CONSOLE TO FULL CAPABILITY      Y02669
***********************************************************************
         SPACE 2
         LA    KOPS,N4(KOPS)       INCREMENT OPERAND POINTER     Y02669
SETFC    EQU   *                                                 Y02669
         CLC   N0(N3,KOPS),FC      WAS FC SPECIFIED? (& BLANK)   Y02669
         BNE   SETSD               NO
         SPACE
         TM    UCMDISP,UCMDISPD    IS CONSOLE HDWARE OUTPUT ONLY?
         BZ    FCCONS              NO, OK TO CHANGE MODE
         SPACE
         TM    UCMSDS5,UCMSDS5G    YES, IS CONSOLE REALLY OUTPUT ONLY?
         BZ    OUTERROR            IF OFF, OUTPUT ONLY HARDWARE
*                                  IT'S IN OUTPUT-ONLY MODE BUT NOT AN
*                                  INPUT-OUTPUT DEVICE IN OUTPUT-ONLY.
*                                  MUST HAVE BOTH IN & OUT CAPABILITY
*                                  TO SERVE AS FULL CAPABILITY
         SPACE  2
***********************************************************************
*   TURN OFF FLAGS IN TARGET UCM ENTRY:                          Y02669
*        UCMDISPF - CONSOLE IS MESSAGE STREAM ONLY               Y02669
*        UCMDISPG - CONSOLE IS STATUS DISPLAY ONLY               Y02669
*        UCMDISPD - OUTPUT ONLY                                  Y02669
*        UCMSDS5G - I/O HARDWARE IN OUTPUT-ONLY STATUS           Y02669
*   TURN ON FLAGS IN TARGET UCM ENTRY:                           Y02669
*        UCMIF    - INPUT-OUTPUT CAPABILITY                      Y02669
*        UCMDISPE - FULL CAPABILITY MODE                         Y02669
***********************************************************************
         SPACE 2
FCCONS   EQU   *
         NI    UCMDISP,XFF-UCMDISPF-UCMDISPG-UCMDISPD            Y02669
         NI    UCMSDS5,XFF-UCMSDS5G                              Y02669
         OI    UCMATR,UCMIF        INDICATE I/O STATUS TO MCS
         OI    UCMDISP,UCMDISPE    SET FULL CAPABILITY BIT ON
         B     SETPOST             NOTIFY DIDOCS CHANGING STATUS
         EJECT
***********************************************************************
*            VALIDITY CHECK AND VARY CONSOLE TO STATUS DISPLAY   Y02669
***********************************************************************
         SPACE 2
SETSD    EQU   *                                                 Y02669
         CLC   N0(N3,KOPS),SD      WAS SD SPECIFIED? (& BLANK)   Y02669
         BNE   SETMS               NO                            Y02669
         TM    UCMDISP,UCMDISPG    IS IT ALREADY IN SD MODE?
         BO    RETURN1             YES, NO WORK REQUIRED
         SPACE
         OI    UCMDISP,UCMDISPG    NO, SET STATUS DISPLAY BIT ON
         NI    UCMDISP,XFF-UCMDISPE-UCMDISPF  TURN FC & MS OFF   Y02669
         B     SETOUT              NOTIFY DIDOCS CHANGING STATUS
         EJECT
***********************************************************************
*            VALIDITY CHECK AND VARY CONSOLE TO MESSAGE STREAM   Y02669
***********************************************************************
         SPACE 2
SETMS    EQU   *                                                 Y02669
         CLC   N0(N3,KOPS),MS      WAS MS SPECIFIED? (& BLANK)   Y02669
         BNE   BADFNS              NO, IEE156I INVALID OPERAND   YM1915
         SPACE
         TM    UCMDISP,UCMDISPF    IS IT ALREADY IN MS MODE?
         BO    RETURN1             YES, NO WORK REQUIRED
         SPACE
         TM    UCMMODE,UCMOGCE     ONLY CRT CONSOLES IN SYSTEM?
         BO    ERRHARD             YES, ERR - MUST HAVE 1 PAPER
         SPACE
         OI    UCMDISP,UCMDISPF    NO, SET MSG STREAM BIT ON
         NI    UCMDISP,XFF-UCMDISPE-UCMDISPG  TURN FC & SD OFF   Y02669
         SPACE 2
***********************************************************************
*  SET ALL BITS NECESSARY TO VARY A CONSOLE TO OUTPUT-ONLY MODE  Y02669
*  FOR STATUS DISPLAY AND MESSAGE STREAM:                        Y02669
*       UCMSDS5G - INPUT-OUTPUT CAPABILITIES BUT IN OUTPUT MODE  Y02669
*       UCMDISPD - SOFTWARE OUTPUT ONLY MODE                     Y02669
***********************************************************************
SETOUT   EQU   *
         NI    UCMATR,XFF-UCMIF    INDICATE OUTPUT ONLY TO MCS   Y02669
         TM    UCMSDS5,UCMSDS5G    IS DEVICE IN OUTPUT ONLY MODE?
         BO    SETPOST             YES
         SPACE
         TM    UCMDISP,UCMDISPD    NO, IS THIS AN OUTPUT DEVICE?
         BO    SETPOST             YES
         SPACE
         OI    UCMDISP,UCMDISPD    NO, FORCE OUTPUT ONLY
         OI    UCMSDS5,UCMSDS5G    SET OUTPUT ONLY BIT ON
         EJECT
***********************************************************************
*                                                                     *
*    TURN ON BITS IN RESIDENT DCM TO TELL DIDOCS WHAT TO DO      Y02669
*       DCMSTSWT - CHANGING STATUS OF OUTPUT ONLY CONSOLE        Y02669
*       DCMKVIP - ENTRY AS THE RESULT OF K VARY COMMAND          Y02669
*    REINIT HARDCOPY AND JOBNAME BITS TO SYSGEN VALUE            Y02669
*    INDICTAES ALL HEADER INFORMATION ON MESSAGE SENT TO THE     Y02669
*    CONSOLE SHOULD BE DELETED I.E. R.C. + TIME STAMP + JOBNAME  Y02669
*                                                                     *
* NOTE: LOCKS ARE ALWAYS HELD ON ENTRY TO IEE7703D               Y02651
*    AND MUST BE RELEASED BEFORE POSTING DIDOCS OR               Y02651
*    RETURNING CONTROL TO THE SYSTEM                             Y02651
***********************************************************************
         SPACE 3
SETPOST  EQU   *
         OI    DCMR3FLG,DCMSTSWT+DCMKVIP
         NI    UCMDISP2,XFF-UCMDISPI-UCMDISPJ                    Y02669
         BAL   R15,LOCKSOFF        GO RELEASE LOCKS AND          Y02651
         POST  UCMECB,POSTCOD1     POST THE ECB
         B     RETURN              THEN RETURN TO SYSTEM         Y02651
         SPACE 3
RETURN1  EQU   *
         BAL   R15,LOCKSOFF                                      Y02651
RETURN   EQU   *                                                 Y02651
         L     RE,XAP                                            Y02669
         BR    RE                  RETURN TO CALLER
         SPACE 2
LOCKSOFF EQU   *                                                 Y02651
         STM   R11,RE,XASAVLOC     SAVE REGS AROUND SETLOCK      Y02651
         SETLOCK RELEASE,TYPE=CMS,RELATED=(K,IEE7503D(LOCKSET))
         SETLOCK RELEASE,TYPE=LOCAL,RELATED=(K,IEE7503D(LOCKSET))
         LM    R11,RE,XASAVLOC     RESTORE REGS AFTER SETLOCK    Y02651
         NI    XASSDS,XFF-XASSDS5  INDICATE LOCKS NOW OFF        Y02651
         BR    R15                                               Y02651
         EJECT
***********************************************************************
*                                                                     *
*                        ERROR EXITS                                  *
*                                                                     *
***********************************************************************
         SPACE  2
NOTMAST  EQU   *                   CAN'T K VARY MASTER CONSOLE
         MVI   XAE,IEE345I         IEE345I AUTHORITY INVALID
         MVC   XASOPTR(N3),XAD+N1  SAVE ADDR INVALID OPERAND   @YM4792P
         B     D5603D              BRANCH TO ERROR EXIT          Y02669
         SPACE  2
OUTERROR EQU   *                   CAN'T VARY OUTPUT ONLY TO FC  Y02669
         MVI   XASTSAVE,ERR29      SECOND ERROR CODE-OUTPUT ONLY Y02669
         MVI   XAE,IEE926I         ERROR CODE - L OPERAND INVALID
         B     STOREOP             BRANCH FOR ERROR EXIT         Y02669
         SPACE  2
ERRHARD  EQU   *                   CAN'T K VARY A 2250
         MVI   XASTSAVE,ERR28      MINOR - DEVICE NOT SUPPORTED  Y02669
         MVI   XAE,IEE921I         ERROR CODE - CM OP REJECTED
         MVC   XASOPTR(N3),XAD+N1  SAVE ADDR OF INVALID OPERAND  Y02669
         B     D5603D              BRANCH FOR ERROR EXIT         Y02669
         SPACE  2
BADFNS   EQU   *                                                 YM1915
         CLI   N2(KOPS),COMMA      IS FNS FOLLOWED BY ,?         YM1915
         BNE   BADKOP              NO, WHOLE OPERAND BAD         YM1915
         LA    KOPS,N2(KOPS)       YES, POINT TO BAD INFO        YM1915
BADKOP   EQU   *
         MVI   XAE,IEE156I         IEE156I INVALID OPERAND
STOREOP  EQU   *
         STCM  KOPS,M7,XASOPTR     SAVE ADDR OF INVALID OPERAND  Y02669
D5603D   EQU   *                                                 Y02669
         L     RE,XAP              RESTORE RETURN ADDRESS        Y02669
         L     R15,VCON5603        GET ADDRESS OF ERR MODULE     Y02669
         BR    R15                 GO ISSUE ERROR MESSAGES       Y02669
         EJECT
***********************************************************************
*                                                                     *
*                            CONSTANTS                                *
*                                                                     *
***********************************************************************
         SPACE  2
VCON5603 DC    V(IEE5603D)         SVC 34 MSG MOD NAME           Y02669
USE      DC    C'USE='             CONSTANT FOR VARY COMMAND
FC       DC    C'FC '              FULL CAPCITY CONSTANT         Y02669
SD       DC    C'SD '              STATUS DISPLAY CONSTANT       Y02669
MS       DC    C'MS '              MASTER CONSOLE CONSTANT       Y02669
         SPACE  5
***********************************************************************
*                                                                     *
*                         PATCH AREA                                  *
*                                                                     *
***********************************************************************
         SPACE  2
PATCH    DC    C'****PATCH AREA****'
PATCH2   DC    C'IEE7703D-TEST ONLY'
PATCH3   DC    36C' '
         EJECT
         IEECUCM FORMAT=NEW
         EJECT
         IEERDCM
         EJECT
FAUXSA   DSECT
         IEEXSA
         EJECT
CVTSECT  DSECT
         CVT
         EJECT
         IHAPSA
         END
