         TITLE 'IEAVERER- RESTART FLIH RECOVERY ROUTINE'
* /* START OF SPECIFICATIONS ****
*
*01*  MODULE-NAME = IEAVERER
*
*02*     CSECT-NAME = IEAVERER
*
*01*  DESCRIPTIVE-NAME = RESTART FLIH RECOVERY ROUTINE
*
*01*  COPYRIGHT = NONE
*
*01*  STATUS =     VERSION 1
*
*01*  FUNCTION =
*        WHEN RECOVERY IS INITIATED FOR THE RESTART FIRST LEVEL
*        INTERRUPT HANDLER, THE SUPER FRR WILL INDICATE A RETRY
*        TO IEAVERER.  THIS ROUTINE WILL CLEAN UP THE RESTART
*        FLIH RESOURCES, MAKING THE FUNCTION OPERABLE.  IT WILL
*        ALLOW PERCOLATION TO THE PREVIOUS LEVEL OF RECOVERY.
*
*02*     OPERATION =
*             IEAVERER ENTRY =
*                  CLEAR THE RESTART FLIH RECURSION INDICATOR;
*                  CLEAR THE RESTART RESOURCE LOCK WORD;
*                  FLIP THE FRR STACK TO THE PREVIOUS CURRENT STACK;
*                  SET ABEND CODE IN REG 1 FOR SVC 13;
*                  EXIT TO IEAVEABD TO PERFORM ABEND;
*
*01*   NOTES =
*
*02*     DEPENDENCIES =
*             RTM:
*             RECOVERY IS CONTROLLED BY USE OF THE FRR STACK MECHANISM.
*
*             IEAVESVR:
*             THE SVC FLIH RECOVERY ROUTINE CONTAINS A COMMON
*             SECTION, STARTING AT THE EXTERNAL LABEL IEAVEABD,
*             WHICH WILL ABEND THE CURRENT TASK, AND PERCOLATE
*             THE ERROR.
*
*03*        CHARACTER-CODE-DEPENDENCIES =   NONE
*
*02*     RESTRICTIONS = NONE
*
*02*     REGISTER-CONVENTIONS = SEE EQUATE SECTION OF MODULE
*
*02*     PATCH-LABEL =
*
*01*  MODULE-TYPE = PROCEDURE
*
*02*     PROCESSOR = ASSEMBLER
*
*02*     MODULE-SIZE = 36 BYTES
*
*02*     ATTRIBUTES =   NUCLEUS
*                       REUSABLE
*                       REENTRANT
*                       DISABLED
*                       ZERO PROTECT KEY
*                       SUPERVISOR MODE
*
*01*  ENTRY-POINT =     IEAVERER
*
*02*     PURPOSE =  THIS IS THE ONLY ENTRY TO IEAVERER
*
*02*     LINKAGE =  SUPER FRR WILL INDICATE A RETRY TO IEAVERER
*                   RTM WILL PASS CONTROL VIA BR 15.
*
*02*     INPUT =
*
*03*          ENVIRONMENT =  DISABLED
*                            SUPERVISOR
*                            KEY 0
*                            NO LOCKS HELD
*
*03*          DATA = DATA AREAS IN PSA AND CVT
*
*03*          REGISTERS = NONE
*
*02*     OUTPUT =
*
*03*          DATA =    CURRENT FRR STACK = PREVIOUS CURRENT
*                       RESTART FLIH RECURSION BYTE CLEARED
*                       RESTART RESOURCE LOCKWORD CLEARED
*
*03*          REGISTERS=
*                       REG 1 = ABEND CODE
*                       ALL OTHERS AS ENTERED
*
*02*     REGISTERS-SAVED =   NONE
*
*02*     REGISTER-USAGE =    REG 1 = ABEND CODE
*
*02*     REGISTERS-RESTORED =  ALL AS ENTRY EXCEPT REG 1
*
*01*  EXIT-NORMAL = TO IEAVEABD WHO ISSUES AN SVC 13
*
*02*     CONDITIONS = THE CURRENT TASK MUST BE TERMINATED FOR
*                     RECOVERY PURPOSES.
*
*02*     OUTPUT =  NONE
*
*02*     RETURN-CODES = NONE
*
*01*  EXIT-ERROR = NONE
*
*01*  EXTERNAL-REFERENCES =
*
*02*     ROUTINES =     TO IEAVEABD (ON EXIT) TO ISSUE AN SVC 13
*
*02*     DATA-AREAS =
*             PSARSAV   --   PREVIOUS CURRENT FRR STACK POINTER
*             PSACSTK   --   CURRENT FRR STACK POINTER
*             PSARECUR  --   RESTART FLIH RECURSION BYTE
*             CVTRSTWD  --   RESTART RESOURCE LOCK WORD
*
*02*     CONTROL-BLOCKS =    PSA R/W
*                            CVT R/W
*
*01*  TABLES = NONE
*
*01*  MACROS = NONE
*
*02*     SERIALIZATION = DISABLE
*
*01*  CHANGE-ACTIVITY = Y021104
*
*01*  MESSAGES = NONE
*
*01*  ABEND-CODES = X'5FC'
*
**** END OF SPECIFICATIONS ***/
         EJECT
IEAVERER CSECT
      SPACE 2
*/*IEAVERER: CHART RES FLIH FRR */
*/* HEADER
*/*
*/*
*/*
*/*                                                 SECTION 3.1.8.3
*/* RESTART FLIH RECOVERY ROUTINE
*/*                                                 PAGE # */
*/* E ENTRY IEAVERER */
*/* P CLEAR THE RESTART FLIH RECURSION INDICATOR */
*/* P CLEAR THE RESTART RESOURCE LOCK WORD */
*/* P FLIP THE FRR STACK TO PREVIOUS CURRENT STACK */
*/* P SET ABEND CODE IN REG 1 FOR SVC 13 */
*/* P EXIT TO IEAVEABD TO PERFORM ABEND */
*/* R IEAVEABD */
*/* FOOTING
*/*
*/*  */
*/*IEAVERER: END RES FLIH  FRR */
         EJECT
***********************************************************************
*                                                                     *
*                            EQUATES                                  *
*                                                                     *
***********************************************************************
RESCODE  EQU   X'5FC'             RESTART FLIH ABEND CODE
R1       EQU   1                  GENERAL REGISTER 1
R15      EQU   15                 GENERAL REGISTER 15
ZERO     EQU   0                  CONSTANT VALUE = 0
CVTBASE  EQU   1                  BASE REGISTER FOR CVT MAPPING
***********************************************************************
*                                                                     *
*                        IEAVERER MAINLINE                            *
*                                                                     *
***********************************************************************
         USING PSA,0
         USING CVT,CVTBASE
         USING *,R15              REG 15 = ENTRY POINT ADDRESS
         L     CVTBASE,FLCCVT     ADDRESS OF CVT
         NI    PSARECUR,ZERO      CLEAR RESTART RECURSION BIT
         XC    CVTRSTWD,CVTRSTWD  CLEAR RESTART LOCK WORD
         MVC   PSACSTK,PSARSAV    CURRENT FRR STACK = PREVIOUS CURRENT
         LA    R1,RESCODE         PUT RESTART FLIH ABEND CODE IN REG 1
         L     R15,ADABEND        BRANCH TO ABEND ROUTINE LOCATED IN
         BR    R15                IEAVESVR (SVC FLIH FRR)
***********************************************************************
*                                                                     *
*                      CONSTANTS & STORAGE                            *
*                                                                     *
***********************************************************************
         EXTRN IEAVEABD
ADABEND  DC    V(IEAVEABD)        ADDRESS COMMON ABEND ROUTINE
         TITLE 'IEAVERER- PSA MAPPING'
         IHAPSA
         TITLE 'IEAVERER- CVT DSECT MAPPING'
         CVT   DSECT=YES,PREFIX=NO,LIST=NO
         END
