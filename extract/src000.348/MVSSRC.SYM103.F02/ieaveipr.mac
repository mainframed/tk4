         TITLE 'IEAVEIPR-IPC RECOVERY'
* /* START OF SPECIFICATION ****
*        MODULE NAME=   IEAVEIPR
*
*             CSECT NAME=    IEAVEIPR
*
*        DESCRIPTIVE NAME=   IPC FUNCTIONAL RECOVERY ROUTINE
*
*        COPYRIGHT=     NONE
*
*        STATUS=   VERSION 1
*
*        FUNCTION=
*                  RISGNL RECOVERY CONSISTS  SOLELY  OF  CLEARING  ITS
*                  BUFFER AND INDICATORS, IF ACR IS NOT ACTIVE SO THAT
*                  RISGNL IS RE-INVOCABLE.
*
*        OPERATION=
*
*             RISGNL=
*                  IF ACR IS NOT ACTIVE
*                       THEN DO;
*                            CLEAR CURRENT PCCA BUFFER;
*                            CLEAR RISIGNAL SUPER BIT;
*                       END;
*                  EXIT;
*
*
*        NOTES=    SEE ENTRY POINT DESCRIPTIONS BELOW
*
*             DEPENDENCIES=
*
*                  RTM=
*                       1. SDWA MUST BE SUPPLIED AND BE POINTED TO  BY
*                            REG 1.
*                       2.  ALL  SDWA  INFORMATION MUST BE PRESENT AND
*                            CORRECT.
*                       3. THE RISGNL FRR MUST ALWAYS RECEIVE  CONTROL
*                            IF IT IS ON THE STACK.
*
*             CHARACTER CODE DEPENDENCIES=  NONE
*
*             RESTRICTIONS=  NONE
*
*             REGISTER CONVENTIONS=    REFERENCE  EQUATE  SECTION   OF
*                                      MODULE.
*
*        MODULE TYPE=   PROCEDURE
*
*        PROCESSOR=     ASSEMBLER
*
*        MODULE SIZE=   REFERENCE ESD LENGTH AT BEGINNING OF MODULE
*
*        ATTRIBUTES=    NUCLEUS
*                       ZERO PROTECT KEY
*                       DISABLED
*                       REFRESHABLE
*                       ADDRESS SPACE = FIXED
*                       SUPERVISOR MODE
*
*
*
*        ENTRY POINT=   IEAVEIP1
*
*             PURPOSE=  THIS  IS  THE  ONLY  ENTRY   TO   THE   RISGNL
*                       FUNCTIONAL  RECOVERY  ROUTINE.   IT WILL CLEAR
*                       ITS BUFFER AND PERCOLATE THE ERROR.
*
*             LINKAGE=  SEE INPUT SECTION BELOW
*
*             INPUT=
*
*                  ENVIRONMENT=   DISABLED
*                                 SUPERVISOR STATE
*                                 KEY 0
*                                 NO LOCKS REQUIRED
*
*                  DATA=     SDWA CONTROL BLOCK
*
*                  REGISTERS=     1  = SDWA ADDRESS
*                                 14 = RETURN ADDRESS
*                                 15 = ENTRY POINT ADDRESS
*
*             OUTPUT=   SEE EXIT -- NORMAL
*
*                  DATA=     NONE
*
*                  REGISTERS=     NONE
*
*        REGISTERS SAVED=    NONE
*
*        REGISTERS USED=     ALL
*
*        REGISTERS RESTORED= REG 14
*
*        EXIT -- NORMAL=     RTM (VIA BR 14)
*
*             CONDITIONS=    EXIT WILL ALWAYS BE BACK TO RTM
*
*             OUTPUT=
*
*             RETURN CODE=   NONE
*
*        EXIT -- ERROR= NONE
*
*        EXTERNAL REFERENCES=
*
*             ROUTINES=
*
*             DATA AREAS=    NONE
*
*             CONTROL BLOCKS=     SDWA R/W
*                                 PCCA W
*                                 PSA  W
*
*        TABLES=   NONE
*
*        MACROS=   NONE
*
*        SERIALIZATION= DISABLE
*
*        MESSAGES= NONE
*
*
*        ABEND CODES=   NONE
*
**** END OF SPECIFICATIONS ***/
*/*IEAVEIPR: CHART IEAVEIPR */
*/* HEADER
*/*
*/*
*/*
*/*                                            SECTION 3.1.8.14
*/* IPC RECOVERY
*/*                                            PAGE # */
         SPACE 2
IEAVEIPR CSECT
         USING PSA,0                   PSA ADDRESSABILITY
         USING PCCA,PCCAREG            PCCA ADDRESSABILITY
         USING LCCA,LCCAREG            LCCA ADDRESSABILITY
         USING SDWA,R1                 SDWA ADDRESSABILITY
*        EQUATE SECTION
R1       EQU   1                       REGISTER 1
RETADD   EQU   4                       RETURN ADDRESS REG
PCCAREG  EQU   9                       PCCA ADDRESS REG
LCCAREG  EQU   11                      LCCA ADDRESS REG
BASEREG  EQU   10                      BASE REGISTER
R14      EQU   14                      REGISTER 14
*        VALUE SECTION
ZERO     EQU   0                       DISPLACEMENT VALUE
L16      EQU   16                      DISPLACEMENT VALUE
         SPACE 2
*/*IEAVEIP1: E RISGNL RECOVERY */
         SPACE 2
         ENTRY IEAVEIP1
IEAVEIP1 DS    0H                      RISGNL RECOVERY ENTRY
         SPACE 2
*/* P ESTABLISH ADDRESSABILITY */
         SPACE 2
         BALR  BASEREG,ZERO            ESTABLISH
         USING *,BASEREG               ADDRESSABILITY
         SPACE 2
*/* D (YES,EXIT,NO,) IS ACR ACTIVE? */
         SPACE 2
         TM    SDWAMCHD,SDWAACR        IS ACR ACTIVE
         BO    EXIT                    IF SO FINISHED
         SPACE 2
*/* P CLEAR PCCA BUFFER FOR RISGNL */
         SPACE 2
         L     PCCAREG,PSAPCCAV        GET PCCA ADDRESS
         XC    PCCAEMSB(L16),PCCAEMSB  CLEAR PCCA BUFFER
         SPACE 2
*/*EXIT: P (,FINI) CLEAR RISGNL SUPER BIT */
         SPACE 2
EXIT     NI    PSASUP2,X'FF'-PSAIPCRI  CLEAR SUPER BIT
         L     LCCAREG,PSALCCAV        GET LCCA ADDRESS
         NI    LCCASPN1,X'FF'-LCCASIGP CLEAR SIGP SPIN BIT
         BR    R14                     RETURN TO RTM
         SPACE 2
*/*FINI: R END RISGNL RECOVERY */
*/*IEAVEIPR: END IPC RECOVERY */
         SPACE 2
         IHAPCCA
         IHASDWA
         IHAPSA
         IHALCCA
         END
