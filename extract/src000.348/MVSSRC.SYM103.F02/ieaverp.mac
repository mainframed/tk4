         TITLE 'IEAVERP - REMOTE PENDABLE SERVICE ROUTINE'
*/*IEAVERP: CHART RPSGNL SR */
*/* HEADER
*/*
*/*
*/*
*/*                                                  ID: 3.1.7.13.10.2
*/* RPSGNL SERVICE ROUTINE
*/*                                                  PAGE # */
*/*IEAVERP: E IEAVERP */
*/* P DISABLE EXT & I/O SAVE SYS MASK IN MODEL STOSM INSTR */
*/* P SAVE REG 1 IN PSA GET LCCA ADDR TO SAVE CALLER'S REGS */
*/* P EST ADDRESSABILITY AND FRR */
*/* D (NO,RPINVPCA,YES,) PCCA ADDR VALID? */
*/* D (NO,RPRC4,YES,) RECEIVING CPU ON LINE? */
*/* P TURN ON PROPER FUNCTION CODE IN RECEIVING CPU PENDABLE BUFFER */
*/* RPSIGPRT: P PLACE EXT CALL FCN CODE IN R0 */
*/* P SAVE CALLERS RETURN REGISTER */
*/* P SAVE OWN REGS & EST. ADDR. FOR IEAVEDR */
*/* S IEAVEDR2:ISSUE EXT. CALL SIGP REQUEST */
*/* D (YES,RPEXIT,NO,) SUCCESS? (RC=0) */
*/* D (YES,RPSIGPRT,NO,) BUSY? (RC=4?) */
*/* RPRCCHK: D (NO,RPEXIT,YES,) STATUS STORED? (RC=8) */
*/* D (NO,RPEXIT,YES,) EXT CALL PENDING? */
*/* P (,RPEXIT) SET RC=0 */
*/*RPINVPCA: P RESTORE CALLER'S REGISTERS */
*/* P RESTORE CALLER'S SYSTEM MASK */
*/*S () ABEND:X'07B' DUMP SYSTEM */
*/*RPRC4: P (,RPEXIT2) SET RC=4 */
*/*RPEXIT: P RESTORE CALLER'S RETURN REGISTER */
*/*RPEXIT2: P RESTORE CALLER'S REGISTERS */
*/* P RESTORE CALLER'S SYSTEM MASK  */
*/* R RETURN */
*/* FOOTING
*/*
*/*  */
*/*IEAVERP: END RPSGNL SR */
*MODULE NAME= IEAVERP
*
*DESCRIPTIVE NAME= INTERPROCESSOR COMMUNICATION REMOTE PENDABLE SERVICE
*                  ROUTINE.
*
*COPYRIGHT= N/A
*
*STATUS= REL. 02.0
*
*FUNCTION= PROVIDES THE USER WITH THE NECESSARY INTERFACE TO SIGNAL
*          (SIGP) OTHER CPUS IN ORDER TO INVOKE A SPECIFIC PROGRAM
*          ON ANY OF THE ONLINE MP CONFIGURED CPUS.  REMOTE PENDABLE
*          SERVICE ROUTINE WILL GENERATE A EXTERNAL CALL SIGNAL
*          TO THE SPECIFIED CPU WHOSE EXTERNAL CALL SLIH WILL
*          ROUTE CONTROL TO THE SPECIFIED PROGRAM.
*
*  OPERATION= THE RP SIGNAL SERVICE ROUTINE TESTS TO SEE IF IT WAS
*             PASSED A VALID PCCA ADDRESS.  IF THE PCCA ADDRESS
*             WAS VALID, THE PHYSICAL ADDRESS IN IN THE PCCA WILL BE
*             USED AS AN OPERAND FOR THE SIGP INSTRUCTION IN ORDER
*             TO DIRECT THE REQUEST TO THE APPROPRIATE CPU. THE REQUEST
*             CODE THAT WAS PASSED IN REGISTER 0 IS USED  TO POST A
*             BIT IN A BUFFER IN THE RECEIVING CPU'S PCCA.  THIS
*             BIT INDICATES TO THE RECEIVING CPU'S EXTERNAL CALL
*             SLIH THAT A SPECIFIC PROGRAM IS TO BE GIVEN CONTROL.
*             AFTER BUILDING THE BUFFER, IEAVERP CALLS IEAVEDR REQUEST-
*             ING A EXTERNAL CALL ORDER CODE BE SIGP'D TO THE RECEIVING
*             CPU.  IF THE RETURN CODE IS A 0 OR A 8 WITH THE EXTERNAL
*             CALL PENDING STATUS SET, THE CALLER IS GIVEN A RETURN
*             CODE 0.  IF THE RETURN CODE FROM IEAVEDR IS 4 (BUSY COND)
*             THE REQUEST IS RETRIED.  ALL OTHER IEAVEDR RETURN CODES
*             ARE PASSED DIRECTLY BACK TO THE CALLER.  EVEN THOUGH THE
*             SIGP WAS UNSUCCESSFUL THE REQUEST BIT IN THE BUFFER IS
*             NOT TURNED OFF.  THIS IS BECAUSE THE REQUESTS ARE OR'ABLE
*             AND ANOTHER CPU MAY HAVE ALSO REQUESTED THE SAME PROGRAM.
*             IF THE SYSTEM IS A UNIPROCESSOR (NOT EQUIPPED TO SEND
*             OR RECEIVING SIGP SIGNALS) A RETURN CODE OF 16 WILL BE
*             GENERATED.
*
*NOTES
*  DEPENDENCIES=  NONE.
*    CHARACTER-CODE DEPENDENCIES= NONE
*  RESTRICTIONS=AVAILABLE ONLY TO SUPERVISOR STATE, ZERO PROTECT KEY
*               CALLERS.
*  REGISTER-CONVENTIONS= REFERENCE EQUATE SECTION
*  PATCH-LABEL= NONE, THIS IS A RESIDENT NUCLEUS MODULE.
*
*MODULE TYPE= PROCEDURE
*  PROCESSOR= ASSEM
*  MODULE SIZE= N/A
*  ATTRIBUTES= NUCLEUS,ZERO PROTECT KEY,DISABLED,REFRESHABLE,
*              SUPERVISOR MODE,ADDRSPC=FIXED.
*
*ENTRY POINT= IEAVERP
*  PURPOSE= TO EXECUTE A ROUTINE VIA EXTERNAL CALL ON ANY OF THE
*          ONLINE MP CONFIGURED CPUS.
*  LINKAGE= THE RPSGNL MACRO WHICH EXPANDS INTO A BALR 14,15.
*  INPUT= REGISTER 1 - CONTAINS THE PCCA ADDRESS.
*         REGISTER 0 - CONTAINS THE SPECIFIC REQUEST CODE.
*         REGISTER 14 - CONTAINS THE RETURN ADDRESS.
*         REGISTER 15 - CONTAINS THE ENTRY POINT ADDRESS.
*  REGISTERS SAVED= REGISTER 2 THROUGH 5.
*  REGISTER USAGE= REGISTER 1 IS USED TO PASS THE PCCA ADDR TO IEAVEDR.
*                  REGISTER 2 IS USED TO SAVE REGS THROUGH LCCA.
*                  REGISTER 4 IS USED AS A WORK REGISTER.
*                  REGISTER 5 USED AS THE BASE REGISTER.
*                  REGISTER 15 IS USED AS A WORK REG AND EP TO IEAVEDR.
*  REGISTERS RESTORED= REGISTER 2 THROUGH 5.
*
*EXIT - NORMAL= IEAVERP.
*  CONDITIONS= NORMAL COMPLETION OF FUNCTION REGARDLESS OF REQUEST
*             BEING INITIATED OR NOT.
*  OUTPUT= AN INITIATED EXT CALL ORDER IF SUCCESSFUL AND RETURN CODES
*         INDICATING THE STATUS.
*  RETURN CODES= AS LISTED BELOW.
*  R.C. - 0  -  FUNCTION SUCCESSFULLY INITIATED.  THE FUNCTION
*              IS NOT NECESSARILY COMPLETED UPON RETURN TO THE
*              CALLER.
*  R.C. - 4  -  FUNCTION NOT COMPLETED BECAUSE CPU THAT WAS TO RECEIVE
*              THE CALL WAS NOT ONLINE.
*  R.C. - 8  -  FUNCTION UNSUCCESSFUL.  STATUS RETURNED IN
*              REGISTER 0.
*              REGISTER 0 STATUS ON R.C. 8.
*              BIT   0 = EQUIPMENT CHECK.
*                  1-24 = RESERVED
*                    25 = STOPPED
*                    26 = OPERATOR INTERVENING
*                    27 = CHECK STOP
*                    28 = NOT READY
*                    29 = RESERVED
*                    30 = INVALID FUNCTION
*                    31 = RECEIVER CHECK
*  R.C. - 12 - NOT OPERATIONAL.  THE SPECIFIEC CPU IS EITHER NOT
*              INSTALLED OR IS NOT CONFIQURED INTO THE SYSTEM OR
*              IS POWERED OFF.
*  R.C. - 16 - SIGP FUNCTION UNSUCCESSFUL.  CPU IS AN UNIPROCESSOR
*              AND DOES NOT HAVE SIGP SENDING AND RECEIVING
*              CAPABILITIES.
*
*EXIT - ERROR= NONE.
*  RETURN CODES= NONE.
*
*EXTERNAL REFERENCES=
*  ROUTINES= NONE
*  DATA AREAS=REMOTE PENDABLE BUFFER.
*  CONTROL BLOCKS=
*                 PSA - R,M.  LCCA - R,M.
*                 PCCA - R,M.  CVT - R.  CSD - R.
*
*TABLES= NONE.
*
*MACROS= ABEND.
*
*  SERIALIZATION= DISABLEMENT.
*
*CHANGE ACTIVITY= N/A
*
*MESSAGES= XXXXXXX - MSG. TEXT
*
*ABEND CODES= SYSTEM ABEND CODE 07B - INVALID PCCA ADDRESS.
*
***********************************************************************
IEAVERP  CSECT
*        SYMBOLIC EQUATES
REG0     EQU   0                        REGISTER 0
REG1     EQU   1                        REGISTER 1
REG2     EQU   2                        REGISTER 2
REG3     EQU   3                        REGISTER 3
REG4     EQU   4                        REGISTER 4
REG5     EQU   5                        REGISTER 5
REG6     EQU   6                        REGISTER 6
REG14    EQU   14                       REGISTER 14
REG15    EQU   15                       REGISTER 15
ZERO     EQU   0                        0 VALUE
STATUSR0 EQU   0                        REGISTER 0
MULTBY4  EQU   2                        SLL CONSTANT TO MULT BY 4
RPBUFFER EQU   3                        REG 3 ADDR OF EXT CALL BUFFER
CVTREG4  EQU   4                        REG4 BASE FOR CVT
CSDREG4  EQU   4                        REGISTER 4 BASE FOR CSD
FOUR     EQU   4                        VALUE 4
BASER5   EQU   5                        REG 5 BASE FOR IEAVERP
EIGHT    EQU   8                        CONSTANT 8
RETURN   EQU   14                       CALLERS RETURN ADDRESS
FUNCCDE  EQU   15                       REG 15 SIGP FUNCTION CODE
RETCODE  EQU   15                       REG 15, RETURN CODE REG
DISABLE  EQU   X'FC'                    DISABLE MASK
EXTCALL  EQU   2                        EXTERNAL CALL FUNCTION CODE
         EJECT
         USING LCCA,REG1
         USING PSA,0
         USING *,REG15
BASEADR EQU *
IEAVERP  MODID BR=YES
         STNSM PSAIPCR+1,DISABLE        DISABLE CALLER & SAVE SYS MASK
         ST    REG1,PSAIPCSA            SAVE CALLERS REG 1
         L     REG1,PSALCCAV            LOCATE LCCA
         STM   REG2,REG5,LCCARPR2       SAVE CALLERS REGS 2-5
         DROP  REG1
         LR    BASER5,REG15             EST ADDRESSABILITY
         USING BASEADR,BASER5
         DROP  REG15
         L     REG1,PSAIPCSA            RESTORE CALLERS REG 1
         USING PCCA,REG1
         EJECT
**********************************************************************
*        ROUTINE VALIDITY CHECKS THE PCCA ADDRESS
**********************************************************************
         LTR   REG1,REG1                WAS PCCA ADDRESS 0?
         BZ    RPINVPCA                 0 ADDRESS IS INVALID
         L     CVTREG4,CVTPTR           LOCATE THE CVT
         USING CVT,REG4
         L     REG3,CVTPCCAT            LOCATE PCCAVT
         LH    REG2,PCCACPUA            OBTAIN PURPORTED CPUID
         SLL   REG2,MULTBY4             INDEX VALUE FOR PCCAVT
         L     REG15,0(REG2,REG3)       OBTAIN PCCA ADDRESS FOR CPUID
         CR    REG1,REG15               DOES PCCA ADDRESSES AGREE?
         BNE   RPINVPCA                 INVALID PCCA ADDRESS
         SPACE 2
**********************************************************************
*        PCCA IS A VALID PCCA ADDRESS, DETERMINE IF CPU IS ALIVE
**********************************************************************
         L     CSDREG4,CVTCSD           LOCATE THE CSD
         USING CSD,REG4
         LH    REG3,CSDCPUAL            LOCATE SYSTEM ALIVE BITS
         LH    REG4,PCCACAFM            LOCATE THE RECEIVING CPU ALIVE
*                                       MASK
         NR    REG4,REG3                AND THE MASKS TO DETERMINE IF
*                                       THE RECEIVING CPU IS ONLINE
         BZ    RPRC4                    THE RECEIVING CPU IS NOT ONLINE
         LR    FUNCCDE,REG0             LOAD CALLERS FUNCTION CODE
         LA    RPBUFFER,ZERO            CHECK FOR EMPTY BUFFER MASK
RPCSLOOP EQU   *
         CS    RPBUFFER,FUNCCDE,PCCARPB COMPARE AND SWAP THE FUNCTION
*                                       ON IN THE REMOTE PENDABLE
*                                       BUFFER IN RECEIVING CPUS PCCA
         BZ    RPSIGP                   FUNCTION CODE WAS C&S ON
         LR    FUNCCDE,RPBUFFER         BUFFER IS NOT EMPTY INCLUDE
*                                       PRIOR BITS IN C&S
         OR    FUNCCDE,REG0             CALLERS FUNCTION CODE ADDED
*                                       TO VALUE ALREADY IN BUFFER
         B     RPCSLOOP                 REPEAT THE COMPARE AND SWAP
         EJECT
**********************************************************************
*        REMOTE PENDABLE'S BUFFER HAS BEEN ESTABLISHED FOR THIS
*        REQUEST.  THE CALLERS RETURN ADDRESS IS SAVED.  THE PCCA
*        ADDRESS IS SAVED IN CASE OF A BUSY CONDITION IS RETURNED
*        FROM DIRECT SIGNAL.  DIRECT SIGNAL IS CALLED TO ISSUED A
*        SIGP WITH AN EXTERNAL CALL ORDER CODE.
**********************************************************************
RPSIGP   EQU   *
         L     REG2,PSALCCAV            OBTAIN LCCA FOR SAVING REGS
         ST    REG14,LCCADSR2-LCCA(REG2) SAV R14 IN IEAVEDR'S S.A.
         ST    REG1,LCCADSR3-LCCA(REG2)  SAV R1 IN IEAVEDR'S S.A.
         ST    REG5,LCCADSR4-LCCA(REG2)  SAV R5 (BASE REG)
RPSIGPRT EQU   *
         LA    REG0,EXTCALL             LOAD EXT CALL FUNCTION CODE
         L     REG2,CVTPTR              SET CVT ADDR IN R2 FOR IEAVEDR
         L     REG15,AIEAVDR2           OBTAIN IEAVEDR SECONDARY ENTRY
         L     REG5,AIEAVEDR            ESTABLISH BASE FOR IEAVEDR
         BALR  REG14,REG15              ENTER IEAVEDR AT SECONDARY ENTY
         L     REG2,PSALCCAV            OBTAIN LCCA ADDRESS FOR S.A
         L     REG5,LCCADSR4-LCCA(REG2) RE-ESTABLISH ADDRESSABILITY
         LTR   RETCODE,RETCODE          WAS DIRECT SIGNAL SUCCESSFUL
         BZ    RPEXIT                   SUCCESSFUL SIGP INITIATED
         C     RETCODE,BUSY             WAS SIGP TEMPRORARY BUSY
         BNE   RPRCCHK                  NOT BUSY, CONTINUE RC CHECK
         L     REG1,LCCADSR3-LCCA(REG2) RE-OBTAIN SAVED PCCA ADDR
         B     RPSIGPRT                 REPEAT SIGP UNTIL TEMPORARY
*                                       BUSY CONDITION CLEARS
RPRCCHK  EQU   *
         C     RETCODE,RCEIGHT          WAS SIGP UNSUCCESSFUL AND
*                                       STATUS STORED?
         BNE   RPEXIT                   NO, CHECK FOR UNIPROCESSOR
         C     STATUSR0,EXTCPEND        EXTERNAL CALL PENDING
         BNE   RPEXIT                   NO, RETURN IEAVEDR'S RET CODE
         LA    RETCODE,ZERO             EXT CALL PENDING, TREAT LIKE
*                                       SUCCESSFUL INITIATION
         B     RPEXIT                   RETURN TO CALLER
         EJECT
**********************************************************************
*        THE PCCA ADDRESS SUPPLIED BY THE CALLER IS INVALID
*        CALLER IS ABENDED WITH A SYSTEM ABEND CODE OF XXX
**********************************************************************
RPINVPCA EQU   *
         L     REG2,PSALCCAV
         USING LCCA,REG2
         LM    REG2,REG5,LCCARPR2       RESTORE CALLERS REGS 2-5
         EX    REG0,PSAIPCR             RESTORE CALLERS SYSTEM MASK
         ABEND X'07B',DUMP,,SYSTEM
RPRC4    EQU   *
         LA    RETCODE,FOUR             RETURN CODE FOUR NOT ONLINE
         L     REG2,PSALCCAV
         USING LCCA,REG2
         B     RPEXIT2                  RETURN TO CALLER
RPEXIT   EQU   *
         L     REG14,LCCADSR2-LCCA(REG2) RESTORE RETURN REG
RPEXIT2  EQU   *
         LM    REG2,REG5,LCCARPR2       RESTORE CALLERS REGS 2-5
         EX    REG0,PSAIPCR             RESTORE CALLERS SYSTEM MASK
         BR    RETURN                   RETURN TO CALLER
         DS    0F
AIEAVEDR DC    V(IEAVEDR)               ADDRESS OF IEAVEDR MAIN ENTRY
AIEAVDR2 DC    V(IEAVEDR2)              ADDRESS OF IEAVEDR'S 2ND ENTRY
*                                       USED BY IEAVERP AND IEAVERI
BUSY     DC    F'4'                     CONSTANT TO TEST FOR BUSY COND.
RCEIGHT  DC    F'8'                     CONSTANT TO TEST FOR RC 8.
EXTCPEND DC    X'00000080'              EXTERNAL CALL PENDING MASK
         IHAFRRS
         IHAPCCA
         IHALCCA
         IHACSD
         IHAPSA
         CVT   LIST=YES,DSECT=YES,PREFIX=NO
         END   IEAVERP
