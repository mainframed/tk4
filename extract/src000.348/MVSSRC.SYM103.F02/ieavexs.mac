         TITLE 'IEAVEXS - EXTERNAL CALL SECOND LEVEL INTERRUPT HANDLER'
*/*IEAVEXS: CHART EXT CALL SLIH */
*/* HEADER
*/*
*/*
*/*
*/*                                                   ID: 3.1.7.13.10.4
*/* EXTERNAL CALL SLIH
*/*                                                   PAGE # */
*/*IEAVEXS: E IEAVEXS */
*/* P TURN ON EXTERNAL CALL SLIH ACTIVE INDICATOR */
*/* P GET PCCA ADDRESS FROM PSA */
*/* D (YES,EXCEXIT,NO,) PENDABLE BUFFER = 0? */
*/* P SAVE RETURN REGISTER */
*/* D (NO,EXCSIO,YES,) SWITCH INDICATOR ON? */
*/* P TURN OFF SWITCH INDICATOR VIA CS */
*/* S IEAVEMS1:BALR 14,15 MEMORY SWITCH ROUTINE */
*/*EXCSIO: D (NO,EXCRQCK,YES,) SIO INDICATOR ON? */
*/* P TURN OFF SIO INDICATOR VIA CS */
*/* S IECIPC:BALR 14,15 IOS SIO ROUTINE */
*/*EXCRQCK: D (NO,EXCGTF,YES,) RQCHECK INDICATOR ON? */
*/* P TURN OFF RQCHECK VIA CS */
*/* S IEAVRQCK:BALR 14,15 RQ CHECK ROUTINE */
*/*EXCGTF: D (NO,EXCMODE,YES,) GTFCRM INDICATOR ON? */
*/* P TURN OFF GTFCRM INDICATOR VIA CS */
*/* S AHLSTCLS:BALR 14,15 GTF CONTROL REG MGMNT ROUTINE */
*/*EXCMODE: D (NO,EXCMF1,YES,) MODE INDICATOR ON? */
*/* P TURN OFF MODE INDICATOR VIA CS */
*/* S IGFPEXI2:BALR 14,15 RMS CONTROL REG ROUTINE */
*/*EXCMF1: D (NO,EXCOUT,YES,) MF1 INDICATOR ON? */
*/* P TURN OFF MF1 INDICATOR VIA CS */
*/* S MACRO CALL:MFROUTER MF1TCH */
*/*EXCOUT: P RESTORE RETURN REGISTERS */
*/*EXCEXIT: P TURN OFF ACTIVE INDICATOR */
*/* R RETURN TO EXTFLIH */
*/* FOOTING
*/*
*/*  */
*/*IEAVEXS: END EXT CALL SLIH */
*MODULE NAME= IEAVEXS
*
*DESCRIPTIVE NAME= EXTERNAL CALL SECOND LEVEL INTERRUPT HANDLER
*
*COPYRIGHT= N/A
*
*STATUS= REL. 02.0
*
*FUNCTION= UPON AN EXTERNAL CALL INTERRUPT, ROUTES CONTROL FROM THE
*          EXTERNAL FLIH TO THE APPROPRIATE RECEIVING ROUTINE AS
*          INDICATED BY THE BIT SETTING IN THE REMOTE PENDABLE BUFFER.
*          THE BITS ARE SET BY THE IEAVERP SERVICE ROUTINE
*          WHENEVER A CALLER HAS ISSUED THE RPSGNL MACRO.
*
*  OPERATION= WHENEVER THE EXTERNAL FLIH RECOGNIZES A EXTERNAL CALL
*             INTERRUPT CODE, IT PASSES CONTROL TO THE EXTERNAL
*             CALL SECOND LEVEL INTERRUPT HANDLER.  REGISTER 10
*             CONTAINS THE ENTRY POINT ADDRESS OF IEAVEXS AND
*             REGISTER 2 THE RETURN ADDRESS.  WHEN ENTERED, IEAVEXS
*             WILL TURN ON ITS BIT IN PSASUPER TO INDICATE IT IS
*             IN CONTROL.  THE PCCA ADDRESS WILL BE OBTAINED FROM
*             THE PSA IN ORDER TO REFERENCE THE REMOTE PENDABLE BUFFER.
*             IEAVEXS WILL LOAD THE RPBUFFER WORD INTO A REGISTER
*             AND TEST FOR SPECIFIC BITS SET BY IEAVERP WHENEVER
*             WHENEVER IT WAS INVOKED THROUGH A RPSGNL MACRO REQUEST.
*             IF IEAVEXS FINDS A BIT ON, IT LOADS REGISTER 15 WITH
*             THE THE APPROPRIATE PROGRAM'S ENTRY POINT, THEN COMPARE
*             AND SWAPS THE BIT OFF AND BALRS 14,15 TO THE PROGRAM.
*             ON RECEIVING CONTROL BACK FROM THE PROGRAM, IEAVEXS AGAIN
*             LOADS THE RPBUFFER WORD AND TESTS FOR OTHER BITS.  IF ANY
*             ARE ON, THE PROCEDURE TO CALL THE PROGRAM IS REPEATED.
*             OTHERWISE, IEAVEXS TURNS OFF ITS PSASUPER BIT AND RETURNS
*             CONTROL TO THE EXTERNAL FLIH VIA REGISTER 2.  IF THE
*             MF1TCH REQUEST BIT WAS ON, IEAVEXS USES THE MF1ROUTER
*             MCARO EXPANSION TO FIRST TEST IF MF1 IS ACTIVE IN THE
*             SYSTEM BEFORE ROUTING CONTROL TO MF1TCH.  IF MF1 IS NOT
*             ACTIVE, IEAVEXS TURNS OF ITS REQUEST BIT IN RPBUFFER AND
*             CONTINUES NORMAL PROCESSING.
*
*NOTES
*  DEPENDENCIES=  RECEIVING ROUTINES MUST RETURN CONTROL TO IEAVEXS
*                BR 14.  THEY WILL RECEIVE CONTROL DISABLED EXCEPT
*                FOR MACHINE CHECK AND MUST REMAIN IN THAT STATE.  THEY
*                MAY REQUEST GLOBAL SPIN LOCKS DURING THEIR PROCESSING
*                BUT CANNOT UNCONDITIONALLY OBTAIN OR RELEASE THE CMS
*                OR LOCAL LOCK.  THEY MAY NOT CAUSE ANY PAGE FAULTS
*                DURING THEIR PROCESSING OR CALL ANY SUPERVISORY
*                WHICH DOES NOT MAINTAIN THE ABOVE RESTRICTIONS.
*    CHARACTER-CODE DEPENDENCIES= NONE
*  RESTRICTIONS=AVAILABLE ONLY TO SUPERVISOR STATE, ZERO PROTECT KEY
*               CALLERS.
*  REGISTER-CONVENTIONS= REFERENCE EQUATE SECTION
*  PATCH-LABEL= NONE, THIS IS A RESIDENT NUCLEUS MODULE.
*
*MODULE TYPE= PROCEDURE
*  PROCESSOR= ASSEM
*  MODULE SIZE= N/A
*  ATTRIBUTES= NUCLEUS,ZERO PROTECT KEY,DISABLED,REFRESHABLE,
*              SUPERVISOR MODE,ADDRSPC=FIXED.
*
*ENTRY POINT= IEAVEXS
*  PURPOSE= TO ROUTE CONTROL TO THE APPROPRIATE ROUTINE WHENEVER
*         THE EXTERNAL FLIH RECOGNIZES A EXTERNAL CALL INTERRUPT.
*  LINKAGE= EXTERNAL FLIH BALRS 2,10 TO IEAVEXS'S ENTRY POINT.
*  INPUT= REGISTER 10 - CONTAINS IEAVEXS'S ENTRY POINT ADDRESS.
*         REGISTER 2 - CONTAINS EXTERNAL FLIH'S RETURN ADDRESS.
*  REGISTERS SAVED= REGISTER 2 IN THE LCCA - LCCAECSA.
*  REGISTER USAGE= REGISTER 1 IS USED AS THE PCCA BASE REGISTER.
*                  REGISTER 2 IS THE RETURN ADDRESS TO THE EXTERNAL
*                  FLIH.
*                  REGISTER 4 IS USED TO HOLD RPBUFFER VALUE.
*                  REGISTER 5 IS USED TO HOLD RPBUFFER'S NEW VAULE.
*                  REGISTER 6 & 7 HOLD MASKS.
*                  REGISTER 8 IS USED AS LCCA BASE.
*                  REGISTER 14 IS A BALR REGISTER FOR RECEIVING CONTROL
*                              BACK FROM RECEIVING ROUTINES.
*                  REGISTER 15 IS USED AS A BALR REG TO BRANCH
*                              TO RECEIVING ROUTINE'S ENTRY POINTS.
*  REGISTERS RESTORED= REGISTER 2.
*
*EXIT - NORMAL= IEAVEXS.
*  CONDITIONS= WHENEVER RPBUFFER IS EMPTY OR AFTER MF1 HAS BEEN CALLED.
*  OUTPUT= NONE, EXCEPT THE CALLED RECEIVING ROUTINES REQUEST BIT
*         IS AND OFF WHENEVER IT IS GIVEN CONTROL.
*  RETURN CODES= NONE.
*
*EXIT - ERROR= NONE.
*  CONDITIONS= N/A
*  OUTPUT= N/A
*  RETURN CODES= NONE.
*
*EXTERNAL REFERENCES=
*  ROUTINES= MEMORY SWITCH - (IEAVEMS1)
*            TIMER TQE CHECK ROUTINE - (IEAVRQCK)
*            GTF ROUTINE - (AHLSTCLS)
*            RMS CONTROL REGISTER MAINTENANCE ROUTINE - (IGFPEXI2)
*            IOS SIO ROUTINE - (IECIPC)
*  DATA AREAS=REMOTE PENDABLE BUFFER.
*  CONTROL BLOCKS=  PSA - R,M.  LCCA - R,M.
*                 PCCA - R,M.  CVT - R.
*
*TABLES= NONE.
*
*MACROS= MFROUTER.
*  SERIALIZATION= DISABLEMENT.
*
*CHANGE ACTIVITY= N/A
*
*MESSAGES= XXXXXXX - MSG. TEXT
*
*ABEND CODES= NONE.
*
***********************************************************************
IEAVEXS  CSECT
         USING *,BASER10
EXCENTRY EQU   *
IEAVEXS  MODID BR=YES
***********************************************************************
*        SYMBOLIC EQUATES
***********************************************************************
ZERO     EQU   0                        VALUE ZERO
REG0     EQU   0                        REGISTER 0
PCCAPTR1 EQU   1                        REG 1 - BASE FOR PCCA
RETURN2  EQU   2                        REG 2 - EXT FLIHS RETURN ADDR
RBBUFOLD EQU   4                        REG 4 - COMPERAND FOR C&S, WILL
*                                       BE UPDATED  IF C&S FAILS
RBBUFNEW EQU   5                        REG 5 - NEW VALUE TO REPLACE
*                                       OLD VALUE IF C&S IS SUCCESSFUL
MASKON   EQU   6                        REG 6 - HOLDS TESTING MASK TO
*                                       DETERMINE IF A FUNCTION HAS
*                                       BEEN REQUESTED
MASKOFF  EQU   7                        REG 7 - HOLDS MASK TO AND OFF
*                                       FUNCTION BIT BEFORE CALLING
*                                       THE FUNCTION
LCCAPTR8 EQU   8                        REG 8 - LCCA BASE
CVTREG   EQU   9                        REG 9 - CVT BASE REG
BASER10  EQU   10                       REG 10 - BASE REGISTER FOR
*                                       IEAVEXS
RETURN14 EQU   14                       RETURN ADDRESS FROM CALLERS
*                                       SERVICE ROUTINES
REG15    EQU   15                       ENTRY POINT ADDRESS OF
*                                       CALLERS SERVICE ROUTINES
XFF      EQU   X'FF'                    CONSTANT FOR AND'NG OFF BITS
         EJECT
         USING PSA,0
         USING PCCA,PCCAPTR1
         USING LCCA,LCCAPTR8
         OI    PSASUP2,PSAIPCEC         IEAVEXS ACTIVE INDICATOR
         L     PCCAPTR1,PSAPCCAV        LOCATE PCCA
         L     RBBUFOLD,PCCARPB         IS EXT CALL BUFFER EMPTY?
         LTR   RBBUFOLD,RBBUFOLD        WAS MEMORY SWITCH REQUESTED?
         BZ    EXCEXIT                  BUFFER IS EMPTY
         L     LCCAPTR8,PSALCCAV        LOCATE LCCA
         ST    RETURN2,LCCAECSA         SAVE FLIH RETURN ADDRESS
         BNM   EXCSIO                   NO MEMORY SW, TEST FOR SIO
EXCMEMSW EQU   *
         LR    RBBUFNEW,RBBUFOLD        OBTAIN VALUE THAT WAS IN BUFFER
         N     RBBUFNEW,MEMSWOFF        AND OFF MEMORY SWITCH INDICATOR
         CS    RBBUFOLD,RBBUFNEW,PCCARPB  TRY TO UPDATE BUFFER
         BNZ   EXCMEMSW                 IF BUFFER HAS CHANGED RETRY
         L     REG15,AMEMSW             BUFFER HAS BEEN UPDATED AND
*                                       MEMORY SWITCH INDICATOR HAS
*                                       BEEN TURNED OFF
         BALR  RETURN14,REG15           INVOKE MEMORY SWITCH
EXCRET1  EQU   *
         L     BASER10,ABASE-EXCRET1(RETURN14)  REESTABLISH BASE ADDR
         L     PCCAPTR1,PSAPCCAV        LOCATE PCCA
         L     RBBUFOLD,PCCARPB         OBTAIN RP BUFFER VALUE
         LTR   RBBUFOLD,RBBUFOLD        IS THE BUFFER EMPTY?
         BZ    EXCOUT                   BUFFER IS EMPTY, EXIT SLIH
EXCSIO   EQU   *
         L     MASKON,SIOON             MASK TO TEST FOR SIO
         NR    MASKON,RBBUFOLD          DETERMINE IF SIO REQUESTED
         BZ    EXCRQCK                  SIO NOT REQUESTED
EXCSIOX  EQU   *
         LR    RBBUFNEW,RBBUFOLD        OBTAIN VALUE THAT WAS IN BUFFER
         N     RBBUFNEW,SIOOFF          AND OFF SIO INDICATOR
         CS    RBBUFOLD,RBBUFNEW,PCCARPB  TRY TO UPDATE BUFFER
         BNZ   EXCSIOX                  IF BUFFER HAS CHANGED RETRY
         L     REG15,ASIO               BUFFER HAS BEEN UPDATED AND
*                                       SIO INDICATOR HAS
*                                       BEEN TURNED OFF
         BALR  RETURN14,REG15           INVOKE SIO RECEIVING ROUTINE
EXCRET2  EQU   *
         L     BASER10,ABASE-EXCRET2(RETURN14)  REESTABLISH BASE ADDR
         L     PCCAPTR1,PSAPCCAV        LOCATE PCCA
         L     RBBUFOLD,PCCARPB         LOCATE RP BUFFER'S VALUE
         LTR   RBBUFOLD,RBBUFOLD        IS THE BUFFER EMPTY?
         BZ    EXCOUT                   BUFFER IS EMPTY, EXIT SLIH
EXCRQCK  EQU   *
         L     MASKON,RQCKON            MASK TO TEST FOR RQCHECK
         NR    MASKON,RBBUFOLD          DETERMINE IF RQCHECK REQUESTED
         BZ    EXCGTF                   RQCHECK NOT REQUESTED
EXCRQCKX EQU   *
         LR    RBBUFNEW,RBBUFOLD        OBTAIN VALUE THAT WAS IN BUFFER
         L     MASKOFF,RQCKOFF          OBTAIN MASK TO TURN OFF
*                                       RQCHECK VALUE
         N     RBBUFNEW,RQCKOFF         AND OFF RQCHECK INDICATOR
         CS    RBBUFOLD,RBBUFNEW,PCCARPB  TRY TO UPDATE BUFFER
         BNZ   EXCRQCKX                 IF BUFFER HAS CHANGED RETRY
         L     REG15,ARQCK              BUFFER HAS BEEN UPDATED AND
*                                       RQCHECK INDICATOR HAS
*                                       BEEN TURNED OFF
         BALR  RETURN14,REG15           GIVE CONTROL TO RQCHECK ROUTINE
EXCRET3  EQU   *
         L     BASER10,ABASE-EXCRET3(RETURN14)  REESTABLISH BASE ADDR
         L     PCCAPTR1,PSAPCCAV        LOCATE PCCA
         L     RBBUFOLD,PCCARPB         LOCATE RP BUFFER'S VALUE
         LTR   RBBUFOLD,RBBUFOLD        IS THE BUFFER EMPTY?
         BZ    EXCOUT                   BUFFER IS EMPTY, EXIT SLIH
EXCGTF   EQU   *
         L     MASKON,GTFON             MASK TO TEST FOR GTF
         NR    MASKON,RBBUFOLD          DETERMINE IF GTF REQUESTED
         BZ    EXCMODE                  GTF NOT REQUESTED
EXCGTFX  EQU   *
         LR    RBBUFNEW,RBBUFOLD        OBTAIN VALUE THAT WAS IN BUFFER
         N     RBBUFNEW,GTFOFF          AND OFF GTF INDICATOR
         CS    RBBUFOLD,RBBUFNEW,PCCARPB  TRY TO UPDATE BUFFER
         BNZ   EXCGTFX                  IF BUFFER HAS CHANGED RETRY
         L     REG15,AGTF               BUFFER HAS BEEN UPDATED AND
*                                       GTF INDICATOR HAS
*                                       BEEN TURNED OFF
         BALR  RETURN14,REG15           GIVE CONTROL TO GTF ROUTINE
EXCRET4  EQU   *
         L     BASER10,ABASE-EXCRET4(RETURN14)  REESTABLISH BASE ADDR
         L     PCCAPTR1,PSAPCCAV        LOCATE PCCA
         L     RBBUFOLD,PCCARPB         OBTAIN RP BUFFER'S VALUE
         LTR   RBBUFOLD,RBBUFOLD        IS THE BUFFER EMPTY?
         BZ    EXCOUT                   BUFFER IS EMPTY, EXIT SLIH
EXCMODE  EQU   *
         L     MASKON,MODEON            MASK TO TEST FOR MODE
         NR    MASKON,RBBUFOLD          DETERMINE IF MODE REQUESTED
         BZ    EXCMF1                   MODE NOT REQUESTED
EXCMODEX EQU   *
         LR    RBBUFNEW,RBBUFOLD        OBTAIN VALUE THAT WAS IN BUFFER
         N     RBBUFNEW,MODEOFF         AND OFF MODE INDICATOR
         CS    RBBUFOLD,RBBUFNEW,PCCARPB TRY TO UPDATE BUFFER
         BNZ   EXCMODEX                 IF BUFFER HAS CHANGED RETRY
         L     REG15,AMODE              BUFFER HAS BEEN UPDATED AND
*                                       MODE INDICATOR HAS
*                                       BEEN TURNED OFF
         BALR  RETURN14,REG15           GIVE CONTROL TO MODE ROUTINE
EXCRET5  EQU   *
         L     BASER10,ABASE-EXCRET5(RETURN14)  REESTABLISH BASE ADDR
         L     PCCAPTR1,PSAPCCAV        LOCATE PCCA
         L     RBBUFOLD,PCCARPB         OBTAIN RP BUFFER'S VALUE
         LTR   RBBUFOLD,RBBUFOLD        IS THE BUFFER EMPTY?
         BZ    EXCOUT                   BUFFER IS EMPTY, EXIT SLIH
EXCMF1   EQU   *
         L     MASKON,MF1ON             MASK TO TEST FOR MF1
         NR    MASKON,RBBUFOLD          DETERMINE IF MF1 REQUESTED
         BZ    EXCOUT                   MF1 NOT REQUESTED
EXCMF1X  EQU   *
         LR    RBBUFNEW,RBBUFOLD        OBTAIN VALUE THAT WAS IN BUFFER
         N     RBBUFNEW,MF1OFF          AND OFF MODE INDICATOR
         CS    RBBUFOLD,RBBUFNEW,PCCARPB  TRY TO UPDATE BUFFER
         BNZ   EXCMF1X                 IF BUFFER HAS CHANGED RETRY
         L     CVTREG,CVTPTR            LOCATE CVT FOR MF1
         USING CVT,CVTREG
         MFROUTER MF1TCH                INVOKE MF1 MACRO TO CALL MF1
*                                       IF MF1 IS ACTIVE IN SYSTEM
         BALR  RETURN14,REG0            REQD IF MF1 IS NOT ACTIVE
EXCRET6  EQU   *
         L     BASER10,ABASE-EXCRET6(RETURN14)  REESTABLISH BASE ADDR
EXCOUT   EQU   *
         L     LCCAPTR8,PSALCCAV        LOCATE LCCA
         L     RETURN2,LCCAECSA         RESTORE FLIHS RETURN ADDRESS
EXCEXIT  EQU   *
         NI    PSASUP2,XFF-PSAIPCEC     TURN OFF NOT ACTIVE BIT
         BR    RETURN2                  RETURN TO FLIH
         DS    0F
MEMSWON  DC    X'80000000'              MEMORY SWITCH REQUEST
MEMSWOFF DC    X'7FFFFFFF'              MASK TO TURN OFF MEMORY SWITCH
SIOON    DC    X'40000000'              SIO REQUEST
SIOOFF   DC    X'BFFFFFFF'              MASK TO TURN OFF SIO
RQCKON   DC    X'20000000'              RQCHECK REQUEST
RQCKOFF  DC    X'DFFFFFFF'              MASK TO TURN OFF RQCHECK
GTFON    DC    X'10000000'              GTF REQUEST
GTFOFF   DC    X'EFFFFFFF'              MASK TO TURN OFF GTF
MODEON   DC    X'04000000'              MODE REQUEST
MODEOFF  DC    X'FBFFFFFF'              MASK TO TURN OFF MODE
MF1ON    DC    X'02000000'              MF1 REQUEST
MF1OFF   DC    X'FDFFFFFF'              MASK TO TURN OFF MF1
ABASE    DC    A(EXCENTRY)              ADDRESS OF IEAVEXS ENTRY
AMEMSW   DC    V(IEAVEMS1)              MEMORY SWITCH ROUTINE
ARQCK    DC    V(IEAVRQCK)              TQE CHECK ROUTINE
AGTF     DC    V(AHLSTCLS)              GTF ROUTINE
AMODE    DC    V(IGFPEXI2)              RMS C.R MAINT. ROUTINE
ASIO     DC    V(IECIPC)                CURRENTLY NOT SUPPORTED
         IHAPSA
         IHALCCA
         IHAPCCA
         CVT   DSECT=YES,PREFIX=NO,LIST=YES
         END   IEAVEXS
