      TITLE 'IEAVESVR- SVC FLIH RECOVERY ROUTINE'
* /* START OF SPECIFICATIONS ****
*
*01*  MODULE-NAME = IEAVESVR
*
*02*     CSECT-NAME = IEAVESVR
*
*01*  DESCRIPTIVE-NAME = SVC FLIH RECOVERY ROUTINE
*
*01*  COPYRIGHT = NONE
*
*01*  STATUS = VERSION 1
*
*01*  FUNCTION =   WHEN RECOVERY IS INITIATED FOR THE SVC FIRST LEVEL
*                  INTERRUPT HANDLER, THE SUPER FRR WILL INDICATE A
*                  RETRY TO IEAVESVR.  THIS ROUTINE WILL CLEAR THE SVC
*                  FLIH RECURSION INDICATOR, MAKING THE FUNCTION
*                  OPERABLE.  IT WILL ALLOW PERCOLATION TO THE
*                  PREVIOUS LEVEL OF RECOVERY.
*
*                  THE OTHER FLIH RECOVERY ROUTINES WILL ALL ENTER
*                  THIS MODULE AT THE EXTERNAL LABEL IEAVEABD TO
*                  PERFORM THE ABEND IN ORDER TO PERCOLATE THE ERROR.
*02*     OPERATION =
*
*             IEAVESVR ENTRY:
*
*                  CLEAR THE SVC FLIH RECURSION INDICATOR;
*                  FLIP THE FRR STACK TO THE NORMAL STACK;
*                  IF TYPE 6 SVC, RESET THE TYPE 6 FLAG AND    @ZA15739
*                  ISSUE ABEND WITH THE COMP. CODE SAVED BY    @ZA15739
*                  IEAVESPR.                                   @ZA15739
*                  IF NOT A TYPE 6 SVC, ISSUE ABEND WITH COMP. @ZA15739
*                  CODE 1FC.                                   @ZA15739
*             IEAVEABD: ABEND THE CURRENT TASK;
*
*01*   NOTES =
*
*02*     DEPENDENCIES =
*                     RTM:   RECOVERY IS CONTROLLED BY USE OF THE FRR
*                            STACK MECHANISM.
*
*03*        CHARACTER-CODE-DEPENDENCIES =   NONE
*
*02*     RESTRICTIONS = NONE
*
*02*     REGISTER-CONVENTIONS = SEE EQUATE SECTION OF MODULE
*
*02*     PATCH-LABEL = NONE
*
*01*  MODULE-TYPE = PROCEDURE
*
*02*     PROCESSOR = ASSEMBLER
*
*02*     MODULE-SIZE =  50 BYTES                               @Z40FPYK
*
*02*     ATTRIBUTES =   NUCLEUS
*                       REUSABLE
*                       REENTRANT
*                       DISABLED
*                       ZERO PROTECT KEY
*                       SUPERVISOR MODE
*
*01*  ENTRY-POINT = IEAVESVR
*
*02*     PURPOSE = THIS ENTRY PERFORMS THE SVC FLIH RECOVERY
*
*02*     LINKAGE = SUPER FRR WILL INDICATE A RETRY TO IEAVESVR
*                  RTM WILL PASS CONTROL VIA BR 15.
*
*02*     INPUT =
*
*02*          ENVIRONMENT = DISABLED
*                           SUPERVISOR
*                           KEY 0
*                           NO LOCKS HELD
*
*02*          DATA = DATA AREAS IN PSA
*
*02*          REGISTERS = REG 15 = ENTRY POINT ADDRESS
*
*02*     OUTPUT =
*
*02*          DATA = CURRENT FRR STACK = NORMAL
*                    SVC FLIH RECURSION BIT CLEARED
*
*02*          REGISTERS =
*                       REG 0 DESTROYED IN ABEND EXPANSION
*                       REG 1 = ABEND CODE
*                       ALL OTHERS AS ENTERED
*
*01*  ENTRY-POINT = IEAVEABD
*
*02*     PURPOSE = PERFORM THE ABEND TO CONTINUE WITH PERCOLATION
*
*02*     LINKAGE = CALLER RESOLVES V-CON AND BR 15.
*
*02*     INPUT =
*
*02*          ENVIRONMENT = DISABLED
*                           SUPERVISOR
*                           KEY 0
*                           NO LOCKS HELD
*
*02*          DATA = NONE
*
*02*          REGISTERS = REG 15 = ENTRY POINT ADDRESS
*
*02*     OUTPUT =
*
*02*          DATA = NONE
*
*02*          REGISTERS = REG 1 = ABEND CODE
*
*02*     REGISTERS-SAVED = NONE
*
*02*     REGISTER-USAGE = REG 0 = DESTROYED
*                         REG 1 = ABEND CODE
*
*02*     REGISTERS-RESTORED = ALL AS ON ENTRY EXCEPT REG 0 & 1
*
*01*  EXIT-NORMAL = VIA SVC 13
*
*02*     CONDITIONS = THE CURRENT TASK IS TO BE TERMINATED FOR
*                     RECOVERY PURPOSES.
*
*02*     OUTPUT = NONE
*
*02*     RETURN-CODES = NONE
*
*01*  EXIT-ERROR = NONE
*
*01*  EXTERNAL-REFERENCES =
*
*02*     ROUTINES = ABEND (1),DUMP,,SYSTEM
*
*02*     DATA-AREAS =
*             PSANSTK   -- NORMAL FRR STACK POINTER
*             PSACSTK   -- CURRENT FRR STACK POINTER
*             PSASVC    -- SVC FLIH RECURSION BIT
*             PSATYPEL  -- TYPE 6 SVC                          @Z40FPYK
*
*02*     CONTROL-BLOCKS = PSA R/W
*
*01*  TABLES =     NONE
*
*01*  MACROS =     ABEND -- EXPANDS IN-LINE CODE TO ISSUE SVC 13
*
*01*  CHANGE-ACTIVITY = Y021104 Z40FPYK                        @Z40FPYK
*
*01*  SERIALIZATION = DISABLE
*
*01*  MESSAGES = NONE
*
*01*  ABEND-CODES = X'1FC'
*
**** END OF SPECIFICATIONS ***/
         EJECT
IEAVESVR CSECT
      SPACE 2
*/*IEAVESVR: CHART SVC FLIH FRR */
*/* HEADER
*/*
*/*
*/*
*/*                                                 SECTION 3.1.8.3
*/* SVC FLIH RECOVERY ROUTINE
*/*                                                 PAGE # */
*/*IEAVESVR: E IEAVESVR */
*/* P CLEAR THE SVC FLIH RECURSION INDICATOR */
*/* P FLIP THE FRR STACK TO THE NORMAL STACK */
*/* P SET ABEND CODE IN REG 1 FOR SVC 13 */
*/* D (YES,,NO,IEAVEABD) TYPE 6 SVC FAILED ? */                @Z40FPYK
*/*         P RESET TYPE 6 INDICATOR */                        @Z40FPYK
*/* P SET UP TYPE 6 ABEND CODE */                              @Z40FPYK
*/*IEAVEABD: E IEAVEABD */
*/* P NOTE: ALL OTHER FLIH FRRS ENTER HERE TO PERFORM ABEND */
*/* R ABEND */
*/* FOOTING
*/*
*/*  */
*/*IEAVESVR: END SVCFLIH FRR */
      SPACE 2
***********************************************************************
*                                                                     *
*                            EQUATES                                  *
*                                                                     *
***********************************************************************
SVCCODE  EQU   X'1FC'             SVC FLIH ABEND CODE
CODE4    EQU   4                  ABEND REASON CODE 4          @Z40FPYK
CODEREG  EQU   1                  GENERAL REGISTER 1
BASEREG  EQU   15                 GENERAL REGISTER 15          @Z40FPYK
R0       EQU   0                                               @ZA15739
R1       EQU   1                                               @ZA15739
R15      EQU   15                                              @ZA15739
ABENDSVC EQU   13                                              @ZA15739
CXFF     EQU   X'FF'              CONSTANT = X'FF'
         EJECT                                                 @Z40FPYK
***********************************************************************
*                                                                     *
*                        IEAVESVR MAINLINE                            *
*                                                                     *
***********************************************************************
         USING PSA,0
         USING *,BASEREG                                       @Z40FPYK
         NI    PSASUP1,CXFF-PSASVC
*                                 CLEAR SVC FLIH RECURSION BIT
         MVC   PSACSTK,PSANSTK    MAKE CURRENT FRR STACK = NORMAL
         TM    PSASUP1,PSATYPE6   TYPE 6 SVC FAILURE ?         @Z40FPYK
         BNO   COMMON             NO, ABEND WITH REASON CODE 0 @Z40FPYK
         SPACE 2
*        PROCESSING FOR ABEND WHICH WAS ISSUED BY A TYPE 6 SVC @ZA15739
         SPACE
         NI    PSASUP1,CXFF-PSATYPE6 RESET TYPE 6 CODE         @Z40FPYK
*        IEAVESPR HAD SET UP TO RETRY IN THIS ROUTINE WITH     @ZA15739
*        REGISTER 1 SET UP FOR SVC 13 WITH THE CONTENTS OF     @ZA15739
*        SDWAABCC AND REG 0 SET UP WITH THE REASON CODE FROM   @ZA15739
*        REG 15, IN CASE THE ABEND WAS ISSUED BY A TYPE 6 SVC. @ZA15739
*        IF IT WAS A TYPE 6 SVC ROUTINE WHICH ABENDED, COMP.   @ZA15739
*        CODE 1FC WOULD NOT BE APPROPRIATE, BECAUSE 1FC        @ZA15739
*        INDICATES IT WAS THE FLIH WHICH ABENDED.              @ZA15739
*        THE BIT FOR DUMP OPTIONS IS TURNED OFF IN CASE DUMP   @ZA15739
*        OPTIONS WERE SPECIFIED ON THE ABEND. IT IS NOT        @ZA15739
*        PRACTICAL TO SAVE DUMP OPTIONS OR LIKELY THAT THEY    @ZA15739
*        WERE SPECIFIED ON THE ABEND.                          @ZA15739
         N     R1,NODMPOPT                                     @ZA15739
         LR    R15,R0             SET THE REASON CODE USED THE @ZA15739
*                                 FIRST TIME THE ABEND WAS     @ZA15739
*                                 ISSUED
         SVC   ABENDSVC                                        @ZA15739
         SPACE 2
*        ABEND WITH COMP. CODE 1FC
         SPACE
COMMON   DS    0H
         SLR   R15,R15            PUT REASON CODE IN REG 15    @ZA15739
*        NOTE: BASE REGISTER 15 IS DESTROYED AFTER THE ABEND   @Z40FPYK
*              REASON CODE IS SET.  THE CODE FOLLOWING THIS    @Z40FPYK
*              INSTRUCTION AND BEFORE THE ABEND SVC SHOULD NOT @Z40FPYK
*              REQUIRE ADDRESSABLITIY IN THIS CSECT.           @Z40FPYK
         DROP  BASEREG                                         @ZA15739
         LA    CODEREG,SVCCODE    PUT SVC FLIH ABEND CODE IN REG 1
         SPACE 2                                               @Z40FPYK
***********************************************************************
*                                                                     *
*                     IEAVEABD -- ABEND ROUTINE                       *
*                                                                     *
***********************************************************************
         ENTRY IEAVEABD
IEAVEABD DS    0H                 *****COMMON ABEND ROUTINE*****
*                                 USED BY ALL FLIH RECOVERY ROUTINES TO
*                                 ISSUE AN SVC 13 AND PERCOLATE
         ABEND (1),DUMP,,SYSTEM   ABEND THE CURRENT TASK
         SPACE 3
NODMPOPT DC    X'DFFFFFFF'                                     @ZA15739
         TITLE 'IEAVESVR- PSA MAPPING'
         IHAPSA
         END
