         TITLE 'IFCST005 SUMMARY FOR 3705 27RN'
         GBLC  &DSECT,&CSECT
&CSECT   SETC  'IFCST005'
&DSECT   SETC  'S3705'
&CSECT   CSECT
&DSECT   DSECT
         DSGEN (RECT,8),(OPSYS,8),(SWITCH,24),(RCDID,8),(SPARE1,16)
         DSGEN (DATE,32),(TIME,32)
         DSGEN (SPARE2,8),(CPUSER,24),(MODEL,16),(SPARE3,16)
         DSGEN (CUA,16),(TERMN1,16),(TERMN2,32)
         DSGEN (TERMN3,16),(RESID,16),(LINEADD,16),(RECMD,8),(RECID,8)
* NOTE THAT THERE WILL BE FOUR BYTES EXTRA IN HERE IF BIT ZERO OF
* RECMD IS ON.                                                 @D3502BL
         DSGEN (ISBOX,8),(BTUMOD,8),(REG100,8),(REG108,8),             C
               (REG200,8),(REG208,8),(REG300,8),(REG308,8)
         DSGEN (REG400,16),(REG500,8),(REG508,8)
         DSGEN (REG600,8),(REG608,8),(REG700,16)
         DSGEN (REG800,8),(,8),(,16)
         EJECT
&CSECT   CSECT                                                 @D3502BL
         USING &DSECT,R2                                       @D3502BL
         USING *,R9
         DC    A(ENTRIES)
         DC    A(PEDIT)
REG8     DC    F'0'
SCSIZE   DC    AL2(TABSIZE1)       # OF HALF-WORD COUNTERS
         SPACE 2                                               @D3502BL
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
         SPACE 2                                               @D3502BL
L0       EQU   0
L1       EQU   1
L2       EQU   2
L3       EQU   3
L4       EQU   4
L5       EQU   5
L6       EQU   6
L7       EQU   7
L8       EQU   8
L9       EQU   9
L10      EQU   10
L12      EQU   12
L14      EQU   14
L16      EQU   16
L18      EQU   18
L20      EQU   20
L22      EQU   22
L23      EQU   23
L24      EQU   24
L26      EQU   26
L28      EQU   28
L34      EQU   34                                              @D3502BL
L46      EQU   46
H00      EQU   X'00'
H01      EQU   X'01'
H10      EQU   X'10'
HEX01    EQU   X'01'
HEX02    EQU   X'02'
HEX03    EQU   X'03'
HEX04    EQU   X'04'
HEX08    EQU   X'08'
HEX10    EQU   X'10'
HEX11    EQU   X'11'
HEX12    EQU   X'12'
HEX13    EQU   X'13'
HEX20    EQU   X'20'
HEX40    EQU   X'40'
HEX80    EQU   X'80'
HEX84    EQU   X'84'
HEXC0    EQU   X'C0'
SRECMD   DC    X'00'                 SAVE RECORDING MODE       @D3502BL
*
*        SET UP FROM - TO DATE
*
XDATE1   DS    0H                                              @D3502BL
         CLI   RECMD,X'84'             MODE 84?                @D3502BL
         BE    NOSUM                   YES...DON'T SUM         @D3502BL
         TM    RECMD,X'A0'             MODE AX?                @D3502BL
         BO    NOSUM                   YES...DON'T SUM THIS EITHER
         STM   R5,R8,XSAVE1            SAVE REGS               @D3502BL
         CLC   DATE(L4),HEX00
         BE    RETURNX                                         @D3502BL
         CLC   L0(L4,R3),HEX00
         BNE   XDATE2
         MVC   L0(L4,R3),DATE
XDATE2   CLC   DATE(L4),L0(R3)         CK FOR LO DATE            SM5193
         BL    SAVELOW                 LOWER SAVE IT             SM5193
         CLC   DATE(L4),L4(R3)                                   SM5193
         BNH   RETURNX                                           SM5193
SAVEHI   MVC   L4(L4,R3),DATE
         B     RETURNX                                           OM5193
SAVELOW  MVC   L0(L4,R3),DATE          MOVE LOW DATE             OM5193
         B     RETURNX                                           OM5193
*
RETURN1  L     R2,SAVER2                                       @D3502BL
RETURNX  LM    R5,R8,XSAVE1                                    @D3502BL
DUMMY    BR 14                                                 @XA12830
         SPACE 3                                               @D3502BL
NOSUM    MVC   REG8(4),ASUMEND         GO TO END OF SUMMARY    @D3502BL
         BR    R14                                             @D3502BL
ASUMEND  DC    A(SUMEND-6)                                     @D3502BL
         SPACE 3                                               @D3502BL
**
**   LOCATE THE PROPER ENTRY FOR THIS RECORD
**
LOCATE   STM   R5,R8,XSAVE1        SAVE REGISTERS
         MVC   SRECMD(1),RECMD     SAVE RECORDING MODE         @D3502BL
         NI    SRECMD,X'7F'        BLANK OUT THE 'LONG' BIT    @D3502BL
         CLI   SRECMD,HEX10        IS THE RECORDING MODE LESS THAN
         BL    FIND                CHANNEL ADAPTER             @VS48714
         XC    LINEADD(2),LINEADD  IF SO, CLEAR LIBADR
FIND     LA    R5,200              NUMBER OF ENTRIES           @VS48714
         LR    R6,R3               START OF TABLE
         MVC   VALUES(L12),TERMN1  MOVE IN SORT KEY
FIND1    CLC   TERMN1(L12),L0(R6)  IS THIS THE RIGHT SET OF CTRS
         BE    UPDATE              YES, EXIT
         OC    L0(L12,R6),L0(R6)   END OF ENTRIES
         BZ    UPDATE              YES, EXIT
         LA    R6,L34(R6)          STEP TO NEXT ENTRY          @D3502BL
         BCT   R5,FIND1            NEXT ENTRY
         XC    VALUES(L12),VALUES  CLEAR THE SORT KEY OF 51ST ENTRY
         MVC   VALUES(L8),OVERFLOW INDICATE OVERFLOW
UPDATE   MVC   VALUES+L12(L22),L12(R6) MOVE IN COUNTERS        @D3502VS
         ST    R6,WHEREGOT         SAVE LOCATION
         B     RETURNX             EXIT                        @D3502BL
         SPACE 3                                               @D3502BL
**
**   TOTAL THE RECORD INTO THE APPROPRIATE COUNTERS
**
TOTAL    STM   R5,R8,XSAVE1        SAVE REGISTERS
         ST    R2,SAVER2           SAVE RECORD POINTER         @D3502BL
         TM    RECMD,X'80'         IS THE 'LONG' BIT ON?       @D3502BL
         BZ    NOT8X1              NO...                       @D3502BL
         AH    R2,HWD4             BACK UP 4 BYTES             @D3502BL
NOT8X1   CLI   SRECMD,HEX01        LINE STATISTICS             @D3502BL
         BNE   TOTAL2              NO, BYPASS
         SR    R6,R6
         ICM   R6,X'1',REG800      GET TEMP COUNTERS
         ICM   R7,X'3',VTEMPS      GET TOTALS
         AR    R7,R6               SUM
         STCM  R7,X'3',VTEMPS      STORE
         ICM   R6,X'3',REG700      GET IO OPERS
         ICM   R7,X'F',VIOPS       GET TOTALS
         AR    R7,R6
         STCM  R7,X'F',VIOPS       STORE TOTALS
         B     RETURN1
TOTAL2   SR    R6,R6
         ICM   R6,X'3',VPERMS      GET PERMS
         LA    R6,L1(R6)           ADD ONE
         STCM  R6,X'3',VPERMS      STORE
         CLI   SRECMD,HEX10        CHANNEL ADAPTER?            @D3502BL
         BL    TOTAL1              LESS THAN, EXIT
         CLI   RECMD,HEX10+HEX01   COMMUNICATIONS SCAN
         BH    TOTAL3              HIGH, TOTAL AS MISC
         SR    R6,R6
         ICM   R6,X'3',VHDWR       GET HARDWARE TOTALS
         LA    R6,L1(R6)           ADD ONE
         STCM  R6,X'3',VHDWR       STORE
         B     RETURN1
TOTAL3   SR    R6,R6
         ICM   R6,X'3',VMISC       GET MISCELLANEOUS TOTALS
         LA    R6,L1(R6)           ADD ONE
         STCM  R6,X'3',VMISC       STORE
         B     RETURN1
TOTAL1   SR    R6,R6
         ICM   R6,X'1',REG800      GET TEMP COUNTERS
         ICM   R7,X'3',VTEMPS      GET TOTALS
         AR    R7,R6               SUM
         STCM  R7,X'3',VTEMPS      STORE
         ICM   R6,X'3',REG700      GET IO OPERS
         ICM   R7,X'F',VIOPS       GET TOTALS
         AR    R7,R6
         STCM  R7,X'F',VIOPS       STORE TOTALS
         MVC   STATUS,REG508       GET INITAL ERROR STATUS
         OC    STATUS,STATUS       IS IT ZERO
         BNZ   TOTAL6              IF NO, BYPASS
         MVC   STATUS,REG400       IF YES, USE FINAL ERROR STATUS
TOTAL6   TM    STATUS,X'10'        DATA CHECK?                 @ZA36953
         BO    TOTAL7              YES, GO TOTAL               @ZA36953
         TM    STATUS,X'20'        SYNC CHECK?                 @ZA36953
         BNO   TOTAL8              NO, GO TO NEXT TEST         @ZA36953
TOTAL7   SR    R6,R6
         ICM   R6,X'3',VDATACK     GET DATA CHECK TOTALS
         LA    R6,L1(R6)           ADD ONE
         STCM  R6,X'3',VDATACK     STORE
         B     RETURN1
TOTAL8   TM    STATUS,X'40'        STAT FORM                   @ZA36953
         BNO   TOTAL9              NO, GO TO NEXT TEST         @ZA36953
TOTAL13  SR    R6,R6
         ICM   R6,X'3',VRCV        GET RECEIVER TOTALS
         LA    R6,L1(R6)           ADD ONE
         STCM  R6,X'3',VRCV        STORE
         B     RETURN1
TOTAL9   OC    STATUS+L2(L1),STATUS+L2 IS STATUS ZERO
         BZ    ANALYZE             IF YES, GO TO ANALYZE SECTION
         TM    STATUS+L2,HEX80+HEX08+HEX02 OVERRUN/UNDERRUN/ABORT?
         BZ    TOTAL10             NO, GO TO NEXT TEST
TOTAL11  SR    R6,R6
         ICM   R6,X'3',VMISC       GET MISCELLANEOUS TOTALS
         LA    R6,L1(R6)           ADD ONE
         STCM  R6,X'3',VMISC       STORE
         B     RETURN1
TOTAL10  TM    STATUS+L2,HEX40+HEX20+HEX01 LINE-QUIET/DLE/SDLC
         BZ    TOTAL11             NO, GO TOTAL AS MISCELLANEOUS
         B     TOTAL13
ANALYZE  NI    STATUS+L1,X'FE'     SET LEADING GRAPHICS BIT OFF
         LA    R8,LXBSTATB         GET START OF LXB TABLE
         LA    R7,LLXBSTB          GET LENGTH OF LXB TABLE
         CLI   SRECMD,H00          IS IT A PERM LINE ERROR     @D3502BL
         BNE   TRT                 NO, GO LOOK UP IN TABLE
         LA    R8,IOBSTATB         GET START OF IOB TABLE
         LA    R7,LIOBSTB          GET LENGTH OF IOB TABLE
         CLI   STATUS+1,H00        IS IT ZERO?
         BNE   TRT                 NO, GO LOOK UP IN TABLE
         NI    STATUS,HEX08+HEX04+HEX02 AND OFF BITS
         CLI   STATUS,HEX04
         BE    TOTAL7              GO ADD TO DATA CHECK COUNTERS
         CLI   STATUS,HEX04+HEX02
         BNE   TOTAL11             GO ADD TO MISCELLANEOUS
         SR    R6,R6
         ICM   R6,X'3',VINTRQD     GET INTERVENTION REQUIRED TOTALS
         LA    R6,L1(R6)           ADD ONE
         STCM  R6,X'3',VINTRQD     STORE
         B     RETURN1
TRT      CLC   STATUS+L1(L1),L0(R8) CHECK FOR VALUE IN TABLE
         BE    FOUND               YES, EXIT
         LA    R8,L2(R8)           UP TO NEXT VALUE
         BCT   R7,TRT              LOOP
FOUND    SR    R7,R7
         ICM   R7,X'1',L1(R8)      GET INDEX VALUE
         LA    R7,VALUES(R7)       GET TO COUNTER
         SR    R6,R6
         ICM   R6,X'3',L0(R7)      GET COUNTER VALUE
         LA    R6,L1(R6)           ADD ONE
         STCM  R6,X'3',L0(R7)      STORE
         B     RETURN1
         SPACE 3                                               @D3502BL
**
**   RESTORE THE COUNTERS TO THE TABLE
**
RESTORE  STM   R5,R8,XSAVE1
         L     R6,WHEREGOT         LOAD THE COUNTER LOCATION
         MVC   L0(L34,R6),VALUES   PLACE BACK INTO TABLE       @D3502BL
         B     RETURNX                                         @D3502BL
         EJECT
         DS    0D
XSAVE1   DC    4F'0'                                           @D3502BL
SAVER2   DC    F'0'                                            @D3502BL
HEX00    DC    F'0'
WHEREGOT DC    F'0'
HWD4     DC    H'4'                                            @D3502BL
VALUES   DS    0CL34                                           @D3502BL
VTERMN   DS    CL8
VRESID   DS    CL2
VLIBADR  DS    CL2
VIOPS    DS    CL4
VTEMPS   DS    CL2
VPERMS   DS    CL2
VHDWR    DS    CL2
VTMOUT   DS    CL2
VDATACK  DS    CL2
VRCV     DS    CL2
VINTRQD  DS    CL2
VMISC    DS    CL2
VMODEM   DS    CL2                                             @D3502BL
OVERFLOW DC    8X'FF'                                          @D3502BL
STATUS   DS    CL3
         EJECT
IOBSTATB DC    X'E4',AL1(VHDWR-VALUES)
         DC    X'E8',AL1(VHDWR-VALUES)
         DC    X'EA',AL1(VHDWR-VALUES)
         DC    X'F6',AL1(VHDWR-VALUES)                         @D3502BL
         DC    X'F0',AL1(VMODEM-VALUES)                        @D3502BL
         DC    X'F2',AL1(VMODEM-VALUES)                        @D3502BL
         DC    X'F4',AL1(VMODEM-VALUES)                        @D3502BL
         DC    X'F8',AL1(VMODEM-VALUES)                        @D3502BL
         DC    X'20',AL1(VTMOUT-VALUES)
         DC    X'40',AL1(VTMOUT-VALUES)
         DC    X'60',AL1(VTMOUT-VALUES)
         DC    X'80',AL1(VTMOUT-VALUES)
         DC    X'FC',AL1(VTMOUT-VALUES)
         DC    X'0C',AL1(VTMOUT-VALUES)
         DC    X'06',AL1(VRCV-VALUES)
         DC    X'08',AL1(VRCV-VALUES)
         DC    X'0A',AL1(VRCV-VALUES)
         DC    X'92',AL1(VRCV-VALUES)
         DC    X'9A',AL1(VRCV-VALUES)
LIOBSTB  EQU   (*-IOBSTATB)/2
         DC    X'00',AL1(VMISC-VALUES)
LXBSTATB DC    X'E4',AL1(VHDWR-VALUES)
         DC    X'E8',AL1(VHDWR-VALUES)
         DC    X'EA',AL1(VHDWR-VALUES)
         DC    X'F6',AL1(VHDWR-VALUES)                         @D3502BL
         DC    X'F0',AL1(VMODEM-VALUES)                        @D3502BL
         DC    X'F2',AL1(VMODEM-VALUES)                        @D3502BL
         DC    X'F4',AL1(VMODEM-VALUES)                        @D3502BL
         DC    X'F8',AL1(VMODEM-VALUES)                        @D3502BL
         DC    X'00',AL1(VTMOUT-VALUES)
         DC    X'20',AL1(VTMOUT-VALUES)
         DC    X'60',AL1(VTMOUT-VALUES)
         DC    X'80',AL1(VTMOUT-VALUES)
         DC    X'A0',AL1(VTMOUT-VALUES)
         DC    X'FC',AL1(VTMOUT-VALUES)
         DC    X'0C',AL1(VDATACK-VALUES)
         DC    X'0E',AL1(VDATACK-VALUES)
         DC    X'2C',AL1(VDATACK-VALUES)
         DC    X'2E',AL1(VDATACK-VALUES)
         DC    X'24',AL1(VRCV-VALUES)
         DC    X'64',AL1(VRCV-VALUES)
         DC    X'A2',AL1(VRCV-VALUES)
         DC    X'A4',AL1(VRCV-VALUES)
         DC    X'A6',AL1(VRCV-VALUES)
LLXBSTB  EQU   (*-LXBSTATB)/2
         DC    X'00',AL1(VMISC-VALUES)
         EJECT
ENTRIES  DS    0F
         SPECIAL XDATE1,00000000,SPACE=4
         SPACE 2                                               @D3502BL
         SPECIAL LOCATE,00000000,SPACE=3
         SPACE 2                                               @D3502BL
         SPECIAL TOTAL,00000000,SPACE=4
         SPACE 2                                               @D3502BL
         SPECIAL RESTORE,00000000,SPACE=4
         SPACE 2                                               @D3502BL
SUMEND   DC    X'FF'                                           @D3502BL
         EJECT
*
PEDIT    DS    0F
*
&DSECT   SETC  'T3705'
&CSECT   SETC  'PEDIT'
         USING *,9
         USING &DSECT,R2                                       @D3502BL
         DC    A(PENTRY)
         DC    C'IFCST005'
YR9PTR   DS    F
YLABEL   DS    F
&DSECT   DSECT                                                 @D3502BL
         DS    3H
         DSGEN (YDUMA,16),(YNOREC,16)
         DSGEN (YTYPE,8),(YCHAN,8),(YCUA,24),(YSER,24)
         DSGEN (YCPU,16),(YSMSW,16)
         DSGEN (TDAY1,32),(TDAY2,32)
ITEM1    DSGEN (TERMN,64)
         DSGEN (RESID1,16),(LIBADDR1,16)
         DSGEN (IOPS1,32)
         DSGEN (TEMPS1,16),(PERMS1,16)
         DSGEN (HDWR1,16),(TMOUT1,16),(DATACK1,16),(RCV1,16)
         DSGEN (INTRQD1,16),(MISC1,16),(MODEM1,16)             @D3502BL
TERMNO2  DS    CL34                                            @D3502BL
TERMNO3  DS    CL34                                            @D3502BL
TERMNO4  DS    CL34                                            @D3502BL
TERMNO5  DS    CL34                                            @D3502BL
TERMNO6  DS    CL34                                            @D3502BL
TERMNO7  DS    CL34                                            @D3502BL
TERMNO8  DS    CL34                                            @D3502BL
TERMNO9  DS    CL34                                            @D3502BL
TERMNO10 DS    CL34                                            @D3502BL
TERMNO11 DS    CL34                                            @D3502BL
TERMNO12 DS    CL34                                            @D3502BL
TERMNO13 DS    CL34                                            @D3502BL
TERMNO14 DS    CL34                                            @D3502BL
TERMNO15 DS    CL34                                            @D3502BL
TERMNO16 DS    CL34                                            @D3502BL
TERMNO17 DS    CL34                                            @D3502BL
TERMNO18 DS    CL34                                            @D3502BL
TERMNO19 DS    CL34                                            @D3502BL
TERMNO20 DS    CL34                                            @D3502BL
TERMNO21 DS    CL34                                            @D3502BL
TERMNO22 DS    CL34                                            @D3502BL
TERMNO23 DS    CL34                                            @D3502BL
TERMNO24 DS    CL34                                            @D3502BL
TERMNO25 DS    CL34                                            @D3502BL
TERMNO26 DS    CL34                                            @D3502BL
TERMNO27 DS    CL34                                            @D3502BL
TERMNO28 DS    CL34                                            @D3502BL
TERMNO29 DS    CL34                                            @D3502BL
TERMNO30 DS    CL34                                            @D3502BL
TERMNO31 DS    CL34                                            @D3502BL
TERMNO32 DS    CL34                                            @D3502BL
TERMNO33 DS    CL34                                            @D3502BL
TERMNO34 DS    CL34                                            @D3502BL
TERMNO35 DS    CL34                                            @D3502BL
TERMNO36 DS    CL34                                            @D3502BL
TERMNO37 DS    CL34                                            @D3502BL
TERMNO38 DS    CL34                                            @D3502BL
TERMNO39 DS    CL34                                            @D3502BL
TERMNO40 DS    CL34                                            @D3502BL
TERMNO41 DS    CL34                                            @D3502BL
TERMNO42 DS    CL34                                            @D3502BL
TERMNO43 DS    CL34                                            @D3502BL
TERMNO44 DS    CL34                                            @D3502BL
TERMNO45 DS    CL34                                            @D3502BL
TERMNO46 DS    CL34                                            @D3502BL
TERMNO47 DS    CL34                                            @D3502BL
TERMNO48 DS    CL34                                            @D3502BL
TERMNO49 DS    CL34                                            @D3502BL
TERMNO50 DS    CL34                                            @D3502BL
TERMNO51 DS    CL34                                            @D3502BL
TERMS    DS    150CL34                                         @VS48714
TABSIZE  EQU   (*-T3705)
TABSIZE1 EQU   (*-TDAY1)/2
         EJECT
IFCST005 CSECT
*
*        CHECK FOR CPU AND SERIAL NUMBER
*
SETCPUNO STM   R5,R8,XSAVE             SAVE REGS
         SR    R5,R5                   CLEAR REG 5
         IC    R5,L0(R4)               PICK UP MODEL NUMBER
         SLL   R5,L8                    INTO
         IC    R5,L1(R4)               R5
         LTR   R5,R5                   TEST FOR ZERO
         BNZ   RETURN                  RETURN
         LR    R5,R3                   PUT LINE ADDRESS IN R5
         LA    R6,L46                  OFFSET IN REG 6
         SR    R5,R6                   SUBTRACT OFFSET FROM POINTER
         MVC   L0(L6,R5),SHARED         PUT IN SHARED          @Z427VLS
RETURN   LM    R5,R8,XSAVE
SKIPPER  BR    R14                 NOP RETURN
         SPACE 3                                               @D3502BL
*
*        SET THE DATE READY FOR PRINT
*
SETDATE  UNPK  XSAVE(L8),L1(L3,R4)     UNPACK THE DATE
         MVC   L0(L3,R3),XSAVE+L5      MOVE DAY
         MVC   L6(L2,R3),XSAVE+L3      MOVE YEAR
         BR    R14
         SPACE 3                                               @D3502BL
**
**    FREE THIS SET OF COUNTERS AND CHECK FOR ANOTHER SET.  IF
**    YES, GO TO PRINT THE NEXT SET OF COUNTERS.
**
THEEND   STM   R14,R12,L12(R13)    SAVE ALL REGISTERS
         LR    R12,R13             SAVE THE SAVE ADDRESS
         LA    R13,FSAVE           POINT TO MY SAVE
         ST    R12,L4(R13)         SET CHAINING POINTERS
         ST    R13,L8(R12)
         L     R3,L4(R2)           PICK UP POINTER TO NEXT COUNTER
         LH    R0,SIZE             PICK UP SIZE OF AREA
         LR    R1,R2               SET ADDRESS
         FREEMAIN R,LV=(0),A=(1)
         LTR   R3,R3               ANY MORE COUNTERS
         BZ    RETURNO             NO RETURN
         LA    R5,PENTRY+L10       POINT TO TOP ENTRY LESS 4
         ST    R5,YR9PTR
         ST    R3,L28(R12)         SAVE POINTER IN REG SIZE   @ZA00435
         LA    R5,LABELS-L1        POINT TO FIRST LABEL LESS 1
         ST    R5,YLABEL           STORE IT IN TABLE
RETURNO  L     R13,L4(R13)         PICK UP &
         LM    R14,R12,L12(R13)    RESTORE REGS
         XC    VALINDX,VALINDX     CLEAR THE INDEX
         BR    R14
         SPACE 3                                               @D3502BL
MVCNAME  MVC   L0(L8,R3),TERMN     TERMINAL NAME
         LA    R3,L3(R3)           INCREMENT LINE POINTER BY THREE
         BR    R14
         SPACE 3                                               @D3502BL
SUB12    SH    R3,TWELVE           DECREMENT THE LINE POINTER
         BR    R14                 EXIT
SUB5     SH    R3,FIVE             DECREMENT THE LINE POINTER
         BR    R14                 EXIT
SUB8     SH    R3,EIGHT            DECREMENT THE LINE POINTER
         BR    R14                 EXIT
SUB7     SH    R3,SEVEN            DECREMENT THE LINE POINTER
         BR    R14                 EXIT
         SPACE 3                                               @D3502BL
**
**   CHECK FOR THE NEXT ENTRY IN THE TABLE.  IF THERE IS ONE,
**   LOOP BACK TO PRINT IT.  IF NOT, TERMINATE THE SUMMARY SECTION.
**
NEXTVAL  STM   R5,R8,XSAVE         SAVE REGISTERS
         L     R5,VALINDX          INDEX INTO TABLE
         LA    R5,L34(R5)          NEXT ENTRY                  @D3502BL
         ST    R5,VALINDX          SAVE
         LA    R6,TERMN(R5)        ADDRESS OF START            @D3502BL
         CLC   TERMN(L8),OVERFL    IS THIS THE 51ST ENTRY
         BE    THATSIT             YES,EXIT
         MVC   TERMN(L34),L0(R6)                               @D3502BL
         OC    TERMN(L12),TERMN    IS THE NAME ZEROS
         BZ    THATSIT             IF YES, EXIT
         LA    R6,OUTLOOP-L4
         LA    R7,OUTLOOP1-L1
         STM   R6,R7,YR9PTR        SAVE NEW POINTERS
THATSIT  LM    R5,R8,XSAVE
         SR    R15,R15             DONT PRINT THE LINE
         BR    R14
         SPACE 3                                               @D3502BL
SORTT    DS    0H                                              @D3502BL
         STM   R5,R8,XSAVE             SAVE REGS               @D3502BL
SORTL0   LR    R5,R4                   POINT R5 TO FIRST ENTRY @D3502BL
         XR    R8,R8                   USE TO INDICATE END OF SORT
SORTL1   OC    34(8,R5),34(R5)         NEXT ENTRY EMPTY?       @D3502BL
         BZ    ENDPASS                 YES...                  @D3502BL
         CLC   34(8,R5),OVERFL         TABLE OVERFLOW?         @D3502BL
         BE    ENDPASS                 YES...                  @D3502BL
         CLC   0(8,R5),34(R5)          COMPARE TRMINAL NAMES   @D3502BL
         BNH   NOSWAP                  DON'T SWAP              @D3502BL
         XC    0(34,R5),34(R5)         SWAP                    @D3502BL
         XC    34(34,R5),0(R5)             THE                 @D3502BL
         XC    0(34,R5),34(R5)                ENTRIES          @D3502BL
         LA    R8,1                    INDICATE SWAP           @D3502BL
NOSWAP   LA    R5,34(R5)               POINT TO NEXT ENTRY     @D3502BL
         B     SORTL1                                          @D3502BL
ENDPASS  LTR   R8,R8                   SORT DONE?              @D3502BL
         BZ    RETURN                  YES...                  @D3502BL
         B     SORTL0                                          @D3502BL
         SPACE 3                                               @D3502BL
         DS    0D
XSAVE    DC    5F'0'
FSAVE    DS    18F
VALINDX  DC    F'0'
TWELVE   DC    H'12'
FIVE     DC    H'5'
SEVEN    DC    H'7'
EIGHT    DC    H'8'
SIZE     DC    AL2(TABSIZE)
OVERFL   DC    8X'FF'                                          @D3502BL
SHARED   DC    C'SHARED'
         EJECT
PENTRY   DS    0F
         DC    X'00010001'
         DC    X'00010001'
         DC    A(LABELS)
         DC    H'0'
         DC    X'01'
         DC    X'000B8B00'
         LABEL ' '
         SPACE 2                                               @D3502BL
         LINE  (77,23),(1,88),SKIP=2
         HEX   YCPU,0,2B
         HEX   YSER,0,3B
         ROUTINE  YCPU,SETCPUNO
         LABEL '---SUMMARY OF ENTRY TYPE - 3705 MDR        DEVICE TYPE C
               3705          MODEL-','SERIAL NO.'
         SPACE 2                                               @D3502BL
         LINE  ,(16,13),SKIP=1
         LABEL 'DAY YEAR','DAY YEAR'
         SPACE 2                                               @D3502BL
         LINE  (16,13),(4,21),SKIP=2
         ROUTINE  TDAY1,SETDATE
         ROUTINE  TDAY2,SETDATE
         LABEL 'DATE RANGE-','TO'                                OM5193
         SPACE 2                                               @D3502BL
         LINE  (26,49),(5,45)
         HEX   YCUA,0,3B
         CONVT YNOREC
         LABEL 'CHANNEL UNIT ADDRESS','TOTAL NUMBER OF RECORDS'
         SPACE 2                                               @D3502BL
         LINE  ,,SKIP=1
         LABEL ' '
         SPACE 2                                               @D3502BL
         LINE  ,(64,30)                                        @D3502BL
         LABEL '- - - - - - - - - - PERMANENT ERROR TYPES - - - - - - -X
                - - -'                                         @D3502BL
         SPACE 2                                               @D3502BL
         LINE  ,(20,10)
         LABEL 'LIB',' ',' TEMP',' PERM',,,,,,,'MODEM/'        @D3502BL
         SPACE 2                                               @D3502BL
         LINE  ,(1,30)
         LABEL 'TERM NAME   RID   ADDR    # I/',               @D3502BLX
               'O OPS    ERRORS    ERRORS     ',                       X
               '  HDWR    TM OUT   DATA CK   ',                        X
               '  RCV    ITV RQD    MISC     INTFC'            @D3502BL
         SPACE 2                                               @D3502BL
         LINE
         ROUTINE TERMN,SORTT                                   @D3502BL
         LABEL ' '
         SPACE 2                                               @D3502BL
OUTLOOP  LINE  (1,9),(60)
         ROUTINE TERMN,MVCNAME
         HEX   RESID1,0,2B
         ROUTINE TERMN,SUB12
         HEX   LIBADDR1,0,2B
         CONVT IOPS1,4,8
         ROUTINE TERMN,SUB5
         CONVT TEMPS1,2,6
         ROUTINE TERMN,SUB8
         CONVT PERMS1,2,6
         ROUTINE TERMN,SUB5
         CONVT HDWR1,2,5
         CONVT TMOUT1,2,5
         CONVT DATACK1,2,5
         ROUTINE TERMN,SUB7
         CONVT RCV1,2,5
         CONVT INTRQD1,2,5
         CONVT MISC1,2,5
         CONVT MODEM1,2,5                                      @D3502BL
OUTLOOP1 LABEL '%%'
         SPACE 2                                               @D3502BL
         LINE
         ROUTINE TERMN,NEXTVAL
         LABEL ' '
         SPACE 2                                               @D3502BL
         LINE
         ROUTINE TERMN,THEEND
         LABEL ' '
         SPACE 2                                               @D3502BL
         DC    X'FF'
         DC    XL2'0000'
         DC    XL2'0018'
         DC    XL2'0000'
         EJECT ,                                               @D3502BL
         DC    C'$'
LABELS   EQU   *
         LSTART
         END
