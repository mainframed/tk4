BEPS TITLE 'TSO EDIT COMMAND  PROCESSOR TABLE SEARCH  IKJEBEPS'
IKJEBEPS CSECT
***********************************************************************
*                                                                     *
*STATUS  --  VERSION NO. 01, O/S RELEASE NO. 20.                      *
*                                                                     *
*FUNCTION  --  THIS ROUTINE SEARCHES A TABLE OF CONSTANTS (CONTAINED  *
*   IN THE CSECT 'IKJEBEPD') FOR AN ENTRY CONTAINING A GIVEN DATA SET *
*   TYPE NAME OR FOR AN ENTRY CONTAINING A GIVEN DSNAME QUALIFIER AND *
*   THE DEFAULT DATA SET TYPE NAME FOR THAT QUALIFIER.                *
*                                                                     *
*   IKJEBEPS AND IKJEBEPD ARE THE 1ST AND 2ND CSECTS OF 1 LOAD MODULE.*
*                                                                     *
*ENTRY POINTS  --                                                     *
*        IKJEBEPS - BY A BRANCH-AND-LINK, FOR A TABLE SEARCH.         *
*                                                                     *
*INPUT  --  IF REGISTER 1 IS NEGATIVE, IT IS CONSIDERED THE COMPLE-   *
*   MENTED ADDRESS OF A NAME LEFT-JUSTIFIED IN AN 8-BYTE AREA AND     *
*   PADDED WITH BLANKS, AND THE TABLE SEARCH IS DONE ON THE DSNAME    *
*   QUALIFIERS IN THE TABLE.  OTHERWISE REGISTER 1 IS CONSIDERED THE  *
*   ADDRESS OF A NAME, AND THE SEARCH IS DONE ON DATA SET TYPE NAMES. *
*                                                                     *
*OUTPUT  --  IF A TABLE ENTRY IS FOUND, REGISTER 15 IS SET TO 0 AND   *
*   REGISTER 1 POINTS TO THE BEGINNING OF THE TABLE ENTRY.  OTHERWISE *
*   REGISTER 15 IS SET TO 4.                                          *
*                                                                     *
*EXTERNAL REFERENCES  --                                              *
*        TABLE    - IN THE CSECT 'IKJEBEPD'                           *
*   MACROS USED --                                                    *
*   IKJEBECA -- DEFINES PROCESSOR DATA TABLE PORTION OF EDIT          *
*   COMMUNICATION AREA.                                               *
*                                                                     *
*EXITS,NORMAL  --  BY A BRANCH TO THE CALLER, WITH RETURN CODE IN     *
*   REGISTER 15 AND POINTER IN REGISTER 1 (WHEN RETURN CODE IS ZERO). *
*                                                                     *
*EXITS,ERROR  --  NONE.                                               *
*                                                                     *
*TABLES/WORK AREAS  --  PROCESSOR DATA TABLE, CSECT 'IKJEBEPD'        *
*                                                                     *
*ATTRIBUTES  --  REFRESHABLE, ENABLED, NON-PRIVILEGED, READ-ONLY.     *
*                                                                     *
*NOTES  --  THERE IS CHARACTER-CODE INDEPENDENCE.                     *
*                                                                     *
***********************************************************************
         EJECT
         SPACE
***********************************************************************
* REGISTER EQUATES -
***********************************************************************
         SPACE
PARMREG  EQU   1                        ADDR OF INPUT DSTYPE OR
*                                       COMPLEMENT ADDR OF DSNAME
*                                       QUALIFIER OR ADDR OF FOUND
*                                       TABLE ENTRY.
REG02    EQU   2
TBLNTRY@ EQU   7                        WORKING PT TO TABLE
REG12    EQU   12
BASEREG  EQU   12                       BASE REGISTER
SAVAREA@ EQU   13                       ADDR OF REGISTER SAVE AREA
REG14    EQU   14
RETURN@  EQU   14                       ADDR OF RETURN POINT.
CODEREG  EQU   15                       RETURN CODE - 0/FOUND, 4/NOT
         SPACE
***********************************************************************
* CONSTANT EQUATES -
***********************************************************************
         SPACE
O        EQU   0
RETCOD4  EQU   4                        'NOT FOUND' RETURN CODE
FOURTHWD EQU   12
EIGHTHWD EQU   28
TBLEND   EQU   X'0'                     TO TEST END-OF-TABLE MARKER.
ATRIBUT2 EQU   42                       OFFSET TO 2ND DATA SET
*                                       ATTRIBUTE BYTE IN TABLE.
DEFAULT  EQU   X'40'                    BIT SIGNIFYING WHETHER
*                                       DSTYPE IS THE DEFAULT FOR
*                                       THE DSNAME QUALIFIER -
*                                       1/YES, 0/NO.
*                                       1/YES, 0/NO.
*                                       1/YES, 0/NO.
         EJECT
***********************************************************************
* ENTRY LINKAGE -
***********************************************************************
         SPACE
         STM   REG14,REG12,FOURTHWD(SAVAREA@) SAVE REG 0-12,14,15.
         BALR  BASEREG,O                ESTABLISH ADDRESSABILITY.
         USING *,BASEREG
         SPACE
***********************************************************************
* PROCESSING -
***********************************************************************
         SPACE
         L     TBLNTRY@,ATABLE          GET ADDR OF 1ST TBL ENTRY.
         USING IKJEBECA,TBLNTRY@
         LTR   PARMREG,PARMREG          IS INPUT NEGATIVE -
         BM    QUALFIER                  IF YES, BRANCH TO SEARCH
*                                                DSNAME QUALIFIERS.
*                                        IF NO, SEARCH DSTYPES.
TYPECOMP CLC   O(L'CADSTYPE,PARMREG),CADSTYPE CMP INPUT NAME WITH
*                                       CURRENT DSTYPE NAME -
         BE    POINT                     IF SAME, BRANCH TO SET PT
*                                                 TO CURR TBL ENTRY.
         CLI   TBLNTRYL(TBLNTRY@),TBLEND IF DIFFERENT, ARE THERE ANY
*                                                      MORE ENTRIES-
         BE    NOTFOUND                   IF NOT, BRANCH TO SET 'NOT
*                                                 FOUND' RET CODE.
         LA    TBLNTRY@,TBLNTRYL(TBLNTRY@) IF SO, GET ADDR NEXT
*                                                 ENTRY.
         B     TYPECOMP                 BRANCH TO DSTYPE COMPARE.
QUALFIER LCR   PARMREG,PARMREG          COMPLEMENT INPUT.
QUALCOMP CLC   O(L'CADSQUAL,PARMREG),CADSQUAL  CMP INPUT
*                                       NAME WITH CURR QUALIFIER -
         BE    DFALTASK                  IF SAME, BRANCH TO FIND
*                                                 IF DEFAULT TYPE.
ANYMORQL CLI   TBLNTRYL(TBLNTRY@),TBLEND IF DIFFERENT, ARE THERE ANY
*                                                      MORE ENTRIES-
         BE    NOTFOUND                   IF NOT, BRANCH TO SET 'NOT
*                                                 FOUND' RET CODE.
         LA    TBLNTRY@,TBLNTRYL(TBLNTRY@) IF SO, GET ADDR NEXT
*                                                 ENTRY.
         B     QUALCOMP                 BRANCH TO QUALIFIER CMP.
DFALTASK TM    CADSATR2,CADSNDEF  IS THIS THE DEFAULT
*                                       DSTYPE FOR THE QUALIFIER -
         BNO   ANYMORQL                  IF NOT, BRANCH TO END-TEST.
POINT    LR    PARMREG,TBLNTRY@          IF SO, SET PT CURR ENTRY.
         SR    CODEREG,CODEREG          SET RET CODE 0 (FOUND).
         B     EXEUNT                   GO TO EXIT LINKAGE.
NOTFOUND LA    CODEREG,RETCOD4          SET RET CODE 4 (NOT FOUND).
         EJECT
***********************************************************************
* EXIT LINKAGE -
***********************************************************************
         SPACE
         SPACE
         SPACE
EXEUNT   L     RETURN@,FOURTHWD(SAVAREA@) RESTORE REGISTERS 14 AND
         LM    REG02,REG12,EIGHTHWD(SAVAREA@) 2 THROUGH 12.
         BR    RETURN@                  RETURN CONTROL TO CALLER.
         SPACE
***********************************************************************
* ADDRESS CONSTANT -
***********************************************************************
         SPACE
ATABLE   DC    V(IKJEBEPD)         ADDR OF PROCESSOR DATA TABLE
         IKJEBECA COMAREA=PDTABLE
TBLNTRYL EQU   CAPDEND-CAPD        LENGTH OF TABLE
         END
