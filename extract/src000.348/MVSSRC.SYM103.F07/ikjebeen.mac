BEEN     TITLE 'TSO EDIT - IKJEBEEN - END SUBCOMMAND PROCESSOR'
IKJEBEEN CSECT
*
*A271984,273960                                                  A45718
*D272000,273000                                                  A45718
*C258580,422000-423000                                           A50474
*D258900-258920                                                  A50474
*A283500-285500,293100-292500                                    A50473
*D284000-288000                                                  A50473
*A367500,531500                                                 YA00035
*C371000                                                        YA00035
*D368000-370000                                                 YA00035
*A130500,209100-210500,273940,389500-391349,528500-528960,       Y01676
*A530900,531520-531900,533500,581100-581517,581560,581600,       Y01676
*A581800                                                         Y01676
*A389000,083000                                                  Y02676
*A199700-200656,250500,530900-536500                             Y02676
*A165500,178100-178900,179100,273720-273730,307300,             Y30NQKC
*A485350-485900,486040-486920,487040-487840,                    Y30NQKC
*A514100-514700,530960,546560-546880,564300-564600,             Y30NQKC
*A585700,594500                                                 Y30NQKC
*C180000,486000,487000                                          Y30NQKC
*D181000,182000,183000,184000,185000,185500,185600              Y30NQKC
*A200638,200644                                                @OZ00809
*D389000                                                       @ZM32367
*A357010                                                       @ZM32367
* MSG IKJ52555I ISSUED WHEN USER IS CANCELED; FIX=BYPASS MSG   @ZA25432
         ENTRY IKJEBEEN
         ENTRY IKJEBEXT
         EJECT
***********************************************************************
*                                                                     *
* STATUS -- OS/VS2 RELEASE NO. 3                                      *
*                                 LINE DROP FROM PUTGET        @ZA11393
*                                                                     *
* FUNCTION --                                                         *
*                                                                     *
*    THE END SUBCOMMAND PROCESSOR (IKJEBEEN) PERFORMS ALL             *
*    CLEAN-UP FUNCTIONS REQUIRED FOR NORMAL OR ABNORMAL               *
*    TERMINATION OF THE EDIT COMMAND PROCESSOR.                       *
*                                                                     *
*    1. IF OPERANDS ARE PRESENT THEN PARSE THEM. IF NO OPERANDS THEN  *
*    CHECK TO SEE IF THE PUTGET BUFFER SHOULD BE FREED, IF SO THEN    *
*    FREE IT. NEXT, IF END WAS FOUND IN A CLIST THEN WE DO NOT WANT   *
*    TO PROMPT FOR SAVE OR END, WE JUST WANT TO TERMINATE WITHOUT     *
*    SAVING THE DATASET. IF THERE ARE NO OPERANDS AND END WAS NOT     *
*    FOUND IN A CLIST THEN WE CHECK TO SEE IF THE DATASET HAS BEEN    *
*    MODIFIED SINCE IT WAS LAST SAVED. IF IT HASN'T BEEN MODIFIED     *
*    THEN WE TERMINATE WITHOUT SAVING. IF IT HAS BEEN MODIFIED THEN   *
*    WE PROMPT FOR SAVE OR END.                                       *
*                                                                     *
*    2. AFTER THE USER HAS ENTERED A SUBCOMMAND, IKJSCAN IS           *
*    INVOKED TO SCAN THE INPUT BUFFER.  WHEN A 'SAVE' SUBCOMMAND      *
*    IS INPUT, IKJEBESA IS INVOKED TO PERFORM THE SAVE OPERATION.     *
*    UPON RETURN FROM IKJEBESA, IKJSCAN IS INVOKED AGAIN TO SCAN      *
*    THE INPUT BUFFER.  IF A SUBCOMMAND OTHER THAN 'END' IS           *
*    ENTERED, CONTROL IS RETURNED TO IKJEBEMA.                        *
*                                                                     *
*    3. WHEN AN 'END' SUBCOMMAND IS ENTERED IN 1. OR 2. ABOVE,        *
*    OR WHEN EDIT IS TERMINATING ABNORMALLY, IKJEBEEX IS INVOKED      *
*    TO DELETE THE EDIT ACCESS METHOD AND TO UNALLOCATE THE           *
*    UTILITY DATA SET.  WHEN SYNTAX CHECKING IS IN USE, OR WHEN       *
*    THE DATA SET TYPE IS BASIC OR IPLI, IKJEBESC IS INVOKED TO       *
*    PERFORM FINAL PROCESSING FOR THE SYNTAX CHECKER OR LANGUAGE      *
*    PREPROCESSOR.  THE ATTENTION EXIT AND THE ABNORMAL END EXIT      *
*    ARE CANCELLED, THE EDIT DATA SET IS UNALLOCATED (IF              *
*    ALLOCATED), AND THE PERMANENTLY RESIDENT SERVICE ROUTINES,       *
*    IKJEBEMS AND IKJEBELE, ARE DELETED.  CONTROL IS THEN             *
*    RETURNED TO IKJEBEMA.                                            *
*                                                                     *
*    THE CONTROL SECTION IKJEBEN0 CONTAINS THE PROMPT MESSAGE         *
*    USED TO PROMPT THE USER FOR AN 'END' OR 'SAVE' SUBCOMMAND.       *
*                                                                     *
* ENTRY POINTS --                                                     *
*         IKJEBEEN -- POINT OF ENTRY FOR 'END' SUBCOMMAND             *
*              PROCESSING.  CONTROL IS RECEIVED FROM IKJEBEMA         *
*              WITH AN 'END' SUBCOMMAND IN THE CURRENT INPUT          *
*              BUFFER.                                                *
*                                                                     *
*         IKJEBEXT -- POINT OF ENTRY FOR ABNORMAL TERMINATION         *
*              OF EDIT.                                               *
*                                                                     *
* INPUT --                                                            *
*    REGISTER 1 (ONE) CONTAINS THE ADDRESS OF THE EDIT COMMUNI-       *
*    CATION AREA IN ALL CASES.                                        *
*                                                                     *
* OUTPUT --                                                           *
*    A RETURN CODE IN REGISTER 15 (FIFTEEN).  RETURN CODES ARE --     *
*                                                                     *
*    00 - NORMAL RETURN TO IKJEBEMA, A SUBCOMMAND OTHER THAN          *
*    'SAVE' OR 'END' WAS ENCOUNTERED.  THE OFFSET FIELD IN THE        *
*    CURRENT INPUT BUFFER IS ZEROED AND THE 'NEXT SUBCOMMAND BUFFER'  *
*    (CAPTNBFR) IS SET TO THE ADDRESS OF THE BUFFER                   *
*    CONTAINING THE SUBCOMMAND TO BE PROCESSED NEXT.                  *
*                                                                     *
*    12 - ABNORMAL RETURN TO IKJEBEMA, TERMINATION PROCESSING         *
*    COMPLETE.  THIS RETURN CODE WITH BIT 2, BYTE 28 OF CAMAWKA       *
*    SET TO 1 (MAENDPRC = 1) INDICATES THAT END PROCESSING IS         *
*    COMPLETE.                                                        *
*                                                                     *
* EXTERNAL REFERENCES --                                              *
*         IKJEBEMS -- EDIT MESSAGE SELECTION ROUTINE.                 *
*         IKJPTGT  -- PUTGET SERVICE ROUTINE.                         *
*         IKJDAIR  -- DYNAMIC ALLOCATION SERVICE ROUTINE.             *
*         IKJEFF18 -- DAIRFAIL (DYNAMIC ALLOC. ERROR DIAGNOSIS RTN.). *
*         IKJEBESA -- SAVE SUBCOMMAND.                                *
*         IKJEBEEX -- UTILITY ACCESS METHOD CLEAN-UP ROUTINE.         *
*         IKJEBESC -- SCAN SUBCOMMAND.                                *
*         IKJSCAN  -- COMMAND SCAN SERVICE ROUTINE.                   *
*         IKJPARS  -- PARSE SERVICE ROUTINE                           *
*                                                                     *
*    MACROS USED --                                                   *
*         DELETE   -- FREE SERVICE ROUTINES.                          *
*         FREEMAIN -- RETURN GOTTEN CORE TO SYSTEM.                   *
*         LINK     -- INVOKE IKJSCAN,IKJDAIR AND DAIRFAIL SER. RTNS.  *
*         PUTGET   -- INVOKE PUTGET SERVICE ROUTINE.                  *
*         SAVE     -- SAVE ENTRY REGISTERS.                           *
*         ESTAE    -- CANCEL ABNORMAL END EXIT.                       *
*         STAX     -- CANCEL ATTENTION EXIT.                          *
*         IKJCPPL  -- MAP COMMAND PROCESSOR PARAMETER LIST.           *
*         IKJCSOA  -- MAP COMMAND SCAN OUTPUT AREA.                   *
*         IKJDAP18 -- MAP ENTRY CODE X'18' PARAMETER BLOCK.           *
*         IKJDAPL  -- MAP IJKDAIR PARAMETER LIST.                     *
*         IKJPGPB  -- MAP PUTGET PARAMETER BLOCK                      *
*         IKJEBECA -- DEFINE EDIT COMMUNICATION AREA.                 *
*         IKJEBEMG -- DEFINE INSERTION FOR EDIT MESSAGE.              *
*         IKJEBEMI -- DEFINE SYMBOLIC REFERENCES FOR MESSAGES.        *
*         IKJEBEML -- INVOKE EDIT MESSAGE SELECTION ROUTINE.          *
*         IKJEBERT -- RETURN TO CALLING ROUTINE.                      *
*         IKJEBESH -- INVOKE 'SCAN' OR 'SAVE' SUBCOMMAND.             *
*         CALLTSSR -- INVOKE IKJPARSE                                 *
*         IKJRLSA  -- RELEASE PARSE DESCRIPTOR LIST                   *
*                                                                     *
* EXITS, NORMAL --                                                    *
*    CONTROL IS RETURNED TO IKJEBEMA IN ALL CASES WITH A RETURN       *
*    CODE IS REGISTER 15 (FIFTEEN) AS OUTLINED IN OUTPUT ABOVE.       *
*                                                                     *
* EXITS, ERROR --                                                     *
*    DOES NOT APPLY.                                                  *
*                                                                     *
* TABLES/WORK AREAS --                                                *
*    THE EDIT COMMUNICATION AREA (IKJEBECA) CONTAINS ALL POINTERS     *
*    AND OTHER AREAS REFERENCED BY THIS SUBCOMMAND.                   *
*                                                                     *
* ATTRIBUTES --                                                       *
*    RE-ENTRANT, READ-ONLY, REFRESHABLE, NON-PRIVILEGED, ENABLED.     *
*                                                                     *
* CHARACTER CODE DEPENDENCY --                                        *
*    THIS MODULE IS DEPENDENT UPON THE EBCDIC REPRESENTATION          *
*    OF PACKED DECIMAL DIGITS USED IN THE CONVERSION OF RETURN        *
*    CODES TO PRINTABLE EBCDIC.                                       *
*                                                                     *
* NOTES --                                                            *
*    THIS SUBCOMMAND PROCESSOR USES THE MAIN CONTROLLER WORK AREA     *
*    (CAMAWKA) IN THE COMMUNICATION AREA.  THE COMMUNICATION OF       *
*    END PROCESSING BETWEEN THIS MODULE AND IKJEBEMA IS DEPENDENT     *
*    UPON BIT 2, BYTE 28 OF CAMAWKA.  CARE MUST BE EXERCISED IN       *
*    MODIFYING CAMAWKA IN EITHER THIS MODULE OR IKJEBEMA.             *
*                                                                     *
*    BIT 2, BYTE 28 OF 'CAMAWKA' ('MAENDPRC') IS USED IN THE          *
*    FOLLOWING WAY --                                                 *
*                                                                     *
*    UPON ENTRY TO THIS ROUTINE --                                    *
*    'MAENDPRC' IS SET TO ZERO IF A SUBCOMMAND BUFFER FOR 'END'       *
*    EXISTS AND ITS ADDRESS IS IN THE EDIT COMMUNICATION AREA.        *
*    'MAENDPRC' IS SET TO ONE IF NO SUBCOMMAND BUFFER EXISTS FOR THIS *
*    ROUTINE.                                                         *
*                                                                     *
*    UPON EXIT FROM THIS ROUTINE --                                   *
*    'MAENDPRC' IS SET TO ONE IF THE RETURN CODE FROM THIS ROUTINE IS *
*    12.  ( END PROCESSING IS COMPLETE.)                              *
*    'MAENDPRC' IS SET TO ZERO IF THE RETURN CODE FROM THIS ROUTINE   *
*    IS ZERO. ( A SUBCOMMAND OTHER THAN 'END' HAS BEEN ENTERED IN     *
*    RESPONSE TO THE 'ENTER SAVE OR END' OR 'SAVED' MODE MESSAGE.)    *
*                                                                     *
***********************************************************************
         EJECT
***********************************************************************
*                                                                     *
*   GENERAL REGISTER EQUATES USED IN THIS ROUTINE.                    *
*                                                                     *
***********************************************************************
PARMREG0 EQU   0              PARAMETER REGISTER 0 (ZERO).
PARMREG1 EQU   1              PARAMETER REGISTER 1.
FLAGREG  EQU   2              FLAG WORD FOR IKJSCAN.
LEV1REG  EQU   2              FIRST LEVEL MESSAGE NUMBER.
OUTPTREG EQU   2              OUTPUT FOR PUTGET                  Y01676
SCLENREG EQU   2              LENGTH OF SUBCOMMAND.
REG2     EQU   2              WORK REGISTER                     Y30NQKC
LEV2REG  EQU   3              SECOND LEVEL MESSAGE NUMBER.
OAREAREG EQU   3              OUTPUT AREA FOR IKJSCAN.
SCPTREG  EQU   3              ADDRESS OF SUBCOMMAND NAME.
INS1REG  EQU   4              ADDRESS OF FIRST LEVEL INSERTION.
INS2REG  EQU   5              ADDRESS OF SECOND LEVEL INSERTION.
BFRREG   EQU   6              ADDRESS OF SUBCOMMAND BUFFER.
PTGTREG  EQU   7              ADDRESS OF PUTGET PARM BLOCK.
BRNCHREG EQU   8              RETURN ADDRESS AFTER SUBROUTINE
*                                  PROCESSING.
COMMREG  EQU   9              COMMUNICATION AREA POINTER.
DSECTREG EQU   10             ADDRESSES PARAMETER BLOCK DSECT
BASEREG  EQU   11             BASE REGISTER.
DATAREG  EQU   12             POINTER TO DATA.
SAVEREG  EQU   13             SAVE AREA POINTER.
RETREG   EQU   14             ADDRESS OF RETURN POINT.
EPLOCREG EQU   15             ENTRY POINT REGISTER.
RETCDREG EQU   15             RETURN CODE REGISTER.
SCKNMREG EQU   2              FLOATING POINT REGISTER 2 - NAME OF
*                                  SYNTAX CHECKER.
         EJECT
         SAVE  (14,12),,*     SAVE ENTRY REGISTERS.
EN001010 EQU   *
         BALR  BASEREG,X0     ESTABLISH ADDRESSABILITY FOR THIS
         USING *,BASEREG           ROUTINE.
         LR    COMMREG,PARMREG1 SAVE ADDRESS OF COMMUNICATION
*                                  AREA.
         USING IKJEBECA,COMMREG ESTABLISH ADDRESSABILITY TO THE
*                                  COMMUNICATION AREA.
         L     EPLOCREG,CANXTSVA ADDRESS OF NEXT AVAILABLE SAVE
*                                  AREA.
         ST    SAVEREG,X4(EPLOCREG) CROSS CHAIN PREVIOUS AND
         ST    EPLOCREG,X8(,SAVEREG) CURRENT SAVE AREAS.
         LR    SAVEREG,EPLOCREG INITIALIZE SAVE AREA POINTER.
         LA    EPLOCREG,X72(,EPLOCREG) COMPUTE ADDRESS OF NEXT
         ST    EPLOCREG,CANXTSVA   SAVE AREA AND SAVE.
         SPACE 5
***********************************************************************
*                                                                     *
*   WHEN AN END SUBCOMMAND IS ENTERED, DETERMINE IF CURRENT INPUT     *
*   BUFFER MUST BE FREED.  WHEN BUFFER MUST BE FREED, ISSUE THE       *
*   FREEMAIN MACRO INSTRUCTION SPECIFYING THE LAST INPUT BUFFER       *
*   ADDRESS.  OTHERWISE, CHECK IF THE DATA SET HAS BEEN MODIFIED      *
*   SINCE THE LAST SAVE.  IF DATA SET HAS BEEN SAVED, PROCEED WITH    *
*   TERMINATION PROCESSING. IF OPERANDS ARE PRESENT THEN INVOKE PARSE *
*   TO SEE IF SAVE IS AN OPERAND. IF DATA SET HAS BEEN MODIFIED AND   *
*   THERE ARE NO OPERANDS THEN PROMPT THE USER FOR SAVE OR END.       *
*                                                                     *
***********************************************************************
         OI    ENCFLAGS,MAENDPRC SHOW END PROCESSING ATTEMPTED.
         TM    CAPTIBFR,CAOPERND   ARE OPERANDS PRESENT         Y30NQKC
         BO    EN007000            YES, THEN PARSE OPERANDS     Y30NQKC
*
EN001020 EQU   *
         TM    CACFLAG4,CAPTGTBF CHECK IF BUFFER MUST BE FREED. Y30NQKC
         BZ    EN001030       IF NOT, BRANCH TO CHECK IF INPUT  Y30NQKC
*                             IS FROM IN-CORE PROC              Y30NQKC
         BAL   BRNCHREG,EN005090 BRANCH TO FREE BUFFER.         Y30NQKC
         SPACE 2                                                Y30NQKC
EN001030 EQU   *                                                Y30NQKC
         TM    CACFLAG4,CAINPROC DETERMINE IF CURRENT SOURCE OF
*                                 INPUT IS AN IN-CORE PROCEDURE.
         BO    EN004050       IF SO, BRANCH TO TERMINATE.
         TM    CACFLAG2,CADSMODS DETERMINE IF DATA SET HAS BEEN
*                                  MODIFIED SINCE LAST SAVE.
         BZ    EN004050       IF DATA SET HAS NOT BEEN MODIFIED,
*                                  BRANCH TO TERMINATE.
         TM    CACFLAG2,CAX22ABN   WAS USER CANCELED?          @ZA25432
         BO    EN001060            YES-TREAT AS IF LINE DROPPED@ZA25432
         XC    ENMSPLST(X12),ENMSPLST ZERO PARAMETER LIST AND
         IKJEBEML M555,,,,MF=(E,ENMSPLST) CALL IKJEBEMS.
         B     EN001050(RETCDREG) RETURN CODE BRANCH TABLE.
EN001050 EQU   *
         B     EN002030       00 - NORMAL RETURN.
         B     EN003090       04 - ATTENTION OCCURRED.
         B     EN001090       08 - NOWAIT, NO LINE OUTPUT.
         B     EN001090       12 - INVALID PARAMETERS.
         B     EN006010       16 - NO CORE AVAILABLE.
         B     EN001060       20 - LINE DROP                   @ZA11393
         SPACE 5
***********************************************************************
*                                                                     *
*    A RETURN CODE OF 20 FROM PUTLINE INDICATES A LINE DROP.    Y02676*
*    RECOVERY WILL BE ATTEMPTED BY SAVING THE WORKING DATASET   Y02676*
*    INTO A DATASET CALLED 'USERID'.EDITSAVE.'QUALIFIER'.       Y02676*
*    CLEANUP THEN CONTINUES AS WITH OTHER ABEND SITUATIONS.     Y02676*
*    IF USER CANCELED-TREAT AS IF THE LINE WAS DROPPED.        @ZA25432
*                                                                     *
***********************************************************************
EN001060 EQU   *                   PUTLINE RETURN CODE OF 20     Y02676
*  BUILD A SUBCOMMAND BUFFER FOR SAVE                            Y02676
         MVC   CALDROP(12),LINEDROP MOVE SAVE SUBCMD BUFFER TO   Y02676
*                                   COMMUNICATIONS AREA          Y02676
         LA    BFRREG,CALDROP   ADDRESS OF SUBCOMMAND BUFFER     Y02676
         ST    BFRREG,CAPTIBFR  POINTER TO INPUT BUFFER          Y02676
         NI    CACFLAG4,X255-CAPTGTBF  INDICATE NO BUFFER        Y02676
*                                      TO BE FREED               Y02676
         XI    CACFLAG5,CAEDLNDP  SET LINEDROP INDICATOR FOR   @OZ00809
*                                 SAVE & FINAL COPY            @OZ00809
         MVI   CAPTIBFR,CAOPERND  INDICATE OPERANDS PRESENT      Y02676
         B     EN003020       BRANCH TO CALL SAVE                Y02676
         SPACE 5
EN001090 EQU   *
         LA    INS1REG,ENMLPUTL INDICATE PUTLINE FAILURE.
EN002010 EQU   *
         LR    INS2REG,RETCDREG SAVE RETURN CODE FOR MESSAGE.
         B     EN006020       BRANCH TO PUT MESSAGE.
         SPACE 5
EN002030 EQU   *
         TM    CACFLAG4,CAPTGTBF  CHECK IF BUFFER TO FREE      @ZA09480
         BZ    EN002033         IF NOT, CONTINUE               @ZA09480
         BAL   BRNCHREG,EN005090 GO FREE BUFFER                @ZA09480
EN002033 EQU   *                                               @ZA09480
         LA    PTGTREG,ENPGPB ADDRESS OF PUTGET PARAMETER BLOCK.
         MVC   X0(ENSAVLN,PTGTREG),ENSAVE MOVE PARAMETER BLOCK TO
*                                  WORK AREA.
         L     OUTPTREG,MSGADDR  OUTPUT MSG ADDRESS              Y01676
EN002035 EQU   *
         PUTGET PARM=(PTGTREG),OUTPUT=((OUTPTREG),SINGLE,MODE),        X
               MF=(E,CATMPLST)  PUT PROMPT MESSAGE               Y01676
         B     EN002050(RETCDREG) RETURN CODE BRANCH TABLE.
EN002050 EQU   *
         B     EN002085       00 - NORMAL RETURN FROM TERMINAL.
         B     EN002090       04 - INPUT NOT FROM TERMINAL.
         B     EN003090       08 - ATTENTION OCCURRED.
         B     EN002080       12 - SECOND-LEVEL MESSAGE PRESENT.
         B     EN002070       16 - NOWAIT SPECIFIED FOR OUTPUT.
         B     EN002070       20 - NOWAIT SPECIFIED FOR INPUT.
         B     EN002070       24 - INVALID PARAMETERS.
         B     EN006010       28 - NO CORE AVAILABLE.
         B     EN001060       32 - LINE DROP                   @ZA11393
         SPACE 5
EN002070 EQU   *
         LA    INS1REG,ENMLPTGT INDICATE PUTGET FAILURE.
         B     EN002010       BRANCH TO GET RETURN CODE.
         SPACE 5
EN002080 EQU   *
         L     DATAREG,CAPTECT ADDRESS OF ECT.
         OI    (ECTMSGF-ECT)(DATAREG),ENLEV2MS INDICATE FLUSH OF
*                                  SECOND-LEVEL MESSAGES.
         B     EN002035       BRANCH TO RE-ISSUE PUTGET.
         EJECT
***********************************************************************
*                                                                     *
*   ONCE A LINE OF INPUT IS AVAILABLE FROM THE TERMINAL, INVOKE       *
*   IKJSCAN TO SCAN THE INPUT BUFFER.  IF THE SUBCOMMAND ENTERED      *
*   IS NOT A SAVE SUBCOMMAND, RETURN TO MAINLINE PROCESSING.  IF      *
*   A SAVE SUBCOMMAND IS ENTERED, INVOKE IKJEBESA.                    *
*                                                                     *
***********************************************************************
EN002085 EQU   *
         NI    CACFLAG4,X'FF'-CAINPROC INSURE THAT CURRENT INPUT
*                                  SOURCE INDICATION IS TERMINAL.
         B     EN002095       BRANCH TO CONTINUE PROCESSING.
EN002090 EQU   *
         OI    CACFLAG4,CAINPROC INDICATE THAT IN-CORE PROCEDURE
*                                  IS CURRENT INPUT SOURCE.
EN002095 EQU   *
         L     BFRREG,(PGPBIBUF-PGPB)(,PTGTREG) ADDRESS OF INPUT
*                                  BUFFER.
         ST    BFRREG,CAPTIBFR SAVE ADDRESS OF INPUT BUFFER IN
*                                  THE COMMUNICATION AREA.
         OI    CACFLAG4,CAPTGTBF INDICATE PUTGET GOT BUFFER.
         BAL   BRNCHREG,EN005030 BRANCH TO LINK TO IKJSCAN.
         CH    SCLENREG,ENCON001 CHECK FOR LENGTH = 1.
         BE    EN003010       IF = 1, BRANCH TO CHECK FOR 'S'.
         CH    SCLENREG,ENCON002 CHECK FOR LENGTH = 4.
         BNE   EN003060       IF NOT = 4, BRANCH TO CHECK FOR
*                                  AN 'END' SUBCOMMAND.
EN003010 EQU   *
         BCTR  SCLENREG,X0    REDUCE LENGTH BY 1 FOR COMPARE.
         EX    SCLENREG,ENCLC001 CHECK FOR 'SAVE' OR 'S'.
         BNE   EN002030       IF NEITHER, REPROMPT             @ZA09480
EN003020 EQU   *                                                 Y02676
         IKJEBESH (COMMREG),IKJEBESA,PARAM=((COMMREG)),                X
               MF=(E,ENLINKSF) INVOKE SAVE SUBCOMMAND.
         B     EN003030(RETCDREG) RETURN CODE BRANCH TABLE.
EN003030 EQU   *
         B     EN003050       00 - NORMAL RETURN.
         B     EN003050       04 - NOT USED BY SAVE.
         B     EN003040       08 - UNSUCCESSFUL SAVE OPERATION.
         B     EN004050       12 - I/O ERROR ON UTILITY DATA SET.
         EJECT
***********************************************************************
*                                                                     *
*   UPON RETURN FROM SAVE WITH A RETURN CODE OF EIGHT(8), THE         *
*   USER IS GIVEN ANOTHER CHANCE TO SAVE IF ABEND IN PROGRESS.   A50474
*                                                                     *
***********************************************************************
EN003040 EQU   *
         TM    CACFLAG4,CAABEND   CHECK IF ABEND IN PROCESS
         BZ    EN004000       IF NO ABEND, RETURN TO IKJEBEMA
         CLC   CALDROP+X4(8),LINEDROP+X4    LINE DROP RECOVERY ? Y02676
         BE    EN004050         YES -  CLEANUP AND RETURN        Y02676
         B     EN002030       REPROMPT USER FOR 'SAVE' OR 'END'
         EJECT
***********************************************************************
*                                                                     *
*   UPON RETURN FROM SAVE, IF SAVE WAS A KEYWORD THEN TERMINATE,      *
*   IF NOT THEN RETURN TO MAINLINE PROCESSING.                 @ZA09480
*                                                                     *
***********************************************************************
EN003050 EQU   *
         TM    CACFLAG4,CAABEND CHECK IF ABEND IN PROGRESS.
         BO    EN004050       IF AN ABEND, DO NOT PROCESS
*                                  REMAINING BUFFER.
         TM    CACFLAG4,CAPTGTBF CHECK IF BUFFER OBTAINED        A45718
*                                  THROUGH PUTGET.               A45718
         BZ    EN003055       IF NOT, BRANCH AROUND FREEMAIN.    A45718
         BAL   BRNCHREG,EN005090 BRANCH TO FREE BUFFER.          A45718
EN003055 EQU   *                                                 A45718
         TM    ENCFLAGS,ENDSAVE    SAVE KEYWORD SUPPLIED        Y30NQKC
         BO    EN004050            YES, THEN TERMINATE          Y30NQKC
         B     EN003090      NO, GO CHECK FOR ABEND IN PROCESS @ZA09480
EN003060 EQU   *
         CH    SCLENREG,ENCON003 CHECK FOR LENGTH = 3.
         BNE   EN002030       IF NOT = 3, REPROMPT.            @ZA09480
         BCTR  SCLENREG,X0    REDUCE LENGTH BY 1 FOR COMPARE.
         EX    SCLENREG,ENCLC002 CHECK FOR 'END' SUBCOMMAND.
         BE    EN004050       IF 'END', BRANCH TO COMPLETE
*                                  PROCESSING.
         B     EN002030       IF NOT, REPROMPT                 @ZA09480
         EJECT
***********************************************************************
*                                                                     *
*    WHEN THE SUBCOMMAND SAVE IS ENTERED AT THE TERMINAL,      @ZA09480
*    RETURN TO IKJEBEMA WITH RETURN CODE ZERO. IF AN ABEND IS  @ZA09480
*    IN PROGRESS, CONTINUE TERMINATION PROCESSING.             @ZA09480
*                                                                     *
***********************************************************************
EN003090 EQU   *
         TM    CACFLAG4,CAABEND CHECK IF AN ABEND IN PROGRESS.
         BO    EN004050       IF SO, BRANCH TO COMPLETE 'END'.
         SR    RETCDREG,RETCDREG SET ZERO RETURN CODE.
EN004000 EQU   *
         NI    ENCFLAGS,X'FF'-MAENDPRC INDICATE THAT END PROCESSING
*                                   IS NOT COMPLETE.
         EJECT
***********************************************************************
*                                                                     *
*   EXIT -- RETURN TO MAINLINE (IKJEBEMA) WITH THE RETURN CODE        *
*   SET ELSEWHERE.  RETURN CODES ARE 0 (ZERO) - RESUME NORMAL         *
*   PROCESSING - AND 12 - TERMINATE EDIT.                             *
*                                                                     *
***********************************************************************
EN004010 EQU   *
         BAL   BRNCHREG,EN008000   RELEASE PDL                  Y30NQKC
*
         IKJEBERT (14,12),,RC=(15) RETURN TO IKJEBEMA.
         EJECT
***********************************************************************
*                                                                     *
*   ENTRY POINT -- IKJEBEXT                                           *
*                                                                     *
*   CONTROL IS PASSED TO THIS LOCATION WHEN THE MAIN CONTROLLER       *
*   DISCOVERS A TERMINAL ERROR CONDITION.                             *
*                                                                     *
***********************************************************************
IKJEBEXT SAVE  (14,12)        SAVE ENTRY REGISTERS.
EN004030 EQU   *
         BALR  BASEREG,X0     ESTABLISH ADDRESSABILITY FOR THIS
         LA    RETCDREG,EN004030-EN001010 ROUTINE BASED ON MAIN
         SR    BASEREG,RETCDREG    ENTRY POINT.
         LR    COMMREG,PARMREG1 SAVE ADDRESS OF COMMUNICATION
*                                  AREA.
         L     EPLOCREG,CANXTSVA ADDRESS OF NEXT AVAILABLE SAVE
*                                  AREA.
         ST    SAVEREG,X4(EPLOCREG) CROSS CHAIN PREVIOUS AND
         ST    EPLOCREG,X8(,SAVEREG) CURRENT SAVE AREAS.
         LR    SAVEREG,EPLOCREG INITIALIZE SAVE AREA POINTER.
         LA    EPLOCREG,X72(,EPLOCREG) COMPUTE ADDRESS OF NEXT
         ST    EPLOCREG,CANXTSVA   SAVE AREA AND SAVE.
         SPACE 5
***********************************************************************
*                                                                     *
*   END PROCESSING.                                                   *
*                                                                     *
*   (1) INVOKE IKJEBEEX TO DELETE THE EDIT ACCESS METHOD AND TO       *
*   UNALLOCATE THE UTILITY DATA SET.  (2) INVOKE THE SCAN             *
*   SUBCOMMAND PROCESSOR FOR FINAL ENTRY TO SYNTAX CHECKER IF         *
*   THE DATA SET TYPE IS BASIC OR IPLI OR IF THE SCAN SWITCH IS       *
*   ON.  (3) CANCEL THE ABNORMAL END EXIT AND THE ATTENTION EXIT.     *
*   (4) INVOKE IKJDAIR VIA LINK FOR UNALLOCATION OF THE EDIT DATA     *
*   SET (IF CURRENTLY ALLOCATED). IF THE UNALLOCATION IS              *
*   SUCCESSFUL, PROCESSING CONTINUES NORMALLY. IF UNSUCCESSFUL,       *
*   DAIRFAIL IS INVOKED FOR ERROR DIAGNOSIS PRIOR TO RESUMING         *
*   TERMINATION PROCESSING. (5) DELETE THE PERMANENTLY RESIDENT       *
*   SERVICE ROUTINES -- IKJEBEUT,IKJEBEMS, AND IKJEBELE.              *
*                                                                     *
***********************************************************************
EN004050 EQU   *
         TM    CACFLAG4,CAPTGTBF CHECK IF BUFFER MUST BE FREED.
         BZ    EN004070       IF NOT, BRANCH TO CHECK IF SYNTAX
*                                  CHECKING IN USE.
         BAL   BRNCHREG,EN005090 BRANCH TO FREE BUFFER.
EN004070 EQU   *
         L     PARMREG0,CAPTCDCB ADDRESS OF CURRENT UTILITY DCB.
         LR    PARMREG1,COMMREG ADDRESS OF COMMUNICATION AREA.
         LINK  EP=IKJEBEEX    LINK TO IKJEBEEX                         X
                                   TO DELETE THE UTILITY ACCESS        X
                                   METHOD AND WORK AREA.
* --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- *
         ESTAE  0,,MF=(E,CASTAEPL) CANCEL ABNORMAL END EXIT.   @ZM32367
         CLI   CADSCODE,CABASIC TEST FOR BASIC DATA SET TYPE.
         BE    EN004090       IF BASIC, BRANCH TO TERMINATE
*                                  PREPROCESSOR.
         CLI   CADSCODE,CAIPLI TEST FOR IPLI DATA SET TYPE.
         BE    EN004090       IF IPLI, BRANCH TO TERMINATE
*                                  PREPROCESSOR.
         TM    CACFLAG1,CASCANSW TEST IF SYNTAX CHECKING IS IN US
         BZ    EN005010       IF NOT, BRANCH TO CANCEL STAX AND
*                                  STAE EXITS.
EN004090 EQU   *
         TM    CASYNAME,ALLBITS CHECK IF SYN CHECKER IN SYSTEM  YA00035
         BNM   EN005010       IF NOT IN SYSTEM, BR TO CANCEL    YA00035
*                                  STAX AND STAE EXITS.
         MVC   CABFRPL(SCNOFEND-SCNOFMSG),SCNOFMSG MOVE SCAN
*                                  OFF MESSAGE TO GOTTEN CORE.
         LA    PARMREG1,CABFRPL OBTAIN ADDRESS OF SCAN OFF
*                                  MESSAGE.
         OI    CACFLAG1,CAENDSC INDICATE TO SCAN THAT FINAL CALL
*                                  IS FROM THE CONTROLLER.
         ST    PARMREG1,CAPTIBFR PLACE ADDRESS OF DUMMY
*                                  SUBCOMMAND IN COMMUNICATION
*                                  AREA FOR IKJEBESC.
         MVI   CAPTIBFR,CAOPERND INDICATE THAT OPERANDS ARE
*                                  PRESENT ON SUBCOMMAND.
         IKJEBESH (COMMREG),IKJEBESC,PARAM=((COMMREG)),                X
               MF=(E,ENLINKSF) TERMINATE SYNTAX CHECKER.
* --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- *
EN005010 EQU   *
         STAX  ,MF=(E,CASTAXPL) CANCEL ATTENTION EXIT.
         TM    CAEDFLAG,CAEDALOC  IS EDIT DATA SET ALLOCATED -   Y01676
         BZ    AROUND             IF NOT, BYPASS UNALLOC.        Y01676
***********************************************************************
*                                                                     *
*   SET UP PARAMETER LIST TO BE PASSED TO IKJDAIR FOR UNALLOCATION    *
*   OF THE EDIT DATA SET.                                             *
*                                                                     *
***********************************************************************
         LA    DSECTREG,CATMPLST  ADDR TMP SUBROUTINE PARMLIST.  Y01676
         USING DAPL,DSECTREG      ADDRESSABILITY OF DAPL DSECT.  Y01676
         L     PARMREG1,CAPTTMP   OBTAIN CPPL ADDRESS.           Y01676
         L     PARMREG1,(CPPLPSCB-CPPL)(,PARMREG1)  PSCB PTR.    Y01676
         ST    PARMREG1,DAPLPSCB  STORE PSCB PTR IN PARMLIST.    Y01676
***********************************************************************
*                                                                     *
*   SET UP PARAMETER BLOCK FOR ENTRY CODE X'18'. THE DSNAME IS        *
*   OBTAINED FROM THE COMMUNICATIONS AREA.                            *
*                                                                     *
***********************************************************************
         LA    PARMREG1,DAPARM18  ADDRESS OF PARAMETER BLOCK.    Y01676
         ST    PARMREG1,DAPLDAPB  STORE IN PARAMETER LIST.       Y01676
         USING DAPB18,PARMREG1    EST. PARMBLK ADDRESSABILITY.   Y01676
         XC    DA18CD(K12),DA18CD ZERO FIRST 12 BYTES OF PARMBLK.Y01676
         MVI   DA18CD+X1,DAEC18   ENTRY CODE = X'18'.            Y01676
         MVI   DA18MNM,DABLANK    INITIALIZE LAST 20 BYTES OF    Y01676
         MVC   DA18MNM+X1(K21),DA18MNM   PARAM. TO BLANKS.       Y01676
         XC    DA18DPS2(K02),DA18DPS2  ZERO DISP AND CTL.        Y01676
         MVI   DA18DPS2,DA18KEEP  SET DISPOSITION TO KEEP.       Y01676
         MVC   DA18DDN,CAEDDDN    PLACE DDNAME IN PARMBLK.       Y01676
         LA    PARMREG1,CATMPLST  OBTAIN PARAMETER LIST ADDRESS. Y01676
         LINK  EP=IKJDAIR         INVOKE DAIR SERVICE ROUTINE    Y01676X
                                  FOR UNALLOCATION OF EDIT D.S.  Y01676
         LTR   RETCDREG,RETCDREG  TEST FOR RETURN CODE OF ZERO.  Y01676
         BZ    AROUND             IF ZERO, BRANCH TO CONTINUE    Y01676
*                                 NORMAL PROCESSING.             Y01676
         ST    RETCDREG,RETCODE   OTHERWISE, SAVE DAIR RETURN    Y01676
         LA    PARMREG1,CATMPLST  CODE AND SET UP THE REQUIRED   Y01676
*                                 PARAMETER LIST FOR DAIRFAIL.   Y01676
         ST    PARMREG1,DAPLPTR   PUT ADDR. DAIR PARMLIST INTO   Y01676
*                                 DAIRFAIL PARAMETER LIST.       Y01676
         LA    PARMREG1,RETCODE   OBTAIN ADDR. DAIR RETURN CODE. Y01676
         ST    PARMREG1,RETPTR    PLACE ADDR. IN DAIRFAIL PLIST. Y01676
         LA    PARMREG1,MSGSIGN   ADDR. SIGNAL TO DAIRFAIL       Y01676
*                                 TO LOAD, CALL, AND DELETE THE  Y01676
*                                 MESSAGE WRITER.                Y01676
         ST    PARMREG1,MSGWTR    PUT ADDR. INTO DAIRFAIL PLIST. Y01676
         LA    PARMREG1,CALLERID  OBTAIN ADDR. OF INTERFACE ID.  Y01676
         ST    PARMREG1,IDPTR     PUT ADDR. INTO DAIRFAIL PLIST. Y01676
         LA    PARMREG1,FAILPLST  INVOKE THE DAIRFAIL SERVICE    Y01676
*                                 ROUTINE FOR DIAGNOSIS OF       Y01676
         LINK  EP=IKJEFF18        UNALLOCATION ERROR.            Y01676
AROUND   EQU   *                                                 Y01676
* --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- --- *
         DELETE EP=IKJEBELE   DELETE LINE EDIT ROUTINE.
         DELETE EP=IKJEBEMS   DELETE MESSAGE SELECTION ROUTINE.
         LA    RETCDREG,X12(,X0) SET RETURN CODE = 12 AND BRANCH
         B     EN004010            TO EXIT.
         EJECT
***********************************************************************
*                                                                     *
*   THIS SUBROUTINE INVOKES IKJSCAN AND INTERROGATES ITS RETURN       *
*   CODES.  IT RETURNS THE SUBCOMMAND ADDRESS AND LENGTH TO THE       *
*   CALLER.                                                           *
*                                                                     *
***********************************************************************
EN005030 EQU   *
         LA    FLAGREG,ENSCNFLG ADDRESS OF FLAG WORD.
         XC    ENSCNFLG(L'ENSCNFLG),ENSCNFLG ZERO FLAG WORD.
         OI    ENSCNFLG,ENNOSCHK INDICATE NO SYNTAX CHECKING.
         LA    OAREAREG,ENSCNOA ADDRESS OF IKJSCAN OUTPUT AREA.
         XC    ENLINKSF(X8),ENLINKSF ZERO LINK PARAMETER AREA.
         LINK  EP=IKJSCAN,PARAM=(,,,(FLAGREG),(OAREAREG),(BFRREG)),    X
               MF=(E,CATMPLST),SF=(E,ENLINKSF) INVOKE IKJSCAN.
         LTR   RETCDREG,RETCDREG CHECK IF IKJSCAN WAS SUCCESSFUL.
         BZ    EN005050       IF SO, BRANCH TO GET ADDRESS AND
*                                  LENGTH OF SUBCOMMAND.
         LA    INS1REG,ENMLSCAN INDICATE SCAN FAILURE.
         B     EN002010       BRANCH TO GET RETURN CODE.
EN005050 EQU   *
         CLI   ENSCNOA+(CSOAFLG-CSOA),CSOAVNP CHECK IF VALID
*                                  WITH NO PARAMETERS.
         BE    EN005070       IF SO, BRANCH TO SET OPERAND FLAG.
         CLI   ENSCNOA+(CSOAFLG-CSOA),CSOAVWP CHECK IF VALID
*                                  WITH PARAMETERS.
         BNE   EN002030       IF NEITHER, BRANCH TO RE-ISSUE     A50474
*                                  PUTGET (WITH MSG.).           A50474
         MVI   CAPTIBFR,CAOPERND INDICATE THAT OPERANDS ARE
*                                  PRESENT ON SUBCOMMAND.
EN005070 EQU   *
         LH    SCLENREG,ENSCNOA+(CSOALNM-CSOA) SET UP LENGTH AND
         L     SCPTREG,ENSCNOA+(CSOACNM-CSOA) ADDRESS RETURNED BY
*                                  IKJSCAN.
         BR    BRNCHREG       RETURN TO CALLER.
         EJECT
***********************************************************************
*                                                                     *
*   THIS SUBROUTINE IS USED TO FREE THE LAST USED SUBCOMMAND          *
*   BUFFER.                                                           *
*                                                                     *
***********************************************************************
EN005090 EQU   *
         L     BFRREG,CAPTIBFR ADDRESS OF BUFFER TO BE FREED.
         LH    BFRREG,X0(,BFRREG) LENGTH OF BUFFER.
         NI    CAPTIBFR,X255-CAOPERND SET OPERAND FLAG TO ZERO.
         FREEMAIN E,LV=(BFRREG),A=CAPTIBFR,SP=SUBPOOL1,                X
               MF=(E,ENFREMNL) FREE INPUT BUFFER.
         NI    CACFLAG4,X255-CAPTGTBF INDICATE THAT BUFFER WAS
*                                  FREED.
         BR    BRNCHREG       RETURN TO CALLER.
         SPACE 5
***********************************************************************
*                                                                     *
*   THIS SUB-SECTION COMPLETES MESSAGE PROCESSING FOR THE END         *
*   SUBCOMMAND PROCESSOR.  SINCE ALL MESSAGES ARE OF A TERMINAL       *
*   NATURE, TERMINATION PROCESSING CONTINUES AFTER THE MESSAGE        *
*   IS PUT.                                                           *
*                                                                     *
***********************************************************************
EN006010 EQU   *
         LA    LEV1REG,M312   RELATIVE MESSAGE TO BE PUT.
         LA    INS1REG,ENINS001 ADDRESS OF FIRST LEVEL INSERTION
*                                  LIST.
         SR    LEV2REG,LEV2REG SHOW NO SECOND LEVEL MESSAGE OR
         SR    INS2REG,INS2REG     INSERTION.
         B     EN006030       BRANCH TO PUT MESSAGE.
EN006020 EQU   *
         LA    LEV1REG,M313   RELATIVE MESSAGE TO BE PUT.
         LA    INS1REG,ENMLTABL(INS1REG) INDEX TO SERVICE ROUTINE
*                                  NAME INSERTION.
         MVC   ENLNIN1(X12),X0(INS1REG) MOVE ROUTINE NAME TO
*                                  INSERTION RECORD.
         LA    INS1REG,ENPTIN2 ADDRESS OF SECOND INSERTION RECORD
         MVC   X0(X8,INS1REG),ENCON004 INITIALIZE POINTER, LENGTH
*                                  AND OFFSET.
         CVD   INS2REG,X8(,INS1REG) CONVERT RETURN CODE TO
*                                  DECIMAL.
         OI    X15(INS1REG),ENMLZONE CHANGE ZONE ON LAST BYTE.
         UNPK  X9(X3,INS1REG),X14(X2,INS1REG) CONVERT TO EBCDIC
         MVC   X8(X2,INS1REG),X10(INS1REG) AND MOVE TO INSERTION.
         ST    INS1REG,ENPTIN1 CHAIN INSERTION RECORDS.
         LA    LEV2REG,M3131  SET UP SECOND LEVEL MESSAGE NUMBER,
         LA    INS2REG,ENMSGIN1    ADDRESS OF INSERTION LIST, AND
         SR    INS1REG,INS1REG     SHOW NO FIRST LEVEL INSERTION.
EN006030 EQU   *
         IKJEBEML (LEV1REG),(LEV2REG),(INS1REG),(INS2REG),             X
               MF=(E,ENMSPLST) BRANCH TO PUT MESSAGE.
         B     EN004050       BRANCH TO CONTINUE TERMINATION
*                                  PROCESSING.
         EJECT
***********************************************************************
*                                                                     *
* PARSE THE END OPERANDS                                              *
*                                                                     *
***********************************************************************
EN007000 EQU   *                                                Y30NQKC
         NI    CAPTIBFR,X'FF'-CAOPERND  NO OPERANDS FOR SAVE    Y30NQKC
         NI    ENCFLAGS,X'FF'-ENDSAVE  TENTATIVE NOSAVE KEYWORD Y30NQKC
         LA    PARMREG1,CATMPLST   ADDR OF TMP SERV RTN PLIST   Y30NQKC
         USING PPL,PARMREG1        ADDRESSABILITY TO PPL        Y30NQKC
*
         L     REG2,PCLADDR        GET ADDR OF PCL              Y30NQKC
         ST    REG2,PPLPCL         STORE PCL ADDR IN PPL        Y30NQKC
         LA    REG2,CAPTPRSD       GET ADDR FOR PDL ADDRESS     Y30NQKC
         ST    REG2,PPLANS         STORE ADDR FOR PDL ADDR      Y30NQKC
         L     REG2,CAPTIBFR       GET ADDR OF INPUT BUFFER     Y30NQKC
         ST    REG2,PPLCBUF        STORE INPUT BUFFER ADDR      Y30NQKC
         ST    SAVEREG,PPLUWA      ADDRESS OF USER WORK AREA    Y30NQKC
*
         DROP  PARMREG1            DROP ADDRESSABILITY TO PPL   Y30NQKC
*
         CALLTSSR EP=IKJPARS       INVOKE IKJPARS               Y30NQKC
*
         L     REG2,CAPTPRSD       GET ADDR OF PDL              Y30NQKC
         C     RETCDREG,ENDC16     RETCODE > 16                 Y30NQKC
         BH    EN007020            HANDLE RC ERROR              Y30NQKC
         B     EN007010(RETCDREG)  RETURN CODE BRANCH TABLE     Y30NQKC
         EJECT
EN007010 EQU   *
         B     EN007040            00 - NORMAL RETURN           Y30NQKC
         B     EN003055            04 - UNABLE TO PROMPT        Y30NQKC
         B     EN003090            08 - ATTENTION ISSUED        Y30NQKC
         B     EN007020            0C - INVALID PARAMETERS      Y30NQKC
         B     EN006010            10 - NOT ENOUGH CORE         Y30NQKC
*
EN007020 EQU   *                   RETCODE OF 12 AND GT 16      Y30NQKC
         BAL   BRNCHREG,EN008000   RELEASE PARSE DESC LIST      Y30NQKC
         LA    INS1REG,ENMLPARS    INDICATE PARSE ERROR         Y30NQKC
         B     EN002010            GO SAVE RETCODE, SEND MSG    Y30NQKC
*                                                               Y30NQKC
*
EN007030 EQU   *
         BAL   BRNCHREG,EN008000   RELEASE PARSE DESC LIST      Y30NQKC
         TM    CACFLAG4,CAPTGTBF   FREE PUTGET BUF              Y30NQKC
         BZ    EN003090            0=NO NEED TO FREE            Y30NQKC
*
         BAL   BRNCHREG,EN005090   FREE PUTGET BUFFER           Y30NQKC
         B     EN003090            TERMINATE                    Y30NQKC
EN007040 EQU   *                   NORMAL PARSE END             Y30NQKC
         USING PDL,REG2            ADDRESSABILITY TO PDL        Y30NQKC
         CLI   SAVEPCE+1,X'00'     NEITHER SAVE NOR NOSAVE      Y30NQKC
         BE    EN001020            CHECK IF DSN MODIFIED        Y30NQKC
*
         CLI   SAVEPCE+1,1         IS SAVE KEYWORD SUPPLIED     Y30NQKC
         BNE   EN004050            NO SAVE KEYWORD              Y30NQKC
         OI    ENCFLAGS,ENDSAVE    INDICATE SAVE KEYWORD        Y30NQKC
         B     EN003020            SAVE DATA SET                Y30NQKC
         DROP  REG2                DROP ADDRESSABILITY TO PDL   Y30NQKC
         EJECT
***********************************************************************
*                                                                     *
* RELEASE PARSE DESCRIPTOR LIST                                       *
*                                                                     *
***********************************************************************
EN008000 EQU   *                                                Y30NQKC
         TM    CAPTPRSD,CAFREEDL   PDL SWITCH ON                Y30NQKC
         BNZR  BRNCHREG            SWITCH ON THEN RETURN        Y30NQKC
         LR    REG2,RETCDREG       SAVE RETURN CODE             Y30NQKC
*
         IKJRLSA CAPTPRSD          FREE PDL                     Y30NQKC
*
         LR    RETCDREG,REG2       RESTORE RETURN CODE          Y30NQKC
         OI    CAPTPRSD,CAFREEDL   TURN SWITCH ON               Y30NQKC
         BR    BRNCHREG            RETURN TO CALLER             Y30NQKC
         EJECT
***********************************************************************
*                                                                     *
*   EQUATES, CONSTANTS, AND DATA AREAS USED BY THE END SUBCOMMAND     *
*   PROCESSOR.                                                        *
*                                                                     *
***********************************************************************
ENMLTABL DS    0F
ENMLPUTL EQU   0*12           OFFSET TO PUTLINE ENTRY.
ENNAME01 EQU   *
         DC    AL2(ENNAME02-*) INSERTION LENGTH.
         DC    AL2(M3131IN1)  OFFSET TO INSERTION.
         DC    C'PUTLINE'     SERVICE ROUTINE NAME.
ENNAME02 EQU   *
         DC    (12-(ENNAME02-ENNAME01))C' ' FILL CHARACTERS.
ENMLPTGT EQU   1*12           OFFSET TO PUTGET ENTRY.
ENNAME03 EQU   *
         DC    AL2(ENNAME04-*) INSERTION LENGTH.
         DC    AL2(M3131IN1)  OFFSET TO INSERTION.
         DC    C'PUTGET'      SERVICE ROUTINE NAME.
ENNAME04 EQU   *
         DC    (12-(ENNAME04-ENNAME03))C' ' FILL CHARACTERS.
ENMLSCAN EQU   2*12           OFFSET TO SCAN ENTRY.
ENNAME05 EQU   *
         DC    AL2(ENNAME06-*) INSERTION LENGTH.
         DC    AL2(M3131IN1)  OFFSET TO INSERTION.
         DC    C'SCAN'        SERVICE ROUTINE NAME.
ENNAME06 EQU   *
         DC    (12-(ENNAME06-ENNAME05))C' ' FILL CHARACTERS.
ENMLPARS EQU   3*12           OFFSET TO PARSE ENTRY             Y30NQKC
ENNAME07 EQU   *                                                Y30NQKC
         DC    AL2(ENNAME08-*) INSERTION LENGTH                 Y30NQKC
         DC    AL2(M3131IN1)  OFFSET TO INSERTION               Y30NQKC
         DC    C'PARSE'       SERVICE ROUTINE NAME              Y30NQKC
ENNAME08 EQU   *                                                Y30NQKC
         DC    (12-(ENNAME08-ENNAME07))C' '  FILL CHARS         Y30NQKC
ENCON001 DC    H'1'           LENGTH 1.
ENCON002 DC    H'4'           LENGTH 4.
ENCON003 DC    H'3'           LENGTH 3.
ENCON004 DC    A(0),H'6',AL2(M3131IN2) POINTER, LENGTH AND OFFSET
*                                  FOR INSERTION RECORD.
ENCON005 DC    CL4'SAVE'      SAVE SUBCOMMAND NAME.
ENCON006 DC    CL3'END'       END SUBCOMMAND NAME.
ENCLC001 CLC   X0(*-*,SCPTREG),ENCON005 CHECK FOR SAVE.
ENCLC002 CLC   X0(*-*,SCPTREG),ENCON006 CHECK FOR END.
ENINS001 IKJEBEMG 0,M312IN1,'EDIT' COMMAND NAME INSERTION.
SCNOFMSG DC    AL2(SCNOFEND-*),AL2(0),CL3'OFF' SCAN OFF MESSAGE.
SCNOFEND EQU   *              SCAN OFF MESSAGE END.
ENSAVE   PUTGET TERMGET=(EDIT,WAIT),                                   X
               TERMPUT=(EDIT,WAIT,NOHOLD,NOBREAK),MF=L
CALLERID DC    H'1'           USED BY IKJEFF18 TO IDENTIFY       Y01676
*                             INTERFACE VIA WHICH AN ERROR       Y01676
*                             OCCURRED.  ID=1 INDICATES THAT     Y01676
*                             THE GENERAL CALLER IS USING THE    Y01676
*                             DAIR INTERFACE PARAMETER LIST.     Y01676
MSGSIGN  DC    F'0'           SIGNAL TO IKJEFF18(DAIRFAIL) TO    Y01676
*                             LOAD, CALL, AND DELETE THE MESSAGE Y01676
*                             WRITER (IKJEFF02).                 Y01676
ENSAVLN  EQU   *-ENSAVE       LENGTH OF PARAMETER BLOCK.
ENMLZONE EQU   X'0F'          EBCDIC NUMERIC ZONE BITS.
ENLEV2MS EQU   X'80'          BIT SETTING FOR 'ECTMSGF' BYTE OF
*                                  ECT TO INDICATE THAT EXISTING
*                                  SECOND-LEVEL MESSAGES ARE TO
*                                  DELETED.
MSGADDR  DC    A(IKJEBMSG)    ADDRESS OF OUTPUT MESSAGE          Y01676
LINEDROP DC    X'000C0000'    SUBCOMMAND BUFFER HEADER           Y02676
         DC    CL8'EDITSAVE'  SUBCOMMAND BUFFER                  Y02676
ENDC16   DC    F'16'          LARGEST VALID PARSE RC           @Y30NQKC
ENNOSCHK EQU   X'80'          NO SYNTAX CHECKING REQUIRED FLAG.
ALLBITS  EQU   X'FF'          MASK OF BINARY 1'S FOR TM OPCODE  YA00035
DABLANK  EQU   C' '           CHARACTER BLANK.                   Y01676
DAEC18   EQU   X'18'          DAIR ENTRY CODE X'18'.             Y01676
K02      EQU   2              CONSTANT OF 2.                     Y01676
K12      EQU   12             CONSTANT OF 12.                    Y01676
K21      EQU   21             CONSTANT OF 21.                    Y01676
SUBPOOL1 EQU   1              SUBPOOL VALUE.
X0       EQU   0              OFFSET 0.
X1       EQU   1              OFFSET 1.                          Y01676
X2       EQU   2              OFFSET 2.
X3       EQU   3              OFFSET 3.
X4       EQU   4              OFFSET 4.
X8       EQU   8              OFFSET 8.
X9       EQU   9              OFFSET 9.
X10      EQU   10             OFFSET 10.
X12      EQU   12             OFFSET 12.
X14      EQU   14             OFFSET 14.
X15      EQU   15             OFFSET 15.
X28      EQU   28             OFFSET 28.
X72      EQU   72             OFFSET 72.
X255     EQU   255            OFFSET 255.
         IKJEBEMI (312,313,555) MESSAGE EQUATES.
         EJECT
***********************************************************************
*                                                                     *
* PARSE CONTROL DATA                                                  *
*                                                                     *
***********************************************************************
PCLADDR  DC    A(PCL)              ADDR OF PCL                  Y30NQKC
PCL      IKJPARM DSECT=PDL                                      Y30NQKC
SAVEPCE  IKJKEYWD
         IKJNAME 'SAVE'                                         Y30NQKC
         IKJNAME 'NOSAVE'                                       Y30NQKC
         IKJENDP
         EJECT
         IKJEBECA
         SPACE 5
***********************************************************************
*                                                                     *
*   DEFINE END SUBCOMMAND PROCESSOR WORKAREA.                         *
*                                                                     *
***********************************************************************
         ORG   CAMAWKA        DEFINE WORK AREA FOR END SUBCOMMAND
ENLINKSF DS    2F             PARAMETER LIST FOR SUBCOMMAND.
ENSCNFLG DS    F              FLAG WORD FOR IKJSCAN.
ENSCNOA  DS    2F             IKJSCAN OUTPUT AREA.
         ORG   CAMAWKA        RE-USE WORKAREA.
ENMSPLST DS    5F             PARAMETER LIST FOR MESSAGE ROUTINE.
         ORG   CAMAWKA        RE-USE WORK AREA.
ENFREMNL DS    3F             PARAMETER LIST FOR FREEMAIN.
         ORG   CAMAWKA        RE-USE WORK AREA.
ENPGPB   DS    4F             PUTGET PARAMETER BLOCK.
         ORG   CAMAWKA+X28    ORIGIN TO LAST WORD OF CAMAWKA.
ENCFLAGS EQU   MACFLAGS       CONTROL FLAGS, BYTE 1.
*                             BITS 0, 1 RESERVED.
*                             BIT 2, END PROCESSING COMPLETE.
*                                  0 = NOT COMPLETE.
*                                  1 = COMPLETE.
*                             BIT 3, SAVE KEYWORD FOR END       Y30NQKC
*                                  0 = NOSAVE KEYWORD           Y30NQKC
*                                  1 = SAVE KEYWORD             Y30NQKC
*                                  BITS 4-7 RESERVED            Y30NQKC
ENDSAVE  EQU   X'10'          SAVE KEYWORD SUPPLIED             Y30NQKC
         SPACE 2
         ORG   CATEMPBF       DEFINE INSERTION RECORD FORMAT.
ENMSGIN1 DS    0F             INSERTION RECORD FOR IKJEBEMS.
ENPTIN1  DS    A              POINTER TO NEXT INSERTION.
ENLNIN1  DS    H              LENGTH OF INSERTION RECORD.
ENOFFIN1 DS    H              OFFSET IN MESSAGE TO INSERTION.
ENINS1   DS    CL80           INSERTION TEXT.
ENMSGIN2 DS    0F             INSERTION RECORD FOR IKJEBEMS.
ENPTIN2  DS    A              POINTER TO NEXT INSERTION.
ENLNIN2  DS    H              LENGTH OF INSERTION RECORD.
ENOFFIN2 DS    H              OFFSET IN MESSAGE TO INSERTION.
ENINS2   DS    CL80           INSERTION TEXT.
         ORG   CASRWKA        WORK AREA                          Y01676
FAILPLST DS    0F             DAIRFAIL PARAMETER LIST.           Y01676
DAPLPTR  DS    A              POINTER TO DAIR PARAMETER LIST     Y01676
RETPTR   DS    A              POINTER TO DAIR RETURN CODE        Y01676
MSGWTR   DS    A              POINTER TO MSGSIGN                 Y01676
IDPTR    DS    A              POINTER TO CALLERID                Y01676
RESERVED DS    F              RESERVED WORD PASSED TO IKJEFF18   Y01676
DAPARM18 DS    10F            PARAMETER BLOCK FOR UNALLOCATION   Y01676
RETCODE  DS    F              RETURN CODE FROM IKJDAIR.          Y01676
         EJECT
         IKJCPPL                                                 Y01676
         EJECT
         IKJDAPL                                                 Y01676
         EJECT
         IKJDAP18                                                Y01676
         EJECT
         IKJCSOA
         EJECT
         IKJPGPB
         IKJECT
         IKJPPL                                                 Y30NQKC
         TITLE 'TSO EDIT - IKJEBEN0 - MESSAGE CSECT FOR END SUBCOMMAND'
IKJEBEN0 CSECT
         ENTRY IKJEBMSG
IKJEBMSG DC    A(1)           NO. OF INSERTIONS TO BE MADE.
         DC    A(ENMSG531)    ADDRESS OF MESSAGE.
ENMSG531 DC    AL2(ENMSGEND-ENMSG531) MESSAGE LENGTH.
         DC    H'0'           CURRENT OFFSET FIELD.
         DC    C' ENTER SAVE OR END-' PROMPT MESSAGE.
ENMSGEND EQU   *
         CVT   DSECT=YES
         END
