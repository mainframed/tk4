BEIA     TITLE 'IKJEBEIA - INITIALIZATION MESSAGES - EDIT CP TSO/360'
***********************************************************************
*                                                                     *
* STATUS -- VERSION NO.01, OS/360 RELEASE NO. 20                      *
*                                                                     *
* FUNCTION -- THIS ROUTINE IS INVOKED VIA LINK WHENEVER EDIT          *
*    INITIALIZATION REQUIRES TERMINAL OUTPUT OF AN INFORMATIONAL      *
*    MESSAGE.  SPECIFIC MESSAGES ARE SELECTED BY USING AN             *
*    OFFSET SUPPLIED IN THE PARAMETER LIST AS AN INDEX TO A           *
*    BRANCH TABLE.  IN THIS WAY, A PARTICULAR MESSAGE SUBROUTINE      *
*    IS GIVEN CONTROL TO FORMAT THE MESSAGE FOR IKJEBEMS, THE         *
*    EDIT MESSAGE SELECTION ROUTINE.  IKJEBEMS IS INVOKED             *
*    THROUGH THE IKJEBESH MACRO FOR TERMINAL OUTPUT OF THE            *
*    MESSAGE VIA THE PUTLINE SERVICE ROUTINE.                         *
*                                                                     *
* ENTRY POINTS --                                                     *
*         IKJEBEIA - VIA LINK MACRO FROM THE EDIT INITIALIZATION      *
*         PROGRAM (IKJEBEIN) FOR OUTPUT OF TERMINAL MESSAGES          *
*         REQUIRED DURING INITIALIZATION OF THE EDIT COMMAND.         *
*                                                                     *
* INPUT --                                                            *
*    REGISTER ONE (1) CONTAINS A POINTER TO A SIX-WORD PARAMETER LIST *
*    FOR THIS ROUTINE --                                              *
*    WORD 1 - ADDRESS OF THE EDIT COMMUNICATION AREA                  *
*    WORD 2 - MESSAGE INDEX FOR ADDRESSING APPROPRIATE SUBROUTINE     *
*    WORD 3 - RELATIVE NUMBER OF SECOND-LEVEL MESSAGE (OPTIONAL)      *
*    WORD 4 - FIRST-LEVEL INSERTION INFORMATION (OPTIONAL)            *
*    WORD 5 - SECOND-LEVEL INSERTION INFORMATION (OPTIONAL)           *
*    WORD 6 - ADDRESS OF 100 FULLWORD WORK AREA FOR THIS ROUTINE      *
*                                                                     *
* OUTPUT --                                                           *
*    REGISTER FIFTEEN (15) CONTAINS THE PUTLINE RETURN CODE           *
*    RETURNED BY IKJEBEMS.                                            *
*                                                                     *
* EXTERNAL REFERENCES --                                              *
*         IKJEBEMS - EDIT MESSAGE SELECTION ROUTINE, INVOKED VIA      *
*         IKJEBESH MACRO FOR TERMINAL OUTPUT OF INFORMATIONAL         *
*         MESSAGES.                                                   *
*                                                                     *
*    MACROS USED --                                                   *
*                                                                     *
*         IKJCPPL  - DEFINES COMMAND PROCESSOR PARAMETER LIST.        *
*         IKJEBEMG - BUILDING MESSAGE INSERTION LISTS.                *
*         IKJEBEMI - SYMBOLIC EQUATES FOR RELATIVE MESSAGE            *
*                    NUMBERS AND INSERTION OFFSETS.                   *
*         IKJEBERT - RETURN LINKAGE FOR EDIT COMMAND PROCESSOR.       *
*         IKJEBESH - LINKAGE TO EDIT SERVICE ROUTINES.                *
*         IKJEBESV - ENTRY LINKAGE FOR EDIT COMMAND PROCESSOR.        *
*         IKJPSCB  - DEFINES PROTECTED STEP CONTROL BLOCK.            *
*                                                                     *
* EXITS, NORMAL --                                                    *
*    THIS ROUTINE UNCONDITIONALLY RETURNS TO THE CALLING ROUTINE      *
*    BY BRANCHING ON REGISTER FOURTEEN (14).                          *
*                                                                     *
* EXITS, ERROR -- NONE.                                               *
*                                                                     *
* TABLES/WORK AREAS --                                                *
*                                                                     *
*    THE EDIT COMMUNICATION AREA CONTAINS BUFFERS, WORK AREAS,        *
*    AND CONTROL INFORMATION FOR THE EDIT COMMAND PROCESSOR.          *
*    ITS ADDRESS IS PASSED AS A PARAMETER TO THIS ROUTINE.            *
*                                                                     *
* ATTRIBUTES --                                                       *
*    REFRESHABLE, ENABLED, NON-PRIVILEGED.                            *
*                                                                     *
* CHARACTER CODE DEPENDENCY --                                        *
*    THIS ROUTINE ASSUMES THAT THE HIGH ORDER ZONE OF A SIGNED,       *
*    UNPACKED DECIMAL DIGIT WILL CONTAIN NO BITS UNCOMMON TO THE HIGH *
*    ORDER ZONE OF ANY CHARACTER DIGIT.                               *
*                                                                     *
* NOTES -- NONE.                                                      *
*                                                                     *
***********************************************************************
         EJECT
IKJEBEIA CSECT
*A114600,138400-138584,686500-687322,837000                      A45714
*C765100,786800                                                  A45714
***********************************************************************
*                                                                     *
*   GENERAL PURPOSE REGISTER EQUATES FOR THIS ROUTINE.                *
*                                                                     *
***********************************************************************
PARMREG0 EQU   0              PARAMETER REGISTER.
PARMREG1 EQU   1              PARAMETER REGISTER.
LENREG   EQU   2              REGISTER USED TO CONTAIN LENGTH FOR
*                                  CURRENT OPERATION.
LEV1REG  EQU   2              RELATIVE NUMBER OF FIRST LEVEL
*                                  MESSAGE.
COUNTREG EQU   3              LOOP CONTROL COUNTER.
LEV2REG  EQU   3              RELATIVE NUMBER OF SECOND LEVEL
*                                  MESSAGE.
INS1REG  EQU   4              ADDRESS OF INSERTION LIST FOR FIRST
*                                  LEVEL MESSAGE.
DSNAMREG EQU   5              ADDRESS OF DATA SET NAME.
INS2REG  EQU   5              ADDRESS OF INSERTION LIST FOR
*                                  SECOND LEVEL MESSAGE.
PLISTREG EQU   6              POINTER TO INPUT PARAMETER LIST FOR
*                                  A SUBROUTINE.
TMPREG   EQU   6              ADDRESS OF TMP PARAMETER LIST.
DSORGREG EQU   7              ADDRESSABILITY TO DSORG FIELD
*                                  OF FORMAT 1 DSCB.
INLENREG EQU   7              COUNTER OF NUMBER OF BYTES IN AN
*                                  INSERTION RECORD.
PASSREG  EQU   8              ADDRESS OF PSCB.
SRRTNREG EQU   8              REGISTER USED TO LINK TO INTERNAL
*                                  SUBROUTINES.
COMMREG  EQU   9              BASE REGISTER USED TO ADDRESS THE
*                                  COMMUNICATION AREA.
BASEREG  EQU   11             BASE REGISTER FOR THIS CSECT
DATAREG  EQU   12             WORK AREA ADDRESSABILITY.
SAVEREG  EQU   13             ADDRESS OF SAVE AREA.
RETREG   EQU   14             ADDRESS OF RETURN POINT IN CALLING
*                                  MODULE.
EPLOCREG EQU   15             ENTRY POINT ADDRESS OF MODULE TO BE
*                                  INVOKED.
RETCDREG EQU   15             CONDITION CODE RETURNED TO CALLING
*                                  ROUTINE.
         EJECT
         IKJEBESV (14,12),,*,COMMREG=(9)
         USING IKJEBECA,COMMREG ADDRESSABILITY TO THE
*                                  COMMUNICATION AREA.
         L     DATAREG,D20(,PARMREG1) ADDRESS OF WORK AREA.
         USING IAWORKA,DATAREG ESTABLISH ADDRESSABILITY TO WORK
*                                  AREA.
         LR    PLISTREG,PARMREG1 SAVE ADDRESS OF INPUT PARAMETER
*                                  LIST.
         L     PARMREG1,D4(,PLISTREG) OBTAIN RELATIVE MESSAGE
*                                  NUMBER TO BE PROCESSED.
         EJECT
***********************************************************************
*
*    BELOW IS A BRANCH TABLE FOR SELECTING EXECUTABLE CODE FOR
*    PARTICULAR MESSAGES, BASED ON THE SECOND WORD OF THE PARAMETER
*    LIST (MESSAGE INDEX).  THE TABLE IS STRICTLY POSITION
*    DEPENDENT.
*
***********************************************************************
         SPACE 2
IA001001 EQU   *
         B     IA001001+X4(PARMREG1) ERROR PROCESSING BRANCH
*                                  TABLE.
         B     IA002010       01 - NO MAIN STORAGE AVAILABLE.
         B     IA003010       02 - COMMAND SYSTEM ERROR.
         B     IA004010       03 - DATA SET NOT PARTITIONED.
         B     IA005010       04 - INVALID DSORG ENCOUNTERED.
         B     IA006010       05 - INVALID RECFM ENCOUNTERED.
         B     IA007010       06 - INVALID LRECL FOR OLD DATA SET
         B     IA008010       07 - DSNAME NOT IN CATALOG.
         B     IA009010       08 - LINE VALUE ENTERED ON COMMAND
*                                  (OR LRECL FIELD IN DSCB) IS
*                                  INVALID.
         B     IA010010       09 - BLOCK VALUE ENTERED INCORRECT.
         B     IA011010       10 - SYNTAX CHECKING NOT ALLOWED.
         B     IA012010       11 - DATA SET MUST BE NUMBERED.
         B     IA013010       12 - PLI SOURCE MARGIN ERROR.
         B     IA014010       13 - INVALID NUM OPERAND SUBFIELDS.
         B     IA015010       14 - CAPS REQUIRED.
         B     IA016010       15 - UNALLOCATION ERROR OR DDNAME
*                                  NOT FOUND IN TIOT.
         B     IA017010       16 - LINE/BLOCK OPERAND INVALID.
         B     IA018010       17 - LINE LE 8 FOR NUM DATA SETS.
         B     IA019010       18 - MEMBER NOT FOUND.
         B     IA021010       19 - OLD DATA SET HAS RECFM=V WHERE
*                                  F IS REQUIRED.
         B     IA022010       20 - DSNAME ALREADY IN USE.
         B     IA023010       21 - DATA SET NOT USABLE.
         B     IA024010       22 - TOO MANY DATA SETS ALLOCATED.
         B     IA025010       23 - REQUIRED VOLUME NOT MOUNTED.
         B     IA026010       24 - DSNAME NOT ON VOLUME.
         B     IA027010       25 - ALLOCATION SYSTEM ERROR.
         B     IA028010       26 - DEVICE NOT DIRECT ACCESS.
         B     IA029010       27 - INVALID DSNAME.
         B     IA030010       28 - DSNAME WILL CREATE INVALID
*                                  CATALOG STRUCTURE.
         B     IA031010       29 - DSNAME NOT QUALIFIED.
         B     IA032010       30 - DSNAME NOT RESOLVED.
         B     IA033010       31 - GOFORT(FREE) SPECIFIED FOR RECORD
*                                  F DATA SET.
         B     IA034010       32 - INVALID UNIT OR NO UNIT
*                                  AVAILABLE.
         B     IA035010       33 - MISSING DATA SET TYPE.        A45714
         EJECT
***********************************************************************
*
*    MESSAGE ID -- IKJ52312I
*
*    CONDITION(S) --
*
*         NOT ENOUGH MAIN STORAGE IS AVAILABLE TO EXECUTE THE EDIT
*         COMMAND.
*
*    PARAMETER FORMAT --
*         WORD 1 - ADDRESS OF THE EDIT COMMUNICATION AREA
*         WORD 2 - MESSAGE INDEX = 00
*         WORD 3 - NOT USED
*         WORD 4 - NOT USED
*         WORD 5 - NOT USED
*         WORD 6 - ADDRESS OF THE IKJEBEIA WORK AREA
*
***********************************************************************
IA002010 EQU   *
         LA    LEV1REG,M312   RELATIVE MESSAGE TO BE PUT TO THE
*                                  TERMINAL.
         LA    INS1REG,IAINS001 ADDRESS OF INSERTION RECORD FOR
*                                  FIRST LEVEL MESSAGE.
         SR    LEV2REG,LEV2REG SHOW THAT NO SECOND LEVEL MESSAGE
         SR    INS2REG,INS2REG     OR INSERTIONS ARE REQUIRED.
         B     IA020001        BRANCH TO CALL IKJEBEMS.
         EJECT
***********************************************************************
*
*    MESSAGE ID -- IKJ52313I
*
*    CONDITION(S) --
*
*         AN ERROR HAS OCCURRED IN A SERVICE ROUTINE OR SYSTEM
*         ROUTINE.
*
*    PARAMETER FORMAT --
*         WORD 1 - ADDRESS OF THE EDIT COMMUNICATION AREA
*         WORD 2 - MESSAGE INDEX = 04
*         WORD 3 - NOT USED
*         WORD 4 - NOT USED
*         WORD 5 - INSERTION INDICATOR FOR SECOND-LEVEL MESSAGE
*              BYTE 1 - INDICATES SERVICE ROUTINE
*              BYTES 2-4 - RETURN CODE (BINARY)
*         WORD 6 - ADDRESS OF THE IKJEBEIA WORK AREA
*
***********************************************************************
IA003010 EQU   *
         LA    LEV1REG,M313   RELATIVE MESSAGE TO BE PUT TO THE
*                                  TERMINAL.
         SR    INS1REG,INS1REG SHOW NO INSERTION REQUIRED IN
*                                  FIRST LEVEL MESSAGE.
         CLI   D16(PLISTREG),IANORTN CHECK IF SERVICE ROUTINE
*                                  INSERTION PREVIOUSLY DEFINED.
         BE    IA003040       IF SO, BRANCH TO PUT MESSAGE.
         CLI   D16(PLISTREG),IASVCOBT DETERMINE IF SERVICE
*                                  ROUTINE IS OBTAIN SVC.
         BNE   IA003014       IF NOT SVC OBTAIN, BRANCH TO CHECK
*                                  IF DEVTYPE.
         MVC   INMSGIN1(IAINS005-IAINS004),IAINS004 MOVE SERVICE
*                                  ROUTINE NAME TO INSERTION.
         B     IA003030       BRANCH TO CONVERT RETURN CODE.
IA003014 EQU   *
         CLI   D16(PLISTREG),IADEVTYP DETERMINE IF SERVICE
*                                  ROUTINE IS DEVTYPE SVC.
         BNE   IA003015       IF NOT DEVTYPE, BRANCH TO DETERMINE
*                                  IF IKJDAIR.
         MVC   INMSGIN1(IAINS014-IAINS013),IAINS013 MOVE SERVICE
*                                  ROUTINE NAME TO INSERTION.
         B     IA003030       BRANCH TO CONVERT RETURN CODE.
IA003015 EQU   *
         CLI   D16(PLISTREG),IAPUTGET CHECK IF SERVICE ROUTINE   A45714
*                                  IS IKJPTGT.                   A45714
         BNE   IA003018       IF NO, BRANCH TO CHECK IF DAIR.    A45714
         MVC   INMSGIN1(IAINS015-IAINS014),IAINS014 MOVE SERVICE A45714
*                                  ROUTINE NAME TO INSERTION     A45714
         B     IA003030       BRANCH TO CONVERT RETURN CODE.     A45714
IA003018 EQU   *                                                 A45714
         CLI   D16(PLISTREG),IADAIR DETERMINE IF SERVICE ROUTINE
*                                  IS IKJDAIR.
         BNE   IA003020       IF NOT IKJDAIR, BRANCH TO CHECK
*                                  IF DEFAULT.
         MVC   INMSGIN1(IAINS011-IAINS010),IAINS010 MOVE SERVICE
*                                  ROUTINE NAME TO INSERTION.
         B     IA003030       BRANCH TO CONVERT RETURN CODE.
IA003020 EQU   *
         CLI   D16(PLISTREG),IADFAULT DETERMINE IF SERVICE
*                                  ROUTINE IS DEFAULT.
         BNE   IA003022       IF NOT DEFAULT, BRANCH TO CHECK
*                                  IF DELETE SVC.
         MVC   INMSGIN1(IAINS009-IAINS008),IAINS008 MOVE SERVICE
*                                  ROUTINE NAME TO INSERTION.
         B     IA003030       BRANCH TO CALL IKJEBEMS.
IA003022 EQU   *
         CLI   D16(PLISTREG),IASVCDEL DETERMINE IF SERVICE
*                                  ROUTINE IS DELETE SVC.
         BNE   IA003024       IF NOT DELETE SVC, ASSUME THAT
*                                  SERVICE ROUTINE IS PARSE.
         MVC   INMSGIN1(IAINS010-IAINS009),IAINS009 MOVE SERVICE
*                                  ROUTINE NAME TO INSERTION.
         B     IA003030       BRANCH TO CALL IKJEBEMS.
IA003024 EQU   *
         MVC   INMSGIN1(IAINS012-IAINS011),IAINS011 MOVE SERVICE
*                                  ROUTINE NAME TO INSERTION.
IA003030 EQU   *
         MVI   D16(PLISTREG),IAZEROES ZERO SERVICE ROUTINE NUMBER
         L     PARMREG1,D16(,PLISTREG) OBTAIN RETURN CODE FOR
*                                  CONVERSION.
         BAL   EPLOCREG,IA003035 INSURE REGISTER NEGATIVE FOR
*                                  PROCESSING 2-DIGIT RETURN
*                                  CODE.
IA003035 EQU   *
         LA    LEV2REG,INMSGIN2 ADDRESS OF AREA FOR CONVERSION.
         BAL   SRRTNREG,IA100003 BRANCH TO CONVERT RETURN CODE TO
*                                  PRINTABLE DECIMAL.
         ST    LEV2REG,INPTIN1 CHAIN FIRST AND SECOND INSERTION
*                                  RECORDS.
         LA    INS2REG,INMSGIN1 ADDRESS OF INSERTION CHAIN FOR
*                                  SECOND LEVEL MESSAGE.
         LA    LEV2REG,M3131IN2 OBTAIN OFFSET TO SECOND INSERTION
         STH   LEV2REG,INOFFIN2    AND SAVE IN PARAMETER LIST.
         B     IA003050       BRANCH TO COMPLETE PARAMETER LIST.
IA003040 EQU   *
         L     INS2REG,D16(,PLISTREG) ADDRESS OF PREFORMATTED
*                                  INSERTION LIST.
IA003050 EQU   *
         LA    LEV2REG,M3131  RELATIVE SECOND LEVEL MESSAGE TO BE
*                                  PUT TO THE USER.
         B     IA020001       BRANCH TO CALL IKJEBEMS.
         EJECT
***********************************************************************
*
*    MESSAGE ID -- IKJ52311I
*
*    CONDITION(S) --
*
*         A MEMBER NAME HAS BEEN SPECIFIED FOR A SEQUENTIAL DATA SET.
*
*    PARAMETER FORMAT --
*         WORD 1 - ADDRESS OF THE EDIT COMMUNICATION AREA
*         WORD 2 - MESSAGE INDEX = 08
*         WORD 3 - NOT USED
*         WORD 4 - LENGTH OF MEMBER NAME
*         WORD 5 - NOT USED
*         WORD 6 - ADDRESS OF THE IKJEBEIA WORK AREA
*
***********************************************************************
IA004010 EQU   *
         LA    LEV1REG,M311   RELATIVE MESSAGE TO BE PUT TO THE
*                                  TERMINAL.
         LA    INS1REG,INMSGIN1 ADDRESS OF INSERTION LIST FOR
*                                  FIRST LEVEL MESSAGE.
         L     INS2REG,D12(,PLISTREG) OBTAIN LENGTH OF MEMBER
         BCTR  INS2REG,D0          NAME, REDUCE BY 1, AND MOVE TO
         EX    INS2REG,IAMVC006    INSERTION RECORD.
         LA    INS2REG,D5(,INS2REG) COMPUTE LENGTH OF INSERTION
         STH   INS2REG,INLNIN1     AND SAVE IN PARAMETER LIST.
         LA    INS2REG,M311IN1 OBTAIN OFFSET TO MEMBER INSERTION
         STH   INS2REG,INOFFIN1    AND SAVE IN PARAMETER LIST.
         LA    INS2REG,CADSNPTR OBTAIN ADDRESS OF DSNAME AND SAVE
         ST    INS2REG,INPTIN1     IN PARAMETER LIST.
         LA    INS2REG,M311IN2 OBTAIN OFFSET TO DSNAME INSERTION
         STH   INS2REG,CADSNOFF    AND SAVE IN PARAMETER LIST.
         SR    LEV2REG,LEV2REG SHOW NO SECOND LEVEL MESSAGE OR
         SR    INS2REG,INS2REG     INSERTION REQUIRED.
         ST    INS2REG,CADSNPTR SHOW ONLY TWO INSERTIONS REQUIRED
*                                  IN THIS MESSAGE.
         B     IA020001        BRANCH TO CALL IKJEBEMS.
         EJECT
***********************************************************************
*
*    MESSAGE ID -- IKJ52330I
*
*    CONDITION(S) --
*
*         THE DATA SET ORGANIZATION SPECIFIED IN THE FORMAT 1 DSCB IS
*         NOT ACCEPTABLE FOR THE EDIT COMMAND.
*
*    PARAMETER FORMAT --
*         WORD 1 - ADDRESS OF THE EDIT COMMUNICATION AREA
*         WORD 2 - MESSAGE INDEX = 12
*         WORD 3 - NOT USED
*         WORD 4 - ADDRESS OF DSORG FIELD IN FORMAT 1 DSCB
*         WORD 5 - NOT USED
*         WORD 6 - ADDRESS OF THE IKJEBEIA WORK AREA
*
***********************************************************************
IA005010 EQU   *
         L     DSORGREG,D12(,PLISTREG) ADDRESS OF DSORG FIELD.
         CLI   X0(DSORGREG),DS1ORGIS CHECK IF INDEXED SEQUENTIAL
*                                  ORGAINZATION.
         BNE   IA005020       IF NOT INDEXED SEQUENTIAL, BRANCH
*                                  TO CHECK IF DIRECT.
         MVC   INMSGIN1(IAINS004-IAINS003),IAINS003 MOVE FIRST
*                                  INSERTION RECORD TO PARAMETER
*                                  LIST.
         B     IA005040       BRANCH TO SET UP DATA SET NAME.
IA005020 EQU   *
         CLI   D0(DSORGREG),DS1ORGDA CHECK IF DIRECT ORGANIZATION.
         BNE   IA005030       IF NOT DIRECT, BRANCH TO PUT
*                                  UNKNOWN DSORG INSERTION.
         MVC   INMSGIN1(IAINS003-IAINS002),IAINS002 MOVE FIRST
*                                  INSERTION RECORD TO PARAMETER
*                                  LIST.
         B     IA005040       BRANCH TO SET UP DATA SET NAME.
IA005030 EQU   *
         MVC   INMSGIN1(IAINS013-IAINS012),IAINS012 MOVE FIRST
*                                  INSERTION RECORD TO PARAMETER
*                                  LIST.
IA005040 EQU   *
         LA    INS1REG,INMSGIN1 ADDRESS OF INSERTION PARAMETER
*                                  LIST FOR FIRST LEVEL MESSAGE.
         LA    LEV1REG,CADSNPTR ADDRESS OF DSNAME INSERTION TO
         ST    LEV1REG,INPTIN1     PARAMETER LIST.
         LA    LEV1REG,M330IN2 OBTAIN OFFSET IN MESSAGE TO DSNAME
         STH   LEV1REG,CADSNOFF    INSERTION AND SAVE.
         LA    LEV1REG,M330   RELATIVE MESSAGE TO BE PUT TO THE
*                                  TERMINAL.
         LA    LEV2REG,M3301  RELATIVE SECOND LEVEL MESSAGE
*                                  NUMBER.
         SR    INS2REG,INS2REG SHOW NO INSERTIONS REQUIRED IN
*                                  SECOND LEVEL MESSAGE.
         ST    INS2REG,CADSNPTR SHOW NO MORE THAN TWO INSERTIONS
*                                  ARE REQUIRED IN MESSAGE.
         B     IA020001       BRANCH TO CALL IKJEBEMS.
         EJECT
***********************************************************************
*
*    MESSAGE ID -- IKJ52331I
*
*    CONDITION(S) --
*
*         THE EDIT DATA SET HAS A RECORD FORMAT OTHER THAN F, V, FB,
*         OR VB.
*
*    PARAMETER FORMAT --
*         WORD 1 - ADDRESS OF THE EDIT COMMUNICATION AREA
*         WORD 2 - MESSAGE INDEX = 16
*         WORD 3 - NOT USED
*         WORD 4 - ADDRESS OF RECFM FIELD IN FORMAT 1 DSCB
*         WORD 5 - NOT USED
*         WORD 6 - ADDRESS OF THE IKJEBEIA WORK AREA
*
***********************************************************************
IA006010 EQU   *
         L     INS1REG,D12(,PLISTREG) ADDRESS RECFM BYTE.
         LA    INS2REG,D1     COUNT OF CHARACTERS = 1.
         TM    D0(INS1REG),DS1FMUDF CHECK FOR UNDEFINED LRECL.
         BNO   IA006015       IF VARIABLE OR FIXED, BRANCH TO
*                                  PROCESS.
         MVI   ININS1,IAFMU   MOVE 'U' TO INSERTION.
         TM    D0(INS1REG),DS1FMOVF CHECK FOR TRACK OVERFLOW.
         BZ    IA006040       IF NO TRACK OVERFLOW, BRANCH
*                                  TO CHECK FOR CONTROL
*                                  CHARACTERS.
         MVI   ININS1+D1,IAFMT MOVE 'T' TO INSERTION.
         LA    INS2REG,D2     COUNT OF CHARACTERS = 2.
         B     IA006040       BRANCH TO CHECK FOR CONTROL
*                                  CHARACTERS.
IA006015 EQU   *
         TM    D0(INS1REG),DS1FMFX CHECK FOR FIXED FORMAT.
         BZ    IA006020       IF NOT FIXED, BRANCH TO MOVE 'V'
*                                  TO INSERTION.
         MVI   ININS1,IAFMF   MOVE 'F' TO INSERTION.
         B     IA006030       BRANCH TO CONTINUE.
IA006020 EQU   *
         MVI   ININS1,IAFMV   MOVE 'V' TO INSERTION.
IA006030 EQU   *
         IC    LEV1REG,D0(,INS1REG) ACQUIRE RECFM FIELD FROM DSCB.
         N     LEV1REG,IAFMMASK INSURE ALL BITS ZERO EXCEPT
*                                  B,S, AND T BITS.
         BZ    IA006040       IF NONE OF THE 3 BITS ARE 1,
*                                  BRANCH TO CHECK CONTROL
*                                  CHARACTERS.
         SRL   LEV1REG,D1     SHIFT RESULT FOR INDEXING.
         SR    LEV2REG,LEV2REG ZERO REGISTER.
         IC    LEV2REG,IAFMTAB(LEV1REG) ACQUIRE LENGTH FOR MOVE.
         LA    LEV1REG,IAFMTAB+D1(LEV1REG) ADDRESS OF DATA TO
*                                  BE MOVED.
         EX    LEV2REG,IAMVC012 MOVE DATA TO INSERTION RECORD.
         LA    INS2REG,D1(LEV2REG,INS2REG) UPDATE INSERTION
*                                  LENGTH.
IA006040 EQU   *
         LA    LEV2REG,ININS1(INS2REG) COMPUTE ADDRESS FOR
*                                  NEXT INSERTION CHARACTER.
         TM    D0(INS1REG),DS1FMA+DS1FMM CHECK FOR CONTROL
*                                  CHARACTER FORMAT.
         BZ    IA006070       IF NO CONTROL CHARACTERS,
*                                  BRANCH TO FINISH PARAMETERS.
         TM    D0(INS1REG),DS1FMA CHECK FOR ASA CONTROL.
         BZ    IA006050       IF NOT, ASSUME MACHINE CONTROL.
         MVI   D0(LEV2REG),IAFMA IF SO, MOVE 'A' TO INSERTION.
         B     IA006060       BRANCH TO UPDATE INSERTION LENGTH.
IA006050 EQU   *
         MVI   D0(LEV2REG),IAFMM MOVE 'M' TO INSERTION.
IA006060 EQU   *
         LA    INS2REG,D1(,INS2REG) UPDATE INSERTION LENGTH.
IA006070 EQU   *
         LA    INS2REG,D4(,INS2REG) ADD 4 TO INSERTION LENGTH
*                                  FOR CONTROL WORD.
         STH   INS2REG,INLNIN1 STORE LENGTH IN INSERTION LIST.
         LA    INS2REG,M331IN1 OBTAIN OFFSET FOR INSERTION.
         STH   INS2REG,INOFFIN1 STORE OFFSET INTO LIST.
         SR    INS2REG,INS2REG SPECIFY NO LEVEL-2 INSERTIONS.
         LR    LEV2REG,INS2REG SPECIFY NO LEVEL-2 MESSAGE.
         ST    LEV2REG,INPTIN1 SPECIFY ONE FIRST-LEVEL INSERTION.
         LA    INS1REG,INPTIN1 ADDRESS OF FIRST-LEVEL INSERTION.
         LA    LEV1REG,M331   RELATIVE FIRST-LEVEL MESSAGE.
         B     IA020001       BRANCH TO CALL IKJEBEMS.
         EJECT
***********************************************************************
*
*    MESSAGE ID -- IKJ52336I
*
*    CONDITION(S) --
*
*         THE LOGICAL RECORD LENGTH OF THE DATA SET SPECIFIED IS
*         INVALID FOR THE DATA SET TYPE SPECIFIED.
*
*    PARAMETER FORMAT --
*         WORD 1 - ADDRESS OF THE EDIT COMMUNICATION AREA
*         WORD 2 - MESSAGE INDEX = 20
*         WORD 3 - NOT USED
*         WORD 4 - FULLWORD BINARY LOGICAL RECORD LENGTH
*         WORD 5 - NOT USED
*         WORD 6 - ADDRESS OF THE IKJEBEIA WORK AREA
*
***********************************************************************
IA007010 EQU   *
         L     PARMREG1,D12(,PLISTREG) OBTAIN LRECL FOR MESSAGE.
         LA    LEV2REG,INMSGIN1 ADDRESS OF INSERTION RECORD.
         BAL   SRRTNREG,IA100000 BRANCH TO CONVERT LRECL TO
*                                  EBCDIC AND SUPPRESS LEADING
*                                  ZEROES.
         LA    INS1REG,M336IN1 OBTAIN OFFSET TO INSERTION TEXT
         STH   INS1REG,INOFFIN1    AND SAVE IN PARAMETER LIST.
         MVC   INMSGIN2(IAINS008-IAINS007),IAINS007 CREATE SECOND
         MVC   ININS2(L'CADSTYPE),CADSTYPE INSERTION RECORD (DATA
*                                  SET TYPE) FOR MESSAGE.
         LA    INS1REG,M336IN2 OBTAIN OFFSET TO INSERTION TEXT
         STH   INS1REG,INOFFIN2    AND SAVE IN PARAMETER LIST.
         LA    INS1REG,INMSGIN2 OBTAIN ADDRESS OF SECOND
         ST    INS1REG,INPTIN1     INSERTION RECORD AND SAVE IN
*                                  PARAMETER LIST.
         BAL   SRRTNREG,IA110010 BRANCH TO SUPPRESS TRAILING
*                                  BLANKS FROM DATA SET TYPE.
         LA    INS1REG,INMSGIN1 ADDRESS OF INSERTION LIST.
         LA    LEV1REG,M336   RELATIVE MESSAGE TO BE PUT TO THE
*                                  TERMINAL.
         SR    LEV2REG,LEV2REG SHOW THAT NO SECOND LEVEL OR
         SR    INS2REG,INS2REG     INSERTION IS REQUIRED.
         B     IA020001       BRANCH TO CALL IKJEBEMS.
         EJECT
***********************************************************************
*
*    MESSAGE ID -- IKJ52307I
*
*    CONDITION(S) --
*
*         THE DATA SET NAME SPECIFIED COULD NOT BE FOUND IN THE
*         CATALOG AND 'OLD' WAS SPECIFIED ON THE EDIT COMMAND.
*
*    PARAMETER FORMAT --
*         WORD 1 - ADDRESS OF THE EDIT COMMUNICATION AREA
*         WORD 2 - MESSAGE INDEX = 24
*         WORD 3 - NOT USED
*         WORD 4 - NOT USED
*         WORD 5 - NOT USED
*         WORD 6 - ADDRESS OF THE IKJEBEIA WORK AREA
*
***********************************************************************
IA008010 EQU   *
         LA    INS1REG,CADSNPTR ADDRESS OF INSERTION PARAMETER
*                                  LIST.
         LA    LEV1REG,M307IN1 OBTAIN OFFSET IN FIRST LEVEL
         STH   LEV1REG,CADSNOFF    MESSAGE FOR DATA SET NAME
*                                  INSERTION AND SAVE IN
*                                  PARAMETER LIST.
         LA    LEV1REG,M307   RELATIVE MESSAGE TO BE PUT TO THE
*                                  TERMINAL.
         SR    LEV2REG,LEV2REG SHOW NO SECOND LEVEL MESSAGE
*                                  IS REQUIRED.
         SR    INS2REG,INS2REG SHOW NO INSERTIONS REQUIRED IN
*                                  SECOND LEVEL MESSAGE.
         ST    LEV2REG,CADSNPTR SHOW ONLY ONE INSERTION IN THIS
*                                  MESSAGE.
         B     IA020001        BRANCH TO CALL IKJEBEMS.
         EJECT
***********************************************************************
*
*    MESSAGE ID -- IKJ52335I
*
*    CONDITION(S) --
*
*        A. THE LINE VALUE SPECIFIED FOR A NEW DATA SET IS -
*           (1) OTHER THAN 80 WHEN 80 IS REQUIRED    OR-
*           (2) GREATER THAN THE MAXIMUM LRECL FOR THE DATA SET TYPE
*           SPECIFIED.
*
*        B. THE LRECL OF AN OLD DATA SET IS ZERO.
*
*    PARAMETER FORMAT --
*         WORD 1 - ADDRESS OF THE EDIT COMMUNICATION AREA
*         WORD 2 - MESSAGE INDEX=28
*         WORD 3 - SECOND-LEVEL RELATIVE MESSAGE NUMBER
*         WORD 4 - FULLWORD BINARY VALUE OF QUANTITY TO BE USED AS
*                  LINE VALUE
*         WORD 5 - NOT USED
*         WORD 6 - ADDRESS OF THE IKJEBEIA WORK AREA
*
***********************************************************************
IA009010 EQU   *
         MVC   ININS1(L'CADSTYPE),CADSTYPE BUILD DATA SET TYPE
         MVC   ININS3(L'CADSTYPE),CADSTYPE INSERTIONS FOR FIRST
*                                  AND SECOND LEVEL MESSAGES.
         LA    INS1REG,INMSGIN1 ADDRESS OF INSERTION RECORD.
         BAL   SRRTNREG,IA110010 BRANCH TO SUPPRESS TRAILING
*                                  BLANKS.
         LA    INS1REG,M335IN1 OBTAIN OFFSET TO INSERTION AND
         STH   INS1REG,INOFFIN1    SAVE IN PARAMETER LIST.
         LA    INS1REG,INMSGIN3 ADDRESS OF INSERTION RECORD.
         BAL   SRRTNREG,IA110010 BRANCH TO SUPPRESS TRAILING
*                                  BLANKS.
         L     PARMREG1,D12(,PLISTREG) LRECL TO BE USED BY EDIT.
         LA    LEV2REG,INMSGIN2 ADDRESS OF INSERTION RECORD.
         BAL   SRRTNREG,IA100000 BRANCH TO CONVERT LRECL TO
*                                  EBCDIC AND SUPPRESS LEADING
*                                  ZEROES.
         ST    LEV2REG,INPTIN1 CHAIN LINE VALUE TO DATA SET TYPE.
         LA    LEV2REG,M335IN2 OBTAIN OFFSET TO INSERTION AND
         STH   LEV2REG,INOFFIN2    SAVE IN PARAMETER LIST.
         CLI   D11(PLISTREG),M3353 DETERMINE IF LRECL IN DSCB IS ZERO.
         BE    IA009040       IF SO, BRANCH TO COMPLETE MESSAGE
*                                  PARAMETERS.
         TM    CADSATTR,CALRECLX CHECK IF LRECL MUST BE 80
         BO    IA009020       IF 80 REQUIRED, BRANCH TO COMPLETE
*                                  MESSAGE.
         LH    PARMREG1,CAFLRLMX MAXIMUM LRECL PERMITTED FOR DATA
*                                  SET TYPE.
         LA    LEV2REG,INMSGIN4 ADDRESS OF INSERTION RECORD.
         BAL   SRRTNREG,IA100000 BRANCH TO CONVERT LRECL TO
*                                  EBCDIC AND SUPPRESS LEADING
*                                  ZEROES.
         ST    LEV2REG,INPTIN3     INSERTION AND SAVE.
         LA    LEV2REG,M3352IN1 OBTAIN OFFSET TO DATA SET TYPE
         STH   LEV2REG,INOFFIN3    INSERTION AND SAVE.
         LA    LEV2REG,M3352IN2 OBTAIN OFFSET TO INSERTION AND
         STH   LEV2REG,INOFFIN4    SAVE IN PARAMETER LIST.
         LA    LEV2REG,M3352  RELATIVE SECOND LEVEL MESSAGE.
         B     IA009030       BRANCH TO COMPLETE PARAMETER LIST.
IA009020 EQU   *
         LA    LEV2REG,M3351IN1 OBTAIN OFFSET TO DATA SET TYPE
         STH   LEV2REG,INOFFIN3    INSERTION AND SAVE.
         LA    LEV2REG,M3351  RELATIVE SECOND LEVEL MESSAGE.
IA009030 EQU   *
         LA    LEV1REG,M335   RELATIVE MESSAGE TO BE PUT TO THE
*                                  TERMINAL.
         LA    INS1REG,INMSGIN1 ADDRESS OF INSERTION LISTS FOR
         LA    INS2REG,INMSGIN3    FIRST AND SECOND LEVEL MESSAGE
         B     IA020001       BRANCH TO CALL IKJEBEMS.
IA009040 EQU   *
         LA    LEV1REG,M335   RELATIVE MESSAGE TO BE PUT TO THE
*                                  TERMINAL.
         LA    LEV2REG,M3353  SECOND-LEVEL RELATIVE MESSAGE NUMBER.
         LA    INS1REG,INMSGIN1 ADDRESS OF FIRST-LEVEL INSERTIONS.
         SR    INS2REG,INS2REG SPECIFY NO SECOND-LEVEL INSERTIONS.
         B     IA020001       BRANCH TO CALL IKJEBEMS.
         EJECT
***********************************************************************
*
*    MESSAGE ID -- IKJ52333I
*
*    CONDITION(S) --
*
*         THE BLOCK VALUE ENTERED FOR A NEW DATA SET IS INVALID.
*
*    PARAMETER FORMAT --
*         WORD 1 - ADDRESS OF THE EDIT COMMUNICATION AREA
*         WORD 2 - MESSAGE INDEX = 32
*         WORD 3 - RELATIVE SECOND-LEVEL MESSAGE NUMBER
*         WORD 4 - FULLWORD BINARY BLOCK SIZE
*         WORD 5 - NOT USED
*         WORD 6 - ADDRESS OF THE IKJEBEIA WORK AREA
*
***********************************************************************
IA010010 EQU   *
         L     PARMREG1,D12(,PLISTREG) OBTAIN BLOCK VALUE FOR
*                                  MESSAGE.
         LA    LEV2REG,INMSGIN1 ADDRESS OF INSERTION RECORD.
         BAL   SRRTNREG,IA100000 BRANCH TO CONVERT TO EBCDIC
*                                  AND SUPPRESS LEADING ZEROES.
         LR    INS1REG,LEV2REG ADDRESS OF INSERTION RECORD.
         LA    LEV2REG,M333IN1 OBTAIN OFFSET TO INSERTION AND
         STH   LEV2REG,INOFFIN1    DEFAULT BLKSIZE INSERTION
         LA    LEV1REG,M333   RELATIVE FIRST LEVEL MESSAGE TO BE
*                                  PUT TO THE TERMINAL.
         L     LEV2REG,D8(,PLISTREG) RELATIVE SECOND LEVEL
*                                  MESSAGE NUMBER.
         SR    INS2REG,INS2REG SHOW NO INSERTION REQUIRED IN
*                                  SECOND LEVEL MESSAGE.
         B     IA020001       BRANCH TO CALL IKJEBEMS.
         EJECT
***********************************************************************
*
*    MESSAGE ID -- IKJ52363I
*    MESSAGE ID -- IKJ52364I
*
*    CONDITION(S) --
*
*         (1) 'SCAN' HAS BEEN SPECIFIED FOR A DATA SET TYPE HAVING
*             NO ASSOCIATED LANGUAGE PROCESSOR     OR
*
*         (2) 'SCAN' HAS BEEN SPECIFIED BUT THE ASSOCIATED LANGUAGE
*             PROCESSOR IS NOT IN THE SYSTEM
*
*    PARAMETER FORMAT --
*         WORD 1 - ADDRESS OF THE EDIT COMMUNICATION AREA
*         WORD 2 - MESSAGE INDEX = 36
*         WORD 3 - NOT USED
*         WORD 4 - NOT USED
*         WORD 5 - NOT USED
*         WORD 6 - ADDRESS OF THE IKJEBEIA WORK AREA
*
***********************************************************************
IA011010 EQU   *
         MVC   INPTIN2(IAINS008-IAINS007),IAINS007 CREATE DATA
         MVC   ININS2(L'CADSTYPE),CADSTYPE SET TYPE INSERTION
*                                  RECORD FOR MESSAGE.
         LA    INS1REG,M363IN2 OBTAIN OFFSET TO DATA SET TYPE.
         STH   INS1REG,INOFFIN2    AND PLACE IN PARAMETER LIST.
         LA    INS1REG,INMSGIN2 ADDRESS OF INSERTION RECORD FOR
*                                  FIRST LEVEL MESSAGE.
         BAL   SRRTNREG,IA110010 BRANCH TO SUPPRESS TRAILING
*                                  BLANKS FROM DATA SET TYPE.
         MVC   INMSGIN1(IAINS006-IAINS005),IAINS005 MOVE
*                                  INSERTION TO WORK AREA.
         ST    INS1REG,INPTIN1 CHAIN INSERTION RECORDS.
         LA    INS1REG,INMSGIN1 ADDRESS OF INSERTION CHAIN.
         TM    CADSATTR,CASCAN CHECK IF SYNTAX CHECKING ALLOWED.
         BO    IA011020       IF ALLOWED, BRANCH TO COMPLETE
*                                  MESSAGE.
         LA    LEV1REG,M363   RELATIVE MESSAGE TO BE PUT TO THE
*                                  TERMINAL.
         B     IA011030       BRANCH TO CALL IKJEBEMS.
IA011020 EQU   *
         LA    LEV1REG,M364   RELATIVE MESSAGE TO BE PUT TO THE
*                                  TERMINAL.
         LA    LEV2REG,M3641  RELATIVE SECOND LEVEL MESSAGE.
         LA    INS2REG,M364IN2 OBTAIN OFFSET TO DATA SET TYPE
         STH   INS2REG,INOFFIN2    AND PLACE IN PARAMETER LIST.
         B     IA011040       BRANCH TO CALL IKJEBEMS.
IA011030 EQU   *
         SR    LEV2REG,LEV2REG SHOW THAT NO SECOND LEVEL MESSAGE
IA011040 EQU   *
         SR    INS2REG,INS2REG     OR INSERTION REQUIRED.
         B     IA020001       BRANCH TO CALL IKJEBEMS.
         EJECT
***********************************************************************
*
*    MESSAGE ID -- IKJ52362I
*
*    CONDITION(S) --
*
*         'NONUM' HAS BEEN SPECIFIED FOR A DATA SET TYPE THAT
*         REQUIRES THE DATA SET TO BE LINE NUMBERED.
*
*    PARAMETER FORMAT --
*         WORD 1 - ADDRESS OF THE EDIT COMMUNICATION AREA
*         WORD 2 - MESSAGE INDEX = 40
*         WORD 3 - NOT USED
*         WORD 4 - NOT USED
*         WORD 5 - NOT USED
*         WORD 6 - ADDRESS OF THE IKJEBEIA WORK AREA
*
***********************************************************************
IA012010 EQU   *
         MVC   INPTIN1(IAINS008-IAINS007),IAINS007 CREATE DATA
         MVC   ININS1(L'CADSTYPE),CADSTYPE SET TYPE INSERTION
*                                  RECORD FOR MESSAGE.
         LA    LEV1REG,M362   RELATIVE MESSAGE TO BE PUT TO THE
*                                  TERMINAL.
         LA    INS1REG,M362IN1 OBTAIN OFFSET FOR DATA SET TYPE
         STH   INS1REG,INOFFIN1    AND PLACE IN PARAMETER LIST.
         LA    INS1REG,INMSGIN1 ADDRESS OF INSERTION RECORD FOR
*                                  FIRST LEVEL MESSAGE.
         BAL   SRRTNREG,IA110010 BRANCH TO SUPPRESS TRAILING
*                                  BLANKS FROM DATA SET TYPE.
         SR    LEV2REG,LEV2REG SHOW THAT NO SECOND LEVEL MESSAGE
         SR    INS2REG,INS2REG     OR INSERTION REQUIRED.
         B     IA020001       BRANCH TO CALL IKJEBEMS.
         EJECT
***********************************************************************
*
*    MESSAGE ID -- IKJ52365I
*
*    CONDITION(S) --
*
*         THE SOURCE MARGINS SPECIFIED FOR A PLI OR PLIF DATA SET
*         ARE INVALID.
*
*    PARAMETER FORMAT --
*         WORD 1 - ADDRESS OF THE EDIT COMMUNICATION AREA
*         WORD 2 - MESSAGE INDEX = 44
*         WORD 3 - NOT USED
*         WORD 4 - MARGIN INSERTIONS
*              FIRST HALFWORD - LEFT MARGIN (DECIMAL)
*              SECOND HALFWORD - RIGHT MARGIN (DECIMAL)
*         WORD 5 - NOT USED
*         WORD 6 - ADDRESS OF THE IKJEBEIA WORK AREA
*
***********************************************************************
IA013010 EQU   *
         LH    PARMREG1,D12(,PLISTREG) OBTAIN START POSITION FOR
*                                  MESSAGE.
         LA    LEV2REG,INMSGIN1 ADDRESS OF INSERTION RECORD.
         BAL   SRRTNREG,IA100000 BRANCH TO CONVERT TO EBCDIC
*                                  AND SUPPRESS LEADING ZEROES.
         LA    LEV2REG,M365IN1 OBTAIN OFFSET TO INSERTION AND
         STH   LEV2REG,INOFFIN1    SAVE IN PARAMETER LIST.
         LH    PARMREG1,D14(,PLISTREG) OBTAIN LENGTH FOR MESSAGE.
         LTR   PARMREG1,PARMREG1 CHECK IF LENGTH ENTERED.
         BZ    IA013020       IF NOT ENTERED, BRANCH TO COMPLETE
*                                  PARAMETER LIST.
         LA    LEV2REG,INMSGIN2 ADDRESS OF INSERTION RECORD.
         BAL   SRRTNREG,IA100000 BRANCH TO CONVERT TO EBCDIC.
*                                  AND SUPPRESS LEADING ZEROES.
         ST    LEV2REG,INPTIN1 CHAIN INSERTION RECORDS.
         LA    LEV2REG,M365IN2 OBTAIN OFFSET TO INSERTION AND
         STH   LEV2REG,INOFFIN2    SAVE IN PARAMETER LIST.
IA013020 EQU   *
         LA    LEV1REG,M365   RELATIVE MESSAGE TO BE PUT TO THE
*                                  TERMINAL.
         LA    INS1REG,INMSGIN1 ADDRESS OF INSERTION LIST FOR
*                                  FIRST LEVEL MESSAGE.
         LA    LEV2REG,M3651  RELATIVE SECOND LEVEL MESSAGE.
         SR    INS2REG,INS2REG SHOW NO INSERTION REQUIRED IN
*                                  SECOND LEVEL MESSAGE.
         B     IA020001       BRANCH TO PUT MESSAGE.
         EJECT
***********************************************************************
*
*    MESSAGE ID -- IKJ52360I
*
*    CONDITION(S) --
*
*         INVALID SUBFIELD OPERANDS APPEAR ON THE 'NUM' OPERAND
*         OF THE COMMAND.
*
*    PARAMETER FORMAT --
*         WORD 1 - ADDRESS OF THE EDIT COMMUNICATION AREA
*         WORD 2 - MESSAGE INDEX = 48
*         WORD 3 - RELATIVE SECOND-LEVEL MESSAGE NUMBER
*         WORD 4 - NOT USED
*         WORD 5 - NOT USED
*         WORD 6 - ADDRESS OF THE IKJEBEIA WORK AREA
*
***********************************************************************
IA014010 EQU   *
         SR    PARMREG1,PARMREG1 OBTAIN START POSITION FOR
         IC    PARMREG1,CALINE     MESSAGE.
         LA    LEV2REG,INMSGIN1 ADDRESS OF INSERTION RECORD.
         BAL   SRRTNREG,IA100000 BRANCH TO CONVERT TO EBCDIC
*                                  AND SUPPRESS LEADING ZEROES.
         LA    LEV2REG,M360IN1 OBTAIN OFFSET TO INSERTION AND
         STH   LEV2REG,INOFFIN1    SAVE IN PARAMETER LIST.
         SR    PARMREG1,PARMREG1 OBTAIN LINE NUMBER LENGTH FOR
         IC    PARMREG1,CALENGTH   MESSAGE.
         LA    LEV2REG,INMSGIN2 ADDRESS OF INSERTION RECORD.
         BAL   SRRTNREG,IA100000 BRANCH TO CONVERT TO EBCDIC.
*                                  AND SUPPRESS LEADING ZEROES.
         ST    LEV2REG,INPTIN1 CHAIN INSERTION RECORDS.
         LA    LEV2REG,M360IN2 OBTAIN OFFSET TO INSERTION AND
         STH   LEV2REG,INOFFIN2    SAVE IN PARAMETER LIST.
         LA    LEV1REG,M360   RELATIVE MESSAGE TO BE PUT TO THE
*                                  TERMINAL.
         LA    INS1REG,INMSGIN1 ADDRESS OF INSERTION LIST FOR
*                                  FIRST LEVEL MESSAGE.
         L     LEV2REG,D8(,PLISTREG) RELATIVE SECOND LEVEL NUMBER
         SR    INS2REG,INS2REG SHOW NO INSERTION REQUIRED IN
*                                  SECOND LEVEL MESSAGE.
         B     IA020001       BRANCH TO PUT MESSAGE.
         EJECT
***********************************************************************
*
*    MESSAGE ID -- IKJ52361I
*
*    CONDITION(S) --
*
*         'CAPS' ARE REQUIRED FOR THE DATA SET TYPE BEING EDITED
*         BUT THE 'ASIS' KEYWORD HAS BEEN ENTERED ON THE COMMAND.
*
*    PARAMETER FORMAT --
*         WORD 1 - ADDRESS OF THE EDIT COMMUNICATION AREA
*         WORD 2 - MESSAGE INDEX = 52
*         WORD 3 - NOT USED
*         WORD 4 - NOT USED
*         WORD 5 - NOT USED
*         WORD 6 - ADDRESS OF THE IKJEBEIA WORK AREA
*
***********************************************************************
IA015010 EQU   *
         MVC   INPTIN1(IAINS008-IAINS007),IAINS007 CREATE DATA
         MVC   ININS1(L'CADSTYPE),CADSTYPE SET TYPE INSERTION
*                                  RECORD FOR MESSAGE.
         LA    LEV1REG,M361   RELATIVE MESSAGE TO BE PUT TO THE
*                                  TERMINAL.
         LA    INS1REG,M361IN1 OBTAIN OFFSET FOR DATA SET TYPE.
         STH   INS1REG,INOFFIN1    AND PLACE IN PARAMETER LIST.
         LA    INS1REG,INMSGIN1 ADDRESS OF INSERTION RECORD FOR
*                                  FIRST LEVEL MESSAGE.
         BAL   SRRTNREG,IA110010 BRANCH TO SUPPRESS TRAILING
*                                  BLANKS FROM DATA SET TYPE.
         SR    LEV2REG,LEV2REG SHOW THAT NO SECOND LEVEL MESSAGE
         SR    INS2REG,INS2REG     OR INSERTION REQUIRED.
         B     IA020001       BRANCH TO CALL IKJEBEMS.
         EJECT
***********************************************************************
*
*    MESSAGE ID -- IKJ52424I
*
*    CONDITION(S) --
*
*         ONE OF THE FOLLOWING HAS OCCURRED -
*           (1) THE DDNAME RETURNED BY IKJDAIR COULD NOT BE LOCATED
*               IN THE TIOT.
*           (2) A CATALOG OR DYNAMIC ALLOCATION ERROR OCCURRED
*               WHEN IKJDAIR WAS INVOKED TO UNALLOCATE A PDS.
*
*    PARAMETER FORMAT --
*         WORD 1 - ADDRESS OF THE EDIT COMMUNICATION AREA
*         WORD 2 - MESSAGE INDEX = 56
*         WORD 3 - RELATIVE SECOND-LEVEL MESSAGE NUMBER
*         WORD 4 - NOT USED
*         WORD 5 - RIGHT-JUSTIFIED DARC OR CTRC FIELD
*         WORD 6 - ADDRESS OF THE IKJEBEIA WORK AREA
*
***********************************************************************
IA016010 EQU   *
         CLI   D11(PLISTREG),M4245 CHECK IF DDNAME NOT FOUND IN
*                                  TIOT.
         BNE   IA016020       IF NOT, BRANCH TO PROCESS FOR
*                                  UNALLOCATION ERROR.
         MVC   INMSGIN1(D8),IAINS015 MOVE INSERTION CONTROL
*                                  INFORMATION.
         MVC   ININS1(D8),CAEDDDN MOVE DDNAME TO INSERTION
*                                  RECORD.
         LA    INS1REG,INMSGIN1 ADDRESS INSERTION LIST.
         BAL   SRRTNREG,IA110010 BRANCH TO SUPPRESS TRAILING
*                                  BLANKS.
         B     IA016060       BRANCH TO COMPLETE PARAMETERS.
IA016020 EQU   *
         SR    INS2REG,INS2REG SPECIFY DARC OR CTRC AS LAST
         ST    INS2REG,INPTIN2     SECOND-LEVEL INSERTION.
         LA    INS2REG,D16(,PLISTREG) ADDRESS DARC OR CTRC FIELD.
         LA    LEV2REG,INMSGIN2 ADDRESS SECOND INSERTION LIST.
         BAL   SRRTNREG,IA100010 BRANCH TO FORMAT INSERTION.
         CLI   D11(PLISTREG),M4242 CHECK IF DYNAMIC ALLOCATION
*                                  ERROR.
         BNE   IA016030       IF NOT, BRANCH TO PROCESS CATALOG
*                                  ERROR INSERTION.
         LA    INS2REG,M4242IN2 SET OFFSET FOR DARC INSERTION.
         LA    INS1REG,M4242IN1 SET OFFSET FOR DSNAME INSERTION.
         B     IA016040       BRANCH TO CONTINUE PROCESSING.
IA016030 EQU   *
         LA    INS1REG,M4244IN1 SET OFFSET FOR DSNAME INSERTION.
         LA    INS2REG,M4244IN2 SET OFFSET FOR CTRC INSERTION.
IA016040 EQU   *
         STH   INS2REG,INOFFIN2 STORE OFFSET FOR SECOND INSERTION.
         STH   INS1REG,CADSNOFF STORE OFFSET FOR DSNAME INSERTION.
         LA    INS2REG,INMSGIN2
         ST    INS2REG,CADSNPTR CHAIN INSERTION LISTS.
         MVC   INMSGIN1(D8),CADSNPTR MOVE CONTROL PORTION OF
*                                  INSERTION RECORD.
         CLI   CADSNREC,INSNGLQT IS INSERTION FULLY QUALIFIED.
         BNE   IA016050       IF NOT, BRANCH TO APPEND USERID
*                                  AND ENCLOSE IN QUOTES.
         MVC   ININS1(D56),CADSNREC MOVE DATA SET NAME, ASSUMING
*                                  MAXIMUM LENGTH.
         B     IA016060       BRANCH TO COMPLETE MESSAGE
*                                  PARAMETERS.
IA016050 EQU   *
         L     PASSREG,CAPTTMP ADDRESS COMMAND PARAMETER LIST.
         L     PASSREG,CPPLPSCB-CPPL(,PASSREG) ADDRESS PSCB.
         SR    INS1REG,INS1REG
         IC    INS1REG,PSCBUSRL-PSCB(,PASSREG) GET USERID LENGTH.
         MVI   ININS1,INSNGLQT APPEND QUOTE TO INSERTION RECORD.
         EX    INS1REG,IAMVC011  MOVE USERID TO INSERTION RECORD.
         LA    DSNAMREG,ININS1(INS1REG) COMPUTE ADDRESS FOR MOVE
*                                  OF DSNAME.
         MVI   D1(DSNAMREG),INPERIOD MOVE PERIOD AFTER USERID.
         MVC   D2(D56,DSNAMREG),CADSNREC MOVE DSNAME.
         LA    INS1REG,D3(,INS1REG) COMPUTE INCREMENT TO DSNAME
*                                  LENGTH.
         AH    INS1REG,CADSNLEN ADD LENGTH OF UNFORMATTED DSNAME.
         STH   INS1REG,INLNIN1 UPDATE LENGTH IN INSERTION RECORD.
         LA    INS1REG,ININS1-D5(INS1REG) COMPUTE POSITION OF
*                                  FINAL QUOTE.
         MVI   D0(INS1REG),INSNGLQT MOVE FINAL QUOTE.
IA016060 EQU   *
         LA    LEV1REG,M424   SET RELATIVE FIRST-LEVEL NUMBER.
         L     LEV2REG,D8(,PLISTREG) SET SECOND-LEVEL NUMBER.
         SR    INS1REG,INS1REG SPECIFY NO FIRST-LEVEL INSERTIONS.
         LA    INS2REG,INMSGIN1 ADDRESS FIRST LEVEL-2 INSERTION.
         B     IA020001       BRANCH TO CALL IKJEBEMS TO PUT
*                                  MESSAGE TO TERMINAL.
         EJECT
***********************************************************************
*
*    MESSAGE ID -- IKJ52334I
*
*    CONDITION(S) --
*
*         'LINE' AND/OR 'BLOCK' OPERANDS HAVE BEEN SPECIFIED FOR
*         AN OLD DATA SET WHICH IS NOT EMPTY.
*
*    PARAMETER FORMAT --
*         WORD 1 - ADDRESS OF THE EDIT COMMUNICATION AREA
*         WORD 2 - MESSAGE INDEX = 60
*         WORD 3 - NOT USED
*         WORD 4 - ADDRESS OF FORMATTED INSERTION LIST
*         WORD 5 - NOT USED
*         WORD 6 - ADDRESS OF THE IKJEBEIA WORK AREA
*
***********************************************************************
IA017010 EQU   *
         LA    LEV1REG,M334   RELATIVE MESSAGE TO BE PUT TO THE
*                                  TERMINAL.
         L     INS1REG,D12(,PLISTREG) ADDRESS OF INSERTION FOR
*                                  MESSAGE.
         SR    LEV2REG,LEV2REG SHOW NO SECOND LEVEL MESSAGE OR
         SR    INS2REG,INS2REG     INSERTION REQUIRED.
         B     IA020001       BRANCH TO CALL IKJEBEMS.
         EJECT
***********************************************************************
*
*    MESSAGE ID -- IKJ52342I
*
*    CONDITION(S) --
*
*         THE LINE VALUE SPECIFIED OR LRECL ENCOUNTERED IS TOO SMALL
*         TO PERMIT LINE NUMBERING.
*
*    PARAMETER FORMAT --
*         WORD 1 - ADDRESS OF THE EDIT COMMUNICATION AREA
*         WORD 2 - MESSAGE INDEX = 64
*         WORD 3 - NOT USED
*         WORD 4 - NOT USED
*         WORD 5 - NOT USED
*         WORD 6 - ADDRESS OF THE IKJEBEIA WORK AREA
*
***********************************************************************
IA018010 EQU   *
         LA    LEV1REG,M342   RELATIVE MESSAGE TO BE PUT TO THE
*                                  TERMINAL.
         LA    LEV2REG,M3421  RELATIVE SECOND LEVEL MESSAGE
*                                  NUMBER.
         SR    INS1REG,INS1REG SHOW NO INSERTION REQUIRED IN
         SR    INS2REG,INS2REG     EITHER MESSAGE.
         B     IA020001       BRANCH TO CALL IKJEBEMS.
         EJECT
***********************************************************************
*
*    MESSAGE ID -- IKJ52308I
*
*    CONDITION(S) --
*
*         THE MEMBER SPECIFIED ON THE COMMAND DOES NOT EXIST AND
*         THE USER SPECIFIED 'OLD'.
*
*    PARAMETER FORMAT --
*         WORD 1 - ADDRESS OF THE EDIT COMMUNICATION AREA
*         WORD 2 - MESSAGE INDEX = 68
*         WORD 3 - NOT USED
*         WORD 4 - LENGTH OF MEMBER NAME FOR INSERTION
*         WORD 5 - NOT USED
*         WORD 6 - ADDRESS OF THE IKJEBEIA WORK AREA
*
***********************************************************************
IA019010 EQU   *
         LA    LEV1REG,M308   RELATIVE MESSAGE TO BE PUT TO THE
*                                  TERMINAL.
         LA    INS1REG,INMSGIN1 ADDRESS OF INSERTION LIST FOR
*                                  FIRST LEVEL MESSAGE.
         L     INS2REG,D12(,PLISTREG) OBTAIN LENGTH OF MEMBER
         BCTR  INS2REG,D0          NAME, REDUCE BY 1, AND MOVE TO
         EX    INS2REG,IAMVC006    INSERTION RECORD.
         LA    INS2REG,D5(,INS2REG) COMPUTE LENGTH OF INSERTION
         STH   INS2REG,INLNIN1     AND SAVE IN PARAMETER LIST.
         LA    INS2REG,M308IN1 OBTAIN OFFSET TO MEMBER INSERTION
         STH   INS2REG,INOFFIN1    AND SAVE IN PARAMETER LIST.
         CLC   CAEDMEMB(D8),IAMEMNAM CHECK FOR MEMBER NAME OF
*                                  'TEMPNAME'.
         BE    IA019050       IF 'TEMPNAME' DEFAULTED, BYPASS
*                                  STRIPPING MEMBER FROM DSNAME
*                                  INSERTION.
         LA    INS2REG,CADSNPTR+D3 ADDRESS LAST CHARACTER OF
         AH    INS2REG,CADSNLEN    INSERTION RECORD FOR DSNAME.
         LA    COUNTREG,D10(,D0) SET COUNTER TO 1 LESS THAN
*                                  MAXIMUM NUMBER OF CHARACTERS
*                                  TO BE REMOVED FROM INSERTION.
IA019020 EQU   *
         CLI   D0(INS2REG),INLFTPRN IS CHARACTER A LEFT
*                                  PARENTHESIS.
         BE    IA019030       IF SO, BRANCH TO PROCESS.
         BCTR  INS2REG,D0     IF NOT, ADDRESS NEXT BYTE TO LEFT.
         BCT   COUNTREG,IA019020 REPEAT LOOP 10 TIMES.
         B     IA019050       IF NO MEMBER FOUND, BRANCH
*                                  TO USE DSNAME INSERTION AS
*                                  IT EXISTS.
IA019030 EQU   *
         LA    INS2REG,D11(,D0) COMPUTE NUMBER OF CHARACTERS TO
         SR    COUNTREG,INS2REG    BE REMOVED. (RESULT IS NEG.)
         AH    COUNTREG,CADSNLEN COMPUTE NEW LENGTH FOR INSERTION.
         CLI   CADSNREC,INSNGLQT IS INSERTION TO BE ENCLOSED IN
*                                  QUOTES.
         BNE   IA019040       IF NOT,BRANCH TO SET UP REMAINING
*                                  PARAMETERS.
         LA    INS2REG,CADSNLEN(COUNTREG) ADDRESS BYTE FOLOWING
*                                  INSERTION RECORD.
         MVI   D0(INS2REG),INSNGLQT MOVE SINGLE QUOTE TO END OF
*                                  DSNAME.
         LA    COUNTREG,D1(,COUNTREG) INCREMENT INSERTION LENGTH
*                                  BY ONE.
IA019040 EQU   *
         STH   COUNTREG,CADSNLEN UPDATE INSERTION LENGTH.
IA019050 EQU   *
         LA    INS2REG,CADSNPTR OBTAIN ADDRESS OF DSNAME AND SAVE
         ST    INS2REG,INPTIN1     IN PARAMETER LIST.
         LA    INS2REG,M308IN2 OBTAIN OFFSET TO DSNAME INSERTION
         STH   INS2REG,CADSNOFF    AND SAVE IN PARAMETER LIST.
         SR    LEV2REG,LEV2REG SHOW NO SECOND LEVEL MESSAGE OR
         SR    INS2REG,INS2REG     INSERTION REQUIRED.
         ST    INS2REG,CADSNPTR SHOW ONLY TWO INSERTIONS REQUIRED
*                                  IN THIS MESSAGE.
         B     IA020001       BRANCH TO CALL IKJEBEMS.
         EJECT
***********************************************************************
*
*    MESSAGE ID -- IKJ52332I
*
*    CONDITION(S) --
*
*         THE EDIT DATA SET HAS RECORD FORMAT OF V OR VB.  EDIT
*         ACCEPTS ONLY FIXED-LENGTH RECORDS FOR THE DATA SET TYPE
*         SPECIFIED.
*
*    PARAMETER FORMAT --
*         WORD 1 - ADDRESS OF THE EDIT COMMUNICATION AREA
*         WORD 2 - MESSAGE INDEX = 72
*         WORD 3 - NOT USED
*         WORD 4 - NOT USED
*         WORD 5 - NOT USED
*         WORD 6 - ADDRESS OF THE IKJEBEIA WORK AREA
*
***********************************************************************
IA021010 EQU   *
         LA    LEV1REG,M332   RELATIVE MESSAGE TO BE PUT TO THE
*                                  TERMINAL.
         MVC   INPTIN1(IAINS008-IAINS007),IAINS007 BUILD BASIC
*                                  INSERTION RECORD FOR DATA SET
*                                  TYPE.
         LA    INS1REG,M332IN1 OBTAIN OFFSET TO INSERTION AND
         STH   INS1REG,INOFFIN1    SAVE IN PARAMETER LIST.
         MVC   ININS1(L'CADSTYPE),CADSTYPE MOVE DATA SET TYPE TO
*                                  INSERTION RECORD.
         LA    INS1REG,INMSGIN1 ADDRESS OF INSERTION CHAIN FOR
*                                  FIRST LEVEL MESSAGE.
         BAL   SRRTNREG,IA110010 BRANCH TO SUPPRESS TRAILING
*                                  BLANKS.
         SR    LEV2REG,LEV2REG SHOW NO SECOND LEVEL MESSAGE OR
         SR    INS2REG,INS2REG     INSERTION REQUIRED.
         B     IA020001       BRANCH TO CALL IKJEBEMS.
         EJECT
***********************************************************************
*
*    MESSAGE ID -- IKJ52306I
*
*    CONDITION(S) --
*
*         THE DATA SET SPECIFIED ON THE COMMAND IS ALLOCATED
*         TO ANOTHER USER OR JOB.
*
*    PARAMETER FORMAT --
*         WORD 1 - ADDRESS OF THE EDIT COMMUNICATION AREA
*         WORD 2 - MESSAGE INDEX = 76
*         WORD 3 - NOT USED
*         WORD 4 - NOT USED
*         WORD 5 - NOT USED
*         WORD 6 - ADDRESS OF THE IKJEBEIA WORK AREA
*
***********************************************************************
IA022010 EQU   *
         LA    LEV1REG,M306   RELATIVE MESSAGE TO BE PUT TO THE
*                                  TERMINAL.
         LA    INS1REG,CADSNPTR ADDRESS OF FIRST LEVEL INSERTION
*                                  RECORD.
         LA    LEV2REG,M306IN1 OBTAIN OFFSET TO DSNAME INSERTION
         STH   LEV2REG,CADSNOFF    AND SAVE IN PARAMETER LIST.
         SR    INS2REG,INS2REG INDICATE ONLY 1 INSERTION REQUIRED
         ST    INS2REG,CADSNPTR    IN FIRST LEVEL MESSAGE.
         LA    LEV2REG,M3061  RELATIVE SECOND LEVEL MESSAGE
*                                  NUMBER.
         B     IA020001       BRANCH TO CALL IKJEBEMS.
         EJECT
***********************************************************************
*
*    MESSAGE ID -- IKJ52309I
*
*    CONDITION(S) --
*
*         THE DCB FOR A PARTITIONED DATA SET COULD NOT BE OPENED.
*
*    PARAMETER FORMAT --
*         WORD 1 - ADDRESS OF THE EDIT COMMUNICATION AREA
*         WORD 2 - MESSAGE INDEX = 80
*         WORD 3 - RELATIVE SECOND-LEVEL MESSAGE NUMBER
*         WORD 4 - NOT USED
*         WORD 5 - NOT USED
*         WORD 6 - ADDRESS OF THE IKJEBEIA WORK AREA
*
***********************************************************************
IA023010 EQU   *
         LA    LEV1REG,M309   RELATIVE MESSAGE TO BE PUT TO THE
*                                  TERMINAL.
         LA    INS1REG,CADSNPTR ADDRESS OF FIRST LEVEL INSERTION
*                                  RECORD.
         LA    LEV2REG,M309IN1 OBTAIN OFFSET TO DSNAME INSERTION
         STH   LEV2REG,CADSNOFF    AND SAVE IN PARAMETER LIST.
         SR    INS2REG,INS2REG INDICATE ONLY 1 INSERTION REQUIRED
         ST    INS2REG,CADSNPTR    IN FIRST LEVEL MESSAGE.
         L     LEV2REG,D8(,PLISTREG) OBTAIN RELATIVE SECOND LEVEL
         SR    INS2REG,INS2REG     NUMBER AND SHOW NO INSERTIONS.
         B     IA020001       BRANCH TO CALL IKJEBEMS.
         EJECT
***********************************************************************
*
*    MESSAGE ID -- IKJ52301I
*
*    CONDITION(S) --
*
*         THE DATA SET SPECIFIED ON THE EDIT COMMAND CANNOT BE
*         ALLOCATED BECAUSE THE TIOT IS FILLED.
*
*    PARAMETER FORMAT --
*         WORD 1 - ADDRESS OF THE EDIT COMMUNICATION AREA
*         WORD 2 - MESSAGE INDEX = 84
*         WORD 3 - NOT USED
*         WORD 4 - NOT USED
*         WORD 5 - NOT USED
*         WORD 6 - ADDRESS OF THE IKJEBEIA WORK AREA
*
***********************************************************************
IA024010 EQU   *
         LA    LEV1REG,M301   RELATIVE MESSAGE TO BE PUT TO THE
*                                  TERMINAL.
         LA    INS1REG,CADSNPTR ADDRESS OF FIRST LEVEL INSERTION
*                                  RECORD.
         LA    LEV2REG,M301IN1 OBTAIN OFFSET TO DSNAME INSERTION
         STH   LEV2REG,CADSNOFF    AND SAVE IN PARAMETER LIST.
         SR    INS2REG,INS2REG INDICATE ONLY 1 INSERTION REQUIRED
         ST    INS2REG,CADSNPTR    IN FIRST LEVEL MESSAGE.
         LA    LEV2REG,M3011  RELATIVE SECOND LEVEL MESSAGE
*                                  NUMBER.
         B     IA020001       BRANCH TO CALL IKJEBEMS.
         EJECT
***********************************************************************
*
*    MESSAGE ID -- IKJ52303I
*
*    CONDITION(S) --
*
*         A REQUIRED VOLUME OR CONTROL VOLUME IS NOT MOUNTED ON
*         THE SYSTEM.
*
*    PARAMETER FORMAT --
*         WORD 1 - ADDRESS OF THE EDIT COMMUNICATION AREA
*         WORD 2 - MESSAGE INDEX = 88
*         WORD 3 - NOT USED
*         WORD 4 - NOT USED
*         WORD 5 - NOT USED
*         WORD 6 - ADDRESS OF THE IKJEBEIA WORK AREA
*
***********************************************************************
IA025010 EQU   *
         LA    LEV1REG,M303   RELATIVE MESSAGE TO BE PUT TO THE
*                                  TERMINAL.
         LA    INS1REG,CADSNPTR ADDRESS OF FIRST LEVEL INSERTION
*                                  RECORD.
         LA    LEV2REG,M303IN1 OBTAIN OFFSET TO DSNAME INSERTION
         STH   LEV2REG,CADSNOFF    AND SAVE IN PARAMETER LIST.
         SR    INS2REG,INS2REG INDICATE ONLY 1 INSERTION REQUIRED
         ST    INS2REG,CADSNPTR    IN FIRST LEVEL MESSAGE.
         LA    LEV2REG,M3031  RELATIVE SECOND LEVEL MESSAGE
*                                  NUMBER.
         B     IA020001       BRANCH TO CALL IKJEBEMS.
         EJECT
***********************************************************************
*
*    MESSAGE ID -- IKJ52302I
*
*    CONDITION(S) --
*
*         THE EDIT DATA SET DOES NOT RESIDE ON THE VOLUME SPECIFIED
*         IN THE CATALOG.
*
*    PARAMETER FORMAT --
*         WORD 1 - ADDRESS OF THE EDIT COMMUNICATION AREA
*         WORD 2 - MESSAGE INDEX = 92
*         WORD 3 - NOT USED
*         WORD 4 - NOT USED
*         WORD 5 - NOT USED
*         WORD 6 - ADDRESS OF THE IKJEBEIA WORK AREA
*
***********************************************************************
IA026010 EQU   *
         LA    LEV1REG,M302   RELATIVE MESSAGE TO BE PUT TO THE
*                                  TERMINAL.
         LA    INS1REG,CADSNPTR ADDRESS OF FIRST LEVEL INSERTION
*                                  RECORD.
         LA    LEV2REG,M302IN1 OBTAIN OFFSET TO DSNAME INSERTION
         STH   LEV2REG,CADSNOFF    AND SAVE IN PARAMETER LIST.
         SR    INS2REG,INS2REG INDICATE ONLY 1 INSERTION REQUIRED
         ST    INS2REG,CADSNPTR    IN FIRST LEVEL MESSAGE.
         LA    LEV2REG,M3021  RELATIVE SECOND LEVEL MESSAGE
*                                  NUMBER.
         B     IA020001       BRANCH TO CALL IKJEBEMS.
         EJECT
***********************************************************************
*
*    MESSAGE ID -- IKJ52304I
*
*    CONDITION(S) --
*
*         A CATALOG MANAGEMENT ERROR, DYNAMIC ALLOCATION ERROR,
*         OR CATALOG I/O ERROR HAS OCCURRED.
*
*    PARAMETER FORMAT --
*         WORD 1 - ADDRESS OF THE EDIT COMMUNICATION AREA
*         WORD 2 - MESSAGE INDEX = 96
*         WORD 3 - RELATIVE SECOND-LEVEL MESSAGE NUMBER
*         WORD 4 - NOT USED
*         WORD 5 - RIGHT-JUSTIFIED DARC OR CTRC FIELD
*         WORD 6 - ADDRESS OF THE IKJEBEIA WORK AREA
*
***********************************************************************
IA027010 EQU   *
         LA    LEV1REG,M304   RELATIVE MESSAGE TO BE PUT TO THE
*                                  TERMINAL.
         LA    INS1REG,CADSNPTR ADDRESS OF FIRST LEVEL INSERTION
*                                  RECORD.
         LA    LEV2REG,M304IN1 OBTAIN OFFSET TO DSNAME INSERTION
         STH   LEV2REG,CADSNOFF    AND SAVE IN PARAMETER LIST.
         SR    INS2REG,INS2REG INDICATE ONLY 1 INSERTION REQUIRED
         ST    INS2REG,CADSNPTR    IN FIRST LEVEL MESSAGE.
         CLI   D11(PLISTREG),M3043 CHECK FOR CATALOG I/O ERROR.
         BE    IA027030       IF SO, BRANCH TO FINISH PARAMETERS.
         LA    INS2REG,D16(,PLISTREG) OBTAIN RETURN CODE FOR
*                                  CONVERSION.
         LA    LEV2REG,INMSGIN1 ADDRESS OF AREA FOR CONVERSION.
         BAL   SRRTNREG,IA100010 BRANCH TO CONVERT RETURN CODE TO
*                                  PRINTABLE HEXADECIMAL.
         LR    INS2REG,LEV2REG ADDRESS OF INSERTION LIST FOR
*                                  SECOND LEVEL MESSAGE.
         SR    LEV2REG,LEV2REG INDICATE ONLY 1 INSERTION IN
         ST    LEV2REG,INPTIN1     SECOND LEVEL MESSAGE.
         CLI   D11(PLISTREG),M3041 DETERMINE IF FIRST SECOND
*                                  LEVEL MESSAGE REQUESTED.
         BNE   IA027020       IF NOT, BRANCH TO SET OFFSET FOR
*                                  SECOND SECOND LEVEL MESSAGE.
         LA    LEV2REG,M3041IN1 OBTAIN OFFSET TO INSERTION AND
         STH   LEV2REG,INOFFIN1    SAVE IN PARAMETER LIST.
         B     IA027030       BRANCH TO SET UP SECOND LEVEL
*                                  MESSAGE.
IA027020 EQU   *
         LA    LEV2REG,M3042IN1 OBTAIN OFFSET TO INSERTION AND
         STH   LEV2REG,INOFFIN1    SAVE IN PARAMETER LIST.
IA027030 EQU   *
         L     LEV2REG,D8(,PLISTREG) RELATIVE SECOND LEVEL
*                                  MESSAGE NUMBER.
         B     IA020001       BRANCH TO CALL IKJEBEMS.
         EJECT
***********************************************************************
*
*    MESSAGE ID -- IKJ52317I
*
*    CONDITION(S) --
*
*         THE DATA SET SPECIFIED ON THE COMMAND DOES NOT RESIDE
*         ON A DIRECT ACCESS VOLUME.
*
*    PARAMETER FORMAT --
*         WORD 1 - ADDRESS OF THE EDIT COMMUNICATION AREA
*         WORD 2 - MESSAGE INDEX = 100
*         WORD 3 - NOT USED
*         WORD 4 - NOT USED
*         WORD 5 - NOT USED
*         WORD 6 - ADDRESS OF THE IKJEBEIA WORK AREA
*
***********************************************************************
IA028010 EQU   *
         LA    LEV1REG,M317   RELATIVE MESSAGE TO BE PUT TO THE
*                                  TERMINAL.
         LA    INS1REG,CADSNPTR ADDRESS OF FIRST LEVEL INSERTION
*                                  RECORD.
         LA    LEV2REG,M317IN1 OBTAIN OFFSET TO DSNAME INSERTION
         STH   LEV2REG,CADSNOFF    AND SAVE IN PARAMETER LIST.
         SR    INS2REG,INS2REG INDICATE ONLY 1 INSERTION REQUIRED
         ST    INS2REG,CADSNPTR    IN FIRST LEVEL MESSAGE.
         LR    LEV2REG,INS2REG INDICATE NO SECOND-LEVEL MESSAGE.
         B     IA020001       BRANCH TO CALL IKJEBEMS.
         EJECT
***********************************************************************
*
*    MESSAGE ID -- IKJ52310I
*
*    CONDITION(S) --
*
*         WHEN FULLY QUALIFIED, THE PARTIALLY QUALIFIED DATA SET
*         NAME ENTERED ON THE EDIT COMMAND EXCEEDS 44 CHARACTERS.
*
*    PARAMETER FORMAT --
*         WORD 1 - ADDRESS OF THE EDIT COMMUNICATION AREA
*         WORD 2 - MESSAGE INDEX = 104
*         WORD 3 - NOT USED
*         WORD 4 - NOT USED
*         WORD 5 - NOT USED
*         WORD 6 - ADDRESS OF THE IKJEBEIA WORK AREA
*
***********************************************************************
IA029010 EQU   *
         LA    DSNAMREG,INDSNREC ADDRESS TEXT FIELD OF DSNAME
*                                  INSERTION RECORD.
         MVI   D0(DSNAMREG),INSNGLQT MOVE SINGLE QUOTE TO FIRST
*                                  CHARACTER POSITION.
         L     TMPREG,CAPTTMP ADDRESS COMMAND PARAMETER LIST.
         L     PASSREG,CPPLPSCB-CPPL(,TMPREG) ADDRESS PROTECTED
*                                  STEP CONTROL BLOCK.
         SR    LENREG,LENREG  ZERO LENGTH.
         IC    LENREG,PSCBUSRL-PSCB(,PASSREG) SET LENGTH OF
*                                  USERID.
         LR    COUNTREG,LENREG ESTABLISH LENGTH COUNTER FOR
*                                  INSERTION.
         BCTR  LENREG,D0      ADJUST USERID LENGTH FOR MVC.
         EX    LENREG,IAMVC007 MOVE USERID TO INSERTION RECORD.
         LA    DSNAMREG,D2(DSNAMREG,LENREG) UPDATE FIELD ADDRESS.
         MVI   D0(DSNAMREG),INPERIOD MOVE '.' TO INSERTION
*                                  RECORD.
         LH    LENREG,CAEDDSNL SET USER-SUPPLIED NAME LENGTH.
         AR    COUNTREG,LENREG UPDATE LENGTH COUNTER.
         BCTR  LENREG,D0      ADJUST USER-SUPPLIED NAME LENGTH.
         EX    LENREG,IAMVC008 MOVE USER-SUPPLIED NAME.
         LA    DSNAMREG,D2(DSNAMREG,LENREG) UPDATE FIELD ADDRESS.
         MVI   D0(DSNAMREG),INPERIOD MOVE '.' TO INSERTION RECORD.
         LA    RETREG,CADSQUAL+D7 COMPARES BEGIN AT LAST
*                                  CHARACTER IN QUALIFIER FIELD.
         LA    EPLOCREG,D7    SET LOOP COUNTER.
IA029020 EQU   *
         CLI   D0(RETREG),CHRBLANK IS CHARACTER A BLANK -
         BNE   IA029030       IF NOT, BRANCH TO MOVE QUALIFIER.
         BCTR  RETREG,D0      ADDRESS NEXT BYTE TO LEFT.
         BCT   EPLOCREG,IA029020 REPEAT LOOP SEVEN TIMES.
IA029030 EQU   *
         EX    EPLOCREG,IAMVC009 MOVE QUALIFIER TO INSERTION
*                                  RECORD.
         LA    DSNAMREG,D2(EPLOCREG,DSNAMREG) UPDATE FIELD
*                                  ADDRESS.
         MVI   D0(DSNAMREG),INSNGLQT MOVE SINGLE QUOTE TO
*                                  INSERTION RECORD.
         LA    COUNTREG,D9(EPLOCREG,COUNTREG) UPDATE LENGTH COUNT
*                                  FOR INSERTION RECORD.
         STH   COUNTREG,INDSNLEN SET INSERTION LENGTH.
         LA    LENREG,M310IN1 SET INSERTION OFFSET.
         STH   LENREG,INDSNOFF
         LA    LEV1REG,M310   SET FIRST-LEVEL RELATIVE MESSAGE.
         SR    LEV2REG,LEV2REG SPECIFY NO SECOND-LEVEL MESSAGE.
         ST    LEV2REG,INDSNPTR SPECIFY ONE FIRST-LEVEL
*                                  INSERTION ONLY.
         LA    INS1REG,INDSNPTR SET FIRST-LEVEL INSERTION CHAIN
*                                  ADDRESS.
         LR    INS2REG,LEV2REG SPECIFY NO SECOND-LEVEL INSERTIONS.
         B     IA020001       BRANCH TO CALL IKJEBEMS.
         EJECT
***********************************************************************
*
*    MESSAGE ID -- IKJ52316I
*
*    CONDITION(S) --
*
*         WHEN FULLY QUALIFIED, THE PARTIALLY QUALIFIED DATA SET
*         NAME ENTERED ON THE EDIT COMMAND IS INCOMPATIBLE WITH
*         AN EXISTING INDEX STRUCTURE IN THE CATALOG.
*
*    PARAMETER FORMAT --
*         WORD 1 - ADDRESS OF THE EDIT COMMUNICATION AREA
*         WORD 2 - MESSAGE INDEX = 108
*         WORD 3 - NOT USED
*         WORD 4 - NOT USED
*         WORD 5 - NOT USED
*         WORD 6 - ADDRESS OF THE IKJEBEIA WORK AREA
*
***********************************************************************
IA030010 EQU   *
         BAL   SRRTNREG,IA100050 PROCESS DSNAME INSERTION.
         LA    DSNAMREG,M316IN1 SET OFFSET FOR INSERTION.
         STH   DSNAMREG,INDSNOFF
         LA    LEV1REG,M316   SPECIFY RELATIVE FIRST-LEVEL NO.
         LA    LEV2REG,M3161  SPECIFY RELATIVE SECOND-LEVEL NO.
         LA    INS1REG,INDSNPTR ADDRESS LEVEL-1 INSERTION.
         SR    INS2REG,INS2REG SPECIFY NO LEVEL TWO INSERTIONS.
         B     IA020001       BRANCH TO CALL IKJEBEMS.
         EJECT
***********************************************************************
*
*    MESSAGE ID -- IKJ52314I
*
*    CONDITION(S) --
*
*         THE EDIT DATA SET RESIDES ON MULTIPLE VOLUMES.  MULTIPLE
*         VOLUME DATA SETS ARE NOT SUPPORTED BY THE EDIT COMMAND.
*
*    PARAMETER FORMAT --
*         WORD 1 - ADDRESS OF THE EDIT COMMUNICATION AREA
*         WORD 2 - MESSAGE INDEX = 112
*         WORD 3 - NOT USED
*         WORD 4 - NOT USED
*         WORD 5 - NOT USED
*         WORD 6 - ADDRESS OF THE IKJEBEIA WORK AREA
*
***********************************************************************
IA031010 EQU   *
         LA    LEV1REG,M314   RELATIVE MESSAGE TO BE PUT TO THE
*                                  TERMINAL.
         LA    INS1REG,M314IN1 OBTAIN OFFSET FOR DSNAME INSERTION.
         STH   INS1REG,CADSNOFF STORE IN INSERTION LIST.
         SR    LEV2REG,LEV2REG SPECIFY NO LEVEL-2 MESSAGE.
         ST    LEV2REG,CADSNPTR SPECIFY ONE INSERTION ONLY.
         LR    INS2REG,LEV2REG SPECIFY NO LEVEL-2 INSERTIONS.
         LA    INS1REG,CADSNPTR ADDRESS LEVEL-1 INSERTION.
         B     IA020001       BRANCH TO CALL IKJEBEMS.
         EJECT
***********************************************************************
*
*    MESSAGE ID -- IKJ52315I
*
*    CONDITION(S) --
*
*         THE DEFAULT SERVICE ROUTINE (IKJDFLT) HAS BEEN UNABLE TO
*         QUALIFY A DATA SET NAME DUE TO A SYSTEM ERROR.
*
*    PARAMETER FORMAT --
*         WORD 1 - ADDRESS OF THE EDIT COMMUNICATION AREA
*         WORD 2 - MESSAGE INDEX = 116
*         WORD 3 - NOT USED
*         WORD 4 - NOT USED
*         WORD 5 - FULLWORD BINARY RETURN CODE FOR INSERTION
*         WORD 6 - ADDRESS OF THE IKJEBEIA WORK AREA
*
***********************************************************************
IA032010 EQU   *
         BAL   SRRTNREG,IA100050 PROCESS DSNAME INSERTION.
         LA    DSNAMREG,M315IN1 SET OFFSET IN INSERTION LIST.
         STH   DSNAMREG,INDSNOFF
         L     PARMREG1,D16(,PLISTREG) OBTAIN RETURN CODE
*                                  FOR CONVERSION.
         BAL   EPLOCREG,IA032020 INSURE REGISTER NEGATIVE
*                                  FOR PROCESSING AS TWO-DIGIT
*                                  RETURN CODE.
IA032020 EQU   *
         LA    LEV2REG,INMSGIN2 ADDRESS OF AREA FOR CONVERSION.
         BAL   SRRTNREG,IA100003 BRANCH TO CONVERT RETURN CODE TO
*                                  PRINTABLE DECIMAL.
         LR    INS2REG,LEV2REG SPECIFY LEVEL-2 INSERTION ADDRESS.
         LA    INS1REG,M3151IN1 SET OFFSET FOR CODE INSERTION.
         STH   INS1REG,INOFFIN2
         LA    LEV1REG,M315   SPECIFY RELATIVE FIRST-LEVEL NO.
         LA    LEV2REG,M3151  SPECIFY RELATIVE SECOND-LEVEL NO.
         LA    INS1REG,INDSNPTR ADDRESS LEVEL-ONE INSERTION.
         B     IA020001       BRANCH TO CALL IKJEBEMS.
         EJECT
***********************************************************************
*
*    MESSAGE ID -- IKJ52343I
*
*    CONDITIONS --
*
*    GOFORT(FREE) WAS SPECIFIED OR DEFAULTED AND --
*         (1) LINE OPERAND WAS SPECIFIED FOR A NEW DATA SET OR
*         (2) RECORD FORMAT IS FIXED FOR AN OLD DATA SET
*    PARAMETER FORMAT --
*         WORD 1 - ADDRESS OF THE EDIT COMMUNICATION AREA
*         WORD 2 - MESSAGE INDEX = 120
*         WORD 3 - NOT USED
*         WORD 4 - NOT USED
*         WORD 5 - NOT USED
*         WORD 6 - ADDRESS OF THE IKJEBEIA WORK AREA
*
***********************************************************************
         SPACE 2
IA033010 EQU   *
         TM    CAEDFLAG,CAEDDISP DETERMINE IF DATA SET IS NEW.
         BZ    IA033020       IF SO, BRANCH TO SET UP SECOND-LEVEL
*                                  MESSAGE FOR NEW DATA SET.
         LA    LEV2REG,M3431  OTHERWISE, SET UP SECOND-LEVEL
*                                  MESSAGE FOR OLD DATA SET.
         B     IA033030            BRANCH TO COMPLETE PARAMETERS.
IA033020 EQU   *
         LA    LEV2REG,M3432  SET UP SECOND-LEVEL MESSAGE FOR
*                                  NEW DATA SET.
IA033030 EQU   *
         LA    LEV1REG,M343   SPECIFY RELATIVE FIRST-LEVEL NUMBER.
         SR    INS1REG,INS1REG SPECIFY NO FIRST-LEVEL INSERTIONS.
         LR    INS2REG,INS1REG SPECIFY NO SECOND-LEVEL INSERTIONS.
         B     IA020001       BRANCH TO CALL IKJEBEMS.
         EJECT
***********************************************************************
*
*    MESSAGE ID -- IKJ52318I
*
*    CONDITION(S) --
*
*         ONE OF THE FOLLOWING HAS OCCURRED -
*           (1) THE USER HAS AN INVALID UNIT NAME IN THE USER
*               ATTRIBUTE DATA SET (UADS).
*           (2) NO UNIT IS AVAILABLE FOR DYNAMIC ALLOCATION.
*
* PARAMETER FORMAT --
*         WORD 1 - ADDRESS OF THE EDIT COMMUNICATION AREA
*         WORD 2 - MESSAGE INDEX = 124
*         WORD 3 - RELATIVE SECOND-LEVEL MESSAGE NUMBER
*         WORD 4 - NOT USED
*         WORD 5 - NOT USED
*         WORD 6 - ADDRESS OF THE IKJEBEIA WORK AREA
*
***********************************************************************
IA034010 EQU   *
         L     LEV2REG,D8(,PLISTREG) RELATIVE SECOND-LEVEL
*                                  MESSAGE NUMBER.
         LA    INS1REG,M318IN1 OBTAIN OFFSET FOR DSNAME.
         STH   INS1REG,CADSNOFF STORE IN INSERTION LIST.
         SR    INS2REG,INS2REG SPECIFY NO SECOND-LEVEL INSERTION.
         ST    INS2REG,CADSNPTR SPECIFY ONE FIRST-LEVEL INSERTION.
         LA    INS1REG,CADSNPTR ADDRESS OF FIRST-LEVEL INSERTION.
         LA    LEV1REG,M318   RELATIVE ERROR TO BE PUT TO THE
*                                  TERMINAL.
         B     IA020001       BRANCH TO CALL IKJEBEMS.
         EJECT
**************************************************************** A45714
*                                                                A45714
*    MESSAGE ID -- IKJ52568I                                     A45714
*                                                                A45714
*    CONDITION(S) --                                             A45714
*                                                                A45714
*         DATA SET TYPE WAS NOT ENTERED ON THE EDIT COMMAND AND  A45714
*         THE USER HAS INVOKED EDIT WITHIN A PROCEDURE.  EDIT    A45714
*         IS UNABLE TO PROMPT THE USER FOR DATA SET TYPE.        A45714
*                                                                A45714
* PARAMETER FORMAT --                                            A45714
*         WORD 1 - ADDRESS OF THE EDIT COMMUNICATION AREA        A45714
*         WORD 2 - MESSAGE INDEX = 128                           A45714
*         WORDS 3-6 - NOT USED                                   A45714
*                                                                A45714
**************************************************************** A45714
IA035010 EQU   *                                                 A45714
         LA    LEV1REG,M568   RELATIVE ERROR TO BE PUT TO TERM.  A45714
         SR    LEV2REG,LEV2REG NO SECOND LEVEL MESSAGE.          A45714
         SR    INS1REG,INS1REG NO FIRST LEVEL INSERTIONS.        A45714
         SR    INS2REG,INS2REG NO SECOND LEVEL INSERTIONS.       A45714
         B     IA020001       BRANCH TO CALL IKJEBEMS.           A45714
         EJECT
***********************************************************************
*                                                                     *
*   INVOKE IKJEBEMS TO PUT THE MESSAGE TO THE TERMINAL USER.  UPON    *
*   RETURN FROM IKJEBEMS, RETURN TO THE CALLING ROUTINE.  REGISTER 15 *
*   ON EXIT FROM THIS ROUTINE WILL CONTAIN THE RETURN CODE PASSED     *
*   BACK BY IKJEBEMS.                                                 *
*                                                                     *
***********************************************************************
IA020001 EQU   *
         IKJEBEML (LEV1REG),(LEV2REG),(INS1REG),(INS2REG),             X
               MF=(E,IAPRMLST) CALL IKJEBEMS.
         SPACE 2
IA020002 EQU   *
         IKJEBERT (14,12),,RC=(15) RESTORE REGISTERS AND RETURN.
         EJECT
***********************************************************************
*
*    THE FOLLOWING SECTION IS EXECUTED FOR DECIMAL AND HEXADECIMAL
*    CONVERSIONS AND FORMATTING OF NUMERIC MESSAGE INSERTIONS.
*
***********************************************************************
         SPACE 2
IA100000 EQU   *
         SR    EPLOCREG,EPLOCREG SPECIFY THAT INSERTION IS NOT A
*                                  RETURN CODE
IA100003 EQU   *
         CVD   PARMREG1,INDECBIN CONVERT DATA TO PACKED DECIMAL
         UNPK  D16(D10,LEV2REG),INDECBIN+D4(D5) AND TO EBCDIC.
         LTR   EPLOCREG,EPLOCREG DETERMINE IF INSERTION IS TO BE
*                                  PROCESSED AS 2-DIGIT CODE.
         BM    IA100005       IF SO, BRANCH TO PROCESS.
         LTR   PARMREG1,PARMREG1 CHECK FOR NUMERIC INSERTION
*                                  EQUAL TO ZERO.
         BNZ   IA100004       IF NOT ZERO, BRANCH TO PROCESS
*                                  LEADING ZEROES.
         MVI   D8(LEV2REG),IACHRZRO MOVE CHARACTER ZERO TO
*                                  INSERTION LIST.
         LA    INLENREG,D5    LENGTH OF INSERTION IS FIVE BYTES.
         B     IA100045       BRANCH TO COMPLETE INSERTION.
IA100004 EQU   *
         LA    INS2REG,D16(,LEV2REG) ADDRESS OF CONVERTED DATA.
         LA    INLENREG,D8(,D0) SET COUNTER EQUAL TO DATA LENGTH.
         B     IA100020       BRANCH TO SUPPRESS LEADING ZEROES.
IA100005 EQU   *
         LA    INS2REG,D22(,LEV2REG) ADDRESS OF CONVERTED DATA.
         LA    INLENREG,D1(,D0) TWO CHARACTER DIGITS TO BE MOVED.
         B     IA100040       BRANCH TO MOVE RC TO INSERTION.
IA100010 EQU   *
         UNPK  D16(D9,LEV2REG),D0(D5,INS2REG) CONVERT DATA TO
         TR    D16(D8,LEV2REG),IATRNSTB-IACHRZRO PRINTABLE EBCDIC
         LA    INS2REG,D20(,LEV2REG) ADDRESS OF CONVERTED DATA.
         LA    INLENREG,D3(,D0) SET DATA LENGTH FOR MVC.
         B     IA100040       BRANCH TO MOVE 4-CHARACTER FIELD
*                                  TO INSERTION RECORD.
IA100020 EQU   *
         CLI   D0(INS2REG),IACHRZRO CHECK IF CHARACTER IS A ZERO.
         BNE   IA100030       IF NOT, BRANCH TO COMPLETE
*                                  INSERTION RECORD.
         LA    INS2REG,D1(,INS2REG) UPDATE POINTER TO NEXT BYTE.
         BCT   INLENREG,IA100020   REDUCE COUNTER BY 1 AND BRANCH
*                             TO CHECK NEXT BYTE.
IA100030 EQU   *
         BCTR  INLENREG,D0    REDUCE COUNT BY ONE FOR MVC.
IA100040 EQU   *
         EX    INLENREG,IAMVC005 MOVE DATA INTO INSERTION RECORD.
         LA    INLENREG,D5(,INLENREG) COMPUTE LENGTH OF INSERTION
*                                  RECORD.
IA100045 EQU   *
         XC    D0(D8,LEV2REG),D0(LEV2REG) ZERO CONTROL PORTION OF
*                             INSERTION RECORD.
         STH   INLENREG,D4(,LEV2REG) SAVE LENGTH OF INSERTION
*                                  RECORD.
         BR    SRRTNREG       RETURN TO CALLER.
         EJECT
***********************************************************************
*
*    THE FOLLOWING SUBROUTINE IS EXECUTED TO PROCESS A PARTIALLY
*    QUALIFIED DATA SET NAME AS A MESSAGE INSERTION.
*
***********************************************************************
         SPACE 2
IA100050 EQU   *
         LA    DSNAMREG,CAEDDSN+D1 ADDRESS SECOND CHARACTER OF
*                                  DATA SET NAME.
         LA    COUNTREG,D2    MINUMUM NUMBER OF CHARACTERS TO
*                                  BE STRIPPED IS TWO.
IA100060 EQU   *
         CLI   D0(DSNAMREG),INPERIOD CHECK FOR PERIOD FOLLOWING
*                                  USERID -
         BE    IA100070       IF PERIOD ENCOUNTERED, BRANCH
*                             TO MOVE INSERTION TEXT.
         LA    COUNTREG,D1(,COUNTREG) BUMP COUNT BY ONE.
         LA    DSNAMREG,D1(,DSNAMREG) ADDRESS NEXT CHARACTER
*                                  OF DSNAME.
         B     IA100060       REPEAT LOOP UNTIL PERIOD FOUND.
IA100070 EQU   *
         LH    LENREG,CAEDDSNL GET DSNAME LENGTH.
         SR    LENREG,COUNTREG DETERMINE INSERTION TEXT LENGTH.
         BCTR  LENREG,D0      DECREMENT BY 1 FOR MVC.
         EX    LENREG,IAMVC010 MOVE INSERTION TEXT TO INSERTION
*                                  RECORD.
         LA    LENREG,D5(,LENREG) COMPUTE LENGTH FOR INSERTION
*                                  RECORD.
         STH   LENREG,INDSNLEN SET LENGTH IN INSERTION RECORD.
         SR    LENREG,LENREG  SET INSERTION CHAIN WORD TO ZERO.
         ST    LENREG,INDSNPTR (THIS IS LAST MESSAGE INSERTION.)
         BR    SRRTNREG       RETURN TO MAINLINE ROUTINE.
         SPACE 5
         EJECT
***********************************************************************
*    THE FOLLOWING SUBROUTINE IS EXECUTED TO STRIP TRAILING BLANKS
*    FROM MESSAGE INSERTIONS.
*
*
***********************************************************************
         SPACE 2
IA110010 EQU   *
         LA    RETREG,D8(,D0) LOOP CONTROL COUNTER.
         LA    EPLOCREG,D15(,INS1REG) ADDRESS OF LAST CHARACTER
*                                  IN DATA SET TYPE.
IA110020 EQU   *
         CLI   D0(EPLOCREG),IACHBLNK CHECK FOR BLANK.
         BNE   IA110030       IF NOT A BLANK, ALL TRAILING BLANKS
*                                  HAVE BEEN SUPPRESSED.
         BCTR  EPLOCREG,D0    BACK POINTER UP ONE CHARACTER.
         BCT   RETREG,IA110020 REDUCE COUNTER BY ONE AND BRANCH
*                                  TO CHECK NEXT BYTE.
IA110030 EQU   *
         XC    D0(D4,INS1REG),D0(INS1REG) ZERO CONTROL FIELDS.
         LA    RETREG,D4(,RETREG) COMPUTE LENGTH OF INSERTION
         STH   RETREG,D4(,INS1REG) RECORD AND SAVE.
         BR    SRRTNREG       RETURN TO CALLER.
         EJECT
***********************************************************************
*                                                                     *
*   EQUATES USED BY THIS ROUTINE TO REFERENCE (1) RELATIVE FIRST      *
*   LEVEL MESSAGES, (2) RELATIVE SECOND LEVEL MESSAGES WITHIN FIRST   *
*   LEVEL MESSAGES, AND (3) OFFSETS TO INSERTIONS IN BOTH FIRST AND   *
*   SECOND LEVEL MESSAGES.                                            *
*                                                                     *
***********************************************************************
         IKJEBEMI (301,302,303,304,305,306,307,308,309,310,311,312,    X
               313,314,315,316,317,318,330,331,332,333,334,335,336,    X
               342,343,360,361,362,363,364,365,424,568)          A45714
         EJECT
***********************************************************************
*                                                                     *
*   INSERTION RECORDS USED IN FIRST AND SECOND LEVEL MESSAGES.        *
*                                                                     *
***********************************************************************
IAINS001 IKJEBEMG 0,M312IN1,'EDIT' COMMAND NAME FOR INSERTION IN       X
                                   MESSAGE IKJ52382I.
IAINS002 IKJEBEMG 0,M330IN1,'DIRECT' DSORG INSERTION IN MESSAGE        X
                                   MESSAGE IKJ52502I.
IAINS003 IKJEBEMG 0,M330IN1,'INDEXED SEQUENTIAL' DSORG INSERTION       X
                                   IN MESSAGE IKJ52502I.
IAINS004 IKJEBEMG 0,M3131IN1,'OBTAIN SVC' SERVICE ROUTINE NAME         X
                                   INSERTION.
IAINS005 IKJEBEMG 0,M363IN1,'SCAN' INSERTION IN MESSAGE                X
                                   IKJ52430I.
IAINS006 EQU   *
IAINS007 IKJEBEMG 0,M335IN1,'        ' DATA SET TYPE INSERTION         X
                                   IN MESSAGE IKJ52430I.
IAINS008 IKJEBEMG 0,M3131IN1,'DEFAULT' SERVICE ROUTINE NAME            X
                                   INSERTION.
IAINS009 IKJEBEMG 0,M3131IN1,'DELETE SVC' SERVICE ROUTINE NAME         X
                                   INSERTION.
IAINS010 IKJEBEMG 0,M3131IN1,'DAIR' SERVICE ROUTINE NAME               X
                                   INSERTION
IAINS011 IKJEBEMG 0,M3131IN1,'PARSE' SERVICE ROUTINE NAME              X
                                   INSERTION
IAINS012 IKJEBEMG 0,M330IN1,'UNKNOWN' DSORG INSERTION.
IAINS013 IKJEBEMG 0,M3131IN1,'DEVTYPE' SERVICE ROUTINE                 X
                                   INSERTION.
IAINS014 IKJEBEMG 0,M3131IN1,'PUTGET' SERVICE RTN INSERTION.     A45714
IAINS015 IKJEBEMG 0,M4245IN1,' ' DDNAME INSERTION IN MESSAGE           X
                                   IKJ52424I.
IATRNSTB DC    CL16'0123456789ABCDEF' TRANSLATE TABLE FOR
*                                  CONVERSION TO HEXADECIMAL.
IAMEMNAM DC    C'TEMPNAME'    CHARACTER CONSTANT USED TO DETERMINE
*                                  IF A MEMBER NAME WAS
*                                  ENTERED ON THE EDIT COMMAND.
         EJECT
***********************************************************************
*                                                                     *
*   OTHER EQUATES, INSTRUCTIONS EXECUTED OUT OF SEQUENCE AND DATA     *
*   AREAS.                                                            *
*                                                                     *
***********************************************************************
DS1ORGDA EQU   B'00100000'    DSCB DSORG = DIRECT.
DS1ORGIS EQU   B'10000000'    DSCB DSORG = INDEXED SEQUENTIAL.
IAFMA    EQU   C'A'           ASA CONTROL FORMAT.
IAFMF    EQU   C'F'           FIXED FORMAT.
IAFMM    EQU   C'M'           MACHINE CONTROL FORMAT.
IAFMT    EQU   C'T'           TRACK OVERFLOW FORMAT.
IAFMU    EQU   C'U'           UNDEFINED FORMAT.
IAFMV    EQU   C'V'           VARIABLE FORMAT.
DS1FMFX  EQU   B'10000000'    DSCB RECFM = FIXED.
DS1FMUDF EQU   B'11000000'    DSCB RECFM = UNDEFINED.
DS1FMOVF EQU   B'00100000'    DSCB RECFM = TRACK OVERFLOW.
DS1FMA   EQU   B'00000100'    DSCB RECFM = ASA CONTROL CHARACTER.
DS1FMM   EQU   B'00000010'    DSCB RECFM = MACHINE CONTROL.
*                                  CHARACTER.
X0       EQU   0              OFFSET ZERO.
X4       EQU   4              OFFSET 4.
D0       EQU   0              OFFSET ZERO (DECIMAL).
D1       EQU   1              OFFSET 1 (DECIMAL).
D2       EQU   2              OFFSET 2 (DECIMAL).
D3       EQU   3              OFFSET 3 (DECIMAL).
D4       EQU   4              OFFSET 4 (DECIMAL).
D5       EQU   5              OFFSET 5 (DECIMAL).
D7       EQU   7              OFFSET 7 (DECIMAL).
D8       EQU   8              OFFSET 8 (DECIMAL).
D9       EQU   9              OFFSET 9 (DECIMAL).
D10      EQU   10             OFFSET 10 (DECIMAL).
D11      EQU   11             OFFSET 11 (DECIMAL).
D12      EQU   12             OFFSET 12 (DECIMAL).
D14      EQU   14             OFFSET 14 (DECIMAL).
D15      EQU   15             OFFSET 15 (DECIMAL).
D16      EQU   16             OFFSET 16 (DECIMAL).
D20      EQU   20             OFFSET 20 (DECIMAL).
D22      EQU   22             OFFSET 22 (DECIMAL).
D56      EQU   56             OFFSET 56 (DECIMAL).
IACHRZRO EQU   C'0'           CHARACTER ZERO.
CHRBLANK EQU   C' '           CHARACTER BLANK.
IACHBLNK EQU   C' '           CHARACTER BLANK.
INSNGLQT EQU   C''''          SINGLE QUOTE.
INPERIOD EQU   C'.'           PERIOD.
INLFTPRN EQU   C'('           LEFT PARENTHESIS.
IAZEROES EQU   X'00'          USED TO ZERO A BYTE.
IANORTN  EQU   X'00'          USED TO DETERMINE WHETHER THIS
*                                  ROUTINE MUST BUILD INSERTION
*                                  FOR COMMAND SYSTEM ERROR
*                                  MESSAGE.
IADAIR   EQU   X'02'          INDICATOR FOR IKJDAIR SERVICE
*                                  ROUTINE.
IADEVTYP EQU   X'03'          INDICATOR FOR DEVTYPE SERVICE
*                                  ROUTINE.
IASVCOBT EQU   X'04'          INDICATOR FOR OBTAIN SVC.
IADFAULT EQU   X'05'          INDICATOR FOR DEFAULT SERVICE
*                                  ROUTINE.
IASVCDEL EQU   X'06'          INDICATOR FOR DELETE SVC.
IAPUTGET EQU   X'07'          INDICATOR FOR PUTGET SERVICE RTN.  A45714
         SPACE 2
IAMVC005 MVC   D8(*-*,LEV2REG),D0(INS2REG) MOVE NUMERIC DATA TO
*                                  INSERTION RECORD.
IAMVC006 MVC   ININS1(*-*),CAEDMEMB MOVE MEMBER NAME TO INSERTION
*                                  RECORD.
IAMVC007 MVC   D1(*-*,DSNAMREG),PSCBUSER-PSCB(PASSREG) MOVE
*                                  USERID TO INSERTION RECORD.
IAMVC008 MVC   D1(*-*,DSNAMREG),CAEDDSN MOVE USER-SUPPLIED NAME
*                                  TO INSERTION RECORD.
IAMVC009 MVC   D1(*-*,DSNAMREG),CADSQUAL MOVE QUALIFIER TO
*                                  INSERTION RECORD.
IAMVC010 MVC   INDSNREC(*-*),D1(DSNAMREG) MOVE DSNAME INSERTION
*                                  TEXT TO INSERTION RECORD.
IAMVC011 MVC   ININS1+D1(*-*),PSCBUSER-PSCB(PASSREG) MOVE USERID.
IAMVC012 MVC   ININS1+D1(*-*),D0(LEV1REG) MOVE RECFM INSERTION
*                                  DATA.
         SPACE 2
IAFMTAB  DS    0F             RECFM TRANSLATION TABLE.
IAFMMASK DC    XL4'38'        MASK FOR ISOLATING B,S, AND T BITS
*                                  OF DS1RECFM FIELD.
         DC    AL1(0),CL3'S'  STANDARD.
         DC    AL1(0),CL3'B'  BLOCKED.
         DC    AL1(1),CL3'BS' BLOCKED, STANDARD.
         DC    AL1(0),CL3'T'  TRACK OVERFLOW.
         DC    AL1(1),CL3'ST' STANDARD,TRACK OVERFLOW.
         DC    AL1(1),CL3'BT' BLOCKED, TRACK OVERFLOW.
         DC    AL1(2),C'BST'  BLOCKED, STANDARD, TRACK OVERFLOW.
         EJECT
***********************************************************************
*
*    WORK AREA MAP
*
***********************************************************************
IAWORKA  DSECT                WORK AREA FOR THIS ROUTINE.
         SPACE 2
INDECBIN DS    D              DOUBLE WORD USED FOR CONVERT TO
*                                  BINARY OPERATIONS.
IAPRMLST DS    7F             REMOTE PROGRAM PARAMETER LIST.
INMSGIN1 DS    0F             INSERTION RECORD FOR IKJEBEMS.
INPTIN1  DS    F              POINTER TO NEXT INSERTION.
INLNIN1  DS    H              LENGTH OF INSERTION RECORD.
INOFFIN1 DS    H              OFFSET IN MESSAGE TO INSERTION.
ININS1   DS    CL80           INSERTION TEXT.
         SPACE 2
INMSGIN2 DS    0F             INSERTION RECORD FOR IKJEBEMS.
INPTIN2  DS    F              POINTER TO NEXT INSERTION.
INLNIN2  DS    H              LENGTH OF INSERTION RECORD.
INOFFIN2 DS    H              OFFSET IN MESSAGE TO INSERTION.
ININS2   DS    CL80           INSERTION TEXT.
         SPACE 2
INMSGIN3 DS    0F             INSERTION RECORD FOR IKJEBEMS.
INPTIN3  DS    F              POINTER TO NEXT INSERTION.
INLNIN3  DS    H              LENGTH OF INSERTION RECORD.
INOFFIN3 DS    H              OFFSET IN MESSAGE TO INSERTION.
ININS3   DS    CL80           INSERTION TEXT.
         SPACE 2
INMSGIN4 DS    0F             INSERTION RECORD FOR IKJEBEMS.
INPTIN4  DS    F              POINTER TO NEXT INSERTION.
INLNIN4  DS    H              LENGTH OF INSERTION RECORD.
INOFFIN4 DS    H              OFFSET IN MESSAGE TO INSERTION.
ININS4   DS    CL80           INSERTION TEXT.
         SPACE 2
INDSNINS DS    0F             THIS AREA IS USED FOR DSNAME
*                                  INSERTIONS IN DIAGNOSTIC
*                                  MESSAGES CONCERNED WITH
*                                  PARTIALLY QUALIFIED DSNAMES.
INDSNPTR DS    F              ADDRESS OF NEXT INSERTION RECORD.
INDSNLEN DS    H              LENGTH OF INSERTION RECORD.
INDSNOFF DS    H              OFFSET OF INSERTION INTO BASIC
*                                  MESSAGE.
INDSNREC DS    CL63           INSERTION TEXT.
         EJECT
         IKJEBECA
         EJECT
         IKJCPPL
         EJECT
         IKJPSCB
   END
